#Область ПеременныеФормы

&НаКлиенте
Перем УИДЗамера;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнтеграцияВИнформационнойБазеВключена = ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВИнформационнойБазеВключена();
	ИнтеграцияСБанкамиПодключена = ИнтеграцияВИнформационнойБазеВключена
		И ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	УстановитьОтборыПриСозданииНаСервере(Параметры);
	
	МожноРедактироватьБанк = ПравоДоступа("Редактирование", Метаданные.Документы.СписаниеСРасчетногоСчета)
		И ПравоДоступа("Редактирование", Метаданные.Документы.ПоступлениеНаРасчетныйСчет);
	МожноРедактироватьКассу = ПравоДоступа("Редактирование", Метаданные.Документы.РасходныйКассовыйОрдер)
		И ПравоДоступа("Редактирование", Метаданные.Документы.ПриходныйКассовыйОрдер);
	
	ИспользуетсяНПД = ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые();
	
	УправлениеКассовойСменойДоступно = КассовыеСменыВызовСервераБП.ДоступноУправлениеКассовойСменой();
	НастроитьБаннерКассаНеПодключена();
	ВедетсяРозничнаяТорговля = ПолучитьФункциональнуюОпцию("ВедетсяРозничнаяТорговля");
	
	Элементы.ЗагрузитьИзКлиентБанк.Видимость = МожноРедактироватьБанк;
	
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактироватьКассу И МожноРедактироватьБанк;
	Элементы.Список.ИзменятьСоставСтрок = МожноРедактироватьБанк И МожноРедактироватьКассу;
	
	Элементы.СчетВРежимеИнтеграции.Видимость = ИнтеграцияВИнформационнойБазеВключена;
	Если ИнтеграцияСБанкамиПодключена Тогда
		ОбработатьРежимИнтеграцииСБанком();
	Иначе
		ВидимостьЭлементовДиректБанк = ?(УОрганизацииТолькоОдинБанковскийСчет,
			ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(ОтборОрганизация, ОсновнойБанковскийСчет),
			"НеПоказывать");
	КонецЕсли;
	
	ДанныеИтогов(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	
	ИмеютсяДействующиеПатенты = УчетПСН.ИмеютсяДействующиеПатенты(ТекущаяДатаСеанса());
	Если ИмеютсяДействующиеПатенты Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяПатент");
	КонецЕсли;
	
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.ЖурналДокументов.Деньги",
		"ФормаСпискаПростойИнтерфейс",
		НСтр("ru = 'Новости: Деньги'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	УстановитьФункциональныеОпцииФормы();
	
	ОбновитьБаннерИПодменюКомандУправленияКассовойСменой(Элементы, Истина, Истина);
	
	ТарификацияБП.РазместитьИнформациюОбОграниченииПоКоличествуОбъектов(ЭтотОбъект);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	УправлениеПанельюПодсказки.ПриСозданииНаСервере(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы(МожноРедактироватьБанк И МожноРедактироватьКассу);
	МобильныйКлиентБП.ПроверитьИЗарегистроватьСобытиеДляОценки("просмотрдокументов", ПоказыватьФормуОценкиМК);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(ОтборыПриОткрытии) Тогда
		
		Если ОтборыПриОткрытии.Свойство("Организация") И ЗначениеЗаполнено(ОтборыПриОткрытии.Организация) Тогда
			ОтборОрганизация = ОтборыПриОткрытии.Организация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		КонецЕсли;
		
	ИначеЕсли ОткрытаДляПроверкиЗагрузкиДокументов Тогда
		ОтборОрганизацияИспользование = Ложь;
		ОтборБанковскийСчетИспользование = Ложь;
		ОтборДатаОтбораИспользование = Ложь;
		
	Иначе
		Если ОтборОрганизация <> ОсновнаяОрганизация Тогда
			ОтборОрганизация = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ИначеЕсли НЕ ОтборОрганизацияИспользование Тогда
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВосстановленныеОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("ПоказатьПредупреждениеПоРаботеСМаркетплейсами", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_ПоказатьБаннерДиректБанк", 3, Истина);
	
	Если ИспользуетсяНПД И ЧекиНПДКлиент.ЕстьПравоОбновленияЧековНПД() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСтатусОфлайнЧековНПД", 1, Истина);
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСведенияОСформированныхЧекахНПД", 3, Истина);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_АУСН", 0.1, Истина);
	
	ЗаявкиНаОткрытиеСчетаКлиент.ПослеОткрытияЖурналаДеньги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОсновнаяОрганизация = Параметр;
		Если ОсновнаяОрганизация <> ОтборОрганизация Тогда
			ОтборОрганизация                 = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование    = ЗначениеЗаполнено(ОтборОрганизация);
			
			ОтборОрганизацияПриИзмененииСервер();
			
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
			ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
		
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = ОтборОрганизация Тогда
		
		ИзменениеСоставаБанковскихСчетовОрганизации();
		ПодключитьОбработчикОжидания("Подключаемый_ПоказатьБаннерДиректБанк", 3, Истина);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
		
	ИначеЕсли ИмяСобытия = "ИзменениеВыписки" Тогда
		
		ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		Оповестить("ПоказатьИнформациюОПравеПримененияСпецрежима",
			?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено),
			ЭтотОбъект);
		
		ПодключитьОбработчикОжидания("ПоказатьПредупреждениеПоРаботеСМаркетплейсами", 0.1, Истина);
		
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
		
	ИначеЕсли ИмяСобытия = "ПоказатьИнформациюОПравеПримененияСпецрежима" Тогда
		
		Если Источник <> ЭтотОбъект И (Не ОтборОрганизацияИспользование Или Параметр = ОтборОрганизация) Тогда
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		КонецЕсли;
	
	ИначеЕсли ИмяСобытия = "ИзмененоПредупреждениеПриЗагрузкеВыписки" Тогда
		
		ПоказатьБаннерПредупрежденийПриЗагрузкеИУстановитьОтбор();
		Элементы.Список.Обновить();
		
	ИначеЕсли (ИмяСобытия = "ИзмененоСостояниеИнтеграцииСПлатформойСамозанятые"
				Или ИмяСобытия = "ИзменениеУчетнойПолитики") Тогда
		
		Если ОтборОрганизацияИспользование И ОтборОрганизация = Параметр Тогда
			ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		КонецЕсли;
		УстановитьФункциональныеОпцииФормы();
		
	ИначеЕсли ИмяСобытия = "ИнтеграцияАУСН_ИзмененоСостояние" Тогда
		
		Если ОтборОрганизацияИспользование
			И ОтборОрганизация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация") Тогда
			
			ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
		КонецЕсли;
		
		УстановитьФункциональныеОпцииФормы();
		
	ИначеЕсли ИмяСобытия = "ИзмененаНастройкаОбмена" Тогда
		
		Если ИнтеграцияСБанкамиПодключена Тогда
			ВидимостьЭлементовДиректБанк = ?(ЗначениеЗаполнено(ОсновнойБанковскийСчет),
				ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(ОтборОрганизация, ОсновнойБанковскийСчет),
				"НеПоказывать");
		Иначе
			ВидимостьЭлементовДиректБанк = ?(УОрганизацииТолькоОдинБанковскийСчет,
				ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(ОтборОрганизация, ОсновнойБанковскийСчет),
				"НеПоказывать");
		КонецЕсли;
		
		ПоказатьБаннерДиректБанк(ЭтотОбъект);
		
	ИначеЕсли ИмяСобытия = ИнтеграцияАУСНКлиент.ИмяСобытияОбменССервисом() Тогда
		
		ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 0.1, Истина);
		Если Источник = ЭтотОбъект Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПоказатьУведомленияАУСНОтФНС", 0.1, Истина);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ИнтеграцияАУСНКлиент.ИмяСобытияИзмененияВОшибкахОтправки() Тогда
		
		Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость = Ложь;
		ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 5, Истина);
		
	ИначеЕсли ИмяСобытия = ЧекиНПДКлиент.ИмяСобытияОбновленияСведенийОСформированныхЧеках() Тогда
		
		Элементы.Список.Обновить();
		
	ИначеЕсли ИмяСобытия = "ПростойИнтерфейс_ФункциональностьИзменение"
		Или (ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьПродажиЧерезМаркетплейс") Тогда
		
		ПодключитьОбработчикОжидания("ПоказатьПредупреждениеПоРаботеСМаркетплейсами", 0.1, Истина);
		
	ИначеЕсли Не ВедетсяРозничнаяТорговля Тогда
		
		Если ИмяСобытия = КассовыеСменыКлиентБП.СобытиеВыполняетсяОперацияКассовойСмены() Тогда
			Доступность = Ложь;
		ИначеЕсли ИмяСобытия = КассовыеСменыКлиентБП.СобытиеЗавершиласьОперацияКассовойСмены() Тогда
			Доступность = Истина;
		ИначеЕсли ИмяСобытия = КассовыеСменыКлиентБП.СобытиеОшибкаОткрытияСмены() Тогда
			ПроверитьНаличиеПодключеннойКассовойТехники();
		ИначеЕсли ИмяСобытия = "ИзменениеСтатусаКассовойСмены" Тогда
			ОбновитьБаннерИПодменюКомандУправленияКассовойСменой(Элементы, Ложь, Истина);
		ИначеЕсли ИмяСобытия = "ОбновитьБаннеры_РозничнаяТорговля" Тогда
			ОбновитьБаннерИПодменюКомандУправленияКассовойСменой(Элементы, Истина, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеПанельюПодсказкиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	ПрисоединенныеФайлыБПКлиент.ОбновитьСписокПослеДобавленияФайла(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		ВыборСчетаДляЗагрузкиВыписки(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы И Элементы.ГруппаСостояниеАвтозагрузки.Видимость Тогда
		РаботаСОповещениямиОСостоянииОбменаССервисамиВызовСервера.ЗаписатьДатуЗакрытияБаннера();
	КонецЕсли;
	
	Если ПоказыватьФормуОценкиМК И НЕ ЗавершениеРаботы Тогда
		ОткрытьФорму("ОбщаяФорма.ОценитьПриложение");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьПоступлениеНаРасчетныйСчет(Команда)
	
	КлючеваяОперация = "СозданиеФормыПоступлениеНаРасчетныйСчет";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
	СтруктураОтбора = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	
	ОткрытьФорму("Документ.ПоступлениеНаРасчетныйСчет.ФормаОбъекта",
		Новый Структура("ЗначенияЗаполнения", СтруктураОтбора), ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСписаниеСРасчетногоСчета(Команда)
	
	КлючеваяОперация = "СозданиеФормыСписаниеСРасчетногоСчета";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
	СтруктураОтбора = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	ОткрытьФорму("Документ.СписаниеСРасчетногоСчета.ФормаОбъекта",
		Новый Структура("ЗначенияЗаполнения", СтруктураОтбора), ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПриходныйКассовыйОрдер(Команда)
	
	КлючеваяОперация = "СозданиеФормыПриходныйКассовыйОрдер";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
	СтруктураОтбора = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	ОткрытьФорму("Документ.ПриходныйКассовыйОрдер.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", СтруктураОтбора), ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРасходныйКассовыйОрдер(Команда)
	
	КлючеваяОперация = "СозданиеФормыРасходныйКассовыйОрдер";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
	СтруктураОтбора = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	ОткрытьФорму("Документ.РасходныйКассовыйОрдер.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", СтруктураОтбора), ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзКлиентБанк(Команда)
	
	Если ИнтеграцияСБанкамиПодключена И Не ЕстьБанковскиеСчетаБезИнтеграции Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзКлиентБанкЗавершение", ЭтотОбъект);
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить("ОткрытьНастройки", НСтр("ru = 'Открыть настройки'"));
		СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
		ТекстВопроса = НСтр("ru = 'Для загрузки выписки из другого банка укажите банковский счет в разделе Настройки'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок,, КодВозвратаДиалога.Отмена);
	Иначе
		Организация = ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено);
		ЗагрузитьВыписку(Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетАнализДвиженийДенежныхСредств(Команда)
	
	ОткрытьФормуОтчета("АнализДвиженийДенежныхСредств");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетОстаткиДенежныхСредств(Команда)
	
	ОткрытьФормуОтчета("ОстаткиДенежныхСредств");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоступленияДенежныхСредств(Команда)
	
	ОткрытьФормуОтчета("ПоступленияДенежныхСредств");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетРасходыДенежныхСредств(Команда)
	
	ОткрытьФормуОтчета("РасходыДенежныхСредств");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетАнализПродаж(Команда)
	
	ОткрытьФормуОтчета("ВаловаяПрибыль");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетДоходыИРасходы(Команда)
	
	ОткрытьФормуОтчета("ДоходыРасходы");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетОборотныеСредства(Команда)

	ОткрытьФормуОтчета("ОборотныеСредства");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСравнениеПродажПоКонтрагентам(Команда)
	
	ПараметрыОтчета = Новый Структура;
	
	ПараметрыОтчета.Вставить("КлючВарианта", "СравнениеПродажПоКонтрагентам");
	Уникальность = "Отчет.СравнениеПродаж/КлючВарианта." + ПараметрыОтчета.КлючВарианта;
	
	ПараметрыОтчета.Вставить("КлючПараметровПечати",        Уникальность);
	ПараметрыОтчета.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
	
	ОткрытьФормуОтчета("СравнениеПродаж", ПараметрыОтчета);
	
КонецПроцедуры

#Область КомандыУправленияКассовойСменой

&НаКлиенте
Процедура ОткрытьКассовуюСмену(Команда)
	
	Если ДоступнаРаботаСКассовойСменой() Тогда
		ОчиститьСообщения();
		КассовыеСменыКлиентБП.НачатьОткрытиеКассовойСмены(УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетОТекущемСостоянииРасчетов(Команда)
	
	Если ДоступнаРаботаСКассовойСменой() Тогда
		ОчиститьСообщения();
		Доступность = Ложь;
		
		ПараметрыОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыВыполненияОперации();
		КассовыеСменыВызовСервераБП.ЗаполнитьПараметрыОперацииДляОрганизацииПоУмолчанию(ПараметрыОперации);
		
		ОповещениеЗавершения = Новый ОписаниеОповещения("ОперацияЗавершение", ЭтотОбъект);
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФормированиеОтчетаОТекущемСостоянииРасчетов(
			ОповещениеЗавершения, УникальныйИдентификатор, ,ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетБезГашения(Команда)
	
	Если ДоступнаРаботаСКассовойСменой() Тогда
		ОчиститьСообщения();
		Доступность = Ложь;
		
		ПараметрыОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыВыполненияОперации();
		КассовыеСменыВызовСервераБП.ЗаполнитьПараметрыОперацииДляОрганизацииПоУмолчанию(ПараметрыОперации);
			
		ОповещениеЗавершения = Новый ОписаниеОповещения("ОперацияЗавершение", ЭтотОбъект);
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФормированиеОтчетаБезГашения(
			ОповещениеЗавершения, УникальныйИдентификатор, ,ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКассовуюСмену(Команда)
	
	Если ДоступнаРаботаСКассовойСменой() Тогда
		ОчиститьСообщения();
		КассовыеСменыКлиентБП.ЗакрытьСмену(УникальныйИдентификатор, Список.КомпоновщикНастроек);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодключаемоеОборудование(Команда)
	
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.ФормаСписка", , , , , ,
		Новый ОписаниеОповещения("ОповещениеОбновленияБаннера", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВидовОплат(Команда)
	
	ОткрытьФорму("Справочник.ВидыОплатОрганизаций.ФормаСписка", , , , , ,
		Новый ОписаниеОповещения("ОповещениеОбновленияБаннера", ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИзменитьВидимостьПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	УИДЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Ложь, "ОтборПоОрганизациямВЖурналеВыпискиПоБанку");
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
	ОтборОрганизацияПриИзмененииСервер();
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.3, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_ПоказатьБаннерДиректБанк", 3, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
	Если ОтборОрганизацияИспользование Тогда
		СкрытьРезультатСозданияДокументов(ЭтотОбъект);
	КонецЕсли;
	
	ПоказатьБаннерПредупрежденийПриЗагрузкеИУстановитьОтбор();
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.3, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстИнформацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьПозжеНажатие(Элемент)
	
	ОтложитьПоказНапоминанияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееНажатие(Элемент)
	
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстДополнительнойИнформацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияНапомнитьПозжеНажатие(Элемент)
	
	ОтложитьПоказДополнительногоНапоминанияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияПодробнееНажатие(Элемент)
	
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект, СсылкаНаДополнительноеПояснение);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияТекстРекламыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПодключитьДиректБанк" Тогда
		
		СтандартнаяОбработка = Ложь;
		Банк = ЗначениеРеквизитаОбъекта(ОсновнойБанковскийСчет, "Банк");
		ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(ОтборОрганизация, Банк);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСкрытьРекламуДиректБанкНажатие(Элемент)
	
	Банк = ЗначениеРеквизитаОбъекта(ОсновнойБанковскийСчет, "Банк");
	ЭлектронноеВзаимодействиеБПВызовСервера.СохранитьНастройкуВидимостиРекламыДиректБанк(Банк, Ложь);
	ВидимостьЭлементовДиректБанк = "ПоказатьГиперссылку";
	ПоказатьБаннерДиректБанк(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтбораОшибокЗагрузкиПриИзменении(Элемент)
	
	УстановитьОтборПредупрежденийПриЗагрузке(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, ОтборОрганизация);
	
КонецПроцедуры
&НаКлиенте
Процедура ДекорацияСкрытьНажатиеТекстКонтекстнаяПодсказкиИнтеграцияСБанком(Элемент)
	
	СкрытьНажатиеТекстКонтекстнаяПодсказкиИнтеграцияСБанком();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

#Область Панель_Подсказки

&НаКлиенте
Процедура НавигацияВПрограммеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.НавигацияВПрограммеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяФункциональностьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.ДополнительнаяФункциональностьОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылка,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе4ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ТекстПредупреждениеПоРаботеСМаркетплейсамиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ГруппаТорговля" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("ТекущаяСтраница", НавигационнаяСсылкаФорматированнойСтроки);
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСкрытьПредупреждениеПоРаботеСМаркетплейсамиНажатие(Элемент)
	
	СкрытьПредупреждениеПоРаботеСМаркетплейсами();
	Элементы.ГруппаПредупреждениеПоРаботеСМаркетплейсами.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ДанныеСтроки = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеСтроки.Ссылка) = Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет") Тогда
		КлючеваяОперация = "ОткрытиеФормыПоступлениеНаРасчетныйСчет";
	ИначеЕсли ТипЗнч(ДанныеСтроки.Ссылка) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		КлючеваяОперация = "ОткрытиеФормыСписаниеСРасчетногоСчета";
	ИначеЕсли ТипЗнч(ДанныеСтроки.Ссылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		КлючеваяОперация = "ОткрытиеФормыПриходныйКассовыйОрдер";
	ИначеЕсли ТипЗнч(ДанныеСтроки.Ссылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		КлючеваяОперация = "ОткрытиеФормыРасходныйКассовыйОрдер";
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	Оповестить("ИзменениеВыписки");
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	Оповестить("ПоказатьИнформациюОПравеПримененияСпецрежима",
		?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено),
		ЭтотОбъект);
	ПодключитьОбработчикОжидания("ПоказатьПредупреждениеПоРаботеСМаркетплейсами", 0.1, Истина);
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка <> Неопределено Тогда
		
		Если ПрисоединенныеФайлыБПКлиент.ПараметрыПеретаскиванияСодержатФайлы(ПараметрыПеретаскивания) Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Ссылка"                 , Строка);
			ДополнительныеПараметры.Вставить("ПараметрыПеретаскивания", ПараметрыПеретаскивания);
			ДополнительныеПараметры.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПеретаскиваниеФайловОтветПолучен",
				ПрисоединенныеФайлыБПКлиент,
				ДополнительныеПараметры);
			ШаблонВопроса = НСтр("ru='Присоединить файлы к документу %1?'");
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонВопроса, Строка);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ОтборыСписков.СброситьИспользованиеПользовательскихОтборовВНастройке(Настройки);
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Отборы

&НаСервере
Процедура УстановитьОтборыПриСозданииНаСервере(Параметры)
	
	УстановитьВариантОтбораПредупрежденийПриЗагрузкеИзПараметров(Параметры);
	
	ВходящийОтборПоОрганизации = Ложь;
	СтруктураОтбора = Неопределено;
	Если Параметры.Свойство("Отбор", СтруктураОтбора) И ТипЗнч(СтруктураОтбора) = Тип("Структура") Тогда
		ВходящийОтборПоОрганизации = СтруктураОтбора.Свойство("Организация", ОтборОрганизация);
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		
		ЗаполнитьОтборПриОткрытииИзПараметров(Параметры.Отбор);
		
	КонецЕсли;
	
	ОсновнаяОрганизация = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	Если НЕ ВходящийОтборПоОрганизации И ОтборОрганизация <> ОсновнаяОрганизация Тогда
		ОтборОрганизация              = ОсновнаяОрганизация;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	КонецЕсли;
	
	ОтборОрганизацияПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтборПриОткрытииИзПараметров(Отбор)
	
	// Нужно переложить отборы из параметров в отдельную структуру,
	// которую потом будем использовать в ПриЗагрузкеДанныхИзНастроекНаСервере
	// Так как мы устанавливаем отбор самостоятельно, то нужно очистить те поля
	// структуры "Параметры.Отбор", для которых установлен отбор из кода.
	// Если не очистить поля - то будет вызвана ошибка пересечения отборов.
	
	ОтборыПриОткрытии = Новый Структура;
	
	Если Отбор.Свойство("Организация")
		И ЗначениеЗаполнено(Отбор.Организация) Тогда
		
		ОтборыПриОткрытии.Вставить("Организация", Отбор.Организация);
		Отбор.Удалить("Организация");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменениеСоставаБанковскихСчетовОрганизации()
	
	УстановитьОтборыПриСозданииНаСервере(Новый Структура);
	
	Если ЕстьБанковскиеСчетаБезИнтеграции Тогда
		ВидимостьКонтекстнаяПодсказкаИнтеграцияСБанком = Ложь;
		БанкИКассаФормы.ПоказатьКонтекстнаяПодсказкаИнтеграцияСБанком(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаВыписки

&НаКлиенте
Процедура ЗагрузитьВыписку(Организация, БанковскийСчет = Неопределено)
	
	СоглашениеЭД = Неопределено;
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		БанковскийСчет = ОсновнойБанковскийСчет;
	КонецЕсли;
	
	НастройкиБанковскогоСчета = ЭлектронноеВзаимодействиеБПВызовСервера.НастройкиКлиентБанкаДляБанковскогоСчета(
		Организация, ?(Организация <> Неопределено, БанковскийСчет, Неопределено));
	Если ИнтеграцияСБанкамиПодключена И ЕстьБанковскиеСчетаБезИнтеграции Тогда
		НастройкиБанковскогоСчета.Вставить("РежимИнтеграцииОграничиватьПериодВыписки", Истина);
	КонецЕсли;
	
	РежимЗагрузки = "";
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиБанковскогоСчета, "ЭтоЦифровойСчет", Ложь) Тогда
		РежимЗагрузки = "ЦифровойСчет";
	ИначеЕсли ЗначениеЗаполнено(ОтборОрганизация) И ЗначениеЗаполнено(СчетЦифровогоРубля) Тогда
		РежимЗагрузки = "Универсальная";
	КонецЕсли;
	Если НастройкиБанковскогоСчета.ПодключенСервисАУСН Или
		НастройкиБанковскогоСчета.ПередЗагрузкойПоказыватьФормуОбменаСБанком Тогда
		Если ЗначениеЗаполнено(БанковскийСчет) Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("БанковскийСчет",  БанковскийСчет);
			ПараметрыФормы.Вставить("РежимЗагрузки",   РежимЗагрузки);
		Иначе
			ПараметрыФормы = Неопределено;
		КонецЕсли;
		ОткрытьФормуКлиентБанка(ПараметрыФормы);
	ИначеЕсли НастройкиБанковскогоСчета.ЕстьПрямойОбменСБанками Тогда
		СоглашениеЭД = НастройкиБанковскогоСчета.СоглашениеПрямогоОбменаСБанками;
		ЗагружатьВыпискуПоВсемСчетамНастройка = Неопределено;
		Если ЗначениеЗаполнено(СоглашениеЭД) Тогда
			ЗагружатьВыпискуПоВсемСчетамНастройка =
				НастройкиБанковскогоСчета.ПользовательскиеНастройки.ЗагружатьВыпискуПоВсемСчетам[СоглашениеЭД];
		КонецЕсли;
		
		// В ЗагружатьВыпискуПоВсемСчетамНастройка может быть Неопределено - это значит,
		// что вариант загрузки, для данной настройки обмена с банком, еще не задан пользователем.
		ЗагружатьВыпискуПоВсемСчетам =
			ЗначениеЗаполнено(ЗагружатьВыпискуПоВсемСчетамНастройка) И ЗагружатьВыпискуПоВсемСчетамНастройка;
		Если ЗначениеЗаполнено(СоглашениеЭД) Тогда
			Если ЗагружатьВыпискуПоВсемСчетам Тогда
				НастройкиБанковскогоСчета.БанковскийСчет = Неопределено;
			ИначеЕсли ЗначениеЗаполнено(СоглашениеЭД) И ЗагружатьВыпискуПоВсемСчетамНастройка = Неопределено
				И ЗначениеЗаполнено(БанковскийСчет)
				И ПолучитьФункциональнуюОпциюФормы("ИспользоватьНесколькоБанковскихСчетовОрганизации") Тогда
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("Настройка", НастройкиБанковскогоСчета.СоглашениеПрямогоОбменаСБанками);
				ПараметрыНастройки = Новый Структура("ПараметрыНастройки", ПараметрыФормы);
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьПослеНастройкиЗавершение", ЭтотОбъект, НастройкиБанковскогоСчета);
				
				ОткрытьФорму("Обработка.КлиентБанк.Форма.ФормаНастройкиЗагрузки",
					ПараметрыНастройки, ЭтотОбъект,,,, ОписаниеОповещения);
				
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ЗагрузитьВыпискуПоПрямомуОбмену(НастройкиБанковскогоСчета);
	Иначе
		ЗагрузитьВыпискуЧерезФайл(
			НастройкиБанковскогоСчета.ФайлЗагрузки, НастройкиБанковскогоСчета.Кодировка, РежимЗагрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборСчетаДляЗагрузкиВыписки(ВыбранныйБанковскийСчет)
	
	Если ВыбранныйБанковскийСчет = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если НЕ ОтборОрганизацияИспользование ИЛИ НЕ ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизация              = ПолучитьРеквизитСчета(ВыбранныйБанковскийСчет, "Владелец");
		ОтборОрганизацияИспользование = Истина;
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	КонецЕсли;
	
	// Загрузим выписку по выбранному счету
	ЗагрузитьВыписку(ОтборОрганизация, ВыбранныйБанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлВыпискиНаКлиенте(ОписаниеФайла, Кодировка = Неопределено, РежимЗагрузки = "") Экспорт
	
	ДлительнаяОперация = ЗагрузитьБанковскуюВыпискуНаСервере(ОписаниеФайла, Кодировка, РежимЗагрузки);
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	ИначеЕсли ДлительнаяОперация.Статус = "ОшибкаЗагрузкиЦР" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация, Истина);
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ТекстСообщения = НСтр("ru = 'Загрузка выписки из банка'");
		
		Обработчик = Новый ОписаниеОповещения("ПослеЗагрузкиБанковскойВыпискиВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузкиБанковскойВыпискиВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		РезультатЗагрузки = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		ОбработатьЗагрузкуБанковскойВыписки(РезультатЗагрузки);
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация, ОшибкаЗагрузкиЦР = Ложь)
	
	ШаблонСообщения = НСтр("ru = 'При выполнении операции произошла ошибка:
							|%1
							|Подробности в журнале регистрации.'");
	Если ОшибкаЗагрузкиЦР Тогда
		СообщениеОбОшибке = ДлительнаяОперация.КраткоеПредставлениеОшибки;
	ИначеЕсли ЗначениеЗаполнено(ДлительнаяОперация.КраткоеПредставлениеОшибки) Тогда
		СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, ДлительнаяОперация.КраткоеПредставлениеОшибки);
	ИначеЕсли ЗначениеЗаполнено(ДлительнаяОперация.ПодробноеПредставлениеОшибки) Тогда
		СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, ДлительнаяОперация.ПодробноеПредставлениеОшибки);
	Иначе
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьБанковскуюВыпискуНаСервере(ОписаниеФайла, Кодировка, РежимЗагрузки = "")
	
	СтруктураПараметров = Новый Структура;
	
	Если РасширениеРаботыСФайламиПодключено
		И ТипЗнч(ОписаниеФайла) = Тип("ОписаниеПереданногоФайла")
		И ЗначениеЗаполнено(ОписаниеФайла.ПолноеИмя) Тогда
		СтруктураПараметров.Вставить("ФайлЗагрузки", ОписаниеФайла.ПолноеИмя);
	Иначе
		СтруктураПараметров.Вставить("ФайлЗагрузки", ОписаниеФайла.Имя);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("АдресХранилищаРаспознанныеДанныеИзБанка",
		ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор));
	СтруктураПараметров.Вставить("Кодировка", ?(РежимЗагрузки = "ЦифровойСчет", "UTF-8", Кодировка));
	СтруктураПараметров.Вставить("РежимЗагрузки", РежимЗагрузки);
	Если Не ПустаяСтрока(ОписаниеФайла.Хранение) Тогда
		СтруктураПараметров.Вставить("ДвоичныеДанные", ПолучитьИзВременногоХранилища(ОписаниеФайла.Хранение));
	КонецЕсли;
	
	Результат = ЗагрузитьБанковскуюВыпискуНаСервереОбщая(СтруктураПараметров);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьЗагрузкуБанковскойВыписки(РезультатЗагрузки)
	
	Для Каждого ТекстСообщения Из РезультатЗагрузки.СообщенияПользователю Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЦикла;
	
	Если РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиЧтенияФайла" Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Не удалось прочитать файл'"));
		
	ИначеЕсли РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиРаспознавания" ИЛИ
		РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиСозданияКонтрагентов" ИЛИ
		РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиСозданияДокументов" ИЛИ
		РезультатЗагрузки.РезультатВыполнения = "НеобходимоСоздатьКонтрагентов" Тогда
		
		Если РезультатЗагрузки.Свойство("Организация") Тогда
			ОтборОрганизация = РезультатЗагрузки.Организация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(РезультатЗагрузки.Организация);
			ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		КонецЕсли;
		
		ПараметрыФормыКлиентБанка = Новый Структура;
		ПараметрыФормыКлиентБанка.Вставить("АдресХранилищаРаспознанныеДанныеИзБанка", РезультатЗагрузки.АдресХранилищаРаспознанныеДанныеИзБанка);
		ПараметрыФормыКлиентБанка.Вставить("СообщенияПользователюВФормеОбменСБанком", РезультатЗагрузки.СообщенияПользователюВФормеОбменСБанком);
		ПараметрыФормыКлиентБанка.Вставить("НачалоПериода",                           ПериодНачало);
		ПараметрыФормыКлиентБанка.Вставить("КонецПериода",                            ПериодОкончание);
		ПараметрыФормыКлиентБанка.Вставить("СоглашениеЭД",                            СоглашениеЭД);
		ПараметрыФормыКлиентБанка.Вставить("ЭлектроннаяВыпискаБанка",                 ВыпискиБанка);
		
		Если РезультатЗагрузки.Свойство("ИмяФайла") Тогда
			ПараметрыФормыКлиентБанка.Вставить("ФайлЗагрузки", РезультатЗагрузки.ИмяФайла);
		ИначеЕсли РезультатЗагрузки.Свойство("АдресВыписки") Тогда
			ПараметрыФормыКлиентБанка.Вставить("АдресВыписки", РезультатЗагрузки.АдресВыписки);
		КонецЕсли;
		
		ОткрытьФормуКлиентБанка(ПараметрыФормыКлиентБанка);
		
	ИначеЕсли РезультатЗагрузки.РезультатВыполнения = "УспешнаяЗагрузка" Тогда
		
		Если РезультатЗагрузки.Свойство("Организация") Тогда
			ОтборОрганизация = РезультатЗагрузки.Организация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(РезультатЗагрузки.Организация);
			ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		КонецЕсли;
		
		Элементы.Список.Обновить();
		
		Если РезультатЗагрузки.Свойство("РезультатСозданияДокументов") Тогда
			ВалютаСчета = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗагрузки, "ВалютаСчета");
			ПоказатьРезультатСозданияДокументов(РезультатЗагрузки.РезультатСозданияДокументов, ВалютаСчета);
			
			ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплатуПокупателю"));
		КонецЕсли;
		
		Оповестить("ИзменениеВыписки");
		
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
		ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		Оповестить("ПоказатьИнформациюОПравеПримененияСпецрежима",
			?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено),
			ЭтотОбъект);
		
		ПодключитьОбработчикОжидания("ПоказатьПредупреждениеПоРаботеСМаркетплейсами", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКлиентБанка(ПараметрыФормы = Неопределено) Экспорт
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("РежимПоУмолчанию", "ГруппаЗагрузка");
	Если ОтборОрганизацияИспользование И ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ПараметрыФормы.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если Не ПараметрыФормы.Свойство("РежимЗагрузки") Тогда
		ПараметрыФормы.Вставить("РежимЗагрузки", "");
	КонецЕсли;
	
	БанковскийСчет = Неопределено;
	ПараметрыФормы.Свойство("БанковскийСчет", БанковскийСчет);
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		БанковскийСчет = ОсновнойБанковскийСчет;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		ПараметрыФормы.Вставить("БанковскийСчет", БанковскийСчет);
		Если БанковскийСчет = СчетЦифровогоРубля Тогда
			ПараметрыФормы.РежимЗагрузки = "ЦифровойСчет";
		КонецЕсли
	КонецЕсли;
	
	ОткрытьФорму("Обработка.КлиентБанк.Форма.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#Область ПрямойОбменСБанком

&НаКлиенте
Процедура ЗагрузитьВыпискуПоПрямомуОбмену(НастройкиБанковскогоСчета)
	
	// Счет определен, начинаем получение выписки
	Если ЗначениеЗаполнено(НастройкиБанковскогоСчета.БанковскийСчет)
		Или ЗначениеЗаполнено(НастройкиБанковскогоСчета.БанкДляПрямогоОбмена) Тогда
		ПолучитьВыпискиПоПрямомуОбменуСБанком(НастройкиБанковскогоСчета);
		
	// Счет не определен, спросим у пользователя по какому счету необходимо производить загрузку.
	// Может быть выбран счет по которому не подключен прямой обмен.
	// В этом случае производим загрузку через файл
	Иначе
		ПараметрыФормы = Новый Структура;
		СтруктураОтбора = Новый Структура;
		
		Владелец = НастройкиБанковскогоСчета.Организация;
		Если Владелец = Неопределено Тогда
			// Типизируем отбор по владельцу, если он пустой
			Владелец = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		КонецЕсли;
		
		СтруктураОтбора.Вставить("Владелец", Владелец);
		Если ИнтеграцияСБанкамиПодключена И ЕстьБанковскиеСчетаБезИнтеграции Тогда
			СтруктураОтбора.Вставить("Ссылка", БанковскиеСчетаБезИнтеграции.Счета);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
		ОткрытьФорму("Справочник.БанковскиеСчета.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВыпискиПоПрямомуОбменуСБанком(НастройкиБанковскогоСчета)
	
	НомерСчета = ПолучитьРеквизитСчета(НастройкиБанковскогоСчета.БанковскийСчет, "НомерСчета");
	
	// Необходимо определить период загрузки
	СтруктураПериода = ПериодЗагрузкиВыписки(НастройкиБанковскогоСчета,
		НастройкиБанковскогоСчета.РежимИнтеграцииОграничиватьПериодВыписки);
	
	БанкИКассаФормыКлиент.ПолучитьВыпискуБанка(ЭтотОбъект, НастройкиБанковскогоСчета, СтруктураПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВыпискиПоПрямомуОбменуСБанкомЗавершение(ПараметрыФормы) Экспорт
	
	ОткрытьФормуКлиентБанка(ПараметрыФормы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПериодЗагрузкиВыписки(Знач НастройкиБанковскогоСчета, Знач РежимИнтеграцииОграничиватьПериодВыписки)
	
	Возврат Обработки.КлиентБанк.ПериодЗагрузкиВыписки(НастройкиБанковскогоСчета, РежимИнтеграцииОграничиватьПериодВыписки);
	
КонецФункции

#КонецОбласти

#Область ОбменЧерезФайл

&НаКлиенте
Процедура ЗагрузитьВыпискуЧерезФайл(ФайлЗагрузки, Кодировка, РежимЗагрузки)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ФайлЗагрузки",    ФайлЗагрузки);
	ДополнительныеПараметры.Вставить("Кодировка",       Кодировка); 
	ДополнительныеПараметры.Вставить("РежимЗагрузки",   РежимЗагрузки); 
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьВыпискуЧерезФайлЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВыпискуЧерезФайлЗавершение(ПодключеноРасширениеРаботыСФайлами, ДополнительныеПараметры) Экспорт
	
	РасширениеРаботыСФайламиПодключено = ПодключеноРасширениеРаботыСФайлами;
	РежимЗагрузки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "РежимЗагрузки", "");
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.ФайлЗагрузки) И РасширениеРаботыСФайламиПодключено Тогда
		// Файл определен - можно начинаем чтение
		ОбменСБанкомКлиент.ЗагрузитьВыбранныйФайл(
			ДополнительныеПараметры.ФайлЗагрузки,
			ЭтотОбъект,
			ДополнительныеПараметры.Кодировка,
			РежимЗагрузки);
	Иначе
		// Файл не определен - запросим файл у пользователя
		Если Не ПустаяСтрока(РежимЗагрузки) Тогда
			ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект,, РежимЗагрузки);
		Иначе
			ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект, ДополнительныеПараметры.Кодировка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедуры

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	СкрытьРезультатСозданияДокументов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СкрытьРезультатСозданияДокументов(Форма)
	
	Элементы = Форма.Элементы;
	Если Элементы.ГруппаСостояниеЗагрузкиИзКлиентБанка.Видимость Тогда
		Элементы.ГруппаСостояниеЗагрузкиИзКлиентБанка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатСозданияДокументов(РезультатСозданияДокументов, ВалютаСчета)
	
	Элементы.ГруппаСостояниеЗагрузкиИзКлиентБанка.Видимость = Истина;
	Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Ложь;
	Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость = Ложь;
	
	Если НЕ РезультатСозданияДокументов.Свойство("КоличествоЗагружено") Тогда
		Элементы.ТекстСостояниеЗагрузки.Заголовок = НСтр("ru='Новых документов для загрузки не обнаружено'");
	Иначе
		ШаблонТекста = НСтр("ru = 'Загружено: [КоличествоДокументов]. Поступило: [СуммаПоступило] Списано: [СуммаСписано]'");
		
		СтрокаКоличествоДокументов = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(РезультатСозданияДокументов.КоличествоЗагружено, "документ, документа, документов");
		СтрокаСуммаПоступило       = ОбщегоНазначенияБПВызовСервера.ФорматСумм(РезультатСозданияДокументов.СуммаПоступило, ВалютаСчета, "0,00");
		СтрокаСуммаПоступило       = ?((Прав(СтрокаСуммаПоступило, 1) = "."), СтрокаСуммаПоступило, СтрокаСуммаПоступило + ".");
		СтрокаСуммаСписано         = ОбщегоНазначенияБПВызовСервера.ФорматСумм(РезультатСозданияДокументов.СуммаСписано, ВалютаСчета, "0,00");
		СтрокаСуммаСписано         = ?((Прав(СтрокаСуммаСписано, 1) = "."),   СтрокаСуммаСписано,   СтрокаСуммаСписано + ".");
		
		ВставляемыеЗначения = Новый Структура;
		ВставляемыеЗначения.Вставить("КоличествоДокументов", СтрокаКоличествоДокументов);
		ВставляемыеЗначения.Вставить("СуммаПоступило",       СтрокаСуммаПоступило);
		ВставляемыеЗначения.Вставить("СуммаСписано",         СтрокаСуммаСписано);
		Элементы.ТекстСостояниеЗагрузки.Заголовок = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ВставляемыеЗначения);
		
		Если ЗначениеЗаполнено(РезультатСозданияДокументов.ПервыйЗагруженный) Тогда
			Элементы.Список.ТекущаяСтрока = РезультатСозданияДокументов.ПервыйЗагруженный;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитСчета(Знач БанковскийСчет, Знач ИмяРеквизита)
	
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, ИмяРеквизита);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура СкрытьСостояниеАвтозагрузкиНажатие(Элемент)
	
	Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Не Элементы.ГруппаСостояниеАвтозагрузки.Видимость;
	
	РаботаСОповещениямиОСостоянииОбменаССервисамиВызовСервера.ЗаписатьДатуЗакрытияБаннера();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьСостояниеАвтозагрузкиОшибкаНажатие(Элемент)
	
	Элементы.ГруппаСостояниеАвтоЗагрузкиОшибка.Видимость = Не Элементы.ГруппаСостояниеАвтоЗагрузкиОшибка.Видимость;
	
	РаботаСОповещениямиОСостоянииОбменаССервисамиВызовСервера.ЗаписатьДатуЗакрытияБаннера();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстСостояниеАвтозагрузкиОшибкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки =
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиентСервер.ИмяДействияОткрытьЖурналРегистрации() Тогда
		СтандартнаяОбработка = Ложь;
		Отбор = Новый Структура("Пользователь, СобытиеЖурналаРегистрации, ДатаНачала, ДатаОкончания, Данные, Сеанс, Уровень");
		Отбор.Пользователь	= Строка(ПользователиКлиентСервер.ТекущийПользователь());
		Отбор.ДатаНачала	= НачалоДня(ИнформацияАвтозагрузкиДиректБанк_ДатаОшибки);
		Отбор.ДатаОкончания	= КонецДня(ИнформацияАвтозагрузкиДиректБанк_ДатаОшибки);
		Отбор.Уровень		= "Ошибка";  
		Отбор.СобытиеЖурналаРегистрации = "Электронное взаимодействие.Обмен с банками.Журнал";
		ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(Отбор, ЭтотОбъект);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки =
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиентСервер.ИмяДействияОбращениеВТехподдержку1С() Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиент.НаписатьОбращениеВТехподдержку1СОшибкаАУСН(
			АдресДанныхСостоянияОбмена, УникальныйИдентификатор);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки =
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиентСервер.ИмяДействияИнформацияОбОшибкеАУСН() Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиент.ОткрытьИнформациюОбОшибкеАУСН(
			АдресДанныхСостоянияОбмена, УникальныйИдентификатор);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки =
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиентСервер.ИмяДействияЛичныйКабинетБанка() Тогда
		СтандартнаяОбработка = Ложь;
		ИнтеграцияАУСНКлиент.ОткрытьЛКБанка(АдресДанныхСостоянияОбмена);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки =
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиентСервер.ИмяДействияИсправитьОшибкиОтправкиАУСН() Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСОповещениямиОСостоянииОбменаССервисамиКлиент.ОткрытьОшибкиОтправкиАУСН(
			АдресДанныхСостоянияОбмена, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеобходимоПовторноеЧтениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВариантОтбора", "СОшибками");
	Если ОтборОрганизацияИспользование И ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ПараметрыФормы.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	ОткрытьФорму("РегистрСведений.ДокументыАУСН.Форма.ЖурналОбмена",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗагрузитьИзКлиентБанкЗавершение(ВыбранныйВариант, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйВариант = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке("e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.НастройкиБазовый");
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьВыпискуНаКлиенте(АдресВыписки, Кодировка = Неопределено) Экспорт
	
	ДлительнаяОперация = ЗагрузитьБанковскуюВыпискуДБНаСервере(АдресВыписки, Кодировка);
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ТекстСообщения = НСтр("ru = 'Загрузка выписки из банка'");
		
		Обработчик = Новый ОписаниеОповещения("ПослеЗагрузкиБанковскойВыпискиВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьБанковскуюВыпискуДБНаСервере(АдресВыписки, Кодировка)
	
	Если ПустаяСтрока(АдресВыписки) Или Не ЭтоАдресВременногоХранилища(АдресВыписки) Тогда
		Результат = Новый Структура;
		Результат.Статус = "Ошибка";
		Результат.Вставить("КраткоеПредставлениеОшибки",   "");
		Результат.Вставить("ПодробноеПредставлениеОшибки", "");
		Возврат Результат;
	КонецЕсли;
	
	ВыпискаБанка = ПолучитьИзВременногоХранилища(АдресВыписки);
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("АдресХранилищаРаспознанныеДанныеИзБанка",
		ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор));
	СтруктураПараметров.Вставить("Кодировка",    Кодировка);
	СтруктураПараметров.Вставить("ВыпискаБанка", ВыпискаБанка);
	
	Результат = ЗагрузитьБанковскуюВыпискуНаСервереОбщая(СтруктураПараметров);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗагрузитьБанковскуюВыпискуНаСервереОбщая(СтруктураПараметров)
	
	Элементы.ГруппаСостояниеЗагрузкиИзКлиентБанка.Видимость = Ложь;
	
	СведенияОВладельцеБанковскогоСчетаИзКонтекста = НовыйСведенияОВладельцеБанковскогоСчетаИзКонтекста();
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		СведенияОВладельцеБанковскогоСчетаИзКонтекста.Организация.Добавить(ОтборОрганизация);
		
		РежимЗагрузки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПараметров, "РежимЗагрузки", "");
		
		Если РежимЗагрузки = "ЦифровойСчет" Тогда
			ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
			БанковскиеСчетаЦифровые = Справочники.БанковскиеСчета.БанковскиеСчетаОрганизации(
				ОтборОрганизация, ВалютаРеглУчета, , Истина);
			Если БанковскиеСчетаЦифровые.Количество() > 0 Тогда
				СчетЦифровогоРубля = БанковскиеСчетаЦифровые[0].Ссылка;
				СведенияОВладельцеБанковскогоСчетаИзКонтекста.БанковскийСчетОрганизации.Добавить(СчетЦифровогоРубля);
			Иначе
				Результат = Новый Структура("Статус, КраткоеПредставлениеОшибки");
				Результат.Статус = "ОшибкаЗагрузкиЦР";
				Результат.КраткоеПредставлениеОшибки = НСтр("ru = 'У организации нет счета цифрового рубля'");
				Возврат Результат;
			КонецЕсли;
		ИначеЕсли ИнтеграцияСБанкамиПодключена И ЕстьБанковскиеСчетаБезИнтеграции Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				СведенияОВладельцеБанковскогоСчетаИзКонтекста.БанковскийСчетОрганизации,
				БанковскиеСчетаБезИнтеграции.Счета,
				Истина);
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПараметров.Вставить("СведенияОВладельцеБанковскогоСчетаИзКонтекста",
		СведенияОВладельцеБанковскогоСчетаИзКонтекста);
	Если ИнтеграцияСБанкамиПодключена И ЕстьБанковскиеСчетаБезИнтеграции Тогда
		СтруктураПараметров.Вставить("РежимИнтеграцииОграничиватьПериодВыписки", Истина);
	КонецЕсли;
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка выписки из банка'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.КлючФоновогоЗадания = Обработки.КлиентБанк.КлючФоновогоЗаданияЗагрузкаВыписки();
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.КлиентБанк.ФоноваяЗагрузкаБанковскойВыпискиИзЖурнала",
		СтруктураПараметров,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПослеНастройкиЗавершение(Результат, ДополнительныеНастройки) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат Тогда
		ДополнительныеНастройки.БанковскийСчет = Неопределено;
	КонецЕсли;
	
	ЗагрузитьВыпискуПоПрямомуОбмену(ДополнительныеНастройки);
	
КонецПроцедуры

#КонецОбласти

#Область ФоновоеЗадание

&НаКлиенте
Процедура ЗафиксироватьДлительностьКлючевойОперации()
	
	ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура Подключаемый_ОбновитьИтогиОбработчик()
	
	ДанныеИтогов(ЭтотОбъект);
	ЗафиксироватьДлительностьКлючевойОперации();
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	
	Если ИмеютсяДействующиеПатенты Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяПатент");
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьАвтоматическоеПолучениеВыпискиАктивно()
	
	НастройкиОбмена = НастройкиАвтозагрузкиДиректБанк();
	ЭтотОбъект.АвтоматическоеПолучениеВыпискиАктивно = НастройкиОбмена.Количество() <> 0;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьИнформациюИБаннерыАвтозагрузки()
	
	РезультатОбновленияБаннера = ОбновитьИнформациюИБаннерыАвтозагрузкиНаСервере();
	Если РезультатОбновленияБаннера.Свойство("ОжидатьОкончанияОбмена") Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 10, Истина);
	КонецЕсли;
	Если РезультатОбновленияБаннера.Свойство("ЗакрытьБаннер") Тогда
		ЗакрытьБаннерНажатие("");
	КонецЕсли;
	Если РезультатОбновленияБаннера.Свойство("ОбновитьИтоги") Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьИнформациюИБаннерыАвтозагрузкиНаСервере()
	
	РезультатОбновления = Новый Структура;
	ПоказатьБаннерПредупрежденийПриЗагрузкеИУстановитьОтбор();
	
	Если Не ИнтеграцияАУСН.ВозможноПрочитатьДанныеОбмена() Тогда
		РезультатОбновления.Вставить("ОжидатьОкончанияОбмена", Истина);
		Возврат РезультатОбновления;
	КонецЕсли;
	
	ОбновитьИнформациюАвтозагрузки();
	ПараметрыОтображенияБаннеров = РаботаСОповещениямиОСостоянииОбменаССервисами.ПараметрыОтображенияБаннера(
		АдресДанныхСостоянияОбмена);
	
	// Правила отображения баннеров:
	// 1. Если есть ошибка автоматической загрузки, то отображем красный баннер про ошибку
	//    и прячем желтый баннер об успешной синхронизации (если он активен)
	// 2. Если необходимо повторно прочитать документы сервиса АУСН, то появится красный баннер со ссылкой на
	//    журнал обмена с сервисом АУСН, где можно вызвать повторное чтение документов
	//    и прячем желтый баннер об успешной синхронизации (если он активен)
	// 3. Если есть ошибка по учетным документам, то появится красный баннер про ошибки в учетных документах,
	//    и прячем желтый баннер об успешной синхронизации (если он активен)
	// 4. Если нет ошибок, и баннер про успешную синхронизацию не показан,
	//    то проверяем новую дату синхронизации, если она изменилась, то покажем желтый баннер об успешной синхронизации
	//
	// Правило 1
	Если ПараметрыОтображенияБаннеров.ОтображатьБаннерОшибки Тогда
		Элементы.ГруппаСостояниеАвтоЗагрузкиОшибка.Видимость = Истина;
		Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Ложь; 
		Элементы.ГруппаНеобходимоПовторноеЧтение.Видимость = Ложь;
		ЭтотОбъект.ИнформацияАвтозагрузкиДиректБанк_ТекущаяДатаСинхронизации = Неопределено;
		Если Элементы.Баннер.Видимость Тогда
			РезультатОбновления.Вставить("ЗакрытьБаннер", Истина);
		КонецЕсли;
		Возврат РезультатОбновления;
	Иначе
		Элементы.ГруппаСостояниеАвтоЗагрузкиОшибка.Видимость = Ложь;
		ЭтотОбъект.ИнформацияАвтозагрузкиДиректБанк_ДатаОшибки = Неопределено;
	КонецЕсли;
	
	// Правило 2
	Если ПараметрыОтображенияБаннеров.ОтображатьНеобходимостьПовторногоЧтения Тогда
		Элементы.ГруппаНеобходимоПовторноеЧтение.Видимость = Истина;
		Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость = Ложь;
		Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Ложь;
		Если Элементы.Баннер.Видимость Тогда
			РезультатОбновления.Вставить("ЗакрытьБаннер", Истина);
		КонецЕсли;
	Иначе
		Элементы.ГруппаНеобходимоПовторноеЧтение.Видимость = Ложь;
	КонецЕсли;
	
	// Правило 3
	Если ЕстьПредупреждения Тогда
		Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Ложь;
		Если Элементы.Баннер.Видимость Тогда
			РезультатОбновления.Вставить("ЗакрытьБаннер", Истина);
		КонецЕсли;
		Возврат РезультатОбновления;
	КонецЕсли;
	
	//Правило 4
	Если ПараметрыОтображенияБаннеров.ОтображатьБаннерСостоянияЗагрузки
		И Не Элементы.ГруппаСостояниеАвтозагрузки.Видимость
		И (ИнформацияАвтозагрузкиДиректБанк_ТекущаяДатаСинхронизации <> ИнформацияАвтозагрузки_НоваяДатаСинхронизации)
		И Не ЕстьПредупреждения И ЗначениеЗаполнено(Элементы.ТекстСостояниеАвтозагрузки.Заголовок) Тогда
		Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Истина;
		Элементы.ГруппаНеобходимоПовторноеЧтение.Видимость = Ложь;
		ИнформацияАвтозагрузкиДиректБанк_ТекущаяДатаСинхронизации = ИнформацияАвтозагрузки_НоваяДатаСинхронизации;
		Элементы.Список.Обновить();
		РезультатОбновления.Вставить("Подключаемый_ОбновитьИтогиОбработчик", Истина);
	КонецЕсли;
	
	Возврат РезультатОбновления;
	
КонецФункции

#КонецОбласти

#Область УправлениеФормой

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Организация
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Организация");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизацияИспользование", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизация", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	Если ИнтеграцияСБанкамиПодключена Тогда
		
		// Комментарий
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Комментарий");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ИнтеграцияСБанкамиПодключена", ВидСравненияКомпоновкиДанных.Равно, Истина);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	Иначе
		
		// БанковскийСчет
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "БанковскийСчет");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ИнтеграцияСБанкамиПодключена", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	КонецЕсли;
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Дата");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Доход");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Расход");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СтатусАУСН");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидОперации");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Тип");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Комментарий");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Номер");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Организация");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ЕстьПредупреждения", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.БанковскиеДокументыСПредупреждениямиПриЗагрузке);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДанныеИтогов(Форма)
	
	ТекОрганизация = ?(Форма.ОтборОрганизацияИспользование, Форма.ОтборОрганизация, Неопределено);
	
	СтруктураИтогов = ПолучитьИтоги(ТекОрганизация);
	
	Форма.ИтогДата  = СтруктураИтогов.ИтогДата;
	Форма.ИтогБанк  = СтруктураИтогов.ИтогБанк;
	Форма.ИтогКасса = СтруктураИтогов.ИтогКасса;
	Форма.ИтогВсего = СтруктураИтогов.ИтогВсего;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоМобильныйКлиент() Тогда
		УстановитьЗаголовкиГруппыИтогиМК(Форма.Элементы.ГруппаИтоги, Форма.ИтогДата, Форма.ИтогВсего);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
// Возвращает текущие остатки по кассе и банку
//
Функция ПолучитьИтоги(Знач ТекОрганизация)
	
	ИтогДата = КонецДня(ТекущаяДатаСеанса());
	СтруктураРезультат = Новый Структура("ИтогДата, ИтогБанк, ИтогКасса, ИтогВсего", ИтогДата, 0, 0, 0);
	
	СписокДоступныхОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	// Если нет доступных организаций, то Итоги не вычисляем
	Если СписокДоступныхОрганизаций.Количество() = 0 Тогда
		Возврат СтруктураРезультат;
	ИначеЕсли ЗначениеЗаполнено(ТекОрганизация) Тогда
		// Если организация не числится в списке доступных, то Итоги не вычисляем
		Если СписокДоступныхОрганизаций.Найти(ТекОрганизация) = Неопределено Тогда
			Возврат СтруктураРезультат;
		КонецЕсли;
	КонецЕсли;
	
	СчетаУчетаНаличныхДС = Новый Массив;
	СчетаУчетаНаличныхДС.Добавить(ПланыСчетов.Хозрасчетный.Касса);
	СчетаУчетаНаличныхДС.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизации);
	СчетаУчетаНаличныхДС.Добавить(ПланыСчетов.Хозрасчетный.ОперационнаяКасса);
	СчетаУчетаНаличныхДС.Добавить(ПланыСчетов.Хозрасчетный.КассаПлатежногоАгента);
	СчетаУчетаНаличныхДС.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизацииВал);
	
	СчетаУчетаБезналичныхДС = Новый Массив;
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.ЦифровойРубль);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.СпециальныеСчета);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.Аккредитивы);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.ЧековыеКнижки);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.ДепозитныеСчета);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеСпециальныеСчета);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.АккредитивыВал);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.ДепозитныеСчетаВал);
	СчетаУчетаБезналичныхДС.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеСпециальныеСчетаВал);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаИтогов", Новый Граница(ИтогДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", ТекОрганизация);
	Запрос.УстановитьПараметр("СчетаУчетаНаличныхДС", СчетаУчетаНаличныхДС);
	Запрос.УстановитьПараметр("СчетаУчетаБезналичныхДС", СчетаУчетаБезналичныхДС);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОстатокПоКассе.ВалютнаяСуммаОстаток <> 0
	|			ТОГДА ОстатокПоКассе.ВалютнаяСуммаОстаток
	|		ИНАЧЕ ОстатокПоКассе.СуммаОстаток
	|	КОНЕЦ КАК ИтогКасса
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаИтогов, Счет В (&СчетаУчетаНаличныхДС), , %ОтборПоОрганизации%) КАК ОстатокПоКассе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОстатокПоБанку.ВалютнаяСуммаОстаток <> 0
	|			ТОГДА ОстатокПоБанку.ВалютнаяСуммаОстаток
	|		ИНАЧЕ ОстатокПоБанку.СуммаОстаток
	|	КОНЕЦ КАК ИтогБанк
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаИтогов, Счет В (&СчетаУчетаБезналичныхДС), , %ОтборПоОрганизации%) КАК ОстатокПоБанку";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоОрганизации%",
		?(ЗначениеЗаполнено(ТекОрганизация), "Организация = &Организация", ""));
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// Касса
	Если НЕ РезультатЗапроса[0].Пустой() Тогда
		Выборка = РезультатЗапроса[0].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, Выборка);
	КонецЕсли;
	
	// Банк
	Если НЕ РезультатЗапроса[1].Пустой() Тогда
		Выборка = РезультатЗапроса[1].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, Выборка);
	КонецЕсли;
	
	СтруктураРезультат.ИтогВсего = СтруктураРезультат.ИтогКасса + СтруктураРезультат.ИтогБанк;
	СтруктураРезультат.ИтогДата = ИтогДата;
	
	Возврат СтруктураРезультат;
	
КонецФункции

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
	ПоказатьБаннерПредупрежденийПриЗагрузкеИУстановитьОтбор();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРежимИнтеграцииСБанком()
	
	// Интеграция подключена, значит основной банковский счет организации - это счет интеграции.
	БанковскийСчетОрганизации = ЗначениеРеквизитаОбъекта(ОтборОрганизация, "ОсновнойБанковскийСчет");
	Если Не ЗначениеЗаполнено(БанковскийСчетОрганизации) Тогда
		БанковскиеСчетаИнтеграции = Справочники.НастройкиИнтеграцииСБанками.БанковскиеСчетаОрганизацииВРежимеИнтеграции(
			ОтборОрганизация);
		Если БанковскиеСчетаИнтеграции.Количество() > 0 Тогда
			БанковскийСчетОрганизации = БанковскиеСчетаИнтеграции[0];
		КонецЕсли;
	КонецЕсли;
	
	БанкИнтеграции = ЗначениеРеквизитаОбъекта(БанковскийСчетОрганизации, "Банк");
	Если ЗначениеЗаполнено(БанкИнтеграции) Тогда
		РеквизитыБанкаИнтеграции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БанкИнтеграции,
			"Наименование, Код");
		НазваниеБанкаИнтеграции  = РеквизитыБанкаИнтеграции.Наименование;
		БИКБанкаИнтеграции       = РеквизитыБанкаИнтеграции.Код;
	КонецЕсли;
	
	Если Не ЕстьБанковскиеСчетаБезИнтеграции Тогда
		ВидимостьИзНастроек = ХранилищеОбщихНастроек.Загрузить(
			"ВидимостьКонтекстнаяПодсказкаИнтеграцияСБанком", БИКБанкаИнтеграции);
		ВидимостьКонтекстнаяПодсказкаИнтеграцияСБанком = ?(ВидимостьИзНастроек = Неопределено, Истина, ВидимостьИзНастроек);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ПометкаУдаления", Ложь,,, Истина);
	
	ВидимостьЭлементовДиректБанк = ?(ЗначениеЗаполнено(ОсновнойБанковскийСчет),
		ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(ОтборОрганизация, ОсновнойБанковскийСчет),
		"НеПоказывать");
	
	БанкИКассаФормы.НастроитьФормуЖурналаДляРежимаИнтеграции(ЭтотОбъект, ИнтеграцияСБанкамиПодключена);
	БанкИКассаФормы.ПоказатьКонтекстнаяПодсказкаИнтеграцияСБанком(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимостьПодсказокНаСервере()
	
	УправлениеПанельюПодсказки.ИзменитьВидимостьПодсказок(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ПравоПримененияСпецрежима

&НаКлиенте
Процедура ПоказатьИнформациюОПравеПримененияСпецрежима()
	
	Если Не ОтборОрганизацияИспользование
		Или Не КонтрольПраваПримененияСпецрежимаКлиент.Контролировать(ОтборОрганизация) Тогда
		Элементы.ИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	Иначе
		ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере()
	
	ИнформацияОПравеПримененияСпецрежима = КонтрольПраваПримененияСпецрежима.ИнформацияОПравеПримененияСпецрежима(
		ОтборОрганизация,
		КонтрольПраваПримененияСпецрежима.ИмяПоказателяСпецрежимаДоходы());
		
	СледующееЗначениеНапоминания         = ИнформацияОПравеПримененияСпецрежима.СледующееЗначениеНапоминания;
	СсылкаНаПояснение                    = ИнформацияОПравеПримененияСпецрежима.СсылкаНаПояснение;
	
	Элементы.ИнформацияОПравеПримененияСпецрежима.Видимость = ИнформацияОПравеПримененияСпецрежима.Показать;
	Элементы.ИнформацияОПравеПримененияСпецрежима.ЦветФона = ИнформацияОПравеПримененияСпецрежима.ЦветФонаГруппы;
	Элементы.ТекстИнформации.Заголовок = ИнформацияОПравеПримененияСпецрежима.ТекстИнформации;
	Элементы.ТекстИнформацииРасширеннаяПодсказка.Заголовок = ИнформацияОПравеПримененияСпецрежима.ПодсказкаТекстИнформации;
	
	Элементы.Подробнее.Видимость       = ЗначениеЗаполнено(СсылкаНаПояснение);
	Элементы.Подробнее.Заголовок       = ИнформацияОПравеПримененияСпецрежима.ЗаголовокПояснения;
	
	Элементы.НапомнитьПозже.Заголовок  = ИнформацияОПравеПримененияСпецрежима.ТекстНапомнитьПозже;
	Элементы.НапомнитьПозже.Видимость  = ИнформацияОПравеПримененияСпецрежима.ОтображатьНапоминание;
	
	ДополнительнаяИнформацияСпецрежима = ИнформацияОПравеПримененияСпецрежима.ДополнительнаяИнформация;
	
	Если ДополнительнаяИнформацияСпецрежима <> Неопределено Тогда
		ИмяДополнительногоПоказателяСпецрежима      = ДополнительнаяИнформацияСпецрежима.ИмяПоказателя;
		СледующееЗначениеДополнительногоНапоминания = ДополнительнаяИнформацияСпецрежима.СледующееЗначениеНапоминания;
		СсылкаНаДополнительноеПояснение             = ДополнительнаяИнформацияСпецрежима.СсылкаНаПояснение;
		
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = ДополнительнаяИнформацияСпецрежима.Показать;
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.ЦветФона = ДополнительнаяИнформацияСпецрежима.ЦветФонаГруппы;
		Элементы.ТекстДополнительнойИнформации.Заголовок = ДополнительнаяИнформацияСпецрежима.ТекстИнформации;
		Элементы.ТекстДополнительнойИнформацииРасширеннаяПодсказка.Заголовок = ДополнительнаяИнформацияСпецрежима.ПодсказкаТекстИнформации;
		
		Элементы.ДополнительнаяИнформацияПодробнее.Видимость = ЗначениеЗаполнено(СсылкаНаДополнительноеПояснение);
		Элементы.ДополнительнаяИнформацияПодробнее.Заголовок = ДополнительнаяИнформацияСпецрежима.ЗаголовокПояснения;
		
		Элементы.ДополнительнаяИнформацияНапомнитьПозже.Заголовок = ДополнительнаяИнформацияСпецрежима.ТекстНапомнитьПозже;
		Элементы.ДополнительнаяИнформацияНапомнитьПозже.Видимость = ДополнительнаяИнформацияСпецрежима.ОтображатьНапоминание;
	Иначе
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	КонецЕсли;
	
	ИмяНастройкиПравоПримененияПатента = КонтрольПраваПримененияСпецрежима.ИмяНастройкиПравоПримененияСпецрежимаПатент();
	ВыводитсяИнформацияОПатенте = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ИнформацияОПравеПримененияСпецрежима,
		ИмяНастройкиПравоПримененияПатента,
		Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ОтложитьПоказНапоминанияНаСервере()
	
	Если ВыводитсяИнформацияОПатенте Тогда
		ИмяПоказателя = КонтрольПраваПримененияСпецрежима.ИмяНастройкиПравоПримененияСпецрежимаПатент();
	Иначе
		ИмяПоказателя = КонтрольПраваПримененияСпецрежима.ИмяПоказателяСпецрежимаДоходы();
	КонецЕсли;
	
	КонтрольПраваПримененияСпецрежима.ОтложитьПоказНапоминания(
		ОтборОрганизация,
		ИмяПоказателя,
		СледующееЗначениеНапоминания);
		
	Элементы.ИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	
	Если ВыводитсяИнформацияОПатенте Тогда
		ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтложитьПоказДополнительногоНапоминанияНаСервере()
	
	КонтрольПраваПримененияСпецрежима.ОтложитьПоказНапоминания(
		ОтборОрганизация,
		ИмяДополнительногоПоказателяСпецрежима,
		СледующееЗначениеДополнительногоНапоминания);
		
	Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ПоказатьБаннерДиректБанк()

	ПоказатьБаннерДиректБанк(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьБаннерДиректБанк(Форма)
	
	Элементы = Форма.Элементы;
	ВидимостьБаннера = Ложь;
	Если Не Элементы.Баннер.Видимость // если не виден баннер персонализированного предложения
		И ЗначениеЗаполнено(Форма.ОсновнойБанковскийСчет)
		И Форма.ВидимостьЭлементовДиректБанк = "ПоказатьБаннер" Тогда 
		
		ВидимостьБаннера = Истина;
		
		Если ЗначениеЗаполнено(Форма.НазваниеБанкаОсновногоБанковскогоСчета) Тогда
			ЧастиБанера = Новый Массив;
			ЧастиБанера.Добавить(Символы.ПС);
			ЧастиБанера.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Обменивайтесь с банком'"), Новый Шрифт(Новый Шрифт,, 14)));
			ЧастиБанера.Добавить(" ");
			ЧастиБанера.Добавить(Новый ФорматированнаяСтрока(
				Форма.НазваниеБанкаОсновногоБанковскогоСчета, Новый Шрифт(Новый Шрифт,, 14)));
			ЧастиБанера.Добавить(" ");
			ЧастиБанера.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'напрямую!'"), Новый Шрифт(Новый Шрифт,, 14)));
			ЧастиБанера.Добавить(Символы.ПС);
			ЧастиБанера.Добавить(Символы.ПС);
			ЧастиБанера.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Подключите сервис 1С:ДиректБанк'"),,,, "ПодключитьДиректБанк"));
			ЧастиБанера.Добавить(" ");
			ЧастиБанера.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'для отправки платежных документов и получения выписок'")));
			ЧастиБанера.Добавить(Символы.ПС);
			ЧастиБанера.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'напрямую из 1С:Предприятия, без использования клиент-банка.'")));
			ЧастиБанера.Добавить(" ");
			ЧастиБанера.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Подробнее'"),,,, "https://portal.1c.ru/applications/44"));
			
			Элементы.ДекорацияТекстРекламы.Заголовок = Новый ФорматированнаяСтрока(ЧастиБанера);
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ГруппаРекламаДиректБанк.Видимость = ВидимостьБаннера;
	
КонецПроцедуры

&НаСервере
Функция НаименованиеБанковскогоСчета(БанковскийСчет)
	
	Результат = "";
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Банк.Наименование");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОтборОрганизацияПриИзмененииСервер()
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	СкрытьРезультатСозданияДокументов(ЭтотОбъект);
	
	УстановитьВосстановленныеОтборы();
	
	ОсновнойБанковскийСчет = Справочники.БанковскиеСчета.ПустаяСсылка();
	
	УОрганизацииТолькоОдинБанковскийСчет = Справочники.БанковскиеСчета.КоличествоБанковскихСчетовОрганизации(ОтборОрганизация) = 1;
	Если УОрганизацииТолькоОдинБанковскийСчет Тогда
		ОсновнойБанковскийСчет = ЗначениеРеквизитаОбъекта(ОтборОрганизация, "ОсновнойБанковскийСчет");
	Иначе
		БанкИКассаФормы.ОпределитьСчетаБезИнтеграции(ЭтотОбъект);
	КонецЕсли;
	
	НазваниеБанкаОсновногоБанковскогоСчета = НаименованиеБанковскогоСчета(ОсновнойБанковскийСчет);
	
	ВидимостьЭлементовДиректБанк = ?(ЗначениеЗаполнено(ОсновнойБанковскийСчет),
		ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(ОтборОрганизация, ОсновнойБанковскийСчет),
		"НеПоказывать");
	
	СчетЦифровогоРубля = Справочники.БанковскиеСчета.ПустаяСсылка();
	Если Не ЗначениеЗаполнено(ОсновнойБанковскийСчет) Тогда
		ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		БанковскиеСчетаЦифровые = Справочники.БанковскиеСчета.БанковскиеСчетаОрганизации(
			ОтборОрганизация, ВалютаРеглУчета, , Истина);
		Если БанковскиеСчетаЦифровые.Количество() > 0 Тогда
			СчетЦифровогоРубля = БанковскиеСчетаЦифровые[0].Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеРеквизитаОбъекта(Знач Ссылка, Знач Реквизит)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, Реквизит);

КонецФункции

&НаКлиенте
Процедура ОткрытьФормуОтчета(ИмяОтчета, ПараметрыОтчета = Неопределено)

	ФормаОтчета = ПолучитьФорму(СтрШаблон("Отчет.%1.ФормаОбъекта", ИмяОтчета), ПараметрыОтчета, ЭтотОбъект);
	
	ФормаОтчета.ПодключитьОбработчикОжидания("Подключаемый_СформироватьПриОткрытии",
		БухгалтерскиеОтчетыКлиент.ИнтервалЗапускаФормированияОтчетаПриОткрытии(),
		Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ФормаОтчета.Элементы.Результат,
		"ФормированиеОтчета");
	ФормаОтчета.Открыть();

КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(ЭтотОбъект, ОтборОрганизация);
	
КонецПроцедуры

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	Если (ЕстьПредупреждения ИЛИ ЗначениеЗаполнено(ИнформацияАвтозагрузкиДиректБанк_ДатаОшибки)) Тогда
		Если Элементы.Баннер.Видимость Тогда
			ЗакрытьБаннерНажатие("");
		КонецЕсли;
	Иначе
		ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
		
		Если ДлительнаяОперация = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
			
			НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
			
			Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено));
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияДеньги());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
	// Скрываем баннер ДиректБанка, если есть персонализированное предложение
	Если Элементы.Баннер.Видимость Тогда
		Элементы.ГруппаРекламаДиректБанк.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НовыйСведенияОВладельцеБанковскогоСчетаИзКонтекста()
	
	СведенияОВладельцеБанковскогоСчетаИзКонтекста = Новый Структура;
	СведенияОВладельцеБанковскогоСчетаИзКонтекста.Вставить("Организация",               Новый Массив);
	СведенияОВладельцеБанковскогоСчетаИзКонтекста.Вставить("БанковскийСчетОрганизации", Новый Массив);
	
	Возврат СведенияОВладельцеБанковскогоСчетаИзКонтекста;
	
КонецФункции

&НаСервере
Процедура СкрытьНажатиеТекстКонтекстнаяПодсказкиИнтеграцияСБанком()
	
	БанкИКассаФормы.СкрытьНажатиеТекстКонтекстнаяПодсказкиИнтеграцияСБанком(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияСБанком

&НаСервере
Функция НастройкиАвтозагрузкиДиректБанк()
	
	ОтборОрганизацияАвтозагрукаДиректБанк = Справочники.Организации.ПустаяСсылка();
	Если ОтборОрганизацияИспользование Тогда
		ОтборОрганизацияАвтозагрукаДиректБанк = ОтборОрганизация;
	КонецЕсли;
	ОтборБанкАвтозагрукаДиректБанк = Справочники.Банки.ПустаяСсылка();
	ОтборБанковскийСчетАвтозагрукаДиректБанк = ЗначениеРеквизитаОбъекта(ОтборОрганизация, "ОсновнойБанковскийСчет");
	Если Не ЗначениеЗаполнено(ОтборБанковскийСчетАвтозагрукаДиректБанк) Тогда
		БанковскиеСчетаИнтеграции = Справочники.НастройкиИнтеграцииСБанками.БанковскиеСчетаОрганизацииВРежимеИнтеграции(
			ОтборОрганизация);
		Если БанковскиеСчетаИнтеграции.Количество() > 0 Тогда
			ОтборБанковскийСчетАвтозагрукаДиректБанк = БанковскиеСчетаИнтеграции[0];
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборБанковскийСчетАвтозагрукаДиректБанк) Тогда
		ОтборБанкАвтозагрукаДиректБанк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборБанковскийСчетАвтозагрукаДиректБанк, "Банк");
	КонецЕсли;
	Возврат ЭлектронноеВзаимодействиеБПВызовСервера.НастройкиОбменаАвтоматическоеПолучениеВыписки(
		ОтборОрганизацияАвтозагрукаДиректБанк,
		ОтборБанкАвтозагрукаДиректБанк);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки()
	
	// Сбросим видимость баннеров
	Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Ложь;
	Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость = Ложь;
	// Очистим информацию по датам
	ИнформацияАвтозагрузкиДиректБанк_ДатаОшибки = Неопределено;
	ИнформацияАвтозагрузкиДиректБанк_ТекущаяДатаСинхронизации = Неопределено;
	
	ПроверитьАвтоматическоеПолучениеВыпискиАктивно();
	ПодключенСервисАУСН = ИнтеграцияАУСНВызовСервераПовтИсп.СервисПодключен()
		Или ИнтеграцияАУСНВызовСервераПовтИсп.СервисЛКАУСНПодключен();
	Если АвтоматическоеПолучениеВыпискиАктивно Или ПодключенСервисАУСН Тогда
		Подключаемый_ОбновитьИнформациюИБаннерыАвтозагрузки();
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИнформациюИБаннерыАвтозагрузки", 300);
	Иначе
		ОтключитьОбработчикОжидания("Подключаемый_ОбновитьИнформациюИБаннерыАвтозагрузки");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюАвтозагрузки()
	
	НастройкиОбмена = РаботаСОповещениямиОСостоянииОбменаССервисами.НовыйНастройкиОбмена();
	НастройкиОбмена.НастройкиДиректБанк = НастройкиАвтозагрузкиДиректБанк();
	НастройкиОбмена.НастройкиАУСН = ИнтеграцияАУСНБП.НастройкиИнтеграции(ОтборОрганизацияИспользование, ОтборОрганизация);
	ДанныеСостоянияОбмена = РаботаСОповещениямиОСостоянииОбменаССервисами.ДанныеСостоянияОбмена(
		НастройкиОбмена, УникальныйИдентификатор);
	
	ИнформацияАвтозагрузкиДиректБанк_ДатаОшибки = ДанныеСостоянияОбмена.ДатаОшибкиДиректБанк;
	ИнформацияАвтозагрузки_НоваяДатаСинхронизации = Макс(
		ДанныеСостоянияОбмена.ДатаСинхронизацииДиректБанк, ДанныеСостоянияОбмена.ДатаСинхронизацииАУСН);
	
	ЗаголовкиСостоянияОбмена = РаботаСОповещениямиОСостоянииОбменаССервисами.ЗаголовкиСостоянияОбмена(
		ДанныеСостоянияОбмена);
	
	Элементы.ВариантОтбораПредупрежденийПриЗагрузке.Заголовок =
		ЗаголовкиСостоянияОбмена.ЗаголовокВариантОтбораПредупрежденийПриЗагрузке;
	Элементы.ТекстСостояниеАвтозагрузкиОшибка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		ЗаголовкиСостоянияОбмена.ЗаголовокСостояниеАвтозагрузкиОшибка);
	Элементы.ТекстСостояниеАвтозагрузки.Заголовок = ЗаголовкиСостоянияОбмена.ЗаголовокСостояниеАвтозагрузки;
	
	АдресДанныхСостоянияОбмена = ПоместитьВоВременноеХранилище(ДанныеСостоянияОбмена, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ЧекиНПД

&НаКлиенте
Процедура Подключаемый_ОбновитьСтатусОфлайнЧековНПД()
	
	ЧекиНПДКлиент.ОбновитьСтатусыОфлайнЧековНПД();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСведенияОСформированныхЧекахНПД()
	
	ЧекиНПДКлиент.ОбновитьСведенияОСформированныхЧекахНПД();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииКассовойСмены

// Проверяет есть ли хоть один экземпляр подключенной контрольно-кассовой техники
// и в случае его отсутствия предлагает пользователю перейти к настройке
// подключаемого оборудования
//
&НаКлиенте
Процедура ПроверитьНаличиеПодключеннойКассовойТехники()
	
	СписокОборудованияОтбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
	СписокОборудованияОтбор.ТипыПО = МенеджерОборудованияКлиентСерверБП.ТипыКонтрольноКассовойТехники();
	
	ПодключеннаяКассоваяТехника = МенеджерОборудованияВызовСервера.СписокОборудования(СписокОборудованияОтбор);
	Если ПодключеннаяКассоваяТехника.Количество() = 0 Тогда
		Оповещение = Новый ОписаниеОповещения("ПерейтиКПодключаемомуОборудованиюЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru =
			|'Касса не подключена, работа с кассовой сменой недоступна.
			|Перейти к подключению оборудования?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДоступнаРаботаСКассовойСменой()
	
	Если Не УправлениеКассовойСменойДоступно Тогда
		ПоказатьПредупреждение( , НСтр("ru='Недостаточно прав для выполнения операции'"));
	КонецЕсли;
	Возврат УправлениеКассовойСменойДоступно;
	
КонецФункции

&НаКлиенте
Процедура ОперацияЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Доступность = Истина;
	
	Если РезультатВыполнения <> Неопределено И Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = РезультатВыполнения.ОписаниеОшибки;
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОбновленияБаннера(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Оповестить("ОбновитьБаннеры_РозничнаяТорговля");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКПодключаемомуОборудованиюЗавершение(Ответ, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОткрытьПодключаемоеОборудование(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьБаннерИПодменюКомандУправленияКассовойСменой(Элементы, ОбновитьБаннер, ОбновитьПодменюУправленияКассовойСменой)
	
	ПараметрыРаботыСКассой = ИмяБаннераИПараметрыПодменюУправленияКассовойСменой(ОбновитьБаннер, ОбновитьПодменюУправленияКассовойСменой);
	Баннер = ПараметрыРаботыСКассой.Баннер;
	ПараметрыУправленияКассовойСменой = ПараметрыРаботыСКассой.ПараметрыУправленияКассовойСменой;
	
	Если ОбновитьБаннер Тогда
		Если ЗначениеЗаполнено(Баннер)
			И Элементы.БаннерСтраницы.ПодчиненныеЭлементы.Найти(Баннер) <> Неопределено Тогда
			Элементы.БаннерСтраницы.ТекущаяСтраница = Элементы[Баннер];
		Иначе
			Элементы.БаннерСтраницы.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбновитьПодменюУправленияКассовойСменой Тогда
		Если ПараметрыУправленияКассовойСменой = Неопределено Тогда
			Элементы.ПодменюКомандУправленияКассовойСменой.Видимость = Ложь;
		Иначе
			Элементы.ПодменюКомандУправленияКассовойСменой.Видимость = Истина;
			ДоступныеКоманды = ПараметрыУправленияКассовойСменой.ДоступныеКоманды;
			Для Каждого Команда Из Элементы.КассоваяСменаПодменю.ПодчиненныеЭлементы Цикл
				Команда.Доступность
					= ДоступныеКоманды.Найти(Команда.Имя) <> Неопределено;
			КонецЦикла;
			Элементы.КассоваяСменаПодменю.Картинка = ПараметрыУправленияКассовойСменой.КартинкаСостоянияСмены;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяБаннераИПараметрыПодменюУправленияКассовойСменой(ОбновитьБаннер, ОбновитьПодменюУправленияКассовойСменой)
	
	Если ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой")
		И ПолучитьФункциональнуюОпцию("ИспользоватьФискальныйРегистратор")
		И НЕ ПолучитьФункциональнуюОпцию("ВедетсяРозничнаяТорговля") Тогда
		РаботаСКассой = Истина;
	Иначе
		РаботаСКассой = Ложь;
	КонецЕсли;
	
	Баннер = "";
	ПараметрыУправленияКассовойСменой = Неопределено;
	
	Если РаботаСКассой И ОбновитьБаннер Тогда
		Баннер = ЖурналыДокументов.РозничныеПродажи.ИмяВидимогоБаннера_РозничнаяТорговля();
	КонецЕсли;
	
	Если РаботаСКассой И ОбновитьПодменюУправленияКассовойСменой Тогда
		ПараметрыУправленияКассовойСменой = ЖурналыДокументов.РозничныеПродажи.ПараметрыПодменюУправленияКассовойСменой();
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Баннер", Баннер);
	Результат.Вставить("ПараметрыУправленияКассовойСменой", ПараметрыУправленияКассовойСменой);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура НастроитьБаннерКассаНеПодключена()
	
	НастройкиБаннера = ЖурналыДокументов.РозничныеПродажи.НастройкиБаннераКассаНеПодключена();
	Элементы.НадписьКассаНеПодключена.Заголовок = НастройкиБаннера.ЗаголовокБаннера;
	Элементы.ОткрытьПодключаемоеОборудование.Видимость = НастройкиБаннера.ПодключениеКассыДоступно;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоказатьПредупреждениеПоРаботеСМаркетплейсами()
	
	ПоказатьПредупреждениеПоРаботеСМаркетплейсамиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПредупреждениеПоРаботеСМаркетплейсамиНаСервере()
	
	ЖурналыДокументов.Деньги.ПоказатьПредупреждениеПоРаботеСМаркетплейсами(Элементы, ОтборОрганизация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СкрытьПредупреждениеПоРаботеСМаркетплейсами()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ЖурналыДокументов.Деньги.ИмяНастройкиПредупрежденияПоРаботеСМаркетплейсами(),
		ЖурналыДокументов.Деньги.КлючНастройкиПредупрежденияПоРаботеСМаркетплейсами(),
		Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПредупрежденийПриЗагрузке

&НаСервере
Процедура УстановитьВариантОтбораПредупрежденийПриЗагрузкеИзПараметров(Параметры)
	
	Если Параметры.Свойство("ПоказатьПредупреждения")
		И Параметры.ПоказатьПредупреждения Тогда
		
		ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораТребующиеПроверки();
		ОткрытаДляПроверкиЗагрузкиДокументов = Истина;
		
	Иначе
		
		ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораВсе();
		ОткрытаДляПроверкиЗагрузкиДокументов = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВариантОтбораВсе()
	Возврат "Все";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантОтбораТребующиеПроверки()
	Возврат "ТребующиеПроверки";
КонецФункции

&НаСервере
Процедура ПоказатьБаннерПредупрежденийПриЗагрузкеИУстановитьОтбор()
	
	ОрганизацияПредупреждений = ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено);
	
	ЕстьПредупреждения = ЕстьДокументыСПредупреждениямиПриЗагрузке(ОрганизацияПредупреждений);
	
	НачальнаяВидимостьБаннера = Элементы.ГруппаПредупрежденияПриЗагрузкеИзКлиентБанка.Видимость;
	Элементы.ГруппаПредупрежденияПриЗагрузкеИзКлиентБанка.Видимость = ЕстьПредупреждения;
	ПараметрыОтбора = Новый Структура("Организация", ОрганизацияПредупреждений);
	Если Не ЕстьПредупреждения Тогда
		ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораВсе();
	ИначеЕсли ЕстьПредупреждения И ПолучитьФункциональнуюОпцию("ПрименяетсяАУСН", ПараметрыОтбора)
		И Не НачальнаяВидимостьБаннера Тогда
		ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораТребующиеПроверки();
	КонецЕсли;
	
	УстановитьОтборПредупрежденийПриЗагрузке(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьДокументыСПредупреждениямиПриЗагрузке(Организация)
	
	ПараметрыОтбораПредупреждений = РегистрыСведений.ПредупрежденияПриЗагрузкеВыписки.НовыеПараметрыОтбораПредупреждений();
	ПараметрыОтбораПредупреждений.Организация = Организация;
	
	Возврат РегистрыСведений.ПредупрежденияПриЗагрузкеВыписки.ЕстьПредупрежденияВСписке(ПараметрыОтбораПредупреждений);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПредупрежденийПриЗагрузке(Форма)
	
	Настройки = Форма.Список.КомпоновщикНастроек.Настройки;
	
	ИмяПоля = "Ссылка";
	
	Если Форма.ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораТребующиеПроверки() Тогда
		
		ОрганизацияПредупреждений = ?(Форма.ОтборОрганизацияИспользование, Форма.ОтборОрганизация, Неопределено);
		ДокументыСПредупреждениямиПриЗагрузке = ДокументыСПредупреждениямиПриЗагрузке(ОрганизацияПредупреждений);
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Настройки.Отбор,
			ИмяПоля);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Настройки.Отбор,
			ИмяПоля,
			ДокументыСПредупреждениямиПриЗагрузке,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Настройки.Отбор,
			ИмяПоля);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументыСПредупреждениямиПриЗагрузке(Организация)
	
	ПараметрыОтбораПредупреждений = РегистрыСведений.ПредупрежденияПриЗагрузкеВыписки.НовыеПараметрыОтбораПредупреждений();
	ПараметрыОтбораПредупреждений.Организация = Организация;
	
	Возврат РегистрыСведений.ПредупрежденияПриЗагрузкеВыписки.ДокументыСПредупреждениями(ПараметрыОтбораПредупреждений);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_АУСН()
	
	ИнтеграцияАУСНКлиент.ВыполнитьОбменССервисом(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы(МожноРедактировать)
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.ГруппаИтоги);
	
	АдаптироватьМККоманднаяПанельФормы(МожноРедактировать);
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
	АдаптироватьМКИтогиСпискаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы(МожноРедактировать)
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.РазрешеноИзменение = МожноРедактировать;
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ПеренестиКомандыВМККомандыФормы(ЭтотОбъект, Элементы.ГруппаВажныеКоманды.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаСписка.ПеренестиКомандыВМККомандыФормы(ЭтотОбъект, Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКСоздать(Команда)
	
	СписокВыбораОперации = Новый СписокЗначений;
	СписокВыбораОперации.Добавить("ДоходПоБанку", "Доход по банку");
	СписокВыбораОперации.Добавить("РасходПоБанку", "Расход по банку");
	СписокВыбораОперации.Добавить(Неопределено);
	СписокВыбораОперации.Добавить("ДоходПоКассе","Доход по кассе");
	СписокВыбораОперации.Добавить("РасходПоКассе","Расход по кассе");
	
	ДопПараметры = Новый Структура("Команда", Команда);
	ОписаниеЗавершения = Новый ОписаниеОповещения("КомандыМК_Создать_Завершение", ЭтотОбъект, ДопПараметры);
	ПоказатьВыборИзМеню(ОписаниеЗавершения, СписокВыбораОперации, Команда);

КонецПроцедуры

&НаКлиенте
Процедура КомандыМК_Создать_Завершение(ВыбранныйЭлемент, ДопПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено
		Или ВыбранныйЭлемент.Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "ДоходПоБанку" Тогда
		СоздатьПоступлениеНаРасчетныйСчет(ДопПараметры.Команда);
	ИначеЕсли ВыбранныйЭлемент.Значение = "РасходПоБанку" Тогда
		СоздатьСписаниеСРасчетногоСчета(ДопПараметры.Команда);
	ИначеЕсли ВыбранныйЭлемент.Значение = "ДоходПоКассе" Тогда
		СоздатьПриходныйКассовыйОрдер(ДопПараметры.Команда);
	ИначеЕсли ВыбранныйЭлемент.Значение = "РасходПоКассе" Тогда
		СоздатьРасходныйКассовыйОрдер(ДопПараметры.Команда);
	Иначе
		ВызватьИсключение "Неизвестный вид документа";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьОтбор(Команда)
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОтбораМК(ЭтотОбъект, "Список", "Организация");
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораОрганизация(ЭтотОбъект, Элементы.ОтборОрганизация, "", "Все организации");
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.ГруппаДополнительныеОтборы);

КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()

	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "СписокКонтекстноеМенюГруппа");
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы,
		Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы,
		Элементы.Список.КонтекстноеМеню);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормы(ЭтотОбъект);
	
	//В реквизитах установить "Использовать всегда" для источника КолонкаСостояниеДокумента
	МобильныйКлиентБПФормаСписка.ДобавитьКолонкаСостояниеДокумента(ЭтотОбъект, "СтандартнаяКартинка", "Список");
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СостояниеДокумента(ЭтотОбъект, Элементы.СчетВРежимеИнтеграции);
	
	Элементы.Дата.РастягиватьПоГоризонтали = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.Дата);
	
	Элементы.Доход.ЦветТекста = ЦветаСтиля.ЦветТекстаПоложительногоЗначения;
	Элементы.Расход.ЦветТекста = ЦветаСтиля.ЦветТекстаОтрицательногоЗначения;
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СуммаДокумента(ЭтотОбъект, Элементы.Доход);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СуммаДокумента(ЭтотОбъект, Элементы.Расход);
	
	Элементы.Контрагент.РастягиватьПоГоризонтали = Истина;
	Элементы.Контрагент.АвтоВысотаЯчейки = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Контрагент);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.ВидОперации, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Тип, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.БанковскийСчет, 3);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Прочее(ЭтотОбъект, Элементы.Организация);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Доход");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.Доход", ВидСравненияКомпоновкиДанных.Равно, 0);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Расход");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.Расход", ВидСравненияКомпоновкиДанных.Равно, 0);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКИтогиСпискаФормы()
	
	МобильныйКлиентБПФормаСписка.СкрытьЭлементФормыСписка(ЭтотОбъект, Элементы.ИтогДата);
	
	Элементы.ГруппаИтоги.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	Элементы.ГруппаИтоги.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	Элементы.ГруппаИтоги.ОтображатьЗаголовок = Истина;
	Элементы.ГруппаИтоги.Скрыть();
	
	Элементы.ГруппаОстатки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	Элементы.ГруппаОстатки.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Авто;
	Элементы.ГруппаОстатки.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Авто;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовкиГруппыИтогиМК(ЭлементГруппы, ДатаИтогов, ИтогиНаКонецДня)
	
	ПредставлениеДатаИтогов = Формат(ДатаИтогов, "ДФ=dd.MM.yyyy");
	ЭлементГруппы.Заголовок = СтрШаблон(НСтр("ru = 'Остаток (%1)'"), ПредставлениеДатаИтогов);
	ЭлементГруппы.ЗаголовокСвернутогоОтображения = СтрШаблон(НСтр("ru = 'Остаток (%1): %2'"),
		ПредставлениеДатаИтогов,
		Формат(ИтогиНаКонецДня, "ЧДЦ=2; ЧН=0,00"));
	
КонецПроцедуры

#КонецОбласти
