// Построена на базе ФормыСписка из БСП версии 3.1.9.403

#Область ОписаниеПеременных

&НаКлиенте
Перем ОткрытыеФормы;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
		
	ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	РазделениеВключено         = ОбщегоНазначения.РазделениеВключено();
	
	Если КлючНазначенияИспользования = "ДокументыПоКлиенту" Тогда
		Элементы.Клиент.Видимость  = Ложь;
	КонецЕсли;
	
	ОтборПоВидамВзаимодействий = "Все";
	 	
	Если Параметры.Свойство("Заголовок") И Не ПустаяСтрока(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Вставить("КоманднаяПанель", Элементы.КоманднаяПанель);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюСоздатьНаОсновании", "Видимость", Ложь);
	
	Взаимодействия.ЗаполнитьСписокДоступныхДляСозданияДокументов(ДокументыДоступныеДляСоздания);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	ОбщегоНазначенияБП.УстановитьВидимостьКолонокДополнительнойИнформации(ЭтотОбъект);

	ЕстьПравоДобавленияВстреча = ДокументыДоступныеДляСоздания.НайтиПоЗначению("Встреча") <> Неопределено;
	ЕстьПравоДобавлениеТелефонныйЗвонок = ДокументыДоступныеДляСоздания.НайтиПоЗначению("ТелефонныйЗвонок") <> Неопределено;
	ЕстьПравоДобавлениеПрочееВзаимодействие = ДокументыДоступныеДляСоздания.НайтиПоЗначению("ПрочееВзаимодействие") <> Неопределено;
	ЕстьПравоДобавлениеЭлектронноеПисьмоИсходящее = ДокументыДоступныеДляСоздания.НайтиПоЗначению("ЭлектронноеПисьмоИсходящее") <> Неопределено;

	Элементы.СписокСоздатьВстречу.Видимость = ЕстьПравоДобавленияВстреча;
	Элементы.СписокСоздатьТелефонныйЗвонок.Видимость = ЕстьПравоДобавлениеТелефонныйЗвонок;
	Элементы.СписокСоздатьПрочееВзаимодействие.Видимость = ЕстьПравоДобавлениеЭлектронноеПисьмоИсходящее;
	Элементы.СписокСоздатьЭлектронноеПисьмо.Видимость = ЕстьПравоДобавлениеЭлектронноеПисьмоИсходящее;

	Элементы.ГруппаСоздать.Видимость = ЕстьПравоДобавленияВстреча Или ЕстьПравоДобавлениеТелефонныйЗвонок
		Или ЕстьПравоДобавлениеПрочееВзаимодействие Или ЕстьПравоДобавлениеЭлектронноеПисьмоИсходящее;
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если РазделениеВключено
		И Не ОтправкаПолучениеПисемВыполняется Тогда
		
		ОтправитьПолучитьПочтуПользователяКлиент();
		
	Иначе
		
		ПодключитьОбработчикОжидания("ПроверитьНеобходимостьОтправкиПолученияПочты", 30, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если РазделениеВключено 
		И Не ОтправкаПолучениеПисемВыполняется Тогда
		ОтправитьПолучитьПочтуПользователяКлиент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ВРег(ИмяСобытия) = ВРег("Запись_ПапкиЭлектронныхПисем") 
		Или ВРег(ИмяСобытия) = ВРег("ПримененыПравилаОбработкиПисем")
		Или ВРег(ИмяСобытия) = ВРег("ВыполненаОтправкаПолучениеПисем") Тогда
		
		Если ВРег(ИмяСобытия) = ВРег("ВыполненаОтправкаПолучениеПисем") Тогда

			Если РазделениеВключено Тогда
				ПодключитьОбработчикОжидания("ПроверитьНеобходимостьОтправкиПолученияПочты", 150, Истина);
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	ПрисоединенныеФайлыБПКлиент.ОбновитьСписокПослеДобавленияФайла(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПоВидамВзаимодействийПриИзменении(Элемент)
	УстановитьОтборПоВидуВзаимодействия();
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПодсказкаПКНажатие(Элемент)
	
	Элементы.ГруппаПочтовыйКлиент.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаПКОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	СтруктураОтбора = Новый Структура;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	ПараметрыФормы.Вставить("ТолькоПочта", Истина);
	
	Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
		ПростойИнтерфейсКлиент.ОткрытьФормуВОкнеФормы("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаЭлектроннаяПочтаБП",
			ЭтотОбъект, ПараметрыФормы, , ОткрытыеФормы);
	Иначе
		ОткрытьФорму("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаЭлектроннаяПочтаБП", ПараметрыФормы);
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ОтборыСписков.СброситьИспользованиеПользовательскихОтборовВНастройке(Настройки);
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);

КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Список.РежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		ОповеститьОВыборе(ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ТолькоПочта = Ложь;
	ВзаимодействияКлиент.СписокПередНачаломДобавления(Элемент, Отказ, Копирование, ТолькоПочта, ДокументыДоступныеДляСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка <> Неопределено Тогда
		
		Если ПрисоединенныеФайлыБПКлиент.ПараметрыПеретаскиванияСодержатФайлы(ПараметрыПеретаскивания) Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Ссылка", Строка);
			ДополнительныеПараметры.Вставить("ПараметрыПеретаскивания", ПараметрыПеретаскивания);
			ДополнительныеПараметры.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПеретаскиваниеФайловОтветПолучен",
				ПрисоединенныеФайлыБПКлиент,
				ДополнительныеПараметры);
			ШаблонВопроса = НСтр("ru='Присоединить файлы к документу %1?'");
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонВопроса, Строка);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьВстречу(Команда)
	
	СоздатьНовоеВзаимодействие("Встреча");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПрочееВзаимодействие(Команда)
	СоздатьНовоеВзаимодействие("ПрочееВзаимодействие");
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТелефонныйЗвонок(Команда)
	
	СоздатьНовоеВзаимодействие("ТелефонныйЗвонок");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектронноеПисьмо(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЭлектронноеПисьмоПродолжение", ЭтотОбъект, );
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьЭлектронноеПисьмоПродолжение(ПроверкаВыполнена, ДополнительныеПараметры) Экспорт
	
	Если ПроверкаВыполнена = Истина Тогда
		СоздатьНовоеВзаимодействие("ЭлектронноеПисьмоИсходящее");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоВидуВзаимодействия()
	
	Использование = Не  ПустаяСтрока(ОтборПоВидамВзаимодействий);
	Если ОтборПоВидамВзаимодействий = "Прочее" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Тип", ТипПрочее(), , , Использование);
	ИначеЕсли ОтборПоВидамВзаимодействий = "Звонок" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Тип", ТипТелефонныйЗвонок(), , , Использование);
	ИначеЕсли ОтборПоВидамВзаимодействий = "Встреча" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Тип", ТипВстреча(), , , Использование);
		ИначеЕсли ОтборПоВидамВзаимодействий = "Письмо" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Тип", ТипЭлектроннаяПочта(), ВидСравненияКомпоновкиДанных.ВСписке , , Использование);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Тип");	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.Дата", Элементы.Дата.Имя);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Процедуры и функции обработки команд.

&НаКлиенте
Процедура СоздатьНовоеВзаимодействие(ТипОбъекта)
	
	Контакт = Неопределено;
	
	ПараметрыСоздания = Новый Структура;
	
	ПолеКонтрагент = Новый ПолеКомпоновкиДанных("Контрагент");
	ПолеКонтактноеЛицо = Новый ПолеКомпоновкиДанных("КонтактноеЛицо");
	
	Для Каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		Если ЭлементОтбора.Использование
		И (ЭлементОтбора.ЛевоеЗначение = ПолеКонтрагент 
			Или ЭлементОтбора.ЛевоеЗначение = ПолеКонтактноеЛицо) Тогда
				Контакт = ЭлементОтбора.ПравоеЗначение;
				Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Контакт) Тогда
		ДанныеУчастника = Новый Структура;
		ДанныеУчастника.Вставить("Контакт", Контакт);
		ДанныеУчастника.Вставить("КакСвязаться", "");
		ДанныеУчастника.Вставить("Представление", Строка(Контакт));
		ПараметрыСоздания = Новый Структура;
		ПараметрыСоздания.Вставить("ДанныеУчастника", ДанныеУчастника);
	КонецЕсли; 
	
	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие(ТипОбъекта, ПараметрыСоздания, ЭтотОбъект);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Прочее

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ОтправитьПолучитьПочтуПользователяКлиент(ВыводитьПрогресс = Ложь)
	
	ОчиститьСообщения();
	УправлениеЭлектроннойПочтойКлиент.ОтправитьЗагрузитьПочтуПользователя(УникальныйИдентификатор, ЭтотОбъект, 
		Элементы.Список, ВыводитьПрогресс);
	ПодключитьОбработчикОжидания("ПроверитьНеобходимостьОтправкиПолученияПочты", 30, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНеобходимостьОтправкиПолученияПочты()

	Если Не РазделениеВключено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОтправкаПолучениеПисемВыполняется
		И (ОбщегоНазначенияКлиент.ДатаСеанса() > ДатаПредыдущегоПолученияОтправкиПочты + 300) Тогда
		ОтправитьПолучитьПочтуПользователяКлиент();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПроверитьНеобходимостьОтправкиПолученияПочты", 30, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТипПрочее()
	Возврат Тип("ДокументСсылка.ПрочееВзаимодействие");
КонецФункции

&НаСервереБезКонтекста
Функция ТипТелефонныйЗвонок()
	Возврат Тип("ДокументСсылка.ТелефонныйЗвонок");
КонецФункции

&НаСервереБезКонтекста
Функция ТипВстреча()
	Возврат Тип("ДокументСсылка.Встреча");
КонецФункции

&НаСервереБезКонтекста
Функция ТипЭлектроннаяПочта()
	Типы = Новый Массив;
	Типы.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее"));
	Типы.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее"));
	Возврат Типы;
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	
	Элементы.ГруппаОтбор.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ОтборПоВидамВзаимодействий.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.ГруппаОтбор);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.ГруппаСписок);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.РазрешеноИзменение = Ложь;
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСоздать(ЭтотОбъект, Элементы.ГруппаСоздать);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьОтбор(Команда)
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойОтбораМК(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы,
		Элементы.СписокКонтекстноеМенюГруппаРассмотрениеПредмет.ПодчиненныеЭлементы,
		Элементы.Список.КонтекстноеМеню);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормыСправочника(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Дата);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.ЕстьФайлы);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.ГруппаТемаИОписание, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Клиент, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Ответственный, 2);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти
