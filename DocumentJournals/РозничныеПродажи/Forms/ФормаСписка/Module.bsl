
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОсновнаяОрганизация = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	Если Не ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизация = ОсновнаяОрганизация;
	КонецЕсли;
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	УстановитьВосстановленныеОтборы();
	
	УстановитьУсловноеОформление();
	
	УправлениеКассовойСменойДоступно = КассовыеСменыВызовСервераБП.ДоступноУправлениеКассовойСменой();
	НастроитьБаннерКассаНеПодключена();
	
	НастроитьВидимостьЭлементовФормы();
	
	НастроитьПодменюУправленияКассовойСменой(Элементы);
	
	АдресХранилищаНастройкиДинСпискаДляРеестра = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = КассовыеСменыКлиентБП.СобытиеВыполняетсяОперацияКассовойСмены() Тогда
		Доступность = Ложь;
	ИначеЕсли ИмяСобытия = КассовыеСменыКлиентБП.СобытиеЗавершиласьОперацияКассовойСмены() Тогда
		Доступность = Истина;
	ИначеЕсли ИмяСобытия = КассовыеСменыКлиентБП.СобытиеОшибкаОткрытияСмены() Тогда
		ПроверитьНаличиеПодключеннойКассовойТехники();
	ИначеЕсли ИмяСобытия = "ИзменениеСтатусаКассовойСмены" Тогда
		НастроитьПодменюУправленияКассовойСменой(Элементы);
	ИначеЕсли ИмяСобытия = "ОбновитьБаннеры_РозничнаяТорговля" Тогда
		ОбновитьВидимостьБаннера(Элементы);
	ИначеЕсли ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОсновнаяОрганизация = Параметр;
		Если ОсновнаяОрганизация <> ОтборОрганизация Тогда
			ОтборОрганизация = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
			УстановитьВосстановленныеОтборы();
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		Если Параметр = ОтборОрганизация И ОтборОрганизацияИспользование Тогда
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ПоказатьИнформациюОПравеПримененияСпецрежима" Тогда
		
		Если Источник <> ЭтотОбъект И (Не ОтборОрганизацияИспользование Или Параметр = ОтборОрганизация) Тогда
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если СтрНачинаетсяС(ИсточникВыбора.ИмяФормы, "Обработка.ЗагрузкаИзОФД.Форма")  Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьИзОФДПоСпискуКассЗавершение", ЭтотОбъект);
		ЗагрузкаИзОФДКлиент.ЗагрузитьИзОФДПоСпискуКасс(ВыбранноеЗначение, УникальныйИдентификатор, ОповещениеОЗавершении);
	ИначеЕсли ЗначениеЗаполнено(ОтборДокумент) Тогда
		
		ПараметрыФормы = Новый Структура;
		Если ВыбранноеЗначение = "РозничныйВозврат" Тогда
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("ВидОперации",
				ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Возврат"));
			ЗначенияЗаполнения.Вставить("Основание", ОтборДокумент);
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ПараметрыФормы.Вставить("ИзменитьВидОперации", Истина);
			ИмяДокумента = "РозничнаяПродажа";
		ИначеЕсли ВыбранноеЗначение = "РозничнаяПродажа" Тогда
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("ВидОперации",
				ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Продажа"));
			ЗначенияЗаполнения.Вставить("Основание", ОтборДокумент);
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ПараметрыФормы.Вставить("ИзменитьВидОперации", Истина);
			ИмяДокумента = "РозничнаяПродажа";
		Иначе
			ПараметрыФормы.Вставить("Основание", ОтборДокумент);
			ИмяДокумента = ВыбранноеЗначение;
		КонецЕсли;
		
		ОтборДокумент = Неопределено;
		ОткрытьФорму("Документ." + ИмяДокумента + ".ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьВосстановленныеОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстИнформацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьПозжеНажатие(Элемент)
	
	ОтложитьПоказНапоминанияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееНажатие(Элемент)
	
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстДополнительнойИнформацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияНапомнитьПозжеНажатие(Элемент)
	
	ОтложитьПоказДополнительногоНапоминанияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияПодробнееНажатие(Элемент)
	
	КонтрольПраваПримененияСпецрежимаКлиент.ПодробнееНажатие(ЭтотОбъект, СсылкаНаДополнительноеПояснение);
	
КонецПроцедуры


&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	Оповестить("ПоказатьИнформациюОПравеПримененияСпецрежима",
		?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено),
		ЭтотОбъект);
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область КомандыУправленияКассовойСменой

&НаКлиенте
Процедура ОткрытьКассовуюСмену(Команда)
	Если НЕ ДоступнаРаботаСКассовойСменой() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	КассовыеСменыКлиентБП.НачатьОткрытиеКассовойСмены(УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетОТекущемСостоянииРасчетов(Команда)
	
	Если НЕ ДоступнаРаботаСКассовойСменой() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	Доступность = Ложь;
	
	ПараметрыОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыВыполненияОперации();
	КассовыеСменыВызовСервераБП.ЗаполнитьПараметрыОперацииДляОрганизацииПоУмолчанию(ПараметрыОперации);
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ОперацияЗавершение", ЭтотОбъект);
	ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФормированиеОтчетаОТекущемСостоянииРасчетов(
		ОповещениеЗавершения, УникальныйИдентификатор, ,ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетБезГашения(Команда)
	Если НЕ ДоступнаРаботаСКассовойСменой() Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетБезГашенияАсинх();
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКассовуюСмену(Команда)
	Если НЕ ДоступнаРаботаСКассовойСменой() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	КассовыеСменыКлиентБП.ЗакрытьСмену(УникальныйИдентификатор, Список.КомпоновщикНастроек);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СоздатьРозничнаяПродажаЧек(Команда)
	
	КлючеваяОперация = "СозданиеФормыРозничнаяПродажа";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	СтруктураПараметров = Новый Структура;
	
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	ЗначенияЗаполнения.Вставить("ВидОперации", ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Продажа"));
	Если ТекущаяСтрока <> Неопределено
		И ТекущаяСтрока.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Предоплата") Тогда
		
		ЗначенияЗаполнения.Вставить("Основание", ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	СтруктураПараметров.Вставить("ИзменитьВидОперации", Истина);
	
	ОткрытьФорму("Документ.РозничнаяПродажа.Форма.ФормаДокументаОбщая", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВозвратПоЧеку(Команда)
	
	КлючеваяОперация = "СозданиеФормыРозничнаяПродажа";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	СтруктураПараметров = Новый Структура;
	ЗначенияЗаполнения = Новый Структура;
	
	ВидыОперацийДляВозврата = Новый Массив;
	ВидыОперацийДляВозврата.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Предоплата"));
	ВидыОперацийДляВозврата.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Продажа"));
	
	
	ЗначенияЗаполнения.Вставить("ВидОперации", ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Возврат"));
	Если ТекущаяСтрока <> Неопределено
		И ВидыОперацийДляВозврата.Найти(ТекущаяСтрока.ВидОперации) <> Неопределено Тогда
		
		ЗначенияЗаполнения.Вставить("Основание", ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	СтруктураПараметров.Вставить("ИзменитьВидОперации", Истина);
	
	ОткрытьФорму("Документ.РозничнаяПродажа.Форма.ФормаДокументаОбщая", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодключаемоеОборудование(Команда)
	
	ОткрытьФорму("Обработка.ПодключенныеКассы.Форма.ПомощникПодключенияКассы", , ЭтаФорма, , , , 
		Новый ОписаниеОповещения("ОповещениеОбновленияБаннера", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВидовОплат(Команда)
	
	ОткрытьФорму("Справочник.ВидыОплатОрганизаций.ФормаСписка", , , , , ,
		Новый ОписаниеОповещения("ОповещениеОбновленияБаннера", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНаОсновании(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования",
		?(ПустаяСтрока(КлючНазначенияИспользования), "ДокументыРозничнойТорговли", КлючНазначенияИспользования));
	
	Если ТекущиеДанные <> Неопределено Тогда
		ОтборДокумент = ТекущиеДанные.Ссылка;
		ПараметрыФормы.Вставить("Основание", ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ВыборТипаДокументаПоОснованию", ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзОФД(Команда)
	
	СписокКассОФДПоОрганизации = Новый Массив;
	Если ОтборОрганизацияИспользование 
		Или Не ЗагрузкаИзОФДВызовСервера.ЕстьНастройкиПоНесколькимОрганизациям() Тогда
		
		СписокКассОФДПоОрганизации = ЗагрузкаИзОФДВызовСервера.СписокКассОФДПоОрганизации(ОтборОрганизация);
	КонецЕсли;
	
	Если СписокКассОФДПоОрганизации.Количество() > 0 Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьИзОФДПоСпискуКассЗавершение", ЭтотОбъект);
		ЗагрузкаИзОФДКлиент.ЗагрузитьИзОФДПоСпискуКасс(СписокКассОФДПоОрганизации, УникальныйИдентификатор, ОповещениеОЗавершении);
	Иначе
		ОткрытьФорму("Обработка.ЗагрузкаИзОФД.Форма",,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЗагрузкуИзОФД(Команда)
	ОткрытьФорму("Обработка.ЗагрузкаИзОФД.Форма",,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЧекНаПредоплату(Команда)
	КлючеваяОперация = "СозданиеФормыРозничнаяПродажа";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);

	СтруктураПараметров = Новый Структура;
	
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	ЗначенияЗаполнения.Вставить("ВидОперации", ПредопределенноеЗначение("Перечисление.ВидыОперацийРозничнаяПродажа.Предоплата"));
	
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	СтруктураПараметров.Вставить("ИзменитьВидОперации", Истина);
	
	ОткрытьФорму("Документ.РозничнаяПродажа.Форма.ФормаДокументаОбщая", СтруктураПараметров, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

#Область СлужебныеПроцедурыИФункцииБСП

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	Если УправлениеПечатьюБПКлиентСервер.ЭтоИмяКомандыРеестрДокументов(Команда.Имя) Тогда
		НастройкиДинамическогоСписка();
	КонецЕсли;
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	ОбновитьДоступностьКомандыСоздатьНаОсновании();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандыСоздатьНаОсновании()
	
	Элементы.СоздатьНаОсновании.Доступность = Элементы.Список.ТекущаяСтрока <> Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеВидимостьюИДоступностьюЭлементовФормы

&НаСервере
Процедура НастроитьВидимостьЭлементовФормы()
	
	ОбновитьВидимостьБаннера(Элементы);
	
	ДоступСозданиеРозничнойПродажи = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.РозничнаяПродажа);
	
	Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(Метаданные.Документы.РозничнаяПродажа) Тогда
		Элементы.ЧекСоздать.Видимость = ДоступСозданиеРозничнойПродажи;
		Элементы.ВозвратСоздать.Видимость = ДоступСозданиеРозничнойПродажи;
	КонецЕсли;
	
	Элементы.ФормаГруппаЗагрузитьИзОФД.Видимость = ЗагрузкаИзОФД.ЗагрузкаИзОФДДоступна();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьПодменюУправленияКассовойСменой(Элементы)
	
	Параметры = ПараметрыПодменюУправленияКассовойСменой();
	Если Параметры <> Неопределено Тогда
		ДоступныеКоманды = Параметры.ДоступныеКоманды;
		Для Каждого Команда Из Элементы.КассоваяСменаПодменю.ПодчиненныеЭлементы Цикл
			Команда.Доступность
				= ДоступныеКоманды.Найти(Команда.Имя) <> Неопределено;
		КонецЦикла;
		Элементы.КассоваяСменаПодменю.Картинка = Параметры.КартинкаСостоянияСмены;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыПодменюУправленияКассовойСменой()
	
	Возврат ЖурналыДокументов.РозничныеПродажи.ПараметрыПодменюУправленияКассовойСменой();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьВидимостьБаннера(Элементы)
	
	Баннер = ИмяВидимогоБаннераНаСервере();
	Если ЗначениеЗаполнено(Баннер)
		И Элементы.БаннерСтраницы.ПодчиненныеЭлементы.Найти(Баннер) <> Неопределено Тогда
		Элементы.БаннерСтраницы.ТекущаяСтраница = Элементы[Баннер];
	Иначе
		Элементы.БаннерСтраницы.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление(ЗагруженныеИзОФД = Неопределено)
	
	УсловноеОформление.Элементы.Очистить();
	
	// Организация
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Организация");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизацияИспользование", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизация", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Выделение загруженных из ОФД
	Если ЗначениеЗаполнено(ЗагруженныеИзОФД) Тогда
		
		СписокВыделенияЗначений = Новый СписокЗначений();
		СписокВыделенияЗначений.ЗагрузитьЗначения(ЗагруженныеИзОФД);
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.Ссылка", ВидСравненияКомпоновкиДанных.ВСписке, СписокВыделенияЗначений);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(Новый Шрифт(),,, Истина));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура НастроитьБаннерКассаНеПодключена()
	
	НастройкиБаннера = ЖурналыДокументов.РозничныеПродажи.НастройкиБаннераКассаНеПодключена();
	Элементы.НадписьКассаНеПодключена.Заголовок = НастройкиБаннера.ЗаголовокБаннера;
	Элементы.ОткрытьПодключаемоеОборудование.Видимость = НастройкиБаннера.ПодключениеКассыДоступно;
	
КонецПроцедуры

// Проверяет есть ли хоть один экземпляр подключенной контрольно-кассовой техники
// и в случае его отсутствия предлагает пользователю перейти к настройке
// подключаемого оборудования
//
&НаКлиенте
Процедура ПроверитьНаличиеПодключеннойКассовойТехники()
	
	СписокОборудованияОтбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
	СписокОборудованияОтбор.ТипыПО = МенеджерОборудованияКлиентСерверБП.ТипыКонтрольноКассовойТехники();
	
	ПодключеннаяКассоваяТехника = МенеджерОборудованияВызовСервера.СписокОборудования(СписокОборудованияОтбор);
	Если ПодключеннаяКассоваяТехника.Количество() = 0 Тогда
		Оповещение = Новый ОписаниеОповещения("ПерейтиКПодключаемомуОборудованиюЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru =
			|'Касса не подключена, работа с кассовой сменой недоступна.
			|Перейти к подключению оборудования?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДоступнаРаботаСКассовойСменой()
	
	Если Не УправлениеКассовойСменойДоступно Тогда
		ПоказатьПредупреждение( , НСтр("ru='Недостаточно прав для выполнения операции'"));
	КонецЕсли;
	Возврат УправлениеКассовойСменойДоступно;
	
КонецФункции

&НаКлиенте
Процедура ОперацияЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Доступность = Истина;
	
	Если РезультатВыполнения <> Неопределено И Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = РезультатВыполнения.ОписаниеОшибки;
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОбновленияБаннера(Результат = Неопределено, Параметры = Неопределено) Экспорт
	
	Оповестить("ОбновитьБаннеры_РозничнаяТорговля");
	
КонецПроцедуры

&НаСервере
Процедура НастройкиДинамическогоСписка()
	
	Отчеты.РеестрДокументов.НастройкиДинамическогоСписка(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяВидимогоБаннераНаСервере() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяРозничнаяТорговля") Тогда
		ИмяБаннера = "";
	Иначе
		ИмяБаннера = ЖурналыДокументов.РозничныеПродажи.ИмяВидимогоБаннера_РозничнаяТорговля();
	КонецЕсли;
	
	Возврат ИмяБаннера;
	
КонецФункции

&НаКлиенте
Процедура ПерейтиКПодключаемомуОборудованиюЗавершение(Ответ, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОткрытьПодключаемоеОборудование(Неопределено);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Асинх Процедура ОтчетБезГашенияАсинх()
	ОчиститьСообщения();
	Доступность = Ложь;
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		РабочееМесто = МенеджерОборудованияКлиентПовтИсп.РабочееМестоКлиента()
	Иначе
		РабочееМесто = Неопределено;
	КонецЕсли;
	
	ДанныеЗакрытия = КассовыеСменыВызовСервераБП.ДанныеЗакрытияКассовойСмены(РабочееМесто,
		УникальныйИдентификатор, Список.КомпоновщикНастроек);
		
	Если Не ЗначениеЗаполнено(ДанныеЗакрытия.ОткрытыеСмены) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Смена не открыта.'"));
		Возврат;
	ИначеЕсли ДанныеЗакрытия.ОткрытыеСмены.Количество() = 1 Тогда
		ИдентификаторУстройства = ДанныеЗакрытия.ОткрытыеСмены[0].ИдентификаторУстройства;
	Иначе
		СписокОборудования = Новый СписокЗначений;
		Для каждого ДанныеСмены Из ДанныеЗакрытия.ОткрытыеСмены Цикл
			СписокОборудования.Добавить(ДанныеСмены.ИдентификаторУстройства);
		КонецЦикла;
		РезультатВыбора = Ждать ВыбратьИзСпискаАсинх(СписокОборудования);
		Если РезультатВыбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ИдентификаторУстройства = РезультатВыбора.Значение;
	КонецЕсли;
	
	ПараметрыОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыВыполненияОперации();
	КассовыеСменыВызовСервераБП.ЗаполнитьПараметрыОперацииДляОрганизацииПоУмолчанию(ПараметрыОперации);
		
	ОповещениеЗавершения = Новый ОписаниеОповещения("ОперацияЗавершение", ЭтотОбъект);
	ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФормированиеОтчетаБезГашения(
		ОповещениеЗавершения, УникальныйИдентификатор, ИдентификаторУстройства, ПараметрыОперации);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

#Область ПравоПримененияСпецрежима

&НаКлиенте
Процедура ПоказатьИнформациюОПравеПримененияСпецрежима()
	
	Если Не ОтборОрганизацияИспользование
		Или Не КонтрольПраваПримененияСпецрежимаКлиент.Контролировать(ОтборОрганизация) Тогда
		Элементы.ИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	Иначе
		ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере()
	
	ИнформацияОПравеПримененияСпецрежима = КонтрольПраваПримененияСпецрежима.ИнформацияОПравеПримененияСпецрежима(
		ОтборОрганизация,
		КонтрольПраваПримененияСпецрежима.ИмяПоказателяСпецрежимаДоходы());
		
	СледующееЗначениеНапоминания         = ИнформацияОПравеПримененияСпецрежима.СледующееЗначениеНапоминания;
	СсылкаНаПояснение                    = ИнформацияОПравеПримененияСпецрежима.СсылкаНаПояснение;
	
	Элементы.ИнформацияОПравеПримененияСпецрежима.Видимость = ИнформацияОПравеПримененияСпецрежима.Показать;
	Элементы.ИнформацияОПравеПримененияСпецрежима.ЦветФона = ИнформацияОПравеПримененияСпецрежима.ЦветФонаГруппы;
	Элементы.ТекстИнформации.Заголовок = ИнформацияОПравеПримененияСпецрежима.ТекстИнформации;
	Элементы.ТекстИнформацииРасширеннаяПодсказка.Заголовок = ИнформацияОПравеПримененияСпецрежима.ПодсказкаТекстИнформации;
	
	Элементы.Подробнее.Видимость       = ЗначениеЗаполнено(СсылкаНаПояснение);
	Элементы.Подробнее.Заголовок       = ИнформацияОПравеПримененияСпецрежима.ЗаголовокПояснения;
	
	Элементы.НапомнитьПозже.Заголовок  = ИнформацияОПравеПримененияСпецрежима.ТекстНапомнитьПозже;
	Элементы.НапомнитьПозже.Видимость  = ИнформацияОПравеПримененияСпецрежима.ОтображатьНапоминание;
	
	ДополнительнаяИнформацияСпецрежима = ИнформацияОПравеПримененияСпецрежима.ДополнительнаяИнформация;
	Если ДополнительнаяИнформацияСпецрежима <> Неопределено Тогда
		ИмяДополнительногоПоказателяСпецрежима      = ДополнительнаяИнформацияСпецрежима.ИмяПоказателя;
		СледующееЗначениеДополнительногоНапоминания = ДополнительнаяИнформацияСпецрежима.СледующееЗначениеНапоминания;
		СсылкаНаДополнительноеПояснение             = ДополнительнаяИнформацияСпецрежима.СсылкаНаПояснение;
		
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = ДополнительнаяИнформацияСпецрежима.Показать;
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.ЦветФона = ДополнительнаяИнформацияСпецрежима.ЦветФонаГруппы;
		Элементы.ТекстДополнительнойИнформации.Заголовок = ДополнительнаяИнформацияСпецрежима.ТекстИнформации;
		Элементы.ТекстДополнительнойИнформацииРасширеннаяПодсказка.Заголовок = ДополнительнаяИнформацияСпецрежима.ПодсказкаТекстИнформации;
		
		Элементы.ДополнительнаяИнформацияПодробнее.Видимость = ЗначениеЗаполнено(СсылкаНаДополнительноеПояснение);
		Элементы.ДополнительнаяИнформацияПодробнее.Заголовок = ДополнительнаяИнформацияСпецрежима.ЗаголовокПояснения;
		
		Элементы.ДополнительнаяИнформацияНапомнитьПозже.Заголовок = ДополнительнаяИнформацияСпецрежима.ТекстНапомнитьПозже;
		Элементы.ДополнительнаяИнформацияНапомнитьПозже.Видимость = ДополнительнаяИнформацияСпецрежима.ОтображатьНапоминание;
	Иначе
		Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	КонецЕсли;
	
	ИмяНастройкиПравоПримененияПатента = КонтрольПраваПримененияСпецрежима.ИмяНастройкиПравоПримененияСпецрежимаПатент();
	ВыводитсяИнформацияОПатенте = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ИнформацияОПравеПримененияСпецрежима,
		ИмяНастройкиПравоПримененияПатента,
		Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ОтложитьПоказНапоминанияНаСервере()
	
	Если ВыводитсяИнформацияОПатенте Тогда
		ИмяПоказателя = КонтрольПраваПримененияСпецрежима.ИмяНастройкиПравоПримененияСпецрежимаПатент();
	Иначе
		ИмяПоказателя = КонтрольПраваПримененияСпецрежима.ИмяПоказателяСпецрежимаДоходы();
	КонецЕсли;
	
	КонтрольПраваПримененияСпецрежима.ОтложитьПоказНапоминания(
		ОтборОрганизация,
		ИмяПоказателя,
		СледующееЗначениеНапоминания);
		
	Элементы.ИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	
	Если ВыводитсяИнформацияОПатенте Тогда
		ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтложитьПоказДополнительногоНапоминанияНаСервере()
	
	КонтрольПраваПримененияСпецрежима.ОтложитьПоказНапоминания(
		ОтборОрганизация,
		ИмяДополнительногоПоказателяСпецрежима,
		СледующееЗначениеДополнительногоНапоминания);
		
	Элементы.ДополнительнаяИнформацияОПравеПримененияСпецрежима.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаИзОФД

&НаКлиенте
Процедура ЗагрузитьИзОФДПоСпискуКассЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление(Результат);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.РазрешеноИзменение = Истина;
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ПеренестиЭлементВМККомандыФормы(ЭтотОбъект, Элементы.ФормаГруппаЗагрузитьИзОФД);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКСоздать(Команда)
	
	СписокВыбораОперации = Новый СписокЗначений;
	СписокВыбораОперации.Добавить("СоздатьРозничнаяПродажаЧек", "Чек");
	СписокВыбораОперации.Добавить("СоздатьВозвратПоЧеку", "Возврат по чеку");
	//Отступ
	СписокВыбораОперации.Добавить(Неопределено);
	СписокВыбораОперации.Добавить("ОтчетОРозничныхПродажахСоздать","Отчет о розничных продажах");
	
	ДопПараметры = Новый Структура("Команда", Команда);
	ОписаниеЗавершения = Новый ОписаниеОповещения("КомандыМК_Создать_Завершение", ЭтотОбъект, ДопПараметры);
	ПоказатьВыборИзМеню(ОписаниеЗавершения, СписокВыбораОперации, Команда);

КонецПроцедуры

&НаКлиенте
Процедура КомандыМК_Создать_Завершение(ВыбранныйЭлемент, ДопПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено
		Или ВыбранныйЭлемент.Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "СоздатьРозничнаяПродажаЧек" Тогда
		СоздатьРозничнаяПродажаЧек(ДопПараметры.Команда);
	ИначеЕсли ВыбранныйЭлемент.Значение = "СоздатьВозвратПоЧеку" Тогда
		СоздатьВозвратПоЧеку(ДопПараметры.Команда);
	ИначеЕсли ВыбранныйЭлемент.Значение = "ОтчетОРозничныхПродажахСоздать" Тогда
		ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаОбъекта");
	Иначе
		ВызватьИсключение "Неизвестный вид документа";
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.ГруппаБыстрыеОтборы);
	МобильныйКлиентБПФормаСписка.УстановитьВидимостьМКБыстрыеОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.ПодменюПечать, Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.СтруктураПодчиненности, Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.СоздатьНаОсновании, Элементы.Список.КонтекстноеМеню);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормы(ЭтотОбъект);
	
	//В реквизитах установить "Использовать всегда" для источника КолонкаСостояниеДокумента
	МобильныйКлиентБПФормаСписка.ДобавитьКолонкаСостояниеДокумента(ЭтотОбъект, "СтандартнаяКартинка", "Список");
	
	Элементы.Дата.РастягиватьПоГоризонтали = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.Дата);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.НомерККМ);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СуммаДокумента(ЭтотОбъект, Элементы.Сумма);
	
	Элементы.ТипДокумента.РастягиватьПоГоризонтали = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.ТипДокумента);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти
