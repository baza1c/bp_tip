
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ИмяВидимогоБаннера_РозничнаяТорговля() Экспорт
	
	ИмяБаннера = "";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьФискальныйРегистратор") Тогда
		СписокОборудованияОтбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
		СписокОборудованияОтбор.ТипыПО = МенеджерОборудованияКлиентСерверБП.ТипыКонтрольноКассовойТехники();
		СписокДоступныхУстройств = МенеджерОборудования.СписокОборудования(СписокОборудованияОтбор);
		Если Не ЗначениеЗаполнено(СписокДоступныхУстройств) Тогда
			Если НЕ ЕстьПодключенноеКассовоеОборудование() Тогда
				ИмяБаннера = "БаннерКассаНеПодключена_РозничнаяТорговля";
			КонецЕсли;
		Иначе
			Для Каждого Устройство Из СписокДоступныхУстройств Цикл
				ОписаниеПоследнейСмены = КассовыеСменыВызовСервера.ОписаниеПоследнейКассовойСмены(Устройство.Ссылка);
				Если ОписаниеПоследнейСмены <> Неопределено И ОписаниеПоследнейСмены.Статус = Перечисления.СтатусыКассовойСмены.Открыта
					И ОписаниеПоследнейСмены.ДатаИстеченияСрокаДействия < ТекущаяДатаСеанса() Тогда
					ИмяБаннера = "БаннерЗакрытьСмену_РозничнаяТорговля";
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИмяБаннера) И ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПоПлатежнымКартам") Тогда
		Если Не Справочники.ВидыОплатОрганизаций.ПроверитьЗаполненностьВидовОплат(
			Неопределено, Перечисления.ТипыОплат.ПлатежнаяКарта) Тогда
			ИмяБаннера = "БаннерНастроитьВидыОплат_РозничнаяТорговля";
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИмяБаннера;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварныйЧек";
	КомандаПечати.Представление = НСтр("ru = 'Товарный чек'");
	КомандаПечати.МенеджерПечати = "Документ.РозничнаяПродажа";
	КомандаПечати.ТипыОбъектовПечати = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		Тип("ДокументСсылка.РозничнаяПродажа"));
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОтчетККМ";
	КомандаПечати.Представление = НСтр("ru = 'Справка-отчет кассира (КМ-6)'");
	КомандаПечати.МенеджерПечати = "Документ.ОтчетОРозничныхПродажах";
	КомандаПечати.ТипыОбъектовПечати = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
	
	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов розничной торговли'");
		КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		// Реестр документов выводим в списке последним
		КомандаПечати.Порядок = 100;
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыПодменюУправленияКассовойСменой() Экспорт
	
	Параметры = НовыеПараметрыПодменюУправленияКассовойСменой();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьФискальныйРегистратор") Тогда
		Параметры.ДоступныеКоманды = ДоступныеКомандыПодменюУправленияКассовойСменой(Неопределено);
		Параметры.КартинкаСостоянияСмены
			= КассовыеСменыВызовСервераБП.ПиктограммаСостоянияКассовойСмены(Истина);
		Возврат Параметры;
	КонецЕсли;
	
	СменаОткрыта = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	СписокОборудованияОтбор              = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
	СписокОборудованияОтбор.ТипыПО       = МенеджерОборудованияКлиентСерверБП.ТипыКонтрольноКассовойТехники();
	СписокОборудованияОтбор.РабочееМесто = МенеджерОборудованияВызовСервера.РабочееМестоКлиента();
	
	СписокДоступныхУстройств = МенеджерОборудованияВызовСервера.СписокОборудования(СписокОборудованияОтбор);
	Для Каждого Устройство Из СписокДоступныхУстройств Цикл
		ОписаниеПоследнейСмены = КассовыеСмены.ОписаниеПоследнейКассовойСмены(Устройство.Ссылка);
		Если ОписаниеПоследнейСмены <> Неопределено
			И ОписаниеПоследнейСмены.Статус = Перечисления.СтатусыКассовойСмены.Открыта Тогда
			СменаОткрыта = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Параметры.ДоступныеКоманды = ДоступныеКомандыПодменюУправленияКассовойСменой(СменаОткрыта);
	Параметры.КартинкаСостоянияСмены
		= КассовыеСменыВызовСервераБП.ПиктограммаСостоянияКассовойСмены(СменаОткрыта);
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Параметры;
	
КонецФункции

Функция НовыеПараметрыПодменюУправленияКассовойСменой()
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДоступныеКоманды");
	Параметры.Вставить("КартинкаСостоянияСмены");
	Возврат Параметры;
	
КонецФункции

Функция ДоступныеКомандыПодменюУправленияКассовойСменой(СменаОткрыта)
	
	ИменаДоступныхКоманд = Новый Массив;
	Если СменаОткрыта = Истина Тогда
		ИменаДоступныхКоманд.Добавить("КассоваяСменаОткрыть");
		ИменаДоступныхКоманд.Добавить("КассоваяСменаОтчетОТекущемСостоянииРасчетов");
		ИменаДоступныхКоманд.Добавить("КассоваяСменаОтчетБезГашения");
		ИменаДоступныхКоманд.Добавить("КассоваяСменаЗакрыть");
	ИначеЕсли СменаОткрыта = Ложь Тогда
		ИменаДоступныхКоманд.Добавить("КассоваяСменаОткрыть");
	ИначеЕсли СменаОткрыта = Неопределено Тогда
		// Не используется подключаемое оборудование
		ИменаДоступныхКоманд.Добавить("КассоваяСменаЗакрыть");
	КонецЕсли;
	Возврат ИменаДоступныхКоманд;
	
КонецФункции

#Область СлужебныйПрограммныйИнтерфейс

Функция НастройкиБаннераКассаНеПодключена() Экспорт
	
	НастройкиБаннера = Новый Структура;
	ПодключениеКассыДоступно = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.ПодключаемоеОборудование)
		И ПравоДоступа("Чтение", Метаданные.Справочники.РабочиеМеста); 
	НастройкиБаннера.Вставить("ПодключениеКассыДоступно", ПодключениеКассыДоступно);
	Если ПодключениеКассыДоступно Тогда
		ЗаголовокБаннера = НСтр("ru = 'Касса не подключена'");
	Иначе
		ЗаголовокБаннера = НСтр("ru = 'Обратитесь к администратору программы для подключения кассы'");
	КонецЕсли;
	НастройкиБаннера.Вставить("ЗаголовокБаннера", ЗаголовокБаннера);
	Возврат НастройкиБаннера;
	
КонецФункции

Функция ЕстьПодключенноеКассовоеОборудование() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПодключаемоеОборудование.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	ПодключаемоеОборудование.ТипОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыПодключаемогоОборудования.ККТ)
	|	И ПодключаемоеОборудование.УстройствоИспользуется
	|	И НЕ ПодключаемоеОборудование.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОфлайнОборудование.Ссылка
	|ИЗ
	|	Справочник.ОфлайнОборудование КАК ОфлайнОборудование
	|ГДЕ
	|	(ОфлайнОборудование.ТипОфлайнОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыОфлайнОборудования.ККМ)
	|			ИЛИ ОфлайнОборудование.ТипОфлайнОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыОфлайнОборудования.ОФД))
	|	И НЕ ОфлайнОборудование.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.Ссылка
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК СинхронизацияДанныхЧерезУниверсальныйФормат
	|ГДЕ
	|	НЕ СинхронизацияДанныхЧерезУниверсальныйФормат.ЭтотУзел
	|	И СинхронизацияДанныхЧерезУниверсальныйФормат.ВариантНастройки = ""ОбменМК""
	|	И НЕ СинхронизацияДанныхЧерезУниверсальныйФормат.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОжиданиеПодключенияМК.ИдентификаторМК
	|ИЗ
	|	РегистрСведений.ОжиданиеПодключенияМК КАК ОжиданиеПодключенияМК";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат.Следующий();
	
КонецФункции
	
#КонецОбласти

#КонецЕсли








