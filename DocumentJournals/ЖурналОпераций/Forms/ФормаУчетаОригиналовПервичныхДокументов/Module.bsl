#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПараметрыРазмещенияПодключаемыхКоманд();
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаСписка(ЭтотОбъект, Элементы.Список, Элементы.СуммаДокумента);
	УчетОригиналовПервичныхДокументов.УстановитьУсловноеОформлениеВФормеСписка(ЭтотОбъект, Элементы.Список);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
	УстановитьОтборПоВидамДокументовУчетаОригиналов(Параметры.РежимОтображенияДокументов);
	
	Если Параметры.РежимОтображенияДокументов = "Поставщики" Тогда
		Заголовок =  НСтр("ru = 'Оригиналы первичных документов поставщиков'");
	ИначеЕсли Параметры.РежимОтображенияДокументов = "Покупатели" Тогда
		Заголовок =  НСтр("ru = 'Оригиналы первичных документов покупателей'");
	КонецЕсли;
	
	ОтображениеСписка = ИмяОтображениеСпискаПоДокументам();
	ОтображениеСпискаПриИзмененииНаСервере();
	
	УстановитьУсловноеОформление();
	
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	// Команды печати мы здесь не показываем. Печатать отсюда документы не требуется. Здесь работаем
	// с уже сформированными и подписанными печатными формами.
	// Код в ПодключаемыеКоманды.ПриСозданииНаСервере добавляет команды установки
	// состояний печатных форм. 
	Элементы.ПодменюПечать.Видимость = Ложь;
	
	ЗаполнитьСписокВыбораСостоянияОригиналаОтбор();
	
	Элементы.Организация.Видимость = ИспользуетсяНесколькоОрганизаций;
	// Отбор по основной организации
	Если ИспользуетсяНесколькоОрганизаций Тогда
		ОтборОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.ЖурналДокументов.ФормаУчетаОригиналовПервичныхДокументов",
		"ФормаСписка",
		НСтр("ru = 'Новости: Оригиналы первичных документов'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОтображениеСпискаПриИзмененииНаСервере();
	
	ОтборКонтрагентПриИзмененииНаСервере();
	
	ОтборСостояниеОригиналаПервичногоДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		
		ОтборОрганизация = Параметр;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	ПрисоединенныеФайлыБПКлиент.ОбновитьСписокПослеДобавленияФайла(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументовКлиент.ОбработчикОповещенияФормаСписка(ИмяСобытия, ЭтотОбъект, Элементы.Список);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизацияИспользование = Истина;
	КонецЕсли;
	
	ОтборОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентИспользованиеПриИзменении(Элемент)
	
	ОтборКонтрагентПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонрагентПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
		ОтборКонтрагентИспользование = Истина;
	КонецЕсли;
	
	ОтборКонтрагентПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОригиналаПервичногоДокументаИспользованиеПриИзменении(Элемент)
	
	ОтборСостояниеОригиналаПервичногоДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОригиналаПервичногоДокументаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборСостояниеОригиналаПервичногоДокумента) Тогда
		ОтборСостояниеОригиналаПервичногоДокументаИспользование = Истина;
	КонецЕсли;
	
	ОтборСостояниеОригиналаПервичногоДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеСпискаПриИзменении(Элемент)
	
	ОтображениеСпискаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	Если Поле.Имя = "СостояниеОригиналаПервичногоДокумента" Или Поле.Имя = "СостояниеОригиналПолучен" Тогда
		УчетОригиналовПервичныхДокументовКлиент.СписокВыбор(Поле.Имя, ЭтотОбъект, Элементы.Список, СтандартнаяОбработка);
	Иначе
		ДанныеВыбраннойСтроки = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);
		Если ДанныеВыбраннойСтроки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ПоказатьЗначение( , ДанныеВыбраннойСтроки.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеОбработчикиБСП

// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов

// Параметры:
//  Команда - КомандаФормы
//
&НаКлиенте
Процедура Подключаемый_УстановитьСостояниеОригинала(Команда)
	
	УчетОригиналовПервичныхДокументовКлиент.УстановитьСостояниеОригинала(Команда.Имя,ЭтотОбъект,Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыСостоянияОригинала()
	
	ОбновитьКомандыСостоянияОригинала()
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКомандыСостоянияОригинала()
	
	ПараметрыРазмещения = ПараметрыРазмещенияПодключаемыхКоманд();
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	
КонецПроцедуры

//Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ПодключаемыеОбработчикиБИП

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыРазмещенияПодключаемыхКоманд()
	
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПараметрыРазмещения.Источники = Метаданные.ОпределяемыеТипы.ОбъектСУчетомОригиналовПервичныхДокументов.Тип;
	Возврат ПараметрыРазмещения;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	Элемент = Список.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПолеОтступа");

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.Использование  = Истина;
	ГруппаОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбщееСостояние"); 
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбщееСостояние");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	Элемент = Список.УсловноеОформление.Элементы.Добавить();

	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбщееСостояние");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение =Истина;
	ЭлементОтбора.Использование = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ЦветДополнительнойНавигации);

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоВидамДокументовУчетаОригиналов(РежимОтображенияДокументов)
	
	Если РежимОтображенияДокументов = "Поставщики" Тогда
		
		ТипыДокументовСОригиналамиПервичныхДокументов = УчетОригиналовПервичныхДокументовБП.ТипыДокументовСОригиналамиПервичныхДокументовПоставщиков();
		
	ИначеЕсли РежимОтображенияДокументов = "Покупатели" Тогда
		
		ТипыДокументовСОригиналамиПервичныхДокументов = УчетОригиналовПервичныхДокументовБП.ТипыДокументовСОригиналамиПервичныхДокументовПокупателей();
		
	Иначе
		
		ТипыДокументовСОригиналамиПервичныхДокументов = УчетОригиналовПервичныхДокументовБП.ТипыДокументовСОригиналамиПервичныхДокументов();
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ТипыДокументовСОригиналамиПервичныхДокументов", ТипыДокументовСОригиналамиПервичныхДокументов, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОтборОрганизацияПриИзмененииНаСервере()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

&НаСервере
Процедура ОтборКонтрагентПриИзмененииНаСервере()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
	
КонецПроцедуры

&НаСервере
Процедура ОтборСостояниеОригиналаПервичногоДокументаПриИзмененииНаСервере()
	
	Если ТипЗнч(ОтборСостояниеОригиналаПервичногоДокумента) = Тип("СправочникСсылка.СостоянияОригиналовПервичныхДокументов") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СостояниеОригиналаПервичногоДокумента",
			ОтборСостояниеОригиналаПервичногоДокумента, ВидСравненияКомпоновкиДанных.Равно, , ОтборСостояниеОригиналаПервичногоДокументаИспользование);
	ИначеЕсли ОтборСостояниеОригиналаПервичногоДокумента = ИмяСостоянияИзвестно() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СостояниеОригиналаПервичногоДокумента", ,
			ВидСравненияКомпоновкиДанных.Заполнено, , ОтборСостояниеОригиналаПервичногоДокументаИспользование);
	ИначеЕсли ОтборСостояниеОригиналаПервичногоДокумента = ИмяСостоянияНеИзвестно() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СостояниеОригиналаПервичногоДокумента", ,
			ВидСравненияКомпоновкиДанных.НеЗаполнено, , ОтборСостояниеОригиналаПервичногоДокументаИспользование);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СостояниеОригиналаПервичногоДокумента", ,
			ВидСравненияКомпоновкиДанных.Равно, , Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораСостоянияОригиналаОтбор()
	
	СостоянияОригинала = УчетОригиналовПервичныхДокументов.ВсеСостояния();
	
	Элементы.ОтборСостояниеОригиналаПервичногоДокумента.СписокВыбора.Очистить();
	Для Каждого Состояние Из СостоянияОригинала Цикл
		Элементы.ОтборСостояниеОригиналаПервичногоДокумента.СписокВыбора.Добавить(Состояние);
	КонецЦикла;
	Элементы.ОтборСостояниеОригиналаПервичногоДокумента.СписокВыбора.Добавить(ИмяСостоянияИзвестно(), НСтр("ru = '<Состояние известно>'"));
	Элементы.ОтборСостояниеОригиналаПервичногоДокумента.СписокВыбора.Добавить(ИмяСостоянияНеИзвестно(), НСтр("ru = '<Состояние неизвестно>'"));
	
КонецПроцедуры

&НаСервере
Процедура ОтображениеСпискаПриИзмененииНаСервере()

	// Обнуляем отбор по общему состоянию
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
		Список,"ОбщееСостояние");
	
	Если ОтображениеСписка = ИмяОтображениеСпискаПоДокументам() Тогда
		
		ГруппаОтбора = Список.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.Использование  = Истина;
		ГруппаОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		
		// Задаем новый отбор для отображения списка "по документам"
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбщееСостояние");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбщееСостояние");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Истина;
		
		Элементы.ПолеОтступа.Видимость = Ложь;
		
	ИначеЕсли ОтображениеСписка = ИмяОтображениеСпискаПоПечатнымФормам() Тогда
		Элементы.ПолеОтступа.Видимость = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСостоянияИзвестно()
	Возврат "СостояниеИзвестно";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСостоянияНеИзвестно()
	Возврат "СостояниеНеизвестно";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяОтображениеСпискаПоДокументам()
	Возврат "ПоДокументам";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяОтображениеСпискаПоПечатнымФормам()
	Возврат "ПоПечатнымФормам";
КонецФункции

#КонецОбласти