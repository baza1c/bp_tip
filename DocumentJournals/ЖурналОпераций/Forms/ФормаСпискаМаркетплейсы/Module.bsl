
&НаКлиенте
Перем ПараметрыОбработчикаОжидания Экспорт;

&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаГлобальныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	Элементы.Организация.Видимость = ИспользуетсяНесколькоОрганизаций;
	
	ВидимостьМегамаркет = ПолучитьФункциональнуюОпцию("ВедетсяРозничнаяТорговля") И ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПоПлатежнымКартам");
	Элементы.ФормаЗагрузитьИзФайлаМегамаркет.Видимость = ВидимостьМегамаркет;
	Элементы.ФормаЗагрузитьУслугиМегаМаркет.Видимость  = ВидимостьМегамаркет;
	
	ОсновнаяОрганизация           = Справочники.Организации.ОрганизацияПоУмолчанию();
	ОтборОрганизация              = ОсновнаяОрганизация;
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
	
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеДокументаПередачаТоваров", НСтр("ru='Передача на реализацию'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеУслугиКомиссионера", НСтр("ru='Поступление услуг (акт, УПД)'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеОтчетОбОптовыхПродажах", НСтр("ru='Отчет о продажах (оптовые продажи)'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеОтчетОРозничныхПродажах", НСтр("ru='Отчет о продажах (розничные продажи)'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеОтчетОСписании", НСтр("ru='Отчет о списании (акт о списании)'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеОтчетОбОприходовании", НСтр("ru='Отчет об оприходовании (декомпенсация)'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеВозвратСРеализации", НСтр("ru='Возврат переданного на реализацию'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеДокументаВыкупТоваровКомиссионером", НСтр("ru='Выкуп комиссионером'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеДокументаВозвратВыкупленныеКомиссионеромТовары", НСтр("ru='Возврат выкупленного комиссионером'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеДокументаПродажаТоваровСоСкладаКомиссионера", НСтр("ru='Продажа со склада комиссионера'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПредставлениеДокументаВозвратТоварыНаСкладКомиссионера", НСтр("ru='Возврат на склад комиссионера'"));
	
	МассивТиповДокументов = Новый Массив;
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"));
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.ОтчетМаркетплейса"));
	Список.Параметры.УстановитьЗначениеПараметра("ОтборТипыДокументов", МассивТиповДокументов);
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	ИначеЕсли ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров") Тогда
		Заголовок = "Маркетплейсы и комиссионеры";
		АвтоЗаголовок = Ложь;
	Иначе
		Заголовок = "Маркетплейсы";
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.ЖурналДокументов.ЖурналОпераций",
		"ФормаСпискаМаркетплейсы",
		СтрШаблон(НСтр("ru = 'Новости: %1'"), Заголовок),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ТарификацияБП.РазместитьИнформациюОбОграниченииПоКоличествуОбъектов(ЭтотОбъект);
	
	АдресХранилищаНастройкиДинСпискаДляРеестра = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	//Логирование обмена с маркетплейсами
	ДлительныеОперации.ВыполнитьПроцедуру(ДлительныеОперации.ПараметрыВыполненияПроцедуры()
						, "РегистрыСведений.ЖурналОбменаСМаркетплейсами.УдалитьУстаревшиеЗаписиЖурналаОбменаМаркетплейсов");
	//Конец Логирование обмена с маркетплейсами
	
	ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораВсе();
	УстановитьУсловноеОФормление();
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		
		ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список, ,Параметр);
		
	ИначеЕсли ИмяСобытия = "ИзмененыДокументыМаркетплейсов" Тогда
		
		Элементы.Список.Обновить();
		УправлениеВидимостью();
		
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
	
	РеквизитыОрганизацииЗаполнены = ОбщегоНазначенияБПКлиент.ПроверитьНаличиеОрганизаций();
	
	Если РеквизитыОрганизацииЗаполнены Тогда
		
		// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
		// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	СтруктураОтбора = Неопределено;
	Если Параметры.Свойство("Отбор", СтруктураОтбора) И ЗначениеЗаполнено(СтруктураОтбора) Тогда
		
		Если СтруктураОтбора.Свойство("Организация") И ЗначениеЗаполнено(СтруктураОтбора.Организация) Тогда
			ОтборОрганизация = СтруктураОтбора.Организация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
			Параметры.Отбор.Удалить("Организация");
		КонецЕсли;
		Если СтруктураОтбора.Свойство("Контрагент") И ЗначениеЗаполнено(СтруктураОтбора.Контрагент) Тогда
			ОтборКонтрагент = СтруктураОтбора.Контрагент;
			ОтборКонтрагентИспользование = ЗначениеЗаполнено(ОтборКонтрагент);
			Параметры.Отбор.Удалить("Контрагент");
		КонецЕсли;
		
	Иначе
		Если ЗначениеЗаполнено(ОсновнаяОрганизация) И ОтборОрганизация <> ОсновнаяОрганизация Тогда
			ОтборОрганизация = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ИначеЕсли НЕ ОтборОрганизацияИспользование Тогда
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		КонецЕсли;
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		Если Элементы.ГруппаСостояниеАвтозагрузки.Видимость
			Или Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость Тогда
			ОбменСМаркетплейсВызовСервера.ЗаписатьДатуЗакрытияБаннераСостояниеАвтозагрузки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВариантОтбораПредупрежденийПриЗагрузкеПриИзменении(Элемент)
	
	УстановитьОтборПредупрежденийПриЗагрузке(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьНакладнуюПередача(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПередачаТоваров"));
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВозвратТоваров(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ПереданныеТовары"));
	ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтчетОПродажахРозница(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах"));
	ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтчетОПродажахОпт(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОПродажах"));
	ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтчетОСписании(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОСписании"));
	ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьУслугиКомиссионера(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги"));
	ОткрытьФорму("Документ.ПоступлениеТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетКомиссионераИзФайла(Команда)
	
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьОтчетКомиссионераИзФайла(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУведомлениеОВыкупеВайлдберризИзФайла(Команда)
	
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьУведомлениеОВыкупеВайлдберризИзФайла(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлыWB(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Маркетплейс", ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
	ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаФайлов", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлыOzon(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Маркетплейс", ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
	ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаФайлов", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	ОтборКонтрагентИспользование = ЗначениеЗаполнено(ОтборКонтрагент);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзМаркетПлейсWB(Команда)
	
	ЗагрузитьИзМаркетплейс(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзМаркетПлейсOZON(Команда)
	
	ЗагрузитьИзМаркетплейс(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНоменклатуруWB(Команда)
	ОткрытьФормуЗагрузкаКаталога(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНоменклатуруЯндекс(Команда)
	ОткрытьФормуЗагрузкаКаталога(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры   

&НаКлиенте
Процедура ЗагрузитьНоменклатуруОзон(Команда)
	ОткрытьФормуЗагрузкаКаталога(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
КонецПроцедуры 

&НаКлиенте
Процедура ВыгрузитьОстаткиWB(Команда)
	ОткрытьФормуОстаткиМаркетплейсов(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОстаткиЯндекс(Команда)
	ОткрытьФормуОстаткиМаркетплейсов(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры  

&НаКлиенте
Процедура ВыгрузитьОстаткиOZON(Команда)
	ОткрытьФормуОстаткиМаркетплейсов(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлыМегамаркет(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Маркетплейс", ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Мегамаркет"));
	ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаФайлов", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПомощникИзмененияЮрЛица(Команда)
	ОткрытьФорму("Обработка.WildberriesПереходРВБ.Форма.Форма", , , , , , , РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетЯндексМаркетИзФайла(Команда)
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьОтчетЯндексМаркетИзФайла(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзМаркетплейсЯндекс(Команда)
	ЗагрузитьИзМаркетплейс(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВыкупТовараНаРеализации(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером"));
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьВозвратВыкупленныхТоваров(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ВыкупленныеКомиссионеромТовары"));
	ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПродажаТоваровСоСкладаКомиссионера(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера"));
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВозвратТоваровНаСкладКомиссионера(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ТоварыНаСкладКомиссионера"));
	ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналОбмена(Команда)
	
	ОбменСМаркетплейсКлиент.ОткрытьЖурналОбмена(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУслугиWB(Команда)
	
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьПоступлениеУслугМаркетплейсаИзФайла(
		ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУслугиЯндексМаркет(Команда)
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьПоступлениеУслугМаркетплейсаИзФайла(
		ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУслугиМегаМаркет(Команда)
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьПоступлениеУслугМаркетплейсаИзФайла(
		ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Мегамаркет"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУслугиОЗОН(Команда)
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьПоступлениеУслугМаркетплейсаИзФайла(
		ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтчетОбОприходовании(Команда)
	
	СтруктураПараметров = ПолучитьПараметрыФормыДокумента(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОбОприходовании"));
	ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуру(Команда)
	
	СопоставитьНоменклатуруНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуруOzon(Команда)
	
	СопоставитьНоменклатуруНаКлиенте(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуруWildberries(Команда)
	
	СопоставитьНоменклатуруНаКлиенте(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуруЯндексМаркет(Команда)
	
	СопоставитьНоменклатуруНаКлиенте(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаАвтозагрузки(Команда)
	
	ОбменСМаркетплейсКлиент.ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТекстСостояниеАвтозагрузкиРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЖурналОбмена" Тогда
		Отбор = Новый Структура("ДатаПоследнейАвтозагрузки", ДатаПоследнейАвтозагрузки());
		ОбменСМаркетплейсКлиент.ОткрытьЖурналОбмена(ЭтотОбъект, Отбор);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "Настройка" Тогда
		ОбменСМаркетплейсКлиент.ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстСостояниеАвтозагрузкиОшибкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЖурналОбмена" Тогда
		Отбор = Новый Структура("ДатаПоследнейАвтозагрузки", ДатаПоследнейАвтозагрузки());
		ОбменСМаркетплейсКлиент.ОткрытьЖурналОбмена(ЭтотОбъект, Отбор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьСостояниеАвтозагрузкиНажатие(Элемент)
	
	Элементы.ГруппаСостояниеАвтозагрузки.Видимость = Не Элементы.ГруппаСостояниеАвтозагрузки.Видимость;
	
	ОбменСМаркетплейсВызовСервера.ЗаписатьДатуЗакрытияБаннераСостояниеАвтозагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьСостояниеАвтозагрузкиОшибкаНажатие(Элемент)
	
	Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость = Не Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость;
	
	ОбменСМаркетплейсВызовСервера.ЗаписатьДатуЗакрытияБаннераСостояниеАвтозагрузки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	
	ОтборыСписков.СброситьИспользованиеПользовательскихОтборовВНастройке(Настройки);
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Если УправлениеПечатьюБПКлиентСервер.ЭтоИмяКомандыРеестрДокументов(Команда.Имя) Тогда
		НастройкиДинамическогоСписка(Команда.Имя);
	КонецЕсли;
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастройкиДинамическогоСписка(ИмяВФорме)
	
	Отчеты.РеестрДокументов.НастройкиДинамическогоСписка(ЭтотОбъект,, ИмяВФорме);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыФормыДокумента(ВидОперации = Неопределено)
		
	СтруктураПараметров = Новый Структура;
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	
	Если ЗначениеЗаполнено(ВидОперации) Тогда
		ЗначенияЗаполнения.Вставить("ВидОперации", ВидОперации);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОжиданиеКонвертацииФайла()
	
	Попытка
		
		Если ЭлектронноеВзаимодействиеБПВызовСервера.ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			ЭлектронноеВзаимодействиеБПКлиент.НачатьЗагрузкуИзСтруктуры(АдресХранилища, УникальныйИдентификатор);
			
		Иначе
			
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ОжиданиеКонвертацииФайла", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
				
			КонецЕсли;
			
	Исключение			
		
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ОперацияВыполненаУспешно(ДлительнаяОперация) Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено));
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияМаркетПлейсы());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура Подключаемый_ОжиданиеКонвертацииФайлаЯндекс()
	
	Попытка
		
		Если ЭлектронноеВзаимодействиеБПВызовСервера.ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			ЭлектронноеВзаимодействиеБПКлиент.ОткрытьПомощникЗагрузкиОтчетаЯндекс(
				АдресХранилища, УникальныйИдентификатор,,, ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиЧерезФайл());
		Иначе
			
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ОжиданиеКонвертацииФайлаЯндекс", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
				
			КонецЕсли;
			
	Исключение			
		
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОстаткиМаркетплейсов(ВидМаркетплейса)
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыОткрытияФормы.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.ОстаткиТоваровМаркетплейсов.Форма.ФормаСписка", ПараметрыОткрытияФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗагрузкаКаталога(ВидМаркетплейса)
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	ПараметрыОткрытияФормы.Вставить("ВидОперации",     "ЗагрузкаКаталога");
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыОткрытияФормы.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаКаталога", ПараметрыОткрытияФормы,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#Область ЗагрузкаОтчетовПоAPI
	
&НаКлиенте
Процедура ЗагрузитьИзМаркетплейс(ВидМаркетплейс)
	
	Если Не ОбменСМаркетплейсВызовСервера.ДоступенОбменСМаркетплейс() Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Недостаточно прав для обмена с маркетплейсом'"));
		Возврат;
		
	КонецЕсли;
	
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидМаркетплейс", ВидМаркетплейс);

	Если ЗначенияЗаполнения.Свойство("Организация") Тогда
		ПараметрыФормы.Вставить("Организация", ЗначенияЗаполнения.Организация);
	КонецЕсли;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаИзМаркетплейсПродолжение",ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаЗагрузкиИзМаркетплейс", ПараметрыФормы, ЭтотОбъект,,,,ОповещениеОЗавершении,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаИзМаркетплейсПродолжение(ПараметрыЗагрузки, ДопПараметры) Экспорт
	Если НЕ ЗначениеЗаполнено(ПараметрыЗагрузки) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Форма", ЭтотОбъект);
	
	Если ПараметрыЗагрузки.ВидМаркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет") Тогда
		ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("ОткрытьПомощникЗагрузкиОтчетаЯндекс", ЭтотОбъект, ПараметрыЗагрузки));
		ПараметрыВыполнения.Вставить("ВыводитьПрогрессВыполнения", Истина);
		ПараметрыВыполнения.Вставить("Заголовок", НСтр("ru = 'Яндекс.Маркет готовит отчет'"));
	Иначе
		ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("НачатьЗагрузкуИзСтруктуры", ЭтотОбъект));
		ПараметрыВыполнения.Вставить("ВыводитьПрогрессВыполнения", Ложь);
		ПараметрыВыполнения.Вставить("Заголовок", НСтр("ru = 'Загружаем данные из маркетплейс'"));
	КонецЕсли;
	
	ОбменСМаркетплейсКлиент.ПрочитатьДанныеОтчетаВСтруктуру(ПараметрыЗагрузки, ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Функция ОперацияВыполненаУспешно(Результат)
	Возврат Результат <> Неопределено И Результат.Статус = "Выполнено";
КонецФункции 

&НаКлиенте
Процедура НачатьЗагрузкуИзСтруктуры(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ ОперацияВыполненаУспешно(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронноеВзаимодействиеБПКлиент.НачатьЗагрузкуИзСтруктуры(Результат.АдресРезультата, УникальныйИдентификатор);
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазобратьПолученныеДанные(АдресРезультата)
	РезультатОперации = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если РезультатОперации.ДанныеДокумента <> Неопределено Тогда
		РезультатОперации.ДанныеДокумента = ПоместитьВоВременноеХранилище(РезультатОперации.ДанныеДокумента, АдресРезультата);
	КонецЕсли;
	
	Возврат РезультатОперации;
КонецФункции

&НаКлиенте
Процедура ОткрытьПомощникЗагрузкиОтчетаЯндекс(Результат, ПараметрыЗагрузки) Экспорт
	
	Если НЕ ОперацияВыполненаУспешно(Результат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("ru = 'При получении данных о продажах произошла ошибка. Подробности в журнале регистрации'");
		Возврат;
	КонецЕсли;
	
	РезультатРазбораДанных = РазобратьПолученныеДанные(Результат.АдресРезультата);
	
	Если РезультатРазбораДанных.КодОтветаСервера = 401 или РезультатРазбораДанных.КодОтветаСервера = 403 Тогда
		ОбменСМаркетплейсКлиент.ПредложитьНовуюАвторизацию(ПараметрыЗагрузки.Организация);
	ИначеЕсли ЗначениеЗаполнено(РезультатРазбораДанных.ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатРазбораДанных.ТекстОшибки);
	ИначеЕсли РезультатРазбораДанных.ДанныеДокумента = Неопределено Тогда
		ПоказатьПредупреждение(,
			СтрШаблон(НСтр("ru = 'Нет продаж за %1'"), ФОрмат(ПараметрыЗагрузки.НачалоПериода, "ДФ='ММММ гггг'")),15);
	Иначе
		ЭлектронноеВзаимодействиеБПКлиент.ОткрытьПомощникЗагрузкиОтчетаЯндекс(РезультатРазбораДанных.ДанныеДокумента, УникальныйИдентификатор, ПараметрыЗагрузки.Организация, ПараметрыЗагрузки.ИдентификаторСклада);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Строки с предупреждениями при загрузке
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Дата");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Номер");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Тип");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СуммаДокумента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Организация");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДоговорКонтрагента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Ответственный");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Комментарий");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ЕстьПредупреждения", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ДокументыМаркетплейсовСПредупреждениямиПриЗагрузке);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостью()
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	ПараметрыОтбораПредупреждений = Предупреждения.НовыеПараметрыОтбораПредупреждений();
	ПараметрыОтбораПредупреждений.Организация = ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено);
	
	ДатаАвтозагрузки = НастройкиАвтозагрузки.ДатаПоследнейЗагрузки();
	ЕстьОшибкаЗагрузки = НастройкиАвтозагрузки.ЕстьОшибкаЗагрузки();
	КоличествоПредупреждений = Предупреждения.КоличествоПредупреждений(ПараметрыОтбораПредупреждений);
	ЕстьПредупреждения = Предупреждения.ЕстьПредупреждения();
	ЕстьПредупрежденияВСписке = Не ЕстьОшибкаЗагрузки И КоличествоПредупреждений > 0;
	МаркетплейсыКСопоставлению = ОбменСМаркетплейс.МаркеплейсыКСопоставлениюНоменклатуры();
	
	ПоказыватьСостояниеАвтозагрузкиОшибка = ЕстьОшибкаЗагрузки
		И ДатаАвтозагрузки > ОбменСМаркетплейсВызовСервера.ПолучитьДатуЗакрытияБаннераСостояниеАвтозагрузки();
	
	ПоказыватьСостояниеАвтозагрузки = Не ЕстьОшибкаЗагрузки И Не ЕстьПредупреждения
		И ДатаАвтозагрузки > ОбменСМаркетплейсВызовСервера.ПолучитьДатуЗакрытияБаннераСостояниеАвтозагрузки();
		
	Элементы.ГруппаПредупрежденияПриЗагрузкеИзМаркетплейсов.Видимость = ЕстьПредупрежденияВСписке;
	Элементы.ГруппаСостояниеАвтозагрузки.Видимость = ПоказыватьСостояниеАвтозагрузки;
	Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость = ПоказыватьСостояниеАвтозагрузкиОшибка;
	
	Если ЕстьПредупрежденияВСписке Тогда
		СписокВыбора = Элементы.ВариантОтбораПредупрежденийПриЗагрузке.СписокВыбора;
		ЭлементСписка = СписокВыбора.НайтиПоЗначению(ВариантОтбораТребующиеПроверки());
		Если ЭлементСписка <> Неопределено Тогда
			ЭлементСписка.Представление = СтрШаблон(НСтр("ru = 'Требующие проверки (%1)'"), КоличествоПредупреждений);
		КонецЕсли;
	КонецЕсли;
	
	Маркетплейсы = Перечисления.ВидыМаркетплейсов;
	Если ЗначениеЗаполнено(МаркетплейсыКСопоставлению) Тогда
		Элементы.СопоставитьНоменклатуру.Видимость = МаркетплейсыКСопоставлению.Количество() = 1;
		Элементы.ПанельСопоставитьНоменклатуру.Видимость = МаркетплейсыКСопоставлению.Количество() > 1;
		
		Элементы.СопоставитьНоменклатуруOzon.Видимость =
			МаркетплейсыКСопоставлению.Найти(Маркетплейсы.OZON) <> Неопределено;
		
		Элементы.СопоставитьНоменклатуруЯндексМаркет.Видимость =
			МаркетплейсыКСопоставлению.Найти(Маркетплейсы.ЯндексМаркет) <> Неопределено;
		
		Элементы.СопоставитьНоменклатуруWildberries.Видимость =
			МаркетплейсыКСопоставлению.Найти(Маркетплейсы.Wildberries) <> Неопределено;
	Иначе
		Элементы.СопоставитьНоменклатуру.Видимость = Ложь;
		Элементы.ПанельСопоставитьНоменклатуру.Видимость = Ложь;
	КонецЕсли;
	
	Если ПоказыватьСостояниеАвтозагрузкиОшибка Тогда
		Элементы.ТекстСостояниеАвтозагрузкиОшибка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = '%1 при загрузке отчетов маркетплейсов произошла ошибка. Подробнее в <a href = ""%2"">Журнале обмена</a>.'"),
				Формат(ДатаАвтозагрузки, "ДФ='dd.MM.yyyy HH:mm'"),
				"ЖурналОбмена"));
	КонецЕсли;
	
	Если ПоказыватьСостояниеАвтозагрузки Тогда
		Элементы.ТекстСостояниеАвтозагрузки.Заголовок =
			СтрШаблон(НСтр("ru = '%1 в %2 созданы новые документы по данным маркетплейсов'"),
			Формат(ДатаАвтозагрузки, "ДФ='dd.MM.yyyy'"),
			Формат(ДатаАвтозагрузки, "ДФ='HH:mm'"));
			Элементы.ТекстСостояниеАвтозагрузки.РасширеннаяПодсказка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				СтрШаблон("Подробности автоматической загрузки отчетов в <a href = ""%1"">Журнале обмена</a>.
				|Изменить расписание автоматической загрузки отчетов можно в <a href = ""%2"">Форме настройки</a>.",
				"ЖурналОбмена",
				"Настройка"));
	КонецЕсли;
	
	Если Не ЕстьПредупрежденияВСписке И ВариантОтбораПредупрежденийПриЗагрузке <> ВариантОтбораВсе() Тогда
		ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораВсе();
		УстановитьОтборПредупрежденийПриЗагрузке(ЭтотОбъект);
	КонецЕсли;
	
	Если ЕстьПредупрежденияВСписке И ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораТребующиеПроверки() Тогда
		УстановитьОтборПредупрежденийПриЗагрузке(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаAPIOzon(Команда)
	ОткрытьНастройкуМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
КонецПроцедуры

&НаКлиенте
Процедура НастройкаAPIWB(Команда)
	ОткрытьНастройкуМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
КонецПроцедуры

&НаКлиенте
Процедура НастройкаAPIЯндекс(Команда)
	ОткрытьНастройкуМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуМаркетплейса(Маркетплейс)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидМаркетплейса", Маркетплейс);
	КоличествоНастроек = 0;
	Если НЕ ИспользуетсяНесколькоОрганизаций Тогда
		Настройка = ЕдинственнаяНастройкаМаркетплейса(Маркетплейс, КоличествоНастроек);
		Если ЗначениеЗаполнено(Настройка) Тогда
			ПараметрыФормы.Вставить("Ключ", Настройка);
		КонецЕсли;
	КонецЕсли;
		
	ИмяФормыНастроек = СтрШаблон("Справочник.НастройкиИнтеграцииМаркетплейс.%1", 
		?(ЗначениеЗаполнено(Настройка)
			ИЛИ (НЕ ИспользуетсяНесколькоОрганизаций И КоличествоНастроек < 2), "ФормаОбъекта",  "ФормаСписка"));
	ОткрытьФорму(ИмяФормыНастроек, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ЕдинственнаяНастройкаМаркетплейса(ВидМаркетплейса, КоличествоНастроек)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	НастройкиИнтеграцииМаркетплейс.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НЕ НастройкиИнтеграцииМаркетплейс.ПометкаУдаления
	|	И НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = &ВидМаркетплейса";
	
	Выборка = Запрос.Выполнить().Выбрать();
	КоличествоНастроек = Выборка.Количество();
	Если КоличествоНастроек = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.НастройкиИнтеграцииМаркетплейс.ПустаяСсылка();
	КонецЕсли;
		
КонецФункции

#Область АвтозагрузкаОтчетовМаркетплейсов

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПредупрежденийПриЗагрузке(Форма)
	
	Настройки = Форма.Список.КомпоновщикНастроек.Настройки;
	
	ИмяПоля = "Ссылка";
	
	Если Форма.ВариантОтбораПредупрежденийПриЗагрузке = ВариантОтбораТребующиеПроверки() Тогда
		
		ОрганизацияПредупреждений = ?(Форма.ОтборОрганизацияИспользование, Форма.ОтборОрганизация, Неопределено);
		ДокументыСПредупреждениямиПриЗагрузке = ДокументыСПредупреждениямиПриЗагрузке(ОрганизацияПредупреждений);
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Настройки.Отбор,
			ИмяПоля);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Настройки.Отбор,
			ИмяПоля,
			ДокументыСПредупреждениямиПриЗагрузке,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Настройки.Отбор,
			ИмяПоля);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументыСПредупреждениямиПриЗагрузке(Организация)
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	
	ПараметрыОтбораПредупреждений = Предупреждения.НовыеПараметрыОтбораПредупреждений();
	ПараметрыОтбораПредупреждений.Организация = Организация;
	
	Возврат Предупреждения.ДокументыСПредупреждениями(ПараметрыОтбораПредупреждений);
	
КонецФункции

&НаСервереБезКонтекста
Функция ДатаПоследнейАвтозагрузки()
	
	ПараметрыАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	Возврат ПараметрыАвтозагрузки.ДатаПоследнейЗагрузки();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантОтбораТребующиеПроверки()
	Возврат "ТребующиеПроверки";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантОтбораВсе()
	Возврат "Все";
КонецФункции

#Область ОтложенноеСопоставлениеНоменклатуры

&НаКлиенте
Процедура СопоставитьНоменклатуруНаКлиенте(Маркетплейс = Неопределено)
	
	Если Маркетплейс = Неопределено Тогда
		Маркетплейс = МаркетплейсКСопоставлениюНоменклатуры();
		Если Маркетплейс = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеДляСопоставленияНоменклатуры = ДанныеДляСопоставленияНоменклатуры(Маркетплейс);
	Если ДанныеДляСопоставленияНоменклатуры <> Неопределено Тогда
		ДанныеДляСопоставленияНоменклатуры.НастройкиСопоставленияНоменклатуры.Вставить(
			"РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

		ДополнительныеПараметры = Новый Структура("Маркетплейс, НоменклатураОрганизаций",
			Маркетплейс, ДанныеДляСопоставленияНоменклатуры.НоменклатураОрганизаций);
		ОбработчикОповещения = Новый ОписаниеОповещения("СопоставлениеНоменклатурыЗавершение", ЭтотОбъект, ДополнительныеПараметры);

		СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры(
			ДанныеДляСопоставленияНоменклатуры.НоменклатураКонтрагента,
			ДанныеДляСопоставленияНоменклатуры.НастройкиСопоставленияНоменклатуры,
			ОбработчикОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеНоменклатурыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		
		НоменклатураКонтрагента = Новый Массив;
		Для Каждого СтрокаРезультата Из РезультатЗакрытия Цикл
			НоменклатураКонтрагента.Добавить(СтрокаРезультата.НоменклатураКонтрагента.Идентификатор);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(НоменклатураКонтрагента) Тогда
			ДлительнаяОперация = ОбменСМаркетплейсВызовСервера.ОбновитьНесопоставленнуюНоменклатуру(
				ДополнительныеПараметры.Маркетплейс, НоменклатураКонтрагента, УникальныйИдентификатор);
			Если ДлительнаяОперация = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьНесопоставленнуюНоменклатуруЗавершение", ЭтотОбъект);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		КонецЕсли;
		
		ОбменСМаркетплейсКлиент.ОбновитьНоменклатуруМаркетплейсов(
			РезультатЗакрытия, ДополнительныеПараметры.НоменклатураОрганизаций, ДополнительныеПараметры.Маркетплейс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНесопоставленнуюНоменклатуруЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Обновить();
	УправлениеВидимостью();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МаркетплейсКСопоставлениюНоменклатуры()
	
	МаркеплейсыКСопоставлению = ОбменСМаркетплейс.МаркеплейсыКСопоставлениюНоменклатуры();
	Если ЗначениеЗаполнено(МаркеплейсыКСопоставлению) Тогда
		Возврат МаркеплейсыКСопоставлению[0];
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеДляСопоставленияНоменклатуры(Маркетплейс)
	
	Возврат ОбменСМаркетплейс.ДанныеДляСопоставленияНоменклатуры(Маркетплейс);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

