#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	// Отбор по основной организации
	Если ИспользуетсяНесколькоОрганизаций Тогда
		ОтборОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		
		ОтборОрганизация = Параметр;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизацияИспользование = Истина;
	КонецЕсли;
	
	ОтборОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ГрупповаяОтправка(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ВыбраннаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ВыбраннаяСтрока <> Неопределено Тогда
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);
		ПараметрыОткрытияФормы.Вставить("Организация", ДанныеСтроки.Организация);
		СоответствиеШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов(ДанныеСтроки.Ссылка);
		Для Каждого КиЗ Из СоответствиеШагов Цикл
			Если КиЗ.Значение = ДанныеСтроки.ТекущийШаг Тогда
				ПараметрыОткрытияФормы.Вставить("ТекущийТитул", КиЗ.Ключ);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ОтборОрганизацияИспользование Тогда
		ПараметрыОткрытияФормы.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	
	СписокИнтервал = Элементы.Список.Период;
	Если ЗначениеЗаполнено(СписокИнтервал.ДатаНачала)
	Или ЗначениеЗаполнено(СписокИнтервал.ДатаОкончания) Тогда
		ПараметрыОткрытияФормы.Вставить("СписокИнтервал", СписокИнтервал);
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ГрупповаяОтправкаЭПД.Форма.ФормаГрупповойОтправки", 
		ПараметрыОткрытияФормы, 
		ЭтотОбъект, 
		УникальныйИдентификатор, 
		, 
		, 
		, 
		РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтборОрганизацияПриИзмененииНаСервере()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

#КонецОбласти
