#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// База распределения, используемая по умолчанию
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходов
//
Функция ЗначениеПоУмолчанию() Экспорт
	Возврат ПрямыеЗатраты;
КонецФункции

// Определяет представление базы распределения в дательном падеже
// (например "затраты распределяются пропорционально объему выпуска")
//
// Параметры:
//  Значение - ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходов
// 
// Возвращаемое значение:
//  Строка
//
Функция ПредставлениеДательныйПадеж(Значение) Экспорт
	
	Если Значение = ОбъемВыпуска Тогда
		Возврат НСтр("ru = 'объему выпуска'");
	ИначеЕсли Значение = ПлановаяСебестоимость Тогда
		Возврат НСтр("ru = 'плановой себестоимости выпуска'");
	ИначеЕсли Значение = ОплатаТруда Тогда
		Возврат НСтр("ru = 'суммам прямых затрат на оплату труда'");
	ИначеЕсли Значение = МатериальныеЗатраты Тогда
		Возврат НСтр("ru = 'суммам прямых материальных затрат'");
	ИначеЕсли Значение = ПрямыеЗатраты Тогда
		Возврат НСтр("ru = 'суммам всех прямых затрат'");
	ИначеЕсли Значение = ОтдельныеСтатьиПрямыхЗатрат Тогда
		Возврат НСтр("ru = 'суммам прямых затрат по отдельным статьям'");
	ИначеЕсли Значение = Выручка Тогда
		Возврат НСтр("ru = 'выручке'");
	ИначеЕсли Значение = ВыручкаОтРеализацииУслуг Тогда
		Возврат НСтр("ru = 'выручке от реализации услуг'");
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Определяет представление базы распределения в родительном падеже
// (например "затраты переносятся на счет при наличии объема выпуска")
//
// Параметры:
//  Значение - ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходов
// 
// Возвращаемое значение:
//  Строка
//
Функция ПредставлениеРодительныйПадеж(Значение) Экспорт
	
	Если Значение = ОбъемВыпуска Тогда
		Возврат НСтр("ru = 'объема выпуска'");
	ИначеЕсли Значение = ПлановаяСебестоимость Тогда
		Возврат НСтр("ru = 'плановой себестоимости выпуска'");
	ИначеЕсли Значение = ОплатаТруда Тогда
		Возврат НСтр("ru = 'прямых затрат на оплату труда'");
	ИначеЕсли Значение = МатериальныеЗатраты Тогда
		Возврат НСтр("ru = 'прямых материальных затрат'");
	ИначеЕсли Значение = ПрямыеЗатраты Тогда
		Возврат НСтр("ru = 'прямых затрат'");
	ИначеЕсли Значение = ОтдельныеСтатьиПрямыхЗатрат Тогда
		Возврат НСтр("ru = 'прямых затрат по отдельным статьям'");
	ИначеЕсли Значение = Выручка Тогда
		Возврат НСтр("ru = 'выручки'");
	ИначеЕсли Значение = ВыручкаОтРеализацииУслуг Тогда
		Возврат НСтр("ru = 'выручки от реализации услуг'");
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Содержит пояснение для пользователя о правилах расчета базы распределения.
//
// Параметры:
//  Значение - ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходов - поясняемая база распределения
// 
// Возвращаемое значение:
//  Строка
//
Функция Пояснение(Значение) Экспорт
	
	ВидРасходов = ВидРасходовОтборЗатрат(Значение);
	
	Если ВидРасходов <> Неопределено Тогда
		Возврат СтрШаблон(НСтр("ru = 'Суммы затрат по статьям с видом %1, учтенные на калькуляционных счетах'"), ВидРасходов);
	ИначеЕсли Значение = ОбъемВыпуска Тогда
		Возврат НСтр("ru = 'Количество выпущенной продукции (услуг)'");
	ИначеЕсли Значение = ПлановаяСебестоимость Тогда
		Возврат НСтр("ru = 'Плановая себестоимость выпущенной продукции (оказанных услуг)'");
	ИначеЕсли Значение = Выручка Тогда
		Возврат НСтр("ru = 'Выручка от реализации выпущенной продукции (услуг).
                      |В базу распределения включается только та часть выручки, которая может быть сопоставлена с деятельностью, затраты на которую учитываются на калькуляционных счетах'");
	ИначеЕсли Значение = ВыручкаОтРеализацииУслуг Тогда
		Возврат НСтр("ru = 'Выручка от реализации любых услуг'");
	ИначеЕсли Значение = ПрямыеЗатраты Тогда
		Возврат СтрШаблон(НСтр("ru = 'Суммы затрат, учтенные на калькуляционных счетах'"));
	ИначеЕсли Значение = ОтдельныеСтатьиПрямыхЗатрат Тогда
		Возврат СтрШаблон(НСтр("ru = 'Суммы затрат по статьям из перечня, учтенные на калькуляционных счетах'"));
	ИначеЕсли Значение = СобственнаяБазаРаспределения Тогда
		Возврат НСтр("ru = 'Произвольные показатели, описывающие деятельность предприятия'");
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Определяет перечень способов распределения, которые ограничены в использовании:
// их можно использовать исключительно для распределения со счета косвенных затрат на калькуляционные счета.
// 
// Возвращаемое значение:
//  Массив Из ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходов
//
Функция ТолькоРаспределениеСчетаКосвенныеРасходы() Экспорт
	
	ОграниченноеИспользование = Новый Массив;
	ОграниченноеИспользование.Добавить(Выручка);
	ОграниченноеИспользование.Добавить(СобственнаяБазаРаспределения);
	Возврат ОграниченноеИспользование;
	
КонецФункции

// Определяет перечень способов распределения, которые нельзя использовать для распределения
// затрат для счетов затрат по ремонту основных средств.
// 
// Возвращаемое значение:
//  Массив Из ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходов
//
Функция ОграничениеРаспределенияТолькоПоПрямымЗатратам() Экспорт
	
	ОграниченноеИспользование = Новый Массив;
	ОграниченноеИспользование.Добавить(Выручка);
	ОграниченноеИспользование.Добавить(ВыручкаОтРеализацииУслуг);
	ОграниченноеИспользование.Добавить(ОбъемВыпуска);
	ОграниченноеИспользование.Добавить(ПлановаяСебестоимость);
	// Настройка собственной базы могла бы иметь смысл при отнесении затрат отдельных видов ремонтов на конкретные основные средства.
	// Но поскольку собственная база в текущий момент имеет привязку к учетной политике, а состав основных средств, находящихся
	// в ремонте, может меняться довольно часто, то на использование Собственной базы накладываем ограничение
	// при распределение затрат на ремонты основных средств, блокируем ее использование.
	ОграниченноеИспользование.Добавить(СобственнаяБазаРаспределения);
	
	Возврат ОграниченноеИспользование;
	
КонецФункции

// Определяет отбор статей затрат для баз распределения, основанных на величине затрат определенного вида.
//
// Параметры:
//  Значение - ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходов - проверяемая база распределения
// 
// Возвращаемое значение:
//   - ПеречислениеСсылка.ВидыРасходовНУ - вид расходов, статьи с которым образуют базу распределения
//   - Неопределено - состав базы нельзя определить видом расходов
//
Функция ВидРасходовОтборЗатрат(Значение) Экспорт
	
	Если Значение = МатериальныеЗатраты Тогда
		Возврат Перечисления.ВидыРасходовНУ.МатериальныеРасходы;
	ИначеЕсли Значение = ОплатаТруда Тогда
		Возврат Перечисления.ВидыРасходовНУ.ОплатаТруда;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
