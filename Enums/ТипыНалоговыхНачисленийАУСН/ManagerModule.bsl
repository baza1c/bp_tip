#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает идентификатор типа налогового начисления сервиса Автоматизированная УСН
//
// Параметры:
//  ТипНачисления - ПеречислениеСссылка.ТипыНалоговыхНачисленийАУСН
//
// Возвращаемое значение:
//  Строка - строковый идентификатор типа, указанного в параметре
//
Функция Идентификатор(ТипНачисления) Экспорт
	
	Если Не ЗначениеЗаполнено(ТипНачисления) Тогда
		Возврат "";
	КонецЕсли;
	
	ТипыДокументов = ВсеТипыНалоговыхНачисленийАУСН();
	Возврат ТипыДокументов[ТипНачисления];
	
КонецФункции

// Возвращает значение перечисления, соответствующее переданному идентификатору,
// возвращает пустую ссылку, если не найдено значение перечисления по идентификатору
//
// Параметры:
//  Идентификатор - Строка - возможные идентификаторы см. ВсеТипыНалоговыхНачисленийАУСН()
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыНалоговыхНачисленийАУСН
//
Функция ЗначениеПоИдентификатору(Идентификатор) Экспорт
	
	ИскомоеЗначение = СтрЗаменить(Идентификатор, "_", "");
	Значение = ПустаяСсылка();
	
	ТипыДокументов = ВсеТипыНалоговыхНачисленийАУСН();
	Для Каждого ТипДокумента Из ТипыДокументов Цикл
		Если ТипДокумента.Значение = ИскомоеЗначение Тогда
			Значение = ТипДокумента.Ключ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Значение;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВсеТипыНалоговыхНачисленийАУСН()
	
	Идентификаторы = Новый Соответствие;
	
	Идентификаторы.Вставить(Налог, "ACCRUED");
	Идентификаторы.Вставить(КорректировкаНаУвеличение, "ACCRUEDCORRECTION");
	Идентификаторы.Вставить(КорректировкаНаУменьшение, "REDUCEDCORRECTION");
	
	Возврат Идентификаторы; 
	
КонецФункции

#КонецОбласти

#КонецЕсли