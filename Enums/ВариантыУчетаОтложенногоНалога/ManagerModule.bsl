// До версии 3.0.75 в перечислении был элемент БалансовыйМетод.
// В версии 3.0.75 он переименован в БалансовыйМетодПостоянныеРазницы.
// Таким образом, внутренний идентификатор элемента БалансовыйМетод версии 3.0.74
// совпадает с внутренним идентификатором элемента БалансовыйМетодПостоянныеРазницы в версии 3.0.75.
// Это нужно, чтобы упростить (исключить) обработку обновления:
// то, что пользователи называли "балансовым методом" до версии 3.0.75,
// теперь называется "балансовым методом с постоянными и временными разницами".

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет, что переданный вариант учета соответствует одной из разновидностей балансового метода.
//
// Параметры:
//  ВариантУчета - ПеречислениеСсылка.ВариантыУчетаОтложенногоНалога
// 
// Возвращаемое значение:
//  Булево - Истина, если соответствует.
//
Функция ЭтоБалансовыйМетод(ВариантУчета) Экспорт
	
	Возврат ВариантУчета = БалансовыйМетод Или ВариантУчета = БалансовыйМетодПостоянныеРазницы;
	
КонецФункции

// Проверяет, что переданный вариант учета соответствует затратному методу учета.
//
// Параметры:
//  ВариантУчета - ПеречислениеСсылка.ВариантыУчетаОтложенногоНалога
// 
// Возвращаемое значение:
//  Булево - Истина, если соответствует.
//
Функция ЭтоЗатратныйМетод(ВариантУчета) Экспорт
	
	Возврат ВариантУчета = ЗатратныйМетод;
	
КонецФункции

#Область ОбработчикиОбновления

// Собирает при обновлении программы статистику о ходе постепенного отказа от применения устаревшего (затратного) варианта учета.
//
// Параметры:
//  ПараметрыОтложенногоОбработчика - Структура - управляющая коллекция, см. документацию к подсистеме ОбновлениеИнформационнойБазы БСП.
//
Процедура ЗаписатьБизнесСтатистику(ПараметрыОтложенногоОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВариантыУчета.Ссылка КАК Значение
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика.СрезПоследних КАК УчетнаяПолитика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ВариантыУчетаОтложенногоНалога КАК ВариантыУчета
	|		ПО УчетнаяПолитика.ВариантУчетаОтложенногоНалога = ВариантыУчета.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИмяОперации = СтрШаблон(
			"СтатистикаБП.НастройкиУчета.ВариантыУчетаОтложенногоНалога.%1",
			ОбщегоНазначения.ИмяЗначенияПеречисления(Выборка.Значение));
	
		ОбщегоНазначенияБП.ЗаписатьОперациюБизнесСтатистики(ИмяОперации);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
