#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ОсновнаяСистемаНалогообложения(ДляФизЛица = Ложь) Экспорт
	
	РазрешенныеСистемыНалогообложения = РазрешенныеСистемыНалогообложения(ДляФизЛица);
	Если РазрешенныеСистемыНалогообложения.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОсновнаяСистемаНалогообложения = Константы.ОсновнаяСистемаНалогообложения.Получить();
	
	Если Не ЗначениеЗаполнено(ОсновнаяСистемаНалогообложения)
		Или РазрешенныеСистемыНалогообложения.Найти(ОсновнаяСистемаНалогообложения) = Неопределено Тогда
		
		ОсновнаяСистемаНалогообложения = РазрешенныеСистемыНалогообложения[0];
	КонецЕсли;
	
	Возврат ОсновнаяСистемаНалогообложения;
	
КонецФункции

Функция ДатаНачалаПрименения(СистемаНалогообложения, ПрименяетсяУСНПатент = Ложь) Экспорт
	
	Если СистемаНалогообложения = НалогНаПрофессиональныйДоход Тогда
		Возврат НалогНаПрофессиональныйДоходКлиентСервер.ДатаНачалаЭксперимента();
	ИначеЕсли ЭтоПатентнаяСистемаНалогообложения(СистемаНалогообложения, ПрименяетсяУСНПатент) Тогда
		Возврат УчетПСН.ДатаНачалаДействияПатентнойСистемы();
	ИначеЕсли СистемаНалогообложения = АУСН Тогда 
		Возврат НачалоГода(УчетУСНПовтИсп.ДатаНачалаПримененияАУСН());
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает доступные системы налогообложения, для переданного владельца,
// используя стандартное событие ОбработкаПолученияДанныхВыбора
//
// Параметры:
//  Владелец - СправочникСсылка.Организации, СправочникСсылка.Контрагенты
//
// Возвращаемое значение:
//  СписокЗначений - см. описание метода ПеречислениеМенеджер.ПолучитьДанныеВыбора
//
Функция ДанныеВыбора(Владелец) Экспорт
	
	Фильтр = Новый Структура;
	Фильтр.Вставить("Отбор", Новый Структура);
	Фильтр.Вставить("СтрокаПоиска", Неопределено);
	Фильтр.Вставить("Владелец", Владелец);
	Возврат ПолучитьДанныеВыбора(Фильтр);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Владелец = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Владелец", Неопределено);
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.Добавить(ПустаяСсылка(), НСтр("ru = 'Не выбрано'"));
		Если Не ЗначениеЗаполнено(Владелец) Тогда
			Возврат;
		КонецЕсли;
		ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ЮридическоеФизическоеЛицо");
		ЭтоФизическоеЛицо =
			ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		Если ЭтоФизическоеЛицо Тогда
			ДанныеВыбора.Добавить(НалогНаПрофессиональныйДоход);
			ДанныеВыбора.Добавить(ОсобыйПорядок, НСтр("ru = 'Патент'"));
		КонецЕсли;
		ДанныеВыбора.Добавить(АУСН);
		ДанныеВыбора.Добавить(Упрощенная);
		ДанныеВыбора.Добавить(Общая);
		ДанныеВыбора.Добавить(ЕСХН);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоПатентнаяСистемаНалогообложения(СистемаНалогообложения, ПрименяетсяУСНПатент)
	
	Возврат СистемаНалогообложения = ОсобыйПорядок И ПрименяетсяУСНПатент;
	
КонецФункции

Функция РазрешенныеСистемыНалогообложения(ДляФизЛица)
	
	// Возвращаем в том порядке, в котором располагаются настройки системы налогообложения
	СистемыНалогообложения = Новый Массив;
	Если ДляФизЛица Тогда
		Если ТарификацияБП.РазрешенНалогНаПрофессиональныйДоход() Тогда
			СистемыНалогообложения.Добавить(НалогНаПрофессиональныйДоход);
		КонецЕсли;
		Если ТарификацияБП.РазрешенаПатентнаяСистемаНалогообложения() Тогда
			СистемыНалогообложения.Добавить(ОсобыйПорядок);
		КонецЕсли;
	КонецЕсли;
	Если ТарификацияБП.РазрешенаАУСН() Тогда
		СистемыНалогообложения.Добавить(АУСН);
	КонецЕсли;
	Если ТарификацияБП.РазрешенаУСН() Тогда
		СистемыНалогообложения.Добавить(Упрощенная);
	КонецЕсли;
	Если ТарификацияБП.РазрешенаОбщаяСистемаНалогообложения() Тогда
		СистемыНалогообложения.Добавить(Общая);
	КонецЕсли;
	
	Возврат СистемыНалогообложения;
	
КонецФункции

#КонецОбласти

#КонецЕсли