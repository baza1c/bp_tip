#Область ПрограммныйИнтерфейс

// Возвращает список значений, доступных по функциональным опциям.
//
// Параметры:
//   Отбор - Массив значений ПеречислениеСсылка.ВидыОперацийРеализацияТоваров
//         или самое значение - дополнительный отбор
//
// Возвращаемое значение:
//   СписокЗначений - доступные значения
Функция СписокДоступныхЗначений(Отбор = Неопределено) Экспорт
	
	ДоступныеЗначения = ПолучитьДоступныеЗначения(Отбор, Неопределено);
	
	СписокДоступныхЗначений = Новый СписокЗначений;
	
	ОтсортированныйСписок = ОтсортированныйСписокВидовОпераций();
	
	Для Каждого ВидОперации Из ОтсортированныйСписок Цикл
		Если ДоступныеЗначения.НайтиПоЗначению(ВидОперации.Значение) <> Неопределено Тогда
			ДоступноеЗначение = СписокДоступныхЗначений.Добавить();
			ЗаполнитьЗначенияСвойств(ДоступноеЗначение, ВидОперации);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокДоступныхЗначений;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ПолучитьДоступныеЗначения(Параметры.Отбор, Параметры.СтрокаПоиска);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДоступныеЗначения(Отбор, СтрокаПоиска)
	
	Исключения = Новый Массив;
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяОтгрузкаБезПереходаПраваСобственности") Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности);
	КонецЕсли;
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств") Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.Оборудование);
	КонецЕсли;
	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПродажиЧерезМаркетплейс"))
		И (НЕ ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров")) Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров);
		Исключения.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером);
		Исключения.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера);
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСКонтрагентами") Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров);
	КонецЕсли;
	
 	ДоступныеЗначения = Новый СписокЗначений;
	
	Для каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыОперацийРеализацияТоваров.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(СтрокаПоиска)) <> 1 Тогда
			Продолжить;
		КонецЕсли;
		Ссылка = Перечисления.ВидыОперацийРеализацияТоваров[ЗначениеПеречисления.Имя];
		Если ТипЗнч(Отбор) = Тип("ПеречислениеСсылка.ВидыОперацийРеализацияТоваров")
			И Отбор <> Ссылка Тогда
			Продолжить;
		ИначеЕсли (ТипЗнч(Отбор) = Тип("ФиксированныйМассив") Или ТипЗнч(Отбор) = Тип("Массив"))
			И Отбор.Найти(Ссылка) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Исключения.Найти(Ссылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ДоступныеЗначения.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;
	
	Возврат ДоступныеЗначения;
	
КонецФункции

Функция ОтсортированныйСписокВидовОпераций()
	
	СписокВидовОпераций = Новый СписокЗначений;
	
	ВсеВидыОпераций = Перечисления.ВидыОперацийРеализацияТоваров;
	
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.Товары, НСтр("ru = 'Товары (накладная, УПД)'"));
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.Услуги, НСтр("ru = 'Услуги (акт, УПД)'"));
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.ПродажаКомиссия, Строка(ВсеВидыОпераций.ПродажаКомиссия));
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.ПередачаТоваров, Строка(ВсеВидыОпераций.ПередачаТоваров));
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.ВыкупТоваровКомиссионером, Строка(ВсеВидыОпераций.ВыкупТоваровКомиссионером));
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.ПродажаТоваровСоСкладаКомиссионера, Строка(ВсеВидыОпераций.ПродажаТоваровСоСкладаКомиссионера));
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.ОтгрузкаБезПереходаПраваСобственности, Строка(ВсеВидыОпераций.ОтгрузкаБезПереходаПраваСобственности));
	СписокВидовОпераций.Добавить(ВсеВидыОпераций.Оборудование, Строка(ВсеВидыОпераций.Оборудование));
	
	Возврат СписокВидовОпераций;
	
КонецФункции

#КонецОбласти
