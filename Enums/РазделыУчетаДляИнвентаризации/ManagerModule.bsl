
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ДанныеВыбораПоФункциональнымОпциям();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Убирает из полного списка разделы, по которым фунцкионал ведения учета не включен.
//  Параметры - Структура:
//    ТолькоДляДокументаИнвентаризацияОбъектовУчета - Булево
//
// Возвращаемое значение:
//  СписокЗначений - список, куда добавлены все возможные (из используемых) разделы учета.
//
Функция ДанныеВыбораПоФункциональнымОпциям(ТолькоДляДокументаИнвентаризацияОбъектовУчета = Ложь) Экспорт
	
	СписокРазделов = Новый СписокЗначений;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РазделыУчетаДляИнвентаризации.Ссылка КАК СсылкаЗначение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РазделыУчетаДляИнвентаризации.Ссылка) КАК СсылкаПредставление
	|ИЗ
	|	Перечисление.РазделыУчетаДляИнвентаризации КАК РазделыУчетаДляИнвентаризации
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазделыУчетаДляИнвентаризации.Порядок");
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВедетсяУчетОсновныхСредств = УчетОСПереопределяемый.ВедетсяУчетОсновныхСредств();
	ВедетсяУчетНМА = ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА");
	
	РазделыИсключения = Новый Массив;
	РазделыИсключения.Добавить(ОсновныеСредства);
	РазделыИсключения.Добавить(НематериальныеАктивы);
	РазделыИсключения.Добавить(Запасы);
	РазделыИсключения.Добавить(Касса);
	РазделыИсключения.Добавить(РасчетыСКонтрагентами);
	Пока Выборка.Следующий() Цикл
		
		Раздел = Выборка.СсылкаЗначение;
		Если Раздел = ОсновныеСредства И Не ВедетсяУчетОсновныхСредств
		Или Раздел = КапитальныеВложения И Не ВедетсяУчетОсновныхСредств И Не ВедетсяУчетНМА
		Или Раздел = НематериальныеАктивы И Не ВедетсяУчетНМА Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТолькоДляДокументаИнвентаризацияОбъектовУчета Тогда
			Если РазделыИсключения.Найти(Раздел) = Неопределено Тогда
				СписокРазделов.Добавить(Выборка.СсылкаЗначение, Выборка.СсылкаПредставление);
			КонецЕсли;
		Иначе
			СписокРазделов.Добавить(Выборка.СсылкаЗначение, Выборка.СсылкаПредставление);
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат СписокРазделов;
	
КонецФункции

#КонецОбласти
	
#КонецЕсли