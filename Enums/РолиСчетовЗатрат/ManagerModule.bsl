#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет роль счета по умолчанию.
//
// Параметры:
//  Счет - ПланСчетовСсылка.Хозрасчетный
// 
// Возвращаемое значение:
//  Перечисление.РолиСчетовЗатрат
//
Функция ОсновнаяРольСчета(Счет) Экспорт
	
	ОсновныеРоли = ОсновныеРолиСчетов();
	
	Возврат ЗначениеРоли(Счет, ОсновныеРоли);

КонецФункции

// Определяет счет финансого результата в зависимости от роли счета.
//
// Параметры:
//  РольСчета - ПеречислениеСсылка.РолиСчетовЗатрат
// 
// Возвращаемое значение:
//  ПланСчетовСсылка.Хозрасчетный
//
Функция СчетФинансовогоРезультата(РольСчета) Экспорт
	
	СчетФинансовогоРезультата = ПланыСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД;
	Если РольСчета = Перечисления.РолиСчетовЗатрат.УправленческиеРасходы Тогда
		СчетФинансовогоРезультата = ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходыНеЕНВД;
	ИначеЕсли РольСчета = Перечисления.РолиСчетовЗатрат.ПрочиеРасходы Тогда
		СчетФинансовогоРезультата = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	ИначеЕсли РольСчета = Перечисления.РолиСчетовЗатрат.РасходыНаПродажу Тогда
		СчетФинансовогоРезультата = ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажуНеЕНВД;
	КонецЕсли;
	
	Возврат СчетФинансовогоРезультата;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗначениеРоли(Счет, ОсновныеРоли)
	
	Роль = ОсновныеРоли[Счет];
	Если ЗначениеЗаполнено(Роль) Тогда
		Возврат Роль;
	КонецЕсли;
	
	ГруппаСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Счет, "Родитель");
	Если ЗначениеЗаполнено(ГруппаСчета) Тогда
		Возврат ЗначениеРоли(ГруппаСчета, ОсновныеРоли);
	КонецЕсли;
	
	Возврат ОсновныеРоли[ПланыСчетов.Хозрасчетный.ПустаяСсылка()];
	
КонецФункции

Функция ОсновныеРолиСчетов()
	
	РолиСчетов = Новый Соответствие;
	
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_,               КалькуляцияСебестоимости);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.СобственныеИсследованияИРазработки,  КалькуляцияСтоимостиНИОКР);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства_,        КалькуляцияСебестоимости);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ВыполнениеРемонтовОС,                КалькуляцияСтоимостиРемонтовОС);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы,         КосвенныеЗатраты);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы,            УправленческиеРасходы);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу,                    РасходыНаПродажу);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.БракВПроизводстве,                   СебестоимостьПродаж);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства,           ПрочиеРасходы);
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ВыпускПродукции,                     ВыпускПродукции);
	
	РолиСчетов.Вставить(ПланыСчетов.Хозрасчетный.ПустаяСсылка(),                      СебестоимостьПродаж); // Остальные счета
	
	Возврат РолиСчетов;
	
КонецФункции

#КонецОбласти

#КонецЕсли
