#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет группу обработки "Закрытие месяца", к которой относится регламентная операция.
// Группа определяет последовательность операций (вводит дополнительное ограничение на нее):
// как правило, нельзя выполнить операцию группы, если не выполнены все потребные операции предыдущих групп.
//
// Параметры:
//  ВидОперации - ПеречислениеСсылка.ВидыРегламентныхОпераций
// 
// Возвращаемое значение:
//  Число - номер группы, от 1 до 6.
//
Функция НомерГруппы(ВидОперации) Экспорт
	
	// Также номер группы следует указать в комментарии элемента перечисления,
	// чтобы легко можно было проверить корректность последовательности элементов перечисления.
	
	Если ВидОперации = РасчетДолейСписанияКосвенныхРасходов Тогда
		НомерГруппы = 2;
	ИначеЕсли ВидОперации = ЗачетАвансаПоЕдиномуНалоговомуСчету Тогда
		НомерГруппы = 2;
	ИначеЕсли ВидОперации = ЗакрытиеСчетов20_23_25_26 Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = ЗакрытиеСчета44 Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = РасчетРезервовПоСомнительнымДолгам Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = НачислениеСтраховыхВзносовИП Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = ВключениеАвансовВПрочиеДоходыДляИП Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = РаспределениеРасходовПоВидамДеятельностиДляУСН Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = РаспределениеРасходовПоВидамДеятельностиДляИП Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = РасчетРасходовУменьшающихОтдельныеНалоги Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = ОбесценениеЗапасов Тогда
		НомерГруппы = 3;
	ИначеЕсли ВидОперации = ЗакрытиеСчетов90_91 Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = ПереносУбытковПоНалогуНаПрибыль Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = РасчетНалогаНаПрибыль Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = РасчетОтложенногоНалога Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = РасчетНалогаУСН Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = НачислениеНалогаАУСН Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = РасчетЕНВД Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = РеформацияБаланса Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = РасчетНДФЛДляИП Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = НачислениеНалогаНаПрофессиональныйДоход Тогда
		НомерГруппы = 4;
	ИначеЕсли ВидОперации = ПереходНаФСБУ25 Тогда
		НомерГруппы = 5;
	ИначеЕсли ВидОперации = ПереходНаФСБУ6 Тогда
		НомерГруппы = 5;
	ИначеЕсли ВидОперации = ПереводОСВМалоценноеОборудование Тогда
		НомерГруппы = 5;
	ИначеЕсли ВидОперации = ПереходНаФСБУ14 Тогда
		НомерГруппы = 5;
	ИначеЕсли ВидОперации = КорректировкаПартийПриПереходеСФИФОНаУчетПоСредней Тогда
		НомерГруппы = 5;
	ИначеЕсли ВидОперации = НачислениеНалоговНаЕНС Тогда
		НомерГруппы = 6;
	ИначеЕсли ВидОперации = КорректировкаОтложенногоНалога Тогда
		НомерГруппы = 6;
	ИначеЕсли ВидОперации = ЗакрытиеУчетныхРегистровУСН Тогда
		НомерГруппы = 6;
	Иначе
		НомерГруппы = 1;
	КонецЕсли;
	
	Возврат НомерГруппы;
	
КонецФункции

// Описывает перечень регламентных операций, для выполнения которых не используется документ РегламентнаяОперация
// - то есть, они выполняются иными, специальными документами.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыРегламентныхОпераций
//
Функция ВыполняютсяСпециальнымиДокументами() Экспорт
	
	Операции = Новый Массив;
	Операции.Добавить(НачислениеЗарплаты);
	Операции.Добавить(НачислениеРезервовПоОплатеТруда);
	Операции.Добавить(НачислениеНДС);
	Возврат Операции;
	
КонецФункции

// Описывает перечень регламентных операций, недоступных для использования в документе РегламентнаяОперация.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыРегламентныхОпераций
//
Функция НедоступныеРегламентныеОперации() Экспорт
	
	Операции = ВыполняютсяСпециальнымиДокументами();
	
	Если Не УчетнаяПолитика.ПоддерживаетсяДисконтированиеОбязательств() Тогда
		Операции.Добавить(НачислениеПроцентныхРасходов);
	КонецЕсли;
	Если Не ПолучитьФункциональнуюОпцию("РасширенныйФункционал") Тогда
		Операции.Добавить(ОбесценениеЗапасов);
	КонецЕсли;
	
	Возврат Операции;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	// Не все регламентные операции допустимо выбирать в формах
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Исключения", НедоступныеРегламентныеОперации());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыРегламентныхОпераций.Ссылка КАК Значение,
	|	ПРЕДСТАВЛЕНИЕ(ВидыРегламентныхОпераций.Ссылка) КАК Представление,
	|	ВидыРегламентныхОпераций.Порядок КАК Порядок
	|ИЗ
	|	Перечисление.ВидыРегламентныхОпераций КАК ВидыРегламентныхОпераций
	|ГДЕ
	|	НЕ ВидыРегламентныхОпераций.Ссылка В (&Исключения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Значение, Выборка.Представление);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
