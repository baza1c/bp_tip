#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область НалогНаПрибыль

// Определяет статьи, квалифицируемые для целей налогового учета как "Внереализационные расходы" (п. 1 ст. 265 НК РФ).
// Не включает статьи, квалифицируемые как "Убытки, приравненные к внереализационным расходам" (п. 2 ст. 265 НК РФ)
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыПрочихДоходовИРасходов
//
Функция ВнереализационныеРасходы() Экспорт
	
	Расходы = Новый Массив;
	
	// Явно упомянутые в п. 1 ст. 265
	Расходы.Добавить(СдачаИмуществаВАрендуИлиСубаренду); // подп. 1
	Расходы.Добавить(ПроцентыКПолучениюУплате); // подп. 2
	Расходы.Добавить(ПроцентыКУплате); // подп. 2
	Расходы.Добавить(ПроцентыНачисленныеПоСт269); // подп. 2
	Расходы.Добавить(КурсовыеРазницы); // подп. 5 и 6
	Расходы.Добавить(КурсовыеРазницыПоРасчетамВУЕ); // подп. 5 и 6
	Расходы.Добавить(ОтчисленияВОценочныеРезервы); // подп. 7
	Расходы.Добавить(ЛиквидацияОсновныхСредств); // подп. 8
	Расходы.Добавить(ШтрафыПениНеустойкиКПолучениюУплате); // подп. 13
	Расходы.Добавить(ВозмещениеУбытковКПолучениюУплате); // подп. 13
	Расходы.Добавить(НДСПоСписаннойКредиторскойЗадолженности); // подп. 14
	Расходы.Добавить(РасходыНаУслугиБанков); // подп. 15
	Расходы.Добавить(ОперацииСФинансовымиИнструментамиСрочныхСделок); // подп. 18
	Расходы.Добавить(ОперацииСФинансовымиИнструментамиСрочныхСделокОР); // подп. 18
	Расходы.Добавить(ПремияПокупателю); // подп. 19.1
	Расходы.Добавить(ПрочиеВнереализационныеДоходыРасходы); // подп. 20 и иные, не перечисленные выше
	
	// Используемые для целей, не связанных с налогом на прибыль
	Расходы.Добавить(РасходыОтСниженияСтоимостиАктивов);
	Расходы.Добавить(РасходыПоПередачеТоваровБезвозмездноИДляСобственныхНужд);
	Расходы.Добавить(ДолевоеУчастиеВИностранныхОрганизациях);
	Расходы.Добавить(ДолевоеУчастиеВРоссийскихОрганизациях);
	Расходы.Добавить(УчастиеВДругихОрганизациях);
	Расходы.Добавить(РазницаМеждуПервоначальнойИНоминальнойСтоимостьюПоДолговымЦеннымБумагам);
	
	// Используемые для учета доходов, однако, технически по ним могут быть отражены и расходы.
	// В последнем случае, нет оснований квалифицировать их иначе, чем по подп. 20 п. 1 ст. 265
	Расходы.Добавить(БезвозмездноеПолучениеИмуществаРаботУслугИмущественныхПрав);
	Расходы.Добавить(ПроцентыПоГосударственнымЦеннымБумагам);
	Расходы.Добавить(ПроцентыПоГосударственнымЦеннымБумагамПоСтавке0);
	Расходы.Добавить(ВосстановлениеАмортизационнойПремии);
	Расходы.Добавить(КорректировкаПрибылиДоРыночныхЦен);
	
	Возврат Расходы;
	
КонецФункции

// Определяет статьи, квалифицируемые как "Убытки, приравненные к внереализационным расходам" (п. 2 ст. 265 НК РФ)
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыПрочихДоходовИРасходов
//
Функция ВнереализационныеУбытки() Экспорт
	
	Убытки = Новый Массив;
	
	Убытки.Добавить(ПрибыльУбытокПрошлыхЛет); // подп. 1
	Убытки.Добавить(СписаниеДебиторскойКредиторскойЗадолженности); // подп. 2
	Убытки.Добавить(ПрочиеУбыткиПриравненныеКВнереализационнымРасходам); // подп. 3-7
	
	Возврат Убытки;
	
КонецФункции

// Определяет, используется ли статья для учета операций, связанных с реализацией объектов,
// квалифицируемых в налоговом учете как "амортизируемое имущество".
// По такой статье отражаются обороты и по кредиту (выручка от реализации) и по дебету (стоимость выбывающего объекта).
//
// Параметры:
//  ВидСтатьи - ПеречислениеСсылка.ВидыПрочихДоходовИРасходов - проверяемый вид статьи
// 
// Возвращаемое значение:
//  Булево
//
Функция ИспользуетсяДляРеализацииАмортизируемогоИмущества(ВидСтатьи) Экспорт
	
	Возврат ВидСтатьи = РеализацияОсновныхСредств
		Или ВидСтатьи = РеализацияНематериальныхАктивов;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать ИспользуетсяДляРеализацииАмортизируемогоИмущества
Функция ИспользуетсяДляДоходов(ВидСтатьи) Экспорт
	
	Возврат ИспользуетсяДляРеализацииАмортизируемогоИмущества(ВидСтатьи);
	
КонецФункции

#КонецОбласти

#КонецОбласти

// Возвращает перечисления, которые устарели и являются архивными.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыПрочихДоходовИРасходов - устаревшие виды прочих доходов и расходов.
// 
Функция УстаревшиеВидыПрочихДоходовИРасходов() Экспорт 
	
	Результат = Новый Массив;
	Результат.Добавить(ПроцентыКПолучениюУплате);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
