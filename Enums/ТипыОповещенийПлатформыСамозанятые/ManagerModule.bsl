#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значение перечисления, соответствующее идентификатору платформы Самозанятые
//
// Параметры:
//  Идентификатор - Строка - возможные идентификаторы см. ВсеИдентификаторы()
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыОповещенийПлатформыСамозанятые
//
Функция ЗначениеПоИдентификатору(Идентификатор) Экспорт
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Значение = ВсеИдентификаторы().Получить(НРег(СокрЛП(Идентификатор)));
	Если Значение <> Неопределено Тогда
		Возврат Значение;
	Иначе
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'Получен неизвестный тип оповещения: %1'"),
			Идентификатор);
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация, , ,
			КомментарийЖурналаРегистрации);
		Возврат ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВсеИдентификаторы()
	
	Идентификаторы = Новый Соответствие;
	Идентификаторы.Вставить("tax_payment", ОплатаНалога);
	Идентификаторы.Вставить("bind", ПривязкаКПартнеру);
	Идентификаторы.Вставить("permissions", ИзменениеРазрешений);
	Идентификаторы.Вставить("unbind", ОтвязкаОтПартнера);
	Идентификаторы.Вставить("info", Информация);
	
	Возврат Новый ФиксированноеСоответствие(Идентификаторы);
	
КонецФункции

#КонецОбласти

#КонецЕсли