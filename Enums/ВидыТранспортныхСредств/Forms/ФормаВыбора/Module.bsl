
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидыИКатегорииТС = РасчетИмущественныхНалогов.ВидыИКатегорииТранспортныхСредств();
	
	ГруппыТС = ВидыИКатегорииТС.Скопировать(, "Группа");
	ГруппыТС.Свернуть("Группа");
	
	Отбор = Новый Структура("Группа");
	
	Для Каждого СтрокаГруппы Из ГруппыТС Цикл
		НоваяГруппа = Дерево.ПолучитьЭлементы().Добавить();
		НоваяГруппа.Представление = ВРег(СтрокаГруппы.Группа);
		Отбор.Группа = СтрокаГруппы.Группа;
		Для Каждого СтрокаВидаТС Из ВидыИКатегорииТС.НайтиСтроки(Отбор) Цикл
			НоваяСтрока = НоваяГруппа.ПолучитьЭлементы().Добавить();
			НоваяСтрока.ВидТранспортногоСредства = Перечисления.ВидыТранспортныхСредств[СтрокаВидаТС.Вид];
			НоваяСтрока.Представление = НоваяСтрока.ВидТранспортногоСредства;
			НоваяСтрока.Описание = СтрокаВидаТС.Описание;
			Если ЗначениеЗаполнено(НоваяСтрока.Описание) Тогда
				НоваяСтрока.Представление = СтрШаблон("%1*", НоваяСтрока.Представление);
			КонецЕсли;
			Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока)
				 И НоваяСтрока.ВидТранспортногоСредства = Параметры.ТекущаяСтрока Тогда
				Элементы.Дерево.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриАктивизацииСтроки(Элемент)
	
	СтрокаДерева = Элементы.Дерево.ТекущиеДанные;
	
	Если СтрокаДерева = Неопределено Тогда
		Описание = "";
		Возврат;
	КонецЕсли;
	
	Если СтрокаДерева.ПолучитьЭлементы().Количество() > 0
		 Или Не ЗначениеЗаполнено(СтрокаДерева.Описание) Тогда
		Описание = "";
		Возврат;
	КонецЕсли;
	
	Описание = СтрШаблон("*%1", СтрокаДерева.Описание);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыборЗначения(Элемент, Значение, СтандартнаяОбработка)

	ВыборЭлемента();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборЭлемента()
	
	СтрокаДерева = Элементы.Дерево.ТекущиеДанные;
	Если СтрокаДерева.ПолучитьЭлементы().Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	ОповеститьОВыборе(СтрокаДерева.ВидТранспортногоСредства);
	
КонецПроцедуры

#КонецОбласти
