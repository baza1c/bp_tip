#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет ставки НДС, операции, отраженные по которым, подлежат регистрации в книге продаж.
//
// Параметры:
//   Параметры - Структура - параметры для определения ставок
//     * ВерсияПостановленияНДС1137 - Число - см. УчетНДСПереопределяемый.ВерсияПостановленияНДС1137.
//
// Возвращаемое значение:
//   Массив - ставки НДС
Функция СтавкиПоОперациямОтражаемымВКнигеПродаж(ВерсияПостановленияНДС1137) Экспорт
	
	МассивСтавок = Новый Массив;
	
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС0);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС10);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС10_110);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС18);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС18_118);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС20);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС20_120);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС5);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС5_105);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС7);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС7_107);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС22);
	МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС22_122);
	
	Если ВерсияПостановленияНДС1137 < 3 Тогда
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.БезНДС);
	КонецЕсли;
	
	Возврат МассивСтавок;
	
КонецФункции

Функция СтавкаНДС(ВидСтавки, Период) Экспорт
	
	Если ВидСтавки = Перечисления.ВидыСтавокНДС.Общая Тогда
		Возврат УчетНДСКлиентСервер.ОбщаяСтавкаНДС(Период);
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ОбщаяРасчетная Тогда
		Возврат УчетНДСКлиентСервер.ОбщаяРасчетнаяСтавкаНДС(Период);
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.Пониженная Тогда
		Возврат Перечисления.СтавкиНДС.НДС10;
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ПониженнаяРасчетная Тогда
		Возврат Перечисления.СтавкиНДС.НДС10_110;
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.Нулевая Тогда
		Возврат Перечисления.СтавкиНДС.НДС0;
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.БезНДС Тогда
		Возврат Перечисления.СтавкиНДС.БезНДС;
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
		Возврат УчетНДСКлиентСервер.ОбщаяУСНСтавкаНДС(Период);
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
		Возврат УчетНДСКлиентСервер.ПониженнаяУСНСтавкаНДС(Период);
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ОбщаяУСНРасчетная Тогда
		Возврат УчетНДСКлиентСервер.РасчетнаяУСНСтавкаНДС(Период);
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ПониженнаяУСНРасчетная Тогда
		Возврат УчетНДСКлиентСервер.ПониженнаяРасчетнаяУСНСтавкаНДС(Период);
	Иначе
		Возврат Перечисления.СтавкиНДС.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// Для переданной ставки НДС возвращает расчетную ставку.
// Если для переданной ставки нет расчетной, то возвращает переданную в параметре ставку без изменения.
//
// Параметры:
//   СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - обычная ставка НДС для которой нужно получить обычную
//
// Возвращаемое значение:
//   СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - расчетная ставка для переданной
Функция РасчетнаяСтавкаНДСПоОбычной(СтавкаНДС) Экспорт
	
	Если ТипЗнч(СтавкаНДС) <> Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
		Возврат СтавкаНДС;
	КонецЕсли;
	
	Если СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
		Возврат Перечисления.СтавкиНДС.НДС18_118;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда
		Возврат Перечисления.СтавкиНДС.НДС10_110;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
		Возврат Перечисления.СтавкиНДС.НДС20_120;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС22 Тогда
		Возврат Перечисления.СтавкиНДС.НДС22_122;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС5 Тогда
		Возврат Перечисления.СтавкиНДС.НДС5_105;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС7 Тогда
		Возврат Перечисления.СтавкиНДС.НДС7_107;
	Иначе
		Возврат СтавкаНДС;
	КонецЕсли;
	
КонецФункции

// Для переданной расчетной ставки НДС возвращает обычную.
// Если для переданной ставки нет соответствия, то возвращает переданную в параметре ставку без изменения.
//
// Параметры:
//   СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - расчетная ставка НДС для которой нужно получить обычную
//
// Возвращаемое значение:
//   СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - обычная ставка для переданной расчетной
Функция ОбычнаяСтавкаНДСПоРасчетной(СтавкаНДС) Экспорт
	
	Если ТипЗнч(СтавкаНДС) <> Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
		Возврат СтавкаНДС;
	КонецЕсли;
	
	Если СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
		Возврат Перечисления.СтавкиНДС.НДС18;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
		Возврат Перечисления.СтавкиНДС.НДС10;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
		Возврат Перечисления.СтавкиНДС.НДС20;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС22_122 Тогда
		Возврат Перечисления.СтавкиНДС.НДС22;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС5_105 Тогда
		Возврат Перечисления.СтавкиНДС.НДС5;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС7_107 Тогда
		Возврат Перечисления.СтавкиНДС.НДС7;
	Иначе
		Возврат СтавкаНДС;
	КонецЕсли;
	
КонецФункции

Функция ЭтоРасчетнаяСтавкаНДС(СтавкаНДС) Экспорт
	
	Если ТипЗнч(СтавкаНДС) <> Тип("ПеречислениеСсылка.СтавкиНДС")
		Или Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118
		Или СтавкаНДС = Перечисления.СтавкиНДС.НДС5_105
		Или СтавкаНДС = Перечисления.СтавкиНДС.НДС7_107
		Или СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110
		Или СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120
		Или СтавкаНДС = Перечисления.СтавкиНДС.НДС22_122;
	
КонецФункции

// Заполняет список выбора ставок, исключая значения не характеризующие номенклатурную позицию.
//
// Параметры:
//   Форма - управляемая форма, в которой расположено поле для выбора ставки НДС
//   ИмяЭлемента - элемент формы - поле выбора ставки НДС.
//
Процедура ЗаполнитьСписокВыбораСтавокНДС(Форма, ИмяЭлемента) Экспорт
	
	Элементы = Форма.Элементы;
	
	Элементы[ИмяЭлемента].РежимВыбораИзСписка = Истина;
	Элементы[ИмяЭлемента].СписокВыбора.Очистить();
	Для Каждого ЗначениеПеречисления Из Перечисления.ВидыСтавокНДС Цикл
		Если ЗначениеПеречисления <> Перечисления.ВидыСтавокНДС.ПониженнаяУСН
		   И ЗначениеПеречисления <> Перечисления.ВидыСтавокНДС.ПониженнаяУСНРасчетная
		   И ЗначениеПеречисления <> Перечисления.ВидыСтавокНДС.ОбщаяУСН 
		   И ЗначениеПеречисления <> Перечисления.ВидыСтавокНДС.ОбщаяУСНРасчетная Тогда
			
			Элементы[ИмяЭлемента].СписокВыбора.Добавить(ЗначениеПеречисления,
				Строка(СтавкаНДС(ЗначениеПеречисления, ОбщегоНазначения.ТекущаяДатаПользователя())));
			
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	До2019Года = ТекущаяДатаПользователя < '20190101';
	До2025Года = ТекущаяДатаПользователя < '20250101';
	До2026Года = ТекущаяДатаПользователя < '20260101';
	Применяется176ФЗ = УчетНДСКлиентСервер.Применяется176ФЗ(ТекущаяДатаПользователя);
	
	Если Параметры.Свойство("ОсуществляетсяРеализацияТоваровУслугКомитентов") Тогда
		ВозможнаРеализацияТоваровУслугКомитентов = Истина;
	Иначе
		ВозможнаРеализацияТоваровУслугКомитентов = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Авансы") Тогда
		
		// Ограниченный список для счета-фактуры на аванс.
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
		ИначеЕсли Параметры.Свойство("СтавкаНДСПриУСН") Тогда
			Если До2026Года Тогда
				Если Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
					КонецЕсли;
				ИначеЕсли Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
					КонецЕсли;
				Иначе
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
					КонецЕсли;
				КонецЕсли;
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
			Иначе
				Если Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22_122);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
					КонецЕсли;
				ИначеЕсли Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22_122);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
					КонецЕсли;
				Иначе
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22_122);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
					КонецЕсли;
				КонецЕсли;
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли Параметры.Свойство("СтавкиНДСДоговора") Тогда
		
		// Ограниченный список для ставки НДС в договоре с покупателем.
		Если Параметры.Свойство("СтавкаНДСПриУСН") Тогда 
			// 2025-2026 и далее
			Если До2026Года Тогда
				Если Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					КонецЕсли;
				ИначеЕсли Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					КонецЕсли;
				Иначе
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					КонецЕсли;
				КонецЕсли;
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			Иначе
				Если Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					КонецЕсли;
				ИначеЕсли Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					КонецЕсли;
				Иначе
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
					Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
						ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					КонецЕсли;
				КонецЕсли;
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			КонецЕсли;
			
		// Остальные виды договоров.
		ИначеЕсли До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
		ИначеЕсли До2026Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
		Иначе
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
		КонецЕсли;
		
		Если Не Параметры.Свойство("СтавкаНДСПриУСН") Тогда
			
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);

			Если Применяется176ФЗ Тогда
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
			КонецЕсли;
			
			Если До2019Года Тогда
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ИначеЕсли До2026Года Тогда
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			Иначе
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС0);
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
		
	ИначеЕсли Параметры.Свойство("СтавкаНДСПриУСН") Тогда
		
		// Документы реализации.
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС0);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
		ИначеЕсли До2026Года Тогда
			Если Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
				Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
				КонецЕсли;
			ИначеЕсли Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
				Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
				КонецЕсли;
			Иначе
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
				Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
				КонецЕсли;
			КонецЕсли;
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС0);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118); 
		Иначе
			Если Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22_122);
				Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
				КонецЕсли;
			ИначеЕсли Параметры.СтавкаНДСПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22_122);
				Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
				КонецЕсли;
			Иначе
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22_122);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
				ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
				Если ВозможнаРеализацияТоваровУслугКомитентов Тогда
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
					ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
				КонецЕсли;
			КонецЕсли;
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС0);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		КонецЕсли;
		
	Иначе
		// Список выбора в документах поступления
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		ИначеЕсли До2026Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
		Иначе
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС22_122);
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС0);
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
		
		Если Применяется176ФЗ Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС5_105);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС7_107);
		КонецЕсли;
		
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
		ИначеЕсли До2026Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		Иначе
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли