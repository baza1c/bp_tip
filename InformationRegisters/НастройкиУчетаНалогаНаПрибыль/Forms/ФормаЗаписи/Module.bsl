
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастройкиУчетаНалогаНаПрибыльФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Отслеживание фонового задания по календарю бухгалтера будет происходить без открытия окна ожидания.
	Если ПараметрыЗаписи.Свойство("РезультатВыполненияЗаданияКалендаряБухгалтера") Тогда
		КалендарьБухгалтераКлиент.ОжидатьЗавершениеЗаполненияВФоне(ПараметрыЗаписи.РезультатВыполненияЗаданияКалендаряБухгалтера);
	КонецЕсли;
	
	// Очистку регистра ПроверенныеДокументы должны сделать в любом случае. Она продолжится, даже если форма закроется.
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗаписиЗавершение", ЭтотОбъект);
	ПроведениеКлиент.НачатьОчисткуРегистраПроверенныхДокументов(
		ОповещениеОЗавершении, НастройкиУчетаНалогаНаПрибыль.Период, НастройкиУчетаНалогаНаПрибыль.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыОповещения = Новый Структура("Организация, Период", НастройкиУчетаНалогаНаПрибыль.Организация, НастройкиУчетаНалогаНаПрибыль.Период);
	Оповестить("Запись_НастройкиУчетаНалогаНаПрибыль", ПараметрыОповещения);
	
	Оповестить("ИзменениеУчетнойПолитики", НастройкиУчетаНалогаНаПрибыль.Организация);
	ОповеститьОбИзменении(Тип("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ПорядокУплатыАвансов = Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.ПоФактическойПрибыли Тогда
		РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.ЗаписатьСтатистикуИзмененияПорядкаУплатыАвансов(
			ТекущийОбъект.Организация,
			ТекущийОбъект.Период);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	РезультатВыполнения = КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(УникальныйИдентификатор, ТекущийОбъект.Организация);
	ПараметрыЗаписи.Вставить("РезультатВыполненияЗаданияКалендаряБухгалтера", РезультатВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкиСистемыНалогообложения" Тогда
		
		ОрганизацияОповещения = Неопределено;
		Если Параметр.Свойство("Организация", ОрганизацияОповещения) И ОрганизацияОповещения = НастройкиУчетаНалогаНаПрибыль.Организация Тогда
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзмененыЗаписиРегистраМетодовОпределенияПрямыхРасходовНУ" Тогда
		
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьЗаголовокПереченьПрямыхЗатрат", 1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОрганизацииНаСервере();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеОбОшибкеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СистемаНалогообложения" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		НастройкиУчетаКлиент.ОбработкаНавигационнойСсылкиСистемаНалогообложения(
			ЭтотОбъект, НастройкиУчетаНалогаНаПрибыль.Организация, НастройкиУчетаНалогаНаПрибыль.Период);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Если Период = 0 Тогда
		Период = Год(НастройкиУчетаНалогаНаПрибыль.Период);
		Возврат
	КонецЕсли;
	
	НастройкиУчетаНалогаНаПрибыль.Период = Дата(Период, 1, 1);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименяютсяРазныеСтавкиНалогаНаПрибыльПриИзменении(Элемент)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.ПоложенияПереходногоПериодаУСНПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУплатыНалогаПриИзменении(Элемент)
	
	НастройкиУчетаНалогаНаПрибыль.УплачиватьНалогПоГруппамОбособленныхПодразделений = (ПорядокПодачиДекларации = 1);
	Элементы.НалоговыеОрганыГруппОбособленныхПодразделений.Доступность = НастройкиУчетаНалогаНаПрибыль.УплачиватьНалогПоГруппамОбособленныхПодразделений;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПримененияИнвестиционногоВычетаПриИзменении(Элемент)
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.ВариантПримененияИнвестиционногоВычетаПриИзменении(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВариантПрямыхРасходовПриИзменении(Элемент)
	
	НастройкиУчетаНалогаНаПрибыль.ФормироватьСтоимостьПоПравиламБУ = ВариантНастройкиПрямыхРасходов;
	
	Элементы.ПереченьПрямыхРасходов.Доступность = (ВариантНастройкиПрямыхРасходов = 0);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СтавкиНалогаНаПрибыльВБюджетСубъектовРФ(Команда)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.СтавкиНалогаНаПрибыльВБюджетСубъектовРФ(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатурныеГруппыРеализацииПродукцииУслуг(Команда)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.НоменклатурныеГруппыРеализацииПродукцииУслуг(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереченьПрямыхРасходов(Команда)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.ПереченьПрямыхРасходов(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыеОрганыГруппОбособленныхПодразделений(Команда)
	
	УчетОбособленныхПодразделенийКлиент.НалоговыеОрганыГруппОбособленныхПодразделений(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	НастройкиУчета.ПодготовитьФормуНаСервере(ЭтотОбъект, НастройкиУчетаНалогаНаПрибыль);
	
	ЗаполнитьРеквизитыФормы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	Период = Год(НастройкиУчетаНалогаНаПрибыль.Период);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Запись   = Форма.НастройкиУчетаНалогаНаПрибыль;
	
	Элементы.ПроверьтеНастройкиСистемыНалогообложения.Видимость = Не ПлательщикНалогаНаПрибыль(Запись.Организация, Запись.Период);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПлательщикНалогаНаПрибыль(Знач Организация, Знач Период)
	
	Возврат УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период);
	
КонецФункции

&НаСервере
Процедура ПриИзмененииОрганизацииНаСервере() Экспорт
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкиУчетаНалогаНаПрибыль.Организация, "ГоловнаяОрганизация");
	ЕстьОбособленныеПодразделения = НалоговыйУчет.ЕстьОбособленныеПодразделения(ГоловнаяОрганизация);
	Элементы.ГруппаПорядокПодачиДекларации.Видимость = ЕстьОбособленныеПодразделения;
	Элементы.ГруппаОпределениеДолиПрибыли.Видимость  = ЕстьОбособленныеПодразделения;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьЗаголовокПереченьПрямыхЗатрат()

	НастройкиУчетаНалогаНаПрибыльФормыКлиентСервер.УстановитьЗаголовокПереченьПрямыхЗатрат(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти
