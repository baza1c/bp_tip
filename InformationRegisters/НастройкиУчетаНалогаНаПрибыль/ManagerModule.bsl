#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает ключ действующей настройки на заданную дату
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация,
//                 для которой требуется получить настройки
//  Период      - Дата или Неопределено
// 
// Возвращаемые значения:
//  Неопределено         - когда не задана организация или нет записей в регистре
//  Ключ записи регистра - запись, соответствующая условию
//
Функция КлючЗаписиДействующейНастройки(Организация, Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ?(Период = Неопределено, ТекущаяДатаСеанса(), Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиУчетаНалогаНаПрибыль.Период КАК Период
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&Период, Организация = &Организация) КАК НастройкиУчетаНалогаНаПрибыль";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ЗначенияКлюча = Новый Структура;
		ЗначенияКлюча.Вставить("Организация", Организация);
		ЗначенияКлюча.Вставить("Период", Выборка.Период);
		
		Возврат СоздатьКлючЗаписи(ЗначенияКлюча);
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Возвращает сведения о порядке уплаты авансовых платежей
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация, для которой необходимо получить данные.
//   Период      - Дата - период, на который необходимо получить данные.
// 
// Возвращаемые значения:
//  Неопределено                                             - когда нет записей в регистре
//  ПеречислениеСсылка.ПорядокУплатыАвансовПоНалогуНаПрибыль - значение ресурса ПорядокУплатыАвансов
//
Функция ПорядокУплатыАвансовДействующейНастройки(Организация, Период = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ?(Период = Неопределено, ТекущаяДатаСеанса(), Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиУчетаНалогаНаПрибыльСрезПоследних.ПорядокУплатыАвансов КАК ПорядокУплатыАвансов
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&Период, Организация = &Организация) КАК НастройкиУчетаНалогаНаПрибыльСрезПоследних";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПорядокУплатыАвансовПоНалогуНаПрибыль = Выборка.ПорядокУплатыАвансов;
	Иначе
		ПорядокУплатыАвансовПоНалогуНаПрибыль = Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.Ежеквартально;
	КонецЕсли;
	
	Возврат ПорядокУплатыАвансовПоНалогуНаПрибыль;
	
КонецФункции

// Процедура устанавливает значения регистра по умолчанию
//
// Параметры:
//   Запись           - РегистрСведенийЗапись - запись регистра
//   ДанныеЗаполнения - Структура - где ключ - имя ресурса
//
Процедура УстановкаНастроекПоУмолчанию(Запись, ДанныеЗаполнения) Экспорт
	
	Если Не ЗначениеЗаполнено(Запись.Период) Тогда
		НастройкиУчета.УстановитьЗначениеПоУмолчанию(
			Запись,
			ДанныеЗаполнения,
			"Период",
			НачалоГода(ТекущаяДатаСеанса()));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.Организация) Тогда
		НастройкиУчета.УстановитьЗначениеПоУмолчанию(
			Запись,
			ДанныеЗаполнения,
			"Организация",
			БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
	КонецЕсли;
	
	ЭтоФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Организация, "ЮридическоеФизическоеЛицо")
		= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		
	Если ЭтоФизЛицо Тогда
			Запись.Организация = Справочники.Организации.ПустаяСсылка();
		Возврат;
	КонецЕсли;
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"МетодНачисленияАмортизации",
		Перечисления.МетодыНачисленияАмортизации.Линейный);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"СпособПогашенияСтоимостиСпецодежды",
		Перечисления.СпособыПогашенияСтоимостиНУ.ПриПередачеВЭксплуатацию);
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"СтавкаФБ",
		ЗначениеПоУмолчаниюФедеральныйБюджет(Запись.Период));
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"СтавкаСубъектРФ",
		ЗначениеПоУмолчаниюРегиональныйБюджет(Запись.Период));
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ПорядокУплатыАвансов",
		Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.Ежеквартально);
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"БазаРаспределенияКосвенныхРасходовПоВидамДеятельности",
		Перечисления.БазыРаспределенияКосвенныхРасходовПоВидамДеятельности.ДоходыОтРеализации);
	
КонецПроцедуры

// Определяет ставку налога на прибыль, подлежащего зачислению в федеральный бюджет, указанную в п. 1 ст. 284 НК РФ
//
// Параметры:
//  Период - Дата - Дата, на которую выбирается значение по умолчанию
//
Функция ЗначениеПоУмолчаниюФедеральныйБюджет(Период) Экспорт
	
	Если Год(Период) >= 2025 Тогда
		Возврат 8; // Федеральный закон от 12.07.2024 № 176-ФЗ
	ИначеЕсли ПрименяютсяВременныеСтавкиНалога(Период) Тогда
		Возврат 3;
	КонецЕсли;
	
	Возврат 2;
	
КонецФункции

// Определяет ставку налога на прибыль, подлежащего зачислению в бюджеты субъектов Российской Федерации, указанную в п. 1 ст. 284 НК РФ
//
// Параметры:
//  Период - Дата - Дата, на которую выбирается значение по умолчанию
//
Функция ЗначениеПоУмолчаниюРегиональныйБюджет(Период) Экспорт
	
	Если ПрименяютсяВременныеСтавкиНалога(Период) Тогда
		Возврат 17;
	КонецЕсли;	
	
	Возврат 18;
	
КонецФункции

// Записывает событие в Бизнес-статистику, если порядок уплаты авансов по налогу на прибыль ранее был
// "Ежемесячно по расчетной прибыли". Используется для сбора статистики в 2022 году при изменении порядка уплаты
// на "Ежемесячно по фактической прибыли".
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация по которой изменяется порядок учета авансов.
//  Период - Дата - период, для которого действует настройка учета.
//
Процедура ЗаписатьСтатистикуИзмененияПорядкаУплатыАвансов(Организация, Период) Экспорт
	
	ПорядокУплаты = ПорядокУплатыАвансовДействующейНастройки(Организация, Период);
	
	Если ПорядокУплаты = Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.Ежемесячно Тогда
		ОбщегоНазначенияБП.ЗаписатьОперациюБизнесСтатистики(
			"СтатистикаБП.НастройкиУчетаНалогаНаПрибыль.ИзменениеПорядкаУплатыАвансовПоНалогуНаПрибыль");
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверки заполненности ставок налога на прибыль в 2025 году.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Булево
//
Функция ЗаполненыСтавкиНалогаНаПрибыльС2025Года(Организация) Экспорт
	
	Период = '2025-01-01';
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Период", Период);
	Запрос.Параметры.Вставить("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиУчетаНалогаНаПрибыль.СтавкаФБ КАК СтавкаФБ
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК НастройкиУчетаНалогаНаПрибыль
	|ГДЕ
	|	НастройкиУчетаНалогаНаПрибыль.Организация = &Организация
	|	И НастройкиУчетаНалогаНаПрибыль.Период = &Период";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Организация.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	| ЗначениеРазрешено(ЭтотСписок.Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет, попадает ли указанный период в интервал,
// на который установлены особые ставки налога на прибыль
// (от 21.11.2022 N 443-ФЗ (ред. от 31.07.2023))
Функция ПрименяютсяВременныеСтавкиНалога(Период)
	
	Возврат (НачалоГода(Период) >= '2017-01-01'
		И НачалоГода(Период) <= '2030-01-01');
	
КонецФункции	

#КонецОбласти

#Область ОбработчикиОбновления

// Процедура - обработчик обновления
// Заполняет ставки налога на прибыль, введенные на период с 2017 по 2020 годы (401-ФЗ от 30.11.2016)
Процедура ЗаполнитьСтавкиНалогаНаПрибыльС2017По2020Годы(Параметры) Экспорт
								
	НачалоПериода 	= '2017-01-01';
	КонецПериода 	= КонецДня('2020-12-31');
		
	НачатьТранзакцию();
	
	// Заблокируем данные регистра сведений от чтения и записи
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиУчетаНалогаНаПрибыль");
		ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(НачалоПериода, КонецПериода));
		Блокировка.Заблокировать();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
			
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", 		НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  		КонецПериода);
	
	// Выбираем настройки налога на прибыль с целью их дальнейшей перезаписи с измененными ставками налога
	// (1) Выбираем установленные настройки налога на прибыль за период с 2017 по 2020 годы
	// (2) Если на 2017 год настройка не устанавливалась,
	// то копируем настройки из действующих настроек на начало 2017 года
	// Настройки (1) поправляем, даже если организация более не является плательщиком налога
	// Добавлять настройки (2) имеет смысл только для организаций - плательщиков налога на прибыль
	
	// Поля регистра сведений добавляем в запрос через СхемаЗапроса ниже
	// Это необходимо для того, чтобы при добавлении новых ресурсов и реквизитов обработчик их не затер
	
	// Вторая таблица второго запроса выбирает записи в срез последних только в том случае,
	// если последняя запись по организации ранее 2017 года (см. условие "ГДЕ НастройкиСрезПоследних.Период < &НачалоПериода").
	// В этом случае первый запрос объединения заведомо не вернет запись за 2017 год (ее не будет в базе)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаНалогообложения.Организация КАК Организация
	|ПОМЕСТИТЬ ПлательщикиНалогаНаПрибыль
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(
	|			&НачалоПериода) КАК НастройкаНалогообложения
	|ГДЕ
	|	НастройкаНалогообложения.ПлательщикНалогаНаПрибыль
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Настройки.Период КАК Период,
	|	Настройки.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК Настройки
	|ГДЕ
	|	Настройки.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПериода,
	|	НастройкиСрезПоследних.Организация
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(
	|			&НачалоПериода,
	|			Организация В
	|				(ВЫБРАТЬ
	|					ПлательщикиНалогаНаПрибыль.Организация
	|				ИЗ
	|					ПлательщикиНалогаНаПрибыль КАК ПлательщикиНалогаНаПрибыль)) КАК НастройкиСрезПоследних
	|ГДЕ
	|	НастройкиСрезПоследних.Период < &НачалоПериода";
		
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	// Заполняем поля 2-го запроса
	// Выбираем все ресурсы и реквизиты регистра сведений "НастройкиУчетаНалогаНаПрибыль"
	// Первые 2 поля (измерения регистра) уже указаны явно
	МассивПолейРегистра = Новый Массив;
	Для Каждого РесурсРегистра Из Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.Ресурсы Цикл
		МассивПолейРегистра.Добавить(РесурсРегистра.Имя);
	КонецЦикла;	
		
	Для Каждого РеквизитРегистра Из Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.Реквизиты Цикл
		МассивПолейРегистра.Добавить(РеквизитРегистра.Имя);
	КонецЦикла;	
	
	ЗапросПоРегистру 	= СхемаЗапроса.ПакетЗапросов[1];
	Подзапрос1			= ЗапросПоРегистру.Операторы[0];
	Подзапрос2			= ЗапросПоРегистру.Операторы[1];
	
	Для Каждого Поле Из МассивПолейРегистра Цикл
		
		// Добавляем поле в первый подзапрос
		ВыражениеЗапроса = Подзапрос1.ВыбираемыеПоля.Добавить("Настройки."+Поле);
		КолонкаЗапроса = ЗапросПоРегистру.Колонки.Найти(ВыражениеЗапроса);
		КолонкаЗапроса.Псевдоним = Поле;
		
		// Добавляем поле во второй подзапрос в ту же позицию,
		// чтобы совпали колонки объединяемых запросов
		ПозицияПоляЗапроса = Подзапрос1.ВыбираемыеПоля.Индекс(ВыражениеЗапроса);
		Подзапрос2.ВыбираемыеПоля.Добавить("НастройкиСрезПоследних."+Поле, ПозицияПоляЗапроса);
		
	КонецЦикла;	
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		ЗафиксироватьТранзакцию();
		Возврат;
	КонецЕсли;	
	
	ВыборкаНастройки = РезультатЗапроса.Выбрать();
	
	Набор = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.СоздатьНаборЗаписей(); // Наборы записываются в цикле, каждый набор содержит одну запись
	
	Пока ВыборкаНастройки.Следующий() Цикл
		
		// Пропускаем "нестандартные" ставки
		Если ВыборкаНастройки.СтавкаФБ <> 2 ИЛИ ВыборкаНастройки.СтавкаСубъектРФ <> 18 Тогда
			Продолжить;
		КонецЕсли;
		
		Набор.Очистить();
		// В отборе набора записей всегда есть Период и Организация
		Набор.Отбор.Период.Установить(ВыборкаНастройки.Период);
		Набор.Отбор.Организация.Установить(ВыборкаНастройки.Организация);
				
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаНастройки);
		
		Запись.СтавкаФБ        = ЗначениеПоУмолчаниюФедеральныйБюджет(ВыборкаНастройки.Период);
		Запись.СтавкаСубъектРФ = ЗначениеПоУмолчаниюРегиональныйБюджет(ВыборкаНастройки.Период);
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор, Истина);
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
		    	НСтр("ru = 'Процедуре ЗаполнитьСтавкиНалогаНаПрибыльС2017По2020Годы не удалось обновить настройки налога на прибыль по организации ""%1"" за %2 год:
		     	|%3'"),
			 	ВыборкаНастройки.Организация,
				Год(ВыборкаНастройки.Период),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ВызватьИсключение ТекстСообщения;
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Истина;
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры	

// Процедура - обработчик обновления
// Заполняет ставки налога на прибыль, установленные на период с 2021 по 2024 годы (301-ФЗ от 03.08.2018)
// 
Процедура ЗаполнитьСтавкиНалогаНаПрибыльС2021По2024Годы(Параметры) Экспорт
	
	// Переход с обычных ставок налога (до 2017 года) на "временные" (401-ФЗ от 30.11.2016 установил на 2017-2020 годы ставки 3% и 17%)
	// выполнен предыдущим обработчиком (см. ЗаполнитьСтавкиНалогаНаПрибыльС2017По2020Годы()).
	// Теперь указанный период применения "временных" ставок продлен на 2021-2024 годы (см. 301-ФЗ от 03.08.2018).
	// Поэтому добавлять "переходные" записи не требуется: считаем, что до 2021 настройки либо отсуствуют,
	// либо уже содержат правильные ставки налога. Ниже только исправим настройки на 2021-2024 годы с обычными ставками налога,
	// которые успел ввести пользователь.  
	
	НачалоПериода 	= '2021-01-01';
	КонецПериода 	= КонецДня('2024-12-31');
		
	НачатьТранзакцию();
	
	Попытка
		
		// Заблокируем данные регистра сведений от чтения и записи
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиУчетаНалогаНаПрибыль");
		ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(НачалоПериода, КонецПериода));
		Блокировка.Заблокировать();
			
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НачалоПериода", 		    НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода",  		    КонецПериода);
		Запрос.УстановитьПараметр("ОбычнаяСтавкаФБ",        2);
		Запрос.УстановитьПараметр("ОбычнаяСтавкаСубъектРФ", 18);
			
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Настройки.Период КАК Период,
		|	Настройки.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК Настройки
		|ГДЕ
		|	Настройки.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Настройки.СтавкаФБ = &ОбычнаяСтавкаФБ
		|	И Настройки.СтавкаСубъектРФ = &ОбычнаяСтавкаСубъектРФ";
			
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ЗафиксироватьТранзакцию();
			Параметры.ОбработкаЗавершена = Истина;
			Возврат;
		КонецЕсли;	
		
		ВыборкаНастройки = РезультатЗапроса.Выбрать();
		
		Набор = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.СоздатьНаборЗаписей(); // Наборы записываются в цикле, каждый набор содержит одну запись
		
		Пока ВыборкаНастройки.Следующий() Цикл
					
			Набор.Очистить();
			// В отборе набора записей всегда есть Период и Организация
			Набор.Отбор.Период.Установить(ВыборкаНастройки.Период);
			Набор.Отбор.Организация.Установить(ВыборкаНастройки.Организация);
			
			Набор.Прочитать(); 
			
			// В наборе должна быть 1 запись, но проверим для надежности обработчика
			Если Набор.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;	
			
			Запись = Набор[0];		
			Запись.СтавкаФБ        = ЗначениеПоУмолчаниюФедеральныйБюджет(ВыборкаНастройки.Период);
			Запись.СтавкаСубъектРФ = ЗначениеПоУмолчаниюРегиональныйБюджет(ВыборкаНастройки.Период);
		
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор, Истина);
						
		КонецЦикла;
	
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = СтрШаблон(
	    	НСтр("ru = 'Процедуре ЗаполнитьСтавкиНалогаНаПрибыльС2021По2024Годы не удалось обновить настройки налога на прибыль по причине:
	     	|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры	

// Процедура - обработчик обновления
// Заполняет ставки налога на прибыль, установленные на период с 2025 по 2030 годы (№176-ФЗ)
// 
Процедура ЗаполнитьСтавкиНалогаНаПрибыльС2025Года(Параметры) Экспорт
	
	Период = '2025-01-01';
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиУчетаНалогаНаПрибыль");
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ПредыдущийПериод", Период - 1);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ИЗ
		|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&ПредыдущийПериод, ) КАК Настройки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Настройки.Организация";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Запрос.УстановитьПараметр("Период", Период);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Настройки.Организация КАК Организация,
		|	Настройки.СтавкаФБ КАК СтавкаФБ,
		|	Настройки.СтавкаСубъектРФ КАК СтавкаСубъектРФ
		|ИЗ
		|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК Настройки
		|ГДЕ
		|	Настройки.Период = &Период";
		
		Настройки2025 = Запрос.Выполнить().Выгрузить();
		Настройки2025.Индексы.Добавить("Организация");
		
		ДействующаяСтавкаФБ = ЗначениеПоУмолчаниюФедеральныйБюджет(Период - 1);
		НоваяСтавкаФБ = ЗначениеПоУмолчаниюФедеральныйБюджет(Период);
		СтавкаСубъектРФ = ЗначениеПоУмолчаниюРегиональныйБюджет(Период);
		
		Пока Выборка.Следующий() Цикл
			
			Настройка2025 = Неопределено;
			
			Если Выборка.СтавкаФБ = ДействующаяСтавкаФБ И Выборка.СтавкаСубъектРФ = СтавкаСубъектРФ Тогда
				// Установлены стандартные ставки налога на прибыль.
				НайденыеСтроки = Настройки2025.НайтиСтроки(Новый Структура("Организация", Выборка.Организация));
				Если НайденыеСтроки.Количество() > 0 Тогда
					Настройка2025 = НайденыеСтроки[0];
					Если Настройка2025.СтавкаФБ = НоваяСтавкаФБ И Настройка2025.СтавкаСубъектРФ = СтавкаСубъектРФ Тогда
						// У организации уже установлены новые ставки. Не требуется ничего делать.
						Продолжить;
					ИначеЕсли Настройка2025.СтавкаФБ <> ДействующаяСтавкаФБ И Настройка2025.СтавкаСубъектРФ <> СтавкаСубъектРФ Тогда
						// Для существующих ставок выполним корректировку только в том случае, если на 2025 год указаны заведомо
						// неверные ставки. Например, в 2024 году были стандартные ставки 3% и 17%, для 2025 года указаны тоже 3% и 17%.
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				// У организации есть льготы. Не следует устанавливать стандартную ставку налога на прибыль.
				Продолжить;
			КонецЕсли;
			
			Набор = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.СоздатьНаборЗаписей();
			Набор.Отбор.Период.Установить(Период);
			Набор.Отбор.Организация.Установить(Выборка.Организация);
			Набор.Прочитать();
			
			Если Набор.Количество() = 0 Тогда
				Запись = Набор.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
				Запись.Период = Период;
			Иначе
				Запись = Набор[0];
			КонецЕсли;
			
			Запись.СтавкаФБ = НоваяСтавкаФБ;
			Запись.СтавкаСубъектРФ = СтавкаСубъектРФ;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор, Истина);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Процедуре ЗаполнитьСтавкиНалогаНаПрибыльС2025По2030Годы не удалось обновить настройки налога на прибыль по причине:
			|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

// Заполняет поле ПоказательОпределенияДолиПрибыли.
// До версии, в которой появилось это поле, поддерживался только один показатель - РасходыНаОплатуТруда
//
// Параметры:
//  Параметры - Структура - стандартный параметр отложенного обработчика обновления.
//     В данном случае не используется: обрабатываются все данные за один раз, исключения пропускаются.
//
Процедура ЗаполнитьПоказательОпределенияДолиПрибыли(Параметры) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		// Заблокируем данные регистра сведений от чтения и записи
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиУчетаНалогаНаПрибыль");
		Блокировка.Заблокировать();
		
		Набор = СоздатьНаборЗаписей();
		Набор.Прочитать();
		
		Для Каждого Запись Из Набор Цикл
			Если ЗначениеЗаполнено(Запись.ПоказательОпределенияДолиПрибыли) Тогда
				Продолжить;
			КонецЕсли;
			Запись.ПоказательОпределенияДолиПрибыли = Перечисления.ПоказателиДляОпределенияДолиПрибыли.РасходыНаОплатуТруда;
		КонецЦикла;
		
		Если Набор.Модифицированность() Тогда
			Набор.Записать(Истина);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Процедуре ЗаполнитьПоказательОпределенияДолиПрибыли не удалось обновить настройки налога на прибыль по причине:
		          |%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыль,
			, // Данные
			ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли