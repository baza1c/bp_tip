#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	СтрокаПолейКлюча = "";
	ОписаниеКлючаЗаписи = Новый Структура;
	Для Каждого Измерение Из ЭтотОбъект.Метаданные().Измерения Цикл
		ОписаниеКлючаЗаписи.Вставить(Измерение.Имя);
		СтрокаПолейКлюча = СтрокаПолейКлюча + ?(СтрокаПолейКлюча = "","", ", ")+ Измерение.Имя;
	КонецЦикла;
	
	КонтролируемыеСделки.ПроверитьПравильностьЗаполненияКонтрагентаРегистра(Отказ, ЭтотОбъект.Выгрузить(, СтрокаПолейКлюча), ОписаниеКлючаЗаписи);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Запись.Период >= КонтролируемыеСделки.ДатаОтказаОтЕНВД() Тогда
			Запись.ЯвляетсяПлательщикомЕНВД = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
