#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ЗафиксироватьЗарегистрированные(НастройкаСсылка) Экспорт
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования");
		ЭлементБлокировки.УстановитьЗначение("МеткаВремени", 0);
		
		ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиСовместногоИспользованияПриложений");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", НастройкаСсылка);
		
		Блокировка.Заблокировать();
		
		МеткаВремени = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаСсылка, "МеткаВремениВыборкиИзменений");
		
		НаборИзмененийТекущий = СоздатьНаборЗаписей();
		НаборИзмененийТекущий.Отбор.МеткаВремени.Установить(0);
		НаборИзмененийТекущий.Прочитать();
		
		Если НаборИзмененийТекущий.Количество() > 0 Тогда
			МеткаВремени = Макс(ТекущаяУниверсальнаяДатаВМиллисекундах(), МеткаВремени + 1);
			
			БлокировкаМетка = Новый БлокировкаДанных;
		
			ЭлементБлокировки = БлокировкаМетка.Добавить("РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования");
			ЭлементБлокировки.УстановитьЗначение("МеткаВремени", МеткаВремени);
			
			БлокировкаМетка.Заблокировать();
			
			ТаблицаИзменений = НаборИзмененийТекущий.Выгрузить(, "Ссылка");
		
			ТаблицаИзменений.Колонки.Добавить("МеткаВремени", ОбщегоНазначения.ОписаниеТипаЧисло(18, 0));
			ТаблицаИзменений.ЗаполнитьЗначения(МеткаВремени, "МеткаВремени");
			
			НаборИзмененийНовый = СоздатьНаборЗаписей();
			НаборИзмененийНовый.Отбор.МеткаВремени.Установить(МеткаВремени);
			НаборИзмененийНовый.Загрузить(ТаблицаИзменений);
			
			НаборИзмененийТекущий.Очистить();
			
			НаборИзмененийТекущий.Записать();
			
			НаборИзмененийНовый.Записать();
			
			НастройкаОбъект = НастройкаСсылка.ПолучитьОбъект();
			НастройкаОбъект.МеткаВремениВыборкиИзменений = МеткаВремени;
			НастройкаОбъект.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат МеткаВремени;
	
КонецФункции

Процедура Зарегистрировать(Ссылка) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		// Регистрация изменений может выполняться и пользователями, у которых нет доступа к
		// ИзмененияСинхронизируемыхОбъектовСовместногоИспользования. Поэтому будет записывать в привилегированном режиме.
		УстановитьПривилегированныйРежим(Истина);
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
		ЭлементБлокировки.УстановитьЗначение("МеткаВремени", 0);
		
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(Ссылка);
		НаборЗаписей.Отбор.МеткаВремени.Установить(0);
		
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Ссылка = Ссылка;
			
			НаборЗаписей.Записать();
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура УдалитьРегистрацию(МеткаВремени) Экспорт
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования");
		ЭлементБлокировки.УстановитьЗначение("МеткаВремени", МеткаВремени);
		
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.МеткаВремени.Установить(МеткаВремени);
		
		НаборЗаписей.Записать();
	
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
		
КонецПроцедуры

Функция ЗарегистрированныеТипыСсылок(МеткаВремени) Экспорт
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования");
		ЭлементБлокировки.УстановитьЗначение("МеткаВремени", МеткаВремени);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Ссылка) КАК ТипСсылки
		|ИЗ
		|	РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования КАК ТаблицаИзменений
		|ГДЕ
		|	ТаблицаИзменений.МеткаВремени = &МеткаВремени");
		Запрос.УстановитьПараметр("МеткаВремени", МеткаВремени);
		
		ТипыСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТипСсылки");
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ТипыСсылок;
	
КонецФункции

Процедура ПодготовитьТаблицуИдентификаторов(МенеджерВТ, МеткаВремени, ТипСсылки, УзелОбмена) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Ссылка КАК Ссылка,
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ТаблицаИзменений.Ссылка) КАК Идентификатор,
	|	ПубличныеИдентификаторы.Идентификатор КАК ПубличныйИдентификатор
	|ПОМЕСТИТЬ ТаблицаИдентификаторы
	|ИЗ
	|	РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов КАК ПубличныеИдентификаторы
	|		ПО (ПубличныеИдентификаторы.Ссылка = ТаблицаИзменений.Ссылка)
	|			И (ПубличныеИдентификаторы.УзелИнформационнойБазы = &УзелОбмена)
	|ГДЕ
	|	ТаблицаИзменений.МеткаВремени = &МеткаВремени
	|	И ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Ссылка) = &ТипСсылки");
	Запрос.УстановитьПараметр("МеткаВремени", МеткаВремени);
	Запрос.УстановитьПараметр("ТипСсылки", ТипСсылки);
	Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция СпискиИзмененийСинхронизируемыхОбъектов(НастройкаСсылка) Экспорт
	
	МеткаВремени = РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.ЗафиксироватьЗарегистрированные(НастройкаСсылка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаИзменений.МеткаВремени КАК МеткаВремени,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаИзменений.Ссылка) КАК КоличествоЗаписей
	|ИЗ
	|	РегистрСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования КАК ТаблицаИзменений
	|ГДЕ
	|	ТаблицаИзменений.МеткаВремени <= &МеткаВремени
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.МеткаВремени
	|
	|УПОРЯДОЧИТЬ ПО
	|	МеткаВремени");
	Запрос.УстановитьПараметр("МеткаВремени", МеткаВремени);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли