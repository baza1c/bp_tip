#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Конструктор таблицы с колонками, соответствующими измерениям регистра.
// Используется часто, в том числе при определении статусов задач, поэтому коллекция конструируется без обращения к метаданным.
// При изменении структуры регистра, включая состав типов измерений, следует вносить изменения в эту функцию.
//
Функция КлючиЗадач() Экспорт
	
	ТипыПравил = ТипыПравил();
	
	КлючиЗадач = Новый ТаблицаЗначений;
	
	КлючиЗадач.Колонки.Добавить("Организация",   Новый ОписаниеТипов("СправочникСсылка.Организации"));
	КлючиЗадач.Колонки.Добавить("Правило",       Новый ОписаниеТипов(ТипыПравил));
	КлючиЗадач.Колонки.Добавить("ПериодСобытия", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	КлючиЗадач.Колонки.Добавить("РегистрацияВНалоговомОргане",
		Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	КлючиЗадач.Колонки.Добавить("Действие",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДействийКалендаряБухгалтера"));
	
	Возврат КлючиЗадач;
	
КонецФункции

Функция ТипыПравил()
	
	ТипыПравил = Новый Массив;
	ТипыПравил.Добавить(Тип("ПеречислениеСсылка.ЗадачиНачалаРаботы"));
	ТипыПравил.Добавить(Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов"));
	ТипыПравил.Добавить(Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов"));
	ТипыПравил.Добавить(Тип("ДокументСсылка.ТранспортноеСообщение"));
	
	ЗадачиБухгалтераПереопределяемый.ПриПолученииТиповПравил(ТипыПравил);
	
	Возврат ТипыПравил;
	
КонецФункции

Функция КлючиЗадачСтруктурой() Экспорт
	
	КлючиЗадач = КлючиЗадач();
	КлючиЗадач.Добавить();
	
	Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(КлючиЗадач[0]);
	
КонецФункции

Функция ТипСтатуса() Экспорт
	Возврат Метаданные.РегистрыСведений.ЗадачиБухгалтера.Ресурсы.Статус.Тип;
КонецФункции

Функция ОбновитьИЗаполнитьЗадачиНачалаРаботы(Знач ОтборОрганизации = Неопределено, СозданаНоваяОрганизация = Ложь) Экспорт
	
	ИзмененыЗаписи = Ложь;
	
	// Ищем новые задачи начала работы
	ЗадачиНачалаРаботы = НовыеЗадачиНачалаРаботы(ОтборОрганизации);
	
	Если ЗадачиНачалаРаботы.Количество() = 0 Тогда
		
		// Завершение неактуальных задач проверки банковских операций АУСН
		Если ВыполнениеЗадачБухгалтера.ЗавершитьПроверкуБанковскихОперацийАУСН() Тогда
			Возврат Истина;
		КонецЕсли;
	
		Возврат Ложь;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтера");
	ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ЗадачиНачалаРаботы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
	
	НачатьТранзакцию();
	
	Блокировка.Заблокировать();
	
	// Создадим новые задачи
	Для Каждого Задача Из ЗадачиНачалаРаботы Цикл
		
		Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Задача);
		Если Задача.Правило = Перечисления.ЗадачиНачалаРаботы.ПроверитьОсновнойВидДеятельности Тогда
			Запись.Срок = ДатаПроверкиВидаДеятельности();
		КонецЕсли;
		Запись.Записать();
		
		ИзмененыЗаписи = Истина;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	Возврат ИзмененыЗаписи;
	
КонецФункции

Функция ОбновитьИЗаполнитьРегулярныеЗадачи(Знач ОтборОрганизации = Неопределено, Упреждение = 0, Знач Правила = Неопределено, ОбновитьСписокОтчетов = Истина) Экспорт
	
	Организации = МассивОрганизаций(ОтборОрганизации);
	Если Организации.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИзмененыЗаписи = Ложь;
	
	// Заблокируем для обновления
	КоличествоОрганизаций = Организации.Количество();
	Для Номер = 1 По КоличествоОрганизаций Цикл
		
		Индекс = КоличествоОрганизаций - Номер;// Обходим с конца, потому что будем удалять те, что не удалось заблокировать
		Организация = Организации[Индекс];
		
		ПараметрыКонструктора = Новый Массив;
		ПараметрыКонструктора.Добавить(Новый Структура("Организация", Организация));
		КлючЗаписи = Новый(Тип("РегистрСведенийКлючЗаписи.АктуальностьСпискаЗадачБухгалтера"), ПараметрыКонструктора);
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(КлючЗаписи); // До окончания вызова сервера
		Исключение
			Организации.Удалить(Индекс);
		КонецПопытки;
		
	КонецЦикла;
	
	// Задачи уже созданы? За какой период?
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	ЕСТЬNULL(АктуальностьСписка.ДатаАктуальности, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаАктуальности
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АктуальностьСпискаЗадачБухгалтера КАК АктуальностьСписка
	|		ПО Организации.Ссылка = АктуальностьСписка.Организация
	|ГДЕ
	|	Организации.Ссылка В(&Организации)";
	
	ТекущаяДата      = НачалоДня(ТекущаяДатаСеанса());
	ДатаАктуализации = ТекущаяДата + Упреждение * 24 * 60 * 60;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// К появлению задач могут приводить какие-то события прошлого.
		
		// Делим это прошлое на две части:
		// - "некогда", "когда-то давно"
		// - "недавно"
		
		// События давнего прошлого могут приводить только к таким задачам,
		// которые еще можно выполнить в срок.
		// Просроченные задачи могут появляться только исходя из недавних событий.
		
		// При первом запуске (для новой организации, в новой версии) 
		// все прошлое считаем давним.
		// Другими словами, _дополняем_ список задач по событиям давнего прошлого.
		
		// При повторных запусках составляем расписание для давнего прошлого
		// на случай, если в периодах за это прошлое изменились данные ИБ,
		// приводящие к тому, что нужно уплачивать какие-то другие налоги 
		// или сдавать какие-то другие отчеты.
		// То есть в этом случае по событиям давнего прошлого мы _обновляем_ список задач.
		// А по недавним событиям - _дополняем_ список задач.
		
		// При обновлении (не дополнении) списка задач, как правило, не потребуется
		// вносить никаких изменений в список - достаточно лишь убедиться,
		// что список правильный.
		// Реже придется что-то поменять, но не все.
		// Поэтому будем записывать только отличия между правильным (рассчитанным в этой процедуре)
		// расписанием и уже имеющимся.
		
		Если ЗначениеЗаполнено(Выборка.ДатаАктуальности) Тогда
			
			// Обновим задачи по давним событиям
			Расписание = РасписаниеПоДавнимСобытиям(
				Выборка.Организация,
				Выборка.ДатаАктуальности, // До ДатаАктуальности
				ТекущаяДата,
				Правила,
				ОбновитьСписокОтчетов);
			ЗаписатьИзменения(
				Выборка.Организация, 
				Расписание,
				ТекущаяДата,
				ИзмененыЗаписи);
				
			// Добавим задачи по недавним событиям
			Расписание = РасписаниеПоНедавнимСобытиям(
				Выборка.Организация,
				Выборка.ДатаАктуальности, // С  ДатаАктуальности
				ДатаАктуализации,         // По ДатаАктуализации
				Правила,
				ОбновитьСписокОтчетов);
			ЗаписатьНовыеЗадачи(
				Выборка.Организация,
				Расписание,
				Выборка.ДатаАктуальности, // Старая ДатаАктуальности
				ДатаАктуализации,         // Новая ДатаАктуальности
				ТекущаяДата,
				ИзмененыЗаписи); 
				
		Иначе
			
			// Добавим задачи по давним событиям
			Расписание = РасписаниеПоДавнимСобытиям(
				Выборка.Организация,
				ДатаАктуализации, // Все считаем давними
				ТекущаяДата,
				Правила,
				ОбновитьСписокОтчетов);
			ЗаписатьНовыеЗадачи(
				Выборка.Организация,
				Расписание,
				Выборка.ДатаАктуальности,
				ДатаАктуализации,
				ТекущаяДата,
				ИзмененыЗаписи);
				
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИзмененыЗаписи;
	
КонецФункции

Процедура ОбновитьИДополнитьРегулярныеЗадачи(Знач Организация, Знач ДатаАктуальности, Знач Правила) Экспорт
	
	// Заблокируем для обновления
	ПараметрыКонструктора = Новый Массив;
	ПараметрыКонструктора.Добавить(Новый Структура("Организация", Организация));
	КлючЗаписи = Новый(Тип("РегистрСведенийКлючЗаписи.АктуальностьСпискаЗадачБухгалтера"), ПараметрыКонструктора);
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(КлючЗаписи); // До окончания вызова сервера
	Исключение
		ВызватьИсключение;
	КонецПопытки;
		
	ТекущаяДата      = НачалоДня(ТекущаяДатаСеанса());
	ДатаАктуализации = ТекущаяДата;
	
	// Добавим задачи по недавним событиям
	Расписание = РасписаниеПоНедавнимСобытиям(
		Организация,
		ДатаАктуальности, // С  ДатаАктуальности
		ДатаАктуализации, // По ДатаАктуализации
		Правила,
		Ложь);
	ЗаписатьНовыеЗадачи(
		Организация,
		Расписание,
		ДатаАктуальности, // Старая ДатаАктуальности
		ДатаАктуализации, // Новая ДатаАктуальности
		ТекущаяДата);
	
КонецПроцедуры

Функция ОбновитьИЗаполнитьЗадачиСнятияСУчета(Знач ОтборОрганизации) Экспорт
	
	ИзмененыЗаписи = Ложь;
	
	// Ищем новые задачи начала работы
	ЗадачиНачалаРаботы = НовыеЗадачиСнятияСУчета(ОтборОрганизации);
	
	Если ЗадачиНачалаРаботы.Количество() = 0 Тогда
		
		// Завершение неактуальных задач
		Если ЗавершитьЗадачуУказанияДатыЗакрытияИП() Тогда
			Возврат Истина;
		КонецЕсли;
	
		Возврат Ложь;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтера");
	ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ЗадачиНачалаРаботы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
	
	НачатьТранзакцию();
	
	Блокировка.Заблокировать();
	
	// Создадим новые задачи
	Для Каждого Задача Из ЗадачиНачалаРаботы Цикл
		
		Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Задача);
		Запись.Записать();
		
		ИзмененыЗаписи = Истина;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	Возврат ИзмененыЗаписи;
	
КонецФункции

Функция БлижайшаяДатаПоявленияЗадач(Организация = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Возврат БлижайшаяДатаПоявленияЗадачОрганизации(Организация);
	КонецЕсли;
	
	// По всем организациям
	Завтра = КонецДня(ТекущаяДатаСеанса()) + 1;
	БлижайшаяДата = Неопределено;
	
	Для Каждого ОднаОрганизация Из МассивОрганизаций(Организация) Цикл
		
		БлижайшаяДатаОрганизации = БлижайшаяДатаПоявленияЗадачОрганизации(ОднаОрганизация, БлижайшаяДата);
		
		Если БлижайшаяДата = Неопределено Тогда
			БлижайшаяДата = БлижайшаяДатаОрганизации;
		ИначеЕсли ТипЗнч(БлижайшаяДатаОрганизации) = Тип("Дата") Тогда
			БлижайшаяДата = Мин(БлижайшаяДата, БлижайшаяДатаОрганизации);
		КонецЕсли;
		
		Если БлижайшаяДата = Завтра Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат БлижайшаяДата;
	
КонецФункции

Функция УстановитьСтатусВыполнено(ИсточникДанныхКлючаЗаписи, Выполнено = Истина) Экспорт
	
	ЗначенияКлюча = КлючиЗадачСтруктурой();
	ЗаполнитьЗначенияСвойств(ЗначенияКлюча, ИсточникДанныхКлючаЗаписи);
	
	МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначенияКлюча);
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Тогда
		// Обработаем задачи, созданные до регистрации действий
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначенияКлюча);
		МенеджерЗаписи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ПустаяСсылка();
		МенеджерЗаписи.Прочитать();
	КонецЕсли;
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.ВАрхиве = Выполнено;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
	Возврат РегистрыСведений.ЗадачиБухгалтера.СоздатьКлючЗаписи(ЗначенияКлюча);
	
КонецФункции

// Удаляем непросроченные задачи по заданному правилу
Функция УдалитьЗаписиПоПравилу(Организация, Правило, ТолькоАктуальные = Истина) Экспорт
	
	ИзмененыЗаписи = Ложь;
	
	ЗаписиКУдалению = ПолучитьСписокЗадачПоПравилу(Организация, Правило, ТолькоАктуальные);
	
	Если ЗаписиКУдалению.Количество() > 0 Тогда
		
		НачатьТранзакцию();
		
		// Установим управляемые блокировки
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтера");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.УстановитьЗначение("Правило", Правило);
		Блокировка.Заблокировать();
		
		Для каждого ЗаписьКУдалению Из ЗаписиКУдалению Цикл
			
			Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, ЗаписьКУдалению);
			Запись.Удалить();
			
			ИзмененыЗаписи = Истина;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	КонецЕсли;
	
	Возврат ИзмененыЗаписи;
	
КонецФункции

// Определяет порядок платежа по налогу за конкретный период (налоговый, отчетный или иной период платежа).
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - организация-налогоплательщик
//  ВидНалога	 - ПеречислениеСсылка.ВидыНалогов - указывает на налог
//  Период		 - Дата - период, к которому относится платеж
//  ПроверятьУсловияТолькоНаГраницахОбзора - Булево - проверять некоторые правила только на границах интервала
// 
// Возвращаемое значение:
//  Структура    - описание события, соответствующего уплате налога за указанный период.
//                 Состав полей соответствует колонкам из НовыйРасписание()
//  Неопределено - нет возможности определить порядок платежа (например, налог не должен уплачиваться)
//
Функция ПорядокУплатыНалогаЗаПериод(Организация, ВидНалога, Период, ПроверятьУсловияТолькоНаГраницахОбзора = Ложь) Экспорт

	Порядок = ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(Организация, ВидНалога, Период,
		Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога, ПроверятьУсловияТолькоНаГраницахОбзора);
	
	Если Порядок <> Неопределено Тогда
		
		Если ТипЗнч(Порядок.Уплата) = Тип("ТаблицаЗначений") Тогда // для НДС уплата содержит все платежи
			Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Порядок.Уплата[0]);
		Иначе
			Возврат Порядок.Уплата;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

// Находит задачу по переданному хешу и возвращает заполненный ключ записи регистра.
// 
// Параметры:
//   ХешЗадачи - Строка - хеш-сумма задачи.
//
// Возвращаемое значение:
//   РегистрСведенийКлючЗаписи.ЗадачиБухгалтера, Неопределено - ключ записи регистра сведений или Неопределено,
//      если не найдена запись по указанной хеш-сумме.
//
Функция КлючЗадачиПоХешу(ХешЗадачи) Экспорт
	
	КлючЗаписи = Неопределено;
	
	Если ПустаяСтрока(ХешЗадачи) Тогда
		Возврат КлючЗаписи;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТаблицаКлючиЗадач = КлючиЗадач();
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ОператорЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	ОператорЗапроса.Источники.Добавить("РегистрСведений.ЗадачиБухгалтера", "ЗадачиБухгалтера");
	
	МассивИменКлючей = Новый Массив; // Используется для формирования структуры из ключей записи
	Для Каждого Колонка Из ТаблицаКлючиЗадач.Колонки Цикл
		ОператорЗапроса.ВыбираемыеПоля.Добавить("ЗадачиБухгалтера." + Колонка.Имя);
		МассивИменКлючей.Добавить(Колонка.Имя);
	КонецЦикла;
	
	ОператорЗапроса.Отбор.Добавить("ЗадачиБухгалтера.ХешЗадачи = &ХешЗадачи");
	Запрос.УстановитьПараметр("ХешЗадачи", ХешЗадачи);
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураКлюча = Новый Структура(СтрСоединить(МассивИменКлючей, ","));
		ЗаполнитьЗначенияСвойств(СтруктураКлюча, Выборка);
		КлючЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьКлючЗаписи(СтруктураКлюча);
	КонецЕсли;
	
	Возврат КлючЗаписи;
	
КонецФункции

// Возвращает сохраненную хеш-сумму задачи.
//
// Параметры:
//   ЗначенияКлючаЗаписи - Структура - структура с ключами из КлючиЗадач().
//
// Возвращаемое значение:
//   Строка - хеш-сумма задачи.
//
Функция СохраненнаяХешСуммаЗадачи(ЗначенияКлючаЗаписи) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначенияКлючаЗаписи);
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		Возврат МенеджерЗаписи.ХешЗадачи;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Получает все расписание по уплате налогов и сдаче отчетности по организации за указанный период.
//
// Параметры:
//  ОтборПравил  - Структура - см. Справочник.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения()
//                 Если в качестве начала интервала времени передать пустое значение, то это будет означать, что нужно
//                 учитывать только недавние события, задачи, связанные с которыми еще можно выполнить в срок.
// 
// Возвращаемое значение:
//   Расписание - см. НовыйРасписание() - коллекция с рассчитанным расписанием.
//
Функция РасписаниеПоНалогамОтчетамЗаПериод(ОтборПравил) Экспорт
	
	Расписание = НовыйРасписание();
	Расписание.Колонки.Добавить("ПеренестиНаРабочийДень", Новый ОписаниеТипов("Число"));
	Расписание.Колонки.Добавить("ПеренестиСрокНачалаНаРабочийДень", Новый ОписаниеТипов("Число"));
	
	ЗаполнитьРасписаниеПоНалогамИСборам(Расписание, ОтборПравил, Ложь);
	
	ПеренестиНаРабочиеДни(Расписание);
	
	Расписание.Колонки.Удалить("ПеренестиНаРабочийДень");
	Расписание.Колонки.Удалить("ПеренестиСрокНачалаНаРабочийДень");
	
	Расписание.ЗаполнитьЗначения(ОтборПравил.Организация, "Организация");
	
	Если ЗначениеЗаполнено(ОтборПравил.ТекущийПериод) Тогда
		УдалитьПросроченныеЗадачи(Расписание, ОтборПравил.ТекущийПериод);
	КонецЕсли;
	
	Возврат Расписание;
	
КонецФункции

Функция ПериодСобытияПоНалогамИСборам(Периодичность, ДатаПериода) Экспорт
	
	Возврат НачалоДня(ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, ДатаПериода));
	
КонецФункции

// Возвращает порядок предоставления отчета уплаты налога за период
//
// Параметры:
//  Организация                              - СправочникСсылка.Организации - Организация,
//                                             которую нужно установить в качестве параметра
//  ВидНалогаПравила                         - ПеречислениеСсылка.ВидыНалогов,
//                                             Структура:
//                                              * Задача - Строка - отбор по коду задачи
//                                              * Уплата - Строка - отбор по коду задачи
//                                             - Вид налога, для отбора правил
//  Период                                   - Дата - Период за который необходимо вернуть правила
//  Действие                                 - Перечисления.ВидыДействийКалендаряБухгалтера - 
//                                             действие для правила
//  ПроверятьУсловияТолькоНаГраницахОбзора   - Булево - флаг проверки условий на границах
// 
// Возвращаемое значение:
//   Структура:
//      *Отчет       - Структура - см. НовыйРасписание() - порядок предоставления отчетности
//      *Уплата      - Структура - см. НовыйРасписание() - порядок оплаты
//      *Уведомление - Структура - см. НовыйРасписание() - порядок предоставления уведолений
//
Функция ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(Организация, ВидНалогаПравила, Период, Действие = Неопределено, ПроверятьУсловияТолькоНаГраницахОбзора = Ложь, Знач Периодичность = Неопределено) Экспорт
	
	// Проверим корректность переданного периода.
	КорректныйПериод = Новый Структура;
	ЗадачиБухгалтераПереопределяемый.ЗаполнитьКорректныйПериодВводаДокументов(КорректныйПериод);
	Если Период > КорректныйПериод.КонецКорректногоПериода Тогда
		// На 9 лет вперед (и больше) исполняемые задачи по предоставлению отчетности и уплате налогов не планируются.
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяЗадачи = "";
	ЭтоНДС = Ложь;
	ОтборПравил = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения();
	ОтборПравил.ПроверятьУсловияТолькоНаГраницахОбзора = ПроверятьУсловияТолькоНаГраницахОбзора;

	Если ТипЗнч(ВидНалогаПравила) = Тип("ПеречислениеСсылка.ВидыНалогов") Тогда
		ИмяЗадачи = Неопределено;
		ЗадачиБухгалтераПереопределяемый.ПолучитьИмяЗадачиБухгалтера(ИмяЗадачи, ВидНалогаПравила, Организация, Период);
		// Признак для определения особого порядка уплаты.
		ЭтоНДС = (ВидНалогаПравила = Перечисления.ВидыНалогов.НДС);
		Если ЭтоНДС Тогда
			Периодичность = Перечисления.Периодичность.Квартал;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ВидНалогаПравила) = Тип("Структура") Тогда
		ИмяЗадачи = ВидНалогаПравила.Задача;
		
		ИмяПравилаОтчет  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВидНалогаПравила, "Отчет", "");
		ИмяПравилаУплата = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВидНалогаПравила, "Уплата", "");
		
		Если Не ПустаяСтрока(ИмяПравилаОтчет) Тогда
			ОтборПравил.Правила.Добавить(ИмяПравилаОтчет);
		КонецЕсли;
		Если Не ПустаяСтрока(ИмяПравилаУплата) Тогда
			ОтборПравил.Правила.Добавить(ИмяПравилаУплата);
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(ИмяЗадачи) Тогда
		ОтборПравил.ИмяЗадачи = ИмяЗадачи;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	Если ЗначениеЗаполнено(Действие) Тогда
		ОтборПравил.Действие = Действие;
	КонецЕсли;
	
	ДатаНачалаДеятельности = КалендарьБухгалтера.ДатаНачалаДеятельности(Организация);
	
	ОтборПравил.Организация     = Организация;
	ОтборПравил.НачалоИнтервала = НачалоГода(ДобавитьМесяц(Период, -6)) - 1;
	ОтборПравил.КонецИнтервала  = КонецМесяца(ДобавитьМесяц(Период, 6));
	
	Расписание = НовыйРасписание();
	// Служебная колонка, будет удалена в конце процедуры
	// Если значение ПеренестиНаРабочийДень = 1, то задача переносится на следующий рабочий день
	// Если значение ПеренестиНаРабочийДень = -1, то задача переносится на предыдущий рабочий день
	// Если значение ПеренестиНаРабочийДень = 0, то задача никуда не переносится
	Расписание.Колонки.Добавить("ПеренестиНаРабочийДень", Новый ОписаниеТипов("Число"));
	Расписание.Колонки.Добавить("ПеренестиСрокНачалаНаРабочийДень", Новый ОписаниеТипов("Число"));
	
	ОбновитьСписокОтчетов = (Действие <> Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога);
	
	ЗаполнитьРасписаниеПоНалогамИСборам(Расписание, ОтборПравил, ОбновитьСписокОтчетов);
	Расписание.Сортировать("ПериодСобытия Убыв, Срок Возр");
	
	Правила = ОбщегоНазначения.ВыгрузитьКолонку(Расписание, "Правило", Истина);
	ДействияПравил = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Правила, "Действие");
	
	ИндексСобытияОтчета         = Неопределено;
	ИндексСобытияУплаты         = Неопределено;
	ИндексСобытияУведомление    = Неопределено;
	// Уплата НДС производится 3-мя платежами, необходимо получить порядок уплаты по каждому.
	ИндексСобытийУплатыНДС = Новый Массив; 
	
	Для Индекс = 0 По Расписание.Количество() - 1 Цикл
		СтрокаРасписания = Расписание[Индекс];
		Если ЗначениеЗаполнено(ДатаНачалаДеятельности) И СтрокаРасписания.Срок < ДатаНачалаДеятельности Тогда
			// Задача попала в выборку, но не относится к организации
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(Периодичность) И СтрокаРасписания.Периодичность <> Периодичность Тогда
			// Если задана нужная периодичность, задачи с иной периодичностью не интересуют.
			Продолжить;
		КонецЕсли;
		Если ИсключитьЗадачуИзУплатыНалога(СтрокаРасписания.Правило, ВидНалогаПравила) Тогда
			// В одной задаче 2 подчиненных правила с разным порядком выполнения
			Продолжить;
		КонецЕсли;
		Если СтрокаРасписания.Срок > СтрокаРасписания.ПериодСобытия Тогда
			// Обязанность уплаты наступает после завершения периода.
			// Например, уплата по декларации.
			// Подходит самый поздний закончившийся период.
			Если СтрокаРасписания.ПериодСобытия <= НачалоДня(Период) Тогда
				ДействиеПравила = ДействияПравил[СтрокаРасписания.Правило];
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет
					И ИндексСобытияОтчета = Неопределено Тогда
					ИндексСобытияОтчета = Индекс;
				КонецЕсли;
				Если ЭтоНДС Тогда
					Если СтрокаРасписания.ПериодСобытия = НачалоДня(Период) Тогда 
						ИндексСобытийУплатыНДС.Добавить(Индекс);
					КонецЕсли;
				ИначеЕсли ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога 
					И ИндексСобытияУплаты = Неопределено Тогда
					ИндексСобытияУплаты = Индекс;
				КонецЕсли;
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление
					И ИндексСобытияУведомление = Неопределено Тогда
					ИндексСобытияУведомление = Индекс;
				КонецЕсли;
			ИначеЕсли СтрокаРасписания.Периодичность = Перечисления.Периодичность.День Тогда
				ДействиеПравила = ДействияПравил[СтрокаРасписания.Правило];
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет
					И ИндексСобытияОтчета = Неопределено Тогда
					ИндексСобытияОтчета = Индекс;
				КонецЕсли;
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога 
					И ИндексСобытияУплаты = Неопределено Тогда
					ИндексСобытияУплаты = Индекс;
				КонецЕсли;
			КонецЕсли;
		Иначе
			// Обязанность уплаты наступает до завершения периода.
			// Например, уплата авансового платежа.
			// Нужно найти самое раннее событие в пределах интервала выполнения.
			Если СтрокаРасписания.Срок >= НачалоДня(Период)
				И СтрокаРасписания.НачалоВыполнения <= НачалоДня(Период)
				Или ОбязанностьУплатыНаступаетДоОкончанияПериода(СтрокаРасписания.Срок, Период, СтрокаРасписания.Периодичность) Тогда
				ДействиеПравила = ДействияПравил[СтрокаРасписания.Правило];
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет Тогда
					ИндексСобытияОтчета = Индекс;
				КонецЕсли;
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога Тогда
					ИндексСобытияУплаты = Индекс;
				КонецЕсли;
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление Тогда
					ИндексСобытияУведомление = Индекс;
				КонецЕсли;
			ИначеЕсли СтрокаРасписания.Периодичность = Перечисления.Периодичность.День Тогда
				ДействиеПравила = ДействияПравил[СтрокаРасписания.Правило];
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет
					И ИндексСобытияОтчета = Неопределено Тогда
					ИндексСобытияОтчета = Индекс;
				КонецЕсли;
				Если ДействиеПравила = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога
					И ИндексСобытияУплаты = Неопределено Тогда
					ИндексСобытияУплаты = Индекс;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПеренестиНаРабочиеДни(Расписание);
	Расписание.Колонки.Удалить("ПеренестиНаРабочийДень");
	Расписание.Колонки.Удалить("ПеренестиСрокНачалаНаРабочийДень");
	
	Порядок = Новый Структура("Отчет, Уплата, Уведомление");
	
	Если ИндексСобытияОтчета <> Неопределено Тогда
		Порядок.Отчет = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Расписание[ИндексСобытияОтчета]);
	КонецЕсли;
	
	Если ЭтоНДС И ИндексСобытийУплатыНДС.Количество() = 3 Тогда // ожидаем получение порядка по всем платежам
		УплатаНДС = Расписание.СкопироватьКолонки();
		Для Каждого ИндексПлатежа ИЗ ИндексСобытийУплатыНДС Цикл
			СтрокаУплытыНДС = УплатаНДС.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаУплытыНДС, Расписание[ИндексПлатежа]);
		КонецЦикла;
		Порядок.Уплата = УплатаНДС;
	ИначеЕсли ИндексСобытияУплаты <> Неопределено Тогда
		Порядок.Уплата = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Расписание[ИндексСобытияУплаты]);
	КонецЕсли;
	
	Если ИндексСобытияУведомление <> Неопределено Тогда
		Порядок.Уведомление = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Расписание[ИндексСобытияУведомление]);
	КонецЕсли;
	
	Возврат Порядок;
	
КонецФункции

Функция ХешЗаписи(Запись, КлючиЗадач) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	
	Для Каждого Колонка Из КлючиЗадач.Колонки Цикл
		ХешированиеДанных.Добавить(XMLСтрока(Запись[Колонка.Имя]));
	КонецЦикла;
	
	Возврат СтрЗаменить(ХешированиеДанных.ХешСумма, " ", "");
	
КонецФункции

// Возвращает представление налогового органа для вывода в Списке задач и Календаре отчетности.
//
// Параметры:
//  КодКонтролирующегоОргана - Строка - код ИФНС.
//
Функция ПредставлениеНалоговогоОргана(КодКонтролирующегоОргана) Экспорт
	
	Свойства = Новый Структура("КодКонтролирующегоОргана, ВидКонтролирующегоОргана",
		КодКонтролирующегоОргана, Перечисления.ТипыКонтролирующихОрганов.ФНС);
	ПредставлениеКонтролирующегоОргана =
		РегламентированнаяОтчетностьВызовСервера.ПредставлениеКонтролирующегоОргана(Свойства);
	НалоговыйОрган = СтрШаблон(" (%1)", ПредставлениеКонтролирующегоОргана);
	
	Возврат НалоговыйОрган;
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Отправляет по электронной почте уведомления о задачах, срок выполнения которых наступает через 5 дней.
//
Процедура ОтправитьУведомленияПоЭлектроннойПочте() Экспорт
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если Не СписокЗадачУведомленияПоЭлектроннойПочте.ОтправлятьУведомления() Тогда
			Возврат;
		КонецЕсли;
		
		Если Не СписокЗадачУведомленияПоЭлектроннойПочте.КорректностьНастройки() Тогда
			Возврат;
		КонецЕсли;
		
		// В режиме сервиса сразу планируется очередное обновление задач бухгалтера
		КалендарьБухгалтера.ЗапланироватьОбновлениеЗадачБухгалтера();
		
		КоличествоСекундВСутках = 86400;
		КоличествоДней = 5;
		Интервал5Дней = КоличествоДней * КоличествоСекундВСутках;
		
		Период = НачалоДня(ТекущаяДатаСеанса()) + Интервал5Дней;
		
		ВыборкаЗадач = ВыбратьЗадачиПоСрокуВыполнения(Период);
		
		Если ВыборкаЗадач.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Кому = СписокЗадачУведомленияПоЭлектроннойПочте.Получатель();
		
		ШаблонПисьма = ПолучитьМакет("УведомлениеПоЭлектроннойПочте").ПолучитьТекст();
		
		ЗначенияПодстановки = Новый Структура;
		ЗначенияПодстановки.Вставить("Заголовок", УведомленияПоЭлектроннойПочтеЗаголовок());
		ЗначенияПодстановки.Вставить("Подзаголовок", УведомленияПоЭлектроннойПочтеПодзаголовок(Период));
		ЗначенияПодстановки.Вставить("Строки", УведомленияПоЭлектроннойПочтеСтроки(ВыборкаЗадач));
		ЗначенияПодстановки.Вставить("Кнопка", УведомленияПоЭлектроннойПочтеКнопка());
		ТекстПисьма = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонПисьма, ЗначенияПодстановки);
		
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("Кому", Кому);
		ПараметрыОтправки.Вставить("Тема", УведомленияПоЭлектроннойПочтеЗаголовок());
		ПараметрыОтправки.Вставить("Тело", ТекстПисьма);
		ПараметрыОтправки.Вставить("ТипТекста", "HTML");
		ПараметрыОтправки.Вставить("ОбрабатыватьТексты", Ложь);
		
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),
			ПараметрыОтправки);
	Исключение
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Уведомления о задачах по электронной почте'", КодОсновногоЯзыка),
			УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Отправляет по электронной почте уведомления о задачах СЭДО.
//
Процедура ОтправитьУведомленияСЭДОПоЭлектроннойПочте() Экспорт
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если Не СписокЗадачУведомленияПоЭлектроннойПочте.ОтправлятьУведомленияСЭДО()
			Или Не СписокЗадачУведомленияПоЭлектроннойПочте.КорректностьНастройки() Тогда
			Возврат;
		КонецЕсли;
		
		// В режиме сервиса сразу планируется очередное обновление задач бухгалтера
		КалендарьБухгалтера.ЗапланироватьОбновлениеЗадачБухгалтера();
		
		Период = НачалоДня(ТекущаяДатаСеанса());
		
		ВыборкаЗадачПоСрокам = ВыбратьЗадачиСЭДОТребующиеУведомления(Период);
		
		Если ВыборкаЗадачПоСрокам.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Кому = СписокЗадачУведомленияПоЭлектроннойПочте.Получатель();
		
		ШаблонПисьма = ПолучитьМакет("УведомлениеПоЭлектроннойПочте").ПолучитьТекст();
		
		ЗначенияПодстановки = Новый Структура;
		ЗначенияПодстановки.Вставить("Заголовок", УведомленияПоЭлектроннойПочтеЗаголовок());
		Если ВыборкаЗадачПоСрокам.Следующий() Тогда
			ЗначенияПодстановки.Вставить("Подзаголовок", УведомленияПоЭлектроннойПочтеПодзаголовок(ВыборкаЗадачПоСрокам.Срок));
			ВыборкаЗадач = ВыборкаЗадачПоСрокам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ЗначенияПодстановки.Вставить("Строки", УведомленияПоЭлектроннойПочтеСтроки(ВыборкаЗадач));
		КонецЕсли;
		ЗначенияПодстановки.Вставить("Кнопка", УведомленияПоЭлектроннойПочтеКнопка());
		ТекстПисьма = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонПисьма, ЗначенияПодстановки);
		
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("Кому", Кому);
		ПараметрыОтправки.Вставить("Тема", УведомленияПоЭлектроннойПочтеЗаголовок());
		ПараметрыОтправки.Вставить("Тело", ТекстПисьма);
		ПараметрыОтправки.Вставить("ТипТекста", "HTML");
		ПараметрыОтправки.Вставить("ОбрабатыватьТексты", Ложь);
		
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),
			ПараметрыОтправки);
	Исключение
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Уведомления о задачах СЭДО по электронной почте'", КодОсновногоЯзыка),
			УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Возвращает сумму, зачтенную с ЕНС, при выполнении задач по переданным правилам.
// Не включаются задачи с истекшим сроком уплаты, т.к. списание с ЕНС уже выполнено.
//
// Параметры:
//   Организация - СправочникСсылка.Организации - Организация, по которой нужно вычислить сумму зачета с ЕНС
//   Период      - Дата - Период за который необходимо вычсилить сумму зачета с ЕНС
//   Действие    - Перечисления.ВидыДействийКалендаряБухгалтера - Тип задачи (уплата налога, отчет и пр.)
//   Правила     - Массив (из СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов) - Список правил, по которым нужно вычислить сумму к зачету с ЕНС
//
// Возвращаемое значение:
//   Число
//
Функция СуммаЗачетаЕНС(Организация, ПериодСобытия, Действие, Правила) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ЗадачиБухгалтера.СуммаЗачетаЕНС), 0) КАК Сумма
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ЗадачиБухгалтера.Действие = &Действие
	|	И ЗадачиБухгалтера.Правило В(&Правила)
	|	И ЗадачиБухгалтера.Срок >= &ТекущаяДата");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодСобытия", НачалоДня(ПериодСобытия));
	Запрос.УстановитьПараметр("Действие", Действие);
	Запрос.УстановитьПараметр("Правила", Правила);
	Запрос.УстановитьПараметр("ТекущаяДата", ОбщегоНазначения.ТекущаяДатаПользователя());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].Сумма;
	
КонецФункции

// Определяет параметры правила задачи по уплате налогов и сдаче отчетности на основе данных в регистре сведений ЗадачиБухгалтера.
//
// Параметры:
//  ОтборПравил  - Структура - см. Справочник.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения()
// 
// Возвращаемое значение:
//   Структура - см. ВыполнениеЗадачБухгалтераКлиентСервер.НовыйПараметрыФормыПомощника()
//   Неопределено - нет данных по задаче
//
Функция ПараметрыПравилаИзСуществующихЗадачБухгалтера(ОтборПравил) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачиБухгалтера.Наименование КАК Описание,
		|	ЗадачиБухгалтера.Правило КАК Правило,
		|	ЗадачиБухгалтера.Правило.Владелец.Код КАК ИдентификаторЗадачи,
		|	ЗадачиБухгалтера.Правило.ЕстьИнформацияНаИТС КАК ЕстьИнформацияНаИТС,
		|	ЗадачиБухгалтера.Срок КАК Срок,
		|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия
		|ИЗ
		|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|ГДЕ
		|	ЗадачиБухгалтера.Организация = &Организация
		|	И ЗадачиБухгалтера.Действие = &Действие
		|	И ЗадачиБухгалтера.Правило.Владелец.Код = &ИмяЗадачи
		|	И ЗадачиБухгалтера.ПериодСобытия МЕЖДУ &НачалоИнтервала И &КонецИнтервала
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодСобытия УБЫВ";
	
	Запрос.УстановитьПараметр("Организация",     ОтборПравил.Организация);
	Запрос.УстановитьПараметр("ИмяЗадачи",       ОтборПравил.ИмяЗадачи);
	Запрос.УстановитьПараметр("Действие",        ОтборПравил.Действие);
	Запрос.УстановитьПараметр("НачалоИнтервала", ОтборПравил.НачалоИнтервала);
	Запрос.УстановитьПараметр("КонецИнтервала",  ОтборПравил.КонецИнтервала);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыПравила = ВыполнениеЗадачБухгалтераКлиентСервер.НовыйПараметрыФормыПомощника();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ПараметрыПравила, Выборка);
	
	Возврат ПараметрыПравила;
	
КонецФункции

// см. ДокументооборотСКОПереопределяемый.ОбновитьИЗаполнитьТребованияФНС
Процедура ОбновитьИЗаполнитьТребованияФНС(Организация, Дата) Экспорт
	
	НачалоИнтервала = Дата;
	ТекущаяДата  = НачалоДня(ТекущаяДатаСеанса());
	// Срок отправки ответа на требование может максимально составлять 30 рабочих дней (п.4.6 ст. 83 НК РФ) + 6 рабочих дней (п. 8 ст. 1  N 259-ФЗ),
	// поэтому добавляем два месяца для обновления задач по требованиям ФНС.
	КонецИнтервала = ДобавитьМесяц(ТекущаяДата, 2);
		
	Расписание = НовыйРасписание();
	ЗаполнитьРасписаниеТребованияФНС(
		Организация,
		Расписание,
		НачалоИнтервала,
		КонецИнтервала);
	
	ОтборДействие = Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС;
	ЗаписатьИзменения(
		Организация,
		Расписание,
		НачалоИнтервала,
		,
		ОтборДействие);
	
КонецПроцедуры

// Обновляет уже созданную или создает новую задачу бухгалтера для документа СЭДО
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ВходящийЗапросФССДляРасчетаПособия,
//                   ДокументСсылка.ИзвещениеФСС,
//                   ДокументСсылка.ОтказВВозмещенииВыплатРодителямДетейИнвалидов,
//                   ДокументСсылка.УведомлениеОСтатусеВыплатыПособия - ссылка на документ СЭДО
//
Процедура ОбновитьИЗаполнитьДокументСЭДО(ДокументСсылка) Экспорт
	
	Расписание = НовыйРасписание();
	ЗаполнитьЗаписьРасписанияДокументСЭДО(Расписание, ДокументСсылка);
	
	Если Расписание.Количество() > 0 Тогда
		ОтборДействие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО;
		ЗаписатьИзменения(
			Расписание[0].Организация,
			Расписание,
			Расписание[0].Срок,
			,
			ОтборДействие);
			
		ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачиДокументСЭДО(ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет расписание по требованиям ФНС. Отбираем требования, которые нужно подтвердить либо дать ответ в интервале НачалоОбзора - КонецОбзора.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - организация
//  Расписание	 - ТаблицаЗначений - см. НовыйРасписание()
//  НачалоОбзора - Дата - дата начала обзора
//  КонецОбзора	 - Дата - дата конца обзора
//  ПроверятьПодключена1СОтчетность - Булево - выполнять проверку подключения 1С Отчетности
//
Процедура ЗаполнитьРасписаниеТребованияФНС(Организация, Расписание, НачалоОбзора, КонецОбзора, ПроверятьПодключена1СОтчетность = Истина) Экспорт
	
	Если ПроверятьПодключена1СОтчетность И Не ИнтерфейсыВзаимодействияБРО.ОрганизацияИмеетУчетнуюЗапись(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	СписокТребований = ТребованияФНС.НерасшифрованныеТребованияФНС(Организация, НачалоОбзора, КонецОбзора);
	
	Для Каждого ТранспортноеСообщение Из СписокТребований Цикл
		СрокТребования = ТребованияФНС.СрокНерасшифрованногоТребования(ТранспортноеСообщение);
		Если СрокТребования < НачалоОбзора Или СрокТребования > КонецОбзора Тогда
			Продолжить;
		КонецЕсли;
		НоваяЗапись = Расписание.Добавить();
		НоваяЗапись.Организация = Организация;
		НоваяЗапись.Правило = ТранспортноеСообщение;
		НоваяЗапись.Наименование = НСтр("ru = 'Требование ФНС'");
		НоваяЗапись.НаименованиеСокращенное = НСтр("ru = 'Требование ФНС'");
		НоваяЗапись.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС;
		НоваяЗапись.ПериодСобытия = ТранспортноеСообщение.Дата;
		НоваяЗапись.НачалоВыполнения = ТранспортноеСообщение.Дата;
		НоваяЗапись.Срок = СрокТребования;
	КонецЦикла;
	
	РезультатЗапроса = ТребованияФНС.ВыбратьТребованияФНС(Организация, НачалоОбзора, КонецОбзора);
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НаименованиеСокращенное = НСтр("ru = 'Требование ФНС'");
		Если ЗначениеЗаполнено(Выборка.ИсточникОтчета) Тогда
			НаименованиеНалога = РегламентированнаяОтчетностьПовтИсп.НаименованиеНалогаПоРеглОтчету(Выборка.ИсточникОтчета);
			Если ЗначениеЗаполнено(НаименованиеНалога) Тогда
				НаименованиеСокращенное = СтрШаблон(
					НСтр("ru = 'Требование ФНС (%1)'"), НаименованиеНалога);
			КонецЕсли;
		КонецЕсли;
		
		// Определяем так же, как и в см. ТребованияФНСВызовСервера.ЭДООбязателенПоСсылке
		ЭДООбязателен = ТребованияФНСВызовСервера.ПолученоПозже5февраля2025(Выборка.Правило) И Выборка.ОтветилиНаВопросПроЭДО И Выборка.ЭДООбязателен;
		Если Не ЭДООбязателен Тогда
			// Необходимо подтверждать прием для следующих требований:
			// - для всех требований полученных до 05.02.2025г.;
			// - для требований полученных начиная с 05.02.2025г., если для организации не установлен обязательный электронный документооборот.
			Если Выборка.ПодтвердитьДо >= НачалоОбзора И Выборка.ПодтвердитьДо <= КонецОбзора Тогда
				НоваяЗапись = Расписание.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка); 
				НоваяЗапись.НаименованиеСокращенное = НаименованиеСокращенное;
				// В случае, если дата отправки требования совпадает с датой подтверждения, запишем в ПериодСобытия дату на день раньше,
				// чтобы избежать записей с одинаковыми ключевыми полями.
				НоваяЗапись.ПериодСобытия = ?(Выборка.Дата = Выборка.ДатаПодтверждения, Выборка.Дата - 60 * 60 * 24, Выборка.Дата);
				НоваяЗапись.НачалоВыполнения = Выборка.Дата;
				НоваяЗапись.Срок = Выборка.ПодтвердитьДо;
			КонецЕсли;
		КонецЕсли;
		
		Если ЭДООбязателен Или Выборка.СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПриемПодтвержден Тогда
			Если Выборка.ТребуетОтвета И Выборка.ОтветитьДо >= НачалоОбзора И Выборка.ОтветитьДо <= КонецОбзора Тогда
				НоваяЗапись = Расписание.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				НоваяЗапись.НаименованиеСокращенное = НаименованиеСокращенное;
				ПериодСобытия = ?(ЭДООбязателен, Выборка.Дата, Выборка.ДатаПодтверждения);
				НоваяЗапись.ПериодСобытия = ПериодСобытия;
				НоваяЗапись.НачалоВыполнения = ПериодСобытия;
				ДатаОтвета = ?(ЗначениеЗаполнено(Выборка.ДатаОтветаИзмененная), Выборка.ДатаОтветаИзмененная, Выборка.ОтветитьДо);
				НоваяЗапись.Срок = ДатаОтвета;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет расписание по документам СЭДО за период
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - организация
//  Расписание    - ТаблицаЗначений - см. НовыйРасписание()
//  НачалоПериода - Дата - дата начала периода
//  КонецПериода  - Дата - дата конца периода
//
Процедура ЗаполнитьРасписаниеДокументыСЭДО(Организация, Расписание, НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыПредставленияДокументовСЭДО(Запрос);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	
	ЗадачиБухгалтераПереопределяемый.ЗаполнитьРасписаниеДокументыСЭДО(Запрос, Расписание);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СоставлениеРасписания

// Рассчитывает расписание задач, связанных с уплатой налогов, сборов, представлению отчетов.
//
// Параметры:
//  Расписание   - см. НовыйРасписание() - коллекция, которая будет дополнена рассчитанным расписанием
//  ОтборПравил  - Структура - см. Справочник.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения()
//                 Если в качестве начала интервала времени передать пустое значение, то это будет означать, что нужно
//                 учитывать только недавние события, задачи, связанные с которыми еще можно выполнить в срок.
//  Правила      - см. Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НовыйТаблицаПравил() - 
//                 правила, которые должна выполнять организация в периоде обзора.
//                 Можно передать Неопределено, тогда правила будут определены в процедуре.
//  ОбновитьСписокОтчетов - Булево - если Истина, то дополнить список избранных отчетов
//
Процедура ЗаполнитьРасписаниеПоНалогамИСборам(Расписание, ОтборПравил, ОбновитьСписокОтчетов = Истина)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; // Хранит ПравилаУплатыНалоговНаМестах
	Организация = ОтборПравил.Организация;
	
	Правила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НовыйПравилаОрганизации();
	Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ЗаполнитьПравилаОрганизации(
		Правила, ОтборПравил, МенеджерВременныхТаблиц);

	Если Не ЗначениеЗаполнено(Правила) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПродленияСроков = Новый Структура;
	ЗадачиБухгалтераПереопределяемый.ПолучитьПараметрыПродленияСроков(ПараметрыПродленияСроков, Организация);
	
	ОсобыеСрокиСверкиИмущественныхНалогов = Неопределено;
	ЗадачиБухгалтераПереопределяемый.ПолучитьОсобыеСрокиСверкиИмущественныхНалогов(ОсобыеСрокиСверкиИмущественныхНалогов, Организация);
	
	ЗадачиБухгалтераПереопределяемый.СоздатьПравилоВыплатаЗарплатыУплатаНДФЛ(МенеджерВременныхТаблиц, Организация, ОтборПравил);
		
	// Правила: Правило, ФинансовыйПериод
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Правила", Правила);
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрацияВНалоговомОргане"));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Правила.Правило КАК Правило,
	|	Правила.ФинансовыйПериод КАК ФинансовыйПериод
	|ПОМЕСТИТЬ Правила
	|ИЗ
	|	&Правила КАК Правила
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Правила.Правило КАК Правило,
	|	Правила.ФинансовыйПериод КАК ФинансовыйПериод,
	|	МАКСИМУМ(ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.ПериодДействияПравила, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодДействияПравила,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане, &ОсновнойНалоговыйОрган) КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ МестныеПравила
	|ИЗ
	|	Правила КАК Правила
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаУплатыНалоговНаМестах КАК ПравилаУплатыНалоговНаМестах
	|		ПО Правила.Правило = ПравилаУплатыНалоговНаМестах.Правило
	|			И Правила.ФинансовыйПериод >= ПравилаУплатыНалоговНаМестах.ПериодДействияПравила
	|
	|СГРУППИРОВАТЬ ПО
	|	Правила.ФинансовыйПериод,
	|	Правила.Правило,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане, &ОсновнойНалоговыйОрган)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило,
	|	ПериодДействияПравила,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПравилаУплатыНалоговНаМестах.СрокДней КАК СрокДней,
	|	ПравилаУплатыНалоговНаМестах.СрокМесяцев КАК СрокМесяцев,
	|	ПравилаУплатыНалоговНаМестах.Правило КАК Правило
	|ПОМЕСТИТЬ ПравилаУплатыНДФЛ
	|ИЗ
	|	ПравилоУплатыНДФЛНалоговымАгентом КАК ПравилаУплатыНалоговНаМестах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МестныеПравила.ФинансовыйПериод КАК НачалоФинансовогоПериода,
	|	Регламент.Ссылка КАК Ссылка,
	|	Регламент.Владелец КАК Владелец,
	|	ПРЕДСТАВЛЕНИЕ(Регламент.Владелец) КАК ВладелецПредставление,
	|	Регламент.Описание КАК Описание,
	|	Регламент.Действие КАК Действие,
	|	Регламент.ФинансовыйПериод КАК ФинансовыйПериод,
	|	Регламент.Периодичность КАК Периодичность,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.ОграничениеПериода, Регламент.ОграничениеПериода) КАК ОграничениеПериода,
	|	ВЫБОР
	|		КОГДА ПравилаУплатыНДФЛ.СрокМесяцев ЕСТЬ NULL
	|				И ПравилаУплатыНалоговНаМестах.СрокМесяцев ЕСТЬ NULL
	|			ТОГДА Регламент.СрокМесяцев
	|		ИНАЧЕ ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.СрокМесяцев, ПравилаУплатыНДФЛ.СрокМесяцев)
	|	КОНЕЦ КАК СрокМесяцев,
	|	ВЫБОР
	|		КОГДА ПравилаУплатыНДФЛ.СрокДней ЕСТЬ NULL
	|				И ПравилаУплатыНалоговНаМестах.СрокДней ЕСТЬ NULL
	|			ТОГДА Регламент.СрокДней
	|		ИНАЧЕ ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.СрокДней, ПравилаУплатыНДФЛ.СрокДней)
	|	КОНЕЦ КАК СрокДней,
	|	Регламент.ПеренестиНаРабочийДень КАК ПеренестиНаРабочийДень,
	|	Регламент.БазовыйПериод КАК БазовыйПериод,
	|	Регламент.ОтставаниеБазовогоПериода КАК ОтставаниеБазовогоПериода,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане, ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)) КАК РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.Код, ""0000"") КАК КодКонтролирующегоОргана,
	|	Регламент.СчитатьСрокВРабочихДнях КАК СчитатьСрокВРабочихДнях,
	|	Регламент.ИсключатьПериодИзПредставленияЗадачи КАК ИсключатьПериодИзПредставленияЗадачи,
	|	Регламент.Код КАК ИдентификаторПравила,
	|	Регламент.Владелец.Код КАК ИдентификаторЗадачи,
	|	Регламент.НачальныйСрокМесяцев КАК НачальныйСрокМесяцев,
	|	Регламент.НачальныйСрокДней КАК НачальныйСрокДней,
	|	Регламент.ПеренестиСрокНачалаНаРабочийДень КАК ПеренестиСрокНачалаНаРабочийДень,
	|	Регламент.ИсключатьИмяРодительскойЗадачиИзПредставленияЗадачи КАК ИсключатьИмяРодительскойЗадачиИзПредставленияЗадачи,
	|	Регламент.ВыполняетсяЕдинымПомощником КАК ВыполняетсяЕдинымПомощником,
	|	Регламент.ОснованияПродленияСроков.(
	|		ОснованиеПродленияСроковНалоговОтчетов КАК ОснованиеПродленияСроковНалоговОтчетов
	|	) КАК ОснованияПродленияСроков,
	|	Регламент.КонецДействия КАК КонецДействияПравила
	|ИЗ
	|	МестныеПравила КАК МестныеПравила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Регламент
	|		ПО МестныеПравила.Правило = Регламент.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаУплатыНалоговНаМестах КАК ПравилаУплатыНалоговНаМестах
	|		ПО МестныеПравила.Правило = ПравилаУплатыНалоговНаМестах.Правило
	|			И МестныеПравила.ПериодДействияПравила = ПравилаУплатыНалоговНаМестах.ПериодДействияПравила
	|			И МестныеПравила.РегистрацияВНалоговомОргане = ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО МестныеПравила.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаУплатыНДФЛ КАК ПравилаУплатыНДФЛ
	|		ПО МестныеПравила.Правило = ПравилаУплатыНДФЛ.Правило
	|ГДЕ
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.Включено, ИСТИНА)";
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// "Релевантным" называется период, к которому относится действие
		// Составим перечень таких периодов
		РелевантныеПериоды = Новый Массив;
		// Для каждого релевантного периода определим дату, когда задачи за этот период появятся в списке
		НачалоВыполнения   = Новый Соответствие;
		
		// Часто задачу требуется выполнять после окончания периода, к которому она относится. 
		// Например: налог за январь платим в феврале.
		// В таких случаях часто базовый и релевантный период совпадают:
		// налог за январь платим по данным января.
		// Это - простые правила.
		// 
		// Кроме них есть и более сложные, когда базовый и релевантный период не совпадают.
		// Сложности, специфика начинаются только тогда, когда речь идет о разного рода 
		// _прогнозах_ и некоторых случаях _авансов_.
		// В большинстве (но не во всех) сложных правилах 
		// срок, отведенный на выполнение задачи, истекает до окончания периода, к которому она относится: 
		// аванс за январь платим в январе.
		
		СобытиеПозжеРелевантногоПериода = СобытиеПозжеРелевантногоПериода(Выборка);
		ПростойБазовыйПериод            = КалендарьБухгалтера.БазовыйПериодОпределяетсяПросто(Выборка);
		
		КонецФинансовогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Выборка.ФинансовыйПериод, Выборка.НачалоФинансовогоПериода);
		
		Если СобытиеПозжеРелевантногоПериода И ПростойБазовыйПериод Тогда
			
			// Даты начала выполнения будут лежать в интервале:
			НачалоИнтервала = Макс(Выборка.НачалоФинансовогоПериода, ОтборПравил.НачалоИнтервала);
			
			Если ЗначениеЗаполнено(Выборка.КонецДействияПравила) И Выборка.КонецДействияПравила < ОтборПравил.КонецИнтервала Тогда
				КонецИнтервала = Мин(КонецФинансовогоПериода, КонецДня(Выборка.КонецДействияПравила));
			Иначе
				КонецИнтервала = Мин(КонецФинансовогоПериода, КонецДня(ОтборПравил.КонецИнтервала));
			КонецЕсли;
			
			Если НачалоИнтервала > КонецИнтервала Тогда
				Продолжить;
			КонецЕсли;
			
			РелевантныеПериоды = ЗавершенныеИнтервалы(
				НачалоИнтервала,
				КонецИнтервала,
				Выборка.Периодичность);
			
			Для Каждого НачалоПериода Из РелевантныеПериоды Цикл
				КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Выборка.Периодичность, НачалоПериода);
				НачалоВыполнения.Вставить(НачалоПериода, КонецПериода + 1);
			КонецЦикла;
				
		Иначе 
			
			// Базовый и релевантный периоды не совпадают.
			// Базовый период включает в себя один или несколько релевантных.
			Если ПростойБазовыйПериод Тогда
				БазовыйПериод = Выборка.Периодичность;
				ОтставаниеБазовогоПериода = 0;
			Иначе
				БазовыйПериод = Выборка.БазовыйПериод;
				ОтставаниеБазовогоПериода = Выборка.ОтставаниеБазовогоПериода;
			КонецЕсли;
			
			// Базовый период всегда предшествует сроку
			КалендарьБухгалтера.УточнитьОтставаниеБазовогоПериода(
				ОтставаниеБазовогоПериода, 
				БазовыйПериод, 
				Выборка.СрокМесяцев);
			
			// Интервал базовых периодов также не совпадает - опережает соответствующий финансовый период
			ОпережениеРелевантногоПериода = 1 + ОтставаниеБазовогоПериода;
			НачалоИнтервалаБазовыхПериодов = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
				Выборка.НачалоФинансовогоПериода,
				БазовыйПериод,
				- ОпережениеРелевантногоПериода);
			// Для расчета конца интервала нельзя прибавлять месяцы к дате конца другого периода,
			// так как в разных месяцах разное количество дней.
			// Поэтому конец интервала рассчитываем через начало следующего интервала.
			НачалоСледующегоФинансовогоПериода = КонецФинансовогоПериода + 1;
			НачалоИнтервалаСледующегоЗаИнтерваломБазовыхПериодов = 
				ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
					НачалоСледующегоФинансовогоПериода,
					БазовыйПериод,
					- ОпережениеРелевантногоПериода);
			КонецИнтервалаБазовыхПериодов = НачалоИнтервалаСледующегоЗаИнтерваломБазовыхПериодов - 1;
				
			// Даты окончания базовых периодов будут лежать в интервале:
			НачалоИнтервалаБазовыхПериодов = Макс(НачалоИнтервалаБазовыхПериодов, ОтборПравил.НачалоИнтервала);
			КонецИнтервалаБазовыхПериодов  = Мин(КонецИнтервалаБазовыхПериодов, КонецДня(ОтборПравил.КонецИнтервала));
			
			Если НачалоИнтервалаБазовыхПериодов > КонецИнтервалаБазовыхПериодов Тогда
				Продолжить;
			КонецЕсли;
			
			// Составим перечень базовых периодов, которые завершились внутри интервала
			ЗавершенныеБазовыеПериоды = ЗавершенныеИнтервалы(
				НачалоИнтервалаБазовыхПериодов,
				КонецИнтервалаБазовыхПериодов,
				БазовыйПериод);
				
			ОтношениеБазовогоПериодаИРелевантного = 1;
			Если БазовыйПериод <> Выборка.Периодичность Тогда
				ПроизвольнаяДата = '0001-01-01';
				ДлинаБазовогоПериода     = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(ПроизвольнаяДата, БазовыйПериод)         - ПроизвольнаяДата;
				ДлинаРелевантногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(ПроизвольнаяДата, Выборка.Периодичность) - ПроизвольнаяДата;
				Если ДлинаРелевантногоПериода <> 0 Тогда
					ОтношениеБазовогоПериодаИРелевантного = ДлинаБазовогоПериода / ДлинаРелевантногоПериода;
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого НачалоБазовогоПериода Из ЗавершенныеБазовыеПериоды Цикл
				
				КонецБазовогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(БазовыйПериод, НачалоБазовогоПериода);
				
				// Рассчитаем релевантный период.
				// Этот период всегда позже базового и, как правило (но не всегда), следует непосредственно за ним.
				РелевантныйПериод = КонецБазовогоПериода + 1;
				Если ОтставаниеБазовогоПериода > 0 Тогда
					РелевантныйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
						РелевантныйПериод, 
						БазовыйПериод,
						ОтставаниеБазовогоПериода);
				КонецЕсли;
				
				НачалоДиапазонаРелевантныхПериодов = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(БазовыйПериод, РелевантныйПериод);
				КонецДиапазонаРелевантныхПериодов  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(БазовыйПериод, РелевантныйПериод);
				
				Если ОтношениеБазовогоПериодаИРелевантного <= 1 Тогда
					
					// Одному базовому периоду соответствует один релевантный
					// Они не совпадают по датам (базовый раньше релевантного - прогноз на февраль по данным января) 
					// и могут не совпадать по длине (прогноз на следующий год по данным 9 месяцев)
					РелевантныеПериоды.Добавить(НачалоДиапазонаРелевантныхПериодов);
					НачалоВыполнения.Вставить(НачалоДиапазонаРелевантныхПериодов, КонецБазовогоПериода + 1);
					
				ИначеЕсли ОтношениеБазовогоПериодаИРелевантного > 1 Тогда
					
					// Базовому периоду соответствует несколько релевантных
					// Например, 
					// - оплата за 1 полугодие по результатам прошлого года
					// - в январе, феврале и марте платим по 1/3 платежа за 4 квартал
					
					ПериодыВнутриБазового = ЗавершенныеИнтервалы(
						НачалоДиапазонаРелевантныхПериодов,
						КонецДиапазонаРелевантныхПериодов,
						Выборка.Периодичность);
						
					Для Каждого НачалоПериода Из ПериодыВнутриБазового Цикл
						РелевантныеПериоды.Добавить(НачалоПериода);
						НачалоВыполнения.Вставить(НачалоПериода, КонецБазовогоПериода + 1);
					КонецЦикла;
					
				КонецЕсли; 
			КонецЦикла;	// По базовым периодам
		КонецЕсли;
		
		// Исключим запрещенные периоды
		Если Не ПустаяСтрока(Выборка.ОграничениеПериода) Тогда
			
			// Ограничение периодов применяется к релевантным периодам, но не к базовым!
			
			Если Выборка.ФинансовыйПериод = Выборка.Периодичность Тогда
				// Особый случай - ограничение задается в номерах периодов в течение года.
				ПериодОграничения = Перечисления.Периодичность.Год;
			Иначе
				ПериодОграничения = Выборка.ФинансовыйПериод;
			КонецЕсли;
				
			ЗапрещенныеПериодыПоФинансовым = Новый Соответствие;
			РазмерМассива = РелевантныеПериоды.Количество();
			Для НомерСКонца = 1 По РазмерМассива Цикл
				Индекс = РазмерМассива - НомерСКонца;
				Период = РелевантныеПериоды[Индекс];
				ФинансовыйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ПериодОграничения, Период);
				ЗапрещенныеПериоды = ЗапрещенныеПериодыПоФинансовым[ФинансовыйПериод];
				Если ЗапрещенныеПериоды = Неопределено Тогда
					КонецФинансовогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПериодОграничения, Период);
					ЗапрещенныеПериоды = ЗапрещенныеИнтервалы(
						ФинансовыйПериод, 
						КонецФинансовогоПериода, 
						Выборка.Периодичность, 
						Выборка.ОграничениеПериода);
					ЗапрещенныеПериодыПоФинансовым.Вставить(ФинансовыйПериод, ЗапрещенныеПериоды);
				КонецЕсли;
				Если ЗапрещенныеПериоды.Найти(Период) <> Неопределено Тогда
					РелевантныеПериоды.Удалить(Индекс);
				КонецЕсли;
			КонецЦикла;
				
		КонецЕсли;
		
		// Заполним перечень событий
		Для Каждого НачалоПериода Из РелевантныеПериоды Цикл
			
			ПериодСобытия = ПериодСобытияПоНалогамИСборам(Выборка.Периодичность, НачалоПериода);
			
			СтрокаРасписания = Расписание.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаРасписания, 
				ОписаниеДействия(Выборка, ПериодСобытия, Организация, ПараметрыПродленияСроков, ОсобыеСрокиСверкиИмущественныхНалогов));
			
			СтрокаРасписания.НачалоВыполнения = НачалоВыполнения[НачалоПериода];
			
		КонецЦикла;
		
	КонецЦикла; // Выборка правил
	
	Если ОбновитьСписокОтчетов Тогда
		ЗадачиБухгалтераПереопределяемый.ДополнитьИзбранныеРегламентированныеОтчеты(Правила, Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗаписьРасписанияДокументСЭДО(Расписание, ДокументСсылка)

	Запрос = Новый Запрос;
	ЗаполнитьПараметрыПредставленияДокументовСЭДО(Запрос);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	ЗадачиБухгалтераПереопределяемый.ЗаполнитьЗаписьРасписанияДокументСЭДО(Запрос, Расписание);
	
КонецПроцедуры

Процедура ПеренестиНаРабочиеДни(Расписание)

	// Перенесем на рабочие дни
	ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	
	// Если производственный календарь не заполнен - то ничего не делаем
	Если НЕ ЗначениеЗаполнено(ПроизводственныйКалендарь) Тогда
		Возврат;
	КонецЕсли; 
	
	ВариантыПереноса = Новый Соответствие;
	ВариантыПереноса.Вставить(1,  Ложь);
	ВариантыПереноса.Вставить(-1, Истина);
	
	ВариантыУчетаНерабочихДней = Новый Массив;
	ВариантыУчетаНерабочихДней.Добавить(Истина); // Считать нерабочие дни нерабочими
	ВариантыУчетаНерабочихДней.Добавить(Ложь);
	
	ПараметрыПолученияРабочихДат = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат();
	ПараметрыПолученияРабочихДат.ВызыватьИсключение = Ложь;
	
	ОтборСтрок = Новый Структура("ПеренестиНаРабочийДень,УчитыватьНерабочиеДни");
	Для Каждого ВариантПереноса Из ВариантыПереноса Цикл
		
		ОтборСтрок.ПеренестиНаРабочийДень = ВариантПереноса.Ключ;
		
		ПараметрыПолученияРабочихДат.ПолучатьПредшествующие = ВариантПереноса.Значение;
		
		Для Каждого ВариантУчетаНерабочихДней Из ВариантыУчетаНерабочихДней Цикл
			
			ОтборСтрок.УчитыватьНерабочиеДни = ВариантУчетаНерабочихДней;
			
			ПараметрыПолученияРабочихДат.УчитыватьНерабочиеПериоды = ВариантУчетаНерабочихДней;
			
			Интервалы = Расписание.НайтиСтроки(ОтборСтрок);
			
			ИсходныеДаты = ОбщегоНазначения.ВыгрузитьКолонку(Интервалы, "Срок", Истина);
			
			ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
				ПроизводственныйКалендарь,
				ИсходныеДаты,
				ПараметрыПолученияРабочихДат);
			
			Для Каждого Интервал Из Интервалы Цикл
				
				ДатаПереноса = ГрафикПереноса[Интервал.Срок];
				Если ЗначениеЗаполнено(ДатаПереноса) Тогда
					Интервал.Срок = ДатаПереноса;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
	// Перенесем так же сроки начала сдачи отчетов
	ОтборСтрок = Новый Структура("ПеренестиСрокНачалаНаРабочийДень");
	Для Каждого ВариантПереноса Из ВариантыПереноса Цикл
		
		ОтборСтрок.ПеренестиСрокНачалаНаРабочийДень = ВариантПереноса.Ключ;
		
		ПараметрыПолученияРабочихДат.ПолучатьПредшествующие = ВариантПереноса.Значение;
		
		ПараметрыПолученияРабочихДат.УчитыватьНерабочиеПериоды = Истина;
		
		Интервалы = Расписание.НайтиСтроки(ОтборСтрок);
		
		ИсходныеДаты = ОбщегоНазначения.ВыгрузитьКолонку(Интервалы, "НачальныйСрок", Истина);
		ПустойНачальныйСрок = ИсходныеДаты.Найти(Дата(1, 1, 1));
		Если ПустойНачальныйСрок <> Неопределено Тогда
			ИсходныеДаты.Удалить(ПустойНачальныйСрок);
		КонецЕсли;
		
		ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
			ПроизводственныйКалендарь,
			ИсходныеДаты,
			ПараметрыПолученияРабочихДат);
		
		Для Каждого Интервал Из Интервалы Цикл
			
			ДатаПереноса = ГрафикПереноса[Интервал.НачальныйСрок];
			Если ЗначениеЗаполнено(ДатаПереноса) Тогда
				Интервал.НачальныйСрок = ДатаПереноса;
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьПросроченныеЗадачи(Расписание, ТекущаяДата, УчитыватьОтсроченнуюРегистрацию = Ложь)

	КоличествоСтрок = Расписание.Количество();
	Для НомерСтроки = 1 По КоличествоСтрок Цикл
		Индекс            = КоличествоСтрок - НомерСтроки;
		ЭлементРасписания = Расписание[Индекс];
		
		ПоправкаПериода = 0;
		Если УчитыватьОтсроченнуюРегистрацию И ЗадачаДопускающаяОтсроченнуюРегистрацию(ЭлементРасписания.Правило) Тогда
			День = 24 * 60 * 60;
			ПоправкаПериода = КалендарьБухгалтера.УпреждениеЗаполненияСписка() * День;
		КонецЕсли;
		
		Если ЭлементРасписания.Срок < ТекущаяДата - ПоправкаПериода Тогда
			Расписание.Удалить(Индекс);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция ЗадачаДопускающаяОтсроченнуюРегистрацию(Правило)
	
	// Условия выполнения правила могут возникнуть день в день или с задержкой.
	// Например при внешней зарплате сведения для регистрации НДФЛ загружают после предельной даты
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	Если ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2023_НДФЛ_Уведомление_ЕдиныйПомощник"
		Или ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2023_НДФЛ_Уведомление_Декабрь"
		Или ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2024_НДФЛ_Уведомление_КонецМесяца"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_ЕдиныйПомощник"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_Декабрь"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2024_НДФЛ_Уплата_КонецМесяца" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСписокЗадачПоПравилу(Организация, Правило, ТолькоАктуальные)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Правило",     Правило);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Правило = &Правило";
	
	
	Если ТолькоАктуальные Тогда
		Запрос.Текст = ТекстЗапроса + " И ЗадачиБухгалтера.Срок >= &ТекущаяДата И НЕ ЗадачиБухгалтера.ВАрхиве";
	Иначе
		Запрос.Текст = ТекстЗапроса;
	КонецЕсли; 
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Составим набор интервалов, которые завершились в периоде обзора
Функция ЗавершенныеИнтервалы(НачалоРасписания, КонецРасписания, Периодичность)
	
	ЗавершенныеИнтервалы = Новый Массив;
	
	КонецКрайнегоИнтервала = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, КонецРасписания + 1) - 1;
	
	Пока КонецКрайнегоИнтервала >= НачалоРасписания Цикл
		НачалоКрайнегоИнтервала = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, КонецКрайнегоИнтервала);
		ЗавершенныеИнтервалы.Вставить(0, НачалоКрайнегоИнтервала); // Потому что обходим периоды задом наперед
		КонецКрайнегоИнтервала = НачалоКрайнегоИнтервала - 1; // Шаг назад
	КонецЦикла;
	
	Возврат ЗавершенныеИнтервалы;
	
КонецФункции

Функция ЗапрещенныеИнтервалы(НачалоФинансовогоПериода, КонецФинансовогоПериода, Периодичность, Ограничение)
	
	ЗапрещенныеИнтервалы = Новый Массив;
	
	Если ПустаяСтрока(Ограничение) Тогда
		Возврат ЗапрещенныеИнтервалы;
	КонецЕсли;
		
	НомераЗапрещенныхИнтервалов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ограничение);
	
	ВсеИнтервалы = КалендарьБухгалтера.Периоды(НачалоФинансовогоПериода, КонецФинансовогоПериода, Периодичность);
	
	КоличествоИнтервалов = ВсеИнтервалы.Количество();
		
	Для Каждого НомерЗапрещенногоИнтервалаСтрокой Из НомераЗапрещенныхИнтервалов Цикл
		
		Попытка
			НомерЗапрещенногоИнтервала = Число(НомерЗапрещенногоИнтервалаСтрокой);
		Исключение
			Продолжить;
		КонецПопытки;
		
		Если НомерЗапрещенногоИнтервала < 1 Тогда
			Продолжить;
		КонецЕсли;
		
		Если НомерЗапрещенногоИнтервала > КоличествоИнтервалов Тогда
			Продолжить;
		КонецЕсли;
		
		ЗапрещенныеИнтервалы.Добавить(ВсеИнтервалы[НомерЗапрещенногоИнтервала - 1]);
			
	КонецЦикла;
	
	Возврат ЗапрещенныеИнтервалы;
	
КонецФункции

Функция РасписаниеПоДавнимСобытиям(Организация, КонецОбзора, ТекущаяДата, Знач Правила, ОбновитьСписокОтчетов)
	
	НачалоОбзора = '0001-01-01';
	
	Расписание = НовыйРасписание();
	
	// Служебная колонка, будет удалена в конце процедуры
	// Если значение ПеренестиНаРабочийДень = 1, то задача переносится на следующий рабочий день
	// Если значение ПеренестиНаРабочийДень = -1, то задача переносится на предыдущий рабочий день
	// Если значение ПеренестиНаРабочийДень = 0, то задача никуда не переносится
	Расписание.Колонки.Добавить("ПеренестиНаРабочийДень", Новый ОписаниеТипов("Число"));
	Расписание.Колонки.Добавить("ПеренестиСрокНачалаНаРабочийДень", Новый ОписаниеТипов("Число"));
	
	ОтборПравил = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения();
	ОтборПравил.Организация     = Организация;
	ОтборПравил.НачалоИнтервала = НачалоОбзора;
	ОтборПравил.КонецИнтервала  = КонецОбзора;
	ОтборПравил.Правила         = Правила;
	
	ЗаполнитьРасписаниеПоНалогамИСборам(
		Расписание,
		ОтборПравил,
		ОбновитьСписокОтчетов);
		
	ЗадачиБухгалтераПереопределяемый.ПриОпределенииРасписанияПоДавнимСобытиям(Организация, Расписание, ТекущаяДата, НачалоОбзора, КонецОбзора);
		
	ЗаполнитьРасписаниеТребованияФНС(
		Организация,
		Расписание,
		НачалоОбзора,
		КонецОбзора);
	
	ПеренестиНаРабочиеДни(Расписание);
	
	Расписание.Колонки.Удалить("ПеренестиНаРабочийДень");
	Расписание.Колонки.Удалить("ПеренестиСрокНачалаНаРабочийДень");
	
	// Исключим из расписания просроченные задачи - их там будет немного,
	// так устроены функции ПравилаОрганизации() и ЗаполнитьРасписаниеПоНалогамИСборам(), ЗаполнитьРасписаниеРегулярныеПлатежи()
	УдалитьПросроченныеЗадачи(Расписание, ТекущаяДата, Истина);
	
	// Скорректируем задачи предыдущих периодов с учетом закрытия организации
	СкорректироватьРасписаниеСУчетомЗакрытияОрганизации(Организация, Расписание, ТекущаяДата);
	
	Расписание.ЗаполнитьЗначения(Организация, "Организация");
	
	Возврат Расписание;
	
КонецФункции

Функция РасписаниеПоНедавнимСобытиям(Организация, НачалоОбзора, КонецОбзора, Знач Правила, ОбновитьСписокОтчетов)
	
	Расписание = НовыйРасписание();
	
	// Служебная колонка, будет удалена в конце процедуры
	// Если значение ПеренестиНаРабочийДень = 1, то задача переносится на следующий рабочий день
	// Если значение ПеренестиНаРабочийДень = -1, то задача переносится на предыдущий рабочий день
	// Если значение ПеренестиНаРабочийДень = 0, то задача никуда не переносится
	Расписание.Колонки.Добавить("ПеренестиНаРабочийДень", Новый ОписаниеТипов("Число"));
	Расписание.Колонки.Добавить("ПеренестиСрокНачалаНаРабочийДень", Новый ОписаниеТипов("Число"));
	
	ОтборПравил = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения();
	ОтборПравил.Организация     = Организация;
	ОтборПравил.НачалоИнтервала = НачалоОбзора;
	ОтборПравил.КонецИнтервала  = КонецОбзора;
	ОтборПравил.Правила         = Правила;
	
	ЗаполнитьРасписаниеПоНалогамИСборам(
		Расписание,
		ОтборПравил,
		ОбновитьСписокОтчетов);
	
	ЗадачиБухгалтераПереопределяемый.ПриОпределенииРасписанияПоНедавнимСобытиям(Организация, Расписание, НачалоОбзора, КонецОбзора);
		
	ЗаполнитьРасписаниеТребованияФНС(
		Организация,
		Расписание,
		НачалоОбзора,
		КонецОбзора);
		
	ПеренестиНаРабочиеДни(Расписание);
	
	Расписание.Колонки.Удалить("ПеренестиНаРабочийДень");
	Расписание.Колонки.Удалить("ПеренестиСрокНачалаНаРабочийДень");

	Расписание.ЗаполнитьЗначения(Организация, "Организация");
		
	Возврат Расписание;
		
КонецФункции

Процедура ЗаписатьНовыеЗадачи(Организация, Расписание, СтараяДатаАктуальности, НоваяДатаАктуальности, ТекущаяДата, ИзмененыЗаписи = Ложь)
	
	Если Расписание.Количество() = 0 И СтараяДатаАктуальности > ТекущаяДата Тогда
		// Не меняем дату актуальности, потому что РасписаниеПоНедавнимСобытиям() работает заведомо быстрее, чем РасписаниеПоДавнимСобытиям().
		// В том случае, если список задач бухгалтера не сочтет данные сильно устаревшими.
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заблокируем на время транзакции
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтера");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	
	НачатьТранзакцию();
	
	Блокировка.Заблокировать();
	
	ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(Расписание);
	
	// Создадим новые задачи
	КлючиЗаписи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	Для Каждого ЭлементРасписания Из Расписание Цикл
		МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючиЗаписи);
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЭлементРасписания);
		МенеджерЗаписи.Записать();
		ИзмененыЗаписи = Истина;
	КонецЦикла;
	
	// Запишем, что актуализировали список
	Состояние = РегистрыСведений.АктуальностьСпискаЗадачБухгалтера.СоздатьМенеджерЗаписи();
	Состояние.Организация      = Организация;
	Состояние.ДатаАктуальности = НоваяДатаАктуальности;
	Состояние.Записать();
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Процедура ЗаписатьИзменения(Организация, Расписание, ТекущаяДата, ИзмененыЗаписи = Ложь, ОтборДействие = Неопределено)
	
	// Определять изменения - отличия между правильным (только что рассчитанным)
	// расписанием и уже имеющимся - будем запросом (позже, в транзакции).
	// Для передачи порядка строк таблицы в запрос добавляем в таблицу колонку Индекс.
	Расписание.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	Для Каждого СтрокаРасписания Из Расписание Цикл
		СтрокаРасписания.Индекс = Расписание.Индекс(СтрокаРасписания);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		// Заблокируем на время транзакции
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтера");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
			
		Блокировка.Заблокировать();
	
		// Получим перечень отличий
		// Отличиями считаем добавившиеся или удаленные события, а также те, у которых изменился срок
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Расписание",       Расписание);
		Запрос.УстановитьПараметр("Организация",      Организация);
		Запрос.УстановитьПараметр("ТекущаяДата",      ТекущаяДата);
		Запрос.УстановитьПараметр("НеТипПравилаСЭДО", Истина);
	
		// Тк при неналоговых регулярных платежах возможно изменение правила после выполнения по нему платежа, исключаем записи, по которым сделаны платежи, из обработки
		// Это делается для того, чтобы задачи по которым уже сделан платеж не исчезали из списка при изменении условий правила.
		// Текст запроса может быть модифицирован, см. &ОтборДействие.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Расписание.Индекс КАК Индекс,
		|	Расписание.Организация КАК Организация,
		|	Расписание.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	Расписание.Правило КАК Правило,
		|	Расписание.ПериодСобытия КАК ПериодСобытия,
		|	Расписание.Срок КАК Срок,
		|	Расписание.НачальныйСрок КАК НачальныйСрок,
		|	Расписание.Наименование КАК Наименование,
		|	Расписание.Действие КАК Действие
		|ПОМЕСТИТЬ Расписание
		|ИЗ
		|	&Расписание КАК Расписание
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Правило,
		|	ПериодСобытия,
		|	РегистрацияВНалоговомОргане
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗадачиБухгалтера.Организация КАК Организация,
		|	ЗадачиБухгалтера.Правило КАК Правило,
		|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
		|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ЗадачиБухгалтера.Действие КАК Действие
		|ПОМЕСТИТЬ МогутБытьУдалены
		|ИЗ
		|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|ГДЕ
		|	ЗадачиБухгалтера.Организация = &Организация
		|	И НЕ ЗадачиБухгалтера.ВАрхиве
		|	И ЗадачиБухгалтера.Комментарий = """"
		|	И НЕ ЗадачиБухгалтера.СтатусУстановленВручную
		|	И &НеТипПравилаСЭДО
		|	И (ЗадачиБухгалтера.Срок >= &ТекущаяДата
		|			ИЛИ ЗадачиБухгалтера.ПериодСобытия = ДАТАВРЕМЯ(1, 1, 1))
		|	И &ОтборДействие
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Правило,
		|	ПериодСобытия,
		|	РегистрацияВНалоговомОргане
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расписание.Индекс КАК Индекс
		|ИЗ
		|	Расписание КАК Расписание
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|		ПО Расписание.Организация = ЗадачиБухгалтера.Организация
		|			И Расписание.Правило = ЗадачиБухгалтера.Правило
		|			И Расписание.ПериодСобытия = ЗадачиБухгалтера.ПериодСобытия
		|			И Расписание.РегистрацияВНалоговомОргане = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
		|			И Расписание.Действие = ЗадачиБухгалтера.Действие
		|ГДЕ
		|	ЗадачиБухгалтера.Организация ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МогутБытьУдалены.Организация КАК Организация,
		|	МогутБытьУдалены.Правило КАК Правило,
		|	МогутБытьУдалены.ПериодСобытия КАК ПериодСобытия,
		|	МогутБытьУдалены.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	МогутБытьУдалены.Действие КАК Действие
		|ИЗ
		|	МогутБытьУдалены КАК МогутБытьУдалены
		|		ЛЕВОЕ СОЕДИНЕНИЕ Расписание КАК Расписание
		|		ПО МогутБытьУдалены.Организация = Расписание.Организация
		|			И МогутБытьУдалены.Правило = Расписание.Правило
		|			И МогутБытьУдалены.ПериодСобытия = Расписание.ПериодСобытия
		|			И МогутБытьУдалены.РегистрацияВНалоговомОргане = Расписание.РегистрацияВНалоговомОргане
		|			И МогутБытьУдалены.Действие = Расписание.Действие
		|ГДЕ
		|	Расписание.Организация ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расписание.Индекс КАК Индекс,
		|	ЗадачиБухгалтера.Статус КАК Статус
		|ИЗ
		|	Расписание КАК Расписание
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|		ПО Расписание.Организация = ЗадачиБухгалтера.Организация
		|			И Расписание.Правило = ЗадачиБухгалтера.Правило
		|			И Расписание.ПериодСобытия = ЗадачиБухгалтера.ПериодСобытия
		|			И Расписание.РегистрацияВНалоговомОргане = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
		|			И Расписание.Действие = ЗадачиБухгалтера.Действие
		|ГДЕ
		|	НЕ ЗадачиБухгалтера.ВАрхиве
		|	И Расписание.Срок <> ЗадачиБухгалтера.Срок";
		
		Если ЗначениеЗаполнено(ОтборДействие) Тогда
			Запрос.УстановитьПараметр("Действие", ОтборДействие);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборДействие", "ЗадачиБухгалтера.Действие = &Действие");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборДействие", "Истина");
		КонецЕсли;
	
		ЗадачиБухгалтераПереопределяемый.ПриОпределенииЗапросаИзменений(Запрос, ОтборДействие);
		
		Результат = Запрос.ВыполнитьПакет();
		ИндексыНовыхЗаписей = Результат[2].Выбрать();
		ЗаписиКУдалению     = Результат[3].Выбрать();
		ЗаписиИзмененСрок   = Результат[4].Выбрать();
	
		// Для новых записей необходимо рассчитать статус
		НовыеЗадачи = Расписание.СкопироватьКолонки();
		
		Пока ИндексыНовыхЗаписей.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НовыеЗадачи.Добавить(), Расписание[ИндексыНовыхЗаписей.Индекс]);
		КонецЦикла;
		
		ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(НовыеЗадачи);
	
		// Создадим новые задачи
		Для Каждого Задача Из НовыеЗадачи Цикл
			Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Задача);
			Запись.Записать();
			ИзмененыЗаписи = Истина;
		КонецЦикла;
	
		// Удалим задачи, ставшие неактуальными
		Пока ЗаписиКУдалению.Следующий() Цикл
			Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, ЗаписиКУдалению);
			Запись.Удалить();
			ИзмененыЗаписи = Истина;
		КонецЦикла;
	
		// Сменим срок
		Пока ЗаписиИзмененСрок.Следующий() Цикл
			Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Расписание[ЗаписиИзмененСрок.Индекс]);
			Запись.Прочитать();
			Если Не Запись.Выбран() Тогда
				// Обработаем задачи, созданные до регистрации действий
				ЗаполнитьЗначенияСвойств(Запись, Расписание[ЗаписиИзмененСрок.Индекс]);
				Запись.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ПустаяСсылка();
				Запись.Прочитать();
				ЗаполнитьЗначенияСвойств(Запись, Расписание[ЗаписиИзмененСрок.Индекс]);
			КонецЕсли;
			Запись.Срок = Расписание[ЗаписиИзмененСрок.Индекс].Срок;
			Запись.Статус = ЗаписиИзмененСрок.Статус;
			Запись.Записать();
			ИзмененыЗаписи = Истина;
		КонецЦикла;
		
		ЗадачиБухгалтераПереопределяемый.ЗаписатьИзменения(Результат, Расписание, ИзмененыЗаписи);
		
		// Дата актуальности не изменилась
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
			
КонецПроцедуры

Функция ОписаниеДействия(Правило, ПериодСобытия, Организация, ПараметрыПродленияСроков, ОсобыеСрокиСверкиИмущественныхНалогов)
	
	День = 24 * 60 * 60;
	
	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("Правило",                     Правило.Ссылка);
	ОписаниеДействия.Вставить("Действие",                    Правило.Действие);
	ОписаниеДействия.Вставить("ПериодСобытия",               ПериодСобытия);
	ОписаниеДействия.Вставить("РегистрацияВНалоговомОргане", Правило.РегистрацияВНалоговомОргане);
	
	КонецПериода = Неопределено;
	ЭтоЗадачаЗакрытияИП = ВыполнениеЗадачБухгалтераПовтИспНаВызов.ЭтоЗадачаЗакрытияИП(Правило.ИдентификаторПравила);
	Если ЭтоЗадачаЗакрытияИП Тогда
		ЗадачиБухгалтераПереопределяемый.ОписаниеДействияЗадачаЗакрытияИП(Правило, Организация, ОписаниеДействия);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонецПериода) Тогда
		КонецПериода = КонецДня(ПериодСобытия);
	КонецЕсли;
	
	// Учтем срок в месяцах.
	Срок = ДобавитьМесяц(КонецПериода, Правило.СрокМесяцев);
	Если Не ЭтоЗадачаЗакрытияИП
		И Правило.Периодичность <> Перечисления.Периодичность.День
		И Правило.Периодичность <> Перечисления.Периодичность.Неделя Тогда
		Срок = КонецМесяца(Срок);
	КонецЕсли;
	
	Если Правило.СчитатьСрокВРабочихДнях Тогда
		// Попытаемся рассчитать срок в рабочих днях, если заполнен производственный календарь.
		ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
		
		РабочийДень = КалендарьБухгалтера.РабочаяДатаПоКалендарю(ПроизводственныйКалендарь, Срок, Правило.СрокДней);
		Если РабочийДень <> Неопределено Тогда
			Срок = РабочийДень;
		Иначе
			// Возможно производственный календарь не заполнен, тогда рассчитаем срок в календарных днях.
			Срок = Срок + Правило.СрокДней * День;
		КонецЕсли;
	Иначе
		// Срок выполнения задачи считается в календарных днях.
		Срок = Срок + Правило.СрокДней * День;
	КонецЕсли;
	
	ЗадачиБухгалтераПереопределяемый.СдвинутьСроки(Срок, ПериодСобытия, Правило, День, ОсобыеСрокиСверкиИмущественныхНалогов, ПараметрыПродленияСроков);
	
	Если ВыполнениеЗадачБухгалтераПовтИспНаВызов.ЭтоЗадачаОплатыУставногоКапитала(Правило.ИдентификаторПравила) Тогда
		ЗадачиБухгалтераПереопределяемый.ОписаниеДействияЗадачаОплатыУставногоКапитала(Правило, Организация, ОписаниеДействия, КонецПериода, Срок);
	КонецЕсли;
	
	ОписаниеДействия.Вставить("Срок", Срок);
	ОписаниеДействия.Вставить("ПеренестиНаРабочийДень", Правило.ПеренестиНаРабочийДень);
	
	НачальныйСрок = Дата(1, 1, 1);
	Если ЕстьСрокНачалаВыполненияТребования(Правило) Тогда
		
		НачальныйСрок = ДобавитьМесяц(КонецПериода, Правило.НачальныйСрокМесяцев);
		Если Правило.Периодичность <> Перечисления.Периодичность.День Тогда
			НачальныйСрок = КонецМесяца(НачальныйСрок);
		КонецЕсли;
		
		НачальныйСрок = НачальныйСрок + Правило.НачальныйСрокДней * День;
		
	КонецЕсли;
	
	ОписаниеДействия.Вставить("НачальныйСрок", НачальныйСрок);
	ОписаниеДействия.Вставить("ПеренестиСрокНачалаНаРабочийДень", Правило.ПеренестиСрокНачалаНаРабочийДень);
	
	УчитыватьНерабочиеДни = Ложь;
	ЗадачиБухгалтераПереопределяемый.ОпределитьУчитыватьНерабочиеДни(УчитыватьНерабочиеДни, Правило, ПараметрыПродленияСроков);
	
	ОписаниеДействия.Вставить("УчитыватьНерабочиеДни", УчитыватьНерабочиеДни);
	
	Если Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет Тогда
		ОписаниеДействия.Вставить("Периодичность", Правило.ФинансовыйПериод);
	Иначе
		ОписаниеДействия.Вставить("Периодичность", Правило.Периодичность);
	КонецЕсли;
	
	Если ВыполнениеЗадачБухгалтераПовтИспНаВызов.ЭтоЗадачаЗакрытияИП(Правило.ИдентификаторПравила) Тогда
		// Для событий с датой закрытия описание генерируется с учетом конечного периода действия организации
		ЗавершающийПериод = КонецГода(Правило.НачалоФинансовогоПериода);
		ПредставлениеПериода = КалендарьБухгалтера.ПредставлениеПериодаСобытия(
			ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Перечисления.Периодичность.Год, ЗавершающийПериод),
			ЗавершающийПериод);
	Иначе
		ПредставлениеПериода = КалендарьБухгалтера.ПредставлениеПериодаСобытия(
			ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ОписаниеДействия.Периодичность, КонецПериода),
			КонецПериода);
	КонецЕсли;
	ОписаниеДействия.Вставить("ПредставлениеПериода", ПредставлениеПериода);
	
	НалоговыйОрган = "";
	Если ЗначениеЗаполнено(Правило.РегистрацияВНалоговомОргане) Тогда
		НалоговыйОрган = ПредставлениеНалоговогоОргана(Правило.КодКонтролирующегоОргана);
	КонецЕсли;
	
	// Наименование действия состоит из трех частей:
	// названия задачи, описания действия и периода (для местных налогов дополняется налоговым органом).
	
	// Описание действия может отсутствовать.
	// Описание задачи может отсутствовать.
	Если Правило.ИсключатьИмяРодительскойЗадачиИзПредставленияЗадачи Тогда
		ШаблонСобытие = НСтр("ru = '[Описание]'");
		ШаблонТекста  = НСтр("ru = '[Событие]'");
	Иначе
		ШаблонСобытие = НСтр("ru = ', [Описание]'");
		ШаблонТекста  = НСтр("ru = '[Задача][Событие]'");
	КонецЕсли;
	
	// Описание периода может отсутствовать.
	Если Правило.ИсключатьПериодИзПредставленияЗадачи Тогда
		ШаблонТекста = СтрШаблон(НСтр("ru = '%1 [НалоговыйОрган]'"), ШаблонТекста);
	ИначеЕсли ЭтоПравилоЕдиногоПомощника(Правило) Тогда
		ШаблонТекста = СтрШаблон(НСтр("ru = '%1 в [Период]'"), ШаблонТекста);
		ПредставлениеПериода = КалендарьБухгалтера.ПредставлениеПериодаСобытия(НачалоМесяца(Срок), КонецМесяца(Срок));
		ПериодВПредложномПадеже = ПолучитьСклоненияСтроки(ПредставлениеПериода, ,"ПД=Предложный")[0];
		ПредставлениеПериода = ПериодВПредложномПадеже;
	ИначеЕсли ВыполнениеЗадачБухгалтера.ЭтоПравилоТринадцатогоНДФЛ(
		СтрШаблон("%1.%2", Правило.ИдентификаторЗадачи, Правило.ИдентификаторПравила)) Тогда
		ШаблонТекста  = НСтр("ru = '[Задача][Событие] [ПериодГод]'");
	ИначеЕсли ВыполнениеЗадачБухгалтера.ЭтоПравилоНДФЛКонецМесяца(
		СтрШаблон("%1.%2", Правило.ИдентификаторЗадачи, Правило.ИдентификаторПравила)) Тогда
		ПредставлениеПериода = КалендарьБухгалтера.ПредставлениеПериодаСобытия(НачалоМесяца(ПериодСобытия), КонецМесяца(ПериодСобытия));
		ПериодВРодительномПадеже = ПолучитьСклоненияСтроки(ПредставлениеПериода, ,"ПД=Родительный")[0];
		ПредставлениеПериода = СтрШаблон(" по %1 %2", День(КонецМесяца(ПериодСобытия)), ПериодВРодительномПадеже);
		ШаблонТекста  = НСтр("ru = '[Задача][Событие][Период]'");
	ИначеЕсли ВыполнениеЗадачБухгалтера.ЭтоПравилоВыплатыЗарплатыАванса(
		СтрШаблон("%1.%2", Правило.ИдентификаторЗадачи, Правило.ИдентификаторПравила)) Тогда
		ШаблонТекста  = СтрШаблон(НСтр("ru = '%1 за [Период]'"), ШаблонТекста);
	Иначе
		ШаблонТекста = СтрШаблон(НСтр("ru = '%1 за [Период][НалоговыйОрган]'"), ШаблонТекста);
	КонецЕсли;

	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("Задача",         Справочники.ЗадачиБухгалтера.ПредставлениеЗадачи(Правило.Владелец));
	ПараметрыТекста.Вставить("Описание",       Правило.Описание);
	ПараметрыТекста.Вставить("Период",         ПредставлениеПериода);
	ПараметрыТекста.Вставить("ПериодГод",      СтрШаблон("%1 г.", Формат(ПериодСобытия, "ДФ=гггг")));
	ПараметрыТекста.Вставить("НалоговыйОрган", НалоговыйОрган);
	
	Если ПустаяСтрока(ПараметрыТекста.Описание) Тогда
		ПараметрыТекста.Вставить("Событие", "");
	ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетСтраховыхВзносовИП(Правило.Ссылка) Тогда
		ЭтоПравилоУплатыВзносовСДоходов = Ложь;
		ЗадачиБухгалтераПереопределяемый.ПроверитьЭтоПравилоУплатыВзносовСДоходов(ЭтоПравилоУплатыВзносовСДоходов, Правило.Ссылка);
		Если ЭтоПравилоУплатыВзносовСДоходов Тогда
			ПараметрыТекста.Вставить("Событие", НСтр("ru = ' с доходов'"));
		Иначе
			ПараметрыТекста.Вставить("Событие", НСтр("ru = ' по единому тарифу'"));
		КонецЕсли;
	Иначе
		ПараметрыТекста.Вставить(
			"Событие", 
			СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонСобытие, ПараметрыТекста));
	КонецЕсли;
	
	ОписаниеДействия.Вставить("Наименование",
		СокрЛП(СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста)));
	ОписаниеДействия.Вставить("НаименованиеСокращенное", ПараметрыТекста.Задача);
	
	ЗадачиБухгалтераПереопределяемый.ПриОпределенииОписанияДействия(ОписаниеДействия, Правило, ПериодСобытия, Организация, ОсобыеСрокиСверкиИмущественныхНалогов);
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция БлижайшаяДатаПоявленияЗадачОрганизации(Организация, Знач КонецИнтервалаПоиска = '00010101')
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Упреждение  = КалендарьБухгалтера.УпреждениеЗаполненияСписка() * 24 * 60 * 60;
	
	ДатаАктуальности = РегистрыСведений.АктуальностьСпискаЗадачБухгалтера.ДатаАктуальности(Организация);
	Если Не ЗначениеЗаполнено(ДатаАктуальности) Тогда
		ТекущаяДата = ТекущаяДатаСеанса() + Упреждение;
	Иначе
		ТекущаяДата = ДатаАктуальности + 1; // Текущая дата обозначает период, за который еще не созданы задачи
	КонецЕсли;
	
	ДатаНачалаДеятельности = КалендарьБухгалтера.ДатаНачалаДеятельности(Организация);
	ТекущаяДата = Макс(ТекущаяДата, ДатаНачалаДеятельности);
	
	Если Не ЗначениеЗаполнено(КонецИнтервалаПоиска) Тогда
		КонецИнтервалаПоиска = КонецГода(ДобавитьМесяц(ТекущаяДата, 6));
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; // Хранит ПравилаУплатыНалоговНаМестах
	
	Правила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НовыйПравилаОрганизации();
	ОтборПравил = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения();
	ОтборПравил.Организация     = Организация;
	ОтборПравил.НачалоИнтервала = ТекущаяДата;
	ОтборПравил.КонецИнтервала  = КонецИнтервалаПоиска;
	Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ЗаполнитьПравилаОрганизации(
		Правила, ОтборПравил, МенеджерВременныхТаблиц);
		
	Если Не ЗначениеЗаполнено(Правила) Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	// Правила: Правило, ФинансовыйПериод
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Правила", Правила);
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрацияВНалоговомОргане"));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Правила.Правило КАК Правило,
	|	Правила.ФинансовыйПериод КАК ФинансовыйПериод
	|ПОМЕСТИТЬ Правила
	|ИЗ
	|	&Правила КАК Правила
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Правила.Правило КАК Правило,
	|	Правила.ФинансовыйПериод,
	|	МАКСИМУМ(ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.ПериодДействияПравила, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодДействияПравила,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане, &ОсновнойНалоговыйОрган) КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ МестныеПравила
	|ИЗ
	|	Правила КАК Правила
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаУплатыНалоговНаМестах КАК ПравилаУплатыНалоговНаМестах
	|		ПО Правила.Правило = ПравилаУплатыНалоговНаМестах.Правило
	|			И Правила.ФинансовыйПериод >= ПравилаУплатыНалоговНаМестах.ПериодДействияПравила
	|
	|СГРУППИРОВАТЬ ПО
	|	Правила.ФинансовыйПериод,
	|	Правила.Правило,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане, &ОсновнойНалоговыйОрган)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило,
	|	ПериодДействияПравила,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МестныеПравила.ФинансовыйПериод КАК НачалоФинансовогоПериода,
	|	Регламент.Ссылка КАК Ссылка,
	|	Регламент.Владелец КАК Владелец,
	|	ПРЕДСТАВЛЕНИЕ(Регламент.Владелец),
	|	Регламент.Описание КАК Описание,
	|	Регламент.Действие КАК Действие,
	|	Регламент.ФинансовыйПериод КАК ФинансовыйПериод,
	|	Регламент.Периодичность КАК Периодичность,
	|	Регламент.ОграничениеПериода КАК ОграничениеПериода,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.СрокМесяцев, Регламент.СрокМесяцев) КАК СрокМесяцев,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.СрокДней, Регламент.СрокДней) КАК СрокДней,
	|	Регламент.ПеренестиНаРабочийДень КАК ПеренестиНаРабочийДень,
	|	Регламент.БазовыйПериод КАК БазовыйПериод,
	|	Регламент.ОтставаниеБазовогоПериода КАК ОтставаниеБазовогоПериода,
	|	Регламент.Код КАК ИдентификаторПравила,
	|	ЕСТЬNULL(ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане, ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)) КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	МестныеПравила КАК МестныеПравила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Регламент
	|		ПО МестныеПравила.Правило = Регламент.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаУплатыНалоговНаМестах КАК ПравилаУплатыНалоговНаМестах
	|		ПО МестныеПравила.Правило = ПравилаУплатыНалоговНаМестах.Правило
	|			И МестныеПравила.ПериодДействияПравила = ПравилаУплатыНалоговНаМестах.ПериодДействияПравила
	|			И МестныеПравила.РегистрацияВНалоговомОргане = ПравилаУплатыНалоговНаМестах.РегистрацияВНалоговомОргане
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачалоФинансовогоПериода,
	|	Регламент.Периодичность.Порядок
	|ИТОГИ
	|	МАКСИМУМ(СрокМесяцев),
	|	МАКСИМУМ(СрокДней)
	|ПО
	|	Ссылка";
	
	ВыборкаПравил  = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДалекоеБудущее = '29990101';
	БлижайшаяДата  = ДалекоеБудущее;
	Пока ВыборкаПравил.Следующий() Цикл
		
		БлижайшаяДатаПравила = БлижайшаяДата;
		
		СобытиеПозжеРелевантногоПериода = СобытиеПозжеРелевантногоПериода(ВыборкаПравил);
		ПростойБазовыйПериод            = КалендарьБухгалтера.БазовыйПериодОпределяетсяПросто(ВыборкаПравил);
		
		Периодичность = ВыборкаПравил.Периодичность;
		Если Не ПростойБазовыйПериод Тогда
			Периодичность = ВыборкаПравил.БазовыйПериод;
		КонецЕсли;
		
		Выборка = ВыборкаПравил.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			
			// Определим интервал базовых периодов
			
			КонецФинансовогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Выборка.ФинансовыйПериод, Выборка.НачалоФинансовогоПериода);
			
			Если СобытиеПозжеРелевантногоПериода И ПростойБазовыйПериод Тогда
				
				НачалоИнтервалаБазовыхПериодов = Выборка.НачалоФинансовогоПериода;
				КонецИнтервалаБазовыхПериодов  = КонецФинансовогоПериода;
				
			Иначе
				
				// Базовый период всегда предшествует сроку
				ОтставаниеБазовогоПериода = Выборка.ОтставаниеБазовогоПериода;
				КалендарьБухгалтера.УточнитьОтставаниеБазовогоПериода(
					ОтставаниеБазовогоПериода, 
					Периодичность, 
					Выборка.СрокМесяцев);
				
				// Интервал базовых периодов опережает соответствующий финансовый период
				ОпережениеРелевантногоПериода = 1 +  ОтставаниеБазовогоПериода;
				НачалоИнтервалаБазовыхПериодов = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
					Выборка.НачалоФинансовогоПериода,
					Периодичность,
					- ОпережениеРелевантногоПериода);
				// Для расчета конца интервала нельзя прибавлять месяцы к дате конца другого периода,
				// так как в разных месяцах разное количество дней.
				// Поэтому конец интервала рассчитываем через начало следующего интервала.
				НачалоСледующегоФинансовогоПериода = КонецФинансовогоПериода + 1;
				НачалоИнтервалаСледующегоЗаИнтерваломБазовыхПериодов = 
					ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
						НачалоСледующегоФинансовогоПериода,
						Периодичность,
						- ОпережениеРелевантногоПериода);
				КонецИнтервалаБазовыхПериодов = НачалоИнтервалаСледующегоЗаИнтерваломБазовыхПериодов - 1;

			КонецЕсли;
			
			Если НачалоИнтервалаБазовыхПериодов >= БлижайшаяДата Тогда
				Прервать; // Переходим к следующему правилу - все следующие периоды этого правила заведомо позже
			КонецЕсли;
			
			Если ТекущаяДата > КонецИнтервалаБазовыхПериодов Тогда
				Продолжить; // Переходим к следующему финансовому периоду - по этому периоду задачи уже должны быть созданы
			КонецЕсли;
			
			Если ТекущаяДата <= НачалоИнтервалаБазовыхПериодов Тогда
				
				БлижайшаяДатаПравила = НачалоИнтервалаБазовыхПериодов;
				
			Иначе
				
				// Найдем самый ранний из базовых периодов, за который не созданы задачи
				// ТекущаяДата относится к еще не завершившемуся периоду
				БлижайшийБазовыйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, ТекущаяДата);
				
				БазовыеПериоды = КалендарьБухгалтера.Периоды(НачалоИнтервалаБазовыхПериодов, КонецИнтервалаБазовыхПериодов, Периодичность);
				Если БазовыеПериоды.Найти(БлижайшийБазовыйПериод) = Неопределено Тогда
					// В этом финансовом периоде подходящих нет.
					// Переходим к следующему финансовому периоду
					Продолжить; 
				КонецЕсли;
				
				БлижайшаяДатаПравила = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, БлижайшийБазовыйПериод) + 1;
					
				// Пренебрежем тем, что некоторые периоды могут быть ограничены.
					
			КонецЕсли;
			
			Прервать; // Если дошли до сюда, то определили ближайшую дату правила. А следующие периоды по определению позже нее.
			
		КонецЦикла; //Выборка (финансовых периодов)
		
		БлижайшаяДата = Мин(БлижайшаяДата, БлижайшаяДатаПравила);
		
	КонецЦикла; // ВыборкаПравил
	
	Если БлижайшаяДата >= ДалекоеБудущее Тогда
		Возврат Неопределено;
	Иначе
		Возврат БлижайшаяДата - Упреждение;
	КонецЕсли;
	
КонецФункции

Функция ЕстьСрокНачалаВыполненияТребования(Правило)
	Возврат Правило.НачальныйСрокМесяцев <> 0 Или Правило.НачальныйСрокДней <> 0;
КонецФункции

// Проверяет входит ли срок уплаты в указанный период.
// Нужна для проверки правил, срок у которых наступает до окончания периода.
//
// Параметры:
//  Срок          - Дата - срок выполнения задачи
//  Период        - Дата - конец проверяемого периода
//  Периодичность - ПеречислениеСсылка.Периодичность - периодичность задачи
// 
// Возвращаемое значение:
//  Булево - Истина если срок находится в заданном периоде
//
Функция ОбязанностьУплатыНаступаетДоОкончанияПериода(Срок, Период, Периодичность)
	КонецПериода = Неопределено;
	Если Периодичность = Перечисления.Периодичность.Год Тогда
		КонецПериода = НачалоДня(КонецГода(Срок));
	ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда
		КонецПериода = НачалоДня(КонецКвартала(Срок));
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		КонецПериода = НачалоДня(КонецМесяца(Срок));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		Возврат Срок <= НачалоДня(Период) И КонецПериода = НачалоДня(Период);
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьПараметрыПредставленияДокументовСЭДО(Запрос)
	
	Запрос.УстановитьПараметр("ВходящийЗапросФССДляРасчетаПособияНаименование", НСтр("ru = 'Входящий запрос СФР для расчета пособия'"));
	Запрос.УстановитьПараметр("ВходящийЗапросФССДляРасчетаПособияНаименованиеСокращенное", НСтр("ru = 'Входящий запрос СФР'"));
	Запрос.УстановитьПараметр("ИзвещениеФССНаименование", НСтр("ru = 'Извещение СФР'"));
	Запрос.УстановитьПараметр("ИзвещениеФССНаименованиеСокращенное", НСтр("ru = 'Извещение СФР'"));
	Запрос.УстановитьПараметр("ОтказВВозмещенииВыплатРодителямДетейИнвалидовНаименование", НСтр("ru = 'Отказ СФР в возмещении выплат родителям детей инвалидов'"));
	Запрос.УстановитьПараметр("ОтказВВозмещенииВыплатРодителямДетейИнвалидовНаименованиеСокращенное", НСтр("ru = 'Отказ СФР в возмещении выплат'"));
	Запрос.УстановитьПараметр("УведомлениеОСтатусеВыплатыПособияНаименование", НСтр("ru = 'Уведомление СФР о статусе выплаты пособия'"));
	Запрос.УстановитьПараметр("УведомлениеОСтатусеВыплатыПособияНаименованиеСокращенное", НСтр("ru = 'Уведомление СФР о статусе выплаты'"));
	
КонецПроцедуры

#КонецОбласти

#Область ПередачаПараметров

Функция МассивОрганизаций(ОтборОрганизации) Экспорт
	
	Организации = Новый Массив;
	
	ДоступныеОрганизации = Новый Массив;
	ЗадачиБухгалтераПереопределяемый.ЗаполнитьДоступныеОрганизации(ДоступныеОрганизации);
	
	Если ТипЗнч(ОтборОрганизации) = Тип("Массив") Тогда
		Для Каждого ТекущаяОрганизация Из ОтборОрганизации Цикл
			Если ДоступныеОрганизации.Найти(ТекущаяОрганизация) <> Неопределено Тогда
				Организации.Добавить(ТекущаяОрганизация);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если ЗначениеЗаполнено(ОтборОрганизации) Тогда
			Если ДоступныеОрганизации.Найти(ОтборОрганизации) <> Неопределено Тогда
				Организации.Добавить(ОтборОрганизации);
			КонецЕсли;
		Иначе
			Организации = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ДоступныеОрганизации);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Организации;
	
КонецФункции

#КонецОбласти

#Область ЗадачиНачалаРаботы

Функция НовыеЗадачиНачалаРаботы(ОтборОрганизации)
	
	ЗадачиНачалаРаботы = Новый ТаблицаЗначений;
	ЗадачиНачалаРаботы.Колонки.Добавить("Организация",   Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Правило",       Новый ОписаниеТипов("ПеречислениеСсылка.ЗадачиНачалаРаботы"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Наименование",  Новый ОписаниеТипов("Строка"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Выполнено",     Новый ОписаниеТипов("Булево"));
	
	ЗадачиНовыхОрганизаций = ЗадачиНовыхОрганизаций();
	
	// Добавим в список задачи настройки заполнения форм статистики
	// задачи настройки заполнения форм статистики зависят от настроек конкретной организации
	НастраиваемыеФормыСтатистики = Новый ТаблицаЗначений; // Ссылки на формы.
	НастраиваемыеФормыСтатистики.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ЗадачиБухгалтераПереопределяемый.ПолучитьНастраиваемыеФормыСтатистикиПредоставляемыеОрганизацией(НастраиваемыеФормыСтатистики, ОтборОрганизации);
	
	ЗадачиУплатыНалоговВзносовПрошлыхЛет = ЗадачиОплатыЗадолженностиПоНалогамВзносамИП(ОтборОрганизации);
	
	ЗадачиПроверкиВидаДеятельности = ЗадачиПроверкиВидаДеятельности(ОтборОрганизации);
	
	// Составим список задач
	// Задача ПараметрыУчета в списке должна быть только одна
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗадачиНовыхОрганизаций.Задача КАК Задача,
	|	ЗадачиНовыхОрганизаций.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ЗадачиНовыхОрганизаций.УникальнаяЗадача КАК УникальнаяЗадача
	|ПОМЕСТИТЬ ЗадачиНачалаРаботы
	|ИЗ
	|	&ЗадачиНовыхОрганизаций КАК ЗадачиНовыхОрганизаций
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбособленноеПодразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастраиваемыеФормыСтатистики.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ЗадачиНачалаРаботы.НастройкаЗаполненияФормСтатистики) КАК Задача
	|ПОМЕСТИТЬ НастраиваемыеФормыСтатистики
	|ИЗ
	|	&НастраиваемыеФормыСтатистики КАК НастраиваемыеФормыСтатистики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиУплатыНалоговВзносовПрошлыхЛет.Организация КАК Организация,
	|	ЗадачиУплатыНалоговВзносовПрошлыхЛет.Задача КАК Задача,
	|	ЗадачиУплатыНалоговВзносовПрошлыхЛет.Наименование КАК Наименование
	|ПОМЕСТИТЬ ЗадачиУплатыНалоговВзносовПрошлыхЛет
	|ИЗ
	|	&ЗадачиУплатыНалоговВзносовПрошлыхЛет КАК ЗадачиУплатыНалоговВзносовПрошлыхЛет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиПроверкиВидаДеятельности.Организация КАК Организация,
	|	ЗадачиПроверкиВидаДеятельности.Задача КАК Задача,
	|	ЗадачиПроверкиВидаДеятельности.Наименование КАК Наименование
	|ПОМЕСТИТЬ ЗадачиПроверкиВидаДеятельности
	|ИЗ
	|	&ЗадачиПроверкиВидаДеятельности КАК ЗадачиПроверкиВидаДеятельности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	&ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|ПОМЕСТИТЬ Организации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	&УсловиеПоОрганизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбособленноеПодразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Организация КАК Организация,
	|	ВсеЗадачиНачалаРаботы.Задача КАК Задача,
	|	ПРЕДСТАВЛЕНИЕ(ВсеЗадачиНачалаРаботы.Задача) КАК Наименование
	|ПОМЕСТИТЬ ПотребныеЗадачи
	|ИЗ
	|	ЗадачиНачалаРаботы КАК ВсеЗадачиНачалаРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Организации КАК Организации
	|		ПО ВсеЗадачиНачалаРаботы.ОбособленноеПодразделение = Организации.ОбособленноеПодразделение
	|ГДЕ
	|	НЕ ВсеЗадачиНачалаРаботы.УникальнаяЗадача
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|	УникальныеЗадачиНачалаРаботы.Задача,
	|	ПРЕДСТАВЛЕНИЕ(УникальныеЗадачиНачалаРаботы.Задача)
	|ИЗ
	|	ЗадачиНачалаРаботы КАК УникальныеЗадачиНачалаРаботы
	|ГДЕ
	|	УникальныеЗадачиНачалаРаботы.УникальнаяЗадача
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастраиваемыеФормыСтатистики.Организация,
	|	НастраиваемыеФормыСтатистики.Задача,
	|	ПРЕДСТАВЛЕНИЕ(НастраиваемыеФормыСтатистики.Задача)
	|ИЗ
	|	НастраиваемыеФормыСтатистики КАК НастраиваемыеФормыСтатистики
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиУплатыНалоговВзносовПрошлыхЛет.Организация,
	|	ЗадачиУплатыНалоговВзносовПрошлыхЛет.Задача,
	|	ВЫРАЗИТЬ(ЗадачиУплатыНалоговВзносовПрошлыхЛет.Наименование КАК СТРОКА(150))
	|ИЗ
	|	ЗадачиУплатыНалоговВзносовПрошлыхЛет КАК ЗадачиУплатыНалоговВзносовПрошлыхЛет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиПроверкиВидаДеятельности.Организация,
	|	ЗадачиПроверкиВидаДеятельности.Задача,
	|	ВЫРАЗИТЬ(ЗадачиПроверкиВидаДеятельности.Наименование КАК СТРОКА(150))
	|ИЗ
	|	ЗадачиПроверкиВидаДеятельности КАК ЗадачиПроверкиВидаДеятельности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Задача
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПотребныеЗадачи.Организация КАК Организация,
	|	ПотребныеЗадачи.Задача КАК Правило,
	|	ПотребныеЗадачи.Наименование КАК Наименование,
	|	ЗначенияУпорядочивания.Порядок КАК Порядок
	|ИЗ
	|	ПотребныеЗадачи КАК ПотребныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК СуществующиеЗадачи
	|		ПО ПотребныеЗадачи.Организация = СуществующиеЗадачи.Организация
	|			И ПотребныеЗадачи.Задача = СуществующиеЗадачи.Правило
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ЗадачиНачалаРаботы КАК ЗначенияУпорядочивания
	|		ПО ПотребныеЗадачи.Задача = ЗначенияУпорядочивания.Ссылка
	|ГДЕ
	|	СуществующиеЗадачи.Организация ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	Если ТипЗнч(ОтборОрганизации) = Тип("Массив") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "Организации.Ссылка В(&ОтборОрганизации)");
	ИначеЕсли ЗначениеЗаполнено(ОтборОрганизации) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "Организации.Ссылка = &ОтборОрганизации");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "НЕ Организации.ПометкаУдаления");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ОбособленноеПодразделение", Метаданные.Справочники.Организации) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбособленноеПодразделение", "Организации.ОбособленноеПодразделение");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбособленноеПодразделение", "ЛОЖЬ");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЗадачиНовыхОрганизаций",               ЗадачиНовыхОрганизаций);
	Запрос.УстановитьПараметр("НастраиваемыеФормыСтатистики",         НастраиваемыеФормыСтатистики);
	Запрос.УстановитьПараметр("ЗадачиУплатыНалоговВзносовПрошлыхЛет", ЗадачиУплатыНалоговВзносовПрошлыхЛет);
	Запрос.УстановитьПараметр("ЗадачиПроверкиВидаДеятельности",       ЗадачиПроверкиВидаДеятельности);
	Запрос.УстановитьПараметр("ОтборОрганизации",                     ОтборОрганизации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НеТребуетсяПодключение1СЭДО = Истина;
		ЗадачиБухгалтераПереопределяемый.ОпределитьНеТребуетсяЗадачаНачалаРаботыПодключение1СЭДО(НеТребуетсяПодключение1СЭДО, Выборка.Организация);
		Если Выборка.Правило = Перечисления.ЗадачиНачалаРаботы.Подключение1СЭДО 
			И НеТребуетсяПодключение1СЭДО Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЗадачиНачалаРаботы.Добавить(), Выборка);
		
	КонецЦикла;
	
	Возврат ЗадачиНачалаРаботы;
	
КонецФункции

Функция ЗадачиНовыхОрганизаций() Экспорт
	
	ЗадачиОрганизаций = Новый ТаблицаЗначений;
	ЗадачиОрганизаций.Колонки.Добавить("Задача",                    Новый ОписаниеТипов("ПеречислениеСсылка.ЗадачиНачалаРаботы"));
	ЗадачиОрганизаций.Колонки.Добавить("ОбособленноеПодразделение", Новый ОписаниеТипов("Булево"));
	ЗадачиОрганизаций.Колонки.Добавить("УникальнаяЗадача",          Новый ОписаниеТипов("Булево"));
	
	Если РегламентированнаяОтчетность.Используется1СОтчетность() Тогда
		НоваяЗадача = ЗадачиОрганизаций.Добавить();
		НоваяЗадача.Задача                    = Перечисления.ЗадачиНачалаРаботы.Подключение1СОтчетности;
		НоваяЗадача.ОбособленноеПодразделение = Ложь;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ОбособленноеПодразделение", Метаданные.Справочники.Организации) Тогда
			НоваяЗадача = ЗадачиОрганизаций.Добавить();
			НоваяЗадача.Задача                    = Перечисления.ЗадачиНачалаРаботы.Подключение1СОтчетности;
			НоваяЗадача.ОбособленноеПодразделение = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗадачиБухгалтераПереопределяемый.ПриПолученииЗадачНовыхОрганизаций(ЗадачиОрганизаций);
	
	Возврат ЗадачиОрганизаций;
	
КонецФункции

Функция ЗадачиОплатыЗадолженностиПоНалогамВзносамИП(Организации)
	
	ЗадачиНачалаРаботы = Новый ТаблицаЗначений;
	ЗадачиНачалаРаботы.Колонки.Добавить("Организация",  Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Задача",       Новый ОписаниеТипов("ПеречислениеСсылка.ЗадачиНачалаРаботы"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	
	ЗадачиБухгалтераПереопределяемый.ПриПолученииЗадачОплатыЗадолженностиПоНалогамВзносамИП(ЗадачиНачалаРаботы, Организации);
	
	Возврат ЗадачиНачалаРаботы;
	
КонецФункции

Функция ЗадачиПроверкиВидаДеятельности(Организации) Экспорт
	
	ЗадачиНачалаРаботы = Новый ТаблицаЗначений;
	ЗадачиНачалаРаботы.Колонки.Добавить("Организация",  Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Задача",       Новый ОписаниеТипов("ПеречислениеСсылка.ЗадачиНачалаРаботы"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	
	ЗадачиБухгалтераПереопределяемый.ПриПолученииЗадачПроверкиВидаДеятельности(ЗадачиНачалаРаботы, Организации);
	
	Возврат ЗадачиНачалаРаботы;
	
КонецФункции

Процедура ЗавершитьЗадачуПроверкиВидаДеятельности(Организация) Экспорт
	
	ЗадачиБухгалтераПереопределяемый.ОпределитьЗавершитьЗадачуПроверкиВидаДеятельности(Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ЗадачиСнятияСУчета

Функция ЗадачиСнятияСУчетаВНалоговой(Организации) Экспорт
	
	ЗадачиНачалаРаботы = Новый ТаблицаЗначений;
	ЗадачиНачалаРаботы.Колонки.Добавить("Организация",  Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Задача",       Новый ОписаниеТипов("ПеречислениеСсылка.ЗадачиНачалаРаботы"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	
	ЗадачиБухгалтераПереопределяемый.ПриПолученииЗадачСнятияСУчетаВНалоговой(Организации, ЗадачиНачалаРаботы);
	
	Возврат ЗадачиНачалаРаботы;
	
КонецФункции

Функция НовыеЗадачиСнятияСУчета(ОтборОрганизации)
	
	ЗадачиНачалаРаботы = Новый ТаблицаЗначений;
	ЗадачиНачалаРаботы.Колонки.Добавить("Организация",  Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Правило",      Новый ОписаниеТипов("ПеречислениеСсылка.ЗадачиНачалаРаботы"));
	ЗадачиНачалаРаботы.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	ЗадачиНачалаРаботы.Колонки.Добавить("ВАрхиве", Новый ОписаниеТипов("Булево"));
	
	ЗадачиБухгалтераПереопределяемый.ПриПолученииНовыхЗадачСнятияСУчета(ОтборОрганизации, ЗадачиНачалаРаботы);
	
	Возврат ЗадачиНачалаРаботы;
	
КонецФункции

Функция ЗавершитьЗадачуУказанияДатыЗакрытияИП()
	
	Завершить = Ложь;
	ЗадачиБухгалтераПереопределяемый.ОпределитьЗавершитьЗадачуУказанияДатыЗакрытияИП(Завершить);
	Возврат Завершить;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

// Заполняет статусы для существующих задач.
//
Процедура ЗаполнитьСтатусы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПравилаПредставленияОтчетовУплатыНалогов
	|			ТОГДА ЗадачиБухгалтера.Правило.Действие
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ПустаяСсылка)
	|	КОНЕЦ КАК Действие,
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.ВАрхиве КАК ВАрхиве,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ЗадачиБухгалтера.НачалоВыполнения КАК НачалоВыполнения,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ЗадачиБухгалтера.ДатаСоздания КАК ДатаСоздания
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера";
	
	ТаблицаЗадачи = Запрос.Выполнить().Выгрузить();
	
	ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(ТаблицаЗадачи);
	
	Набор = РегистрыСведений.ЗадачиБухгалтера.СоздатьНаборЗаписей();
	
	Набор.Загрузить(ТаблицаЗадачи);
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
	
КонецПроцедуры

// Заполняет новый реквизит "ХешЗадачи" в регистре сведений "ЗадачиБухгалтера".
//
Процедура ЗаполнитьХешЗадач() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.ХешЗадачи = &ПустаяСтрока";
	
	Результат = Запрос.Выполнить();
	
	НаборЗаписей = РегистрыСведений.ЗадачиБухгалтера.СоздатьНаборЗаписей();
	КлючиЗадач = КлючиЗадач();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Прочитать();
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Запись.ХешЗадачи = ХешЗаписи(Запись, КлючиЗадач);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтатусыОтчетов(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Истина;
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтера");
		ЭлементБлокировки.УстановитьЗначение("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
		Блокировка.Заблокировать();
		
		Набор = РегистрыСведений.ЗадачиБухгалтера.СоздатьНаборЗаписей();
		Набор.Отбор.Действие.Установить(Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
		Набор.Прочитать();
		
		ТаблицаЗадач = Набор.Выгрузить();
		
		ТаблицаЗаполненныеСтатусы = ТаблицаЗадач.СкопироватьКолонки();
		ТаблицаНезаполненныеСтатусы = ТаблицаЗадач.СкопироватьКолонки();
		
		Для Каждого СтрокаТаблицы Из ТаблицаЗадач Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.Статус) Тогда
				ЗаполнитьЗначенияСвойств(ТаблицаЗаполненныеСтатусы.Добавить(), СтрокаТаблицы);
			Иначе
				ЗаполнитьЗначенияСвойств(ТаблицаНезаполненныеСтатусы.Добавить(), СтрокаТаблицы);
			КонецЕсли;
		КонецЦикла;
		
		ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(ТаблицаНезаполненныеСтатусы);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаЗаполненныеСтатусы, ТаблицаНезаполненныеСтатусы);
		Набор.Загрузить(ТаблицаНезаполненныеСтатусы);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Параметры.ОбработкаЗавершена = Ложь;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить обработчик в процедуре ЗадачиБухгалтера.ЗаполнитьСтатусыОтчтеов() по причине:
				|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
			РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаполнитьСтатусыОплат(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Истина;
	
	СрокНачалаОбработки = ДобавитьМесяц(НачалоМесяца(ОбщегоНазначения.ТекущаяДатаПользователя()), -1);
	
	ОбрабатываемыеДействия = Новый Массив;
	ОбрабатываемыеДействия.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога);
	ОбрабатываемыеДействия.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбрабатываемыеДействия", ОбрабатываемыеДействия);
	Запрос.УстановитьПараметр("СрокНачалаОбработки", СрокНачалаОбработки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.Наименование КАК Наименование
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Действие В(&ОбрабатываемыеДействия)
	|	И НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).ВыполняетсяЕдинымПомощником, ЛОЖЬ)
	|	И ЗадачиБухгалтера.Статус = """"
	|	И ЗадачиБухгалтера.Срок >= &СрокНачалаОбработки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтера");
			ЭлементБлокировки.УстановитьЗначение("Организация", Выборка.Организация);
			ЭлементБлокировки.УстановитьЗначение("Правило", Выборка.Правило);
			ЭлементБлокировки.УстановитьЗначение("ПериодСобытия", Выборка.ПериодСобытия);
			ЭлементБлокировки.УстановитьЗначение("РегистрацияВНалоговомОргане", Выборка.РегистрацияВНалоговомОргане);
			ЭлементБлокировки.УстановитьЗначение("Действие", Выборка.Действие);
			Блокировка.Заблокировать();
			
			Набор = РегистрыСведений.ЗадачиБухгалтера.СоздатьНаборЗаписей();
			Набор.Отбор.Организация.Установить(Выборка.Организация);
			Набор.Отбор.Правило.Установить(Выборка.Правило);
			Набор.Отбор.ПериодСобытия.Установить(Выборка.ПериодСобытия);
			Набор.Отбор.РегистрацияВНалоговомОргане.Установить(Выборка.РегистрацияВНалоговомОргане);
			Набор.Отбор.Действие.Установить(Выборка.Действие);
			Набор.Прочитать();
			
			ТаблицаЗадач = Набор.Выгрузить();
			ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(ТаблицаЗадач);
			
			Набор.Загрузить(ТаблицаЗадач);
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать задачу ""%1"" в процедуре ЗадачиБухгалтера.ЗаполнитьСтатусыОплат() по причине:
					|%2'"),
					Выборка.Наименование,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
				РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Актуализирует значение измерения "Действие" в существующих задачах для правила уплаты НДФЛ - НДФЛ_Агент.2023_НДФЛ_Уплата.
// У данного правила ранее менялся реквизит "Действие", в существующих задачах могло остаться прежнее значение.
//
Процедура АктуализироватьДействиеВСуществующихЗадачахУплатыНДФЛ() Экспорт
	
	ПравилоУплатыНДФЛ = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("НДФЛ_Агент", "2023_НДФЛ_Уплата");
	Если Не ЗначениеЗаполнено(ПравилоУплатыНДФЛ) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не удалось выполнить обработчик актуализации вида действия в списке задач для задач уплаты НДФЛ. 
			|Не удалось найти соответствующее правило ""НДФЛ_Агент.2023_НДФЛ_Уплата""'");
		
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
			
		Возврат;
		
	КонецЕсли;
	
	АктуализироватьДействиеВСуществующихЗадачахПоПравилу(ПравилоУплатыНДФЛ);
	
КонецПроцедуры

// Обработчик обновления, заполняет задачи по требованиям ФНС с начала текущего года.
//
Процедура ЗаполнитьТребованияФНС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ";
	РезультатЗапроса = Запрос.Выполнить();
	СписокОрганизаций = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Дата = НачалоМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -3));
	Для Каждого Организация Из СписокОрганизаций Цикл
		Попытка
			Если ИнтерфейсыВзаимодействияБРО.ОрганизацияИмеетУчетнуюЗапись(Организация) Тогда
				
				ОбновитьИЗаполнитьТребованияФНС(Организация, Дата);
				
			КонецЕсли;
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить обработчик в процедуре ЗадачиБухгалтера.ЗаполнитьТребованияФНС по причине:
				|%1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления, обновляет задачи по требованиям ФНС за последние три месяца для организаций,
// по которым не установлен обязательный электронный документооборот.
//
// Параметры:
//  ПараметрыОбработчика - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗаполнитьТребованияФНСЭДОНеОбязателен(ПараметрыОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбязательностьЭДООрганизаций.Организация КАК Организация
	|ПОМЕСТИТЬ ОрганизацииЭДООбязателен
	|ИЗ
	|	РегистрСведений.ОбязательностьЭДООрганизаций КАК ОбязательностьЭДООрганизаций
	|ГДЕ
	|	ОбязательностьЭДООрганизаций.ЭДООбязателен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ
	|	И НЕ Организации.Ссылка В
	|				(ВЫБРАТЬ
	|					ОрганизацииЭДООбязателен.Организация КАК Организация
	|				ИЗ
	|					ОрганизацииЭДООбязателен КАК ОрганизацииЭДООбязателен)";
	РезультатЗапроса = Запрос.Выполнить();
	СписокОрганизаций = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Дата = НачалоМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -3));
	Для Каждого Организация Из СписокОрганизаций Цикл
		Попытка
			Если ИнтерфейсыВзаимодействияБРО.ОрганизацияИмеетУчетнуюЗапись(Организация) Тогда
				
				ОбновитьИЗаполнитьТребованияФНС(Организация, Дата);
				
			КонецЕсли;
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить обработчик в процедуре ЗадачиБухгалтера.ЗаполнитьТребованияФНС по причине:
				|%1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

// Обработчик обновления, заполняет задачи по документам СЭДО за последние три месяца,
// и включает флаг отправки уведомлений по документам СЭДО.
//
// Параметры:
//  ПараметрыОбработчика - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗаполнитьДокументыСЭДО(ПараметрыОбработчика) Экспорт
	
	УчетЗарплатыВЭтойПрограмме = Истина;
	ЗадачиБухгалтераПереопределяемый.ПроверитьУчетЗарплатыВЭтойПрограмме(УчетЗарплатыВЭтойПрограмме);
	Если Не УчетЗарплатыВЭтойПрограмме Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ";
	РезультатЗапроса = Запрос.Выполнить();
	СписокОрганизаций = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	НачалоПериода = НачалоМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -3));
	КонецПериода  = ДобавитьМесяц(ТекущаяДатаСеанса(), 1);
	
	Для Каждого Организация Из СписокОрганизаций Цикл
		Попытка
			Расписание = НовыйРасписание();
			ЗаполнитьРасписаниеДокументыСЭДО(Организация, Расписание, НачалоПериода, КонецПериода);
			
			ОтборДействие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО;
			ЗаписатьИзменения(
				Организация,
				Расписание,
				НачалоПериода,
				,
				ОтборДействие);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить обработчик в процедуре ЗадачиБухгалтера.ЗаполнитьДокументыСЭДО по причине:
				|%1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	// по умолчанию устанавливаем флаг необходимости отправки сообщений по электронной почте
	Попытка
		СписокЗадачУведомленияПоЭлектроннойПочте.УстановитьПоУмолчаниюОтправлятьУведомленияСЭДО();
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить обработчик в процедуре ЗадачиБухгалтера.ЗаполнитьДокументыСЭДО по причине:
			|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
	КонецПопытки;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

// Обработчик обновления, заполняет задачи по проверке основного вида деятельности.
//
// Параметры:
//  ПараметрыОбработчика - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗаполнитьЗадачиПроверкиВидаДеятельности(ПараметрыОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Не Организации.ПометкаУдаления
	|	И &УсловиеПоОрганизации";
	
	Если ОбщегоНазначенияБРО.ИспользуетсяАрхив() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоОрганизации", "НЕ Организации.ВАрхиве");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоОрганизации", "Истина");
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить();
	СписокОрганизаций = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ЗадачиПроверкиВидаДеятельности = ЗадачиПроверкиВидаДеятельности(СписокОрганизаций);
	Если ЗадачиПроверкиВидаДеятельности.Количество() = 0 Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Попытка
		
		Для Каждого Задача Из ЗадачиПроверкиВидаДеятельности Цикл
			
			Запись = СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Задача);
			Запись.Правило = Задача.Задача;
			Запись.Срок = ДатаПроверкиВидаДеятельности();
			Запись.Записать();
			
		КонецЦикла;
		
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить обработчик в процедуре ЗадачиБухгалтера.ЗаполнитьЗадачиПроверкиВидаДеятельности по причине:
			|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
	КонецПопытки;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

#КонецОбласти

Процедура СкорректироватьРасписаниеСУчетомЗакрытияОрганизации(Организация, Расписание, ТекущаяДата)
	
	ЗадачиБухгалтераПереопределяемый.СкорректироватьРасписаниеСУчетомЗакрытияОрганизации(Организация, Расписание, ТекущаяДата);
	
КонецПроцедуры

Функция ИсключитьЗадачуИзУплатыНалога(Правило, ВидНалогаПравила)
	
	// Уплата по некоторым видам налога обрабатывается отдельно от группы,
	// если в одной задаче 2 подчиненных правила с разным порядком выполнения
	
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	ИсключитьЗадачу = Ложь;
	
	Если (ВидНалогаПравила = Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ) Тогда
		ЭтоТравматизм = Не (ПолноеИмяПравила = "СтраховыеВзносы.2023_Уплата_Травматизм");
	Иначе
		
		Если (ВидНалогаПравила = Перечисления.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф) Тогда
			ИсключитьЗадачу = (ПолноеИмяПравила = ЗадачиБухгалтераКлиентСервер.ПолноеИмяПравилаСтраховыеВзносыИП_ВНиМ());
		ИначеЕсли ВидНалогаПравила = Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела Тогда
			ПолноеИмяПравилаСДоходов = СтрШаблон("%1.%2",
				ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносыИП(),
				ВыполнениеЗадачБухгалтера.ИдентификаторПравилаСтраховыеВзносыИПСДоходов());
			ИсключитьЗадачу = (ПолноеИмяПравилаСДоходов <> ПолноеИмяПравила);
		КонецЕсли;
		
		ЭтоТравматизм = (ПолноеИмяПравила = "СтраховыеВзносы.2023_Уплата_Травматизм");
		
	КонецЕсли;
	
	ЗадачиБухгалтераПереопределяемый.ПриОпределенииИсключитьЗадачуИзУплатыНалога(ИсключитьЗадачу, Правило, ВидНалогаПравила, ПолноеИмяПравила);
	
	Возврат (ЭтоТравматизм Или ИсключитьЗадачу);
	
КонецФункции

Процедура АктуализироватьДействиеВСуществующихЗадачахПоПравилу(Правило)
	
	Если Не ЗначениеЗаполнено(Правило) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	ДействиеПравила = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "Действие");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило",         Правило);
	Запрос.УстановитьПараметр("ДействиеПравила", ДействиеПравила);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Правило = &Правило
	|	И ЗадачиБухгалтера.Действие <> &ДействиеПравила";
	
	Выборка = Запрос.Выполнить().Выбрать();
	НаборЗаписей = РегистрыСведений.ЗадачиБухгалтера.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Правило.Установить(Правило);
			НаборЗаписей.Прочитать();
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.Действие = ДействиеПравила;
			КонецЦикла;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			НаборЗаписей.Очистить();
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить обработчик актуализации вида действия в списке задач для правила ""%1"" по причине:
					|%2'"),
					ИдентификаторПравила,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
				РегистрыСведений.ЗадачиБухгалтера,, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#Область УведомленияПоЭлектроннойПочте

Функция УведомленияПоЭлектроннойПочтеЗаголовок()
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		ЗаголовокПриложения = Константы.ЗаголовокСистемы.Получить();
		Если ПустаяСтрока(ЗаголовокПриложения) Тогда
			ЗаголовокПриложения = Метаданные.КраткаяИнформация;
		КонецЕсли;
	Иначе
		ЗаголовокПриложения = РаботаВМоделиСервиса.ПолучитьИмяПриложения();
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru = '%1: приближается срок выполнения задач'"), ЗаголовокПриложения);
	
КонецФункции

Функция УведомленияПоЭлектроннойПочтеПодзаголовок(Период)
	
	Возврат СтрШаблон(НСтр("ru = 'Выполните до %1:'"), Формат(Период, СтрШаблон("Л=%1; ДФ='d MMMM'", ОбщегоНазначения.КодОсновногоЯзыка())));
	
КонецФункции

Функция УведомленияПоЭлектроннойПочтеСтроки(ВыборкаЗадач)
	
	ШаблонСтроки = ПолучитьМакет("УведомлениеПоЭлектроннойПочтеСтрока").ПолучитьТекст();
	
	Результат = "";
	
	Пока ВыборкаЗадач.Следующий() Цикл
		Задача = ВыборкаЗадач.Наименование;
		
		МассивОрганизаций = Новый Массив;
		ВыборкаОрганизаций = ВыборкаЗадач.Выбрать();
		Пока ВыборкаОрганизаций.Следующий() Цикл
			МассивОрганизаций.Добавить(ВыборкаОрганизаций.Организация);
		КонецЦикла;
		Организации = СтрСоединить(МассивОрганизаций, ", ");
		
		ЗначенияПодстановки = Новый Структура;
		ЗначенияПодстановки.Вставить("ЗаголовокСтроки", Задача);
		ЗначенияПодстановки.Вставить("ПодзаголовокСтроки", Организации);
		Результат = Результат + СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонСтроки, ЗначенияПодстановки);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция УведомленияПоЭлектроннойПочтеКнопка()
	
	ШаблонКнопки = ПолучитьМакет("УведомлениеПоЭлектроннойПочтеКнопка").ПолучитьТекст();
	АдресИнформационнойБазы = СписокЗадачУведомленияПоЭлектроннойПочте.АдресИнформационнойБазы();
	
	Если ПустаяСтрока(АдресИнформационнойБазы) Тогда
		Кнопка = "";
	Иначе
		ПутьКСпискуЗадач = "ОбщаяКоманда.СписокЗадач";
		ЗадачиБухгалтераПереопределяемый.ПриПолученииПутиКСпискуЗадач(ПутьКСпискуЗадач);
		ЗначенияПодстановки = Новый Структура;
		ЗначенияПодстановки.Вставить("Гиперссылка", СтрШаблон("%1/#e1cib/command/%2", АдресИнформационнойБазы, ПутьКСпискуЗадач));
		Кнопка = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонКнопки, ЗначенияПодстановки);
	КонецЕсли;
	
	Возврат Кнопка;
	
КонецФункции

Функция ВыбратьЗадачиПоСрокуВыполнения(Период)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ФинальныеСтатусы", ФинальныеСтатусы());
	Запрос.УстановитьПараметр("ПравилаЕдиногоНалоговогоПлатежа", ВыполнениеЗадачБухгалтера.ПравилаЕдиногоНалоговогоПлатежа());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Срок = &Период
	|	И НЕ ЗадачиБухгалтера.Статус В (&ФинальныеСтатусы)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И НЕ ЗадачиБухгалтера.Правило В (&ПравилаЕдиногоНалоговогоПлатежа)
	|	И ЗадачиБухгалтера.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование,
	|	Организация
	|ИТОГИ ПО
	|	Наименование";
	
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

Функция ВыбратьЗадачиСЭДОТребующиеУведомления(Период)
	
	КоличествоСекундВСутках = 86400;
	КоличествоДней = 5; // Максимально возможный срок ответа по входящим запросам СЭДО
	Интервал5Дней = КоличествоДней * КоличествоСекундВСутках;
	НачалоПериода = НачалоДня(Период);
	КонецПериода  = НачалоПериода + Интервал5Дней;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода" , КонецПериода);
	Запрос.УстановитьПараметр("ФинальныеСтатусы", ФинальныеСтатусы());
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Срок МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ ЗадачиБухгалтера.Статус В (&ФинальныеСтатусы)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Срок,
	|	Наименование,
	|	Организация
	|ИТОГИ ПО
	|	Срок,
	|	Наименование";
	
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

Функция ФинальныеСтатусы()
	
	ФинальныеСтатусы = Новый Массив;
	ФинальныеСтатусы.Добавить(НСтр("ru = 'Выполнено'"));
	ФинальныеСтатусы.Добавить(НСтр("ru = 'Сдано'"));
	ФинальныеСтатусы.Добавить(НСтр("ru = 'Отклонено'"));
	ФинальныеСтатусы.Добавить(НСтр("ru = 'Просрочено'"));
	ФинальныеСтатусы.Добавить(НСтр("ru = 'Оплачено'"));
	ФинальныеСтатусы.Добавить(НСтр("ru = 'Ответ отправлен'"));
	ФинальныеСтатусы.Добавить(НСтр("ru = 'Прием подтвержден'"));
	
	Возврат ФинальныеСтатусы;
	
КонецФункции

#КонецОбласти

Функция ЭтоПравилоЕдиногоПомощника(Правило)
	
	Возврат Правило.Владелец.Код = "ЕдиныйНалоговыйСчет";
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НовыйРасписание() Экспорт

	Расписание = Новый ТаблицаЗначений;
	МетаданныеСпискаЗадач = Метаданные.РегистрыСведений.ЗадачиБухгалтера;
	
	Расписание.Колонки.Добавить("Организация",                 МетаданныеСпискаЗадач.Измерения.Организация.Тип);
	Расписание.Колонки.Добавить("РегистрацияВНалоговомОргане", МетаданныеСпискаЗадач.Измерения.РегистрацияВНалоговомОргане.Тип);
	Расписание.Колонки.Добавить("Правило",                     МетаданныеСпискаЗадач.Измерения.Правило.Тип);
	Расписание.Колонки.Добавить("ПериодСобытия",               МетаданныеСпискаЗадач.Измерения.ПериодСобытия.Тип);
	Расписание.Колонки.Добавить("Периодичность",               МетаданныеСпискаЗадач.Реквизиты.Периодичность.Тип);
	Расписание.Колонки.Добавить("Срок",                        МетаданныеСпискаЗадач.Реквизиты.Срок.Тип);
	Расписание.Колонки.Добавить("Наименование",                МетаданныеСпискаЗадач.Реквизиты.Наименование.Тип);
	Расписание.Колонки.Добавить("НаименованиеСокращенное",     МетаданныеСпискаЗадач.Реквизиты.НаименованиеСокращенное.Тип);
	Расписание.Колонки.Добавить("НачалоВыполнения",            МетаданныеСпискаЗадач.Реквизиты.НачалоВыполнения.Тип);
	Расписание.Колонки.Добавить("Действие",                    Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДействийКалендаряБухгалтера"));
	Расписание.Колонки.Добавить("УчитыватьНерабочиеДни",       Новый ОписаниеТипов("Булево")); // Считать нерабочие дни нерабочими.
	Расписание.Колонки.Добавить("НачальныйСрок",               МетаданныеСпискаЗадач.Реквизиты.НачальныйСрок.Тип);
	
	Возврат Расписание;

КонецФункции

Функция НовыйЗадачаСоСтатусом() Экспорт
	
	ЗадачаСоСтатусом = КлючиЗадачСтруктурой();
	ЗадачаСоСтатусом.Вставить("Статус", "");
	
	Возврат ЗадачаСоСтатусом;
	
КонецФункции

Функция СобытиеПозжеРелевантногоПериода(Выборка)
	
	Если ВыполнениеЗадачБухгалтераПовтИспНаВызов.ЭтоЗадачаЗакрытияИП(Выборка.ИдентификаторПравила) Тогда
		// Для задач закрытия ИП событие наступает раньше релевантного периода:
		// Сдать декларацию и оплатить налог НДФЛ, УСН, НДС после снятия ИП с учета нужно в конкретный срок за текущий год
		Результат = Ложь;
	Иначе
		Результат = КалендарьБухгалтера.СрокИстекаетПослеОкончанияРелевантногоПериода(Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДатаПроверкиВидаДеятельности() Экспорт
	
	Возврат Дата(2026, 02, 28);
	
КонецФункции

#КонецОбласти

#КонецЕсли
