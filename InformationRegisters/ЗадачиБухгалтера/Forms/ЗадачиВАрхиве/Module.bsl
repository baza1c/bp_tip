#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ЗадачиБухгалтераПереопределяемый.УстановитьОтборПоОсновнойОрганизации(ЭтотОбъект);
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ЗадачиБухгалтера) Тогда
		Элементы.ФормаВосстановитьИзАрхива.Доступность = Ложь;
		Элементы.СписокКонтекстноеМенюВосстановитьЗадачуИзАрхива.Доступность = Ложь;
	КонецЕсли;
	
	ЗадачиБухгалтераПереопределяемый.ПриСозданииНаСервереЗадачиВАрхиве(ЭтотОбъект);
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ЗадачиБухгалтераКлиентПереопределяемый.ИзменитьОтборПоОсновнойОрганизации(Список, , Параметр);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ЗадачиБухгалтераПереопределяемый.СписокПередЗагрузкойПользовательскихНастроекНаСервере(Список, Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыборЗапрещен = Ложь;
	ТекстПредупреждения = "";
	ПроверкаВыбораВСпискеЗадачиВАрхиве(ЭтотОбъект, ВыборЗапрещен, ТекстПредупреждения);
	Если ВыборЗапрещен Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ПараметрыКоманды = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
	ЗаполнитьЗначенияСвойств(ПараметрыКоманды, Элемент.ТекущиеДанные);
	
	Если ЕстьПравоВыполненияЗадачи(ПараметрыКоманды.Правило) Тогда
		ОписаниеДействия = ЗадачиБухгалтераКлиентСервер.ОписаниеДействия(ПараметрыКоманды);
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаУбратьИзВыполненных(Команда)
	
	КлючЗаписи = Элементы.Список.ТекущаяСтрока;
	
	Если КлючЗаписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Задача = Элементы.Список.ТекущиеДанные;
	СтатусОтчета = УбратьИзВыполненных(КлючЗаписи, Задача);

	ОповеститьОбИзменении(КлючЗаписи);
	
	// Оповестим форму списка задач бухгалтера. Возможно, необходимо сменить текущую страницу формы.
	// Например, если до добавления нового налога, все задачи на сегодня выполнены.
	Оповестить("СписокЗадачБухгалтера_Изменение");
	
	// При извлечении из архива статус задачи документа СЭДО не меняется
	Если Задача.Действие <> ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО") Тогда
		КалендарьОтчетностиКлиент.ОповеститьКалендарьОтчетности(Задача, СтатусОтчета, Задача.Комментарий);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЕстьПравоВыполненияЗадачи(Знач Правило)
	
	Возврат КалендарьБухгалтера.ПравоВыполненияЗадачи(Правило);
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ЗеленыйЛес);
	
	ЗадачиБухгалтераПереопределяемый.ДобавитьОформляемоеПоле(ЭлементУсловногоОформления.Поля, "Статус");
	
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУсловногоОформления.Отбор,
		"Список.Статус", ВидСравненияКомпоновкиДанных.НеРавно, СтатусыОтчета.НеТребуется);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УбратьИзВыполненных(КлючЗаписи, Знач Задача)
	
	Результат = Неопределено;
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	Если Задача.Статус = СтатусыОтчета.НеТребуется Тогда
		Результат = ВыполнениеЗадачБухгалтера.ВернутьАвтоматическийСтатусЗадачи(Задача);
	КонецЕсли;
	
	РегистрыСведений.ЗадачиБухгалтера.УстановитьСтатусВыполнено(КлючЗаписи, Ложь);
	
	Если Задача.ИдентификаторЗадачи = "ЕдиныйНалоговыйСчет" Тогда
	
		ЗадачиБухгалтераПереопределяемый.УбратьИзВыполненныхЕдиныйНалоговыйСчет(Задача);
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверкаВыбораВСпискеЗадачиВАрхиве(Форма, ВыборЗапрещен, ТекстПредупреждения)
	
	ЗадачиБухгалтераКлиентПереопределяемый.ПроверкаВыбораВСпискеЗадачиВАрхиве(Форма, ВыборЗапрещен, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	ЗадачиБухгалтераПереопределяемый.МобильныйКлиентАдаптацияФормыЗадачиВАрхиве(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти