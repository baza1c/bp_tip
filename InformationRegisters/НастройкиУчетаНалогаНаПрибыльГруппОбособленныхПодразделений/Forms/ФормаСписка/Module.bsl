#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастройкиУчетаФормаСписка.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// Настроим элементы отбора по региону
	// Не используем отбор через пользовательские настройки, т.к. список выбора региона настраивается специальной процедурой 
	Если Параметры.Отбор.Свойство("КодРегиона") Тогда
		ОтборКодРегиона              = XMLСтрока(Параметры.Отбор.КодРегиона);
		ОтборКодРегионаИспользование = Истина;
	КонецЕсли;
	
	НастройкиУчетаНалогаНаПрибыльФормы.ЗаполнитьСписокВыбораРегиона(Элементы.ОтборКодРегиона.СписокВыбора, Истина);
	
	Если Не ОтборКодРегионаИспользование Тогда
		
		// Устанавливаем значение отбора, но не включаем сам отбор
		КодРегионаОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборОрганизация, "РегистрацияВНалоговомОргане.КодРегиона");
		Если ЗначениеЗаполнено(КодРегионаОрганизации) Тогда
			ОтборКодРегиона = XMLСтрока(КодРегионаОрганизации);
		ИначеЕсли ЗначениеЗаполнено(Элементы.ОтборКодРегиона.СписокВыбора) Тогда
			ОтборКодРегиона = Элементы.ОтборКодРегиона.СписокВыбора[0];
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"КодРегионаСириус",
		ДанныеГосударственныхОрганов.КодРегионаФедеральнойТерриторииСириусЧислом());
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"НаименованиеРегионаСириус",
		ДанныеГосударственныхОрганов.НаименованиеФедеральнойТерриторииСириус());
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	ПараметрыОповещения = Новый Структура;
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ПараметрыОповещения.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;	
	Оповестить("Запись_НастройкиНалогаНаПрибыльГруппыОбособленныхПодразделений", ПараметрыОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКодРегионаИспользованиеПриИзменении(Элемент)
	
	УстановитьОтборПоРегиону();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКодРегионаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборКодРегиона) Тогда
		ОтборКодРегионаИспользование = Истина;
	КонецЕсли;
	
	УстановитьОтборПоРегиону();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборПоРегиону()
	
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 0));
	КодРегиона = ТипЧисло.ПривестиЗначение(ОтборКодРегиона);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		"КодРегиона",
		КодРегиона,
		,
		,
		ОтборКодРегионаИспользование);
				
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Настроим видимость колонки региона в списке
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НаименованиеРегиона");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборКодРегионаИспользование", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

#КонецОбласти
