
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьНастройки();
	
	Если Параметры.Свойство("ВключитьАвтозагрузку") И Не АвтозагрузкаВключена Тогда
		АвтозагрузкаВключена = Параметры.ВключитьАвтозагрузку;
		Модифицированность = Истина;
	КонецЕсли;
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		ВопросОСохраненииНастроек(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АвтозагрузкаВключенаПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньЗагрузкиЕжемесячныхОтчетовПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньЗагрузкиЕженедельныхОтчетовПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьOzon(Команда)
	
	ОткрытьНастройкуМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"), КоличествоOzon);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьWB(Команда)
	
	ОткрытьНастройкуМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"), КоличествоWB);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЯндекс(Команда)
	
	ОткрытьНастройкуМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"), КоличествоЯндекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	Если Модифицированность 
		ИЛИ ИзмененыНастройкиПодключенияМаркетплейсов Тогда
		
		СохранитьНастройки();
		Оповестить("ПараметрыАвтозагрузкиОтчетовМаркетплейсовИзменены");
	КонецЕсли;
		
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьНастройкуМаркетплейса(Маркетплейс, КоличествоНастроек = 0)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидМаркетплейса", Маркетплейс);
	ПараметрыФормы.Вставить("СкрытьАвтозагрузку", Истина);
	Если КоличествоНастроек = 1 И НЕ ИспользуетсяНесколькоОрганизаций Тогда
		Настройка = ЕдинственнаяНастройкаМаркетплейса(Маркетплейс);
		Если ЗначениеЗаполнено(Настройка) Тогда
			ПараметрыФормы.Вставить("Ключ", Настройка);
		КонецЕсли;
	КонецЕсли;
		
	ИмяФормыНастроек = СтрШаблон("Справочник.НастройкиИнтеграцииМаркетплейс.%1", 
		?(ЗначениеЗаполнено(Настройка)
			ИЛИ (НЕ ИспользуетсяНесколькоОрганизаций И КоличествоНастроек < 2), "ФормаОбъекта",  "ФормаСписка"));
	ОткрытьФорму(ИмяФормыНастроек, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ЕдинственнаяНастройкаМаркетплейса(ВидМаркетплейса)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиИнтеграцииМаркетплейс.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НЕ НастройкиИнтеграцииМаркетплейс.ПометкаУдаления
	|	И НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = &ВидМаркетплейса";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат Справочники.НастройкиИнтеграцииМаркетплейс.ПустаяСсылка();
	КонецЕсли;
		
КонецФункции

&НаСервере
Процедура ПриИзмененииНастроекПодключенияНаСервере()
	
	ЗаполнитьНастройки();
	УправлениеВидимостью();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостью()
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	ПериодичностьОтчетов = ОбменСМаркетплейс.ПериодичностьОтчетовМаркетплейсов();
	МаркетплейсыЕжемесячно = ПериодичностьОтчетов.МаркетплейсыЕжемесячно;
	МаркетплейсыЕженедельно = ПериодичностьОтчетов.МаркетплейсыЕженедельно;
	
	Элементы.ГруппаЯндекс.Видимость = ПолучитьФункциональнуюОпцию("ВедетсяОтгрузкаБезПереходаПраваСобственности");
	
	Если КоличествоOzon = 0 Тогда
		Элементы.ДобавитьOzon.Заголовок = НСтр("ru = 'Настроить подключение к Ozon'");
		Элементы.OzonПодключен.Видимость = Ложь;
		Элементы.OzonНеПодключен.Видимость = Истина;
	Иначе
		Элементы.OzonПодключен.Видимость = Истина;
		Элементы.OzonНеПодключен.Видимость = Ложь;
		Если КоличествоOzon = 1 И (НЕ ИспользуетсяНесколькоОрганизаций) Тогда
			Элементы.ДобавитьOzon.Заголовок = НСтр("ru = 'Редактировать'");
		Иначе
			Элементы.ДобавитьOzon.Заголовок = НСтр("ru = 'Открыть список'");
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоWB = 0 Тогда
		Элементы.ДобавитьWB.Заголовок = НСтр("ru = 'Настроить подключение к Wildberries'");
		Элементы.WBПодключен.Видимость = Ложь;
		Элементы.WBНеПодключен.Видимость = Истина;
	Иначе
		Элементы.WBПодключен.Видимость = Истина;
		Элементы.WBНеПодключен.Видимость = Ложь;
		Если КоличествоWB = 1 И (НЕ ИспользуетсяНесколькоОрганизаций) Тогда
			Элементы.ДобавитьWB.Заголовок = НСтр("ru = 'Редактировать'");
		Иначе
			Элементы.ДобавитьWB.Заголовок = НСтр("ru = 'Открыть список'");
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоЯндекс = 0 Тогда
		Элементы.ДобавитьЯндекс.Заголовок = НСтр("ru = 'Настроить подключение к Яндекс.Маркет'");
		Элементы.ЯМПодключен.Видимость = Ложь;
		Элементы.ЯМНеПодключен.Видимость = Истина;
	Иначе
		Элементы.ЯМПодключен.Видимость = Истина;
		Элементы.ЯМНеПодключен.Видимость = Ложь;
		Если КоличествоЯндекс = 1 И (НЕ ИспользуетсяНесколькоОрганизаций) Тогда
			Элементы.ДобавитьЯндекс.Заголовок = НСтр("ru = 'Редактировать'");
		Иначе
			Элементы.ДобавитьЯндекс.Заголовок = НСтр("ru = 'Открыть список'");
		КонецЕсли;
	КонецЕсли;
	
	Элементы.АвтозагрузкаВключена.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	
	Элементы.ГруппаПараметры.Видимость = АвтозагрузкаВключена;
	
	Элементы.ГруппаЕженедельныеОтчеты.Видимость = ЕстьЕженедельныеОтчеты;
	Элементы.ГруппаЕжемесячныеОтчеты.Видимость = ЕстьЕжемесячныеОтчеты;
	
	Элементы.ЗаголовокЕжемесячныеОтчетыДеньЗагрузки.Заголовок =
		СтрШаблон(НСтр("ru = 'Загружать отчеты %1 с'"),
		МаркетплейсыЕжемесячно);
	
	Элементы.ЗаголовокЕженедельныеОтчетыДеньЗагрузки.Заголовок =
		СтрШаблон(НСтр("ru = 'Загружать отчеты %1 по'"),
		МаркетплейсыЕженедельно);
	
	Элементы.ЕжемесячныеОтчетыСледующаяЗагрузка.Заголовок =
		НастройкиАвтозагрузки.ПредставлениеСледующейЗагрузки(Перечисления.Периодичность.Месяц, ДеньЗагрузкиЕжемесячныхОтчетов);
	
	Элементы.ЕженедельныеОтчетыСледующаяЗагрузка.Заголовок = 
		НастройкиАвтозагрузки.ПредставлениеСледующейЗагрузки(Перечисления.Периодичность.Неделя, ДеньЗагрузкиЕженедельныхОтчетов);
	
КонецПроцедуры

#Область ЧтениеЗаписьНастроек

&НаСервере
Процедура ЗаполнитьНастройки()
	
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	ПериодичностьОтчетов = ОбменСМаркетплейс.ПериодичностьОтчетовМаркетплейсов();
	ЕстьЕжемесячныеОтчеты = ПериодичностьОтчетов.Ежемесячно;
	ЕстьЕженедельныеОтчеты = ПериодичностьОтчетов.Еженедельно;
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	АвтозагрузкаВключена = НастройкиАвтозагрузки.АвтозагрузкаВключена();
	
	Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя;
	ДеньЗагрузкиПоУмолчанию = НастройкиАвтозагрузки.ДеньЗагрузкиПоУмолчанию(Периодичность);
	ДеньЗагрузкиЕженедельныхОтчетов = НастройкиАвтозагрузки.ЗначениеПараметраАвтозагрузки(Периодичность, "ДеньЗагрузки", ДеньЗагрузкиПоУмолчанию);
	
	Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц;
	ДеньЗагрузкиПоУмолчанию = НастройкиАвтозагрузки.ДеньЗагрузкиПоУмолчанию(Периодичность);
	ДеньЗагрузкиЕжемесячныхОтчетов = НастройкиАвтозагрузки.ЗначениеПараметраАвтозагрузки(Периодичность, "ДеньЗагрузки", ДеньЗагрузкиПоУмолчанию);
	
	//Количество настроек
	КоличествоOzon   = 0;
	КоличествоWB     = 0;
	КоличествоЯндекс = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса КАК ВидМаркетплейса,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НастройкиИнтеграцииМаркетплейс.Ссылка) КАК КоличествоСсылок
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НЕ НастройкиИнтеграцииМаркетплейс.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса";
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Если Результат.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON Тогда
			КоличествоOzon = КоличествоOzon + Результат.КоличествоСсылок;
		ИначеЕсли Результат.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
			КоличествоWB = КоличествоWB + Результат.КоличествоСсылок;
		ИначеЕсли Результат.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
			КоличествоЯндекс = КоличествоЯндекс + Результат.КоличествоСсылок;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНастроек(ЗакрытиеФормы = Ложь)
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, "Да");
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	
	ТекстВопроса = НСтр("ru = 'Настройки не были сохранены.
		|Продолжить без сохранения?'");
	
	Оповещение = Новый ОписаниеОповещения("ВопросОСохраненииНастроекПродолжение", ЭтотОбъект, ЗакрытиеФормы);
	ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНастроекПродолжение(Ответ, ЗакрытиеФормы) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если ЗакрытиеФормы Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	Если АвтозагрузкаВключена Тогда
		Если ЕстьЕженедельныеОтчеты Тогда
			НовыеНастройки = НастройкиАвтозагрузки.НовыеПараметрыНастройки();
			НовыеНастройки.ДеньЗагрузки = ДеньЗагрузкиЕженедельныхОтчетов;
			НовыеНастройки.ИдентификаторЗадания = НастройкиАвтозагрузки.ИдентификаторЗадания(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя);
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя, НовыеНастройки);
		Иначе
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя);
		КонецЕсли;
		Если ЕстьЕжемесячныеОтчеты Тогда
			НовыеНастройки = НастройкиАвтозагрузки.НовыеПараметрыНастройки();
			НовыеНастройки.ИдентификаторЗадания = НастройкиАвтозагрузки.ИдентификаторЗадания(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц);
			НовыеНастройки.ДеньЗагрузки = ДеньЗагрузкиЕжемесячныхОтчетов;
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц, НовыеНастройки);
		Иначе
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц);
		КонецЕсли;
		Если ИзмененыНастройкиПодключенияМаркетплейсов Тогда
			НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(Неопределено);
			НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Автозагрузка из маркетплейсов в фоне'");
			НастройкиЗапуска.ЗапуститьВФоне = Истина;
			НастройкиЗапуска.ОжидатьЗавершение = 0;
			ДлительныеОперации.ВыполнитьВФоне("ОбменСМаркетплейс.ЗапуститьАвтозагрузку", , НастройкиЗапуска);
		КонецЕсли;
	Иначе
		НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя);
		НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьНастройкаИнтеграцииМаркетплейс" Тогда
		ИзмененыНастройкиПодключенияМаркетплейсов = Истина;
		ПриИзмененииНастроекПодключенияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти