#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция НовыеПараметрыНастройки() Экспорт
	
	ПараметрыАвтозагрузки = Новый Структура;
	ПараметрыАвтозагрузки.Вставить("ДеньЗагрузки", 0);
	ПараметрыАвтозагрузки.Вставить("ДатаПоследнейЗагрузки", '00010101');
	ПараметрыАвтозагрузки.Вставить("ИдентификаторЗадания", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	ПараметрыАвтозагрузки.Вставить("ОшибкаЗагрузки", Ложь);
	
	Возврат ПараметрыАвтозагрузки;
	
КонецФункции

Функция АвтозагрузкаВключена() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Настройки.Периодичность КАК Периодичность
	|ИЗ
	|	РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов КАК Настройки";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Проверяет есть ли настройка автозагрузки для периодичности получения отчетов маркетплейса
//
// Параметры:
//  Маркетплейс - ПеречислениеСсылка.ВидыМаркетплейсов - маркетплейс
// 
// Возвращаемое значение:
//  Булево - наличие настройки
//
Функция ПодключенаАвтозагрузкаДляМаркетплейса(Маркетплейс) Экспорт
	
	Периодичность = ПериодичностьМаркетплейса(Маркетплейс);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.Периодичность КАК Периодичность
	|ИЗ
	|	РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов КАК Настройки
	|ГДЕ
	|	Настройки.Периодичность = &Периодичность";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура СохранитьНастройки(Периодичность, НовыеНастройки = Неопределено) Экспорт
	
	ПараметрыРегламентногоЗадания = ПараметрыРегламентногоЗадания(Периодичность);
	НаименованиеЗадания = ПараметрыРегламентногоЗадания.НаименованиеЗадания;
	АвтозагрузкаВключена = НовыеНастройки <> Неопределено
		И ПараметрыРегламентногоЗадания.ЕстьДействующиеПодключения;
	
	НачатьТранзакцию();
	
	Попытка
		
		// Заблокируем регистр с настройками на время записи настроек и регламентного задания
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов");
		ЭлементБлокировки.УстановитьЗначение("Периодичность", Периодичность);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Периодичность.Установить(Периодичность);
		Если НовыеНастройки <> Неопределено Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Периодичность = Периодичность;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, НовыеНастройки);
		КонецЕсли;
		
		УдалитьЗадание = Ложь;
		
		Если Не ОбщегоНазначения.РазделениеВключено() Тогда
			Если АвтозагрузкаВключена И НовыеНастройки <> Неопределено Тогда
				МоментЗапуска = БлижайшаяЗагрузка(Периодичность, НовыеНастройки.ДеньЗагрузки);
				ДатаСервера = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
				Если ДатаСервера > МоментЗапуска Тогда
					ДатаНачала = КонецДня(ДатаСервера) + 1;
				Иначе
					ДатаНачала = МоментЗапуска;
				КонецЕсли;
				
				ПараметрыЗаписиЗадания = ОбменСМаркетплейс.НовыеПараметрыЗаписиРегламентногоЗаданияАвтозагрузкиОтчетов();
				ПараметрыЗаписиЗадания.ДатаНачала    = НачалоДня(ДатаНачала);
				ПараметрыЗаписиЗадания.ВремяЗапуска  = МоментЗапуска;
				ПараметрыЗаписиЗадания.Периодичность = Периодичность;
				ПараметрыЗаписиЗадания.Наименование  = НаименованиеЗадания;
				ПараметрыЗаписиЗадания.ДеньЗагрузки  = НовыеНастройки.ДеньЗагрузки;
				ПараметрыЗаписиЗадания.ИдентификаторЗадания = НовыеНастройки.ИдентификаторЗадания;
				Задание = ОбменСМаркетплейс.ЗаписатьРегламентноеЗаданиеАвтозагрузкиОтчетов(ПараметрыЗаписиЗадания);
				
				// Обновим идентификатор задания, на случай если он изменился.
				ИдентификаторЗадания = Задание.УникальныйИдентификатор;
				
				НоваяЗапись.ИдентификаторЗадания = ИдентификаторЗадания;
			Иначе
				УдалитьЗадание = Истина;
			КонецЕсли;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если УдалитьЗадание Тогда
			ИдентификаторЗадания = ЗначениеПараметраАвтозагрузки(Периодичность, "ИдентификаторЗадания");
			Если ИдентификаторЗадания <> Неопределено Тогда
				РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторЗадания);
			КонецЕсли;
		КонецЕсли;
		НаборЗаписей.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ИнформацияОбОшибке;
	КонецПопытки;
	
	ОбменСМаркетплейс.ЗапланироватьАвтозагрузкуОтчетовМаркетплейсовДляОбластиДанных(Периодичность);
	
КонецПроцедуры

Процедура СохранитьНастройкиПоУмолчанию(Маркетплейс) Экспорт
	
	Периодичность = ПериодичностьМаркетплейса(Маркетплейс);
	Настройки = Настройки(Периодичность);
	СохранитьНастройки(Периодичность, Настройки);
	
КонецПроцедуры

Функция ЕстьОшибкаЗагрузки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.ОшибкаЗагрузки КАК ОшибкаЗагрузки
	|ИЗ
	|	РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов КАК Настройки";
	
	ОшибкаЗагрузки = Ложь;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОшибкаЗагрузки = ОшибкаЗагрузки Или Выборка.ОшибкаЗагрузки;
	КонецЦикла;
	
	Возврат ОшибкаЗагрузки;
	
КонецФункции

Процедура СохранитьОшибкуЗагрузки(Периодичность) Экспорт
	
	ДатаПоследнейЗагрузки = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	
	ПараметрыАвтозагрузки = Настройки(Периодичность);
	ПараметрыАвтозагрузки.Вставить("ДатаПоследнейЗагрузки", ДатаПоследнейЗагрузки);
	ПараметрыАвтозагрузки.Вставить("ОшибкаЗагрузки", Истина);
	
	СохранитьПараметры(Периодичность, ПараметрыАвтозагрузки);
	
КонецПроцедуры

Функция ДатаПоследнейЗагрузки() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(Настройки.ДатаПоследнейЗагрузки), ДАТАВРЕМЯ(1,1,1)) КАК ДатаПоследнейЗагрузки
	|ИЗ
	|	РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов КАК Настройки";
	
	ДатаПоследнейЗагрузки = Дата(1, 1, 1);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатаПоследнейЗагрузки = Выборка.ДатаПоследнейЗагрузки;
	КонецЕсли;
	
	Возврат ДатаПоследнейЗагрузки;
	
КонецФункции

Процедура СохранитьДатуПоследнейЗагрузки(Периодичность) Экспорт
	
	ДатаПоследнейЗагрузки = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	
	ПараметрыАвтозагрузки = Настройки(Периодичность);
	ПараметрыАвтозагрузки.Вставить("ДатаПоследнейЗагрузки", ДатаПоследнейЗагрузки);
	
	СохранитьПараметры(Периодичность, ПараметрыАвтозагрузки);
	
КонецПроцедуры

Функция ИдентификаторЗадания(Периодичность) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.ИдентификаторЗадания КАК ИдентификаторЗадания
	|ИЗ
	|	РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов КАК Настройки
	|ГДЕ
	|	Настройки.Периодичность = &Периодичность";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИдентификаторЗадания;
	Иначе
		Возврат ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор();
	КонецЕсли;
	
КонецФункции

Функция ДеньЗагрузкиПоУмолчанию(Периодичность) Экспорт
	
	Если Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя Тогда
		Возврат ДеньЗагрузкиЕженедельныхОтчетовПоУмолчанию();
	Иначе
		Возврат ДеньЗагрузкиЕжемесячныхОтчетовПоУмолчанию();
	КонецЕсли;
	
КонецФункции

Функция ПредставлениеСледующейЗагрузки(Периодичность, ДеньЗагрузки) Экспорт
	
	Результат = "";
	
	Если Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя Тогда
		ДатаСледующейЗагрузки = БлижайшаяЕженедельнаяЗагрузка(ДеньЗагрузки, Ложь);
		ШаблонСледующаяЗагрузка = НСтр("ru = 'Следующая загрузка отчетов будет выполнена %1 (%2)'");
		СледующаяЗагрузка = СтрШаблон(ШаблонСледующаяЗагрузка,
			Формат(ДатаСледующейЗагрузки, "ДФ=dd.MM.yyyy"),
			Формат(ДатаСледующейЗагрузки, "ДФ=дддд"));
		Результат = Новый ФорматированнаяСтрока(СледующаяЗагрузка);
	Иначе
		ДатаСледующейЗагрузки = БлижайшаяЕжемесячнаяЗагрузка(ДеньЗагрузки, Ложь);
		ШаблонСледующаяЗагрузка = НСтр("ru = 'Следующая загрузка отчетов будет выполнена %1'");
		СледующаяЗагрузка = СтрШаблон(ШаблонСледующаяЗагрузка,
			Формат(ДатаСледующейЗагрузки, "ДФ=dd.MM.yyyy"));
		Результат = Новый ФорматированнаяСтрока(СледующаяЗагрузка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеСледующейЗагрузкиДляМаркетплейса(Маркетплейс) Экспорт
	
	Периодичность = ПериодичностьМаркетплейса(Маркетплейс);
	ДеньЗагрузкиПоУмолчанию = ДеньЗагрузкиПоУмолчанию(Периодичность);
	ДеньЗагрузки = ЗначениеПараметраАвтозагрузки(Периодичность, "ДеньЗагрузки", ДеньЗагрузкиПоУмолчанию);
	
	Возврат ПредставлениеСледующейЗагрузки(Периодичность, ДеньЗагрузки);
	
КонецФункции

Функция ЗначениеПараметраАвтозагрузки(Периодичность, ИмяПараметра, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Перем Значение;
	
	Настройки = Настройки(Периодичность);
	
	Настройки.Свойство(ИмяПараметра, Значение);
	
	Если ЗначениеЗаполнено(Значение) Тогда
		Возврат Значение;
	Иначе
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
КонецФункции

Функция НастройкиРегламентныхЗаданий(Периодичность) Экспорт
	
	Результат = ОбменСМаркетплейс.НовыеПараметрыЗаписиРегламентногоЗаданияАвтозагрузкиОтчетов();
	
	ПараметрыРегламентногоЗадания = ПараметрыРегламентногоЗадания(Периодичность);
	ЕстьДействующиеПодключения = ПараметрыРегламентногоЗадания.ЕстьДействующиеПодключения;
	НаименованиеЗадания = ПараметрыРегламентногоЗадания.НаименованиеЗадания;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	Запрос.УстановитьПараметр("НаименованиеЗадания", НаименованиеЗадания);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиАвтозагрузкиОтчетовМаркетплейсов.Периодичность КАК Периодичность,
	|	НастройкиАвтозагрузкиОтчетовМаркетплейсов.ДеньЗагрузки КАК ДеньЗагрузки,
	|	НастройкиАвтозагрузкиОтчетовМаркетплейсов.ИдентификаторЗадания КАК ИдентификаторЗадания
	|ИЗ
	|	РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов КАК НастройкиАвтозагрузкиОтчетовМаркетплейсов
	|ГДЕ
	|	НастройкиАвтозагрузкиОтчетовМаркетплейсов.Периодичность = &Периодичность";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		Результат.Наименование = НаименованиеЗадания;
		
		МоментЗапуска = БлижайшаяЗагрузка(Периодичность, Выборка.ДеньЗагрузки);
		Результат.ВремяЗапуска = МоментЗапуска;
		ДатаСервера = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
		Если ДатаСервера > МоментЗапуска Тогда
			Результат.ДатаНачала = КонецДня(ДатаСервера) + 1;
		Иначе
			Результат.ДатаНачала = НачалоДня(МоментЗапуска);
		КонецЕсли;
		
		Результат.Вставить("ЕстьДействующиеПодключения", ЕстьДействующиеПодключения);
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыРегламентногоЗадания(Периодичность)
	
	ШаблонНаименования = НСтр("ru = 'Автоматическая загрузка отчетов (%1)'");
	ПериодичностьОтчетовМаркетплейсов = ОбменСМаркетплейс.ПериодичностьОтчетовМаркетплейсов();
	Если Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя Тогда
		ЕстьДействующиеПодключения = ПериодичностьОтчетовМаркетплейсов.Еженедельно;
		НаименованиеЗадания = СтрШаблон(ШаблонНаименования, ПериодичностьОтчетовМаркетплейсов.МаркетплейсыЕженедельно);
	Иначе
		ЕстьДействующиеПодключения = ПериодичностьОтчетовМаркетплейсов.Ежемесячно;
		НаименованиеЗадания = СтрШаблон(ШаблонНаименования, ПериодичностьОтчетовМаркетплейсов.МаркетплейсыЕжемесячно);
	КонецЕсли;
	
	Возврат Новый Структура("ЕстьДействующиеПодключения, НаименованиеЗадания",
		ЕстьДействующиеПодключения, НаименованиеЗадания);
	
КонецФункции

Функция ПериодичностьМаркетплейса(Маркетплейс)
	
	Если Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		Возврат Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя;
	Иначе
		Возврат Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц;
	КонецЕсли;
	
КонецФункции

Функция БлижайшаяЗагрузка(Периодичность, ДеньЗагрузки) Экспорт
	
	Если Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя Тогда
		Возврат БлижайшаяЕженедельнаяЗагрузка(ДеньЗагрузки);
	Иначе
		Возврат БлижайшаяЕжемесячнаяЗагрузка(ДеньЗагрузки);
	КонецЕсли;
	
КонецФункции

Функция БлижайшаяЕженедельнаяЗагрузка(Знач ДеньНедели, ОпределятьВремя = Истина)
	
	Если ДеньНедели = 0 Тогда
		Возврат '00010101'
	КонецЕсли;
	
	ТекущаяДата = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	ТекущийДеньНедели = ДеньНедели(ТекущаяДата);
	
	Если ОпределятьВремя Тогда
		ВремяЗагрузки = ОпределитьСлучайноеВремяЗагрузки();
	Иначе
		ВремяЗагрузки = 0;
	КонецЕсли;
	
	Если ТекущийДеньНедели < ДеньНедели Тогда
		НачальнаяДата = НачалоНедели(ТекущаяДата) + ВремяЗагрузки;
	Иначе
		НачальнаяДата = КонецНедели(ТекущаяДата) + 1 + ВремяЗагрузки;
	КонецЕсли;
	
	Возврат ОбщегоНазначенияБПКлиентСервер.ДобавитьПериод(
		НачальнаяДата,
		Перечисления.Периодичность.День,
		ДеньНедели - 1);
	
КонецФункции

Функция БлижайшаяЕжемесячнаяЗагрузка(Знач ЧислоМесяца, ОпределятьВремя = Истина)
	
	Если ЧислоМесяца = 0 Тогда
		Возврат '00010101'
	КонецЕсли;
	
	ТекущаяДата = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	ТекущийДеньМесяца = День(ТекущаяДата);
	
	Если ОпределятьВремя Тогда
		ВремяЗагрузки = ОпределитьСлучайноеВремяЗагрузки();
	Иначе
		ВремяЗагрузки = 0;
	КонецЕсли;
	
	Если ТекущийДеньМесяца < ЧислоМесяца Тогда
		НачальнаяДата = НачалоМесяца(ТекущаяДата) + ВремяЗагрузки;
	Иначе
		НачальнаяДата = КонецМесяца(ТекущаяДата) + 1 + ВремяЗагрузки;
	КонецЕсли;
		
	ЧислоМесяца = Мин(ЧислоМесяца, День(КонецМесяца(НачальнаяДата)));
	
	Возврат ОбщегоНазначенияБПКлиентСервер.ДобавитьПериод(
		НачальнаяДата,
		Перечисления.Периодичность.День,
		ЧислоМесяца - 1);
	
КонецФункции

Функция ОпределитьСлучайноеВремяЗагрузки()
	
	// Для серверных баз установим запуск регламентного задания на нерабочее время, чтобы избежать повышенной нагрузки
	// на сервис и не расходовать лимит обращений к сервису, которые могут понадобиться пользователю во время работы.
	// Для файловых баз необходимо запускать задание в рабочее время,
	// так как если база не запущена, то задание не выполнится.
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ВремяНачала = 13 * 60 + 30; // с 13:30 по 17:30
		ДлительностьПериода = 60 * 4;
	Иначе
		ВремяНачала = 19 * 60; // с 00:00 по 07:00 или с 19:00 по 00:00
		ДлительностьПериода = 60 * 12;
	КонецЕсли;
	
	ГенераторЧисел = Новый ГенераторСлучайныхЧисел();
	СлучайноеВремя = ВремяНачала + ГенераторЧисел.СлучайноеЧисло(0, ДлительностьПериода);
	СлучайноеВремя = СлучайноеВремя % (24 * 60); // не переходим на следующий день
	СлучайноеВремя = СлучайноеВремя * 60;
	Возврат СлучайноеВремя;
	
КонецФункции

Функция ДеньЗагрузкиЕженедельныхОтчетовПоУмолчанию()
	
	Возврат 3;
	
КонецФункции

Функция ДеньЗагрузкиЕжемесячныхОтчетовПоУмолчанию()
	
	Возврат 7;
	
КонецФункции

Процедура СохранитьПараметры(Периодичность, НовыеНастройки)
	
	ТекущиеНастройки = Настройки(Периодичность);
		
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Периодичность.Установить(Периодичность);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Периодичность = Периодичность;
	ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущиеНастройки);
	Для Каждого Параметр Из НовыеНастройки Цикл
		Если ЗначениеЗаполнено(Параметр.Значение) Тогда
			НоваяЗапись[Параметр.Ключ] = Параметр.Значение;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция Настройки(Периодичность)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.ДеньЗагрузки КАК ДеньЗагрузки,
	|	Настройки.ДатаПоследнейЗагрузки КАК ДатаПоследнейЗагрузки,
	|	Настройки.ИдентификаторЗадания КАК ИдентификаторЗадания,
	|	Настройки.ОшибкаЗагрузки КАК ОшибкаЗагрузки
	|ИЗ
	|	РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов КАК Настройки
	|ГДЕ
	|	Настройки.Периодичность = &Периодичность";
	
	ПараметрыНастройки = НовыеПараметрыНастройки();
	ПараметрыНастройки.ДеньЗагрузки = ДеньЗагрузкиПоУмолчанию(Периодичность);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыНастройки, Выборка);
	КонецЕсли;
	
	Возврат ПараметрыНастройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли