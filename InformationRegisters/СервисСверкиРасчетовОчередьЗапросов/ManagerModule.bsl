#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗаблокированныеМесяцаДляПолученияРасхождений(Организация, ДатаНачала, ДатаОкончания) Экспорт
	
	ЗаблокированныеМесяца = Новый массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("ДатаНачала",        НачалоМесяца(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",     КонецМесяца(ДатаОкончания));
	Запрос.УстановитьПараметр("ТекущаяДатаСеанса", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ТаймаутБлокировки", 60); // если блокировка установлена более 60 минут, то не учитываем
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовОчередьЗапросов.Организация КАК Организация,
	|	СервисСверкиРасчетовОчередьЗапросов.ПериодСверки КАК ПериодСверки
	|
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОчередьЗапросов КАК СервисСверкиРасчетовОчередьЗапросов
	|ГДЕ
	|	СервисСверкиРасчетовОчередьЗапросов.Организация = &Организация
	|	И СервисСверкиРасчетовОчередьЗапросов.ПериодСверки >= &ДатаНачала
	|	И СервисСверкиРасчетовОчередьЗапросов.ПериодСверки <= &ДатаОкончания
	|	И СервисСверкиРасчетовОчередьЗапросов.ЗапросОбрабатывается = ИСТИНА
	|	И РАЗНОСТЬДАТ(СервисСверкиРасчетовОчередьЗапросов.ВремяБлокировки, &ТекущаяДатаСеанса,  МИНУТА) < &ТаймаутБлокировки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодСверки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		ЗаблокированныеМесяца = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ПериодСверки");
	КонецЕсли;
	
	Возврат ЗаблокированныеМесяца;
	
КонецФункции

Процедура ОбновитьОчередь(Организация, Отрезки, ЗапросОбрабатывается, ТекущаяДатаСеанса, ОшибкаПолученияДанных = Ложь) Экспорт
	
	Для Каждого Отрезок Из Отрезки Цикл
		ТекущийМесяцОтрезка  = НачалоМесяца(Отрезок.ДатаНачала);
		КонечныйМесяцОтрезка = НачалоМесяца(Отрезок.ДатаОкончания);
		
		Пока ТекущийМесяцОтрезка <= КонечныйМесяцОтрезка Цикл
			НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Организация);
			НаборЗаписей.Отбор.ПериодСверки.Установить(НачалоМесяца(ТекущийМесяцОтрезка));
			
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				Запись = НаборЗаписей[0];
			Иначе
				Запись = НаборЗаписей.Добавить();
			КонецЕсли;
			Запись.Организация          = Организация;
			Запись.ПериодСверки         = НачалоМесяца(ТекущийМесяцОтрезка);
			Запись.ЗапросОбрабатывается = ЗапросОбрабатывается;
			Если Запись.ЗапросОбрабатывается Тогда
				Запись.ВремяБлокировки = ТекущаяДатаСеанса;
			Иначе
				Если Не ОшибкаПолученияДанных Тогда
					Запись.ДатаПолученияСведений = ТекущаяДатаСеанса;
				КонецЕсли;
				Запись.ВремяБлокировки       = '00010101';
			КонецЕсли;
			ТекущийМесяцОтрезка = ДобавитьМесяц(ТекущийМесяцОтрезка, 1);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ДатаАктуальностиРасхождений(Организация, ДатаНачала, ДатаОкончания) Экспорт
	
	ДатаАктуальностиРасхождений = '00010101';
	
	ДатаОкончанияПриведенная = КонецМесяца(ДатаОкончания);
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Если ДатаОкончанияПриведенная > КонецМесяца(ТекущаяДата) Тогда
		// Дата получения актуальности не позже конца текущего месяца.
		ДатаОкончанияПриведенная = КонецМесяца(ТекущаяДата);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",        Организация);
	Запрос.УстановитьПараметр("ОтборОрганизация",   ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("ДатаНачала",         НачалоМесяца(ДатаНачала));
	Запрос.УстановитьПараметр("ОтборДатаНачала",    ЗначениеЗаполнено(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",      ДатаОкончанияПриведенная);
	Запрос.УстановитьПараметр("ОтборДатаОкончания", ЗначениеЗаполнено(ДатаОкончания));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СервисСверкиРасчетовОчередьЗапросов.Организация КАК Организация,
	|	СервисСверкиРасчетовОчередьЗапросов.ДатаПолученияСведений КАК ДатаПолученияСведений
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОчередьЗапросов КАК СервисСверкиРасчетовОчередьЗапросов
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтборОрганизация
	|				ТОГДА СервисСверкиРасчетовОчередьЗапросов.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ОтборДатаНачала
	|				ТОГДА СервисСверкиРасчетовОчередьЗапросов.ПериодСверки >= &ДатаНачала
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ОтборДатаОкончания
	|				ТОГДА СервисСверкиРасчетовОчередьЗапросов.ПериодСверки <= &ДатаОкончания
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПолученияСведений";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДатаАктуальностиРасхождений = Выборка.ДатаПолученияСведений;
	КонецЕсли;
	
	Возврат ДатаАктуальностиРасхождений;
	
КонецФункции

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиОбновления

Процедура УстановитьДатуПолученияРасхожденийПоОрганизациям() Экспорт
	
	Если Не СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПолученияСведений = Константы.ДатаПолученияДанныхСервисаСверкиРасчетов.Получить();
	Если Не ЗначениеЗаполнено(ДатаПолученияСведений) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
	|	МИНИМУМ(СервисСверкиРасчетовРеестрДокументов.ДатаДокумента) КАК ДатаДокумента
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|ГДЕ
	|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента >= &МинимальнаяДата
	|
	|СГРУППИРОВАТЬ ПО
	|	СервисСверкиРасчетовРеестрДокументов.Организация";
	
	// Установим минимально возможную дату для заполнения, чтобы застраховаться от наличия в регистре "старых" документов.
	Запрос.УстановитьПараметр("МинимальнаяДата", '20240101');
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяПроцедуры = "РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.УстановитьДатуПолученияРасхожденийПоОрганизациям()";
	КонечныйМесяц = НачалоМесяца(ТекущаяДатаСеанса());
	Пока Выборка.Следующий() Цикл
		// Заполним дату получения сведений по организации, начиная с месяца первого документа в регистре расхождений.
		ТекущийМесяц = НачалоМесяца(Выборка.ДатаДокумента);
		Пока ТекущийМесяц <= КонечныйМесяц Цикл
			Попытка
				
				НаборЗаписей = СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
				НаборЗаписей.Отбор.ПериодСверки.Установить(ТекущийМесяц);
				
				НоваяСтрока = НаборЗаписей.Добавить();
				НоваяСтрока.Организация           = Выборка.Организация;
				НоваяСтрока.ПериодСверки          = ТекущийМесяц;
				НоваяСтрока.ДатаПолученияСведений = ДатаПолученияСведений;
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				
			Исключение
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось установить идентификатор расхождения по причине:
					|%2'"), ИмяПроцедуры, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,
					Метаданные.РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов,
					,
					ТекстСообщения);
				
			КонецПопытки;
			ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц, 1);
		КонецЦикла;
	КонецЦикла;
	
	// Очистим константу
	МенеджерКонстанты = Константы.ДатаПолученияДанныхСервисаСверкиРасчетов.СоздатьМенеджерЗначения();
	МенеджерКонстанты.Значение = '00010101';
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
