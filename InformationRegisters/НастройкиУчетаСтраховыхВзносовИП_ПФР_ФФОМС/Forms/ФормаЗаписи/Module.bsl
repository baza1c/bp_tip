
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура(
		"Организация, Период",
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Организация,
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Период);
		
	Оповестить("Запись_НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС", ПараметрыОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПлательщикСтраховыхВзносовПФР_ФФОМСПриИзменении(Элемент)
	
	Если Не НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.ПлательщикСтраховыхВзносовПФР_ФФОМС Тогда
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф = Неопределено;
	Иначе
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф =
			ПредопределенноеЗначение("Перечисление.ИПТарифыФиксированныхВзносов.Единый");
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ДанныеВыбораТарифа = ДанныеВыбораТарифа(НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Период);
	Если ДанныеВыбораТарифа.НайтиПоЗначению(НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф) = Неопределено Тогда
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф = ДанныеВыбораТарифа[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.Тариф.Видимость = Форма.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.ПлательщикСтраховыхВзносовПФР_ФФОМС;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВыбораТарифа(Период)
	
	ПараметрыВыбора = Новый Структура("Отбор, СтрокаПоиска", Неопределено, "");
	ПараметрыВыбора.Вставить("Период", Период);
	Возврат Перечисления.ИПТарифыФиксированныхВзносов.ПолучитьДанныеВыбора(ПараметрыВыбора);
	
КонецФункции

#КонецОбласти
