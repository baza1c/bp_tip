#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

Процедура ЗаписатьИдентификаторМаркетплейса(Номенклатура, Маркетплейс, ИдентификаторМаркетплейса, Штрихкоды, Организация = Неопределено) Экспорт
	
	Если НЕ (ЗначениеЗаполнено(ИдентификаторМаркетплейса)
		И ЗначениеЗаполнено(Маркетплейс)) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрРазделить(ИдентификаторМаркетплейса, "#", Истина).Количество() > 2 Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторТовара = ИдентификаторМаркетплейса;
	
	ПозицияРазделителя = ?(Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries, 
		СтрНайти(ИдентификаторМаркетплейса, "#"), 0);
		
	Если ПозицияРазделителя > 0 Тогда
		Штрихкод = Сред(ИдентификаторМаркетплейса, ПозицияРазделителя + 1);
		ИдентификаторТовара = Лев(ИдентификаторМаркетплейса, ПозицияРазделителя - 1);
		
		МассивШтрихкодов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Штрихкод);
	Иначе
		МассивШтрихкодов = СтрРазделить(Штрихкоды, ",", Ложь);
	КонецЕсли;
	
	// Штрихкодов нет
	Если МассивШтрихкодов.Количество() = 0 Тогда
		МассивШтрихкодов.Добавить("");
	КонецЕсли;
	
	Для каждого Штрихкод Из МассивШтрихкодов Цикл
		МенеджерЗаписи                     = РегистрыСведений.НоменклатураМаркетплейсов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Номенклатура        = Номенклатура;
		МенеджерЗаписи.Маркетплейс         = Маркетплейс;
		МенеджерЗаписи.ИдентификаторТовара = ИдентификаторТовара;
		МенеджерЗаписи.Штрихкод            = Штрихкод; 
		МенеджерЗаписи.Организация         = Организация; 
		
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры 

Процедура ЗаполнитьРегистрПоДаннымКонтрагента(Маркетплейс, НастройкаИнтеграции) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.Wildberries)
	|			ТОГДА ЛЕВ(НоменклатураКонтрагентов.Идентификатор, СТРНАЙТИ(НоменклатураКонтрагентов.Идентификатор, ""#"") - 1)
	|		ИНАЧЕ НоменклатураКонтрагентов.Штрихкод
	|	КОНЕЦ КАК Идентификатор,
	|	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса КАК Маркетплейс,
	|	НоменклатураКонтрагентов.Штрихкод КАК Штрихкод,
	|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура,
	|	НастройкиИнтеграцииМаркетплейс.Владелец КАК Организация
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО НастройкиИнтеграцииМаркетплейс.Контрагент = НоменклатураКонтрагентов.ВладелецНоменклатуры
	|ГДЕ
	|	НЕ НоменклатураКонтрагентов.Родитель ЕСТЬ NULL";
	
	ТаблицаНоменклатураМаркетплейсов = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаНоменклатураМаркетплейсов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СправочникОбъект = НастройкаИнтеграции.ПолучитьОбъект();
	СправочникОбъект.КаталогЗагружен = Истина;
	
	НаборЗаписей = РегистрыСведений.НоменклатураМаркетплейсов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Маркетплейс.Установить(Маркетплейс);
	НаборЗаписей.Загрузить(ТаблицаНоменклатураМаркетплейсов); 
	
	Попытка
		НаборЗаписей.Записать();
		СправочникОбъект.Записать();
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать набор записей по причине:
				|%1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.НоменклатураМаркетплейсов, , ТекстСообщения); 
	КонецПопытки
	
КонецПроцедуры

// Удаляет сопоставление номенклатуры с номенклатурой контрагентов и идентификатор номенклатуры маркетплейса
//
// Параметры:
//  ПараметрыУдаления - Структура - данные для отбора записей
//  ПараметрыУдаления содержит свойства: - Номенклатура - СправочникСсылка.Номенклатура
//                                       - ИдентификаторТовара - Строка
//                                       - Штрихкод - Строка
//                                       - Маркетплейс - ПеречислениеСсылка.ВидыМаркетплейсов
//                                       - Организация - СправочникСсылка.Организации
//                                       - ВнутреннийИдентификатор- Строка
//                                       - Владелец - СправочникСсылка.Контрагенты
// Возвращаемое значение:
// УдалениеВыполнено - Булево
//
Функция УдалитьСопоставление(ПараметрыУдаления) Экспорт
	
	УдалениеВыполнено = Ложь;
	
	//Подготовка данных для удаления идентификатора НоменклатураМаркетплейсов
	Идентификатор = ПараметрыУдаления.ИдентификаторТовара;;
	Штрихкод = ПараметрыУдаления.Штрихкод;
	
	Если ПараметрыУдаления.Свойство("ИзменениеЗаписи") Тогда
		СтруктураИдентификатора = СтруктураИдентификатораМаркетплейса(Идентификатор);
		
		Идентификатор = СтруктураИдентификатора.ИдентификаторТовара;
		Штрихкод = СтруктураИдентификатора.Штрихкод;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.НоменклатураМаркетплейсов.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыУдаления);
	МенеджерЗаписи.ИдентификаторТовара = Идентификатор;
	МенеджерЗаписи.Штрихкод = Штрихкод;
	
	//Подготовка данных для удаления сопоставления НоменклатураКонтрагентовБЭД
	НоменклатураКонтрагента = Новый Структура("Владелец, Идентификатор", ПараметрыУдаления.Владелец, ПараметрыУдаления.ВнутреннийИдентификатор);
	
	НачатьТранзакцию();
	
	//Удаление идентификатора НоменклатураМаркетплейсов
	Попытка
		МенеджерЗаписи.Удалить();
	Исключение
		ЗаписьЖурналаРегистрации("Удаление идентификатора маркетплейса", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначения.СообщитьПользователю("Не удалось удалить идентификатор маркетплейса. Подробности в журнале регистрации.");
		
		ОтменитьТранзакцию();
		
		Возврат УдалениеВыполнено;
	КонецПопытки;
	
	//Удаление сопоставления НоменклатураКонтрагентовБЭД
	Попытка
		СопоставлениеНоменклатурыКонтрагентов.УдалитьСоответствиеНоменклатуры(НоменклатураКонтрагента);
	Исключение
		ЗаписьЖурналаРегистрации("Удаление сопоставления", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначения.СообщитьПользователю("Не удалось удалить сопоставление. Подробности в журнале регистрации.");
		
		ОтменитьТранзакцию();
		
		Возврат УдалениеВыполнено;
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
	УдалениеВыполнено = Истина;
	
	Возврат УдалениеВыполнено;
	
КонецФункции

Функция КонтрагентыМаркетплейсов() Экспорт
	
	КонтрагентыМаркетплейсов = Новый Соответствие;
	
#Область Wildberries
	
	//ООО "РВБ"
	РеквизитыМаркетплейса = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB(КонецДня('20240805'));
	
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыМаркетплейсов.Вставить(КонтрагентПоИНН, Перечисления.ВидыМаркетплейсов.Wildberries);
	КонецЕсли;
	
	//ООО "Вайлдберриз"
	РеквизитыМаркетплейса = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB();
	
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыМаркетплейсов.Вставить(КонтрагентПоИНН, Перечисления.ВидыМаркетплейсов.Wildberries);
	КонецЕсли;
	
#КонецОбласти
	
#Область OZON
	
	РеквизитыМаркетплейса = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыOzon();
	
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	
	//Если указан КПП по месту регистрации
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, "770301001");
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыМаркетплейсов.Вставить(КонтрагентПоИНН, Перечисления.ВидыМаркетплейсов.OZON);
	КонецЕсли;
	
#КонецОбласти
	
#Область Мегамаркет
	
	РеквизитыМаркетплейса = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыМегамаркет();
	
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыМаркетплейсов.Вставить(КонтрагентПоИНН, Перечисления.ВидыМаркетплейсов.Мегамаркет);
	КонецЕсли;
	
#КонецОбласти
	
#Область ЯндексМаркет
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НастройкиИнтеграцииМаркетплейс.Контрагент КАК Контрагент
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		Если ЗначениеЗаполнено(Результат.Контрагент) Тогда
			КонтрагентыМаркетплейсов.Вставить(Результат.Контрагент, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
		КонецЕсли;
	КонецЦикла;
	
#КонецОбласти
	
	Возврат КонтрагентыМаркетплейсов;
	
КонецФункции

Функция СтруктураИдентификатораМаркетплейса(Идентификатор) Экспорт
	
	СтруктураИдентификатора = Новый Структура("ИдентификаторТовара, Штрихкод", "", "");
	
	Если СтрНайти(Идентификатор, "#") <> 0 Тогда
		Подстроки = СтрРазделить(Идентификатор, "#", Ложь);
		
		Если Подстроки.Количество() > 1 Тогда
			СтруктураИдентификатора.ИдентификаторТовара = Подстроки[0];
			СтруктураИдентификатора.Штрихкод = Подстроки[1];
		Иначе
			СтруктураИдентификатора.ИдентификаторТовара = Подстроки[0];
			СтруктураИдентификатора.Штрихкод = "";
		КонецЕсли;
	Иначе
		СтруктураИдентификатора.ИдентификаторТовара = Идентификатор;
		СтруктураИдентификатора.Штрихкод = "";
	КонецЕсли;
	
	Возврат СтруктураИдентификатора;
	
КонецФункции

#КонецОбласти

#КонецЕсли