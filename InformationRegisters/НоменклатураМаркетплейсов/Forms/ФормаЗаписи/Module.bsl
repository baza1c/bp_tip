
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.Организация) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Поле ""Организация"" не заполнено'"),,
		"Запись.Организация",, Отказ);
		
		Возврат;
	КонецЕсли;
	
	ИдентификаторИзменен = ИдентификаторИзменялся();
	
	Если ИдентификаторИзменен Тогда
		ПараметрыПроверки = Новый Структура("ИдентификаторТовара, Штрихкод, Маркетплейс");
		
		ЗаполнитьЗначенияСвойств(ПараметрыПроверки, ТекущийОбъект);
		
		ПроверитьЗаписьСуществует(ПараметрыПроверки, Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыУдаления = Новый Структура("Номенклатура, ИдентификаторТовара, Штрихкод, Маркетплейс, 
		|Организация, ВнутреннийИдентификатор, Владелец, ИзменениеЗаписи");
		
		ЗаполнитьЗначенияСвойств(ПараметрыУдаления, Запись);
		
		ПараметрыУдаления.ВнутреннийИдентификатор = ТекущийИдентификатор;
		ПараметрыУдаления.Владелец = Владелец;
		
		УдалениеВыполнено = РегистрыСведений.НоменклатураМаркетплейсов.УдалитьСопоставление(ПараметрыУдаления);
		
		Если НЕ УдалениеВыполнено Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось записать идентификатор номенклатуры маркетплейса.
				|См. подробности в журнале регистрации'"),,,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ОповещатьОбИзмененииЗаписи = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ИдентификаторИзменен Тогда
		УправлениеФормой();
		
		ТекущийИдентификатор = ТекущийВнутреннийИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОповещатьОбИзмененииЗаписи И НЕ ЗавершениеРаботы Тогда
		Оповестить("ИзмененаЗаписьНоменклатураМаркетплейсов");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьСопоставление(Команда)
	
	Оповещение = Новый ОписаниеОповещения("УдалитьСопоставлениеПослеЗакрытияВопроса", ЭтотОбъект);
	
	ПоказатьВопрос(Оповещение,
		НСтр("ru = 'Сопоставление будет удалено. Товар маркетплейса будет пересопоставлен при следующей загрузке отчета. Продолжить?'"), 
		РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИдентификаторИзменялся()
	
	ИдентификаторИзменялся = Ложь;
	
	ИдентификаторСравнения = Запись.ИдентификаторТовара;
	ШтрихкодСравнения = Запись.Штрихкод;
	
	СтруктураИдентификатора = РегистрыСведений.НоменклатураМаркетплейсов.СтруктураИдентификатораМаркетплейса(ТекущийИдентификатор);
	
	ИдентификаторСравнения = СтруктураИдентификатора.ИдентификаторТовара;
	ШтрихкодСравнения = СтруктураИдентификатора.Штрихкод;
	
	Если Запись.ИдентификаторТовара <> ИдентификаторСравнения
	ИЛИ ШтрихкодСравнения <> Запись.Штрихкод Тогда
		ИдентификаторИзменялся = Истина;
	КонецЕсли;
	
	Возврат ИдентификаторИзменялся;
	
КонецФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЗаполнитьРеквизитыФормы();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.НаименованиеМаркетплейса.Видимость = ЗначениеЗаполнено(Запись.ИдентификаторТовара);
	Элементы.ФормаУдалитьСопоставление.Видимость = ЗначениеЗаполнено(Запись.ИдентификаторТовара);
	
	Элементы.Маркетплейс.ТолькоПросмотр = ЗначениеЗаполнено(Запись.ИдентификаторТовара);
	
	Если Параметры.Свойство("ОткрываетсяИзОбщегоСписка") Тогда
		Если Параметры.ОткрываетсяИзОбщегоСписка Тогда
			Элементы.НаименованиеМаркетплейса.Видимость = Ложь;
			Элементы.ФормаУдалитьСопоставление.Видимость = Ложь;
			
			Элементы.Организация.ТолькоПросмотр = Истина;
			Элементы.Номенклатура.ТолькоПросмотр = Истина;
			Элементы.ИдентификаторТовара.ТолькоПросмотр = Истина;
			Элементы.Штрихкод.ТолькоПросмотр = Истина;
			
			Элементы.ФормаЗаписатьИЗакрыть.Доступность = Ложь;
			Элементы.ФормаЗаписать.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НаименованиеМаркетплейса) Тогда
		НаименованиеМаркетплейса = НСтр("ru = '<Товар будет сопоставлен при следующей загрузке отчета>'");
		Элементы.НаименованиеМаркетплейса.ЦветТекста = ЦветаСтиля.ЦветНеАктивнойСтроки;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
		Элементы.Организация.АвтоОтметкаНезаполненного = Истина;
		Элементы.Организация.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
	
	ДоступноИзменение = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НоменклатураМаркетплейсов);
	
	Элементы.ФормаУдалитьСопоставление.Доступность = ДоступноИзменение;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	Если Параметры.Свойство("ДополнительныеПараметры") Тогда
		Если Параметры.ДополнительныеПараметры <> Неопределено Тогда
			Если Параметры.ДополнительныеПараметры.Свойство("Наименование")
			И Параметры.ДополнительныеПараметры.Свойство("Владелец") Тогда
				НаименованиеМаркетплейса = Параметры.ДополнительныеПараметры.Наименование;
				Владелец = Параметры.ДополнительныеПараметры.Владелец;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийИдентификатор = ТекущийВнутреннийИдентификатор();
	
КонецПроцедуры

&НаСервере
Функция ТекущийВнутреннийИдентификатор()
	
	Результат = "";
	
	Если ЗначениеЗаполнено(Запись.Штрихкод) Тогда
		Результат = Запись.ИдентификаторТовара + "#" + Запись.Штрихкод;
	Иначе
		Результат = Запись.ИдентификаторТовара;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УдалитьСопоставлениеПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыУдаления = Новый Структура("Номенклатура, ИдентификаторТовара, Штрихкод, Маркетплейс,
			|Организация, ВнутреннийИдентификатор, Владелец");
		
		ЗаполнитьЗначенияСвойств(ПараметрыУдаления, Запись);
		
		ПараметрыУдаления.ВнутреннийИдентификатор = ТекущийИдентификатор;
		ПараметрыУдаления.Владелец = Владелец;
		
		УдалениеВыполнено = УдалитьСопоставлениеНоменклатурыМаркетплейса(ПараметрыУдаления);
		
		Если УдалениеВыполнено Тогда
			ОповещатьОбИзмененииЗаписи = Истина;
			
			ЭтотОбъект.Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УдалитьСопоставлениеНоменклатурыМаркетплейса(ПараметрыУдаления)
	
	УдалениеВыполнено = РегистрыСведений.НоменклатураМаркетплейсов.УдалитьСопоставление(ПараметрыУдаления);
	
	Возврат УдалениеВыполнено;
	
КонецФункции

&НаСервере
Процедура ПроверитьЗаписьСуществует(ПараметрыПроверки, Отказ)
	
	НайденнаяЗапись = НайтиЗаписьПоПараметрамПроверки(ПараметрыПроверки);
	
	Если НайденнаяЗапись = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НайденнаяЗапись.Штрихкод <> "" Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Идентификатор %1 с баркодом %2 принадлежит номенклатуре ""%3""'"),
		НайденнаяЗапись.ИдентификаторТовара, НайденнаяЗапись.Штрихкод, НайденнаяЗапись.Номенклатура);
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Идентификатор %1 принадлежит номенклатуре ""%2""'"),
		НайденнаяЗапись.ИдентификаторТовара, НайденнаяЗапись.Номенклатура);
	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	
КонецПроцедуры

Функция НайтиЗаписьПоПараметрамПроверки(ПараметрыПроверки)
	
	НайденнаяЗапись = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатураМаркетплейсов.Номенклатура КАК Номенклатура,
		|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
		|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод
		|ИЗ
		|	РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
		|ГДЕ
		|	НоменклатураМаркетплейсов.ИдентификаторТовара = &ИдентификаторТовара
		|	И НоменклатураМаркетплейсов.Штрихкод = &Штрихкод
		|	И НоменклатураМаркетплейсов.Маркетплейс = &Маркетплейс";
	
	Запрос.УстановитьПараметр("ИдентификаторТовара", ПараметрыПроверки.ИдентификаторТовара);
	Запрос.УстановитьПараметр("Штрихкод", ПараметрыПроверки.Штрихкод);
	Запрос.УстановитьПараметр("Маркетплейс", ПараметрыПроверки.Маркетплейс);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		НайденнаяЗапись = Новый Структура("Номенклатура, ИдентификаторТовара, Штрихкод");
		ЗаполнитьЗначенияСвойств(НайденнаяЗапись, Выборка);
	КонецЕсли;
	
	Возврат НайденнаяЗапись;
	
КонецФункции

#КонецОбласти




