
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетПоНесколькимОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийБухгалтерскийУчет");
	
	Если Параметры.Свойство("Отбор") Тогда
		ЕстьОтбор = Параметры.Отбор.Свойство("Номенклатура");
		
		Если ЕстьОтбор Тогда
			Номенклатура = Параметры.Отбор.Номенклатура;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		ПоказыватьОбщийСписок = Истина;
	КонецЕсли;
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаЗаписьНоменклатураМаркетплейсов" Тогда
		ЗаполнитьСписокСопоставлений();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьСопоставление(Команда)
	
	УдалитьТекущееСопоставление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	
	ЗаполнитьСписокСопоставлений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокОрганизацияНажатие(Элемент, СтандартнаяОбработка)
	
	ОткрытьФормуЗаписи();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуЗаписи();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ОткрытьФормуЗаписи(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	УдалитьТекущееСопоставление();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УдалитьТекущееСопоставление()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыУдаления = Новый Структура("Номенклатура, ИдентификаторТовара, Штрихкод, Маркетплейс,
		|Организация, ВнутреннийИдентификатор, Владелец");
	
	ЗаполнитьЗначенияСвойств(ПараметрыУдаления, ТекущиеДанные);
	
	Оповещение = Новый ОписаниеОповещения("УдалениеПослеЗакрытияВопроса", ЭтотОбъект, ПараметрыУдаления);
	
	ПоказатьВопрос(Оповещение,
		НСтр("ru = 'Сопоставление будет удалено. Новое сопоставление будет создано при следующей загрузке. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Если ПоказыватьОбщийСписок Тогда
		Список.Очистить();
		Элементы.СписокДобавить.Видимость = Ложь;
		Элементы.СписокУдалитьСопоставление.Доступность = Ложь;
		Элементы.СписокОбновитьСписок.Видимость = Ложь;
		Элементы.СписокНаименование.Видимость = Ложь;
		Элементы.СписокНоменклатура.Видимость = Истина;
	КонецЕсли;
	
	Если НЕ УчетПоНесколькимОрганизациям Тогда
		Элементы.СписокОрганизация.Видимость = Ложь;
	КонецЕсли;
	
	ДоступноИзменение = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НоменклатураМаркетплейсов);
	
	Элементы.СписокДобавить.Доступность = ДоступноИзменение;
	Элементы.СписокУдалитьСопоставление.Доступность = ДоступноИзменение;
	Элементы.СписокОбновитьСписок.Доступность = ДоступноИзменение;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УправлениеФормой();
	
	ЗаполнитьСписокСопоставлений();
	
	Если НЕ УчетПоНесколькимОрганизациям И НЕ ПоказыватьОбщийСписок Тогда
		Попытка
			ПроверитьСоздатьЗаписиНоменклатураМаркетплейсов();
		Исключение
			ЗаписьЖурналаРегистрации("Создание записей НоменклатураМаркетплейсов", УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Элементы.СписокОрганизация.Видимость = Истина;
		КонецПопытки;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСопоставлений()
	
	ТаблицаСопоставлений = УправлениеСопоставлениямиТаблицаСопоставлений();
	
	Запрос = Новый Запрос;
	
	Если НЕ ПоказыватьОбщийСписок Тогда
		ДоступныеОрганизации = Справочники.Организации.ДоступныеОрганизацииПоRLS();
		ДоступныеОрганизации.Добавить(Справочники.Организации.ПустаяСсылка());
		
		Запрос.Текст = УправлениеСопоставлениямиСписокТекстЗапросаПоНоменклатуре();
		
		Запрос.УстановитьПараметр("ТаблицаСопоставлений", ТаблицаСопоставлений);
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("ДоступныеОрганизации", ДоступныеОрганизации);
		
		УстановитьПривилегированныйРежим(Истина);
		
		Список.Загрузить(Запрос.Выполнить().Выгрузить());
		
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		Запрос.Текст = УправлениеСопоставлениямиСписокТекстЗапросаОбщий();
		
		Список.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСоздатьЗаписиНоменклатураМаркетплейсов()
	
	ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	СтруктураОтбора = Новый Структура("Организация", Справочники.Организации.ПустаяСсылка());
	
	НайденныеСтроки = Список.НайтиСтроки(СтруктураОтбора);
	
	Для каждого Строка Из НайденныеСтроки Цикл
		МенеджерЗаписи = РегистрыСведений.НоменклатураМаркетплейсов.СоздатьМенеджерЗаписи();
		
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Строка);
		
		МенеджерЗаписи.Организация = ОрганизацияПоУмолчанию;
		МенеджерЗаписи.Записать(Истина);
		
		Строка.Организация = ОрганизацияПоУмолчанию;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УправлениеСопоставлениямиСписокТекстЗапросаОбщий()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод,
	|	НоменклатураМаркетплейсов.Маркетплейс КАК Маркетплейс,
	|	НоменклатураМаркетплейсов.Номенклатура КАК Номенклатура,
	|	НоменклатураМаркетплейсов.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция УправлениеСопоставлениямиСписокТекстЗапросаПоНоменклатуре()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаСопоставлений.Номенклатура КАК Номенклатура,
	|	ТаблицаСопоставлений.Владелец КАК Владелец,
	|	ТаблицаСопоставлений.ИдентификаторТовара КАК ИдентификаторТовара,
	|	ТаблицаСопоставлений.ВнутреннийИдентификатор КАК ВнутреннийИдентификатор,
	|	ТаблицаСопоставлений.Штрихкод КАК Штрихкод,
	|	ТаблицаСопоставлений.Наименование КАК Наименование,
	|	ТаблицаСопоставлений.Маркетплейс КАК Маркетплейс,
	|	ТаблицаСопоставлений.Артикул КАК Артикул
	|ПОМЕСТИТЬ ВТ_ТаблицаСопоставлений
	|ИЗ
	|	&ТаблицаСопоставлений КАК ТаблицаСопоставлений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод,
	|	НоменклатураМаркетплейсов.Маркетплейс КАК Маркетплейс,
	|	НоменклатураМаркетплейсов.Номенклатура КАК Номенклатура,
	|	НоменклатураМаркетплейсов.Организация КАК Организация
	|ПОМЕСТИТЬ НоменклатураМаркетплейсов
	|ИЗ
	|	РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|ГДЕ
	|	НоменклатураМаркетплейсов.Номенклатура = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод,
	|	НоменклатураМаркетплейсов.Маркетплейс КАК Маркетплейс,
	|	НоменклатураМаркетплейсов.Номенклатура КАК Номенклатура,
	|	НоменклатураМаркетплейсов.Организация КАК Организация,
	|	ЕСТЬNULL(ВТ_ТаблицаСопоставлений.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(ВТ_ТаблицаСопоставлений.Артикул, """") КАК Артикул,
	|	ЕСТЬNULL(ВТ_ТаблицаСопоставлений.ВнутреннийИдентификатор, """") КАК ВнутреннийИдентификатор,
	|	ЕСТЬNULL(ВТ_ТаблицаСопоставлений.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Владелец
	|ПОМЕСТИТЬ ВТ_Итоговая
	|ИЗ
	|	НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаСопоставлений КАК ВТ_ТаблицаСопоставлений
	|		ПО НоменклатураМаркетплейсов.ИдентификаторТовара = ВТ_ТаблицаСопоставлений.ИдентификаторТовара
	|			И НоменклатураМаркетплейсов.Штрихкод = ВТ_ТаблицаСопоставлений.Штрихкод
	|			И НоменклатураМаркетплейсов.Номенклатура = ВТ_ТаблицаСопоставлений.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСопоставлений.ИдентификаторТовара,
	|	ВТ_ТаблицаСопоставлений.Штрихкод,
	|	ВТ_ТаблицаСопоставлений.Маркетплейс,
	|	ВТ_ТаблицаСопоставлений.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|	ВТ_ТаблицаСопоставлений.Наименование,
	|	ВТ_ТаблицаСопоставлений.Артикул,
	|	ВТ_ТаблицаСопоставлений.ВнутреннийИдентификатор,
	|	ВТ_ТаблицаСопоставлений.Владелец
	|ИЗ
	|	ВТ_ТаблицаСопоставлений КАК ВТ_ТаблицаСопоставлений
	|ГДЕ
	|	НЕ (ВТ_ТаблицаСопоставлений.ИдентификаторТовара, ВТ_ТаблицаСопоставлений.Штрихкод) В
	|				(ВЫБРАТЬ
	|					ВТ.ИдентификаторТовара,
	|					ВТ.Штрихкод
	|				ИЗ
	|					НоменклатураМаркетплейсов КАК ВТ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Итоговая.ИдентификаторТовара КАК ИдентификаторТовара,
	|	ВТ_Итоговая.Штрихкод КАК Штрихкод,
	|	ВТ_Итоговая.Маркетплейс КАК Маркетплейс,
	|	ВТ_Итоговая.Номенклатура КАК Номенклатура,
	|	ВТ_Итоговая.Организация КАК Организация,
	|	ВТ_Итоговая.Наименование КАК Наименование,
	|	ВТ_Итоговая.Артикул КАК Артикул,
	|	ВТ_Итоговая.ВнутреннийИдентификатор КАК ВнутреннийИдентификатор,
	|	ВТ_Итоговая.Владелец КАК Владелец
	|ИЗ
	|	ВТ_Итоговая КАК ВТ_Итоговая
	|ГДЕ
	|	ВТ_Итоговая.Организация В(&ДоступныеОрганизации)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция УправлениеСопоставлениямиТаблицаСопоставлений()
	
	НоменклатураИБ = Новый Массив;
	
	НоменклатураИБОписание = Новый Структура("Номенклатура, Упаковка, Характеристика", Номенклатура, "", "");
	
	НоменклатураИБ.Добавить(НоменклатураИБОписание);
	
	Отбор = Новый Структура("НоменклатураИБ", НоменклатураИБ);
	
	СопоставленияНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Отбор, Истина);
	
	Если СопоставленияНоменклатуры.Количество() = 0 Тогда
		НоменклатураИБОписание.Упаковка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
		СопоставленияНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Отбор, Истина);
	КонецЕсли;
	
	ТаблицаРезультат = ПолучитьОписаниеРезультирующейТаблицы();
	
	КонтрагентыВладельцы = РегистрыСведений.НоменклатураМаркетплейсов.КонтрагентыМаркетплейсов();
	
	Для каждого Сопоставление Из СопоставленияНоменклатуры Цикл
		НоменклатураКонтрагента = Сопоставление.НоменклатураКонтрагента;
		
		Если СтрРазделить(НоменклатураКонтрагента.Идентификатор, "#", Истина).Количество() > 2 Тогда
			Продолжить;
		КонецЕсли;
		
		Маркетплейс = КонтрагентыВладельцы.Получить(НоменклатураКонтрагента.Владелец);
		
		Если НЕ ЗначениеЗаполнено(Маркетплейс) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРезультат.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, НоменклатураКонтрагента);
		
		НоваяСтрока.Номенклатура = Номенклатура;
		НоваяСтрока.Маркетплейс = Маркетплейс;
		НоваяСтрока.ВнутреннийИдентификатор = НоменклатураКонтрагента.Идентификатор;
		
		СтруктураИдентификатора = РегистрыСведений.НоменклатураМаркетплейсов.СтруктураИдентификатораМаркетплейса(НоменклатураКонтрагента.Идентификатор);
		
		НоваяСтрока.ИдентификаторТовара = СтруктураИдентификатора.ИдентификаторТовара;
		
		Если ЗначениеЗаполнено(НоменклатураКонтрагента.ШтрихкодКомбинации) Тогда
			НоваяСтрока.Штрихкод = НоменклатураКонтрагента.ШтрихкодКомбинации;
		Иначе
			НоваяСтрока.Штрихкод = СтруктураИдентификатора.Штрихкод;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

&НаСервере
Функция ПолучитьОписаниеРезультирующейТаблицы()
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	
	ОписаниеТипаСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(200);
	ОписаниеТипаВсеСсылки = ОбщегоНазначения.ОписаниеТипаВсеСсылки();
	
	ТаблицаРезультат.Колонки.Добавить("Номенклатура", ОписаниеТипаВсеСсылки);
	ТаблицаРезультат.Колонки.Добавить("ИдентификаторТовара", ОписаниеТипаСтрока);
	ТаблицаРезультат.Колонки.Добавить("ВнутреннийИдентификатор", ОписаниеТипаСтрока);
	ТаблицаРезультат.Колонки.Добавить("Штрихкод", ОписаниеТипаСтрока);
	ТаблицаРезультат.Колонки.Добавить("Наименование", ОписаниеТипаСтрока);
	ТаблицаРезультат.Колонки.Добавить("Маркетплейс", ОписаниеТипаВсеСсылки);
	ТаблицаРезультат.Колонки.Добавить("Артикул", ОписаниеТипаСтрока);
	ТаблицаРезультат.Колонки.Добавить("Владелец", ОписаниеТипаВсеСсылки);
	
	Возврат ТаблицаРезультат;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокОрганизация");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Организация", ВидСравненияКомпоновкиДанных.Равно, Справочники.Организации.ПустаяСсылка());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Заполните организацию>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокНаименование");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Наименование", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", 
		НСтр("ru = '<Будет сопоставлено с товаром маркетплейса при следующей загрузке отчета>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНеАктивнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаписи(СозданиеНового = Ложь)
	
	Если СозданиеНового Тогда
		ОткрытьФормуНовойЗаписи(СозданиеНового);
		
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Организация) Тогда
		ОткрытьФормуНовойЗаписи(СозданиеНового, ТекущиеДанные);
	Иначе
		ОткрытьФормуСуществующейЗаписи(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСуществующейЗаписи(ТекущиеДанные)
	
	СтруктураКлючаЗаписи = Новый Структура;
	
	СтруктураКлючаЗаписи.Вставить("Номенклатура");
	СтруктураКлючаЗаписи.Вставить("Организация");
	СтруктураКлючаЗаписи.Вставить("Маркетплейс");
	СтруктураКлючаЗаписи.Вставить("ИдентификаторТовара");
	СтруктураКлючаЗаписи.Вставить("Штрихкод");
	
	ЗаполнитьЗначенияСвойств(СтруктураКлючаЗаписи, ТекущиеДанные);
	
	КлючЗаписи = КлючЗаписиНоменклатураМаркетплейсов(СтруктураКлючаЗаписи);
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("Наименование", ТекущиеДанные.Наименование);
	ДополнительныеПараметры.Вставить("Владелец", ТекущиеДанные.Владелец);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Артикул) Тогда
		ДополнительныеПараметры.Наименование = СтрШаблон("%1, %2", ТекущиеДанные.Наименование, ТекущиеДанные.Артикул);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ, ДополнительныеПараметры, ОткрываетсяИзОбщегоСписка", КлючЗаписи, ДополнительныеПараметры, ПоказыватьОбщийСписок);
	
	ОткрытьФорму("РегистрСведений.НоменклатураМаркетплейсов.Форма.ФормаЗаписи", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНовойЗаписи(СозданиеНового, ТекущиеДанные = Неопределено)
	
	ЗначенияЗаполнения = Новый Структура;
	
	Если СозданиеНового Тогда
		ЗначенияЗаполнения.Вставить("Номенклатура", Номенклатура);
	Иначе
		ЗначенияЗаполнения.Вставить("Организация");
		ЗначенияЗаполнения.Вставить("Номенклатура");
		ЗначенияЗаполнения.Вставить("Маркетплейс");
		ЗначенияЗаполнения.Вставить("ИдентификаторТовара");
		ЗначенияЗаполнения.Вставить("Штрихкод");
		
		ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, ТекущиеДанные);
		
		ДополнительныеПараметры = Новый Структура;
		
		ДополнительныеПараметры.Вставить("Наименование", ТекущиеДанные.Наименование);
		ДополнительныеПараметры.Вставить("Владелец", ТекущиеДанные.Владелец);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Артикул) Тогда
			ДополнительныеПараметры.Наименование = СтрШаблон("%1, %2", ТекущиеДанные.Наименование, ТекущиеДанные.Артикул);
		КонецЕсли;
		
		Если НЕ ПоказыватьОбщийСписок Тогда
			ЗначенияЗаполнения.Номенклатура = Номенклатура
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения, ДополнительныеПараметры", ЗначенияЗаполнения, ДополнительныеПараметры);
	
	ОткрытьФорму("РегистрСведений.НоменклатураМаркетплейсов.Форма.ФормаЗаписи", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КлючЗаписиНоменклатураМаркетплейсов(СтруктураКлючаЗаписи)
	
	КлючЗаписи = РегистрыСведений.НоменклатураМаркетплейсов.СоздатьКлючЗаписи(СтруктураКлючаЗаписи);
	
	Возврат КлючЗаписи;
	
КонецФункции

&НаКлиенте
Процедура УдалениеПослеЗакрытияВопроса(Результат, ПараметрыУдаления) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьСопоставлениеНоменклатурыМаркетплейса(ПараметрыУдаления);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСопоставлениеНоменклатурыМаркетплейса(ПараметрыУдаления)
	
	УдалениеВыполнено = РегистрыСведений.НоменклатураМаркетплейсов.УдалитьСопоставление(ПараметрыУдаления);
	
	Если УдалениеВыполнено Тогда
		ЗаполнитьСписокСопоставлений();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

#КонецОбласти



