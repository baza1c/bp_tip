#Область КомандыФормы

&НаКлиенте
Процедура Печать(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.КассоваяСсылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("ru = 'Кассовая ссылка не сформирована'");
		Возврат;
	КонецЕсли;
	
	ДанныеСсылки = Новый Структура("КассоваяСсылка, ИдентификаторОплаты");
	ЗаполнитьЗначенияСвойств(ДанныеСсылки, ТекущиеДанные);
	
	МакетКарточкиКассовойСсылки = СформироватьКарточкуКассовойСсылки(ДанныеСсылки);
	
	Если МакетКарточкиКассовойСсылки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторПечатнойФормы = "ПечатнаяФорма";
	НазваниеПечатнойФормы = НСтр("ru = 'Ссылка QR'");
	
	КоллекцияПечатныхФорм               = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
	ПечатнаяФорма                       = УправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
	ПечатнаяФорма.СинонимМакета         = НазваниеПечатнойФормы;
	ПечатнаяФорма.ТабличныйДокумент     = МакетКарточкиКассовойСсылки;
	ПечатнаяФорма.ИмяФайлаПечатнойФормы = НазваниеПечатнойФормы;
	
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСуществующую(Команда)
	Отборы = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "НастройкаИнтеграции");
	Если НЕ Отборы.Количество() = 1 Тогда
		Возврат;
	КонецЕсли;
	НастройкаИнтеграции = Отборы[0].ПравоеЗначение;
	ПереводыСБПc2bКлиент.ПодключитьКассовуюСсылку(НастройкаИнтеграции, 
		Новый ОписаниеОповещения("ПодключитьКассовуюСсылкуЗавершение", ЭтотОбъект, НастройкаИнтеграции),ЭтотОбъект);
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодключитьКассовуюСсылкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Результат.Вставить("НастройкаИнтеграции", ДополнительныеПараметры);
	
	ПараметрыОткрытияФормы = Новый Структура("ЗначенияЗаполнения", Результат);
	
	ОткрытьФорму("РегистрСведений.КассовыеСсылки.ФормаЗаписи", ПараметрыОткрытияФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьКарточкуКассовойСсылки(ДанныеСсылки)

	Возврат ПереводыСБПc2b.КарточкаКассовойСсылки(ДанныеСсылки, Новый Структура("ТипПечати", 1));

КонецФункции 

#КонецОбласти
