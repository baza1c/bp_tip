#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПараметрыТорговойТочки = РегистрыСведений.СоответствиеНастроекИнтеграции.НастройкиТорговойТочки(Запись.НастройкаИнтеграции);
	Если ПараметрыТорговойТочки = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Настройка интеграции не завершена.'");
	КонецЕсли;
	
	НастройкиТорговойТочки = СистемаБыстрыхПлатежей.НастройкиПодключения(Запись.НастройкаИнтеграции);
	Если Не НастройкиТорговойТочки.НастройкиСБПc2b.КассовыеСсылки Тогда
		ВызватьИсключение НСтр("ru = 'Кассовые ссылки не поддерживаются.'");
	КонецЕсли;
	
	Запись.Организация = ПараметрыТорговойТочки.Организация;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.ОсновнаяСтраница;
	
	ПроверитьПолучитьКассовуюСсылку();
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступомКонецПроцедуры
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КопироватьВБуфер(Команда)
	// Копирование происходит с предварительной очисткой через обработчик, для обхода поведения платформы
	// при повторном копировании - при определенных условиях копирование не происходит.
	ЭтотОбъект.БуферОбмена = "";
	ПодключитьОбработчикОжидания("КопироватьСсылкуВБуфер", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ЗначениеЗаполнено(Запись.КассоваяСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСсылки = Новый Структура("КассоваяСсылка, ИдентификаторОплаты");
	ЗаполнитьЗначенияСвойств(ДанныеСсылки, Запись);
	
	МакетКарточкиКассовойСсылки = СформироватьКарточкуКассовойСсылки(ДанныеСсылки);
	
	Если МакетКарточкиКассовойСсылки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторПечатнойФормы = "ПечатнаяФорма";
	НазваниеПечатнойФормы = НСтр("ru = 'Ссылка QR'");
	
	КоллекцияПечатныхФорм               = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
	ПечатнаяФорма                       = УправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
	ПечатнаяФорма.СинонимМакета         = НазваниеПечатнойФормы;
	ПечатнаяФорма.ТабличныйДокумент     = МакетКарточкиКассовойСсылки;
	ПечатнаяФорма.ИмяФайлаПечатнойФормы = НазваниеПечатнойФормы;
	
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьNFC(Команда)
	ФункциональнаяСсылкаNFC = ПереводыСБПc2bКлиентСервер.ФункциональнаяСсылкаNFC(Запись.КассоваяСсылка);
	ПодключитьОбработчикОжидания("КопироватьМеткуВБуфер", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	Элементы.КопироватьВБуфер.Доступность = ЗначениеЗаполнено(Запись.КассоваяСсылка);
	Элементы.Печать.Доступность = ЗначениеЗаполнено(Запись.КассоваяСсылка);
	Элементы.ЗаписатьNFC.Доступность = ЗначениеЗаполнено(Запись.КассоваяСсылка);
КонецПроцедуры

&НаСервере
Процедура ПроверитьПолучитьКассовуюСсылку()
	Если НЕ ПустаяСтрока(Запись.КассоваяСсылка) Тогда
		Возврат;
	КонецЕсли; 
	
	РезультатОперации = ПереводыСБПc2b.КассоваяСсылка(Запись.НастройкаИнтеграции);
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Не удалось получить кассовую ссылку. Код ошибки %1'"), РезультатОперации.КодОшибки));
		
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьЗначенияСвойств(Запись, РезультатОперации, "КассоваяСсылка, ИдентификаторОплаты");
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСсылкуВБуфер()
	
	КопироватьРеквизитВБуфер(Запись.КассоваяСсылка);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Ссылка получена'"),
		,
		НСтр("ru = 'Ссылка для оплаты через СБП скопирована в буфер обмена'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьМеткуВБуфер()
	
	КопироватьРеквизитВБуфер(ФункциональнаяСсылкаNFC);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Ссылка получена'"),
		,
		НСтр("ru = 'Ссылка для программирования NFC метки скопирована в буфер обмена'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьРеквизитВБуфер(Реквизит)

	ЭтотОбъект.БуферОбмена = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<!DOCTYPE html>
		|<html>
		|	<body onload='copy()'>
		|		<input id='input' type='text'/>
		|		<script>
		|			function copy() {
		|				var text = '%1';
		|				var ua = navigator.userAgent;
		|				if (ua.search(/MSIE/) > 0 || ua.search(/Trident/) > 0) {
		|					window.clipboardData.setData('Text', text);
		|				} else {
		|					var copyText = document.getElementById('input');
		|					copyText.value = text;
		|					copyText.select();
		|					document.execCommand('copy');
		|				}
		|			}
		|		</script>
		|	</body>
		|</html>",
		Реквизит);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьКарточкуКассовойСсылки(ДанныеСсылки)
	Возврат ПереводыСБПc2b.КарточкаКассовойСсылки(ДанныеСсылки, Новый Структура("ТипПечати", 1));
КонецФункции 

#КонецОбласти
