#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает порядок работы с лидом в режиме Канбан.
//
// Параметры:
//   Лид - СправочникСсылка.КонтактныеЛица - лид.
//
// Возвращаемое значение:
//   Число - порядок.
Функция ПорядокКанбанПоЛиду(Лид) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПорядокРаботыСЛидами.ПорядокКанбан КАК ПорядокКанбан
	|ИЗ
	|	РегистрСведений.ПорядокРаботыСЛидами КАК ПорядокРаботыСЛидами
	|ГДЕ
	|	ПорядокРаботыСЛидами.Лид = &Лид";
	
	Запрос.УстановитьПараметр("Лид", Лид);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ПорядокКанбан;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Записывает порядок работы с лидами, устанавливаемый вручную, в режиме Канбан
//
// Параметры:
//   МассивЛидов - Массив из СправочникСсылка.КонтактныеЛица - массив лидов.
//   Состояние - СправочникСсылка.СостоянияРаботыСЛидами - текущее состояние.
//   ПорядокКанбан - Число - устанавливаемый порядок.
//
Процедура УстановитьРучнойПорядокРаботыСЛидами(МассивЛидов, Состояние, Знач ПорядокКанбан) Экспорт
	
	// Оставляем прежнюю дату изменения
	
	НачальныйПорядокКанбан = ПорядокКанбан;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Состояние.Установить(Состояние);
	НаборЗаписей.Прочитать();
	
	Отступ = МассивЛидов.Количество();
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Если МассивЛидов.Найти(Запись.Лид) <> Неопределено Тогда
			Запись.ПорядокКанбан = ПорядокКанбан;
			ПорядокКанбан = ПорядокКанбан + 1;
		ИначеЕсли Запись.ПорядокКанбан < НачальныйПорядокКанбан Тогда
			Продолжить;
		Иначе
			Запись.ПорядокКанбан = Запись.ПорядокКанбан + Отступ;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Очищает порядок работы с лидом в режиме Канбан.
//
// Параметры:
//   Лид - СправочникСсылка.КонтактныеЛица - лид.
//
Процедура ОчиститьПорядокКанбанПоЛиду(Лид) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Лид.Установить(Лид);
	НаборЗаписей.Прочитать();
	
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает порядок работы с лидом в режиме Канбан при записи текущего состояния
//
// Параметры:
//   Лид - СправочникСсылка.КонтактныеЛица - лид.
//   ТекущееСостояние - СправочникСсылка.СостоянияРаботыСЛидами - текущее состояние.
//   ДатаИзменения - дата, на которую устанавливается текущее состояние.
//
Процедура УстановитьПорядокКанбанПоЛиду(Лид, ТекущееСостояние, Знач ДатаИзменения = Неопределено) Экспорт
	
	Если ДатаИзменения = Неопределено Тогда
		ДатаИзменения = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// В наборе по лиду должны быть записи, относящиеся только к текущему состоянию
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Лид.Установить(Лид);
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Состояние = ТекущееСостояние;
	НоваяЗапись.Лид = Лид;
	НоваяЗапись.ПорядокКанбан = ПоследнийПорядокКанбан(ТекущееСостояние) + 1;
	НоваяЗапись.ДатаИзменения = ДатаИзменения;
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ПоследнийПорядокКанбан(Состояние)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокРаботыСЛидами.ПорядокКанбан КАК ПорядокКанбан
	|ИЗ
	|	РегистрСведений.ПорядокРаботыСЛидами КАК ПорядокРаботыСЛидами
	|ГДЕ
	|	ПорядокРаботыСЛидами.Состояние = &Состояние
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокКанбан УБЫВ";
	
	Запрос.УстановитьПараметр("Состояние", Состояние);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ПорядокКанбан;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли