#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	СервисПодключен = Ложь;
	СостояниеПодключено = Перечисления.СостоянияИнтеграцииАУСН.Подключено;
	БанкиНеПоддерживающиеПередачуНДФЛ = Обработки.ПодключениеАУСН.БанкиНеПоддерживающиеПередачуНДФЛ();
	Для Каждого Запись Из ЭтотОбъект Цикл
		СервисПодключен = СервисПодключен Или Запись.Состояние = СостояниеПодключено;
		Запись.ПередачаНДФЛ = СервисПодключен;
		Если СервисПодключен И БанкиНеПоддерживающиеПередачуНДФЛ.Найти(Запись.Банк) <> Неопределено Тогда
			// Банк не поддерживает передачу НДФЛ
			Запись.ПередачаНДФЛ = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если СервисПодключен Тогда
		ИнтеграцияАУСН.ИзменитьПрименениеЗаданияПоОбменуССервисом(Истина);
		ИнтеграцияАУСНБП.ОтключитьАвтообменДиректБанк(ЭтотОбъект);
	КонецЕсли;
	
	Если ИнтеграцияАУСНВызовСервераПовтИсп.СервисПодключен() <> СервисПодключен Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Банк = Неопределено;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Банк", Банк) Тогда
		ИдентификаторБанка = ИнтеграцияАУСНПовтИсп.ИдентификаторБанкаПоКлассификатору(Банк);
	Иначе
		ИдентификаторБанка = "";
	КонецЕсли;
	
	Если ДанныеЗаполнения = Неопределено
		Или (ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И Не ДанныеЗаполнения.Свойство("Организация")) Тогда
		
		ОсновнаяОрганизация = ИнтеграцияАУСН.ОсновнаяОрганизация();
		Организация = ОсновнаяОрганизация;
		Для Каждого Запись Из ЭтотОбъект Цикл
			Запись.Организация = ОсновнаяОрганизация;
		КонецЦикла;
	Иначе
		Организация = ДанныеЗаполнения.Организация;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостоянияИнтеграцииАУСН.Организация КАК Организация,
		|	МАКСИМУМ(СостоянияИнтеграцииАУСН.ИдентификаторПриложения) КАК ИдентификаторПриложения,
		|	БанкиАУСН.Идентификатор КАК Идентификатор,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА СостоянияИнтеграцииАУСН.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияИнтеграцииАУСН.Подключено)
		|				ТОГДА СостоянияИнтеграцииАУСН.Состояние
		|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияИнтеграцииАУСН.ПустаяСсылка)
		|		КОНЕЦ) КАК СостояниеИнтеграции
		|ИЗ
		|	РегистрСведений.СостоянияИнтеграцииАУСН КАК СостоянияИнтеграцииАУСН
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БанкиАУСН КАК БанкиАУСН
		|		ПО СостоянияИнтеграцииАУСН.Банк = БанкиАУСН.Банк
		|ГДЕ
		|	СостоянияИнтеграцииАУСН.Организация = &Организация
		|	И БанкиАУСН.Идентификатор = &ИдентификаторБанка
		|
		|СГРУППИРОВАТЬ ПО
		|	СостоянияИнтеграцииАУСН.Организация,
		|	БанкиАУСН.Идентификатор";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ИдентификаторБанка", ИдентификаторБанка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ИдентификаторПриложения = Выборка.ИдентификаторПриложения;
		СостояниеИнтеграции = Выборка.СостояниеИнтеграции;
	Иначе
		ИдентификаторПриложения = Строка(Новый УникальныйИдентификатор);
		СостояниеИнтеграции = Перечисления.СостоянияИнтеграцииАУСН.ПустаяСсылка();
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.ИдентификаторПриложения = ИдентификаторПриложения;
		Запись.Состояние = СостояниеИнтеграции;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли