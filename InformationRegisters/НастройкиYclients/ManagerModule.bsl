#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс 

// СтандартныеПодсистемы.УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
// Параметры:
//  Ограничение - Структура:
//    * Текст                             - Строка - ограничение доступа для пользователей.
//                                          Если пустая строка, значит, доступ разрешен.
//    * ТекстДляВнешнихПользователей      - Строка - ограничение доступа для внешних пользователей.
//                                          Если пустая строка, значит, доступ запрещен.
//    * ПоВладельцуБезЗаписиКлючейДоступа - Неопределено - определить автоматически.
//                                        - Булево - если Ложь, то всегда записывать ключи доступа,
//                                          если Истина, тогда не записывать ключи доступа,
//                                          а использовать ключи доступа владельца (требуется,
//                                          чтобы ограничение было строго по объекту-владельцу).
//   * ПоВладельцуБезЗаписиКлючейДоступаДляВнешнихПользователей - Неопределено, Булево - также
//                                          как у параметра ПоВладельцуБезЗаписиКлючейДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Менеджер = "РегистрСведений.НастройкиYclients";
	МенеджерОборудованияВызовСервераПереопределяемый.ПриЗаполненииОграниченияДоступа(Менеджер, Ограничение);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

Функция UserToken(Логин, Ключи = "Bearer, User") Экспорт
	
	Результат = Неопределено;
	
	Если ЗначениеЗаполнено(Логин) Тогда
		Владелец = СтрШаблон("YclientsLogin:%1", СокрЛП(Логин));
		УстановитьПривилегированныйРежим(Истина);
		Результат = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключи);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция BearerToken() Экспорт
	
	Возврат "tnxntpwa4x57s78ja426";
	
КонецФункции

Функция Авторизация(Логин, Пароль, СообщениеОбОшибке, Параметны2ФА) Экспорт
	
	Токен = "";
	МетодЗапроса = "/api/v1/auth";
	BearerToken  = BearerToken();
	Таймаут      = 240;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api.yclients.com");
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , ,
		ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
		
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Accept",       "application/vnd.api.v2+json");
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", BearerToken);
	
	ПараметрыЗапроса = Новый Соответствие;
	ПараметрыЗапроса.Вставить("login",    Логин);
	ПараметрыЗапроса.Вставить("password", Пароль);
	Если Параметны2ФА.Есть2ФА Тогда
		ПараметрыЗапроса.Вставить("2fa", Новый Структура("uuid, secret", Параметны2ФА.Ключ2ФА, Параметны2ФА.Ответ2ФА));
	КонецЕсли;
			
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	ТелоЗапроса = ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(ПараметрыЗапроса);
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF8", ИспользованиеByteOrderMark.НеИспользовать);
	
	Попытка 
		HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос); // POST
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат "";
	КонецПопытки;
	
	КодОтвета = HTTPОтвет.КодСостояния;
	Если КодОтвета <> 201 И КодОтвета <> 200 Тогда
		Попытка
			СообщениеОбОшибке = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет).meta.message;
		Исключение
			СообщениеОбОшибке = HTTPОтвет.ПолучитьТелоКакСтроку();
		КонецПопытки;
		Возврат "";
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет, Истина);
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных. Причина: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат "";
	КонецПопытки;
	
	Если КодОтвета = 201 Тогда
		Если ТипЗнч(ДанныеОтвета.Получить("data")) = Тип("Соответствие") Тогда
			Параметны2ФА.Есть2ФА = Ложь;
			Возврат ДанныеОтвета.Получить("data").Получить("user_token");
		КонецЕсли;
	ИначеЕсли КодОтвета = 200 Тогда
		Если ТипЗнч(ДанныеОтвета.Получить("data")) = Тип("Соответствие") Тогда
			Параметны2ФА.Есть2ФА = Истина;
			Параметны2ФА.Ключ2ФА = ДанныеОтвета.Получить("data").Получить("uuid");
			ТекстПолучатель = ДанныеОтвета.Получить("data").Получить("transport").Получить("recipient");
			Параметны2ФА.Текст2ФА = СтрШаблон(НСтр("ru = 'Введите код подтверждения, отправленный на %1'"), ТекстПолучатель);
		КонецЕсли;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Процедура СохранитьДанныеАвторизации(Логин, Токен) Экспорт
		
	//Сохраним токен пользователя
	Если ЗначениеЗаполнено(Токен) Тогда
		Владелец = СтрШаблон("YclientsLogin:%1", СокрЛП(Логин));
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, Токен, "User");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, BearerToken(), "Bearer");
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьНастроенные() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиYclients.company_id КАК company_id
	|ИЗ
	|	РегистрСведений.НастройкиYclients КАК НастройкиYclients";
	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат.Следующий();
	
КонецФункции

#КонецОбласти

#КонецЕсли