
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("РегистрСведений.НастройкиYclients.Форма.ВыборПериода") Тогда
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
		Если ВыбранноеЗначение = Неопределено
			ИЛИ ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеНастройки = СтруктураНастроек();
		ЗаполнитьЗначенияСвойств(ДанныеНастройки, ТекущиеДанные);
		ДанныеНастройки.ДатаПоследнейЗагрузки = НачалоДня(ВыбранноеЗначение.ДатаНачала) - 1;
		ДанныеНастройки.ДатаКонцаЗагрузки = ВыбранноеЗначение.ДатаОкончания;
		
		ТекCompany_id = ТекущиеДанные.company_id;
		ДатаНачала    = ДанныеНастройки.ДатаПоследнейЗагрузки;
		ДатаОкончания = ДанныеНастройки.ДатаКонцаЗагрузки;
		
		ПараметрыВыполнения = Новый Структура;
		ПараметрыВыполнения.Вставить("Форма", ЭтотОбъект);
		ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("ПослеВыполненияОбменаСYclients", ЭтотОбъект));
		ПараметрыВыполнения.Вставить("Заголовок", НСтр("ru = 'Загружаем данные из Yclients'"));
		
		ОбменСYclientsКлиент.ВыполнитьОбменСYclients(ДанныеНастройки, ПараметрыВыполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьНастройку(Команда)
	
	НоваяНастройка();
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ДобавитьНастройку(Команда);
		Возврат;
	Конецесли;
	
	ДанныеНастройки = СтруктураНастроек();
	ЗаполнитьЗначенияСвойств(ДанныеНастройки, ТекущиеДанные);
	ДанныеНастройки.ДатаКонцаЗагрузки = НачалоДня(ТекущаяДата())-1;
	
	Если ДанныеНастройки.ДатаКонцаЗагрузки < ДанныеНастройки.ДатаПоследнейЗагрузки Тогда
		Сообщить(Нстр("ru='Все продажи до сегодняшнего дня уже загружены.'"));
		Возврат;
	КонецЕсли;
	
	ТекCompany_id = ТекущиеДанные.company_id;
	ДатаНачала    = ДанныеНастройки.ДатаПоследнейЗагрузки;
	ДатаОкончания = ДанныеНастройки.ДатаКонцаЗагрузки;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Форма", ЭтотОбъект);
	ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("ПослеВыполненияОбменаСYclients", ЭтотОбъект));
	ПараметрыВыполнения.Вставить("Заголовок", НСтр("ru = 'Загружаем данные из Yclients'"));

	ОбменСYclientsКлиент.ВыполнитьОбменСYclients(ДанныеНастройки, ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЗаПериод(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		НоваяНастройка();
		Возврат;
	Конецесли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачала", КонецДня(ТекущиеДанные.ДатаПоследнейЗагрузки)+1);
	ПараметрыФормы.Вставить("ДатаОкончания",  НачалоДня(ТекущаяДата())-1);
	
	ОткрытьФорму("РегистрСведений.НастройкиYclients.Форма.ВыборПериода", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НоваяНастройка()
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеНастройкиОбменаСYclients", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.НастройкиYclients.Форма.ПомощникПодключения", , ЭтотОбъект,
		, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияОбменаСYclients(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		РезультатЗагрузки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ТипЗнч(РезультатЗагрузки) = Тип("Структура") Тогда
			Если РезультатЗагрузки.Результат Тогда
				Если РезультатЗагрузки.ДанныеДокументов.Количество() > 0 Тогда
					СтруктураПараметров = Новый Структура;
					СтруктураПараметров.Вставить("ДанныеДокументов", РезультатЗагрузки.ДанныеДокументов);
					ОткрытьФорму("Обработка.ОбменСYclients.Форма.ЗагруженныеДокументы", СтруктураПараметров, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				Иначе
					Сообщить(НСтр("ru = 'Нет документов для загрузки.'"));
				КонецЕсли;
				Если ЗначениеЗаполнено(РезультатЗагрузки.СообщениеОбОшибке) Тогда
					Сообщить(РезультатЗагрузки.СообщениеОбОшибке);
				Иначе
					УстановитьДатаПоследнейЗагрузки(РезультатЗагрузки.company_id, РезультатЗагрузки.ДатаПоследнейЗагрузки);
				КонецЕсли;
				Элементы.Список.Обновить();
			ИначеЕсли РезультатЗагрузки.ОшибкаАвторизации Тогда
				ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеПолученияТокена", ЭтотОбъект);
				СтруктураПараметров = Новый Структура;
				СтруктураПараметров.Вставить("User_Login", РезультатЗагрузки.User_Login);
				ОткрытьФорму("РегистрСведений.НастройкиYclients.Форма.Авторизация", СтруктураПараметров, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			ИначеЕсли ЗначениеЗаполнено(РезультатЗагрузки.СообщениеОбОшибке) Тогда
				Сообщить(РезультатЗагрузки.СообщениеОбОшибке);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеНастройкиОбменаСYclients(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		СопоставитьНоменклатуру = Ложь;
		НастройкаВыполнена      = Ложь;
		Результат.Свойство("СопоставитьНоменклатуру", СопоставитьНоменклатуру);
		Результат.Свойство("НастройкаВыполнена", НастройкаВыполнена);
		Если НастройкаВыполнена
			И СопоставитьНоменклатуру Тогда
			АдресХранилища = "";
			СообщениеОбОшибке = "";
			ПолноеСопоставлениеНоменклатуры(Результат.company_id, АдресХранилища, СообщениеОбОшибке);
			Если ЗначениеЗаполнено(АдресХранилища) Тогда
				ПараметрыСопоставления = Новый Структура("АдресХранилища", АдресХранилища);
				ОткрытьФорму("Обработка.ОбменСYclients.Форма.ФормаСопоставления", ПараметрыСопоставления, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			ИначеЕсли ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
				Сообщить(СообщениеОбОшибке, СтатусСообщения.Внимание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияТокена(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Булево")
		И Результат Тогда
		
		ДанныеНастройки = ПолучитьНастройкиПоСompany_id(ТекCompany_id);
		
		ДанныеНастройки.ДатаПоследнейЗагрузки = ДатаНачала;
		ДанныеНастройки.ДатаКонцаЗагрузки = ДатаОкончания;
		
		ПараметрыВыполнения = Новый Структура;
		ПараметрыВыполнения.Вставить("Форма", ЭтотОбъект);
		ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("ПослеВыполненияОбменаСYclients", ЭтотОбъект));
		ПараметрыВыполнения.Вставить("Заголовок", НСтр("ru = 'Загружаем данные из Yclients'"));
		
		ОбменСYclientsКлиент.ВыполнитьОбменСYclients(ДанныеНастройки, ПараметрыВыполнения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНастройкиПоСompany_id(company_id)
	
	ДанныеНастройки = СтруктураНастроек();
	
	Запись = РегистрыСведений.НастройкиYclients.СоздатьМенеджерЗаписи();
	Запись.company_id = company_id;
	Запись.Прочитать();
	ЗаполнитьЗначенияСвойств(ДанныеНастройки, Запись);
	
	Возврат ДанныеНастройки;
	
КонецФункции

&НаСервере
Процедура ПолноеСопоставлениеНоменклатуры(company_id, АдресХранилища, СообщениеОбОшибке)
	
	Если Не ЗначениеЗаполнено(company_id) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНоменклатура = ОбменСYclients.НовыйТаблицаНоменклатуры();
	
	Логин = РегистрыСведений.НастройкиYclients.Получить(Новый Структура("company_id",company_id)).User_Login;
	Токены = РегистрыСведений.НастройкиYclients.UserToken(Логин);
	
	Таймаут      = 240;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api.yclients.com");
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , ,
		ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
		
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Accept",       "application/vnd.api.v2+json");
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", СтрШаблон("%1,%2", Токены.Bearer, Токены.User));
	
	//Товары
	МетодЗапроса = СтрШаблон("api/v1/goods/%1", Формат(company_id, "ЧГ=0"));
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	Попытка 
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос); // GET
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат;
	КонецПопытки;
	
	КодОтвета = HTTPОтвет.КодСостояния;
	Если КодОтвета <> 200 Тогда
		Попытка
			СообщениеОбОшибке = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет).meta.message;
		Исключение
			СообщениеОбОшибке = HTTPОтвет.ПолучитьТелоКакСтроку();
		КонецПопытки;
		Возврат;
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет);
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных. Причина: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат;
	КонецПопытки;
	
	Если ДанныеОтвета.success Тогда
		Для Каждого Товар Из ДанныеОтвета.data Цикл
			Если НЕ ЗначениеЗаполнено(Товар.good_id) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаНоменклатура.Добавить();
			НоваяСтрока.Идентификатор    = Товар.good_id;
			НоваяСтрока.Наименование     = Товар.title;
			НоваяСтрока.Артикул          = Товар.article;
			НоваяСтрока.Услуга           = Ложь;
			НоваяСтрока.Единица          = Товар.unit_short_title;
			НоваяСтрока.Сертификат       = ЗначениеЗаполнено(Товар.loyalty_certificate_type_id);
			НоваяСтрока.Абонимент        = ЗначениеЗаполнено(Товар.loyalty_abonement_type_id);
		КонецЦикла;
	КонецЕсли;
	
	//Услуги
	МетодЗапроса = СтрШаблон("api/v1/company/%1/services", Формат(company_id, "ЧГ=0"));
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	Попытка 
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос); // GET
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат;
	КонецПопытки;
	
	КодОтвета = HTTPОтвет.КодСостояния;
	Если КодОтвета <> 200 Тогда
		Попытка
			СообщениеОбОшибке = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет).meta.message;
		Исключение
			СообщениеОбОшибке = HTTPОтвет.ПолучитьТелоКакСтроку();
		КонецПопытки;
		Возврат;
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет);
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных. Причина: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат;
	КонецПопытки;
	
	Если ДанныеОтвета.success Тогда
		Для Каждого Услуга Из ДанныеОтвета.data Цикл
			Если НЕ ЗначениеЗаполнено(Услуга.id) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаНоменклатура.Добавить();
			НоваяСтрока.Идентификатор    = Услуга.id;
			НоваяСтрока.Наименование     = Услуга.title;
			НоваяСтрока.Артикул          = "";
			НоваяСтрока.Услуга           = Истина;
			НоваяСтрока.Единица          = "шт";
			НоваяСтрока.Сертификат       = Ложь;
			НоваяСтрока.Абонимент        = Ложь;
		КонецЦикла;
	КонецЕсли;
	
	МассивИдНоменклатуры                = ТаблицаНоменклатура.ВыгрузитьКолонку("Идентификатор");
	ИдентификаторыНоменклатуры          = ОбменСYclients.НоменклатураПоИдентификаторам(МассивИдНоменклатуры);
	МассивНесопоставленнойНоменклатуры  = Новый Массив;
	
	Для Каждого СтрокаНоменклатуры Из ТаблицаНоменклатура Цикл
		Если СтрокаНоменклатуры.Сертификат ИЛИ СтрокаНоменклатуры.Абонимент Тогда
			//Абонименты и сертификаты отражаются как предоплата, а не как продажа товаров
			Продолжить;
		КонецЕсли;
		СтрокаНоменклатуры.Номенклатура = ИдентификаторыНоменклатуры.Получить(СтрокаНоменклатуры.Идентификатор);
		Если Не ЗначениеЗаполнено(СтрокаНоменклатуры.Номенклатура) Тогда
			МассивНесопоставленнойНоменклатуры.Добавить(СтрокаНоменклатуры);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаНесопоставленнойНоменклатуры = ТаблицаНоменклатура.Скопировать(МассивНесопоставленнойНоменклатуры);
		
	АдресХранилища = ПоместитьВоВременноеХранилище(ТаблицаНесопоставленнойНоменклатуры, УникальныйИдентификатор);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьДатаПоследнейЗагрузки(company_id, ДатаПоследнейЗагрузки)
	
	Запись = РегистрыСведений.НастройкиYclients.СоздатьМенеджерЗаписи();
	Запись.company_id = company_id;
	Запись.Прочитать();
	Если Запись.ДатаПоследнейЗагрузки < ДатаПоследнейЗагрузки Тогда
		Запись.ДатаПоследнейЗагрузки = ДатаПоследнейЗагрузки;
		Запись.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураНастроек()
	
	Возврат Новый Структура("company_id, company_title, User_Login,
		|Организация, Склад, Покупатель, ВидОплатыКартой, ДатаПоследнейЗагрузки, ДатаКонцаЗагрузки");
	
КонецФункции

#КонецОбласти


