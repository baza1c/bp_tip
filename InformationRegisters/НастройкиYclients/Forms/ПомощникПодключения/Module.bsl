#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
		Запись.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	Элементы.ПользовательYclients.СписокВыбора.Очистить();
	Запись.ДатаНачалаЗагрузки = НачалоМесяца(ДобавитьМесяц(ТекущаяДата(), -1));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НастройкиYclients.User_Login КАК User_Login
	|ИЗ
	|	РегистрСведений.НастройкиYclients КАК НастройкиYclients";
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Если ЗначениеЗаполнено(Результат.User_Login) Тогда
			Элементы.ПользовательYclients.СписокВыбора.Добавить(Результат.User_Login);
		КонецЕсли;
	КонецЦикла;
	Если Элементы.ПользовательYclients.СписокВыбора.Количество() > 0 Тогда
		Элементы.ПользовательYclients.СписокВыбора.Добавить("", "Новый пользователель", , БиблиотекаКартинок.СоздатьЭлементСписка);
		ПользовательYclients = Элементы.ПользовательYclients.СписокВыбора[0].Значение;
		Элементы.User_Login.Видимость = Ложь;
		Элементы.Пароль.Видимость = Ложь;
	Иначе
		Элементы.ПользовательYclients.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаНазад.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.User_Login)
		И ЗначениеЗаполнено(Пароль) Тогда
		СообщениеОбОшибке = "";
		Параметны2ФА = Новый Структура("Есть2ФА, Ключ2ФА, Ответ2ФА, Текст2ФА", Ложь, "", "", "");
		НастройкаСНовымПользователем(СообщениеОбОшибке, Параметны2ФА);
		Если Параметны2ФА.Есть2ФА = Истина Тогда
			РежимВвода2ФА = Истина;
			Ключ2ФА = Параметны2ФА.Ключ2ФА;
			Элементы.User_Login.Доступность       = Ложь;
			Элементы.Пароль.Доступность           = Ложь;
			Элементы.КодПодтверждения.Доступность = Истина;
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки;
			Элементы.ФормаНазад.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	Если Элемент.РежимПароля Тогда
		Элемент.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;
	Иначе
		Элемент.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыСкрыты;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательYclientsПриИзменении(Элемент)
	
	Элементы.User_Login.Видимость = Не ЗначениеЗаполнено(ПользовательYclients);
	Элементы.Пароль.Видимость = НЕ ЗначениеЗаполнено(ПользовательYclients);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если НЕ ПроверкаЗаполнения() Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация Тогда
		СообщениеОбОшибке = "";
		Если ЗначениеЗаполнено(ПользовательYclients) Тогда
			Запись.User_Login = ПользовательYclients;
			НастройкаСАвторизованнымПользователем(СообщениеОбОшибке);
		ИначеЕсли РежимВвода2ФА Тогда
			Параметны2ФА = Новый Структура("Есть2ФА, Ключ2ФА, Ответ2ФА, Текст2ФА", Истина, Ключ2ФА, КодПодтверждения, "");
			НастройкаСНовымПользователем(СообщениеОбОшибке, Параметны2ФА);
		ИначеЕсли НЕ РежимВвода2ФА Тогда
			Параметны2ФА = Новый Структура("Есть2ФА, Ключ2ФА, Ответ2ФА, Текст2ФА", Ложь, "", "", "");
			НастройкаСНовымПользователем(СообщениеОбОшибке, Параметны2ФА);
			Если Параметны2ФА.Есть2ФА = Истина Тогда
				РежимВвода2ФА = Истина;
				Ключ2ФА = Параметны2ФА.Ключ2ФА;
				Элементы.User_Login.Доступность       = Ложь;
				Элементы.Пароль.Доступность           = Ложь;
				Элементы.КодПодтверждения.Доступность = Истина;
				Возврат;
			КонецЕсли;
		Конецесли;
		Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			Сообщить(СообщениеОбОшибке, СтатусСообщения.Важное);
		Иначе
			Если НЕ ЗначениеЗаполнено(ПользовательYclients) Тогда
				Элементы.User_Login.Доступность       = Истина;
				Элементы.Пароль.Доступность           = Истина;
				Элементы.КодПодтверждения.Доступность = Ложь;
			КонецЕсли;
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки;
			Элементы.ФормаНазад.Видимость = Истина;
		КонецЕсли;
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки Тогда
		Запись.ДатаПоследнейЗагрузки = Запись.ДатаНачалаЗагрузки;
		Запись.company_title = Элементы.ОрганизацияYclients.СписокВыбора.НайтиПоЗначению(Запись.company_id).Представление;
		Если ПроверитьУстановитьФункциональныеОпции() Тогда
			ОбновитьИнтерфейс();
		КонецЕсли;
		Записать();
		
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНоменклатура;
		Элементы.ФормаДалее.Заголовок = "Готово";
		
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНоменклатура Тогда
		
		РезультатНастройки = Новый Структура;
		РезультатНастройки.Вставить("company_title",           Запись.company_title);
		РезультатНастройки.Вставить("company_id",              Запись.company_id);
		РезультатНастройки.Вставить("НастройкаВыполнена",      Истина);
		РезультатНастройки.Вставить("СопоставитьНоменклатуру", СопособСопоставленияНоменклатуры = 0);
		
		Закрыть(РезультатНастройки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки Тогда
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация;
		Элементы.ФормаНазад.Видимость = Ложь;
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНоменклатура Тогда
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки;
		Элементы.ФормаДалее.Заголовок = "Далее";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверкаЗаполнения()
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация Тогда
		Если ЗначениеЗаполнено(ПользовательYclients) Тогда
			Возврат Истина;
		ИначеЕсли ЗначениеЗаполнено(Запись.User_Login) И ЗначениеЗаполнено(Пароль) Тогда
			Возврат Истина;
		Иначе
			Сообщить(Нстр("ru='Выберите ранее авторизованного пользователя или укажите логин и пароль нового пользователя Yclients.'"));
			Возврат Ложь;
		КонецЕсли;
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки Тогда
		Результат = Истина;
		Если НЕ ЗначениеЗаполнено(Запись.company_id) Тогда
			Сообщить(Нстр("ru='Не выбрана компания в Yclients.'"));
			Результат = Ложь;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
			Сообщить(Нстр("ru='Не выбрана организация.'"));
			Результат = Ложь;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Запись.Склад) Тогда
			Сообщить(Нстр("ru='Не выбран склад (торговая точка).'"));
			Результат = Ложь;
		КонецЕсли;
		Возврат Результат;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

&НаСервере
Процедура НастройкаСАвторизованнымПользователем(СообщениеОбОшибке)
	
	Токен = РегистрыСведений.НастройкиYclients.UserToken(Запись.User_Login, "User");
	Если Токен = Неопределено Тогда
		СообщениеОбОшибке = СтрШаблон(Нстр("ru='Токен пользователя %1 не найден.'"), Запись.User_Login); 
		ПользовательБезТокена = Элементы.ПользовательYclients.СписокВыбора.НайтиПоЗначению(Запись.User_Login);
		Если ЗначениеЗаполнено(ПользовательБезТокена) Тогда
			Элементы.ПользовательYclients.СписокВыбора.Удалить(ПользовательБезТокена);
			ПользовательYclients = "";
			Элементы.User_Login.Видимость = Истина;
			Элементы.Пароль.Видимость = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПолучитьДоступныеОрганизации(СообщениеОбОшибке);
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(ОрганизацияYclients) Тогда
		СообщениеОбОшибке = Нстр("ru='Нет доступных для подключения команий Yclients.'");
		Возврат;
	Иначе
		Запись.company_id = ОрганизацияYclients;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастройкаСНовымПользователем(СообщениеОбОшибке, Параметны2ФА)
	
	Токен = РегистрыСведений.НастройкиYclients.Авторизация(Запись.User_Login, Пароль, СообщениеОбОшибке, Параметны2ФА);
	
	Если Параметны2ФА.Есть2ФА Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(Токен) Тогда
		СообщениеОбОшибке = Нстр("ru='Не удалось получить токен пользователя Yclients.'");
		Возврат;
	Иначе
		РегистрыСведений.НастройкиYclients.СохранитьДанныеАвторизации(Запись.User_Login, Токен);
		ПользовательYclients = Запись.User_Login;
		Если ЗначениеЗаполнено(ПользовательYclients)
			И Элементы.ПользовательYclients.СписокВыбора.НайтиПоЗначению(ПользовательYclients) = Неопределено Тогда
			Элементы.ПользовательYclients.СписокВыбора.Вставить(0, Запись.User_Login);
		КонецЕсли;
	КонецЕсли;
	
	ПолучитьДоступныеОрганизации(СообщениеОбОшибке);
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(ОрганизацияYclients) Тогда
		СообщениеОбОшибке = Нстр("ru='Нет доступных для подключения команий Yclients.'");
		Возврат;
	Иначе
		Запись.company_id = ОрганизацияYclients;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДоступныеОрганизации(СообщениеОбОшибке)
	
	ОрганизацияYclients = 0;
	
	МетодЗапроса = "api/v1/companies";
	Таймаут      = 240;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api.yclients.com");
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , ,
		ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
		
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Accept",       "application/vnd.api.v2+json");
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", СтрШаблон("%1,%2", 
		РегистрыСведений.НастройкиYclients.BearerToken(), Токен));
	
	СписокПараметровМетода = Новый Структура;
	СписокПараметровМетода.Вставить("my", 1);
	ПараметрыСтрокой = Новый Массив;
	Для Каждого Параметр Из СписокПараметровМетода Цикл
		ПараметрыСтрокой.Добавить(Параметр.Ключ+"="+Параметр.Значение);
	КонецЦикла;
	МетодЗапроса = МетодЗапроса+ "?" + СтрСоединить(ПараметрыСтрокой,"&");
	
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	Попытка 
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос); // GET
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат;
	КонецПопытки;
	
	КодОтвета = HTTPОтвет.КодСостояния;
	Если КодОтвета <> 200 Тогда
		Попытка
			СообщениеОбОшибке = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет).meta.message;
		Исключение
			СообщениеОбОшибке = HTTPОтвет.ПолучитьТелоКакСтроку();
		КонецПопытки;
		Возврат;
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет);
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных. Причина: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат;
	КонецПопытки;
	
	Если ДанныеОтвета.success Тогда
		
		Элементы.ОрганизацияYclients.СписокВыбора.Очистить();

		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	НастройкиYclients.company_id КАК company_id
		|ИЗ
		|	РегистрСведений.НастройкиYclients КАК НастройкиYclients";
		МассивНастоенных = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("company_id");
			
		Если ТипЗнч(ДанныеОтвета.data) = Тип("Массив")
			И ДанныеОтвета.data.Количество() > 0 Тогда
			Для Каждого ЭлементОрганизации Из ДанныеОтвета.data Цикл
				ПостфиксОрганизации = "";
				Пометка = Ложь;
				Если НЕ МассивНастоенных.Найти(ЭлементОрганизации.id) = Неопределено Тогда
					ПостфиксОрганизации = Нстр("ru = ' <интеграция уже настроена>'");
					Пометка = Истина;
				ИначеЕсли ОрганизацияYclients = 0 Тогда
					ОрганизацияYclients = ЭлементОрганизации.id;
				КонецЕсли;
				Элементы.ОрганизацияYclients.СписокВыбора.Добавить(ЭлементОрганизации.id, 
					СтрШаблон("%1%2", ЭлементОрганизации.title, ПостфиксОрганизации), Пометка, );
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьУстановитьФункциональныеОпции()
	
	ОбновитьИнтерфейс = Ложь;
	
	Если ЗначениеЗаполнено(Запись.ВидОплатыКартой) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Если Не Константы.ИспользоватьОплатуПоПлатежнымКартам.Получить() Тогда
			Константы.ИспользоватьОплатуПоПлатежнымКартам.Установить(Истина);
			ОбновитьИнтерфейс = Истина;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.Покупатель) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Если Не Константы.ИспользоватьПредоплатыВРознице.Получить() Тогда
			Константы.ИспользоватьПредоплатыВРознице.Установить(Истина);
			ОбновитьИнтерфейс = Истина;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Возврат ОбновитьИнтерфейс;
	
КонецФункции

&НаКлиенте
Процедура ОрганизацияYclientsПриИзменении(Элемент)
	
	Если Элементы.ОрганизацияYclients.СписокВыбора.НайтиПоЗначению(ОрганизацияYclients).Пометка Тогда
		Сообщить(Нстр("ru='Интеграция с этой компанией уже настроена'"));
		ОрганизацияYclients = Запись.company_id;
	Иначе
		Запись.company_id = ОрганизацияYclients;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
