
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура УстановитьВерсиюПроверкиДрайверов(ИдентификаторОборудования, Версия) Экспорт
	НаборЗаписей = РегистрыСведений.ОборудованиеПоОрганизациям.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Оборудование.Установить(ИдентификаторОборудования);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 1 Тогда
		НаборЗаписей[0].ВерсияПроверкиДрайверов = Версия;
		НаборЗаписей.Записать();
	КонецЕсли;
КонецПроцедуры

// Установление параметров оборудования
//
// Параметры:
//  <ИдентификаторОборудования>  - <Справочника.Вид> - ссылка на справочник
//  <СтруктураПараметров>  - <Структура> - см. МенеджерОборудованияБП.НовыйПараметрыОборудования
//
Процедура УстановитьПараметрыОборудования(ИдентификаторОборудования, СтруктураПараметров) Экспорт
	НаборЗаписей = РегистрыСведений.ОборудованиеПоОрганизациям.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Оборудование.Установить(ИдентификаторОборудования);
	
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 1 Тогда
		Запись = НаборЗаписей[0];
	Иначе
		Запись = НаборЗаписей.Добавить();
		Запись.Оборудование = ИдентификаторОборудования;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, СтруктураПараметров);
	
	НаборЗаписей.Записать(Истина);
КонецПроцедуры


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации 
	|	ПО СправочникОрганизации.Ссылка = ЭтотСписок.Организация
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|ЗначениеРазрешено(Организация, ПустаяСсылка КАК Истина)
	|ИЛИ ЗначениеРазрешено(СправочникОрганизации.ГоловнаяОрганизация, ПустаяСсылка КАК Истина)
	|";


КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПризнакПечатьЧека(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОборудованиеПоОрганизациям.Оборудование КАК Оборудование
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|		ПО ОборудованиеПоОрганизациям.Оборудование = ПодключаемоеОборудование.Ссылка
	|ГДЕ
	|	ОборудованиеПоОрганизациям.ПечататьЧек = ЛОЖЬ
	|	И ОборудованиеПоОрганизациям.ОтправлятьEMail = ЛОЖЬ
	|	И ОборудованиеПоОрганизациям.ОтправлятьSMS = ЛОЖЬ
	|	И ПодключаемоеОборудование.ТипОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыПодключаемогоОборудования.ККТ)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НеУдалосьОбработать = 0;
	ОбработаноЗаписей = 0;
	
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОборудованиеПоОрганизациям");
			ЭлементБлокировки.УстановитьЗначение("Оборудование", Выборка.Оборудование);
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ОборудованиеПоОрганизациям.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Оборудование.Установить(Выборка.Оборудование);
			НаборЗаписей.Прочитать();
			
			Для каждого Запись Из НаборЗаписей Цикл
				Запись.ПечататьЧек = Истина;
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина);
			ОбработаноЗаписей = ОбработаноЗаписей + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не удалось установить признак печати чека по причине
                                   |%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.ПодключаемоеОборудование,
				Выборка.Оборудование, 
				ТекстСообщения);
				
			НеУдалосьОбработать = НеУдалосьОбработать + 1;
		КонецПопытки;
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ОборудованиеПоОрганизациям, ,
		СтрШаблон(
		НСтр("ru = 'Процедура РегистрСведений.ОборудованиеПоОрганизациям.ЗаполнитьПризнакПечатьЧека() обработала %1 записей. Не удалось обработать %2'"), 
		ОбработаноЗаписей, НеУдалосьОбработать));
	
	Параметры.ОбработкаЗавершена = (ОбработаноЗаписей = 0 ИЛИ НеУдалосьОбработать = 0);
КонецПроцедуры

#КонецОбласти 

#КонецЕсли
