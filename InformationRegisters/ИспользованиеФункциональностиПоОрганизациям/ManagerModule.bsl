#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
//
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Обработчик первоначального заполнения видимости команд раздела Организация
//
Процедура УстановитьЗначенияПриОбновленииИБ() Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	ЗаполнитьОпцииПростогоИнтерфейса(НаборЗаписей);
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

// Устанавливает видимость команд раздела Организация
//
Процедура УстановитьЗначения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей = СоздатьНаборЗаписей();
	ЗаполнитьОпцииПростогоИнтерфейса(НаборЗаписей);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Устанавливает видимость команд элемента справочника Организации
//
// Параметры:
//   Организация - СправочникСсылка.Организации
//
Процедура УстановитьЗначенияПоОрганизации(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	ЗаполнитьОпцииПростогоИнтерфейса(НаборЗаписей, Организация);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьОпцииПростогоИнтерфейса(НаборЗаписей, Организация = Неопределено)
	
	ЭтоПростойИнтерфейс = ОбщегоНазначенияБП.ЭтоПростойИнтерфейс();
	
	Если Организация <> Неопределено Тогда
		Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	Иначе
		Организации = Неопределено;
	КонецЕсли;
	ВидыОрганизаций = Справочники.Организации.ВидыОрганизаций(Организации);
			
	Для Каждого ВидОрганизации Из ВидыОрганизаций Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Организация = ВидОрганизации.Организация;
		Запись.ФормированиеУставногоКапиталаПростойИнтерфейс = ЭтоПростойИнтерфейс
			И ВидОрганизации.ЭтоЮрЛицо;
		Запись.ИнтерфейсТаксиПростойИПиЮрЛица = ЭтоПростойИнтерфейс
			И (ВидОрганизации.ЭтоЮрЛицо Или ВидОрганизации.ЭтоИП);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли