#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// СтандартныеПодсистемы.УправлениеДоступом
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#Область ПолучениеДанных

// Возвращает список документов, для которых есть предупреждение при загрузке из маркетплейсов.
//
// Параметры:
//  Отбор - Структура - см. НовыеПараметрыОтбораПредупреждений()
//
// Возвращаемое значение:
//  - Массив - Документы, для которых есть предупреждения при загрузке.
//
Функция ДокументыСПредупреждениями(Отбор) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)";
	
	Запрос = ЗапросДляОтбораПредупреждений(ТекстЗапроса, Отбор);
	
	ДокументыТребующиеПроверки = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументыТребующиеПроверки.Добавить(Выборка.Документ);
	КонецЦикла;
	
	Возврат ДокументыТребующиеПроверки;
	
КонецФункции

// Возвращает параметры отбора предупреждений при загрузке из маркетплейсов.
//
// Возвращаемое значение:
//  Структура:
//    * Организация     - СправочникСсылка.Организации     - Организация, для которой ищется предупреждение.
//                                                           Если не задана, то по всем организациям.
//    * НачалоПериода   - Дата                             - Дата, с которой ищутся документы с предупреждениями.
//    * КонецПериода    - Дата                             - Дата, по которую ищутся документы с предупреждениями.
//                                                           Если задан, то приводится к концу дня.
//    * ТипДокументов   - Строка                           - Тип документов, для которых нужно проверить наличие предупреждений.
//                                                           Если не задан, то по всем документам.
//
Функция НовыеПараметрыОтбораПредупреждений() Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ПараметрыОтбора.Вставить("Маркетплейс", Перечисления.ВидыМаркетплейсов.ПустаяСсылка());
	ПараметрыОтбора.Вставить("НачалоПериода", Дата(1,1,1));
	ПараметрыОтбора.Вставить("КонецПериода", Дата(1,1,1));
	ПараметрыОтбора.Вставить("ТипДокументов", "");
	
	Возврат ПараметрыОтбора;
	
КонецФункции

Функция ДокументыБезПредупреждений(Документы) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документы", Документы);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Документ В(&Документы)
	|	И НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)";
	
	ДокументыСПредупреждениями = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ");
	
	Возврат ОбщегоНазначенияКлиентСервер.РазностьМассивов(Документы, ДокументыСПредупреждениями);
	
КонецФункции

// Определяет количество документов, требующих проверки
//
// Параметры:
//  Отбор - Структура - см. НовыеПараметрыОтбораПредупреждений()
//
// Возвращаемое значение:
//  Число - количество документов
//
Функция КоличествоПредупреждений(Отбор) Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|			ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.ВозвратТоваровОтПокупателя).ОтчетМаркетплейса
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияОтгруженныхТоваров
	|			ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияОтгруженныхТоваров).ОтчетМаркетплейса
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияТоваровУслуг).ОтчетМаркетплейса
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ОтчетМаркетплейса.ПустаяСсылка)
	|	КОНЕЦ КАК ОтчетМаркетплейса
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Документы.Документ) КАК Количество
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы
	|ГДЕ
	|	ВТ_Документы.ОтчетМаркетплейса = ЗНАЧЕНИЕ(Документ.ОтчетМаркетплейса.ПустаяСсылка)";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ОператорыЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	
	Если Отбор.Организация = Неопределено Тогда
		ДоступныеОрганизации = Справочники.Организации.ДоступныеОрганизацииПоRLS();
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Организация В (&ДоступныеОрганизации)");
		Запрос.УстановитьПараметр("ДоступныеОрганизации", ДоступныеОрганизации);
	Иначе
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Отбор.Организация);
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Количество = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

// Определяет есть ли документы с предупреждениями
// 
// Возвращаемое значение:
//  Булево - наличие документов с предупреждениями
//
Функция ЕстьПредупреждения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Предупреждения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ЕстьПредупреждениеДляДокумента(Документ) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Предупреждения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ОператорыЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ОтчетМаркетплейса") Тогда
		ОператорыЗапроса.Отбор.Добавить(
			"ВЫБОР
			|	КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
			|		ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияТоваровУслуг).ОтчетМаркетплейса
			|	КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияОтгруженныхТоваров
			|		ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияОтгруженныхТоваров).ОтчетМаркетплейса
			|	КОГДА Предупреждения.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
			|		ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.ВозвратТоваровОтПокупателя).ОтчетМаркетплейса
			|	КОНЕЦ = &Документ");
	Иначе
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Документ = &Документ");
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СохранениеДанных

// Сохраняет предупреждения по непроведенным документам
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Маркетплейс - ПеречислениеСсылка.ВидыМаркетплейсов
//  НепроведенныеДокументы - Массив - Структур
//   * Ссылка         - ДокументСсылка - документ, который не удалось провести,
//   * ОписаниеОшибки - Строка         - текст описания ошибки при проведении.
//
Процедура СохранитьНепроведенныеДокументы(Организация, Маркетплейс, НепроведенныеДокументы) Экспорт
	
	ПараметрыПредпреждения = НовыеПараметрыПредупреждения(
		Перечисления.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НепроведенДокумент);
	ПараметрыПредпреждения.Маркетплейс = Маркетплейс;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		СписокДокументов = Новый Массив;
		Для Каждого НепроведенныйДокумент Из НепроведенныеДокументы Цикл
			СписокДокументов.Добавить(НепроведенныйДокумент.Ссылка);
		КонецЦикла;
		ОрганизацииДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокДокументов, "Организация");
	КонецЕсли;
	
	Для Каждого НепроведенныйДокумент Из НепроведенныеДокументы Цикл
		// У отчета маркетплейса не может быть статуса НепроведенДокумент
		Если ТипЗнч(НепроведенныйДокумент.Ссылка) = Тип("ДокументСсылка.ОтчетМаркетплейса") Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(Организация) Тогда
			ПараметрыПредпреждения.Организация = Организация;
		Иначе
			ПараметрыПредпреждения.Организация = ОрганизацииДокументов.Получить(НепроведенныйДокумент.Ссылка);
		КонецЕсли;
		ПараметрыПредпреждения.Документ = НепроведенныйДокумент.Ссылка;
		ПараметрыПредпреждения.ОписаниеОшибки = НепроведенныйДокумент.ОписаниеОшибки;
		ЗаписатьПредупреждение(ПараметрыПредпреждения);
	КонецЦикла;
	
КонецПроцедуры

Функция НовыеПараметрыПредупреждения(СтатусДокумента = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Документ");
	Параметры.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Параметры.Вставить("Маркетплейс", Перечисления.ВидыМаркетплейсов.ПустаяСсылка());
	Параметры.Вставить("СтатусДокумента", СтатусДокумента);
	Параметры.Вставить("ДанныеОтчета");
	Параметры.Вставить("ОписаниеОшибки", "");
	
	Возврат Параметры;
	
КонецФункции

Процедура ЗаписатьПредупреждение(Параметры) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Организация.Установить(Параметры.Организация);
	Набор.Отбор.Маркетплейс.Установить(Параметры.Маркетплейс);
	Набор.Отбор.Документ.Установить(Параметры.Документ);
	Если ЗначениеЗаполнено(Параметры.СтатусДокумента) Тогда
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Параметры);
	КонецЕсли;
	Набор.Записать();
	
КонецПроцедуры

Процедура УдалитьПредупреждениеДляДокумента(Документ) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура УдалитьПредупреждениеДляОтчетаМаркетплейса(ПодчиненныйДокумент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодчиненныйДокумент", ПодчиненныйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|ПОМЕСТИТЬ ВТ_ОтчетМаркетплейса
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ПодчиненныйДокумент
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.ОтчетМаркетплейса
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка = &ПодчиненныйДокумент
	|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияОтгруженныхТоваров.ОтчетМаркетплейса
	|ИЗ
	|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|ГДЕ
	|	РеализацияОтгруженныхТоваров.Ссылка = &ПодчиненныйДокумент
	|	И НЕ РеализацияОтгруженныхТоваров.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВЫБОР
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА Предупреждения.Документ.ОтчетМаркетплейса
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияОтгруженныхТоваров
	|			ТОГДА Предупреждения.Документ.ОтчетМаркетплейса
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|			ТОГДА Предупреждения.Документ.ОтчетМаркетплейса
	|	КОНЕЦ КАК ОтчетМаркетплейса
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Маркетплейс = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)
	|	И ВЫБОР
	|			КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияТоваровУслуг).ОтчетМаркетплейса
	|			КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияОтгруженныхТоваров
	|				ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияОтгруженныхТоваров).ОтчетМаркетплейса
	|			КОГДА Предупреждения.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|				ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.ВозвратТоваровОтПокупателя).ОтчетМаркетплейса
	|		КОНЕЦ В
	|			(ВЫБРАТЬ
	|				ВТ_ОтчетМаркетплейса.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|			ИЗ
	|				ВТ_ОтчетМаркетплейса КАК ВТ_ОтчетМаркетплейса)
	|	И НЕ Предупреждения.Документ ССЫЛКА Документ.ОтчетМаркетплейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Документ В
	|			(ВЫБРАТЬ
	|				ВТ_ОтчетМаркетплейса.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|			ИЗ
	|				ВТ_ОтчетМаркетплейса КАК ВТ_ОтчетМаркетплейса)
	|	И Предупреждения.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НесозданыДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОтчетМаркетплейса.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|ИЗ
	|	ВТ_ОтчетМаркетплейса КАК ВТ_ОтчетМаркетплейса";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	Если ПакетЗапросов[1].Пустой() // нет подчиненных документов с предупреждениями
		И ПакетЗапросов[2].Пустой() Тогда // нет несвязанных документов
		Выборка = ПакетЗапросов[3].Выбрать();
		Если Выборка.Следующий() Тогда
			УдалитьПредупреждениеДляДокумента(Выборка.ОтчетМаркетплейса);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДляОтчетовМаркетплейсов() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.Документ КАК Документ
	|ПОМЕСТИТЬ ВТ_ЕстьНесозданныеДокументы
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Документ ССЫЛКА Документ.ОтчетМаркетплейса
	|	И Предупреждения.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НесозданыДокументы)
	|	И Предупреждения.Маркетплейс = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияТоваровУслуг).ОтчетМаркетплейса
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияОтгруженныхТоваров
	|			ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияОтгруженныхТоваров).ОтчетМаркетплейса
	|		КОГДА Предупреждения.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|			ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.ВозвратТоваровОтПокупателя).ОтчетМаркетплейса
	|	КОНЕЦ КАК Документ,
	|	Предупреждения.Организация КАК Организация,
	|	Предупреждения.Маркетплейс КАК Маркетплейс,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НеотработаныПодчиненныеДокументы) КАК СтатусДокумента
	|ПОМЕСТИТЬ ВТ_ПредупрежденияДляЗаписи
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Маркетплейс = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)
	|	И НЕ Предупреждения.Документ ССЫЛКА Документ.ОтчетМаркетплейса
	|	И НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Предупреждения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Документ ССЫЛКА Документ.ОтчетМаркетплейса
	|	И Предупреждения.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НеотработаныПодчиненныеДокументы)
	|	И Предупреждения.Маркетплейс = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПредупрежденияДляЗаписи.Документ КАК Документ,
	|	ВТ_ПредупрежденияДляЗаписи.Организация КАК Организация,
	|	ВТ_ПредупрежденияДляЗаписи.Маркетплейс КАК Маркетплейс,
	|	ВТ_ПредупрежденияДляЗаписи.СтатусДокумента КАК СтатусДокумента
	|ИЗ
	|	ВТ_ПредупрежденияДляЗаписи КАК ВТ_ПредупрежденияДляЗаписи
	|ГДЕ
	|	НЕ ВТ_ПредупрежденияДляЗаписи.Документ В
	|				(ВЫБРАТЬ
	|					ВТ_ЕстьНесозданныеДокументы.Документ КАК Документ
	|				ИЗ
	|					ВТ_ЕстьНесозданныеДокументы КАК ВТ_ЕстьНесозданныеДокументы)";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ПредупрежденияКУдалению = ПакетЗапросов[2].Выбрать();
	Пока ПредупрежденияКУдалению.Следующий() Цикл
		УдалитьПредупреждениеДляДокумента(ПредупрежденияКУдалению.Документ);
	КонецЦикла;
	
	ПредупрежденияДляЗаписи = ПакетЗапросов[3].Выбрать();
	Пока ПредупрежденияДляЗаписи.Следующий() Цикл
		Параметры = НовыеПараметрыПредупреждения(ПредупрежденияДляЗаписи.СтатусДокумента);
		ЗаполнитьЗначенияСвойств(Параметры, ПредупрежденияДляЗаписи);
		ЗаписатьПредупреждение(Параметры);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗапросДляОтбораПредупреждений(ШаблонТекстаЗапроса, ПараметрыЗапроса)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ШаблонТекстаЗапроса);
	
	ОператорыЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	
	Запрос = Новый Запрос;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Организация) Тогда
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", ПараметрыЗапроса.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Маркетплейс) Тогда
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Маркетплейс = &Маркетплейс");
		Запрос.УстановитьПараметр("Маркетплейс", ПараметрыЗапроса.Маркетплейс);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.НачалоПериода) Тогда
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Документ.Дата >= &НачалоПериода");
		Запрос.УстановитьПараметр("НачалоПериода", ПараметрыЗапроса.НачалоПериода);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Документ.Дата <= &КонецПериода");
		Запрос.УстановитьПараметр("КонецПериода", КонецДня(ПараметрыЗапроса.КонецПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.ТипДокументов) Тогда
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Документ ССЫЛКА Документ." + ПараметрыЗапроса.ТипДокументов);
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#КонецЕсли