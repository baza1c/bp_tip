#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Предоставляет новый код доступа к шине для клиентского приложения.
//
// Параметры:
//  Приложение - Строка - имя приложения в шине.
// 
// Возвращаемое значение:
//  Строка - код доступа к шине мобильных приложений
//  Неопределено - не удалось предоставить код
//
Функция НовыйКодДоступа(Приложение) Экспорт
	
	Если Не РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ВключеноСканированиеЧеков() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Получим новый код
	КодДоступа = ШинаПрикладныхПриложений.КодДоступа(
		ИмяМобильногоПриложения(),
		НовыйНачальныеДанные());
	
	Если КодДоступа = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.КлиентскоеПриложениеСканированиеЧеков");
		ЭлементБлокировкиДанных.УстановитьЗначение("Приложение", Приложение);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.Приложение = Приложение;
		
		УстановитьПривилегированныйРежим(Истина);
		Запись.Прочитать();
		
		Запись.Приложение = Приложение;
		Запись.КодДоступа = КодДоступа.Код;
		
		Запись.Записать(Истина);
		УстановитьПривилегированныйРежим(Ложь);
	
		ЗафиксироватьТранзакцию();
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Клиентское приложение сканирования чеков'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат КодДоступа.Код;
	
КонецФункции

#Область ПодключениеПодсистемы

// Методы раздела - это обязательный API для подключения к шине

// Имя мобильного приложения в шине мобильных приложений
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяМобильногоПриложения() Экспорт
	
	// Клиентское приложение работает, "притворяясь" мобильным приложением.
	// Имя приложения у клиентского и мобильного приложений одинаковое.
	Возврат РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ИмяМобильногоПриложения();
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйНачальныеДанные()
	
	Данные = Новый Структура;
	
	Данные.Вставить("КодПриложения", Метаданные.Имя);
	Данные.Вставить("ПодключениеЧерезКлиентскоеПриложение", Истина);
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти

#КонецЕсли
