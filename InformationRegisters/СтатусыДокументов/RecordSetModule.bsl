#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НапоминанияПользователя.ИспользуютсяНапоминанияПользователя() Тогда
		Возврат;
	КонецЕсли;
	
	СтарыеЗаписи = ОбщегоНазначения.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение, СписокПолей());
	ДополнительныеСвойства.Вставить("СтарыеЗаписи", СтарыеЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НапоминанияПользователя.ИспользуютсяНапоминанияПользователя() Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("СтарыеЗаписи") Тогда
		ОтправитьОповещенияПриИзмененииСтатусов(Замещение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СписокПолей()
	МетаданныеРегистра = Метаданные();
	
	Поля = Новый Массив;
	Поля.Добавить(МетаданныеРегистра.Измерения.Организация.Имя);
	Поля.Добавить(МетаданныеРегистра.Измерения.Документ.Имя);
	
	Поля.Добавить(МетаданныеРегистра.Ресурсы.Статус.Имя);
	Поля.Добавить(МетаданныеРегистра.Ресурсы.ДополнительныйСтатус.Имя);
	Поля.Добавить(МетаданныеРегистра.Ресурсы.СтатусСчетаФактуры.Имя);
	Поля.Добавить(МетаданныеРегистра.Ресурсы.СтатусОригиналаСчетаФактуры.Имя);
	Поля.Добавить(МетаданныеРегистра.Ресурсы.НомерСчетаФактуры.Имя);
	Поля.Добавить(МетаданныеРегистра.Ресурсы.СтатусЧекаНПД.Имя);
	Поля.Добавить(МетаданныеРегистра.Ресурсы.УдалитьСтатусЧекаНПД.Имя);
	
	Возврат СтрСоединить(Поля, ",");
КонецФункции

Процедура ОтправитьОповещенияПриИзмененииСтатусов(Замещение)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СтарыеЗаписи", ДополнительныеСвойства.СтарыеЗаписи);
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		НовыеЗаписи = ЭтотОбъект.Выгрузить(Новый Массив, СписокПолей());
	Иначе
		НовыеЗаписи = ЭтотОбъект.Выгрузить(, СписокПолей());
	КонецЕсли;
	Запрос.УстановитьПараметр("НовыеЗаписи", НовыеЗаписи);
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НовыеЗаписи.Организация КАК Организация,
	|	НовыеЗаписи.Документ КАК Документ,
	|	НовыеЗаписи.Статус КАК Статус,
	|	НовыеЗаписи.ДополнительныйСтатус КАК ДополнительныйСтатус
	|ПОМЕСТИТЬ ВТНовыеЗаписи
	|ИЗ
	|	&НовыеЗаписи КАК НовыеЗаписи
	|ГДЕ
	|	НовыеЗаписи.Документ ССЫЛКА Документ.СчетНаОплатуПокупателю
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыеЗаписи.Организация КАК Организация,
	|	СтарыеЗаписи.Документ КАК Документ,
	|	СтарыеЗаписи.Статус КАК Статус,
	|	СтарыеЗаписи.ДополнительныйСтатус КАК ДополнительныйСтатус
	|ПОМЕСТИТЬ ВТСтарыеЗаписи
	|ИЗ
	|	&СтарыеЗаписи КАК СтарыеЗаписи
	|ГДЕ
	|	СтарыеЗаписи.Документ ССЫЛКА Документ.СчетНаОплатуПокупателю
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНовыеЗаписи.Организация КАК Организация,
	|	ВТНовыеЗаписи.Документ КАК Документ,
	|	ВТНовыеЗаписи.Статус КАК Статус,
	|	ВТНовыеЗаписи.ДополнительныйСтатус КАК ДополнительныйСтатус,
	|	ЕСТЬNULL(НастройкиОповещенийПоСчетамПокупателю.ОповещатьПриИзмененииСтатусаОплаты, ЛОЖЬ)
	|	И ВТНовыеЗаписи.Статус <> ЕСТЬNULL(ВТСтарыеЗаписи.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусОплатыСчета.НеОплачен)) КАК
	|		ОповещатьПриИзмененииСтатусаОплаты,
	|	ЕСТЬNULL(НастройкиОповещенийПоСчетамПокупателю.ОповещатьПриИзмененииСтатусаОтгрузки, ЛОЖЬ)
	|	И ВТНовыеЗаписи.ДополнительныйСтатус <> ЕСТЬNULL(ВТСтарыеЗаписи.ДополнительныйСтатус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтгрузки.НеОтгружен)) КАК ОповещатьПриИзмененииСтатусаОтгрузки
	|ИЗ
	|	ВТНовыеЗаписи КАК ВТНовыеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|		ПО ВТНовыеЗаписи.Документ = СчетНаОплатуПокупателю.Ссылка
	|		И СчетНаОплатуПокупателю.Ответственный <> &Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОповещенийПоСчетамПокупателю КАК НастройкиОповещенийПоСчетамПокупателю
	|		ПО ВТНовыеЗаписи.Организация = НастройкиОповещенийПоСчетамПокупателю.Организация
	|		И ВТНовыеЗаписи.Документ = НастройкиОповещенийПоСчетамПокупателю.СчетПокупателю
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтарыеЗаписи КАК ВТСтарыеЗаписи
	|		ПО ВТНовыеЗаписи.Организация = ВТСтарыеЗаписи.Организация
	|		И ВТНовыеЗаписи.Документ = ВТСтарыеЗаписи.Документ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ОповещатьПриИзмененииСтатусаОплаты Тогда
				ЭтоИзменениеСтатуса = Выборка.Статус = Перечисления.СтатусОплатыСчета.НеОплачен
					Или Выборка.Статус = Перечисления.СтатусОплатыСчета.Отменен;
				ОповещенияПользователейОСобытиях.ОтправитьОповещениеОбОплатеСчета(Выборка.Документ, ЭтоИзменениеСтатуса);
			КонецЕсли;
			
			Если Выборка.ОповещатьПриИзмененииСтатусаОтгрузки Тогда
				ЭтоИзменениеСтатуса = Выборка.ДополнительныйСтатус = Перечисления.СтатусыОтгрузки.НеОтгружен;
				ОповещенияПользователейОСобытиях.ОтправитьОповещениеОбОтгрузкеСчета(Выборка.Документ, ЭтоИзменениеСтатуса);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли