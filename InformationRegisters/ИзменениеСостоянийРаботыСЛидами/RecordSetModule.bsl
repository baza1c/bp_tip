#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтарыеЗаписи = ОбщегоНазначения.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение, СписокПолей());
	ДополнительныеСвойства.Вставить("СтарыеЗаписи", СтарыеЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтарыеЗаписи = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "СтарыеЗаписи");
	Если ЗначениеЗаполнено(СтарыеЗаписи)
		И (Количество() = 0 Или ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение)) Тогда
		
		СписокЛидов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтарыеЗаписи.ВыгрузитьКолонку("Лид"));
		СостоянияЛидов = РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.СостоянияРаботыСЛидами(СписокЛидов);
		Для Каждого ТекущийЛид Из СписокЛидов Цикл
			ПредыдущееСостояние = СостоянияЛидов.Найти(ТекущийЛид, "Лид");
			Если ЗначениеЗаполнено(ПредыдущееСостояние) Тогда
				РегистрыСведений.ПорядокРаботыСЛидами.УстановитьПорядокКанбанПоЛиду(
					ТекущийЛид,
					ПредыдущееСостояние.Состояние,
					ПредыдущееСостояние.Период);
			Иначе
				РегистрыСведений.ПорядокРаботыСЛидами.ОчиститьПорядокКанбанПоЛиду(ТекущийЛид);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		СписокЛидов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВыгрузитьКолонку("Лид"));
		ЛидыДляОбработки = КачественныеЛиды(СписокЛидов);
		
		Для Каждого ТекущаяЗапись Из ЭтотОбъект Цикл
			Если ЛидыДляОбработки.Найти(ТекущаяЗапись.Лид) = Неопределено
				Или Не ЗначениеЗаполнено(ТекущаяЗапись.Состояние) Тогда
				
				Продолжить;
			КонецЕсли;
			РегистрыСведений.ПорядокРаботыСЛидами.УстановитьПорядокКанбанПоЛиду(
				ТекущаяЗапись.Лид,
				ТекущаяЗапись.Состояние,
				ТекущаяЗапись.Период);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СписокПолей()
	МетаданныеРегистра = Метаданные();
	
	Поля = Новый Массив;
	Поля.Добавить(МетаданныеРегистра.Измерения.Лид.Имя);
	
	Поля.Добавить(МетаданныеРегистра.Ресурсы.Состояние.Имя);
	
	Возврат СтрСоединить(Поля, ",");
КонецФункции

Функция КачественныеЛиды(КонтактныеЛица)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КонтактныеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.Ссылка В(&КонтактныеЛица)
	|	И КонтактныеЛица.Лид
	|	И НЕ КонтактныеЛица.НекачественныйЛид
	|	И (КонтактныеЛица.ОбъектВладелец = НЕОПРЕДЕЛЕНО
	|			ИЛИ КонтактныеЛица.ОбъектВладелец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("КонтактныеЛица", КонтактныеЛица);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#КонецЕсли