#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текущее состояние работы с лидом.
//
// Параметры:
//   Лид - СправочникСсылка.КонтактныеЛица - лид.
//   Период - Дата - дата, на которую возвращается состояние.
//
// Возвращаемое значение:
//   СправочникСсылка.СостоянияРаботыСЛидами - состояние работы с лидом.
Функция ПолучитьСостояниеРаботыСЛидом(Лид, Период = Неопределено) Экспорт
	
	Состояние = НовыеПараметрыСостоянияПоЛиду();
	
	Если Не ЗначениеЗаполнено(Лид) Тогда
		Возврат Состояние;
	КонецЕсли;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзменениеСостоянийРаботыСЛидами.Период КАК Период,
	|	ИзменениеСостоянийРаботыСЛидами.Состояние КАК Состояние
	|ИЗ
	|	РегистрСведений.ИзменениеСостоянийРаботыСЛидами.СрезПоследних(&Период, Лид = &Лид) КАК ИзменениеСостоянийРаботыСЛидами";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Лид", Лид);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Состояние, Выборка);
	КонецЕсли;

	Возврат Состояние;
	
КонецФункции

// Устанавливает новое состояние работы с лидом.
//
// Параметры:
//   Лид - СправочникСсылка.КонтактныеЛица - лид.
//   НовоеСостояние - СправочникСсылка.СостоянияРаботыСЛидами - новое состояние.
//   Период - Дата - дата, на которую устанавливается новое состояние.
//
// Возвращаемое значение:
//   Булево - Истина, если состояние установлено.
Функция УстановитьСостояниеРаботыСЛидом(Лид, НовоеСостояние, Период = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Лид) Тогда
		МассивЛидов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Лид);
		ИзмененыЛиды = УстановитьСостояниеРаботыСЛидами(МассивЛидов, НовоеСостояние, Период);
		СостояниеИзменено = ИзмененыЛиды.Количество() > 0;
	Иначе
		СостояниеИзменено = Ложь;
	КонецЕсли;
	
	Возврат СостояниеИзменено;
	
КонецФункции

// Устанавливает новое состояние работы с несколькими лидами.
//
// Параметры:
//   МассивЛидов - Массив из СправочникСсылка.КонтактныеЛица - список лидов.
//   НовоеСостояние - СправочникСсылка.СостоянияРаботыСЛидами - новое состояние.
//   Период - Дата - дата, на которую устанавливается новое состояние.
//
// Возвращаемое значение:
//   Массив из СправочникСсылка.КонтактныеЛица - список с лидами, по которым установлено новое состояние.
Функция УстановитьСостояниеРаботыСЛидами(МассивЛидов, НовоеСостояние, Период = Неопределено) Экспорт
	
	МассивИзмененныхЛидов = Новый Массив;
	
	Если Не ЗначениеЗаполнено(МассивЛидов) Тогда
		Возврат МассивИзмененныхЛидов;
	КонецЕсли;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтактныеЛица.Ссылка КАК Лид,
	|	ИзменениеСостоянийРаботыСЛидами.Состояние КАК Состояние
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзменениеСостоянийРаботыСЛидами.СрезПоследних(&Период, Лид В (&МассивЛидов)) КАК
	|			ИзменениеСостоянийРаботыСЛидами
	|		ПО КонтактныеЛица.Ссылка = ИзменениеСостоянийРаботыСЛидами.Лид
	|ГДЕ
	|	КонтактныеЛица.Ссылка В (&МассивЛидов)";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("МассивЛидов", МассивЛидов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Состояние = НовоеСостояние Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Период);
		НаборЗаписей.Отбор.Лид.Установить(Выборка.Лид);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = Период;
		НоваяЗапись.Лид = Выборка.Лид;
		НоваяЗапись.Состояние = НовоеСостояние;
		
		НаборЗаписей.Записать();
		
		МассивИзмененныхЛидов.Добавить(Выборка.Лид);
		
	КонецЦикла;
	
	Возврат МассивИзмененныхЛидов;
	
КонецФункции

// Возвращает на предыдущее состояние работы с лидом при заданном условии.
//
// Параметры:
//   Лид - СправочникСсылка.КонтактныеЛица - лид.
//   УсловиеИзменения - ПеречислениеСсылка.УсловияИзмененияСостоянияРаботыСЛидами - условие изменения состояния.
//
// Возвращаемое значение:
//   Булево - Истина, если возвращено предыдущее состояние.
Функция ОчиститьСостояниеРаботыСЛидомПриУсловииИзменения(Лид, УсловиеИзменения) Экспорт
	
	СостояниеИзменено = Ложь;
	
	СостоянияИзменения = Справочники.СостоянияРаботыСЛидами.СостояниеПриУсловииИзменения(УсловиеИзменения);
	Если Не ЗначениеЗаполнено(СостоянияИзменения) Тогда
		Возврат СостояниеИзменено;
	КонецЕсли;
	
	ТекущееСостояние = ПолучитьСостояниеРаботыСЛидом(Лид);
	
	Если СостоянияИзменения = ТекущееСостояние.Состояние Тогда
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = ТекущееСостояние.Период;
		МенеджерЗаписи.Лид = Лид;
		МенеджерЗаписи.Удалить();
		
		СостояниеИзменено = Истина;
	КонецЕсли;
	
	Возврат СостояниеИзменено;
	
КонецФункции

// Устанавливает состояние работы с лидом при заданном условии.
//
// Параметры:
//   Лид - СправочникСсылка.КонтактныеЛица - лид.
//   УсловиеИзменения - ПеречислениеСсылка.УсловияИзмененияСостоянияРаботыСЛидами - условие изменения состояния.
//
// Возвращаемое значение:
//   Булево - Истина, если состояние установлено.
Функция УстановитьСостояниеРаботыСЛидомПриУсловииИзменения(Лид, УсловиеИзменения) Экспорт
	
	СостояниеИзменено = Ложь;
	
	НовоеСостояние = Справочники.СостоянияРаботыСЛидами.СостояниеПриУсловииИзменения(УсловиеИзменения);
	Если Не ЗначениеЗаполнено(НовоеСостояние) Тогда
		Возврат СостояниеИзменено;
	КонецЕсли;
	
	ТекущееСостояние = ПолучитьСостояниеРаботыСЛидом(Лид);
	
	// Проверям, что устанавливается состояние, порядок которого больше текущего, или текущее помечено на удаление
	РеквизитыТекущегоСостояния = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущееСостояние.Состояние,
		"ПометкаУдаления, РеквизитДопУпорядочивания");
	РеквизитыНовогоСостояния = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НовоеСостояние,
		"РеквизитДопУпорядочивания");
	
	Если РеквизитыТекущегоСостояния.ПометкаУдаления
		Или РеквизитыНовогоСостояния.РеквизитДопУпорядочивания > РеквизитыТекущегоСостояния.РеквизитДопУпорядочивания Тогда
		СостояниеИзменено = УстановитьСостояниеРаботыСЛидом(Лид, НовоеСостояние);
	КонецЕсли;
	
	Возврат СостояниеИзменено;
	
КонецФункции

// Возвращает текущее состояние работы с лидами.
//
// Параметры:
//   Лиды - Массив из СправочникСсылка.КонтактныеЛица - список лидов, по которым требуется получить состояние.
//   Период - Дата - дата, на которую возвращается состояние.
//
// Возвращаемое значение:
//    ТаблицаЗначений - см. НоваяТаблицаСостоянийПоЛидам()
//
Функция СостоянияРаботыСЛидами(Лиды, Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Лиды) Тогда
		Возврат НоваяТаблицаСостоянийПоЛидам();
	КонецЕсли;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИзменениеСостоянийРаботыСЛидами.Период КАК Период,
	|	ИзменениеСостоянийРаботыСЛидами.Лид КАК Лид,
	|	ИзменениеСостоянийРаботыСЛидами.Состояние КАК Состояние
	|ИЗ
	|	РегистрСведений.ИзменениеСостоянийРаботыСЛидами.СрезПоследних(&Период, Лид В (&Лиды)) КАК ИзменениеСостоянийРаботыСЛидами
	|
	|УПОРЯДОЧИТЬ ПО
	|	Лид");
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Лиды", Лиды);
	
	Результат = НоваяТаблицаСостоянийПоЛидам();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Результат.Добавить(), Выборка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеПараметрыСостоянияПоЛиду()
	
	ПараметрыСостояния = Новый Структура;
	ПараметрыСостояния.Вставить("Период", '00010101');
	ПараметрыСостояния.Вставить("Состояние", Справочники.СостоянияРаботыСЛидами.ПустаяСсылка());
	
	Возврат ПараметрыСостояния;
	
КонецФункции

Функция НоваяТаблицаСостоянийПоЛидам()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Результат.Колонки.Добавить("Лид", Новый ОписаниеТипов("СправочникСсылка.КонтактныеЛица"));
	Результат.Колонки.Добавить("Состояние", Новый ОписаниеТипов("СправочникСсылка.СостоянияРаботыСЛидами"));
	
	Результат.Индексы.Добавить("Лид");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли