#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Переопределяет ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
// 
// Параметры:
//   Ограничение - Структура
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает дату окончания периода последнего вызова метода получения списка аннулированных чеков
//
// Параметры:
//  Организация - СправочникСсылка.Организации - самозанятый
//
// Возвращаемое значение:
//  Дата - дата окончания периода
//
Функция ДатаОкончания(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыАннулированныхЧековМойНалог.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	РегистрСведений.ПараметрыАннулированныхЧековМойНалог КАК ПараметрыАннулированныхЧековМойНалог
	|ГДЕ
	|	ПараметрыАннулированныхЧековМойНалог.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДатаОкончания;
	Иначе
		Возврат ДатаОкончанияПоУмолчанию();
	КонецЕсли;
	
КонецФункции

// Обновляет дату окончания периода последнего вызова метода получения списка аннулированных чеков
//
// Параметры:
//  Организация - СправочникСсылка.Организации - самозанятый
//  ДатаОкончания - Дата - дата окончания периода
//
Процедура ОбновитьДатуОкончания(Организация, ДатаОкончания) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Организация = Организация;
	Запись.ДатаОкончания = ДатаОкончания;
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДатаОкончанияПоУмолчанию()
	
	Возврат '20190101';
	
КонецФункции

#КонецОбласти

#КонецЕсли