#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру настроек оповещения для счета покупателю.
// 
// Параметры:
//  СчетПокупателю - ДокументСсылка.СчетНаОплатуПокупателю - Счет покупателю, для которого необходимо получить настройки.
// 
// Возвращаемое значение:
//  Структура - см. НовыйНастройкиОповещения - Настройки оповещения по счету покупателю.
Функция УстановленныеНастройкиОповещенияПоСчетуПокупателю(СчетПокупателю) Экспорт
	
	НастройкиОповещения = НовыйНастройкиОповещения();
	
	Если Не ЗначениеЗаполнено(СчетПокупателю) Тогда
		Возврат НастройкиОповещения;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СчетПокупателю", СчетПокупателю);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиОповещенийПоСчетамПокупателю.ОповещатьПриИзмененииСтатусаОплаты КАК ОповещатьПриИзмененииСтатусаОплаты,
	|	НастройкиОповещенийПоСчетамПокупателю.ОповещатьПриИзмененииСтатусаОтгрузки КАК ОповещатьПриИзмененииСтатусаОтгрузки
	|ИЗ
	|	РегистрСведений.НастройкиОповещенийПоСчетамПокупателю КАК НастройкиОповещенийПоСчетамПокупателю
	|ГДЕ
	|	НастройкиОповещенийПоСчетамПокупателю.СчетПокупателю = &СчетПокупателю";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(НастройкиОповещения, Выборка);
	КонецЕсли;
	
	Возврат НастройкиОповещения;
	
КонецФункции

// Устанавливает настройки оповещения для счета покупателю.
// 
// Параметры:
//  СчетПокупателю - ДокументСсылка.СчетНаОплатуПокупателю - Счет покупателю, для которого устанавливаются настройки оповещения.
//  ОповещатьПриИзмененииСтатусаОплаты - Булево - Устанавливаемая настройка оповещения при изменении статуса опаты.
//  ОповещатьПриИзмененииСтатусаОтгрузки - Булево - Устанавливаемая настройки оповещения при изменении статуса отгрузки.
Процедура УстановитьНастройкиОповещенияПоСчетуПокупателя(СчетПокупателю, ОповещатьПриИзмененииСтатусаОплаты, ОповещатьПриИзмененииСтатусаОтгрузки) Экспорт
	
	Если Не ЗначениеЗаполнено(СчетПокупателю) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОповещатьПриИзмененииСтатусаОплаты И Не ОповещатьПриИзмененииСтатусаОтгрузки Тогда
		УдалитьНастройкиОповещения(СчетПокупателю);
	Иначе
		НастройкиОповещения = НовыйНастройкиОповещения();
		НастройкиОповещения.ОповещатьПриИзмененииСтатусаОплаты = ОповещатьПриИзмененииСтатусаОплаты;
		НастройкиОповещения.ОповещатьПриИзмененииСтатусаОтгрузки = ОповещатьПриИзмененииСтатусаОтгрузки;

		ЗаписатьНастройкиОповещения(СчетПокупателю, НастройкиОповещения);
	КонецЕсли;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьНастройкиОповещения(СчетПокупателю, Настройки)
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.СчетПокупателю.Установить(СчетПокупателю);
	
	Запись = Набор.Добавить();
	Запись.СчетПокупателю = СчетПокупателю;
	Запись.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетПокупателю, "Организация");
	ЗаполнитьЗначенияСвойств(Запись, Настройки);
	
	Набор.Записать();
	
КонецПроцедуры

Процедура УдалитьНастройкиОповещения(СчетПокупателю)
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.СчетПокупателю.Установить(СчетПокупателю);
	Набор.Записать();
	
КонецПроцедуры

// Возвращает структуру настроек оповещения по умолчанию.
// 
// Возвращаемое значение:
//  Структура - Настройки оповещения:
// * ОповещатьПриИзмененииСтатусаОплаты - Булево - Признак, что включено оповещение при изменении статуса оплаты.
// * ОповещатьПриИзмененииСтатусаОтгрузки - Булево - Признак, что включено оповещение при изменении статуса отгрузки.
Функция НовыйНастройкиОповещения()
	
	НастройкиОповещения = Новый Структура;
	НастройкиОповещения.Вставить("ОповещатьПриИзмененииСтатусаОплаты", Ложь);
	НастройкиОповещения.Вставить("ОповещатьПриИзмененииСтатусаОтгрузки", Ложь);
	
	Возврат НастройкиОповещения;
	
КонецФункции

#КонецОбласти

#КонецЕсли