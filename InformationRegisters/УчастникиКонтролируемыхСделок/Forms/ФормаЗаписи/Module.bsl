#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой()
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
		ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	Если Параметры.Ключ.Пустой() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьКлючСохраненияПоложенияОкна(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("УчастникКонтролируемойСделкиЗаписан", Новый Структура("Контрагент, 
	|КодВидаДеятельностиФизическогоЛица, ФизическоеЛицо, ЮрЛицо",
		Запись.Контрагент, СтранаРегистрации,
		Запись.КодВидаДеятельностиФизическогоЛица, Запись.ФизическоеЛицо, НЕ ФизическоеЛицо));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПриИзмененииКонтрагентаНаСервере();
	
	УстановитьКлючСохраненияПоложенияОкна(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипВыводимогоНомераПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	СписокКодовВидовДеятельностиФизЛиц = КонтролируемыеСделки.ПолучитьКодыВидовДеятельностиФизЛиц();
	Элементы.КодВидаДеятельностиФизическогоЛица.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовВидовДеятельностиФизЛиц Цикл
		НовыйКод = Элементы.КодВидаДеятельностиФизическогоЛица.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	ПриИзмененииКонтрагентаНаСервере();
	
	ДоступностьКлючевыхЗаписей = Истина;
	Если Параметры.Свойство("ДоступностьКлючевыхЗаписей") Тогда
		ДоступностьКлючевыхЗаписей = Параметры.ДоступностьКлючевыхЗаписей;
	КонецЕсли;
	
	Элементы.Контрагент.ТолькоПросмотр = НЕ ДоступностьКлючевыхЗаписей;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ФизическоеЛицо.Видимость = Форма.ФизическоеЛицо;
	Элементы.КодВидаДеятельностиФизическогоЛица.Видимость = Форма.ФизическоеЛицо;
	
	Элементы.ГруппаНаименование.Видимость = Не Форма.ФизическоеЛицо
		И Форма.ИностранныйКонтрагент;
		
	Элементы.ГруппаРегистрационныйНомер.Видимость = Не Форма.ФизическоеЛицо
		И Форма.ИностранныйКонтрагент;
	
	Если Элементы.ГруппаРегистрационныйНомер.Видимость Тогда
		ЗаполнитьЗначениеРегистрационногоНомера(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаполнитьЗначениеРегистрационногоНомера(Форма)
	
	ЗначениеРегистрационногоНомера = "";
	
	Если (Форма.Запись.ТипВыводимогоНомера = 0 Или Форма.Запись.ТипВыводимогоНомера = 1)
		И ЗначениеЗаполнено(Форма.НалоговыйНомер) Тогда
		
		ЗначениеРегистрационногоНомера = СтрШаблон(НСтр("ru = '%1 (налоговый номер из карточки контрагента)'"), Форма.НалоговыйНомер);
		
	ИначеЕсли (Форма.Запись.ТипВыводимогоНомера = 0 Или Форма.Запись.ТипВыводимогоНомера = 2)
		И ЗначениеЗаполнено(Форма.РегистрационныйНомер) Тогда
		
		ЗначениеРегистрационногоНомера = СтрШаблон(НСтр("ru = '%1 (регистр. номер из карточки контрагента)'"), Форма.РегистрационныйНомер);
		
	Иначе
		
		Если Форма.Запись.ТипВыводимогоНомера = 1 Тогда
			ЗначениеРегистрационногоНомера = НСтр("ru = 'Налоговый номер не указан в карточке контрагента'");
		ИначеЕсли Форма.Запись.ТипВыводимогоНомера = 2 Тогда
			ЗначениеРегистрационногоНомера = НСтр("ru = 'Регистрационный номер не указан в карточке контрагента'");
		Иначе
			ЗначениеРегистрационногоНомера = НСтр("ru = 'Налоговый и регистрационные номера не указаны в карточке контрагента'");
		КонецЕсли;
		
	КонецЕсли;
	
	Форма.ЗначениеРегистрационногоНомера = ЗначениеРегистрационногоНомера;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииКонтрагентаНаСервере()
	
	РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.Контрагент,
		"ЮридическоеФизическоеЛицо, СтранаРегистрации, НалоговыйНомер, РегистрационныйНомер");
	ФизическоеЛицо = РеквизитыКонтрагента.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	СтранаРегистрации = РеквизитыКонтрагента.СтранаРегистрации;
	ИностранныйКонтрагент = СтранаРегистрации <> Справочники.СтраныМира.Россия;
	
	РегистрационныйНомер = РеквизитыКонтрагента.РегистрационныйНомер;
	НалоговыйНомер = РеквизитыКонтрагента.НалоговыйНомер;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКлючСохраненияПоложенияОкна(Форма)
	
	Если Форма.ФизическоеЛицо Тогда
		ИмяКлюча = "ФизическоеЛицо";
	ИначеЕсли Форма.ИностранныйКонтрагент Тогда
		ИмяКлюча = "ЮридическоеЛицоИностранный";
	Иначе
		ИмяКлюча = "ЮридическоеЛицоРоссийский";
	КонецЕсли;
	
	Форма.КлючСохраненияПоложенияОкна = ИмяКлюча;
	
КонецПроцедуры

#КонецОбласти

