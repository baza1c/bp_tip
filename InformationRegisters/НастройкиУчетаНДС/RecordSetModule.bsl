#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения = Неопределено ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Для каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.НастройкиУчетаНДС.УстановкаНастроекПоУмолчанию(Запись, ?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Строка Из ЭтотОбъект Цикл
		
		Если (Строка.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ОбщаяУСН")
			ИЛИ Строка.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ПониженнаяУСН")) Тогда
			
			Если Строка.РаздельныйУчетНДСНаСчете19 Тогда
				Строка.РаздельныйУчетНДСБезВычетов = Истина;
				Строка.РаздельныйУчетНДСНаСчете19  = Ложь;
			КонецЕсли 
			
		Иначе
			
			Если Строка.РаздельныйУчетНДСБезВычетов Тогда
				Строка.РаздельныйУчетНДСНаСчете19  = Строка.СложныйУчетНДС;
				Строка.РаздельныйУчетНДСБезВычетов = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Строка.РаздельныйУчетНДСДо2014Года = Строка.СложныйУчетНДС
				И НЕ Строка.РаздельныйУчетНДСНаСчете19
				И НЕ Строка.РаздельныйУчетНДСБезВычетов;
			
	КонецЦикла;
	
	НастройкиУчета.ПередЗаписью(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПриЗаписи(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли