#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура устанавливает значения регистра по умолчанию
//
// Параметры:
//   Запись           - РегистрСведенийЗапись - запись регистра
//   ДанныеЗаполнения - Структура - где ключ - имя ресурса
//
Процедура УстановкаНастроекПоУмолчанию(Запись, ДанныеЗаполнения) Экспорт
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Период",
		НачалоГода(ТекущаяДатаСеанса()));
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Организация",
		БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"НачислятьНДСПоОтгрузке",
		Истина);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"НачислятьНДСПриПередачеНедвижимости",
		Истина);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ПорядокРегистрацииСчетовФактурНаАванс",
		Перечисления.ПорядокРегистрацииСчетовФактурНаАванс.НаВсеАвансы);
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"КонтролироватьДолюВычета",
		Истина);
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"БезопаснаяДоляВычета",
		УчетНДСКлиентСервер.БезопаснаяДоляВычетаПоУмолчанию());
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"СтавкаНалогообложенияПриУСН",
		?(УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Запись.Организация, Запись.Период), 
			Перечисления.ВидыСтавокНДС.ПониженнаяУСН, Перечисления.ВидыСтавокНДС.ПустаяСсылка()));
	
КонецПроцедуры

Функция ПолучитьИспользуетсяРаздельныйУчетНДС() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользуетсяРаздельныйУчетНДС = Константы.ИспользуетсяРаздельныйУчетНДС.Получить();
	
	Возврат ИспользуетсяРаздельныйУчетНДС;

КонецФункции

Функция СтавкаНалогообложенияПриУСН(Организация, Дата) Экспорт
	
	Если Не УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, Дата) Тогда
		Возврат Перечисления.ВидыСтавокНДС.ПустаяСсылка();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НастройкиУчетаНДССрезПоследних.СтавкаНалогообложенияПриУСН = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.ПониженнаяУСН)
	|		ИНАЧЕ НастройкиУчетаНДССрезПоследних.СтавкаНалогообложенияПриУСН
	|	КОНЕЦ КАК СтавкаНалогообложенияПриУСН
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Дата, Организация = &Организация) КАК НастройкиУчетаНДССрезПоследних" ;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата",        Дата);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() > 0 Тогда
		Возврат Результат[0].СтавкаНалогообложенияПриУСН;
	КонецЕсли;
	
	Возврат Перечисления.ВидыСтавокНДС.ПониженнаяУСН;
	
КонецФункции

Функция ПрименяютсяСтавкиНДСДляУСН(Организация, Дата) Экспорт
	
	СтавкаНалогообложенияПриУСН = СтавкаНалогообложенияПриУСН(Организация, Дата);
	
	Возврат СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН
		ИЛИ СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН;
	
КонецФункции

Функция ВедетсяУчетНДСНаУСН() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.ИспользуетсяУСНСНДС.Получить();
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Организация.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	| ЗначениеРазрешено(ЭтотСписок.Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Подключаемый_ПередЗаписью(Запись) Экспорт
	
	// Проверим, что на плане счетов достаточно аналитики для выбранных настроек учетной политики
	НастройкиУчета.ВключитьРасширеннуюАналитику(НеобходимыеРазрезыАналитики(Запись));
	
	Если Не УчетнаяПолитика.ПлательщикНДС(Запись.Организация, Запись.Период)
	   И Не ЗначениеЗаполнено(Запись.СтавкаНалогообложенияПриУСН) Тогда
		// Признак в УчетнаяПолитика.ПлательщикНДСПрименяющийУСН() может быть еще не записан.
		Запись.СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН;
	КонецЕсли;
	
КонецПроцедуры

Функция НеобходимыеРазрезыАналитики(Запись)
	
	РазрезыАналитики = Новый Массив;
	
	Если Запись.РаздельныйУчетНДСНаСчете19 Тогда
		РазрезыАналитики.Добавить("ВестиУчетНДСПоСпособам");
	КонецЕсли;
	
	Возврат РазрезыАналитики;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ВключитьКонтрольДолиВычета() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК КонтролироватьДолиВычетаВключен
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|ГДЕ
	|	НастройкиУчетаНДС.КонтролироватьДолюВычета";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	БезопаснаяДоляВычета = УчетНДСКлиентСервер.БезопаснаяДоляВычетаПоУмолчанию();
	
	Набор = РегистрыСведений.НастройкиУчетаНДС.СоздатьНаборЗаписей();
	Набор.Прочитать();
	Для каждого Запись Из Набор Цикл
		Запись.КонтролироватьДолюВычета = Истина;
		Запись.БезопаснаяДоляВычета = БезопаснаяДоляВычета;
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор, Истина);
	
КонецПроцедуры

Процедура ВключитьРаздельныйУчетНДСБезВычетов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК РаздельныйУчетНДСНаСчете19
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|ГДЕ
	|	НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
	|	И (НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.ОбщаяУСН)
	|			ИЛИ НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.ПониженнаяУСН))";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацииДляСбросаПоследовательности = Новый Массив();
	Набор = РегистрыСведений.НастройкиУчетаНДС.СоздатьНаборЗаписей();
	Набор.Прочитать();
	Для каждого Запись Из Набор Цикл
		Если Запись.РаздельныйУчетНДСНаСчете19
			И (Запись.СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН
			ИЛИ Запись.СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН) Тогда
			// Дополнительно проверим, может СтавкаНалогообложенияПриУСН установлена ошибочно
			ПлательщикНДСПрименяющийУСН = УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Запись.Организация, Запись.Период);
			
			Если ПлательщикНДСПрименяющийУСН Тогда
				Запись.РаздельныйУчетНДСНаСчете19  = Ложь;
				Запись.РаздельныйУчетНДСБезВычетов = Истина;
				ОрганизацииДляСбросаПоследовательности.Добавить(Запись.Организация);
			Иначе
				// Очистим реквизит "СтавкаНалогообложенияПриУСН" для тех, кто не является плательщикм НДС на УСН.
				Запись.СтавкаНалогообложенияПриУСН = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор, Истина);
	
	// Сбросим последовательность на 01.01.2025 года для организаций на УСН 5,7%, которые использовали раздельный учет НДС
	Период = Дата(2025, 1, 1);
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(ОрганизацииДляСбросаПоследовательности);
	Для Каждого Организация Из ОрганизацииДляСбросаПоследовательности Цикл
		МоментВремениПервогоДокумента = РаботаСПоследовательностями.МоментВремениПервогоДокументаВПоследовательности(Организация, Период);
		Если МоментВремениПервогоДокумента <> Неопределено Тогда
			РаботаСПоследовательностями.СброситьСостояниеПоследовательностиДокумента(
				МоментВремениПервогоДокумента.Ссылка, МоментВремениПервогоДокумента.Дата, Организация);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли