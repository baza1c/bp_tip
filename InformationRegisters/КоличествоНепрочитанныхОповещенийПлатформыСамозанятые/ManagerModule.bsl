
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Переопределяет ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
// 
// Параметры:
//   Ограничение - Структура
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция Обновить(Организация, Быстро = Истина) Экспорт
	
	Реквизиты = Прочитать(Организация);
	Если Быстро И ДанныеАктуальны(Реквизиты) Тогда
		// Обновление не требуется
		Возврат Истина;
	КонецЕсли;
	
	Если ОповещенияПлатформыСамозанятые.ПолучитьКоличествоНепрочитанных(Организация) Тогда
		ОповещенияПлатформыСамозанятые.ОбновитьКоличествоНепрочитанных(Организация);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает реквизиты записи для выбранной организации.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  - Структура:
//   * Количество - Число
//   * ДатаИзменения - Дата - Универсальная дата изменения
//
Функция Прочитать(Организация) Экспорт
	
	Реквизиты = НовыеРеквизиты();
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Реквизиты;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КоличествоНепрочитанныхОповещенийПлатформыСамозанятые.Количество КАК Количество,
		|	КоличествоНепрочитанныхОповещенийПлатформыСамозанятые.ДатаИзменения КАК ДатаИзменения
		|ИЗ
		|	РегистрСведений.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые КАК КоличествоНепрочитанныхОповещенийПлатформыСамозанятые
		|ГДЕ
		|	КоличествоНепрочитанныхОповещенийПлатформыСамозанятые.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

// Записывает новое количество непрочитанных оповещений.
// 
// Параметры:
//  ДанныеЗаполнения - Структура:
//   * Организация - СправочникСсылка.Организации
//   * Количество - Число
// 
// Возвращаемое значение:
//  Булево - Истина, если функция выполнена успешно
//
Функция Записать(ДанныеЗаполнения) Экспорт
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Организация");
	Если ЗначениеЗаполнено(Организация) Тогда
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Заполнить(ДанныеЗаполнения);
		МенеджерЗаписи.ДатаИзменения = ТекущаяУниверсальнаяДата();
		МенеджерЗаписи.Записать(Истина);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли
	
КонецФункции

Функция ДанныеАктуальны(Реквизиты) Экспорт
	
	ДатаАктуальности = ТекущаяУниверсальнаяДата() - ОповещенияПлатформыСамозанятыеКлиентСервер.ИнтервалПроверки();
	
	Возврат ДатаАктуальности <= Реквизиты.ДатаИзменения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеРеквизиты()
	
	Реквизиты = Новый Структура();
	Реквизиты.Вставить("Количество", 0);
	Реквизиты.Вставить("ДатаИзменения", Дата(1, 1, 1));
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти

#КонецЕсли
