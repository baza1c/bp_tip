#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НалогиПорядокКоторыхУстанавливаетсяНаМестах() Экспорт
	
	Налоги = КодыНалоговПорядокКоторыхУстанавливаетсяНаМестах();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Налоги", Налоги);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Ссылка
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Код В(&Налоги)";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Функция определяет, по каким из имущественных налогов, есть действующие настройки уплаты авансовых платежей
//
// Параметры:
//  Организация	 - СправочникСсылка.Организация	- организация, по которой выполняется поиск настроек
//  Период		 - Дата	- дата, на клоторую выбираются действующие настройки
// 
// Возвращаемое значение:
//   - Массив - массив строк с идентификаторами налогов в справочнике задач бухгалтера
//
Функция НалогиСНастройкамиАвансов(Организация, Период) Экспорт 

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	// С 2021 года настройки уплаты авансов по транспортному налогу хранятся в отдельном регистре сведений по регионам
	Запрос.УстановитьПараметр("ТранспортныйНалог", ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	Запрос.УстановитьПараметр("ИспользоватьНовыеНастройкиУплатыАвансовПоТранспортномуНалогу",
		Период >= РегистрыСведений.ПорядокУплатыНалоговПоРегионам.НачалоИспользованияНастроек(ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог()));
	// С 2022 года настройки уплаты авансов по налогу на имущество хранятся в отдельном регистре сведений по регионам
	Запрос.УстановитьПараметр("НалогНаИмущество", ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество());
	Запрос.УстановитьПараметр("ИспользоватьНовыеНастройкиУплатыАвансовПоНалогуНаИмущество",
		Период >= РегистрыСведений.ПорядокУплатыНалоговПоРегионам.НачалоИспользованияНастроек(ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество()));
		
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Налог.Код КАК ИдентификаторЗадачи
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(&Период, Организация = &Организация) КАК
	|		ПорядокУплатыНалоговНаМестахСрезПоследних
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы
	|	И (НЕ &ИспользоватьНовыеНастройкиУплатыАвансовПоТранспортномуНалогу
	|	ИЛИ ПорядокУплатыНалоговНаМестахСрезПоследних.Налог.Код <> &ТранспортныйНалог)
	|	И (НЕ &ИспользоватьНовыеНастройкиУплатыАвансовПоНалогуНаИмущество
	|	ИЛИ ПорядокУплатыНалоговНаМестахСрезПоследних.Налог.Код <> &НалогНаИмущество)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПорядокУплатыНалоговПоРегионамСрезПоследних.Налог.Код
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговПоРегионам.СрезПоследних(&Период, Организация = &Организация) КАК
	|		ПорядокУплатыНалоговПоРегионамСрезПоследних
	|ГДЕ
	|	ПорядокУплатыНалоговПоРегионамСрезПоследних.УплачиваютсяАвансы
	|	И &ИспользоватьНовыеНастройкиУплатыАвансовПоТранспортномуНалогу
	|	И ПорядокУплатыНалоговПоРегионамСрезПоследних.Налог.Код = &ТранспортныйНалог
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПорядокУплатыНалоговПоРегионамСрезПоследних.Налог.Код
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговПоРегионам.СрезПоследних(&Период, Организация = &Организация) КАК
	|		ПорядокУплатыНалоговПоРегионамСрезПоследних
	|ГДЕ
	|	ПорядокУплатыНалоговПоРегионамСрезПоследних.УплачиваютсяАвансы
	|	И &ИспользоватьНовыеНастройкиУплатыАвансовПоНалогуНаИмущество
	|	И ПорядокУплатыНалоговПоРегионамСрезПоследних.Налог.Код = &НалогНаИмущество";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторЗадачи");
				
КонецФункции

Процедура СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(МенеджерВременныхТаблиц, СписокОрганизаций, Период, Налог) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтера.Ссылка КАК Задача
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Код = &Налог";
	
	Запрос.УстановитьПараметр("Налог", Налог);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	НалогВЗадачах = Неопределено;
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		НалогВЗадачах = Выборка.Задача;
	КонецЕсли;	
	
	// Настройки уплаты авансов хранятся в отдельном регистре сведений по регионам:
	//  По транспортному налогу - с 2021 года;
	//  По налогу на имущество - с 2022 года.
	ИспользоватьНовыеНастройкиУплатыАвансов =
		РегистрыСведений.ПорядокУплатыНалоговПоРегионам.ПорядокУплатыУстанавливаетсяПоРегионам(Налог)
		И Период >= РегистрыСведений.ПорядокУплатыНалоговПоРегионам.НачалоИспользованияНастроек(Налог);

	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(Период));
	Запрос.УстановитьПараметр("НалогВЗадачах", НалогВЗадачах);	
	Запрос.УстановитьПараметр("ИспользоватьНовыеНастройкиУплатыАвансов", ИспользоватьНовыеНастройкиУплатыАвансов);
	Запрос.УстановитьПараметр("ЭтоНалогНаИмущество",  Налог = ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество());
	Запрос.УстановитьПараметр("ЭтоТранспортныйНалог", Налог = ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	
	Если ИспользоватьНовыеНастройкиУплатыАвансов Тогда
	
		// Выбираем все регистрации в налоговых органах по организации и ее подразделениям.
		// Ниже отберем только те налоговые органы, которые находятся в регионах с установленной уплатой авансов по налогу.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка КАК Подразделение
		|ПОМЕСТИТЬ ПодразделенияОрганизаций
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Владелец В (&СписокОрганизаций)
		|	И ПодразделенияОрганизаций.ОбособленноеПодразделение
		|	И НЕ ПодразделенияОрганизаций.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница КАК Организация,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане КАК НалоговыйОрган,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане.КодРегиона КАК КодРегиона
		|ПОМЕСТИТЬ ВсеНалоговыеОрганы
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(&НачалоГода, СтруктурнаяЕдиница В
		|	(&СписокОрганизаций)) КАК ИсторияРегистрацийВНалоговомОрганеСрезПоследних
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница КАК Организация,
		|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане,
		|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане.КодРегиона
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
		|ГДЕ
		|	ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница В (&СписокОрганизаций)
		|	И ИсторияРегистрацийВНалоговомОргане.Период > &НачалоГода
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница.Владелец КАК Организация,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане.КодРегиона
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(&НачалоГода, СтруктурнаяЕдиница В
		|		(ВЫБРАТЬ
		|			Подразделение
		|		ИЗ
		|			ПодразделенияОрганизаций КАК ПодразделенияОрганизаций)) КАК ИсторияРегистрацийВНалоговомОрганеСрезПоследних
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница.Владелец КАК Организация,
		|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане,
		|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане.КодРегиона
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
		|ГДЕ
		|	СтруктурнаяЕдиница В
		|		(ВЫБРАТЬ
		|			Подразделение
		|		ИЗ
		|			ПодразделенияОрганизаций КАК ПодразделенияОрганизаций)
		|	И ИсторияРегистрацийВНалоговомОргане.Период > &НачалоГода
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган.КодРегиона
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
		|			&НачалоГода,
		|			&ЭтоНалогНаИмущество
		|				И Организация В (&СписокОрганизаций)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган.КодРегиона
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		|ГДЕ
		|	&ЭтоНалогНаИмущество
		|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация В(&СписокОрганизаций)
		|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период > &НачалоГода
		|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.Организация,
		|	РегистрацияТранспортныхСредствСрезПоследних.НалоговыйОрган,
		|	РегистрацияТранспортныхСредствСрезПоследних.НалоговыйОрган.КодРегиона
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
		|			&НачалоГода,
		|			&ЭтоТранспортныйНалог
		|				И Организация В (&СписокОрганизаций)) КАК РегистрацияТранспортныхСредствСрезПоследних
		|ГДЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|	И РегистрацияТранспортныхСредствСрезПоследних.ВключатьВНалоговуюБазу
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрацияТранспортныхСредств.Организация,
		|	РегистрацияТранспортныхСредств.НалоговыйОрган,
		|	РегистрацияТранспортныхСредств.НалоговыйОрган.КодРегиона
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	&ЭтоТранспортныйНалог
		|	И РегистрацияТранспортныхСредств.Организация В(&СписокОрганизаций)
		|	И РегистрацияТранспортныхСредств.Период > &НачалоГода
		|	И РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПодразделенияОрганизаций";
	
	Иначе
	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка) КАК НалоговыйОрган,
		|	"""" КАК КодРегиона
		|ПОМЕСТИТЬ ВсеНалоговыеОрганы
		|ГДЕ
		|	ЛОЖЬ";
	
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК НалоговыйОрган,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(&Период, Организация В (&СписокОрганизаций)
	|	И Налог = &НалогВЗадачах) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|ГДЕ
	|	НЕ &ИспользоватьНовыеНастройкиУплатыАвансов
	|	И ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВсеНалоговыеОрганы.НалоговыйОрган,
	|	ВсеНалоговыеОрганы.Организация
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговПоРегионам.СрезПоследних(&Период, Организация В (&СписокОрганизаций)
	|	И Налог = &НалогВЗадачах) КАК ПорядокУплатыНалоговПоРегионамСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеНалоговыеОрганы КАК ВсеНалоговыеОрганы
	|		ПО ПорядокУплатыНалоговПоРегионамСрезПоследних.Организация = ВсеНалоговыеОрганы.Организация
	|		И ПорядокУплатыНалоговПоРегионамСрезПоследних.КодРегиона = ВсеНалоговыеОрганы.КодРегиона
	|ГДЕ
	|	&ИспользоватьНовыеНастройкиУплатыАвансов
	|	И ПорядокУплатыНалоговПоРегионамСрезПоследних.УплачиваютсяАвансы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговыйОрган,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеНалоговыеОрганы";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьМестныеНалогиВРазрезеНалоговыхОрганов(МенеджерВременныхТаблиц, Организация, ТекущийПериод = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	НачалоПериода = НачалоГода(ДобавитьМесяц(ТекущаяДатаСеанса(), -3)) - 1;
	КонецПериода = КонецГода(ТекущаяДатаСеанса());
	
	УсловияИсключения = УсловияПримененияТребованийЗаконодательства.УсловияИсключенияПоТипуОрганизации(Организация, ТекущийПериод);
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",      КонецПериода);
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("НалогНаИмущество",  ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество());
	Запрос.УстановитьПараметр("ЗемельныйНалог",    ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог());
	Запрос.УстановитьПараметр("ТранспортныйНалог", ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	Запрос.УстановитьПараметр("УсловияИсключения", УсловияИсключения);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Код КАК Налог,
	|	ЗадачиБухгалтера.Ссылка КАК Задача
	|ПОМЕСТИТЬ НалогиНаМестах
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Код В (&НалогНаИмущество, &ЗемельныйНалог, &ТранспортныйНалог)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Правило,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец КАК Задача,
	|	НалогиНаМестах.Налог КАК Налог
	|ПОМЕСТИТЬ ВсеПравилаНалога
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ НалогиНаМестах КАК НалогиНаМестах
	|		ПО ПравилаПредставленияОтчетовУплатыНалогов.Владелец = НалогиНаМестах.Задача
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка.Владелец В
	|			(ВЫБРАТЬ
	|				НалогиНаМестах.Задача
	|			ИЗ
	|				НалогиНаМестах)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец,
	|	НалогиНаМестах.Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка КАК Правило,
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка.Владелец КАК Задача,
	|	НалогиНаМестах.Налог КАК Налог
	|ПОМЕСТИТЬ ПравилаИсключения
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов.Условия КАК ПравилаПредставленияОтчетовУплатыНалоговУсловия
	|		ЛЕВОЕ СОЕДИНЕНИЕ НалогиНаМестах КАК НалогиНаМестах
	|		ПО ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка.Владелец = НалогиНаМестах.Задача
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Условие В(&УсловияИсключения)
	|	И ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка.Владелец В
	|			(ВЫБРАТЬ
	|				НалогиНаМестах.Задача
	|			ИЗ
	|				НалогиНаМестах)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка,
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка.Владелец,
	|	НалогиНаМестах.Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеПравилаНалога.Задача КАК Задача,
	|	ВсеПравилаНалога.Налог КАК Налог
	|ПОМЕСТИТЬ ДоступныеНалогиНаМестах
	|ИЗ
	|	ВсеПравилаНалога КАК ВсеПравилаНалога
	|ГДЕ
	|	НЕ ВсеПравилаНалога.Задача В
	|				(ВЫБРАТЬ
	|					ПравилаИсключения.Задача КАК ПравилаИсключения
	|				ИЗ
	|					ПравилаИсключения)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеПравилаНалога.Задача,
	|	ВсеПравилаНалога.Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПериода КАК Период,
	|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане КАК НалоговыйОрган
	|ПОМЕСТИТЬ РегистрацииОрганизации
	|ИЗ
	|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(&НачалоПериода, СтруктурнаяЕдиница = &Организация) КАК ИсторияРегистрацийВНалоговомОрганеСрезПоследних
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ИсторияРегистрацийВНалоговомОргане.Период,
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
	|ГДЕ
	|	ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница = &Организация
	|	И ИсторияРегистрацийВНалоговомОргане.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацииОрганизации.Период КАК НачалоПериода,
	|	ЕСТЬNULL(МИНИМУМ(СледующиеРегистрации.Период), ДОБАВИТЬКДАТЕ(&КонецПериода, ДЕНЬ, 1)) КАК КонецПериода,
	|	РегистрацииОрганизации.НалоговыйОрган КАК НалоговыйОрган
	|ИЗ
	|	РегистрацииОрганизации КАК РегистрацииОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацииОрганизации КАК СледующиеРегистрации
	|		ПО РегистрацииОрганизации.Период < СледующиеРегистрации.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацииОрганизации.Период,
	|	РегистрацииОрганизации.НалоговыйОрган";
	
	// Регистрация организации могла меняться в течение рассматриваемого периода. И если объект налогообложения
	// зарегистрирован по месту нахождения организации, то для определения этого "местонахождения организации" нужно
	// учитывать историю регистрации организации в налоговых органах. Фактически здесь нужно учесть как то, что сама
	// регистрация объекта по месту нахождения организации может быть временной, так и то, что внутри указанного
	// периода действия отдельной записи по объекту организация могла поменять регистрацию в налоговом органе.
	//
	// Для этого в каждом отдельном периоде действия конкретной регистрации организации определяем, были ли зарегистрированы
	// объекты налогообложения по месту нахождения организации. Если такие объекты были, то такой налоговый орган
	// (регистрацию в налогом органе) включим в итоговую выборку.
	
	// Для налога на имущества считаем, что всегда есть объекты по месту нахождения организации, т.к. вся недвижимость,
	// принятая к учету в ОС, облагается по среднегодовой стоимости по месту нахождения организации (если только специально не
	// указать для каждого объекта, что он находится вне местонахождения организации - но это мы игнорируем для упрощения). 
	
	НалоговыеОрганыОрганизации = Новый ТаблицаЗначений;
	НалоговыеОрганыОрганизации.Колонки.Добавить("Налог", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	НалоговыеОрганыОрганизации.Колонки.Добавить("НалоговыйОрган", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	
	ПериодыРегистрацийОрганизации = Запрос.Выполнить().Выбрать();
	
	ЗапросЗаПериод = Новый Запрос;
	ЗапросЗаПериод.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросЗаПериод.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	&НалогНаИмущество КАК Налог
	|ГДЕ
	|	&НалогНаИмущество В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	&ЗемельныйНалог
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК РегистрацияЗемельныхУчастковСрезПоследних
	|ГДЕ
	|	РегистрацияЗемельныхУчастковСрезПоследних.ВключатьВНалоговуюБазу
	|	И &ЗемельныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане <> ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	&ЗемельныйНалог
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|ГДЕ
	|	РегистрацияЗемельныхУчастков.Организация = &Организация
	|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|	И РегистрацияЗемельныхУчастков.Период >= &НачалоПериода
	|	И РегистрацияЗемельныхУчастков.Период < &КонецПериода
	|	И &ЗемельныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане <> ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	&ТранспортныйНалог
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК РегистрацияТранспортныхСредствСрезПоследних
	|ГДЕ
	|	РегистрацияТранспортныхСредствСрезПоследних.ВключатьВНалоговуюБазу
	|	И &ТранспортныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияТранспортныхСредствСрезПоследних.ПостановкаНаУчетВНалоговомОргане <> ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	&ТранспортныйНалог
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
	|	И РегистрацияТранспортныхСредств.Период >= &НачалоПериода
	|	И РегистрацияТранспортныхСредств.Период < &КонецПериода
	|	И &ТранспортныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане <> ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)";
	
	ЗапросЗаПериод.УстановитьПараметр("Организация", Организация);
	ЗапросЗаПериод.УстановитьПараметр("НалогНаИмущество",  ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество());
	ЗапросЗаПериод.УстановитьПараметр("ЗемельныйНалог",    ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог());
	ЗапросЗаПериод.УстановитьПараметр("ТранспортныйНалог", ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	
	Пока ПериодыРегистрацийОрганизации.Следующий() Цикл
	
		ЗапросЗаПериод.УстановитьПараметр("НачалоПериода", ПериодыРегистрацийОрганизации.НачалоПериода);
		ЗапросЗаПериод.УстановитьПараметр("КонецПериода",  ПериодыРегистрацийОрганизации.КонецПериода);
		
		ВыборкаЗаПериод = ЗапросЗаПериод.Выполнить().Выбрать();
		
		Пока ВыборкаЗаПериод.Следующий() Цикл
			НоваяСтрока = НалоговыеОрганыОрганизации.Добавить();
			НоваяСтрока.Налог = ВыборкаЗаПериод.Налог;
			НоваяСтрока.НалоговыйОрган = ПериодыРегистрацийОрганизации.НалоговыйОрган;
		КонецЦикла;
		
	КонецЦикла;
	
	// Далее определим налоговые органы вне местонахождения организации, где зарегистрированы объекты. И далее соберем все вместе.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НалогНаИмущество КАК Налог,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ РегистрацииВДругихНалоговыхОрганах
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
	|ГДЕ
	|	&НалогНаИмущество В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НалогНаИмущество,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация = &Организация
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &НалогНаИмущество В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ЗемельныйНалог,
	|	РегистрацияЗемельныхУчастковСрезПоследних.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК РегистрацияЗемельныхУчастковСрезПоследних
	|ГДЕ
	|	РегистрацияЗемельныхУчастковСрезПоследних.ВключатьВНалоговуюБазу
	|	И &ЗемельныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ЗемельныйНалог,
	|	РегистрацияЗемельныхУчастков.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|ГДЕ
	|	РегистрацияЗемельныхУчастков.Организация = &Организация
	|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &ЗемельныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ТранспортныйНалог,
	|	РегистрацияТранспортныхСредствСрезПоследних.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК РегистрацияТранспортныхСредствСрезПоследних
	|ГДЕ
	|	РегистрацияТранспортныхСредствСрезПоследних.ВключатьВНалоговуюБазу
	|	И &ТранспортныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияТранспортныхСредствСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ТранспортныйНалог,
	|	РегистрацияТранспортныхСредств.НалоговыйОрган
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
	|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &ТранспортныйНалог В
	|			(ВЫБРАТЬ
	|				ДоступныеНалогиНаМестах.Налог
	|			ИЗ
	|				ДоступныеНалогиНаМестах)
	|	И РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыеОрганыОрганизации.Налог КАК Налог,
	|	НалоговыеОрганыОрганизации.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ НалоговыеОрганыОрганизации
	|ИЗ
	|	&НалоговыеОрганыОрганизации КАК НалоговыеОрганыОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НалогиНаМестах.Задача КАК Налог,
	|	РегистрацииВДругихНалоговыхОрганах.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ МестныеНалогиВРазрезеНалоговыхОрганов
	|ИЗ
	|	НалогиНаМестах КАК НалогиНаМестах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацииВДругихНалоговыхОрганах КАК РегистрацииВДругихНалоговыхОрганах
	|		ПО НалогиНаМестах.Налог = РегистрацииВДругихНалоговыхОрганах.Налог
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НалогиНаМестах.Задача,
	|	НалоговыеОрганыОрганизации.НалоговыйОрган
	|ИЗ
	|	НалогиНаМестах КАК НалогиНаМестах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НалоговыеОрганыОрганизации КАК НалоговыеОрганыОрганизации
	|		ПО НалогиНаМестах.Налог = НалоговыеОрганыОрганизации.Налог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог,
	|	НалоговыйОрган";
	
	Запрос.УстановитьПараметр("НалоговыеОрганыОрганизации", НалоговыеОрганыОрганизации);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьПорядокУплатыНалоговНаМестах(МенеджерВременныхТаблиц, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачаНалогНаИмущество.Ссылка КАК ЗадачаНалогНаИмущество,
	|	ЗадачаТранспортныйНалог.Ссылка КАК ЗадачаТранспортныйНалог,
	|	ЗадачаЗемельныйНалог.Ссылка КАК ЗадачаЗемельныйНалог
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачаНалогНаИмущество,
	|	Справочник.ЗадачиБухгалтера КАК ЗадачаТранспортныйНалог,
	|	Справочник.ЗадачиБухгалтера КАК ЗадачаЗемельныйНалог
	|ГДЕ
	|	ЗадачаНалогНаИмущество.Код = &НалогНаИмущество
	|	И ЗадачаТранспортныйНалог.Код = &ТранспортныйНалог
	|	И ЗадачаЗемельныйНалог.Код = &ЗемельныйНалог";
	
	Запрос.УстановитьПараметр("НалогНаИмущество",  ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество());
	Запрос.УстановитьПараметр("ЗемельныйНалог",    ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог());
	Запрос.УстановитьПараметр("ТранспортныйНалог", ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Запрос.УстановитьПараметр("Организация",             Организация);
	Запрос.УстановитьПараметр("Период2020Год",           '20200101');
	Запрос.УстановитьПараметр("Период2021Год",           '20210101');
	Запрос.УстановитьПараметр("Период2022Год",           '20220101');
	Запрос.УстановитьПараметр("ЗадачаНалогНаИмущество",  Выборка.ЗадачаНалогНаИмущество);
	Запрос.УстановитьПараметр("ЗадачаЗемельныйНалог",    Выборка.ЗадачаЗемельныйНалог);
	Запрос.УстановитьПараметр("ЗадачаТранспортныйНалог", Выборка.ЗадачаТранспортныйНалог);
	
	ТекстОбъединитьВсе =
	"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
		
	// ДО 2020 ГОДА
	// Данные о порядке уплаты всех местных (региональных) налогов берем из РС ПорядокУплатыНалоговНаМестах (далее - прежний регистр).
	// В этот интервал устанавливается общий срок уплаты авансов для всех кварталов.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Период КАК Период,
	|	ПорядокУплатыНалоговНаМестах.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаМесяцев КАК СрокУплатыНалогаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаДней КАК СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев КАК СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней КАК СрокУплатыАвансаДней,
	|	ЛОЖЬ КАК СрокУплатыУстанавливаетсяДляКаждогоАванса,
	|	NULL КАК СрокУплатыАванса1КвМесяцев,
	|	NULL КАК СрокУплатыАванса1КвДней,
	|	NULL КАК СрокУплатыАванса2КвМесяцев,
	|	NULL КАК СрокУплатыАванса2КвДней,
	|	NULL КАК СрокУплатыАванса3КвМесяцев,
	|	NULL КАК СрокУплатыАванса3КвДней
	|ПОМЕСТИТЬ ПорядокУплатыНалоговНаМестах
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестах.Организация = &Организация
	|	И ПорядокУплатыНалоговНаМестах.Период < &Период2020Год";
	
	// 2020 ГОД
	// Учтем особенности этого года:
	// 1) По налогу на имущество сроки уплаты авансовых платежей могут быть продлены на местах,
	//    причем продление может устанавливаться отдельно за каждый квартал (см. 102-ФЗ от 01.04.2020).
	//    Берем данные из прежнего регистра.
	Запрос.Текст = Запрос.Текст + ТекстОбъединитьВсе +
	"ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Период КАК Период,
	|	ПорядокУплатыНалоговНаМестах.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаМесяцев КАК СрокУплатыНалогаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаДней КАК СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев КАК СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней КАК СрокУплатыАвансаДней,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыУстанавливаетсяДляКаждогоАванса КАК СрокУплатыУстанавливаетсяДляКаждогоАванса,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса1КвМесяцев КАК СрокУплатыАванса1КвМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса1КвДней КАК СрокУплатыАванса1КвДней,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса2КвМесяцев КАК СрокУплатыАванса2КвМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса2КвДней КАК СрокУплатыАванса2КвДней,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса3КвМесяцев КАК СрокУплатыАванса3КвМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса3КвДней КАК СрокУплатыАванса3КвДней
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестах.Организация = &Организация
	|	И ПорядокУплатыНалоговНаМестах.Период = &Период2020Год
	|	И ПорядокУплатыНалоговНаМестах.Налог = &ЗадачаНалогНаИмущество";
	
	// 2) По транспортному и земельному налогу сроки уплаты авансовых платежей также могут быть продлены на местах,
	//    причем продление может устанавливаться отдельно за каждый квартал (см. 102-ФЗ от 01.04.2020).
	//    Но сроки уплаты налога начиная с периода 2020 года общие для всех регионов (см. 325-ФЗ от 29.09.2019) - используем срок по общему правилу.
	//    Данные по порядку уплаты авансов берем из прежнего регистра. Но здесь учтем, что актуальная настройка могла быть установлена ранее -
	//    тогда в 2020 году нужно использовать ее, но с корректировкой сроков уплаты налога. Для этого используем срез последних.
	Запрос.Текст = Запрос.Текст + ТекстОбъединитьВсе +
	"ВЫБРАТЬ
	|	&Период2020Год КАК Период,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	NULL КАК СрокУплатыНалогаМесяцев,
	|	NULL КАК СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАвансаМесяцев КАК СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАвансаДней КАК СрокУплатыАвансаДней,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыУстанавливаетсяДляКаждогоАванса КАК СрокУплатыУстанавливаетсяДляКаждогоАванса,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАванса1КвМесяцев КАК СрокУплатыАванса1КвМесяцев,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАванса1КвДней КАК СрокУплатыАванса1КвДней,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАванса2КвМесяцев КАК СрокУплатыАванса2КвМесяцев,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАванса2КвДней КАК СрокУплатыАванса2КвДней,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАванса3КвМесяцев КАК СрокУплатыАванса3КвМесяцев,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАванса3КвДней КАК СрокУплатыАванса3КвДней
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период2020Год,
	|			Организация = &Организация
	|				И Налог В (&ЗадачаТранспортныйНалог, &ЗадачаЗемельныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних";
	
	// 2021 ГОД - НАЛОГ НА ИМУЩЕСТВО
	// В 2021 году по налогу на имущество порядок уплаты налога устанавливается на местах, но срок уплаты авансов 
	// единый для всех кварталов.
	// Сроки уплаты налога начиная с периода 2021 года (т.е. в 2022 году и далее) общие для всех регионов 
	// (см. 305-ФЗ от 02.07.2021) - используем срок по общему правилу.
	// Берем данные из прежнего регистра. Но здесь учтем, что актуальная настройка могла быть установлена ранее -
	// тогда в 2021 году нужно использовать ее, но с корректировкой сроков уплаты налога. Для этого используем срез последних.
	Запрос.Текст = Запрос.Текст + ТекстОбъединитьВсе +
	"ВЫБРАТЬ
	|	&Период2021Год КАК Период,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	NULL КАК СрокУплатыНалогаМесяцев,
	|	NULL КАК СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАвансаМесяцев КАК СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыАвансаДней КАК СрокУплатыАвансаДней,
	|	ЛОЖЬ КАК СрокУплатыУстанавливаетсяДляКаждогоАванса,
	|	NULL КАК СрокУплатыАванса1КвМесяцев,
	|	NULL КАК СрокУплатыАванса1КвДней,
	|	NULL КАК СрокУплатыАванса2КвМесяцев,
	|	NULL КАК СрокУплатыАванса2КвДней,
	|	NULL КАК СрокУплатыАванса3КвМесяцев,
	|	NULL КАК СрокУплатыАванса3КвДней
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(&Период2021Год, Организация = &Организация
	|	И Налог = &ЗадачаНалогНаИмущество) КАК ПорядокУплатыНалоговНаМестахСрезПоследних";
	
	// 2021 ГОД И ДАЛЕЕ - ЗЕМЕЛЬНЫЙ И ТРАНСПОРТНЫЙ НАЛОГИ
	// 1) По земельному налогу сроки уплаты налога и авансовых платежей общие для всех регионов (см. 325-ФЗ от 29.09.2019) - используем срок по общему правилу.
	//    Данные по порядку уплаты авансов берем из прежнего регистра. Но здесь учтем, что актуальная настройка (уплачиваются авансы или нет)
	//    могла быть установлена ранее - тогда в 2021 году нужно использовать ее, но с корректировкой сроков уплаты. Для этого используем срез последних.
	//    После 2021 года используем таблицу регистра, но также учитываем, что применяюися общие сроки.
	Запрос.Текст = Запрос.Текст + ТекстОбъединитьВсе +
	"ВЫБРАТЬ
	|	&Период2021Год КАК Период,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	NULL КАК СрокУплатыНалогаМесяцев,
	|	NULL КАК СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	NULL КАК СрокУплатыАвансаМесяцев,
	|	NULL КАК СрокУплатыАвансаДней,
	|	ЛОЖЬ КАК СрокУплатыУстанавливаетсяДляКаждогоАванса,
	|	NULL КАК СрокУплатыАванса1КвМесяцев,
	|	NULL КАК СрокУплатыАванса1КвДней,
	|	NULL КАК СрокУплатыАванса2КвМесяцев,
	|	NULL КАК СрокУплатыАванса2КвДней,
	|	NULL КАК СрокУплатыАванса3КвМесяцев,
	|	NULL КАК СрокУплатыАванса3КвДней
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период2021Год,
	|			Организация = &Организация
	|				И Налог = &ЗадачаЗемельныйНалог) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Период,
	|	ПорядокУплатыНалоговНаМестах.Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане,
	|	NULL,
	|	NULL,
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестах.Организация = &Организация
	|	И ПорядокУплатыНалоговНаМестах.Налог = &ЗадачаЗемельныйНалог
	|	И ПорядокУплатыНалоговНаМестах.Период > &Период2021Год";
	
	// 2) По транспортному налогу сроки уплаты налога и авансовых платежей общие для всех регионов (см. 325-ФЗ от 29.09.2019) - используем срок по общему правилу.
	//    Но данные по уплате авансов в регионах берем из нового РС ПорядокУплатыНалоговПоРегионам.
	Запрос.Текст = Запрос.Текст + ТекстОбъединитьВсе +
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ПорядокУплатыНалоговПоРегионам.Период, &Период2021Год) КАК Период,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог КАК Налог,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган КАК РегистрацияВНалоговомОргане,
	|	NULL КАК СрокУплатыНалогаМесяцев,
	|	NULL КАК СрокУплатыНалогаДней,
	|	ЕСТЬNULL(ПорядокУплатыНалоговПоРегионам.УплачиваютсяАвансы, ЛОЖЬ) КАК УплачиваютсяАвансы,
	|	NULL КАК СрокУплатыАвансаМесяцев,
	|	NULL КАК СрокУплатыАвансаДней,
	|	ЛОЖЬ КАК СрокУплатыУстанавливаетсяДляКаждогоАванса,
	|	NULL КАК СрокУплатыАванса1КвМесяцев,
	|	NULL КАК СрокУплатыАванса1КвДней,
	|	NULL КАК СрокУплатыАванса2КвМесяцев,
	|	NULL КАК СрокУплатыАванса2КвДней,
	|	NULL КАК СрокУплатыАванса3КвМесяцев,
	|	NULL КАК СрокУплатыАванса3КвДней
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыНалоговПоРегионам КАК ПорядокУплатыНалоговПоРегионам
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПорядокУплатыНалоговПоРегионам.Налог
	|			И МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган.КодРегиона = ПорядокУплатыНалоговПоРегионам.КодРегиона
	|			И (ПорядокУплатыНалоговПоРегионам.Организация = &Организация)
	|			И (ПорядокУплатыНалоговПоРегионам.Период >= &Период2021Год)
	|ГДЕ
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог = &ЗадачаТранспортныйНалог";	
	
	// 2022 ГОД И ДАЛЕЕ - НАЛОГ НА ИМУЩЕСТВО
	// Сроки уплаты налога и авансовых платежей общие для всех регионов (см. 305-ФЗ от 02.07.2021) - используем срок по общему правилу.
	// Но данные по уплате авансов в регионах берем из нового РС ПорядокУплатыНалоговПоРегионам.
	Запрос.Текст = Запрос.Текст + ТекстОбъединитьВсе +
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ПорядокУплатыНалоговПоРегионам.Период, &Период2022Год) КАК Период,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог КАК Налог,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган КАК РегистрацияВНалоговомОргане,
	|	NULL КАК СрокУплатыНалогаМесяцев,
	|	NULL КАК СрокУплатыНалогаДней,
	|	ЕСТЬNULL(ПорядокУплатыНалоговПоРегионам.УплачиваютсяАвансы, ЛОЖЬ) КАК УплачиваютсяАвансы,
	|	NULL КАК СрокУплатыАвансаМесяцев,
	|	NULL КАК СрокУплатыАвансаДней,
	|	ЛОЖЬ КАК СрокУплатыУстанавливаетсяДляКаждогоАванса,
	|	NULL КАК СрокУплатыАванса1КвМесяцев,
	|	NULL КАК СрокУплатыАванса1КвДней,
	|	NULL КАК СрокУплатыАванса2КвМесяцев,
	|	NULL КАК СрокУплатыАванса2КвДней,
	|	NULL КАК СрокУплатыАванса3КвМесяцев,
	|	NULL КАК СрокУплатыАванса3КвДней
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыНалоговПоРегионам КАК ПорядокУплатыНалоговПоРегионам
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПорядокУплатыНалоговПоРегионам.Налог
	|		И МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган.КодРегиона = ПорядокУплатыНалоговПоРегионам.КодРегиона
	|		И (ПорядокУплатыНалоговПоРегионам.Организация = &Организация)
	|		И (ПорядокУплатыНалоговПоРегионам.Период >= &Период2022Год)
	|ГДЕ
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог = &ЗадачаНалогНаИмущество";	

	Запрос.Текст = Запрос.Текст + "
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог,
	|	РегистрацияВНалоговомОргане";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ПорядокУплатыРегиональныхМестныхНалогов(Организация, Период) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ПорядокУплатыНалоговНаМестах;
	
	ПорядокУплаты = Новый ТаблицаЗначений;
	ПорядокУплаты.Колонки.Добавить("Период",                      Новый ОписаниеТипов("Дата"));
	ПорядокУплаты.Колонки.Добавить("Налог",                       МетаданныеРегистра.Измерения.Налог.Тип);
	ПорядокУплаты.Колонки.Добавить("РегистрацияВНалоговомОргане", МетаданныеРегистра.Измерения.РегистрацияВНалоговомОргане.Тип);
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		ПорядокУплаты.Колонки.Добавить(Ресурс.Имя, Ресурс.Тип);
	КонецЦикла;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; // Хранит МестныеНалогиВРазрезеНалоговыхОрганов
	СоздатьМестныеНалогиВРазрезеНалоговыхОрганов(МенеджерВременныхТаблиц, Организация, Период);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период",      Период);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Период КАК Период,
	|	ПорядокУплатыНалоговНаМестах.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане КАК НалоговыйОрган,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаДней,
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней,
	|	ПорядокУплатыНалоговНаМестах.Описание
	|ПОМЕСТИТЬ ПравилаЗаданыПользователем
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(&Период, Организация = &Организация) КАК ПорядокУплатыНалоговНаМестах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог КАК Налог,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган
	|ПОМЕСТИТЬ НалогиПоФедеральнымПравилам
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаЗаданыПользователем КАК ПравилаЗаданыПользователем
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПравилаЗаданыПользователем.Налог
	|			И МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган = ПравилаЗаданыПользователем.НалоговыйОрган
	|ГДЕ
	|	ПравилаЗаданыПользователем.Налог ЕСТЬ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаЗаданыПользователем.Период КАК Период,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.Налог КАК Налог,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган КАК РегистрацияВНалоговомОргане,
	|	ПравилаЗаданыПользователем.СрокУплатыНалогаМесяцев,
	|	ПравилаЗаданыПользователем.СрокУплатыНалогаДней,
	|	ПравилаЗаданыПользователем.УплачиваютсяАвансы,
	|	ПравилаЗаданыПользователем.СрокУплатыАвансаМесяцев,
	|	ПравилаЗаданыПользователем.СрокУплатыАвансаДней,
	|	ПравилаЗаданыПользователем.Описание
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаЗаданыПользователем КАК ПравилаЗаданыПользователем
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПравилаЗаданыПользователем.Налог
	|			И МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган = ПравилаЗаданыПользователем.НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалогиПоФедеральнымПравилам.Налог КАК Налог,
	|	НалогиПоФедеральнымПравилам.НалоговыйОрган КАК РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА ПравилаПредставленияОтчетовУплатыНалогов.ФинансовыйПериод = ПравилаПредставленияОтчетовУплатыНалогов.Периодичность
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Аванс,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокМесяцев,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокДней
	|ИЗ
	|	НалогиПоФедеральнымПравилам КАК НалогиПоФедеральнымПравилам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО НалогиПоФедеральнымПравилам.Налог = ПравилаПредставленияОтчетовУплатыНалогов.Владелец
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.НачалоДействия <= &Период
	|	И (ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия >= &Период)
	|	И НЕ ПравилаПредставленияОтчетовУплатыНалогов.ПометкаУдаления
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|ИТОГИ ПО
	|	Налог,
	|	РегистрацияВНалоговомОргане,
	|	Аванс";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// Правила, заданные пользователем
	Выборка = РезультатЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ПорядокУплаты.Добавить(), Выборка);
	КонецЦикла;
	
	// Значения по умолчанию
	ВыборкаНалоги = РезультатЗапроса[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНалоги.Следующий() Цикл
		
		ВыборкаРегистрацияВНалоговомОргане = ВыборкаНалоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаРегистрацияВНалоговомОргане.Следующий() Цикл
		
			НоваяСтрока = ПорядокУплаты.Добавить();
			НоваяСтрока.Период = НачалоГода(ТекущаяДатаСеанса());
			НоваяСтрока.Налог  = ВыборкаНалоги.Налог;
			НоваяСтрока.УплачиваютсяАвансы = Ложь;
			НоваяСтрока.РегистрацияВНалоговомОргане = ВыборкаРегистрацияВНалоговомОргане.РегистрацияВНалоговомОргане;
			
			ВыборкаАванс = ВыборкаРегистрацияВНалоговомОргане.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаАванс.Следующий() Цикл
				
				Выборка = ВыборкаАванс.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Выборка.Следующий() Цикл
					
					Если Выборка.Аванс Тогда
						НоваяСтрока.СрокУплатыАвансаМесяцев = Выборка.СрокМесяцев;
						НоваяСтрока.СрокУплатыАвансаДней    = Выборка.СрокДней;
					Иначе
						НоваяСтрока.СрокУплатыНалогаМесяцев = Выборка.СрокМесяцев;
						НоваяСтрока.СрокУплатыНалогаДней    = Выборка.СрокДней;
					КонецЕсли;
					УстановитьОписание(НоваяСтрока);
					Прервать;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПорядокУплаты;
	
КонецФункции

Процедура УстановитьОписание(Запись) Экспорт
	
	День = 24 * 60 * 60;
	
	Описание = Новый Массив;
	
	КодНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Налог, "Код");
	НастраиваетсяСрокУплатыНалога = Не РегистрыСведений.ПорядокУплатыНалоговНаМестах.ЕдиныйСрокУплатыНалога(КодНалога, Запись.Период);
	НастраиваетсяСрокУплатыАвансов = Не РегистрыСведений.ПорядокУплатыНалоговНаМестах.ЕдиныйСрокУплатыАвансов(КодНалога, Запись.Период);
	
	Если Не НастраиваетсяСрокУплатыНалога Тогда
		СрокУплатыНалога = СтрШаблон(
			НСтр("ru = 'Срок уплаты налога - %1'"), 
			Формат('20210301', "ДФ='d MMMM'"));  // "1 марта"
	ИначеЕсли Запись.СрокУплатыНалогаМесяцев = 0 И Запись.СрокУплатыНалогаДней = 0 Тогда
		СрокУплатыНалога = НСтр("ru = 'Срок уплаты налога не установлен'");
	ИначеЕсли Запись.СрокУплатыНалогаМесяцев = 0 И Запись.СрокУплатыНалогаДней > 31 Тогда
		СрокУплатыНалога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Срок уплаты налога - %1'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Запись.СрокУплатыНалогаДней, "день,дня,дней"));
	Иначе
		Дата = ДобавитьМесяц('20000101', Запись.СрокУплатыНалогаМесяцев) + Запись.СрокУплатыНалогаДней * День - 1;
		СрокУплатыНалога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Срок уплаты налога - %1'"),
			Формат(Дата, "ДФ='dd MMMM'"));
	КонецЕсли;
	Описание.Добавить(СрокУплатыНалога);
	
	Если Не Запись.УплачиваютсяАвансы Тогда
		СрокУплатыАванса = НСтр("ru = 'Авансы не уплачиваются'");
	ИначеЕсли Не НастраиваетсяСрокУплатыАвансов Тогда
		СрокУплатыАванса = СтрШаблон(
			СокрП(НСтр("ru = 'Срок уплаты авансов - %1, %2, %3'")),
			Формат('20210430', "ДФ='dd MMMM'"),  // "30 апреля"
			Формат('20210731', "ДФ='dd MMMM'"),  // "31 июля"
			Формат('20211031', "ДФ='dd MMMM'")); // "31 октября"
	ИначеЕсли Не Запись.СрокУплатыУстанавливаетсяДляКаждогоАванса Тогда
		
		Если Запись.СрокУплатыАвансаМесяцев = 0 Тогда
			СтрокаСрокУплатыАвансаМесяцев = НСтр("");
		Иначе
			СтрокаСрокУплатыАвансаМесяцев = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
				Запись.СрокУплатыАвансаМесяцев, 
				НСтр("ru = 'месяц,месяца,месяцев'"));
		КонецЕсли;
		
		Если Запись.СрокУплатыАвансаДней = 0 Тогда
			СтрокаСрокУплатыАвансаДней = НСтр("");
		Иначе
			СтрокаСрокУплатыАвансаДней = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
				Запись.СрокУплатыАвансаДней, 
				НСтр("ru = 'день,дня,дней'"));
		КонецЕсли;
		
		СрокУплатыАванса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СокрП(НСтр("ru = 'Срок уплаты авансов - %1 %2'")),
			СтрокаСрокУплатыАвансаМесяцев,
			СтрокаСрокУплатыАвансаДней);
	Иначе
			
		СрокУплатыАванса1Кв = СрокУплатыАвансаЗаКвартал(Запись, 1);
		СрокУплатыАванса2Кв = СрокУплатыАвансаЗаКвартал(Запись, 2);
		СрокУплатыАванса3Кв = СрокУплатыАвансаЗаКвартал(Запись, 3);
		
		СрокУплатыАванса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СокрП(НСтр("ru = 'Срок уплаты авансов - %1, %2, %3'")),
			Формат(СрокУплатыАванса1Кв, "ДФ='dd MMMM'"),
			Формат(СрокУплатыАванса2Кв, "ДФ='dd MMMM'"),
			Формат(СрокУплатыАванса3Кв, "ДФ='dd MMMM'"));
			
	КонецЕсли;
		
	Описание.Добавить(СрокУплатыАванса);
	
	Запись.Описание = СтрСоединить(Описание, "; ");
	
КонецПроцедуры

Процедура СоздатьПравилаУплатыНалоговНаМестах(МенеджерВременныхТаблиц, Организация, ИмяЗадачиУплатыНалога = "", ТекущийПериод = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяЗадачиУплатыНалога) Тогда
		КодыНалоговПорядокКоторыхУстанавливаетсяНаМестах = КодыНалоговПорядокКоторыхУстанавливаетсяНаМестах();
		Если КодыНалоговПорядокКоторыхУстанавливаетсяНаМестах.Найти(ИмяЗадачиУплатыНалога) = Неопределено Тогда
			// Создадим пустую таблицу
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Справочник.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка) КАК Правило,
			|	ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка) КАК РегистрацияВНалоговомОргане,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодДействияПравила,
			|	ИСТИНА КАК Включено,
			|	0 КАК СрокМесяцев,
			|	0 КАК СрокДней,
			|	"""" КАК ОграничениеПериода
			|ПОМЕСТИТЬ ПравилаУплатыНалоговНаМестах
			|ГДЕ
			|	ЛОЖЬ
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Правило,
			|	ПериодДействияПравила";
			Запрос.Выполнить();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Сохраним список тех временных таблиц, которые были в менеджере, -
	// ниже из менеджера удалим все временные талицы кроме указанных в этом списке.
	// Иными словами, ниже почистим все "промежуточные" временные таблицы, которые использованы в этой процедуре.
	НеудаляемыеВременныеТаблицы = ОбщегоНазначенияБП.СписокВременныхТаблиц(МенеджерВременныхТаблиц);
	
	СоздатьМестныеНалогиВРазрезеНалоговыхОрганов(МенеджерВременныхТаблиц, Организация, ТекущийПериод);
	СоздатьПорядокУплатыНалоговНаМестах(МенеджерВременныхТаблиц, Организация);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец КАК Налог,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Ссылка,
	|	ПравилаПредставленияОтчетовУплатыНалогов.ФинансовыйПериод КАК ФинансовыйПериод,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Периодичность КАК Периодичность,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Действие КАК Действие,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокМесяцев КАК СрокМесяцев,
	|	ПравилаПредставленияОтчетовУплатыНалогов.СрокДней КАК СрокДней,
	|	МестныеНалогиВРазрезеНалоговыхОрганов.НалоговыйОрган КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ Правила
	|ИЗ
	|	МестныеНалогиВРазрезеНалоговыхОрганов КАК МестныеНалогиВРазрезеНалоговыхОрганов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО МестныеНалогиВРазрезеНалоговыхОрганов.Налог = ПравилаПредставленияОтчетовУплатыНалогов.Владелец
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РегистрацияВНалоговомОргане,
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Правила.Налог КАК Налог,
	|	Правила.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ОтборПравил
	|ИЗ
	|	Правила КАК Правила
	|ГДЕ
	|	Правила.ФинансовыйПериод <> Правила.Периодичность
	|	И Правила.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Налог КАК Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.Период КАК Период,
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев КАК СрокУплатыАвансаМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней КАК СрокУплатыАвансаДней,
	|	""4"" КАК ОграничениеПериода
	|ПОМЕСТИТЬ МестныеПравилаУплатыАвансов
	|ИЗ
	|	ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПравил КАК ОтборПравил
	|		ПО ПорядокУплатыНалоговНаМестах.Налог = ОтборПравил.Налог
	|			И ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане = ОтборПравил.РегистрацияВНалоговомОргане
	|ГДЕ
	|	НЕ ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы 
	|		ИЛИ НЕ ПорядокУплатыНалоговНаМестах.СрокУплатыУстанавливаетсяДляКаждогоАванса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.Период,
	|	ИСТИНА,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса1КвМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса1КвДней,
	|	""2,3,4""
	|ИЗ
	|	ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПравил КАК ОтборПравил
	|		ПО ПорядокУплатыНалоговНаМестах.Налог = ОтборПравил.Налог
	|			И ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане = ОтборПравил.РегистрацияВНалоговомОргане
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы
	|	И ПорядокУплатыНалоговНаМестах.СрокУплатыУстанавливаетсяДляКаждогоАванса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.Период,
	|	ИСТИНА,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса2КвМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса2КвДней,
	|	""1,3,4""
	|ИЗ
	|	ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПравил КАК ОтборПравил
	|		ПО ПорядокУплатыНалоговНаМестах.Налог = ОтборПравил.Налог
	|			И ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане = ОтборПравил.РегистрацияВНалоговомОргане
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы
	|	И ПорядокУплатыНалоговНаМестах.СрокУплатыУстанавливаетсяДляКаждогоАванса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокУплатыНалоговНаМестах.Налог,
	|	ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.Период,
	|	ИСТИНА,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса3КвМесяцев,
	|	ПорядокУплатыНалоговНаМестах.СрокУплатыАванса3КвДней,
	|	""1,2,4""
	|ИЗ
	|	ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПравил КАК ОтборПравил
	|		ПО ПорядокУплатыНалоговНаМестах.Налог = ОтборПравил.Налог
	|			И ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане = ОтборПравил.РегистрацияВНалоговомОргане
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы
	|	И ПорядокУплатыНалоговНаМестах.СрокУплатыУстанавливаетсяДляКаждогоАванса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Правила.Налог КАК Налог,
	|	Правила.Ссылка КАК Правило,
	|	Правила.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ПериодДействияПравила,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.УплачиваютсяАвансы, ЛОЖЬ) КАК Включено,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаМесяцев, Правила.СрокМесяцев) КАК СрокМесяцев,
	|	ЕСТЬNULL(ПорядокУплатыНалоговНаМестах.СрокУплатыАвансаДней, Правила.СрокДней) КАК СрокДней,
	|	ПорядокУплатыНалоговНаМестах.ОграничениеПериода КАК ОграничениеПериода
	|ПОМЕСТИТЬ ПравилаУплатыАвансов
	|ИЗ
	|	Правила КАК Правила
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестныеПравилаУплатыАвансов КАК ПорядокУплатыНалоговНаМестах
	|		ПО Правила.Налог = ПорядокУплатыНалоговНаМестах.Налог 
	|			И Правила.РегистрацияВНалоговомОргане = ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане
	|ГДЕ
	|	Правила.ФинансовыйПериод <> Правила.Периодичность";
	
	Запрос.Выполнить();
	
	// Учтем особенности некоторых налогов на местах.
	
	// Создает временную таблицу ПравилаПодачиЗаявленияОЛьготеПоТранспортномуЗемельномуНалогу - 
	// она содержит правила, которые будут добавлены в итоговую временную таблицу ПравилаУплатыНалоговНаМестах (см. ниже) 
	СоздатьПравилаПодачиЗаявленияОЛьготеПоТранспортномуЗемельномуНалогу(МенеджерВременныхТаблиц);
	
	// В периодах, предшествующих началу действия местных (региональных) настроек, используем общие правила по уплате налога.
	// Для этого в 1-й таблице выбираем общие правила по умолчанию, а во второй - с учетом местных настроек по периодам действия.  
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Правила.Ссылка КАК Правило,
	|	Правила.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодДействияПравила,
	|	ИСТИНА КАК Включено,
	|	Правила.СрокМесяцев КАК СрокМесяцев,
	|	Правила.СрокДней КАК СрокДней,
	|	NULL КАК ОграничениеПериода
	|ПОМЕСТИТЬ ПравилаУплатыНалоговНаМестах
	|ИЗ
	|	Правила КАК Правила
	|ГДЕ
	|	Правила.ФинансовыйПериод = Правила.Периодичность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Правила.Ссылка,
	|	Правила.РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестах.Период,
	|	ИСТИНА,
	|	ВЫБОР
	|		КОГДА Правила.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ТОГДА ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаМесяцев
	|		ИНАЧЕ Правила.СрокМесяцев
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Правила.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ТОГДА ПорядокУплатыНалоговНаМестах.СрокУплатыНалогаДней
	|		ИНАЧЕ Правила.СрокДней
	|	КОНЕЦ,
	|	NULL
	|ИЗ
	|	Правила КАК Правила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПорядокУплатыНалоговНаМестах КАК ПорядокУплатыНалоговНаМестах
	|		ПО Правила.Налог = ПорядокУплатыНалоговНаМестах.Налог
	|			И Правила.РегистрацияВНалоговомОргане = ПорядокУплатыНалоговНаМестах.РегистрацияВНалоговомОргане
	|ГДЕ
	|	Правила.ФинансовыйПериод = Правила.Периодичность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПравилаУплатыАвансов.Правило,
	|	ПравилаУплатыАвансов.РегистрацияВНалоговомОргане,
	|	ПравилаУплатыАвансов.ПериодДействияПравила,
	|	ПравилаУплатыАвансов.Включено,
	|	ПравилаУплатыАвансов.СрокМесяцев,
	|	ПравилаУплатыАвансов.СрокДней,
	|	ПравилаУплатыАвансов.ОграничениеПериода
	|ИЗ
	|	ПравилаУплатыАвансов КАК ПравилаУплатыАвансов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Правила.Правило,
	|	ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка),
	|	Правила.ПериодДействияПравила,
	|	Правила.Включено,
	|	Правила.СрокМесяцев,
	|	Правила.СрокДней,
	|	NULL
	|ИЗ
	|	ПравилаПодачиЗаявленияОЛьготеПоТранспортномуЗемельномуНалогу КАК Правила
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило,
	|	ПериодДействияПравила";
		
	Запрос.Выполнить();
	
	НеудаляемыеВременныеТаблицы.Добавить("ПравилаУплатыНалоговНаМестах");
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц,, НеудаляемыеВременныеТаблицы);
	
КонецПроцедуры

// Проверяет, установлено ли продление сроков уплаты налогов на региональном/местном уровне
//
// Параметры:
//  Организация - СправочникСсылка.Организации	 - организация, для которой проверяются настройки
//  Налог - Строка - идентификатор (код) налога, как он задан в справочнике ЗадачиБухгалтера, например "ТранспортныйНалог"
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - ИФНС, в которой установлены продленные сроки уплаты региональных/местных налогов
//  Период - Дата - дата, на которую проверяются настройки
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПродленыРегиональныеСрокиУплатыНалога(Организация, Налог, РегистрацияВНалоговомОргане, Период) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Налог КАК Налог
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И Налог.Код = &Налог
	|				И РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|ГДЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.СрокУплатыУстанавливаетсяДляКаждогоАванса";
	
	Запрос.УстановитьПараметр("Налог",       Налог);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции 

// Функция проверяет, устанавливаются ли даты уплаты налога отдельно в каждом регионе или же срок единый для всех 
//
// Параметры:
//  Налог	 - Строка - код задачи бухгалтера по одному из имущественных налогов
//  Период	 - Дата - дата, на которую выполняется проверка
// 
// Возвращаемое значение:
//  Булево - если Истина, то налог уплачивается в единый срок во всех регионах 
//
Функция ЕдиныйСрокУплатыНалога(Налог, Период) Экспорт 

	Если Налог = ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог()
		 Или Налог = ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог() Тогда
		Возврат Период >= '20200101'; // единый срок начиная с уплаты налога за 2020 год
	КонецЕсли;	
				
	Возврат Ложь;

КонецФункции

// Функция проверяет, устанавливаются ли даты уплаты авансов по налогу отдельно в каждом регионе или же срок единый для всех 
//
// Параметры:
//  Налог	 - Строка - код задачи бухгалтера по одному из имущественных налогов
//  Период	 - Дата - дата, на которую выполняется проверка
// 
// Возвращаемое значение:
//  Булево - если Истина, то авансы уплачиваются в единый срок во всех регионах, где установлена уплата авансов по налогу 
//
Функция ЕдиныйСрокУплатыАвансов(Налог, Период) Экспорт 

	Если Налог = ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог()
		 Или Налог = ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог() Тогда
		Возврат Период >= '20210101'; // единый срок начиная с 2021 года
	КонецЕсли;	
				
	Возврат Ложь;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьПравилаПодачиЗаявленияОЛьготеПоТранспортномуЗемельномуНалогу(МенеджерВременныхТаблиц)
	
	// Если организация уплачивает авансы по транспортному или земельному налогу хотя бы в одном регионе,
	// то срок задачи по представлению заявления о льготе по этим налогам перенесем на самую раннюю дату уплаты аванса (срок по умолчанию - конец года).  
	// Но установить срок задачи непосредственно нельзя - нужно манипулировать значениями СрокМесяцев и СрокДней правила представления заявления.
	// Алгоритм следующий:
	// 1. Находим правило уплаты аванса с самым ранним сроком.
	//    Для этого 
	//		- Находим все периоды (годы), за которые введены записи в регистре по транспортному и земельному налогу (см. временную таблицу Периоды)
	//		- На каждый такой период получаем "срез последних" (см. СрезПоПериодам и СамыеРанниеДатыАванса)
	//      - На основании такого "среза последних" вычисляем минимальную дату аванса в каждом году (СамыеРанниеДатыАванса)
	//        Чтобы не вычислять соотношение между СрокМесяцев и СрокДней, по каждому правилу просто рассчитываем фактический срок уплаты аванса за 1-й квартал.
	// 2. Вычисляем "сдвиг назад" срока из п.1 относительно конца года (СрокМесяцев и СрокДней будут отрицательные). 
	// 3. В целях такого расчета год не важен (берем текущий), т.к. "лишний" день - это 29 февраля. В то же время срок уплаты авансов за 1-й квартал -
	//    не ранее 1 апреля. Таким образом, этот "лишний" день никогда не попадет в интервал между датой уплаты аванса и концом года.
	// 4. Если авансы не уплачиваются (Включено = Ложь), то берем конец года как срок уплаты, чтобы заведомо не повлиять на расчет минимальной даты уплаты аванса
	//    (см. СамыеРанниеДатыАванса).
	// 5. СрокМесяцев и СрокДней из п.2 впоследствии будут "подставлены" в правило представления заявления (см. РегистрыСведений.ЗадачиБухгалтера.ЗаполнитьРасписаниеПоНалогамИСборам()).
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЗемельныйНалог",    ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог());
	Запрос.УстановитьПараметр("ТранспортныйНалог", ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	Запрос.УстановитьПараметр("ТочкаОтсчета", Дата(Год(ТекущаяДатаСеанса()), 3, 31)); // 31 марта
	Запрос.УстановитьПараметр("КонецГода", КонецГода(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ПравилоЗаявлениеОЛьготеПоТранспортномуЗемельномуНалогу",
		Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("ЛьготыПоТранспортномуЗемельномуНалогу", "2020_Заявление"));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Правила.ПериодДействияПравила КАК ПериодДействияПравила
	|ПОМЕСТИТЬ Периоды
	|ИЗ
	|	ПравилаУплатыАвансов КАК Правила
	|ГДЕ
	|	Правила.Налог.Код В (&ТранспортныйНалог, &ЗемельныйНалог)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.ПериодДействияПравила КАК ПериодДействияПравила,
	|	МАКСИМУМ(Правила.ПериодДействияПравила) КАК ПериодДействующейЗаписи,
	|	Правила.Налог КАК Налог,
	|	Правила.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ СрезПоПериодам
	|ИЗ
	|	Периоды КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаУплатыАвансов КАК Правила
	|		ПО Периоды.ПериодДействияПравила >= Правила.ПериодДействияПравила
	|			И (Правила.Налог.Код В (&ТранспортныйНалог, &ЗемельныйНалог))
	|
	|СГРУППИРОВАТЬ ПО
	|	Периоды.ПериодДействияПравила,
	|	Правила.Налог,
	|	Правила.РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезПоПериодам.ПериодДействияПравила КАК ПериодДействияПравила,
	|	МАКСИМУМ(Правила.Включено) КАК Включено,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА Правила.Включено
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(&ТочкаОтсчета, МЕСЯЦ, Правила.СрокМесяцев), ДЕНЬ, Правила.СрокДней)
	|			ИНАЧЕ КОНЕЦПЕРИОДА(&ТочкаОтсчета, ГОД)
	|		КОНЕЦ) КАК Срок
	|ПОМЕСТИТЬ СамыеРанниеДатыАванса
	|ИЗ
	|	СрезПоПериодам КАК СрезПоПериодам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаУплатыАвансов КАК Правила
	|		ПО СрезПоПериодам.ПериодДействующейЗаписи = Правила.ПериодДействияПравила
	|			И СрезПоПериодам.Налог = Правила.Налог
	|			И СрезПоПериодам.РегистрацияВНалоговомОргане = Правила.РегистрацияВНалоговомОргане
	|
	|СГРУППИРОВАТЬ ПО
	|	СрезПоПериодам.ПериодДействияПравила
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПравилоЗаявлениеОЛьготеПоТранспортномуЗемельномуНалогу КАК Правило,
	|	СамыеРанниеДатыАванса.ПериодДействияПравила КАК ПериодДействияПравила,
	|	СамыеРанниеДатыАванса.Включено КАК Включено,
	|	РАЗНОСТЬДАТ(&КонецГода, СамыеРанниеДатыАванса.Срок, МЕСЯЦ) КАК СрокМесяцев,
	|	РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(&КонецГода, МЕСЯЦ, РАЗНОСТЬДАТ(&КонецГода, СамыеРанниеДатыАванса.Срок, МЕСЯЦ)), СамыеРанниеДатыАванса.Срок, ДЕНЬ) КАК СрокДней
	|ПОМЕСТИТЬ ПравилаПодачиЗаявленияОЛьготеПоТранспортномуЗемельномуНалогу
	|ИЗ
	|	СамыеРанниеДатыАванса КАК СамыеРанниеДатыАванса";
	
	Запрос.Выполнить();
	
КонецПроцедуры	
 
Функция КодыНалоговПорядокКоторыхУстанавливаетсяНаМестах() Экспорт
	
	Налоги = Новый Массив;
	Налоги.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество());
	Налоги.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог());
	Налоги.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	// См. также МестныеНалогиВРазрезеНалоговыхОрганов()
	
	Возврат Налоги;
	
КонецФункции

Функция СрокУплатыАвансаЗаКвартал(Запись, НомерКвартала) Экспорт
		
	Если НомерКвартала = 1 Тогда
		КонецКвартала = Дата(Год(Запись.Период), 3, 31);
	ИначеЕсли НомерКвартала = 2 Тогда
		КонецКвартала = Дата(Год(Запись.Период), 6, 30);
	ИначеЕсли НомерКвартала = 3 Тогда
		КонецКвартала = Дата(Год(Запись.Период), 9, 30);
	Иначе
		Возврат '00010101';		
	КонецЕсли;	
	
	РесурсСрокМесяцев = СтрШаблон("СрокУплатыАванса%1КвМесяцев", НомерКвартала);
	РесурсСрокДней = СтрШаблон("СрокУплатыАванса%1КвДней", НомерКвартала);
	День = 24 * 60 * 60;
	
	Возврат КонецМесяца(ДобавитьМесяц(КонецКвартала, Запись[РесурсСрокМесяцев])) + День * Запись[РесурсСрокДней];
	
КонецФункции	

#КонецОбласти

#КонецЕсли
