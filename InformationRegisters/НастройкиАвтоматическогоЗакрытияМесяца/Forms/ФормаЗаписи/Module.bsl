
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимПоВсемОрганизациям = 
		ЗакрытиеМесяцаВызовСервера.ТекущийРежимАвтоматическогоЗакрытияМесяца() = "ПоВсемОрганизациям";
	ВсегоОднаОрганизация = Не Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(
		Элементы.ПолеОрганизация, СоответствиеОрганизаций, , Истина);
	
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		Организация = Параметры.Ключ.Организация;
		Если Организация.Пустая() Тогда
			РежимПоВсемОрганизациям = Истина;
			Заголовок = СтрШаблон(НСтр("ru = '%1 (по всем организациям)'"), Заголовок);
		Иначе
			Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"), Заголовок, Организация);
		КонецЕсли;
		РежимРедактированияЗаписи = Истина;
	ИначеЕсли НЕ РежимПоВсемОрганизациям Тогда
		СписокВыбора = Элементы.ПолеОрганизация.СписокВыбора;
		Если СписокВыбора.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Нет ни одной доступной организации для настройки расписания.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			Возврат;
		Иначе
			Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
			Если Не ЗначениеЗаполнено(Организация) Тогда
				ЗначениеСпискаВыбора = СписокВыбора[0].Значение;
				Организация = СоответствиеОрганизаций[ЗначениеСпискаВыбора].Организация;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПрочитатьНастройкиРегламентногоЗадания();

	ОбщегоНазначенияБПКлиентСервер.УстановитьЗначениеПолеОрганизация(
		ПолеОрганизация, Организация, Ложь);
		
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокВыбораВремени(Элементы.ВремяЗапуска);
	
	ЗаполнитьСписокДнейМесяца(Элементы.ДеньПервогоЗапуска);
	ЗаполнитьСписокДнейМесяца(Элементы.ДеньПоследнегоЗапуска);
	
	ОбновитьПодсказкуРазрешенногоПериода(Элементы.ГруппаГоризонтальнаяСПодсказкой.Подсказка, РазрешенноеЧислоМесяцевНазад);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриВключенииОтключенииРегламентногоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		ВопросОСохраненииНастроек(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		СтандартнаяОбработка = Ложь;
		ВопросОСохраненииНастроек();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияПриИзменении(Элемент, ПолеОрганизация,
		Организация, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение,
		СтандартнаяОбработка, СоответствиеОрганизаций, Организация, Ложь);
		
	РезультатПроверкиАктуальности = Неопределено;
		
	ПрочитатьНастройкиРегламентногоЗадания();
	ПриВключенииОтключенииРегламентногоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
    
КонецПроцедуры
	
&НаКлиенте
Процедура ВыполнениеВключеноПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ИзменитьВидимостьИДоступностьНастроек();
	ПриВключенииОтключенииРегламентногоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВключенииОтключенииРегламентногоЗадания()
	
	Если Не РежимПоВсемОрганизациям И ВыполнениеВключено Тогда
		ЗапуститьПроверкуАктуальности();
	Иначе
		Элементы.РисПриАктуализации.Видимость = Ложь;
		Если ЗначениеЗаполнено(ИдентификаторЗаданияПроверкиАктуальности) Тогда
			ЗакрытиеМесяцаВызовСервера.ОтменитьВыполнениеЗадания(
				ИдентификаторЗаданияПроверкиАктуальности);
			КонецЕсли;
		ОбновитьПодсказкуСледующегоЗапуска();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьЗапускаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Если ПериодичностьЗапуска = "Ежедневно" Тогда
		ДеньПервогоЗапуска = 1;
		ДеньПоследнегоЗапуска = 31;
	КонецЕсли;
	
	ИзменитьВидимостьИДоступностьНастроек();
	ОбновитьПодсказкуСледующегоЗапуска();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньПервогоЗапускаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьПодсказкуСледующегоЗапуска();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньПоследнегоЗапускаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьПодсказкуСледующегоЗапуска();
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяЗапускаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьПодсказкуСледующегоЗапуска();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗакрытияМесяцаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьПодсказкуСледующегоЗапуска();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПериодаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Элементы.РазрешенноеЧислоМесяцевНазад.Доступность =
		ВариантПериодаЗакрытияМесяца = "ТолькоПредыдущиеМесяцы";
		
	ОбновитьПодсказкуСледующегоЗапуска();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчетОВыполненииНажатие(Элемент)
	
	ЗакрытиеМесяцаКлиент.ПоказатьОшибкиПоследнейАктуализации(Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если ДеньПоследнегоЗапуска < ДеньПервогоЗапуска Тогда
		ТекстСообщения = НСтр("ru = 'День последнего запуска не может быть меньше, чем день первого запуска!'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Не РежимПоВсемОрганизациям И Организация.Пустая() Тогда
		ТекстСообщения = НСтр("ru = 'Организация не может быть пустой!'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЗаписатьНастройкиРегламентногоЗадания();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьПодсказкуСледующегоЗапуска()
	
	Если Элементы.РисПриАктуализации.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.РазрешенноеЧислоМесяцевНазад.Заголовок =
		ПросклонятьСтроку(НСтр("ru = 'предыдущий месяц'"), РазрешенноеЧислоМесяцевНазад);
	
	ПараметрыПроверки = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыЗаданияЗакрытияМесяца();
	ЗаполнитьЗначенияСвойств(ПараметрыПроверки, ЭтотОбъект);
	ПараметрыПроверки.ВыполнениеВключено = ВыполнениеВключено;
	ПараметрыПроверки.ВыполнятьВОтчетномПериоде = ВариантПериодаЗакрытияМесяца = "ОтчетныйПериод";
	ПараметрыПроверки.Ежедневно = ПериодичностьЗапуска = "Ежедневно";
	ПараметрыПроверки.ТолькоПерепроведениеДокументов = ВариантЗакрытияМесяца = "ТолькоПерепроведениеДокументов";
	
	ЗакрытиеМесяцаКлиент.ОбновитьПодсказкуСледующегоЗапуска(ЭтотОбъект, ПараметрыПроверки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодсказкуРазрешенногоПериода(Подсказка, РазрешенноеЧислоМесяцевНазад)
	
	ШаблонПодсказки = НСтр("ru = 'Выполнение прервется, если потребуется закрытие месяца за %1 или ранее'");
	НачалоЭтогоМесяца = НачалоМесяца(ОбщегоНазначенияБПВызовСервера.ТекущаяДатаНаСервере());
	ЗаМесяцДоРазрешеннойДаты = ДобавитьМесяц(НачалоЭтогоМесяца, -РазрешенноеЧислоМесяцевНазад) - 1;
	МесяцНачалаЗапрета = НРег(Формат(ЗаМесяцДоРазрешеннойДаты,  НСтр("ru = 'ДФ=''ММММ гггг ""г.""'''")));
	
	Подсказка = СтрШаблон(ШаблонПодсказки, МесяцНачалаЗапрета);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимостьИДоступностьНастроек()
	
	Если ВсегоОднаОрганизация Или РежимПоВсемОрганизациям Или РежимРедактированияЗаписи Тогда
		Элементы.ГруппаШапка.Видимость = Ложь;
	КонецЕсли;
	
	Если РежимРедактированияЗаписи Тогда
		Элементы.ГруппаРасширенныеНастройки.Показать();
	КонецЕсли;
	
	Элементы.ГруппаРасписание.Видимость  = Не РежимРедактированияЗаписи;
	Элементы.ГруппаСостояние.Видимость   = Не РежимРедактированияЗаписи И Не РежимПоВсемОрганизациям;
	Элементы.ГруппаНастройки.Доступность = ВыполнениеВключено Или РежимПоВсемОрганизациям Или РежимРедактированияЗаписи;
	Элементы.ГруппаПериодичностьЕжемесячно.Видимость = ПериодичностьЗапуска = "ВОтдельныеДни";
	Элементы.РазрешенноеЧислоМесяцевНазад.Доступность =
		ВариантПериодаЗакрытияМесяца = "ТолькоПредыдущиеМесяцы";
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Процедура ЗаполнитьСписокДнейМесяца(ПолеВводаФормы)
	
	СписокДней = ПолеВводаФормы.СписокВыбора;
	СписокДней.Очистить();
	
	Для Инд = 1 По 31 Цикл
		СписокДней.Добавить(Инд);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешенноеЧислоМесяцевПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьПодсказкуСледующегоЗапуска();
	ОбновитьПодсказкуРазрешенногоПериода(Элементы.ГруппаГоризонтальнаяСПодсказкой.Подсказка, РазрешенноеЧислоМесяцевНазад);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПросклонятьСтроку(Знач СтрокаДляСклонения, Знач ЧислоДляСклонения)
	
	Результат = ПолучитьСклоненияСтрокиПоЧислу(СтрокаДляСклонения,
		ЧислоДляСклонения, , "ЧС=Количественное", "ПД=Винительный;");
	
	Возврат СтрЗаменить(Результат[0], СтрШаблон("%1 ", ЧислоДляСклонения), "");
	
КонецФункции

#Область ПроверкаАктуальности

&НаКлиенте
Процедура ЗапуститьПроверкуАктуальности()
	
	Если РежимРедактированияЗаписи Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверкаАктуальностиЗавершение",
		ЭтотОбъект, Новый Структура("Форма", ЭтотОбъект));

	ЗакрытиеМесяцаКлиент.ЗапуститьПроверкуАктуальностиДляПодсказки(ЭтотОбъект, ОповещениеОЗавершении, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаАктуальностиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
	 ИЛИ ДополнительныеПараметры.ИдентификаторЗадания <> ИдентификаторЗаданияПроверкиАктуальности Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.РисПриАктуализации.Видимость = Ложь;
	
	// Обработаем результат проверки актуальности и оформим подсказку
	РезультатПроверкиАктуальности = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ОбновитьПодсказкуСледующегоЗапуска();
	
КонецПроцедуры

#КонецОбласти

#Область ЧтениеЗаписьНастроек

&НаСервере
Процедура ПрочитатьНастройкиРегламентногоЗадания()
	
	Модифицированность = Ложь;
	
	// Чтение настроек регламентного задания
	НастройкиЗадания = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.НастройкиРегламентногоЗадания(Организация);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиЗадания, , "Организация");
	
	// Определение значений для реквизитов формы
	ПериодичностьЗапуска = ?(НастройкиЗадания.Ежедневно, "Ежедневно", "ВОтдельныеДни");
	ВариантЗакрытияМесяца = ?(НастройкиЗадания.ТолькоПерепроведениеДокументов,
		"ТолькоПерепроведениеДокументов", "ПолноеЗакрытиеМесяца");
	ВариантПериодаЗакрытияМесяца = ?(НастройкиЗадания.ВыполнятьВОтчетномПериоде,
		"ОтчетныйПериод", "ТолькоПредыдущиеМесяцы");
	
	Если Не Организация.Пустая() Тогда
		ОбновитьСостояниеРегламентногоЗадания();
	КонецЕсли;
	
	ИзменитьВидимостьИДоступностьНастроек();
	ОбновитьПодсказкуРазрешенногоПериода(Элементы.ГруппаГоризонтальнаяСПодсказкой.Подсказка, РазрешенноеЧислоМесяцевНазад);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиРегламентногоЗадания()
	
	НачатьТранзакцию();
	
	Попытка 
		
		// Заблокируем регистр с настройками на время записи настроек и регламентного задания
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация = Организация;
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЭтотОбъект);
		МенеджерЗаписи.Ежедневно = ПериодичностьЗапуска = "Ежедневно";
		МенеджерЗаписи.ВыполнятьВОтчетномПериоде = ВариантПериодаЗакрытияМесяца = "ОтчетныйПериод";
		МенеджерЗаписи.ТолькоПерепроведениеДокументов = ВариантЗакрытияМесяца = "ТолькоПерепроведениеДокументов";
		
		УдалитьЗадание = Ложь;
		
		Если Не ОбщегоНазначения.РазделениеВключено() Тогда
			Если ВыполнениеВключено Тогда // Запишем регламентное задание
				
				ПараметрыПроверкиЗапуска = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыБлижайшейДатыЗапускаЗадания();
				ЗаполнитьЗначенияСвойств(ПараметрыПроверкиЗапуска, МенеджерЗаписи);
				МоментЗапуска = ЗакрытиеМесяцаВызовСервера.БлижайшаяДатаЗапускаРегламентногоЗадания(ПараметрыПроверкиЗапуска);
				
				ДатаСервера = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
				Если ДатаСервера > МоментЗапуска Тогда
					ДатаНачала = КонецДня(ДатаСервера) + 1;
				Иначе
					ДатаНачала = МоментЗапуска;
				КонецЕсли;
				
				ПараметрыЗаписиЗадания = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыЗаписиРегламентногоЗаданияЗакрытияМесяца();
				ПараметрыЗаписиЗадания.Организация          = Организация;
				ПараметрыЗаписиЗадания.ВремяЗапуска         = ВремяЗапуска;
				ПараметрыЗаписиЗадания.ИдентификаторЗадания = ИдентификаторЗадания;
				ПараметрыЗаписиЗадания.ДатаНачала           = НачалоДня(ДатаНачала);
				Задание = ЗакрытиеМесяцаВызовСервера.ЗаписатьРегламентноеЗаданиеЗакрытияМесяца(ПараметрыЗаписиЗадания);
					
				// Обновим идентификатор задания, на случай если он изменился.	
				ИдентификаторЗадания = Задание.УникальныйИдентификатор;
				МенеджерЗаписи.ИдентификаторЗадания = ИдентификаторЗадания;
			Иначе
				УдалитьЗадание = Истина;
			КонецЕсли;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);

		Если УдалитьЗадание Тогда
			РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторЗадания);
		КонецЕсли;
		МенеджерЗаписи.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ИнформацияОбОшибке;
	КонецПопытки;
	
	ЗакрытиеМесяцаВызовСервера.ЗапланироватьАвтоматическоеЗакрытиеМесяцаДляОбластиДанных();
	
	Модифицированность = Ложь;
	
КонецПроцедуры 

&НаСервере
Процедура ОбновитьСостояниеРегламентногоЗадания()
	
	Элементы.ДекорацияОшибкиВыполненияОпераций.Видимость = Ложь;
	Элементы.ГруппаСостояние.ЦветФона = Новый Цвет;
	
	// Проверим не выполняется-ли регламентное задание прямо сейчас.
	ВыполняемоеЗадание = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(
		Организация, Новый УникальныйИдентификатор, Ложь);
		
	Если ВыполняемоеЗадание.УникальныйИдентификатор <> Неопределено
	 И ЗакрытиеМесяцаКлиентСервер.ЭтоКлючРегламентногоЗаданияЗакрытияМесяца(ВыполняемоеЗадание.Ключ) Тогда
		Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Выполняется прямо сейчас.'");
		Возврат;
	КонецЕсли;
	
	ПоследнееЗакрытие = РегистрыСведений.РезультатыЗакрытияМесяца.РезультатПоследнегоЗакрытияМесяца(Организация);
	Если Не ЗначениеЗаполнено(ПоследнееЗакрытие.ДатаВыполнения) Тогда
		Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Автоматическое закрытие месяца не выполнялось'");
	Иначе
	    ШаблонТекста = НСтр("ru = 'Последнее выполнение %1, %2'");
		Если ПоследнееЗакрытие.Выполнено Тогда
			Состояние = НСтр("ru = 'завершено'");
		Иначе
			Состояние = НСтр("ru = 'не выполнено'");
		КонецЕсли;
		Элементы.ДекорацияСостояние.Заголовок =
			СтрШаблон(ШаблонТекста, Формат(ПоследнееЗакрытие.ДатаВыполнения, НСтр("ru = 'ДЛФ=DT'")), Состояние);
	КонецЕсли;
	
	Если Не ПоследнееЗакрытие.Выполнено И ЗначениеЗаполнено(ПоследнееЗакрытие.ДатаВыполнения) Тогда
		Элементы.ГруппаСостояние.ЦветФона = ЗакрытиеМесяцаВызовСервера.ПолучитьПредупреждающийЦвет();
		Элементы.ДекорацияОшибкиВыполненияОпераций.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНастроек(ЗакрытиеФормы = Ложь)
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, "Да");
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	
	ШаблонВопроса = НСтр("ru = 'Настройки для %1 не были сохранены.
							   |Продолжить без сохранения?'");
	
	Оповещение = Новый ОписаниеОповещения("ВопросОСохраненииНастроекПродолжение", ЭтотОбъект, ЗакрытиеФормы);
	ПоказатьВопрос(Оповещение, СтрШаблон(ШаблонВопроса, Организация), Кнопки, , КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНастроекПродолжение(Ответ, ЗакрытиеФормы) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
        Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если ЗакрытиеФормы Тогда
		Закрыть();
	Иначе
		ПрочитатьНастройкиРегламентногоЗадания();
		Оповещение = Новый ОписаниеОповещения("ВыборОрганизацииИзСпискаПродолжение", ЭтотОбъект, ЗакрытиеФормы);
		ПоказатьВыборИзСписка(Оповещение, Элементы.ПолеОрганизация.СписокВыбора, Элементы.ПолеОрганизация); 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыборОрганизацииИзСпискаПродолжение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	ПолеОрганизация = ВыбранныйЭлемент.Значение;
	ОрганизацияПриИзменении(Неопределено);
	ОрганизацияОбработкаВыбора(Неопределено, ВыбранныйЭлемент.Значение, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
