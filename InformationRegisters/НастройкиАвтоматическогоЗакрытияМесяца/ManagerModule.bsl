#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройки регламентного задания по организации из регистра сведений.
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - организация для которой требуется прочитать настройки.
// 
// Возвращаемое значение:
//  Структура - см. ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыЗаданияЗакрытияМесяца(). 
//
Функция НастройкиРегламентногоЗадания(Организация) Экспорт
	
	Результат = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыЗаданияЗакрытияМесяца();
	Результат.Организация = Организация;
	
	// Для настроек по умолчанию предложим оптимальное время для запуска.
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Результат.ВремяЗапуска = '00010101153000'; // 15:30.
	Иначе
		Результат.ВремяЗапуска = '00010101023000'; // 02:30.
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца КАК Настройки
	|ГДЕ
	|	Настройки.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	МоментЗапуска = ЗакрытиеМесяцаВызовСервера.БлижайшаяДатаЗапускаРегламентногоЗадания(Результат);
	ДатаСервера = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	Если ДатаСервера > МоментЗапуска Тогда
		Результат.ДатаНачала = КонецДня(ДатаСервера) + 1;
	Иначе
		Результат.ДатаНачала = НачалоДня(МоментЗапуска);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает настройки всех регламентных заданий согласно текущего режима автоматического закрытия месяца.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица значений соответствующая структуре регистра сведений.
//
Функция ВсеНастройкиРегламентныхЗаданий() Экспорт
	
	ПоВсем = ТекущийРежимАвтоматическогоЗакрытияМесяца() = "ПоВсемОрганизациям";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПоВсем", ПоВсем);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	*
	|ИЗ
	|	РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца КАК Настройки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ПоВсем
	|				ТОГДА Настройки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИНАЧЕ Настройки.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		КОНЕЦ";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Переключает режим автоматического закрытия месяца.
//
// Параметры:
//  Режим - Строка - возможные варианты - "ПоВсемОрганизациям", "ПоОтдельнымОрганизациям", "Отключено".
//
Процедура ПереключитьРежимАвтоматическогоЗакрытияМесяца(Режим) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца КАК Настройки";
	
	НачатьТранзакцию();
	
	Попытка
		
		// Заблокируем весь регистр с настройками на время записи настроек и регламентного задания.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.ВыполнениеВключено = Ложь;
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОднаОрганизация = Не Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
		ВноситьЗаписьОВключенииНастроек = Режим = "ПоВсемОрганизациям" Или ОднаОрганизация;
		
		Если Режим <> "Отключено" И ВноситьЗаписьОВключенииНастроек Тогда
			Если ОднаОрганизация Тогда
				Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
			Иначе
				Организация = Справочники.Организации.ПустаяСсылка();
			КонецЕсли;
			Настройки = НастройкиРегламентногоЗадания(Организация);
			МенеджерЗаписи = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Настройки);
			МенеджерЗаписи.ВыполнениеВключено = Истина;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
    	ОтменитьТранзакцию();
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Настройки автоматического закрытия месяца, переключение режима.'"),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца,
				Организация,
				ИнформацияОбОшибке);
		
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
	// После изменения режима требуется пересоздание всех регламентных заданий.
	ПересоздатьРегламентныеЗаданияЗакрытияМесяца();
	
КонецПроцедуры

// Возвращает текущий режим автоматического закрытия месяца.
// 
// Возвращаемое значение:
//  Строка - возможные варианты - "ПоВсемОрганизациям", "ПоОтдельнымОрганизациям", "Отключено".
//
Функция ТекущийРежимАвтоматическогоЗакрытияМесяца() Экспорт	
	
	Режим = "Отключено";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Настройки.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца КАК Настройки
	|ГДЕ
	|	Настройки.ВыполнениеВключено
	|	И Настройки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗпроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗпроса.Пустой() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "=", "<>");
		РезультатЗпроса = Запрос.Выполнить();
		Если НЕ РезультатЗпроса.Пустой() Тогда
			Режим = "ПоОтдельнымОрганизациям";
		КонецЕсли;
	Иначе
		Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
			Режим = "ПоВсемОрганизациям";
		Иначе
			Режим = "Отключено";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Режим;
	
КонецФункции

// Отключает автоматическое закрытие месяца, если оно настроено для конкретной организации.
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - организация, для которой требуется отключить автоматическое закрытие месяца.
//
Процедура ОтключитьАвтоматическоеЗакрытиеМесяцаПоОрганизации(Организация) Экспорт
	
	Если ТекущийРежимАвтоматическогоЗакрытияМесяца() <> "ПоОтдельнымОрганизациям" Тогда
		Возврат;
	КонецЕсли;
	
	ВнешняяТранзакция = ТранзакцияАктивна();
	Если Не ВнешняяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация = Организация;
		УстановитьПривилегированныйРежим(Истина);
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			Возврат;
		КонецЕсли;
		МенеджерЗаписи.ВыполнениеВключено = Ложь;
		
		МенеджерЗаписи.Записать();
		РегламентныеЗаданияСервер.УдалитьЗадание(МенеджерЗаписи.ИдентификаторЗадания);
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Не ВнешняяТранзакция Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		Если Не ВнешняяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Отключение автоматического закрытия месяца по организации.'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца,
				Организация,
				ИнформацияОбОшибке);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Удаляет все регламентные задания автоматического закрытия месяца, и создает их заново, согласно текущего режима.
//
Процедура ПересоздатьРегламентныеЗаданияЗакрытияМесяца()
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		// В модели сервиса регламентные задания не создаются.
		ЗакрытиеМесяцаВызовСервера.ЗапланироватьАвтоматическоеЗакрытиеМесяцаДляОбластиДанных();
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.АвтоматическоеЗакрытиеМесяца);
	
	ВсеЗаданияЗакрытияМесяца = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	Для Каждого Задание Из ВсеЗаданияЗакрытияМесяца Цикл
		РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
	КонецЦикла;
	
	ТекущийРежим = ТекущийРежимАвтоматическогоЗакрытияМесяца();
	Если ТекущийРежим = "Отключено" Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();

	Попытка
		
		// Заблокируем регистр с настройками на время записи регламентных заданий.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиАвтоматическогоЗакрытияМесяца");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Организация = Справочники.Организации.ПустаяСсылка();
		
		Если ТекущийРежим = "ПоВсемОрганизациям" Тогда
			
			Настройки = НастройкиРегламентногоЗадания(Организация);
			Если Не Настройки.ВыполнениеВключено Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			
			МенеджерЗаписи = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Организация = Организация;
			МенеджерЗаписи.Прочитать();
			
			ПараметрыПроверкиЗапуска = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыБлижайшейДатыЗапускаЗадания();
			ЗаполнитьЗначенияСвойств(ПараметрыПроверкиЗапуска, МенеджерЗаписи);
			
			ПараметрыЗаписи = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыЗаписиРегламентногоЗаданияЗакрытияМесяца();
			ПараметрыЗаписи.ВремяЗапуска         = Настройки.ВремяЗапуска;
			ПараметрыЗаписи.ИдентификаторЗадания = Настройки.ИдентификаторЗадания;
			ПараметрыЗаписи.ДатаНачала           = ДатаНачалаЗапускаРегламентногоЗадания(ПараметрыПроверкиЗапуска);
			Задание = ЗакрытиеМесяцаВызовСервера.ЗаписатьРегламентноеЗаданиеЗакрытияМесяца(ПараметрыЗаписи);
			
			МенеджерЗаписи.ИдентификаторЗадания = Задание.УникальныйИдентификатор;
			МенеджерЗаписи.Записать();
			
		ИначеЕсли ТекущийРежим = "ПоОтдельнымОрганизациям" Тогда
			
			СписокОрганизаций = БухгалтерскийУчетПереопределяемый.ПолучитьСписокГоловныхОрганизаций();
			
			Для Каждого ЭлементСписка Из СписокОрганизаций Цикл
				Организация = ЭлементСписка.Значение;
				Настройки = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.НастройкиРегламентногоЗадания(Организация);
				Если Не Настройки.ВыполнениеВключено Тогда
					Продолжить;
				КонецЕсли;
				
				ПараметрыПроверкиЗапуска = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыБлижайшейДатыЗапускаЗадания();
				ЗаполнитьЗначенияСвойств(ПараметрыПроверкиЗапуска, Настройки);
				
				ПараметрыЗаписи = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыЗаписиРегламентногоЗаданияЗакрытияМесяца();
				ПараметрыЗаписи.Организация          = Организация;
				ПараметрыЗаписи.ВремяЗапуска         = Настройки.ВремяЗапуска;
				ПараметрыЗаписи.ИдентификаторЗадания = Настройки.ИдентификаторЗадания;
				ПараметрыЗаписи.ДатаНачала           = ДатаНачалаЗапускаРегламентногоЗадания(ПараметрыПроверкиЗапуска);
				Задание = ЗакрытиеМесяцаВызовСервера.ЗаписатьРегламентноеЗаданиеЗакрытияМесяца(ПараметрыЗаписи);
				
				МенеджерЗаписи = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.Организация = Организация;
				МенеджерЗаписи.Прочитать();
				МенеджерЗаписи.ИдентификаторЗадания = Задание.УникальныйИдентификатор;
				МенеджерЗаписи.Записать();
			КонецЦикла;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Настройки автоматического закрытия месяца, пересоздание регламентных заданий.'"),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца,
				Организация,
				ИнформацияОбОшибке);
		
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
КонецПроцедуры

Функция ДатаНачалаЗапускаРегламентногоЗадания(Параметры)
	
	МоментЗапуска = ЗакрытиеМесяцаВызовСервера.БлижайшаяДатаЗапускаРегламентногоЗадания(Параметры);
	
	ДатаСервера = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	Если ДатаСервера > МоментЗапуска Тогда
		ДатаНачала = КонецДня(ДатаСервера) + 1;
	Иначе
		ДатаНачала = МоментЗапуска;
	КонецЕсли;
	
	Возврат НачалоДня(ДатаНачала);
	
КонецФункции

#КонецОбласти

#КонецЕсли
