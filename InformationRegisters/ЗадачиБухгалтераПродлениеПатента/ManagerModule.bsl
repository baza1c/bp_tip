#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет в набор записей по правилу новое заявление.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация
//  Заявление - ДокументСсылка.УведомлениеОСпецрежимахНалогообложения - Заявление на получение патента
//  Правило - СправочникСсылка.Патенты - Правило задачи
//  ПериодСобытия - Дата - Период события задачи
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане, Неопределено - Регистрация в налоговом органе
// 
// Возвращаемое значение:
//  Булево - Если добавление не нужно, то возвращается Ложь, иначе Истина
Функция ДобавитьЗаписьПравила(Организация, Заявление, Правило, ПериодСобытия, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	ДанныеЗаполнения = НовыйСтруктураРегистра();
	СтруктураЗаписи = НовыйСтруктураРегистра();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Заявление.Установить(Заявление);
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		ЗаписьПравила = НаборЗаписей[0];
	Иначе
		ЗаписьПравила = НаборЗаписей.Добавить();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		СтруктураЗаписи,
		ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(ЗаписьПравила, Метаданные.РегистрыСведений.ЗадачиБухгалтераПродлениеПатента));
	
	ДанныеЗаполнения.Вставить("Организация",                 Организация);
	ДанныеЗаполнения.Вставить("Заявление",                   Заявление);
	ДанныеЗаполнения.Вставить("Правило",                     Правило);
	ДанныеЗаполнения.Вставить("ПериодСобытия",               ПериодСобытия);
	ДанныеЗаполнения.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	ЗаписьИзменена = Ложь;
	
	Если Не ОбщегоНазначения.ДанныеСовпадают(СтруктураЗаписи, ДанныеЗаполнения) Тогда
		
		ЗаполнитьЗначенияСвойств(ЗаписьПравила, ДанныеЗаполнения);
		
		НаборЗаписей.Записать();
		
		ЗаписьИзменена = Истина;
	
	КонецЕсли;
	
	Возврат ЗаписьИзменена;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеЗадачиПоЗаявлению(Организация, Заявление) Экспорт
	
	Результат = НовыйСтруктураРегистра();
	Если Не ЗначениеЗаполнено(Заявление) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Заявление", Заявление);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтераПродлениеПатента.Организация КАК Организация,
	|	ЗадачиБухгалтераПродлениеПатента.Заявление КАК Заявление,
	|	ЗадачиБухгалтераПродлениеПатента.Правило КАК Правило,
	|	ЗадачиБухгалтераПродлениеПатента.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтераПродлениеПатента.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераПродлениеПатента КАК ЗадачиБухгалтераПродлениеПатента
	|ГДЕ
	|	ЗадачиБухгалтераПродлениеПатента.Организация = &Организация
	|	И ЗадачиБухгалтераПродлениеПатента.Заявление = &Заявление";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйСтруктураРегистра()

	Возврат Новый Структура("Организация, Заявление, Правило, ПериодСобытия, РегистрацияВНалоговомОргане");

КонецФункции 

#КонецОбласти

#КонецЕсли