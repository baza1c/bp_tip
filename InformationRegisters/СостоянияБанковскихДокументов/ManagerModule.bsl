#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура УстановитьСостояниеДокумента(Документ, СостояниеДокумента, ПереходСРедакции20 = Ложь, РучноеИзменение = Ложь) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПлатежноеПоручение")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.ПлатежноеТребование") Тогда
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Организация, Проведен, ПометкаУдаления");
		Ссылка			 = Документ;
		Организация		 = РеквизитыДокумента.Организация;
		ДокументПроведен = РеквизитыДокумента.Проведен;
		ПометкаУдаления  = РеквизитыДокумента.ПометкаУдаления;
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.ПлатежноеПоручение")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументОбъект.ПлатежноеТребование") Тогда
		
		Ссылка			 = Документ.Ссылка;
		ДокументПроведен = Документ.Проведен;
		Организация		 = Документ.Организация;
		ПометкаУдаления  = Документ.ПометкаУдаления;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		УдалитьЗапись(Ссылка, Организация);
		ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиПлатежа(Ссылка, ПереходСРедакции20);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СостояниеДокумента) = Тип("Строка") Тогда
		
		Если СостояниеДокумента = "Подготовлено" Тогда
			
			СостояниеДокумента = Перечисления.СостоянияБанковскихДокументов.Подготовлено;
			
		ИначеЕсли СостояниеДокумента = "НаПодписи" Тогда
			
			СостояниеДокумента = Перечисления.СостоянияБанковскихДокументов.НаПодписи;
			
		ИначеЕсли СостояниеДокумента = "Отклонено" Тогда
			
			СостояниеДокумента = Перечисления.СостоянияБанковскихДокументов.Отклонено;
			
		ИначеЕсли СостояниеДокумента = "Отправлено" Тогда
			
			СостояниеДокумента = Перечисления.СостоянияБанковскихДокументов.Отправлено;
			
		Иначе
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаНаОбъект", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияБанковскихДокументов.Организация,
	|	СостоянияБанковскихДокументов.СсылкаНаОбъект,
	|	СостоянияБанковскихДокументов.Состояние
	|ПОМЕСТИТЬ ВТСостоянияБанковскихДокументов
	|ИЗ
	|	РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|ГДЕ
	|	СостоянияБанковскихДокументов.СсылкаНаОбъект = &СсылкаНаОбъект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСостоянияБанковскихДокументов.Организация,
	|	ВТСостоянияБанковскихДокументов.СсылкаНаОбъект
	|ИЗ
	|	ВТСостоянияБанковскихДокументов КАК ВТСостоянияБанковскихДокументов
	|ГДЕ
	|	ВТСостоянияБанковскихДокументов.Организация <> &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСостоянияБанковскихДокументов.Организация,
	|	ВТСостоянияБанковскихДокументов.СсылкаНаОбъект,
	|	ВТСостоянияБанковскихДокументов.Состояние
	|ИЗ
	|	ВТСостоянияБанковскихДокументов КАК ВТСостоянияБанковскихДокументов
	|ГДЕ
	|	ВТСостоянияБанковскихДокументов.Организация = &Организация";
	
	Результат = Запрос.ВыполнитьПакет();
	
	// Удаляем запись регистра, если в уже проведенном документе изменили организацию.
	Если Не Результат[1].Пустой() Тогда
		
		Выборка = Результат[1].Выбрать();
		Выборка.Следующий();
		
		УдалитьЗапись(Выборка.СсылкаНаОбъект, Выборка.Организация);
		
	КонецЕсли;
	
	Если Не Результат[2].Пустой() Тогда // уже есть записи в регистре сведений
		
		Выборка = Результат[2].Выбрать();
		Выборка.Следующий();
		Если Выборка.Состояние <> СостояниеДокумента И СостояниеДокумента <> Неопределено Тогда // если состояние не изменилось, регистр не перезаписываем
			
			ЗаписатьСостояние(Ссылка, Организация, СостояниеДокумента, ПереходСРедакции20, РучноеИзменение);
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(СостояниеДокумента) Тогда // еще нет записей в регистре, записываем, если состояние указано
		
		ЗаписатьСостояние(Ссылка, Организация, СостояниеДокумента, ПереходСРедакции20, РучноеИзменение);
		
	ИначеЕсли ДокументПроведен Тогда // если документ проведен, устанавливаем состояние "Подготовлено"
		
		ЗаписатьСостояние(Ссылка, Организация, Перечисления.СостоянияБанковскихДокументов.Подготовлено, ПереходСРедакции20, РучноеИзменение);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекущееСостояниеДокумента(ДокументСсылка) Экспорт
	
	Если ДокументСсылка.Пустая() Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Результат = Неопределено;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаНаОбъект", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияБанковскихДокументов.Состояние
	|ИЗ
	|	РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|ГДЕ
	|	СостоянияБанковскихДокументов.СсылкаНаОбъект = &СсылкаНаОбъект";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Результат = Выборка.Состояние;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция СостояниеДокумента(ДокументСсылка) Экспорт
	
	Если ДокументСсылка.Пустая() Тогда 	
		Возврат Неопределено;      	
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаНаОбъект", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияБанковскихДокументов.Состояние КАК Состояние,
	|	СостоянияБанковскихДокументов.Ответственный КАК Ответственный,
	|	СостоянияБанковскихДокументов.Согласовано КАК Согласовано
	|ИЗ
	|	РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|ГДЕ
	|	СостоянияБанковскихДокументов.СсылкаНаОбъект = &СсылкаНаОбъект";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Неопределено;
	Если Выборка.Следующий() Тогда
		Результат = Новый Структура; 
		Результат.Вставить("Состояние", Выборка.Состояние);
		Результат.Вставить("Ответственный", Выборка.Ответственный); 
		Результат.Вставить("Согласовано", Выборка.Согласовано); 
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция СписокСостоянийНеСогласовано() Экспорт
	
	СписокСостоянийНеСогласовано = Новый Массив;
	СписокСостоянийНеСогласовано.Добавить(Перечисления.СостоянияБанковскихДокументов.Подготовлено);
	СписокСостоянийНеСогласовано.Добавить(Перечисления.СостоянияБанковскихДокументов.Отклонено);
	
	Возврат СписокСостоянийНеСогласовано;
	
КонецФункции

Процедура ИзменитьСостояниеСогласованияДокумента(ДокументСсылка, НовоеСостояние) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СсылкаНаОбъект.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СписокРедактируемыхСостояний = Новый Массив; 
	СписокРедактируемыхСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.ПустаяСсылка());
	СписокРедактируемыхСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.Подготовлено);
	СписокРедактируемыхСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.Отклонено);
	СписокРедактируемыхСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.Согласовано);
	
	ТекущееСостояние = НаборЗаписей[0].Состояние;
	
	Если СписокРедактируемыхСостояний.Найти(ТекущееСостояние) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СостоянияНеСогласовано = СписокСостоянийНеСогласовано();
	
	НаборЗаписей[0].Состояние = НовоеСостояние;
	НаборЗаписей[0].Ответственный = Пользователи.ТекущийПользователь();
	НаборЗаписей[0].Согласовано = СостоянияНеСогласовано.Найти(НовоеСостояние) = Неопределено;
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьСостоянияБанковскихДокументов() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПлатежноеПоручение.Ссылка,
	|	ПлатежноеПоручение.Проведен,
	|	ПлатежноеПоручение.Организация,
	|	ПлатежноеПоручение.ДокументОснование,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, НЕОПРЕДЕЛЕНО) КАК Состояние
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО ПлатежноеПоручение.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПлатежноеТребование.Ссылка,
	|	ПлатежноеТребование.Проведен,
	|	ПлатежноеТребование.Организация,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, НЕОПРЕДЕЛЕНО) КАК Состояние
	|ИЗ
	|	Документ.ПлатежноеТребование КАК ПлатежноеТребование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО ПлатежноеТребование.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// Установка состояний платежных поручений
	Выборка = РезультатЗапроса[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Состояние) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		СписаниеСРасчетногоСчета = Документы.ПлатежноеПоручение.НайтиДокументСписания(
			Выборка.Ссылка, Выборка.ДокументОснование);
			
		Если ЗначениеЗаполнено(СписаниеСРасчетногоСчета)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СписаниеСРасчетногоСчета, "Проведен") Тогда
			
			УстановитьСостояниеДокумента(Выборка.Ссылка, Перечисления.СостоянияБанковскихДокументов.Оплачено, Истина);
			
		ИначеЕсли Выборка.Проведен Тогда
			
			УстановитьСостояниеДокумента(Выборка.Ссылка, Перечисления.СостоянияБанковскихДокументов.Подготовлено, Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗапросПоступленияНаСчет = Новый Запрос;
	ЗапросПоступленияНаСчет.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ПоступлениеНаРасчетныйСчет.Ссылка
	|ИЗ
	|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
	|ГДЕ
	|	ПоступлениеНаРасчетныйСчет.ДокументОснование = &ДокументОснование
	|	И ПоступлениеНаРасчетныйСчет.Проведен";
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Состояние) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ЗапросПоступленияНаСчет.УстановитьПараметр("ДокументОснование", Выборка.Ссылка);
		ВыборкаПоступления = ЗапросПоступленияНаСчет.Выполнить().Выбрать();
		
		Если ВыборкаПоступления.Следующий() Тогда
			
			УстановитьСостояниеДокумента(Выборка.Ссылка, Перечисления.СостоянияБанковскихДокументов.Оплачено, Истина);
			
		ИначеЕсли Выборка.Проведен Тогда
			
			УстановитьСостояниеДокумента(Выборка.Ссылка, Перечисления.СостоянияБанковскихДокументов.Подготовлено, Истина);
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьСостояние(СсылкаНаОбъект, Организация, СостояниеДокумента, ПереходСРедакции20, РучноеИзменение = Ложь)
	
	НаборЗаписей = РегистрыСведений.СостоянияБанковскихДокументов.СоздатьНаборЗаписей();   
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.СсылкаНаОбъект.Установить(СсылкаНаОбъект);
	НаборЗаписей.Прочитать(); 
	
	Если НаборЗаписей.Количество() = 0 Тогда      
		ЗаписьРегистра = НаборЗаписей.Добавить();
		ЗаписьРегистра.Организация    = Организация; 
		ЗаписьРегистра.СсылкаНаОбъект = СсылкаНаОбъект; 
	Иначе
		ЗаписьРегистра = НаборЗаписей[0];
	КонецЕсли;  	
 
	ЗаписьРегистра.Состояние  = СостояниеДокумента;  
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСогласованиеПлатежей") И РучноеИзменение Тогда  
		СостоянияНеСогласовано = СписокСостоянийНеСогласовано();  
		ЗаписьРегистра.Согласовано = СостоянияНеСогласовано.Найти(СостояниеДокумента) = Неопределено;
		ЗаписьРегистра.Ответственный = Пользователи.ТекущийПользователь(); 
	КонецЕсли;    
	
	НаборЗаписей.Записать();
	
	ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиПлатежа(СсылкаНаОбъект, ПереходСРедакции20);
	
	Если СостояниеДокумента = Перечисления.СостоянияБанковскихДокументов.Оплачено Тогда
		РегистрыСведений.ДокументыИнтеграцииСБанком.УстановитьСостояниеОбменЗавершен(СсылкаНаОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиПлатежа(СсылкаНаОбъект, ПереходСРедакции20)
	
	Если ПереходСРедакции20 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРасчетаСтатусов = Новый Структура;
	ПараметрыРасчетаСтатусов.Вставить("ПропуститьОбновлениеСвязиДокументЗадача", Истина);
	ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиПлатежа(
		СсылкаНаОбъект, ПараметрыРасчетаСтатусов);
	
КонецПроцедуры
	
Процедура УдалитьЗапись(СсылкаНаОбъект, Организация)
	
	ЗаписьРегистра = СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Организация = Организация;
	ЗаписьРегистра.СсылкаНаОбъект = СсылкаНаОбъект;
	ЗаписьРегистра.Удалить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли