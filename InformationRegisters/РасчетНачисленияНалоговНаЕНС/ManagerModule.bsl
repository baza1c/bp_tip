#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаменитьСчетУчетаНДФЛ() Экспорт
	
	ИмяПроцедуры = "РегистрыСведений.РасчетНачисленияНалоговНаЕНС.ЗаменитьСчетУчетаНДФЛ()";
	
	ИсключаемыйСчет_Агент = ПланыСчетов.Хозрасчетный.УдалитьНДФЛ_ДоходыСвышеПредельнойВеличины;
	ИсключаемыйСчет_ИП    = ПланыСчетов.Хозрасчетный.УдалитьНДФЛ_ИП_НалоговаяБазаСвышеПредельнойВеличины;
	
	СчетаУчета = Новый Массив;
	СчетаУчета.Добавить(ИсключаемыйСчет_Агент);
	СчетаУчета.Добавить(ИсключаемыйСчет_ИП);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНачисленияНалоговНаЕНС.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.РасчетНачисленияНалоговНаЕНС КАК РасчетНачисленияНалоговНаЕНС
	|ГДЕ
	|	РасчетНачисленияНалоговНаЕНС.СчетУчета В(&СчетаУчета)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	КолонкиСвертки = "";
	Измерения = Метаданные.РегистрыСведений.РасчетНачисленияНалоговНаЕНС.Измерения;
	Для Каждого Измерение Из Измерения Цикл
		КолонкиСвертки = КолонкиСвертки + ?(ПустаяСтрока(КолонкиСвертки), "", ",") + Измерение.Имя;
	КонецЦикла;
	Реквизиты = Метаданные.РегистрыСведений.РасчетНачисленияНалоговНаЕНС.Реквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		КолонкиСвертки = КолонкиСвертки + ?(ПустаяСтрока(КолонкиСвертки), "", ",") + Реквизит.Имя;
	КонецЦикла;
	КолонкиСвертки = КолонкиСвертки + ",Период";
	
	КолонкиСуммирования = "";
	Ресурсы = Метаданные.РегистрыСведений.РасчетНачисленияНалоговНаЕНС.Ресурсы;
	Для Каждого Ресурс Из Ресурсы Цикл
		КолонкиСуммирования = КолонкиСуммирования + ?(ПустаяСтрока(КолонкиСуммирования), "", ",") + Ресурс.Имя;
	КонецЦикла;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			НачатьТранзакцию();
			
			НаборЗаписей = РегистрыСведений.РасчетНачисленияНалоговНаЕНС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Прочитать();
			ТаблицаДвижений = НаборЗаписей.Выгрузить();
			МассивСтрок = ТаблицаДвижений.НайтиСтроки(Новый Структура("СчетУчета", ИсключаемыйСчет_Агент));
			Для Каждого СтрокаМассива Из МассивСтрок Цикл
				СтрокаМассива.СчетУчета = ПланыСчетов.Хозрасчетный.НДФЛ;
			КонецЦикла;
			МассивСтрок = ТаблицаДвижений.НайтиСтроки(Новый Структура("СчетУчета", ИсключаемыйСчет_ИП));
			Для Каждого СтрокаМассива Из МассивСтрок Цикл
				СтрокаМассива.СчетУчета = ПланыСчетов.Хозрасчетный.НДФЛ_ИП;
			КонецЦикла;
			
			ТаблицаДвижений.Свернуть(КолонкиСвертки, КолонкиСуммирования);
			НаборЗаписей.Загрузить(ТаблицаДвижений);
		
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В процедуре %1 не удалось заменить счет учета НДФЛ с сумм превышения предельной величины по причине:
					|%2'"),
					ИмяПроцедуры,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.РасчетНачисленияНалоговНаЕНС,
				,
				ТекстСообщения);
		КонецПопытки
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПустойПериод() Экспорт
	
	ИмяПроцедуры = "РегистрыСведений.РасчетНачисленияНалоговНаЕНС.ЗаполнитьПустойПериод()";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНачисленияНалоговНаЕНС.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.РасчетНачисленияНалоговНаЕНС КАК РасчетНачисленияНалоговНаЕНС
	|ГДЕ
	|	РасчетНачисленияНалоговНаЕНС.Период = ДАТАВРЕМЯ(1, 1, 1)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРегистраторов = Результат.Выгрузить();
	
	МассивРегистраторов = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаРегистраторов, "Регистратор", Истина);
	РеквизитыРегистраторов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивРегистраторов, "Дата");
	
	Для Каждого Регистратор Из МассивРегистраторов Цикл
		
		Попытка
			НачатьТранзакцию();
			
			НаборЗаписей = РегистрыСведений.РасчетНачисленияНалоговНаЕНС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
			НаборЗаписей.Прочитать();
			ТаблицаДвижений = НаборЗаписей.Выгрузить();
			ТаблицаДвижений.ЗаполнитьЗначения(РеквизитыРегистраторов[Регистратор], "Период");
			НаборЗаписей.Загрузить(ТаблицаДвижений);
		
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В процедуре %1 не удалось заполнить период по причине:
					|%2'"),
					ИмяПроцедуры,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.РасчетНачисленияНалоговНаЕНС,
				,
				ТекстСообщения);
		КонецПопытки
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
