#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриИзмененииПериода(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Организация",
		"ТолькоПросмотр",
		Параметры.КонтекстныйВызов);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ОтчетныйПериод",
		"ТолькоПросмотр",
		Параметры.КонтекстныйВызов);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Не УчетСтраховыхВзносовИППовтИсп.УпрощенныйЗачетФиксированныхВзносов(Запись.Организация, Запись.Период) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'В текущем периоде нельзя использовать взносы, подлежащие уплате.'"), , , , Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Запись.Организация);
	ПараметрыОповещения.Вставить("Период", Запись.Период);
	
	Оповестить("Запись_УменьшениеУСННаФиксированныеВзносы", ПараметрыОповещения, ЭтотОбъект);
	Оповестить("Запись_УменьшениеПСННаФиксированныеВзносы", ПараметрыОповещения, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не Запись.ДеятельностьНаПатенте Тогда
		УСНДоходы = УчетнаяПолитика.ПрименяетсяУСНДоходыЗаПериод(
			Запись.Организация, НачалоГода(Запись.Период), КонецГода(Запись.Период));
		РегистрыСведений.УменьшениеНалогаНаВзносыПодлежащиеУплате.СдвинутьГраницуАктуальности(
			Запись.Организация, Запись.Период, УСНДоходы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтчетныйПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачалоПериода = НачалоГода(Запись.Период);
	КонецПериода  = КонецКвартала(Запись.Период);
	
	ПараметрыВыбора = Новый Структура;
	
	ПараметрыВыбора.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыВыбора.Вставить("КонецПериода",  КонецПериода);
	ПараметрыВыбора.Вставить("НарастающимИтогом", Истина);
	ПараметрыВыбора.Вставить("МинимальныйПериод", МинимальныйПериод(Запись.Организация));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаКвартал",
		ПараметрыВыбора, , , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетныйПериодОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьНаПСНПриИзменении(Элемент)
	// Записи по патентам делаем на конец года
	Если Запись.ДеятельностьНаПатенте Тогда
		Запись.Период = КонецГода(Запись.Период);
		ПриИзмененииПериода(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Запись.ДеятельностьНаПатенте Тогда
		Запись.Период = КонецГода(РезультатВыбора.КонецПериода);
	Иначе
		Запись.Период = КонецКвартала(РезультатВыбора.КонецПериода);
	КонецЕсли;
	
	ПриИзмененииПериода(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииПериода(Форма)
	
	Запись = Форма.Запись;
		
	НачалоПериода = НачалоГода(Запись.Период);
	КонецПериода  = КонецКвартала(Запись.Период);
	
	Форма.ОтчетныйПериод =
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МинимальныйПериод(Организация)
	
	Возврат НачалоКвартала(Справочники.Организации.ДатаРегистрацииОрганизации(Организация));
	
КонецФункции

#КонецОбласти
