#Область ПрограммныйИнтерфейс
// В процедуре выполняется заполнение ставок для расчета акцизов по указанному виду товаров
// процедура вызывается при заполнении настроек налогов и отчетов
// если пользователем было указано, что организация занимается импором или производством определенных видов товаров
// ставка акциза привязана к определенной единице измерения, если эта единица измерения не найдена
// она будет подобрана из классификатора по коду единицы измерения
// Входящие параметры - 
//     - Вид товара - ПеречисленияСсылка.ВидыПодакцизныхТоваров
//     - ОбработчикОбновления - Булево, по умолчанию значение Ложь
Процедура ЗаполнитьСтавкиАкцизаПоВидуПодакцизногоТовара(ВидТовара, ОбработчикОбновления = Ложь) Экспорт
	
	Макет = РегистрыСведений.СтавкиАкциза.ПолучитьМакет("СтавкиАкциза");
	ОбластьПоВидуТовара = Макет.Области.Найти(СтрЗаменить(ВидТовара, " ", ""));
	Если ОбластьПоВидуТовара <> Неопределено Тогда
		ОбластьПоВидуТовара = Макет.ПолучитьОбласть(СтрЗаменить(ВидТовара, " ", ""));
		ТаблицаСтавок = Новый ТаблицаЗначений();
		ТаблицаСтавок.Колонки.Добавить("Год_2024");
		ТаблицаСтавок.Колонки.Добавить("Май_2024");
		ТаблицаСтавок.Колонки.Добавить("Год_2025");
		ТаблицаСтавок.Колонки.Добавить("Год_2026");
		ТаблицаСтавок.Колонки.Добавить("Год_2027");
		ТаблицаСтавок.Колонки.Добавить("Год_2028");
		ТаблицаСтавок.Колонки.Добавить("КодТовара");
		ТаблицаСтавок.Колонки.Добавить("КодЕдиницыИзмерения");
		ТаблицаСтавок.Колонки.Добавить("Округлять");
		
		КолвоСтрок = ОбластьПоВидуТовара.ВысотаТаблицы;
		Для Стр = 1 По КолВоСтрок Цикл
			НоваяСтрока = ТаблицаСтавок.Добавить();
			НоваяСтрока.КодТовара           = ОбластьПоВидуТовара.Область(Стр, 1).Текст;
			НоваяСтрока.КодЕдиницыИзмерения = ОбластьПоВидуТовара.Область(Стр, 2).Текст;
			НоваяСтрока.Год_2024            = ОбластьПоВидуТовара.Область(Стр, 3).Текст;
			НоваяСтрока.Май_2024            = ОбластьПоВидуТовара.Область(Стр, 4).Текст;
			НоваяСтрока.Год_2025            = ОбластьПоВидуТовара.Область(Стр, 5).Текст;
			НоваяСтрока.Год_2026            = ОбластьПоВидуТовара.Область(Стр, 6).Текст;
			НоваяСтрока.Год_2027            = ОбластьПоВидуТовара.Область(Стр, 7).Текст;
			НоваяСтрока.Год_2028            = ОбластьПоВидуТовара.Область(Стр, 8).Текст;
			НоваяСтрока.Округлять           = ОбластьПоВидуТовара.Область(Стр, 9).Текст;
		КонецЦикла;
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		|	КлассификаторЕдиницИзмерения.Ссылка КАК Ссылка,
		|	КлассификаторЕдиницИзмерения.Код КАК Код
		|ИЗ
		|	Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
		|ГДЕ
		|	КлассификаторЕдиницИзмерения.Код В(&МассивЕдиницИзмерения)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыВидовПодакцизныхТоваров.Ссылка КАК Ссылка,
		|	КодыВидовПодакцизныхТоваров.Код КАК Код
		|ИЗ
		|	Справочник.КодыВидовПодакцизныхТоваров КАК КодыВидовПодакцизныхТоваров
		|ГДЕ
		|	КодыВидовПодакцизныхТоваров.Код В(&МассивКодовТоваров)";
		Запрос.УстановитьПараметр("МассивЕдиницИзмерения", ТаблицаСтавок.ВыгрузитьКолонку("КодЕдиницыИзмерения"));
		Запрос.УстановитьПараметр("МассивКодовТоваров",    ТаблицаСтавок.ВыгрузитьКолонку("КодТовара"));
		МассивРезультата = Запрос.ВыполнитьПакет();
		ТаблицаКодовТоваров = МассивРезультата[1].Выгрузить();
		ТаблицаЕдиницИзмерения = МассивРезультата[0].Выгрузить();
		МассивПериодов = Новый Массив();
		Если ТекущаяДата() < '20250101' Тогда
			МассивПериодов.Добавить('20240101');
			МассивПериодов.Добавить('20240501');
		КонецЕсли;
		МассивПериодов.Добавить('20250101');
		МассивПериодов.Добавить('20260101');
		МассивПериодов.Добавить('20270101');
		МассивПериодов.Добавить('20280101');

		Для Каждого Строка Из ТаблицаСтавок Цикл
			КодПодакцизногоТовара = ТаблицаКодовТоваров.Найти(Строка.КодТовара, "Код");
			Если КодПодакцизногоТовара <> Неопределено Тогда
				// сначала получим Единицу измерения для ставки акциза
				ЕдиницаИзмерения = ТаблицаЕдиницИзмерения.Найти(Строка.КодЕдиницыИзмерения, "Код");
				Если ВидТовара = Перечисления.ВидыПодакцизныхТоваров.АвтомобилиИМотоциклы Тогда
					// по автомобилям и мотоциклам акциз может быть учтен в квт/ч и в л.с.
					// изначально пробуем взять ту единицу измерения, которая уже есть у пользователя
					Если ТаблицаЕдиницИзмерения.Количество() > 0 Тогда
						ЕдиницаИзмерения = ТаблицаЕдиницИзмерения[0].Ссылка;
					Иначе
						ЕдиницаИзмерения = Неопределено;
					КонецЕсли;
				КонецЕсли;
				
				Если ЕдиницаИзмерения <> Неопределено Тогда
					ЕдиницаИзмерения = ЕдиницаИзмерения.Ссылка;
				Иначе
					МакетКлассификатора = Справочники.КлассификаторЕдиницИзмерения.ПолучитьМакет("КлассификаторЕдиницИзмерения");
					КолонкаЧисловогоКода = МакетКлассификатора.Области.КодЧисловой.Лево;
					Результат = МакетКлассификатора.НайтиТекст(Строка.КодЕдиницыИзмерения,, МакетКлассификатора.Области.КодЧисловой, Истина, Истина, Истина, Истина);
					Если Результат <> Неопределено Тогда
						НомерСтроки = Результат.Верх;
						КодЕдиницыИзмерения = МакетКлассификатора.Область(НомерСтроки, КолонкаЧисловогоКода).Текст;
						ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ЕдиницаИзмеренияПоКоду(Строка.КодЕдиницыИзмерения);
					КонецЕсли;
				КонецЕсли;
				
				// Заполним общие параметры
				СтруктураПараметров = СтруктураПараметров();
				СтруктураПараметров.КодПодакцизногоТовара = КодПодакцизногоТовара.Ссылка;
				СтруктураПараметров.ЕдиницаИзмерения      = ЕдиницаИзмерения;
				СтруктураПараметров.Округлять             = Строка.Округлять = "Истина";
				
				// в случае обновления уже существующих ставок необходимо перезаполнить данные по всем периодам
				Если ОбработчикОбновления Тогда
					Если ТекущаяДата() >= '20250101' Тогда
						Сч = 2;
					Иначе
						Сч = 0;
					КонецЕсли;
					Для Каждого СтрокаПериода Из МассивПериодов Цикл
						Если ЗначениеЗаполнено(Строка[Сч]) Тогда
							НаборЗаписей = РегистрыСведений.СтавкиАкциза.СоздатьНаборЗаписей();
							НаборЗаписей.Отбор.КодВидаПодакцизногоТовара.Установить(КодПодакцизногоТовара.Ссылка);
							НаборЗаписей.Отбор.Период.Установить(СтрокаПериода);
							НаборЗаписей.Прочитать();
							Если НаборЗаписей.Количество() = 0 Тогда
								НоваяЗапись = НаборЗаписей.Добавить();
							Иначе
								НоваяЗапись = НаборЗаписей[0];
							КонецЕсли;
							СтруктураПараметров.Период = СтрокаПериода;
							СтруктураПараметров.Сумма = Строка[Сч];
							СоздатьЗапись(НоваяЗапись, СтруктураПараметров);
							НаборЗаписей.Записать(Истина);
						КонецЕсли;
						
						Сч = Сч + 1;
					КонецЦикла;
					
				Иначе
					НаборЗаписей = РегистрыСведений.СтавкиАкциза.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.КодВидаПодакцизногоТовара.Установить(КодПодакцизногоТовара.Ссылка);
					НаборЗаписей.Прочитать();
					Если НаборЗаписей.Количество() = 0 Тогда
						Если ТекущаяДата() >= '20250101' Тогда
						Сч = 2;
					Иначе
						Сч = 0;
					КонецЕсли;
						Для Каждого СтрокаПериода Из МассивПериодов Цикл
							
							Если ЗначениеЗаполнено(Строка[Сч]) Тогда
								
								НоваяЗапись = НаборЗаписей.Добавить();
								СтруктураПараметров.Период = СтрокаПериода;
								СтруктураПараметров.Сумма = Строка[Сч];
								СоздатьЗапись(НоваяЗапись, СтруктураПараметров);
							КонецЕсли;
							
							Сч = Сч + 1;
						КонецЦикла;
						НаборЗаписей.Записать(Истина);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗапись(Запись, ПараметрыЗаписи)
	Запись.Период                    = ПараметрыЗаписи.Период;
	Запись.КодВидаПодакцизногоТовара = ПараметрыЗаписи.КодПодакцизногоТовара;
	Запись.Сумма                     = ПараметрыЗаписи.Сумма;
	Запись.ОкруглятьДоЦелого         = ПараметрыЗаписи.Округлять;
	Если ПараметрыЗаписи.ЕдиницаИзмерения <> Неопределено Тогда
		Запись.ЕдиницаИзмерения = ПараметрыЗаписи.ЕдиницаИзмерения;
	КонецЕсли;
КонецПроцедуры

Функция СтруктураПараметров()
	Возврат Новый Структура("Период, КодПодакцизногоТовара, ЕдиницаИзмерения, Сумма, Округлять");
КонецФункции
#КонецОбласти