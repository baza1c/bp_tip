#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти
	
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает результат последнего выполнения закрытия месяца.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - ссылка на организацию, для которой требуется получение отчета
// 
// Возвращаемое значение:
// 	Структура - структура:
//  	* ОтчетОбОшибках - ТабличныйДокумент - отчет об ошибка в процессе последнего выполнения.
//  	* ПериодОтчета - Дата - период, за который был сформирован и записан отчет о закрытии месяца.
//  	* ДатаЗаписи - Дата - дата, когда был записан отчет о закрытии месяца.
//  	* Пользователь - СправочникСсылка - пользователь, от имени которого была выполнена запись отчета.
//  	* Выполнено - Булево - статус успешного закрытия месяца, или завершения с ошибками.
//
Функция РезультатПоследнегоЗакрытияМесяца(Организация) Экспорт
	
	Результат = НовыйРезультатЗакрытияМесяца();
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	*
	|ИЗ
	|	РегистрСведений.РезультатыЗакрытияМесяца КАК РезультатыЗакрытияМесяца
	|ГДЕ
	|	РезультатыЗакрытияМесяца.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация",  Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		Результат.ОтчетОбОшибках = Выборка.ОтчетОбОшибках.Получить();
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Записывает идентификатор фонового задания, для проверки в каком состоянии фоновое задание перед запуском актуализации.
//
// Параметры:
//  Организация			 - СправочникСсылка.Организации - ссылка на организацию, по которой запускается регламентное задание.
//  ИдентификаторЗадания - УникальныйИдентификатор	- уникальный идентификатор фонового задания, которое запустило регламентное задание.
//
Процедура ЗаписатьИдентификаторФоновогоЗадания(Организация, ИдентификаторЗадания) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.РезультатыЗакрытияМесяца.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Организация = Организация;
	КонецЕсли;
	МенеджерЗаписи.ИдентификаторФоновогоЗадания = ИдентификаторЗадания;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Проверяет состояние выполнения фонового задания закрытия месяца в данный момент времени.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - ссылка на организацию, по которой проверяется работа регламентного задания.
// 
// Возвращаемое значение:
//  Структура - структура со свойствами:
//		* Выполняется - Булево - фоновое задание автоматического закрытия месяца выполняется.
//		* ИдентификаторФоновогоЗадания - УникальныйИдентификатор - идентификатор фонового зададния.
//
Функция СостояниеВыполненияЗаданияЗакрытияМесяца(Организация) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Выполняется", Ложь);
	Результат.Вставить("ИдентификаторФоновогоЗадания", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПустойИдентификатор", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Результаты.ИдентификаторФоновогоЗадания КАК Идентификатор
	|ИЗ
	|	РегистрСведений.РезультатыЗакрытияМесяца КАК Результаты
	|ГДЕ
	|	Результаты.Организация = &Организация
	|	И Результаты.ИдентификаторФоновогоЗадания <> &ПустойИдентификатор";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		УстановитьПривилегированныйРежим(Истина);
		Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Выборка.Идентификатор);
		УстановитьПривилегированныйРежим(Ложь);
		
		Результат.Выполняется = Задание <> Неопределено И Задание.Состояние = СостояниеФоновогоЗадания.Активно;
		Результат.ИдентификаторФоновогоЗадания = Выборка.Идентификатор;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйРезультатЗакрытияМесяца()
	
	Результат = Новый Структура;
	Результат.Вставить("ОтчетОбОшибках", Новый ТабличныйДокумент);
	Результат.Вставить("ПериодОтчета",   '00010101');
	Результат.Вставить("ДатаВыполнения", '00010101');
	Результат.Вставить("Пользователь",   Справочники.Пользователи.ПустаяСсылка());
	Результат.Вставить("Выполнено",      Истина);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли