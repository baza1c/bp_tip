
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьТекстРекомендаций();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеЧтоСделатьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "НастройкиНалоговОтчетов" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = ТарификацияБПКлиентСервер.ИмяДействияОплатаСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		ТарификацияБПКлиент.ОткрытьФормуОплатыСервиса();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекстРекомендаций()
	
	СистемыНалогообложения = 
		РегистрыСведений.НастройкиСистемыНалогообложения.ПредставлениеИспользуемыхСистемНалогообложения(ТекущаяДатаСеанса());
	
	Если СистемыНалогообложения.Количество() > 1 Тогда
		ТекстРекомендаций = СтрШаблон(НСтр("ru = 'В программе используются системы налогообложения %1, которые не входят в ваш тариф.
			|Возможность проведения документов заблокирована.'"), СтрСоединить(СистемыНалогообложения, ", "));
	Иначе
		ТекстРекомендаций = СтрШаблон(НСтр("ru = 'В программе используется система налогообложения %1, которая не входит в ваш тариф.
			|Возможность проведения документов заблокирована.'"), СтрСоединить(СистемыНалогообложения, ", "));
	КонецЕсли;
	
	Элементы.ОписаниеЗаголовок.Заголовок = ТекстРекомендаций;
	
	ТекстРекомендаций = СтроковыеФункции.ФорматированнаяСтрока(
		СтрЗаменить(
			НСтр("ru = 'Восстановить возможность проведения документов можно, изменив систему налогообложения
			|в <a href=""НастройкиНалоговОтчетов"">Настройках налогов и отчетов</a>'"),
			Символы.ПС, " ")
	);
	
	Элементы.ОписаниеФункциональность.Заголовок = ТекстРекомендаций;
	
	ТекстРекомендаций = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Если же эта система налогообложения вам действительно нужна, <a href=""%1"">перейдите на другой тариф</a>'"),
		ТарификацияБПКлиентСервер.ИмяДействияОплатаСервиса());
	
	Элементы.ОписаниеТариф.Заголовок = ТекстРекомендаций;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаКартинкаОписание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
КонецПроцедуры

#КонецОбласти
