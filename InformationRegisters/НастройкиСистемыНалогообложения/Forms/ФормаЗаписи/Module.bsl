#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПравоРедактированияНастроек = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения);
	
	НастройкиСистемыНалогообложенияФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	УстановитьЗначенияРеквизитовБезЗакрывающихДокументов();
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если РежимБезЗакрывающихДокументов И 
		НЕ (НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы
			ИЛИ НастройкиСистемыНалогообложения.ПрименяетсяАУСН
			ИЛИ НастройкиСистемыНалогообложения.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок")
			ИЛИ НастройкиСистемыНалогообложения.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход")) Тогда
		Отказ = Истина;
		Таймаут = 30;
		ТекстИнформация = НСтр("ru = 'Для выбранной системы налогообложения надо формировать закрывающие документы (Накладная, Счет-фактура, УПД и т.д.). Для этого необходимо включить функционал ""Расчеты с покупателями и поставщиками"", в разделе Настройки - Функциональность - Расчеты.'");
		МассивЧастейВопроса = Новый Массив;
		МассивЧастейВопроса.Добавить(ТекстИнформация);
		МассивЧастейВопроса.Добавить(Символы.ПС);
		Если ВозможноИзменитьФункциональностьРасчеты Тогда
			МассивЧастейВопроса.Добавить(НСтр("ru = 'Включить автоматически?'"));
			Оповещение = Новый ОписаниеОповещения("ВопросВключенияРасчетов", ЭтотОбъект);
			РежимДиалога = Новый СписокЗначений;
			РежимДиалога.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Включить'"));
			РежимДиалога.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
		Иначе
			МассивЧастейВопроса.Добавить(НСтр("ru = 'Перейти в настройки?'"));
			Оповещение = Новый ОписаниеОповещения("ВопросПереходВНастройки", ЭтотОбъект);
			РежимДиалога = РежимДиалогаВопрос.ДаНет;
		КонецЕсли;
		ТекстВопроса = Новый ФорматированнаяСтрока(МассивЧастейВопроса);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалога, Таймаут);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.СброситьПризнакПлательщикЕНВД(ЭтотОбъект);
	НастройкиСистемыНалогообложенияФормыКлиентСервер.СброситьПризнакПлательщикНДСПрименяющийУСН(ЭтотОбъект);
	
	ПараметрыЗаписи.Вставить("ИспользуемыеСистемыНалогообложенияПередЗаписью", 
		РегистрыСведений.НастройкиСистемыНалогообложения.ИспользуемыеСистемыНалогообложения());

	ПараметрыЗаписи.Вставить("АдресХранилищаПереключениеОтложенногоПроведения",
		НастройкиУчетаВызовСервера.ПодготовитьДанныеДляПереключенияОтложенногоПроведения(УникальныйИдентификатор, ТекущийОбъект.Организация));

КонецПроцедуры

&НаКлиенте
Процедура ПериодГодПриИзменении(Элемент)
	
	НастройкиСистемыНалогообложения.Период = Дата(ПериодГод, 1, 1);
	ПериодОбработатьИзменение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	// Отслеживание фонового задания по календарю бухгалтера будет происходить без открытия окна ожидания.
	ПараметрыЗаписи.Вставить("Организация", НастройкиСистемыНалогообложения.Организация);
	ПараметрыЗаписи.Вставить("Период",      НастройкиСистемыНалогообложения.Период);
	НастройкиСистемыНалогообложенияФормыКлиент.ПослеЗаписи(ПараметрыЗаписи);
	
	// Очистку регистра ПроверенныеДокументы должны сделать в любом случае. Она продолжится, даже если форма закроется.
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗаписиЗавершение", ЭтотОбъект, ПараметрыЗаписи);
	ПроведениеКлиент.НачатьОчисткуРегистраПроверенныхДокументов(
		ОповещениеОЗавершении, НастройкиСистемыНалогообложения.Период, НастройкиСистемыНалогообложения.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиЗавершение(Результат, ПараметрыЗаписи) Экспорт

	// Открытие окна ожидания по отложенному проведению произойдет после завершения очистки ПроверенныеДокументы.
	ПроведениеКлиент.ОжидатьПереключенияОтложенногоПроведения(ПараметрыЗаписи.РезультатЗаданияПереключенияОтложенногоПроведения);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ИспользуемыеСистемыНалогообложенияПослеЗаписи", 
		РегистрыСведений.НастройкиСистемыНалогообложения.ИспользуемыеСистемыНалогообложения());
	
	РезультатВыполнения = КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(УникальныйИдентификатор, ТекущийОбъект.Организация);
	ПараметрыЗаписи.Вставить("РезультатВыполненияЗаданияКалендаряБухгалтера", РезультатВыполнения);

	РезультатЗаданияПереключенияОтложенногоПроведения = НастройкиУчетаВызовСервера.ПроверитьОтложенноеПроведениеПослеИзмененияНастроек(
		УникальныйИдентификатор,
		ПараметрыЗаписи.АдресХранилищаПереключениеОтложенногоПроведения);
	ПараметрыЗаписи.Вставить("РезультатЗаданияПереключенияОтложенногоПроведения", РезультатЗаданияПереключенияОтложенногоПроведения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(НастройкиСистемыНалогообложения.ДатаИзменения) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Применяется с'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "НастройкиСистемыНалогообложения.ДатаИзменения", Отказ);
	КонецЕсли;
	
	ДатаНачалаПрименения = Перечисления.СистемыНалогообложения.ДатаНачалаПрименения(
	НастройкиСистемыНалогообложения.СистемаНалогообложения, НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент);
	Если ДатаНачалаПрименения <> Неопределено И НастройкиСистемыНалогообложения.ДатаИзменения < ДатаНачалаПрименения Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Система налогообложения ""%1"" применяется с %2'"),
			НастройкиСистемыНалогообложения.СистемаНалогообложения,
			Формат(ДатаНачалаПрименения, "ДЛФ=D"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "НастройкиСистемыНалогообложения.ДатаИзменения", Отказ);
	КонецЕсли;
	
	НастройкиСистемыНалогообложенияФормы.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, "СистемаНалогообложения");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = ТарификацияБПКлиентСервер.ИмяДействияОплатаСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		Если ЭтоАдресВременногоХранилища(АдресЗначенийТарифицируемыхОпций) Тогда
			УдалитьИзВременногоХранилища(АдресЗначенийТарифицируемыхОпций);
		КонецЕсли;
		АдресЗначенийТарифицируемыхОпций = ТарификацияБПКлиент.ОткрытьФормуОплатыСервиса(УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ОформленыПодписки" И ТарификацияБПВызовСервера.ОтображатьБаннерОжиданияОплатыТарифа() Тогда
		
		Элементы.ГруппаОжиданиеОплатыТарифа.Видимость = Истина;
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПроверитьОбновлениеТарифицируемыхУслуг", 
			ТарификацияБПКлиент.ИнтервалПервойПроверкиОплатыТарифа(),
			Истина);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
	Если ЭтоЮрЛицо И НастройкиСистемыНалогообложения.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок") Тогда
		СистемаНалогообложенияПредставление = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
			ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая"));
		НастройкиСистемыНалогообложенияФормыКлиент.СистемаНалогообложенияПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(НастройкиСистемыНалогообложения.ДатаИзменения) Тогда
		НастройкиСистемыНалогообложения.Период = НачалоМесяца(НастройкиСистемыНалогообложения.ДатаИзменения);
		ПериодГод = Год(НастройкиСистемыНалогообложения.Период);
		ПериодОбработатьИзменение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПредставлениеПриИзменении(Элемент)
	
	НастройкиСистемыНалогообложенияФормыКлиент.СистемаНалогообложенияПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоложенияПереходногоПериодаУСНПриИзменении(Элемент)
	
	НастройкиСистемыНалогообложенияФормыКлиент.ПоложенияПереходногоПериодаУСНПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода", НачалоМесяца(НастройкиСистемыНалогообложения.Период));
	ПараметрыВыбора.Вставить("КонецПериода",  КонецМесяца(НастройкиСистемыНалогообложения.Период));
	ПараметрыВыбора.Вставить("ВидПериода",    ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НачалоПериода = НачалоМесяца(НастройкиСистемыНалогообложения.Период);
	КонецПериода  = КонецМесяца(НастройкиСистемыНалогообложения.Период);
	
	ВыборПериодаКлиент.ПериодОбработкаВыбора(
		Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		Период,
		НачалоПериода,
		КонецПериода);
	
	Модифицированность = Истина;
	
	НастройкиСистемыНалогообложения.Период = НачалоПериода;
	ПериодОбработатьИзменение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодАвтоПодбор(
		Элемент,
		Текст,
		ДанныеВыбора,
		Ожидание,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		Период,
		НачалоМесяца(НастройкиСистемыНалогообложения.Период),
		КонецМесяца(НастройкиСистемыНалогообложения.Период));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	НастройкиУчета.ПодготовитьФормуНаСервере(ЭтотОбъект, НастройкиСистемыНалогообложения);
	НастройкиСистемыНалогообложенияФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	
	Период = ПолучитьПредставлениеПериода(НастройкиСистемыНалогообложения.Период);
	ПериодГод = Год(НастройкиСистемыНалогообложения.Период);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.УправлениеФормой(Форма);
	
	Элементы = Форма.Элементы;
	НастройкиСистемыНалогообложения = Форма.НастройкиСистемыНалогообложения;
	
	ИспользоватьДатуИзменения = НастройкиСистемыНалогообложенияФормыВызовСервера.ИспользуетсяДатаИзменения(
		НастройкиСистемыНалогообложения.Организация,
		НастройкиСистемыНалогообложения.СистемаНалогообложения,
		НастройкиСистемыНалогообложения.Период);
	ПереходСНачалаГода = НастройкиСистемыНалогообложенияФормыКлиентСервер.ПереходНаСистемуНалогообложенияСНачалаГода(
		НастройкиСистемыНалогообложения.СистемаНалогообложения);
	Элементы.ДатаИзменения.Видимость = ИспользоватьДатуИзменения И Не ПереходСНачалаГода;
	Элементы.Период.Видимость = Не ИспользоватьДатуИзменения И Не ПереходСНачалаГода;
	Элементы.ПериодГод.Видимость = ПереходСНачалаГода;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеПериода(Период)
	
	Возврат БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоМесяца(Период),
		КонецМесяца(Период),
		Истина);
	
КонецФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(НастройкиСистемыНалогообложения.Организация);
	НастройкиСистемыНалогообложенияФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЗавершениеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		Модифицированность = Истина;
		НастройкиСистемыНалогообложения.Период = РезультатВыбора.НачалоПериода;
		ПериодГод = Год(НастройкиСистемыНалогообложения.Период);
		ПериодОбработатьИзменение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработатьИзменение()
	
	Период = ПолучитьПредставлениеПериода(НастройкиСистемыНалогообложения.Период);
	
	Если НачалоМесяца(НастройкиСистемыНалогообложения.ДатаИзменения) <> НастройкиСистемыНалогообложения.Период Тогда
		НастройкиСистемыНалогообложения.ДатаИзменения = НастройкиСистемыНалогообложения.Период;
	КонецЕсли;
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.ЗаполнитьСписокВыбораСистемНалогообложения(ЭтотОбъект);
	СистемаНалогообложенияДоИзмененияПериода = СистемаНалогообложенияПредставление;
	НастройкиСистемыНалогообложенияФормыКлиентСервер.УстановитьСистемуНалогообложения(ЭтотОбъект);
	Если СистемаНалогообложенияДоИзмененияПериода <> СистемаНалогообложенияПредставление Тогда
		НастройкиСистемыНалогообложенияФормыКлиент.СистемаНалогообложенияПредставлениеПриИзменении(
			ЭтотОбъект, Элементы.СистемаНалогообложенияПредставление);
	КонецЕсли;
	НастройкиСистемыНалогообложенияФормыКлиентСервер.СброситьПризнакПлательщикЕНВД(ЭтотОбъект);
	НастройкиСистемыНалогообложенияФормыКлиентСервер.СброситьПризнакПлательщикНДСПрименяющийУСН(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьОбновлениеТарифицируемыхУслуг()
	
	Если ТарификацияБПВызовСервера.ТарифицируемыеОпцииИзменены(АдресЗначенийТарифицируемыхОпций) Тогда
		Элементы.ГруппаОжиданиеОплатыТарифа.Видимость = Ложь;
		ОбновитьИнтерфейс();
	Иначе
		УвеличениеИнтервала = 30;
		ИнтервалПроверкиОплатыТарифа = 
			?(ЗначениеЗаполнено(ИнтервалПроверкиОплатыТарифа),
			ИнтервалПроверкиОплатыТарифа + УвеличениеИнтервала,
			60);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПроверитьОбновлениеТарифицируемыхУслуг", 
			ИнтервалПроверкиОплатыТарифа,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросВключенияРасчетов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		УстановитьЗначенияРеквизитовБезЗакрывающихДокументов(Истина);
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПереходВНастройки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ТекущаяСтраница", "ГруппаРасчеты");
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыОткрытияФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияРеквизитовБезЗакрывающихДокументов(УстановитьОпцию = Ложь)
	РежимБезЗакрывающихДокументов = РежимБезЗакрывающихДокументов(УстановитьОпцию);
	ВозможноИзменитьФункциональностьРасчеты =
		Обработки.ФункциональностьПрограммы.РазрешеноИспользоватьФункциональностьВДоступномИнтерфейсе("ВестиУчетРасчетовСКонтрагентами");
КонецПроцедуры
	
&НаСервереБезКонтекста
Функция РежимБезЗакрывающихДокументов(УстановитьОпцию)
	
	Если УстановитьОпцию Тогда
		Константы.ВестиУчетРасчетовСКонтрагентами.Установить(Истина);
		Возврат Ложь;
	Иначе
		Возврат Не Константы.ВестиУчетРасчетовСКонтрагентами.Получить();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

