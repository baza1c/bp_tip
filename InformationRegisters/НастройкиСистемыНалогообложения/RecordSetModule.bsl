#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения = Неопределено Или ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Для каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.НастройкиСистемыНалогообложения.УстановкаНастроекПоУмолчанию(Запись, ?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТарифВзносов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗаписьИсторииДанных.ДополнительныеДанные, "Тариф");
	НастройкиУчета.ПередЗаписью(ЭтотОбъект, Отказ, Замещение);
	
	НастройкиПлатежейСтраховыхВзносов =
		РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.НовыеНастройкиУчетаСтраховыхВзносов_ПФР_ФФОМС();
	
	Для каждого Строка Из ЭтотОбъект Цикл
		
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Строка.Организация, "ЮридическоеФизическоеЛицо,ОбособленноеПодразделение");
			
		Если РеквизитыОрганизации.ОбособленноеПодразделение Тогда
			Продолжить; // Записи по ОП создаются автоматически, их не проверяем
		ИначеЕсли ДополнительныеСвойства.ГоловныеОрганизации.Найти(Строка.Организация) = Неопределено Тогда
			ДополнительныеСвойства.ГоловныеОрганизации.Добавить(Строка.Организация);
		КонецЕсли;
		
		ЭтоФизЛицо = РеквизитыОрганизации.ЮридическоеФизическоеЛицо
			= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
			
		Если НЕ ЭтоФизЛицо И Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок Тогда
			Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая;
		КонецЕсли;

		Строка.ПлательщикНалогаНаПрибыль = Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая 
			И НЕ ЭтоФизЛицо;

		Строка.ПрименяетсяАУСН = Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.АУСН;
		
		Строка.ПрименяетсяАУСНДоходы = Строка.ПрименяетсяАУСН
			И Строка.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы;
		
		Строка.ПрименяетсяУСН = Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная;

		Строка.ПрименяетсяУСНДоходы = Строка.ПрименяетсяУСН
			И Строка.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы;

		Строка.ПрименяетсяУСНДоходыМинусРасходы = Строка.ПрименяетсяУСН
			И Строка.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы;

		Строка.ПлательщикНДС = Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая
			И Строка.ПлательщикНДС;
			
		Строка.ПлательщикНДСПрименяющийУСН = Строка.ПрименяетсяУСН И Строка.ПлательщикНДСПрименяющийУСН;
			
		Строка.ПлательщикНДФЛ = ЭтоФизЛицо
			И Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая;
		
		Строка.ПрименяетсяУСНПатент = ЭтоФизЛицо И Строка.ПрименяетсяУСНПатент;
		
		Строка.ПрименяетсяОсобыйПорядокНалогообложения =
			(Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок);
		
		Строка.ПрименяетсяНалогНаПрофессиональныйДоход =
			(Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход);
			
		Если Не ЗначениеЗаполнено(Строка.ДатаИзменения) Тогда
			Строка.ДатаИзменения = Строка.Период;
		КонецЕсли;
		
		Если ЭтоФизЛицо Тогда
			СтрокаНастройкиПлатежей = НастройкиПлатежейСтраховыхВзносов.Добавить();
			СтрокаНастройкиПлатежей.Организация = Строка.Организация;
			СтрокаНастройкиПлатежей.Период = Строка.Период;
			СтрокаНастройкиПлатежей.ДатаИзменения = Строка.ДатаИзменения;
			СтрокаНастройкиПлатежей.УплачиваютсяСтраховыеВзносы =
				Не Строка.ПрименяетсяНалогНаПрофессиональныйДоход
				И Не Строка.ПрименяетсяАУСН;
			СтрокаНастройкиПлатежей.ПлательщикАУСН = Строка.ПрименяетсяАУСН;
			СтрокаНастройкиПлатежей.Тариф = ТарифВзносов;
		КонецЕсли;
		
		Если Строка.Период < НастройкиУчетаВызовСервера.ДатаНачалаПримененияЕНП() Тогда
			Строка.ПлательщикЕНП = Ложь;
		КонецЕсли;
		
		// Счетчик переходов между объектами УСН
		// Строка - это изменяемое значение, чаще текущего периода. Необходимо посмотреть предыдущий период.
		// Если в прошлом периоде применялся УСН
		// и произошла смена объекта
		// Тогда записываем счетчик.
		Если Строка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
			ПредыдущийПериод = НачалоГода(Строка.Период - 1);
			ОтборДляНастройкиПредыдущегоПериода = Новый Структура("Организация", Строка.Организация);
			// Настройка в регистре, которая была до текущей.
			ПредыдущаяНастройка = РегистрыСведений.НастройкиСистемыНалогообложения.СрезПоследних(ПредыдущийПериод,
				ОтборДляНастройкиПредыдущегоПериода);
			Если ПредыдущаяНастройка.Количество() > 0 Тогда
				ПредыдущаяНастройка = ПредыдущаяНастройка[0];
				Если ПредыдущаяНастройка.ПрименяетсяУСН
					И ПредыдущаяНастройка.ПрименяетсяУСНДоходы = Строка.ПрименяетсяУСНДоходыМинусРасходы Тогда
					Если Строка.ПрименяетсяУСНДоходыМинусРасходы Тогда
						ЗаписатьОперациюБизнесСтатистики("ПереходНаРасходы");
					ИначеЕсли Строка.ПрименяетсяУСНДоходы Тогда
						ЗаписатьОперациюБизнесСтатистики("ПереходНаДоходы");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

	Если ЭтотОбъект.Количество() = 0 Тогда
		РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.ПриУдаленииЗаписиНастройкиСистемыНалогообложения(
			Отбор.Организация.Значение, Отбор.Период.Значение, ТарифВзносов);
	Иначе
		РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.ОтразитьИзменениеНастройкиПлательщикСтраховыхВзносов(
			НастройкиПлатежейСтраховыхВзносов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПриЗаписи(ЭтотОбъект, Отказ, Замещение);
	СпециальныйНалоговыйРежимПриИзменении(ЭтотОбъект);
	СброситьПризнакНастройкиОнлайнКассы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СпециальныйНалоговыйРежимПриИзменении(ЭтотОбъект) Экспорт
	
	ТаблицаЗаписей = ЭтотОбъект.Выгрузить();
	Если ТаблицаЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись ИЗ ТаблицаЗаписей Цикл
		ПроверкаПорядкаУчетаНДС(Запись);                    // для УСН
		ПроверкаПорядкаОтраженияАванса(Запись);             // для УСН
		ПроверкаПоследовательностиРасчетаУСН(Запись);       // для УСН
		ПроверкаВидовДеятельности(Запись);                  // для ИП
		ПроверкаОсобогоПорядкаНалогообложения(Запись);      // для ИП на ЕНВД
		ПроверкаРеквизитовЕдиногоНалоговогоПлатежа(Запись); // для ЕНП
	КонецЦикла;
	
КонецПроцедуры

Процедура СброситьПризнакНастройкиОнлайнКассы()
	ТаблицаЗаписей = Выгрузить();
	
	Если ТаблицаЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СписокОрганизаций = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗаписей, "Организация", Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОборудованиеПоОрганизациям.Оборудование КАК Оборудование
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|ГДЕ
	|	ОборудованиеПоОрганизациям.Организация В(&СписокОрганизаций)
	|	И ОборудованиеПоОрганизациям.НастройкиСистемыНалогообложенияПроверены";
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
	
		МенеджерЗаписи = РегистрыСведений.ОборудованиеПоОрганизациям.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Оборудование = РезультатЗапроса.Оборудование;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.НастройкиСистемыНалогообложенияПроверены = Ложь;
			МенеджерЗаписи.Записать();
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры

Процедура ПроверкаПорядкаУчетаНДС(Запись)
	
	Если Не Запись.ПрименяетсяУСН
		И Не Запись.ПрименяетсяОсобыйПорядокНалогообложения
		И Не Запись.ПрименяетсяНалогНаПрофессиональныйДоход Тогда
		Возврат;
	КонецЕсли;
	
	Если УчетнаяПолитика.РаздельныйУчетНДС(Запись.Организация, Запись.Период)
		ИЛИ УчетнаяПолитика.УпрощенныйУчетНДС(Запись.Организация, Запись.Период) Тогда
		
		МенеджерЗаписи = РегистрыСведений.НастройкиУчетаНДС.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация = Запись.Организация;
		МенеджерЗаписи.Период      = Запись.Период;
		МенеджерЗаписи.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверкаПоследовательностиРасчетаУСН(Запись)
	
	// Если УСН перестал применяться в середине года, в начале квартала, то в регламентной операции по расчету УСН
	// за прошлый квартал необходимо учесть, что выполняется итоговый расчет налога
	Если Запись.ПрименяетсяУСН Или Запись.ДатаИзменения <> НачалоКвартала(Запись.ДатаИзменения)
		Или Месяц(Запись.ДатаИзменения) = 1 Тогда
		Возврат;
	КонецЕсли;
	
	УСНИспользоваласьВПрошломКвартале = УчетнаяПолитика.ПрименяетсяУСН(Запись.Организация, Запись.ДатаИзменения - 1);
	
	Если УСНИспользоваласьВПрошломКвартале Тогда
		ВидРегламентнойОперации = Перечисления.ВидыРегламентныхОпераций.РасчетНалогаУСН;
		НомерГруппыРегламентнойОперации = Перечисления.ВидыРегламентныхОпераций.НомерГруппы(ВидРегламентнойОперации);
		РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
			Запись.Организация, НачалоКвартала(Запись.ДатаИзменения - 1), НомерГруппыРегламентнойОперации,
			ВидРегламентнойОперации, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверкаПорядкаОтраженияАванса(Запись)
	
	Если НЕ Запись.ПрименяетсяУСН Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Запись.ПлательщикЕНВД И НЕ Запись.ПрименяетсяУСНПатент Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.НастройкиУчетаУСН.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Запись.Организация;
	МенеджерЗаписи.Период      = НачалоГода(Запись.Период);
	МенеджерЗаписи.Прочитать();
	
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		РегистрыСведений.НастройкиУчетаУСН.УстановкаНастроекПоУмолчанию(МенеджерЗаписи, Новый Структура);
	КонецЕсли;
	
	МенеджерЗаписи.Организация = Запись.Организация;
	МенеджерЗаписи.Период      = НачалоГода(Запись.Период);
	
	Если (НЕ Запись.ПлательщикЕНВД И МенеджерЗаписи.ПорядокОтраженияАванса = ПредопределенноеЗначение("Перечисление.ПорядокОтраженияАвансов.ДоходЕНВД"))
		ИЛИ (НЕ Запись.ПрименяетсяУСНПатент И МенеджерЗаписи.ПорядокОтраженияАванса = ПредопределенноеЗначение("Перечисление.ПорядокОтраженияАвансов.ДоходПатент")) Тогда
		
		СписокОтраженияАванса = Новый СписокЗначений;
		СписокПатентов        = Новый СписокЗначений;
		НастройкиУчетаУСНФормыВызовСервера.СписокВыбораОтраженияАвансов(МенеджерЗаписи, СписокОтраженияАванса, СписокПатентов);
		
		Если СписокОтраженияАванса.Количество() = 1 Тогда
			МенеджерЗаписи.ПорядокОтраженияАванса = СписокОтраженияАванса[0].Значение;
		Иначе
			МенеджерЗаписи.ПорядокОтраженияАванса = ПредопределенноеЗначение("Перечисление.ПорядокОтраженияАвансов.ДоходУСН");
			МенеджерЗаписи.Патент                 = Неопределено;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(МенеджерЗаписи.ПорядокОтраженияАванса) Тогда
			МенеджерЗаписи.ПорядокОтраженияАванса = ПредопределенноеЗначение("Перечисление.ПорядокОтраженияАвансов.ДоходУСН");
			МенеджерЗаписи.Патент                 = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

Процедура ПроверкаВидовДеятельности(Запись)
	
	Если НЕ Запись.ПлательщикНДФЛ Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Запись.ПлательщикЕНВД И НЕ Запись.ПрименяетсяУСНПатент Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.НастройкиУчетаНДФЛ.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Запись.Организация;
	МенеджерЗаписи.Период      = НачалоГода(Запись.Период);
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Организация = Запись.Организация;
	МенеджерЗаписи.Период      = НачалоГода(Запись.Период);
	
	ХарактерыДеятельности = РегистрыСведений.НастройкиУчетаНДФЛ.ПолучитьХарактерыВидовДеятельности(МенеджерЗаписи.ОсновнойВидДеятельности, МенеджерЗаписи.ВидДеятельностиДоходовПоАвансам);
	
	Если НЕ Запись.ПлательщикЕНВД Тогда
		
		Если ХарактерыДеятельности.ОсновнойХарактерДеятельности = ПредопределенноеЗначение("Перечисление.ХарактерДеятельности.РозничнаяТорговляЕНВД")
			ИЛИ ХарактерыДеятельности.ОсновнойХарактерДеятельности = ПредопределенноеЗначение("Перечисление.ХарактерДеятельности.УслугиЕНВД") Тогда
			
			МенеджерЗаписи.ОсновнойВидДеятельности = Неопределено;
			
		КонецЕсли;
	
		Если ХарактерыДеятельности.ХарактерДеятельностиДоходовПоАвансамИП = ПредопределенноеЗначение("Перечисление.ХарактерДеятельности.РозничнаяТорговляЕНВД")
			ИЛИ ХарактерыДеятельности.ХарактерДеятельностиДоходовПоАвансамИП = ПредопределенноеЗначение("Перечисление.ХарактерДеятельности.УслугиЕНВД") Тогда
			
			МенеджерЗаписи.ВидДеятельностиДоходовПоАвансам = МенеджерЗаписи.ОсновнойВидДеятельности;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Запись.ПрименяетсяУСНПатент Тогда
	
		Если ХарактерыДеятельности.ОсновнойХарактерДеятельности = ПредопределенноеЗначение("Перечисление.ХарактерДеятельности.ВсяДеятельностьНаПатенте") Тогда
			
			МенеджерЗаписи.ОсновнойВидДеятельности = Неопределено;
			
		КонецЕсли;
	
		Если ХарактерыДеятельности.ХарактерДеятельностиДоходовПоАвансамИП = ПредопределенноеЗначение("Перечисление.ХарактерДеятельности.ВсяДеятельностьНаПатенте") Тогда
			
			МенеджерЗаписи.ВидДеятельностиДоходовПоАвансам = МенеджерЗаписи.ОсновнойВидДеятельности;
			
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

Процедура ПроверкаОсобогоПорядкаНалогообложения(Запись)
	
	УчетЗарплаты.УточнитьОтношениеКЕНВД(
		Запись.Организация,
		Запись.Период,
		Запись.ПлательщикЕНВД,
		Запись.ПрименяетсяОсобыйПорядокНалогообложения);
		
	Если Запись.ПрименяетсяАУСН Тогда
		УчетЗарплаты.ВключитьИспользоватьРасчетПервойПоловиныМесяца();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверкаРеквизитовЕдиногоНалоговогоПлатежа(Запись)
	
	Если НЕ Запись.ПлательщикЕНП Тогда
		Возврат;
	КонецЕсли;
	
	ЕдиныйНалоговыйСчет.ПроверкаРеквизитовЕдиногоНалоговогоПлатежа(Запись.Организация)
	
КонецПроцедуры

Процедура ЗаписатьОперациюБизнесСтатистики(Направление)
	ИмяКлючевойОперации = СтрШаблон("СменаОбъектаУСН.%1", Направление);
	
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(ИмяКлючевойОперации, 1);
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяКлючевойОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли