// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обновить характеристики потребителя.
// 
// Параметры:
//  ТаблицаХарактеристикРекламы - Неопределено    - значение по умолчанию
//  							- ТаблицаЗначений - см. Справочники.Реклама.НовыйТаблицаТаргетыРекламыИзДанныхСервиса
//
Процедура ОбновитьЗначенияХарактеристикПотребителя(ТаблицаХарактеристикРекламы = Неопределено) Экспорт
	
	ДатаНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ПараметрыХарактеристик = НовыйПараметрыХарактеристик();
	
	Если ТаблицаХарактеристикРекламы <> Неопределено Тогда
		ПараметрыХарактеристик.ТаблицаХарактеристикРекламы = ТаблицаХарактеристикРекламы;
	КонецЕсли;
	
	ЗаполнитьТаблицуВидамиХарактеристик(ПараметрыХарактеристик);
	
	УдалитьНеАктуальныеХарактеристики(ПараметрыХарактеристик);
	
	Попытка
		
		ЗаполнитьНовыеВидыХарактеристик(ПараметрыХарактеристик);
		ТаблицаХарактеристик = ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик;
		
		ПараметрыХарактеристик.Очистить();
		
		Если ТаблицаХарактеристик.Количество() > 0 Тогда
			
			СоздатьОбновитьНаборыЗаписей(ТаблицаХарактеристик);
			
		КонецЕсли;
		
		ОценкаПроизводительности.ЗакончитьЗамерВремени(
			НСтр("ru = 'РекламныйСервис: ОбновитьЗначенияХарактеристикПотребителя'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), 
			ДатаНачала);
		
	Исключение
		
		ИмяСобытия = РекламныйСервисСлужебный.ИмяСобытияХарактеристикаПрофиля(
			РекламныйСервисСлужебный.ИмяДействияОбновление());
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.РегистрыСведений.ЗначенияХарактеристикПотребителяРекламы, , 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОписаниеХарактеристик

// РАЗМЕР ПРЕДПРИЯТИЯ ЧИСЛЕННОСТЬ.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * НЕТ_НАЕМНЫХ_СОТРУДНИКОВ - Строка - ИП или самозанятый.
// * МИКРОПРЕДПРИЯТИЕ - Строка - до 5 сотрудников.
// * МАЛОЕ_ПРЕДПРИЯТИЕ - Строка - до 25 сотрудников.
// * СРЕДНЕЕ_ПРЕДПРИЯТИЕ - Строка - до 60 сотрудников.
// * КРУПНОЕ_ПРЕДПРИЯТИЕ - Строка - более 60 сотрудников.
//
Функция РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ()
	
	Результат = Новый Структура;
	Результат.Вставить("НЕТ_НАЕМНЫХ_СОТРУДНИКОВ", "НЕТ_НАЕМНЫХ_СОТРУДНИКОВ");
	Результат.Вставить("МИКРОПРЕДПРИЯТИЕ", "МИКРОПРЕДПРИЯТИЕ");
	Результат.Вставить("МАЛОЕ_ПРЕДПРИЯТИЕ", "МАЛОЕ_ПРЕДПРИЯТИЕ");
	Результат.Вставить("СРЕДНЕЕ_ПРЕДПРИЯТИЕ ", "СРЕДНЕЕ_ПРЕДПРИЯТИЕ");
	Результат.Вставить("КРУПНОЕ_ПРЕДПРИЯТИЕ", "КРУПНОЕ_ПРЕДПРИЯТИЕ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// РАЗМЕР ПРЕДПРИЯТИЯ ДОХОД.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * МИКРОПРЕДПРИЯТИЕ - Строка - до 2,4 млн руб (по границе самозанятого).
// * МАЛОЕ_ПРЕДПРИЯТИЕ - Строка - до 60 млн (по границе АУСН).
// * СРЕДНЕЕ_ПРЕДПРИЯТИЕ - Строка - до 150 млн (по границе УСН).
// * КРУПНОЕ_ПРЕДПРИЯТИЕ - Строка - более 150 млн.
//
Функция РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД()
	
	Результат = Новый Структура;
	Результат.Вставить("МИКРОПРЕДПРИЯТИЕ ", "МИКРОПРЕДПРИЯТИЕ");
	Результат.Вставить("МАЛОЕ_ПРЕДПРИЯТИЕ ", "МАЛОЕ_ПРЕДПРИЯТИЕ");
	Результат.Вставить("СРЕДНЕЕ_ПРЕДПРИЯТИЕ ", "СРЕДНЕЕ_ПРЕДПРИЯТИЕ");
	Результат.Вставить("КРУПНОЕ_ПРЕДПРИЯТИЕ ", "КРУПНОЕ_ПРЕДПРИЯТИЕ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// РАЗМЕР ПРЕДПРИЯТИЯ ОБОРОТ п о СЧЕТАМ.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * МИКРОПРЕДПРИЯТИЕ - Строка - до 2,4 млн руб (по границе самозанятого).
// * МАЛОЕ_ПРЕДПРИЯТИЕ - Строка - до 60 млн (по границе АУСН).
// * СРЕДНЕЕ_ПРЕДПРИЯТИЕ - Строка - до 150 млн (по границе УСН).
// * КРУПНОЕ_ПРЕДПРИЯТИЕ - Строка - более 150 млн.
//
Функция РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ()
	
	Результат = Новый Структура;
	Результат.Вставить("МИКРОПРЕДПРИЯТИЕ ", "МИКРОПРЕДПРИЯТИЕ");
	Результат.Вставить("МАЛОЕ_ПРЕДПРИЯТИЕ ", "МАЛОЕ_ПРЕДПРИЯТИЕ");
	Результат.Вставить("СРЕДНЕЕ_ПРЕДПРИЯТИЕ ", "СРЕДНЕЕ_ПРЕДПРИЯТИЕ");
	Результат.Вставить("КРУПНОЕ_ПРЕДПРИЯТИЕ ", "КРУПНОЕ_ПРЕДПРИЯТИЕ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// ОСНОВНЫЕ СРЕДСТВА (остаточная стоимость на балансе).
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * НЕТ - Строка
// * В_ПРЕДЕЛАХ_УСН - Строка - до границы УСН (150 млн руб).
// * СВЫШЕ_ПРЕДЕЛА_УСН - Строка - свыше границы УСН (свыше 150 млн руб).
//
Функция ОСНОВНЫЕ_СРЕДСТВА()
	
	Результат = Новый Структура;
	Результат.Вставить("НЕТ", "НЕТ");
	Результат.Вставить("В_ПРЕДЕЛАХ_УСН", "В_ПРЕДЕЛАХ_УСН");
	Результат.Вставить("СВЫШЕ_ПРЕДЕЛА_УСН", "СВЫШЕ_ПРЕДЕЛА_УСН");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// СРОК ЖИЗНИ ПРЕДПРИЯТИЯ.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * ДО_1_ГОДА - Строка - до 1 года.
// * ДО_3_ЛЕТ - Строка - до 3 лет.
// * ДО_10_ЛЕТ - Строка -  до 10 лет.
// * СВЫШЕ_10_ЛЕТ - Строка - свыше 10 лет.
//
Функция СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ()
	
	Результат = Новый Структура;
	Результат.Вставить("ДО_1_ГОДА", "ДО_1_ГОДА");
	Результат.Вставить("ДО_3_ЛЕТ", "ДО_3_ЛЕТ");
	Результат.Вставить("ДО_10_ЛЕТ", "ДО_10_ЛЕТ");
	Результат.Вставить("СВЫШЕ_10_ЛЕТ", "СВЫШЕ_10_ЛЕТ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// ВОЗРАСТ ИП.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * ДО_25_ЛЕТ - Строка - до 25 лет.
// * ДО_65_ЛЕТ - Строка - от 25-65 лет.
// * СВЫШЕ_65_ЛЕТ - Строка - свыше 65 лет.
//
Функция ВОЗРАСТ_ИП()
	
	Результат = Новый Структура;
	Результат.Вставить("ДО_25_ЛЕТ", "ДО_25_ЛЕТ");
	Результат.Вставить("ДО_65_ЛЕТ", "ДО_65_ЛЕТ");
	Результат.Вставить("СВЫШЕ_65_ЛЕТ", "СВЫШЕ_65_ЛЕТ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// УЧЕТ КОМАНДИРОВОК за прошлый месяц было. Авансовых отчетов с типом 
// 	Командировка или авансовых отчетов с ключевыми словами, являющимися признаком командировки
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * НЕТ - Строка -
// * МЕНЕЕ_10 - Строка
// * БОЛЕЕ_10 - Строка
// * БОЛЕЕ_50 - Строка
//
Функция УЧЕТ_КОМАНДИРОВОК()
	
	Результат = Новый Структура;
	Результат.Вставить("НЕТ", "НЕТ");
	Результат.Вставить("МЕНЕЕ_10", "МЕНЕЕ_10");
	Результат.Вставить("БОЛЕЕ_10", "БОЛЕЕ_10");
	Результат.Вставить("БОЛЕЕ_50", "БОЛЕЕ_50");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// КОЛ в о КРЕДИТНЫХ ДОГОВОРОВ.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * НЕТ - Строка
// * ОТ_1_ДО_3 - Строка
// * БОЛЕЕ_3 - Строка
//
Функция КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ()
	
	Результат = Новый Структура;
	Результат.Вставить("НЕТ", "НЕТ");
	Результат.Вставить("ОТ_1_ДО_3", "ОТ_1_ДО_3");
	Результат.Вставить("БОЛЕЕ_3", "БОЛЕЕ_3");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// ФОРМА ПОСТАВКИ ПП.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * КОРОБОЧНЫЙ - Строка
// * ФРЕШ - Строка
//
Функция ФОРМА_ПОСТАВКИ_ПП()
	
	Результат = Новый Структура;
	Результат.Вставить("КОРОБОЧНЫЙ", "КОРОБОЧНЫЙ");
	Результат.Вставить("ФРЕШ", "ФРЕШ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// ВЕРСИЯ ПП.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * БАЗОВАЯ - Строка
// * ПРОФ - Строка
// * КОРП - Строка
//
Функция ВЕРСИЯ_ПП() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("БАЗОВАЯ", "БАЗОВАЯ");
	Результат.Вставить("ПРОФ", "ПРОФ");
	Результат.Вставить("КОРП", "КОРП");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// СИСТЕМА НАЛОГООБЛОЖЕНИЯ.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * ОСН_ЮР_ЛИЦА - Строка
// * УСН_ДОХОДЫ - Строка
// * УСН_ДОХОДЫ_МИНУС_РАСХОДЫ - Строка
// * ОСН_ИП - Строка
// * ПАТЕНТ - Строка
// * САМОЗАНЯТЫЙ - Строка
// * АУСН_ДОХОДЫ - Строка
// * АУСН_ДОХОДЫ_МИНУС_РАСХОДЫ - Строка
//
Функция СИСТЕМА_НАЛОГООБЛОЖЕНИЯ() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОСН_ЮР_ЛИЦА", "ОСН_ЮР_ЛИЦА");
	Результат.Вставить("УСН_ДОХОДЫ", "УСН_ДОХОДЫ");
	Результат.Вставить("УСН_ДОХОДЫ_МИНУС_РАСХОДЫ", "УСН_ДОХОДЫ_МИНУС_РАСХОДЫ");
	Результат.Вставить("ОСН_ИП", "ОСН_ИП");
	Результат.Вставить("ПАТЕНТ", "ПАТЕНТ");
	Результат.Вставить("САМОЗАНЯТЫЙ", "САМОЗАНЯТЫЙ");
	Результат.Вставить("АУСН_ДОХОДЫ", "АУСН_ДОХОДЫ");
	Результат.Вставить("АУСН_ДОХОДЫ_МИНУС_РАСХОДЫ", "АУСН_ДОХОДЫ_МИНУС_РАСХОДЫ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// ГРУППА КОНФИГУРАЦИЙ.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * БП - Строка
// * БП_НКО - Строка
// * БП_Садовод - Строка
// * БП_Гаражи - Строка
// * БП_МСФО - Строка
// * БП_УХ - Строка
//
Функция ГРУППА_КОНФИГУРАЦИЙ() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("БП", "БП");
	Результат.Вставить("БП_НКО", "БП_НКО");
	Результат.Вставить("БП_Садовод", "БП_Садовод");
	Результат.Вставить("БП_Гаражи", "БП_Гаражи");
	Результат.Вставить("БП_МСФО", "БП_МСФО");
	Результат.Вставить("БП_УХ", "БП_УХ");
	
	Результат = Новый ФиксированнаяСтруктура(Результат);
	
	Возврат Результат;
	
КонецФункции

// Сопоставленные таргеты "КРОМЕ". Соотношение исключающих таргетов "...КРОМЕ" основным таргетам
// Пример "РЕГИОНЫ_ПОКАЗА_КРОМЕ" - "РЕГИОНЫ_ПОКАЗА"
// 
// Возвращаемое значение:
//  Структура:
//  * РЕГИОНЫ_ПОКАЗА_КРОМЕ - Строка 
//  * ОКОПФ_КРОМЕ - Строка
//  * КОД_ОКВЭД_КРОМЕ - Строка
Функция СопоставленныеТаргетыКРОМЕ() Экспорт
	
	Результат = Новый Структура;
	
	ВидыХарактеристик = ВидыХарактеристик();
	Результат.Вставить(ВидыХарактеристик.РЕГИОНЫ_ПОКАЗА_КРОМЕ.ID, ВидыХарактеристик.РЕГИОНЫ_ПОКАЗА.ID);
	Результат.Вставить(ВидыХарактеристик.ОКОПФ_КРОМЕ.ID, ВидыХарактеристик.ОКОПФ.ID);
	Результат.Вставить(ВидыХарактеристик.КОД_ОКВЭД_КРОМЕ.ID, ВидыХарактеристик.КОД_ОКВЭД.ID);
	
	Возврат Результат
	
КонецФункции

// Массив имен таргетов вида КРОМЕ.
// 
// Возвращаемое значение:
//  Массив Из ОпределяемыйТип.ВидХарактеристикиРекламы
Функция ТаргетыВидаКРОМЕ()
	
	Результат = Новый Массив; // Массив Из ОпределяемыйТип.ВидХарактеристикиРекламы
	
	СопоставленныеТаргетыКРОМЕ = СопоставленныеТаргетыКРОМЕ();
	Для Каждого СопоставленныйТаргет Из СопоставленныеТаргетыКРОМЕ Цикл
		Результат.Добавить(СопоставленныйТаргет.Ключ);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеХарактеристик

// Заполнить вид характеристики.
// Добавляет в текущий регистр новый вид характеристики с заполненными значениями по каждому владельцу, если их 
// значения соответствуют значениям таргета.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  IDВидаХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы
//
Процедура ЗаполнитьВидХарактеристики(ПараметрыХарактеристик, IDВидаХарактеристики)
	
	ВидыХарактеристик = ПараметрыХарактеристик.ВидыХарактеристик;
		
	// Получение владельцев.
	ВладельцыХарактеристики = ВладельцыХарактеристики(
		IDВидаХарактеристики, ПараметрыХарактеристик.ТаблицаНовыеХарактеристики); 

	ДанныеВладельцев = НовыйДанныеВладельцев();
	
	// Получение значений характеристик в прикладном решении.
	Если IDВидаХарактеристики = ВидыХарактеристик.РЕГИОНЫ_ПОКАЗА.ID 
		ИЛИ IDВидаХарактеристики = ВидыХарактеристик.РЕГИОНЫ_ПОКАЗА_КРОМЕ.ID Тогда
		
		Заполнить_РЕГИОНЫ_ПОКАЗА(ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ.ID Тогда 
		
		Заполнить_РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ( 
			ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД.ID Тогда
		
		Заполнить_РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД(
			ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ.ID Тогда
		
		Заполнить_РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ( 
			ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.ОСНОВНЫЕ_СРЕДСТВА.ID Тогда
		
		Заполнить_ОСНОВНЫЕ_СРЕДСТВА( 
			ВидыХарактеристик.ОСНОВНЫЕ_СРЕДСТВА, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ.ID Тогда
		
		Заполнить_СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ( 
			ВидыХарактеристик.СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.ВОЗРАСТ_ИП.ID Тогда
		
		Заполнить_ВОЗРАСТ_ИП(ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.УЧЕТ_КОМАНДИРОВОК.ID Тогда
		
		Заполнить_УЧЕТ_КОМАНДИРОВОК( 
			ВидыХарактеристик.УЧЕТ_КОМАНДИРОВОК, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ.ID Тогда
		
		Заполнить_КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ( 
			ВидыХарактеристик.КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.ОКОПФ.ID 
		ИЛИ IDВидаХарактеристики = ВидыХарактеристик.ОКОПФ_КРОМЕ.ID Тогда
		
		Заполнить_ОКОПФ(ВладельцыХарактеристики, ДанныеВладельцев);
				
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.КОД_ОКВЭД.ID
		ИЛИ IDВидаХарактеристики = ВидыХарактеристик.КОД_ОКВЭД_КРОМЕ.ID Тогда
		
		Заполнить_КОД_ОКВЭД(ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.СИСТЕМА_НАЛОГООБЛОЖЕНИЯ.ID Тогда
		
		Заполнить_СИСТЕМА_НАЛОГООБЛОЖЕНИЯ( 
			ВидыХарактеристик.СИСТЕМА_НАЛОГООБЛОЖЕНИЯ, ВладельцыХарактеристики, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.ФОРМА_ПОСТАВКИ_ПП.ID Тогда
		
		Заполнить_ФОРМА_ПОСТАВКИ_ПП(ВидыХарактеристик.ФОРМА_ПОСТАВКИ_ПП, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.ВЕРСИЯ_ПП.ID Тогда
		
		Заполнить_ВЕРСИЯ_ПП(ВидыХарактеристик.ВЕРСИЯ_ПП, ДанныеВладельцев);
		
	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.ГРУППА_КОНФИГУРАЦИЙ.ID Тогда
		
		Заполнить_ГРУППА_КОНФИГУРАЦИЙ(ВидыХарактеристик.ГРУППА_КОНФИГУРАЦИЙ, ДанныеВладельцев);

	ИначеЕсли IDВидаХарактеристики = ВидыХарактеристик.СЕРВИС_АКТИВЕН.ID 
		Или IDВидаХарактеристики = ВидыХарактеристик.СЕРВИС_НЕ_АКТИВЕН.ID 
		Или IDВидаХарактеристики = ВидыХарактеристик.СЕРВИС_БЫЛ_АКТИВЕН.ID Тогда
		
		Заполнить_СЕРВИСЫ_ИТС(ПараметрыХарактеристик, IDВидаХарактеристики); 
		
		Возврат; // Общие характеристики активности сервиса индивидуальны, и значительно выбиваются 
				 // из общего алгоритма проверок и заполнения
	Иначе
		
		ШаблонСтроки = 
			НСтр("ru = 'Для характеристики ""%1"" не определена процедура'", ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки, IDВидаХарактеристики);
		
		ВызватьИсключение СтрокаОшибки;
		
	КонецЕсли;
			
	ЗаполнитьЗначенияХарактеристики(
		ВладельцыХарактеристики, ДанныеВладельцев, ПараметрыХарактеристик, IDВидаХарактеристики);
	
КонецПроцедуры

#Область ХарактеристикиПрофиля

// Заполнить РЕГИОНЫ ПОКАЗА. Заполняет характеристики РЕГИОН_ПОКАЗА и РЕГИОН_ПОКАЗА_КРОМЕ.
// 
// Параметры:
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_РЕГИОНЫ_ПОКАЗА(ВладельцыХарактеристики, ДанныеВладельцев)
				
	// Получение значений характеристик в прикладном решении.	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьРегионыПоказа(ВладельцыХарактеристики, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписанияТиповРекламногоСервиса = РекламныйСервисСлужебный.Типы();
	ОписаниеТипаСтрока = ОписанияТиповРекламногоСервиса.Строка;
	ОписаниеТипаМассив = ОписанияТиповРекламногоСервиса.Массив;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьРегионыПоказа";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаСтрока, Контекст, ОписаниеТипаМассив);	
	
КонецПроцедуры

// Заполнить РАЗМЕР ПРЕДПРИЯТИЯ ЧИСЛЕННОСТЬ.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.	
	ПараметрыПериода = НовыйПараметрыПериода(ВидХарактеристики.ЧастотаОбновления);
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьРазмерПредприятияЧисленность(
		ВладельцыХарактеристики, ПараметрыПериода, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеТипаЧисло = РекламныйСервисСлужебный.Типы().Число;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьРазмерПредприятияЧисленность";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаЧисло, Контекст);
	
КонецПроцедуры

// Заполнить РАЗМЕР ПРЕДПРИЯТИЯ ДОХОД.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ПараметрыПериода = НовыйПараметрыПериода(ВидХарактеристики.ЧастотаОбновления);
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьРазмерПредприятияДоход(
		ВладельцыХарактеристики, ПараметрыПериода, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеТипаЧисло = РекламныйСервисСлужебный.Типы().Число;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьРазмерПредприятияДоход";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаЧисло, Контекст);
	
КонецПроцедуры

// Заполнить РАЗМЕР ПРЕДПРИЯТИЯ ОБОРОТ п о СЧЕТАМ.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ПараметрыПериода = НовыйПараметрыПериода(ВидХарактеристики.ЧастотаОбновления);
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьРазмерПредприятияОборотПоСчетам(
		ВладельцыХарактеристики, ПараметрыПериода, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);

	ОписаниеТипаЧисло = РекламныйСервисСлужебный.Типы().Число;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьРазмерПредприятияОборотПоСчетам";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаЧисло, Контекст);
	
КонецПроцедуры

// Заполнить ОСНОВНЫЕ СРЕДСТВА.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_ОСНОВНЫЕ_СРЕДСТВА(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ПараметрыПериода = НовыйПараметрыПериода(ВидХарактеристики.ЧастотаОбновления);
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьОсновныеСредства(
		ВладельцыХарактеристики, ПараметрыПериода, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеТипаЧисло = РекламныйСервисСлужебный.Типы().Число;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьОсновныеСредства";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаЧисло, Контекст);
		
КонецПроцедуры

// Заполнить СРОК ЖИЗНИ ПРЕДПРИЯТИЯ.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ДатаРегистрацииПредприятия(ВладельцыХарактеристики, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеТипаДата = РекламныйСервисСлужебный.Типы().Дата;
	Контекст = "РекламныйСервисПереопределяемый.ДатаРегистрацииПредприятия";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаДата, Контекст);
		
КонецПроцедуры

// Заполнить ВОЗРАСТ ИП.
// 
// Параметры:
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_ВОЗРАСТ_ИП(ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ДатаРожденияИП(ВладельцыХарактеристики, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);

	ОписаниеТипаДата = РекламныйСервисСлужебный.Типы().Дата;
	Контекст = "РекламныйСервисПереопределяемый.ДатаРожденияИП";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаДата, Контекст);
	
КонецПроцедуры

// Заполнить УЧЕТ КОМАНДИРОВОК.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_УЧЕТ_КОМАНДИРОВОК(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ПараметрыПериода = НовыйПараметрыПериода(ВидХарактеристики.ЧастотаОбновления);

	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьУчетКомандировок(
		ВладельцыХарактеристики, ПараметрыПериода, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеТипаЧисло = РекламныйСервисСлужебный.Типы().Число;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьУчетКомандировок";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаЧисло, Контекст);
		
КонецПроцедуры

// Заполнить количество кредитных договоров.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ПараметрыПериода = НовыйПараметрыПериода(ВидХарактеристики.ЧастотаОбновления);
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьКоличествоКредитныхДоговоров(
		ВладельцыХарактеристики, ПараметрыПериода, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);

	ОписаниеТипаЧисло = РекламныйСервисСлужебный.Типы().Число;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьКоличествоКредитныхДоговоров";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаЧисло, Контекст);
		
КонецПроцедуры

// Заполнить ОКОПФ. Заполняет характеристики ОКОПФ и ОКОПФ_КРОМЕ.
// 
// Параметры:
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_ОКОПФ(ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьОКОПФ(ВладельцыХарактеристики, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеТипаСтрока = РекламныйСервисСлужебный.Типы().Строка;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьОКОПФ";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаСтрока, Контекст);
		
КонецПроцедуры

// Заполнить КОД ОКВЭД. Заполняет характеристики КОД_ОКВЭД и КОД_ОКВЭД_КРОМЕ.
// 
// Параметры:
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_КОД_ОКВЭД(ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьКодыОКВЭД(ВладельцыХарактеристики, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеТипаСтрока = РекламныйСервисСлужебный.Типы().Строка;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьКодыОКВЭД";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаСтрока, Контекст);
	
КонецПроцедуры

// Заполнить СИСТЕМА НАЛОГООБЛОЖЕНИЯ.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ВладельцыХарактеристики - см. ВладельцыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_СИСТЕМА_НАЛОГООБЛОЖЕНИЯ(ВидХарактеристики, ВладельцыХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ДопустимыеЗначения = СИСТЕМА_НАЛОГООБЛОЖЕНИЯ();
	ПараметрыПериода = НовыйПараметрыПериода(ВидХарактеристики.ЧастотаОбновления);
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьСистемуНалогообложения(
		ВладельцыХарактеристики, ДопустимыеЗначения, ПараметрыПериода, ДанныеВладельцев);
	УстановитьПривилегированныйРежим(Ложь);

	ОписанияТиповРекламногоСервиса = РекламныйСервисСлужебный.Типы();
	ОписаниеТипаСтрока = ОписанияТиповРекламногоСервиса.Строка;
	ОписаниеТипаМассив = ОписанияТиповРекламногоСервиса.Массив;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьСистемуНалогообложения";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаСтрока, Контекст, ОписаниеТипаМассив);
		
КонецПроцедуры

// Заполняет значения характеритики, предварительно проверяя на соответствие ожидаемым значениям таргета
// 
// Параметры:
//  ВладельцыХарактеристики - Массив из ОпределяемыйТип.ВладелецХарактеристикиРекламы
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  IDВидаХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы
//
Процедура ЗаполнитьЗначенияХарактеристики(
		ВладельцыХарактеристики, ДанныеВладельцев, ПараметрыХарактеристик, IDВидаХарактеристики) 
		
	Отбор = Новый Структура("ВидХарактеристики", IDВидаХарактеристики);
	СтрокиТаргетаРекламы = ПараметрыХарактеристик.ТаблицаНовыеХарактеристикиБезВладельцев.НайтиСтроки(Отбор);

	ЭтоТаргетВидаКРОМЕ = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
		ПараметрыХарактеристик.СопоставленныеТаргетыКРОМЕ, IDВидаХарактеристики);
		
	ВидХарактеристики = ПараметрыХарактеристик.ВидыХарактеристик[IDВидаХарактеристики]; // см. НовыйРеквизитыХарактеристики
	
	Для Каждого ВладелецХарактеристики Из ВладельцыХарактеристики Цикл
						
		ЗначениеХарактеристикиВладельца = ДанныеВладельцев.Получить(ВладелецХарактеристики);		
		Для Каждого СтрокаТаргетаРекламы Из СтрокиТаргетаРекламы Цикл
			
			ЗначениеТаргета = СтрокаТаргетаРекламы.Значение; // ОпределяемыйТип.ЗначениеХарактеристикиРекламы
			Если Не ЗначениеЗаполнено(ЗначениеТаргета) Тогда 
				Продолжить;
			КонецЕсли;
						
			ДанныеТаргета = НовыйДанныеТаргетаДляПроверки(ВидХарактеристики, ЗначениеТаргета, ЭтоТаргетВидаКРОМЕ);
			ОбработатьЗначенияХарактеристикиВладельца(
				ПараметрыХарактеристик, ДанныеТаргета, ВладелецХарактеристики, ЗначениеХарактеристикиВладельца);
						
		КонецЦикла;
		
	КонецЦикла;
			
КонецПроцедуры

#КонецОбласти

#Область ОбщиеХарактеристики

// Заполнить ФОРМА ПОСТАВКИ ПП.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_ФОРМА_ПОСТАВКИ_ПП(ВидХарактеристики, ДанныеВладельцев)
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	Если РазделениеВключено Тогда
		Значение = ФОРМА_ПОСТАВКИ_ПП().ФРЕШ;
	Иначе
		Значение = ФОРМА_ПОСТАВКИ_ПП().КОРОБОЧНЫЙ;
	КонецЕсли;
	
	ВладелецХарактеристики = РекламныйСервисСлужебный.ПустойВладелецХарактеристики();
	ДанныеВладельцев.Вставить(ВладелецХарактеристики, Значение);
	
КонецПроцедуры

// Заполнить ВЕРСИЯ ПП.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_ВЕРСИЯ_ПП(ВидХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ДопустимыеЗначения = ВЕРСИЯ_ПП();
	Значение = "";
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьВерсиюПП(ДопустимыеЗначения, Значение);
	УстановитьПривилегированныйРежим(Ложь);
	
	ВладелецХарактеристики = РекламныйСервисСлужебный.ПустойВладелецХарактеристики();
	ДанныеВладельцев.Вставить(ВладелецХарактеристики, Значение);
	
	ОписаниеТипаСтрока = РекламныйСервисСлужебный.Типы().Строка;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьВерсиюПП";
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаСтрока, Контекст);
	
	Условие = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДопустимыеЗначения, Значение);
	ОбщегоНазначенияКлиентСервер.Проверить(
		Условие, 
		НСтр("ru = 'Не допустимое значение'", ОбщегоНазначения.КодОсновногоЯзыка()), 
		Контекст);
		
КонецПроцедуры

// Заполнить ГРУППА КОНФИГУРАЦИЙ.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ДанныеВладельцев - см. НовыйДанныеВладельцев
//
Процедура Заполнить_ГРУППА_КОНФИГУРАЦИЙ(ВидХарактеристики, ДанныеВладельцев)
	
	// Получение значений характеристик в прикладном решении.
	ДопустимыеЗначения = ГРУППА_КОНФИГУРАЦИЙ();
	Значение = "";
	
	УстановитьПривилегированныйРежим(Истина);
	РекламныйСервисПереопределяемый.ЗаполнитьГруппаКонфигураций(ДопустимыеЗначения, Значение);
	УстановитьПривилегированныйРежим(Ложь);
	
	ВладелецХарактеристики = РекламныйСервисСлужебный.ПустойВладелецХарактеристики();
	ДанныеВладельцев.Вставить(ВладелецХарактеристики, Значение);

	ОписаниеТипаСтрока = РекламныйСервисСлужебный.Типы().Строка;
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьГруппаКонфигураций";	
	ВалидацияТиповЗначенийДанныхВладельцев(ДанныеВладельцев, ОписаниеТипаСтрока, Контекст);
	
	Условие = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДопустимыеЗначения, Значение);
	ОбщегоНазначенияКлиентСервер.Проверить(
		Условие, 
		НСтр("ru = 'Не допустимое значение'", ОбщегоНазначения.КодОсновногоЯзыка()), 
		Контекст);
		
КонецПроцедуры

// Заполнить СЕРВИСЫ ИТС.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  ВидХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы
//
Процедура Заполнить_СЕРВИСЫ_ИТС(ПараметрыХарактеристик, Знач ВидХарактеристики)
		
	ТаблицаНовыеХарактеристики = ПараметрыХарактеристик.ТаблицаНовыеХарактеристики;
	
	ВидыХарактеристик = ПараметрыХарактеристик.ВидыХарактеристик;
	ВсеОпцииИТСВТаргетах = ОпцииСервисовИТС(ТаблицаНовыеХарактеристики, ВидыХарактеристик);
	ОпцииИТСВТаргетахСтрокой = СтрСоединить(ВсеОпцииИТСВТаргетах, ",");
	
	ПараметрыХарактеристик.СтатусыОпцийИТС = 
		РекламныйСервисСлужебныйПовтИсп.СтатусыОпцийИТС(ОпцииИТСВТаргетахСтрокой);
	
	Если ВидХарактеристики = ВидыХарактеристик.СЕРВИС_АКТИВЕН.ID Тогда
		
		Заполнить_СЕРВИС_АКТИВЕН(ПараметрыХарактеристик, ВидыХарактеристик.СЕРВИС_АКТИВЕН);
	
	ИначеЕсли ВидХарактеристики = ВидыХарактеристик.СЕРВИС_НЕ_АКТИВЕН.ID Тогда
		
		Заполнить_СЕРВИС_НЕ_АКТИВЕН(ПараметрыХарактеристик, ВидыХарактеристик.СЕРВИС_НЕ_АКТИВЕН);
		
	ИначеЕсли ВидХарактеристики = ВидыХарактеристик.СЕРВИС_БЫЛ_АКТИВЕН.ID Тогда
		
		Заполнить_СЕРВИС_БЫЛ_АКТИВЕН(ПараметрыХарактеристик, ВидыХарактеристик.СЕРВИС_БЫЛ_АКТИВЕН);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполнить СЕРВИС АКТИВЕН.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//
Процедура Заполнить_СЕРВИС_АКТИВЕН(ПараметрыХарактеристик, Знач ВидХарактеристики)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВидХарактеристики", ВидХарактеристики.ID);
	СтрокиХарактеристикиРекламы = ПараметрыХарактеристик.ТаблицаНовыеХарактеристики.НайтиСтроки(ПараметрыОтбора);
	
	СтатусыОпцийИТС = ПараметрыХарактеристик.СтатусыОпцийИТС;
	ДатаСравнения = ТекущаяДатаСеанса();
	
	Для Каждого СтрокаОпций Из СтатусыОпцийИТС Цикл
		
		Если СтрокаОпций.ДатаНачалаОпции < ДатаСравнения 
			И СтрокаОпций.ДатаОкончанияОпции > ДатаСравнения Тогда
			
			Для Каждого СтрокаХарактеристикиРекламы Из СтрокиХарактеристикиРекламы Цикл
				
				ЗначениеХарактеристики = СтрокаХарактеристикиРекламы.Значение;
				Если Не ЗначениеЗаполнено(ЗначениеХарактеристики) Тогда
					Продолжить;
				КонецЕсли;
				ВладелецХарактеристики = СтрокаХарактеристикиРекламы.ВладелецХарактеристики;
				
				МассивЗначений = СтрРазделить(ЗначениеХарактеристики, ",");
				Если МассивЗначений.Найти(СтрокаОпций.ИмяОпции) <> Неопределено Тогда
					
					ДобавитьСтрокуТаблицы(
						ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик, 
						ВладелецХарактеристики, 
						ВидХарактеристики, 
						ЗначениеХарактеристики, 
						СтрокаОпций.ИмяОпции);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполнить СЕРВИС НЕ АКТИВЕН.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//
Процедура Заполнить_СЕРВИС_НЕ_АКТИВЕН(ПараметрыХарактеристик, Знач ВидХарактеристики)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВидХарактеристики", ВидХарактеристики.ID);
	СтрокиХарактеристикиРекламы = ПараметрыХарактеристик.ТаблицаНовыеХарактеристики.НайтиСтроки(ПараметрыОтбора);
	
	СтатусыОпцийИТС = ПараметрыХарактеристик.СтатусыОпцийИТС;
	Если СтатусыОпцийИТС.Количество() = 0 Тогда
		
		Для Каждого СтрокаХарактеристикиРекламы Из СтрокиХарактеристикиРекламы Цикл
			
			ЗначениеХарактеристики = СтрокаХарактеристикиРекламы.Значение;
			Если Не ЗначениеЗаполнено(ЗначениеХарактеристики) Тогда
				Продолжить;
			КонецЕсли;
			ВладелецХарактеристики = СтрокаХарактеристикиРекламы.ВладелецХарактеристики;
			
			ДобавитьСтрокуТаблицы(
				ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик, 
				ВладелецХарактеристики, 
				ВидХарактеристики, 
				ЗначениеХарактеристики, 
				ЗначениеХарактеристики);
			
		КонецЦикла;
		
		Возврат;
		
	Иначе
		
		// Оставим опции ИТС которые активны.
		ДатаСравнения = ТекущаяДатаСеанса();
		ТекущиеСтатусыОпцийИТС = СтатусыОпцийИТС.Скопировать();
		ТекущиеСтатусыОпцийИТС.Очистить();
		Для Каждого СтрокаОпцииИТС Из СтатусыОпцийИТС Цикл
			
			Если СтрокаОпцииИТС.ДатаНачалаОпции < ДатаСравнения 
				И СтрокаОпцииИТС.ДатаОкончанияОпции > ДатаСравнения Тогда
				
				НоваяСтрокаОпцииИТС = ТекущиеСтатусыОпцийИТС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаОпцииИТС, СтрокаОпцииИТС);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого СтрокаХарактеристикиРекламы Из СтрокиХарактеристикиРекламы Цикл
		
		ЗначениеХарактеристики = СтрокаХарактеристикиРекламы.Значение;
		Если Не ЗначениеЗаполнено(ЗначениеХарактеристики) Тогда
			Продолжить;
		КонецЕсли;
		ВладелецХарактеристики = СтрокаХарактеристикиРекламы.ВладелецХарактеристики;
		
		ДобавитьСтроку = Ложь;
		ТекущееЗначениеХарактеристики = "";
		
		СписокОпцийТаргета = СтрРазделить(ЗначениеХарактеристики, ",");
		Для Каждого ИмяОпции Из СписокОпцийТаргета Цикл
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ИмяОпции", ИмяОпции);
			СтрокиОпцииИТС = ТекущиеСтатусыОпцийИТС.НайтиСтроки(ПараметрыОтбора);
			
			Если СтрокиОпцииИТС.Количество() = 0 Тогда
				
				ДобавитьСтроку = Истина;
				
				ТекущееЗначениеХарактеристики = СтрШаблон("%1%2%3", 
					ТекущееЗначениеХарактеристики, 
					?(ПустаяСтрока(ТекущееЗначениеХарактеристики), "", ","), 
					ИмяОпции);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ДобавитьСтроку Тогда
			
			ДобавитьСтрокуТаблицы(
				ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик, 
				ВладелецХарактеристики, 
				ВидХарактеристики, 
				ЗначениеХарактеристики, 
				ТекущееЗначениеХарактеристики);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполнить СЕРВИС БЫЛ АКТИВЕН.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//
Процедура Заполнить_СЕРВИС_БЫЛ_АКТИВЕН(ПараметрыХарактеристик, ВидХарактеристики)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВидХарактеристики", ВидХарактеристики.ID);
	СтрокиХарактеристикиРекламы = ПараметрыХарактеристик.ТаблицаНовыеХарактеристики.НайтиСтроки(ПараметрыОтбора);
	
	СтатусыОпцийИТС = ПараметрыХарактеристик.СтатусыОпцийИТС;
	
	// Оставим опции ИТС которые были активны в прошлом.
	ДатаСравнения = ТекущаяДатаСеанса();
	ТекущиеСтатусыОпцийИТС = СтатусыОпцийИТС.Скопировать();
	ТекущиеСтатусыОпцийИТС.Очистить();
	Для Каждого СтрокаОпцииИТС Из СтатусыОпцийИТС Цикл
		
		Если СтрокаОпцииИТС.ДатаОкончанияОпции < ДатаСравнения Тогда
			
			НоваяСтрокаОпцииИТС = ТекущиеСтатусыОпцийИТС.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаОпцииИТС, СтрокаОпцииИТС);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаХарактеристикиРекламы Из СтрокиХарактеристикиРекламы Цикл
		
		ЗначениеХарактеристики = СтрокаХарактеристикиРекламы.Значение;
		Если Не ЗначениеЗаполнено(ЗначениеХарактеристики) Тогда
			Продолжить;
		КонецЕсли;
		ВладелецХарактеристики = СтрокаХарактеристикиРекламы.ВладелецХарактеристики;
		
		СписокОпцийТаргета = СтрРазделить(ЗначениеХарактеристики, ",");
		Для Каждого ИмяОпции Из СписокОпцийТаргета Цикл
			
			СтрокаОпцииИТС = ТекущиеСтатусыОпцийИТС.Найти(ИмяОпции, "ИмяОпции");
			Если СтрокаОпцииИТС <> Неопределено Тогда
				
				ДобавитьСтрокуТаблицы(
					ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик, 
					ВладелецХарактеристики, 
					ВидХарактеристики, 
					ЗначениеХарактеристики, 
					ИмяОпции);
				
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Виды характеристик.
// 
// Возвращаемое значение:
//  Структура:
// * РЕГИОНЫ_ПОКАЗА - см. НовыйРеквизитыХарактеристики
// * РЕГИОНЫ_ПОКАЗА_КРОМЕ - см. НовыйРеквизитыХарактеристики
// * РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ - см. НовыйРеквизитыХарактеристики
// * РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД - см. НовыйРеквизитыХарактеристики
// * РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ - см. НовыйРеквизитыХарактеристики
// * ОСНОВНЫЕ_СРЕДСТВА - см. НовыйРеквизитыХарактеристики
// * СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ - см. НовыйРеквизитыХарактеристики
// * ВОЗРАСТ_ИП - см. НовыйРеквизитыХарактеристики
// * УЧЕТ_КОМАНДИРОВОК - см. НовыйРеквизитыХарактеристики
// * КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ - см. НовыйРеквизитыХарактеристики
// * ОКОПФ - см. НовыйРеквизитыХарактеристики
// * ОКОПФ_КРОМЕ - см. НовыйРеквизитыХарактеристики
// * КОД_ОКВЭД - см. НовыйРеквизитыХарактеристики
// * КОД_ОКВЭД_КРОМЕ - см. НовыйРеквизитыХарактеристики
// * СИСТЕМА_НАЛОГООБЛОЖЕНИЯ - см. НовыйРеквизитыХарактеристики
// * ФОРМА_ПОСТАВКИ_ПП - см. НовыйРеквизитыХарактеристики
// * ВЕРСИЯ_ПП - см. НовыйРеквизитыХарактеристики
// * СЕРВИС_НЕ_АКТИВЕН - см. НовыйРеквизитыХарактеристики
// * СЕРВИС_АКТИВЕН - см. НовыйРеквизитыХарактеристики
// * СЕРВИС_БЫЛ_АКТИВЕН - см. НовыйРеквизитыХарактеристики
// * ГРУППА_КОНФИГУРАЦИЙ - см. НовыйРеквизитыХарактеристики
//
Функция ВидыХарактеристик()
	
	ЧастотаОбновления = РекламныйСервисСлужебныйПовтИсп.ВариантыЧастотыОбновленияХарактеристики();
	
	Результат = Новый Структура;
	МетодПолученияВладельцаОрганизация = РекламныйСервисСлужебный.МетодыПолученияВладельцевХарактеристик().Организация;
	
	Результат.Вставить(
		"РЕГИОНЫ_ПОКАЗА", 
		НовыйРеквизитыХарактеристики(
			"РЕГИОНЫ_ПОКАЗА", 
			ЧастотаОбновления.День, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"РЕГИОНЫ_ПОКАЗА_КРОМЕ", 
		НовыйРеквизитыХарактеристики(
			"РЕГИОНЫ_ПОКАЗА_КРОМЕ", 
			ЧастотаОбновления.День, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ", 
		НовыйРеквизитыХарактеристики(
			"РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ", 
			ЧастотаОбновления.Месяц, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД", 
		НовыйРеквизитыХарактеристики(
			"РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД", 
			ЧастотаОбновления.Месяц, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ", 
		НовыйРеквизитыХарактеристики(
			"РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ", 
			ЧастотаОбновления.Месяц, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"ОСНОВНЫЕ_СРЕДСТВА", 
		НовыйРеквизитыХарактеристики(
			"ОСНОВНЫЕ_СРЕДСТВА", 
			ЧастотаОбновления.Месяц, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ", 
		НовыйРеквизитыХарактеристики(
			"СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ", 
			ЧастотаОбновления.Год, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"ВОЗРАСТ_ИП", 
		НовыйРеквизитыХарактеристики(
			"ВОЗРАСТ_ИП", 
			ЧастотаОбновления.Год, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"УЧЕТ_КОМАНДИРОВОК", 
		НовыйРеквизитыХарактеристики(
			"УЧЕТ_КОМАНДИРОВОК", 
			ЧастотаОбновления.Месяц, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ", 
		НовыйРеквизитыХарактеристики(
			"КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ", 
			ЧастотаОбновления.Месяц, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"ОКОПФ", 
		НовыйРеквизитыХарактеристики(
			"ОКОПФ", 
			ЧастотаОбновления.День, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"ОКОПФ_КРОМЕ", 
		НовыйРеквизитыХарактеристики(
			"ОКОПФ_КРОМЕ", 
			ЧастотаОбновления.День, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"КОД_ОКВЭД", 
		НовыйРеквизитыХарактеристики(
			"КОД_ОКВЭД", 
			ЧастотаОбновления.День, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"КОД_ОКВЭД_КРОМЕ", 
		НовыйРеквизитыХарактеристики(
			"КОД_ОКВЭД_КРОМЕ", 
			ЧастотаОбновления.День, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"СИСТЕМА_НАЛОГООБЛОЖЕНИЯ", 
		НовыйРеквизитыХарактеристики(
			"СИСТЕМА_НАЛОГООБЛОЖЕНИЯ", 
			ЧастотаОбновления.Месяц, 
			МетодПолученияВладельцаОрганизация));
	
	Результат.Вставить(
		"ФОРМА_ПОСТАВКИ_ПП", 
		НовыйРеквизитыХарактеристики(
			"ФОРМА_ПОСТАВКИ_ПП", 
			ЧастотаОбновления.День, 
			""));
	
	Результат.Вставить(
		"ВЕРСИЯ_ПП", 
		НовыйРеквизитыХарактеристики(
			"ВЕРСИЯ_ПП", 
			ЧастотаОбновления.День, 
			""));
	
	Результат.Вставить(
		"СЕРВИС_АКТИВЕН", 
		НовыйРеквизитыХарактеристики(
			"СЕРВИС_АКТИВЕН", 
			ЧастотаОбновления.День, 
			""));
	
	Результат.Вставить(
		"СЕРВИС_НЕ_АКТИВЕН", 
		НовыйРеквизитыХарактеристики(
			"СЕРВИС_НЕ_АКТИВЕН", 
			ЧастотаОбновления.День, 
			""));
	
	Результат.Вставить(
		"СЕРВИС_БЫЛ_АКТИВЕН", 
		НовыйРеквизитыХарактеристики(
			"СЕРВИС_БЫЛ_АКТИВЕН", 
			ЧастотаОбновления.День, 
			""));
	
	Результат.Вставить(
		"ГРУППА_КОНФИГУРАЦИЙ", 
		НовыйРеквизитыХарактеристики(
			"ГРУППА_КОНФИГУРАЦИЙ", 
			ЧастотаОбновления.День, 
			""));
	
	Возврат Результат;
	
КонецФункции

// Валидация типов значений данных владельцев.
// 
// Параметры:
//  ДанныеВладельцев - См. НовыйДанныеВладельцев
//  ОписаниеОжидаемогоТипаЗначения - ОписаниеТипов - Описание ожидаемого тип значения характеристики
//  Контекст - Строка - Имя метода результат заполнения которого проверяется
//  ОписаниеОжидаемогоТипаКоллекции - ОписаниеТипов, Неопределено - Описание ожидаемого тип коллекции, 
//  																если значение характеристики множественное
//  
Процедура ВалидацияТиповЗначенийДанныхВладельцев(
		ДанныеВладельцев, ОписаниеОжидаемогоТипаЗначения, Контекст, ОписаниеОжидаемогоТипаКоллекции = Неопределено)

	ОписаниеТиповВладельцев = РекламныйСервисСлужебный.Типы().ВладелецХарактеристикиРекламы;
	Для Каждого КлючИЗначение Из ДанныеВладельцев Цикл // КлючИЗначение
				
		РекламныйСервисСлужебный.ПроверкаТипаЗначения(КлючИЗначение.Ключ, ОписаниеТиповВладельцев, Контекст);
		
		Если ОписаниеОжидаемогоТипаКоллекции = Неопределено Тогда
			РекламныйСервисСлужебный.ПроверкаТипаЗначения(
				КлючИЗначение.Значение, ОписаниеОжидаемогоТипаЗначения, Контекст);
		Иначе
				
			РекламныйСервисСлужебный.ПроверкаТипаЗначения(
				КлючИЗначение.Значение, ОписаниеОжидаемогоТипаКоллекции, Контекст);
				
			Для Каждого Элемент Из КлючИЗначение.Значение Цикл // Строка
			
				РекламныйСервисСлужебный.ПроверкаТипаЗначения(Элемент, ОписаниеОжидаемогоТипаЗначения, Контекст);
					
			КонецЦикла;
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкиУсловийСоответствияЗначенийХарактеристик

// Проверка условий соответствия значений характеристики.
// 
// Параметры:
//  ВидыХарактеристик - см. ВидыХарактеристик
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - Строка, Число, Дата, Массив из Строка - Значение характеристики или список значений характеристики 
//  РезультатПроверки - Булево - Истина, если значения характеристики удовлетворяют условиям соответствия значений таргета
//
Процедура ПроверкаУсловийСоответствияЗначенийХарактеристики(
		ВидыХарактеристик, ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ВидХарактеристики = ДанныеТаргета.ВидХарактеристики;

	Если ВидХарактеристики.ID = ВидыХарактеристик.РЕГИОНЫ_ПОКАЗА.ID
		ИЛИ ВидХарактеристики.ID = ВидыХарактеристик.РЕГИОНЫ_ПОКАЗА_КРОМЕ.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_РЕГИОНЫ_ПОКАЗА(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
			
	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ.ID Тогда
	
		ПроверкаСоответствияЗначенияХарактеристики_РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.ОСНОВНЫЕ_СРЕДСТВА.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_ОСНОВНЫЕ_СРЕДСТВА(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.ВОЗРАСТ_ИП.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_ВОЗРАСТ_ИП(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.УЧЕТ_КОМАНДИРОВОК.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_УЧЕТ_КОМАНДИРОВОК(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.ОКОПФ.ID
		ИЛИ ВидХарактеристики.ID = ВидыХарактеристик.ОКОПФ_КРОМЕ.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_ОКОПФ(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.КОД_ОКВЭД.ID
		ИЛИ ВидХарактеристики.ID = ВидыХарактеристик.КОД_ОКВЭД_КРОМЕ.ID Тогда
		
		ПроверкаСоответствияЗначенияХарактеристики_КОД_ОКВЭД(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.СИСТЕМА_НАЛОГООБЛОЖЕНИЯ.ID Тогда

		ПроверкаСоответствияЗначенияХарактеристики_СИСТЕМА_НАЛОГООБЛОЖЕНИЯ(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.ФОРМА_ПОСТАВКИ_ПП.ID Тогда

		ПроверкаСоответствияЗначенияХарактеристики_ФОРМА_ПОСТАВКИ_ПП(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.ВЕРСИЯ_ПП.ID Тогда

		ПроверкаСоответствияЗначенияХарактеристики_ВЕРСИЯ_ПП(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	ИначеЕсли ВидХарактеристики.ID = ВидыХарактеристик.ГРУППА_КОНФИГУРАЦИЙ.ID Тогда

		ПроверкаСоответствияЗначенияХарактеристики_ГРУППА_КОНФИГУРАЦИЙ(
			ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);

	Иначе
		
		ШаблонСтроки = 
			НСтр("ru = 'Для характеристики ""%1"" не определена процедура'", ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки, ВидХарактеристики.ID);
		
		ВызватьИсключение СтрокаОшибки;
		
	КонецЕсли; 
	
КонецПроцедуры

// Проверка вхождения значения характеристики в значения таргета.
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - Строка, Число, Дата, Массив из Строка - Значение характеристики или список значений характеристики 
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)

	МассивЗначенийТаргета = СтрРазделить(ДанныеТаргета.ЗначениеТаргета, ",");
	РезультатПроверки = Не ДанныеТаргета.ЭтоТаргетВидаКРОМЕ 
		И МассивЗначенийТаргета.Найти(ЗначениеХарактеристикиВладельца) <> Неопределено 
			ИЛИ ДанныеТаргета.ЭтоТаргетВидаКРОМЕ 
				И МассивЗначенийТаргета.Найти(ЗначениеХарактеристикиВладельца) = Неопределено;
	
КонецПроцедуры

#Область ХарактеристикиПрофиля

// Проверка соответствия значения характеристики РЕГИОНЫ_ПОКАЗА значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_РЕГИОНЫ_ПОКАЗА(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)

	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Число - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = РАЗМЕР_ПРЕДПРИЯТИЯ_ЧИСЛЕННОСТЬ();
		
	ЧисленностьРаботников = ЗначениеХарактеристикиВладельца;	
	Если ЧисленностьРаботников >= 60 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.КРУПНОЕ_ПРЕДПРИЯТИЕ;
	ИначеЕсли ЧисленностьРаботников >= 25 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.СРЕДНЕЕ_ПРЕДПРИЯТИЕ;
	ИначеЕсли ЧисленностьРаботников >= 5 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.МАЛОЕ_ПРЕДПРИЯТИЕ;
	ИначеЕсли ЧисленностьРаботников >= 1 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.МИКРОПРЕДПРИЯТИЕ;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.НЕТ_НАЕМНЫХ_СОТРУДНИКОВ;
	КонецЕсли;
		
	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Число - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = РАЗМЕР_ПРЕДПРИЯТИЯ_ДОХОД();
		
	СуммаДохода = ЗначениеХарактеристикиВладельца;
	Если СуммаДохода = Неопределено Тогда
		СуммаДохода = 0;
	КонецЕсли;
			
	Если СуммаДохода >= 450000000 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.КРУПНОЕ_ПРЕДПРИЯТИЕ;
	ИначеЕсли СуммаДохода >= 60000000 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.СРЕДНЕЕ_ПРЕДПРИЯТИЕ;
	ИначеЕсли СуммаДохода >= 2400000 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.МАЛОЕ_ПРЕДПРИЯТИЕ;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.МИКРОПРЕДПРИЯТИЕ;
	КонецЕсли;
					
	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(ДанныеТаргета,
		ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Число - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = РАЗМЕР_ПРЕДПРИЯТИЯ_ОБОРОТ_ПО_СЧЕТАМ();

	СуммаОборота = ЗначениеХарактеристикиВладельца;
	Если СуммаОборота = Неопределено Тогда
		СуммаОборота = 0;
	КонецЕсли;
				
	Если СуммаОборота >= 450000000 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.КРУПНОЕ_ПРЕДПРИЯТИЕ;
	ИначеЕсли СуммаОборота >= 60000000 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.СРЕДНЕЕ_ПРЕДПРИЯТИЕ;
	ИначеЕсли СуммаОборота >= 2400000 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.МАЛОЕ_ПРЕДПРИЯТИЕ;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.МИКРОПРЕДПРИЯТИЕ;
	КонецЕсли;		
					
	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики ОСНОВНЫЕ_СРЕДСТВА значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Число - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_ОСНОВНЫЕ_СРЕДСТВА(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = ОСНОВНЫЕ_СРЕДСТВА();
			
	СуммаОсновныеСредства = ЗначениеХарактеристикиВладельца;
	Если СуммаОсновныеСредства = Неопределено Тогда
		СуммаОсновныеСредства = 0;
	КонецЕсли;
			
	Если СуммаОсновныеСредства >= 200000000 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.СВЫШЕ_ПРЕДЕЛА_УСН;
	ИначеЕсли СуммаОсновныеСредства >= 1 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.В_ПРЕДЕЛАХ_УСН;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.НЕТ;
	КонецЕсли;
		
	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Дата - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = СРОК_ЖИЗНИ_ПРЕДПРИЯТИЯ();
	
	ДатаРегистрации = ЗначениеХарактеристикиВладельца;
	
	Если ДатаРегистрации = Неопределено 
		ИЛИ ДатаРегистрации = Дата(1,1,1) Тогда
		Возврат;
	КонецЕсли;
	
	СрокЖизни = КоличествоЛет(ДатаРегистрации);
	Если СрокЖизни < 1 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.ДО_1_ГОДА;
	ИначеЕсли СрокЖизни < 3 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.ДО_3_ЛЕТ;
	ИначеЕсли СрокЖизни < 10 Тогда 
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.ДО_10_ЛЕТ;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.СВЫШЕ_10_ЛЕТ;
	КонецЕсли;
		
	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики ВОЗРАСТ_ИП значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Дата - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_ВОЗРАСТ_ИП(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = ВОЗРАСТ_ИП();
	
	ДатаРождения = ЗначениеХарактеристикиВладельца;
			
	Если ДатаРождения = Неопределено 
		ИЛИ ДатаРождения = Дата(1,1,1) Тогда
		Возврат;
	КонецЕсли;
	
	ВозрастИП = КоличествоЛет(ДатаРождения);
	Если ВозрастИП < 25 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.ДО_25_ЛЕТ;
	ИначеЕсли ВозрастИП < 65 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.ДО_65_ЛЕТ;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.СВЫШЕ_65_ЛЕТ;
	КонецЕсли;		

	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики УЧЕТ_КОМАНДИРОВОК значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Число - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_УЧЕТ_КОМАНДИРОВОК(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = УЧЕТ_КОМАНДИРОВОК();
	
	КоличествоДокументов = ЗначениеХарактеристикиВладельца;
	Если КоличествоДокументов = Неопределено Тогда
		КоличествоДокументов = 0;
	КонецЕсли;
	
	Если КоличествоДокументов > 50 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.БОЛЕЕ_50;
	ИначеЕсли КоличествоДокументов > 10 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.БОЛЕЕ_10;
	ИначеЕсли КоличествоДокументов > 0 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.МЕНЕЕ_10;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.НЕТ;
	КонецЕсли;		

	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Число - на выходе приводится к типу ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = КОЛ_ВО_КРЕДИТНЫХ_ДОГОВОРОВ();
			
	КоличествоДоговоров = ЗначениеХарактеристикиВладельца;
	Если КоличествоДоговоров = Неопределено Тогда
		КоличествоДоговоров = 0;
	КонецЕсли;
			
	Если КоличествоДоговоров > 3 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.БОЛЕЕ_3;
	ИначеЕсли КоличествоДоговоров >= 1 Тогда
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.ОТ_1_ДО_3;
	Иначе
		ЗначениеХарактеристикиВладельца = ДопустимыеЗначения.НЕТ;
	КонецЕсли;
		
	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики ОКОПФ значениям таргета
// Значения ОКОПФ могут быть такими: "14155", "14000"
// Таргеты могут иметь такие значения ОКОПФ "14" и сопоставлять с имеющимся значением в организации "14155"
// Если у организации указан ОКОПФ "14155" а в таргете "14" - нам такой таргет подходит.
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_ОКОПФ(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ЕстьВхождение = Ложь;
	МассивЗначенийТаргета = СтрРазделить(ДанныеТаргета.ЗначениеТаргета, ",");
	Для Каждого СтрокаЗначения Из МассивЗначенийТаргета Цикл
		
		ЕстьВхождение = СтрНачинаетсяС(ЗначениеХарактеристикиВладельца, СтрокаЗначения);
		Если ЕстьВхождение Тогда
			Прервать;
		КонецЕсли;
				
	КонецЦикла;
	
	РезультатПроверки = ЕстьВхождение И Не ДанныеТаргета.ЭтоТаргетВидаКРОМЕ
		ИЛИ Не ЕстьВхождение И ДанныеТаргета.ЭтоТаргетВидаКРОМЕ;
				
КонецПроцедуры

// Проверка соответствия значения характеристики КОД_ОКВЭД значениям таргета
// Значения ОКВЭД могут быть такими: "51", "51.10", "51.10.3"
// Таргеты могут иметь такие значения ОКВЭД "51" и сопоставлять с имеющимся значением в организации "51.10.3".
// Если у организации указан ОКВЭД "51.10.3" а в таргете указано "51" - нам такой таргет подходит.
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_КОД_ОКВЭД(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ЕстьВхождение = Ложь;
	МассивЗначенийТаргета = СтрРазделить(ДанныеТаргета.ЗначениеТаргета, ",");		
	Для Каждого СтрокаЗначения Из МассивЗначенийТаргета Цикл 
		
		Если СтрДлина(ЗначениеХарактеристикиВладельца) > СтрДлина(СтрокаЗначения) И Не ПустаяСтрока(СтрокаЗначения) Тогда
			
			Если СтрНачинаетсяС(ЗначениеХарактеристикиВладельца, СтрокаЗначения) Тогда
				ЕстьВхождение = Истина;
				Прервать;
			КонецЕсли;
						
		ИначеЕсли СтрДлина(ЗначениеХарактеристикиВладельца) < СтрДлина(СтрокаЗначения) 
			И Не ПустаяСтрока(ЗначениеХарактеристикиВладельца) Тогда
			
			Если СтрНачинаетсяС(СтрокаЗначения, ЗначениеХарактеристикиВладельца) Тогда
				ЕстьВхождение = Истина;
				Прервать;
			КонецЕсли;
			
		ИначеЕсли СтрДлина(ЗначениеХарактеристикиВладельца) = СтрДлина(СтрокаЗначения) 
			И ЗначениеХарактеристикиВладельца = СтрокаЗначения Тогда
			
			ЕстьВхождение = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатПроверки = ЕстьВхождение И Не ДанныеТаргета.ЭтоТаргетВидаКРОМЕ
		ИЛИ Не ЕстьВхождение И ДанныеТаргета.ЭтоТаргетВидаКРОМЕ;	
				
КонецПроцедуры

// Проверка соответствия значения характеристики СИСТЕМА_НАЛОГООБЛОЖЕНИЯ значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_СИСТЕМА_НАЛОГООБЛОЖЕНИЯ(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)
	
	ДопустимыеЗначения = СИСТЕМА_НАЛОГООБЛОЖЕНИЯ();

	Если ПустаяСтрока(ЗначениеХарактеристикиВладельца) Тогда
		Возврат;
	КонецЕсли;
	
	Контекст = "РекламныйСервисПереопределяемый.ЗаполнитьСистемуНалогообложения";			
	Условие = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
		ДопустимыеЗначения, ЗначениеХарактеристикиВладельца);
	ОбщегоНазначенияКлиентСервер.Проверить(
		Условие, 
		НСтр("ru = 'Не допустимое значение'", ОбщегоНазначения.КодОсновногоЯзыка()), 
		Контекст);
				
	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

#Область ОбщиеХарактеристики

// Проверка соответствия значения характеристики ФОРМА_ПОСТАВКИ_ПП значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_ФОРМА_ПОСТАВКИ_ПП(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)

	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики ВЕРСИЯ_ПП значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_ВЕРСИЯ_ПП(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)

	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

// Проверка соответствия значения характеристики ГРУППА_КОНФИГУРАЦИЙ значениям таргета
// 
// Параметры:
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ЗначениеХарактеристикиВладельца - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  РезультатПроверки - Булево - Результат проверки
//  
Процедура ПроверкаСоответствияЗначенияХарактеристики_ГРУППА_КОНФИГУРАЦИЙ(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки)

	ПроверкаВхожденияЗначенияХарактеристикиВЗначенияТаргета(
		ДанныеТаргета, ЗначениеХарактеристикиВладельца, РезультатПроверки);
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ХарактеристикиПрочее

// Владельцы характеристики.
// 
// Параметры:
//  ВидХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы
//  ТаблицаНовыеХарактеристики - см. НовыйТаблицаХарактеристик
// 
// Возвращаемое значение:
//  Массив Из ОпределяемыйТип.ВладелецХарактеристикиРекламы
//
Функция ВладельцыХарактеристики(ВидХарактеристики, ТаблицаНовыеХарактеристики)
		
	Отбор = Новый Структура("ВидХарактеристики", ВидХарактеристики);
	МассивСтрокТаблицы = ТаблицаНовыеХарактеристики.НайтиСтроки(Отбор);
	ТаблицаВладельцев = ТаблицаНовыеХарактеристики.Скопировать(МассивСтрокТаблицы, "ВладелецХарактеристики");
	ТаблицаВладельцев.Свернуть("ВладелецХарактеристики");
	ВладельцыХарактеристики = ТаблицаВладельцев.ВыгрузитьКолонку("ВладелецХарактеристики");
	
	Возврат ВладельцыХарактеристики
		
КонецФункции

// Заполнить период прошлого года.
// 
// Параметры:
//  Параметры - см. НовыйПараметрыПериода
//
Процедура ЗаполнитьПериодПоследнихДвенадцатиМесяцев(Параметры)
	
	ТекущаяДата = ТекущаяДатаСеанса();
	Параметры.КонецПериода = НачалоМесяца(ТекущаяДата) - 1;
	Параметры.НачалоПериода = ДобавитьМесяц(НачалоМесяца(Параметры.КонецПериода), - 11);
	
КонецПроцедуры

// Заполнить период предыдущего месяца.
// 
// Параметры:
//  Параметры - см. НовыйПараметрыПериода
//
Процедура ЗаполнитьПериодПредыдущегоМесяца(Параметры)
	
	ТекущаяДата = ТекущаяДатаСеанса();
	Параметры.КонецПериода = НачалоМесяца(ТекущаяДата) - 1;
	Параметры.НачалоПериода = НачалоМесяца(Параметры.КонецПериода);
	
КонецПроцедуры

// Заполнить таблицу видами характеристик.
// 
// Параметры:
//  ПараметрыХарактеристик см. НовыйПараметрыХарактеристик
//
Процедура ЗаполнитьТаблицуВидамиХарактеристик(ПараметрыХарактеристик)
	
	Таблица = ПараметрыХарактеристик.ТаблицаХарактеристикВсе;
	ВидыХарактеристик = ПараметрыХарактеристик.ВидыХарактеристик;
	
	Для Каждого ВидХарактеристики Из ВидыХарактеристик Цикл
		
		СвойстваХарактеристики = ВидХарактеристики.Значение; // см. НовыйРеквизитыХарактеристики
		ИдентификаторХарактеристики = РекламныйСервисСлужебный.ПривестиТипКВидХарактеристики(СвойстваХарактеристики.ID); // ОпределяемыйТип.ВидХарактеристикиРекламы
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ИдентификаторХарактеристики", ИдентификаторХарактеристики);
		Если ПараметрыХарактеристик.ТаблицаХарактеристикРекламы.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ВладельцыХарактеристики = Новый Массив; // Массив из ОпределяемыйТип.ВладелецХарактеристикиРекламы
		Если ПустаяСтрока(СвойстваХарактеристики.МетодПолученияВладельцев) Тогда
			
			ВладельцыХарактеристики.Добавить(РекламныйСервисСлужебный.ПустойВладелецХарактеристики());
			
		Иначе
			
			Параметры = Новый Массив(); // Массив из Произвольный, Массив
			Параметры.Добавить(ВладельцыХарактеристики);
			
			// см. РекламныйСервисСлужебный.МетодыПолученияВладельцевХарактеристик
			// * РекламныйСервисСлужебный.СписокРазрешенныхОрганизаций
			// * РекламныйСервисСлужебный.РекламныйСервисСлужебный.ТекущийПрофильПотребителяСписком
			ОбщегоНазначения.ВыполнитьМетодКонфигурации(СвойстваХарактеристики.МетодПолученияВладельцев, Параметры);
			
		КонецЕсли;
		
		Для Каждого Владелец Из ВладельцыХарактеристики Цикл
		
			СтрокаХарактеристик = Таблица.Добавить();
			СтрокаХарактеристик.ВладелецХарактеристики = Владелец;
			СтрокаХарактеристик.ВидХарактеристики = ИдентификаторХарактеристики;
			СтрокаХарактеристик.ЧастотаОбновления = СвойстваХарактеристики.ЧастотаОбновления;
		
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Удалить не актуальные характеристики.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//
Процедура УдалитьНеАктуальныеХарактеристики(ПараметрыХарактеристик)
	
	РезультатЗапроса = ВыборкаНеАктуальныхХарактеристик(ПараметрыХарактеристик);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Блокировка = Новый БлокировкаДанных;
		ИмяПространстваБлокировок = "РегистрСведений.ЗначенияХарактеристикПотребителяРекламы";
		ЭлементБлокировки = Блокировка.Добавить(ИмяПространстваБлокировок);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВладелецХарактеристики", "ВладелецХарактеристики");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		Блокировка.Заблокировать();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ВладелецХарактеристики = Выборка.ВладелецХарактеристики; // ОпределяемыйТип.ВладелецХарактеристикиРекламы
			ВидХарактеристики = Выборка.Характеристика; // ОпределяемыйТип.ЗначениеХарактеристикиРекламы
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВладелецХарактеристики.Установить(ВладелецХарактеристики);
			НаборЗаписей.Отбор.Характеристика.Установить(ВидХарактеристики);
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Заполнить новые виды характеристик.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//
Процедура ЗаполнитьНовыеВидыХарактеристик(ПараметрыХарактеристик)
	
	ВыборкаГруппировка = ВыборкаНовыеВидыХарактеристик(ПараметрыХарактеристик);
	
	Пока ВыборкаГруппировка.Следующий() Цикл
		
		ВидХарактеристики = ВыборкаГруппировка.ВидХарактеристики;
		ЗаполнитьВидХарактеристики(ПараметрыХарактеристик, СокрЛП(ВидХарактеристики));
		
	КонецЦикла;

	ОтфильтроватьЗначенияХарактеристикПолностьюПокрывающиеТаргетыРеклам(ПараметрыХарактеристик);
	
КонецПроцедуры

// Отфильтровывает значения характеристик потребителя, оставляя только полностью покрывающие список таргетов рекламы. 
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//
Процедура ОтфильтроватьЗначенияХарактеристикПолностьюПокрывающиеТаргетыРеклам(ПараметрыХарактеристик)
	
	ТаргетыРекламы = ПараметрыХарактеристик.ТаблицаХарактеристикРекламы;
	ЗначенияХарактеристикПотребителя = ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаргетыРекламы.ИдентификаторРекламы КАК ИдентификаторРекламы,
		|	ТаргетыРекламы.ИдентификаторХарактеристики КАК ИдентификаторХарактеристики,
		|	ТаргетыРекламы.ЗначениеХарактеристики КАК ЗначениеХарактеристики
		|ПОМЕСТИТЬ ТаргетыРекламы
		|ИЗ
		|	&ТаргетыРекламы КАК ТаргетыРекламы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторХарактеристики,
		|	ЗначениеХарактеристики
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеЗначенияХарактеристикПотребителя.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.ВидХарактеристики КАК ВидХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.Значение КАК Значение,
		|	НовыеЗначенияХарактеристикПотребителя.ТекущееЗначениеХарактеристики КАК ТекущееЗначениеХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.ЧастотаОбновления КАК ЧастотаОбновления
		|ПОМЕСТИТЬ НовыеЗначенияХарактеристикПотребителя
		|ИЗ
		|	&ЗначенияХарактеристикПотребителя КАК НовыеЗначенияХарактеристикПотребителя
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВладелецХарактеристики,
		|	ВидХарактеристики,
		|	Значение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеЗначенияХарактеристикПотребителя.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.ВидХарактеристики КАК ВидХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.Значение КАК Значение
		|ПОМЕСТИТЬ ВсеЗначенияХарактеристикПотребителя
		|ИЗ
		|	НовыеЗначенияХарактеристикПотребителя КАК НовыеЗначенияХарактеристикПотребителя
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗначенияХарактеристикПотребителя.ВладелецХарактеристики,
		|	ЗначенияХарактеристикПотребителя.Характеристика,
		|	ЗначенияХарактеристикПотребителя.Значение
		|ИЗ
		|	РегистрСведений.ЗначенияХарактеристикПотребителяРекламы КАК ЗначенияХарактеристикПотребителя
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидХарактеристики,
		|	Значение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаргетыРекламы.ИдентификаторРекламы КАК ИдентификаторРекламы,
		|	КОЛИЧЕСТВО(ТаргетыРекламы.ИдентификаторХарактеристики) КАК КоличествоТаргетов
		|ПОМЕСТИТЬ КоличествоТаргетовРекламы
		|ИЗ
		|	ТаргетыРекламы КАК ТаргетыРекламы
		|СГРУППИРОВАТЬ ПО
		|	ТаргетыРекламы.ИдентификаторРекламы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеЗначенияХарактеристикПотребителя.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	ВсеЗначенияХарактеристикПотребителя.ВидХарактеристики КАК ВидХарактеристики,
		|	ВсеЗначенияХарактеристикПотребителя.Значение КАК Значение,
		|	ТаргетыРекламы.ИдентификаторРекламы КАК ИдентификаторРекламы
		|ПОМЕСТИТЬ ВсеЗначенияХарактеристикПотребителяСРекламой
		|ИЗ
		|	ВсеЗначенияХарактеристикПотребителя КАК ВсеЗначенияХарактеристикПотребителя
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаргетыРекламы КАК ТаргетыРекламы
		|		ПО ВсеЗначенияХарактеристикПотребителя.ВидХарактеристики = ТаргетыРекламы.ИдентификаторХарактеристики
		|		И ВсеЗначенияХарактеристикПотребителя.Значение = ТаргетыРекламы.ЗначениеХарактеристики
		|СГРУППИРОВАТЬ ПО
		|	ВсеЗначенияХарактеристикПотребителя.ВладелецХарактеристики,
		|	ВсеЗначенияХарактеристикПотребителя.ВидХарактеристики,
		|	ВсеЗначенияХарактеристикПотребителя.Значение,
		|	ТаргетыРекламы.ИдентификаторРекламы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторРекламы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеЗначенияХарактеристикПотребителяСРекламой.ИдентификаторРекламы КАК ИдентификаторРекламы,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВсеЗначенияХарактеристикПотребителяСРекламой.ВидХарактеристики) КАК КоличествоХарактеристик
		|ПОМЕСТИТЬ КоличествоХарактеристикПотребителя
		|ИЗ
		|	ВсеЗначенияХарактеристикПотребителяСРекламой КАК ВсеЗначенияХарактеристикПотребителяСРекламой
		|СГРУППИРОВАТЬ ПО
		|	ВсеЗначенияХарактеристикПотребителяСРекламой.ИдентификаторРекламы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КоличествоХарактеристикПотребителя.ИдентификаторРекламы КАК ИдентификаторРекламы
		|ПОМЕСТИТЬ РекламыСовпавшиеПолностьюПоЗначениямХарактеристикПотребителя
		|ИЗ
		|	КоличествоХарактеристикПотребителя КАК КоличествоХарактеристикПотребителя
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КоличествоТаргетовРекламы КАК КоличествоТаргетовРекламы
		|		ПО КоличествоХарактеристикПотребителя.ИдентификаторРекламы = КоличествоТаргетовРекламы.ИдентификаторРекламы
		|		И КоличествоХарактеристикПотребителя.КоличествоХарактеристик = КоличествоТаргетовРекламы.КоличествоТаргетов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторРекламы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеЗначенияХарактеристикПотребителяСРекламой.ИдентификаторРекламы КАК ИдентификаторРекламы,
		|	ВсеЗначенияХарактеристикПотребителяСРекламой.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	ВсеЗначенияХарактеристикПотребителяСРекламой.ВидХарактеристики КАК ВидХарактеристики,
		|	ВсеЗначенияХарактеристикПотребителяСРекламой.Значение КАК Значение
		|ПОМЕСТИТЬ ЗначенияХарактеристикСовпавшиеСоВсемиТаргетамиРекламы
		|ИЗ
		|	ВсеЗначенияХарактеристикПотребителяСРекламой КАК ВсеЗначенияХарактеристикПотребителяСРекламой
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РекламыСовпавшиеПолностьюПоЗначениямХарактеристикПотребителя КАК
		|			РекламыСовпавшиеПолностьюПоЗначениямХарактеристикПотребителя
		|		ПО ВсеЗначенияХарактеристикПотребителяСРекламой.ИдентификаторРекламы = РекламыСовпавшиеПолностьюПоЗначениямХарактеристикПотребителя.ИдентификаторРекламы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВладелецХарактеристики,
		|	ВидХарактеристики,
		|	Значение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеЗначенияХарактеристикПотребителя.ВладелецХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.ВидХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.Значение,
		|	НовыеЗначенияХарактеристикПотребителя.ТекущееЗначениеХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.ЧастотаОбновления
		|ИЗ
		|	НовыеЗначенияХарактеристикПотребителя КАК НовыеЗначенияХарактеристикПотребителя
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗначенияХарактеристикСовпавшиеСоВсемиТаргетамиРекламы КАК
		|			ЗначенияХарактеристикСовпавшиеСоВсемиТаргетамиРекламы
		|		ПО НовыеЗначенияХарактеристикПотребителя.ВладелецХарактеристики = ЗначенияХарактеристикСовпавшиеСоВсемиТаргетамиРекламы.ВладелецХарактеристики
		|		И
		|			НовыеЗначенияХарактеристикПотребителя.ВидХарактеристики = ЗначенияХарактеристикСовпавшиеСоВсемиТаргетамиРекламы.ВидХарактеристики
		|		И НовыеЗначенияХарактеристикПотребителя.Значение = ЗначенияХарактеристикСовпавшиеСоВсемиТаргетамиРекламы.Значение
		|СГРУППИРОВАТЬ ПО
		|	НовыеЗначенияХарактеристикПотребителя.ВладелецХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.ВидХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.Значение,
		|	НовыеЗначенияХарактеристикПотребителя.ТекущееЗначениеХарактеристики,
		|	НовыеЗначенияХарактеристикПотребителя.ЧастотаОбновления";
	
	Запрос.УстановитьПараметр("ТаргетыРекламы", ТаргетыРекламы);
	Запрос.УстановитьПараметр("ЗначенияХарактеристикПотребителя", ЗначенияХарактеристикПотребителя);
	
	РезультатЗапроса = Запрос.Выполнить();
	ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик = РезультатЗапроса.Выгрузить();
	
КонецПроцедуры


// Опции сервисов ИТС.
// 
// Параметры:
//  ТаблицаНовыеХарактеристики - см. НовыйТаблицаХарактеристик
//  ВидыХарактеристик - см. ВидыХарактеристик
// 
// Возвращаемое значение:
//  Массив из ОпределяемыйТип.ЗначениеХарактеристикиРекламы - список опций.
//
Функция ОпцииСервисовИТС(ТаблицаНовыеХарактеристики, ВидыХарактеристик)
	
	ВсеОпцииИТСВТаргетах = Новый Массив; // Массив из ОпределяемыйТип.ЗначениеХарактеристикиРекламы
	
	Для Каждого СтрокаСервиса Из ТаблицаНовыеХарактеристики Цикл
		
		Если СтрокаСервиса.ВидХарактеристики = ВидыХарактеристик.СЕРВИС_АКТИВЕН.ID 
			Или СтрокаСервиса.ВидХарактеристики = ВидыХарактеристик.СЕРВИС_НЕ_АКТИВЕН.ID 
			Или СтрокаСервиса.ВидХарактеристики = ВидыХарактеристик.СЕРВИС_БЫЛ_АКТИВЕН.ID Тогда
			
			ЗначениеХарактеристики = СтрокаСервиса.Значение;
			ВсеОпцииИТСВТаргетах.Добавить(ЗначениеХарактеристики);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВсеОпцииИТСВТаргетах;
	
КонецФункции

#КонецОбласти

#Область Конструкторы

// Новый таблица характеристик.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * ВладелецХарактеристики - ОпределяемыйТип.ВладелецХарактеристикиРекламы
// * ВидХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы - идентификатор характеристики.
// * Значение - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Произвольный - значение характеристики.
// * ЧастотаОбновления - Строка
//
Функция НовыйТаблицаХарактеристик()
	
	Типы = РекламныйСервисСлужебный.Типы();
	
	ТаблицаХарактеристик = Новый ТаблицаЗначений;
	ТаблицаХарактеристик.Колонки.Добавить(
		"ВладелецХарактеристики", Метаданные.ОпределяемыеТипы.ВладелецХарактеристикиРекламы.Тип);
	ТаблицаХарактеристик.Колонки.Добавить(
		"ВидХарактеристики", Метаданные.ОпределяемыеТипы.ВидХарактеристикиРекламы.Тип);
	ТаблицаХарактеристик.Колонки.Добавить(
		"Значение", Метаданные.ОпределяемыеТипы.ЗначениеХарактеристикиРекламы.Тип);
	ТаблицаХарактеристик.Колонки.Добавить(
		"ЧастотаОбновления", Типы.Строка100);
	
	Возврат ТаблицаХарактеристик;
	
КонецФункции

// Новый таблица характеристик.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * ВладелецХарактеристики - ОпределяемыйТип.ВладелецХарактеристикиРекламы
// * ВидХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы - идентификатор характеристики.
// * Значение - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Произвольный - значение характеристики.
// * ТекущееЗначениеХарактеристики - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Произвольный - текущее значение характеристики.
// * ЧастотаОбновления - Строка
//
Функция НовыйИтоговаяТаблицаХарактеристик()
	
	ТаблицаХарактеристик = НовыйТаблицаХарактеристик();
	ТаблицаХарактеристик.Колонки.Добавить(
		"ТекущееЗначениеХарактеристики", Метаданные.ОпределяемыеТипы.ЗначениеХарактеристикиРекламы.Тип);
	
	Возврат ТаблицаХарактеристик;
	
КонецФункции

// Новый характеристики без владельцев.
// 
// Возвращаемое значение:
//  ТаблицаЗначений -  Новый характеристики без владельцев:
// * ВидХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы - идентификатор характеристики.
// * Значение - ОпределяемыйТип.ЗначениеХарактеристикиРекламы, Произвольный - значение характеристики.
//
Функция НовыйХарактеристикиБезВладельцев()
	
	ТаблицаХарактеристик = НовыйТаблицаХарактеристик();
	ИндексКолонки = ТаблицаХарактеристик.Колонки.Найти("ВладелецХарактеристики");
	ТаблицаХарактеристик.Колонки.Удалить(ИндексКолонки);
	
	Возврат ТаблицаХарактеристик;
	
КонецФункции

// Новый описание характеристики.
// 
// Параметры:
//  ИмяХарактеристики    - Строка
//  ЧастотаОбновления    - Строка
//  МетодПолученияВладельцев - Строка
// 
// Возвращаемое значение:
//  Структура:
// * ID                   - ОпределяемыйТип.ВидХарактеристикиРекламы, Строка, Произвольный - идентификатор характеристики.
// * ЧастотаОбновления    - Строка
// * МетодПолученияВладельцев - Строка
//
Функция НовыйРеквизитыХарактеристики(
		Знач ИмяХарактеристики, Знач ЧастотаОбновления, Знач МетодПолученияВладельцев = "")
	
	Результат = Новый Структура;
	Результат.Вставить("ID", РекламныйСервисСлужебный.ПривестиТипКВидХарактеристики(ИмяХарактеристики));
	Результат.Вставить("ЧастотаОбновления", ЧастотаОбновления);
	Результат.Вставить("МетодПолученияВладельцев", МетодПолученияВладельцев);
	
	Возврат Результат;
	
КонецФункции

// Новый параметры характеристик.
// 
// Возвращаемое значение:
//  Структура:
// * ВидыХарактеристик см. ВидыХарактеристик
// * ТаблицаХарактеристикРекламы см. Справочники.Реклама.НовыйТаблицаТаргетыРекламыИзДанныхСервиса
// * СтатусыОпцийИТС см. РегистрыСведений.ЗначенияХарактеристикРекламы.НовыйТаблицаСтатусовОпцийИТС
// * ТаблицаХарактеристикВсе см. НовыйТаблицаХарактеристик
// * ТаблицаНовыеХарактеристики см. НовыйТаблицаХарактеристик
// * ТаблицаНовыеХарактеристикиБезВладельцев см. НовыйХарактеристикиБезВладельцев
// * ИтоговаяТаблицаХарактеристик см. НовыйТаблицаХарактеристик
// * СопоставленныеТаргетыКРОМЕ см. СопоставленныеТаргетыКРОМЕ
//
Функция НовыйПараметрыХарактеристик()
	
	Результат = Новый Структура;
	Результат.Вставить("ВидыХарактеристик", ВидыХарактеристик());
	Результат.Вставить("ТаблицаХарактеристикРекламы", Справочники.Реклама.НовыйТаблицаТаргетыРекламыИзДанныхСервиса());
	Результат.Вставить("СтатусыОпцийИТС", РегистрыСведений.ЗначенияХарактеристикРекламы.НовыйТаблицаСтатусовОпцийИТС());
	Результат.Вставить("ТаблицаХарактеристикВсе", НовыйТаблицаХарактеристик());
	Результат.Вставить("ТаблицаНовыеХарактеристики", НовыйТаблицаХарактеристик());
	Результат.Вставить("ТаблицаНовыеХарактеристикиБезВладельцев", НовыйХарактеристикиБезВладельцев());
	Результат.Вставить("ИтоговаяТаблицаХарактеристик", НовыйИтоговаяТаблицаХарактеристик());
	Результат.Вставить("СопоставленныеТаргетыКРОМЕ", СопоставленныеТаргетыКРОМЕ());
	
	Возврат Результат;
	
КонецФункции

// Новый параметры переопределения характеристик.
// 
// Параметры:
//  ЧастотаОбновления - Строка
// 
// Возвращаемое значение:
//  Структура:
// * НачалоПериода - Дата
// * КонецПериода - Дата
//
Функция НовыйПараметрыПериода(ЧастотаОбновления = "") Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НачалоПериода", Дата(1, 1, 1));
	Результат.Вставить("КонецПериода", Дата(1, 1, 1));
	
	ВариантыЧастотыОбновленияХарактеристики = РекламныйСервисСлужебныйПовтИсп.ВариантыЧастотыОбновленияХарактеристики();
	Если ЧастотаОбновления = ВариантыЧастотыОбновленияХарактеристики.Год Тогда
		ЗаполнитьПериодПоследнихДвенадцатиМесяцев(Результат);
	ИначеЕсли ЧастотаОбновления = ВариантыЧастотыОбновленияХарактеристики.Месяц Тогда
		ЗаполнитьПериодПредыдущегоМесяца(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Новый данные владельцев.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - ОпределяемыйТип.ВладелецХарактеристикиРекламы
//  * Значение - Строка, Число, Дата, Массив из Строка - Значение характеристики или список значений характеристик.
//
Функция НовыйДанныеВладельцев()
	
	Результат = Новый Соответствие;
	Возврат Результат;
	
КонецФункции

// Новый данные таргета для проверки.
// 
// Параметры:
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики 
//  ЗначениеТаргета - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  ЭтоТаргетВидаКРОМЕ - Булево
// 
// Возвращаемое значение:
//  Структура - Новый данные таргета для проверки:
//  * ВидХарактеристики - см. НовыйРеквизитыХарактеристики 
//  * ЗначениеТаргета - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  * ЭтоТаргетВидаКРОМЕ - Булево
Функция НовыйДанныеТаргетаДляПроверки(ВидХарактеристики, ЗначениеТаргета, ЭтоТаргетВидаКРОМЕ)
	
	Результат = Новый Структура;
	Результат.Вставить("ВидХарактеристики", ВидХарактеристики);
	Результат.Вставить("ЗначениеТаргета", ЗначениеТаргета);
	Результат.Вставить("ЭтоТаргетВидаКРОМЕ", ЭтоТаргетВидаКРОМЕ);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Запросы

// Выборка не актуальных характеристик.
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
// 
// Возвращаемое значение:
//  РезультатЗапроса:
// * ВладелецХарактеристики - ОпределяемыйТип.ВладелецХарактеристикиРекламы
// * ВидХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы
//
Функция ВыборкаНеАктуальныхХарактеристик(ПараметрыХарактеристик)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВсеВидыХарактеристик.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	ВсеВидыХарактеристик.ВидХарактеристики КАК ВидХарактеристики,
		|	ВсеВидыХарактеристик.ЧастотаОбновления КАК ЧастотаОбновления
		|ПОМЕСТИТЬ ВсеВидыХарактеристик
		|ИЗ
		|	&ТаблицаХарактеристикВсе КАК ВсеВидыХарактеристик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаХарактеристикРекламы.ИдентификаторХарактеристики КАК ИдентификаторХарактеристики,
		|	ТаблицаХарактеристикРекламы.ЗначениеХарактеристики КАК ЗначениеХарактеристики
		|ПОМЕСТИТЬ ТаблицаХарактеристикРекламы
		|ИЗ
		|	&ТаблицаХарактеристикРекламы КАК ТаблицаХарактеристикРекламы
		|ГДЕ
		|	НЕ ТаблицаХарактеристикРекламы.ИдентификаторХарактеристики = """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗначенияХарактеристикПотребителя.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	ЗначенияХарактеристикПотребителя.Характеристика КАК Характеристика
		|ИЗ
		|	РегистрСведений.ЗначенияХарактеристикПотребителяРекламы КАК ЗначенияХарактеристикПотребителя
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеВидыХарактеристик КАК ВсеВидыХарактеристик
		|		ПО ВсеВидыХарактеристик.ВладелецХарактеристики = ЗначенияХарактеристикПотребителя.ВладелецХарактеристики
		|		И ВсеВидыХарактеристик.ВидХарактеристики = ЗначенияХарактеристикПотребителя.Характеристика
		|		И ВЫБОР
		|			КОГДА ЗначенияХарактеристикПотребителя.ТекущееЗначениеХарактеристики = """" 
		|				И Не ЗначенияХарактеристикПотребителя.Характеристика В (&ТаргетыВидаКРОМЕ)
		|				ТОГДА ИСТИНА
		|			КОГДА ВсеВидыХарактеристик.ЧастотаОбновления = &ПериодГод
		|				ТОГДА &ТекущаяДата > ДОБАВИТЬКДАТЕ(ЗначенияХарактеристикПотребителя.ДатаОбновления, Год, 1)
		|			КОГДА ВсеВидыХарактеристик.ЧастотаОбновления = &ПериодМесяц
		|				ТОГДА &ТекущаяДата > ДОБАВИТЬКДАТЕ(ЗначенияХарактеристикПотребителя.ДатаОбновления, Месяц, 1)
		|			КОГДА ВсеВидыХарактеристик.ЧастотаОбновления = &ПериодДень
		|				ТОГДА &ТекущаяДата > ЗначенияХарактеристикПотребителя.ДатаОбновления
		|		КОНЕЦ
		|СГРУППИРОВАТЬ ПО
		|	ЗначенияХарактеристикПотребителя.ВладелецХарактеристики,
		|	ЗначенияХарактеристикПотребителя.Характеристика";
	
	Запрос.УстановитьПараметр("ТаблицаХарактеристикВсе", ПараметрыХарактеристик.ТаблицаХарактеристикВсе);
	Запрос.УстановитьПараметр("ТаблицаХарактеристикРекламы", ПараметрыХарактеристик.ТаблицаХарактеристикРекламы);
	Запрос.УстановитьПараметр("ТекущаяДата", ОбщегоНазначения.ТекущаяДатаПользователя());
	Запрос.УстановитьПараметр(
		"ПериодМесяц", РекламныйСервисСлужебныйПовтИсп.ВариантыЧастотыОбновленияХарактеристики().Месяц);
	Запрос.УстановитьПараметр(
		"ПериодГод", РекламныйСервисСлужебныйПовтИсп.ВариантыЧастотыОбновленияХарактеристики().Год);
	Запрос.УстановитьПараметр(
		"ПериодДень", РекламныйСервисСлужебныйПовтИсп.ВариантыЧастотыОбновленияХарактеристики().День);
	Запрос.УстановитьПараметр("ТаргетыВидаКРОМЕ", ТаргетыВидаКРОМЕ());
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса;
	
КонецФункции

// Выборка новые виды характеристик.
// 
// Параметры:
//  ПараметрыХарактеристик см. НовыйПараметрыХарактеристик
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - выборка по группировкам:
// * ВидХарактеристики - ОпределяемыйТип.ВидХарактеристикиРекламы
//
Функция ВыборкаНовыеВидыХарактеристик(ПараметрыХарактеристик)
	
	ТаблицаХарактеристикВсе = ПараметрыХарактеристик.ТаблицаХарактеристикВсе;
	ТаблицаХарактеристикРекламы = ПараметрыХарактеристик.ТаблицаХарактеристикРекламы;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВсеВидыХарактеристик.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	ВсеВидыХарактеристик.ВидХарактеристики КАК ВидХарактеристики
		|ПОМЕСТИТЬ ВсеВидыХарактеристик
		|ИЗ
		|	&ТаблицаХарактеристикВсе КАК ВсеВидыХарактеристик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаХарактеристикРекламы.ИдентификаторХарактеристики КАК ИдентификаторХарактеристики,
		|	ТаблицаХарактеристикРекламы.ЗначениеХарактеристики КАК ЗначениеХарактеристики
		|ПОМЕСТИТЬ ТаблицаХарактеристикРекламы
		|ИЗ
		|	&ТаблицаХарактеристикРекламы КАК ТаблицаХарактеристикРекламы
		|ГДЕ
		|	НЕ ТаблицаХарактеристикРекламы.ИдентификаторХарактеристики = """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеВидыХарактеристик.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	ВсеВидыХарактеристик.ВидХарактеристики КАК ВидХарактеристики
		|ПОМЕСТИТЬ ОтсутствующиеЗначенияХарактеристикПотребителей
		|ИЗ
		|	ВсеВидыХарактеристик КАК ВсеВидыХарактеристик
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияХарактеристикПотребителяРекламы КАК ЗначенияХарактеристикПотребителяРекламы
		|		ПО ВсеВидыХарактеристик.ВладелецХарактеристики = ЗначенияХарактеристикПотребителяРекламы.ВладелецХарактеристики
		|		И ВсеВидыХарактеристик.ВидХарактеристики = ЗначенияХарактеристикПотребителяРекламы.Характеристика
		|ГДЕ
		|	ЗначенияХарактеристикПотребителяРекламы.Значение ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтсутствующиеЗначенияХарактеристикПотребителей.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	ОтсутствующиеЗначенияХарактеристикПотребителей.ВидХарактеристики КАК ВидХарактеристики,
		|	ТаблицаХарактеристикРекламы.ЗначениеХарактеристики КАК Значение
		|ПОМЕСТИТЬ НовыеЗначенияХарактеристик
		|ИЗ
		|	ОтсутствующиеЗначенияХарактеристикПотребителей КАК ОтсутствующиеЗначенияХарактеристикПотребителей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаХарактеристикРекламы КАК ТаблицаХарактеристикРекламы
		|		ПО ОтсутствующиеЗначенияХарактеристикПотребителей.ВидХарактеристики = ТаблицаХарактеристикРекламы.ИдентификаторХарактеристики
		|СГРУППИРОВАТЬ ПО
		|	ОтсутствующиеЗначенияХарактеристикПотребителей.ВладелецХарактеристики,
		|	ОтсутствующиеЗначенияХарактеристикПотребителей.ВидХарактеристики,
		|	ТаблицаХарактеристикРекламы.ЗначениеХарактеристики
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеЗначенияХарактеристик.ВладелецХарактеристики КАК ВладелецХарактеристики,
		|	НовыеЗначенияХарактеристик.ВидХарактеристики КАК ВидХарактеристики,
		|	НовыеЗначенияХарактеристик.Значение
		|ИЗ
		|	НовыеЗначенияХарактеристик КАК НовыеЗначенияХарактеристик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеЗначенияХарактеристик.ВидХарактеристики КАК ВидХарактеристики,
		|	НовыеЗначенияХарактеристик.Значение
		|ИЗ
		|	НовыеЗначенияХарактеристик КАК НовыеЗначенияХарактеристик
		|СГРУППИРОВАТЬ ПО
		|	НовыеЗначенияХарактеристик.ВидХарактеристики,
		|	НовыеЗначенияХарактеристик.Значение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеЗначенияХарактеристик.ВидХарактеристики
		|ИЗ
		|	НовыеЗначенияХарактеристик КАК НовыеЗначенияХарактеристик
		|СГРУППИРОВАТЬ ПО
		|	НовыеЗначенияХарактеристик.ВидХарактеристики";
	
	Запрос.УстановитьПараметр("ТаблицаХарактеристикВсе", ТаблицаХарактеристикВсе);
	Запрос.УстановитьПараметр("ТаблицаХарактеристикРекламы", ТаблицаХарактеристикРекламы);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
		
	ПараметрыХарактеристик.ТаблицаНовыеХарактеристики = РезультатЗапроса[РезультатЗапроса.Количество() - 3].Выгрузить();
	ПараметрыХарактеристик.ТаблицаНовыеХарактеристикиБезВладельцев 
		= РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
	
	ВыборкаГруппировка = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выбрать();
	
	Возврат ВыборкаГруппировка;
	
КонецФункции

#КонецОбласти

// Создать наборы записей.
// 
// Параметры:
//  ТаблицаХарактеристик - см. НовыйИтоговаяТаблицаХарактеристик
//
Процедура СоздатьОбновитьНаборыЗаписей(ТаблицаХарактеристик)
	
	НачатьТранзакцию();
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Блокировка = Новый БлокировкаДанных;
		ИмяПространстваБлокировок = "РегистрСведений.ЗначенияХарактеристикПотребителяРекламы";
		ЭлементБлокировки = Блокировка.Добавить(ИмяПространстваБлокировок);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ТаблицаХарактеристик;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВладелецХарактеристики", "ВладелецХарактеристики");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "ВладелецХарактеристики");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Значение", "Значение");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТекущееЗначениеХарактеристики", "ТекущееЗначениеХарактеристики");
		Блокировка.Заблокировать();
		
		Для Каждого СтрокаХарактеристик Из ТаблицаХарактеристик Цикл
			
			ВладелецХарактеристики = СтрокаХарактеристик.ВладелецХарактеристики;
			ВидХарактеристики = СтрокаХарактеристик.ВидХарактеристики;
			Значение = СтрокаХарактеристик.Значение;
			ТекущееЗначениеХарактеристики = СтрокаХарактеристик.ТекущееЗначениеХарактеристики;
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВладелецХарактеристики.Установить(ВладелецХарактеристики);
			НаборЗаписей.Отбор.Характеристика.Установить(ВидХарактеристики);
			НаборЗаписей.Отбор.Значение.Установить(Значение);
			НаборЗаписей.Отбор.ТекущееЗначениеХарактеристики.Установить(ТекущееЗначениеХарактеристики);
			
			Запись = НаборЗаписей.Добавить();
			Запись.ВладелецХарактеристики = ВладелецХарактеристики;
			Запись.Характеристика = ВидХарактеристики;
			Запись.Значение = СтрокаХарактеристик.Значение;
			Запись.ТекущееЗначениеХарактеристики = СтрокаХарактеристик.ТекущееЗначениеХарактеристики;
			Запись.ДатаОбновления = ОбщегоНазначения.ТекущаяДатаПользователя();
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Обходит элементы значения характеристики владельца, если оно является коллекцией, и передает для дальнейшей 
// обработки (проверки и добавления в таблицу новых видов характеристик), иначе - только передает для дальнейшей обработки
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ВладелецХарактеристики - ОпределяемыйТип.ВладелецХарактеристикиРекламы
//  ЗначениеХарактеристикиВладельца - Строка, Число, Дата, Массив из Строка - Значение характеристики или список значений характеристик. 
//
Процедура ОбработатьЗначенияХарактеристикиВладельца(
		ПараметрыХарактеристик, ДанныеТаргета, ВладелецХарактеристики, ЗначениеХарактеристикиВладельца)
							
	Если ТипЗнч(ЗначениеХарактеристикиВладельца) = Тип("Массив") Тогда
		
		Для Каждого Элемент Из ЗначениеХарактеристикиВладельца Цикл
			
			ПроверитьДобавитьСтрокуТаблицы(
				ПараметрыХарактеристик, ДанныеТаргета, ВладелецХарактеристики, Элемент)					
					
		КонецЦикла;
		
	Иначе
				
		ПроверитьДобавитьСтрокуТаблицы(
			ПараметрыХарактеристик, ДанныеТаргета, ВладелецХарактеристики, ЗначениеХарактеристикиВладельца)					
	
	КонецЕсли;
			
КонецПроцедуры


// Выполняет проверку и добавления в таблицу новых видов характеристик
// 
// Параметры:
//  ПараметрыХарактеристик - см. НовыйПараметрыХарактеристик
//  ДанныеТаргета - см. НовыйДанныеТаргетаДляПроверки
//  ВладелецХарактеристики - ОпределяемыйТип.ВладелецХарактеристикиРекламы
//  ЗначениеХарактеристикиВладельца - Строка, Число, Дата, Массив из Строка - Значение характеристики или список значений характеристики 
//
Процедура ПроверитьДобавитьСтрокуТаблицы(ПараметрыХарактеристик, ДанныеТаргета, ВладелецХарактеристики,
	Знач ЗначениеХарактеристикиВладельца)
	
	ПроверкаПрошлаУспешно = Ложь;
	ПроверкаУсловийСоответствияЗначенийХарактеристики(
		ПараметрыХарактеристик.ВидыХарактеристик, ДанныеТаргета, ЗначениеХарактеристикиВладельца, ПроверкаПрошлаУспешно);
		
	Если Не ПроверкаПрошлаУспешно Тогда
		Возврат;	
	КонецЕсли;
								
	ДобавитьСтрокуТаблицы(
		ПараметрыХарактеристик.ИтоговаяТаблицаХарактеристик, 
		ВладелецХарактеристики, 
		ДанныеТаргета.ВидХарактеристики, 
		ДанныеТаргета.ЗначениеТаргета, 
		ЗначениеХарактеристикиВладельца);

КонецПроцедуры

// Добавить строку таблицы характеристик.
// 
// Параметры:
//  Таблица - см. НовыйИтоговаяТаблицаХарактеристик
//  ВладелецХарактеристики - ОпределяемыйТип.ВладелецХарактеристикиРекламы
//  ВидХарактеристики - см. НовыйРеквизитыХарактеристики
//  ЗначениеХарактеристикиРекламы - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//  ТекущееЗначениеХарактеристики - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//
Процедура ДобавитьСтрокуТаблицы(
		Таблица, Знач ВладелецХарактеристики, Знач ВидХарактеристики, 
		Знач ЗначениеХарактеристикиРекламы, Знач ТекущееЗначениеХарактеристики = "")
	
	Если ВладелецХарактеристики = Неопределено Тогда
		ВладелецХарактеристики = РекламныйСервисСлужебный.ПустойВладелецХарактеристики();
	КонецЕсли;
	
	ИдентификаторХарактеристики = ВидХарактеристики.ID;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВладелецХарактеристики", ВладелецХарактеристики);
	ПараметрыОтбора.Вставить("ВидХарактеристики", ИдентификаторХарактеристики);
	ПараметрыОтбора.Вставить("Значение", ЗначениеХарактеристикиРекламы);
	
	НайденныеСтрокиТаблицы = Таблица.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтрокиТаблицы.Количество() = 0 Тогда
		
		СтрокаТаблицы = Таблица.Добавить();
		СтрокаТаблицы.ВладелецХарактеристики = ВладелецХарактеристики;
		СтрокаТаблицы.ВидХарактеристики = ИдентификаторХарактеристики;
		СтрокаТаблицы.Значение = ЗначениеХарактеристикиРекламы;
		СтрокаТаблицы.ТекущееЗначениеХарактеристики = ТекущееЗначениеХарактеристики;
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВидХарактеристики);
		
	Иначе
		
		СтрокаТаблицы = НайденныеСтрокиТаблицы[0];
		
		Если СтрокаТаблицы.ТекущееЗначениеХарактеристики <> ТекущееЗначениеХарактеристики Тогда
			
			СтрокаТаблицы.ТекущееЗначениеХарактеристики = 
				СтрШаблон("%1%2%3", 
					СтрокаТаблицы.ТекущееЗначениеХарактеристики, 
					?(ПустаяСтрока(СтрокаТаблицы.ТекущееЗначениеХарактеристики), "", ","), 
					ТекущееЗначениеХарактеристики);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Количество лет.
// 
// Параметры:
//  Дата - Дата
// 
// Возвращаемое значение:
//  Число - Количество лет
//
Функция КоличествоЛет(Дата)
	
	ТекущаяДата = ТекущаяДатаСеанса();
	ГодСекунды = 60 * 60 * 24 * 365; // Секунды, минуты, часы, дней в году.
	
	КоличествоЛет = Цел((ТекущаяДата - Дата) / ГодСекунды);
	
	Возврат КоличествоЛет;
	
КонецФункции

#КонецОбласти

#КонецЕсли
