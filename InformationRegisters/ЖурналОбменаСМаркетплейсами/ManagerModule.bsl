#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Очищает журнал регистрации после окончания установленного периода
//Параметры: ИнтервалОчисткиЖурнала Тип("Число")- необяз
//
//Возвращаемое значение: нет
//
Процедура УдалитьУстаревшиеЗаписиЖурналаОбменаМаркетплейсов(ИнтервалОчисткиЖурнала = 0) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЖурналОбменаСМаркетплейсами.Период КАК Период,
	|	ЖурналОбменаСМаркетплейсами.Организация КАК Организация,
	|	ЖурналОбменаСМаркетплейсами.Маркетплейс КАК Маркетплейс,
	|	ЖурналОбменаСМаркетплейсами.УточнениеПериода КАК УточнениеПериода
	|ИЗ
	|	РегистрСведений.ЖурналОбменаСМаркетплейсами КАК ЖурналОбменаСМаркетплейсами
	|ГДЕ
	|	РАЗНОСТЬДАТ(ЖурналОбменаСМаркетплейсами.Период, &Дата, ДЕНЬ) > &СрокХранения";
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	Запрос.УстановитьПараметр("СрокХранения", ?(ИнтервалОчисткиЖурнала = 0, Константы.ИнтервалОчисткиЖурналаОбменаСМаркетплейсами.Получить(), ИнтервалОчисткиЖурнала));
	
	ТаблицаЖурнала = Запрос.Выполнить().Выгрузить();

	Для Каждого СтрокаТаблицы Из ТаблицаЖурнала Цикл
		Отказ = Ложь;
		УдалитьЗаписьЖурнала(СтрокаТаблицы, Отказ);
		Если Отказ Тогда
			ЗаписьЖурналаРегистрации(ОписаниеОшибки(), УровеньЖурналаРегистрации.Ошибка);
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

//Формирует параметры логирования
//Параметры: НастройкаИнтеграции Тип("СправочникСсылка.НастройкиИнтеграцииМаркетплейс")
//
//Возвращаемое значение: ПараметрыЛогирования Тип("Структура")
//							или Неопределено если логирование не используется
//
Функция ПараметрыЛогирования(НастройкаИнтеграции) Экспорт
	
	ПараметрыЛогирования 				= РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЗаписиЖурнала();
	ПараметрыЛогирования.Организация 	= НастройкаИнтеграции.Владелец;
	ПараметрыЛогирования.Маркетплейс 	= НастройкаИнтеграции.ВидМаркетплейса;
	ПараметрыЛогирования.Вставить("АдресСервера", "");
	ПараметрыЛогирования.Вставить("Соединение"	, Ложь);
	ПараметрыЛогирования.Вставить("Метод"		, "");
	ПараметрыЛогирования.Вставить("Запрос"		, Неопределено);
	ПараметрыЛогирования.Вставить("Ответ"		, Неопределено);
	ПараметрыЛогирования.Вставить("ИмяФайла"	, "");
	
	Возврат ПараметрыЛогирования;
	
КонецФункции

//Возвращает параметры записи для работы с журналом
//Параметры: нет
//
//Возвращаемое значение: ПараметрыЗаписи Тип("Структура")
//
Функция ПараметрыЗаписиЖурнала() Экспорт
	
	ПараметрыЗаписи = Новый Структура;
	
	ПараметрыЗаписи.Вставить("Период"		, ТекущаяДата());
	ПараметрыЗаписи.Вставить("Организация"	, Справочники.Организации.ПустаяСсылка());
	ПараметрыЗаписи.Вставить("Маркетплейс"	, Перечисления.ВидыМаркетплейсов.ПустаяСсылка());
	ПараметрыЗаписи.Вставить("УточнениеПериода", 0);
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

//Очищает журнал регистрации для отдельных маркетплейсов
//Параметры: -Параметры Тип("Структура") ключи:
//			 	*Организация, значение-Тип("СправочиникСсылка.Организации")
//			 	*Маркетплейс, значение-Тип("Перечисления.ВидыМаркетплейсов")
//			 	*Период, значение Тип("Дата")
//			 -Отказ Тип("Булево")
//
//Возвращаемое значение: нет
//
Процедура УдалитьЗаписьЖурнала(ПараметрыЗаписи, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ЖурналОбменаСМаркетплейсами.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ПараметрыЗаписи);
	
	Попытка
		Запись.Удалить();
	Исключение
		Отказ = Истина;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры  

//Обработывает входящую структуру с данными логирования
//Параметры: ПараметрыЗаписи Тип("Структура")
//			ключи: см. -ПараметрыЛогирования(НастройкаИнтеграции)
//
//Возвращаемое значение: нет
//
Процедура ПодготовитьПараметрыДляЗаписиВЖурнал(ПараметрыЗаписи, Лог = "") Экспорт
	
	ТекстСодержания = "";
	Результат = "";
	
	Если ПараметрыЗаписи.Соединение = Ложь Тогда
		ТекстСодержания = "Не удалось соединится с сервером: "+ ПараметрыЗаписи.АдресСервера + Символы.ПС;
		Результат = "Ошибка";
	Иначе
		ТекстСодержания = "Установлено соединение с сервером: "+ ПараметрыЗаписи.АдресСервера + Символы.ПС;
		
		Если Не ПараметрыЗаписи.Запрос = Неопределено Тогда
			ТекстСодержания = ТекстСодержания + ПараметрыЗаписи.Метод + ПараметрыЗаписи.Запрос.АдресРесурса + Символы.ПС;
	        Для Каждого КлючИЗначение Из ПараметрыЗаписи.Запрос.Заголовки Цикл
				Заменить 		= ЗаменитьЗначение(КлючИЗначение, КлючиПоискаДляЗаменыЗначений());
				ТекстСодержания = ТекстСодержания + КлючИЗначение.Ключ + ", " + ?(Заменить, СтрокаЗамены(КлючИЗначение.Значение, "#"), КлючИЗначение.Значение + Символы.ПС);
			КонецЦикла;
		КонецЕсли;
		
		Если ПараметрыЗаписи.Метод = "POST" Тогда
			ТекстСодержания = ТекстСодержания + ПараметрыЗаписи.Запрос.ПолучитьТелоКакСтроку()+ Символы.ПС;
		КонецЕсли;

		Если Не ПараметрыЗаписи.Ответ = Неопределено Тогда
			КодОтветаСервера = ПараметрыЗаписи.Ответ.КодСостояния;
			ТекстСодержания = ТекстСодержания + "Код ответа сервера: " + Строка(КодОтветаСервера) + Символы.ПС;
			Для Каждого КлючИЗначение Из ПараметрыЗаписи.Ответ.Заголовки Цикл
				ТекстСодержания = ТекстСодержания + КлючИЗначение.Ключ + "," + КлючИЗначение.Значение + Символы.ПС;	
			КонецЦикла;
			ТекстОтвета = ПараметрыЗаписи.Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
			ТекстСодержания = ТекстСодержания + "<json>" + Символы.ПС;
			ТекстСодержания = ТекстСодержания + ТекстОтвета + Символы.ПС;
			Результат = ?((КодОтветаСервера = 200 И ЭтоКорректныйОтвет(ТекстОтвета)), "Успешно", "Ошибка");
		КонецЕсли;
	КонецЕсли; 
	
	Если ПараметрыЗаписи.Свойство("Результат") Тогда
		ПараметрыЗаписи.Результат = ?(ЗначениеЗаполнено(Лог), ?(ЭтоКорректныйОтвет(Лог),"Успешно","Ошибка"), Результат);
	Иначе
		ПараметрыЗаписи.Вставить("Результат", ?(ЗначениеЗаполнено(Лог), ?(ЭтоКорректныйОтвет(Лог),"Успешно","Ошибка"), Результат));
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("Текст", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(?(ЗначениеЗаполнено(Лог), Лог, ТекстСодержания)));
		
КонецПроцедуры

//Производит запись в журнал обмена 
//Параметры: -ПараметрыЗаписи Тип("Структура") 
//	ключи: см. -ПараметрыЛогирования(НастройкаИнтеграции)
//
//Возвращаемое значение: нет
//
Процедура ЗаписатьВЖурнал(ПараметрыЗаписи, Отказ = Ложь, Лог = "") Экспорт
	
	ПодготовитьПараметрыДляЗаписиВЖурнал(ПараметрыЗаписи, Лог);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущаяДата = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	УточнениеПериода = ТекущаяУниверсальнаяДатаВМиллисекундах() % 1000;
	
	НаборЗаписей = РегистрыСведений.ЖурналОбменаСМаркетплейсами.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ТекущаяДата);
	НаборЗаписей.Отбор.Организация.Установить(ПараметрыЗаписи.Организация);
	НаборЗаписей.Отбор.Маркетплейс.Установить(ПараметрыЗаписи.Маркетплейс);
	НаборЗаписей.Отбор.УточнениеПериода.Установить(УточнениеПериода);
	
	НоваяЗапись 			= НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗапись, ПараметрыЗаписи);
	НоваяЗапись.Период 		= ТекущаяДата;
	НоваяЗапись.УточнениеПериода = УточнениеПериода;
	НоваяЗапись.Файл 		= ?(ЗначениеЗаполнено(ПараметрыЗаписи.ИмяФайла), ХранилищеДанныхФайла(ПараметрыЗаписи.ИмяФайла), Неопределено);
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Отказ = Истина;
		ЗаписьЖурналаРегистрации(ОписаниеОшибки(), УровеньЖурналаРегистрации.Ошибка);
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

//Включает логирование обмена в журнал по умолчанию при обновлении ИБ
//Параметры: нет
//
//Возвращаемое значение: нет
//
Процедура ВключитьЛогирование() Экспорт
	
	Константы.ИнтервалОчисткиЖурналаОбменаСМаркетплейсами.Установить(15);
	
КонецПроцедуры

// Включает принудительное логирование при автозагрузке отчетов маркетплейсов
//
Процедура ВключитьЛогированиеАвтозагрузки() Экспорт
	
	Если Не ЛогированиеВключено() Тогда
		ВключитьЛогирование();
	КонецЕсли;
	
КонецПроцедуры

//Определяет включение логирования
//Параметры: нет
//
//Возвращаемое значение: ЛогированиеВключено Тип("Булево")
//
Функция ЛогированиеВключено() Экспорт
	
	Возврат Константы.ИнтервалОчисткиЖурналаОбменаСМаркетплейсами.Получить() > 0;
	
КонецФункции

//Полностью очищает журнал обмена после выключения логирования
//Параметры: Отказ Тип("Булево") -необяз.
//
//Возвращаемое значение: нет
//
Процедура ОчиститьЖурнал(Отказ = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ЖурналОбменаСМаркетплейсами.СоздатьНаборЗаписей();
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Отказ = Истина;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Формирует массив имен ключей
//Параметры: нет
//
//Возвращаемое значение: КлючиПоискаДляЗаменыЗначений Тип("Массив")
//
Функция КлючиПоискаДляЗаменыЗначений()
	
	Возврат СтрРазделить("Authorization,Api-Key", ",");
	
КонецФункции

//Проверяет необходимость замены значения структуры для определенных ключей
//Параметры: 
//			-КлючИЗначение Тип("КлючИЗначение")
//			-КлючиПоиска Тип("Массив")
//
//Возвращаемое значение: Заменить Тип("Булево")
Функция ЗаменитьЗначение(КлючИЗначение, КлючиПоиска) 
		
	Возврат Не КлючиПоиска.Найти(КлючИЗначение.Ключ) = Неопределено
	
КонецФункции

//Формирует строку символов по длине исходной строки
//Параметры: 
//			-ИсходнаяСтрока Тип("Строка")
//			- Символ Тип("Строка")
//
//Возвращаемое значениеи: Результат Тип("Строка")
//
Функция СтрокаЗамены(ИсходнаяСтрока, Символ)
	
	Результат = "";
	
	Для Счетчик = 1 По СтрДлина(ИсходнаяСтрока) Цикл
		Результат = Результат + Символ;	
	КонецЦикла;
	
	Возврат Результат + Символы.ПС;
	
КонецФункции

//Проверяет, что переданный в качестве параметра текст json
//и не содержит указания на отсуствие данных
//Параметры: Текст Тип("Строка"), NULL, Неопределено
//
//Возвращаемое значение: результат Тип("Булево")
//
Функция ЭтоКорректныйОтвет(Текст)
	
	Результат = Ложь;
	
	Если ТипЗнч(Текст)= Тип("Строка") 
		И (СтрНачинаетсяС(Текст, "[") ИЛИ СтрНачинаетсяС(Текст, "{"))
		И (СтрЗаканчиваетсяНа(Текст, "}") ИЛИ СтрЗаканчиваетсяНа(Текст, "]"))
		И СтрДлина(Текст) > 2 
		И СтрНайти(Текст, "NO_DATA")   = 0
		И СтрНайти(Текст, "TOO_LARGE") = 0 Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

//Помещает файл в хранилище
//Параметры: ИмяФайла Тип("Строка")- полное имя файла(Путь к файлу и имя с раширением)
//
//Возвращаемое значение: ХранилищеФайла Тип("ХранилищеЗначения")
//
Функция ХранилищеДанныхФайла(ИмяФайла)
	
	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() Тогда
		ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
		ХранилищеФайла = Новый ХранилищеЗначения(ДвоичныеДанныеФайла, Новый СжатиеДанных(9)); 
	Иначе
		ЗаписьЖурналаРегистрации("Сохранение файла отчета Яндекс Маркет", УровеньЖурналаРегистрации.Ошибка, , , "Файл "+ ИмяФайла + " не найден");
	КонецЕсли;
	
	Возврат ХранилищеФайла;
	
КонецФункции  

#КонецОбласти

#КонецЕсли






