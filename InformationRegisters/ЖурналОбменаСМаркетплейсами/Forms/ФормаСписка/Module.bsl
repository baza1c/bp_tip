 &НаКлиенте
 Перем РабочийКаталог;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ЗаполнитьСписокИнтерваловНаСервере();
	
	ИнтервалОчисткиЖурнала = Константы.ИнтервалОчисткиЖурналаОбменаСМаркетплейсами.Получить();
	
	Элементы.ГруппаФайл.Видимость = Ложь;
	
	Элементы.ИнтервалОчисткиЖурнала.Доступность = РольДоступна("ДобавлениеИзменениеДанныхБухгалтерии") ИЛИ РольДоступна("ПолныеПрава");
	
	Если Параметры.Свойство("ДатаПоследнейАвтозагрузки") Тогда
		Элементы.Список.Период = Новый СтандартныйПериод(
			Параметры.ДатаПоследнейАвтозагрузки, Параметры.ДатаПоследнейАвтозагрузки);
		Элементы.Список.ПоложениеСостоянияПросмотра = ПоложениеСостоянияПросмотра.Авто;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РабочийКаталог = "";
	
	#Если ВебКлиент Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПодключениеРасширенияДляРаботыСФайламиЗавершение", ЭтаФорма);
		ТекстПредложения = "Необходимо установить расширение для работы с файлами в 1С:Предприятие";
		ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботы1СПредприятия(ОписаниеОповещения, ТекстПредложения, Истина);
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ПодключениеРасширенияДляРаботыСФайламиЗавершение(РасширениеПодключено, ДополнительныеПараметры) Экспорт
	
	Если РасширениеПодключено Тогда
		НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("ПолучениеКаталогаВременныхФайловЗавершение", ЭтаФорма));	
	Иначе
		Элементы.СписокСохранитьФайлНаДиск.Доступность = Ложь;
		Элементы.ГруппаФайл.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеКаталогаВременныхФайловЗавершение(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	
	РабочийКаталог = ИмяКаталогаВременныхФайлов;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИнтервалОчисткиЖурналаПриИзменении(Элемент)
		
	СтарыйИнтервал 	= ПолучитьУстановленныйИнтервалНаСервере();
	
	Если ИнтервалОчисткиЖурнала < СтарыйИнтервал Тогда
		ДопПараметры 	= Новый Структура("УстановленныйИнтервал", ИнтервалОчисткиЖурнала);
		Оповещение 		= Новый ОписаниеОповещения("ИнтервалОчисткиЖурналаЗавершение", ЭтаФорма, ДопПараметры);
		ТекстВопроса 	= СтрШаблон("Содержимое журнала %1 будет очищено. Продолжить?", ?(ИнтервалОчисткиЖурнала = 0, "", "старше указанного интервала"));
	
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		УстановитьИнтервалОчисткиНаСервере(ИнтервалОчисткиЖурнала);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалОчисткиЖурналаЗавершение(Ответ, ДопПараметры) Экспорт
	
	Отказ = Ложь;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда 
		ВосстановитьИнтервалОчисткиНаСервере();
		Возврат;
	КонецЕсли;	
	Интервал = ДопПараметры.УстановленныйИнтервал;
	УстановитьИнтервалОчисткиНаСервере(Интервал, Отказ);
	
	Если Отказ Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не удалось очистить журнал. Подробности в журнале регистрации'"));	
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОбращениеВТехподдержку(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ПолучитьДанныеСтрокиНаСервере(ТекущаяСтрока);
	Отбор = ПолучитьОтбор(ДанныеСтроки);
	Период = ДанныеСтроки.Период;
	
	Вложения = Новый Массив;
	АдресХранилищаЛога = ЗаписатьТекстВФайл(Период, Отбор);
	Вложения.Добавить(
			Новый Структура("Представление, АдресВоВременномХранилище"
			, ПолучитьИмяФайла(Период, Отбор.Маркетплейс, "txt", "Лог_обмена")
			, АдресХранилищаЛога));
	
	СодержаниеЗаписи = ПолучитьСодержимоеЖурналаНаСервере(Период, Отбор);
	
	Если ЗначениеЗаполнено(СодержаниеЗаписи.ИмяФайла) Тогда
		АдресФайлаОтчета = ПолучитьДанныеФайла(Период, Отбор);
		Если ЭтоАдресВременногоХранилища(АдресФайлаОтчета) Тогда
	    	Вложения.Добавить(
					Новый структура("Представление, АдресВоВременномХранилище"
					, ПолучитьИмяФайла(Период, Отбор.Маркетплейс, "xlsx", "Отчет_по_продажам")
					, АдресФайлаОтчета));
		КонецЕсли;
	КонецЕсли;
	
	ДопПараметры = Новый Структура("ПараметрыЗаписи, Вложения", Отбор, Вложения);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект, ДопПараметры);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлНаДиск(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки 	= ПолучитьДанныеСтрокиНаСервере(ТекущаяСтрока);
	
	Дата 			= ДанныеСтроки.Период;
	Отбор			= ПолучитьОтбор(ДанныеСтроки);
	СодержимоеЛога 	= ПолучитьСодержимоеЖурналаНаСервере(Дата, Отбор);
	ЕстьФайл 		= ЗначениеЗаполнено(СодержимоеЛога.ИмяФайла);
	
	ДиалогВыбораФайла 					= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Заголовок 		= "Выбор файла для сохранения лога";
	Если ЕстьФайл Тогда
		ДиалогВыбораФайла.Фильтр 		= "Файлы zip-архивов(*.zip)|*.zip";
		Расширение = "zip";
	Иначе
		ДиалогВыбораФайла.Фильтр 		= "Текстовые файлы(*.txt)|*.txt";
	КонецЕсли;
	ДиалогВыбораФайла.ПолноеИмяФайла 	= ПолучитьИмяФайла(Дата, ДанныеСтроки.Маркетплейс, Расширение);
	ДопПараметры = Новый Структура("Период, Отбор", Дата, Отбор);
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораФайлаДляСохраненияДанных", ЭтаФорма, ДопПараметры);
	
	ДиалогВыбораФайла.Показать(Оповещение);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаДляСохраненияДанных(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Отказ = Ложь;
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = ВыбранныеФайлы[0];
	
	Если Прав(ПолноеИмяФайла, 3) = "txt" Тогда//Сохраняем только текст
		СохранитьТекстЛогаВФайл(ДопПараметры.Период, ДопПараметры.Отбор, ПолноеИмяФайла, Отказ);
	Иначе
        ФайлыДляСохранения = Новый Массив;
		//Сохраняем текст лога
		ИмяФайлаЛога = ПолучитьИмяФайла(ДопПараметры.Период, ДопПараметры.Отбор.Маркетплейс, "txt", "Лог_обмена");
		АдресЛогаНаСервере = ЗаписатьТекстВФайл(ДопПараметры.Период, ДопПараметры.Отбор);
		ОписаниеФайла = Новый Структура("ИмяФайла, Адрес", ИмяФайлаЛога, АдресЛогаНаСервере);
		ФайлыДляСохранения.Добавить(ОписаниеФайла);
		//Сохранение файла отчета
		ИмяФайлаОтчета = ПолучитьИмяФайла(ДопПараметры.Период, ДопПараметры.Отбор.Маркетплейс, "xlsx", "Отчет_по_продажам");
		АдресОтчетаНаСервере = ПолучитьДанныеФайла(ДопПараметры.Период, ДопПараметры.Отбор);
		ОписаниеФайла = Новый Структура("ИмяФайла, Адрес", ИмяФайлаОтчета, АдресОтчетаНаСервере);
		ФайлыДляСохранения.Добавить(ОписаниеФайла);
		//Сохраняем в архив
		АдресZipФайла = СоздатьZipФайлНаСервере(ФайлыДляСохранения, Отказ);
		СохранитьZipФайл(АдресZipФайла, ПолноеИмяФайла, Отказ); 
	КонецЕсли;
	
	Если Не Отказ Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеНажатияНаОповещение", ЭтаФорма, Новый Структура("ПолноеИмяФайла", ПолноеИмяФайла));
		ПоказатьОповещениеПользователя("Сохранение файла", Оповещение, "Открыть файл можно по этой ссылке");	
	Иначе
		ПоказатьПредупреждение(, "Отсутствуют данные для сохранения");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияФайлНажатие(Элемент)
	
	ТекСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьОтчет(Элементы.Список.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеНажатияНаОповещение(ДопПараметры) Экспорт
	
	#Если ВебКлиент Тогда
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ПослеЗапускаПриложения", ЭтаФорма), ДопПараметры.ПолноеИмяФайла);				
	#Иначе
		ЗапуститьПриложение(ДопПараметры.ПолноеИмяФайла);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаПриложения(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСпискаФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		ТекстСодержания.Очистить();
		Элементы.ДекорацияФайл.Заголовок = "";
		Элементы.ГруппаФайл.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки 	= ПолучитьДанныеСтрокиНаСервере(ТекущаяСтрока);
	Период 			= ДанныеСтроки.Период;
	Отбор 			= ПолучитьОтбор(ДанныеСтроки);
	Содержание 		= ПолучитьСодержимоеЖурналаНаСервере(Период, Отбор);
	
	ТекстСодержания.УстановитьТекст(Содержание.Текст);
	
	Если ЗначениеЗаполнено(Содержание.ИмяФайла) Тогда
		Элементы.ГруппаФайл.Видимость = Истина;
		Элементы.ДекорацияФайл.Заголовок = ПолучитьИмяФайла(Период, ДанныеСтроки.Маркетплейс, "xlsx", "Отчет_по_продажам");
	Иначе
		Элементы.ГруппаФайл.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьТекстЛогаВФайл(Период, Отбор, ИмяФайла = "", Отказ)
	
	АдресХранилищаЛога = ЗаписатьТекстВФайл(Период, Отбор);
	
	Если ЭтоАдресВременногоХранилища(АдресХранилищаЛога) Тогда
		ДвоичныеДанныеФайлаЛога = ПолучитьИзВременногоХранилища(АдресХранилищаЛога);
		Если ТипЗнч(ДвоичныеДанныеФайлаЛога) = Тип("ДвоичныеДанные") Тогда
			Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
				#Если ВебКлиент Тогда
					ИмяФайла = РабочийКаталог + Строка(Новый УникальныйИдентификатор)+ ".xlsx";
				#Иначе
					ИмяФайла = КаталогВременныхФайлов() + Строка(Новый УникальныйИдентификатор)+ ".xlsx";
				#КонецЕсли
			КонецЕсли; 
			#Если ВебКлиент Тогда
				ДвоичныеДанныеФайлаЛога.НачатьЗапись(Новый ОписаниеОповещения("ПослеЗаписиФайлаНаВебКлиенте", ЭтаФорма, Новый Структура("Открывать, ПолноеИмяФайла", Ложь, ИмяФайла)), ИмяФайла);
			#Иначе
				ДвоичныеДанныеФайлаЛога.Записать(ИмяФайла);
			#КонецЕсли
		Иначе
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиФайлаНаВебКлиенте(ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Свойство("Открывать") И ДополнительныеПараметры.Открывать Тогда
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ПослеЗапускаПриложения", ЭтаФорма), ДополнительныеПараметры.ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлОтчета(Период, Отбор, ИмяФайла = "", Отказ, Открывать = Ложь)
	
	АдресФайлаОтчета = ПолучитьДанныеФайла(Период, Отбор);
	
	Если ЭтоАдресВременногоХранилища(АдресФайлаОтчета) Тогда
		ДвоичныеДанныефайлаОтчета = ПолучитьИзВременногоХранилища(АдресФайлаОтчета);
		Если ТипЗнч(ДвоичныеДанныефайлаОтчета) = Тип("ДвоичныеДанные") Тогда
			Если НЕ ЗначениеЗаполнено(ИмяФайла) Тогда
				#Если ВебКлиент Тогда
					ИмяФайла = РабочийКаталог + Строка(Новый УникальныйИдентификатор)+ ".xlsx";
				#Иначе
					ИмяФайла = КаталогВременныхФайлов() + Строка(Новый УникальныйИдентификатор)+ ".xlsx";
				#КонецЕсли
			КонецЕсли;
			#Если ВебКлиент Тогда
				ДвоичныеДанныефайлаОтчета.НачатьЗапись(Новый ОписаниеОповещения("ПослеЗаписиФайлаНаВебКлиенте", ЭтаФорма, Новый Структура("ПолноеИмяФайла, Открывать", ИмяФайла, Открывать)), ИмяФайла);
			#Иначе
				ДвоичныеДанныефайлаОтчета.Записать(ИмяФайла);
			#КонецЕсли
		Иначе
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьZipФайл(Адрес, ИмяФайла, Отказ, Открывать = Ложь)
	
	Если ЭтоАдресВременногоХранилища(Адрес) Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(Адрес);
		Если ТипЗнч(ДанныеФайла) = Тип("ДвоичныеДанные") Тогда
			#Если ВебКлиент Тогда 
				ДанныеФайла.НачатьЗапись(Новый ОписаниеОповещения("ПослеЗаписиФайлаНаВебКлиенте", ЭтаФорма, Новый Структура("ПолноеИмяФайла, Открывать", ИмяФайла, Открывать)), ИмяФайла);
			#Иначе
				ДанныеФайла.Записать(ИмяФайла);	
			#КонецЕсли 
		Иначе
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(ТекущаяСтрока)
	
	Отказ = Ложь;
	
	ПараметрыЗаписиЖурнала 	= ПолучитьДанныеСтрокиНаСервере(ТекущаяСтрока);
	Период 					= ПараметрыЗаписиЖурнала.Период;
	Отбор 					= ПолучитьОтбор(ПараметрыЗаписиЖурнала);
	ПолноеИмяФайла			= "";
	СохранитьФайлОтчета(Период, Отбор, ПолноеИмяФайла, Отказ, Истина);
	
	Если Не Отказ Тогда
		#Если Вебклиент Тогда
			Возврат;
		#Иначе
			ЗапуститьПриложение(ПолноеИмяФайла);
		#КонецЕсли
	Иначе
		ПоказатьПредупреждение(, "Отсутствуют данные отчета");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
    ТехническаяИнформация		= ПолучитьТехническуюИнформацию(ДополнительныеПараметры.ПараметрыЗаписи.Организация);
	
	ПараметрыПисьма 			= РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель 	= "";
	ПараметрыПисьма.Тема 		= ТемаОбращенияВТехподдержку(ДополнительныеПараметры.ПараметрыЗаписи);
	ПараметрыПисьма.Текст 		= СообщениеПользователяОПроблеме(Истина, ТехническаяИнформация);
	ПараметрыПисьма.Вложения 	= ДополнительныеПараметры.Вложения;
	
	ОтправкаПочтовыхСообщенийКлиент.ДополнитьПараметрыПисьмаДляОтправкиЛегкойПочтой(ПараметрыПисьма);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьТекстВФайл(Период, Отбор)
	
	Ресурсы = РегистрыСведений.ЖурналОбменаСМаркетплейсами.Получить(Период, Отбор);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(?(ЗначениеЗаполнено(Ресурсы.Текст), Ресурсы.Текст, "Отсутствуют данные"));
	ИмяФайла = ПолучитьИмяВременногоФайла("txt");
	ТекстовыйДокумент.Записать(ИмяФайла);
	
	ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеФайла(Период, Отбор)
	
	АдресВоВременномХранилище = "";
	
	Ресурсы = РегистрыСведений.ЖурналОбменаСМаркетплейсами.Получить(Период, Отбор);
	Если Не ЗначениеЗаполнено(Ресурсы.ИмяФайла) Тогда
		Возврат АдресВоВременномХранилище;
	КонецЕсли;	
	
	ДвоичныеДанныеФайла = Ресурсы.Файл.Получить();
	Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
		АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат АдресВоВременномХранилище; 
	
КонецФункции	

&НаСервере
Процедура ЗаполнитьСписокИнтерваловНаСервере()
	
	СтрокаИнтервал = "0,1,3,7,10,15,30,60";
	
	Интервалы = СтрРазделить(СтрокаИнтервал, ",");
	Для Каждого Интервал Из Интервалы Цикл
		ЗначениеИнтервала = Число(Интервал);
		Элементы.ИнтервалОчисткиЖурнала.СписокВыбора.Добавить(ЗначениеИнтервала, ПредставлениеИнтервала(ЗначениеИнтервала));	
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеИнтервала(Интервал)
	
	Представление = "";	
	
	Если Интервал = 0 Тогда
		Представление = "не хранить";
	ИначеЕсли Интервал = 1 Тогда
		Представление = "1 день";
	ИначеЕсли Интервал = 3 Тогда
		Представление = "3 дня";
	Иначе
		Представление = Строка(Интервал) + " дней";
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

&НаСервере
Процедура ВосстановитьИнтервалОчисткиНаСервере()
	
	ИнтервалОчисткиЖурнала = Константы.ИнтервалОчисткиЖурналаОбменаСМаркетплейсами.Получить();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСодержимоеЖурналаНаСервере(Период, Отбор)
	
	Возврат РегистрыСведений.ЖурналОбменаСМаркетплейсами.Получить(Период, Отбор);
	
КонецФункции

&НаСервере
Функция СоздатьZipФайлНаСервере(ФайлыДляАрхива, Отказ)
	
	ИмяФайлаАрхива 				= ПолучитьИмяВременногоФайла("zip");
	ЗаписьZip 					= Новый ЗаписьZipФайла(ИмяФайлаАрхива);
	АдресВременногоХранилища 	= "";
	
	Для Каждого ОписаниеФайла Из ФайлыДляАрхива Цикл
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ОписаниеФайла.Адрес);
		ИмяФайлаНаСервере 	= КаталогВременныхФайлов()+ ОписаниеФайла.ИмяФайла;
		ДвоичныеДанныеФайла.Записать(ИмяФайлаНаСервере);
		ЗаписьZip.Добавить(ИмяФайлаНаСервере);
	КонецЦикла;
	Попытка
		ЗаписьZip.Записать();
	Исключение
		Отказ = Истина;
	КонецПопытки;
	Если Не Отказ Тогда
		ДанныеZipФайла = Новый ДвоичныеДанные(ИмяФайлаАрхива);
	    АдресВременногоХранилища = ПоместитьВоВременноеХранилище(ДанныеZipФайла, АдресВременногоХранилища);
	КонецЕсли;
	
	Возврат АдресВременногоХранилища;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьИнтервалОчисткиНаСервере(Интервал, Отказ = Ложь)
	
	Если Интервал = 0 Тогда
		РегистрыСведений.ЖурналОбменаСМаркетплейсами.ОчиститьЖурнал(Отказ);
	Иначе
		РегистрыСведений.ЖурналОбменаСМаркетплейсами.УдалитьУстаревшиеЗаписиЖурналаОбменаМаркетплейсов(Интервал);
	КонецЕсли;
	
	Константы.ИнтервалОчисткиЖурналаОбменаСМаркетплейсами.Установить(Интервал);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяФайла(Период, Маркетплейс, Расширение = "", Наименование = "")
	
	Имя = ?(ЗначениеЗаполнено(Наименование), Наименование, "Обмен");
	
	Возврат СтрШаблон("%1_с_%2_от_%3", Имя, Маркетплейс, Формат(Период, "ДФ =" "дд-ММ-гггг_чч-мм-сс"))
			+ ?(ЗначениеЗаполнено(Расширение),"." + Расширение, "");
	
КонецФункции

&НаКлиенте
Функция ТемаОбращенияВТехподдержку(ПараметрыЗаписиЖурнала)
	
	Возврат СтрШаблон("Ошибка при обмене данными с %1", ПараметрыЗаписиЖурнала.Маркетплейс);
	
КонецФункции

&НаКлиенте
Функция СообщениеПользователяОПроблеме(ЕстьВложениеЖурналаЗагрузки, ТехническаяИнформация)
	
	ЕстьТехническаяИнформация = ?(ЗначениеЗаполнено(ТехническаяИнформация), Истина, Ложь);
		
	Сообщение = Новый Массив;
	Сообщение.Добавить(НСтр("ru = 'Для автоматического разбора ошибок мы сформировали необходимую техническую информацию.
		|
		|========= Не удаляйте эту часть письма и приложенные файлы ==========
		|'"));
	Если ЕстьВложениеЖурналаЗагрузки Или ЕстьТехническаяИнформация Тогда
		Сообщение.Добавить(Символы.ПС);
		Сообщение.Добавить(НСтр("ru = 'Сообщение содержит файлы с подробной информацией об ошибке:'"));
	КонецЕсли;
	Если ЕстьВложениеЖурналаЗагрузки Тогда
		Сообщение.Добавить(Символы.ПС);
		Сообщение.Добавить(НСтр("ru = ' - записи из журнала обмена маркетплейсов'"));
	КонецЕсли;
	Если ЕстьТехническаяИнформация Тогда
		Сообщение.Добавить(Символы.ПС);
		Сообщение.Добавить(НСтр("ru = ' - техническая информация'"));
		Сообщение.Добавить(Символы.ПС);
		Для каждого КлючИЗначение Из ТехническаяИнформация Цикл
			Сообщение.Добавить(КлючИЗначение.Ключ +": "+ КлючИЗначение.Значение + Символы.ПС);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СтрСоединить(Сообщение);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТехническуюИнформацию(Организация)
	
	ТехническаяИнформация 				= Новый Структура("ИНН, Конфигурация, Версия");
	ТехническаяИнформация.Конфигурация 	= Метаданные.Представление();
	ТехническаяИнформация.Версия		= Метаданные.Версия;
	СведенияОбОрганизации 				= БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, ТекущаяДата());
	ТехническаяИнформация.ИНН 			= ?(ЗначениеЗаполнено(СведенияОбОрганизации.ИНН), СведенияОбОрганизации.ИНН, "Не указан");
	
	Возврат ТехническаяИнформация;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеСтрокиНаСервере(КлючЗаписи)
	
	ПараметрыЗаписиЖурнала = РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЗаписиЖурнала();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаписиЖурнала, КлючЗаписи);
	
	Возврат ПараметрыЗаписиЖурнала;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОтбор(ПараметрыЗаписиЖурнала)
	
	Отбор = Новый Структура("Организация, Маркетплейс, УточнениеПериода");
	
	ЗаполнитьЗначенияСвойств(Отбор, ПараметрыЗаписиЖурнала);
	
	Возврат Отбор;
	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьУстановленныйИнтервалНаСервере()
	
	Возврат Константы.ИнтервалОчисткиЖурналаОбменаСМаркетплейсами.Получить();
	
КонецФункции

#КонецОбласти

