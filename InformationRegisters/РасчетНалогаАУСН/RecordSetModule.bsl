#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.НалоговыйПериод = РегистрыСведений.РасчетНалогаАУСН.НалоговыйПериод(Запись.НалоговыйПериод);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РасчетыАУСН = Новый Соответствие;
	ОтборПоОрганизации = Отбор.Найти("Организация");
	Если ОтборПоОрганизации <> Неопределено Тогда
		Организация = ОтборПоОрганизации.Значение;
		ОтборПоПериоду = Отбор.Найти("НалоговыйПериод");
		Если ОтборПоПериоду <> Неопределено Тогда
			РасчетыАУСН.Вставить(Организация, ОтборПоПериоду.Значение);
		КонецЕсли;
	КонецЕсли;
	Для Каждого Запись Из ЭтотОбъект Цикл
		НайденныйПериод = РасчетыАУСН.Получить(Запись.Организация);
		Если НайденныйПериод <> Неопределено И НайденныйПериод > Запись.НалоговыйПериод Тогда
			РасчетыАУСН.Вставить(Запись.Организация, Запись.НалоговыйПериод);
		КонецЕсли;
	КонецЦикла;
	
	ВидРегламентнойОперации = Перечисления.ВидыРегламентныхОпераций.НачислениеНалогаАУСН;
	НомерГруппы = Перечисления.ВидыРегламентныхОпераций.НомерГруппы(ВидРегламентнойОперации);
	
	Для Каждого ЭлементРасчета Из РасчетыАУСН Цикл
		РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
			ЭлементРасчета.Ключ, ЭлементРасчета.Значение, НомерГруппы, ВидРегламентнойОперации, Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
