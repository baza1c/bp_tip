#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Вычисляет и устанавливает факт подключения сотрудника к ЭДО. Все согласия должны быть подписаны со статусом согласие.
//	Параметры:
//		ФизическиеЛица - Массив Из СправочникСсылка.ФизическиеЛица - сотрудники для которого требуется установить факт подключения к ЭДО.
Процедура УстановитьПодключенКЭДОБЗКДляФизическогоЛица(ФизическиеЛица) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СогласиеНаПрисоединениеКЭДОБЗК.Организация КАК Организация,
		|	СогласиеНаПрисоединениеКЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ВЫБОР
		|			КОГДА СостояниеСогласияНаПрисоединениеКЭДОБЗК.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияСогласийНаПрисоединениеКЭДОБЗК.Согласие)
		|					ИЛИ СостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема = ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ПолученыСканкопии)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоНеподтвержденныхСогласий,
		|	СУММА(ВЫБОР
		|			КОГДА СостояниеСогласияНаПрисоединениеКЭДОБЗК.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласийНаПрисоединениеКЭДОБЗК.Согласие)
		|					И (СостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема = ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Подтверждено)
		|						ИЛИ СостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема = ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ОжиданиеОригинала))
		|					И СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоПодтвержденныхСогласий
		|ПОМЕСТИТЬ ВТСогласия
		|ИЗ
		|	Документ.СогласиеНаПрисоединениеКЭДОБЗК КАК СогласиеНаПрисоединениеКЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК КАК СостояниеСогласияНаПрисоединениеКЭДОБЗК
		|		ПО (СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка = СогласиеНаПрисоединениеКЭДОБЗК.Ссылка)
		|ГДЕ
		|	СогласиеНаПрисоединениеКЭДОБЗК.Проведен
		|	И СогласиеНаПрисоединениеКЭДОБЗК.ФизическоеЛицо В(&ФизическиеЛица)
		|
		|СГРУППИРОВАТЬ ПО
		|	СогласиеНаПрисоединениеКЭДОБЗК.ФизическоеЛицо,
		|	СогласиеНаПрисоединениеКЭДОБЗК.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Согласия.Организация КАК Организация,
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
		|	ЕСТЬNULL(Согласия.КоличествоНеподтвержденныхСогласий, 0) КАК КоличествоНеподтвержденныхСогласий,
		|	ЕСТЬNULL(Согласия.КоличествоПодтвержденныхСогласий, 0) КАК КоличествоПодтвержденныхСогласий
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСогласия КАК Согласия
		|		ПО (Согласия.ФизическоеЛицо = ФизическиеЛица.Ссылка)
		|ГДЕ
		|	ФизическиеЛица.Ссылка В(&ФизическиеЛица)";
	
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	ФизическиеЛицаДляОбновления = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация = Выборка.Организация;
		МенеджерЗаписи.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		Если Выборка.КоличествоПодтвержденныхСогласий = 0 Тогда
			МенеджерЗаписи.Удалить();
			Продолжить;
		КонецЕсли;
		МенеджерЗаписи.Подключен = (Выборка.КоличествоПодтвержденныхСогласий > 0);
		МенеджерЗаписи.Записать(Истина);
		
		ФизическиеЛицаДляОбновления.Добавить(Выборка.ФизическоеЛицо);
		
	КонецЦикла; 
	
	ИнтеграцияУправлениеПерсоналомСобытия.ЗарегистрироватьОбновлениеДоступныхФункцийФизическогоЛица(ФизическиеЛицаДляОбновления);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#Область КадровыеДанныеФизическихЛиц

Функция ДобавитьПолеСведенийОПодключенииКЭДОБЗК(Знач ИмяПоля, ТекстыОписанияПолей, ИсточникиДанных) Экспорт
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыДанныеОПодключенииКЭДОБЗК(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("ФизическиеЛицаПрисоединенныеКЭДОБЗК", Истина);
		
		ПутьКДанным = ПутьКДаннымСведенийОПодключенииКЭДОБЗК(ИмяПоля);
		ТекстыОписанияПолей.Добавить(ПутьКДанным + " КАК " + ИмяПоля);
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыДанныеОПодключенииКЭДОБЗК(Знач ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	Возврат ИмяПоля = ВРег("ПодключенКЭДОБЗК");
	
КонецФункции

Функция ПутьКДаннымСведенийОПодключенииКЭДОБЗК(Знач ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("ПодключенКЭДОБЗК") Тогда
		ПутьКДанным = "	СведенияОПодключенииФизическихЛицККЭДО.Подключен";
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОПодключенииКЭДОБЗК(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных) Экспорт
	
	Если ИсточникиДанных.Получить("ФизическиеЛицаПрисоединенныеКЭДОБЗК") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросВТ = ЗапросВТСведенияОПодключенииККЭДО(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов);
	
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	ЧастиЗапроса = Новый Массив;
	ЧастиЗапроса.Добавить(ЗапросВТ.Текст);
	ЧастиЗапроса.Добавить(ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов());
	ЧастиЗапроса.Добавить(Запрос.Текст);
	ЧастиЗапроса.Добавить(
		"	{ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОПодключенииККЭДО КАК СведенияОПодключенииФизическихЛицККЭДО
		|	ПО " + КадровыйУчет.ПутьКПолюФизическоеЛицо("ТаблицаОтборов", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо) + " = СведенияОПодключенииФизическихЛицККЭДО.ФизическоеЛицо}");
	
	Запрос.Текст = СтрСоединить(ЧастиЗапроса, Символы.ПС);
	
КонецПроцедуры

Функция ЗапросВТСведенияОПодключенииККЭДО(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИмяВТСведенияОПодключенииККЭДО = "ВТСведенияОПодключенииККЭДО")
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаОтборов.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ЕСТЬNULL(МИНИМУМ(ФизическиеЛицаПрисоединенныеКЭДОБЗК.Подключен), ЛОЖЬ) КАК Подключен
		|ПОМЕСТИТЬ ВТСведенияОПодключенииККЭДО
		|ИЗ
		|	ВТОтборовФизическихЛиц КАК ТаблицаОтборов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК КАК ФизическиеЛицаПрисоединенныеКЭДОБЗК
		|		ПО ТаблицаОтборов.ФизическоеЛицо = ФизическиеЛицаПрисоединенныеКЭДОБЗК.ФизическоеЛицо
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаОтборов.ФизическоеЛицо";
	
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборовФизическихЛиц", ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц);
	КадровыйУчет.УстановитьВТекстеЗапросаИмяПоляФизическоеЛицо(
		Запрос.Текст, "ТаблицаОтборов.ФизическоеЛицо", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТСведенияОПодключенииККЭДО", ИмяВТСведенияОПодключенииККЭДО);
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

Процедура УстановитьПризнакПолученияСогласияЭДОБЗКРанее(ФизическиеЛица, Организация, Значение) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	НачатьТранзакцию();
	Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл
		
		НаборЗаписей = РегистрыСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		
		Если Значение = Истина Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.ФизическоеЛицо = ФизическоеЛицо;
			Запись.Организация = Организация;
			Запись.Подключен = Истина;
			Запись.СогласиеНаЭДОБЗКПолученоРанее = Истина;
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

// Возвращает таблицу физических лиц подключенных к КЭДО
//	Параметры:
//		ФизическиеЛица - Массив из СправочникСсылка.ФизическиеЛица - массив проверяемых физических лиц.
//	Возвращаемое значение:
//		ТаблицаЗначений - таблица физических лиц и состояния подключения к КЭДО.
Функция ФизическиеЛицаПрисоединенныеКЭДОБЗК(ФизическиеЛица, Приложение) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.ГоловнаяОрганизация КАК Организация,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ЕСТЬNULL(ЛицаПрисоединенныеККЭДО.Подключен, ЛОЖЬ) КАК ИспользуетЭДОБЗК
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъекты1СПерсонал КАК ВыгружаемыеСотрудники
		|		ПО Сотрудники.ФизическоеЛицо = ВыгружаемыеСотрудники.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК КАК ЛицаПрисоединенныеККЭДО
		|		ПО Сотрудники.ГоловнаяОрганизация = ЛицаПрисоединенныеККЭДО.Организация
		|			И Сотрудники.ФизическоеЛицо = ЛицаПрисоединенныеККЭДО.ФизическоеЛицо
		|ГДЕ
		|	ЛицаПрисоединенныеККЭДО.ФизическоеЛицо В(&ФизическиеЛица)";
	
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	
	Если Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВыгружаемыеОбъекты1СПерсонал", "ВыгружаемыеОбъектыКабинетСотрудника");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаполнитьОрганизации(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбработанных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбработанных") Тогда
			МассивОбработанных = ПараметрыОбновления.МассивОбработанных;
		Иначе
			МассивОбработанных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбработанных", МассивОбработанных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбработанных", МассивОбработанных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ФизическиеЛицаПрисоединенныеКЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	РегистрСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК КАК ФизическиеЛицаПрисоединенныеКЭДОБЗК
		|ГДЕ
		|	НЕ ФизическиеЛицаПрисоединенныеКЭДОБЗК.ФизическоеЛицо В (&МассивОбработанных)
		|	И ФизическиеЛицаПрисоединенныеКЭДОБЗК.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Запрос.Выполнить();
	Если Не ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛица") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛицаПрисоединенныеКЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.ГоловнаяОрганизация КАК Организация,
		|	ФизическиеЛицаПрисоединенныеКЭДОБЗК.Подключен КАК Подключен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ВыгружаемыеОбъекты1СПерсонал.Ссылка ЕСТЬ NULL
		|					И ВыгружаемыеОбъектыКабинетСотрудника.Ссылка ЕСТЬ NULL
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ) КАК Выгружается
		|ИЗ
		|	РегистрСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК КАК ФизическиеЛицаПрисоединенныеКЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
		|		ПО ФизическиеЛицаПрисоединенныеКЭДОБЗК.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъекты1СПерсонал КАК ВыгружаемыеОбъекты1СПерсонал
		|			ПО Сотрудники.Ссылка = ВыгружаемыеОбъекты1СПерсонал.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъектыКабинетСотрудника
		|			ПО Сотрудники.Ссылка = ВыгружаемыеОбъектыКабинетСотрудника.Ссылка
		|		ПО ФизическиеЛицаПрисоединенныеКЭДОБЗК.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
		|
		|СГРУППИРОВАТЬ ПО
		|	ФизическиеЛицаПрисоединенныеКЭДОБЗК.ФизическоеЛицо,
		|	Сотрудники.ГоловнаяОрганизация,
		|	ФизическиеЛицаПрисоединенныеКЭДОБЗК.Подключен
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизическоеЛицо,
		|	Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
		
		МассивОбработанных.Добавить(Выборка.ФизическоеЛицо);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "РегистрСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК", "ФизическоеЛицо", Выборка.ФизическоеЛицо) Тогда
			
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
		
		Пока Выборка.Следующий() Цикл
			
			Если Не ЗначениеЗаполнено(Выборка.Организация)
				Или Не Выборка.Выгружается Тогда
				
				Продолжить;
			КонецЕсли;
			
			Запись = НаборЗаписей.Добавить();
			Запись.ФизическоеЛицо = Выборка.ФизическоеЛицо;
			Запись.Организация = Выборка.Организация;
			Запись.Подключен = Выборка.Подключен;
			
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
