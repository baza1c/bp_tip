#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет настройки интеграции с СБП.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация из документа продажи;
//  ВидОплаты - СправочникСсылка.ВидОплаты, Неопределено - вид оплаты, необязательный.
//
// Возвращаемое значение:
//  СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//
Функция ТорговаяТочка(Организация, ВидОплаты = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СоответствиеНастроекИнтеграции.ТорговаяТочка КАК НастройкиИнтеграции
	|ИЗ
	|	РегистрСведений.СоответствиеНастроекИнтеграции КАК СоответствиеНастроекИнтеграции
	|ГДЕ
	|	И СоответствиеНастроекИнтеграции.Организация = &Организация
	|	И СоответствиеНастроекИнтеграции.ВидОплаты = &ВидОплаты";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ВидОплаты = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(ТекстЗапроса,
			"И СоответствиеНастроекИнтеграции.ВидОплаты = &ВидОплаты", "");
	Иначе
		Запрос.УстановитьПараметр("ВидОплаты", ВидОплаты);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.НастройкиИнтеграции;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Определяет настройки торговой точки платежной системы.
//
// Параметры:
//  СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//
// Возвращаемое значение:
//  Структура - настройки торговой точки:
//    *Организация - СправочникСсылка.Организации - организация из документа продажи;
//    *ВидОплаты - СправочникСсылка.ВидОплаты - вид оплаты для документа отражения оплаты.
//
Функция НастройкиТорговойТочки(НастройкиИнтеграции) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНастроекИнтеграции.Организация КАК Организация,
	|	СоответствиеНастроекИнтеграции.ВидОплаты КАК ВидОплаты
	|ИЗ
	|	РегистрСведений.СоответствиеНастроекИнтеграции КАК СоответствиеНастроекИнтеграции
	|ГДЕ
	|	СоответствиеНастроекИнтеграции.ТорговаяТочка = &НастройкиИнтеграции";
	
	Запрос.УстановитьПараметр("НастройкиИнтеграции", НастройкиИнтеграции);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = Новый Структура;
		Результат.Вставить("Организация", ВыборкаДетальныеЗаписи.Организация);
		Результат.Вставить("ВидОплаты",   ВыборкаДетальныеЗаписи.ВидОплаты);
		
		Возврат Результат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ВсеНастройкиИнтеграции() Экспорт
	
	Организации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНастроекИнтеграции.ТорговаяТочка КАК ТорговаяТочка
	|ИЗ
	|	РегистрСведений.СоответствиеНастроекИнтеграции КАК СоответствиеНастроекИнтеграции
	|ГДЕ
	|	СоответствиеНастроекИнтеграции.Организация В(&Организации)
	|	И НЕ СоответствиеНастроекИнтеграции.ТорговаяТочка.ПометкаУдаления
	|	И СоответствиеНастроекИнтеграции.ТорговаяТочка.Используется";
	
	Настройки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТорговаяТочка");
	
	Возврат Настройки;
	
КонецФункции

Функция ТаблицаНастроекИнтеграцииПоОрганизации(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНастроекИнтеграции.ТорговаяТочка КАК НастройкаИнтеграции,
	|	ЕСТЬNULL(СоответствиеНастроекИнтеграции.Банк.Код, """") КАК БИКБанка
	|ИЗ
	|	РегистрСведений.СоответствиеНастроекИнтеграции КАК СоответствиеНастроекИнтеграции
	|ГДЕ
	|	СоответствиеНастроекИнтеграции.Организация = &Организация
	|	И НЕ СоответствиеНастроекИнтеграции.ТорговаяТочка.ПометкаУдаления
	|	И СоответствиеНастроекИнтеграции.ТорговаяТочка.Используется";
	
	Настройки = Запрос.Выполнить().Выгрузить();
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
