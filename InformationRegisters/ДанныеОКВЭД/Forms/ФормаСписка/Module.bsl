///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимВыбора = Параметры.РежимВыбора;
	СтрокаПоиска = Параметры.СтрокаПоиска;
	ТекущийКод = Параметры.ТекущийКод;
	Разрешенные = Параметры.Разрешенные;
	
	Элементы.Выбрать.Видимость = РежимВыбора;
	
	Если РежимВыбора Тогда
		Элементы.Дерево.МножественныйВыбор = Параметры.МножественныйВыбор;
		Элементы.Дерево.РежимВыделения =
			?(Параметры.МножественныйВыбор,
				РежимВыделенияТаблицы.Множественный,
				РежимВыделенияТаблицы.Одиночный);
	КонецЕсли;
	
	ТолькоПросмотр = Истина;
	
	ОбновитьДерево();
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		СвернутьРазвернутьВсе("Развернуть");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Дерево.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекущийКод = "";
	Иначе
		ТекущийКод = ТекущиеДанные.Код;
	КонецЕсли;
	
	ОбновитьДерево();
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		СвернутьРазвернутьВсе("Развернуть");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОшибкаОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:updateClassifier" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Идентификаторы = Новый Массив;
		Идентификаторы.Добавить(КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов());
		
		ПараметрыОбновления = РаботаСКлассификаторамиКлиент.НовыйПараметрыОбновленияКлассификаторов(
			ЭтотОбъект,
			Идентификаторы,
			Новый ОписаниеОповещения(
				"ЗакрытиеФормыОбновления",
				ЭтотОбъект));
			
		РаботаСКлассификаторамиКлиент.ОбновитьКлассификаторы(ПараметрыОбновления);
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДерево

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если РежимВыбора Тогда
		ПриВыборе();
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.ТипЗначения = "ОКВЭД" Тогда
		Ключ = КлючЗаписи(Элемент.ТекущиеДанные.Код);
		ПараметрыФормы = Новый Структура("Ключ, ТолькоПросмотр", Ключ, Истина);
		ОткрытьФорму("РегистрСведений.ДанныеОКВЭД.ФормаЗаписи", ПараметрыФормы);
	Иначе
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Редактирование данных раздела недоступно.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ПриВыборе();
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсе(Команда)
	
	СвернутьРазвернутьВсе("Свернуть");
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьВсе(Команда)
	
	СвернутьРазвернутьВсе("Развернуть");
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДерево();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементРезультатПоиска = УсловноеОформление.Элементы.Добавить();
	ЭлементРезультатПоиска.Использование = Истина;
	
	ГруппаИ = ЭлементРезультатПоиска.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ГруппаИ.Использование = Истина;
	
	НовыйОтбор = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дерево.СодержитТекстПоиска");
	НовыйОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НовыйОтбор.ПравоеЗначение = Истина;
	
	НовыйОтбор = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтрокаПоиска");
	НовыйОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	НовоеПоле = ЭлементРезультатПоиска.Поля.Элементы.Добавить();
	НовоеПоле.Использование = Истина;
	НовоеПоле.Поле = Новый ПолеКомпоновкиДанных("Дерево");
	
	ЭлементРезультатПоиска.Оформление.УстановитьЗначениеПараметра(
		"Шрифт",
		ШрифтыСтиля.ВажнаяНадписьШрифт);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДерево()
	
	СКД = РегистрыСведений.ДанныеОКВЭД.ПолучитьМакет("Иерархия");
	
	Настройки = СКД.НастройкиПоУмолчанию;
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		
		Параметр =
			Настройки.ПараметрыДанных.НайтиЗначениеПараметра(
				Новый ПараметрКомпоновкиДанных("СтрокаПоиска"));
		
		Если Параметр <> Неопределено Тогда
			Параметр.Значение = "%" + СтрокаПоиска + "%";
			Параметр.Использование = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерВременныхТаблиц = МенеджерВременныхТаблиц();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СКД, Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет,,,,, МенеджерВременныхТаблиц);
	
	ДеревоИсточник = Новый ДеревоЗначений;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДеревоИсточник);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ДеревоПолучатель = РеквизитФормыВЗначение("Дерево");
	ДеревоПолучатель.Строки.Очистить();
	
	ВывестиДерево(ДеревоИсточник, ДеревоПолучатель, "");
	
	ЗначениеВРеквизитФормы(ДеревоПолучатель, "Дерево");
	
	УстановитьТекущуюСтроку();
	
	ПроверитьОшибкиРегистра();
	
КонецПроцедуры

&НаСервере
Процедура ВывестиДерево(Источник, Получатель, Знач КодРодителя)
	
	Для Каждого СтрокаИсточник Из Источник.Строки Цикл
		
		Если СтрокаИсточник.Ссылка = КодРодителя Тогда
			ЗаполнитьЗначенияСвойств(
				Получатель,
				СтрокаИсточник);
			Продолжить;
		КонецЕсли;
		
		Если Не СтрокаИсточник.СодержитТекстПоиска Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаПолучатель = Получатель.Строки.Добавить();
		
		Если СтрокаИсточник.Строки.Количество() > 0 Тогда
			ВывестиДерево(
				СтрокаИсточник,
				СтрокаПолучатель,
				СтрокаИсточник.Ссылка);
		Иначе
			ЗаполнитьЗначенияСвойств(
				СтрокаПолучатель,
				СтрокаИсточник);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтроку()
	
	Если Не ЗначениеЗаполнено(ТекущийКод) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Дерево.ТекущаяСтрока = ИдентификаторТекущегоКода(Дерево.ПолучитьЭлементы());
	
КонецПроцедуры

&НаСервере
Функция ИдентификаторТекущегоКода(ЭлементыДерева)
	
	Для Каждого СтрокаДерева Из ЭлементыДерева Цикл
		Если СтрокаДерева.Код = ТекущийКод Тогда
			Возврат СтрокаДерева.ПолучитьИдентификатор();
		КонецЕсли;
		
		ПоискВПодстроках = ИдентификаторТекущегоКода(СтрокаДерева.ПолучитьЭлементы());
		Если ПоискВПодстроках >= 0 Тогда
			Возврат ПоискВПодстроках;
		КонецЕсли;
	КонецЦикла;
	
	Возврат -1;
	
КонецФункции

&НаСервереБезКонтекста
Функция КлючЗаписи(Знач Код)
	
	Ключ = Новый Структура("Код", Код);
	Возврат РегистрыСведений.ДанныеОКВЭД.СоздатьКлючЗаписи(Ключ);
	
КонецФункции

&НаКлиенте
Процедура ПриВыборе()
	
	ТекущиеДанные = Элементы.Дерево.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВывестиСписокНедоступных = Ложь;
	
	Если Элементы.Дерево.МножественныйВыбор
		И Элементы.Дерево.ВыделенныеСтроки.Количество() > 1 Тогда
		ДанныеВыбора = ПриВыбореМножественный();
		ВывестиСписокНедоступных = Истина;
	Иначе
		ДанныеВыбора = ПриВыбореОдиночный();
	КонецЕсли;
	
	Если ДанныеВыбора.ЕстьНедоступные Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Выбрано недопустимое значение.'"));
		
		ОчиститьСообщения();
		Если ВывестиСписокНедоступных Тогда
			Если ДанныеВыбора.НедоступныеДляВыбора.Количество() = 1 Тогда
				ТекстШаблона = НСтр("ru = 'Код %1 недоступен для выбора.'");
			Иначе
				ТекстШаблона = НСтр("ru = 'Коды %1 недоступны для выбора.'");
			КонецЕсли;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстШаблона,
					СтрСоединить(ДанныеВыбора.НедоступныеДляВыбора, ", ")));
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	РезультатВыбора =
		Новый Структура(
			"ВыбранныеЗначения",
			ДанныеВыбора.ВыбранныеЗначения);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Функция ПриВыбореОдиночный()
	
	ДанныеВыбора = НовыйДанныеВыбора(Разрешенные);
	
	ТекущиеДанные = Элементы.Дерево.ТекущиеДанные;
	
	Если ТекущиеДанные.ТипЗначения = "Раздел" Тогда
		
		ДанныеВыбора.НедоступныеДляВыбора.Добавить(ТекущиеДанные.Раздел);
		ДанныеВыбора.ЕстьНедоступные = Истина;
		Возврат ДанныеВыбора;
			
	ИначеЕсли ТекущиеДанные.ТипЗначения = "ОКВЭД" Тогда
			
		Если Разрешенные И Не КлассификаторОКВЭДСлужебныйКлиентСервер.КодРазрешенДляВыбора(ТекущиеДанные.Код) Тогда
			ДанныеВыбора.НедоступныеДляВыбора.Добавить(ТекущиеДанные.Код);
			ДанныеВыбора.ЕстьНедоступные = Истина;
			Возврат ДанныеВыбора;
		КонецЕсли;
	
	Иначе
		
		ДанныеВыбора.НедоступныеДляВыбора.Добавить(ТекущиеДанные.Код);
		ДанныеВыбора.ЕстьНедоступные = Истина;
		Возврат ДанныеВыбора;
		
	КонецЕсли;
	
	ДобавитьЗначениеВДанныеВыбора(
		ДанныеВыбора,
		ТекущиеДанные);
		
	Возврат ДанныеВыбора;
	
КонецФункции

&НаКлиенте
Функция ПриВыбореМножественный()
	
	ВыбранныеКоды = Новый Массив;
	ВыбранныеРазделы = Новый Соответствие;

	Для Каждого Индекс Из Элементы.Дерево.ВыделенныеСтроки Цикл
		Значение = Дерево.НайтиПоИдентификатору(Индекс);
		Если Значение <> Неопределено Тогда
			Если Значение.ТипЗначения = "ОКВЭД" Тогда
				ВыбранныеКоды.Добавить(Значение.Код);
			ИначеЕсли Значение.ТипЗначения = "Раздел" Тогда
				ВыбранныеРазделы.Вставить(
					Значение.Раздел,
					НовыйДанныеРегистра(Значение));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Результат = ДанныеВыбораПоКодам(
		ВыбранныеКоды,
		ВыбранныеРазделы,
		Разрешенные);
	
	Возврат Результат;
	
КонецФункции

// Формирует пустое описание результата обработки выбранных значений.
// 
// Параметры:
//  Разрешенные - Булево - признак допустимости выбора только разрешенных кодов.
// 
// Возвращаемое значение:
//  Структура - результат обработки выбранных значений:
//    * ВыбранныеЗначения - Массив Из Структура - см. НовыйДанныеРегистра.
//    * НедоступныеДляВыбора - Массив Из Строка - список запрещенных кодов, которые выбрал пользователь.
//    * ЕстьНедоступные - Булево - признак того, что пользователь выбрал неразрешенные элементы.
//    * Разрешенные - Булево - признак допустимости выбора только разрешенных кодов.
//
&НаКлиентеНаСервереБезКонтекста
Функция НовыйДанныеВыбора(Разрешенные)
	
	ДанныеВыбора = Новый Структура;
	
	ДанныеВыбора.Вставить("ВыбранныеЗначения", Новый Массив);
	ДанныеВыбора.Вставить("НедоступныеДляВыбора", Новый Массив);
	ДанныеВыбора.Вставить("ЕстьНедоступные", Ложь);
	ДанныеВыбора.Вставить("Разрешенные", Разрешенные);
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Формирует данные по выбранным кодам в зависимости от доступности.
// 
// Параметры:
//  ВыбранныеКоды - Массив Из Строка - выбранные коды, которые необходимо проверить.
//  ВыбранныеРазделы - Соответствие Из КлючИЗначение - выбранные разделы:
//    * Ключ - Строка - раздел.
//    * Значение - см. НовыйДанныеРегистра.
//  Разрешенные - Булево - признак доступности для выбора только разрешенных значений.
// 
// Возвращаемое значение:
//  см. НовыйДанныеВыбора
&НаСервереБезКонтекста
Функция ДанныеВыбораПоКодам(
		Знач ВыбранныеКоды,
		Знач ВыбранныеРазделы,
		Знач Разрешенные = Ложь)
	
	ДанныеВыбора = НовыйДанныеВыбора(Разрешенные);
	
	ДанныеРегистра = КлассификаторОКВЭД.ЗначенияПоКодам(ВыбранныеКоды);
	
	Если ВыбранныеРазделы.Количество() > 0 Тогда
		ДанныеВыбора.ЕстьНедоступные = Истина;
		Для Каждого КлючЗначение Из ВыбранныеРазделы Цикл
			ДанныеВыбора.НедоступныеДляВыбора.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		ВыбранныеРазделы.Очистить();
	КонецЕсли;
	
	Если ДанныеВыбора.Разрешенные Тогда
		
		Для Каждого Выборка Из ДанныеРегистра Цикл
			
			Если Не КлассификаторОКВЭДСлужебныйКлиентСервер.КодРазрешенДляВыбора(Выборка.Код) Тогда
				ДанныеВыбора.НедоступныеДляВыбора.Добавить(Выборка.Код);
				ДанныеВыбора.ЕстьНедоступные = Истина;
			КонецЕсли;
			
			Если ДанныеВыбора.ЕстьНедоступные Тогда
				Продолжить;
			КонецЕсли;
			
			ДобавитьЗначениеВДанныеВыбора(ДанныеВыбора, Выборка);
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого Выборка Из ДанныеРегистра Цикл
			ДобавитьЗначениеВДанныеВыбора(ДанныеВыбора, Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Добавляет значение в возвращаемый результат.
//
// Параметры:
//  ДанныеВыбора - см. НовыйДанныеВыбора.
//  Значение - ВыборкаИзРезультатаЗапроса, Структура, ТекущиеДанные дерева - данные, которые нужно добавить в результат.
//
&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьЗначениеВДанныеВыбора(ДанныеВыбора, Значение)
	
	ДанныеРегистра = НовыйДанныеРегистра(Значение);
	ДанныеВыбора.ВыбранныеЗначения.Добавить(ДанныеРегистра);
	
КонецПроцедуры

// Преобразует переданные значения в формализованную форму
//
// Параметры:
//  Значение - ВыборкаИзРезультатаЗапроса, Структура, ТекущиеДанные дерева - данные, которые нужно добавить в результат.
//
// Возвращаемое значение:
//  Структура - данные регистра:
//    * Раздел - Строка - раздел классификатора, к которому относится элемент.
//    * Код - Строка - код по классификатору.
//    * Наименование - Строка - текстовое описание элемента.
//    * Представление - Строка - код и наименование.
//    * Описание - Строка - описание элемента.
//
&НаКлиентеНаСервереБезКонтекста
Функция НовыйДанныеРегистра(Значение)
	
	ДанныеРегистра = Новый Структура;
	ДанныеРегистра.Вставить("Раздел", Значение.Раздел);
	ДанныеРегистра.Вставить("Код", Значение.Код);
	ДанныеРегистра.Вставить("Наименование", Значение.Наименование);
	ДанныеРегистра.Вставить("Описание", Значение.Описание);
	
	Если ЗначениеЗаполнено(Значение.Код) Тогда
		Представление = Значение.Код + " " + Значение.Наименование;
	Иначе
		Представление = Значение.Раздел + " " + Значение.Наименование;
	КонецЕсли;
	ДанныеРегистра.Вставить("Представление", Представление);
	
	Возврат ДанныеРегистра;
	
КонецФункции

&НаКлиенте
Процедура СвернутьРазвернутьВсе(Вариант)
	
	Если Вариант = "Свернуть" Тогда
		
		// В некоторых случаях не сворачивает ветку, если выделен дочерний элемент, поэтому выделяем самый верхний.
		ВыделитьРодителяПервогоУровня();
		
		Для Каждого Стр Из Дерево.ПолучитьЭлементы() Цикл
			Элементы.Дерево.Свернуть(Стр.ПолучитьИдентификатор());
		КонецЦикла;
		
	ИначеЕсли Вариант = "Развернуть" Тогда
		
		Для Каждого Стр Из Дерево.ПолучитьЭлементы() Цикл
			Элементы.Дерево.Развернуть(Стр.ПолучитьИдентификатор(), Истина);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьРодителяПервогоУровня()
	
	Если Элементы.Дерево.ТекущаяСтрока = Неопределено Тогда
		Элементы.Дерево.ТекущаяСтрока = -1;
		Возврат;
	КонецЕсли;
	
	Значение = Дерево.НайтиПоИдентификатору(Элементы.Дерево.ТекущаяСтрока);
	Если ТипЗнч(Значение) <> Тип("ДанныеФормыЭлементДерева") Тогда
		Элементы.Дерево.ТекущаяСтрока = -1;
		Возврат;
	КонецЕсли;
	
	Родитель = Значение.ПолучитьРодителя();
	Пока Не Родитель = Неопределено Цикл
		Значение = Родитель;
		Родитель = Значение.ПолучитьРодителя();
	КонецЦикла;
	
	Элементы.Дерево.ТекущаяСтрока = Значение.ПолучитьИдентификатор();
	
КонецПроцедуры

// Формирует временные таблицы для СКД
// 
// Возвращаемое значение:
//  МенеджерВременныхТаблиц - временные таблицы.
//  
&НаСервереБезКонтекста
Функция МенеджерВременныхТаблиц()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	ТаблицаРазделов = КлассификаторОКВЭД.Разделы();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗРазделы.Раздел КАК Раздел,
		|	ТЗРазделы.Наименование КАК Наименование,
		|	ТЗРазделы.Описание КАК Описание
		|ПОМЕСТИТЬ ВТРазделы
		|ИЗ
		|	&ТЗРазделы КАК ТЗРазделы";
	Запрос.УстановитьПараметр("ТЗРазделы", ТаблицаРазделов);
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

&НаСервере
Процедура ПроверитьОшибкиРегистра()
	
	Результат = РегистрыСведений.ДанныеОКВЭД.ПроблемыСРегистром();
	
	Элементы.ГруппаОшибки.Видимость = Результат.ЕстьОшибки;
	Элементы.ДекорацияОшибка.Заголовок = Результат.ИнформацияОбОшибке;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыОбновления(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДерево();
	
КонецПроцедуры

#КонецОбласти
