#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает параметры для выбора кода льготы.
// 
// Параметры:
//  НазваниеМакета - Строка - Макет из которого необходимо получить списки кодов.
//  ТекущийПериод - Дата - Период для первоначального позиционирования формы выбора.
// 
// Возвращаемое значение:
//  Структура:
//   * СписокКодов - ТаблицаЗначений
//   * СписокПериодовДействия - СписокЗначений
//   * ТекущийПериод - Строка - наименование области макета с актуальными кодами
Функция ПолучитьПараметрыФормыВыбораДляКода(НазваниеМакета, ТекущийПериод) Экспорт
	
	КодыЛьгот = Новый ТаблицаЗначений;
	
	КодыЛьгот.Колонки.Добавить("Код");
	КодыЛьгот.Колонки.Добавить("Наименование");
	КодыЛьгот.Колонки.Добавить("КодЕдиницыИзмерения");
	
	Макет = ПолучитьМакет(НазваниеМакета);
	
	НазваниеОбласти = "";
	СписокОбластей = Новый СписокЗначений;
	ОпределитьПараметрыСпискаКодов(Макет, ТекущийПериод, НазваниеОбласти, СписокОбластей);
	
	ТекущаяОбласть = Макет.Области.Найти("Область" + НазваниеОбласти);
	
	Если Не (ТекущаяОбласть = Неопределено) Тогда	
		
		Для НомерСтр = ТекущаяОбласть.Верх По ТекущаяОбласть.Низ Цикл
			
			// Перебираем строки макета.
			КодПоказателя       = СокрП(Макет.Область(НомерСтр, 1).Текст);
			Название            = СокрП(Макет.Область(НомерСтр, 2).Текст);
			КодЕдиницыИзмерения = СокрП(Макет.Область(НомерСтр, 3).Текст);
			
			Если КодПоказателя = "###" Тогда
				Прервать;
			ИначеЕсли ПустаяСтрока(КодПоказателя) Тогда
				Продолжить;
			Иначе
				НоваяСтрока = КодыЛьгот.Добавить();
				НоваяСтрока.Код                 = КодПоказателя;
				НоваяСтрока.Наименование        = Название;
				НоваяСтрока.КодЕдиницыИзмерения = КодЕдиницыИзмерения;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("СписокКодов"           , КодыЛьгот);
	Параметры.Вставить("СписокПериодовДействия", СписокОбластей);
	Параметры.Вставить("ТекущийПериод"         , НазваниеОбласти);
	
	Возврат Параметры;
	
КонецФункции

Процедура ПроверитьЗначениеОпцииВедетсяУчетПлатежейВПлатон(НаборЗаписей, Отказ, Замещение) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетПлатежейВПлатон") И НаборЗаписей.Количество() > 0 Тогда
	
		Для Каждого Запись Из НаборЗаписей Цикл
			
			Если Запись.ЗарегистрированоВРеестреСистемыПлатон Тогда
		
				УстановитьПривилегированныйРежим(Истина);
				Константы.ВедетсяУчетПлатежейВПлатон.Установить(Истина);
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьОпциюУплачиваетсяТранспортныйНалог(НаборЗаписей) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	УплачиваетсяТранспортныйНалог = Константы.УплачиваетсяТранспортныйНалог.Получить();
	ВНабореЕстьРегистрация = Ложь;
	
	// Если записывается первая регистрация (других нет), то включаем опцию
	Для Каждого Запись Из НаборЗаписей Цикл		
		ВНабореЕстьРегистрация = ВНабореЕстьРегистрация Или Запись.ВключатьВНалоговуюБазу;
		Если Запись.ВключатьВНалоговуюБазу И Не УплачиваетсяТранспортныйНалог Тогда		
			Константы.УплачиваетсяТранспортныйНалог.Установить(Истина);
			Возврат;			
		КонецЕсли;
	КонецЦикла;
	
	// Если в наборе нет записей регистрации (т.е. либо только записи о снятии с учета, либо удаление регистрации),
	// то, возможно, опцию нужно отключить
	Если УплачиваетсяТранспортныйНалог И Не ВНабореЕстьРегистрация И Не ЕстьТранспортныеСредства() Тогда
		Константы.УплачиваетсяТранспортныйНалог.Установить(Ложь);
	КонецЕсли;		
	
КонецПроцедуры

Функция СписокКодовЕдиницИзмеренияНалоговойБазы() Экспорт
	
	Список = Новый СписокЗначений;
	
	Список.Добавить("251", НСтр("ru='л. с.'"));
	Список.Добавить("999", НСтр("ru='Кгс'"));
	Список.Добавить("181", НСтр("ru='БРТ'"));
	Список.Добавить("796", НСтр("ru='шт'"));
	
	Возврат Список;
	
КонецФункции

Функция ЕстьТранспортныеСредства(Период = Неопределено, Периодичность = Неопределено, Организация = Неопределено) Экспорт
	
	Если Организация = Неопределено Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РегистрацияТранспортныхСредств.ОсновноеСредство
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу";
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НачалоПериода", ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, Период));
		Запрос.УстановитьПараметр("КонецПериода",  ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период));
		Запрос.УстановитьПараметр("Организация",   Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РегистрацияТранспортныхСредств.Организация
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	РегистрацияТранспортныхСредств.Организация
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу";
				
	КонецЕсли;
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ЕстьЛьготыПоТранспортномуНалогу(Организация, Период, Периодичность) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, Период);
	КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	// ТС, снятые с учета до 15.01 или поставленные на учет после 15.12 не попадут в расчет налога
	Запрос.УстановитьПараметр("НачалоПериода", Дата(Год(НачалоПериода), Месяц(НачалоПериода), 15));
	Запрос.УстановитьПараметр("КонецПериода", Дата(Год(КонецПериода), Месяц(КонецПериода), 15));
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегистрацияТССрезПоследних.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК РегистрацияТССрезПоследних
	|ГДЕ
	|	РегистрацияТССрезПоследних.ВключатьВНалоговуюБазу
	|	И НЕ РегистрацияТССрезПоследних.НалоговаяЛьготаДо2020 В (ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.ПустаяССылка), ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.НеПрименяется))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегистрацияТранспортныхСредств.ОсновноеСредство
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
	|	И НЕ РегистрацияТранспортныхСредств.НалоговаяЛьготаДо2020 В (ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.ПустаяССылка), ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.НеПрименяется))";
		
	Возврат Не Запрос.Выполнить().Пустой();	
		
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиОбновления

Процедура УстановитьОпциюУплачиваетсяТранспортныйНалог() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если ЕстьТранспортныеСредства() Тогда
		Константы.УплачиваетсяТранспортныйНалог.Установить(Истина);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗаполнитьНовыеРеквизиты(Параметры) Экспорт
	
	// В прежней структуре регистра вид транспортного средства определялся через ресурс КодВидаТранспортногоСредства[До2020].
	// Для данных кодов ТС было установлено соответствие с категориями ТС, на основании которых установлены ставки налога.
	// Соотвеnствие хранилось в макете КодыВидовИКатегорииТС[До2020].
	// Теперь используется новая классификация на основе ресурса ВидТранспортногоСредства (перечисление). Для заполнения
	// данного ресурса в прежний макет добавлены ссылки на новое перечисление.
	// 
	// В прежней структуре регистра льгота задавалась ресурсом НалоговаяЛьгота[До2020] (перечисление), а ресурсы
	// КодНалоговойЛьготы[До2020], РегиональныйКодЛьготы[До2020] и ПроцентУменьшения[До2020] уточняли параметры льготы.
	// Теперь эта функция перешла к ОснованиеЛьготы (справочник). Процент уменьшения (суммы налога) - это фиксированный
	// атрибут конкретной льготы (в конкретном пункте местного закона указан процент уменьшения), поэтому показатель в
	// регистре больше не требуется.
	// В обработчике обновления для каждого значения прежнего перечисления ищется/создается льгота с определенным кодом.
	
	// Игнорируем записи, где указан несуществующий код вида ТС. Для этого используем отбор по таблице кодов из макета.
	КодыВидовИКатегорииТС = РасчетИмущественныхНалогов.КодыВидовИКатегорииТранспортныхСредств();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(КодыВидовИКатегорииТС.КодВида КАК СТРОКА(5)) КАК КодВидаТС
	|ПОМЕСТИТЬ КодыВидовТС
	|ИЗ
	|	&КодыВидовИКатегорииТС КАК КодыВидовИКатегорииТС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодВидаТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.Организация КАК Организация,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО РегистрацияТранспортныхСредств.Организация = Организации.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ОсновныеСредства.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КодыВидовТС КАК СуществующиеКодыВидовТС
	|		ПО РегистрацияТранспортныхСредств.КодВидаТранспортногоСредстваДо2020 = СуществующиеКодыВидовТС.КодВидаТС
	|ГДЕ
	|	РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
	|	И (РегистрацияТранспортныхСредств.ВидТранспортногоСредства = ЗНАЧЕНИЕ(Перечисление.ВидыТранспортныхСредств.ПустаяСсылка)
	|			ИЛИ РегистрацияТранспортныхСредств.ОснованиеЛьготы = ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
	|				И НЕ РегистрацияТранспортныхСредств.НалоговаяЛьготаДо2020 В (ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.ПустаяСсылка), ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.НеПрименяется)))
	|
	|УПОРЯДОЧИТЬ ПО
	|	РегистрацияТранспортныхСредств.Период УБЫВ";
	
	Запрос.УстановитьПараметр("КодыВидовИКатегорииТС", КодыВидовИКатегорииТС);
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Если РезультатЗапроса.Пустой() Тогда
		// Обработаны все записи
		Параметры.ОбработкаЗавершена = Истина;
		
		// Дополнительно сообщим, если какие-то записи были пропущены из-за ошибочного кода вида ТС
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегистрацияТранспортныхСредств.Период КАК Период,
		|	РегистрацияТранспортныхСредств.Организация КАК Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредстваДо2020 КАК КодВидаТранспортногоСредстваДо2020
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО РегистрацияТранспортныхСредств.Организация = Организации.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
		|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ОсновныеСредства.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ КодыВидовТС КАК СуществующиеКодыВидовТС
		|		ПО РегистрацияТранспортныхСредств.КодВидаТранспортногоСредстваДо2020 = СуществующиеКодыВидовТС.КодВидаТС
		|ГДЕ
		|	РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|	И РегистрацияТранспортныхСредств.ВидТранспортногоСредства = ЗНАЧЕНИЕ(Перечисление.ВидыТранспортныхСредств.ПустаяСсылка)
		|	И СуществующиеКодыВидовТС.КодВидаТС ЕСТЬ NULL";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;
		
		ВыборкаЗаписей = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаЗаписей.Следующий() Цикл
			ТекстСообщения = Новый Массив;
			Описание = НСтр("ru = 'Не удалось обработать запись регистра ""Регистрация транспортных средств""
				|(период: %1, организация: %2, основное средство: %3) по причине:'");
			
			Описание = СтрЗаменить(Описание, Символы.ПС, " ");
			Описание = СтрШаблон(Описание, 
				Формат(ВыборкаЗаписей.Период, "ДЛФ=D"),
				ВыборкаЗаписей.Организация,
				ВыборкаЗаписей.ОсновноеСредство);
			
			ТекстСообщения.Добавить(Описание);
			ТекстСообщения.Добавить(СтрШаблон(
				НСтр("ru = 'Не найден вид ТС для кода ""%1""'"),
				ВыборкаЗаписей.КодВидаТранспортногоСредстваДо2020));
			
			ТекстСообщения = СтрСоединить(ТекстСообщения, Символы.ПС);
				
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств,
				,
				ТекстСообщения);
		КонецЦикла;
			
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ""РегистрСведений.РегистрацияТранспортныхСредств.ЗаполнитьНовыеРеквизиты""
			|не удалось заполнить вид транспортного средства в некоторых записях. Количество ошибок: %1'"), 
			ВыборкаЗаписей.Количество());
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств,
			,
			ТекстСообщения);
			
		Возврат;
		
	КонецЕсли; 
	
	// В любом случае будет выполнен еще хотя бы один вызов обработчика
	Параметры.ОбработкаЗавершена = Ложь;
	
	ВыборкаЗаписей = РезультатЗапроса.Выбрать();
	
	// Созданные элементы справочника льгот кэшируем в виде таблицы в параметрах
	Если Параметры.Свойство("Льготы") Тогда
		Льготы = Параметры.Льготы;
	Иначе
		Льготы = Новый ТаблицаЗначений;
		Льготы.Колонки.Добавить("ОснованиеЛьготы", Новый ОписаниеТипов("СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам"));
		Льготы.Колонки.Добавить("КодЛьготы", ОбщегоНазначения.ОписаниеТипаСтрока(7));
		Льготы.Колонки.Добавить("ВидЛьготы", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЛьготПоИмущественнымНалогам"));
		Льготы.Колонки.Добавить("ПроцентУменьшения", ОбщегоНазначения.ОписаниеТипаЧисло(5, 2, ДопустимыйЗнак.Неотрицательный));
		Льготы.Колонки.Добавить("КодРегиона", ОбщегоНазначения.ОписаниеТипаЧисло(2, 0, ДопустимыйЗнак.Неотрицательный));
		Льготы.Колонки.Добавить("УстановленаМестнымНормативнымАктом", Новый ОписаниеТипов("Булево"));
		Льготы.Колонки.Добавить("ОснованиеМестнойЛьготы", ОбщегоНазначения.ОписаниеТипаСтрока(24));
		
		Параметры.Вставить("Льготы", Льготы);
	КонецЕсли; 
	
	// Также кэшируем в параметрах данные, полученные из регистраций в налоговых органах
	Если Параметры.Свойство("КэшКодовРегионов") Тогда
		КэшКодовРегионов = Параметры.КэшКодовРегионов;
	Иначе
		КэшКодовРегионов = Новый Соответствие;
		Параметры.Вставить("КэшКодовРегионов", КэшКодовРегионов);
	КонецЕсли;
	
	// Также кэшируем коды оснований региональных льгот
	Если Параметры.Свойство("КэшОснованийРегиональныхЛьгот") Тогда
		КэшОснованийРегиональныхЛьгот = Параметры.КэшОснованийРегиональныхЛьгот;
	Иначе
		КэшОснованийРегиональныхЛьгот = Новый Соответствие;
		Параметры.Вставить("КэшОснованийРегиональныхЛьгот", КэшОснованийРегиональныхЛьгот);
	КонецЕсли;
	
	КоличествоОшибок = 0;
	
	Пока ВыборкаЗаписей.Следующий() Цикл
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаЗаписей);
		МенеджерЗаписи.Прочитать();
		
		Если Не МенеджерЗаписи.Выбран() Тогда
			Продолжить;
		КонецЕсли;
		
		КодРегиона = РасчетИмущественныхНалоговБП.КодРегионаПоМенеджеруЗаписи(МенеджерЗаписи, КэшКодовРегионов);
		
		Если Не ЗначениеЗаполнено(МенеджерЗаписи.ВидТранспортногоСредства) Тогда
			
			СтрокаВидаТС = КодыВидовИКатегорииТС.Найти(
				МенеджерЗаписи.КодВидаТранспортногоСредстваДо2020,
				"КодВида");
			МенеджерЗаписи.ВидТранспортногоСредства = Перечисления.ВидыТранспортныхСредств[СтрокаВидаТС.Вид];
			
		КонецЕсли;
		
		ЕстьЛьгота = Ложь;
		Если Не ЗначениеЗаполнено(МенеджерЗаписи.ОснованиеЛьготы)
			 И ЗначениеЗаполнено(МенеджерЗаписи.НалоговаяЛьготаДо2020)
			 И МенеджерЗаписи.НалоговаяЛьготаДо2020 <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.НеПрименяется Тогда 
			ЕстьЛьгота = Истина;
			ПоискЛьготы = СтруктураПоискаЛьготы(МенеджерЗаписи, КодРегиона, КэшОснованийРегиональныхЛьгот);
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			
			Если ЕстьЛьгота Тогда
				МенеджерЗаписи.ОснованиеЛьготы = ЛьготыПоИмущественнымНалогам.НайтиИлиСоздатьЛьготу(
					Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог,
					ПоискЛьготы,
					Льготы);
			КонецЕсли;
			
			МенеджерЗаписи.Записать();
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			КоличествоОшибок = КоличествоОшибок + 1;
			
			ТекстСообщения = Новый Массив;
			Описание = НСтр("ru = 'Не удалось обработать запись регистра ""Регистрация транспортных средств""
				|(период: %1, организация: %2, основное средство: %3) по причине:'");
			
			Описание = СтрЗаменить(Описание, Символы.ПС, " ");
			Описание = СтрШаблон(Описание, 
				Формат(ВыборкаЗаписей.Период, "ДЛФ=D"),
				ВыборкаЗаписей.Организация,
				ВыборкаЗаписей.ОсновноеСредство);
			
			ТекстСообщения.Добавить(Описание);
			ТекстСообщения.Добавить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ТекстСообщения = СтрСоединить(ТекстСообщения, Символы.ПС);
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств,
				,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если КоличествоОшибок > 0 Тогда
			
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ""РегистрСведений.РегистрацияТранспортныхСредств.ЗаполнитьНовыеРеквизиты""
			|не удалось обработать некоторые записи. Количество ошибок: %1'"), 
			КоличествоОшибок);
			
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция проверяет, обработана ли запись (см. ЗаполнитьНовыеРеквизиты()).
//  Определена в свойстве ПроцедураПроверки соответствующего обработчика обновления.
//
// Параметры:
//  Параметры - Структура - см. документацию к БСП
// 
// Возвращаемое значение:
//   - Булево - если Истина, то можно редактировать и записывать данные, Ложь - данные недоступны, пока не будут обработаны процедурой обновления
//
Функция ЗаписьОбработана(Параметры) Экспорт

	Данные = Неопределено;
	
  	Если ТипЗнч(Параметры.Данные) = Тип("РегистрСведенийМенеджерЗаписи.РегистрацияТранспортныхСредств") 
  		 Или ТипЗнч(Параметры.Данные) = Тип("ДанныеФормыСтруктура") Тогда
    	Данные = Параметры.Данные;
  	ИначеЕсли ТипЗнч(Параметры.Данные) = Тип("РегистрСведенийНаборЗаписей.РегистрацияТранспортныхСредств")
  		 И Параметры.Данные.Количество() > 0 Тогда
    	Данные = Параметры.Данные[0];
    КонецЕсли;
	
	Если Данные = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;

	ТребуетсяОбработкаЗаписи = Данные.ВключатьВНалоговуюБазу 
		И (Не ЗначениеЗаполнено(Данные.ВидТранспортногоСредства)
			Или Не ЗначениеЗаполнено(Данные.ОснованиеЛьготы)
				И ЗначениеЗаполнено(Данные.НалоговаяЛьготаДо2020)
				И Данные.НалоговаяЛьготаДо2020 <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.НеПрименяется);
	
	Возврат Не ТребуетсяОбработкаЗаписи;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьПараметрыСпискаКодов(Макет, ТекущийПериод, НазваниеОбласти, СписокОбластей)
	
	Области = Макет.Области;
	Если Области.Количество() = 0 Тогда
		НазваниеОбласти = "";
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекОбласть Из Области Цикл
		СписокОбластей.Добавить(Прав(ТекОбласть.Имя,4));
	КонецЦикла;
	
	ТекущаяОбласть = СписокОбластей[0].Значение;
	Для Каждого ТекОбласть Из СписокОбластей Цикл
		Если Год(ТекущийПериод) < Число(ТекОбласть.Значение) Тогда
			Прервать;
		КонецЕсли;
		
		ТекущаяОбласть = ТекОбласть.Значение;
	КонецЦикла;
	
	НазваниеОбласти = ТекущаяОбласть;
	
КонецПроцедуры

Функция СтруктураПоискаЛьготы(МенеджерЗаписи, КодРегиона, КэшОснованийРегиональныхЛьгот)
	
	ПоискЛьготы = Новый Структура;
	РегиональнаяЛьгота = СтрНачинаетсяС(МенеджерЗаписи.КодНалоговойЛьготыДо2020, "2");
	ОснованиеМестнойЛьготы = "";
	
	Если РегиональнаяЛьгота Тогда
		ПоискЛьготы.Вставить("КодРегиона", КодРегиона);
		ПоискЛьготы.Вставить("УстановленаМестнымНормативнымАктом", Истина);
		Если ЗначениеЗаполнено(МенеджерЗаписи.РегиональныйКодЛьготыДо2020) Тогда
			ОснованиеМестнойЛьготы = КэшОснованийРегиональныхЛьгот.Получить(МенеджерЗаписи.РегиональныйКодЛьготыДо2020);
			Если ОснованиеМестнойЛьготы = Неопределено Тогда
				ОснованиеМестнойЛьготы = ЛьготыПоИмущественнымНалогамКлиентСервер.ПолныйКодОснованияЛьготы(
					МенеджерЗаписи.РегиональныйКодЛьготыДо2020);
				КэшОснованийРегиональныхЛьгот.Вставить(МенеджерЗаписи.РегиональныйКодЛьготыДо2020, ОснованиеМестнойЛьготы);	
			КонецЕсли;
			ПоискЛьготы.Вставить("ОснованиеМестнойЛьготы", ОснованиеМестнойЛьготы);
		КонецЕсли;
	КонецЕсли;
	
	Если МенеджерЗаписи.НалоговаяЛьготаДо2020 =
		Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.ОсвобождениеОтНалогообложения Тогда
	 			
		Если РегиональнаяЛьгота Тогда
			// В прежних настройках можно было выбрать группу "20200", хотя код должен быть строго "20210"
			ПоискЛьготы.Вставить("КодЛьготы", "20210");
		Иначе
			ПоискЛьготы.Вставить("КодЛьготы", МенеджерЗаписи.КодНалоговойЛьготыДо2020);
		КонецЕсли;
		
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.Освобождение);

	ИначеЕсли МенеджерЗаписи.НалоговаяЛьготаДо2020 =
		Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.СнижениеНалоговойСтавки Тогда
		
		// Это местная льгота в виде снижения ставки. Код 20230.
		ПоискЛьготы.Вставить("КодЛьготы", "20230");
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной);
	
	ИначеЕсли МенеджерЗаписи.НалоговаяЛьготаДо2020 =
		Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.УменьшениеСуммыНалогаВПроцентах Тогда
		
		// Это местная льгота в виде снижения суммы налога на процент. Код 20220.
		ПоискЛьготы.Вставить("КодЛьготы", "20220");
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент);
		ПоискЛьготы.Вставить("ПроцентУменьшения", МенеджерЗаписи.ПроцентУменьшенияДо2020);
	
	ИначеЕсли МенеджерЗаписи.НалоговаяЛьготаДо2020 =
		Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогуДо2020.УменьшениеСуммыНалогаНаСумму Тогда
		
		// Это местная льгота в виде снижения суммы налога на указанную сумму. Код 20220.
		ПоискЛьготы.Вставить("КодЛьготы", "20220");
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму);
		
	КонецЕсли;
	
	Возврат ПоискЛьготы;
	
КонецФункции

#КонецОбласти

#КонецЕсли