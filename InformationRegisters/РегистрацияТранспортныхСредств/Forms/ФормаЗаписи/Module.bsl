#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если Параметры.Ключ.Пустой() Тогда
		
		Запись.Период = ТекущаяДатаПользователя;
		
		Если Запись.ВидЗаписи = Перечисления.ВидЗаписиОРегистрации.Регистрация Тогда
			Запись.ВключатьВНалоговуюБазу = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.ОсновноеСредство) Тогда
			ЗаполнитьПараметрыПоОсновномуСредству();
		КонецЕсли;
	
		НастроитьФормуПоОбъекту();
		
	КонецЕсли;
	
	УстановитьЗаголовокФормы(ЭтотОбъект, Параметры.Ключ.Пустой());
	
	СписокКодовЕдиницИзмеренияТС = РегистрыСведений.РегистрацияТранспортныхСредств.СписокКодовЕдиницИзмеренияНалоговойБазы();
	Для Каждого ЕдиницаИзмеренияТС Из СписокКодовЕдиницИзмеренияТС Цикл
		Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.Добавить(ЕдиницаИзмеренияТС.Значение, ЕдиницаИзмеренияТС.Представление);
	КонецЦикла;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	// Кэшируем цвета гиперссылки в форме, чтобы не обращаться потом на сервер
	ЦветаГиперссылкиСтавкиНалога = Новый Структура;
	ЦветаГиперссылкиСтавкиНалога.Вставить("Заполнена", ЦветаСтиля.ЦветГиперссылки);
	ЦветаГиперссылкиСтавкиНалога.Вставить("НеЗаполнена", ЦветаСтиля.НезаполненныйРеквизит);
	
	СведенияОВидахТранспортныхСредств = Новый ФиксированноеСоответствие(
		РасчетИмущественныхНалоговВызовСервера.СведенияОВидахТранспортныхСредств());
	
	ЗаполнитьДанныеОНалоговомОргане();
	
	ЗаполнитьСписокВыбораПовышающегоКоэффициента(
		Элементы.ПовышающийКоэффициент.СписокВыбора, 
		Запись.Период);
	
	УстановитьТекстПодсказкиСпискаПовышающихКоэффициентов(
		Элементы.ПовышающийКоэффициент.РасширеннаяПодсказка.Заголовок, 
		Запись.Период);
		
	ЗаполнитьПараметрыЛьготы();
		
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастроитьФормуПоОбъекту();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Очистим реквизиты, которые не имеют смысла для выбранных параметров записи
	Если ТекущийОбъект.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации Тогда
		ТекущийОбъект.НалоговыйОрган = Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
		ТекущийОбъект.КодПоОКТМО = "";
		ТекущийОбъект.КодПоОКАТОДо2014 = "";
	КонецЕсли;
	
	Если Год(ТекущийОбъект.Период) >= 2014 Тогда
		ТекущийОбъект.КодПоОКАТОДо2014 = "";
	КонецЕсли;
	
	Если Год(ТекущийОбъект.Период) >= 2020 Тогда
		ТекущийОбъект.КодВидаТранспортногоСредстваДо2020 = "";
	КонецЕсли;
	
	Если Не ТекущийОбъект.ОбщаяСобственность Тогда
		ТекущийОбъект.ДоляВПравеОбщейСобственностиЧислитель = 0;
		ТекущийОбъект.ДоляВПравеОбщейСобственностиЗнаменатель = 0;
	КонецЕсли;
	
	Если ВидЛьготы <> Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной Тогда
		ТекущийОбъект.ЛьготнаяСтавка = 0;
	КонецЕсли;
	
	Если ВидЛьготы <> Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму Тогда
		ТекущийОбъект.СуммаУменьшения = 0;
	КонецЕсли;
	
	// Проверка дублей записей
	Если Параметры.Ключ.Организация <> ТекущийОбъект.Организация
		Или Параметры.Ключ.ОсновноеСредство <> ТекущийОбъект.ОсновноеСредство
		Или Параметры.Ключ.Период <> ТекущийОбъект.Период Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Период",           ТекущийОбъект.Период);
		Запрос.УстановитьПараметр("Организация",      ТекущийОбъект.Организация);
		Запрос.УстановитьПараметр("ОсновноеСредство", ТекущийОбъект.ОсновноеСредство);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(РегистрацияТранспортныхСредств.ВидЗаписи) КАК ВидЗаписи
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Период = &Период
		|	И РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.ОсновноеСредство = &ОсновноеСредство";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			
			Выборка.Следующий();
			
			ШаблонСообщения = НСтр("ru = '%1 по основному средству <%2> 
				|уже есть запись <%3> в организации <%4>!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
				Формат(ТекущийОбъект.Период, "ДЛФ=D"), ТекущийОбъект.ОсновноеСредство, ТекущийОбъект.ВидЗаписи,
				ТекущийОбъект.Организация);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Данные информационной базы, влияющие на список КБК
	Если ТекущийОбъект.ВключатьВНалоговуюБазу
		 И Не РегистрыСведений.РегистрацияТранспортныхСредств.ЕстьТранспортныеСредства() Тогда
		ПараметрыЗаписи.Вставить("ПоявилисьТранспортныеСредства", Истина);
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("УплачиваетсяТранспортныйНалогПередЗаписью",
		ПолучитьФункциональнуюОпцию("УплачиваетсяТранспортныйНалог"));
	
	ПараметрыЗаписи.Вставить("ВедетсяУчетПлатежейВПлатонПередЗаписью",
		ПолучитьФункциональнуюОпцию("ВедетсяУчетПлатежейВПлатон"));
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Запись.ВидЗаписи <> Перечисления.ВидЗаписиОРегистрации.Регистрация Тогда
		Возврат;
	КонецЕсли;
		
	Если Запись.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
	
		Если Не ЗначениеЗаполнено(Запись.НалоговыйОрган) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				,
				,
				НСтр("ru = 'Налоговый орган'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				"Запись.НалоговыйОрган",
				,
				Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись.КодПоОКТМО) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				,
				,
				НСтр("ru = 'Код по ОКТМО'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				"Запись.КодПоОКТМО",
				,
				Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись.КодПоОКАТОДо2014) И Год(Запись.Период) < 2014 Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				,
				,
				НСтр("ru = 'Код по ОКАТО'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				"Запись.КодПоОКАТОДо2014",
				,
				Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Запись.ОбщаяСобственность Тогда
		
		Если Не ЗначениеЗаполнено(Запись.ДоляВПравеОбщейСобственностиЧислитель) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				,
				,
				НСтр("ru = 'Доля в праве общей собственности (числитель)'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				"Запись.ДоляВПравеОбщейСобственностиЧислитель",
				,
				Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись.ДоляВПравеОбщейСобственностиЗнаменатель) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				,
				,
				НСтр("ru = 'Доля в праве общей собственности (знаменатель)'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				"Запись.ДоляВПравеОбщейСобственностиЗнаменатель",
				,
				Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Ошибки = Новый Массив;
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект, Ошибки);
	
	Для Каждого Ошибка Из Ошибки Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Ошибка.ТекстСообщения,
			,
			Ошибка.Поле,
			,
			Отказ);
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ВключатьВНалоговуюБазу
	   И ПараметрыЗаписи.Свойство("ПоявилисьТранспортныеСредства") Тогда
		// в связи с записанными данными необходимо добавить КБК
		Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьПоставляемыеЭлементы();
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("УплачиваетсяТранспортныйНалогПослеЗаписи",
		ПолучитьФункциональнуюОпцию("УплачиваетсяТранспортныйНалог"));
	
	ПараметрыЗаписи.Вставить("ВедетсяУчетПлатежейВПлатонПослеЗаписи", 
		ПолучитьФункциональнуюОпцию("ВедетсяУчетПлатежейВПлатон"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Перем УплачиваетсяТранспортныйНалогПередЗаписью, УплачиваетсяТранспортныйНалогПослеЗаписи,
		ВедетсяУчетПлатежейВПлатонПередЗаписью, ВедетсяУчетПлатежейВПлатонПослеЗаписи;
	
	ОбновитьИнтерфейс = Ложь;
	
	Если ПараметрыЗаписи.Свойство("УплачиваетсяТранспортныйНалогПередЗаписью", УплачиваетсяТранспортныйНалогПередЗаписью)
		И ПараметрыЗаписи.Свойство("УплачиваетсяТранспортныйНалогПослеЗаписи", УплачиваетсяТранспортныйНалогПослеЗаписи) Тогда
		
		Если УплачиваетсяТранспортныйНалогПередЗаписью <> УплачиваетсяТранспортныйНалогПослеЗаписи Тогда
			ОбновитьИнтерфейс = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ВедетсяУчетПлатежейВПлатонПередЗаписью", ВедетсяУчетПлатежейВПлатонПередЗаписью)
		И ПараметрыЗаписи.Свойство("ВедетсяУчетПлатежейВПлатонПослеЗаписи", ВедетсяУчетПлатежейВПлатонПослеЗаписи) Тогда
		
		Если ВедетсяУчетПлатежейВПлатонПередЗаписью <> ВедетсяУчетПлатежейВПлатонПослеЗаписи Тогда
			ОбновитьИнтерфейс = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбновитьИнтерфейс Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	Оповестить("ИзмененаИнформацияОС");
	
	УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзмененаРегистрацияВНалоговомОргане" 
		Или ИмяСобытия = "Запись_Организации" Тогда
		ЗаполнитьДанныеОНалоговомОргане();
	ИначеЕсли ИмяСобытия = "ИзмененОбъектОС" Тогда
		ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ЗагруженыСтавкиТранспортногоНалога" Тогда
		ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "Запись_ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам" Тогда
		Если Параметр.Организация = Запись.Организация И Параметр.Льгота = Запись.ОснованиеЛьготы Тогда
			УстановитьТекстПодсказкиЛьготы();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПериодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновноеСредствоПриИзменении(Элемент)
	
	ОсновноеСредствоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияНалоговойБазыПриИзменении(Элемент)
	
	УстановитьЗаголовокНалоговойБазы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭкологическийКлассНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",       "РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта",  "РегистрацияТранспортныхСредств");
	ПараметрыФормы.Вставить("НазваниеМакета",   "КодыЭкологическихКлассов");
	ПараметрыФормы.Вставить("ТекущийПериод",    Запись.Период);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЭкологическийКлассНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПостановкаНаУчетВНалоговомОрганеПриИзменении(Элемент)
	
	НастроитьНалоговыйОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганПриИзменении(Элемент)
	
	НалоговыйОрганПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидТранспортногоСредстваПриИзменении(Элемент)
	
	СведенияОВидеТС = СведенияОВидахТранспортныхСредств[Запись.ВидТранспортногоСредства];
	
	Если СведенияОВидеТС <> Неопределено Тогда
		
		КодЕдиницыИзмерения = Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.НайтиПоЗначению(
			СведенияОВидеТС.ЕдиницаИзмерения);
		Если КодЕдиницыИзмерения <> Неопределено Тогда
			Запись.ЕдиницаИзмеренияНалоговойБазы = СведенияОВидеТС.ЕдиницаИзмерения;
		Иначе
			Запись.ЕдиницаИзмеренияНалоговойБазы = "";
		КонецЕсли;
		
		УстановитьЗаголовокНалоговойБазы(ЭтотОбъект);
		
	КонецЕсли;
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщаяСобственностьПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "НалоговыйОрганСсылка" Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", Запись.Организация);
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОКТМОСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОКТМОСсылка" Тогда
		
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
		
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Ключ", НалоговыйОрган);
			
			ФормаНалоговогоОргана = ОткрытьФорму("Справочник.РегистрацииВНалоговомОргане.ФормаОбъекта", ПараметрыФормы);
			
			ЭлементКодПоОКТМО = ФормаНалоговогоОргана.Элементы.Найти("КодПоОКТМО");
			Если ЭлементКодПоОКТМО <> Неопределено Тогда
				ФормаНалоговогоОргана.ТекущийЭлемент = ЭлементКодПоОКТМО;
			КонецЕсли;
			
			СтандартнаяОбработка = Ложь;
			
		Иначе
			
			ПараметрыФормы = Новый Структура("Ключ", Запись.Организация);
			ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяБазаПриИзменении(Элемент)
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КодПоОКТМОПриИзменении(Элемент)
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяСтавкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "НалоговаяСтавкаСсылка" Тогда
		
		ПараметрыФормы = Новый Структура;
		
		Если ПараметрыСтавки <> Неопределено Тогда
		
			ОтборНабораЗаписей = Новый Структура("НаименованиеОбъектаНалогообложения,ОКТМО");
			ЗаполнитьЗначенияСвойств(ОтборНабораЗаписей, ПараметрыСтавки);
			
			ПараметрыФормы.Вставить("Отбор", ОтборНабораЗаписей);
			ПараметрыФормы.Вставить("ПараметрыСтавки", ПараметрыСтавки);
			
		КонецЕсли;
		
		ОткрытьФорму("РегистрСведений.СтавкиТранспортногоНалога.ФормаСписка", ПараметрыФормы);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПриИзменении(Элемент)
	
	ТекущаяЛьгота = Запись.ОснованиеЛьготы;
	
	ЗаполнитьПараметрыЛьготы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТекущаяЛьгота = ВыбранноеЗначение Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущаяЛьгота = Запись.ОснованиеЛьготы;
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыОткрытие(Элемент, СтандартнаяОбработка)
	ТекущаяЛьгота = Запись.ОснованиеЛьготы;
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Запись.Организация);
	Отбор.Вставить("Владелец", Запись.ОснованиеЛьготы);
	Отбор.Вставить("ОсновноеСредство", Запись.ОсновноеСредство);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Справочник.ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформацияЗаявлениеОЛьготеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЗаявлениеОЛьготеПоНалогу" Тогда
		СтандартнаяОбработка = Ложь;
		ОписаниеДействия = ОписаниеДействияЗаявлениеОЛьготеПоНалогу(Запись.Организация, Запись.Период);
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПараметрыПоОсновномуСредству()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОсновноеСредство", Запись.ОсновноеСредство);
	Запрос.УстановитьПараметр("Организация",      Запись.Организация);
	Запрос.УстановитьПараметр("Период",           Запись.Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.НалоговыйОрган КАК НалоговыйОрган,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.ВидТранспортногоСредства КАК ВидТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|	РегистрацияТранспортныхСредств.НачалоДействияЛьготы КАК НачалоДействияЛьготы,
	|	РегистрацияТранспортныхСредств.ОкончаниеДействияЛьготы КАК ОкончаниеДействияЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ОбщаяСобственность КАК ОбщаяСобственность,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ОсновноеСредство = &ОсновноеСредство) КАК РегистрацияТранспортныхСредств";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		
		КоэффициентВСписке = Элементы.ПовышающийКоэффициент.СписокВыбора.НайтиПоЗначению(Выборка.ПовышающийКоэффициент);
		Если КоэффициентВСписке = Неопределено Тогда
			Запись.ПовышающийКоэффициент = 0;
		КонецЕсли;
		
		НастроитьФормуПоОбъекту();
		
		ТекущаяЛьгота = Запись.ОснованиеЛьготы;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОбъекту()
	
	УстановитьЗаголовокНалоговойБазы(ЭтотОбъект);
	УстановитьГоловнуюОрганизацию(ЭтотОбъект);
	НастроитьНалоговыйОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Запись;
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаПараметрыРегистрации.Видимость =
		(Объект.ВидЗаписи = ПредопределенноеЗначение("Перечисление.ВидЗаписиОРегистрации.Регистрация"));
	Элементы.КодПоОКАТО.Видимость = (Год(Объект.Период) < 2014);
	Элементы.КодВидаТранспортногоСредства.Видимость = (Год(Объект.Период) < 2020);
	
	Форма.Элементы.ГруппаДоляВПравеНаТС.Видимость = Объект.ОбщаяСобственность;
	
	ПрименяетсяЛьгота = ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.ОсновноеСредство)
		И ЗначениеЗаполнено(Объект.ОснованиеЛьготы);
	
	Если ПрименяетсяЛьгота Тогда
		Элементы.ОснованиеЛьготы.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе
		Элементы.ОснованиеЛьготы.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли; 
	
	Элементы.ЛьготнаяСтавка.Видимость =
		(Форма.ВидЛьготы = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной"));
	Элементы.СуммаУменьшения.Видимость =
		(Форма.ВидЛьготы = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму"));
	Элементы.ГруппаПериодДействияЛьготы.Видимость = ПрименяетсяЛьгота;
	Элементы.ГруппаИнформацияЗаявлениеОЛьготе.Видимость = ПрименяетсяЛьгота;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокНалоговойБазы(Форма)
	
	ЕдиницаИзмеренияНалоговойБазы = Форма.Запись.ЕдиницаИзмеренияНалоговойБазы;
	
	Если ПустаяСтрока(ЕдиницаИзмеренияНалоговойБазы) Тогда
		Заголовок = НСтр("ru = 'Налоговая база'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "251" Тогда
		Заголовок = НСтр("ru = 'Мощность двигателя'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "999" Тогда
		Заголовок = НСтр("ru = 'Сила тяги'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "181" Тогда
		Заголовок = НСтр("ru = 'Валовая вместимость'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "796" Тогда
		Заголовок = НСтр("ru = 'Налоговая база'");
	Иначе
		Заголовок = НСтр("ru = 'Налоговая база'");
	КонецЕсли;
	
	Форма.Элементы.НалоговаяБаза.Заголовок = Заголовок;
	
КонецПроцедуры

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.ОсновноеСредство)
		 И ЗначениеЗаполнено(Запись.Организация) Тогда
		ЗаполнитьПараметрыПоОсновномуСредству();
	КонецЕсли;
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	
	ЗаполнитьСписокВыбораПовышающегоКоэффициента(
		Элементы.ПовышающийКоэффициент.СписокВыбора,
		Запись.Период);
	
	УстановитьТекстПодсказкиСпискаПовышающихКоэффициентов(
		Элементы.ПовышающийКоэффициент.РасширеннаяПодсказка.Заголовок, 
		Запись.Период);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.ОсновноеСредство)
		 И ЗначениеЗаполнено(Запись.Организация) Тогда
		ЗаполнитьПараметрыПоОсновномуСредству();
	КонецЕсли;
	
	УстановитьГоловнуюОрганизацию(ЭтотОбъект);
	ЗаполнитьДанныеОНалоговомОргане();
	ЗаполнитьПараметрыЛьготы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьГоловнуюОрганизацию(Форма)
	
	Форма.ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Форма.Запись.Организация);
		
КонецПроцедуры

&НаСервере
Процедура ОсновноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.ОсновноеСредство)
		 И ЗначениеЗаполнено(Запись.Организация) Тогда
		ЗаполнитьПараметрыПоОсновномуСредству();
	КонецЕсли;
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	ЗаполнитьПараметрыЛьготы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭкологическийКлассНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	КодЭкологическогоКласса = РезультатЗакрытия;
	
	Если КодЭкологическогоКласса <> Неопределено И Запись.ЭкологическийКласс <> КодЭкологическогоКласса Тогда
		Запись.ЭкологическийКласс = КодЭкологическогоКласса;
		Модифицированность = Истина;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НалоговыйОрганПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.НалоговыйОрган) Тогда 
		РеквизитыНО = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Запись.НалоговыйОрган, "КодПоОКТМО, КодПоОКАТО");
		Запись.КодПоОКТМО = РеквизитыНО.КодПоОКТМО;
		Запись.КодПоОКАТОДо2014 = РеквизитыНО.КодПоОКАТО;
	КонецЕсли;
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСтавкуНаСервере(ДанныеДляЗаполнения, НалоговаяСтавка, ПараметрыСтавки,
	СведенияОВидахТС, Ошибки, НалоговаяСтавкаНайдена)
	
	// Для кодов автономных округов в составе края (области), являющихся субъектами РФ,
	// значимыми для идентификации субъекта являются 1-й, 2-й и 3-й знаки.
	КодПоОКТМО = Лев(ДанныеДляЗаполнения.ОКТМО, 3);

	// Для остальных субъектов РФ значимые для идентификации знаки 1-й и 2-й.
	Если НЕ (КодПоОКТМО = "118" ИЛИ КодПоОКТМО = "718" ИЛИ КодПоОКТМО = "719") Тогда
		КодПоОКТМО = Лев(КодПоОКТМО, 2);
	КонецЕсли;
	
	СведенияОВидеТС = СведенияОВидахТС[ДанныеДляЗаполнения.ВидТранспортногоСредства];
	Если СведенияОВидеТС = Неопределено Тогда
		НалоговаяСтавкаНайдена = Ложь;
		Возврат;
	Иначе
		КатегорияТС = СведенияОВидеТС.Категория;
	КонецЕсли;
	
	Год = Год(ДанныеДляЗаполнения.Период);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Период", ?(Год > 2014, ДанныеДляЗаполнения.Период, Дата('20150101')));
	Запрос.УстановитьПараметр("ОКТМО", ?(СтрДлина(КодПоОКТМО) = 3, КодПоОКТМО + "00000", КодПоОКТМО + "000000"));
	Запрос.УстановитьПараметр("НаименованиеОбъектаНалогообложения", Перечисления.КатегорииТранспортныхСредств[КатегорияТС]);
	Запрос.УстановитьПараметр("НалоговаяБаза", ДанныеДляЗаполнения.НалоговаяБаза);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтавкиТранспортногоНалогаСрезПоследних.Период КАК Период,
	|	СтавкиТранспортногоНалогаСрезПоследних.ОКТМО КАК ОКТМО,
	|	СтавкиТранспортногоНалогаСрезПоследних.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеЗначениеМощности КАК МинимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеЗначениеМощности КАК МаксимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеКоличествоЛетСГодаВыпускаТС КАК МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеКоличествоЛетСГодаВыпускаТС КАК МаксимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалогаСрезПоследних.НалоговаяСтавка КАК НалоговаяСтавка
	|ПОМЕСТИТЬ ВТВсеПодходящиеЗаписи
	|ИЗ
	|	РегистрСведений.СтавкиТранспортногоНалога.СрезПоследних(
	|			&Период,
	|			ОКТМО = &ОКТМО
	|				И НаименованиеОбъектаНалогообложения = &НаименованиеОбъектаНалогообложения
	|				И МинимальноеЗначениеМощности <= &НалоговаяБаза
	|				И (МаксимальноеЗначениеМощности = 0
	|					ИЛИ &НалоговаяБаза <= МаксимальноеЗначениеМощности)) КАК СтавкиТранспортногоНалогаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеПодходящиеЗаписи.Период) КАК Период
	|ПОМЕСТИТЬ ВТПериодПоследнейЗаписи
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВсеПодходящиеЗаписи.Период КАК Период,
	|	ВТВсеПодходящиеЗаписи.ОКТМО КАК ОКТМО,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	ВТВсеПодходящиеЗаписи.МинимальноеЗначениеМощности КАК МинимальноеЗначениеМощности,
	|	ВТВсеПодходящиеЗаписи.МаксимальноеЗначениеМощности КАК МаксимальноеЗначениеМощности,
	|	ВТВсеПодходящиеЗаписи.МинимальноеКоличествоЛетСГодаВыпускаТС КАК МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.МаксимальноеКоличествоЛетСГодаВыпускаТС КАК МаксимальноеКоличествоЛетСГодаВыпускаТС,
	|	ВЫБОР
	|		КОГДА ВТВсеПодходящиеЗаписи.МинимальноеКоличествоЛетСГодаВыпускаТС > 0
	|				ИЛИ ВТВсеПодходящиеЗаписи.МаксимальноеКоличествоЛетСГодаВыпускаТС > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяСтавка КАК НалоговаяСтавка
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодПоследнейЗаписи КАК ВТПериодПоследнейЗаписи
	|		ПО ВТВсеПодходящиеЗаписи.Период = ВТПериодПоследнейЗаписи.Период";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		НалоговаяСтавкаНайдена = Ложь;
		НалоговаяСтавка = 0;
		
	Иначе
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НалоговаяСтавкаЗависитОтГодаВыпускаТС = Выборка.НалоговаяСтавкаЗависитОтГодаВыпускаТС;
			
			Если НалоговаяСтавкаЗависитОтГодаВыпускаТС Тогда
				ДатаВыпуска = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДляЗаполнения.ОсновноеСредство, "ДатаВыпуска");
					
				Если ЗначениеЗаполнено(ДатаВыпуска) Тогда
						
					КоличествоЛетСГодаВыпускаТС = Год - Год(ДатаВыпуска);
					Если (Не ЗначениеЗаполнено(Выборка.МинимальноеКоличествоЛетСГодаВыпускаТС)
						 Или Выборка.МинимальноеКоличествоЛетСГодаВыпускаТС < КоличествоЛетСГодаВыпускаТС)
						 И (Не ЗначениеЗаполнено(Выборка.МаксимальноеКоличествоЛетСГодаВыпускаТС)
						 Или КоличествоЛетСГодаВыпускаТС <= Выборка.МаксимальноеКоличествоЛетСГодаВыпускаТС) Тогда
						
						ПараметрыСтавки = Новый Структура(
							"Период,
							|ОКТМО,
							|НаименованиеОбъектаНалогообложения,
							|МинимальноеЗначениеМощности,
							|МаксимальноеЗначениеМощности,
							|МинимальноеКоличествоЛетСГодаВыпускаТС,
							|МаксимальноеКоличествоЛетСГодаВыпускаТС");
						
						ЗаполнитьЗначенияСвойств(ПараметрыСтавки, Выборка);
						НалоговаяСтавкаНайдена = Истина;
						НалоговаяСтавка = Выборка.НалоговаяСтавка;
						Возврат;
						
					КонецЕсли;
						
				Иначе
					
					Ошибка = Новый Структура("ТекстСообщения, Поле",
						НСтр("ru = 'Ставка налога установлена с учетом количества лет, прошедших с года выпуска транспортного средства.
						|Укажите дату выпуска основного средства.'"),
						"Запись.ОсновноеСредство");
					
					Ошибки.Добавить(Ошибка);
					
					НалоговаяСтавкаНайдена = Ложь;
					НалоговаяСтавка = 0;
					Возврат;
					
				КонецЕсли;
			Иначе
				
				ПараметрыСтавки = Новый Структура(
					"Период,
					|ОКТМО,
					|НаименованиеОбъектаНалогообложения,
					|МинимальноеЗначениеМощности,
					|МаксимальноеЗначениеМощности,
					|МинимальноеКоличествоЛетСГодаВыпускаТС,
					|МаксимальноеКоличествоЛетСГодаВыпускаТС");
				
				ЗаполнитьЗначенияСвойств(ПараметрыСтавки, Выборка);
				НалоговаяСтавкаНайдена = Истина;
				НалоговаяСтавка = Выборка.НалоговаяСтавка;
				Возврат;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОНалоговомОргане()
	
	НалоговыйОрганПредставление = НСтр("ru = '<не указан>'");
	ОКТМОПредставление = НСтр("ru = '<не указан>'");
	
	НалоговыйОрганГиперссылка = "";
	ОКТМОГиперссылка = "";
	
	Если ЗначениеЗаполнено(Запись.Организация) Тогда
	
		НалоговыйОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Организация, "РегистрацияВНалоговомОргане");
		ОКТМО = "";
		
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
			
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НалоговыйОрган, "Код, КПП, КодПоОКТМО");
			
			Если ЗначениеЗаполнено(Реквизиты.КПП) Тогда
				НалоговыйОрганПредставление = СтрШаблон(НСтр("ru = 'КПП %1 в инспекции %2'"), Реквизиты.КПП, Реквизиты.Код);
			Иначе
				НалоговыйОрганПредставление = СтрШаблон(НСтр("ru = 'В инспекции %1'"), Реквизиты.Код);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Реквизиты.КодПоОКТМО) Тогда
				ОКТМО = Реквизиты.КодПоОКТМО;
				ОКТМОПредставление = СокрЛП(Реквизиты.КодПоОКТМО);
			Иначе
				ОКТМОГиперссылка = "ОКТМОСсылка";
			КонецЕсли;
			
		Иначе
			
			НалоговыйОрганГиперссылка = "НалоговыйОрганСсылка";
			
		КонецЕсли;
		
	КонецЕсли;
	
	НалоговыйОрганСсылка = Новый ФорматированнаяСтрока(НалоговыйОрганПредставление,,,, НалоговыйОрганГиперссылка);
	ОКТМОСсылка = Новый ФорматированнаяСтрока(ОКТМОПредставление,,,, ОКТМОГиперссылка);
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьНалоговыйОрган(Форма)
	
	ПоМестуНахожденияОрганизации = Форма.Запись.ПостановкаНаУчетВНалоговомОргане = 
		ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации");
		
	Форма.Элементы.ГруппаНалоговыйОрганПоМестуНахожденияОрганизации.Видимость = ПоМестуНахожденияОрганизации;
	Форма.Элементы.ГруппаДругойНалоговыйОрган.Видимость = НЕ ПоМестуНахожденияОрганизации;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНалоговуюСтавку(Форма, Ошибки = Неопределено)
	
	Запись = Форма.Запись;
	
	НалоговаяСтавкаПредставление = НСтр("ru = '<не определена>'");
	ЦветГиперссылки = Форма.ЦветаГиперссылкиСтавкиНалога.НеЗаполнена;;
	Форма.ПараметрыСтавки = Неопределено;
	
	Если Ошибки = Неопределено Тогда
		Ошибки = Новый Массив;
	КонецЕсли;
	
	Отказ = Ложь;

	Если Запись.ПостановкаНаУчетВНалоговомОргане =
		ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане") Тогда

		ОКТМОДляЗаполнения = Запись.КодПоОКТМО;

		Если Не ЗначениеЗаполнено(Запись.КодПоОКТМО) Тогда
			Отказ = Истина;
			
			Ошибка = Новый Структура("ТекстСообщения, Поле",
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'ОКТМО'")), "Запись.КодПоОКТМО");
			Ошибки.Добавить(Ошибка);
		КонецЕсли;

	Иначе

		ОКТМОДляЗаполнения = Форма.ОКТМО;

		Если Не ЗначениеЗаполнено(Форма.ОКТМО) Тогда
			Отказ = Истина;
			
			Ошибка = Новый Структура("ТекстСообщения, Поле",
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'ОКТМО'")), "ОКТМОСсылка");
			Ошибки.Добавить(Ошибка);
		КонецЕсли;

	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.ВидТранспортногоСредства) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Вид ТС'")), "Запись.ВидТранспортногоСредства");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Запись.НалоговаяБаза) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Форма.Элементы.НалоговаяБаза.Заголовок), "Запись.НалоговаяБаза");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если Отказ Тогда
		Форма.НалоговаяСтавкаСсылка = Новый ФорматированнаяСтрока(
			НалоговаяСтавкаПредставление,, ЦветГиперссылки,, "НалоговаяСтавкаСсылка");
		Возврат;
	КонецЕсли;
	
	ДанныеДляЗаполнения = Новый Структура("
		|Период,
		|ОсновноеСредство,
		|Организация,
		|ОКТМО,
		|ВидТранспортногоСредства,
		|НалоговаяБаза");
		
	ЗаполнитьЗначенияСвойств(ДанныеДляЗаполнения, Запись);
	ДанныеДляЗаполнения.Период = Форма.ТекущаяДатаПользователя;
	ДанныеДляЗаполнения.ОКТМО = ОКТМОДляЗаполнения;
	
	СтавкаНайдена = Ложь;
	
	ЗаполнитьСтавкуНаСервере(ДанныеДляЗаполнения,
		Форма.НалоговаяСтавка,
		Форма.ПараметрыСтавки,
		Форма.СведенияОВидахТранспортныхСредств,
		Ошибки,
		СтавкаНайдена);
		
	// Если ставка найдена, то в тексте ссылке указываем значение и год, с которого действует ставка. 	
	// Если все параметры указаны, а ставка не найдена, то это не препятсвует записи данных регистрации (в массиве Ошибки нет записи об этом),
	// но гиперссылка ставки при этом остается <не определена>
	Если Ошибки.Количество() = 0 И СтавкаНайдена Тогда
		НалоговаяСтавкаПредставление = СтрШаблон(НСтр("ru='%1 %2 (действует с %3 года)'"),
			СокрЛП(Форма.НалоговаяСтавка),
			Форма.ВалютаРегламентированногоУчета,
			Формат(Форма.ПараметрыСтавки.Период, "ДФ=yyyy"));
		ЦветГиперссылки = Форма.ЦветаГиперссылкиСтавкиНалога.Заполнена;
	КонецЕсли;
	
	Форма.НалоговаяСтавкаСсылка = Новый ФорматированнаяСтрока(
		НалоговаяСтавкаПредставление,, ЦветГиперссылки,, "НалоговаяСтавкаСсылка");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыЛьготы() 
	
	Если ЗначениеЗаполнено(Запись.ОснованиеЛьготы) Тогда
		ВидЛьготы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ОснованиеЛьготы, "ВидЛьготы");
	Иначе
		ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ОснованиеЛьготы)
		 И ЗначениеЗаполнено(Запись.Организация)
		 И ЗначениеЗаполнено(Запись.ОсновноеСредство) Тогда
		УстановитьТекстПодсказкиЛьготы(); 
	Иначе
		Элементы.ОснованиеЛьготыРасширеннаяПодсказка.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстПодсказкиЛьготы()
	
	Элементы.ОснованиеЛьготыРасширеннаяПодсказка.Заголовок = ЛьготыПоИмущественнымНалогам.ТекстПодсказкиЛьготы(
		Запись.ОснованиеЛьготы,
		Запись.Организация,
		ТекущаяДатаПользователя,
		Запись.ОсновноеСредство);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеДействияЗаявлениеОЛьготеПоНалогу(Организация, Период)

	Возврат ВыполнениеЗадачБухгалтера.ОписаниеДействияЗаявлениеОЛьготеПоИмущественнымНалогам(
		Организация,
		Период,
		Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеЛьготаТранспортЗемля);

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокФормы(Форма, НоваяЗапись = Ложь)
	
	Форма.АвтоЗаголовок = Ложь;
	
	Создание = ?(НоваяЗапись, НСтр("ru = ' (создание)'"), "");
	
	Если Форма.Запись.ВидЗаписи = ПредопределенноеЗначение("Перечисление.ВидЗаписиОРегистрации.Регистрация") Тогда
		Форма.Заголовок = СтрШаблон(НСтр("ru = 'Регистрация транспортного средства%1'"), Создание);
	Иначе
		Форма.Заголовок = СтрШаблон(НСтр("ru = 'Снятие с регистрационного учета%1'"), Создание);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораПовышающегоКоэффициента(СписокВыбора, Период)
	
	СписокВыбора.Очистить();
	
	Если Год(Период) >= 2022 Тогда
		
		СписокВыбора.Добавить(0, "-");
		СписокВыбора.Добавить(3, "3");
		
	ИначеЕсли Год(Период) >= 2018 Тогда
		
		СписокВыбора.Добавить(0, "-");
		СписокВыбора.Добавить(1.1, "1,1");
		СписокВыбора.Добавить(2, "2");
		СписокВыбора.Добавить(3, "3");
		
	Иначе
				
		СписокВыбора.Добавить(0, "-");
		СписокВыбора.Добавить(1.1, "1,1");
		СписокВыбора.Добавить(1.3, "1,3");
		СписокВыбора.Добавить(1.5, "1,5");
		СписокВыбора.Добавить(2, "2");
		СписокВыбора.Добавить(3, "3");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстПодсказкиСпискаПовышающихКоэффициентов(Подсказка, Период)
	
	ПолужирныйШрифт = Новый Шрифт(,, Истина);
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru='Исчисление суммы налога производится с учетом повышающего коэффициента:
		|'")));
		
	Если Год(Период) < 2022 Тогда
			
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока("1,1", ПолужирныйШрифт));
		
		Если Год(Период) >= 2018 Тогда
			
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло не более 3 лет;
			|'")));
			
		Иначе
			
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
				"ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло от 2 до 3 лет;
				|'")));
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока("1,3", ПолужирныйШрифт));
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
				"ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло от 1 года до 2 лет;
				|'")));
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока("1,5", ПолужирныйШрифт));
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
				"ru=' - в отношении легковых автомобилей средней стоимостью от 3 миллионов до 5 миллионов рублей включительно, с года выпуска которых прошло не более 1 года;
				|'")));
			
		КонецЕсли;
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока("2", ПолужирныйШрифт));
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
			"ru=' - в отношении легковых автомобилей средней стоимостью от 5 миллионов до 10 миллионов рублей включительно, с года выпуска которых прошло не более 5 лет;
			|'")));
		
	КонецЕсли;	
		
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока("3", ПолужирныйШрифт));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
		"ru=' - в отношении легковых автомобилей средней стоимостью от 10 миллионов до 15 миллионов рублей включительно, с года выпуска которых прошло не более 10 лет;
		|'")));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока("3", ПолужирныйШрифт));
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр(
		"ru=' - в отношении легковых автомобилей средней стоимостью от 15 миллионов рублей, с года выпуска которых прошло не более 20 лет.
		|'")));
	
	МассивСтрок.Добавить(НСтр("ru='При этом исчисление сроков начинается с года выпуска соответствующего легкового автомобиля.
		|'"));
	МассивСтрок.Добавить(НСтр("ru='Порядок расчета средней стоимости легковых автомобилей определяется Минпромторгом РФ.
		|'"));
	
	ТекстПодсказки = НСтр("ru='Перечень легковых автомобилей средней стоимостью от 3 миллионов рублей (с 2022 года - от 10 миллионов рублей)
		|размещается ежегодно не позднее 31 марта на <a href = ""https://minpromtorg.gov.ru/docs/list/"">официальном сайте Минпромторга РФ</a>'");
	ТекстПодсказки = СтрЗаменить(ТекстПодсказки, Символы.ПС, " "); // убираем переносы строк
	МассивСтрок.Добавить(СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ТекстПодсказки));
		
	Подсказка = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

#КонецОбласти
