#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура устанавливает значения регистра по умолчанию
//
// Параметры:
//   Запись           - РегистрСведенийЗапись - запись регистра
//   ДанныеЗаполнения - Структура - где ключ - имя ресурса
//
Процедура УстановкаНастроекПоУмолчанию(Запись, ДанныеЗаполнения) Экспорт
	
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Период",
		НачалоГода(ТекущаяДатаСеанса()));
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Организация",
		БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
		
	ЭтоФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Организация, "ЮридическоеФизическоеЛицо")
		= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		
	Если НЕ ЭтоФизЛицо Тогда
			Запись.Организация = Справочники.Организации.ПустаяСсылка();
		Возврат;
	КонецЕсли;
		
	
	Если ЗначениеЗаполнено(Запись.Организация) Тогда
		
		НастройкиУчета.УстановитьЗначениеПоУмолчанию(
			Запись,
			ДанныеЗаполнения,
			"ОсновнойВидДеятельности",
			РегистрыСведений.ВидыДеятельностиПредпринимателей.ВидДеятельностиПоУмолчанию(Запись.Организация));
		
		НастройкиУчета.УстановитьЗначениеПоУмолчанию(
			Запись,
			ДанныеЗаполнения,
			"ВестиУчетПоВидамДеятельности",
			НеобходимоВестиУчетПоВидамДеятельности(Запись.Организация, Запись.Период, Запись.ОсновнойВидДеятельности));
		
	КонецЕсли;
	
	// Учет доходов
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"АвансыВключаютсяВДоходыВПериодеПолучения",
		Истина);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ВидДеятельностиДоходовПоАвансам",
		Запись.ОсновнойВидДеятельности);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ПорядокПризнанияДоходаОтПродажЧерезКомиссионера",
		Перечисления.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера.ПоФактуОплатыПокупателем);
	
	// Учет расходов
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ДляПризнанияРасходовТребуетсяПолучениеДохода",
		Ложь);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ПризнаватьРасходыПоОперациямПрошлогоГода",
		Ложь);
	
КонецПроцедуры

// Возвращает признак необходимости вести учет по нескольким видам деятельности
//
// Параметры:
//   Организация             - СправочникСсылка.Организации - организация, для которой необходимо проверить данные.
//   Период                  - Дата - период, на который необходимо проверить данные.
//   ОсновнойВидДеятельности - СправочникСсылка.ВидыДеятельностиПредпринимателей - вид деятельности по умолчанию.
//   Причина                 - Строка - в нее возвращается пользовательское описание причины установки учета по нескольким видам деятельности
// 
// Возвращаемые значения:
//  Булево - признак необходимости вести учет по нескольким видам деятельности
//
Функция НеобходимоВестиУчетПоВидамДеятельности(Организация, Период, ОсновнойВидДеятельности, Причина = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ОсновнойВидДеятельности) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НачалоНалоговогоПериода = НачалоГода(Период);
	КонецНалоговогоПериода  = КонецГода(Период);
	
	ХарактерОсновнойДеятельности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнойВидДеятельности, "ХарактерДеятельности");
	
	ПлательщикЕНВД = УчетнаяПолитика.ПлательщикЕНВДЗаПериод(Организация, НачалоНалоговогоПериода, КонецНалоговогоПериода);
	ХарактерыДеятельностиЕНВД = УчетДоходовИРасходовПредпринимателя.ХарактерыДеятельностиЕНВД();
	ОсновнойВидДеятельностиНаЕНВД = (ХарактерыДеятельностиЕНВД.Найти(ХарактерОсновнойДеятельности) <> Неопределено);
	Если ПлательщикЕНВД И Не ОсновнойВидДеятельностиНаЕНВД Тогда
		Причина = НСтр("ru = 'Если предприниматель совмещает ЕНВД с Общей системой налогообложения, небходимо вести раздельный учет доходов и расходов.'");
		Возврат Истина;
	КонецЕсли;
	
	ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоНалоговогоПериода, КонецНалоговогоПериода);
	ХарактерыДеятельностиУСНПатент = УчетДоходовИРасходовПредпринимателя.ХарактерыДеятельностиУСНПатент();
	ОсновнойВидДеятельностиНаПатенте = (ХарактерыДеятельностиУСНПатент.Найти(ХарактерОсновнойДеятельности) <> Неопределено);
	Если ПрименяетсяУСНПатент И Не ОсновнойВидДеятельностиНаПатенте Тогда
		Причина = НСтр("ru = 'Если  предприниматель совмещает Патентную систему налогообложения с Общей, небходимо вести раздельный учет доходов и расходов.'");
		Возврат Истина;
	КонецЕсли;
	
	Если ПлательщикЕНВД И ПрименяетсяУСНПатент Тогда
		Причина = НСтр("ru = 'Если предприниматель совмещает ЕНВД, Патентную и Общую систему налогообложения, небходимо вести раздельный учет доходов и расходов.'");
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает характер деятельности, соответствующий переданным видам деятельности
//
// Параметры:
//   ОсновнойВидДеятельности           - СправочникСсылка.ВидыДеятельностиПредпринимателей - вид деятельности по умолчанию.
//   ВидДеятельностиДоходовПоАвансамИП - СправочникСсылка.ВидыДеятельностиПредпринимателей - вид деятельности по умолчанию для учета авансов.
// 
// Возвращаемые значения:
//  Структура - содержит значения типа ПеречислениеСсылка.ХарактерДеятельности
//
Функция ПолучитьХарактерыВидовДеятельности(ОсновнойВидДеятельности, ВидДеятельностиДоходовПоАвансамИП) Экспорт
	
	ХарактерыДеятельности	= Новый Структура;
	ХарактерыДеятельности.Вставить("ОсновнойХарактерДеятельности",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнойВидДеятельности, "ХарактерДеятельности"));
	ХарактерыДеятельности.Вставить("ХарактерДеятельностиДоходовПоАвансамИП",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДеятельностиДоходовПоАвансамИП, "ХарактерДеятельности"));
		
	Возврат ХарактерыДеятельности;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Организация.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	| ЗначениеРазрешено(ЭтотСписок.Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиОбновления

// Заполняет порядок признания дохода от продаж через комиссионера.
// Устанавливаем значение для нового поля "ПорядокПризнанияДоходаОтПродажЧерезКомиссионера".
// Для настроек с 2025 года устанавливаем значение "По факту получения оплаты от покупателя комиссионером".
// Для настроек до 2025 года устанавливаем значение "При поступлении денежных средств от комиссионера".
//
Процедура ЗаполнитьПорядокПризнанияДоходаОтПродажЧерезКомиссионера() Экспорт
	
	ДатаНачалаДействияНастройки =
			НастройкиУчетаНДФЛФормыКлиентСервер.ДатаНачалаДействияПорядкаПризнанияДоходаПоФактуОплатыОтПокупателя();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ДатаНачалаДействияНастройки);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиУчетаНДФЛ.Организация
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДФЛ КАК НастройкиУчетаНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиУчетаНДФЛСрезПоследних.Период,
	|	НастройкиУчетаНДФЛСрезПоследних.Организация,
	|	НастройкиУчетаНДФЛСрезПоследних.АвансыВключаютсяВДоходыВПериодеПолучения,
	|	НастройкиУчетаНДФЛСрезПоследних.ВестиУчетПоВидамДеятельности,
	|	НастройкиУчетаНДФЛСрезПоследних.ВидДеятельностиДоходовПоАвансам,
	|	НастройкиУчетаНДФЛСрезПоследних.ДляПризнанияРасходовТребуетсяПолучениеДохода,
	|	НастройкиУчетаНДФЛСрезПоследних.ОсновнойВидДеятельности,
	|	НастройкиУчетаНДФЛСрезПоследних.ПризнаватьРасходыПоОперациямПрошлогоГода,
	|	НастройкиУчетаНДФЛСрезПоследних.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДФЛ.СрезПоследних(&Период,) КАК НастройкиУчетаНДФЛСрезПоследних";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаПоОрганизациям = РезультатЗапроса[0].Выбрать();
	ЗаписиПоОрганизациям = РезультатЗапроса[1].Выгрузить();
	
	Пока ВыборкаПоОрганизациям.Следующий() Цикл

		Если Не ЗначениеЗаполнено(ВыборкаПоОрганизациям.Организация) Тогда
			Продолжить;
		КонецЕсли;

		НачатьТранзакцию();

		Попытка

			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(ВыборкаПоОрганизациям.Организация);
			НаборЗаписей.Прочитать();

			ЕстьЗаписьСДатойНачалаНастройки = Ложь;
			Для Каждого Запись Из НаборЗаписей Цикл
				Если Не ЕстьЗаписьСДатойНачалаНастройки И Запись.Период = ДатаНачалаДействияНастройки Тогда
					ЕстьЗаписьСДатойНачалаНастройки = Истина;
				КонецЕсли;

				Если Запись.Период >= ДатаНачалаДействияНастройки Тогда
					Запись.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера =
						Перечисления.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера.ПоФактуОплатыПокупателем;
				Иначе
					Запись.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера =
						Перечисления.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера.ПриПоступленииДенегОтКомиссионера;
				КонецЕсли;
			КонецЦикла;

			Если Не ЕстьЗаписьСДатойНачалаНастройки Тогда
				ДанныеЗаполнения = НовыеНастройкиУчета();
				ДанныеЗаполнения.Организация = ВыборкаПоОрганизациям.Организация;
				СтрокаПоследнейЗаписи = ЗаписиПоОрганизациям.Найти(ВыборкаПоОрганизациям.Организация, "Организация");
				Если СтрокаПоследнейЗаписи <> Неопределено Тогда
					ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, СтрокаПоследнейЗаписи);
				КонецЕсли;

				НоваяЗапись = НаборЗаписей.Добавить();
				УстановкаНастроекПоУмолчанию(НоваяЗапись, ДанныеЗаполнения);
				НоваяЗапись.Организация = ВыборкаПоОрганизациям.Организация;
				НоваяЗапись.Период = ДатаНачалаДействияНастройки;
				НоваяЗапись.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера =
					Перечисления.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера.ПоФактуОплатыПокупателем;
			КонецЕсли;

			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();

			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В процедуре ""РегистрыСведений.НастройкиУчетаНДФЛ.ЗаполнитьПорядокПризнанияДоходаОтПродажЧерезКомиссионера"" не удалось обработать организацию %1 по причине:
					 |%2'"),
				ВыборкаПоОрганизациям.Организация,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.НастройкиУчетаНДФЛ,
				ВыборкаПоОрганизациям.Организация,
				ТекстСообщения);

		КонецПопытки;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеНастройкиУчета()
	
	Настройки = Новый Структура;
	Настройки.Вставить("Период", НачалоГода(ТекущаяДатаСеанса()));
	Настройки.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Настройки.Вставить("ОсновнойВидДеятельности", Справочники.ВидыДеятельностиПредпринимателей.ПустаяСсылка());
	Настройки.Вставить("ВестиУчетПоВидамДеятельности", Ложь);
	Настройки.Вставить("АвансыВключаютсяВДоходыВПериодеПолучения", Ложь);
	Настройки.Вставить("ВидДеятельностиДоходовПоАвансам", Справочники.ВидыДеятельностиПредпринимателей.ПустаяСсылка());
	Настройки.Вставить("ДляПризнанияРасходовТребуетсяПолучениеДохода", Ложь);
	Настройки.Вставить("ПризнаватьРасходыПоОперациямПрошлогоГода", Ложь);
	Настройки.Вставить("ПорядокПризнанияДоходаОтПродажЧерезКомиссионера",
		Перечисления.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера.ПустаяСсылка());
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли