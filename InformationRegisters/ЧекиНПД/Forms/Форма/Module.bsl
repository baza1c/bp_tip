
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ОснованиеЧека) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОснованиеЧека = Параметры.ОснованиеЧека;
	Организация = Параметры.Организация;
	
	НастроитьФормуПоСостояниюЧека(РегистрыСведений.ЧекиНПД.СведенияОЧеке(ОснованиеЧека));
	
	ЗаполнитьСведенияДляОтправкиЧека(Параметры.ПолучательЧека);
	
	УстановитьЗаголовокПоЧеку();
	
	ОтобразитьЭтапПоСведениямОЧеке();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЧекиНПДКлиентСервер.НужноНачатьФормированиеЧека(СведенияОЧеке) Тогда
		
		ДлительнаяОперация = ЗапуститьФормированиеЧекаВФоне();
		ОжидатьФормированиеЧекаВФоне(ДлительнаяОперация);
		
	ИначеЕсли СведенияОЧеке.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЧековНПД.ВыполняетсяАннулирование") Тогда
		
		ДлительнаяОперация = ЗапуститьАннулированиеЧекаВФоне();
		ОжидатьАннулированиеЧекаВФоне(ДлительнаяОперация);
		
	ИначеЕсли Не ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧеке)
		И ЧекиНПДКлиент.ЕстьПравоОбновленияЧековНПД()
		И ЗначениеЗаполнено(СведенияОЧеке.АдресЧекаНаСайте)
		И (Не ЗначениеЗаполнено(СведенияОЧеке.ПрисоединенныйФайлЧека)
		Или ЧекХранитсяВТаблице(СведенияОЧеке.ПрисоединенныйФайлЧека)) Тогда
		
		ДлительнаяОперация = ЗапуститьПолучениеКартинкиВФоне();
		ОжидатьПолучениеКартинкиВФоне(ДлительнаяОперация);
	КонецЕсли;
	
	Если ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧеке)
		И ЧекиНПДКлиент.ЕстьПравоОбновленияЧековНПД() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСтатусОфлайнЧековНПД", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ЧекиНПДКлиент.ИмяСобытияИзменениеСтатусаОфлайнЧека()
		И Параметр = СведенияОЧеке.Организация
		И ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧеке) Тогда
		ОбработатьИзменениеСтатусаОфлайнЧека();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЛичныйКабинетСервисаМойНалог" Тогда
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.АдресСервиса());
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЖурналРегистрации" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтеграцияСПлатформойСамозанятыеКлиент.ОткрытьЖурналРегистрации(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Ошибка"));
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьПомощникИнтеграции" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Организация", СведенияОЧеке.Организация);
		ОткрытьФорму("Обработка.ПомощникИнтеграцииСПлатформойСамозанятые.Форма", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура АдресЧекаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(СведенияОЧеке)
		И ЗначениеЗаполнено(СведенияОЧеке.АдресЧекаНаСайте) Тогда
		ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку(СведенияОЧеке.АдресЧекаНаСайте);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьEmailПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьEmailНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьSMSПриИзменении(Элемент)
	
	Если НастройкаОтправкиSMSВыполнена() Тогда
		НастроитьГруппуОтправки(ЭтотОбъект);
	Иначе
		НастроитьОтправкуСМС();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресЭлектроннойПочтыПриИзменении(Элемент)
	
	ОтправлятьEmail = ЗначениеЗаполнено(АдресЭлектроннойПочты);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьEmailНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаПриИзменении(Элемент)
	
	ОтправлятьSMS = ЗначениеЗаполнено(НомерТелефона);
	
	Если НастройкаОтправкиSMSВыполнена() Тогда
		НастроитьГруппуОтправки(ЭтотОбъект);
	Иначе
		НастроитьОтправкуСМС();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ЗначениеЗаполнено(СведенияОЧеке) Тогда
		Возврат;
	КонецЕсли;
	
	МакетЧека = МакетЧека(СведенияОЧеке);
	
	Если МакетЧека = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторПечатнойФормы = "ПечатнаяФорма";
	НазваниеПечатнойФормы = СведенияОЧеке.ОписаниеЧека;
	
	КоллекцияПечатныхФорм               = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
	ПечатнаяФорма                       = УправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
	ПечатнаяФорма.СинонимМакета         = НазваниеПечатнойФормы;
	ПечатнаяФорма.ТабличныйДокумент     = МакетЧека;
	ПечатнаяФорма.ИмяФайлаПечатнойФормы = НазваниеПечатнойФормы;
	
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
	
КонецПроцедуры

&НаКлиенте
Процедура Аннулировать(Команда)
	
	ТекстВопроса = НСтр(
		"ru = 'Чек будет аннулирован.
		|Доход будет исключен из расчета налога.
		|
		|Продолжить?'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("АннулироватьНаКлиентеЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет,
		НСтр("ru = 'Аннулирование чека'"));
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	Если Не РеквизитыДляОтправкиЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ЗапуститьОтправкуЧекаНаСервере();
	ОжидатьЗавершенияОтправкиЧека(ДлительнаяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура Повторить(Команда)
	
	Если ОперацияДляПовтора = "ФормированиеЧека"
		И ЧекиНПДКлиентСервер.НужноНачатьФормированиеЧека(СведенияОЧеке) Тогда
		ДлительнаяОперация = ЗапуститьФормированиеЧекаВФоне();
		ОжидатьФормированиеЧекаВФоне(ДлительнаяОперация);
	ИначеЕсли ОперацияДляПовтора = "ПолучениеКартинки" 
		И ЗначениеЗаполнено(СведенияОЧеке.АдресЧекаНаСайте) Тогда 
		ДлительнаяОперация = ЗапуститьПолучениеКартинкиВФоне();
		ОжидатьПолучениеКартинкиВФоне(ДлительнаяОперация);
	ИначеЕсли ОперацияДляПовтора = "ОбновлениеЧека"
		И ЧекиНПДКлиентСервер.ТребуетсяОбновитьСведенияОЧекеНПД(СведенияОЧеке) Тогда
		ДлительнаяОперация = ЗапуститьОбновлениеЧекаВФоне();
		ОжидатьОбновлениеЧекаВФоне(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ДлительнаяОперация = ЗапуститьОбновлениеЧекаВФоне();
	ОжидатьОбновлениеЧекаВФоне(ДлительнаяОперация);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкаФормы

&НаСервере
Процедура НастроитьФормуПоСостояниюЧека(НовыеСведенияОЧеке)
	
	СведенияОЧеке = НовыеСведенияОЧеке;
	
	УстановитьЗаголовокПоЧеку();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокПоЧеку()
	
	Если ЧекиНПДКлиентСервер.НужноНачатьФормированиеЧека(СведенияОЧеке) Тогда
		Заголовок = НСтр("ru = 'Чек (создание)'");
	Иначе
		Заголовок = СведенияОЧеке.ОписаниеЧека;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьПараметрыФормыКДлительнойОперации()
	
	ТекстОшибки = "";
	ОперацияДляПовтора = "";
	ВремяОжиданияРезультата = 1;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеСтатусаОфлайнЧека()
	
	НастроитьФормуПоСостояниюЧека(РегистрыСведений.ЧекиНПД.СведенияОЧеке(ОснованиеЧека));
	ОтобразитьЭтапПоСведениямОЧеке();
	
КонецПроцедуры

#КонецОбласти

#Область ЭтапыПечатиЧека

&НаСервере
Процедура ОтобразитьЭтапПоСведениямОЧеке()
	
	Если ЧекиНПДКлиентСервер.НужноНачатьФормированиеЧека(СведенияОЧеке) Тогда
		
		ПоказатьДлительнуюОперациюФормированияЧека();
		
	ИначеЕсли СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.ВыполняетсяАннулирование Тогда
		
		ПоказатьДлительнуюОперациюАннулированиеЧека();
		
	Иначе
		
		Если ЗначениеЗаполнено(СведенияОЧеке.ПрисоединенныйФайлЧека) Тогда
			ПоказатьЧек();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуДляЭтапа(ЭтапДляОтображения)
	
	Этапы = ЭтапыПечатиЧека();
	Если Не Этапы.Свойство(ЭтапДляОтображения) Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Указан неправильный этап печати чека %1'"),
			ЭтапДляОтображения);
	КонецЕсли;
	
	СвойстваЭтапа = Этапы[ЭтапДляОтображения];
	
	НастроитьВидимостьГруппЭтапа(ЭтапДляОтображения, СвойстваЭтапа);
	
	НастроитьКнопкиКоманднойПанели(ЭтапДляОтображения, СвойстваЭтапа);
	
	ЕстьОшибка = ЗначениеЗаполнено(ТекстОшибки);
	
	НастроитьВидимостьБаннераОшибки(ЕстьОшибка);
	
	Элементы.ГруппаЧекОжидаетОтправкиВФНС.Видимость = ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧеке);
	
	ЭтотОбъект.КлючСохраненияПоложенияОкна = ЭтапДляОтображения
		+ ?(ЕстьОшибка, "Ошибка", "");
	
КонецПроцедуры

&НаСервере
Функция ЭтапыПечатиЧека()
	
	Этапы = Новый Структура;
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаДлительнаяОперация";
	СвойстваЭтапа.Кнопки = "";
	СвойстваЭтапа.ДлительнаяОперация = Истина;
	Этапы.Вставить("ДлительнаяОперацияФормированияЧека", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаДлительнаяОперация";
	СвойстваЭтапа.Кнопки = "ФормаПечать,ГруппаОтправить,ФормаАннулировать,ФормаОбновить";
	СвойстваЭтапа.ДлительнаяОперация = Истина;
	Этапы.Вставить("ДлительнаяОперацияПолученияФайла", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаОшибкаФормированияЧека";
	СвойстваЭтапа.Кнопки = "ФормаПовторить";
	Этапы.Вставить("ОшибкаПолученияКартинки", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаОшибкаФормированияЧека";
	СвойстваЭтапа.Кнопки = "ФормаПовторить";
	Этапы.Вставить("ОшибкаФормированияЧека", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаЧек";
	СвойстваЭтапа.Кнопки = "ФормаПечать,ГруппаОтправить,ФормаАннулировать,ФормаОбновить";
	Этапы.Вставить("Чек", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаДлительнаяОперация";
	СвойстваЭтапа.Кнопки = "ФормаПечать,ГруппаОтправить,ФормаАннулировать,ФормаОбновить";
	СвойстваЭтапа.ДлительнаяОперация = Истина;
	Этапы.Вставить("ДлительнаяОперацияАннулированияЧека", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаЧек";
	СвойстваЭтапа.Кнопки = "ФормаПечать,ГруппаОтправить";
	Этапы.Вставить("ЧекАннулирован", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаЧекПечатьНеДоступна";
	СвойстваЭтапа.Кнопки = "";
	Этапы.Вставить("ЧекАннулированПечатьНеДоступна", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаДлительнаяОперация";
	СвойстваЭтапа.Кнопки = "ФормаПечать,ГруппаОтправить";
	СвойстваЭтапа.ДлительнаяОперация = Истина;
	Этапы.Вставить("ДлительнаяОперацияОтправкиЧека", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаДлительнаяОперация";
	СвойстваЭтапа.Кнопки = "ФормаПечать,ГруппаОтправить,ФормаАннулировать,ФормаОбновить";
	СвойстваЭтапа.ДлительнаяОперация = Истина;
	Этапы.Вставить("ДлительнаяОперацияОбновленияЧека", СвойстваЭтапа);
	
	СвойстваЭтапа = НовыйСвойстваЭтапа();
	СвойстваЭтапа.ГруппаЭтапа = "ГруппаОшибкаФормированияЧека";
	СвойстваЭтапа.Кнопки = "ФормаПовторить";
	Этапы.Вставить("ОшибкаОбновленияЧека", СвойстваЭтапа);
	
	Возврат Этапы;
	
КонецФункции

&НаСервере
Процедура НастроитьВидимостьГруппЭтапа(ЭтапДляОтображения, СвойстваЭтапа)
	
	Для Каждого ЭлементЭтапа Из Элементы.ГруппаЭтапы.ПодчиненныеЭлементы Цикл
		ВидимостьЭтапа = ЭлементЭтапа.Имя = СвойстваЭтапа.ГруппаЭтапа;
		ЭлементЭтапа.Видимость = ВидимостьЭтапа;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКнопкиКоманднойПанели(ЭтапДляОтображения, СвойстваЭтапа)
	
	Элементы.ГруппаКоманднаяПанель.Доступность = Не СвойстваЭтапа.ДлительнаяОперация;
	
	ЭлементыЭтапа = СтрРазделить(СвойстваЭтапа.Кнопки, ",");
	
	НастроитьВидимостьЭлементовКоманднойПанели(ЭлементыЭтапа);
	
	УстановитьКнопкуПоУмолчанию(ЭлементыЭтапа);
	
	Если Элементы.ГруппаОтправить.Видимость Тогда
		НастроитьГруппуОтправки(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьЭлементовКоманднойПанели(ЭлементыЭтапа)
	
	Для Каждого ЭлементКоманднойПанели Из Элементы.ГруппаКоманднаяПанель.ПодчиненныеЭлементы Цикл
		
		ВидимостьКнопки = ЭлементыЭтапа.Найти(ЭлементКоманднойПанели.Имя) <> Неопределено;
		ЭлементКоманднойПанели.Видимость = ВидимостьКнопки;
		
	КонецЦикла;
	
	Элементы.ФормаАннулировать.Видимость = Элементы.ФормаАннулировать.Видимость
		И ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые();
	Элементы.ФормаАннулировать.Доступность = Не ЭтотОбъект.ТолькоПросмотр
		И Не ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧеке);
	Элементы.ФормаОбновить.Видимость = Элементы.ФормаОбновить.Видимость
		И ВозможноОбновитьСведенияОСформированномЧекеНПД(Организация);
	Элементы.ФормаОбновить.Доступность = Не ЭтотОбъект.ТолькоПросмотр
		И ЧекиНПДКлиентСервер.ТребуетсяОбновитьСведенияОЧекеНПД(СведенияОЧеке);
	
КонецПроцедуры

Процедура УстановитьКнопкуПоУмолчанию(ЭлементыЭтапа)
	
	// В качестве основной кнопки указываем первую из кнопок командной панели.
	Для Каждого ИмяЭлемента Из ЭлементыЭтапа Цикл
		Если Не ЗначениеЗаполнено(ИмяЭлемента) Тогда
			Продолжить;
		КонецЕсли;
		ЭлементКоманднойПанели = Элементы[ИмяЭлемента];
		Если ТипЗнч(ЭлементКоманднойПанели) = Тип("КнопкаФормы") Тогда
			ЭлементКоманднойПанели.КнопкаПоУмолчанию = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьГруппуОтправки(Форма)
	
	Форма.Элементы.Отправить.Доступность = Форма.ОтправлятьEmail Или Форма.ОтправлятьSMS;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьБаннераОшибки(ЕстьОшибка)
	
	Элементы.ГруппаОшибка.Видимость = ЕстьОшибка;
	
КонецПроцедуры

&НаСервере
Функция НовыйСвойстваЭтапа()
	
	СвойстваЭтапа = Новый Структура;
	СвойстваЭтапа.Вставить("ГруппаЭтапа", "");
	СвойстваЭтапа.Вставить("Кнопки", "");
	СвойстваЭтапа.Вставить("ДлительнаяОперация", Ложь);
	Возврат СвойстваЭтапа;
	
КонецФункции

#КонецОбласти

#Область ФормированиеЧека

&НаСервере
Функция ЗапуститьФормированиеЧекаВФоне()
	
	ПодготовитьПараметрыФормыКДлительнойОперации();
	
	ПараметрыФормированияЧека = ЧекиНПД.ПараметрыФормированияЧека(ОснованиеЧека, Организация);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование чека самозанятых в фоне.'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ЧекиНПД.СформироватьЧекВФоне",
		ПараметрыФормированияЧека,
		ПараметрыВыполнения);
	
	ПоказатьДлительнуюОперациюФормированияЧека();
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Функция ЗапуститьПроверкуРезультатаФормированияЧекаВФоне()
	
	ПараметрыПроцедуры = ЧекиНПДКлиентСервер.НовыйПараметрыОжиданияФормированияЧека();
	ПараметрыПроцедуры.Ссылка = ОснованиеЧека;
	ПараметрыПроцедуры.ПараметрыОжиданияРезультата = ПараметрыОжиданияРезультата;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка результата формирования чека самозанятых в фоне.'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ЧекиНПД.ПроверитьРезультатФормированияЧекаВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
		
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполняется" Тогда
		ПоказатьДлительнуюОперациюФормированияЧека();
	КонецЕсли;
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОжидатьФормированиеЧекаВФоне(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОтправкиЧека(НСтр("ru = 'Фоновое задание формирования чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьФормированиеЧекаВФонеЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьФормированиеЧекаВФонеЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОтправкиЧека(НСтр("ru = 'Фоновое задание формирования чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатФормированияЧекаВФоне(ДлительнаяОперация.АдресРезультата);
		
		ЗапуститьОжиданиеРезультатаФормированияЧека();
		
		ОповеститьОбИзмененияхЧека();
		
		Если ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧеке) Тогда
			ИнтервалПроверкиОтправкиОфлайнЧека = 300; // 5 минут
			ПодключитьОбработчикОжидания(
				"Подключаемый_ОбновитьСтатусОфлайнЧековНПД", ИнтервалПроверкиОтправкиОфлайнЧека);
		КонецЕсли;
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ПоказатьОшибкуФормированияЧека(ДлительнаяОперация.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатФормированияЧекаВФоне(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка формирования чека в фоне: пустой результат.'");
	КонецЕсли;
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Если ЗначениеЗаполнено(Результат.СведенияОЧеке) Тогда
		НастроитьФормуПоСостояниюЧека(Результат.СведенияОЧеке);
	КонецЕсли;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	Если Результат.Ошибка Или Результат.СтатусЗапроса = СтатусыЗапросов.Ошибка Тогда
		ПараметрыОжиданияРезультата = Неопределено;
		ПоказатьОшибкуФормированияЧека(Результат.ТекстОшибки, Результат.КодОшибки);
	ИначеЕсли Результат.СтатусЗапроса = СтатусыЗапросов.Выполняется Тогда
		Если ЗначениеЗаполнено(Результат.ПараметрыОжиданияРезультата) Тогда
			ПараметрыОжиданияРезультата = Результат.ПараметрыОжиданияРезультата;
		КонецЕсли;
	ИначеЕсли Результат.СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
		ПараметрыОжиданияРезультата = Неопределено;
		ПоказатьЧек();
	ИначеЕсли Результат.СтатусЗапроса = СтатусыЗапросов.Отменено Тогда
		ПараметрыОжиданияРезультата = Неопределено;
		ПоказатьОшибкуФормированияЧека(НСтр("ru = 'Регистрация чека отменена.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДлительнуюОперациюФормированияЧека()
	
	Элементы.ДокументыДлительнаяОперацияОписание.Заголовок = НСтр("ru = 'Доход регистрируется в сервисе Мой налог...'");
	НастроитьФормуДляЭтапа("ДлительнаяОперацияФормированияЧека");
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОшибкуФормированияЧека(ТекстОшибки, КодОшибки = Неопределено)
	
	ОписаниеОперации = НСтр("ru = 'При формировании чека возникла ошибка'");
	Если КодОшибки = "НеПодключен"
		Или КодОшибки = "НалогоплательщикНеПривязан"
		Или КодОшибки = "ОшибкаПривязки" Тогда
		УстановитьТекстОшибкиНеПодключен(ОписаниеОперации, ТекстОшибки);
	Иначе
		УстановитьТекстОшибки(ОписаниеОперации, ТекстОшибки);
	КонецЕсли;
	ОперацияДляПовтора = "ФормированиеЧека";
	НастроитьФормуДляЭтапа("ОшибкаФормированияЧека");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОжиданиеРезультатаФормированияЧека()
	
	Если Не ЗначениеЗаполнено(ПараметрыОжиданияРезультата) Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьОжиданиеРезультатаФормированияЧека", ВремяОжиданияРезультата, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапуститьОжиданиеРезультатаФормированияЧека() Экспорт
	
	ДлительнаяОперация = ЗапуститьПроверкуРезультатаФормированияЧекаВФоне();
	ОжидатьФормированиеЧекаВФоне(ДлительнаяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененияхЧека()
	
	// Если не заполнен реквизит ПараметрыОжиданияРезультата, то это значит,
	// что ожидание уже завершилось - можно оповещать об этом формы.
	Если Не ЗначениеЗаполнено(ПараметрыОжиданияРезультата)
		И ЗначениеЗаполнено(СведенияОЧеке) Тогда
		Оповестить(ЧекиНПДКлиентСервер.ИмяСобытияДляОповещенияОбИзмененииЧека(), СведенияОЧеке, ЭтотОбъект);
		ОповеститьОбИзменении(СведенияОЧеке.ДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьПолучениеКартинкиВФоне()
	
	ПодготовитьПараметрыФормыКДлительнойОперации();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение изображения чека самозанятых в фоне.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"РегистрыСведений.ЧекиНПД.ПолучитьФайлЧекаВФоне",
		СведенияОЧеке);
	
	ПоказатьДлительнуюОперациюПолученияФайла();
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОжидатьПолучениеКартинкиВФоне(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуПолучениеКартинки(
			НСтр("ru = 'Не удалось получить QR код чека в сервисе Мой налог. Попробуйте позже.'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьПолучениеКартинкиВФонеЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьПолучениеКартинкиВФонеЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт

	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуПолучениеКартинки(НСтр("ru = 'Фоновое задание получения чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		НовыеСведенияОЧеке = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если НовыеСведенияОЧеке <> Неопределено Тогда
			НастроитьФормуПоСостояниюЧека(НовыеСведенияОЧеке);
			ОтобразитьЭтапПоСведениямОЧеке();
		Иначе
			ПоказатьОшибкуПолучениеКартинки(
				НСтр("ru = 'Не удалось получить QR код чека в сервисе Мой налог. Попробуйте позже.'"));
		КонецЕсли; 
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ПоказатьОшибкуПолучениеКартинки(ДлительнаяОперация.КраткоеПредставлениеОшибки);
		
	КонецЕсли;

КонецПроцедуры 

&НаСервере
Процедура ПоказатьДлительнуюОперациюПолученияФайла()
	
	Элементы.ДокументыДлительнаяОперацияОписание.Заголовок = НСтр("ru = 'Получаем сведения о чеке в сервисе Мой налог...'");
	НастроитьФормуДляЭтапа("ДлительнаяОперацияПолученияФайла");
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОшибкуПолучениеКартинки(ТекстОшибки)
	
	ОписаниеОперации = НСтр("ru = 'При получении изображения чека возникла ошибка'");
	УстановитьТекстОшибки(ОписаниеОперации, ТекстОшибки);
	ОперацияДляПовтора = "ПолучениеКартинки";
	НастроитьФормуДляЭтапа("ОшибкаПолученияКартинки");
	
КонецПроцедуры

#КонецОбласти

#Область ОтображениеЧека

&НаСервере
Процедура ПоказатьЧек()
	
	ЧекАннулирован = ЗначениеЗаполнено(СведенияОЧеке)
		И (СведенияОЧеке.Состояние = Перечисления.СостоянияЧековНПД.Аннулирован
		Или СведенияОЧеке.ПроизведенВозвратПоЧеку);
	
	ПрефиксЭтапа = ?(ЧекАннулирован, "ЧекАннулирован", "Чек");
	
	Если ЗначениеЗаполнено(СведенияОЧеке.ПрисоединенныйФайлЧека) Тогда
		ПоказатьЧекНаФорме(СведенияОЧеке);
		НастроитьФормуДляЭтапа(ПрефиксЭтапа);
	Иначе
		НастроитьФормуДляЭтапа(ПрефиксЭтапа + "ПечатьНеДоступна");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьЧекНаФорме(СведенияОЧеке)
	
	Если Не ЗначениеЗаполнено(СведенияОЧеке.ПрисоединенныйФайлЧека) Тогда
		Элементы.АдресЧека.Видимость = Истина;
		Элементы.ТаблицаЧека.Видимость = Ложь;
		Элементы.АдресЧека.ТекстНевыбраннойКартинки = НСтр("ru = 'Не удалось скачать файл чека'");
		Возврат;
	КонецЕсли;
	
	ПараметрыДанныхФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыДанныхФайла.ИдентификаторФормы = УникальныйИдентификатор;
	ДанныеЧека = РаботаСФайлами.ДанныеФайла(СведенияОЧеке.ПрисоединенныйФайлЧека, ПараметрыДанныхФайла);
	
	ЧекХранитсяВТаблице = НРег(ДанныеЧека.Расширение) = "mxl";
	Если ЧекХранитсяВТаблице Тогда
		ДвоичныеДанныеЧека = ПолучитьИзВременногоХранилища(ДанныеЧека.СсылкаНаДвоичныеДанныеФайла);
		ТаблицаЧека = РегистрыСведений.ЧекиНПД.ТабличныйДокументЧека(ДвоичныеДанныеЧека);
		УдалитьИзВременногоХранилища(ДанныеЧека.СсылкаНаДвоичныеДанныеФайла);
	Иначе
		АдресЧека = ДанныеЧека.СсылкаНаДвоичныеДанныеФайла;
	КонецЕсли;
	
	Элементы.АдресЧека.Видимость = Не ЧекХранитсяВТаблице;
	Элементы.ТаблицаЧека.Видимость = ЧекХранитсяВТаблице;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЧекХранитсяВТаблице(ПрисоединенныйФайлЧека)
	
	ПараметрыДанныхФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыДанныхФайла.ПолучатьСсылкуНаДвоичныеДанные = Ложь;
	ПараметрыДанныхФайла.ВызыватьИсключение = Ложь;
	ДанныеЧека = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайлЧека, ПараметрыДанныхФайла);
	РасширениеФайла = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЧека, "Расширение");
	Возврат НРег(РасширениеФайла) = "mxl";
	
КонецФункции

#КонецОбласти

#Область АннулированиеЧека

&НаКлиенте
Процедура АннулироватьНаКлиентеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ЗапуститьАннулированиеЧекаВФоне();
	ОжидатьАннулированиеЧекаВФоне(ДлительнаяОперация);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьАннулированиеЧекаВФоне()
	
	ПодготовитьПараметрыФормыКДлительнойОперации();
	
	ПараметрыАннулированияЧека = ЧекиНПДКлиентСервер.НовыйПараметрыАннулированияЧека();
	ПараметрыАннулированияЧека.Ссылка = ОснованиеЧека;
	ПараметрыАннулированияЧека.Организация = СведенияОЧеке.Организация;
	ПараметрыАннулированияЧека.НомерЧека = СведенияОЧеке.НомерЧека;
	ПараметрыАннулированияЧека.ПричинаОтменыЧека = 
		ПредопределенноеЗначение("Перечисление.ПричиныОтменыЧекаНПД.ЧекСформированОшибочно");
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Аннулирование чека самозанятых в фоне.'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ЧекиНПД.АннулироватьЧекВФоне",
		ПараметрыАннулированияЧека,
		ПараметрыВыполнения);
	
	ПоказатьДлительнуюОперациюАннулированиеЧека();
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Функция ЗапуститьПроверкуРезультатаАннулированияЧекаВФоне()
	
	ПараметрыПроцедуры = ЧекиНПДКлиентСервер.НовыйПараметрыОжиданияАннулированияЧека();
	ПараметрыПроцедуры.Ссылка = ОснованиеЧека;
	ПараметрыПроцедуры.ПараметрыОжиданияРезультата = ПараметрыОжиданияРезультата;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка результата аннулирования чека самозанятых в фоне.'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ЧекиНПД.ПроверитьРезультатАннулированияЧекаВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	ПоказатьДлительнуюОперациюАннулированиеЧека();
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОжидатьАннулированиеЧекаВФоне(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОтправкиЧека(НСтр("ru = 'Фоновое задание аннулирования чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьАннулированиеЧекаВФонеЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьАннулированиеЧекаВФонеЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОтправкиЧека(НСтр("ru = 'Фоновое задание аннулирования чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатАннулированияЧекаВФоне(ДлительнаяОперация.АдресРезультата);
		
		ЗапуститьОжиданиеРезультатаАннулированияЧека();
		
		ОповеститьОбИзмененияхЧека();
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ПоказатьОшибкуАннулированиеЧекаИВернутьсяКЧеку(ДлительнаяОперация.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатАннулированияЧекаВФоне(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка аннулирования чека в фоне: пустой результат.'");
	КонецЕсли;
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Если ЗначениеЗаполнено(Результат.СведенияОЧеке) Тогда
		НастроитьФормуПоСостояниюЧека(Результат.СведенияОЧеке);
	КонецЕсли;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	Если Результат.Ошибка Или Результат.СтатусЗапроса = СтатусыЗапросов.Ошибка Тогда
		ПараметрыОжиданияРезультата = Неопределено;
		ПоказатьОшибкуАннулированиеЧекаИВернутьсяКЧеку(Результат.ТекстОшибки, Результат.КодОшибки);
	ИначеЕсли Результат.СтатусЗапроса = СтатусыЗапросов.Выполняется Тогда
		Если ЗначениеЗаполнено(Результат.ПараметрыОжиданияРезультата) Тогда
			ПараметрыОжиданияРезультата = Результат.ПараметрыОжиданияРезультата;
		КонецЕсли;
		ВремяОжиданияРезультата = Результат.ВремяОжидания;
	ИначеЕсли Результат.СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
		ПараметрыОжиданияРезультата = Неопределено;
		ПоказатьЧек();
	ИначеЕсли Результат.СтатусЗапроса = СтатусыЗапросов.Отменено Тогда
		ПараметрыОжиданияРезультата = Неопределено;
		ПоказатьОшибкуАннулированиеЧекаИВернутьсяКЧеку(НСтр("ru = 'Аннулирование чека отменено.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОжиданиеРезультатаАннулированияЧека()
	
	Если Не ЗначениеЗаполнено(ПараметрыОжиданияРезультата) Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьОжиданиеРезультатаАннулированияЧека", ВремяОжиданияРезультата, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапуститьОжиданиеРезультатаАннулированияЧека() Экспорт
	
	ДлительнаяОперация = ЗапуститьПроверкуРезультатаАннулированияЧекаВФоне();
	ОжидатьАннулированиеЧекаВФоне(ДлительнаяОперация);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДлительнуюОперациюАннулированиеЧека()
	
	Элементы.ДокументыДлительнаяОперацияОписание.Заголовок = НСтр("ru = 'Чек аннулируется в сервисе Мой налог...'");
	НастроитьФормуДляЭтапа("ДлительнаяОперацияАннулированияЧека");
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОшибкуАннулированиеЧекаИВернутьсяКЧеку(ТекстОшибки, КодОшибки = Неопределено)
	
	ОписаниеОперации = НСтр("ru = 'При аннулировании чека возникла ошибка'");
	Если КодОшибки = "НеПодключен"
		Или КодОшибки = "НалогоплательщикНеПривязан"
		Или КодОшибки = "ОшибкаПривязки" Тогда
		УстановитьТекстОшибкиНеПодключен(ОписаниеОперации, ТекстОшибки);
	Иначе
		УстановитьТекстОшибки(ОписаниеОперации, ТекстОшибки);
	КонецЕсли;
	ПоказатьЧек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеЧека

&НаСервере
Функция ЗапуститьОбновлениеЧекаВФоне()
	
	ПодготовитьПараметрыФормыКДлительнойОперации();
	
	ДлительнаяОперация = ЧекиНПДВызовСервера.ЗапуститьОбновлениеСведенийОЧекеНПД(
		СведенияОЧеке, УникальныйИдентификатор);
	
	ПоказатьДлительнуюОперациюОбновленияЧека();
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Процедура ПоказатьДлительнуюОперациюОбновленияЧека()

	Элементы.ДокументыДлительнаяОперацияОписание.Заголовок = НСтр("ru = 'Обновляем сведения о чеке из сервиса Мой налог...'");
	НастроитьФормуДляЭтапа("ДлительнаяОперацияОбновленияЧека");

КонецПроцедуры

&НаКлиенте
Процедура ОжидатьОбновлениеЧекаВФоне(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОбновленияЧека(
			НСтр("ru = 'Не удалось обновить чек из сервиса Мой налог. Попробуйте позже.'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьОбновлениеЧекаВФонеЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьОбновлениеЧекаВФонеЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОбновленияЧека(НСтр("ru = 'Фоновое задание обновления чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ТекстОшибки");
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ПоказатьОшибкуОбновленияЧека(ТекстОшибки);
		Иначе
			НовыеСведенияОЧеке = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "НовыеСведенияОЧеке");
			Если НовыеСведенияОЧеке <> Неопределено Тогда
				НастроитьФормуПоСостояниюЧека(НовыеСведенияОЧеке);
				ОповеститьОбИзмененияхЧека();
			КонецЕсли;
			ОтобразитьЭтапПоСведениямОЧеке();
		КонецЕсли;
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ПоказатьОшибкуОбновленияЧека(ДлительнаяОперация.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОшибкуОбновленияЧека(ТекстОшибки)
	
	ОписаниеОперации = НСтр("ru = 'При обновлении чека возникла ошибка'");
	УстановитьТекстОшибки(ОписаниеОперации, ТекстОшибки);
	ОперацияДляПовтора = "ОбновлениеЧека";
	НастроитьФормуДляЭтапа("ОшибкаОбновленияЧека");
	
КонецПроцедуры

#КонецОбласти

#Область Ошибки

&НаСервере
Процедура УстановитьТекстОшибки(ОписаниеОперации, Текст)
	
	ПолныйТекстОшибки = "";
	
	ЧастиТекстаОшибки = Новый Массив;
	ЧастиТекстаОшибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. %2.'"), ОписаниеОперации, Текст));
	ЧастиТекстаОшибки.Добавить(Символы.ПС);
	ЧастиТекстаОшибки.Добавить(НСтр("ru = 'Детальную информацию об ошибке можно найти в'"));
	ЧастиТекстаОшибки.Добавить(" ");
	ЧастиТекстаОшибки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Журнале регистрации'"),,,, "ОткрытьЖурналРегистрации"));
	
	ТекстОшибки = Новый ФорматированнаяСтрока(ЧастиТекстаОшибки);
	
	Элементы.ДекорацияТекстОшибки.Заголовок = ТекстОшибки;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстОшибкиНеПодключен(ОписаниеОперации, Текст)
	
	СостояниеПодключения = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПолучитьСостояниеПодключения(
		СведенияОЧеке.Организация);
	
	ПолныйТекстОшибки = "";
	
	ЧастиТекстаОшибки = Новый Массив;
	ЧастиТекстаОшибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. %2.'"), ОписаниеОперации, Текст));
	ЧастиТекстаОшибки.Добавить(Символы.ПС);
	Если СостояниеПодключения = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.ЗапросОтправлен Тогда
		ЧастиТекстаОшибки.Добавить(НСтр("ru = 'Настройте сервис '"));
	Иначе
		ЧастиТекстаОшибки.Добавить(НСтр("ru = 'Подключитесь к сервису '"));
	КонецЕсли;
	
	ЧастиТекстаОшибки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Мой налог'"),,,, "ОткрытьПомощникИнтеграции"));
	ЧастиТекстаОшибки.Добавить(НСтр("ru = ' и повторите попытку.'"));
	
	ТекстОшибки = Новый ФорматированнаяСтрока(ЧастиТекстаОшибки);
	
	Элементы.ДекорацияТекстОшибки.Заголовок = ТекстОшибки;
	
КонецПроцедуры

#КонецОбласти

#Область ОтправкаЧека

&НаСервере
Процедура ЗаполнитьСведенияДляОтправкиЧека(ПолучательЧека)
	
	ПараметрыОтправкиЧека = ЧекиНПД.ПараметрыОтправкиЧека(ПолучательЧека);
	
	ПараметрыОтправкиЧека.Свойство("НомерТелефона", НомерТелефона);
	ПараметрыОтправкиЧека.Свойство("АдресЭлектроннойПочты", АдресЭлектроннойПочты);
	
КонецПроцедуры

&НаКлиенте
Функция РеквизитыДляОтправкиЗаполнены()
	
	EmailЗаполнен = Не ОтправлятьEmail Или ЗначениеЗаполнено(АдресЭлектроннойПочты);
	Если Не EmailЗаполнен Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Email'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "АдресЭлектроннойПочты",);
	КонецЕсли;
	
	НомерТелефонаЗаполнен = Не ОтправлятьSMS Или ЗначениеЗаполнено(НомерТелефона);
	Если Не НомерТелефонаЗаполнен Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Номер телефона'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "НомерТелефона", );
	КонецЕсли;
	
	Возврат EmailЗаполнен И НомерТелефонаЗаполнен;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьEmailНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		ОтправлятьEmail = Ложь;
		Возврат;
	КонецЕсли;
	
	НастроитьГруппуОтправки(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкаОтправкиSMSВыполнена()
	
	Возврат ОтправкаSMS.НастройкаОтправкиSMSВыполнена();
	
КонецФункции

&НаКлиенте
Процедура НастроитьОтправкуСМС()
	
	Если ЕстьПравоНастраиватьСМС() Тогда
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("НастроитьОтправкуСМСЗавершение", ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.НастройкаОтправкиSMS", , ЭтотОбъект, , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		ОтправлятьSMS = Ложь;
		ТекстПредупреждения = НСтр("ru = 'Попросите администратора выполнить настройку отправки СМС'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения, , НСтр("ru = 'Отправка СМС не настроена'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтправкуСМСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НастройкаОтправкиSMSВыполнена() Тогда
		НастроитьГруппуОтправки(ЭтотОбъект);
	Иначе
		ОтправлятьSMS = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьПравоНастраиватьСМС()
	
	Возврат ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.НастройкаОтправкиSMS);
	
КонецФункции

&НаСервере
Функция ЗапуститьОтправкуЧекаНаСервере()
	
	ПодготовитьПараметрыФормыКДлительнойОперации();
	
	ПараметрыОтправкиЧека = ЧекиНПДКлиентСервер.НовыйПараметрыОтправкиЧека();
	ЗаполнитьЗначенияСвойств(ПараметрыОтправкиЧека, ЭтотОбъект);
	ПараметрыОтправкиЧека.УникальныйИдентификатор = УникальныйИдентификатор;
	ПараметрыОтправкиЧека.Ссылка = ОснованиеЧека;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка чека самозанятых в фоне.'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ЧекиНПД.ОтправитьЧекВФоне",
		ПараметрыОтправкиЧека,
		ПараметрыВыполнения);
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполняется" Тогда
		ПоказатьДлительнуюОперациюОтправкиЧека();
	КонецЕсли;
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Процедура ПоказатьДлительнуюОперациюОтправкиЧека()
	
	Элементы.ДокументыДлительнаяОперацияОписание.Заголовок = НСтр("ru = 'Выполняется отправка чека...'");
	НастроитьФормуДляЭтапа("ДлительнаяОперацияОтправкиЧека");
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершенияОтправкиЧека(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОтправкиЧека(НСтр("ru = 'Фоновое задание отправки чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьЗавершенияОтправкиЧекаЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершенияОтправкиЧекаЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьОшибкуОтправкиЧека(НСтр("ru = 'Фоновое задание отправки чека неожиданно завершилось'"));
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ТекстОповещения = ОбработатьРезультатОтправки(ДлительнаяОперация.АдресРезультата);
		Если ЗначениеЗаполнено(ТекстОповещения) Тогда
			ПоказатьОповещениеПользователя(ТекстОповещения);
		КонецЕсли;
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ПоказатьОшибкуОтправкиЧека(ДлительнаяОперация.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатОтправки(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка формирования чека в фоне: пустой результат.'");
	КонецЕсли;
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	ЧастиТекстаОшибки = Новый Массив;
	
	Если Результат.ОшибкаОтправкиEmail Тогда
		ЧастиТекстаОшибки.Добавить(Результат.ТекстОшибкиОтправкиEmail);
	КонецЕсли;
	
	Если Результат.ОшибкаОтправкиSMS Тогда
		ЧастиТекстаОшибки.Добавить(Результат.ТекстОшибкиОтправкиSMS);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЧастиТекстаОшибки) Тогда
		ПоказатьОшибкуОтправкиЧека(СтрСоединить(ЧастиТекстаОшибки, Символы.ПС));
	Иначе
		ОтобразитьЭтапПоСведениямОЧеке();
	КонецЕсли;
	
	Возврат ТекстОповещенияОбОтправке(Результат);
	
КонецФункции

&НаСервере
Функция ТекстОповещенияОбОтправке(Результат)
	
	ЧастиТекстаОповещения = Новый Массив;
	Если Результат.EmailОтправлен Тогда
		ЧастиТекстаОповещения.Добавить(НСтр("ru = 'email'"));
	КонецЕсли;
	Если Результат.SMSОтправлен Тогда
		ЧастиТекстаОповещения.Добавить(НСтр("ru = 'SMS'"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЧастиТекстаОповещения) Тогда
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Чек отправлен по %1'"),
			СтрСоединить(ЧастиТекстаОповещения, " " + НСтр("ru = 'и'") + " "));
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаСервере
Процедура ПоказатьОшибкуОтправкиЧека(ТекстОшибки)
	
	ОписаниеОперации = НСтр("ru = 'При отправке чека возникла ошибка'");
	УстановитьТекстОшибки(ОписаниеОперации, ТекстОшибки);
	ОтобразитьЭтапПоСведениямОЧеке();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСтатусОфлайнЧековНПД()
	
	ЧекиНПДКлиент.ОбновитьСтатусыОфлайнЧековНПД();
	
КонецПроцедуры

#КонецОбласти

#Область ПечатьЧека

&НаСервереБезКонтекста
Функция МакетЧека(СведенияОЧеке)
	
	Возврат РегистрыСведений.ЧекиНПД.МакетЧекаДляПечати(СведенияОЧеке);
	
КонецФункции

#КонецОбласти

&НаСервереБезКонтекста
Функция ВозможноОбновитьСведенияОСформированномЧекеНПД(Организация)
	
	Возврат ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые()
		И ЧекиНПДВызовСервера.ПодключенСервисМойНалог(Организация);
		
КонецФункции

#КонецОбласти