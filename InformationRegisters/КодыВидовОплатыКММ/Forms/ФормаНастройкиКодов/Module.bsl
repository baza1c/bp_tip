#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Оборудование") Тогда
		Оборудование = Параметры.Оборудование;
	ИначеЕсли Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("ОфлайнОборудование") Тогда
		Оборудование = Параметры.Отбор.ОфлайнОборудование;
		Элементы.ФормаЗакрыть.Видимость = Ложь;
	Иначе
		ВызватьИсключение НСтр("ru = 'Чтобы настроить виды оплат, пожалуйста, перейдите в карточку оборудования.'");
	КонецЕсли;
	
	Если ТипЗнч(Оборудование) <> Тип("СправочникСсылка.ОфлайнОборудование") Тогда
		Оборудование = Справочники.ОфлайнОборудование.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Оборудование) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОборудованиеПоОрганизациям.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
		|ГДЕ
		|	ОборудованиеПоОрганизациям.Оборудование = &Оборудование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КодыВидовОплатыКММ.Код КАК Код,
		|	КодыВидовОплатыКММ.ВидОплаты КАК ВидОплаты,
		|	КодыВидовОплатыКММ.ВидОплаты.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.КодыВидовОплатыКММ КАК КодыВидовОплатыКММ
		|ГДЕ
		|	КодыВидовОплатыКММ.ОфлайнОборудование = &Оборудование
		|
		|УПОРЯДОЧИТЬ ПО
		|	Код";
		
		Запрос.УстановитьПараметр("Оборудование", Оборудование);
		Результат = Запрос.ВыполнитьПакет();
		
		ВыборкаОрганизаций = Результат[0].Выбрать();
		
		Если ВыборкаОрганизаций.Следующий() Тогда
			Организация = ВыборкаОрганизаций.Организация;
		КонецЕсли;
		
		ТаблицаКодов.Очистить();
		ВыборкаКодов = Результат[1].Выбрать();
		Пока ВыборкаКодов.Следующий() Цикл
			СтрокаКодов = ТаблицаКодов.Добавить();
			СтрокаКодов.КодВидаОплаты = ВыборкаКодов.Код;
			Если ЗначениеЗаполнено(ВыборкаКодов.Организация) 
				И ВыборкаКодов.Организация = Организация Тогда
				СтрокаКодов.ВидОплаты = ВыборкаКодов.ВидОплаты;
			КонецЕсли;
			НовыйКод = Макс(СтрокаКодов.КодВидаОплаты, НовыйКод);
		КонецЦикла;
		
		НовыйКод = НовыйКод + 1;
		
	Иначе
		Элементы.ТаблицаКодов.Видимость = Ложь;
		Элементы.ДекорацияПредупреждение.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Ошибки = Неопределено;
	
	СписокКодов = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаКодов, "КодВидаОплаты", Истина);
	Для каждого КодВидаОплаты Из СписокКодов Цикл
		СтрокиПоКоду = ТаблицаКодов.НайтиСтроки(Новый Структура("КодВидаОплаты", КодВидаОплаты));
		Если СтрокиПоКоду.Количество() > 1 Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru='Код вида оплаты %1 указан в нескольких строках'"), КодВидаОплаты);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки, КодВидаОплаты, КодВидаОплаты, ТекстОшибки);
		ИначеЕсли КодВидаОплаты > 1 И НЕ ЗначениеЗаполнено(СтрокиПоКоду[0].ВидОплаты) Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru='Для кода %1 не указан вид оплаты'"), КодВидаОплаты);
			ИндексСтроки = ТаблицаКодов.Индекс(СтрокиПоКоду[0]);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ТаблицаКодов[%1].ВидОплаты", ТекстОшибки, "ТаблицаКодовВидОплаты", ,ТекстОшибки, ИндексСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Ошибки = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = ЗавершениеРаботы ИЛИ НЕ ПроверитьЗаполнение();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если Модифицированность И Не ЗавершениеРаботы Тогда
		ПриЗакрытииНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	Набор = РегистрыСведений.КодыВидовОплатыКММ.СоздатьНаборЗаписей();
	Набор.Отбор.ОфлайнОборудование.Установить(Оборудование);
	
	Для Каждого Строка Из ТаблицаКодов Цикл
		Если НЕ ЗначениеЗаполнено(Строка.ВидОплаты) Тогда
			Продолжить;
		КонецЕсли;
		НоваяЗапись = Набор.Добавить();
		
		НоваяЗапись.ОфлайнОборудование = Оборудование;
		НоваяЗапись.Код                = Строка.КодВидаОплаты;
		НоваяЗапись.ВидОплаты          = Строка.ВидОплаты;
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаКодов

&НаКлиенте
Процедура ТаблицаКодовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если НоваяСтрока Тогда
		ТекущиеДанные.КодВидаОплаты = НовыйКод;
		НовыйКод = НовыйКод + 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКодовПослеУдаления(Элемент)
	
	НовыйКод = РасчитатьНовыйКод();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКодовКодВидаОплатыОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ВыбранноеЗначение = Число(Текст);
	ТекущиеДанные = Элементы.ТаблицаКодов.ТекущиеДанные;
	Если ВыбранноеЗначение >= НовыйКод Тогда
		НовыйКод = ВыбранноеЗначение + 1;
	ИначеЕсли (ТекущиеДанные.КодВидаОплаты = НовыйКод - 1) И (ВыбранноеЗначение > 1) Тогда
		ТекущиеДанные.КодВидаОплаты = ВыбранноеЗначение;
		НовыйКод = РасчитатьНовыйКод();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция РасчитатьНовыйКод()
	
	МаксКод = 0;
	Для Каждого Строка Из ТаблицаКодов Цикл
		МаксКод = Макс(МаксКод, Строка.КодВидаОплаты);
	КонецЦикла;
	
	Возврат МаксКод + 1;
		
КонецФункции

#КонецОбласти

