#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Возвращает сумму вычета по заданным параметрам
//
// Параметры:
//   ПараметрыВычета - Структура:
//     * ВсегоДетей           - Число
//     * Несовершеннолетних   - Число
//     * РодныеДетиИнвалиды   - Число
//     * ПриемныеДетиИнвалиды - Число
//     * НомерМесяца          - Число
//   РазмерыВычетов  - Структура - см. ПомощникЗаполнения3НДФЛ.РазмерыВычетов()
//
// Возвращаемое значение:
//   Число
//
Функция РазмерТекущегоВычета(ПараметрыВычета, РазмерыВычетов) Экспорт
	
	СуммаВычета = СуммаВычетаНаДетей(ПараметрыВычета, РазмерыВычетов.СтандартныйВычетНаДетей);
	Возврат СуммаВычета;
	
КонецФункции

// Возвращает рассчитанные данные по суммам вычета на детей.
//
// Параметры:
//    СведенияОДетях    - ТаблицаЗначений, СтрокаТаблицыЗначений - Данные о вычетах на детей.
//    РазмерыВычетов    - Структура - Фиксированные значения стандартных вычетов на детей.
//    КоличествоМесяцев - Число - Необязательный. Количество месяцев применения вычета.
//
// Возвращаемое значение:
//   Структура - см. НовыйСуммыВычетаНаДетей()
//
Функция РассчитатьСуммыВычетаНаДетей(СведенияОДетях, РазмерыВычетов, КоличествоМесяцев) Экспорт
	
	СуммыВычета = НовыйСуммыВычетаНаДетей();
	
	Для Каждого СтрокаТаблицы Из СведенияОДетях Цикл
		
		Если СтрокаТаблицы.НомерМесяца <= КоличествоМесяцев Тогда
			РассчитатьСуммыВычетаНаДетейПоСтроке(СуммыВычета, СтрокаТаблицы, РазмерыВычетов);
		КонецЕсли;
		
	КонецЦикла;
	
	СуммыВычета.Всего = СуммыВычета.СтандартныйВычет + СуммыВычета.ДвойнойВычет
		+ СуммыВычета.ВычетНаИнвалидов + СуммыВычета.ДвойнойВычетНаИнвалидов;
	
	Возврат СуммыВычета;
	
КонецФункции

// Возвращает данные вычета за период
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  НачалоПериода - Дата
//  КонецПериода - Дата
//
// Возвращаемое значение:
//   ТаблицаЗначений
//
Функция ДанныеВычетаЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтандартныеВычетыНаДетей.Период КАК Период,
	|	0 КАК НомерМесяца,
	|	СтандартныеВычетыНаДетей.Организация КАК Организация,
	|	СтандартныеВычетыНаДетей.ВсегоДетей КАК ВсегоДетей,
	|	СтандартныеВычетыНаДетей.Несовершеннолетних КАК Несовершеннолетних,
	|	СтандартныеВычетыНаДетей.РодныеДетиИнвалиды КАК РодныеДетиИнвалиды,
	|	СтандартныеВычетыНаДетей.ПриемныеДетиИнвалиды КАК ПриемныеДетиИнвалиды,
	|	СтандартныеВычетыНаДетей.ПрименяетсяДвойнойВычет КАК ПрименяетсяДвойнойВычет
	|ИЗ
	|	РегистрСведений.ИПСтандартныеВычетыНаДетей.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СтандартныеВычетыНаДетей
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтандартныеВычетыНаДетей.Период,
	|	МЕСЯЦ(СтандартныеВычетыНаДетей.Период),
	|	СтандартныеВычетыНаДетей.Организация,
	|	СтандартныеВычетыНаДетей.ВсегоДетей,
	|	СтандартныеВычетыНаДетей.Несовершеннолетних,
	|	СтандартныеВычетыНаДетей.РодныеДетиИнвалиды,
	|	СтандартныеВычетыНаДетей.ПриемныеДетиИнвалиды,
	|	СтандартныеВычетыНаДетей.ПрименяетсяДвойнойВычет
	|ИЗ
	|	РегистрСведений.ИПСтандартныеВычетыНаДетей КАК СтандартныеВычетыНаДетей
	|ГДЕ
	|	СтандартныеВычетыНаДетей.Организация = &Организация
	|	И СтандартныеВычетыНаДетей.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерМесяца");
	
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает описание структуры суммы вычета на детей
//
// Возвращаемое значение:
//   Структура:
//     * СтандартныйВычет - Число
//     * ДвойнойВычет - Число
//     * ВычетНаИнвалидов - Число
//     * ДвойнойВычетНаИнвалидов - Число
//     * Всего - Число
//
Функция НовыйСуммыВычетаНаДетей() Экспорт
	
	СуммыВычета = Новый Структура;
	СуммыВычета.Вставить("СтандартныйВычет", 0);
	СуммыВычета.Вставить("ДвойнойВычет", 0);
	СуммыВычета.Вставить("ВычетНаИнвалидов", 0);
	СуммыВычета.Вставить("ДвойнойВычетНаИнвалидов", 0);
	СуммыВычета.Вставить("Всего", 0);
	
	Возврат СуммыВычета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьСуммыВычетаНаДетейПоСтроке(СуммыВычета, СтрокаТаблицы, РазмерыВычетов)
	
	СтандартныйВычет = СтандартныйВычетПоКоличествуДетей(СтрокаТаблицы, РазмерыВычетов);
	ВычетНаИнвалидов = СтрокаТаблицы.РодныеДетиИнвалиды * РазмерыВычетов.РоднойИнвалид
		+ СтрокаТаблицы.ПриемныеДетиИнвалиды * РазмерыВычетов.ПриемныйИнвалид;
	
	Если СтрокаТаблицы.ПрименяетсяДвойнойВычет Тогда
		Коэффициент = 2;
		СуммыВычета.ДвойнойВычет = СуммыВычета.ДвойнойВычет + (СтандартныйВычет * Коэффициент);
		СуммыВычета.ДвойнойВычетНаИнвалидов = СуммыВычета.ДвойнойВычетНаИнвалидов + (ВычетНаИнвалидов * Коэффициент);
	Иначе
		СуммыВычета.СтандартныйВычет = СуммыВычета.СтандартныйВычет + СтандартныйВычет;
		СуммыВычета.ВычетНаИнвалидов = СуммыВычета.ВычетНаИнвалидов + ВычетНаИнвалидов;
	КонецЕсли;
	
КонецПроцедуры

Функция СтандартныйВычетПоКоличествуДетей(ТекущаяСтрока, РазмерыВычетов)
	
	Если ТекущаяСтрока.Несовершеннолетних = 1 Тогда
		Возврат РазмерыВычетов.ПервыйРебенок;
	ИначеЕсли ТекущаяСтрока.Несовершеннолетних < 3 Тогда
		Возврат РазмерыВычетов.ПервыйРебенок + РазмерыВычетов.ВторойРебенок;
	Иначе
		ДетейОт3 = ТекущаяСтрока.Несовершеннолетних - 2;
		Возврат РазмерыВычетов.ПервыйРебенок + РазмерыВычетов.ВторойРебенок + (ДетейОт3 * РазмерыВычетов.ТретийРебенок);
	КонецЕсли;
	
КонецФункции

Функция СуммаВычетаНаДетей(ПараметрыВычета, РазмерыВычетов)
	
	СуммыВычета = НовыйСуммыВычетаНаДетей();
	РассчитатьСуммыВычетаНаДетейПоСтроке(СуммыВычета, ПараметрыВычета, РазмерыВычетов);
	СуммыВычетаВсего = СуммыВычета.СтандартныйВычет + СуммыВычета.ДвойнойВычет
		+ СуммыВычета.ВычетНаИнвалидов + СуммыВычета.ДвойнойВычетНаИнвалидов;
	
	Возврат СуммыВычетаВсего;
	
КонецФункции

#КонецОбласти

#КонецЕсли