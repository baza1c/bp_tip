#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДополнитьНаборЗаписей(ДокументыПоДанным, Организация, Контрагент, ПериодСверки, ИдентификаторСверки, НаборЗаписейДанныеДокументовПоСверке, ДокументСсылка = "") Экспорт 
	
	Если ДокументыПоДанным.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторов = Новый Массив;
	Для Каждого ТекущаяСтрокаДокументыПоДанным Из ДокументыПоДанным Цикл

		Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаДокументыПоДанным.ИдентификаторДокумента) Тогда
			// Защита от некорректных данных.
			Продолжить;
		КонецЕсли;
		
		Если МассивИдентификаторов.Найти(ТекущаяСтрокаДокументыПоДанным.ИдентификаторДокумента) <> Неопределено Тогда
			// Защита от дублей.
			Продолжить;
		КонецЕсли;
		МассивИдентификаторов.Добавить(ТекущаяСтрокаДокументыПоДанным.ИдентификаторДокумента);
		
		Если ДокументСсылка = Неопределено Тогда
			
			ДокументСсылка = Документы[ТекущаяСтрокаДокументыПоДанным.ВидДокумента].ПолучитьСсылку(ТекущаяСтрокаДокументыПоДанным.ИдентификаторДокумента);
			Если НЕ ОбщегоНазначения.СсылкаСуществует(ДокументСсылка) Тогда
				// Битые ссылки в регистр не записываем. Документ есть на сервисе, но не существует в базе.
				// Необходимо отправить документ к удаленю на сервис.
				СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(Организация, Контрагент, ДокументСсылка);
				ДокументСсылка = Неопределено;
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		Запись = НаборЗаписейДанныеДокументовПоСверке.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ТекущаяСтрокаДокументыПоДанным);
		Запись.ИдентификаторСверки = ИдентификаторСверки;
		Запись.Организация         = Организация;
		Запись.Контрагент          = Контрагент;
		Запись.ПериодСверки        = ПериодСверки;
		
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьПериодСверки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПериодСверки КАК ПериодСверки,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверки
	|ПОМЕСТИТЬ ИдетнтификаторыСверки
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПериодСверки,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки КАК ИдентификаторСверки,
	|	СервисСверкиРасчетовДанныеСервиса.Организация КАК Организация,
	|	СервисСверкиРасчетовДанныеСервиса.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовДанныеСервиса.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ИдетнтификаторыСверки.ПериодСверки КАК ПериодСверки
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИдетнтификаторыСверки КАК ИдетнтификаторыСверки
	|		ПО СервисСверкиРасчетовДанныеСервиса.Организация = ИдетнтификаторыСверки.Организация
	|			И СервисСверкиРасчетовДанныеСервиса.Контрагент = ИдетнтификаторыСверки.Контрагент
	|			И СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки = ИдетнтификаторыСверки.ИдентификаторСверки
	|ГДЕ
	|	СервисСверкиРасчетовДанныеСервиса.ПериодСверки = ДАТАВРЕМЯ(1, 1, 1)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяПроцедуры = "РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.ЗаполнитьПериодСверки()";
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
			НаборЗаписей.Отбор.ИдентификаторСверки.Установить(Выборка.ИдентификаторСверки);
			НаборЗаписей.Отбор.ИдентификаторДокумента.Установить(Выборка.ИдентификаторДокумента);
			
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				
				Запись.ПериодСверки = НачалоМесяца(Выборка.ПериодСверки);
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
		Исключение
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось установить период сверки по причине:
			|%2'"), ИмяПроцедуры, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.СервисСверкиРасчетовДанныеСервиса,
			,
			ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли