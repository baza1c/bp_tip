#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Запись.Организация) Или Не ЗначениеЗаполнено(Запись.Контрагент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Заголовок = Запись.Контрагент;
	
	Элементы.ДатаЗаписи.Видимость = (ЗначениеЗаполнено(Запись.ДатаЗаписи));
	Элементы.ДатаЗаписи.Заголовок = СтрШаблон(НСтр("ru='(изменен %1)'"), Формат(Запись.ДатаЗаписи, "ДФ=dd.MM.yy;"));
	
	ОбщегоНазначенияБП.ЗаписатьОперациюБизнесСтатистики("СтатистикаБП.МониторСверкиСКонтрагентами.ОткрытКомментарийПоКонтрагенту");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Комментарий = СокрЛП(Запись.Комментарий);
	ТекущийОбъект.ДатаЗаписи = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_СостояниеСверкиСКонтрагентами",
		Новый Структура("Организация, Контрагент, Комментарий",
			Запись.Организация, Запись.Контрагент, Запись.Комментарий));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьСостояниеСверки(Команда)
	
	Комментарий = СокрЛП(Запись.Комментарий);
	Если Не ЗначениеЗаполнено(Комментарий) Тогда
		// Удалим запись, если она уже записана ранее
		Если УдалитьПустуюЗапись() Тогда
			Оповестить("Запись_СостояниеСверкиСКонтрагентами",
				Новый Структура("Организация, Контрагент, Комментарий",
					Запись.Организация, Запись.Контрагент, Запись.Комментарий));
		КонецЕсли;
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если Записать() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция УдалитьПустуюЗапись()
	
	МенеджерЗаписи = РеквизитФормыВЗначение("Запись");
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Удалить();
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти