#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает статус плательщика НПД, для проверяемого контрагента
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
//  Период - Дата в часовом поясе "Europe/Moscow"
//
// Возвращаемое значение:
//  Структура, Неопределено - см. НовыйПлательщикНПД
//
Функция ПлательщикНПД(Контрагент, Период) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Или Не ЗначениеЗаполнено(Период) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнены обязательные параметры РегистрСведений.СтатусыСамозанятых.ПлательщикНПД()'")
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатусыСамозанятых.Период КАК Период,
		|	СтатусыСамозанятых.ПлательщикНПД КАК ПлательщикНПД,
		|	СтатусыСамозанятых.ДатаРегистрации КАК ДатаРегистрации,
		|	СтатусыСамозанятых.ЭтоОкончательноеЗначение КАК ЭтоОкончательноеЗначение,
		|	СтатусыСамозанятых.ДатаПроверки КАК ДатаПроверки
		|ИЗ
		|	РегистрСведений.СтатусыСамозанятых КАК СтатусыСамозанятых
		|ГДЕ
		|	СтатусыСамозанятых.Период = &Период
		|	И СтатусыСамозанятых.Контрагент = &Контрагент";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = НовыйПлательщикНПД();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		Результат.ЭтоОкончательноеЗначение = Выборка.ЭтоОкончательноеЗначение Или (ЗначениеЗаполнено(Выборка.ДатаПроверки)
			И Выборка.ДатаПроверки - Выборка.Период >= ВремяВступленияВСилуСтатусаСамозанятого());
		//@skip-check constructor-function-return-section
		Возврат Результат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура ЗаписатьСтатусыСамозанятости(СтатусыСамозанятости) Экспорт
	
	СписокИНН = ИзвлечьИНН(СтатусыСамозанятости);
	
	НачатьТранзакцию();
	
	ТаблицаКонтрагентов = КонтрагентыПоИНН(СписокИНН);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтатусыСамозанятых");
	ЭлементБлокировки.ИсточникДанных = ТаблицаКонтрагентов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Контрагент", "Контрагент");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Попытка
		Блокировка.Заблокировать();
		Для Каждого СтатусСамозанятого Из СтатусыСамозанятости Цикл
			ЗаписатьСтатусСамозанятого(СтатусСамозанятого, ТаблицаКонтрагентов);
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Время вступления в силу статуса самозанятого. Если с даты проверки прошло указанное время,
// то считаем, что статус на эту дату поменяться уже не может
//
Функция ВремяВступленияВСилуСтатусаСамозанятого() Экспорт
	
	Возврат 48*60*60; // 48 часов
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйПлательщикНПД()
	
	Результат = Новый Структура;
	Результат.Вставить("ПлательщикНПД", Ложь);
	Результат.Вставить("ДатаРегистрации", Дата(1, 1, 1));
	Результат.Вставить("ЭтоОкончательноеЗначение", Ложь);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьСтатусСамозанятого(СтатусСамозанятого, ТаблицаКонтрагентов)
	
	Если Не ЗначениеЗаполнено(СтатусСамозанятого.НаДату)
		Или Не ЗначениеЗаполнено(СтатусСамозанятого.ИНН) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("ИНН", СтатусСамозанятого.ИНН);
	Для Каждого НайденнаяСтрока Из ТаблицаКонтрагентов.НайтиСтроки(ПараметрыОтбора) Цикл
		НоваяЗапись = СоздатьМенеджерЗаписи();
		НоваяЗапись.Период = СтатусСамозанятого.НаДату;
		НоваяЗапись.Контрагент = НайденнаяСтрока.Контрагент;
		НоваяЗапись.ПлательщикНПД = СтатусСамозанятого.ПлательщикНПД;
		НоваяЗапись.ДатаРегистрации = СтатусСамозанятого.ДатаРегистрации;
		НоваяЗапись.ЭтоОкончательноеЗначение = СтатусСамозанятого.ЭтоОкончательноеЗначение;
		НоваяЗапись.ДатаПроверки = ИнтеграцияСтатусСамозанятого.ДатаПоСервисуСтатусаСамозанятого(
			ТекущаяУниверсальнаяДата(), "UTC");
		НоваяЗапись.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция ИзвлечьИНН(СтатусыСамозанятости)
	
	МассивЗначений = Новый Массив;
	УникальныеЗначения = Новый Соответствие;
	
	Для Каждого СтатусСамозанятого Из СтатусыСамозанятости Цикл
		Значение = СтатусСамозанятого.ИНН;
		Если Не ЗначениеЗаполнено(Значение) Тогда
			Продолжить;
		КонецЕсли;
		Если УникальныеЗначения[Значение] = Неопределено Тогда
			МассивЗначений.Добавить(Значение);
			УникальныеЗначения.Вставить(Значение, Истина);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивЗначений;
	
КонецФункции

Функция КонтрагентыПоИНН(СписокИНН)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокИНН", СписокИНН);
	Запрос.Текст = "ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент,
		|	Контрагенты.ИНН КАК ИНН
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.ПометкаУдаления
		|	И НЕ Контрагенты.ЭтоГруппа
		|	И Контрагенты.ИНН В(&СписокИНН)";
	
	ТаблицаКонтрагентов = Запрос.Выполнить().Выгрузить();
	ТаблицаКонтрагентов.Индексы.Добавить("ИНН");
	
	Возврат ТаблицаКонтрагентов;
	
КонецФункции

#КонецОбласти

#КонецЕсли