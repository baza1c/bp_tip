#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВсеСостоянияФормы = ВсеСостоянияФормы();
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ТолькоПросмотр = Истина;
		СостояниеФормы = ВсеСостоянияФормы.СтатусСамозанятого;
	Иначе
		СостояниеФормы = ВсеСостоянияФормы.Неопределено;
	КонецЕсли;
	УстановитьСостояниеФормы(СостояниеФормы, ЭтотОбъект);
	
	Элементы.ФормаВключитьВозможностьРедактирования.Видимость = ЕстьПравоРедактирование();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = ЭтотОбъект Тогда
		Если ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоПолучен() Тогда
			Прочитать();
			ТолькоПросмотр = Истина;
			УстановитьСостояниеФормы(ВсеСостоянияФормы().СтатусСамозанятого, ЭтотОбъект);
		ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоОшибка() Тогда
			ПредставлениеОшибки = Параметр;
			УстановитьСостояниеФормы(ВсеСостоянияФормы().Ошибка, ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЖурналРегистрации" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтеграцияСтатусСамозанятогоКлиент.ОткрытьЖурналРегистрации(ЭтотОбъект, СтрРазделить("Ошибка,Информация", ","));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Запись.Контрагент) И Не ЗначениеЗаполнено(ИНН(Запись.Контрагент)) Тогда
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", Метаданные.РегистрыСведений.СтатусыСамозанятых.Измерения.Контрагент.Синоним);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "Контрагент", , Отказ)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьСтатусСамозанятого();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСтатусСамозанятого();
	
КонецПроцедуры

&НаКлиенте
Процедура Повторить(Команда)
	
	ОбновитьСтатусСамозанятого();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	
	ТолькоПросмотр = Ложь;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Запись = Форма.Запись;
	
	СостояниеФормы = Форма.СостояниеФормы;
	ВсеСостоянияФормы = ВсеСостоянияФормы();
	
	Если СостояниеФормы = ВсеСостоянияФормы.Неопределено Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНеопределено;
	ИначеЕсли СостояниеФормы = ВсеСостоянияФормы.СтатусСамозанятого Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСтатусСамозанятого
	ИначеЕсли СостояниеФормы = ВсеСостоянияФормы.Ожидание Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОжидание;
	ИначеЕсли СостояниеФормы = ВсеСостоянияФормы.Ошибка Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОшибка;
	КонецЕсли;
	
	Элементы.Период.Доступность = СостояниеФормы <> ВсеСостоянияФормы.Ожидание;
	Элементы.Контрагент.Доступность = СостояниеФормы <> ВсеСостоянияФормы.Ожидание;
	Элементы.Контрагент.Подсказка = ИнтеграцияСтатусСамозанятогоКлиентСервер.ОписаниеСтатуса(
		Запись.ПлательщикНПД, Запись.ДатаРегистрации);
	
	Элементы.ДатаРегистрации.Видимость = ЗначениеЗаполнено(Запись.ДатаРегистрации)
		Или Не Элементы.ДатаРегистрации.ТолькоПросмотр;
	
	Элементы.ФормаВключитьВозможностьРедактирования.Доступность = Форма.ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСостояниеФормы(СостояниеФормы, Форма)
	
	Форма.СостояниеФормы = СостояниеФормы;
	УправлениеФормой(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВсеСостоянияФормы()

	Результат = Новый Структура;
	Результат.Вставить("Неопределено", "Неопределено");
	Результат.Вставить("СтатусСамозанятого", "СтатусСамозанятого");
	Результат.Вставить("Ожидание", "Ожидание");
	Результат.Вставить("Ошибка", "Ошибка");
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ОбновитьСтатусСамозанятого()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСостояниеФормы(ВсеСостоянияФормы().Ожидание, ЭтотОбъект);
	ПредставлениеОшибки = "";
	
	ИнтеграцияСтатусСамозанятогоКлиент.НачатьПолучениеСтатусаСамозанятого(
		ЭтотОбъект, ЭтотОбъект, ИНН(Запись.Контрагент), Запись.Период);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИНН(Контрагент)
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		//@skip-check bsl-legacy-check-string-literal
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ИНН");
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьПравоРедактирование()
	
	Возврат ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.СтатусыСамозанятых);
	
КонецФункции

#КонецОбласти
