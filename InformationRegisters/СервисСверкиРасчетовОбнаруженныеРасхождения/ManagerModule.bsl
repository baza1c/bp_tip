#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаписатьДанные(ПараметрыМетода, ДанныеСервиса) Экспорт 
	
	Организация   = ПараметрыМетода.Организация;
	ТекущийМесяц  = НачалоМесяца(ПараметрыМетода.ДатаНачала);
	КонечныйМесяц = НачалоМесяца(ПараметрыМетода.ДатаОкончания);
	
	Если ДанныеСервиса = Неопределено
		ИЛИ ТипЗнч(ДанныеСервиса) <> Тип("ТаблицаЗначений") Тогда
		УдалитьДанныеПоРегистрамРасхождений(Организация, ТекущийМесяц, КонецМесяца(КонечныйМесяц));
		Возврат;
	КонецЕсли;
	
	// Записываем расхождения помесячно.
	Пока ТекущийМесяц <= КонечныйМесяц Цикл
		
		НачалоМесяца = НачалоМесяца(ТекущийМесяц);
		КонецМесяца  = КонецМесяца(ТекущийМесяц);
		
		СтруктураОтбора    = Новый Структура("ПериодСверки", ТекущийМесяц);
		РасхожденияЗаМесяц = ДанныеСервиса.Скопировать(СтруктураОтбора);
		
		Если СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены() Тогда
			ЗаписатьИзменениеРасхождений(РасхожденияЗаМесяц, Организация, НачалоМесяца, КонецМесяца);
		КонецЕсли;
		УдалитьДанныеПоРегистрамРасхождений(Организация, НачалоМесяца, КонецМесяца);
		
		УстановитьПривилегированныйРежим(Истина);
		
		// Текущий регистр.
		НаборЗаписей = СоздатьНаборЗаписей(); 
		НаборЗаписей.РасширенныеРежимыЗамещения = Истина;
		
		// Связанный регистр с детальными записями.
		НаборЗаписейДанныеСервиса = РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.СоздатьНаборЗаписей();
		НаборЗаписейДанныеСервиса.РасширенныеРежимыЗамещения = Истина;
		
		// Открываем транзакцию записи расхождений за месяц.
		// Текущий и связанный регистры пишем в одной транзакции для обеспечения целостности данных.
		НачатьТранзакцию();
		Для Каждого Строка Из РасхожденияЗаМесяц Цикл
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			
			Контрагент     = Строка.Контрагент;
			ДокументСсылка = Неопределено;
			
			// Дополним набор документами по организации.
			РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.ДополнитьНаборЗаписей(
				Строка.МассивДокументовПоОрганизации, 
				Организация, 
				Контрагент,
				ТекущийМесяц,
				Запись.ИдентификаторСверкиПоОрганизации,
				НаборЗаписейДанныеСервиса,
				ДокументСсылка);
			РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.ДополнитьНаборЗаписей(
				Строка.МассивДокументовПоКонтрагенту, 
				Организация, 
				Контрагент,
				ТекущийМесяц,
				Запись.ИдентификаторСверкиКонтрагенту,
				НаборЗаписейДанныеСервиса);
				
			Если ЗначениеЗаполнено(ДокументСсылка) Тогда
				Запись.Документ = ДокументСсылка;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НаборЗаписей.Количество() > 0 Тогда 
			// Блокировка текущего регистра.
			БлокировкаТекущегоРегистра = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаТекущегоРегистра.Добавить("РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
			ЭлементБлокировки.УстановитьЗначение("ПериодСверки", ТекущийМесяц);
			
			// Блокировка связанного регистра с детальными записями.
			БлокировкаСвязанногоРегистра = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаСвязанногоРегистра.Добавить("РегистрСведений.СервисСверкиРасчетовДанныеСервиса");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
			ЭлементБлокировки.УстановитьЗначение("ПериодСверки", ТекущийМесяц);
			
			Попытка
				БлокировкаТекущегоРегистра.Заблокировать();
				НаборЗаписей.Записать(РежимЗамещения.Слияние);
				
				БлокировкаСвязанногоРегистра.Заблокировать();
				НаборЗаписейДанныеСервиса.Записать(РежимЗамещения.Слияние);
				
				// Фиксируем транзакцию записи расхождений за месяц
				ЗафиксироватьТранзакцию();
			Исключение
				// Откатываем транзакцию записи расхождений за месяц.
				ОтменитьТранзакцию();
				ШаблонОшибки = НСтр("ru = 'Не удалось записать расхождения за %1 по организации %2.
				|Подробное описание ошибки:
				|%3'");
				ТекстОшибки = СтрШаблон(ШаблонОшибки,
					Формат(ТекущийМесяц, "ДФ='MMMM yyyy'"), 
					Строка(Организация),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(СервисСверкиРасчетов.ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, ,, ТекстОшибки);
			КонецПопытки;
		Иначе
			// Откатываем транзакцию записи расхождений за месяц.
			ОтменитьТранзакцию();
		КонецЕсли;
		
		ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц, 1);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьИзменениеРасхождений(ДанныеСервиса, Организация, ДатаНачала, ДатаОкончания)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ДанныеСервиса.ИдентификаторРасхождения КАК СТРОКА(38)) КАК ИдентификаторРасхождения
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПолученногоСообщения
	|ИЗ
	|	&ДанныеСервиса КАК ДанныеСервиса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	ВЫБОР
	|		КОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации = 0
	|			ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента
	|		ИНАЧЕ СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации = 0
	|			ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента
	|		ИНАЧЕ СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации
	|	КОНЕЦ КАК НДСДокумента
	|ПОМЕСТИТЬ ТекущиеРасхождения
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку
	|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения КАК ИдентификаторРасхождения
	|ИЗ
	|	ВТ_ИдентификаторыПолученногоСообщения КАК ВТ_ИдентификаторыПолученногоСообщения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеРасхождения КАК ТекущиеРасхождения
	|		ПО ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения = ТекущиеРасхождения.ИдентификаторРасхождения
	|ГДЕ
	|	ТекущиеРасхождения.ИдентификаторРасхождения ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТекущиеРасхождения.Организация КАК Организация,
	|	ТекущиеРасхождения.Контрагент КАК Контрагент,
	|	ТекущиеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	МАКСИМУМ(1) КАК КоличествоДокументов,
	|	МАКСИМУМ(ТекущиеРасхождения.СуммаДокумента) КАК СуммаДокумента,
	|	МАКСИМУМ(ТекущиеРасхождения.НДСДокумента) КАК НДСДокумента
	|ИЗ
	|	ТекущиеРасхождения КАК ТекущиеРасхождения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыПолученногоСообщения КАК ВТ_ИдентификаторыПолученногоСообщения
	|		ПО ТекущиеРасхождения.ИдентификаторРасхождения = ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения
	|ГДЕ
	|	ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ТекущиеРасхождения.ИдентификаторРасхождения,
	|	ТекущиеРасхождения.Организация,
	|	ТекущиеРасхождения.Контрагент";
	
	Запрос.УстановитьПараметр("ДанныеСервиса", ДанныеСервиса);
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("ДатаНачала",    ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	Результат = Запрос.ВыполнитьПакет();
	
	// Запишем новые расхождения.
	НовыеРасхождения = Результат[2].Выгрузить().ВыгрузитьКолонку("ИдентификаторРасхождения");
	РегистрыСведений.СервисСверкиРасчетовСтатусыРасхождений.ЗаписатьДанные(НовыеРасхождения);
	
	// Запишем устраненные расхождения.
	УстраненныеРасхождения = Результат[3].Выбрать();
	РегистрыСведений.СервисСверкиРасчетовУстраненныеРасхождения.ЗаписатьДанные(УстраненныеРасхождения);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьДанныеПоРегистрамРасхождений(Организация, ДатаНачала, ДатаОкончания)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",         Организация);
	Запрос.УстановитьПараметр("ДатаНачала",          ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",       КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("ПустойИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПериодСверки КАК ПериодСверки,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ
	|ПОМЕСТИТЬ ОбнаруженнныеРасхожденияКУдалениюПредварительная
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	(СервисСверкиРасчетовОбнаруженныеРасхождения.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИЛИ СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку)
	|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиКУдалению,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ПериодСверки КАК ПериодСверки,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.Организация КАК Организация
	|ПОМЕСТИТЬ ИдентификаторыСверкиКУдалению
	|ИЗ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная КАК ОбнаруженнныеРасхожденияКУдалениюПредварительная
	|ГДЕ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ИдентификаторСверкиПоОрганизации <> &ПустойИдентификатор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ИдентификаторСверкиКонтрагенту,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ПериодСверки,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.Организация
	|ИЗ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная КАК ОбнаруженнныеРасхожденияКУдалениюПредварительная
	|ГДЕ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ИдентификаторСверкиКонтрагенту <> &ПустойИдентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.Организация КАК Организация,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ПериодСверки КАК ПериодСверки,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.Контрагент КАК Контрагент,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.Дата КАК Дата,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная.Документ КАК Документ
	|ИЗ
	|	ОбнаруженнныеРасхожденияКУдалениюПредварительная КАК ОбнаруженнныеРасхожденияКУдалениюПредварительная
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодСверки
	|ИТОГИ ПО
	|	ПериодСверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИдентификаторыСверкиКУдалению.ПериодСверки КАК ПериодСверки,
	|	СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки КАК ИдентификаторСверки,
	|	СервисСверкиРасчетовДанныеСервиса.Организация КАК Организация,
	|	СервисСверкиРасчетовДанныеСервиса.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовДанныеСервиса.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьДокументыКУдалению
	|ИЗ
	|	ИдентификаторыСверкиКУдалению КАК ИдентификаторыСверкиКУдалению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
	|		ПО ИдентификаторыСверкиКУдалению.Организация = СервисСверкиРасчетовДанныеСервиса.Организация
	|			И ИдентификаторыСверкиКУдалению.ИдентификаторСверкиКУдалению = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
	|
	|СГРУППИРОВАТЬ ПО
	|	СервисСверкиРасчетовДанныеСервиса.Контрагент,
	|	СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки,
	|	СервисСверкиРасчетовДанныеСервиса.Организация,
	|	СервисСверкиРасчетовДанныеСервиса.ИдентификаторДокумента,
	|	ИдентификаторыСверкиКУдалению.ПериодСверки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодСверки
	|ИТОГИ
	|	МАКСИМУМ(ЕстьДокументыКУдалению)
	|ПО
	|	ПериодСверки";
	
	Результат = Запрос.ВыполнитьПакет();
	// При удалении записей текущего регистра необходимо удалить связанные с ними записи в регистре 
	// "СервисСверкиРасчетовДанныеСервиса", для этих целей выборки расхождений и документов группируются синхронно
	// по периодам.
	ВыборкаРасхожденийПоПериодам = Результат[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаДокументовПоПериодам  = Результат[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаРасхожденийПоПериодам.Следующий() Цикл
		
		ВыборкаДокументовПоПериодам.Следующий();
		
		// Устанавливаем блокировки помесячно по организации для очистки регистра.
		БлокировкаТекущегоРегистра = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаТекущегоРегистра.Добавить("РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.УстановитьЗначение("ПериодСверки", ВыборкаРасхожденийПоПериодам.ПериодСверки);
		
		// Набор записей текущего регистра.
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.РасширенныеРежимыЗамещения = Истина;
		
		ВыборкаРасхожденийДетальная = ВыборкаРасхожденийПоПериодам.Выбрать();
		Пока ВыборкаРасхожденийДетальная.Следующий() Цикл
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасхожденийДетальная); 
			
		КонецЦикла;
		
		БлокировкаСвязанногоРегистра= Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаСвязанногоРегистра.Добавить("РегистрСведений.СервисСверкиРасчетовДанныеСервиса");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.УстановитьЗначение("ПериодСверки", ВыборкаДокументовПоПериодам.ПериодСверки);
		
		// Набор записей регистра, связанного с текущим, который необходимо почистить.
		НаборЗаписейДанныеСервиса = РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.СоздатьНаборЗаписей();
		НаборЗаписейДанныеСервиса.РасширенныеРежимыЗамещения = Истина;

		ВыборкаДокументовДетальная  = ВыборкаДокументовПоПериодам.Выбрать();
		Пока ВыборкаДокументовДетальная.Следующий() Цикл
			Если Не ВыборкаДокументовДетальная.ЕстьДокументыКУдалению Тогда
				Продолжить;
			КонецЕсли;
			ЗаписьДанныеСервиса = НаборЗаписейДанныеСервиса.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьДанныеСервиса, ВыборкаДокументовДетальная); 	
		КонецЦикла;
		
		// Чистим 2 регистра в одной транзакции для обеспечения целостности данных.
		НачатьТранзакцию();
		
		Попытка
			БлокировкаТекущегоРегистра.Заблокировать();
			НаборЗаписей.Записать(РежимЗамещения.Удаление);
			
			БлокировкаСвязанногоРегистра.Заблокировать();
			НаборЗаписейДанныеСервиса.Записать(РежимЗамещения.Удаление);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонОшибки = НСтр("ru = 'Не удалось удалить расхождения за %1 по организации %2.
				|Подробное описание ошибки:
				|%3'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки,
				Формат(ВыборкаРасхожденийПоПериодам.ПериодСверки, "ДФ='MMMM yyyy'"), 
				Строка(Организация),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(СервисСверкиРасчетов.ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, ,, ТекстОшибки);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьИдентификаторыРасхождений(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	
	Параметры.ОбработкаЗавершена = Ложь;
	Если Результат.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ИмяПроцедуры = "РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения.ЗаполнитьИдентификаторыРасхождений()";
	ОбработаноЗаписей = 0;
	ПроблемныхЗаписей = 0;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			ОбработаноЗаписей = ОбработаноЗаписей + 1;
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Отбор.Дата.Установить(Выборка.Дата);
			НаборЗаписей.Отбор.ИдентификаторСверкиПоОрганизации.Установить(Выборка.ИдентификаторСверкиПоОрганизации);
			НаборЗаписей.Отбор.ИдентификаторСверкиКонтрагенту.Установить(Выборка.ИдентификаторСверкиКонтрагенту);
			
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				
				// Добавим постфикс для того чтобы отличать расхождение по организации и по контрагенту,
				// когда в одной базе ведется учет по сверяемым между собой организациям.
				// ОК - есть ИД сверки у организации и контрагента
				// О_ - есть ИД сверки только по организации (у контрагента нет документа)
				// _К - есть ИД сверки только по контрагенту (у организации нет документа)
				ПредварительныйИдентификаторРасхождения = "";
				Постфикс = "ОК";
				Если Строка(Запись.ИдентификаторСверкиПоОрганизации)  =  "00000000-0000-0000-0000-000000000000"  Тогда
					Постфикс = "_К"; // у организации нет документа
					ПредварительныйИдентификаторРасхождения = Строка(Запись.ИдентификаторСверкиКонтрагенту);
				Иначе
					Если Строка(Запись.ИдентификаторСверкиКонтрагенту) = "00000000-0000-0000-0000-000000000000" Тогда
						Постфикс = "О_"; // у контрагента нет документа
					КонецЕсли;
					ПредварительныйИдентификаторРасхождения = Строка(Запись.ИдентификаторСверкиПоОрганизации);
				КонецЕсли;
				Запись.ИдентификаторРасхождения = ПредварительныйИдентификаторРасхождения + Постфикс;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
		Исключение
			
			ПроблемныхЗаписей = ПроблемныхЗаписей + 1;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось установить идентификатор расхождения по причине:
				|%2'"), ИмяПроцедуры, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения,
				,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПроблемныхЗаписей > 0 Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось установить идентификаторы расхождений: в %2 из %3 возникли ошибки'"),
			ИмяПроцедуры, ПроблемныхЗаписей, ОбработаноЗаписей);
		ВызватьИсключение ТекстСообщения;
		
	Иначе
		
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения,,
			СтрШаблон(НСтр("ru = 'Процедура %1 обработала очередную порцию идентификаторов: %2 записей'"),
				ИмяПроцедуры, ОбработаноЗаписей));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПериодСверки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПериодСверки = ДАТАВРЕМЯ(1, 1, 1)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяПроцедуры = "РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения.ЗаполнитьПериодСверки()";
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Отбор.Дата.Установить(Выборка.Дата);
			НаборЗаписей.Отбор.ИдентификаторСверкиПоОрганизации.Установить(Выборка.ИдентификаторСверкиПоОрганизации);
			НаборЗаписей.Отбор.ИдентификаторСверкиКонтрагенту.Установить(Выборка.ИдентификаторСверкиКонтрагенту);
			НаборЗаписей.Отбор.ИдентификаторРасхождения.Установить(Выборка.ИдентификаторРасхождения);
			
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				
				Запись.ПериодСверки = НачалоМесяца(Выборка.Дата);
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
		Исключение
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось установить период сверки по причине:
				|%2'"), ИмяПроцедуры, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения,
				,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

