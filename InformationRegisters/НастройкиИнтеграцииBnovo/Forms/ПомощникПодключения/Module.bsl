#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустой() Тогда
		Запись.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		Запись.Покупатель = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.Контрагенты.РозничныйПокупатель");
		Запись.УчетнаяЗапись = Новый УникальныйИдентификатор;

		ПроверитьУстановитьВидОплаты();
		ТуристическийНалогСервер.УстановитьГостиницу(Запись.Гостиница, Запись.Организация);
		ДатаНачалаЗагрузки = НачалоМесяца(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли; 
	ВыполнитьЗагрузку = Истина;
	
	ЗаполненыДанныеАутентификации
		= ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
КонецПроцедуры 
	
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.Свойство("ОбновитьИнтерфейс") Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
КонецПроцедуры 

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОбменСBnovo") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ИспользоватьОбменСBnovo.Установить(Истина);
		УстановитьПривилегированныйРежим(Ложь);
		
		ПараметрыЗаписи.Вставить("ОбновитьИнтерфейс");
	КонецЕсли; 
	
	ПараметрыМетода = Новый Структура;;
	ПараметрыМетода.Вставить("УчетнаяЗапись", Запись.УчетнаяЗапись);
	ПараметрыМетода.Вставить("APIKey",        APIKey);
	ПараметрыМетода.Вставить("ДатаНачалаЗагрузки", ДатаНачалаЗагрузки);
	
	// обновим параметры подключения
	ВыполнитьМетодСервисИнтеграцииНаСервере("ИнтеграцияBnovo.СоздатьОбновитьПодключение", ПараметрыМетода, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(КлючПодключения) 
		ИЛИ ЗавершениеРаботы Тогда
		
		Возврат;
	КонецЕсли;
	
	// если настройка не была завершена, 
	// но подключение к СИ было создано - необходимо его удалить
	УдалениеПодключенияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если НЕ ЗначениеЗаполнено(Запись.Организация) 
		ИЛИ НЕ ЗначениеЗаполнено(Запись.Гостиница) Тогда
	
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Запись.Организация);
	Запрос.УстановитьПараметр("Гостиница",   Запись.Гостиница);
	Запрос.УстановитьПараметр("УчетнаяЗапись",   Запись.УчетнаяЗапись);


	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииBnovo.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииBnovo КАК НастройкиИнтеграцииBnovo
	|ГДЕ
	|	НастройкиИнтеграцииBnovo.Организация = &Организация
	|	И НастройкиИнтеграцииBnovo.Гостиница = &Гостиница
	|	И НастройкиИнтеграцииBnovo.УчетнаяЗапись <> &УчетнаяЗапись";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Для выбранной гостиницы уже настроена интеграция'"),, "Гостиница","Запись",Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры  

&НаКлиенте
Процедура ДатаНачалаЗагрузкиПриИзменении(Элемент)
	ДатаНачалаЗагрузки = Макс(ДатаНачалаЗагрузки, ДобавитьМесяц(ОбщегоНазначенияКлиент.ДатаСеанса(), -12));
КонецПроцедуры 

&НаКлиенте
Процедура ПарольИПППриИзменении(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ПриИзмененииСекретныхДанных(
		Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольИППНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтернетПоддержкаПользователейКлиент.ОтобразитьСекретныеДанные(
		ЭтотОбъект,
		Элемент,
		"ПарольИПП");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Далее(Команда)
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация Тогда
		Если Не ЗаполненыДанныеАутентификации Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу; 
		Иначе
			ПодключитьСервисИнтеграции();
		КонецЕсли; 
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу Тогда
		
		Результат = ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации(
			Новый Структура("Логин, Пароль",
			ЛогинИПП, ПарольИПП));
		
		Если Результат.Отказ Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				Результат.СообщениеОбОшибке,
				,
				Результат.Поле);
			Возврат;
		КонецЕсли;
		
		Отказ = Ложь;
		
		ПроверитьПодключениеКПорталу1СИТС(
			ЛогинИПП,
			ПарольИПП,
			Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ПодключитьСервисИнтеграции();
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация Тогда
		ПодключитьBNovo();
		Элементы.ФормаНазад.Видимость = Истина;
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки Тогда
		
		Элементы.ВидОплатыКартой.Видимость = Запись.ЗагружатьОплатыКарта;
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНоменклатура;
		Если ПроверитьЗаполнение() Тогда
			Элементы.ФормаДалее.Заголовок = "Готово";
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНоменклатура Тогда
		Запись.ДатаПоследнейЗагрузки = ДатаНачалаЗагрузки;
		Запись.РазделятьДоговораПоНомерамБроней = СоздаватьОтдельныйДоговор = 1;
		
		Записать();
		
		// настройка завершена
		КлючПодключения = Неопределено;
		
		Закрыть(РезультатМастера());
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки Тогда
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация;
		Элементы.ФормаНазад.Видимость = Ложь;
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНоменклатура Тогда
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНастройки;
		Элементы.ФормаДалее.Заголовок = "Далее";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Процедура ПроверитьПодключениеКПорталу1СИТС(Логин, Пароль, Отказ)
	
	РезультатПроверки = ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль(
		Логин,
		Пароль);
	
	Если РезультатПроверки.Результат Тогда
		
		ДанныеАутентификации = Новый Структура;
		ДанныеАутентификации.Вставить("Логин", Логин);
		ДанныеАутентификации.Вставить("Пароль", Пароль);
		
		УстановитьПривилегированныйРежим(Истина);
		ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(
			ДанныеАутентификации);
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			РезультатПроверки.СообщениеОбОшибке,
			,
			,
			,
			Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция РезультатМастера()
	Результат = Новый Структура;
	Результат.Вставить("ВыполнитьЗагрузку", ВыполнитьЗагрузку);
	Результат.Вставить("Организация", Запись.Организация);
	Результат.Вставить("Гостиница", Запись.Гостиница);
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	ТуристическийНалогСервер.УстановитьГостиницу(Запись.Гостиница, Запись.Организация);
	ПроверитьУстановитьВидОплаты();
КонецПроцедуры

&НаСервере
Процедура ПроверитьУстановитьВидОплаты()
	Если Запись.Организация.Пустая() Тогда
		Запись.ВидОплатыКартой = Неопределено;
		Возврат;
	КонецЕсли;
	
	СведенияОВидахОплат = Справочники.ВидыОплатОрганизаций.ВидыОплатПлатежнойКартой(Запись.Организация);
	Запись.ВидОплатыКартой = СведенияОВидахОплат.ВидОплатыПоУмолчанию;
КонецПроцедуры

&НаСервере
Процедура УдалениеПодключенияНаСервере()
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыПроцедуры = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыУдалитьПодключение();
	ПараметрыПроцедуры.КлючПодключения = КлючПодключения;
	ПараметрыПроцедуры.ЭтоТекущийКлючПодключения = Истина;
	
	ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "ИнтеграцияBnovo.УдалитьПодключение", ПараметрыПроцедуры);
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьBNovo()
	ПараметрыМетода = Новый Структура;; 
	
	// уникальный идентификатор подключения, по нему, вместо APIKey мы будем потом общаться с СИ
	ПараметрыМетода.Вставить("УчетнаяЗапись", Запись.УчетнаяЗапись);
	// секретный ключ из подключения к сервису, в программе не хранится, используется только для установки нового подкючения
	ПараметрыМетода.Вставить("APIKey",        APIKey);
	// дата начала первой загрузки, будет уточнена на последненем этапе настройки
	ПараметрыМетода.Вставить("ДатаНачалаЗагрузки",    ДатаНачалаЗагрузки);
	
	// создаем новое подключение по APIKey, если ключ невалидный - метод выдаст ошибку и на следующий шаг мы не продвинемся
	ВыполнитьМетодСервисИнтеграции("ИнтеграцияBnovo.СоздатьОбновитьПодключение", ПараметрыМетода, Элементы.ГруппаНастройки , НСтр("ru = 'Подключаем учетную запись BNovo'"));
КонецПроцедуры

#Область РаботаСМетодамиСИ

&НаКлиенте
Процедура ПодключитьСервисИнтеграции()
	ПараметрыМетода = Новый Структура;
	// сначала подключимся к СИ, для этого нужна лишь валидная запись на портале 1С:ИТС
	ВыполнитьМетодСервисИнтеграции("ИнтеграцияBnovo.ПодключитьСервисИнтеграции", ПараметрыМетода, Элементы.ГруппаАвторизация, НСтр("ru = 'Настраиваем сервис интеграции'"));
КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьМетодСервисИнтеграции(ИмяМетода, ПараметрыМетода, СледующийШаг, ЗаголовокФоновогоЗадания)
	ОперацияПодключение = ВыполнитьМетодСервисИнтеграцииНаСервере(ИмяМетода, ПараметрыМетода, УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = ЗаголовокФоновогоЗадания;
	
	ПодключитьсяЗавершение = Новый ОписаниеОповещения("ВыполнитьМетодСервисаЗавершение", ЭтотОбъект, СледующийШаг);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияПодключение, ПодключитьсяЗавершение, ПараметрыОжидания); 
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьМетодСервисИнтеграцииНаСервере(ИмяМетода, ПараметрыМетода, ИдентификаторФормы)
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, ПараметрыМетода);
КонецФункции

&НаКлиенте
Процедура ВыполнитьМетодСервисаЗавершение(РезультатДлительнойОперации, СледующийШаг) Экспорт
	Если РезультатДлительнойОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатДлительнойОперации.Статус <> "Выполнено" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'При выполнении запроса к сервису произошла ошибка. Подробности в журнале регистрации.'"));
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = ПолучитьИзВременногоХранилища(РезультатДлительнойОперации.АдресРезультата);
	Если НЕ РезультатЗапроса.Результат  Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗапроса.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗапроса.Свойство("Ответ") 
		И РезультатЗапроса.Ответ.Свойство("КлючПодключения") Тогда
		
		// Сохраним ключ текущего подключения, 
		// чтобы удалить его если интеграция не будет завершена
		КлючПодключения = РезультатЗапроса.Ответ.КлючПодключения;
	КонецЕсли;
	
	Элементы.ГруппаШаги.ТекущаяСтраница = СледующийШаг;
КонецПроцедуры


#КонецОбласти

#КонецОбласти
