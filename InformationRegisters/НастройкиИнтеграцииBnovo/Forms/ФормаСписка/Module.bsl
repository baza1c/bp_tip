#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ФормаГруппаЗагрузить.Видимость = Параметры.Свойство("ПоказыватьКомандыЗагрузки")
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьНастройку(Команда)
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеНастройкиОбменаСBNovo", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.НастройкиИнтеграцииBnovo.Форма.ПомощникПодключения", , ЭтотОбъект,
		, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзBnovo(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("Организация", ТекущиеДанные.Организация);
	ПараметрыЗагрузки.Вставить("Гостиница",   ТекущиеДанные.Гостиница);
	
	ИнтеграцияBnovoКлиент.ВыполнитьЗагрузку(ЭтотОбъект, ПараметрыЗагрузки);
КонецПроцедуры 

&НаКлиенте
Процедура ЗагрузитьИзBNovo_ЗаПериод(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачала",     ДатаЗагрузки(ТекущиеДанные.Организация, ТекущиеДанные.Гостиница));
	ПараметрыФормы.Вставить("ДатаОкончания",  НачалоДня(ТекущаяДата())-1);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборПериодаЗагрузкиЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ИнтеграцияBnovo.Форма.ВыборПериода", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаЗагрузкиЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("Период",      ВыбранноеЗначение);
	ПараметрыЗагрузки.Вставить("Организация", ТекущиеДанные.Организация);
	ПараметрыЗагрузки.Вставить("Гостиница",   ТекущиеДанные.Гостиница);
	
	ИнтеграцияBnovoКлиент.ВыполнитьЗагрузку(ЭтотОбъект, ПараметрыЗагрузки);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеНастройкиОбменаСBNovo(Результат, ДопПараметры) Экспорт
	Если НЕ ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ВыполнитьЗагрузку", Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура("Гостиница, Организация");
	ЗаполнитьЗначенияСвойств(ПараметрыЗагрузки, Результат);
	
	ИнтеграцияBnovoКлиент.ВыполнитьЗагрузку(ЭтотОбъект, ПараметрыЗагрузки, "ВыполнитьЗагрузкуЗавершение");
КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьЗагрузкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Закрыть();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДатаЗагрузки(Организация, Гостиница)
	Возврат ИнтеграцияBnovo.ДатаЗагрузкиBNovo(Организация, Гостиница);
КонецФункции

#КонецОбласти

