#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
		Запись.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	СоздаватьОтдельныйДоговор = ?(Запись.РазделятьДоговораПоНомерамБроней, 1, 0);
	APIKey = "             ";
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыМетода = Новый Структура;;
	ПараметрыМетода.Вставить("УчетнаяЗапись",      Запись.УчетнаяЗапись);
	ПараметрыМетода.Вставить("APIKey",             ?(ЗначениеЗаполнено(APIKey), APIKey, ""));
	ПараметрыМетода.Вставить("ДатаНачалаЗагрузки", Запись.ДатаПоследнейЗагрузки);
	
	// обновим параметры подключения
	ВыполнитьМетодСервисИнтеграцииНаСервере("ИнтеграцияBnovo.СоздатьОбновитьПодключение", ПараметрыМетода, УникальныйИдентификатор);
	
	ПараметрыЗаписи.Вставить("ОбновитьИнтерфейс", ПроверитьУстановитьФункциональныеОпции());
	
КонецПроцедуры 

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Запись.РазделятьДоговораПоНомерамБроней = СоздаватьОтдельныйДоговор = 1;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.Свойство("ОбновитьИнтерфейс") 
		И ПараметрыЗаписи.ОбновитьИнтерфейс Тогда
	
		ОбновитьИнтерфейс();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ВыполнитьМетодСервисИнтеграцииНаСервере(ИмяМетода, ПараметрыМетода, ИдентификаторФормы)
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, ПараметрыМетода);
КонецФункции


&НаСервере
Функция ПроверитьУстановитьФункциональныеОпции()
	
	ОбновитьИнтерфейс = Ложь;
	
	Если ЗначениеЗаполнено(Запись.ВидОплатыКартой) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Если Не Константы.ИспользоватьОплатуПоПлатежнымКартам.Получить() Тогда
			Константы.ИспользоватьОплатуПоПлатежнымКартам.Установить(Истина);
			ОбновитьИнтерфейс = Истина;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.Покупатель) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Если Не Константы.ИспользоватьПредоплатыВРознице.Получить() Тогда
			Константы.ИспользоватьПредоплатыВРознице.Установить(Истина);
			ОбновитьИнтерфейс = Истина;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Возврат ОбновитьИнтерфейс;
	
КонецФункции

#КонецОбласти




