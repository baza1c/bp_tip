#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс 

Функция ЕстьНастроенныеИнтеграции() Экспорт
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиBnovo.Логин КАК Логин
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииBnovo КАК НастройкиBnovo";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

Функция СписокГостиницДляЗагрузки(Организация, Гостиница) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиBnovo.Организация КАК Организация,
	|	НастройкиBnovo.Гостиница КАК Гостиница,
	|	НастройкиBnovo.ДатаПоследнейЗагрузки КАК ДатаЗагрузки
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииBnovo КАК НастройкиBnovo";
	
	СписокУсловий = Новый Массив;
	СписокУсловий.Добавить("НастройкиBnovo.Организация В (&СписокОрганизаций)");
	
	Запрос.УстановитьПараметр("СписокОрганизаций", ОбщегоНазначенияБП.СписокДоступныхОрганизаций(Организация));
	
	Если ЗначениеЗаполнено(Гостиница) Тогда
		СписокУсловий.Добавить("НастройкиBnovo.Гостиница = &Гостиница");
		Запрос.УстановитьПараметр("Гостиница", Гостиница);
	КонецЕсли;
	
	Если СписокУсловий.Количество() > 0 Тогда
		Запрос.Текст = Запрос.Текст + " ГДЕ " + СтрСоединить(СписокУсловий, " И ");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// СтандартныеПодсистемы.УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
// Параметры:
//  Ограничение - Структура:
//    * Текст                             - Строка - ограничение доступа для пользователей.
//                                          Если пустая строка, значит, доступ разрешен.
//    * ТекстДляВнешнихПользователей      - Строка - ограничение доступа для внешних пользователей.
//                                          Если пустая строка, значит, доступ запрещен.
//    * ПоВладельцуБезЗаписиКлючейДоступа - Неопределено - определить автоматически.
//                                        - Булево - если Ложь, то всегда записывать ключи доступа,
//                                          если Истина, тогда не записывать ключи доступа,
//                                          а использовать ключи доступа владельца (требуется,
//                                          чтобы ограничение было строго по объекту-владельцу).
//   * ПоВладельцуБезЗаписиКлючейДоступаДляВнешнихПользователей - Неопределено, Булево - также
//                                          как у параметра ПоВладельцуБезЗаписиКлючейДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли