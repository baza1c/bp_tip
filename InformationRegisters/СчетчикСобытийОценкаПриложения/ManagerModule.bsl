#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыСобытияОценки(ИдентификаторОперации) Экспорт
	
	Результат = Новый Структура("ПоказыватьФормуОценки, Количество, РегистрироватьСобытие", Ложь, 0, Истина);
	
	Пользователь = Пользователи.ТекущийПользователь();
	
	СтруктураОтбора = Новый Структура("Пользователь", Пользователь);
	
	СтруктураЗаписи = РегистрыСведений.ОценкаПриложения.Получить(СтруктураОтбора);
	
	Если СтруктураЗаписи.Оценка <> 0 Тогда
		Результат.РегистрироватьСобытие = Ложь;
		
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетчикСобытийОценкаПриложения.Количество КАК Количество
		|ИЗ
		|	РегистрСведений.СчетчикСобытийОценкаПриложения КАК СчетчикСобытийОценкаПриложения
		|ГДЕ
		|	СчетчикСобытийОценкаПриложения.Пользователь = &Пользователь
		|	И СчетчикСобытийОценкаПриложения.ИдентификаторОперации = &ИдентификаторОперации";

	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("ИдентификаторОперации", ИдентификаторОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Если ПорогиОпераций().Получить(ИдентификаторОперации) = 0 Тогда
			Результат.ПоказыватьФормуОценки = Истина;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПороговоеКоличество = ПорогиОпераций().Получить(ИдентификаторОперации); 
		
		Если Выборка.Количество = ПороговоеКоличество
			ИЛИ ДостигнутВторойПорог(Выборка.Количество) Тогда
			Результат.ПоказыватьФормуОценки = Истина;
			Результат.РегистрироватьСобытие = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьЗаписьПоСобытию(ИдентификаторОперации) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = Пользователи.ТекущийПользователь();
	МенеджерЗаписи.ИдентификаторОперации = ИдентификаторОперации;
	
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Количество = 0 Тогда
		МенеджерЗаписи.Пользователь = Пользователи.ТекущийПользователь();
		МенеджерЗаписи.ИдентификаторОперации = ИдентификаторОперации;
	КонецЕсли;
	
	МенеджерЗаписи.Количество = МенеджерЗаписи.Количество + 1;
	
	Попытка
		МенеджерЗаписи.Записать(Истина);
	Исключение
		ЗаписьЖурналаРегистрации("ЗаписьПоСобытиюОценкиПриложения", УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

//Для отсчета второго порога после "Показать позже"
Процедура НачатьОтсчетВторогоПорога() Экспорт
	
	Пороги = ПорогиОпераций();
	Пользователь = Пользователи.ТекущийПользователь();
	
	Для каждого Порог Из Пороги Цикл
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Количество = НачалоОтсчетаВторогоПорога();
		МенеджерЗаписи.ИдентификаторОперации = Порог.Ключ;
		
		Попытка
			МенеджерЗаписи.Записать(Истина);
		Исключение
			ЗаписьЖурналаРегистрации("ЗаписьПоСобытиюОценкиПриложения", УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДостигнутВторойПорог(Количество)
	
	Результат = Ложь;
	
	ЗначениеВторогоПорога = ЗначениеВторогоПорога();
	
	Если Количество >= ЗначениеВторогоПорога Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПорогиОпераций()
	
	ПорогиОпераций = Новый Соответствие;
	
	ПорогиОпераций.Вставить("отправкапопочте", 0);
	ПорогиОпераций.Вставить("поделитьсяфайлом", 0);
	ПорогиОпераций.Вставить("просмотрдокументов", 6);
	ПорогиОпераций.Вставить("просмотротчетов", 3);
	
	Возврат ПорогиОпераций;
	
КонецФункции

Функция НачалоОтсчетаВторогоПорога()
	
	Возврат 7;
	
КонецФункции

Функция ЗначениеВторогоПорога()
	
	Возврат 12;
	
КонецФункции

#КонецОбласти

#КонецЕсли
