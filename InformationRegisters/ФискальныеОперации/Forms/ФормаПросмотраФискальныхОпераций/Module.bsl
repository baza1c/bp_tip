#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Отбор") 
		И Параметры.Отбор.Свойство("ДокументОснование", ДокументОснование) Тогда
		
		Элементы.ДокументОснование.Видимость = Ложь;
		Элементы.ФормаЧекКоррекции.Видимость = Ложь;
		
		Заголовок = СтрШаблон(НСтр("ru = 'Пробитые чеки по  %1'"), ДокументОснование);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ДокументОснование", , ВидСравненияКомпоновкиДанных.Равно,,
			Ложь, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		
		ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтаФорма);
	КонецЕсли;
	
		
	Если Параметры.Свойство("РежимВыбора") Тогда
		Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		АктивироватьПоследнююОперацию(ДокументОснование);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
	
		ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список, ,Параметр);
	
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСписка

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ПоказатьЧек", 0.5, Истина);
КонецПроцедуры

&НаСервере
Процедура СписокПриЗагрузкеПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "ДокументОснование");
	Иначе
		ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЧекКоррекции(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НапечататьЧекЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура("ПоказатьБаннерЧекКоррекции, Ключ", Истина, ТекущиеДанные.ДокументОснование);
	
	ОткрытьФорму(ИмяФормыДокумента(ТекущиеДанные.ДокументОснование), ПараметрыФормы,ЭтотОбъект,,,,ОписаниеОповещения);
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИмяФормыДокумента(ДокументСсылка)
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипЗнч(ДокументСсылка));
	
	Возврат МетаданныеДокумента.ОсновнаяФормаОбъекта.ПолноеИмя();
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПоказатьЧек() Экспорт
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьТекстФискальнойОперации(ТекущиеДанные.ДокументОснование, ТекущиеДанные.ИдентификаторЗаписи, ПредварительныйПросмотрЧека);
КонецПроцедуры

&НаКлиенте
Процедура НапечататьЧекЗавершение(Значение, ДополнительныеПараметры) Экспорт
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура АктивироватьПоследнююОперацию(ДокументОснованиеФискальнойОперации)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснованиеФискальнойОперации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФискальныеОперации.ДокументОснование КАК ДокументОснование,
	|	ФискальныеОперации.ИдентификаторЗаписи КАК ИдентификаторЗаписи
	|ИЗ
	|	РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|ГДЕ
	|	ФискальныеОперации.ДокументОснование = &ДокументОснование
	|	И (ФискальныеОперации.ТипРасчета = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств)
	|			ИЛИ ФискальныеОперации.ТипРасчета = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФискальныеОперации.Дата УБЫВ";
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Если ТаблицаРезультата.Количество() > 0 Тогда
		КлючЗаписи = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРезультата[0]);
		Элементы.Список.ТекущаяСтрока = РегистрыСведений.ФискальныеОперации.СоздатьКлючЗаписи(КлючЗаписи);
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьТекстФискальнойОперации(ДокументОснование, ИдентификаторЗаписи, ТабличныйДокумент)
	ТабличныйДокументЧека = ТекстФискальнойОперации(ДокументОснование, ИдентификаторЗаписи);
	
	Если НЕ ТабличныйДокументЧека = Неопределено Тогда
		ТабличныйДокумент = ТабличныйДокументЧека;
	Иначе
		ТабличныйДокумент.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстФискальнойОперации(ДокументОснование, ИдентификаторЗаписи)
	Реквизиты = ОборудованиеЧекопечатающиеУстройстваВызовСервера.ДанныеФискальнойОперации(ДокументОснование, ИдентификаторЗаписи);
	Если Реквизиты <> Неопределено Тогда
		ТекстСообщенияXML = Реквизиты.ДанныеXML.Получить();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекстСообщенияXML) 
		ИЛИ   НЕ Реквизиты.ТипДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧек 
			И НЕ Реквизиты.ТипДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧекКоррекции Тогда
	
		Возврат Неопределено;
	КонецЕсли;
	
	ОбщиеПараметры = ОборудованиеЧекопечатающиеУстройства.ЗагрузитьДанныеФискализацииИзXML(ТекстСообщенияXML);
	ОбщиеПараметры.ДатаВремя = Реквизиты.Дата;
	Если ЗначениеЗаполнено(ОбщиеПараметры.ПокупательEmail) Тогда
		ЭлектроннаяПочта = ОбщиеПараметры.ПокупательEmail;
	Иначе
		ЭлектроннаяПочта = НСтр("ru = '<не задано>'");
	КонецЕсли;
	
	ШиринаЧека = 42;
	Шаблон = ШаблоныФискальныхДокументов.ШаблонКассовыйЧек(ШиринаЧека, ОбщиеПараметры, Реквизиты);
	ШаблоныФискальныхДокументов.СформироватьДокументПоШаблону(Шаблон);
	Возврат ШаблоныФискальныхДокументов.ВывестиКакТабличныйДокумент(Шаблон);
КонецФункции 

#КонецОбласти 


 