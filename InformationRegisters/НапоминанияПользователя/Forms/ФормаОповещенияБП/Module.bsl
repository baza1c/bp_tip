#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьТаблицуНапоминаний();
	
	// Форму не показываем. Отправляем только оповещение пользователю
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	
	ОбновитьТаблицуНапоминаний();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	НапоминанияПользователяКлиент.СброситьТаймерПроверкиТекущихОповещений();
	
	// Принудительное отключение обработчиков необходимо в связи с тем, что форма не выгружается из памяти.
	ОтключитьОбработчикОжидания("ОбновитьТаблицуНапоминаний");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НапоминанияПользователя" Тогда 
		ОбновитьТаблицуНапоминаний();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьТаблицуНапоминаний()

	ОтключитьОбработчикОжидания("ОбновитьТаблицуНапоминаний");
	
	ВремяБлижайшего = Неопределено;
	МассивНапоминаний = НапоминанияПользователяКлиент.ПолучитьТекущиеОповещения(ВремяБлижайшего);
	Для Каждого Напоминание Из МассивНапоминаний Цикл
		НайденныеСтроки = Напоминания.НайтиСтроки(Новый Структура("Источник, ВремяСобытия", Напоминание.Источник, Напоминание.ВремяСобытия));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(НайденныеСтроки[0], Напоминание, , "СрокНапоминания");
		Иначе
			НоваяСтрока = Напоминания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Напоминание);
		КонецЕсли;
	КонецЦикла;
	
	СтрокиНаУдаление = Новый Массив;
	Для Каждого Напоминание Из Напоминания Цикл
		СтрокаНайдена = Ложь;
		Для Каждого СтрокаКэша Из МассивНапоминаний Цикл
			Если СтрокаКэша.Источник = Напоминание.Источник И СтрокаКэша.ВремяСобытия = Напоминание.ВремяСобытия Тогда
				СтрокаНайдена = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не СтрокаНайдена Тогда 
			СтрокиНаУдаление.Добавить(Напоминание);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из СтрокиНаУдаление Цикл
		Напоминания.Удалить(Строка);
	КонецЦикла;
	
	ОбновитьПредставленияПредметов();
	
	Контекст = Новый Структура("Пользователь, ВремяСобытия, Источник, СрокНапоминания, Описание");
	
	Для Каждого Напоминание Из Напоминания Цикл
		
		ЗаполнитьЗначенияСвойств(Контекст, Напоминание);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОбработатьОткрытиеОповещения", ОповещенияПользователейОСобытияхКлиент, Контекст);
		
		Если ЭтоНапоминаниеПоЗадаче(Напоминание.Идентификатор) Тогда
			КартинкаОповещения = БиблиотекаКартинок.ОповещениеПользователяОЗадаче;
		ИначеЕсли ЭтоНапоминаниеОбОплатеСчета(Напоминание.Идентификатор) Тогда
			КартинкаОповещения = БиблиотекаКартинок.ОповещениеПользователяОбОплате;
		ИначеЕсли ЭтоНапоминаниеОбОтгрузкеСчета(Напоминание.Идентификатор) Тогда
			КартинкаОповещения = БиблиотекаКартинок.ОповещениеПользователяОбОтгрузке;
		ИначеЕсли ЭтоНапоминаниеОВыдачеПатента(Напоминание.Идентификатор) Тогда
			КартинкаОповещения = БиблиотекаКартинок.ДиалогИнформация;
		КонецЕсли;
		
		Пояснение = ПояснениеПоИдентификаторуОповещения(Напоминание.Идентификатор, Напоминание.Источник);
		
		ПоказатьОповещениеПользователя(
			Напоминание.Описание,
			ОписаниеОповещения,
			Пояснение,
			КартинкаОповещения,
			СтатусОповещенияПользователя.Важное,
			Напоминание.Идентификатор + Напоминание.ИсточникСтрокой);
		
		ОтключитьПовторноеОповещениеПользователю(Контекст);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПояснениеПоИдентификаторуОповещения(Идентификатор, Предмет)
	
	Пояснение = "";
	
	Если ЭтоНапоминаниеПоЗадаче(Идентификатор) Тогда
		ОписаниеЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Описание");
		Если ЗначениеЗаполнено(ОписаниеЗадачи) Тогда
			Пояснение = ОписаниеЗадачи;
		КонецЕсли;
	ИначеЕсли ЭтоНапоминаниеОбОплатеСчета(Идентификатор)
		Или ЭтоНапоминаниеОбОтгрузкеСчета(Идентификатор) Тогда
		ДанныеСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет, "Дата, Номер");
		Пояснение = СтрШаблон(НСтр("ru = 'Счет покупателя №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеСчета.Номер),
			Формат(ДанныеСчета.Дата, "ДЛФ=D;"));
	ИначеЕсли ЭтоНапоминаниеОВыдачеПатента(Идентификатор) Тогда
		ДанныеПатента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет, "Патент, ДатаНачала, ДатаОкончания");
		Пояснение = СтрШаблон(НСтр("ru = 'Патент %1 на %2 доступен в программе'"),
			ДанныеПатента.Патент,
			ПредставлениеПериода(НачалоДня(ДанныеПатента.ДатаНачала), КонецДня(ДанныеПатента.ДатаОкончания), "Л=ru_RU;ФП=Истина"));
	КонецЕсли;
	
	Возврат Пояснение;
	
КонецФункции

&НаКлиенте
Процедура ОтключитьПовторноеОповещениеПользователю(Контекст)
	
	ПараметрыНапоминания = НапоминанияПользователяКлиентСервер.ОписаниеНапоминания(Контекст);

	ОповещенияПользователейОСобытияхВызовСервера.ОтключитьОповещение(ПараметрыНапоминания);

	НапоминанияПользователяКлиент.УдалитьЗаписьИзКэшаОповещений(ПараметрыНапоминания);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставленияПредметов()
	
	МассивИсточников = Напоминания.Выгрузить(, "Источник").ВыгрузитьКолонку("Источник");
	МассивИсточников = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивИсточников);
	
	ПредставлениеИсточников = ОбщегоНазначения.ПредметыСтрокой(МассивИсточников);
	
	Для Каждого Напоминание Из Напоминания Цикл
		Если ЗначениеЗаполнено(Напоминание.Источник) И ПустаяСтрока(Напоминание.ИсточникСтрокой) Тогда
			ПредставлениеИсточника = ПредставлениеИсточников.Получить(Напоминание.Источник);
			Если ЗначениеЗаполнено(ПредставлениеИсточника) Тогда
				Напоминание.ИсточникСтрокой = ПредставлениеИсточника;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНапоминаниеПоЗадаче(Идентификатор)
	
	Возврат Идентификатор = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияИсполнителяЗадачи()
		Или Идентификатор = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияАвтораЗадачи()
		Или Идентификатор = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОНовойЗадаче()
		Или Идентификатор = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОбИзмененииСтатусаЗадачи();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНапоминаниеОбОплатеСчета(Идентификатор)
	
	Возврат Идентификатор = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОбОплатеСчета();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНапоминаниеОбОтгрузкеСчета(Идентификатор)
	
	Возврат Идентификатор = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОбОтгрузкеСчета();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНапоминаниеОВыдачеПатента(Идентификатор)
	
	Возврат Идентификатор = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОВыдачеПатента();
	
КонецФункции

#КонецОбласти
