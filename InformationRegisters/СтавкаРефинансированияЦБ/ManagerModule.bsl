#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.РегистрыСведений.СтавкаРефинансированияЦБ) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.1.36.40";
		Обработчик.Процедура = "РегистрыСведений.СтавкаРефинансированияЦБ.НачальноеЗаполнение";
		Обработчик.ОбщиеДанные = Истина;
		Обработчик.НачальноеЗаполнение = Истина;
	КонецЕсли;
КонецПроцедуры

// Начальное заполнение и обновление информационной базы.
Процедура НачальноеЗаполнение() Экспорт
	Обновить(ПолучитьМакет("ПредопределенныеЗначения").ПолучитьТекст());
КонецПроцедуры

// Заполняет ставки рефинансирования Центрального Банка Российской Федерации.
Процедура Обновить(ТекстXML) Экспорт
	ЗарплатаКадры.ОбновитьКлассификатор(ТекстXML);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать процедуру НачальноеЗаполнение.
Процедура ЗаполнитьЗначенияСтавкиРефинансированияЦБ() Экспорт
	НачальноеЗаполнение();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция СтавкаРефинансированияСрезПоследних(Период) Экспорт
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкаРефинансированияЦБСрезПоследних.Размер КАК Размер
		|ИЗ
		|	РегистрСведений.СтавкаРефинансированияЦБ.СрезПоследних(&Период, ) КАК СтавкаРефинансированияЦБСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Размер;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли