#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает последнюю активность пользователей в интерактивных сеансах.
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - ссылка на организацию активность по которой требуется проверить.
//	ПроверятьОтменуЗаданийВручную - Булево - возвращает результат с учетом остановки фоновых заданий нажатием кнопки "Отмена".
// 
// Возвращаемое значение:
//  Структура - структура со свойствами:
//		* Организация - СправочникСсылка.Организации - ссылка на справочник Организации активность по которой проверялась.
//		* Пользователь - СправочникСсылка.Пользователи - ссылка на справочник Пользователи, которые последним проводил документы.
//		* ДатаПоследнейАктивности - Дата - дата и время последней активности пользователя местная (когда проводил документ).
//		* ДатаПоследнейАктивностиУниверсальная - Дата - дата и время последней активности пользователя по UTC+0.
//		* Документ - ДокументСсылка - ссылка на последний документ, который проводил пользователь.
//		* ИдентификаторЗаданияДляОстановки - УникальныйИдентификатор - идентификатор задания актуализации или перерповедения для плавной остановки.
//
Функция ПоследняяАктивностьПользователей(Организация, ПроверятьОтменуЗаданийВручную = Ложь) Экспорт
	
	Результат = СтруктураПоследнейАктивности();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтборПоОрганизации", Истина);
	Запрос.УстановитьПараметр("ОтборПоДокументам", Истина);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АктивностьСеансовПользователей.Организация КАК Организация,
	|	АктивностьСеансовПользователей.Пользователь КАК Пользователь,
	|	АктивностьСеансовПользователей.ДатаПоследнейАктивности КАК ДатаПоследнейАктивности,
	|	АктивностьСеансовПользователей.ДатаПоследнейАктивностиУниверсальная КАК ДатаПоследнейАктивностиУниверсальная,
	|	АктивностьСеансовПользователей.Документ КАК Документ,
	|	АктивностьСеансовПользователей.ИдентификаторЗаданияДляОстановки КАК ИдентификаторЗаданияДляОстановки
	|ПОМЕСТИТЬ ДатыАктивностиПоОрганизации
	|ИЗ
	|	РегистрСведений.АктивностьСеансовПользователей КАК АктивностьСеансовПользователей
	|ГДЕ
	|	&ОтборПоОрганизации
	|	И &ОтборПоДокументам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ДатыАктивностиПоОрганизации.ДатаПоследнейАктивностиУниверсальная) КАК МаксимумДатаПоследнейАктивностиУниверсальная
	|ПОМЕСТИТЬ МаксимумПоДате
	|ИЗ
	|	ДатыАктивностиПоОрганизации КАК ДатыАктивностиПоОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыАктивностиПоОрганизации.Организация КАК Организация,
	|	ДатыАктивностиПоОрганизации.Пользователь КАК Пользователь,
	|	ДатыАктивностиПоОрганизации.ДатаПоследнейАктивности КАК ДатаПоследнейАктивности,
	|	ДатыАктивностиПоОрганизации.ДатаПоследнейАктивностиУниверсальная КАК ДатаПоследнейАктивностиУниверсальная,
	|	ДатыАктивностиПоОрганизации.Документ КАК Документ,
	|	ДатыАктивностиПоОрганизации.ИдентификаторЗаданияДляОстановки КАК ИдентификаторЗаданияДляОстановки
	|ИЗ
	|	ДатыАктивностиПоОрганизации КАК ДатыАктивностиПоОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимумПоДате КАК МаксимумПоДате
	|		ПО ДатыАктивностиПоОрганизации.ДатаПоследнейАктивностиУниверсальная = МаксимумПоДате.МаксимумДатаПоследнейАктивностиУниверсальная";
	
	Если Не ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		// В файловой БД при записи документов накладывается блокировка целиком на всю таблицу у журналов документов.
		// Так запись документов в "ЖурналОпераций" по одной организации, блокирует запись для всех остальных.
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации",
			"АктивностьСеансовПользователей.Организация = &Организация");
	КонецЕсли;
	
	Если Не ПроверятьОтменуЗаданийВручную Тогда
		// Отбор требуется при записи результатов закрытия месяца для проверки способа, которым остановили задание.
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоДокументам",
			"АктивностьСеансовПользователей.Документ <> НЕОПРЕДЕЛЕНО");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет фоновое задание на необходимость завершения (пользователь нажал кнопку "Отмена" в форме).
//
// Параметры:
//  ИдентификаторЗадания - УникальныйИдентификатор - идентификатор фонового задания для проверки.
// 
// Возвращаемое значение:
//  Булево - необходимость остановки фонового задания с указанным идентификатором.
//
Функция ФоновоеЗаданиеОтменили(ИдентификаторЗадания) Экспорт
	
	Результат = Ложь;
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда 
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторЗадания", ИдентификаторЗадания);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АктивностьСеансовПользователей.ИдентификаторЗаданияДляОстановки КАК ИдентификаторЗаданияДляОстановки
	|ИЗ
	|	РегистрСведений.АктивностьСеансовПользователей КАК АктивностьСеансовПользователей
	|ГДЕ
	|	АктивностьСеансовПользователей.ИдентификаторЗаданияДляОстановки = &ИдентификаторЗадания";
	
	Результат = Не Запрос.Выполнить().Пустой();
	Возврат Результат;
	
КонецФункции

// Удаляет записи об активности сеансов пользователей по организации.
// Если значение организации не заполнено, то удаляет записи по всем организациям.
//
// Параметры:
//  Организация	- СправочникСсылка.Организации	- организация для которой удаляется активность.
// 
Процедура УдалитьАктивностьПользователейПоОрганизации(Организация) Экспорт
	
	НаборЗаписей = РегистрыСведений.АктивностьСеансовПользователей.СоздатьНаборЗаписей();
	Если ЗначениеЗаполнено(Организация) Тогда
		НаборЗаписей.Отбор.Организация.Установить(Организация);
	КонецЕсли;
	НаборЗаписей.Записать();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураПоследнейАктивности()
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("Пользователь", Справочники.Пользователи.ПустаяСсылка());
	Результат.Вставить("ДатаПоследнейАктивности", Дата(1, 1, 1));
	Результат.Вставить("ДатаПоследнейАктивностиУниверсальная", Дата(1, 1, 1));
	Результат.Вставить("Документ", Неопределено);
	Результат.Вставить("ИдентификаторЗаданияДляОстановки", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
