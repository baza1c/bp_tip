#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустой() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Очистку регистра ПроверенныеДокументы должны сделать в любом случае. Она продолжится, даже если форма закроется.
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗаписиЗавершение", ЭтотОбъект);
	ПроведениеКлиент.НачатьОчисткуРегистраПроверенныхДокументов(
		ОповещениеОЗавершении, НастройкиУчетаСтраховыхВзносовИП.Период, НастройкиУчетаСтраховыхВзносовИП.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыОповещения = Новый Структура("Организация, Период", НастройкиУчетаСтраховыхВзносовИП.Организация, НастройкиУчетаСтраховыхВзносовИП.Период);
	Оповестить("Запись_НастройкиУчетаСтраховыхВзносовИП", ПараметрыОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПрименяетсяСПриИзменении(Элемент)
	
	НастройкиУчетаСтраховыхВзносовИП.Период = Дата(ПрименяетсяС, 1, 1);
	Если УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(НастройкиУчетаСтраховыхВзносовИП.Период) Тогда
		ЗаполнитьСписокСтраховыхСуммСамозанятых();
	Иначе
		СтраховойСлучайВСвязиСМатеринством = ЭтоИП И Не ИндивидуальныйПредпринимательМужчина(
			НастройкиУчетаСтраховыхВзносовИП.Организация);
		ПриИзмененииСтраховогоСлучаяВСвязиСМатеринством();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УплачиватьДобровольныеВзносыВФССПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховойСлучайВСвязиСМатеринствомПриИзменении(Элемент)
	
	ПриИзмененииСтраховогоСлучаяВСвязиСМатеринством();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ПрименяетсяС = Год(НастройкиУчетаСтраховыхВзносовИП.Период);
	
	ЗаполнитьСписокСтраховыхСуммСамозанятых();
	ЭтоИП = Справочники.Организации.ЭтоИП(НастройкиУчетаСтраховыхВзносовИП.Организация);
	
	Если Не НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС Тогда
		СтраховойСлучайВСвязиСМатеринством = ЭтоИП И Не ИндивидуальныйПредпринимательМужчина(
			НастройкиУчетаСтраховыхВзносовИП.Организация);
	Иначе
		СтраховойСлучайВСвязиСМатеринством = ЭтоИП И Не УчетСтраховыхВзносовИПКлиентСервер.ЭтоСтраховойСлучайНаУплатуБольничного(
			НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай);
	КонецЕсли;
	
	ПрименяетсяНПД = УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(
		НастройкиУчетаСтраховыхВзносовИП.Организация,
		НастройкиУчетаСтраховыхВзносовИП.Период);
	
	Элементы.УплачиватьДобровольныеВзносыВФСС.Подсказка =
		УчетСтраховыхВзносовИПКлиентСервер.ТекстПодсказкиДобровольныеВзносыПредпринимателя(ЭтоИП, ПрименяетсяНПД);
	
	Элементы.УплачиватьДобровольныеВзносыВФСС.Заголовок = ЗаголовокДобровольныеВзносыПредпринимателя(ЭтоИП);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Настройки = Форма.НастройкиУчетаСтраховыхВзносовИП;
	
	Элементы.СтраховойСлучайВСвязиСМатеринством.Доступность = Настройки.УплачиватьДобровольныеВзносыВФСС;
	
	ВыбиратьСтраховойСлучай = УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(Настройки.Период);
	Элементы.СтраховойСлучайВСвязиСМатеринством.Видимость = Форма.ЭтоИП И ВыбиратьСтраховойСлучай;
	
	Если Не Форма.СтраховойСлучайВСвязиСМатеринством Тогда
		Элементы.ГруппаРайонныйКоэффициент.Видимость = Не ВыбиратьСтраховойСлучай;
		Элементы.СтраховаяСумма.Видимость = ВыбиратьСтраховойСлучай;
		Элементы.СтраховаяСумма.Доступность = Настройки.УплачиватьДобровольныеВзносыВФСС;
	Иначе
		Элементы.ГруппаРайонныйКоэффициент.Видимость = Истина;
		Элементы.СтраховаяСумма.Видимость = Ложь;
		Элементы.ГруппаРайонныйКоэффициент.Доступность = Настройки.УплачиватьДобровольныеВзносыВФСС
			И УчетСтраховыхВзносовИПКлиентСервер.ПрименяетсяРайонныйКоэффициентСтраховыхВзносовФСС(Настройки.Период);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаголовокДобровольныеВзносыПредпринимателя(ЭтоИП)
	
	Если ЭтоИП Тогда
		Возврат СтрШаблон(НСтр("ru = 'Предприниматель уплачивает взносы на социальное страхование (за себя)'"));
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru = 'Самозанятый уплачивает взносы на социальное страхование'"));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокСтраховыхСуммСамозанятых()
	
	Элементы.СтраховаяСумма.СписокВыбора.Очистить();
	СтраховыеСуммы = УчетСтраховыхВзносовИП.СтраховыеСуммыНаСоциальноеСтрахованиеСамозанятых(
		НастройкиУчетаСтраховыхВзносовИП.Период);
	
	Для Каждого ТекущаяСумма Из СтраховыеСуммы Цикл
		Элементы.СтраховаяСумма.СписокВыбора.Добавить(ТекущаяСумма.Значение, ТекущаяСумма.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСтраховогоСлучаяВСвязиСМатеринством()
	
	НастройкиУчетаСтраховыхВзносовИП.СтраховаяСумма = ПредопределенноеЗначение("Перечисление.ПособияНаСоциальноеСтрахованиеСамозанятых.ПоУмолчанию");
	ВыбиратьСтраховойСлучай = УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(
		НастройкиУчетаСтраховыхВзносовИП.Период);
	
	Если Не СтраховойСлучайВСвязиСМатеринством И ВыбиратьСтраховойСлучай Тогда
		НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай =
			ПредопределенноеЗначение("Перечисление.СлучаиСоциальногоСтрахованияСамозанятых.ВременнаяНетрудоспособность");
	Иначе
		НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай = УчетСтраховыхВзносовИПКлиентСервер.СтраховойСлучайПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИндивидуальныйПредпринимательМужчина(Организация)
	
	Возврат Справочники.Организации.ИндивидуальныйПредпринимательМужчина(Организация);
	
КонецФункции

#КонецОбласти