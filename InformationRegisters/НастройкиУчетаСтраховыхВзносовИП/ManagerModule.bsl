#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает признак того, что в ИБ есть плательщики добровольных взносов в ФСС
//
// Возвращаемые значения:
//  Булево - признак уплаты добровольных взносов в ФСС
//
Функция УплачиваютсяДобровольныеВзносыВФСС() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК УплачиваютсяДобровольныеВзносыВФСС
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносовИП КАК НастройкиУчетаСтраховыхВзносовИП
	|ГДЕ
	|	НЕ НастройкиУчетаСтраховыхВзносовИП.Организация.ПометкаУдаления
	|	И НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Процедура устанавливает значения регистра по умолчанию
//
// Параметры:
//   Запись           - РегистрСведенийЗапись - запись регистра
//   ДанныеЗаполнения - Структура - где ключ - имя ресурса
//
Процедура УстановкаНастроекПоУмолчанию(Запись, ДанныеЗаполнения) Экспорт
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Период",
		НачалоГода(ТекущаяДатаСеанса()));
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Организация",
		БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
		
	ЭтоФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Организация, "ЮридическоеФизическоеЛицо")
		= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		
	Если НЕ ЭтоФизЛицо Тогда
			Запись.Организация = Справочники.Организации.ПустаяСсылка();
		Возврат;
	КонецЕсли;
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"УплачиватьДобровольныеВзносыВФСС",
		Ложь);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"РайонныйКоэффициент",
		РайонныйКоэффициентПоУмолчанию());
	
	ИспользоватьРайонныйКоэффициент = Запись.УплачиватьДобровольныеВзносыВФСС
		И УчетСтраховыхВзносовИПКлиентСервер.ПрименяетсяРайонныйКоэффициентСтраховыхВзносовФСС(Запись.Период);
	Если Не ИспользоватьРайонныйКоэффициент Тогда 
		Запись.РайонныйКоэффициент = РайонныйКоэффициентПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

// Возвращает районный коэффициент, применяемый при расчете страховых взносов ФСС
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация-ИП
//  Период - Дата - страховой период
//
// Возвращаемое значение:
//  Число - районный коэффициент
Функция РайонныйКоэффициентСтраховыхВзносовФСС(Организация, Период) Экспорт 
	
	РайонныйКоэффициент = РайонныйКоэффициентПоУмолчанию();
	
	Если Не УчетСтраховыхВзносовИПКлиентСервер.ПрименяетсяРайонныйКоэффициентСтраховыхВзносовФСС(Период) Тогда 
		Возврат РайонныйКоэффициент;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиУчетаСтраховыхВзносовИП.РайонныйКоэффициент КАК РайонныйКоэффициент
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносовИП.СрезПоследних(&Период, Организация = &Организация) КАК НастройкиУчетаСтраховыхВзносовИП";
	
	Запрос.УстановитьПараметр("Период", НачалоГода(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий()
		И ЗначениеЗаполнено(Выборка.РайонныйКоэффициент) Тогда 
		РайонныйКоэффициент = Выборка.РайонныйКоэффициент;
	КонецЕсли;
	
	Возврат РайонныйКоэффициент;
	
КонецФункции

// Возвращает районный коэффициент, применяемый по умолчанию
//
// Возвращаемое значение:
//  Число - районный коэффициент
Функция РайонныйКоэффициентПоУмолчанию() Экспорт
	
	Возврат 1;
	
КонецФункции

// Возвращает настройки, применяемые при расчете страховых взносов на социальное страхование
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация ИП, самозанятый
//  Период - Дата - страховой период
//
// Возвращаемое значение:
//  Структура - см. НовыеНастройкиВзносовНаСоциальноеСтрахование()
//
Функция НастройкиВзносовНаСоциальноеСтрахование(Организация, Период) Экспорт
	
	Результат = НовыеНастройкиВзносовНаСоциальноеСтрахование();
	
	Если Не УчетСтраховыхВзносовИПКлиентСервер.ПрименяетсяРайонныйКоэффициентСтраховыхВзносовФСС(Период) Тогда 
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиУчетаСтраховыхВзносовИП.РайонныйКоэффициент КАК РайонныйКоэффициент,
	|	НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай КАК СтраховойСлучай,
	|	НастройкиУчетаСтраховыхВзносовИП.СтраховаяСумма КАК СтраховаяСумма,
	|	НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС КАК УплачиватьДобровольныеВзносыВФСС
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносовИП.СрезПоследних(&Период, Организация = &Организация) КАК НастройкиУчетаСтраховыхВзносовИП");
	
	Запрос.УстановитьПараметр("Период", НачалоГода(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Организация.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	| ЗначениеРазрешено(ЭтотСписок.Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиОбновления

// Обработчик обновления БП 3.0.109.54
//
// В существующих записях регистра там, где не заполнен районный коэффициент,
// устанавливает значение районного коэффициента по умолчанию
//
Процедура ЗаполнитьРайонныйКоэффициентПоУмолчанию() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиУчетаСтраховыхВзносовИП.Период КАК Период,
	|	НастройкиУчетаСтраховыхВзносовИП.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносовИП КАК НастройкиУчетаСтраховыхВзносовИП
	|ГДЕ
	|	НастройкиУчетаСтраховыхВзносовИП.РайонныйКоэффициент = 0";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НаборЗаписей = РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Прочитать();
		Для Каждого Запись Из НаборЗаписей Цикл 
			Если Не ЗначениеЗаполнено(Запись.РайонныйКоэффициент) Тогда 
				Запись.РайонныйКоэффициент = РайонныйКоэффициентПоУмолчанию();
			КонецЕсли;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БП 3.0.187.х
// В существующих записях регистра там, где не заполнен страховой случай,
// устанавливает значение по умолчанию
//
// Параметры:
//   Параметры - Структура - Параметры обработчика обновления.
//
Процедура ЗаполнитьСтраховойСлучайПоУмолчанию(Параметры) Экспорт
	
	// Постановление Правительства № ММ-П45-37720 от 09.10.2025
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиУчетаСтраховыхВзносовИП.Период КАК Период,
	|	НастройкиУчетаСтраховыхВзносовИП.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА НастройкиУчетаСтраховыхВзносовИП.Организация.СтатусФизическогоЛица = ЗНАЧЕНИЕ(Перечисление.СтатусыФизическихЛиц.Самозанятый)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СлучаиСоциальногоСтрахованияСамозанятых.ВременнаяНетрудоспособность)
	|		ИНАЧЕ &ЗначениеПоУмолчанию
	|	КОНЕЦ КАК СтраховойСлучай,
	|	ВЫБОР
	|		КОГДА НастройкиУчетаСтраховыхВзносовИП.Организация.СтатусФизическогоЛица = ЗНАЧЕНИЕ(Перечисление.СтатусыФизическихЛиц.Самозанятый)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПособияНаСоциальноеСтрахованиеСамозанятых.ПоУмолчанию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПособияНаСоциальноеСтрахованиеСамозанятых.ПустаяСсылка)
	|	КОНЕЦ КАК СтраховаяСумма
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносовИП КАК НастройкиУчетаСтраховыхВзносовИП
	|ГДЕ
	|	НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай = ЗНАЧЕНИЕ(Перечисление.СлучаиСоциальногоСтрахованияСамозанятых.ПустаяСсылка)
	|	И НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Период
	|ИТОГИ ПО
	|	Организация");
	
	Запрос.УстановитьПараметр("ЗначениеПоУмолчанию", УчетСтраховыхВзносовИПКлиентСервер.СтраховойСлучайПоУмолчанию());
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	ИмяПроцедуры = "РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП.ЗаполнитьСтраховойСлучайПоУмолчанию()";
	ВыборкаОрганизации = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Попытка
		Пока ВыборкаОрганизации.Следующий() Цикл
			
			НачатьТранзакцию();
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиУчетаСтраховыхВзносовИП");
			ЭлементБлокировки.УстановитьЗначение("Организация", ВыборкаОрганизации.Организация);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Выборка = ВыборкаОрганизации.Выбрать();
			Пока Выборка.Следующий() Цикл
				НаборЗаписей = СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
				НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
				НаборЗаписей.Прочитать();
				Для Каждого Запись Из НаборЗаписей Цикл
					Если Не ЗначениеЗаполнено(Запись.СтраховойСлучай) Тогда
						Запись.СтраховойСлучай = Выборка.СтраховойСлучай;
						Запись.СтраховаяСумма = Выборка.СтраховаяСумма;
					КонецЕсли;
				КонецЦикла;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		КонецЦикла;
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОтменитьТранзакцию();
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Уровень = УровеньЖурналаРегистрации.Предупреждение;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось заполнить страховые случаи ИП, возникла ошибка: %2'"),
			ИмяПроцедуры,
			ТекстОшибки);
	Иначе
		Параметры.ОбработкаЗавершена = Истина;
		Уровень = УровеньЖурналаРегистрации.Информация;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедура %1 заполнила страховые случаи ИП по умолчанию: обработано %2 записей'"),
			ИмяПроцедуры, Выборка.Количество());
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
		Уровень,
		Метаданные.РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП, ,
		ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура Подключаемый_ПередЗаписью(Запись) Экспорт
	
	Если Не ЗначениеЗаполнено(Запись.РайонныйКоэффициент)
		Или Не Запись.УплачиватьДобровольныеВзносыВФСС 
		Или Не УчетСтраховыхВзносовИПКлиентСервер.ПрименяетсяРайонныйКоэффициентСтраховыхВзносовФСС(Запись.Период) Тогда 
		Запись.РайонныйКоэффициент = РайонныйКоэффициентПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура Подключаемый_ПриЗаписи(Запись) Экспорт
	
	Если Запись.УплачиватьДобровольныеВзносыВФСС Тогда
		Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьПоставляемыеЭлементы();
	КонецЕсли;
	
КонецПроцедуры

Функция НовыеНастройкиВзносовНаСоциальноеСтрахование()
	
	Результат = Новый Структура;
	Результат.Вставить("РайонныйКоэффициент", РайонныйКоэффициентПоУмолчанию());
	Результат.Вставить("СтраховойСлучай", УчетСтраховыхВзносовИПКлиентСервер.СтраховойСлучайПоУмолчанию());
	Результат.Вставить("СтраховаяСумма", Перечисления.ПособияНаСоциальноеСтрахованиеСамозанятых.ПустаяСсылка());
	Результат.Вставить("УплачиватьДобровольныеВзносыВФСС", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли