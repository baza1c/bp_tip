#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения = Неопределено ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Для каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП.УстановкаНастроекПоУмолчанию(Запись, ?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДоступныеОрганизаций = Выгрузить(, "Организация, Период");
	ДоступныеОрганизаций.Свернуть("Организация, Период");
	СамозанятыеНаПроверку = ДоступныеОрганизаций.СкопироватьКолонки();
	Для Каждого ТекущаяСтрока Из ДоступныеОрганизаций Цикл
		Если УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(ТекущаяСтрока.Организация, ТекущаяСтрока.Период) Тогда
			ЗаполнитьЗначенияСвойств(СамозанятыеНаПроверку.Добавить(), ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	СтраховойСлучайПоУмолчанию = УчетСтраховыхВзносовИПКлиентСервер.СтраховойСлучайПоУмолчанию();
	ОтборСамозанятых = Новый Структура("Организация, Период");
	Для Каждого ТекущаяЗапись Из ЭтотОбъект Цикл
		
		Если Не ТекущаяЗапись.УплачиватьДобровольныеВзносыВФСС Тогда
			ТекущаяЗапись.СтраховаяСумма = Неопределено;
			ТекущаяЗапись.СтраховойСлучай = Неопределено;
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ОтборСамозанятых, ТекущаяЗапись);
		ЭтоСамозанятый = СамозанятыеНаПроверку.НайтиСтроки(ОтборСамозанятых).Количество() > 0;
		
		Если Не ЭтоСамозанятый Или Не ЗначениеЗаполнено(ТекущаяЗапись.СтраховойСлучай) Тогда
			ТекущаяЗапись.СтраховойСлучай = СтраховойСлучайПоУмолчанию;
		КонецЕсли;
		
		Если ТекущаяЗапись.СтраховойСлучай = СтраховойСлучайПоУмолчанию Тогда
			ТекущаяЗапись.СтраховаяСумма = Неопределено;
		ИначеЕсли ЭтоСамозанятый Тогда
			ТекущаяЗапись.РайонныйКоэффициент = РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП.РайонныйКоэффициентПоУмолчанию();
			Если Не УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(ТекущаяЗапись.Период) Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В %1 г. не используется страхование по случаю ""%2"".'"),
					Формат(ТекущаяЗапись.Период, "ДФ=yyyy"),
					ТекущаяЗапись.СтраховойСлучай);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СтраховойСлучай", , Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	НастройкиУчета.ПередЗаписью(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПриЗаписи(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли