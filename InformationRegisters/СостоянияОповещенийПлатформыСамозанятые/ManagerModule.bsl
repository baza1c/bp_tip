#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Переопределяет ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
// 
// Параметры:
//   Ограничение - Структура
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает состояние оповещения
// 
// Параметры:
//  Оповещение - СправочникСсылка.ОповещенияПлатформыСамозанятые - Оповещение
//  Организация - СправочникСсылка.Организации - Организация
// 
// Возвращаемое значение:
//  - Структура:
//    * Статус - ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//    * ДатаИзменения - Дата
//  - Неопределено
//
Функция ПрочитатьСтатус(Оповещение, Организация) Экспорт
	
	Если Не ЗначениеЗаполнено(Оповещение) Тогда
		//@skip-check constructor-function-return-section
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ПрочитатьСтатусы(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Оповещение), Организация);
	
	//@skip-check constructor-function-return-section
	Возврат Результат[Оповещение];
	
КонецФункции

// Возвращает состояние нескольких оповещений
// 
// Параметры:
//  СписокОповещений - Массив из СправочникСсылка.ОповещенияПлатформыСамозанятые - Список оповещений
//  Организация - СправочникСсылка.Организации - Организация
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//    Ключ - СправочникСсылка.ОповещенияПлатформыСамозанятые
//    Значение - Структура:
//      * Статус - ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//      * ДатаИзменения - Дата
//
Функция ПрочитатьСтатусы(СписокОповещений, Организация) Экспорт
	
	Результат = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(СписокОповещений) Или Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокОповещений", СписокОповещений);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СостоянияОповещенийПлатформыСамозанятые.Оповещение КАК Оповещение,
		|	СостоянияОповещенийПлатформыСамозанятые.Статус КАК Статус,
		|	СостоянияОповещенийПлатформыСамозанятые.ДатаИзменения КАК ДатаИзменения
		|ИЗ
		|	РегистрСведений.СостоянияОповещенийПлатформыСамозанятые КАК СостоянияОповещенийПлатформыСамозанятые
		|ГДЕ
		|	СостоянияОповещенийПлатформыСамозанятые.Организация = &Организация
		|	И СостоянияОповещенийПлатформыСамозанятые.Оповещение В (&СписокОповещений)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(
			Выборка.Оповещение,
			Новый Структура("Статус, ДатаИзменения", Выборка.Статус, Выборка.ДатаИзменения));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Записывает новый статус оповещения.
// 
// Параметры:
//  ДанныеЗаполнения - Структура:
//   * Организация - СправочникСсылка.Организации
//   * Оповещение - СправочникСсылка.ОповещенияПлатформыСамозанятые
//   * Статус - ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//   * ДатаИзменения - Дата - Универсальная дата и время
// 
// Возвращаемое значение:
//  Булево - Истина, если функция выполнена успешно
//
Функция ЗаписатьСтатус(ДанныеЗаполнения) Экспорт
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Организация");
	Оповещение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Оповещение");
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Оповещение) Тогда
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Заполнить(ДанныеЗаполнения);
		МенеджерЗаписи.Записать(Истина);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли
	
КонецФункции

// Возвращает оповещения, которые имеют определенные статусы.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Статусы - Массив из ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//  КоличествоПолучаемыхЗаписей - Число, Неопределено - Максимальное количество получаемых записей
//  НаправлениеПоВозрастанию - Булево
// 
// Возвращаемое значение:
//  - Массив из СправочникСсылка.ОповещенияПлатформыСамозанятые
//  - Неопределено
//
Функция НайтиОповещенияПоСтатусам(Организация, Статусы, КоличествоПолучаемыхЗаписей = Неопределено, НаправлениеПоВозрастанию = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(Статусы) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СостоянияОповещенийПлатформыСамозанятые.Оповещение КАК Оповещение,
		|	СостоянияОповещенийПлатформыСамозанятые.Оповещение.ДатаСоздания КАК ДатаСоздания
		|ИЗ
		|	РегистрСведений.СостоянияОповещенийПлатформыСамозанятые КАК СостоянияОповещенийПлатформыСамозанятые
		|ГДЕ
		|	СостоянияОповещенийПлатформыСамозанятые.Организация = &Организация
		|	И СостоянияОповещенийПлатформыСамозанятые.Статус В (&Статусы)
		|	И НЕ СостоянияОповещенийПлатформыСамозанятые.Оповещение.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаСоздания";
		
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Если КоличествоПолучаемыхЗаписей <> Неопределено Тогда
		ОператорВыбрать = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
		ОператорВыбрать.КоличествоПолучаемыхЗаписей = КоличествоПолучаемыхЗаписей;
	КонецЕсли;
	
	Если Не НаправлениеПоВозрастанию Тогда
		ВыражениеПорядка = СхемаЗапроса.ПакетЗапросов[0].Порядок[0];
		ВыражениеПорядка.Направление = НаправлениеПорядкаСхемыЗапроса.ПоУбыванию;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Статусы", Статусы);
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Оповещение");
	
	Возврат Результат;
	
КонецФункции

Функция ДатаИзменения(Организация) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СостоянияОповещенийПлатформыСамозанятые.ДатаИзменения КАК ДатаИзменения
		|ИЗ
		|	РегистрСведений.СостоянияОповещенийПлатформыСамозанятые КАК СостоянияОповещенийПлатформыСамозанятые
		|ГДЕ
		|	СостоянияОповещенийПлатформыСамозанятые.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаИзменения УБЫВ";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДатаИзменения;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ИзмененныеПозже(Организация, ДатаИзменения) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаИзменения", ДатаИзменения);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СостоянияОповещенийПлатформыСамозанятые.Оповещение КАК Оповещение,
		|	СостоянияОповещенийПлатформыСамозанятые.Оповещение.ДатаСоздания КАК ДатаСоздания,
		|	СостоянияОповещенийПлатформыСамозанятые.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.СостоянияОповещенийПлатформыСамозанятые КАК СостоянияОповещенийПлатформыСамозанятые
		|ГДЕ
		|	СостоянияОповещенийПлатформыСамозанятые.Организация = &Организация
		|	И СостоянияОповещенийПлатформыСамозанятые.ДатаИзменения >= &ДатаИзменения
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаСоздания";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
