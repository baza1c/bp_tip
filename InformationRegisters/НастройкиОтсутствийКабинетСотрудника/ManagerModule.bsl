#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция НастройкиДляОбновления() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Настройки.ПричинаОтсутствия КАК ПричинаОтсутствия,
	|	Настройки.ОформлятьЗаднимЧислом КАК ОформлятьЗаднимЧислом,
	|	Настройки.ТребуетсяОбновление КАК ТребуетсяОбновление,
	|	Настройки.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки КАК КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки,
	|	Настройки.ИспользоватьВнутрисменные КАК ИспользоватьВнутрисменные
	|ИЗ
	|	РегистрСведений.НастройкиОтсутствийКабинетСотрудника КАК Настройки
	|ГДЕ
	|	Настройки.ТребуетсяОбновление";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция Настройки(ПричиныОтсутствий) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПричиныОтсутствий", ПричиныОтсутствий);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПричиныОтсутствий.Ссылка КАК ПричинаОтсутствия,
	|	ЕСТЬNULL(Настройки.ОформлятьЗаднимЧислом, ЛОЖЬ) КАК ОформлятьЗаднимЧислом,
	|	ЕСТЬNULL(Настройки.ТребуетсяОбновление, ЛОЖЬ) КАК ТребуетсяОбновление,
	|	ЕСТЬNULL(Настройки.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки, 0) КАК КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки,
	|	ЕСТЬNULL(Настройки.ИспользоватьВнутрисменные, ЛОЖЬ) КАК ИспользоватьВнутрисменные
	|ИЗ
	|	Перечисление.ПричиныОтсутствийЗаявокКабинетСотрудника КАК ПричиныОтсутствий
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтсутствийКабинетСотрудника КАК Настройки
	|		ПО ПричиныОтсутствий.Ссылка = Настройки.ПричинаОтсутствия
	|ГДЕ
	|	ПричиныОтсутствий.Ссылка В(&ПричиныОтсутствий)";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ДоступныеПричиныОтсутствий() Экспорт

	ПричиныОтсутствий = Новый Массив;
	ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск);
	ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет);
	ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск);
	ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка);
	ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отгул);
	ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела);
	Если ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("3.2") Тогда
		ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ИсполнениеГособязанностей);
		ПричиныОтсутствий.Добавить(Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами);
	КонецЕсли;
	
	Возврат ПричиныОтсутствий;

КонецФункции

Процедура УстановитьТребуетсяОбновитьНастройки(ПричинаОтсутствия = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ПричинаОтсутствия) Тогда
		
		НаборЗаписей = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПричинаОтсутствия.Установить(ПричинаОтсутствия);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Если Не НаборЗаписей[0].ТребуетсяОбновление Тогда
				НаборЗаписей[0].ТребуетсяОбновление = Истина;
				Записывать = Истина;
			КонецЕсли;
			НаборЗаписей.Записать();
		КонецЕсли;
		
	Иначе
		
		НаборЗаписей = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		Для каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьНабора.ТребуетсяОбновление = Истина;
		КонецЦикла;
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Записать();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли