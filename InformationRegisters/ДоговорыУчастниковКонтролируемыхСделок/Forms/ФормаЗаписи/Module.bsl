#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	СписокКодовНаименованийСделок = КонтролируемыеСделки.ПолучитьСписокКодовНаименованийСделки();
	Элементы.КодНаименованияСделки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовНаименованийСделок Цикл
		НовыйКод = Элементы.КодНаименованияСделки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	СписокКодовПоставки = КонтролируемыеСделки.ПолучитьКодыУсловийПоставки();
	Элементы.КодУсловийПоставки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовПоставки Цикл
		НовыйКод = Элементы.КодУсловийПоставки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	ДоступностьКлючевыхЗаписей = Истина;
	Если Параметры.Свойство("ДоступностьКлючевыхЗаписей") Тогда
		ДоступностьКлючевыхЗаписей = Параметры.ДоступностьКлючевыхЗаписей;
	КонецЕсли;
	
	Элементы.Организация.ТолькоПросмотр 		= НЕ ДоступностьКлючевыхЗаписей;
	Элементы.ДоговорКонтрагента.ТолькоПросмотр 	= НЕ ДоступностьКлючевыхЗаписей;
	Элементы.Контрагент.ТолькоПросмотр 			= НЕ ДоступностьКлючевыхЗаписей;
	
	УстановитьОтметкуЗаполненного(ЭтотОбъект);
	
	КонтролируемыеСделки.ЗаполнитьСписокГоловныхОрганизаций(Элементы.Организация.СписокВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовШапкиФормы

&НаКлиенте
Процедура СделкаНеЯвляетсяКонтролируемойПоП4ПриИзменении(Элемент)
	
	УстановитьОтметкуЗаполненного(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтметкуЗаполненного(Форма)
	
	Элементы = Форма.Элементы;
	Запись = Форма.Запись;
	
	НужноОтмечатьЗаполненость = Не Форма.ДоступностьКлючевыхЗаписей
		И Не Запись.СделкаНеЯвляетсяКонтролируемойПоП4;
	
	
	Элементы.КодНаименованияСделки.АвтоОтметкаНезаполненного = НужноОтмечатьЗаполненость;
	Элементы.ПризнакОпределенияЦеныСделки.АвтоОтметкаНезаполненного = НужноОтмечатьЗаполненость;
		
	Элементы.КодНаименованияСделки.ОтметкаНезаполненного = НужноОтмечатьЗаполненость
		И Не ЗначениеЗаполнено(Запись.КодНаименованияСделки);
	Элементы.ПризнакОпределенияЦеныСделки.ОтметкаНезаполненного = НужноОтмечатьЗаполненость
		И Не ЗначениеЗаполнено(Запись.СпособОпределенияЦеныСделки);
		
	
КонецПроцедуры

#КонецОбласти