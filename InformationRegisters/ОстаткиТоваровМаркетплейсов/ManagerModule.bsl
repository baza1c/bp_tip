#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Подготавливает текст запроса по остаткам на складах
// Параметры
//      ПараметрыЗаполнения - Новый Структура
Функция ПодготовитьЗапросПоОстаткам(ПараметрыЗаполнения)
	
	// Определим настройки аналитического учета.
	ВестиСкладскойУчетБУ = БухгалтерскийУчет.ВедетсяУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары);
	ВестиСуммовойУчетПоСкладамБУ = ВестиСкладскойУчетБУ
		И БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары);
	
	ДатаОтменыСпецодежды = УчетМатериаловВЭксплуатации.ДатаОтменыСпецодежды();
	
	МассивИсклСчетов = Новый Массив();
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ); // 41.12
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);                            // 42
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ГТД);                                        // ГТД
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.МатериальныеЦенностиВЭксплуатации);      // МЦ
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаВЭксплуатации);   // 10.11
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);   // 45
	
	МассивИсклСчетов = БухгалтерскийУчет.СформироватьМассивСубсчетов(МассивИсклСчетов);
	
	Склад = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗаполнения, "Склад", Неопределено);
	ОтбиратьПоСкладу = ВестиСкладскойУчетБУ И Склад <> Неопределено;
	
	ДатаЗапроса = ТекущаяДатаСеанса();
	
	// Определяем условия отборов по счетам и аналитикам.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментАнализаОстатков",  Новый Граница(ДатаЗапроса, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Склад",		            Склад);
	Запрос.УстановитьПараметр("Организация",	        ПараметрыЗаполнения.Организация);
	Запрос.УстановитьПараметр("ИсклСчета",	 		    МассивИсклСчетов);
	Запрос.УстановитьПараметр("ВидМаркетплейса",        ПараметрыЗаполнения.ВидМаркетплейса);
	
	// Если ведется складской учет, но отбор по складу не установлен, 
	// то выведем остатки с учетом переданных покупателям (в том числе маркетплейсам)
	МассивДополнительныхСчетов = Новый Массив();
	Если ВестиСкладскойУчетБУ И Склад = Неопределено Тогда
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ПокупныеТоварыОтгруженные);
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная);
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеТоварыОтгруженные);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивДополнительныхСчетов", МассивДополнительныхСчетов);
	Запрос.УстановитьПараметр("СубконтоНомеклатура", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Определяем наборы субконто.
	ПорядокСубконтоКоличество = Новый Массив();
	ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Если ОтбиратьПоСкладу Тогда
		ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПорядокСубконтоКоличество", ПорядокСубконтоКоличество);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОстаткиТоваров.ИдентификаторТовара КАК ИдентификаторТовара,
	|	ОстаткиТоваров.Остаток КАК ОстатокМаркетплейс,
	|	ОстаткиТоваров.Штрихкод КАК Штрихкод
	|ПОМЕСТИТЬ ВТ_ОстаткиТоваровМаркетплейс
	|ИЗ
	|	&ОстаткиТоваров КАК ОстаткиТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиКоличество.Счет КАК Счет,
	|	ХозрасчетныйОстаткиКоличество.Субконто1 КАК Субконто1,
	|	СУММА(ХозрасчетныйОстаткиКоличество.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиКоличество
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&МоментАнализаОстатков,
	|			НЕ Счет В (&ИсклСчета),
	|			&ПорядокСубконтоКоличество,
	|			Организация = &Организация
	|				И &ТекстУсловияКоличества) КАК ХозрасчетныйОстаткиКоличество
	|ГДЕ
	|	ХозрасчетныйОстаткиКоличество.КоличествоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиКоличество.Счет,
	|	ХозрасчетныйОстаткиКоличество.Субконто1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиКоличество.Счет,
	|	ХозрасчетныйОстаткиКоличество.Субконто1,
	|	СУММА(ХозрасчетныйОстаткиКоличество.КоличествоОстаток)
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментАнализаОстатков, Счет В (&МассивДополнительныхСчетов), &СубконтоНомеклатура, Организация = &Организация) КАК ХозрасчетныйОстаткиКоличество
	|ГДЕ
	|	ХозрасчетныйОстаткиКоличество.КоличествоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиКоличество.Счет,
	|	ХозрасчетныйОстаткиКоличество.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	НоменклатураМаркетплейсов.Номенклатура КАК Номенклатура,
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиКоличество.КоличествоОстаток, 0) КАК Количество,
	|	0 КАК КоличествоМаркетплейс
	|ПОМЕСТИТЬ ВТ_ТаблицаОстатки
	|ИЗ
	|	РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиКоличество КАК ХозрасчетныйОстаткиКоличество
	|		ПО НоменклатураМаркетплейсов.Номенклатура = ХозрасчетныйОстаткиКоличество.Субконто1
	|ГДЕ
	|	НоменклатураМаркетплейсов.Маркетплейс = &ВидМаркетплейса
	|	И НоменклатураМаркетплейсов.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара,
	|	НоменклатураМаркетплейсов.Номенклатура,
	|	НоменклатураМаркетплейсов.Штрихкод,
	|	0,
	|	ЕСТЬNULL(ВТ_ОстаткиТоваровМаркетплейс.ОстатокМаркетплейс, 0)
	|ИЗ
	|	РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиТоваровМаркетплейс КАК ВТ_ОстаткиТоваровМаркетплейс
	|		ПО НоменклатураМаркетплейсов.ИдентификаторТовара = ВТ_ОстаткиТоваровМаркетплейс.ИдентификаторТовара
	|			И НоменклатураМаркетплейсов.Штрихкод = ВТ_ОстаткиТоваровМаркетплейс.Штрихкод
	|ГДЕ
	|	НоменклатураМаркетплейсов.Маркетплейс = &ВидМаркетплейса
	|	И НоменклатураМаркетплейсов.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаОстатки.ИдентификаторТовара КАК ИдентификаторТовара,
	|	ВТ_ТаблицаОстатки.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаОстатки.Штрихкод КАК Штрихкод,
	|	СУММА(ВТ_ТаблицаОстатки.Количество) КАК Количество,
	|	СУММА(ВТ_ТаблицаОстатки.КоличествоМаркетплейс) КАК КоличествоМаркетплейс
	|ИЗ
	|	ВТ_ТаблицаОстатки КАК ВТ_ТаблицаОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаОстатки.Штрихкод,
	|	ВТ_ТаблицаОстатки.ИдентификаторТовара,
	|	ВТ_ТаблицаОстатки.Номенклатура";
	
	ТекстУсловияКоличества = ?(ОтбиратьПоСкладу, "И Субконто2 В (&Склад)", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстУсловияКоличества", ТекстУсловияКоличества);
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос;
КонецФункции

Функция ОстаткиДляВыгрузкиВМаркетплейс(ПараметрыЗаполнения) Экспорт
	ОстаткиТоваровМаркетплейс = ОбменСМаркетплейс.ПолучитьОстаткиТоваров(ПараметрыЗаполнения.НастройкаИнтеграции);
	
	ЗапросОстатки = ПодготовитьЗапросПоОстаткам(ПараметрыЗаполнения);
	ЗапросОстатки.УстановитьПараметр("ОстаткиТоваров", ОстаткиТоваровМаркетплейс);
	
	Возврат ЗапросОстатки.Выполнить().Выгрузить();
КонецФункции    

#КонецЕсли