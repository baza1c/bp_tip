#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем СообщениеОбОшибке;
	НастройкаИнтеграции = Параметры.НастройкаИнтеграции;
	Если НастройкаИнтеграции.Пустая() Тогда
		ВызватьИсключение НСтр("ru = 'Форма не предназначена для непосредственного использования'");
	КонецЕсли;
	
	РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаИнтеграции, "ИдентификаторКабинета,ИдентификаторСклада,НаименованиеСклада");
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыСклада);
	
	СписокСкладовМаркетплейс = ОбменСМаркетплейс.СписокСкладовМаркетплейс(НастройкаИнтеграции);
	Если СписокСкладовМаркетплейс = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	ЗагружатьКаталог = Истина;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСписок(Элементы.СкладМаркетплейса.СписокВыбора, СписокСкладовМаркетплейс);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура OK(Команда)
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ЗагружатьКаталог",      ЗагружатьКаталог);
	
	РеквизитыСклада = Новый Структура;
	РеквизитыСклада.Вставить("ИдентификаторСклада",   ИдентификаторСклада);
	РеквизитыСклада.Вставить("НаименованиеСклада",    НаименованиеСклада);
	РеквизитыСклада.Вставить("ИдентификаторКабинета", ИдентификаторКабинета);
	
	ПараметрыОповещения.Вставить("РеквизитыСклада", РеквизитыСклада);
	
	Закрыть(ПараметрыОповещения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовШапкиФормы

&НаКлиенте
Процедура СкладМаркетплейсаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ЗначениеСклада = Элементы.СкладМаркетплейса.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСклада = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеСклада.Значение) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначениеСклада.Значение);
	Иначе
		ИдентификаторСклада = ЗначениеСклада.Значение;
	КонецЕсли;
	НаименованиеСклада = ЗначениеСклада.Представление;
КонецПроцедуры

#КонецОбласти
