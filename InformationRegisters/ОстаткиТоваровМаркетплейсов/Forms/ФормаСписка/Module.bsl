
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВидМаркетплейса = Параметры.ВидМаркетплейса; 
	Если НЕ ЗначениеЗаполнено(ВидМаркетплейса) Тогда
		ВызватьИсключение НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Организация", 
		Справочники.Организации.ОрганизацияПоУмолчанию());
		
	ПодготовитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьНастройкаИнтеграцииМаркетплейс"
		И ЗначениеЗаполнено(Параметр) Тогда
		
		ПараметрыИнтеграции = ПолучитьПараметрыИнтеграцииНаСервере(Организация, ВидМаркетплейса); 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыИнтеграции);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.ГруппаИнструкция.Видимость = НЕ СкрытьБаннер;
	
	Если Организация.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПолучитьПараметрыИнтеграции", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	Если ЗначениеЗаполнено(Организация) Тогда
		Настройки.Вставить("Организация", Организация);
	КонецЕсли;
	
	ЗначениеПроцентаВыгрузки = ХранилищеОбщихНастроек.Загрузить(Строка(ВидМаркетплейса), "ПроцентВыгрузки", ЗначениеПроцентаВыгрузки);
	Если НЕ ЗначениеЗаполнено(ЗначениеПроцентаВыгрузки) Тогда
		ЗначениеПроцентаВыгрузки = ПроцентВыгрузкиДляОрганизацииПоУмолчанию(Настройки["Организация"]);
	КонецЕсли;
	
	Настройки.Вставить("ПроцентВыгрузки", ЗначениеПроцентаВыгрузки);
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	ЗначениеПроцентаВыгрузки = Настройки["ПроцентВыгрузки"];
	Если ЗначениеПроцентаВыгрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ХранилищеОбщихНастроек.Сохранить(Строка(ВидМаркетплейса), "ПроцентВыгрузки", ЗначениеПроцентаВыгрузки);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если НЕ ЗначениеЗаполнено(ТаблицаОстатки) Тогда
		ТекстСообщения = 
			НСтр("ru = 'Нет сведений о товарах. Перед передачей остатков загрузите каталог из маркетплейса.'");
			
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"ТаблицаОстатки",,Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСкладПриИзменении(Элемент)
	ИспользоватьСклад = Истина;
	ТаблицаОстатки.Очистить();
	
	ЗаполнитьОстатки();
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	ТаблицаОстатки.Очистить();
	
	ПолучитьПараметрыИнтеграции();
КонецПроцедуры

&НаКлиенте
Процедура ПроцентВыгрузкиПриИзменении(Элемент)
	РассчитатьПроцентМаркетплейс();
КонецПроцедуры

&НаКлиенте
Процедура ПроцентВыгрузкиЧисломПриИзменении(Элемент)
	РассчитатьПроцентМаркетплейс();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСкладПриИзменении(Элемент)
	ТаблицаОстатки.Очистить();
	
	ЗаполнитьОстатки();
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияЗакрытьНажатие(Элемент)
	СкрытьБаннер = Истина;
	Элементы.ГруппаИнструкция.Видимость = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицаОстатки

&НаКлиенте
Процедура ТаблицаОстаткиОстатокИБПриИзменении(Элемент)
	СтрокаОстатки = Элементы.ТаблицаОстатки.ТекущиеДанные;
	СтрокаОстатки.ОстатокКПередаче = Окр(СтрокаОстатки.Количество * ПроцентВыгрузки/100);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура ВыгрузитьОстатки(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторСклада) Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("НастроитьПодключениеВыгрузкаОстатковЗавершение", ЭтотОбъект);
		ОткрытьНастройкиИнтеграции(ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
		
	ВыгрузитьОстаткиНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПодключение(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("НастроитьПодключениеЗавершение", ЭтотОбъект);
	ОткрытьНастройкиИнтеграции(ОповещениеОЗавершении);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗагрузитьНоменклатуру(Команда)
	
	Если НЕ ЗначениеЗаполнено(НастройкаИнтеграции) ИЛИ Контрагент.Пустая() Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("НастроитьПодключениеЗагрузкаНоменклатурыЗавершение", ЭтотОбъект);
		ОткрытьНастройкиИнтеграции(ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	ЗагрузитьНоменклатуруНаКлиенте();
	
КонецПроцедуры 

&НаКлиенте
Процедура СохранитьФайл(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПродолжитьВыгрузкуОстатковВФайл", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервереБезКонтекста
Функция ПроцентВыгрузкиДляОрганизацииПоУмолчанию(Организация)
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НастройкиИнтеграцииМаркетплейс.Ссылка) КАК КоличествоМаркетплейсов
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НастройкиИнтеграцииМаркетплейс.Владелец = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ЗначениеПроцентаВыгрузки = 100;
	Если Выборка.Следующий() 
		И Выборка.КоличествоМаркетплейсов > 1 Тогда
		
		ЗначениеПроцентаВыгрузки = Окр(100/Выборка.КоличествоМаркетплейсов,-1);
	КонецЕсли;
	
	Возврат ЗначениеПроцентаВыгрузки;
КонецФункции


&НаКлиенте
Процедура РассчитатьПроцентМаркетплейс()
	Для каждого СтрокаОстатки Из ТаблицаОстатки Цикл
		СтрокаОстатки.ОстатокКПередаче = Окр(СтрокаОстатки.Количество * ПроцентВыгрузки/100);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФорму()
	Элементы.ГруппаСклад.Видимость = БухгалтерскийУчет.ВедетсяУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары) 
		И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет");
		
	ИспользуетсяНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийБухгалтерскийУчет");
		
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON Тогда 
		Элементы.ТаблицаОстаткиЗагрузитьНоменклатуру.Картинка = БиблиотекаКартинок.ЛоготипOzon;
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		Элементы.ТаблицаОстаткиЗагрузитьНоменклатуру.Картинка = БиблиотекаКартинок.ЛоготипWB;
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыИнтеграцииНаСервере(Организация, ВидМаркетплейса) 
	Результат = Новый Структура;
	Результат.Вставить("НастройкаИнтеграции", 
		Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Организация, ВидМаркетплейса));
		
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Результат.НастройкаИнтеграции, "ИдентификаторСклада, Контрагент, КаталогЗагружен");
	Результат.Вставить("ИдентификаторСклада", Реквизиты.ИдентификаторСклада);
	Результат.Вставить("Контрагент",          Реквизиты.Контрагент);
	Результат.Вставить("КаталогЗагружен",     Реквизиты.КаталогЗагружен);
	
	ТребуетсяНастройкаСклада = Ложь;
	Если НЕ ЗначениеЗаполнено(Реквизиты.ИдентификаторСклада) Тогда
		ТребуетсяНастройкаСклада = ОбменСМаркетплейс.СписокСкладовМаркетплейс(Результат.НастройкаИнтеграции) <> Неопределено;
	КонецЕсли;
	
	Результат.Вставить("ТребуетсяНастройкаСклада", ТребуетсяНастройкаСклада);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ОткрытьНастройкиИнтеграции(ОповещениеОЗавершении)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ВидОперации", "ВыгрузкаОстатков");
	ПараметрыФормы.Вставить("ОшибкаАвторизации", ОшибкаАвторизации);
	
	Если НастройкаИнтеграции.Пустая() Тогда
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Владелец, ВидМаркетплейса", Организация, ВидМаркетплейса));
	Иначе
		ПараметрыФормы.Вставить("Ключ", НастройкаИнтеграции);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.НастройкиИнтеграцииМаркетплейс.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект, , , ,ОповещениеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры 

&НаКлиенте
Процедура НастроитьПодключениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОшибкаАвторизации Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОстатки();
	
КонецПроцедуры 

#Область ЗагрузкаКаталога

&НаКлиенте
Процедура НастроитьПодключениеЗагрузкаНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьНоменклатуруНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНоменклатуруНаКлиенте()
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьНоменклатуруЗавершение", ЭтотОбъект);
	ОбменСМаркетплейсКлиент.ЗагрузитьСписокНоменклатуры(НастройкаИнтеграции, ЭтотОбъект, ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНоменклатуруЗавершение(РезультатВыполнения, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		СопоставитьНоменклатуру(РезультатВыполнения.СписокТоваров);
	ИначеЕсли ЗначениеЗаполнено(РезультатВыполнения.СообщениеОбОшибке) Тогда
		Если РезультатВыполнения.КодОтветаСервера = 401 Или РезультатВыполнения.КодОтветаСервера = 403 Тогда
			ОшибкаАвторизации = Истина;
			ОбменСМаркетплейсКлиент.ПредложитьНовуюАвторизацию(Организация);
		Иначе
			ОшибкаАвторизации = Ложь;
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатВыполнения.СообщениеОбОшибке);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'При выполнении операции произошла ошибка. Подробности в журнале регистрации.'"));
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура СопоставитьНоменклатуру(СписокТоваров)
	Если НЕ ЗначениеЗаполнено(СписокТоваров) Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка завершена'"),,НСтр("ru = 'Новых товаров нет'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыСопоставления = Новый Структура;
	ПараметрыСопоставления.Вставить("ВидМаркетплейса",     ВидМаркетплейса);
	ПараметрыСопоставления.Вставить("НастройкаИнтеграции", НастройкаИнтеграции);
	
	ПараметрыСопоставления.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("СопоставитьНоменклатуруЗавершение", ЭтотОбъект));
	ОбменСМаркетплейсКлиент.СопоставитьНоменклатуруПриЗагрузкеКаталога(СписокТоваров, ПараметрыСопоставления);
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуруЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ Результат.Результат Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОстатки();
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеОстатков

&НаКлиенте
Процедура ЗаполнитьОстатки()
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Организация",         Организация);
	ПараметрыЗаполнения.Вставить("НастройкаИнтеграции", НастройкаИНтеграции);
	ПараметрыЗаполнения.Вставить("ВидМаркетплейса",     ВидМаркетплейса);
	Если ИспользоватьСклад Тогда
		ПараметрыЗаполнения.Вставить("Склад", ОтборСклад);
	КонецЕсли;
	
	ДлительнаяОперация = НачатьЗаполнениеОстатков(ПараметрыЗаполнения, УникальныйИдентификатор);
	
	Если ДлительнаяОперация.Статус <> "Выполняется" Тогда
		ЗаполнитьОстаткиЗавершение(ДлительнаяОперация);
		Возврат;
	КонецЕсли;
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Заголовок = НСтр("ru = 'Получаем актуальные остатки'");
	
	Оповешение = Новый ОписаниеОповещения("ЗаполнитьОстаткиЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповешение, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОстаткиЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус") = "Выполнено" Тогда
		Возврат;
	КонецЕсли; 
		
	ЗаполнитьТаблицаОстаткиНаСервере(Результат.АдресРезультата);

	РассчитатьПроцентМаркетплейс();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицаОстаткиНаСервере(АдресРезультата)
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	ТаблицаОстатки.Загрузить(РезультатВыполнения);
КонецПроцедуры 

&НаСервереБезКонтекста
Функция НачатьЗаполнениеОстатков(ПараметрыЗаполнения, ИдентификаторФормы)
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "РегистрыСведений.ОстаткиТоваровМаркетплейсов.ОстаткиДляВыгрузкиВМаркетплейс", ПараметрыЗаполнения);
КонецФункции

#КонецОбласти

#Область ВыгрузкаОстатковAPI 

&НаКлиенте
Процедура НастроитьПодключениеВыгрузкаОстатковЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьОстаткиНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОстаткиНаКлиенте()
	
	ДлительнаяОперация = ВыгрузитьОстаткиНаСервере();
	
	Если ДлительнаяОперация.Статус <> "Выполняется" Тогда
		ВыгрузитьОстаткиЗавершение(ДлительнаяОперация);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВыгрузитьОстаткиЗавершение", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
КонецПроцедуры
	
&НаКлиенте
Процедура ВыгрузитьОстаткиЗавершение(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	Если НЕ ДлительнаяОперация.Статус = "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	Если Результат.Результат Тогда
		Закрыть();
	ИначеЕсли  ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
		Если Результат.КодОтветаСервера = 401 Или Результат.КодОтветаСервера = 403 Тогда
			ОшибкаАвторизации = Истина;
			ОбменСМаркетплейсКлиент.ПредложитьНовуюАвторизацию(Организация);
		Иначе
			ОшибкаАвторизации = Ложь;
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.СообщениеОбОшибке);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'При выполнении операции произошла ошибка. Подробности в журнале регистрации.'"));
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьОстаткиНаСервере() 
	ПодготовитьОстаткиКПередаче();
	
	Возврат ВыгрузитьОстаткиВМаркетплейс();
КонецФункции

&НаСервере
Процедура ПодготовитьОстаткиКПередаче()
	НаборЗаписей = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НастройкаИнтеграции.Установить(НастройкаИнтеграции);
	НаборЗаписей.Прочитать();
	
	ОстаткиКПередаче = НаборЗаписей.Выгрузить();
	
	Отбор = Новый Структура("ИдентификаторТовара, Штрихкод");
	
	Для каждого СтрокаТовары Из ТаблицаОстатки Цикл
		Если СтрокаТовары.ОстатокКПередаче = СтрокаТовары.КоличествоМаркетплейс Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТовары);
		
		СуществующиеСтроки = ОстаткиКПередаче.НайтиСтроки(Отбор);
		Если СуществующиеСтроки.Количество() > 0 Тогда
			СтрокаОстатки = СуществующиеСтроки[0];
		Иначе
			СтрокаОстатки = ОстаткиКПередаче.Добавить();
		КонецЕсли;
		
		СтрокаОстатки.НастройкаИнтеграции  = НастройкаИнтеграции;
		СтрокаОстатки.ИдентификаторСклада  = ИдентификаторСклада;
		СтрокаОстатки.ИдентификаторТовара  = СтрокаТовары.ИдентификаторТовара;
		СтрокаОстатки.Штрихкод             = СтрокаТовары.Штрихкод;
		СтрокаОстатки.Остаток              = СтрокаТовары.ОстатокКПередаче;
		СтрокаОстатки.ДатаВыгрузкиОстатков = Дата(1,1,1);
		
	КонецЦикла;
	
	НаборЗаписей.Загрузить(ОстаткиКПередаче);
	
	НаборЗаписей.Записать();
КонецПроцедуры

&НаСервере
Функция ВыгрузитьОстаткиВМаркетплейс()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выгрузка остатков в маркетплейс'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "ОбменСМаркетплейс.ПередатьОстаткиТоваров", НастройкаИнтеграции); 
	
КонецФункции

#КонецОбласти

#Область ВыгрузкаОстатковВФайл

&НаКлиенте
Процедура ПродолжитьВыгрузкуОстатковВФайл(РасширениеПодключено,
	ДополнительныеПараметры) Экспорт
	
	Если НЕ РасширениеПодключено = Истина Тогда
		ТекстСообщения = НСтр("ru='Расширение для работы с файлами в веб-клиенте не подключено, выгрузка остатков остановлена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ВыборКаталогаЗавершение = Новый ОписаниеОповещения("ВыборКаталогаЗавершение", ЭтотОбъект);
	ФайловаяСистемаКлиент.ВыбратьКаталог(ВыборКаталогаЗавершение, НСтр("ru = 'Файл с остатками'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаЗавершение(ИмяКаталога, ДополнительныеПараметры) Экспорт
	Если НЕ ЗначениеЗаполнено(ИмяКаталога) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураФайла = ПодготовитьФайлОстатковНаСервере();
	
	ПакетОтображаемыхДокументов = Новый ПакетОтображаемыхДокументов; 
	
	Для каждого КлючИЗначение Из СтруктураФайла.Страницы Цикл
	
		НовыйЛист = ПакетОтображаемыхДокументов.Состав.Добавить(КлючИЗначение.Значение);
		НовыйЛист.Наименование = КлючИЗначение.Ключ;
	
	КонецЦикла;
	
	ПакетОтображаемыхДокументов.Записать(ИмяКаталога+"\"+СтруктураФайла.ИмяФайла, ТипФайлаПакетаОтображаемыхДокументов.XLSX);
КонецПроцедуры 

&НаСервере
Функция ПодготовитьФайлОстатковНаСервере()
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подготовка файла остатков выгрузки в маректплейс'");
	
	ТаблицаОстатковМаркетплейс = ОбменСМаркетплейс.НовыйТаблицаОстатковТоваров(); 
	
	Для каждого СтрокаОстатки Из ТаблицаОстатки Цикл
		Если СтрокаОстатки.ОстатокКПередаче = 0 Тогда
			Продолжить;
		КонецЕсли;
	
		НоваяСтрокаОстатки = ТаблицаОстатковМаркетплейс.Добавить();
		НоваяСтрокаОстатки.ИдентификаторСклада = ИдентификаторСклада;
		НоваяСтрокаОстатки.ИдентификаторТовара = СтрокаОстатки.ИдентификаторТовара;
		НоваяСтрокаОстатки.Штрихкод            = СтрокаОстатки.Штрихкод;
		НоваяСтрокаОстатки.Остаток             = СтрокаОстатки.ОстатокКПередаче;
	КонецЦикла;
	
	ПараметрыИнтеграции = Новый Структура("ВидМаркетплейса, НаименованиеСклада, ИдентификаторСклада, ИдентификаторКабинета");
	Если НЕ НастройкаИнтеграции.Пустая() Тогда
		ПараметрыИнтеграции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаИнтеграции, ПараметрыИнтеграции);
	Иначе
		ПараметрыИнтеграции.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	КонецЕсли;
	
	Возврат ОбменСМаркетплейс.ПодготовитьФайлОстатков(ПараметрыИнтеграции, ТаблицаОстатковМаркетплейс);
КонецФункции

#КонецОбласти

#Область ПолучениеПараметровИнтеграции

// Получение параметров интеграции при открытии формы и изменении организации
//
&НаКлиенте
Процедура ПолучитьПараметрыИнтеграции() 
	ПараметрыИнтеграции = ПолучитьПараметрыИнтеграцииНаСервере(Организация, ВидМаркетплейса); 
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыИнтеграции);
	НоменклатураЗагруженаИзФайла = ЕстьНоменклатураМаркетплейса(Организация, ВидМаркетплейса);
	Если НастройкаИнтеграции.Пустая() И НЕ НоменклатураЗагруженаИзФайла Тогда
		// если интеграция не настроена - предложим загрузить каталог из файла, там же можно заполнить параметры подключения по API
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
		ПараметрыФормы.Вставить("Организация",     Организация);
		ПараметрыФормы.Вставить("ВидОперации",     "ВыгрузкаОстатков");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаКаталогаПриОткрытииФормыЗавершение", ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаКаталога", 
			ПараметрыФормы, 
			ЭтотОбъект,,,,ОповещениеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли НЕ ЗначениеЗаполнено(ИдентификаторСклада) И ПараметрыИнтеграции.ТребуетсяНастройкаСклада Тогда
		// если настройка интеграции уже сделана, но склад еще не настроен, попросим настроить склад
		ОповещениеОЗавершении = Новый ОписаниеОповещения("НастройкаСкладаЗавершение", ЭтотОбъект);
		ОткрытьФорму("РегистрСведений.ОстаткиТоваровМаркетплейсов.Форма.ФормаЗавершенияНастройки", 
			Новый Структура("НастройкаИнтеграции", НастройкаИнтеграции), 
			ЭтотОбъект,,,,ОповещениеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ЗаполнитьОстатки();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьНоменклатураМаркетплейса(Организация, Маркетплейс)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Маркетплейс", Маркетплейс);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара
	|ИЗ
	|	РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|ГДЕ
	|	НоменклатураМаркетплейсов.Организация = &Организация
	|	И НоменклатураМаркетплейсов.Маркетплейс = &Маркетплейс";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

&НаКлиенте
Процедура ЗагрузкаКаталогаПриОткрытииФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	// Пользователь НЕ отказался от загрузки каталога (из файла или по api)
	Если НЕ Результат = ИСТИНА  Тогда
		Возврат;
	КонецЕсли;
	
	// Возможно параметры интеграции появились
	ПараметрыИнтеграции = ПолучитьПараметрыИнтеграцииНаСервере(Организация, ВидМаркетплейса); 
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыИнтеграции);
	
	ЗаполнитьОстатки();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьНастройкиИнтеграции(НастройкаИнтеграции, ПараметрыЗаполнения) 
	НастройкаОбъект = НастройкаИнтеграции.ПолучитьОбъект();
	ЗаполнитьЗначенияСвойств(НастройкаОбъект, ПараметрыЗаполнения);
	НастройкаОбъект.Записать();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСкладаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ ТипЗнч(Результат) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСклада = Результат.РеквизитыСклада.ИдентификаторСклада;
	
	ОбновитьНастройкиИнтеграции(НастройкаИнтеграции, Результат.РеквизитыСклада);
	
	Если Результат.ЗагружатьКаталог Тогда
		ЗагрузитьНоменклатуруНаКлиенте();
	ИначеЕсли НЕ ИспользуетсяНесколькоОрганизаций И НЕ КаталогЗагружен Тогда
		// если отказ от загрузки каталога, но у нас одна организация - заполним организацию в регистре самостоятельно
		ДлительнаяОперация = ЗаполнитьРегистрНоменклатураМаркетплейсов(ВидМаркетплейса, НастройкаИнтеграции, УникальныйИдентификатор);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Подготавливаем форму к первому запуску. Пожалуйста, подождите.'");
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьРегистрНоменклатураМаркетплейсовЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	Иначе
		ЗаполнитьОстатки();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьРегистрНоменклатураМаркетплейсов(ВидМаркетплейса, НастройкаИнтеграции, УникальныйИдентификатор)
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "РегистрыСведений.НоменклатураМаркетплейсов.ЗаполнитьРегистрПоДаннымКонтрагента", ВидМаркетплейса, НастройкаИнтеграции);
КонецФункции   

&НаКлиенте
Процедура ЗаполнитьРегистрНоменклатураМаркетплейсовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ Результат.Статус = "Выполнено" Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьОстатки();
КонецПроцедуры

#КонецОбласти

#КонецОбласти


