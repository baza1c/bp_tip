#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТекущаяДата = Параметры.Период; // Период чтения настроек УСН
	ТекущаяСтавкаНалога = НастройкиУчетаУСН.СтавкаНалога;
	
	Если Не ЗначениеЗаполнено(Параметры.Организация) Тогда
		НастройкиУчетаУСН.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	Иначе
		НастройкиУчетаУСН.Организация = Параметры.Организация;
	КонецЕсли;
	
	ПериодЧтенияНастройки = НастройкиУчетаУСНФормыКлиентСервер.ПериодЧтенияНастройки(ЭтотОбъект);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		НастройкиУчетаУСНФормыВызовСервера.НастройкиОрганизацииИСистемыНалогообложения(
			НастройкиУчетаУСН.Организация, ПериодЧтенияНастройки));
	
	НастройкиУчетаУСНФормы.ЗаполнитьРеквизитыОснованияЛьготнойСтавки(ЭтотОбъект, ПериодЧтенияНастройки);
	ИспользуетсяСервисРегиональныеСтавкиНалогов =
		РегиональныеСтавкиНалоговВызовСервера.ИспользуетсяСервисРегиональныхСтавок();
	ДатаИзменения = НастройкиУчетаУСН.Период;
	ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоКвартала(НастройкиУчетаУСН.Период),
		КонецКвартала(НастройкиУчетаУСН.Период),
		Истина);
	
	ПрименяетсяУСНДоходы = УчетнаяПолитика.ПрименяетсяУСНДоходы(НастройкиУчетаУСН.Организация, НастройкиУчетаУСН.Период);
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(НастройкиУчетаУСН.Организация,
		НастройкиУчетаУСН.Период);
	
	Если Параметры.Ключ.Пустой() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ТекущаяДата = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Период");
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкиСистемыНалогообложения"
			И Параметр.Организация = НастройкиУчетаУСН.Организация Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,
			НастройкиУчетаУСНФормыВызовСервера.НастройкиОрганизацииИСистемыНалогообложения(
			НастройкиУчетаУСН.Организация, НастройкиУчетаУСН.Период));
		
		УправлениеФормой(ЭтотОбъект);
	
	ИначеЕсли ИмяСобытия = "ИзменениеСтавкиУСН" Тогда 
		
		ОбработатьИзменениеСтавокУСН(Параметр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолучитьОписаниеСтавокУСНВФоне();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НастройкиУчетаУСНФормы.ПроверитьЗаполнениеНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РезультатВыполнения = КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(УникальныйИдентификатор, НастройкиУчетаУСН.Организация);
	ПараметрыЗаписи.Вставить("РезультатВыполненияЗаданияКалендаряБухгалтера", РезультатВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Отслеживание фонового задания по календарю бухгалтера будет происходить без открытия окна ожидания.
	Если ПараметрыЗаписи.Свойство("РезультатВыполненияЗаданияКалендаряБухгалтера") Тогда
		КалендарьБухгалтераКлиент.ОжидатьЗавершениеЗаполненияВФоне(ПараметрыЗаписи.РезультатВыполненияЗаданияКалендаряБухгалтера);
	КонецЕсли;
	
	// Очистку регистра ПроверенныеДокументы должны сделать в любом случае. Она продолжится, даже если форма закроется.
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗаписиЗавершение", ЭтотОбъект);
	ПроведениеКлиент.НачатьОчисткуРегистраПроверенныхДокументов(ОповещениеОЗавершении, НастройкиУчетаУСН.Период, НастройкиУчетаУСН.Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияСтавкиУСНВВашемРегионеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РеквизитыОКВЭД = ПолучитьОсновнойОКВЭД(НастройкиУчетаУСН.Организация, НастройкиУчетаУСН.Период);
	
	ПараметрыМетода = РегиональныеСтавкиНалоговКлиент.ПараметрыОтчетаСтавкиНалогаВВашемРегионе();
	ПараметрыМетода.Налог = "УСН";
	ПараметрыМетода.Режим = "НастройкаСтавкиУСН";
	ПараметрыМетода.КодРегиона = КодРегиона;
	ПараметрыМетода.КодВидаДеятельности = РеквизитыОКВЭД.Код;
	ПараметрыМетода.НаименованиеВидаДеятельности = РеквизитыОКВЭД.Наименование;
	ПараметрыМетода.ЭтоЮрЛицо = ЭтоЮрЛицо;
	ПараметрыМетода.ПрименяетсяУСНДоходы = ПрименяетсяУСНДоходы;
	ПараметрыМетода.ПрименяетсяУСНДоходыРасходы = ПрименяетсяУСНДоходыМинусРасходы;
	ПараметрыМетода.Период = НастройкиУчетаУСН.Период;
	ПараметрыМетода.ПоказыватьПрименить = Истина;
	
	РегиональныеСтавкиНалоговКлиент.ОбработатьНавигационнуюСсылкуСтавкиВВашемРегионе(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки,
		ПараметрыМетода);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыеКаникулыПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияСтрокойОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтотОбъект);
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода", НачалоМесяца(ДатаИзменения));
	ПараметрыВыбора.Вставить("КонецПериода",  КонецМесяца(ДатаИзменения));
	ПараметрыВыбора.Вставить("ВидПериода",    ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Квартал"));
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаКвартал", ПараметрыВыбора, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаУСНПриИзменении(Элемент)
	
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаУСНИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтавкаНалога = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Текст);
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОснованиеЛьготыУСНОбработкаНавигационнойСсылки(Элемент, ТекстСсылки, СтандартнаяОбработка)
	
	НастройкиУчетаУСНФормыКлиент.ДекорацияОснованиеЛьготыУСНОбработкаНавигационнойСсылки(ЭтотОбъект, Элемент, ТекстСсылки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыНомерСтатьиПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ОснованиеЛьготыПриИзменении(ЭтотОбъект);
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыНомерСтатьиИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПунктПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ОснованиеЛьготыПриИзменении(ЭтотОбъект);
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПунктИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПодпунктПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ОснованиеЛьготыПриИзменении(ЭтотОбъект);
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПодпунктИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОснованиеЛьготыПримерЗаполненияНажатие(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ДекорацияОснованиеЛьготыПримерЗаполненияНажатие(ЭтотОбъект, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьИзменения();
	ОповеститьОбИзмененииНастроек();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура История(Команда)
	
	Организация = НастройкиУчетаУСН.Организация;
	Отбор = Новый Структура("Организация", Организация);
	СтруктураПараметров = Новый Структура("Отбор, ВедущийОбъект", Отбор, Организация);
	ОткрытьФорму("РегистрСведений.НастройкиУчетаУСН.ФормаСписка", СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИзмененияИстории(Команда)
	
	Элементы.СтраницыИстория.ТекущаяСтраница = Элементы.СтраницаПериоды;
	ПеречитатьНастройкиУчетаУСН();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Если ЗначениеЗаполнено(ТекущаяДата) Тогда
		ПрименяетсяС = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоКвартала(ТекущаяДата),
			КонецКвартала(ТекущаяДата),
			Истина);
	Иначе
		ПрименяетсяС = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоКвартала(НастройкиУчетаУСН.Период),
			КонецКвартала(НастройкиУчетаУСН.Период),
			Истина);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкиУчетаУСН.Организация) Тогда
		НастройкиУчетаУСН.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ПериодЧтенияНастройки = НастройкиУчетаУСНФормыКлиентСервер.ПериодЧтенияНастройки(ЭтотОбъект);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		НастройкиУчетаУСНФормыВызовСервера.НастройкиОрганизацииИСистемыНалогообложения(
			НастройкиУчетаУСН.Организация, ПериодЧтенияНастройки));
	
	ТекущаяСтавкаНалога = НастройкиУчетаУСН.СтавкаНалога;
	НастройкиУчетаУСНФормы.ЗаполнитьРеквизитыОснованияЛьготнойСтавки(ЭтотОбъект, ПериодЧтенияНастройки);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользуетсяСервисРегиональныеСтавкиНалогов =
		РегиональныеСтавкиНалоговВызовСервера.ИспользуетсяСервисРегиональныхСтавок();
	
	ПолучитьОписаниеСтавокУСНВФоне();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОписаниеСтавокУСНВФоне()
	
	Если Не ИспользуетсяСервисРегиональныеСтавкиНалогов Тогда
		Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок =
			РегиональныеСтавкиНалоговКлиент.ТекстПодсказкиИнтернетПоддержкаНеПодключена();
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = "";
	
	Если Не ЗначениеЗаполнено(КодРегиона) Тогда
		Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = НСтр("ru='Не указан регион регистрации организации'");
		Возврат;
	КонецЕсли;
	
	РеквизитыОКВЭД = ПолучитьОсновнойОКВЭД(НастройкиУчетаУСН.Организация, НастройкиУчетаУСН.Период);
	
	ПараметрыМетода = РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаОписаниеСтавокУСН();
	ПараметрыМетода.КодРегиона = КодРегиона;
	ПараметрыМетода.КодВидаДеятельности = РеквизитыОКВЭД.Код;
	ПараметрыМетода.ЭтоЮрЛицо = ЭтоЮрЛицо;
	ПараметрыМетода.ПрименяетсяУСНДоходы = ПрименяетсяУСНДоходы;
	ПараметрыМетода.ПрименяетсяУСНДоходыРасходы = ПрименяетсяУСНДоходыМинусРасходы;
	ПараметрыМетода.Период = НастройкиУчетаУСН.Период;
	
	Обработчик = Новый ОписаниеОповещения("ОбработкаПолученияОписанияСтавокУСН", ЭтотОбъект);
	
	РегиональныеСтавкиНалоговКлиент.ПолучитьОписаниеСтавокУСНВФоне(
		ЭтотОбъект,
		Элементы.ДекорацияСтавкиУСНВВашемРегионе,
		Элементы.ДекорацияСтавкиУСНДлительнаяОперация,
		ПараметрыМетода,
		Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПолученияОписанияСтавокУСН(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	РегиональныеСтавкиНалоговКлиент.ОбработкаПолученияОписанияСтавокНалога(
		ДлительнаяОперация,
		Элементы.ДекорацияСтавкиУСНВВашемРегионе,
		Элементы.ДекорацияСтавкиУСНДлительнаяОперация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОсновнойОКВЭД(Организация, Период)

	СписокРевизитов = Новый Массив;
	СписокРевизитов.Добавить("ОКВЭД2");
	СписокРевизитов.Добавить("ОснВидДеят2");
	
	СведенияОбОрганизации = РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОбОрганизации(
		Организация,
		Период,
		СписокРевизитов);
	
	Результат = Новый Структура("Код, Наименование");
	Результат.Код = СведенияОбОрганизации.ОКВЭД2;
	Результат.Наименование = СведенияОбОрганизации.ОснВидДеят2;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьИзменениеСтавокУСН(НовыеСтавки)
	
	Если ТипЗнч(НовыеСтавки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НовыеСтавки.Свойство("НалоговыеКаникулы") Тогда
		Если Не ЭтоЮрЛицо Тогда
			НастройкиУчетаУСН.НалоговыеКаникулы = Истина;
			НастройкиУчетаУСН.СтавкаНалога = 0;
		КонецЕсли;
	Иначе
		НастройкиУчетаУСН.СтавкаНалога = НовыеСтавки.СтавкаНалога;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	ПоказатьСтраницуПримененияИзменений();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Запись   = Форма.НастройкиУчетаУСН;
	
	Элементы.ГруппаЛьготнаяСтавкаУСН.Видимость = Запись.НалоговыеКаникулы;
	Элементы.НалоговыеКаникулы.Видимость = Не Форма.ЭтоЮрЛицо И Запись.Период >= '20150101';
	Элементы.НалоговыеКаникулы.РасширеннаяПодсказка.Заголовок =
		НастройкиУчетаУСНФормыКлиентСервер.НалоговыеКаникулы_ТекстПодсказки(НастройкиУчетаУСНФормыКлиентСервер.ПериодЧтенияНастройки(Форма));
	
	Элементы.ГруппаОсновнаяСтавкаУСНРазмер.Доступность = Не Запись.НалоговыеКаникулы;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Оповестить("ИзменениеУчетнойПолитики", НастройкиУчетаУСН.Организация);
	ОповеститьОбИзменении(Тип("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	
	ПараметрыОповещения = Новый Структура("Организация, Период", НастройкиУчетаУСН.Организация, НастройкиУчетаУСН.Период);
	Оповестить("Запись_НастройкиУчетаУСН", ПараметрыОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменения()
	
	Если ДатаИзменения = НастройкиУчетаУСН.Период Тогда
		МенеджерЗаписи = РеквизитФормыВЗначение("НастройкиУчетаУСН");
	Иначе
		МенеджерЗаписи = РегистрыСведений.НастройкиУчетаУСН.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, НастройкиУчетаУСН, ,"Период");
		МенеджерЗаписи.Период = ДатаИзменения;
	КонецЕсли;
	
	МенеджерЗаписи.Записать(Истина);
	
	Модифицированность = Ложь;
	ПеречитатьНастройкиУчетаУСН();
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьНастройкиУчетаУСН()
	
	КлючДействующейНастройки = НастройкиУчета.КлючЗаписиДействующейУчетнойПолитики(
		"НастройкиУчетаУСН",
		НастройкиУчетаУСН.Организация,
		ТекущаяДатаСеанса());
		
	МенеджерЗаписи = РегистрыСведений.НастройкиУчетаУСН.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = КлючДействующейНастройки.Организация;
	МенеджерЗаписи.Период = КлючДействующейНастройки.Период;
	МенеджерЗаписи.Прочитать();
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "НастройкиУчетаУСН");
	
	ПодготовитьФормуНаСервере();
	ДатаИзменения = КлючДействующейНастройки.Период;
	ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоКвартала(ДатаИзменения),
		КонецКвартала(ДатаИзменения),
		Истина);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииНастроек()
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.НалогиОтчеты"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ВариантыПримененияТребованийЗаконодательства"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
	Оповестить("Обновить дерево отчетов", "Обновить дерево отчетов", ЭтотОбъект);
	Оповестить("ИзменениеУчетнойПолитики", НастройкиУчетаУСН.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЗавершениеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		
		ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоКвартала(РезультатВыбора.НачалоПериода),
			КонецКвартала(РезультатВыбора.НачалоПериода),
			Истина);
		
		ДатаИзменения = РезультатВыбора.НачалоПериода;
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСтраницуПримененияИзменений()
	
	Модифицированность = Ложь;
	Элементы.СтраницыИстория.ТекущаяСтраница = Элементы.СтраницаИзменение;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если НЕ ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтавкиУСНВВашемРегионе.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаСтавкиУСНВВашемРегионе.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаОсновныеРеквизиты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
КонецПроцедуры

#КонецОбласти