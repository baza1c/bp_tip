#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбработчикиОбновления

// Заполняет регистр по данным прежнего регистра при обновлении
// 
// Параметры:
//  Параметры - Структура - см. описание БСП
Процедура ЗаполнитьДаннымиПрежнегоРегистра(Параметры) Экспорт
	
	// Переносим записи из прежнего регистра в новый порциями, начиная с даты перехода на новый расчет налога.
	ДатаПерехода = РасчетИмущественныхНалоговКлиентСервер.НачалоПримененияНовогоРасчетаТранспортногоНалога();
	
	// Все записи по каждому отдельному документу (регистратору) нужно переносить одновременно.
	// Поэтому используем такой прием:
	// - выбираем все записи последнего по времени регистратора (определяем по ПериодРасчета в записях)
	// - считаем количество записей по этому регистратору
	// - контролируем, чтобы общее количество обрабатываемых записей соответствовало условному размеру порции (1000):
	//   если этот размер достигнут, и есть еще необработанные регистраторы, то обработчик будет запущен еще раз
	// - непосредственно сам перенос записей выполняется по каждому регистратору отдельно
	// 
	// В итоге фактический размер порции может превышать 1000 записей.
	// НАПРИМЕР: в 1000 наиболее "свежих" записей попала только одна из 20 записей документа А (она оказалась 1000-й по порядку).
	// В итоге в данном примере в порции будет обработано 1019 записей (в т.ч. все записи документа А).
	
	// На первой итерации обработчика считаем общее количество регистраторов - это количество объектов к обработке
	Если Не ЗначениеЗаполнено(Параметры.ПрогрессВыполнения.ВсегоОбъектов) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РасчетТранспортногоНалогаДо2020.Регистратор), 0) КАК КоличествоРегистраторов
		|ИЗ
		|	РегистрСведений.РасчетТранспортногоНалогаДо2020 КАК РасчетТранспортногоНалогаДо2020
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО РасчетТранспортногоНалогаДо2020.Организация = Организации.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
		|		ПО РасчетТранспортногоНалогаДо2020.ОсновноеСредство = ОсновныеСредства.Ссылка
		|ГДЕ
		|	РасчетТранспортногоНалогаДо2020.Активность
		|	И РасчетТранспортногоНалогаДо2020.ПериодРасчета >= &ДатаПерехода";
		
		Запрос.УстановитьПараметр("ДатаПерехода", ДатаПерехода);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.КоличествоРегистраторов;
		
		Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
			Параметры.ОбработкаЗавершена = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОстатокПорции = 1000; 
	КоличествоОбрабатываемыхЗаписей = 0;
	КоличествоОшибок = 0;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// В ИБ за один и тот же период расчета могут быть несколько регистраторов (например, регламентная операция, а также
	// ручная операция). Считаем эту ситуацию ошибочной, но чтобы не было ошибки дублей записей регистра сведений, то
	// обрабатываем записи только одного из таких документов, а остальные проигнорируем. Приоритет выбора следующий:
	// - по типу: регламентная операция приоритетнее
	// - по дате: более ранний документ приоритетнее
	// В итоге получим таблицу игнорируемых документов - это все регистраторы из каждого периода расчета, не являющееся
	// первыми по приоритету.
	Если Параметры.Свойство("ИгнорируемыеРегистраторы") Тогда
		ИгнорируемыеРегистраторы = Параметры.ИгнорируемыеРегистраторы;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасчетТранспортногоНалогаДо2020.Организация КАК Организация,
		|	РасчетТранспортногоНалогаДо2020.ПериодРасчета КАК ПериодРасчета,
		|	РасчетТранспортногоНалогаДо2020.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ РегистраторыПоПериодам
		|ИЗ
		|	РегистрСведений.РасчетТранспортногоНалогаДо2020 КАК РасчетТранспортногоНалогаДо2020
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО РасчетТранспортногоНалогаДо2020.Организация = Организации.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
		|		ПО РасчетТранспортногоНалогаДо2020.ОсновноеСредство = ОсновныеСредства.Ссылка
		|ГДЕ
		|	РасчетТранспортногоНалогаДо2020.Активность
		|	И РасчетТранспортногоНалогаДо2020.ПериодРасчета >= &ДатаПерехода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РегистраторыПоПериодам.Организация КАК Организация,
		|	РегистраторыПоПериодам.ПериодРасчета КАК ПериодРасчета
		|ПОМЕСТИТЬ ДублированиеРасчета
		|ИЗ
		|	РегистраторыПоПериодам КАК РегистраторыПоПериодам
		|СГРУППИРОВАТЬ ПО
		|	РегистраторыПоПериодам.Организация,
		|	РегистраторыПоПериодам.ПериодРасчета
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РегистраторыПоПериодам.Регистратор) > 1
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПериодРасчета,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РегистраторыПоПериодам.Организация КАК Организация,
		|	РегистраторыПоПериодам.ПериодРасчета КАК ПериодРасчета,
		|	РегистраторыПоПериодам.Регистратор КАК Регистратор,
		|	ВЫБОР
		|		КОГДА РегистраторыПоПериодам.Регистратор ССЫЛКА Документ.РегламентнаяОперация
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК ПорядокПоТипу,
		|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1,1,1)) КАК Дата
		|ИЗ
		|	РегистраторыПоПериодам КАК РегистраторыПоПериодам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДублированиеРасчета КАК ДублированиеРасчета
		|		ПО РегистраторыПоПериодам.ПериодРасчета = ДублированиеРасчета.ПериодРасчета
		|		И РегистраторыПоПериодам.Организация = ДублированиеРасчета.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО РегистраторыПоПериодам.Организация = ДанныеПервичныхДокументов.Организация
		|		И РегистраторыПоПериодам.Регистратор = ДанныеПервичныхДокументов.Документ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	ПериодРасчета,
		|	ПорядокПоТипу,
		|	Дата,
		|	Регистратор";
		
		Запрос.УстановитьПараметр("ДатаПерехода", ДатаПерехода);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			// Таблица упорядочена по приоритету, поэтому пронумеруем записи, сгруппируем по периодам расчета,
			// и в каждом периоде расчета исключим запись с высшим приоритетом (этот регистратор будет обработан ниже,
			// а все остальные попадут в игнорируемые).
			ВсеДублиРасчета = Результат.Выгрузить();
			ОбщегоНазначенияБПВызовСервера.ПронумероватьТаблицу(ВсеДублиРасчета); // добавляет колонку НомерСтроки
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ВсеДублиРасчета.Организация КАК Организация,
			|	ВсеДублиРасчета.ПериодРасчета КАК ПериодРасчета,
			|	ВсеДублиРасчета.Регистратор КАК Регистратор,
			|	ВсеДублиРасчета.НомерСтроки КАК НомерСтроки
			|ПОМЕСТИТЬ ВсеДублиРасчета
			|ИЗ
			|	&ВсеДублиРасчета КАК ВсеДублиРасчета
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеДублиРасчета.Организация КАК Организация,
			|	ВсеДублиРасчета.ПериодРасчета КАК ПериодРасчета,
			|	МИНИМУМ(ВсеДублиРасчета.НомерСтроки) КАК НомерСтроки
			|ПОМЕСТИТЬ ВысшийПриоритет
			|ИЗ
			|	ВсеДублиРасчета КАК ВсеДублиРасчета
			|СГРУППИРОВАТЬ ПО
			|	ВсеДублиРасчета.Организация,
			|	ВсеДублиРасчета.ПериодРасчета
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ПериодРасчета,
			|	НомерСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеДублиРасчета.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ ИгнорируемыеРегистраторы
			|ИЗ
			|	ВсеДублиРасчета КАК ВсеДублиРасчета
			|ГДЕ
			|	НЕ (ВсеДублиРасчета.Организация, ВсеДублиРасчета.ПериодРасчета, ВсеДублиРасчета.НомерСтроки) В
			|		(ВЫБРАТЬ
			|			Организация,
			|			ПериодРасчета,
			|			НомерСтроки
			|		ИЗ
			|			ВысшийПриоритет КАК ВысшийПриоритет)
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Регистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВсеДублиРасчета
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВысшийПриоритет
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИгнорируемыеРегистраторы.Регистратор
			|ИЗ
			|	ИгнорируемыеРегистраторы КАК ИгнорируемыеРегистраторы";
			
			Запрос.УстановитьПараметр("ВсеДублиРасчета", ВсеДублиРасчета);
			ИгнорируемыеРегистраторы = Запрос.Выполнить().Выгрузить();
			
		Иначе
			// пустая таблица исключений
			ИгнорируемыеРегистраторы = Новый ТаблицаЗначений;
			ИгнорируемыеРегистраторы.Колонки.Добавить(
				"Регистратор",
				Новый ОписаниеТипов("ДокументСсылка.ОперацияБух, ДокументСсылка.РегламентнаяОперация"));
		КонецЕсли;
		
		Параметры.Вставить("ИгнорируемыеРегистраторы", ИгнорируемыеРегистраторы);
		
	КонецЕсли;
	
	// Поместим во временную таблицу игнорируемые регистраторы, если это не было сделано выше
	Если МенеджерВременныхТаблиц.Таблицы.Найти("ИгнорируемыеРегистраторы") = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ИгнорируемыеРегистраторы.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ИгнорируемыеРегистраторы
		|ИЗ
		|	&ИгнорируемыеРегистраторы КАК ИгнорируемыеРегистраторы";
		
		Запрос.УстановитьПараметр("ИгнорируемыеРегистраторы", ИгнорируемыеРегистраторы);
		Запрос.Выполнить();
	
	КонецЕсли;
	
	// Созданные элементы справочника льгот кэшируем в виде таблицы в параметрах
	Если Параметры.Свойство("Льготы") Тогда
		Льготы = Параметры.Льготы;
	Иначе
		Льготы = Новый ТаблицаЗначений;
		Льготы.Колонки.Добавить("ОснованиеЛьготы", Новый ОписаниеТипов("СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам"));
		Льготы.Колонки.Добавить("КодЛьготы", ОбщегоНазначения.ОписаниеТипаСтрока(7));
		Льготы.Колонки.Добавить("ВидЛьготы", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЛьготПоИмущественнымНалогам"));
		Льготы.Колонки.Добавить("ПроцентУменьшения", ОбщегоНазначения.ОписаниеТипаЧисло(5, 2, ДопустимыйЗнак.Неотрицательный));
		Льготы.Колонки.Добавить("КодРегиона", ОбщегоНазначения.ОписаниеТипаЧисло(2, 0, ДопустимыйЗнак.Неотрицательный));
		Льготы.Колонки.Добавить("УстановленаМестнымНормативнымАктом", Новый ОписаниеТипов("Булево"));
		Льготы.Колонки.Добавить("ОснованиеМестнойЛьготы", ОбщегоНазначения.ОписаниеТипаСтрока(24));
		
		Параметры.Вставить("Льготы", Льготы);
	КонецЕсли;
	
	// Также кэшируем коды оснований региональных льгот
	Если Параметры.Свойство("КэшОснованийРегиональныхЛьгот") Тогда
		КэшОснованийРегиональныхЛьгот = Параметры.КэшОснованийРегиональныхЛьгот;
	Иначе
		КэшОснованийРегиональныхЛьгот = Новый Соответствие;
		Параметры.Вставить("КэшОснованийРегиональныхЛьгот", КэшОснованийРегиональныхЛьгот);
	КонецЕсли;
	
	Пока ОстатокПорции > 0 Цикл  // может быть прерван и раньше
		
		// Выбираем наиболее "свежий" (по периоду записей) документ (регистратор) к обработке.
		// Так как записи прежнего регистра не удаляем, то в качестве признака необходимости обработки движений
		// документа используем наличие записей в прежнем регистре и отсутствие записей в новом.
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РасчетТранспортногоНалогаДо2020.Регистратор КАК Регистратор,
		|	РасчетТранспортногоНалогаДо2020.ПериодРасчета КАК ПериодРасчета,
		|	МЕСЯЦ(РасчетТранспортногоНалогаДо2020.ПериодРасчета) = 12 КАК ГодовойРасчет,
		|	РасчетТранспортногоНалогаДо2020.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.РасчетТранспортногоНалогаДо2020 КАК РасчетТранспортногоНалогаДо2020
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
		|		ПО РасчетТранспортногоНалогаДо2020.Регистратор = РасчетТранспортногоНалога.Регистратор
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО РасчетТранспортногоНалогаДо2020.Организация = Организации.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
		|		ПО РасчетТранспортногоНалогаДо2020.ОсновноеСредство = ОсновныеСредства.Ссылка
		|ГДЕ
		|	РасчетТранспортногоНалогаДо2020.Активность
		|	И РасчетТранспортногоНалогаДо2020.ПериодРасчета >= &ДатаПерехода
		|	И РасчетТранспортногоНалога.Регистратор ЕСТЬ NULL
		|	И НЕ РасчетТранспортногоНалогаДо2020.Регистратор В
		|		(ВЫБРАТЬ
		|			Регистратор
		|		ИЗ
		|			ИгнорируемыеРегистраторы КАК ИгнорируемыеРегистраторы)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасчетТранспортногоНалогаДо2020.ПериодРасчета УБЫВ,
		|	РасчетТранспортногоНалогаДо2020.Регистратор УБЫВ";
		
		Запрос.УстановитьПараметр("ДатаПерехода", ДатаПерехода);
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			// Нет больше записей для переноса
			Параметры.ОбработкаЗавершена = Истина;
			Возврат;
		КонецЕсли;
		
		ВыборкаРегистратор = Результат.Выбрать();
		ВыборкаРегистратор.Следующий();
		
		Регистратор = ВыборкаРегистратор.Регистратор;
		
		НачатьТранзакцию();
		
		Попытка
			
			// Блокируем записи нового регистра по организации за период расчета.
			// Так как в течение квартала должен быть только 1 расчет налога, то блокируем записи за весь квартал.
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РасчетТранспортногоНалога");
			
			ДиапазонПериод = Новый Диапазон(НачалоКвартала(ВыборкаРегистратор.ПериодРасчета), КонецКвартала(ВыборкаРегистратор.ПериодРасчета));
			ЭлементБлокировки.УстановитьЗначение("ПериодРасчета", ДиапазонПериод);
			ЭлементБлокировки.УстановитьЗначение("Организация", ВыборкаРегистратор.Организация);
			
			Блокировка.Заблокировать();
			
			Запрос = Новый Запрос;
			
			Запрос.УстановитьПараметр("Регистратор", Регистратор);
			
			// Считаем общее количество записей по регистратору в прежнем регистре - для контроля порции обрабатываемых данных 
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ЕСТЬNULL(КОЛИЧЕСТВО(*), 0) КАК КоличествоЗаписей
			|ИЗ
			|	РегистрСведений.РасчетТранспортногоНалогаДо2020 КАК РасчетТранспортногоНалогаДо2020
			|ГДЕ
			|	РасчетТранспортногоНалогаДо2020.Регистратор = &Регистратор
			|	И РасчетТранспортногоНалогаДо2020.Активность";
			
			ИтогиПоРегистратору = Запрос.Выполнить().Выбрать();
			ИтогиПоРегистратору.Следующий();
			
			// Даже если ниже при записи наборов возникнет ошибка, все равно учитываем записи регистратора в общем размере порции.
			КоличествоОбрабатываемыхЗаписей = ИтогиПоРегистратору.КоличествоЗаписей;
			ОстатокПорции = ОстатокПорции - КоличествоОбрабатываемыхЗаписей;
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	РасчетТранспортногоНалога.ПериодРасчета КАК ПериодРасчета,
			|	РасчетТранспортногоНалога.Организация КАК Организация,
			|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
			|	РасчетТранспортногоНалога.ДатаРегистрационныхДанных КАК ДатаРегистрационныхДанных,
			|	РасчетТранспортногоНалога.ИФНС КАК ИФНС,
			|	РасчетТранспортногоНалога.ИФНС.КодРегиона КАК КодРегиона,
			|	РасчетТранспортногоНалога.КодПоОКТМО КАК КодПоОКТМО,
			|	РасчетТранспортногоНалога.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
			|	РасчетТранспортногоНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
			|	РасчетТранспортногоНалога.Марка КАК Марка,
			|	РасчетТранспортногоНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
			|	РасчетТранспортногоНалога.НалоговаяБаза КАК НалоговаяБаза,
			|	РасчетТранспортногоНалога.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	РасчетТранспортногоНалога.ЭкологическийКласс КАК ЭкологическийКласс,
			|	РасчетТранспортногоНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
			|	РасчетТранспортногоНалога.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС,
			|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
			|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
			|	РасчетТранспортногоНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевИспользования,
			|	ВЫБОР
			|		КОГДА РасчетТранспортногоНалога.ЛьготнаяСтавка <> 0
			|			ТОГДА РасчетТранспортногоНалога.ЛьготнаяСтавка
			|		ИНАЧЕ РасчетТранспортногоНалога.НалоговаяСтавка
			|	КОНЕЦ КАК НалоговаяСтавка,
			|	РасчетТранспортногоНалога.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
			|	РасчетТранспортногоНалога.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
			|	РасчетТранспортногоНалога.ПроцентУменьшения КАК ПроцентУменьшения,
			|	РасчетТранспортногоНалога.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
			|	РасчетТранспортногоНалога.СуммаНалоговойЛьготы КАК СуммаНалоговойЛьготы,
			|	ВЫРАЗИТЬ(РасчетТранспортногоНалога.НалоговаяБаза * ВЫБОР
			|		КОГДА РасчетТранспортногоНалога.ЛьготнаяСтавка <> 0
			|			ТОГДА РасчетТранспортногоНалога.ЛьготнаяСтавка
			|		ИНАЧЕ РасчетТранспортногоНалога.НалоговаяСтавка
			|	КОНЕЦ * ВЫБОР
			|		КОГДА РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЧислитель <> 0
			|		И РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель <> 0
			|			ТОГДА РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЧислитель /
			|				РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель
			|		ИНАЧЕ 1
			|	КОНЕЦ * РасчетТранспортногоНалога.КоличествоМесяцевВладения / 12 * ВЫБОР
			|		КОГДА РасчетТранспортногоНалога.ПовышающийКоэффициент = 0
			|			ТОГДА 1
			|		ИНАЧЕ РасчетТранспортногоНалога.ПовышающийКоэффициент
			|	КОНЕЦ КАК ЧИСЛО(15, 0)) КАК СуммаНалогаБезЛьгот,
			|	ВЫБОР
			|		КОГДА &ГодовойРасчет
			|			ТОГДА РасчетТранспортногоНалога.СуммаНалогаКУплате + РасчетТранспортногоНалога.СуммаАвансовыхПлатежей1Кв +
			|				РасчетТранспортногоНалога.СуммаАвансовыхПлатежей2Кв + РасчетТранспортногоНалога.СуммаАвансовыхПлатежей3Кв
			|		ИНАЧЕ РасчетТранспортногоНалога.СуммаНалогаКУплате
			|	КОНЕЦ КАК СуммаНалога,
			|	ВЫБОР
			|		КОГДА &ГодовойРасчет
			|			ТОГДА РасчетТранспортногоНалога.СуммаАвансовыхПлатежей1Кв + РасчетТранспортногоНалога.СуммаАвансовыхПлатежей2Кв +
			|				РасчетТранспортногоНалога.СуммаАвансовыхПлатежей3Кв
			|		ИНАЧЕ РасчетТранспортногоНалога.СуммаНалогаКУплате
			|	КОНЕЦ КАК СуммаАвансовыхПлатежей,
			|	РасчетТранспортногоНалога.СуммаНалогаКУплате КАК СуммаНалогаКУплате,
			|	РасчетТранспортногоНалога.ДатаРегистрации КАК ДатаРегистрации,
			|	РасчетТранспортногоНалога.ДатаПрекращенияРегистрации КАК ДатаСнятияСУчета
			|ПОМЕСТИТЬ ВТ_ПрежнийРегистр
			|ИЗ
			|	РегистрСведений.РасчетТранспортногоНалогаДо2020 КАК РасчетТранспортногоНалога
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
			|		ПО РасчетТранспортногоНалога.Организация = Организации.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
			|		ПО РасчетТранспортногоНалога.ОсновноеСредство = ОсновныеСредства.Ссылка
			|ГДЕ
			|	РасчетТранспортногоНалога.Активность
			|	И РасчетТранспортногоНалога.Регистратор = &Регистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетНалога.ПериодРасчета КАК ПериодРасчета,
			|	РасчетНалога.Организация КАК Организация,
			|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
			|	РасчетНалога.ДатаРегистрационныхДанных КАК ДатаРегистрационныхДанных,
			|	АВТОНОМЕРЗАПИСИ() КАК Ключ
			|ПОМЕСТИТЬ ГруппировкаПоДатамРегистрационныхДанных
			|ИЗ
			|	ВТ_ПрежнийРегистр КАК РасчетНалога
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ОсновноеСредство,
			|	Организация,
			|	ПериодРасчета,
			|	ДатаРегистрационныхДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РасчетТранспортногоНалога.ПериодРасчета КАК ПериодРасчета,
			|	РасчетТранспортногоНалога.Организация КАК Организация,
			|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
			|	РасчетТранспортногоНалога.ДатаРегистрационныхДанных КАК ДатаРегистрационныхДанных,
			|	РасчетТранспортногоНалога.ИФНС КАК ИФНС,
			|	РасчетТранспортногоНалога.КодРегиона КАК КодРегиона,
			|	РасчетТранспортногоНалога.КодПоОКТМО КАК КодПоОКТМО,
			|	РасчетТранспортногоНалога.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
			|	РасчетТранспортногоНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
			|	РасчетТранспортногоНалога.Марка КАК Марка,
			|	РасчетТранспортногоНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
			|	РасчетТранспортногоНалога.НалоговаяБаза КАК НалоговаяБаза,
			|	РасчетТранспортногоНалога.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	РасчетТранспортногоНалога.ЭкологическийКласс КАК ЭкологическийКласс,
			|	РасчетТранспортногоНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
			|	РасчетТранспортногоНалога.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС,
			|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
			|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
			|	РасчетТранспортногоНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
			|	РасчетТранспортногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
			|	РасчетТранспортногоНалога.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
			|	РасчетТранспортногоНалога.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
			|	РасчетТранспортногоНалога.ПроцентУменьшения КАК ПроцентУменьшения,
			|	РасчетТранспортногоНалога.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
			|	РасчетТранспортногоНалога.СуммаНалоговойЛьготы КАК СуммаНалоговойЛьготы,
			|	РасчетТранспортногоНалога.СуммаНалогаБезЛьгот КАК СуммаНалогаБезЛьгот,
			|	РасчетТранспортногоНалога.СуммаНалогаБезЛьгот - РасчетТранспортногоНалога.СуммаНалога КАК СуммаЛьгот,
			|	РасчетТранспортногоНалога.СуммаНалога КАК СуммаНалога,
			|	РасчетТранспортногоНалога.СуммаАвансовыхПлатежей КАК СуммаАвансовыхПлатежей,
			|	РасчетТранспортногоНалога.СуммаНалогаКУплате КАК СуммаНалогаКУплате,
			|	РасчетТранспортногоНалога.ДатаРегистрации КАК ДатаРегистрации,
			|	РасчетТранспортногоНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
			|	ГруппировкаПоДатамРегистрационныхДанных.Ключ КАК КлючГруппировки,
			|	1 КАК КоличествоЗаписей
			|ИЗ
			|	ВТ_ПрежнийРегистр КАК РасчетТранспортногоНалога
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппировкаПоДатамРегистрационныхДанных КАК ГруппировкаПоДатамРегистрационныхДанных
			|		ПО РасчетТранспортногоНалога.ОсновноеСредство = ГруппировкаПоДатамРегистрационныхДанных.ОсновноеСредство
			|		И РасчетТранспортногоНалога.Организация = ГруппировкаПоДатамРегистрационныхДанных.Организация
			|		И РасчетТранспортногоНалога.ПериодРасчета = ГруппировкаПоДатамРегистрационныхДанных.ПериодРасчета
			|		И РасчетТранспортногоНалога.ДатаРегистрационныхДанных = ГруппировкаПоДатамРегистрационныхДанных.ДатаРегистрационныхДанных
			|ИТОГИ
			|	МАКСИМУМ(ПериодРасчета),
			|	МАКСИМУМ(Организация),
			|	МАКСИМУМ(ОсновноеСредство),
			|	МАКСИМУМ(ДатаРегистрационныхДанных),
			|	МАКСИМУМ(ДатаРегистрации),
			|	СУММА(КоличествоМесяцевИспользования),
			|	СУММА(КоличествоЗаписей)
			|ПО
			|	КлючГруппировки";
			
			Запрос.УстановитьПараметр("Организация", ВыборкаРегистратор.Организация);
			Запрос.УстановитьПараметр("ПериодРасчета", ВыборкаРегистратор.ПериодРасчета);
			Запрос.УстановитьПараметр("ГодовойРасчет", ВыборкаРегистратор.ГодовойРасчет);
			
			ВыборкаПоДатамРегистрационныхДанных = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			НовыйРегистр = СоздатьНаборЗаписей();
			
			НовыйРегистр.Отбор.Регистратор.Установить(Регистратор);
			
			КодыВидовИКатегорииТС = РасчетИмущественныхНалогов.КодыВидовИКатегорииТранспортныхСредств();
			
			Пока ВыборкаПоДатамРегистрационныхДанных.Следующий() Цикл
				
				// Если запись одна, то ДатаСведений = ДатаРегистрационныхДанных.
				// Если записей несколько, то пробуем раскидать даты сведений по отчетному периоду на основании
				// количества месяцев применения конкретной записи расчета.
				// Если сумма по КоличествоМесяцевИспользования больше длины периода (такие ошибки могли возникать в
				// прежнем расчете), то устанавливаем ДатаСведений техногенно: начинаем от начала периода и просто
				// инкрементируем дату для каждой следующей записи.
				
				Если ВыборкаПоДатамРегистрационныхДанных.КоличествоЗаписей > 1 Тогда 
					НачалоПериода = Макс(
						НачалоКвартала(ВыборкаПоДатамРегистрационныхДанных.ПериодРасчета),
						ВыборкаПоДатамРегистрационныхДанных.ДатаРегистрационныхДанных);
					Если Месяц(КонецКвартала(ВыборкаПоДатамРегистрационныхДанных.ПериодРасчета)) = 12 Тогда
						НачалоПериода = Макс(
							НачалоГода(ВыборкаПоДатамРегистрационныхДанных.ПериодРасчета),
							ВыборкаПоДатамРегистрационныхДанных.ДатаРегистрационныхДанных);
					КонецЕсли;
					МесяцевВПериоде = Месяц(ВыборкаПоДатамРегистрационныхДанных.ПериодРасчета) - Месяц(НачалоПериода) + 1;
				КонецЕсли;
				
				ВыборкаЗаписей = ВыборкаПоДатамРегистрационныхДанных.Выбрать();
				ДатаСведений = НачалоПериода;
				Пока ВыборкаЗаписей.Следующий() Цикл
					НоваяЗапись = НовыйРегистр.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаЗаписей);
					
					Если ВыборкаПоДатамРегистрационныхДанных.КоличествоЗаписей = 1 Тогда
						// Считаем, что ДатаРегистрационныхДанных корректна
						НоваяЗапись.ДатаСведений = ВыборкаЗаписей.ДатаРегистрационныхДанных;
					Иначе // более одной записи
						НоваяЗапись.ДатаСведений = ДатаСведений;
						Если ВыборкаПоДатамРегистрационныхДанных.КоличествоМесяцевИспользования > МесяцевВПериоде Тогда
							ДатаСведений = ДобавитьМесяц(ДатаСведений, 1);
						Иначе
							ДатаСведений = ДобавитьМесяц(ДатаСведений, ВыборкаЗаписей.КоличествоМесяцевИспользования);
						КонецЕсли;
					КонецЕсли;
					
					// Определим ВидТранспортногоСредства
					СтрокаВидаТС = КодыВидовИКатегорииТС.Найти(ВыборкаЗаписей.КодВидаТранспортногоСредства, "КодВида");
					Если СтрокаВидаТС = Неопределено Тогда
						ТекстСообщения = Новый Массив;
						Описание = НСтр("ru = 'Не удалось обработать запись регистра ""Расчет транспортного налога""
							|(период расчета: %1, организация: %2, основное средство: %3) по причине:'");
						
						Описание = СтрЗаменить(Описание, Символы.ПС, " ");
						Описание = СтрШаблон(Описание, 
							Формат(ВыборкаЗаписей.ПериодРасчета, "ДЛФ=D"),
							ВыборкаЗаписей.Организация,
							ВыборкаЗаписей.ОсновноеСредство);
						
						ТекстСообщения.Добавить(Описание);
						ТекстСообщения.Добавить(СтрШаблон(
							НСтр("ru = 'Не найден вид ТС для кода ""%1""'"),
							ВыборкаЗаписей.КодВидаТранспортногоСредства));
						
						ТекстСообщения = СтрСоединить(ТекстСообщения, Символы.ПС);
							
						ЗаписьЖурналаРегистрации(
							ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
							УровеньЖурналаРегистрации.Предупреждение,
							Метаданные.РегистрыСведений.РасчетТранспортногоНалога,
							,
							ТекстСообщения);
					Иначе
						НоваяЗапись.ВидТранспортногоСредства = Перечисления.ВидыТранспортныхСредств[СтрокаВидаТС.Вид];
					КонецЕсли;
					
					// Заполняем ссылки на справочник льгот
					ЕстьЛьгота = Ложь;
						
					// Про логику переноса льгот в новые объекты см. в
					// РегистрыСведений.РегистрацияТранспортныхСредств.ЗаполнитьНовыеРеквизиты()
					Если ЗначениеЗаполнено(ВыборкаЗаписей.КодНалоговойЛьготы)
						 И ВыборкаЗаписей.СуммаНалоговойЛьготы <> 0 Тогда 
						ЕстьЛьгота = Истина;
						ПоискЛьготы = СтруктураПоискаЛьготы(ВыборкаЗаписей, КэшОснованийРегиональныхЛьгот);
					КонецЕсли;
						
					Если ЕстьЛьгота Тогда
						НоваяЗапись.ОснованиеЛьготы = ЛьготыПоИмущественнымНалогам.НайтиИлиСоздатьЛьготу(
							Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог,
							ПоискЛьготы,
							Льготы);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
				
			Если НовыйРегистр.Количество() > 0 Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НовыйРегистр, Истина);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
			Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + 1;
			КоличествоОбрабатываемыхЗаписей = 0;
			
		Исключение
			
			ОтменитьТранзакцию();
			
			// Если не удалось перенести записи в новый регистр, повторим попытку снова (позже).
			КоличествоОшибок = КоличествоОшибок + 1;
			
			// Учтем необработанные записи в общем размере порции.
			
			// Возможно, ошибка возникла уже после того, как было рассчитано количество записей регистратора.
			// В этом случае КоличествоОбрабатываемыхЗаписей не равно 0. В ином случае рассчитаем количество записей здесь. 
			Если КоличествоОбрабатываемыхЗаписей = 0 Тогда
				
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("Регистратор", Регистратор);
				
				Запрос.Текст =
				"ВЫБРАТЬ
				|	ЕСТЬNULL(КОЛИЧЕСТВО(*), 0) КАК КоличествоЗаписей
				|ИЗ
				|	РегистрСведений.РасчетТранспортногоНалогаДо2020 КАК РасчетТранспортногоНалогаДо2020
				|ГДЕ
				|	РасчетТранспортногоНалогаДо2020.Регистратор = &Регистратор
				|	И РасчетТранспортногоНалогаДо2020.Активность";
				
				ИтогиПоРегистратору = Запрос.Выполнить().Выбрать();
				ИтогиПоРегистратору.Следующий();
				
				ОстатокПорции = ОстатокПорции - ИтогиПоРегистратору.КоличествоЗаписей;
				
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось перенести данные в регистр ""Расчет транспортного налога"" по документу: %1 по причине:
				|%2'"), 
				Строка(Регистратор),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Регистратор.Метаданные(),
				Регистратор,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ИгнорируемыеРегистраторы.Количество();
	Параметры.ОбработкаЗавершена = (ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ВсегоОбъектов);
	
	Если КоличествоОшибок > 0 Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ""РегистрСведений.РасчетТранспортногоНалога.ЗаполнитьДаннымиПрежнегоРегистра""
			|не удалось перенести данные из прежнего регистра. Количество ошибок: %1'"), 
			КоличествоОшибок);
			
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураПоискаЛьготы(ВыборкаЗаписей, КэшОснованийРегиональныхЛьгот)
	
	ПоискЛьготы = Новый Структура;
	
	РегиональнаяЛьгота = СтрНачинаетсяС(ВыборкаЗаписей.КодНалоговойЛьготы, "2");
	ОснованиеМестнойЛьготы = "";
	
	Если РегиональнаяЛьгота Тогда
		ПоискЛьготы.Вставить("КодРегиона", ВыборкаЗаписей.КодРегиона);
		ПоискЛьготы.Вставить("УстановленаМестнымНормативнымАктом", Истина);
		Если ЗначениеЗаполнено(ВыборкаЗаписей.РегиональныйКодЛьготы) Тогда
			ОснованиеМестнойЛьготы = КэшОснованийРегиональныхЛьгот.Получить(ВыборкаЗаписей.РегиональныйКодЛьготы);
			Если ОснованиеМестнойЛьготы = Неопределено Тогда
				ОснованиеМестнойЛьготы = ЛьготыПоИмущественнымНалогамКлиентСервер.ПолныйКодОснованияЛьготы(
					ВыборкаЗаписей.РегиональныйКодЛьготы);
				КэшОснованийРегиональныхЛьгот.Вставить(ВыборкаЗаписей.РегиональныйКодЛьготы, ОснованиеМестнойЛьготы);
			КонецЕсли;
			ПоискЛьготы.Вставить("ОснованиеМестнойЛьготы", ОснованиеМестнойЛьготы);
		КонецЕсли;
	КонецЕсли;
	
	Если ВыборкаЗаписей.КодНалоговойЛьготы = "20200" Или ВыборкаЗаписей.КодНалоговойЛьготы = "20210" Тогда
		
		// Региональная льгота в виде освобождения
		
		// В прежних настройках можно было выбрать группу "20200", хотя код должен быть строго "20210"
		ПоискЛьготы.Вставить("КодЛьготы", "20210");
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.Освобождение);
		
	ИначеЕсли ВыборкаЗаписей.КодНалоговойЛьготы = "20230" И ВыборкаЗаписей.ЛьготнаяСтавка <> 0 Тогда
		
		// Местная льгота в виде снижения ставки
		
		ПоискЛьготы.Вставить("КодЛьготы", "20230");
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной);
		
	ИначеЕсли ВыборкаЗаписей.КодНалоговойЛьготы = "20220" И ВыборкаЗаписей.ПроцентУменьшения <> 0 Тогда
		
		// Местная льгота в виде снижения суммы налога на процент
		
		ПоискЛьготы.Вставить("КодЛьготы", "20220");
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент);
		ПоискЛьготы.Вставить("ПроцентУменьшения", ВыборкаЗаписей.ПроцентУменьшения);
	
	ИначеЕсли ВыборкаЗаписей.КодНалоговойЛьготы = "20220" Тогда
		
		// Местная льгота в виде снижения суммы налога на указанную сумму
		
		ПоискЛьготы.Вставить("КодЛьготы", "20220");
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму);
		
	Иначе
		// Федеральная льгота в виде освобождения 
		ПоискЛьготы.Вставить("КодЛьготы", ВыборкаЗаписей.КодНалоговойЛьготы);
		ПоискЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам.Освобождение);
		
	КонецЕсли;
	
	Возврат ПоискЛьготы;
	
КонецФункции

#КонецОбласти

#КонецЕсли