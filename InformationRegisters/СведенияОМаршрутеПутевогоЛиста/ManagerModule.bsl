#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбработчикиОбновления

// Заполняет сведения о маршрутах путевых листов, проведенных за последний год,
// в которых указаны пункты отправления и назначения.
//
// Параметры:
//  Параметры - Структура - параметры обработчика обновления.
//
Процедура ЗаполнитьСведенияОМаршрутахПутевыхЛистов(Параметры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПутевойЛистМаршрут.ПунктОтправления КАК ПунктОтправления,
	|	ПутевойЛистМаршрут.ПунктНазначения КАК ПунктНазначения,
	|	ПутевойЛистМаршрут.Расстояние КАК Расстояние,
	|	ПутевойЛистМаршрут.ВидСообщения КАК ВидСообщения,
	|	ПутевойЛистМаршрут.НомерСтроки КАК НомерСтроки,
	|	ПутевойЛист.Дата КАК Дата,
	|	ПутевойЛист.Организация КАК Организация,
	|	ПутевойЛист.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВсеМаршруты
	|ИЗ
	|	Документ.ПутевойЛист.Маршрут КАК ПутевойЛистМаршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПутевойЛист КАК ПутевойЛист
	|		ПО ПутевойЛист.Ссылка = ПутевойЛистМаршрут.Ссылка
	|		И ПутевойЛист.Проведен
	|		И ПутевойЛистМаршрут.ПунктОтправления <> """"
	|		И ПутевойЛистМаршрут.ПунктНазначения <> """"
	|		И ПутевойЛист.Дата >= &НачалоОтбораДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Дата,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеМаршруты.ПунктОтправления КАК ПунктОтправления,
	|	ВсеМаршруты.ПунктНазначения КАК ПунктНазначения,
	|	МАКСИМУМ(ВсеМаршруты.Дата) КАК Дата
	|ПОМЕСТИТЬ НедавниеМаршруты
	|ИЗ
	|	ВсеМаршруты КАК ВсеМаршруты
	|СГРУППИРОВАТЬ ПО
	|	ВсеМаршруты.ПунктОтправления,
	|	ВсеМаршруты.ПунктНазначения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Дата,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НедавниеМаршруты.ПунктОтправления КАК ПунктОтправления,
	|	НедавниеМаршруты.ПунктНазначения КАК ПунктНазначения,
	|	МАКСИМУМ(ВсеМаршруты.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ПоследниеРейсыПоМаршрутам
	|ИЗ
	|	НедавниеМаршруты КАК НедавниеМаршруты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеМаршруты КАК ВсеМаршруты
	|		ПО НедавниеМаршруты.Дата = ВсеМаршруты.Дата
	|		И НедавниеМаршруты.ПунктОтправления = ВсеМаршруты.ПунктОтправления
	|		И НедавниеМаршруты.ПунктНазначения = ВсеМаршруты.ПунктНазначения
	|СГРУППИРОВАТЬ ПО
	|	НедавниеМаршруты.ПунктОтправления,
	|	НедавниеМаршруты.ПунктНазначения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НедавниеМаршруты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследниеРейсыПоМаршрутам.ПунктОтправления КАК ПунктОтправления,
	|	ПоследниеРейсыПоМаршрутам.ПунктНазначения КАК ПунктНазначения,
	|	ПоследниеРейсыПоМаршрутам.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВсеМаршруты.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ПоследниеПоказанияПоРейсам
	|ИЗ
	|	ПоследниеРейсыПоМаршрутам КАК ПоследниеРейсыПоМаршрутам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеМаршруты КАК ВсеМаршруты
	|		ПО ПоследниеРейсыПоМаршрутам.Ссылка = ВсеМаршруты.Ссылка
	|		И ПоследниеРейсыПоМаршрутам.ПунктОтправления = ВсеМаршруты.ПунктОтправления
	|		И ПоследниеРейсыПоМаршрутам.ПунктНазначения = ВсеМаршруты.ПунктНазначения
	|СГРУППИРОВАТЬ ПО
	|	ПоследниеРейсыПоМаршрутам.ПунктОтправления,
	|	ПоследниеРейсыПоМаршрутам.ПунктНазначения,
	|	ПоследниеРейсыПоМаршрутам.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПоследниеРейсыПоМаршрутам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследниеПоказанияПоРейсам.ПунктОтправления КАК ПунктОтправления,
	|	ПоследниеПоказанияПоРейсам.ПунктНазначения КАК ПунктНазначения,
	|	ВсеМаршруты.Расстояние КАК Расстояние,
	|	ВсеМаршруты.ВидСообщения КАК ВидСообщения,
	|	ВсеМаршруты.Дата КАК ДатаРейса,
	|	ВсеМаршруты.Организация КАК Организация
	|ИЗ
	|	ПоследниеПоказанияПоРейсам КАК ПоследниеПоказанияПоРейсам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеМаршруты КАК ВсеМаршруты
	|		ПО ПоследниеПоказанияПоРейсам.Ссылка = ВсеМаршруты.Ссылка
	|		И ПоследниеПоказанияПоРейсам.ПунктОтправления = ВсеМаршруты.ПунктОтправления
	|		И ПоследниеПоказанияПоРейсам.ПунктНазначения = ВсеМаршруты.ПунктНазначения
	|		И ПоследниеПоказанияПоРейсам.НомерСтроки = ВсеМаршруты.НомерСтроки");
	
	Запрос.УстановитьПараметр("НачалоОтбораДокументов", ДобавитьМесяц(НачалоДня(ТекущаяДатаСеанса()), -12));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	ИмяПроцедуры = "РегистрыСведений.СведенияОМаршрутеПутевогоЛиста.ЗаполнитьСведенияОМаршрутахПутевыхЛистов()";
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(РезультатЗапроса.Выгрузить());
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Уровень = УровеньЖурналаРегистрации.Предупреждение;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось обработать маршруты, возникла ошибка: %2'"),
			ИмяПроцедуры);
	Иначе
		Уровень = УровеньЖурналаРегистрации.Информация;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедура %1 обработала первоначальное заполнение: добавлено %2 маршрутов'"),
			ИмяПроцедуры, НаборЗаписей.Количество());
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Истина;
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
		Уровень,
		Метаданные.РегистрыСведений.СведенияОМаршрутеПутевогоЛиста, ,
		ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Записывает в регистр сведения о маршруте путевого листа.
// 
// Параметры:
//  Организация       - СправочникСсылка.Организации - Организация, для которой регистрируются сведения о маршруте.
//  ДатаРейса         - Дата - Дата рейса, для которой регистрируются сведения о маршруте.
//  ДанныеМаршрута    - ТаблицаЗначений - Таблица, содержащая сведения о маршруте путевого листа.
//  РеквизитыДоЗаписи - Структура, Неопределено - Реквизиты документа до его изменения.
//
Процедура ЗарегистрироватьСведенияОМаршруте(Организация, ДатаРейса, ДанныеМаршрута, РеквизитыДоЗаписи) Экспорт
	
	// Удалим сведения о маршруте, которые были введены по документу ранее.
	Если ЗначениеЗаполнено(РеквизитыДоЗаписи) Тогда
		УдалитьТекущиеСведенияМаршрута(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РеквизитыДоЗаписи, "Организация"),
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РеквизитыДоЗаписи, "Дата"),
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РеквизитыДоЗаписи, "Маршрут"));
	КонецЕсли;
	
	// Сохраняем крайние показатели по маршруту от пункта отправления до пункта назначения.
	// Если в документе маршрут повторяется, то считаем показания по последней строке актуальнее.
	МаршрутКСохранению = ДанныеМаршрута.СкопироватьКолонки();
	
	Для Каждого ТекущаяСтрока Из ДанныеМаршрута Цикл
		НоваяСтрока = НайтиСтрокуМаршрута(МаршрутКСохранению,
			ТекущаяСтрока.Контрагент,
			ТекущаяСтрока.ПунктОтправления,
			ТекущаяСтрока.ПунктНазначения);
		Если Не ЗначениеЗаполнено(НоваяСтрока) Тогда
			НоваяСтрока = МаршрутКСохранению.Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЦикла;
	
	// Запишем новые сведения о маршруте
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Загрузить(СведенияОМаршрутеКЗаписи(Организация, ДатаРейса, МаршрутКСохранению));
	
	НаборЗаписей.Записать(РежимЗамещения.Слияние);
	
КонецПроцедуры

// Записывает в регистр сведения о маршруте путевого листа, в котором отменяется проведение документа.
// 
// Параметры:
//  Организация    - СправочникСсылка.Организации - Организация, для которой регистрируются сведения о маршруте.
//  ДатаРейса      - Дата - Дата рейса, для которой регистрируются сведения о маршруте.
//  ДанныеМаршрута - ТаблицаЗначений - Таблица, содержащая сведения о маршруте путевого листа.
//
Процедура ОтменитьСведенияОМаршруте(Организация, ДатаРейса, ДанныеМаршрута) Экспорт
	
	// Удалим сведения о маршруте, которые были введены по документу ранее.
	УдалитьТекущиеСведенияМаршрута(Организация, ДатаРейса, ДанныеМаршрута);
	
	// Выбираем крайние сведения по маршруту, которые были указаны до отменяемого путевого листа.
	ДанныеКОбновлению = СведенияОМаршрутеКОбновлению(Организация, ДатаРейса, ДанныеМаршрута);
	Если ЗначениеЗаполнено(ДанныеКОбновлению) Тогда
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Загрузить(ДанныеКОбновлению);
		НаборЗаписей.Записать(РежимЗамещения.Слияние);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СведенияОМаршрутеКУдалению(Организация, ДатаРейса, УдаляемыеДанные)
	
	Если Не ЗначениеЗаполнено(УдаляемыеДанные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	УдаляемыеДанные.Контрагент КАК Контрагент,
	|	УдаляемыеДанные.ПунктОтправления КАК ПунктОтправления,
	|	УдаляемыеДанные.ПунктНазначения КАК ПунктНазначения,
	|	УдаляемыеДанные.Расстояние КАК Расстояние,
	|	УдаляемыеДанные.ВидСообщения КАК ВидСообщения
	|ПОМЕСТИТЬ УдаляемыеДанные
	|ИЗ
	|	&УдаляемыеДанные КАК УдаляемыеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОМаршрутеПутевогоЛиста.Организация,
	|	СведенияОМаршрутеПутевогоЛиста.Контрагент КАК Контрагент,
	|	СведенияОМаршрутеПутевогоЛиста.ПунктОтправления КАК ПунктОтправления,
	|	СведенияОМаршрутеПутевогоЛиста.ПунктНазначения КАК ПунктНазначения,
	|	СведенияОМаршрутеПутевогоЛиста.ВидСообщения,
	|	СведенияОМаршрутеПутевогоЛиста.Расстояние,
	|	СведенияОМаршрутеПутевогоЛиста.ДатаРейса
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	УдаляемыеДанные КАК УдаляемыеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОМаршрутеПутевогоЛиста КАК СведенияОМаршрутеПутевогоЛиста
	|		ПО СведенияОМаршрутеПутевогоЛиста.Организация = &Организация
	|		И УдаляемыеДанные.Контрагент = СведенияОМаршрутеПутевогоЛиста.Контрагент
	|		И УдаляемыеДанные.ПунктОтправления = СведенияОМаршрутеПутевогоЛиста.ПунктОтправления
	|		И УдаляемыеДанные.ПунктНазначения = СведенияОМаршрутеПутевогоЛиста.ПунктНазначения
	|		И УдаляемыеДанные.ВидСообщения = СведенияОМаршрутеПутевогоЛиста.ВидСообщения
	|		И УдаляемыеДанные.Расстояние = СведенияОМаршрутеПутевогоЛиста.Расстояние
	|		И СведенияОМаршрутеПутевогоЛиста.ДатаРейса = &ДатаРейса
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Контрагент,
	|	ДанныеРегистра.ПунктОтправления,
	|	ДанныеРегистра.ПунктНазначения,
	|	ДанныеРегистра.ВидСообщения,
	|	ДанныеРегистра.Расстояние,
	|	ДанныеРегистра.ДатаРейса,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПутевойЛист.Ссылка) КАК Ссылка
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПутевойЛист.Маршрут КАК ПутевойЛистМаршрут
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПутевойЛист КАК ПутевойЛист
	|			ПО ПутевойЛистМаршрут.Ссылка = ПутевойЛист.Ссылка
	|			И ПутевойЛист.Проведен
	|		ПО ДанныеРегистра.Контрагент = ПутевойЛистМаршрут.Контрагент
	|		И ДанныеРегистра.ПунктОтправления = ПутевойЛистМаршрут.ПунктОтправления
	|		И ДанныеРегистра.ПунктНазначения = ПутевойЛистМаршрут.ПунктНазначения
	|		И ДанныеРегистра.ВидСообщения = ПутевойЛистМаршрут.ВидСообщения
	|		И ДанныеРегистра.Расстояние = ПутевойЛистМаршрут.Расстояние
	|		И ПутевойЛист.Организация = ДанныеРегистра.Организация
	|		И ПутевойЛист.Дата = ДанныеРегистра.ДатаРейса
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Контрагент,
	|	ДанныеРегистра.ПунктОтправления,
	|	ДанныеРегистра.ПунктНазначения,
	|	ДанныеРегистра.ВидСообщения,
	|	ДанныеРегистра.Расстояние,
	|	ДанныеРегистра.ДатаРейса
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПутевойЛист.Ссылка) < 2");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаРейса", ДатаРейса);
	Запрос.УстановитьПараметр("УдаляемыеДанные", УдаляемыеДанные);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СведенияОМаршрутеКЗаписи(Организация, ДатаРейса, ИзменяемыеДанные)
	
	// Выбираем записи маршрута ПунктОтправления-ПунктНазначения по последним рейсам к контрагентам
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИзменяемыеДанные.Контрагент КАК Контрагент,
	|	ИзменяемыеДанные.ПунктОтправления КАК ПунктОтправления,
	|	ИзменяемыеДанные.ПунктНазначения КАК ПунктНазначения,
	|	ИзменяемыеДанные.Расстояние,
	|	ИзменяемыеДанные.ВидСообщения
	|ПОМЕСТИТЬ ДанныеМаршрута
	|ИЗ
	|	&ИзменяемыеДанные КАК ИзменяемыеДанные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ДанныеМаршрута.Контрагент,
	|	ДанныеМаршрута.ПунктОтправления,
	|	ДанныеМаршрута.ПунктНазначения,
	|	&ДатаРейса КАК ДатаРейса,
	|	ДанныеМаршрута.Расстояние,
	|	ДанныеМаршрута.ВидСообщения
	|ИЗ
	|	ДанныеМаршрута КАК ДанныеМаршрута
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОМаршрутеПутевогоЛиста КАК СведенияОМаршрутеПутевогоЛиста
	|		ПО СведенияОМаршрутеПутевогоЛиста.Организация = &Организация
	|		И ДанныеМаршрута.Контрагент = СведенияОМаршрутеПутевогоЛиста.Контрагент
	|		И ДанныеМаршрута.ПунктОтправления = СведенияОМаршрутеПутевогоЛиста.ПунктОтправления
	|		И ДанныеМаршрута.ПунктНазначения = СведенияОМаршрутеПутевогоЛиста.ПунктНазначения
	|		И СведенияОМаршрутеПутевогоЛиста.ДатаРейса > &ДатаРейса
	|ГДЕ
	|	СведенияОМаршрутеПутевогоЛиста.Расстояние ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаРейса", ДатаРейса);
	Запрос.УстановитьПараметр("ИзменяемыеДанные", ИзменяемыеДанные);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СведенияОМаршрутеКОбновлению(Организация, ДатаРейса, ОтменяемыеДанные)
	
	// Выбираем крайние сведения о маршруте по проведенным документам
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтменяемыеДанные.Контрагент КАК Контрагент,
	|	ОтменяемыеДанные.ПунктОтправления КАК ПунктОтправления,
	|	ОтменяемыеДанные.ПунктНазначения КАК ПунктНазначения
	|ПОМЕСТИТЬ ДанныеМаршрута
	|ИЗ
	|	&ОтменяемыеДанные КАК ОтменяемыеДанные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПутевойЛистМаршрут.Контрагент КАК Контрагент,
	|	ПутевойЛистМаршрут.ПунктОтправления КАК ПунктОтправления,
	|	ПутевойЛистМаршрут.ПунктНазначения КАК ПунктНазначения,
	|	ПутевойЛистМаршрут.Расстояние КАК Расстояние,
	|	ПутевойЛистМаршрут.ВидСообщения КАК ВидСообщения,
	|	ПутевойЛистМаршрут.НомерСтроки КАК НомерСтроки,
	|	ПутевойЛист.Организация КАК Организация,
	|	ПутевойЛист.Дата КАК ДатаРейса,
	|	ПутевойЛист.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВсеМаршруты
	|ИЗ
	|	Документ.ПутевойЛист.Маршрут КАК ПутевойЛистМаршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПутевойЛист КАК ПутевойЛист
	|		ПО ПутевойЛистМаршрут.Ссылка = ПутевойЛист.Ссылка
	|			И (ПутевойЛист.Проведен)
	|			И (ПутевойЛист.Дата МЕЖДУ &НачалоОтбораДокументов И &ДатаРейса)
	|			И (ПутевойЛист.Организация = &Организация)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеМаршрута КАК ДанныеМаршрута
	|		ПО (ДанныеМаршрута.Контрагент = ПутевойЛистМаршрут.Контрагент)
	|			И (ДанныеМаршрута.ПунктОтправления = ПутевойЛистМаршрут.ПунктОтправления)
	|			И (ДанныеМаршрута.ПунктНазначения = ПутевойЛистМаршрут.ПунктНазначения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	ПунктОтправления,
	|	ПунктНазначения,
	|	ДатаРейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеМаршруты.Контрагент КАК Контрагент,
	|	ВсеМаршруты.ПунктОтправления КАК ПунктОтправления,
	|	ВсеМаршруты.ПунктНазначения КАК ПунктНазначения,
	|	МАКСИМУМ(ВсеМаршруты.ДатаРейса) КАК ДатаРейса
	|ПОМЕСТИТЬ НедавниеМаршруты
	|ИЗ
	|	ВсеМаршруты КАК ВсеМаршруты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеМаршруты.Контрагент,
	|	ВсеМаршруты.ПунктОтправления,
	|	ВсеМаршруты.ПунктНазначения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	ПунктОтправления,
	|	ПунктНазначения,
	|	ДатаРейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеМаршрута
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеМаршруты.Контрагент КАК Контрагент,
	|	ВсеМаршруты.ПунктОтправления КАК ПунктОтправления,
	|	ВсеМаршруты.ПунктНазначения КАК ПунктНазначения,
	|	ВсеМаршруты.ДатаРейса КАК ДатаРейса,
	|	МАКСИМУМ(ВсеМаршруты.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ПоследниеРейсыПоМаршрутам
	|ИЗ
	|	ВсеМаршруты КАК ВсеМаршруты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НедавниеМаршруты КАК НедавниеМаршруты
	|		ПО ВсеМаршруты.Контрагент = НедавниеМаршруты.Контрагент
	|			И ВсеМаршруты.ПунктОтправления = НедавниеМаршруты.ПунктОтправления
	|			И ВсеМаршруты.ПунктНазначения = НедавниеМаршруты.ПунктНазначения
	|			И ВсеМаршруты.ДатаРейса = НедавниеМаршруты.ДатаРейса
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеМаршруты.Контрагент,
	|	ВсеМаршруты.ПунктОтправления,
	|	ВсеМаршруты.ПунктНазначения,
	|	ВсеМаршруты.ДатаРейса
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НедавниеМаршруты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследниеРейсыПоМаршрутам.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВсеМаршруты.НомерСтроки) КАК НомерСтроки,
	|	ПоследниеРейсыПоМаршрутам.Контрагент КАК Контрагент,
	|	ПоследниеРейсыПоМаршрутам.ПунктОтправления КАК ПунктОтправления,
	|	ПоследниеРейсыПоМаршрутам.ПунктНазначения КАК ПунктНазначения
	|ПОМЕСТИТЬ ПоследниеПоказанияПоРейсам
	|ИЗ
	|	ПоследниеРейсыПоМаршрутам КАК ПоследниеРейсыПоМаршрутам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеМаршруты КАК ВсеМаршруты
	|		ПО ПоследниеРейсыПоМаршрутам.Контрагент = ВсеМаршруты.Контрагент
	|			И ПоследниеРейсыПоМаршрутам.ПунктОтправления = ВсеМаршруты.ПунктОтправления
	|			И ПоследниеРейсыПоМаршрутам.ПунктНазначения = ВсеМаршруты.ПунктНазначения
	|			И ПоследниеРейсыПоМаршрутам.Ссылка = ВсеМаршруты.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоследниеРейсыПоМаршрутам.ПунктОтправления,
	|	ПоследниеРейсыПоМаршрутам.ПунктНазначения,
	|	ПоследниеРейсыПоМаршрутам.Ссылка,
	|	ПоследниеРейсыПоМаршрутам.Контрагент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	ПунктОтправления,
	|	ПунктНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПоследниеРейсыПоМаршрутам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеМаршруты.Организация КАК Организация,
	|	ПоследниеПоказанияПоРейсам.Контрагент КАК Контрагент,
	|	ПоследниеПоказанияПоРейсам.ПунктОтправления КАК ПунктОтправления,
	|	ПоследниеПоказанияПоРейсам.ПунктНазначения КАК ПунктНазначения,
	|	ВсеМаршруты.Расстояние КАК Расстояние,
	|	ВсеМаршруты.ВидСообщения КАК ВидСообщения,
	|	ВсеМаршруты.ДатаРейса КАК ДатаРейса
	|ИЗ
	|	ПоследниеПоказанияПоРейсам КАК ПоследниеПоказанияПоРейсам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеМаршруты КАК ВсеМаршруты
	|		ПО ПоследниеПоказанияПоРейсам.Контрагент = ВсеМаршруты.Контрагент
	|			И ПоследниеПоказанияПоРейсам.ПунктОтправления = ВсеМаршруты.ПунктОтправления
	|			И ПоследниеПоказанияПоРейсам.ПунктНазначения = ВсеМаршруты.ПунктНазначения
	|			И ПоследниеПоказанияПоРейсам.Ссылка = ВсеМаршруты.Ссылка
	|			И ПоследниеПоказанияПоРейсам.НомерСтроки = ВсеМаршруты.НомерСтроки");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаРейса", ДатаРейса);
	Запрос.УстановитьПараметр("НачалоОтбораДокументов", ДобавитьМесяц(НачалоДня(ДатаРейса), -12));
	Запрос.УстановитьПараметр("ОтменяемыеДанные", ОтменяемыеДанные);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура УдалитьТекущиеСведенияМаршрута(Организация, ДатаРейса, ДанныеМаршрута)
	
	СведенияОМаршрутеКУдалению = СведенияОМаршрутеКУдалению(Организация, ДатаРейса, ДанныеМаршрута);
	Если ЗначениеЗаполнено(СведенияОМаршрутеКУдалению) Тогда
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Загрузить(СведенияОМаршрутеКУдалению);;
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
	КонецЕсли;
	
КонецПроцедуры

Функция НайтиСтрокуМаршрута(Маршрут, Контрагент, ПунктОтправления, ПунктНазначения)
	
	// Маршрут пользователь указывает вручную: пункты отправления и назначения могут быть написаны в разном регистре,
	// но при записи в СУБД не учитывается врехний и нижний регист в строках для измерений,
	// поэтому необходимо обработать ситуацию, когда пользователь по-разному может написать адрес.
	// Аналогично учитываются пробелы в конце строки.
	Для Каждого ТекущаяСтрока Из Маршрут Цикл
		
		Если ТекущаяСтрока.Контрагент <> Контрагент Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВРег(СокрП(ТекущаяСтрока.ПунктОтправления)) <> ВРег(СокрП(ПунктОтправления)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВРег(СокрП(ТекущаяСтрока.ПунктНазначения)) = ВРег(СокрП(ПунктНазначения)) Тогда
			Возврат ТекущаяСтрока;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецЕсли
