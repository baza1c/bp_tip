
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Номенклатура") Тогда
		Элементы.Номенклатура.ТолькоПросмотр = Истина;
	ИначеЕсли Параметры.Свойство("ШтрихкодДляРегистрации") Тогда
		РежимРегистрацииШтрихкода            = Истина;
		Запись.Штрихкод = Параметры.ШтрихкодДляРегистрации;
		Элементы.Штрихкод.ТолькоПросмотр     = Истина;
		ЭтаФорма.Заголовок = "Укажите номенклатуру для штрихкода";
	КонецЕсли;
	
	Если НЕ РежимРегистрацииШтрихкода Тогда
		ИспользуютсяСканерыШтрихкода = МенеджерОборудованияБП.ИспользуетсяОборудование("СканерШтрихкода");
	Иначе
		ИспользуютсяСканерыШтрихкода = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользуютсяСканерыШтрихкода  Тогда
		// Попробуем подключить сканер штрихкода
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьСканерШКЗавершение", ЭтотОбъект); 
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, "СканерШтрихкода");
	ИначеЕсли МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент()
		И МобильныйКлиентБПКлиент.ДоступноСканированиеШтрихкодов() Тогда
		Если НЕ РежимРегистрацииШтрихкода
			И Не ЗначениеЗаполнено(Запись.Штрихкод)Тогда
			ОписаниеОповещенияПоискПоШтрихкоду = Новый ОписаниеОповещения("ПоискПоШтрихкодуМКЗавершение", ЭтотОбъект);
			МобильныйКлиентБПКлиент.НачатьСканированиеШтрихКодов("Наведите камеру на штрихкод", ОписаниеОповещенияПоискПоШтрихкоду);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если НЕ РежимРегистрацииШтрихкода И Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				Штрихкод = Параметр[0];
			Иначе
				Штрихкод = Параметр[1][1];
			КонецЕсли;
			
			РезультатРазбора = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьШтриховойКодТовара(Штрихкод);
			Если РезультатРазбора.Разобран 
				И РезультатРазбора.Свойство("EAN") Тогда
				
				Запись.Штрихкод = РезультатРазбора.EAN
			Иначе
				Запись.Штрихкод = Штрихкод;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если РежимРегистрацииШтрихкода 
		И НЕ ЗавершениеРаботы 
		И ЗначениеЗаполнено(Запись.Номенклатура)
		И (НЕ Запись.ИсходныйКлючЗаписи.Пустой()) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		МассивШтрихкодов = Новый Массив;
		МассивШтрихкодов.Добавить(Новый Структура("Штрихкод, Количество", Запись.Штрихкод, 1));
		
		ПараметрЗакрытия = Новый Структура;
		ПараметрЗакрытия.Вставить("ЗарегистрированныеШтрихкоды", МассивШтрихкодов);
		
		Закрыть(ПараметрЗакрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	// ПодключаемоеОборудование
	Если СканерШтрихкодаПодключен И НЕ ЗавершениеРаботы Тогда
		ТипыПО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("СканерШтрихкода");
		ОповещенияПриОтключении = Новый ОписаниеОповещения("ОтключитьСканерШКЗавершение", ЭтотОбъект); 
		МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(ОповещенияПриОтключении, УникальныйИдентификатор, ТипыПО);
	КонецЕсли;
	//Конец  ПодключаемоеОборудование
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодключитьСканерШКЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	СканерШтрихкодаПодключен = РезультатВыполнения.Результат;

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьСканерШКЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'При отключении оборудования произошла ошибка: ""%1"".'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		СканерШтрихкодаПодключен = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ТаблицаНоменклатуры = РегистрыСведений.ШтрихкодыНоменклатуры.НоменклатураПоШтрихкоду(Запись.Штрихкод);
	
	Если ТаблицаНоменклатуры.Количество() = 1 
		И Запись.Штрихкод <> Запись.ИсходныйКлючЗаписи.Штрихкод Тогда
	
		ТекстСообщения = СтрШаблон(НСтр("ru = 'По штрихкоду %1 уже заведена номенклатура ""%2""'"), Запись.Штрихкод, ТаблицаНоменклатуры[0].Номенклатура);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Штрихкод", "Запись", Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуМКЗавершение(ДанныеВыбора, ДополнительныеПараметры = Неопределено) Экспорт
	Если ДанныеВыбора <> Неопределено Тогда
		Запись.Штрихкод = ДанныеВыбора;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти



