#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьГруппыКомпанийБанков(СервисХарактеристики, ОбъектXDTOХарактеристики, Банк) Экспорт 
	
	Если ОбъектXDTOХарактеристики = Неопределено 
		ИЛИ ОбъектXDTOХарактеристики.Свойства().Получить("ГруппыКомпанийБанков") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаИНН Из ОбъектXDTOХарактеристики.ГруппыКомпанийБанков Цикл
		DataJSON = СтрокаИНН.DATA;
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(DataJSON);
		
		Попытка
			Данные = ПрочитатьJSON(ЧтениеJSON);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ИмяСобытия = УниверсальныйОбменСБанкамиОбщегоНазначения.СобытиеЖурналаРегистрации(
				СервисХарактеристики,
				НСтр("ru = 'Чтение группы компаний банков'", ОбщегоНазначения.КодОсновногоЯзыка()));
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			Продолжить;
		КонецПопытки;
		
		Если ТипЗнч(Данные) <> Тип("Массив") Или Данные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ГруппыКомпанийБанков.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Банк.Установить(Банк);
				
		Для Каждого Характеристика Из Данные Цикл
			Если ТипЗнч(Характеристика) <> Тип("Структура")
				Или Не Характеристика.Свойство("ИНН") Или ПустаяСтрока(Характеристика.ИНН) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Банк = Банк;
			ЗаписьНабора.ИНН = СокрЛП(Характеристика.ИНН);
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ИмяСобытия = УниверсальныйОбменСБанкамиОбщегоНазначения.СобытиеЖурналаРегистрации(
				СервисХарактеристики,
				НСтр("ru = 'Запись группы компаний банков'", ОбщегоНазначения.КодОсновногоЯзыка()));
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Функция КонтрагентыГруппыКомпанийБанка(ИНН) Экспорт
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыКомпанийБанков.ИНН КАК ИНН
		|ПОМЕСТИТЬ ВТ_ИНН
		|ИЗ
		|	РегистрСведений.ГруппыКомпанийБанков КАК ГруппыКомпанийБанков
		|ГДЕ
		|	ГруппыКомпанийБанков.Банк.ИНН = &ИНН
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&ИНН
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент
		|ИЗ
		|	ВТ_ИНН КАК ВТ_ИНН
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ВТ_ИНН.ИНН = Контрагенты.ИНН";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);

	ТаблицаИНН = РезультатЗапроса.Выгрузить();
	
	Возврат ТаблицаИНН.ВыгрузитьКолонку("Контрагент");

КонецФункции

Функция ТаблицаИННГруппыКомпанийБанков(РеквизитыБанков) Экспорт
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(РеквизитыБанков.ИНН КАК СТРОКА(10)) КАК ИННБанка
		|ПОМЕСТИТЬ ВТ_РеквизитыБанков
		|ИЗ
		|	&РеквизитыБанков КАК РеквизитыБанков
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ГруппыКомпанийБанков.ИНН, ВТ_РеквизитыБанков.ИННБанка) КАК ИНН,
		|	ВТ_РеквизитыБанков.ИННБанка КАК ИННБанка
		|ИЗ
		|	ВТ_РеквизитыБанков КАК ВТ_РеквизитыБанков
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыКомпанийБанков КАК ГруппыКомпанийБанков
		|		ПО (ГруппыКомпанийБанков.Банк.ИНН = ВТ_РеквизитыБанков.ИННБанка)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_РеквизитыБанков.ИННБанка,
		|	ВТ_РеквизитыБанков.ИННБанка
		|ИЗ
		|	ВТ_РеквизитыБанков КАК ВТ_РеквизитыБанков";
	
	Запрос.УстановитьПараметр("РеквизитыБанков", РеквизитыБанков);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции

#КонецОбласти

#КонецЕсли
