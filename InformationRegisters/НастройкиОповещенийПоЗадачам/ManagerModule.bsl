#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает признак того, что по задаче включено оповещение при изменении статуса.
// 
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - Задача, для которой необходимо получить признак оповещения.
// 
// Возвращаемое значение:
//  Булево - Если Истина, то оповещение о изменении статуса задачи включено.
Функция ОповещениеПриИзмененииСтатусаЗадачиВключено(Задача) Экспорт
	
	Если Не ЗначениеЗаполнено(Задача) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиОповещенийПоЗадачам.ОповещатьПриИзмененииСтатуса КАК ОповещатьПриИзмененииСтатуса
	|ИЗ
	|	РегистрСведений.НастройкиОповещенийПоЗадачам КАК НастройкиОповещенийПоЗадачам
	|ГДЕ
	|	НастройкиОповещенийПоЗадачам.Задача = &Задача";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОповещатьПриИзмененииСтатуса;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Устанавливает настройки оповещения для задачи.
// 
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - Задача, для которой устанавливаются настройки оповещения.
//  ОповещатьПриИзмененииСтатуса - Булево - Устанавливаемая настройка оповещения при изменении статуса.
Процедура УстановитьНастройкиОповещенияПоЗадаче(Задача, ОповещатьПриИзмененииСтатуса) Экспорт
	
	Если Не ЗначениеЗаполнено(Задача) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОповещатьПриИзмененииСтатуса Тогда
		УдалитьНастройкиОповещения(Задача);
	Иначе
		НастройкиОповещения = НовыйНастройкиОповещения();
		НастройкиОповещения.ОповещатьПриИзмененииСтатуса = ОповещатьПриИзмененииСтатуса;

		ЗаписатьНастройкиОповещения(Задача, НастройкиОповещения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьНастройкиОповещения(Задача, Настройки)
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Задача.Установить(Задача);
	
	Запись = Набор.Добавить();
	Запись.Задача = Задача;
	ЗаполнитьЗначенияСвойств(Запись, Настройки);
	
	Набор.Записать();
	
КонецПроцедуры

Процедура УдалитьНастройкиОповещения(Задача)
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Задача.Установить(Задача);
	Набор.Записать();
	
КонецПроцедуры

Функция НовыйНастройкиОповещения()
	
	НастройкиОповещения = Новый Структура;
	НастройкиОповещения.Вставить("ОповещатьПриИзмененииСтатуса", Ложь);
	
	Возврат НастройкиОповещения;
	
КонецФункции

#КонецОбласти

#КонецЕсли