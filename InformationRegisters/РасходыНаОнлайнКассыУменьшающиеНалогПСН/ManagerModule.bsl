#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗарегистрированныеРасходы(Организация, РегистрацияВНалоговомОргане = Неопределено, Дата = Неопределено) Экспорт
	
	Результат = НовыйСведенияОЗарегистрированныхРасходахНаОнлайнКассы();
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ДокументРасходыНаОнлайнКассы.СуммаДокумента), 0) КАК Сумма,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументРасходыНаОнлайнКассы.Ссылка) КАК КоличествоКасс
	|ИЗ
	|	Документ.РасходыНаОнлайнКассыУменьшающиеНалоги КАК ДокументРасходыНаОнлайнКассы
	|ГДЕ
	|	НЕ ДокументРасходыНаОнлайнКассы.ПометкаУдаления
	|	И ДокументРасходыНаОнлайнКассы.Проведен
	|	И ДокументРасходыНаОнлайнКассы.Организация = &Организация
	|	И &УсловиеРегистрацияВНалоговомОргане
	|	И ДокументРасходыНаОнлайнКассы.УменьшаемыйНалог = ЗНАЧЕНИЕ(Перечисление.ВидыНалоговУменьшаемыхНаРасходыККТ.Патент)
	|	И ДокументРасходыНаОнлайнКассы.ДатаРегистрации <= &МаксимальнаяДатаРегистрации
	|	И ДокументРасходыНаОнлайнКассы.ДатаРегистрации >= &МинимальнаяДатаРегистрации";
	
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("МаксимальнаяДатаРегистрации",
		Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.МаксимальнаяДатаРегистрацииКассы(Дата));
	Запрос.УстановитьПараметр("МинимальнаяДатаРегистрации",
		Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.МинимальнаяДатаРегистрацииКассы(Дата));
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане",
			"РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Распределяет зарегистрированные расходы на онлайн-кассы, используемые в деятельности на ПСН,
// на уменьшение предстоящих платежей по патентам.
//
// Параметры:
//  Организация                 - СправочникСсылка.Организации - предприниматель
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - налоговый орган,
//                                  в котором получены патенты и зарегистрированы кассы.
//  УчитыватьКорректировки      - Булево - если ИСТИНА, не изменять суммы уменьшения, скорректированные пользователем в помощнике оплаты патента;
//                                         если ЛОЖЬ, перезаполнить распределение по умолчанию с потерей всех пользовательских корректировок.
//
Процедура РаспределитьРасходыПоПатентам(Организация, РегистрацияВНалоговомОргане, УчитыватьКорректировки = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	// Нужно обеспечить неизменность текущего распределения до окончания записи изменений.
	// Поэтому на время распределения блокируем регистр по организации и налоговой инспекции.
	ВнешняяТранзакция = ТранзакцияАктивна();
	
	Если НЕ ВнешняяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН");
	ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	ЭлементБлокировки.УстановитьЗначение("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	Блокировка.Заблокировать();
	
	ЗарегистрированныеРасходы = ДетальныеСведенияОРасходах(Организация, РегистрацияВНалоговомОргане);
	ЗарегистрированныеРасходы.Свернуть("ДатаРегистрации", "Сумма");
	ЗарегистрированныеРасходы.Сортировать("ДатаРегистрации");
	
	УменьшаемыеПлатежи = ПлатежиПодлежащиеУменьшению(Организация, РегистрацияВНалоговомОргане, ТекущаяДата());
	
	РезультатРаспределения = УменьшаемыеПлатежи.СкопироватьКолонки();
	РезультатРаспределения.Колонки.Добавить("НалоговыйВычет", ОбщегоНазначения.ОписаниеТипаЧисло(8, 0));
	
	// Сначала учтем ручные корректировки.
	
	Если УчитыватьКорректировки Тогда
		
		Отбор = Новый Структура("РучнаяКорректировка", Истина);
		ПлатежиСкорректированныеВручную = УменьшаемыеПлатежи.НайтиСтроки(Отбор);
		
		Для Каждого ПлатежПоПатенту Из ПлатежиСкорректированныеВручную Цикл
			
			СкорректированныйВычет = ПлатежПоПатенту.ТекущийНалоговыйВычет;
			
			// Уменьшим суммы расходов, уменьшающих платежи, на суммы скорректированного вручную вычета.
			Для Каждого Расход Из ЗарегистрированныеРасходы Цикл
				
				Если СкорректированныйВычет <= 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если Расход.Сумма <= 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Если ПлатежПоПатенту.ДатаОкончания < Расход.ДатаРегистрации Тогда
					// Нельзя уменьшать налог, если патент заканчивается раньше даты регистрации кассы
					// (абзацы 4-5 пункта 1.1 статьи 346.51 НК РФ).
					// Этот расход не мог быть использован на уменьшение данного платежа.
					Продолжить;
				КонецЕсли;
				
				Учтено = Мин(Расход.Сумма, СкорректированныйВычет);
				
				Расход.Сумма = Расход.Сумма - Учтено;
				СкорректированныйВычет = СкорректированныйВычет - Учтено;
				
			КонецЦикла;
			
			НоваяСтрока = РезультатРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПлатежПоПатенту);
			
			НоваяСтрока.НалоговыйВычет = ПлатежПоПатенту.ТекущийНалоговыйВычет;
			
		КонецЦикла;
		
		// Очистим учтенные строки с ручными корректировками в таблице уменьшаемых платежей.
		Для Каждого УчтенныйПлатеж Из ПлатежиСкорректированныеВручную Цикл
			УменьшаемыеПлатежи.Удалить(УчтенныйПлатеж);
		КонецЦикла;
		
	КонецЕсли;
	
	// Распределим расходы на уменьшение платежей по патентам.
	
	Для Каждого ПлатежПоПатенту Из УменьшаемыеПлатежи Цикл
		
		НеоплаченнаяСумма = Макс(ПлатежПоПатенту.СуммаПлатежа - ПлатежПоПатенту.Оплачено, 0);
		НалоговыйВычет    = 0;
		
		Для Каждого Расход Из ЗарегистрированныеРасходы Цикл
			
			Если НеоплаченнаяСумма <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			Если Расход.Сумма <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПлатежПоПатенту.ДатаОкончания < Расход.ДатаРегистрации Тогда
				// Нельзя уменьшать налог, если патент заканчивается раньше даты регистрации кассы
				// (абзацы 4-5 пункта 1.1 статьи 346.51 НК РФ).
				Продолжить;
			КонецЕсли;
			
			Учесть = Мин(Расход.Сумма, НеоплаченнаяСумма);
			
			НалоговыйВычет = НалоговыйВычет + Учесть;
			
			НеоплаченнаяСумма = НеоплаченнаяСумма - Учесть;
			Расход.Сумма      = Расход.Сумма - Учесть;
			
		КонецЦикла;
		
		// Даже если использована вся сумма расходов, обход платежей не прерываем -
		// в результатах распределения нужно сохранить данные и о патентах с нулевым вычетом.
		
		НоваяСтрока = РезультатРаспределения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ПлатежПоПатенту, , "РучнаяКорректировка");
		
		НоваяСтрока.НалоговыйВычет = НалоговыйВычет;
		
	КонецЦикла;
	
	// Запишем измененный результат распределения полным набором с замещением, чтобы удалить неактуальные записи.
	
	Набор = РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.СоздатьНаборЗаписей();
	
	Набор.Отбор.Организация.Установить(Организация);
	Набор.Отбор.РегистрацияВНалоговомОргане.Установить(РегистрацияВНалоговомОргане);
	
	Для Каждого ПлатежПоПатенту Из РезультатРаспределения Цикл
		
		НоваяЗапись = Набор.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ПлатежПоПатенту);
		НоваяЗапись.Организация = Организация;
		НоваяЗапись.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
		
	КонецЦикла;
	
	Набор.Записать();
	
	// Обновим статусы задач уплаты патентов.
	
	Патенты = ОбщегоНазначения.ВыгрузитьКолонку(РезультатРаспределения, "Патент", Истина);
	ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачУплатыПатента(Организация, Патенты);
	
	Если НЕ ВнешняяТранзакция Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

Функция ОтразитьРасходыНаОнлайнКассы(КонтекстОперации, ДанныеКРаспределению, Движения) Экспорт
	
	// Нужно обеспечить неизменность текущего распределения до окончания записи изменений.
	// Поэтому на время распределения блокируем регистр по организации и налоговой инспекции.
	ВнешняяТранзакция = ТранзакцияАктивна();
	
	Если Не ВнешняяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	Организация = ДанныеКРаспределению.Организация;
	РегистрацияВНалоговомОргане = ДанныеКРаспределению.РегистрацияВНалоговомОргане;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН");
	ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	ЭлементБлокировки.УстановитьЗначение("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	Блокировка.Заблокировать();
	
	ИзменяемыеПатенты = Новый Массив;
	ОстатокВычета = ДанныеКРаспределению.СуммаОстаток;
	СуммаВычета = 0;
	
	УменьшаемыеПлатежи = ПлатежиПодлежащиеУменьшению(Организация, РегистрацияВНалоговомОргане, КонтекстОперации.Дата);
	ВычетыПоСтраховымВзносам = Новый Соответствие;
	
	Для Каждого ПлатежПоПатенту Из УменьшаемыеПлатежи Цикл
		Если ОстатокВычета = 0 Тогда
			Прервать;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПлатежПоПатенту.ВычетНаСтраховыеВзносы) Тогда
			ВычетыПоСтраховымВзносам.Вставить(ПлатежПоПатенту.Патент, ПлатежПоПатенту.ВычетНаСтраховыеВзносы);
		КонецЕсли;
		НеоплаченнаяСумма = Макс(
			ПлатежПоПатенту.СуммаПлатежа - ПлатежПоПатенту.Оплачено - ПлатежПоПатенту.ТекущийВычетККТ, 0);
		ВычетНаСтраховыеВзносы = ВычетыПоСтраховымВзносам[ПлатежПоПатенту.Патент];
		Если ЗначениеЗаполнено(ВычетНаСтраховыеВзносы) Тогда
			ПрименяемыйВычетНаСтраховыеВзносы = Мин(ВычетНаСтраховыеВзносы, НеоплаченнаяСумма);
			НеоплаченнаяСумма = НеоплаченнаяСумма - ПрименяемыйВычетНаСтраховыеВзносы;
			ВычетыПоСтраховымВзносам.Вставить(
				ПлатежПоПатенту.Патент, ВычетНаСтраховыеВзносы - ПрименяемыйВычетНаСтраховыеВзносы);
		КонецЕсли;
		
		Учесть = Мин(ОстатокВычета, НеоплаченнаяСумма);
		ОстатокВычета = ОстатокВычета - Учесть;
		
		Если Учесть > 0 Тогда
			НоваяЗапись = Движения.РасходыНаОнлайнКассыУменьшающиеНалогПСН.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ПлатежПоПатенту);
			НоваяЗапись.Период = КонтекстОперации.Дата;
			НоваяЗапись.Организация = Организация;
			НоваяЗапись.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
			НоваяЗапись.ОперацияСПатентом = ПлатежПоПатенту.Патент;
			НоваяЗапись.НалоговыйВычет = Учесть;
			ИзменяемыеПатенты.Добавить(ПлатежПоПатенту.Патент);
			СуммаВычета = СуммаВычета + Учесть;
			
			СторноНалогаПоПатенту = Движения.Хозрасчетный.Добавить();
			СторноНалогаПоПатенту.Организация = Организация;
			СторноНалогаПоПатенту.Период = ПлатежПоПатенту.СрокПлатежа;
			СторноНалогаПоПатенту.СчетДт = ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиЕНВД;
			СторноНалогаПоПатенту.СчетКт = ПланыСчетов.Хозрасчетный.НалогПриПСН;
			БухгалтерскийУчет.УстановитьСубконто(СторноНалогаПоПатенту.СчетДт, СторноНалогаПоПатенту.СубконтоДт,
				"ПрибылиИУбытки", Перечисления.ПрибылиИУбытки.НалогНаПрибыль);
			БухгалтерскийУчет.УстановитьСубконто(СторноНалогаПоПатенту.СчетКт, СторноНалогаПоПатенту.СубконтоКт,
				"ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			СторноНалогаПоПатенту.Сумма = - Учесть;
			СторноНалогаПоПатенту.Содержание = СтрШаблон(
				НСтр("ru = 'Уменьшение налога по патенту %1'"), ПлатежПоПатенту.Патент);
			
			СторноПатентЕНС = Движения.Хозрасчетный.Добавить();
			ЗаполнитьЗначенияСвойств(СторноПатентЕНС, СторноНалогаПоПатенту, "Организация, Период, Содержание, Сумма");
			СторноПатентЕНС.СчетДт = ПланыСчетов.Хозрасчетный.НалогПриПСН;
			БухгалтерскийУчет.УстановитьСубконто(СторноПатентЕНС.СчетДт, СторноПатентЕНС.СубконтоДт,
				"ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			СторноПатентЕНС.СчетКт = ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет;
			
			РасчетЕНС = Движения.РасчетНачисленияНалоговНаЕНС.Добавить();
			РасчетЕНС.Организация = Организация;
			РасчетЕНС.Период = КонтекстОперации.Дата;
			РасчетЕНС.ДокументПатент = ПлатежПоПатенту.Патент;
			РасчетЕНС.Начислено = СторноНалогаПоПатенту.Сумма;
			РасчетЕНС.ПериодРасчета = КонтекстОперации.Дата;
			РасчетЕНС.СчетУчета = СторноНалогаПоПатенту.СчетКт;
			РасчетЕНС.Субконто1 = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			РасчетЕНС.СрокУплаты = ПлатежПоПатенту.СрокПлатежа;
			РасчетЕНС.СуммаНалога = СторноНалогаПоПатенту.Сумма;
			РасчетЕНС.НачалоПериода = ПлатежПоПатенту.ДатаНачала;
			РасчетЕНС.КонецПериода = ПлатежПоПатенту.ДатаОкончания;
		КонецЕсли;
	КонецЦикла;
	
	Движения.РасходыНаОнлайнКассыУменьшающиеНалогПСН.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	Движения.РасчетНачисленияНалоговНаЕНС.Записывать = Истина;
	
	// Обновим статусы задач уплаты патентов.
	ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачУплатыПатента(Организация, ИзменяемыеПатенты);
	
	Если Не ВнешняяТранзакция Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Возврат СуммаВычета;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает данные о распределении расходов по покупке онлайн-касс на уменьшение платежей по патентам.
//
// Параметры:
//  Организация                 - СправочникСсылка.Организации - предприниматель.
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе,
//                                для которой определяется уменьшение платежей.
//
// Возвращаемое значение:
//   ТаблицаЗначений - данные об уменьшении платежей по патентам.
//
Функция УменьшениеПлатежейПоПатентам(Организация, РегистрацияВНалоговомОргане) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом КАК Патент,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.НомерПатента КАК НомерПатента,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаВыдачи КАК ДатаВыдачиПатента,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ВидПлатежа КАК ВидПлатежа,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.СуммаПлатежа КАК СуммаПлатежа,
	|	ВЫБОР
	|		КОГДА РасходыНаОнлайнКассыУменьшающиеНалогПСН.ВидПлатежа = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейПоПатенту.ВторойПлатеж)
	|			ТОГДА РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаВторогоПлатежа
	|		ИНАЧЕ РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаПервогоПлатежа
	|	КОНЕЦ КАК СрокПлатежа,
	|	СУММА(РасходыНаОнлайнКассыУменьшающиеНалогПСН.Оплачено) КАК Оплачено,
	|	СУММА(РасходыНаОнлайнКассыУменьшающиеНалогПСН.НалоговыйВычет) КАК НалоговыйВычет
	|ИЗ
	|	РегистрСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН КАК РасходыНаОнлайнКассыУменьшающиеНалогПСН
	|ГДЕ
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.Организация = &Организация
	|	И РасходыНаОнлайнКассыУменьшающиеНалогПСН.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ВидПлатежа,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.СуммаПлатежа,
	|	ВЫБОР
	|		КОГДА РасходыНаОнлайнКассыУменьшающиеНалогПСН.ВидПлатежа = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейПоПатенту.ВторойПлатеж)
	|			ТОГДА РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаВторогоПлатежа
	|		ИНАЧЕ РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаПервогоПлатежа
	|	КОНЕЦ,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.НомерПатента,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаВыдачи
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаОкончания,
	|	РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом.ДатаНачала,
	|	Патент,
	|	СрокПлатежа"
	;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Получает детальные сведения о зарегистрированных расходах на онлайн-кассы -
// с регистрационными и заводскими данными касс.
//
// Параметры:
//  Организация                 - СправочникСсылка.Организации - предприниматель.
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе,
//                                для которой определяются расходы.
//
// Возвращаемое значение:
//   ТаблицаЗначений - данные о расходах по каждой кассе.
//
Функция ДетальныеСведенияОРасходах(Организация, РегистрацияВНалоговомОргане) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументРасходыНаОнлайнКассы.Организация КАК Организация,
	|	ДокументРасходыНаОнлайнКассы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ДокументРасходыНаОнлайнКассы.Модель КАК Модель,
	|	ДокументРасходыНаОнлайнКассы.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ДокументРасходыНаОнлайнКассы.ДатаРегистрации КАК ДатаРегистрации,
	|	ДокументРасходыНаОнлайнКассы.РегистрационныйНомер КАК РегистрационныйНомер,
	|	ДокументРасходыНаОнлайнКассы.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.РасходыНаОнлайнКассыУменьшающиеНалоги КАК ДокументРасходыНаОнлайнКассы
	|ГДЕ
	|	НЕ ДокументРасходыНаОнлайнКассы.ПометкаУдаления
	|	И ДокументРасходыНаОнлайнКассы.Проведен
	|	И ДокументРасходыНаОнлайнКассы.Организация = &Организация
	|	И ДокументРасходыНаОнлайнКассы.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|	И ДокументРасходыНаОнлайнКассы.УменьшаемыйНалог = ЗНАЧЕНИЕ(Перечисление.ВидыНалоговУменьшаемыхНаРасходыККТ.Патент)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРегистрации,
	|	РегистрационныйНомер,
	|	ЗаводскойНомер"
	;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПлатежиПодлежащиеУменьшению(Организация, РегистрацияВНалоговомОргане, Период)
	
	// Получаем причитающиеся платежи по патентам с данными о фактической уплате.
	// Сведения об оплатах берем из документов выполнения задач бухгалтера -
	// то есть только платежи, введенные из помощника.
	// Суммы оплат, введенных самостоятельно без использования помощника, в результат функции не попадут -
	// однозначно связать такие платежи с конкретным патентом нельзя.
	
	// По патентам с двумя платежами возможны ситуации, когда при оплате первого платежа пользователь заплатил больше 1/3.
	// Например, заблаговременно погасил всю стоимость патента - это допускается.
	// В помощнике и при определении статуса задачи оплата патента рассчитыватся нарастающим итогом.
	// Здесь необходимо учесть этот сценарий: найти "переплаты" первых платежей и перенести их на оплату вторых платежей.
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	Запрос.УстановитьПараметр("НачалоПримененияВычета", ВычетыНаОнлайнКассыКлиентСервер.ДатаНачалаПримененияВычета());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОперацияПоПатенту.Ссылка КАК Патент,
	|	ОперацияПоПатенту.ДатаНачала КАК ДатаНачала,
	|	ОперацияПоПатенту.ДатаОкончания КАК ДатаОкончания,
	|	ОперацияПоПатенту.СуммаПервогоПлатежа КАК СуммаПервогоПлатежа,
	|	ОперацияПоПатенту.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	ОперацияПоПатенту.СуммаВторогоПлатежа КАК СуммаВторогоПлатежа,
	|	ОперацияПоПатенту.ДатаВторогоПлатежа КАК ДатаВторогоПлатежа,
	|	ВЫБОР
	|		КОГДА ОперацияПоПатенту.СуммаВторогоПлатежа > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДваПлатежа,
	|	СУММА(ЕСТЬNULL(УменьшениеНалогаПоПатенту.Сумма, 0)) КАК ВычетНаСтраховыеВзносы,
	|	ОперацияПоПатенту.Патент КАК ВидДеятельности
	|ПОМЕСТИТЬ ДанныеПатентов
	|ИЗ
	|	Документ.ОперацияСПатентом КАК ОперацияПоПатенту
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УменьшениеНалогаПоПатенту КАК УменьшениеНалогаПоПатенту
	|		ПО (УменьшениеНалогаПоПатенту.ДокументПатент = ОперацияПоПатенту.Ссылка)
	|ГДЕ
	|	ОперацияПоПатенту.Проведен
	|	И ОперацияПоПатенту.Организация = &Организация
	|	И ОперацияПоПатенту.ДатаНачала >= &НачалоПримененияВычета
	|	И ВЫБОР
	|			КОГДА ОперацияПоПатенту.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|				ТОГДА ОперацияПоПатенту.НалоговыйОрган = &РегистрацияВНалоговомОргане
	|			ИНАЧЕ ОперацияПоПатенту.Организация.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ОперацияПоПатенту.Ссылка,
	|	ОперацияПоПатенту.ДатаНачала,
	|	ОперацияПоПатенту.ДатаОкончания,
	|	ОперацияПоПатенту.СуммаПервогоПлатежа,
	|	ОперацияПоПатенту.ДатаПервогоПлатежа,
	|	ОперацияПоПатенту.СуммаВторогоПлатежа,
	|	ОперацияПоПатенту.ДатаВторогоПлатежа,
	|	ВЫБОР
	|		КОГДА ОперацияПоПатенту.СуммаВторогоПлатежа > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ОперацияПоПатенту.Патент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПатентов.Патент КАК Патент,
	|	ДанныеПатентов.ДатаНачала КАК ДатаНачала,
	|	ДанныеПатентов.ДатаОкончания КАК ДатаОкончания,
	|	ДанныеПатентов.СуммаПервогоПлатежа КАК СуммаПлатежа,
	|	ДанныеПатентов.ДатаПервогоПлатежа КАК СрокПлатежа,
	|	НАЧАЛОПЕРИОДА(ДанныеПатентов.ДатаНачала, ГОД) КАК ПериодСобытия,
	|	ВЫБОР
	|		КОГДА ДанныеПатентов.ДваПлатежа
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейПоПатенту.ПервыйПлатеж)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейПоПатенту.ЕдинственныйПлатеж)
	|	КОНЕЦ КАК ВидПлатежа,
	|	ДанныеПатентов.ВычетНаСтраховыеВзносы КАК ВычетНаСтраховыеВзносы,
	|	ДанныеПатентов.ВидДеятельности КАК ВидДеятельности
	|ПОМЕСТИТЬ ПлатежиПоПатентам
	|ИЗ
	|	ДанныеПатентов КАК ДанныеПатентов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеПатентов.Патент,
	|	ДанныеПатентов.ДатаНачала,
	|	ДанныеПатентов.ДатаОкончания,
	|	ДанныеПатентов.СуммаВторогоПлатежа,
	|	ДанныеПатентов.ДатаВторогоПлатежа,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ДанныеПатентов.ДатаОкончания, ГОД), ДЕНЬ),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейПоПатенту.ВторойПлатеж),
	|	0,
	|	ДанныеПатентов.ВидДеятельности
	|ИЗ
	|	ДанныеПатентов КАК ДанныеПатентов
	|ГДЕ
	|	ДанныеПатентов.ДваПлатежа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Правило КАК Патент,
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение КАК ДокументОплаты,
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия КАК ПериодСобытия
	|ПОМЕСТИТЬ ДокументыОплаты
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация = &Организация
	|	И ЗадачиБухгалтераНалоговыеПлатежи.Правило В
	|			(ВЫБРАТЬ
	|				ДанныеПатентов.ВидДеятельности КАК ВидДеятельности
	|			ИЗ
	|				ДанныеПатентов КАК ДанныеПатентов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыОплаты.Патент КАК Патент,
	|	ДокументыОплаты.ПериодСобытия КАК ПериодСобытия,
	|	ПлатежноеПоручение.СуммаДокумента КАК Сумма
	|ПОМЕСТИТЬ ОплатаПатентов
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыОплаты КАК ДокументыОплаты
	|		ПО ПлатежноеПоручение.Ссылка = ДокументыОплаты.ДокументОплаты
	|ГДЕ
	|	ПлатежноеПоручение.Организация = &Организация
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И ПлатежноеПоручение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыОплаты.Патент,
	|	ДокументыОплаты.ПериодСобытия,
	|	РасходныйКассовыйОрдер.СуммаДокумента
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыОплаты КАК ДокументыОплаты
	|		ПО РасходныйКассовыйОрдер.Ссылка = ДокументыОплаты.ДокументОплаты
	|ГДЕ
	|	РасходныйКассовыйОрдер.Организация = &Организация
	|	И НЕ РасходныйКассовыйОрдер.ПометкаУдаления
	|	И РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежиПоПатентам.Патент КАК Патент,
	|	ПлатежиПоПатентам.ВидПлатежа КАК ВидПлатежа,
	|	ПлатежиПоПатентам.СуммаПлатежа КАК СуммаПлатежа,
	|	СУММА(ЕСТЬNULL(ОплатаПатентов.Сумма, 0)) КАК Оплачено
	|ПОМЕСТИТЬ ПлатежиИОплата
	|ИЗ
	|	ПлатежиПоПатентам КАК ПлатежиПоПатентам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатаПатентов КАК ОплатаПатентов
	|		ПО ПлатежиПоПатентам.ВидДеятельности = ОплатаПатентов.Патент
	|			И ПлатежиПоПатентам.ПериодСобытия = ОплатаПатентов.ПериодСобытия
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлатежиПоПатентам.СуммаПлатежа,
	|	ПлатежиПоПатентам.ВидПлатежа,
	|	ПлатежиПоПатентам.Патент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежиИОплата.Патент КАК Патент,
	|	ПлатежиИОплата.Оплачено - ПлатежиИОплата.СуммаПлатежа КАК СуммаПереплаты,
	|	ПлатежиИОплата.ВидПлатежа КАК ВидПлатежаПереплаты
	|ПОМЕСТИТЬ Переплаты
	|ИЗ
	|	ПлатежиИОплата КАК ПлатежиИОплата
	|ГДЕ
	|	ПлатежиИОплата.Оплачено - ПлатежиИОплата.СуммаПлатежа > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежиПоПатентам.Патент КАК Патент,
	|	ПлатежиПоПатентам.ДатаНачала КАК ДатаНачала,
	|	ПлатежиПоПатентам.ДатаОкончания КАК ДатаОкончания,
	|	ПлатежиПоПатентам.ВидПлатежа КАК ВидПлатежа,
	|	ПлатежиПоПатентам.СрокПлатежа КАК СрокПлатежа,
	|	ПлатежиПоПатентам.СуммаПлатежа КАК СуммаПлатежа,
	|	ВЫБОР
	|		КОГДА ПлатежиИОплата.Оплачено + ЕСТЬNULL(Переплаты.СуммаПереплаты, 0) >= ПлатежиПоПатентам.СуммаПлатежа
	|			ТОГДА ПлатежиПоПатентам.СуммаПлатежа
	|		ИНАЧЕ ПлатежиИОплата.Оплачено + ЕСТЬNULL(Переплаты.СуммаПереплаты, 0)
	|	КОНЕЦ КАК Оплачено,
	|	СУММА(ЕСТЬNULL(РасходыНаОнлайнКассыУменьшающиеНалогПСН.НалоговыйВычет, 0)) КАК ТекущийВычетККТ,
	|	ПлатежиПоПатентам.ВычетНаСтраховыеВзносы КАК ВычетНаСтраховыеВзносы
	|ИЗ
	|	ПлатежиПоПатентам КАК ПлатежиПоПатентам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлатежиИОплата КАК ПлатежиИОплата
	|		ПО ПлатежиПоПатентам.Патент = ПлатежиИОплата.Патент
	|			И ПлатежиПоПатентам.ВидПлатежа = ПлатежиИОплата.ВидПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ Переплаты КАК Переплаты
	|		ПО ПлатежиПоПатентам.Патент = Переплаты.Патент
	|			И ПлатежиПоПатентам.ВидПлатежа <> Переплаты.ВидПлатежаПереплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН КАК РасходыНаОнлайнКассыУменьшающиеНалогПСН
	|		ПО ПлатежиПоПатентам.Патент = РасходыНаОнлайнКассыУменьшающиеНалогПСН.ОперацияСПатентом
	|			И ПлатежиПоПатентам.ВидПлатежа = РасходыНаОнлайнКассыУменьшающиеНалогПСН.ВидПлатежа
	|			И (РасходыНаОнлайнКассыУменьшающиеНалогПСН.Организация = &Организация)
	|			И (РасходыНаОнлайнКассыУменьшающиеНалогПСН.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлатежиПоПатентам.Патент,
	|	ПлатежиПоПатентам.ДатаНачала,
	|	ПлатежиПоПатентам.ДатаОкончания,
	|	ПлатежиПоПатентам.СрокПлатежа,
	|	ПлатежиПоПатентам.ВидПлатежа,
	|	ПлатежиПоПатентам.СуммаПлатежа,
	|	ПлатежиПоПатентам.ВычетНаСтраховыеВзносы,
	|	ВЫБОР
	|		КОГДА ПлатежиИОплата.Оплачено + ЕСТЬNULL(Переплаты.СуммаПереплаты, 0) >= ПлатежиПоПатентам.СуммаПлатежа
	|			ТОГДА ПлатежиПоПатентам.СуммаПлатежа
	|		ИНАЧЕ ПлатежиИОплата.Оплачено + ЕСТЬNULL(Переплаты.СуммаПереплаты, 0)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОкончания,
	|	ДатаНачала,
	|	Патент,
	|	СрокПлатежа";
	
	ДанныеПоПлатежам = Запрос.Выполнить().Выгрузить();
	
	Возврат ДанныеПоПлатежам;
	
КонецФункции

Функция НовыйСведенияОЗарегистрированныхРасходахНаОнлайнКассы()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Сумма", 0);
	Результат.Вставить("КоличествоКасс", 0);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли