#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Или Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНабораЗаписей = Выгрузить();
	ТаблицаНабораЗаписей.Колонки.Добавить("НачалоНалоговогоПериода", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаНабораЗаписей.Индексы.Добавить("НачалоНалоговогоПериода");
	Для Каждого ТекущаяСтрока Из ТаблицаНабораЗаписей Цикл
		ТекущаяСтрока.НачалоНалоговогоПериода = НачалоГода(ТекущаяСтрока.ПериодРасчета);
	КонецЦикла;
	
	ТаблицаНабораЗаписей.Сортировать("НачалоНалоговогоПериода");
	ПериодыСтавок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
		ТаблицаНабораЗаписей.ВыгрузитьКолонку("НачалоНалоговогоПериода"));
	
	ТекущийПериод = Неопределено;
	
	Для Каждого ТекущаяЗапись Из ТаблицаНабораЗаписей Цикл
		
		Если ТекущийПериод <> ТекущаяЗапись.НачалоНалоговогоПериода Тогда
			ТекущийПериод = ТекущаяЗапись.НачалоНалоговогоПериода;
			НалоговыеСтавки = УчетНДФЛПредпринимателя.НалоговыеСтавки(ТекущийПериод);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НалоговыеСтавки) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекущаяЗапись.ВидВычета)
			И НалоговыеСтавки.Найти(ТекущаяЗапись.СтавкаНалога, "СтавкаНалога") = Неопределено Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность",
				НСтр("ru = 'Ставка налога'"),
				ТекущаяЗапись.НомерСтроки,
				"Расчет НДФЛ (ИП)");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли