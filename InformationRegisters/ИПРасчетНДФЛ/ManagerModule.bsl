
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиОбновления

// Выполняет перенос значений из ресурса "Удалить ставка налога" в измерение "Ставка налога" для регистра сведений Расчет НДФЛ (ИП)
// Актуален для версии 3.0.88
//
// Параметры:
//  Параметры - Структура - параметры обработчика обновления.
//
Процедура ПеренестиСтавкуНалогаВИзмерения(Параметры) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ИПРасчетНДФЛ.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.ИПРасчетНДФЛ КАК ИПРасчетНДФЛ
	|ГДЕ
	|	ИПРасчетНДФЛ.СтавкаНалога = 0
	|	И ИПРасчетНДФЛ.ВидВычета = ЗНАЧЕНИЕ(Перечисление.ВычетыФизическихЛиц.ПустаяСсылка)");
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ИмяПроцедуры = "РегистрСведений.ИПРасчетНДФЛ.ПеренестиСтавкуНалогаВИзмерения()";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ИПРасчетНДФЛ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
		Для Каждого ТекущаяСтрока Из НаборЗаписей Цикл
			ТекущаяСтрока.СтавкаНалога = ТекущаяСтрока.УдалитьСтавкаНалога;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		Исключение
			
			// Если не удалось обработать какой-либо документ, повторяем попытку снова
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось обработать документ по причине:
				|%2'"), ИмяПроцедуры, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ИПРасчетНДФЛ,
				Выборка.Регистратор,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось обработать документы: в %2 из %3 возникли ошибки'"),
			ИмяПроцедуры,
			ПроблемныхОбъектов,
			ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
		
	Иначе
		
		Параметры.ОбработкаЗавершена = Истина;
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.РегистрыСведений.ИПРасчетНДФЛ, ,
			СтрШаблон(НСтр("ru = 'Процедура %1 обработала %2'"), ИмяПроцедуры,
				СтрокаСЧислом(";%1 документ;;%1 документа;%1 документов;%1 документа",
					ОбъектовОбработано,
					ВидЧисловогоЗначения.Количественное,
					"L=ru")));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
