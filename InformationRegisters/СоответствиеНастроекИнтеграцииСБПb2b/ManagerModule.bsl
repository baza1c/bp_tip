#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет настройки торговой точки платежной системы.
//
// Параметры:
//  СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//
// Возвращаемое значение:
//  Структура - настройки торговой точки:
//    *Организация - СправочникСсылка.Организации - организация из документа продажи;
//    *ВидОплаты - СправочникСсылка.ВидОплаты - вид оплаты для документа отражения оплаты.
//
Функция НастройкиТорговойТочки(НастройкиИнтеграции) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНастроекИнтеграцииСБПb2b.Организация КАК Организация,
	|	СоответствиеНастроекИнтеграцииСБПb2b.ВидОплаты КАК ВидОплаты,
	|	СоответствиеНастроекИнтеграцииСБПb2b.ВидПоступленияОплатыВБанковскойВыписке КАК ВидПоступленияОплатыВБанковскойВыписке
	|ИЗ
	|	РегистрСведений.СоответствиеНастроекИнтеграцииСБПb2b КАК СоответствиеНастроекИнтеграцииСБПb2b
	|ГДЕ
	|	СоответствиеНастроекИнтеграцииСБПb2b.ТорговаяТочка = &НастройкиИнтеграции";
	
	Запрос.УстановитьПараметр("НастройкиИнтеграции", НастройкиИнтеграции);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = Новый Структура;
		Результат.Вставить("Организация", ВыборкаДетальныеЗаписи.Организация);
		Результат.Вставить("ВидОплаты",   ВыборкаДетальныеЗаписи.ВидОплаты);
		Результат.Вставить("ВидПоступленияОплатыВБанковскойВыписке",
			ВыборкаДетальныеЗаписи.ВидПоступленияОплатыВБанковскойВыписке);
		
		Возврат Результат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ВсеНастройкиИнтеграции() Экспорт
	
	Организации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНастроекИнтеграцииСБПb2b.ТорговаяТочка КАК ТорговаяТочка
	|ИЗ
	|	РегистрСведений.СоответствиеНастроекИнтеграцииСБПb2b КАК СоответствиеНастроекИнтеграцииСБПb2b
	|ГДЕ
	|	СоответствиеНастроекИнтеграцииСБПb2b.Организация В(&Организации)
	|	И НЕ СоответствиеНастроекИнтеграцииСБПb2b.ТорговаяТочка.ПометкаУдаления
	|	И СоответствиеНастроекИнтеграцииСБПb2b.ТорговаяТочка.Используется";
	
	Настройки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТорговаяТочка");
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
