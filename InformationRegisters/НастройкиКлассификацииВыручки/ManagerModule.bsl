#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет настройки заполнения данными из обработки РаспределениеВыручкиПоОКВЭД.
//
// Параметры:
//  АдресРаспределениеВыручки - Строка - адрес во временном хранилище, содержит данные распределения выручки.
//  Организация - СправочникСсылка.Организации - организация, для которой нужно записать настройку.
//  ОбъектНаблюдения - СправочникСсылка.ОбъектыСтатистическогоНаблюдения Или Неопределено.
//  НаборЗаписейНастройкаФормСтатистики - РегистрСведенийНаборЗаписей.НастройкаЗаполненияСвободныхСтрокФормСтатистики или Неопределено.
//
Процедура ЗаполнитьРаспределениеВыручкиПоОКВЭД(АдресРаспределениеВыручки, Организация, ОбъектНаблюдения = Неопределено,
	НаборЗаписейНастройкаФормСтатистики = Неопределено) Экспорт
	
	Если ПустаяСтрока(АдресРаспределениеВыручки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбъектНаблюдения = Неопределено Тогда
		ОбъектНаблюдения = ОбъектНаблюденияСДетализациейПоВидамДеятельности();
		Если ОбъектНаблюдения = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиКлассификацииВыручки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	
	ТаблицаРаспределениеВыручки = ПолучитьИзВременногоХранилища(АдресРаспределениеВыручки);
	
	Для Каждого СтрокаТаблицы Из ТаблицаРаспределениеВыручки Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Организация = Организация;
		
		СправочникДетализации = Справочники.КлассификаторВидовЭкономическойДеятельности.НайтиПоКоду(СтрокаТаблицы.ОКВЭДКод);
		Если Не ЗначениеЗаполнено(СправочникДетализации) Тогда
			СправочникОбъект = Справочники.КлассификаторВидовЭкономическойДеятельности.СоздатьЭлемент();
			СправочникОбъект.Код = СтрокаТаблицы.ОКВЭДКод;
			СправочникОбъект.Наименование = СтрокаТаблицы.ОКВЭДНаименование;
			СправочникОбъект.НаименованиеПолное = СтрокаТаблицы.ОКВЭДНаименование;
			СправочникОбъект.Записать();
			СправочникДетализации = СправочникОбъект.Ссылка;
		КонецЕсли;
		
		Запись.Детализация = СправочникДетализации;
		
		НастройкиКомпоновки = Новый КомпоновщикНастроекКомпоновкиДанных;
		ЗаполнениеФормСтатистики.ИнициализироватьКомпоновщикНастроекПользователя(НастройкиКомпоновки, ОбъектНаблюдения);
		
		ОтборКомпоновки = НастройкиКомпоновки.Настройки.Отбор;
		ГруппаИли = Неопределено;
		РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		ЕстьГруппаИ = СтрокаТаблицы.ПоляГруппировкиНоменклатуры.Свойство("ПорядокГруппыИ");
		ЕстьНесколькоГруппИ = ЕстьГруппаИ И СтрокаТаблицы.ПоляГруппировкиНоменклатуры.ПорядокГруппыИ >= 2;
		
		ПоляОтбора = ПоляОтбора(СтрокаТаблицы.ПоляГруппировкиНоменклатуры);
		// В ТаблицаРаспределениеВыручки могут быть группы И (условия в ней должны выполняться одновременно), таких групп может быть несколько,
		// тогда их нужно объединить в группу Или. Также если в ПоляОтбора содержатся условия, то их тоже нужно объединять в группу Или.
		Если ПоляОтбора.ПоНоменклатурнойГруппе + ПоляОтбора.ПоГруппеНоменклатуры + ПоляОтбора.ПоНоменклатуре + ПоляОтбора.ПоВидуНоменклатуры
			+ ЕстьГруппаИ > 1 Или ЕстьНесколькоГруппИ Тогда
			ГруппаИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
				ОтборКомпоновки, "ГруппаИли", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
			ГруппаИли.РежимОтображения = РежимОтображения;
			ГруппаИли.Представление = "";
		КонецЕсли;
		
		Если ЕстьГруппаИ Тогда
			Счетчик = 1;
			Пока СтрокаТаблицы.ПоляГруппировкиНоменклатуры.Свойство("ГруппаИ" + Счетчик) Цикл
				
				Группа = ГруппаИли;
				Если ГруппаИли <> Неопределено Или ЕстьНесколькоГруппИ Тогда
					ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
						?(ГруппаИли <> Неопределено, ГруппаИли, ОтборКомпоновки), "ГруппаИ", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
					ГруппаИ.РежимОтображения = РежимОтображения;
					ГруппаИ.Представление = "";
					Группа = ГруппаИ;
				КонецЕсли;
				
				ГруппаСтрокиТаблицы = СтрокаТаблицы.ПоляГруппировкиНоменклатуры["ГруппаИ" + Счетчик];
				ПоляОтбораГруппыИ = ПоляОтбора(ГруппаСтрокиТаблицы);
				УстановитьОтборПоСпискуПолей(ПоляОтбораГруппыИ, ГруппаСтрокиТаблицы, ОтборКомпоновки, Группа, РежимОтображения);
				Счетчик = Счетчик + 1;
			КонецЦикла;
			
		КонецЕсли;
		
		УстановитьОтборПоСпискуПолей(ПоляОтбора, СтрокаТаблицы.ПоляГруппировкиНоменклатуры, ОтборКомпоновки, ГруппаИли, РежимОтображения);
		
		РезультатНастройки = ЗаполнениеФормСтатистики.СериализоватьРезультатНастройки(НастройкиКомпоновки);
		ЗаполнитьЗначенияСвойств(Запись, РезультатНастройки);
	КонецЦикла;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьЗаписьНастройкиПоОбъектамНаблюдения", Истина);
	НаборЗаписей.Записать();
	
	РегистрыСведений.НастройкаЗаполненияСвободныхСтрокФормСтатистики.ЗаполнитьНастройкиПоОбъектамНаблюдения(
		НаборЗаписей, Организация, ОбъектНаблюдения, НаборЗаписейНастройкаФормСтатистики);
	
КонецПроцедуры

// Возвращает первый объект наблюдения с детализацией по видам деятельности. Приоритет у объекта наблюдения "Производство и продажи".
// 
// Возвращаемое значение:
//   СправочникСсылка.ОбъектыСтатистическогоНаблюдения
//
Функция ОбъектНаблюденияСДетализациейПоВидамДеятельности() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыСтатистическогоНаблюдения.Ссылка КАК ОбъектНаблюдения,
	|	ВЫБОР
	|		КОГДА ОбъектыСтатистическогоНаблюдения.Код = ""ПроизводствоИПродажи""
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	Справочник.ОбъектыСтатистическогоНаблюдения КАК ОбъектыСтатистическогоНаблюдения
	|ГДЕ
	|	ОбъектыСтатистическогоНаблюдения.Детализация = ЗНАЧЕНИЕ(Перечисление.ВидыСвободныхСтрокФормСтатистики.ВидыДеятельности)
	|	И НЕ ОбъектыСтатистическогоНаблюдения.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Если несколько объектов наблюдения с детализацией по видам деятельности, выбираем первый с приоритетом "Производство и продажи".
	Возврат Выборка.ОбъектНаблюдения;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОтборПоСпискуПолей(ПоляОтбора, ПоляГруппировкиСтрокиТаблицы, ОтборКомпоновки, Группа, РежимОтображения)
	
	Если ПоляОтбора.ПоНоменклатурнойГруппе Тогда
		УстановитьОтбор(
			ПоляГруппировкиСтрокиТаблицы, "НоменклатурнаяГруппа", "ВидДеятельности", ОтборКомпоновки, Группа, РежимОтображения);
	КонецЕсли;
	Если ПоляОтбора.ПоНоменклатуре Тогда
		УстановитьОтбор(
			ПоляГруппировкиСтрокиТаблицы, "Номенклатура", "НоменклатураВыручки", ОтборКомпоновки, Группа, РежимОтображения);
	КонецЕсли;
	Если ПоляОтбора.ПоГруппеНоменклатуры Тогда
		ВидСравненияКомпоновки = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
		УдалитьЭлементКомпоновки = Не ПоляОтбора.ПоНоменклатуре;
		УстановитьОтбор(ПоляГруппировкиСтрокиТаблицы, "ГруппаНоменклатуры", "НоменклатураВыручки", ОтборКомпоновки, Группа,
			РежимОтображения, ВидСравненияКомпоновки, УдалитьЭлементКомпоновки);
	КонецЕсли;
	Если ПоляОтбора.ПоВидуНоменклатуры Тогда
		УстановитьОтбор(ПоляГруппировкиСтрокиТаблицы, "ВидНоменклатуры", "НоменклатураВыручки.ВидНоменклатуры", ОтборКомпоновки,
			Группа, РежимОтображения);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтбор(ПоляГруппировки, ИмяПоляГруппировки, ИмяОтбораКомпоновки, ОтборКомпоновки, Группа, РежимОтображения,
	ВидСравненияКомпоновки = Неопределено, УдалитьЭлементКомпоновки = Истина)
	
	Если ВидСравненияКомпоновки <> Неопределено Тогда
		ПравоеЗначение = ПоляГруппировки[ИмяПоляГруппировки];
	Иначе
		Если ПоляГруппировки[ИмяПоляГруппировки].Количество() = 1 Тогда
			ПравоеЗначение = ПоляГруппировки[ИмяПоляГруппировки][0].Значение;
			ВидСравненияКомпоновки = ВидСравненияКомпоновкиДанных.Равно;
		Иначе
			ПравоеЗначение = ПоляГруппировки[ИмяПоляГруппировки];
			ВидСравненияКомпоновки = ВидСравненияКомпоновкиДанных.ВСписке;
		КонецЕсли;
	КонецЕсли;
	
	Если Группа <> Неопределено Тогда
		Если УдалитьЭлементКомпоновки Тогда
			// Удаляем лишний элемент отбора только на верхнем уровне, в группах удалять не нужно.
			УдалитьЭлементОтбора(ОтборКомпоновки, ИмяОтбораКомпоновки);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			Группа, ИмяОтбораКомпоновки, ВидСравненияКомпоновки, ПравоеЗначение,, Истина, РежимОтображения);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ОтборКомпоновки, ИмяОтбораКомпоновки, ПравоеЗначение, ВидСравненияКомпоновки,,Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ПоляОтбора(ПоляГруппировкиСтрокиТаблицы)
	
	ПоляОтбора = Новый Структура;
	ПоляОтбора.Вставить("ПоНоменклатурнойГруппе", ПоляГруппировкиСтрокиТаблицы.Свойство("НоменклатурнаяГруппа")
		И ЗначениеЗаполнено(ПоляГруппировкиСтрокиТаблицы.НоменклатурнаяГруппа));
	ПоляОтбора.Вставить("ПоНоменклатуре", ПоляГруппировкиСтрокиТаблицы.Свойство("Номенклатура")
		И ЗначениеЗаполнено(ПоляГруппировкиСтрокиТаблицы.Номенклатура));
	ПоляОтбора.Вставить("ПоГруппеНоменклатуры", ПоляГруппировкиСтрокиТаблицы.Свойство("ГруппаНоменклатуры")
		И ЗначениеЗаполнено(ПоляГруппировкиСтрокиТаблицы.ГруппаНоменклатуры));
	ПоляОтбора.Вставить("ПоВидуНоменклатуры", ПоляГруппировкиСтрокиТаблицы.Свойство("ВидНоменклатуры")
		И ЗначениеЗаполнено(ПоляГруппировкиСтрокиТаблицы.ВидНоменклатуры));
		
	Возврат ПоляОтбора;
	
КонецФункции

Процедура УдалитьЭлементОтбора(ОбластьУдаления, ИмяПоля)
	
	ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	МассивЭлементов = Новый Массив;
	Для каждого ЭлементОтбора Из ОбластьУдаления.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		ОбластьУдаления.Элементы.Удалить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли