#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОчиститьДанныеПоРегистру(Организация = Неопределено, Контрагент = Неопределено) Экспорт
	
	ОчиститьПоОрганизации = Ложь;
	ОчиститьПоКонтрагенту = Ложь;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОчиститьПоОрганизации = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ОчиститьПоКонтрагенту = Истина;
	КонецЕсли;
	
	Если Не ОчиститьПоОрганизации
		И Не ОчиститьПоКонтрагенту Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
	|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ,
	|	СервисСверкиРасчетовРеестрДокументов.Статус КАК Статус,
	|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторСообщения КАК ИдентификаторСообщения,
	|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
	|	СервисСверкиРасчетовРеестрДокументов.НомерДокумента КАК НомерДокумента
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|ГДЕ ИСТИНА";
	
	Если ОчиститьПоОрганизации Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИСТИНА", "СервисСверкиРасчетовРеестрДокументов.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИСТИНА", "СервисСверкиРасчетовРеестрДокументов.Контрагент = &Контрагент");
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.РасширенныеРежимыЗамещения = Истина;
	НаборЗаписей.Загрузить(Результат.Выгрузить());
	
	// Устанавливаем блокировки для очистки регистра.
	БлокировкаРегистра      = Новый БлокировкаДанных;
	ЭлементБлокировки       = БлокировкаРегистра.Добавить("РегистрСведений.СервисСверкиРасчетовРеестрДокументов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Если ОчиститьПоОрганизации Тогда 
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	КонецЕсли;
	
	Если ОчиститьПоКонтрагенту Тогда
		ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаРегистра.Заблокировать();
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Если ОчиститьПоКонтрагенту Тогда
			ШаблонОшибки = НСтр("ru = 'Не удалось очистить регистр документов для сверки по контрагенту %1.
			|Подробное описание ошибки:
			|%2'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Строка(Контрагент), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Иначе
			ШаблонОшибки = НСтр("ru = 'Не удалось очистить регистр документов для сверки по организации %1.
			|Подробное описание ошибки:
			|%2'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, Строка(Организация), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СервисСверкиРасчетов.ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, ,, ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
