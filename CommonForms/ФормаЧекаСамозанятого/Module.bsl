#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		Параметры,
		"НомерЧека, СсылкаНаЧек, ЧекСамозанятого, ДокументВладелец, ТолькоПросмотр");
	
	Если Не ЗначениеЗаполнено(ДокументВладелец) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Перед загрузкой чека запишите документ'"), , , ,Отказ);
		Возврат;
	КонецЕсли;
	
	МетаданныеВладельца = ДокументВладелец.Метаданные();
	
	Если Не ПравоДоступа("Изменение", МетаданныеВладельца) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если ТолькоПросмотр Тогда
		Элементы.ГруппаОчиститьЗагрузить.Видимость = Ложь;
	КонецЕсли;
	
	ПредыдущееЗначениеСсылкиНаЧек = СсылкаНаЧек;
	
	ВывестиКартинку(АдресКартинки, ЧекСамозанятого);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаНаЧекПриИзменении(Элемент)
	
	Если СсылкаНаЧек = ПредыдущееЗначениеСсылкиНаЧек Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущееЗначениеСсылкиНаЧек = СсылкаНаЧек;
	
	Если СтрНайти(СсылкаНаЧек, ИнтеграцияСПлатформойСамозанятыеБПВызовСервераПовтИсп.АдресСервисаФНС()) = 0 Тогда
		// Это не ссылка на чек, поэтому ничего не делаем
		Возврат;
	КонецЕсли;
	
	ПолучитьДанныеПоСсылкеНаЧек();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(АдресКартинки) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавлениеЧекаСамозанятогоЗавершение", ЭтотОбъект);
		РаботаСФайламиКлиент.ДобавитьФайл(ОписаниеОповещения, ДокументВладелец, ЭтотОбъект, 2);
	Иначе
		ДанныеФайла = ДанныеФайла(ЧекСамозанятого, УникальныйИдентификатор);
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЧекНажатие(Элемент)
	
	СсылкаНаЧек = "";
	НомерЧека = "";
	АдресКартинки = "";
	ПредыдущееЗначениеСсылкиНаЧек = "";
	ЧекБылОчищен = Истина;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЧекНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавлениеЧекаСамозанятогоЗавершение", ЭтотОбъект);
	РаботаСФайламиКлиент.ДобавитьФайл(ОписаниеОповещения, ДокументВладелец, ЭтотОбъект, 2);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиПоСсылке(Команда)
	Если ЗначениеЗаполнено(СсылкаНаЧек) Тогда
		ПерейтиПоНавигационнойСсылке(СсылкаНаЧек);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Структура = Новый Структура;
	Структура.Вставить("СсылкаНаЧек", СсылкаНаЧек);
	Структура.Вставить("НомерЧека", НомерЧека);
	Если Не ЗначениеЗаполнено(АдресКартинки) Тогда
		Структура.Вставить("ЧекСамозанятого", Неопределено);
	Иначе
		Структура.Вставить("ЧекСамозанятого", ЧекСамозанятого);
	КонецЕсли;
	Структура.Вставить("ЧекБылОчищен", ЧекБылОчищен);
	
	Закрыть(Структура);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавлениеЧекаСамозанятогоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧекСамозанятого = Результат.ФайлСсылка;
	ВывестиКартинку(АдресКартинки, ЧекСамозанятого);

	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиКартинку(АдресКартинки, Знач ЧекСамозанятого)
	
	Если ЗначениеЗаполнено(ЧекСамозанятого) Тогда
		АдресКартинки = РаботаСФайлами.ДанныеФайла(ЧекСамозанятого).СсылкаНаДвоичныеДанныеФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеФайла(ФайлКартинки, УникальныйИдентификатор)
	
	ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ДополнительныеПараметры.ИдентификаторФормы = УникальныйИдентификатор;
	
	Возврат РаботаСФайлами.ДанныеФайла(ФайлКартинки, ДополнительныеПараметры);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ЗагрузитьЧек.Видимость = Не ЗначениеЗаполнено(Форма.АдресКартинки);
	Элементы.ОчиститьЧек.Видимость = ЗначениеЗаполнено(Форма.АдресКартинки);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеПоСсылкеНаЧек()
	
	НомерЧека = ИнтеграцияСПлатформойСамозанятыеБП.НомерЧекаИзСсылки(СсылкаНаЧек);
	ЧекСамозанятого = ИнтеграцияСПлатформойСамозанятыеБП.ЧекСамозанятогоИзИнтернета(
		СсылкаНаЧек, ЧекСамозанятого, ДокументВладелец);
	
	Если ЗначениеЗаполнено(ЧекСамозанятого) Тогда
		ВывестиКартинку(АдресКартинки, ЧекСамозанятого);
	Иначе
		ТекстСообщения = НСтр("ru='Не удалось скачать чек с сайта ФНС. 
			|Проверьте ссылку или попробуйте загрузить чек позднее'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СсылкаНаЧек");
	КонецЕсли;
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("ИнтеграцияСамозанятыеБП.ЗагрузкаЧеков.ПоСсылке");
	
КонецПроцедуры

#КонецОбласти