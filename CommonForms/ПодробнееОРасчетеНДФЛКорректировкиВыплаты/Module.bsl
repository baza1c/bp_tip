
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ФизическоеЛицо = Параметры.ФизическоеЛицо;
	
	ФамилияИнициалы = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Строка(ФизическоеЛицо));
	Заголовок = СтрШаблон(НСтр("ru = '%1 (налог к зачету/возврату)'"), ФамилияИнициалы);
	
	ДанныеКорректировкиВыплаты = ПолучитьИзВременногоХранилища(Параметры.ДанныеКорректировкиВыплаты);
	КорректировкиВыплаты.Загрузить(ДанныеКорректировкиВыплаты);
	
	Элементы.ГруппаКнопокПросмотр.Видимость       = ТолькоПросмотр;
	Элементы.ГруппаКнопокРедактирование.Видимость = НЕ ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаОК.КнопкаПоУмолчанию      = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНДФЛ

&НаКлиенте
Процедура КорректировкиВыплатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока и Не Копирование Тогда
		ТекущаяСтрока = Элементы.КорректировкиВыплаты.ТекущиеДанные;
		ТекущаяСтрока.ФизическоеЛицо = ФизическоеЛицо;
		ТекущаяСтрока.ФиксРасчет     = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Модифицированность Тогда
		Оповестить("ИзмененыКорректировкиВыплаты", РезультатРедактированияКорректировкиВыплаты(), ЭтаФорма);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция РезультатРедактированияКорректировкиВыплаты()
	
	Возврат ПоместитьВоВременноеХранилище(КорректировкиВыплаты.Выгрузить());
	
КонецФункции

#КонецОбласти


