#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИдентификаторУстройстваЭТ = Параметры.ИдентификаторУстройстваЭТ;
	ДокументОснование         = Параметры.Ссылка;
	Отказ = НЕ ЗначениеЗаполнено(ИдентификаторУстройстваЭТ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	ИспользоватьПодключаемоеОборудование =  МенеджерОборудованияБП.ИспользуетсяОборудование("СканерШтрихкода");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ИспользоватьПодключаемоеОборудование Тогда
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
			Неопределено,
			ЭтотОбъект,
			"СканерШтрихкода");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				Штрихкод = Параметр[0];
			Иначе
				Штрихкод = Параметр[1][1];
			КонецЕсли;
			
			// Отсканировали QR с идентификатором корзины
			Если СтрДлина(Штрихкод) = 24 
				И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Штрихкод) Тогда
				
				Закрыть(Штрихкод);
			Иначе
				// Отсканировали кассовый QR
				ПолучениеИдентификатораКорзины(Штрихкод);
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьВводШтрихкода(Команда)
	Если НЕ ЗначениеЗаполнено(Заголовок) Тогда
		Заголовок = НСтр("ru = 'Введите штрихкод'");
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПоказатьВводШтрихкодаЗавершение",
		ЭтотОбъект);
	ПоказатьВводЗначения(Оповещение, "", НСтр("ru = 'Введите идентификатор корзины'"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВводШтрихкодаЗавершение(Значение, ДополнительныеПараметры) Экспорт

	Закрыть(Значение);

КонецПроцедуры

&НаКлиенте
Процедура ПолучениеИдентификатораКорзины(Штрихкод)
	ПараметрыОперации = ЭлектронныеСертификатыНСПКБПВызовСервера.ПараметрыОперацииНСПКПоКассеККМ(
		ДокументОснование, ИдентификаторУстройстваЭТ,,Строка(Новый УникальныйИдентификатор));
		
	ПараметрыОперации.QRКодЧекаККТ = Штрихкод;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПолучениеИдентификатораКорзиныЗавершение", ЭтотОбъект);
	ЭлектронныеСертификатыНСПККлиент.НачатьПолучениеИдентификатораКорзины(ОповещениеОЗавершении, ПараметрыОперации);
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеИдентификатораКорзиныЗавершение(Результат, ДОполнительныеПараметры) Экспорт
	ПараметрЗакрытия = Неопределено;
	Если НЕ Результат.Результат Тогда
		ПараметрЗакрытия = Результат.ИдентификаторКорзины;
	КонецЕсли; 
	
	Закрыть(ПараметрЗакрытия);
КонецПроцедуры

#КонецОбласти
