#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Интерфейс77 = Константы.ИнтерфейсВерсии77.Получить();
	Интерфейс82 = 
		КлиентскоеПриложение.ТекущийВариантИнтерфейса() = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2;
	
	Если Не Интерфейс77 И Не Интерфейс82 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	ВключитьСтандартныйИнтерфейс = Параметры.ВариантПредложения = "ВключитьСтандартныйИнтерфейс";
	ИзменитьКомандныйИнтерфейс = Параметры.ВариантПредложения = "ИзменитьКомандныйИнтерфейс";
	ВключитьИнтерфейсТакси = Параметры.ВариантПредложения = "ВключитьИнтерфейсТакси";
	
	Если Не ВключитьСтандартныйИнтерфейс И Не ИзменитьКомандныйИнтерфейс И Не ВключитьИнтерфейсТакси Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияВключитьСтандартныйИнтерфейс77.Видимость = ВключитьСтандартныйИнтерфейс И Интерфейс77;
	Элементы.ДекорацияИзменитьКомандныйИнтерфейс77.Видимость = ИзменитьКомандныйИнтерфейс И Интерфейс77;
	Элементы.ДекорацияИзменитьКомандныйИнтерфейс82.Видимость = ВключитьИнтерфейсТакси И Интерфейс82;
	
	ПолныеПрава = РольДоступна("ПолныеПрава");
	
	Элементы.ДекорацияИзменениеРежима.Видимость = ПолныеПрава И Интерфейс77;
	Элементы.ДекорацияИзменениеРежимаИнтерфейс82.Видимость = Интерфейс82 И Не Интерфейс77;

	Элементы.ДекорацияОбратитесьКАдминистратору.Видимость = Не ПолныеПрава И Интерфейс77;
	
	Если ИзменитьКомандныйИнтерфейс Тогда
		Элементы.ВключитьСтандартныйИнтерфейс.Заголовок = НСтр("ru='Включить стандартный интерфейс'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьСтандартныйИнтерфейс(Команда)
	
	ЗаписатьНастройки();
	ПропуститьПредупреждениеПередЗавершениемРаботыСистемы = Истина;
	ЗавершитьРаботуСистемы(Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьПозже(Команда)
	
	ОбновитьНастройки();	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНастройки()
	
	Режим = "ИнтерфейсТакси";
	ТекущийИнтерфейсТаксиПростой = Константы.ИнтерфейсТаксиПростой.Получить();
	Если ТекущийИнтерфейсТаксиПростой И Интерфейс82 Тогда
		Режим = "ИнтерфейсТаксиПростой";
	КонецЕсли;
	Если Интерфейс77 Тогда
		ОбщегоНазначенияБП.УстановитьРежимКомандногоИнтерфейса(Режим);
	Иначе
		ПользователиСлужебный.УстановитьНачальныеНастройки(ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
	КонецЕсли;
	
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаСменыИнтерфейса"),,ТекущаяДатаСеанса(),,);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройки()
	
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаСменыИнтерфейса"),,ТекущаяДатаСеанса(),,);
	
КонецПроцедуры

#КонецОбласти
