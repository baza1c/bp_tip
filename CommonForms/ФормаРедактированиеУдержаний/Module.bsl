
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.АдресПараметровВХранилище) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// "Распаковываем" параметры
	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);
	
	ДесериализованныйОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПараметрыРасчета.СериализованныйОбъект);
	ИмяТипа = ДесериализованныйОбъект.Метаданные().Имя;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасчета, "Организация, Сотрудник, ФизическоеЛицо, МесяцНачисления");
	
	ДобавитьРеквизитФормыОбъект();
	
	ЗначениеВДанныеФормы(ДесериализованныйОбъект, ЭтотОбъект["Объект"]);
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	СписокКадровыхДанных = "ФамилияИО";
	КадровыеДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическиеЛица, СписокКадровыхДанных, МесяцНачисления);
	
	ПредставлениеСотрудника = КадровыеДанныеФизЛиц[0].ФамилияИО;
	Заголовок = СтрШаблон(НСтр("ru='Удержания (%1)'"), ПредставлениеСотрудника);
	
	ЗаполнитьТаблицуУдержаний();
	ПолучитьИтоги(ЭтотОбъект);
	
	Элементы.ГруппаКнопокПросмотр.Видимость       = ТолькоПросмотр;
	Элементы.ГруппаКнопокРедактирование.Видимость = НЕ ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаОК.КнопкаПоУмолчанию      = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИдентификаторВладельца = ВладелецФормы.УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросСохранитьИзмененияЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыУдержания

&НаКлиенте
Процедура УдержанияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НЕ Копирование И НоваяСтрока Тогда
		ДанныеТекущейСтроки = Элементы.Удержания.ТекущиеДанные;
		ДанныеТекущейСтроки.Сотрудник      = ФизическоеЛицо;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияУдержаниеПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Удержания.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		ОбработатьИзменениеУдержания(ДанныеСтроки.Удержание, ДанныеСтроки.КатегорияУдержания, ДанныеСтроки.Контрагент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПолучитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПослеУдаления(Элемент)
	
	ПолучитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияУдержаниеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ Модифицированность ИЛИ ТолькоПросмотр Тогда
		Закрыть();
	ИначеЕсли ПроверитьЗаполнениеРеквизитов() Тогда
		ПеренестиИзмененияВОбъектФормыВладельца();
		Закрыть(АдресВХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьРеквизитФормыОбъект()
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Объект", Новый ОписаниеТипов("ДокументОбъект." + ИмяТипа)));
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуУдержаний()
	
	МассивСтрок = ЭтотОбъект["Объект"].Удержания.НайтиСтроки(Новый Структура("Сотрудник", ФизическоеЛицо));
	
	Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
		ЗаполнитьЗначенияСвойств(Удержания.Добавить(), СтрокаМассива);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПолучитьИтоги(Форма)
	
	Форма.ИтогоУдержано = Форма.Удержания.Итог("Результат");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбработатьИзменениеУдержания(Удержание, КатегорияУдержания, Контрагент)

	КатегорияУдержания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Удержание, "КатегорияУдержания");
	Если КатегорияУдержания = Перечисления.КатегорииУдержаний.ДСВ Тогда
		// Для ДСВ не указываем получателя
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;

КонецФункции 

&НаКлиенте
Процедура ВопросСохранитьИзмененияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ПроверитьЗаполнениеРеквизитов() Тогда
			ПеренестиИзмененияВОбъектФормыВладельца();
			Закрыть(АдресВХранилище);
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитов()
	
	ПроверкаПройдена = Истина;
	Для Каждого СтрокаТаблицы ИЗ Удержания Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Удержание) Тогда
			ИндексСтроки = Удержания.Индекс(СтрокаТаблицы);
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",
				"Заполнение",
				"Удержание",
				ИндексСтроки + 1,
				"Удержания");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Удержания[" + ИндексСтроки + "].Удержание");
			ПроверкаПройдена = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПроверкаПройдена;
	
КонецФункции

#Область ПереносВОсновнуюФорму

&НаКлиенте
Процедура ПеренестиИзмененияВОбъектФормыВладельца()
	
	Если Модифицированность Тогда
		Оповестить("ИзмененыРезультатыРачетаУдержания", ПоместитьИзмененныеДанныеВоВременноеХранилище(), ЭтотОбъект);
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьИзмененныеДанныеВоВременноеХранилище()
	
	ВозвращаемыеСведения = Новый Структура;
	
	ДанныеУдержаний = Удержания.Выгрузить();
	ДанныеУдержаний.ЗаполнитьЗначения(ФизическоеЛицо, "Сотрудник");
	
	ВозвращаемыеСведения.Вставить("Удержания",      ДанныеУдержаний);
	ВозвращаемыеСведения.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	ВозвращаемыеСведения.Вставить("Сотрудник",      Сотрудник);
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(ВозвращаемыеСведения, ИдентификаторВладельца);
	
	Возврат АдресВХранилище;
	
КонецФункции

#КонецОбласти

#КонецОбласти
