#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Создание реквизитов для табличных документов.
	НовыеРеквизитыФормы = Новый Массив; // Массив из РеквизитФормы -
	Для НомерПечатнойФормы = 1 По Параметры.КоллекцияПечатныхФорм.Количество() Цикл
		ИмяРеквизита = "ПечатнаяФорма" + Формат(НомерПечатнойФормы,"ЧГ=0");
		РеквизитФормы = Новый РеквизитФормы(
			ИмяРеквизита,
			Новый ОписаниеТипов("ТабличныйДокумент, Строка")
			,
			,
			Параметры.КоллекцияПечатныхФорм[НомерПечатнойФормы - 1].СинонимМакета);
		НовыеРеквизитыФормы.Добавить(РеквизитФормы);
	КонецЦикла;
	ИзменитьРеквизиты(НовыеРеквизитыФормы);
	
	ВнешниеФайлы = Новый Соответствие;
	ОбъектыПечати = Параметры.ОбъектыПечати;
	Для Каждого ЭлементКоллекции Из Параметры.КоллекцияПечатныхФорм Цикл
		НоваяНастройка = НастройкиПечатныхФорм.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяНастройка, ЭлементКоллекции);
		НоваяНастройка.Печатать = Истина;
		НоваяНастройка.ИмяРеквизита = "ПечатнаяФорма" + Формат(НастройкиПечатныхФорм.Количество(),"ЧГ=0");
		НоваяНастройка.Представление = ЭлементКоллекции.СинонимМакета;
		Если ТипЗнч(ЭлементКоллекции.ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтотОбъект,
				НоваяНастройка.ИмяРеквизита,
				ЭлементКоллекции.ТабличныйДокумент);
		Иначе
			ВнешниеФайлы.Вставить(ЭлементКоллекции.ТабличныйДокумент, НоваяНастройка.ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	НастройкиПечатныхФорм.Сортировать("Представление");
	ФайлыРеквизитов = Новый Соответствие;
	Если ЗначениеЗаполнено(ВнешниеФайлы) Тогда
		ДвоичныеДанныеФайлов = РаботаСФайлами.ДвоичныеДанныеФайлов(ОбщегоНазначения.ВыгрузитьКолонку(ВнешниеФайлы, "Ключ"));
		Для Каждого ДвоичныеДанныеФайла Из ДвоичныеДанныеФайлов Цикл
			ИмяРеквизита = ВнешниеФайлы.Получить(ДвоичныеДанныеФайла.Ключ);
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтотОбъект,
				ИмяРеквизита,
				ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла.Значение, УникальныйИдентификатор));
			ФайлыРеквизитов.Вставить(ИмяРеквизита, ДвоичныеДанныеФайла.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	ФайлыРеквизитовФормы = Новый ФиксированноеСоответствие(ФайлыРеквизитов);
	
	ПараметрыПечати = Параметры.ПараметрыПечати;
	Если Параметры.ПараметрыПечати = Неопределено Тогда
		ПараметрыПечати = Новый Структура;
	КонецЕсли;
	Если Не ПараметрыПечати.Свойство("ДополнительныеПараметры") Тогда
		Параметры.ПараметрыПечати = Новый Структура("ДополнительныеПараметры", ПараметрыПечати);
		Для Каждого ПараметрПечати Из ПараметрыПечати Цикл
			Параметры.ПараметрыПечати.Вставить(ПараметрПечати.Ключ, ПараметрПечати.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыВывода = Параметры.ПараметрыВывода;
	Если ПараметрыВывода = Неопределено Тогда
		ПараметрыВывода = УправлениеПечатью.ПодготовитьСтруктуруПараметровВывода();
	КонецЕсли;
	
	Если ИнтеграцияСРаботаВРоссии.ДоступнаПередачаДокументовНаРаботаВРоссии() Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользуетсяЭДОБЗККабинетСотрудника") Тогда
			СвойстваКоманды = ЭДОБЗК.СвойстваКомандыПередачиНаПорталРаботаВРоссииИВКабинетСотрудника();
		Иначе
			СвойстваКоманды = ИнтеграцияСРаботаВРоссии.СвойстваКомандыПередачиНаПорталРаботаВРоссии();
		КонецЕсли;
	Иначе
		СвойстваКоманды = ИнтеграцияКабинетСотрудника.СвойстваКомандыПередачиВКабинетСотрудника();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПередатьПодписанныеPDFВСервисКабинетСотрудника",
		"Заголовок",
		СвойстваКоманды.Заголовок);
	
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		ЭтотОбъект,
		"ПередатьПодписанныеPDFВСервисКабинетСотрудника",
		СвойстваКоманды.Подсказка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПередатьПодписанныеPDFВСервисКабинетСотрудника",
		"Картинка",
		СвойстваКоманды.Картинка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПередатьПодписанныеPDFВСервисКабинетСотрудника",
		"Видимость",
		Параметры.ИнициацияПодписания);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НастроитьИПодписать",
		"Видимость",
		Параметры.ИнициацияПодписания);
	
	Если Параметры.ИнициацияПодписания Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПередатьПодписанныеPDFВСервисКабинетСотрудника",
			"КнопкаПоУмолчанию",
			Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПодписатьПечатныеФормы",
			"Видимость",
			Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОтказатьВПодписанииПечатныхФорм",
			"Видимость",
			Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОзнакомленоСКомментариямиКПечатнойФорме",
			"Видимость",
			Ложь);
	Иначе
		ДоступныеКоманды = ДоступныеКомандыФормыПечатьДокументов();
		ТолькоПодписание = Параметры.Свойство("ПодписаниеПечатныхФорм")
			Или ДоступныеКоманды.Подписать;
		Если ДоступныеКоманды.Ознакомиться Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ПодписатьПечатныеФормы",
				"Видимость",
				Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ОтказатьВПодписанииПечатныхФорм",
				"Видимость",
				Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ОзнакомленоСКомментариямиКПечатнойФорме",
				"КнопкаПоУмолчанию",
				Истина);
		ИначеЕсли ТолькоПодписание Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ОзнакомленоСКомментариямиКПечатнойФорме",
				"Видимость",
				Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиПечатныхФорм.Количество() = 1 Тогда
		Заголовок = СтрШаблон(НСтр("ru = 'Подписание документов (%1)'"),
			НастройкиПечатныхФорм[0].Представление);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"НастройкиПечатныхФорм",
			"Видимость",
			Ложь);
		УстановитьОтображениеДокумента(ЭтотОбъект, ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			ЭтотОбъект, НастройкиПечатныхФорм[0].ИмяРеквизита));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновленыДанныеДокументовЭДОБЗК"
		И Источник = ЭтотОбъект Тогда
		
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиПечатныхФорм

&НаКлиенте
Процедура НастройкиПечатныхФормПриАктивизацииСтроки(Элемент)
	
	Если Элементы.НастройкиПечатныхФорм.ТекущиеДанные <> Неопределено Тогда
		
		ТекущийДокумент = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			ЭтотОбъект, Элементы.НастройкиПечатныхФорм.ТекущиеДанные.ИмяРеквизита);
		
		УстановитьОтображениеДокумента(ЭтотОбъект, ТекущийДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПередатьПодписанныеPDFВСервисКабинетСотрудника(Команда)
	
	ЭДОБЗККлиент.ПечатьДокументовВыполнитьКоманду(ЭтотОбъект, Команда, Ложь, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьПечатныеФормы(Команда)
	
	ЭДОБЗККлиент.ПечатьДокументовВыполнитьКоманду(ЭтотОбъект, Команда, Ложь, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтказатьВПодписанииПечатныхФорм(Команда)
	
	ЭДОБЗККлиент.ПечатьДокументовВыполнитьКоманду(ЭтотОбъект, Команда, Ложь, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОзнакомленоСКомментариямиКПечатнойФорме(Команда)
	
	ЭДОБЗККлиент.ПечатьДокументовВыполнитьКоманду(ЭтотОбъект, Команда, Ложь, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьИПодписать(Команда)
	
	ЭДОБЗККлиент.НастроитьИПодписать(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДоступныеКомандыФормыПечатьДокументов()
	
	ДоступныеКоманды = Новый Структура("Подписать,Ознакомиться", Ложь, Ложь);
	
	ОписанияФайлов = ЭДОБЗККлиентСервер.ОписанияФайловПечатныхФормИзПараметровФормы(Параметры);
	Если ОписанияФайлов <> Неопределено Тогда
		ФайлыПечатныхФорм = Новый Массив;
		Для Каждого ДанныеИдентификаторовПечатныхФорм Из ОписанияФайлов.ПечатныеФормыОбъектов Цикл
			Для Каждого ДанныеИдентификатораПечатнойФормы Из ДанныеИдентификаторовПечатныхФорм.Значение Цикл
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФайлыПечатныхФорм, ДанныеИдентификатораПечатнойФормы.Значение);
			КонецЦикла;
		КонецЦикла;
		Если ЗначениеЗаполнено(ФайлыПечатныхФорм) Тогда
			ФайлыНаПодписьПользователя = РегистрыСведений.ЗапланированныеДействияСФайламиДокументовЭДОБЗК.ФайлыНаПодписьПользователя(Истина, , Истина);
			Если ФайлыНаПодписьПользователя.Количество() >= ФайлыПечатныхФорм.Количество() Тогда
				ДоступныеКоманды.Подписать = Истина;
				Для Каждого ФайлПечатныхФорм Из ФайлыПечатныхФорм Цикл
					СтруктураПоиска = Новый Структура("ФайлОбъекта,Действие", ФайлПечатныхФорм, Перечисления.ДействияСФайламиДокументовЭДОБЗК.Подписать);
					Если ФайлыНаПодписьПользователя.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
						ДоступныеКоманды.Подписать = Ложь;
					КонецЕсли;
					СтруктураПоиска = Новый Структура("ФайлОбъекта,Действие", ФайлПечатныхФорм, Перечисления.ДействияСФайламиДокументовЭДОБЗК.Ознакомиться);
					Если ФайлыНаПодписьПользователя.НайтиСтроки(СтруктураПоиска).Количество() > 0 Тогда
						ДоступныеКоманды.Ознакомиться = Истина;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоступныеКоманды;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеДокумента(УправляемаяФорма, ТекущийДокумент)
	
	ЭтоТабличныйДокумент = ТипЗнч(ТекущийДокумент) = Тип("ТабличныйДокумент");
	Если ЭтоТабличныйДокумент Тогда
		УправляемаяФорма.ТекущаяПечатнаяФорма = ТекущийДокумент;
	Иначе
		УправляемаяФорма.ВнешнийДокумент.Прочитать(ПолучитьИзВременногоХранилища(ТекущийДокумент).ОткрытьПотокДляЧтения());
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ТекущаяПечатнаяФорма",
		"Видимость",
		ЭтоТабличныйДокумент);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ВнешнийДокумент",
		"Видимость",
		Не ЭтоТабличныйДокумент);
	
КонецПроцедуры

#КонецОбласти
