// Для работы с элементами формы используются методы общих модулей, например, "НастройкиУчетаНДФЛФормы", "НастройкиУчетаНДСФормы" и пр.
// Эти же методы вызываются из форм записей соответствующих регистров сведений, поэтому состав их элементов должен соответствовать
// общей форме "НалогиИОтчеты".

#Область ОбъявлениеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЭтоМедленныйРежимРаботы = ОбщегоНазначенияБП.ЭтоМедленныйРежимРаботы();
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	Если Параметры.КонтекстныйВызов Тогда
		Элементы.Организация.ТолькоПросмотр      = Истина;
		Элементы.Организация.РежимВыбораИзСписка = Ложь;
		Элементы.Организация.КнопкаВыбора        = Ложь;
	КонецЕсли;
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	УстановитьПараметрыВыбораОрганизации(ЭтотОбъект, ДоступныеОрганизации);
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организация = Параметры.Организация;
		ПереданОтборПоОрганизации = Истина;
	Иначе
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ПереданОтборПоОрганизации = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("СтатусФизическогоЛица") И ЗначениеЗаполнено(Параметры.СтатусФизическогоЛица) Тогда
		ЗарегистрированВКачествеИП
			= Параметры.СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.ИндивидуальныйПредприниматель;
		
	Иначе
		ЗарегистрированВКачествеИП
			= Организация.СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.ИндивидуальныйПредприниматель;
	КонецЕсли;
	
	ЗаполнитьСведенияОбОрганизации();
	
	ОткрытиеИзБаннераАнтикризис = Параметры.Свойство("ОткрытиеИзБаннераАнтикризис");
	
	ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	ДатаИзменения = НачалоГода(ТекущаяДата);
	
	УчетЗарплатыИКадровСредствамиБухгалтерии = Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Получить();
	ВедетсяУчетИмущества = Константы.ВедетсяУчетНМА.Получить() ИЛИ Константы.ВедетсяУчетОсновныхСредств.Получить();
	ВедетсяУчетЗарплаты  = УчетЗарплаты.ИспользуетсяПодсистемаУчетаЗарплатыИКадров();
	ИнтерфейсТаксиПростой = ОбщегоНазначенияБП.ЭтоПростойИнтерфейс();
	УстановитьЗначенияРеквизитовБезЗакрывающихДокументов();
	
	ПравоРедактированияНастроек =
		ПравоДоступа("Изменение", Метаданные.РегистрыСведений.УчетнаяПолитика)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НалогиОтчеты)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПорядокУплатыАкцизов)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПорядокУплатыНалоговНаМестах)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ВариантыПримененияТребованийЗаконодательства)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаУСН)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаНДС);
		
	Если ЭтоЮрЛицо Тогда
		ПравоРедактированияНастроек = ПравоРедактированияНастроек
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыль)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СтавкиНалогаНаИмущество);
		
		ПравоРедактированияСпискаПрямыхРасходов = ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ);
		
	Иначе
		ПравоРедактированияНастроек = ПравоРедактированияНастроек
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаНДФЛ)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП)
			И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС);
	КонецЕсли;
	
	Если ВедетсяУчетЗарплаты Тогда
		Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
			ПравоРедактированияНастроек = ПравоРедактированияНастроек
				И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов)
				И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев)
				И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаСтраховыхВзносов);
		КонецЕсли;
	КонецЕсли;
	
	ПериодПереносаСроков = РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов.ПериодПереносаСроковНалоговОтчетов();
	ВозможноПродлениеСроковНалоговОтчетов = (ТекущаяДата >= ПериодПереносаСроков.Начало И ТекущаяДата <= КонецДня(ПериодПереносаСроков.Окончание));
	
	ПричинаНедоступностиЗагрузкиСпискаСтатФорм = "";
	ДоступнаЗагрузкаФормИзСервиса = ЗадачиБухгалтераСтатистикаВызовСервера.ДоступнаЗагрузкаФормИзСервиса(ПричинаНедоступностиЗагрузкиСпискаСтатФорм);
	
	ИдентификаторФормыВладельца = Параметры.ИдентификаторФормыВладельца;
	Если ПереданОтборПоОрганизации И ЗначениеЗаполнено(ИдентификаторФормыВладельца) Тогда
		Элементы.Организация.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		УстановитьДоступностьВидимостьНастроек(Ложь);
	Иначе
		Если ДоступныеОрганизации.Найти(Организация) = Неопределено Тогда
			ОрганизацияНедоступна = Истина;
			УстановитьДоступностьВидимостьНастроек(Ложь);
			Возврат;
		Иначе
			ПодготовитьФормуНастройкиСписка();
			ПодготовитьФормуНаСервере();
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ФормированиеОстатковПоНДС Тогда
		ФормированиеОстатковПоНДС = Истина;
		ДатаНачалаВеденияРаздельногоУчетаНДС = Параметры.ДатаНачалаВеденияРаздельногоУчетаНДС;
		ХранилищеОбщихНастроек.Сохранить(ВРег("ФормированиеОстатковПоНДС"),,Ложь);
	КонецЕсли;
	
	Если Параметры.ОсвобождениеОтУплатыНДС Тогда
		ОсвобождениеОтУплатыНДС = Истина;
		ДатаОсвобожденияОтУплатыНДС = Параметры.ДатаОсвобожденияОтУплатыНДС;
		ДатаИзменения = Параметры.ДатаОсвобожденияОтУплатыНДС;
	КонецЕсли;
	
	Если Параметры.ПрименениеНДСНаУСН Тогда
		ПрименениеНДСНаУСН = Истина;
		НастройкиСистемыНалогообложения.ПлательщикНДСПрименяющийУСН = Истина;
		Если ЗначениеЗаполнено(Параметры.ДатаПримененияНДСНаУСН)
			И Параметры.ДатаПримененияНДСНаУСН >= УчетУСНКлиентСервер.ДатаНачалаПримененияНДС() Тогда
			ДатаИзменения = Параметры.ДатаПримененияНДСНаУСН;
		Иначе
			ДатаИзменения = Макс(УчетУСНКлиентСервер.ДатаНачалаПримененияНДС(), ДатаИзменения);
		КонецЕсли;
		Если НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент
			И Не УчетПСН.ИмеютсяДействующиеПатенты(ДатаИзменения, Организация) Тогда
			НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент = Ложь;
		КонецЕсли;
		Элементы.ДекорацияПрименениеНДСПриУСН.Видимость = Истина;
		Элементы.НастройкиСистемыНалогообложенияПлательщикНДСПрименяющийУСН.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СтавкаНДСПриУСН)
		И ЗначениеЗаполнено(Параметры.ДатаИзмененияСтавкиНДСПриУСН) Тогда
		
		ИзменениеСтавкиНДСПриУСН = Истина;
		СтавкаНДСПриУСН = НастройкиСистемыНалогообложенияФормы.ИндексСтавкиНДСПоЗначению(Параметры.СтавкаНДСПриУСН);
		НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН = Параметры.СтавкаНДСПриУСН;
		ДатаИзменения = Параметры.ДатаИзмененияСтавкиНДСПриУСН;
		
	КонецЕсли;
	
	ЗаполнитьНастройкиПоАкцизам();
	ПравоРедактированияАкцизов = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПорядокУплатыАкцизов);
	Элементы.ТаблицаНастроекПоАкцизамВидТовара.Доступность              = ПравоРедактированияАкцизов;
	Элементы.ТаблицаНастроекПоАкцизамВключен.Доступность                = ПравоРедактированияАкцизов;
	Элементы.ТаблицаНастроекПоАкцизамСпособПолученияТоваров.Доступность = ПравоРедактированияАкцизов;
	
	Если ЗначениеЗаполнено(Параметры.ТекущаяЗакладка) Тогда
		НайденныеЗакладки = Список.НайтиСтроки(Новый Структура("Идентификатор", Параметры.ТекущаяЗакладка));
		Если НайденныеЗакладки.Количество() <> 0 Тогда
			Элементы.Список.ТекущаяСтрока = НайденныеЗакладки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ПолныйСписок Тогда
		Элементы.Список.ОтборСтрок = Новый ФиксированнаяСтруктура("Служебный", Ложь);
		ПолныйСписок = Истина;
	КонецЕсли;
	
	// На текущую дату надо получить значения по деятельности НДС 0% и списке кодов операций.
	
	ИсключитьПустоеЗначение = Новый Массив;
	ИсключитьПустоеЗначение.Добавить("");
	ТаблицаВыбора.Загрузить(УчетНДС.КлассификаторКодовОперацийПоСтавке0(ТекущаяДата,,ИсключитьПустоеЗначение));
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.ОбщаяФорма.НалогиИОтчеты",
		"ОбщаяФорма",
		НСтр("ru='Новости: Налоги и отчеты'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОрганизацияНедоступна Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатБлокировки) Тогда
		ПоказатьПредупреждение( , РезультатБлокировки);
		РезультатБлокировки = "";
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ЭтоВебКлиент = Истина;
	#КонецЕсли
	Если ЭтоМедленныйРежимРаботы ИЛИ ЭтоВебКлиент Тогда
		Интервал = ?(ЭтоВебКлиент, 1, 0.5);
		ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьСписокНастройкиНалоговОтчетов", Интервал, Истина);
	Иначе
		ЗаполнитьСписокНастройкиНалоговОтчетов();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
	
	Если ФормированиеОстатковПоНДС Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ФормированиеОстатковПоНДС", 2, Истина);
	КонецЕсли;
	
	Если ОсвобождениеОтУплатыНДС Тогда
		УстановитьСтраницуНДС();
		ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			ДатаОсвобожденияОтУплатыНДС, КонецМесяца(ДатаОсвобожденияОтУплатыНДС), Истина);
	КонецЕсли;
		
	Если ПрименениеНДСНаУСН Тогда
		Модифицированность = Истина;
		ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			ДатаИзменения, КонецМесяца(ДатаИзменения), Истина);
		ОтобразитьПериоды("НастройкиСистемыНалогообложения");
	ИначеЕсли ИзменениеСтавкиНДСПриУСН Тогда
		Модифицированность = Истина;
		ОтобразитьПериоды("НастройкиУчетаНДС");
		УстановитьСтраницуНДС();
		ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			ДатаИзменения, КонецМесяца(ДатаИзменения), Истина);
	КонецЕсли;
	
	Если ОткрытиеИзБаннераАнтикризис Тогда
		УстановитьСтраницуАнтикризис();
	КонецЕсли;
	
	ПолучитьОписаниеСтавокУСНВФоне();
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// В Простом интерфейсе форма является подчиненной форме Организация
	// При вызове ОбновитьИнтерфейс() подчиненные формы переоткрываются.
	// Закрываем подчиненную форму только в случае закрытия основной формы
	ЭтоПодчиненнаяФорма = ИнтерфейсТаксиПростой И ЗначениеЗаполнено(ИдентификаторФормыВладельца);
	Если ЭтоПодчиненнаяФорма И Не ЗавершениеРаботы И Не ЗакрытьФорму Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Не задаем вопрос о сохранении для подчиненных форм:
	// - подчиненная форма может быть не видна на экране в момент закрытия, тогда пользователю будет не ясно о чем вопрос
	// - модифицированность может быть и основного окна, тогда будет два одинаковых вопроса.
	Если Модифицированность И Не ЭтоПодчиненнаяФорма Тогда
		
		Отказ = Истина;
		
		Если Не ОрганизацияВелаРаздельныйУчет 
			И (ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
			ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов) Тогда
			ТекстВопроса = НСтр("ru = 'Изменения не будут сохранены. Закрыть?'");
			Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершениеПриИзменениеНастроекРаздельногоУчета", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе 
			ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
			Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Элементы.ОбщиеСтраницыНастроек.ТекущаяСтраница = Элементы.ОбщаяСтраницаОжидание Тогда
		ОтменитьПостроениеСписка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "Запись_НастройкиСистемыНалогообложения" И Источник <> ЭтотОбъект)
		ИЛИ ИмяСобытия = "Запись_НастройкиУчетаНалогаНаПрибыль"
		ИЛИ ИмяСобытия = "Запись_НастройкиУчетаУСН"
		ИЛИ ИмяСобытия = "Запись_НастройкиУчетаНДС"
		ИЛИ ИмяСобытия = "Запись_НастройкиУчетаНДФЛ"
		ИЛИ ИмяСобытия = "Запись_НастройкиУчетаСтраховыхВзносовИП"
		ИЛИ ИмяСобытия = "Запись_ПрименяемыеТарифыСтраховыхВзносов"
		ИЛИ ИмяСобытия = "Запись_СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев"
		ИЛИ ИмяСобытия = "Запись_УчетнаяПолитикаПоНДФЛ"
		ИЛИ ИмяСобытия = "Запись_НастройкиУчетаСтраховыхВзносов" Тогда
		
		ИмяРаздела = СтрЗаменить(ИмяСобытия, "Запись_","");
		Если Параметр.Свойство("Организация") Тогда
			Если Параметр.Организация <> Организация Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если Параметр.Свойство("ГоловнаяОрганизация") Тогда
			Если Параметр.ГоловнаяОрганизация <> ГоловнаяОрганизация Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если Параметр.Свойство("Период") Тогда
			ПараметрыВыбора = ПараметрыВыбораПериодаНастройки(
				Параметр.Период,
				ИмяРаздела,
				НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
			Если ПараметрыВыбора.НачалоПериода < ЭтотОбъект[ИмяРаздела].Период
				Или (ПараметрыВыбора.НачалоПериода > ЭтотОбъект[ИмяРаздела].Период
					И ПараметрыВыбора.НачалоПериода >= ТекущаяДата) Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяСобытия = "Запись_НастройкиСистемыНалогообложения" Тогда
			Разворачивать = ПолныйСписок;
			ПрочитатьНастройкиОрганизации();
			Разворачивать = Ложь;
		Иначе
			ПеречитатьНастройкиНаТекущуюДату(ИмяРаздела);
			Если ИмяРаздела = "НастройкиУчетаНДС" Тогда  
				ОрганизацияВелаРаздельныйУчет = ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
					ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года
					ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_Организации" Тогда
		
		// При записи новой организации необходимо добавить ее в список доступных организаций.
		ОбработкаОповещенияЗаписьОрганизации(Параметр, Источник);
		
	ИначеЕсли ИмяСобытия = "ИзменениеПатента" Тогда
		
		Если Организация = Параметр.Организация Тогда
			СписокПатентов = НастройкиУчетаУСНФормыВызовСервера.СписокПатентов(Организация, НастройкиУчетаУСН.Период);
			НастройкиУчетаУСНФормыКлиентСервер.УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзмененоСостояниеИнтеграцииСПлатформойСамозанятые" Тогда
		
		Если Организация = Параметр Тогда
			ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
		КонецЕсли
		
	ИначеЕсли ИмяСобытия = "ИнтеграцияАУСН_ИзмененоСостояние" Тогда
		
		Если Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация") Тогда
			ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
		КонецЕсли
		
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет"
		И Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Владелец") Тогда
		
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
		
	ИначеЕсли ИмяСобытия = "ИзменениеСтавокУСННалогиИОтчеты" Тогда 
		
		ОбработатьИзменениеСтавокУСН(Параметр);
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам" Тогда
		
		Если Параметр.Организация = Организация И Параметр.Льгота = СтавкиНалогаНаИмущество.ОснованиеЛьготы Тогда
			УстановитьТекстПодсказкиЛьготыПоНалогуНаИмущество();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ОформленыПодписки" И ТарификацияБПВызовСервера.ОтображатьБаннерОжиданияОплатыТарифа() Тогда
		
		Элементы.ГруппаОжиданиеОплатыТарифа.Видимость = Истина;
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПроверитьОбновлениеТарифицируемыхУслуг", 
			ТарификацияБПКлиент.ИнтервалПервойПроверкиОплатыТарифа(),
			Истина);
			
	ИначеЕсли ИмяСобытия = "ИзмененыЗаписиРегистраМетодовОпределенияПрямыхРасходовНУ" Тогда
		
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьЗаголовокПереченьПрямыхЗатрат", 1, Истина);
		
	ИначеЕсли ИмяСобытия = "Запись_НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС" Тогда
		
		ПеречитатьНастройкиНаТекущуюДату("НастройкиУчетаСтраховыхВзносовИП");
		
	ИначеЕсли ИмяСобытия = "ЗакрытьПодчиненныеФормыОрганизации" И ИдентификаторФормыВладельца = Параметр Тогда
		ЗакрытьФорму = Истина;
		СнятьБлокировкуСРегистраНалогиОтчеты(Организация, ИдентификаторФормыВладельца);
		Если Не ЭтоМобильныйКлиент Тогда
			Закрыть();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ПростойИнтерфейс_ФункциональностьИзменение"
		Или (ИмяСобытия = "Запись_НаборКонстант" И Источник = "ВестиУчетРасчетовСКонтрагентами") Тогда
		РежимБезЗакрывающихДокументов = РежимБезЗакрывающихДокументов();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_СистемаНалогообложения Тогда
		
		Если Не ЗначениеЗаполнено(ДатаИзменения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Применяется с'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "ДатаИзменения", Отказ);
		КонецЕсли;
		
		ДатаНачалаПрименения = Перечисления.СистемыНалогообложения.ДатаНачалаПрименения(
			НастройкиСистемыНалогообложения.СистемаНалогообложения, НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент);
		Если ДатаНачалаПрименения <> Неопределено И ДатаИзменения < ДатаНачалаПрименения Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Система налогообложения ""%1"" применяется с %2'"),
				НастройкиСистемыНалогообложения.СистемаНалогообложения,
				Формат(ДатаНачалаПрименения, "ДЛФ=D"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "ДатаИзменения", Отказ);
		КонецЕсли;
		
		НастройкиСистемыНалогообложенияФормы.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, "СистемаНалогообложения");
	ИначеЕсли Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_НалогНаПрибыль Тогда
		НастройкиСистемыНалогообложенияФормы.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, "НалогНаПрибыль");
	КонецЕсли;
	
	Если Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_УСН Тогда
		НастройкиУчетаУСНФормы.ПроверитьЗаполнениеНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	КонецЕсли;
	
	Если Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_СтраховыеВзносы Тогда
		
		// Проверим заполнение надбавки/скидки, если установлен соответствующий переключатель.
		Если ПрименяютсяНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев = 1
			И НЕ ЗначениеЗаполнено(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Надбавка) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, "Надбавка");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Надбавка", , Отказ);
		КонецЕсли;

		Если ПрименяютсяНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев = 2
			И НЕ ЗначениеЗаполнено(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Скидка) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, "Скидка");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Скидка", , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_СтраховыеВзносы_Предприниматель Тогда
		
		Если НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС
			И УчетСтраховыхВзносовИПКлиентСервер.ЭтоСтраховойСлучайНаУплатуБольничного(НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай)
			И Не УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(ДатаИзменения) Тогда
			
			Если ЭтоИП Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В %1 г. не осуществляется страхование по выбранному случаю.'"),
					Формат(ДатаИзменения, "ДФ=yyyy"));
				ПолеОшибки = "НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай";
			Иначе
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В %1 г. для самозанятого без статуса ИП социальное страхование не осуществляется.'"),
					Формат(ДатаИзменения, "ДФ=yyyy"));
				ПолеОшибки = "НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС";
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПолеОшибки, , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
	
	Если НавигационнаяСсылкаФорматированнойСтроки = ТарификацияБПКлиентСервер.ИмяДействияОплатаСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		Если ЭтоАдресВременногоХранилища(АдресЗначенийТарифицируемыхОпций) Тогда
			УдалитьИзВременногоХранилища(АдресЗначенийТарифицируемыхОпций);
		КонецЕсли;
		АдресЗначенийТарифицируемыхОпций = ТарификацияБПКлиент.ОткрытьФормуОплатыСервиса(УникальныйИдентификатор);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиВНастройкиОрганизации" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Ключ", Организация);
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиВВводОстатковПриСменеОбъектаУСН" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Организация);
		ОткрытьФорму("Обработка.ВводОстатковПриИзмененииОбъектаУСН.Форма.Форма", ПараметрыФормы, ЭтотОбъект);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиВПомощникПоПереходуНаУСН" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Организация);
		ОткрытьФорму("Обработка.ПомощникПоПереходуНаУСН.Форма.Форма", ПараметрыФормы, ЭтотОбъект);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПомощникПоПереходуНаОСНО" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Организация);
		ОткрытьФорму("Обработка.ПомощникПереходаСУСНнаОСНО.Форма.Форма", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередСменойОрганизации", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ПрочитатьНастройкиОрганизации();
	КонецЕсли;
	
	ЗаполнитьНастройкиПоАкцизам();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор); // Все разблокируем
	
	ЗаполнитьСведенияОбОрганизации();
	
	Если ПустаяСтрока(РезультатБлокировки) Тогда
		ПодготовитьФормуНастройкиСписка();
	КонецЕсли;
	
	Параметры.Организация = Организация;
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОрганизация = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация,
		"ГоловнаяОрганизация, КодНалоговогоОргана, КодОКВЭД2, НаименованиеОКВЭД2");
	ГоловнаяОрганизация = СведенияОрганизация.ГоловнаяОрганизация;
	КодРегиона = Лев(СведенияОрганизация.КодНалоговогоОргана, 2);
	КодОКВЭД2 = СведенияОрганизация.КодОКВЭД2;
	НаименованиеОКВЭД2 = СведенияОрганизация.НаименованиеОКВЭД2;
	ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	ЗарегистрированВКачествеИП =
		Организация.СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.ИндивидуальныйПредприниматель;
	
	ЕстьПравоНаПродлениеСроковУСНПостановление512 = ОснованияПродленияСроковНалоговОтчетов.ОрганизацияИмеетПравоНаПереносСроковПоПостановлению512(КодОКВЭД2);
	ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П1 = ОснованияПродленияСроковНалоговОтчетов.ОрганизацияИмеетПравоНаПереносСроковПоПостановлению776П1(КодОКВЭД2);
	ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П2 = ОснованияПродленияСроковНалоговОтчетов.ОрганизацияИмеетПравоНаПереносСроковПоПостановлению776П2(КодОКВЭД2);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; // Незачем очищать
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияПриИзменении(Элемент)
	
	ИмяРегистра = ИмяРегистра(Элемент.Имя);
	ПараметрыДатыИзменения = ПредставленияДатыИзменения(
		ДатаИзменения,
		ИмяРегистра,
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыДатыИзменения);
	
	Если НастройкиВзносовИП_ПФР_ФФОМСИзменены Тогда
		ДоступныеТарифы = ДанныеВыбораТарифаВзносовИП_ПФР_ФФОМС(
			ДатаИзменения);
		Если ДоступныеТарифы.НайтиПоЗначению(НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф) = Неопределено Тогда
			НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф = ДоступныеТарифы[0].Значение;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьРеквизитыФормыДляСамозанятых();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИмяРегистра = ИмяРегистра(ТекущийЭлемент.Имя);
	
	ПараметрыВыбора = ПараметрыВыбораПериодаНастройки(
		ДатаИзменения,
		ИмяРегистра,
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
	
	Если ПараметрыВыбора.ИмяФормы <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму(
			СтрШаблон("ОбщаяФорма.%1", ПараметрыВыбора.ИмяФормы),
			ПараметрыВыбора,
			ЭтотОбъект, , , ,
			Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтотОбъект, ИмяРегистра));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияСтрокойОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияЧисломПриИзменении(Элемент)
	
	ДатаИзменения = Дата(ДатаИзмененияЧислом, 1, 1);
	ЗаполнитьРеквизитыФормыДляСамозанятых();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастройкаПодсказкиИзмененияВСистемеНалогообложения(Форма)
	
	Элементы = Форма.Элементы;
	Организация = Форма.Организация;
	
	Настройки = НастройкиСистемыНалогообложенияФормыВызовСервера.НастройкиДляБаннераСменыОбъектаУСН(Организация);
	Если ЗначениеЗаполнено(Настройки) И Настройки.Напоминать Тогда
		НастройкаПодсказкиВводОстатковУСН(Элементы, Настройки);
	Иначе
		Настройки = НастройкиСистемыНалогообложенияФормыВызовСервера.НастройкиДляБаннераПереходСУСННаОСНО(Организация);
		НастройкаПодсказкиПомощникПоПереходуСУСННаОСНО(Элементы, Настройки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастройкаПодсказкиПомощникПоПереходуСУСННаОСНО(Элементы, Настройки)
	
	ПоказыватьБаннер = Настройки.ПоказыватьБаннер;
	Если Не ПоказыватьБаннер Тогда
		Элементы.ГруппаИзмененияВСистемеНалогообложения.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаИзмененияВСистемеНалогообложения.Видимость = ПоказыватьБаннер;
	ТекстПояснения = НСтр(
		"ru='В %1 году изменилась система налогообложения с УСН на Общую. Для завершения перехода воспользуйтесь '");
	
	Элементы.ПодсказкаИзмененияВСистемеНалогообложения.Заголовок = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(СтрШаблон(ТекстПояснения, Настройки.ГодПерехода)),
		Новый ФорматированнаяСтрока(НСтр("ru='Помощником по переходу с УСН на ОСНО'"), , , ,
		"ПомощникПоПереходуНаОСНО"));
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовТаблицФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.Служебный Тогда
		Элементы.Список.ОтборСтрок = Новый ФиксированнаяСтруктура("Служебный", Ложь);
		ПолныйСписок = Истина;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Идентификатор = ТекущиеДанные.Идентификатор;
	ИмяСтраницы   = "Страница_" + Идентификатор;
	
	Если ТекущиеДанные.Выделен Тогда
		ИдентификаторСтроки = ТекущиеДанные.ПолучитьИдентификатор();
		СтрокаСпискаПоИдентификатору = Список.НайтиПоИдентификатору(ИдентификаторСтроки);
		СтрокаСпискаПоИдентификатору.Выделен = Ложь;
	КонецЕсли;
	
	НеиспользуемаяНастройка = СписокНеиспользуемыхНастроек.НайтиПоЗначению(Идентификатор) <> Неопределено;
	
	Если Элементы.Найти(ИмяСтраницы) = Неопределено ИЛИ НеиспользуемаяНастройка Тогда
		Заголовок_ = ?(ЗначениеЗаполнено(ТекущиеДанные.ПолноеНаименование), ТекущиеДанные.ПолноеНаименование, ТекущиеДанные.Наименование);
		Если ТекущиеДанные.ЕстьПодстроки Тогда
			ТекущаяСтраница = Элементы.Страница_Таблица;
		Иначе
			ТекущаяСтраница = Элементы.Страница_Флажок;
			Флажок = ТекущиеДанные.Включен;
			Элементы.Флажок.ТолькоПросмотр = НЕ ТекущиеДанные.ВключаетсяПользователем;
		КонецЕсли;
		Элементы.СписокПодчиненный.ОтборСтрок = Новый ФиксированнаяСтруктура("Родитель", ТекущиеДанные.Ссылка);
	Иначе
		ТекущаяСтраница = Элементы[ИмяСтраницы];
	КонецЕсли;
	
	Элементы.СтраницыНастроек.ТекущаяСтраница = ТекущаяСтраница;
	
	Если ИмяСтраницы = "Страница_НДС" Тогда
		
		// Переменная КлассифицироватьНоменклатуруПоОперациям0 используется для
		// выбора переключателей режимов "Указывать коды операций вручную" и 
		// Установить соответствие между отгружаемыми ценностями и кодами операций
		
		КлассифицироватьНоменклатуруПоОперациям0 = УчетНДСВызовСервера.КлассифицироватьНоменклатуруПоОперациям0(,Организация);
		
		Элементы.ДекорацияПомощникПодбораКода.Видимость = ПоказыватьПомощникПодбораОпераций0(Организация);
	
	ИначеЕсли ИмяСтраницы = "Страница_СтатистическаяОтчетность" Тогда
		
		Элементы.СписокФормыСтатистики.ОтборСтрок = Новый ФиксированнаяСтруктура("Родитель, Включен", ТекущиеДанные.Ссылка, Истина);
		НачатьПолучениеСпискаСтатФормИзСервиса();
		
	КонецЕсли;
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.СписокНастроекМК.Скрыть();
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Если ЭтоМобильныйКлиент Тогда
		Отказ = Истина;
		
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Элементы.СписокПодчиненный.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	
	РезультатДетальнойНастройки = РезультатЗакрытия;
	
	Если ТипЗнч(РезультатДетальнойНастройки) = Тип("Массив") Тогда
		ОбработатьРезультатДетальнойНастройки(ВыбраннаяСтрока, РезультатДетальнойНастройки);
	КонецЕсли;
	
	Строка = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьСписокНастройкиНалоговОтчетов(Строка);
	ОповеститьОбИзмененииНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПодчиненный

&НаКлиенте
Процедура Список_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Лев(Строка(Поле.Имя), 18) = "ДетальнаяНастройка" Тогда
		
		Строка = СписокПодчиненный.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если Строка = Неопределено Или Не Строка.ЕстьДетальнаяНастройка Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		Если Строка.Ключ <> "" Тогда
			Родитель = Список.НайтиСтроки(Новый Структура("Ссылка", Строка.Родитель));
			ПредметНастройки = Строка.Ключ;
			НалогОтчет = Родитель[0].Наименование + " (" + Строка.Наименование + ")";
		ИначеЕсли ТипЗнч(Строка.Ссылка) = Тип("ПеречислениеСсылка.ВидыПодакцизныхТоваров") Тогда
			ПредметНастройки = Строка.Ссылка;
			НалогОтчет       = Строка.Родитель;
		Иначе
			ПредметНастройки = Строка.Ссылка;
			НалогОтчет       = Строка.Родитель;
		КонецЕсли;
		ПараметрыФормы.Вставить("ПредметНастройки",                 ПредметНастройки);
		ПараметрыФормы.Вставить("НалогОтчет",                       НалогОтчет);
		ПараметрыФормы.Вставить("АдресЗначенияДетальнойНастройки",  АдресЗначенияДетальнойНастройки);
		ПараметрыФормы.Вставить("АдресПараметрыДетальнойНастройки", АдресПараметрыДетальнойНастройки);
		ПараметрыФормы.Вставить("ТолькоПросмотр",                   Элементы.СписокПодчиненный.ТолькоПросмотр);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ВыбраннаяСтрока",   ВыбраннаяСтрока);
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("СписокВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("РегистрСведений.НалогиОтчеты.Форма.ДетальнаяНастройка", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПодчиненныйВключенПриИзменении(Элемент)
	
	Строка = СписокПодчиненный.НайтиПоИдентификатору(Элементы.СписокПодчиненный.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Включен = Ложь;
	
	ОтборСтрок = Новый Структура("Родитель, Включен", Строка.Родитель, Истина);
	Строки = СписокПодчиненный.НайтиСтроки(ОтборСтрок);
	Если Строки.Количество() > 0 Тогда
		Включен = Истина
	КонецЕсли;
	
	ОтборСтрок = Новый Структура("Ссылка", Строка.Родитель);
	Строки = Список.НайтиСтроки(ОтборСтрок);
	Если Строки.Количество() > 0 Тогда
		Строки[0].Включен = Включен;
		СохранитьСписокНастройкиНалоговОтчетов(Строки[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПанелей

#Область ОбработчикиПанели_СистемаНалогообложения

&НаКлиенте
Процедура СистемаНалогообложенияПредставлениеПриИзменении(Элемент)
	
	НастройкиСистемыНалогообложенияФормыКлиент.СистемаНалогообложенияПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	ПараметрыПериодаСистемыНалогообложения =
		НастройкиСистемыНалогообложенияФормыВызовСервера.ПараметрыПериодаСистемыНалогообложения(
			НастройкиСистемыНалогообложения.Организация, НастройкиСистемыНалогообложения.СистемаНалогообложения);
	
	ИмяРаздела = "НастройкиСистемыНалогообложения";
	Если Не ЭтоЮрЛицо Тогда
		Если ЗарегистрированВКачествеИП Или СистемаНалогообложенияПредставление = 0 Тогда
			ОтобразитьПериоды(ИмяРаздела);
		Иначе
			ОтобразитьИсторию(ИмяРаздела);
		КонецЕсли;
	Иначе
		ОтобразитьПериоды(ИмяРаздела);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект, ИмяРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоложенияПереходногоПериодаУСНПриИзменении(Элемент)
	
	НастройкиСистемыНалогообложенияФормыКлиент.ПоложенияПереходногоПериодаУСНПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиСистемыНалогообложения");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСистемыНалогообложенияПриИзменении(Элемент)
	
	ОтобразитьПериоды("НастройкиСистемыНалогообложения");
	
КонецПроцедуры 

&НаКлиенте
Процедура НастройкиСистемыНалогообложенияПлательщикНДСПрименяющийУСНПриИзменении(Элемент)
	
	НастройкиСистемыНалогообложенияФормыКлиент.ПлательщикНДСПрименяющийУСНПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиСистемыНалогообложения");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСистемыНалогообложенияПлательщикЕНППриИзменении(Элемент)
	
	Если НастройкиСистемыНалогообложения.ПлательщикЕНП
		И НастройкиСистемыНалогообложения.Период < ДатаНачалаПримененияЕНП Тогда
		ДатаИзменения = ДатаНачалаПримененияЕНП;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПредставленияДатыИзменения(ДатаИзменения, "НастройкиСистемыНалогообложения"));
	КонецЕсли;
	
	ОтобразитьПериоды("НастройкиСистемыНалогообложения");
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьПодсказкуПомощникПоПереходуНаУСННажатие(Элемент)
	
	ЗакрытьПодсказкуПомощникПоПереходуНаУСННаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_НалогНаПрибыль

&НаКлиенте
Процедура СтавкаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтобразитьПериоды("СтраховыеВзносы");
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименяютсяРазныеСтавкиНалогаНаПрибыльПриИзменении(Элемент)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.ПоложенияПереходногоПериодаУСНПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНалогаНаПрибыль");
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкиНалогаНаПрибыльВБюджетСубъектовРФ(Команда)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.СтавкиНалогаНаПрибыльВБюджетСубъектовРФ(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатурныеГруппыРеализацииПродукцииУслуг(Команда)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.НоменклатурныеГруппыРеализацииПродукцииУслуг(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереченьПрямыхРасходов(Команда)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.ПереченьПрямыхРасходов(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУчетаНалогНаПрибыльПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаНалогаНаПрибыль");
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПримененияИнвестиционногоВычетаПриИзменении(Элемент)
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиент.ВариантПримененияИнвестиционногоВычетаПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаНалогаНаПрибыль");
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУплатыНалогаПриИзменении(Элемент)
	
	НастройкиУчетаНалогаНаПрибыль.УплачиватьНалогПоГруппамОбособленныхПодразделений = (ПорядокПодачиДекларации = 1);
	Элементы.НалоговыеОрганыГруппОбособленныхПодразделений.Доступность = НастройкиУчетаНалогаНаПрибыль.УплачиватьНалогПоГруппамОбособленныхПодразделений;
	ОтобразитьПериоды("НастройкиУчетаНалогаНаПрибыль");
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыеОрганыГруппОбособленныхПодразделений(Команда)
	
	УчетОбособленныхПодразделенийКлиент.НалоговыеОрганыГруппОбособленныхПодразделений(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПрямыхРасходовПриИзменении(Элемент)
	
	// ВариантНастройкиПрямыхРасходов - Число(1,0), принимает значения 0 и 1.
	// Неявное преобразование к типу Булево.
	НастройкиУчетаНалогаНаПрибыль.ФормироватьСтоимостьПоПравиламБУ = ВариантНастройкиПрямыхРасходов;
	
	Элементы.ПереченьПрямыхРасходов.Доступность = (ВариантНастройкиПрямыхРасходов = 0);
	
	ОтобразитьПериоды("НастройкиУчетаНалогаНаПрибыль");
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантУчетаКурсовыхРазниц2022СписокПриИзменении(Элемент)
	
	ЗаписатьВариантУчетаКурсовыхРазниц2022(ВариантУчетаКурсовыхРазниц2022, Организация);
	Если ВариантУчетаКурсовыхРазниц2022 <> "Независимо"
		И ОтложитьОтрицательныеКурсовыеРазницы2022 Тогда
		ОтложитьОтрицательныеКурсовыеРазницы2022 = Ложь;
		ЗаписатьОтложитьОтрицательныеКурсовыеРазницы2022(ОтложитьОтрицательныеКурсовыеРазницы2022, Организация);
	КонецЕсли; 
	УправлениеФормой(ЭтотОбъект, "НастройкиУчетаНалогаНаПрибыль");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложитьОтрицательныеКурсовыеРазницы2022ПриИзменении(Элемент)
	
	ЗаписатьОтложитьОтрицательныеКурсовыеРазницы2022(ОтложитьОтрицательныеКурсовыеРазницы2022, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательОпределенияДолиПрибылиПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаНалогаНаПрибыль");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_НДФЛ_Предприниматель

&НаКлиенте
Процедура ВестиУчетПоВидамДеятельностиПриИзменении(Элемент)
	
	НастройкиУчетаНДФЛФормыКлиент.ВестиУчетПоВидамДеятельностиПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНДФЛ");
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыВключаютсяВДоходыВПериодеПолученияПриИзменении(Элемент)
	
	НастройкиУчетаНДФЛФормыКлиент.АвансыВключаютсяВДоходыВПериодеПолученияПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНДФЛ");
	
КонецПроцедуры

&НаКлиенте
Процедура ДляПризнанияРасходовТребуетсяПолучениеДоходаПриИзменении(Элемент)
	
	НастройкиУчетаНДФЛФормыКлиент.ДляПризнанияРасходовТребуетсяПолучениеДоходаПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНДФЛ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнаватьРасходыПоОперациямПрошлогоГодаПриИзменении(Элемент)
	
	НастройкиУчетаНДФЛФормыКлиент.ПризнаватьРасходыПоОперациямПрошлогоГода(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНДФЛ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПризнанияДоходаОтКомиссионераИППриИзменении(Элемент)
	
	ОтобразитьПериоды("НастройкиУчетаНДФЛ");
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура НастройкиУчетаНДФЛПриИзменении(Элемент)
	
	ОтобразитьПериоды("НастройкиУчетаНДФЛ");
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеВидыДеятельности(Команда)
	
	НастройкиУчетаНДФЛФормыКлиент.ВсеВидыДеятельности(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_НДС

&НаКлиенте
Процедура СложныйУчетНДСПриИзменении(Элемент)
	
	НастройкиУчетаНДСФормыКлиент.СложныйУчетНДСПриИзменении(ЭтотОбъект, Элемент);

	Если ИнтерфейсТаксиПростой Тогда
		ОтобразитьБаннерПереходНаПолныйИнтерфейс();
	Иначе
		ОтобразитьБаннерНеобходимоСформироватьОстатки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УпрощенныйУчетНДСПриИзменении(Элемент)
	
	НастройкиУчетаНДСФормыКлиент.УпрощенныйУчетНДСПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУчетаНДСПриИзменении(Элемент)
	
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролироватьДолюВычетаПриИзменении(Элемент)
	
	НастройкиУчетаНДСФормыКлиент.КонтролироватьДолюВычетаПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура БезопаснаяДоляВычетаПриИзменении(Элемент)
	
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура РучнойВводКодовОпераций0ПриИзменении(Элемент)
	
	НастройкиУчетаНДС.КлассифицироватьНоменклатуруПоОперациям0 = Ложь;
	ИзменениеДеятельностиНДС0 = Истина;
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура КлассифицироватьНоменклатуруПоОперациям0ПриИзменении(Элемент)
	
	Если НЕ НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года Тогда
		
		НастройкиУчетаНДС.КлассифицироватьНоменклатуруПоОперациям0 = Истина;
		ИзменениеДеятельностиНДС0 = Истина;
		Модифицированность = Истина;
		ОтобразитьПериоды("НастройкиУчетаНДС");
		
	Иначе
		ТекстВопроса = НСтр("ru='Для применения настройки необходимо сначала включить раздельный учет входящего НДС по способам учета.
			|
			|Включить раздельный учет входящего НДС по способам учета?'"); 
		Оповещение = Новый ОписаниеОповещения("ВопросВключитьРаздельныйНДСПоСпособамУчетаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоУмолчаниюКодОперации0НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",          "Выбор кода операции НДС 0%");
	ПараметрыФормы.Вставить("ТаблицаЗначений",    ТаблицаВыбора);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура("Код", ПоУмолчаниюКодОперации0));
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиКодОперации0Завершение", ЭтотОбъект, Новый Структура);
		
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы",
	ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения,);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПомощникПодбораКодаНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", НастройкиУчетаНДС.Организация);
	НачалоТекущегоГода = НачалоГода(ТекущаяДата);
	НачалоГодаПериода = НачалоГода(НастройкиУчетаНДС.Период);
	Если НачалоГодаПериода < НачалоТекущегоГода Тогда
		ПараметрыФормы.Вставить("НачалоПериода", НачалоГодаПериода);
	Иначе
		ПараметрыФормы.Вставить("НачалоПериода", НачалоТекущегоГода);
	КонецЕсли;
	ПараметрыФормы.Вставить("КонецПериода", КонецГода(ТекущаяДата));
	ОткрытьФорму("Справочник.Операции0.Форма.ПодборДляНоменклатуры",ПараметрыФормы,ЭтотОбъект,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоУмолчаниюКодОперации0ПриИзменении(Элемент)
	НастройкиУчетаНДС.КодОперации0 = ПоУмолчаниюКодОперации0;
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаНДС");
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриУСНПриИзменении(Элемент)
	
	НастройкиУчетаНДСФормыКлиент.СтавкаНДСПриУСНПриИзменении(ЭтотОбъект, Элемент);
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_УСН

&НаКлиенте
Процедура СтавкаНалогаУСНИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Модифицированность   = Истина;
	
	ТекущаяСтавкаНалога = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Текст);
	
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаПриИзменении(Элемент)
	
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОснованиеЛьготыУСНОбработкаНавигационнойСсылки(Элемент, ТекстСсылки, СтандартнаяОбработка)
	
	НастройкиУчетаУСНФормыКлиент.ДекорацияОснованиеЛьготыУСНОбработкаНавигационнойСсылки(ЭтотОбъект, Элемент, ТекстСсылки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыНомерСтатьиПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ОснованиеЛьготыПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыНомерСтатьиИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПунктПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ОснованиеЛьготыПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПунктИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПодпунктПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ОснованиеЛьготыПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПодпунктИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Модифицированность = Истина;
	
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОснованиеЛьготыПримерЗаполненияНажатие(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ДекорацияОснованиеЛьготыПримерЗаполненияНажатие(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокОтраженияАвансаПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ПорядокОтраженияАвансаПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыеКаникулыПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.НалоговыеКаникулыПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередачаВПроизводствоПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ПередачаВПроизводствоПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшатьНаНЗППриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.УменьшатьНаНЗППриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура РеализацияПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.РеализацияПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеДоходаПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ПолучениеДоходаПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнаниеРасходаПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ПризнаниеРасходаПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ДопРасходыПризнаниеРасходаПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ДопРасходыПризнаниеРасходаПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаможенныеПлатежиПризнаниеРасходаПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ТаможенныеПлатежиПризнаниеРасходаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУчетаУСНПриИзменении(Элемент)
	
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтавкиУСНВВашемРегионеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОтчета = РегиональныеСтавкиНалоговКлиент.ПараметрыОтчетаСтавкиНалогаВВашемРегионе();
	ПараметрыОтчета.Налог = "УСН";
	ПараметрыОтчета.Режим = "НалогиИОтчеты";
	ПараметрыОтчета.КодРегиона = КодРегиона;
	ПараметрыОтчета.КодВидаДеятельности = КодОКВЭД2;
	ПараметрыОтчета.НаименованиеВидаДеятельности = НаименованиеОКВЭД2;
	ПараметрыОтчета.ЭтоЮрЛицо = ЭтоЮрЛицо;
	ПараметрыОтчета.ПрименяетсяУСНДоходы = ПрименяетсяУСНДоходы;
	ПараметрыОтчета.ПрименяетсяУСНДоходыРасходы = ПрименяетсяУСНДоходыМинусРасходы;
	ПараметрыОтчета.Период = ТекущаяДата;
	ПараметрыОтчета.ПоказыватьПрименить = ПравоРедактированияНастроек;
	
	РегиональныеСтавкиНалоговКлиент.ОбработатьНавигационнуюСсылкуСтавкиВВашемРегионе(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки,
		ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаУплатыУСНЗа2019ГодПриИзменении(Элемент)
	НастройкиУчетаВызовСервера.ЗаписатьПродленныйСрокУплатыНалога(Организация, "ДатаУплатыУСНЗа2019Год", ДатаУплатыУСНЗа2019Год);
КонецПроцедуры

&НаКлиенте
Процедура ДатаУплатыУСНЗа1Кв2020ПриИзменении(Элемент)
	НастройкиУчетаВызовСервера.ЗаписатьПродленныйСрокУплатыНалога(Организация, "ДатаУплатыУСНЗа1Кв2020", ДатаУплатыУСНЗа1Кв2020);
КонецПроцедуры

&НаКлиенте
Процедура ДатаУплатыУСНЗа2Кв2020ПриИзменении(Элемент)
	НастройкиУчетаВызовСервера.ЗаписатьПродленныйСрокУплатыНалога(Организация, "ДатаУплатыУСНЗа2Кв2020", ДатаУплатыУСНЗа2Кв2020);
КонецПроцедуры

&НаКлиенте
Процедура ДатаУплатыУСНЗа3Кв2020ПриИзменении(Элемент)
	НастройкиУчетаВызовСервера.ЗаписатьПродленныйСрокУплатыНалога(Организация, "ДатаУплатыУСНЗа3Кв2020", ДатаУплатыУСНЗа3Кв2020);
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПризнанияДоходаОтКомиссионераПриИзменении(Элемент)
	
	НастройкиУчетаУСНФормыКлиент.ПорядокПризнанияДоходаОтКомиссионераПриИзменении(ЭтотОбъект);
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастройкаПодсказкиВводОстатковУСН(Элементы, Переход)
	
	Если НЕ ЗначениеЗаполнено(Переход) Тогда
		Элементы.ГруппаИзмененияВСистемеНалогообложения.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаИзмененияВСистемеНалогообложения.Видимость = Переход.Напоминать;
	
	Если НЕ Переход.Напоминать Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущийОбъект = ?(НЕ Переход.ОбъектДоход, НСтр("ru='""доходы""'"), НСтр("ru='""доходы минус расходы""'"));
	ТекущийОбъект    = ?(Переход.ОбъектДоход, НСтр("ru='""доходы""'"), НСтр("ru='""доходы минус расходы""'"));
	
	ШаблонПоясненияСтрока = НСтр(
	"ru='В %1 году изменился объект налогообложения УСН с %2 на 
	|%3. Для завершения перехода воспользуйтесь помощником
	|'");
	
	Элементы.ПодсказкаИзмененияВСистемеНалогообложения.Заголовок = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(СтрШаблон(ШаблонПоясненияСтрока, Переход.Год, ПредыдущийОбъект, ТекущийОбъект)),
		Новый ФорматированнаяСтрока(НСтр("ru='Ввод остатков при изменении объекта УСН'"), , , , "ПерейтиВВводОстатковПриСменеОбъектаУСН"));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_СтраховыеВзносы_Предприниматель

&НаКлиенте
Процедура УплачиватьДобровольныеВзносыВФССПриИзменении(Элемент)
	
	НастройкиВзносовИП_ФССИзменены = Истина;
	ОтобразитьПериоды("НастройкиУчетаСтраховыхВзносовИП");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМСПлательщикСтраховыхВзносовПФР_ФФОМСПриИзменении(Элемент)
	
	НастройкиВзносовИП_ПФР_ФФОМСИзменены = Истина;
	Если Не НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.ПлательщикСтраховыхВзносовПФР_ФФОМС Тогда
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф = Неопределено;
	Иначе
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф =
			ПредопределенноеЗначение("Перечисление.ИПТарифыФиксированныхВзносов.Единый");
	КонецЕсли;
	
	ОтобразитьПериоды("НастройкиУчетаСтраховыхВзносовИП");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМСТарифПриИзменении(Элемент)
	
	НастройкиВзносовИП_ПФР_ФФОМСИзменены = Истина;
	
	ПараметрыПериода = ПараметрыВыбораПериодаНастройки(
		ДатаИзменения,
		"НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС",
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
	ДатаИзменения = ПараметрыПериода.НачалоПериода;
	
	ОтобразитьПериоды("НастройкиУчетаСтраховыхВзносовИП");
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховойСлучайВСвязиСМатеринствомПриИзменении(Элемент)
	
	НастройкиВзносовИП_ФССИзменены = Истина;
	ПриИзмененииСтраховогоСлучаяВСвязиСМатеринством();
	
	Если УчетСтраховыхВзносовИПКлиентСервер.ЭтоСтраховойСлучайНаУплатуБольничного(НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай)
		И ДатаИзменения < УчетСтраховыхВзносовИПКлиентСервер.НачалоДействияВыбораСуммыПоСоциальномуСтрахованию() Тогда
		
		ДатаИзменения = УчетСтраховыхВзносовИПКлиентСервер.НачалоДействияВыбораСуммыПоСоциальномуСтрахованию();
		ДатаИзмененияЧислом = Год(ДатаИзменения);
		ПредставленияДатыИзменения(ДатаИзменения, "НастройкиУчетаСтраховыхВзносовИП");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РайонныйКоэффициентИППриИзменении(Элемент)
	
	НастройкиВзносовИП_ФССИзменены = Истина;
	ОтобразитьПериоды("НастройкиУчетаСтраховыхВзносовИП");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУчетаСтраховыхВзносовИПСтраховаяСуммаПриИзменении(Элемент)
	
	НастройкиВзносовИП_ФССИзменены = Истина;
	ОтобразитьПериоды("НастройкиУчетаСтраховыхВзносовИП");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_СтраховыеВзносы

&НаКлиенте
Процедура СтраховыеВзносыПриИзменении(Элемент)
	
	Модифицированность   = Истина;
	ОтобразитьПериоды("СтраховыеВзносы");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУчетаСтраховыхВзносовПриИзменении(Элемент)
	
	Если НЕ НастройкиУчетаСтраховыхВзносов.ИспользуютсяРаботыСДосрочнойПенсией Тогда
		НастройкиУчетаСтраховыхВзносов.ПрименяютсяРезультатыСпециальнойОценкиУсловийТруда = Ложь;
	КонецЕсли;
	
	ЗаписатьИзмененияНепосредственно("НастройкиУчетаСтраховыхВзносов");
	ОбработатьИзменениеДополнительныеСтраховыеВзносы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименяетсяБезНадбавокСкидокНаСтрахованиеОтНесчастныхСлучаевПриИзменении(Элемент)

	ОбновитьНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев(ЭтотОбъект, Истина);
	ОтобразитьПериоды("СтраховыеВзносы");

КонецПроцедуры

&НаКлиенте
Процедура ПрименяетсяНадбавкаНаСтрахованиеОтНесчастныхСлучаевПриИзменении(Элемент)

	ОбновитьНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев(ЭтотОбъект, Истина);
	ОтобразитьПериоды("СтраховыеВзносы");

КонецПроцедуры

&НаКлиенте
Процедура СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевНадбавкаПриИзменении(Элемент)

	Модифицированность = Истина;
	ОтобразитьПериоды("СтраховыеВзносы");

КонецПроцедуры

&НаКлиенте
Процедура ПрименяетсяСкидкаНаСтрахованиеОтНесчастныхСлучаевПриИзменении(Элемент)

	ОбновитьНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев(ЭтотОбъект, Истина);
	ОтобразитьПериоды("СтраховыеВзносы");

КонецПроцедуры

&НаКлиенте
Процедура СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевСкидкаПриИзменении(Элемент)

	Модифицированность = Истина;
	ОтобразитьПериоды("СтраховыеВзносы");

КонецПроцедуры

&НаКлиенте
Процедура ПеречисляютсяДобровольныеСтраховыеВзносыПриИзменении(Элемент)
	
	Если Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ОбработатьИзменениеДобровольныеСтраховыеВзносы(ТекущиеДанные.Ссылка);
	СохранитьСписокНастройкиНалоговОтчетов(ТекущиеДанные);
	ОповеститьОбИзмененииНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПрименяютсяЛьготныеТарифыВПодразделенияхНажатие(Элемент)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("РедактированиеЛьготныхТарифовЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("Вид, Организация", "ЛьготныеТарифы", Организация);
	ОткрытьФорму("РегистрСведений.НастройкиУчетаЗарплаты.Форма.ФормаДополнительныхДанных", ПараметрыФормы, ЭтаФорма, , , , ОповещениеОЗакрытии);

КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаДолжностиНажатие(Элемент)
	
	ПараметрыДолжностей = Новый Структура("ИспользуетсяТрудФармацевтов, ИспользуетсяТрудЧленовЛетныхЭкипажей, ИспользуетсяТрудШахтеров", 
	                                      НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудФармацевтов,
	                                      НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудЧленовЛетныхЭкипажей,
	                                      НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудШахтеров);
										  
	ПараметрыФормы = Новый Структура("Вид, ПараметрыДолжностей", "Должности", ПараметрыДолжностей);
	ОткрытьФорму("РегистрСведений.НастройкиУчетаЗарплаты.Форма.ФормаДополнительныхДанных", ПараметрыФормы,ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_НалогНаИмущество

&НаКлиенте
Процедура КартинкаЗакрытьБаннер_НалогНаИмуществоНажатие(Элемент)
	СкрытьБаннерИзмененияПоНалогу(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаНаИмуществоПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаНаДвижимоеИмуществоПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
	
КонецПроцедуры

&НаКлиенте
Процедура СнижениеНалоговойСтавкиДвижимоеИмуществоПриИзменении(Элемент)
	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(ЭтотОбъект);
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПриИзменении(Элемент)
	
	ЗаполнитьПараметрыЛьготыПоНалогуНаИмущество();	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(ЭтотОбъект);
	Модифицированность = Истина;
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоДействияПриИзменении(Элемент)
	Модифицированность = Истина;
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеДействияПриИзменении(Элемент)
	Модифицированность = Истина;
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтавкиНалогаНаИмуществоФормыКлиент.ОснованиеЛьготыРасширеннаяПодсказкаОбработкаНавигационнойСсылки(
		ЭтотОбъект, "СтавкиНалогаНаИмущество", СтандартнаяОбработка);
			
КонецПроцедуры

&НаКлиенте
Процедура ОсвобождениеОтНалогообложенияДвижимогоИмуществаПриИзменении(Элемент)
	
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_ТранспортныйНалог

&НаКлиенте
Процедура КартинкаЗакрытьБаннер_ТранспортныйНалогНажатие(Элемент)
	СкрытьБаннерИзмененияПоНалогу(Элемент.Имя);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_ЗемельныйНалог

&НаКлиенте
Процедура КартинкаЗакрытьБаннер_ЗемельныйНалогНажатие(Элемент)
	СкрытьБаннерИзмененияПоНалогу(Элемент.Имя);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_СтатистическаяОтчетность

&НаКлиенте
Процедура СписокФормыСтатистикиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
	ОповещениеПослеВыбора = Новый ОписаниеОповещения("СписокФормыСтатистикиПослеВыбора", ЭтотОбъект);
	СписокВыбораФормСтатистики = СписокВыбораФормСтатистики();
	
	СтруктураПараметров = Новый Структура("Заголовок, Список, ПоказатьОтметкуЭлементов, Ширина",
		НСтр("ru = 'Добавить напоминания по:'"),
		СписокВыбораФормСтатистики(),
		Истина,
		50);
		
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораИзСписка", СтруктураПараметров, ЭтотОбъект,,,, ОповещениеПослеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФормыСтатистикиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Строка = СписокПодчиненный.НайтиПоИдентификатору(Элементы.СписокФормыСтатистики.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Строка.Включен = Ложь;
	Строка.ОтключенПользователем = ДоступнаЗагрузкаФормИзСервиса;
	Строка.ДобавленПользователем = Ложь;
	
	ОтборСтрок = Новый Структура("Родитель, Включен", Строка.Родитель, Истина);
	Строки = СписокПодчиненный.НайтиСтроки(ОтборСтрок);
	Включен = (Строки.Количество() > 0);
	
	ОтборСтрок = Новый Структура("Ссылка", Строка.Родитель);
	Строки = Список.НайтиСтроки(ОтборСтрок);
	Если Строки.Количество() > 0 Тогда
		Строки[0].Включен = Включен;
		СохранитьСписокНастройкиНалоговОтчетов(Строки[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПоДаннымРосстата(Команда)
	
	СтрокаЗадачи = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Отбор = Новый Структура("Родитель", СтрокаЗадачи.Ссылка);
	ЕстьИзменения = Ложь;
	
	Строки = СписокПодчиненный.НайтиСтроки(Отбор);
	Если Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Строки Цикл
		
		Если Строка.ПолученИзВнешнегоСервиса И Не Строка.Включен Тогда
			Строка.Включен = Истина;
			Строка.ОтключенПользователем = Ложь;
			Строка.ДобавленПользователем = Ложь;
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Если Не Строка.ПолученИзВнешнегоСервиса И Строка.Включен Тогда
			Строка.Включен = Ложь;
			Строка.ОтключенПользователем = Ложь;
			Строка.ДобавленПользователем = Ложь;
			ЕстьИзменения = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтборСтрок = Новый Структура("Родитель, Включен", СтрокаЗадачи.Ссылка, Истина);
	Строки = СписокПодчиненный.НайтиСтроки(ОтборСтрок);
	СтрокаЗадачи.Включен = (Строки.Количество() > 0);
	
	Если ЕстьИзменения Тогда
		СохранитьСписокНастройкиНалоговОтчетов(СтрокаЗадачи);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстБаннера_СтатистическаяОтчетностьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "ПодключитьИнтернетПоддержку" Тогда
		СтандартнаяОбработка = Ложь;
		ОповещениеПослеПодключения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЭтотОбъект);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОповещениеПослеПодключения, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

#Область ОбработчикиПанели_ТуристическийНалог

&НаКлиенте
Процедура НастройкиУчетаТуристическогоНалогаПриИзменении(Элемент)
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаТуристическогоНалога");
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьБаннер_ТуристическийНалогНажатие(Элемент)
	СкрытьБаннерИзмененияПоНалогу(Элемент.Имя);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиВспомогательныхПанелей_Таблица_Флажок

&НаКлиенте
Процедура ФлажокПриИзменении(Элемент)
	
	Строка = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Строка.Включен = Флажок;
	
	СохранитьСписокНастройкиНалоговОтчетов(Строка);
	ОповеститьОбИзмененииНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеВключить(Команда)
	
	Строка = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтборСтрок = Новый Структура("Родитель", Строка.Ссылка);
	ПодчиненныеСтроки = СписокПодчиненный.НайтиСтроки(ОтборСтрок);
	Для Каждого ПодчиненнаяСтрока ИЗ ПодчиненныеСтроки Цикл
		ПодчиненнаяСтрока.Включен = Истина;
	КонецЦикла;
	
	Строка = СписокПодчиненный.НайтиПоИдентификатору(Элементы.СписокПодчиненный.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтборСтрок = Новый Структура("Ссылка", Строка.Родитель);
	Строки = Список.НайтиСтроки(ОтборСтрок);
	Если Строки.Количество() > 0 Тогда
		Строки[0].Включен = Истина;
		СохранитьСписокНастройкиНалоговОтчетов(Строки[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеВыключить(Команда)
	
	Строка = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтборСтрок = Новый Структура("Родитель", Строка.Ссылка);
	ПодчиненныеСтроки = СписокПодчиненный.НайтиСтроки(ОтборСтрок);
	Для Каждого ПодчиненнаяСтрока ИЗ ПодчиненныеСтроки Цикл
		ПодчиненнаяСтрока.Включен = Ложь;
	КонецЦикла;
	
	Строка = СписокПодчиненный.НайтиПоИдентификатору(Элементы.СписокПодчиненный.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтборСтрок = Новый Структура("Ссылка", Строка.Родитель);
	Строки = Список.НайтиСтроки(ОтборСтрок);
	Если Строки.Количество() > 0 Тогда
		Строки[0].Включен = Ложь;
		СохранитьСписокНастройкиНалоговОтчетов(Строки[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_НДФЛ

&НаКлиенте
Процедура УчетнаяПолитикаПоНДФЛПриИзменении(Элемент)
	
	ЗаписатьИзмененияНепосредственно("УчетнаяПолитикаПоНДФЛ");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_Антикризис

&НаКлиенте
Процедура КартинкаЗакрытьПодсказкуПостановление776Нажатие(Элемент)
	ЗакрытьПодсказкуПостановление776НаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьПодсказкуПостановление512Нажатие(Элемент)
	ЗакрытьПодсказкуПостановление512НаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Постановление776ПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИЗаполнитьРегулярныеЗадачи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Постановление512ПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИЗаполнитьРегулярныеЗадачи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура МерыУстойчивогоРазвитияПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИЗаполнитьРегулярныеЗадачи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботаетВНерабочиеДниПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИЗаполнитьРегулярныеЗадачи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура АнтикризисГрафикРаботыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Ключ, ПростаяНастройка", ПолучитьГрафикРаботыОрганизации(Организация), Истина);
	ОткрытьФорму("Справочник.Календари.Форма.ФормаЭлементаПростая", ПараметрыФормы,  ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиСрокиСдачиОтчетовВПФРПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИЗаполнитьРегулярныеЗадачи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьОтнесенаКПострадавшимОтКоронавирусаПриИзменении(Элемент)
	
	ДействияВручную = ЗаписатьИзмененияДеятельностьОтнесенаКПострадавшимОтКоронавируса();
	
	ПоказатьДействияВручную = ЗначениеЗаполнено(ДействияВручную);
	Элементы.ДеятельностьОтнесенаКПострадавшимОтКоронавирусаДействияПослеВключения.Видимость = ПоказатьДействияВручную;
	Если ПоказатьДействияВручную Тогда
		
		РазделительПредложений = НСтр("ru = '.'") + Символы.ПС;
		
		КоличествоСообщений = ДействияВручную.Количество();
		// Разделители должны разделять предложения, то есть, после последнего он не нужен.
		// Поэтому расставляем, начиная со второго с конца
		Для НомерСКонца = 2 По КоличествоСообщений Цикл
			Индекс = КоличествоСообщений - НомерСКонца;
			ДействияВручную.Вставить(Индекс + 1, РазделительПредложений);
		КонецЦикла;
		
		Элементы.ДеятельностьОтнесенаКПострадавшимОтКоронавирусаДействияПослеВключения.Заголовок = Новый ФорматированнаяСтрока(ДействияВручную);
		
	КонецЕсли;
	
	ОповеститьОбИзмененииНастройкиПродленияСроковНалоговОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделУСН(Команда)
	
	СтруктураПоиска = Новый Структура("Идентификатор", "УСН");
	МассивИдентификаторов = Список.НайтиСтроки(СтруктураПоиска);
	
	Если МассивИдентификаторов.Количество() > 0 Тогда
		Элементы.Список.ТекущаяСтрока = МассивИдентификаторов[0].ПолучитьИдентификатор();
		Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_УСН;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделЕНВД(Команда)
	
	СтруктураПоиска = Новый Структура("Идентификатор", "ЕНВД");
	МассивИдентификаторов = Список.НайтиСтроки(СтруктураПоиска);
	
	Если МассивИдентификаторов.Количество() > 0 Тогда
		Элементы.Список.ТекущаяСтрока = МассивИдентификаторов[0].ПолучитьИдентификатор();
		Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_ЕНВД;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВСписокПатентов(Команда)
	
	ПараметрыФормы = Новый Структура("Отбор, Период", Новый Структура("Организация", Организация), ПериодПереносаСроков.Начало);
	ОткрытьФорму("Документ.ОперацияСПатентом.ФормаСписка", 
		ПараметрыФормы, 
		ЭтотОбъект, 
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВНастройкиУплатыНалогаНаИмущество(Команда)
	ПерейтиВНастройкиУплатыАвансов("НалогНаИмущество");
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВНастройкиУплатыТранспортногоНалога(Команда)
	ПерейтиВНастройкиУплатыАвансов("ТранспортныйНалог");
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВНастройкиУплатыЗемельногоНалога(Команда)
	ПерейтиВНастройкиУплатыАвансов("ЗемельныйНалог");
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьОтнесенаКПострадавшимОтКоронавирусаДействияПослеВключенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОтборСтраниц = Новый Структура("Идентификатор", НавигационнаяСсылкаФорматированнойСтроки);
	Страницы = Список.НайтиСтроки(ОтборСтраниц);
	Если Не ЗначениеЗаполнено(Страницы) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ИдентификаторСтроки = Страницы[0].ПолучитьИдентификатор();
	Элементы.Список.ТекущаяСтрока = ИдентификаторСтроки;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуАнтикризис()
	
	СтруктураПоиска = Новый Структура("Идентификатор", "Антикризис");
	МассивИдентификаторов = Список.НайтиСтроки(СтруктураПоиска);
	
	Если МассивИдентификаторов.Количество() > 0 Тогда
		Элементы.Список.ТекущаяСтрока = МассивИдентификаторов[0].ПолучитьИдентификатор();
		Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_Антикризис;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПанели_ЕНВД

&НаКлиенте
Процедура ДатаУплатыЕНВДЗа1Кв2020ПриИзменении(Элемент)
	НастройкиУчетаВызовСервера.ЗаписатьПродленныйСрокУплатыНалога(Организация, "ДатаУплатыЕНВДЗа1Кв2020", ДатаУплатыЕНВДЗа1Кв2020);
КонецПроцедуры

&НаКлиенте
Процедура ДатаУплатыЕНВДЗа2Кв2020ПриИзменении(Элемент)
	НастройкиУчетаВызовСервера.ЗаписатьПродленныйСрокУплатыНалога(Организация, "ДатаУплатыЕНВДЗа2Кв2020", ДатаУплатыЕНВДЗа2Кв2020);
КонецПроцедуры

&НаКлиенте
Процедура ДатаУплатыЕНВДЗа3Кв2020ПриИзменении(Элемент)
	НастройкиУчетаВызовСервера.ЗаписатьПродленныйСрокУплатыНалога(Организация, "ДатаУплатыЕНВДЗа3Кв2020", ДатаУплатыЕНВДЗа3Кв2020);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбъектыСОсобымПорядкомНалогообложения(Команда)
	
	ПараметрыФормы  = Новый Структура("Отбор", Новый Структура("Организация", Организация));
	ОткрытьФорму("РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ФормаСписка",
		ПараметрыФормы,
		ЭтаФорма,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияТранспортныхСредств(Команда)
	
	ПараметрыФормы  = Новый Структура("Отбор", Новый Структура("Организация", Организация));
	ОткрытьФорму("РегистрСведений.РегистрацияТранспортныхСредств.ФормаСписка",
		ПараметрыФормы,
		ЭтаФорма,
		УникальныйИдентификатор);
		
КонецПроцедуры

&НаКлиенте
Процедура СтавкиТранспортногоНалога(Команда)
	
	ОткрытьФорму("РегистрСведений.СтавкиТранспортногоНалога.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияЗемельныхУчастков(Команда)
	
	ПараметрыФормы  = Новый Структура("Отбор", Новый Структура("Организация", Организация));
	ОткрытьФорму("РегистрСведений.РегистрацияЗемельныхУчастков.ФормаСписка",
		ПараметрыФормы,
		ЭтаФорма,
		УникальныйИдентификатор);
		
КонецПроцедуры

&НаКлиенте
Процедура УплатаАвансовПоНалогам(Команда)
	
	Строка = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Налог = Строка.Ссылка;
	
	ПараметрыФормы = Новый Структура("Налог, Организация", Налог, Организация);
		
	ОткрытьФорму("РегистрСведений.ПорядокУплатыНалоговНаМестах.Форма.НастройкаВСписке",
		ПараметрыФормы,
		ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУплатыНалоговНаМестах(Команда)
	
	Строка = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Налог = Строка.Ссылка;
	
	ПараметрыФормы = Новый Структура("Налог, Отбор",
		Налог,
		Новый Структура("Организация", Организация));
	
	ОткрытьФорму("РегистрСведений.ПорядокУплатыНалоговНаМестах.ФормаСписка",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыОтраженияРасходов(Команда)
		
	Строка = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Налог = Строка.Идентификатор;
	
	ВидНалога = ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов." + Налог);
	ПараметрыФормы = Новый Структура("ВидНалога", ВидНалога);
	
	ОткрытьФорму("РегистрСведений.СпособыОтраженияРасходовПоНалогам.ФормаСписка",
		ПараметрыФормы,
		ЭтаФорма,
		ВидНалога);
	
КонецПроцедуры

&НаКлиенте
Процедура Патенты(Команда)
	
	ПараметрыФормы  = Новый Структура("Отбор", Новый Структура("Организация", Организация));
	ОткрытьФорму("Документ.ОперацияСПатентом.ФормаСписка", 
		ПараметрыФормы, 
		ЭтотОбъект, 
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыДеятельностиЕНВД(Команда)
	
	ПараметрыФормы  = Новый Структура("Отбор", Новый Структура("Владелец", Организация));
	ОткрытьФорму("Справочник.ВидыДеятельностиЕНВД.ФормаСписка", 
		ПараметрыФормы, 
		ЭтаФорма, 
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ТорговыеТочки(Команда)
	
	ПараметрыФормы  = Новый Структура("Отбор", Новый Структура("Организация", Организация));
	ОткрытьФорму("Справочник.ТорговыеТочки.ФормаСписка", 
		ПараметрыФормы, 
		ЭтаФорма, 
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура История(Команда)
	
	ИмяРегистра = ИмяРегистра(ТекущийЭлемент.Имя);
	Отбор = Новый Структура("Организация", Организация);
	Если ИмяРегистра = "СтраховыеВзносы" Тогда
		Отбор.Вставить("ГоловнаяОрганизация", ГоловнаяОрганизация);
		ОткрытьФорму("РегистрСведений.НастройкиУчетаЗарплаты.Форма.ФормаСпискаСтраховыхВзносов", Новый Структура("Отбор, ВедущийОбъект, ТекущийПериод", Отбор, Организация, ТекущаяДата));
	ИначеЕсли ИмяРегистра = "НастройкиУчетаСтраховыхВзносовИП" Тогда
		ОткрытьФорму("РегистрСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Форма.ФормаСписка", 
			Новый Структура("Отбор, ВедущийОбъект, ТекущийПериод", Отбор, Организация, ТекущаяДата));
	Иначе
		ОткрытьФорму(СтрШаблон("РегистрСведений.%1.Форма.ФормаСписка", ИмяРегистра), Новый Структура("Отбор, ВедущийОбъект, ТекущийПериод", Отбор, Организация, ТекущаяДата));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура _ОК(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ТекущийЭлемент.Имя, "ОК_") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРегистра = ИмяРегистра(ТекущийЭлемент.Имя);
	
	Если ИмяРегистра = "НастройкиУчетаНДС"
		И (Не КорректныйПериодНачалаВеденияРаздельногоУчетаНДС()
		ИЛИ Не КорректныйПериодНачалаВеденияДеятельностьНДС0()) Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимБезЗакрывающихДокументов И 
		НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.Общая")
		И ИмяРегистра = "НастройкиУчетаНДС" Тогда
		Таймаут = 30;
		ТекстИнформация = НСтр("ru = 'Плательщик НДС с правом на вычет должен формировать закрывающие документы (Накладная, Счет-фактура, УПД и т.д.). Для этого необходимо включить функционал ""Расчеты с покупателями и поставщиками"", в разделе Настройки - Функциональность - Расчеты.'");
		МассивЧастейВопроса = Новый Массив;
		МассивЧастейВопроса.Добавить(ТекстИнформация);
		МассивЧастейВопроса.Добавить(Символы.ПС);
		ДополнительныеПараметры = Новый Структура("ИмяРегистра", ИмяРегистра);
		Если ВозможноИзменитьФункциональностьРасчеты Тогда
			МассивЧастейВопроса.Добавить(НСтр("ru = 'Включить автоматически?'"));
			Оповещение = Новый ОписаниеОповещения("ВопросВключенияРасчетов", ЭтотОбъект, ДополнительныеПараметры);
			РежимДиалога = Новый СписокЗначений;
			РежимДиалога.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Включить'"));
			РежимДиалога.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
		Иначе
			МассивЧастейВопроса.Добавить(НСтр("ru = 'Перейти в настройки?'"));
			Оповещение = Новый ОписаниеОповещения("ВопросПереходВНастройки", ЭтотОбъект);
			РежимДиалога = РежимДиалогаВопрос.ДаНет;
		КонецЕсли;
		ТекстВопроса = Новый ФорматированнаяСтрока(МассивЧастейВопроса);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалога, Таймаут);
	Иначе
		_ОКПродолжить(ИмяРегистра);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перезапустить(Команда)
	
	Если Не КорректныйПериодНачалаВеденияРаздельногоУчетаНДС() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьИзменения("НастройкиУчетаНДС");
	
	УстановитьИнтерфейсНаСервере();
	ЗавершитьРаботуСистемы(Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Функция КорректныйПериодНачалаВеденияРаздельногоУчетаНДС()
	
	Если (ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
		ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов)
		И Не ОрганизацияВелаРаздельныйУчет Тогда
		
		Если ДатаИзменения <> НачалоКвартала(ДатаИзменения) Тогда
			ТекстСообщения = НСтр("ru = 'Раздельный учет НДС можно включить только с начала квартала'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ДатаИзмененияСтрокой");
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПроверьтеДеятельностьОтнесенаКПострадавшимОтКоронавируса(Команда)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОсновнойКодОКВЭД = ОсновнойКодОКВЭД(Организация);
	Иначе
		ОсновнойКодОКВЭД = "";
	КонецЕсли;
	
	ОбщегоНазначенияБПКлиент.ОткрытьПроверкуДеятельностиНаКоронавирус(ОсновнойКодОКВЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверьтеРеестрМСП(Команда)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ИНН = ОрганизацияИНН(Организация);
	Иначе
		ИНН = "";
	КонецЕсли;
	
	ОбщегоНазначенияБПКлиент.ОткрытьПроверкуРеестрМСП(ИНН);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПодсказкуНастройкиСистемыНалогообложения(Команда)
	
	Элементы.ГруппаНастройкиСистемыНалогообложенияПодсказка.Скрыть();
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.СтраницаНастройкиСистемыНалогообложенияПодсказка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция КорректныйПериодНачалаВеденияДеятельностьНДС0()
	
	Если ИзменениеДеятельностиНДС0 Тогда
		
		Если ДатаИзменения <> НачалоКвартала(ДатаИзменения) Тогда
			ТекстСообщения = НСтр("ru = 'Параметры деятельности по ставке НДС 0% можно изменить только с начала квартала.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ДатаИзмененияСтрокой");
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура _ОКЗавершение(Результат, ИмяРегистра) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Сообщить(Результат.ПодробноеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	ЗаписатьСтатистику(ИмяРегистра);
	ЗаписатьИзменения(ИмяРегистра);
	ОповеститьОбИзмененииНастроек(Истина);
	ОтобразитьИсторию(ИмяРегистра);
	
	Если ИмяРегистра = "НастройкиСистемыНалогообложения" Тогда
	
		Разворачивать = ПолныйСписок;
		ПрочитатьНастройкиОрганизации();
		Разворачивать = Ложь;
		
		Элементы.ГруппаНДСПриУСН.ЦветФона = Новый Цвет;
		
		СформироватьПодсказкуПриИзмененииСистемыНалогообложения();
		
		НастройкиСистемыНалогообложенияФормыКлиент.ОбновитьИнтерфейсПослеЗаписиСистемыНалогообложения(
			Новый Структура("ИспользуемыеСистемыНалогообложенияПередЗаписью, ИспользуемыеСистемыНалогообложенияПослеЗаписи",
			ИспользуемыеСистемыНалогообложенияПередЗаписью, ИспользуемыеСистемыНалогообложенияПослеЗаписи));
		
		ПараметрыОповещения = Новый Структура("Организация, Период", НастройкиСистемыНалогообложения.Организация,
			НастройкиСистемыНалогообложения.Период);
		Оповестить("Запись_НастройкиСистемыНалогообложения", ПараметрыОповещения, ЭтотОбъект);

	ИначеЕсли ИмяРегистра = "НастройкиУчетаНДС" Тогда
		
		ИзменениеДеятельностиНДС0 = Ложь;
		Элементы.ДекорацияПомощникПодбораКода.Видимость = ПоказыватьПомощникПодбораОпераций0(Организация);
		
		НастройкиУчетаНДСФормыКлиент.ОбновитьИнтерфейсПослеЗаписиНастройкиУчетаНДС(
			Новый Структура("ИспользуетсяРаздельныйУчетНДСПередЗаписью, ИспользуетсяРаздельныйУчетНДСПослеЗаписи",
						ИспользуетсяРаздельныйУчетНДСПередЗаписью, ИспользуетсяРаздельныйУчетНДСПослеЗаписи)); 
		
		Активизировать();
		ПроведениеКлиент.ОжидатьПереключенияОтложенногоПроведения(РезультатЗаданияПереключенияОтложенногоПроведения);
		Если ЭтотОбъект.НастройкиУчетаНДС.Период > БудущаяДатаВключенияРУ Тогда
			ПериодФормированияОстатков = ЭтотОбъект.НастройкиУчетаНДС.Период;
		Иначе
			ПериодФормированияОстатков = БудущаяДатаВключенияРУ;
		КонецЕсли;

		ЕстьОстаткиДляРУ   = ЕстьОстаткиПоБУ(Организация, ПериодФормированияОстатков);  
		ТребуютсяОстаткиРУ = ЕстьОстаткиДляРУ
			И (ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
			ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов);
			
		Если (Не ОрганизацияВелаРаздельныйУчет И ТребуютсяОстаткиРУ И ЕстьОстаткиДляРУ)
			ИЛИ ВключилиРУБудущейДатой Тогда
			
			Элементы.БаннерВключениеРУ.Видимость = Ложь;
			Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = Ложь;
			
			Результат = ВыполнитьОбработкиПоПереходуНаРаздельныйУчет(
				ЭтотОбъект.Организация, ПериодФормированияОстатков);
			
			Если Результат.Статус = "Выполнено" Тогда
				
				Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = Истина;
				ЕстьВводОстатковПоРУ = Истина;
				ОстаткиПоРУТолькоЧтоСформированы = Истина;
				
			Иначе
				
				ОповещениеОЗавершении = Новый ОписаниеОповещения("КнопкаВыполнитьЗавершение", ЭтотОбъект);
				ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
				ПараметрыОжидания.ВыводитьСообщения = Истина;
				ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
				ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат,
						ОповещениеОЗавершении, ПараметрыОжидания);
				
			КонецЕсли;
		КонецЕсли;
		
		ОрганизацияВелаРаздельныйУчет = ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19 
			ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года
			ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов;
		
		Возврат;
		
	КонецЕсли;
	
	ИзменениеНастроек = НастройкиСистемыНалогообложенияФормыКлиент.НовыйИзменениеНастроек();
	
	Если ИмяРегистра = "НастройкиУчетаСтраховыхВзносовИП" Тогда
		ПараметрыОповещения = Новый Структура("Организация, Период", 
			НастройкиУчетаСтраховыхВзносовИП.Организация, НастройкиУчетаСтраховыхВзносовИП.Период);
		Оповестить("Запись_НастройкиУчетаСтраховыхВзносовИП", ПараметрыОповещения);
		
		ИзменениеНастроек.НастройкаПередЗаписью = УплачиватьДобровольныеВзносыВФССПередЗаписью;
		ИзменениеНастроек.НастройкаПослеЗаписи = УплачиватьДобровольныеВзносыВФССПослеЗаписи;
		
	ИначеЕсли ИмяРегистра = "НастройкиУчетаТуристическогоНалога" Тогда
		
		ИзменениеНастроек.НастройкаПередЗаписью = ПлательщикТуристическогоНалогаПередЗаписью;
		ИзменениеНастроек.НастройкаПослеЗаписи = ПлательщикТуристическогоНалогаПослеЗаписи;

	ИначеЕсли ИмяРегистра = "НастройкиУчетаНалогаНаПрибыль" Тогда

		ИзменениеНастроек.НастройкаПередЗаписью = ИспользуетсяПередачаИнвестиционногоВычетаПередЗаписью;
		ИзменениеНастроек.НастройкаПослеЗаписи = ИспользуетсяПередачаИнвестиционногоВычетаПослеЗаписи;

	КонецЕсли;
	
	Если ИзменениеНастроек.НастройкаПередЗаписью <> Неопределено И ИзменениеНастроек.НастройкаПослеЗаписи <> Неопределено Тогда
		НастройкиСистемыНалогообложенияФормыКлиент.ОбновитьИнтерфейсПослеЗаписиНастройкиУчета(ИзменениеНастроек);
	КонецЕсли;
	
	Активизировать();
	ПроведениеКлиент.ОжидатьПереключенияОтложенногоПроведения(РезультатЗаданияПереключенияОтложенногоПроведения);
	
КонецПроцедуры

&НаКлиенте
Процедура Гостиницы(Команда)
	
	ПараметрыФормы  = Новый Структура("Отбор", Новый Структура("Организация", Организация));
	ОткрытьФорму("Справочник.Гостиницы.ФормаСписка", 
		ПараметрыФормы, 
		ЭтотОбъект, 
		УникальныйИдентификатор);
		
КонецПроцедуры

&НаКлиенте
Процедура _ОКПродолжить(ИмяРегистра)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("_ОКЗавершение", ЭтотОбъект, ИмяРегистра);
	ПроведениеКлиент.НачатьОчисткуРегистраПроверенныхДокументов(ОповещениеОЗавершении, ДатаИзменения, Организация);
	Если ЭтоМобильныйКлиент Тогда
		Элементы.СтраницаНастройкиСистемыНалогообложенияПодсказка.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЧтениеНастроекУчета

&НаСервере
Процедура ОписатьНастройкиУчета()
	
	Список.Очистить();
	
	СкрытыеОтчеты = 0;
	
	// Есть у всех типов организаций
	// содержит в качестве вложений набор налогов
	// т.н. "Описание системы налогообложения"
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Система налогообложения'");
	НоваяСтрока.Идентификатор = "СистемаНалогообложения";
	НоваяСтрока.Включен      = Истина;
	НоваяСтрока.Порядок = 99;
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Антикризис'");
		НоваяСтрока.Идентификатор = "Антикризис";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок      = 100;
	КонецЕсли;
	
	// Описание системы налогообложения
	Если ЭтоЮрЛицо Тогда
		Если НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль Тогда
			НоваяСтрока = Список.Добавить();
			НоваяСтрока.Наименование  = НСтр("ru = 'Налог на прибыль'");
			НоваяСтрока.Идентификатор = "НалогНаПрибыль";
			НоваяСтрока.Включен      = Истина;
			НоваяСтрока.Порядок = 110;
		ИначеЕсли СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНалогаНаПрибыль") <> Неопределено Тогда
			НоваяСтрока = Список.Добавить();
			НоваяСтрока.Наименование  = НСтр("ru = 'Налог на прибыль'");
			НоваяСтрока.Идентификатор = "НалогНаПрибыль";
			НоваяСтрока.Включен      = Истина;
			НоваяСтрока.Порядок = 110;
			НоваяСтрока.БылВключен   = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиСистемыНалогообложения.ПрименяетсяУСН Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'УСН'");
		НоваяСтрока.Идентификатор = "УСН";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 130;
	ИначеЕсли СписокНедействующихНастроек.НайтиПоЗначению("ПрименяетсяУСН") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'УСН'");
		НоваяСтрока.Идентификатор = "УСН";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 130;
		НоваяСтрока.БылВключен   = Истина;
	КонецЕсли;
	
	Если НастройкиСистемыНалогообложения.ПлательщикНДФЛ Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'НДФЛ (ИП)'");
		НоваяСтрока.Идентификатор = "НДФЛ_Предприниматель";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 140;
	ИначеЕсли СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНДФЛ") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'НДФЛ (ИП)'");
		НоваяСтрока.Идентификатор = "НДФЛ_Предприниматель";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 140;
		НоваяСтрока.БылВключен   = Истина;
	КонецЕсли;
	
	Если НастройкиСистемыНалогообложения.ПлательщикНДС 
		Или НастройкиСистемыНалогообложения.ПлательщикНДСПрименяющийУСН Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'НДС'");
		НоваяСтрока.Идентификатор = "НДС";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 150;
	ИначеЕсли СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНДС") <> Неопределено
		Или СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНДСПрименяющийУСН") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'НДС'");
		НоваяСтрока.Идентификатор = "НДС";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 150;
		НоваяСтрока.БылВключен   = Истина;
	КонецЕсли;
	Если НастройкиСистемыНалогообложения.ПлательщикЕНВД Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'ЕНВД'");
		НоваяСтрока.Идентификатор = "ЕНВД";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 160;
	ИначеЕсли СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикЕНВД") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'ЕНВД'");
		НоваяСтрока.Идентификатор = "ЕНВД";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 160;
		НоваяСтрока.БылВключен   = Истина;
	КонецЕсли;
	Если НЕ ЭтоЮрЛицо Тогда
		Если НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент Тогда
			НоваяСтрока = Список.Добавить();
			НоваяСтрока.Наименование  = НСтр("ru = 'Патенты'");
			НоваяСтрока.Идентификатор = "Патент";
			НоваяСтрока.Включен      = Истина;
			НоваяСтрока.Порядок = 170;
		ИначеЕсли СписокНедействующихНастроек.НайтиПоЗначению("ПрименяетсяУСНПатент") <> Неопределено Тогда
			НоваяСтрока = Список.Добавить();
			НоваяСтрока.Наименование  = НСтр("ru = 'Патенты'");
			НоваяСтрока.Идентификатор = "Патент";
			НоваяСтрока.Включен      = Истина;
			НоваяСтрока.Порядок = 170;
			НоваяСтрока.БылВключен   = Истина;
		КонецЕсли;
	КонецЕсли;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Торговый сбор'");
	НоваяСтрока.Идентификатор = "ТорговыйСбор";
	НоваяСтрока.Включен      = Истина;
	НоваяСтрока.Порядок = 180;
	НоваяСтрока.БылВключен   = СписокНедействующихНастроек.НайтиПоЗначению("ТорговыйСбор") <> Неопределено;
	
	Если РегистрыСведений.НастройкиСистемыНалогообложения.ПлательщикФиксированныхВзносов(НастройкиСистемыНалогообложения) Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Страховые взносы (ИП)'");
		НоваяСтрока.Идентификатор = "СтраховыеВзносы_Предприниматель";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 190;
	ИначеЕсли СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикФиксированныхВзносов") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Страховые взносы (ИП)'");
		НоваяСтрока.Идентификатор = "СтраховыеВзносы_Предприниматель";
		НоваяСтрока.Включен      = Истина;
		НоваяСтрока.Порядок = 190;
		НоваяСтрока.БылВключен   = Истина;
	КонецЕсли;
	
	Если ЭтоЮрЛицо Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Налог на имущество'");
		НоваяСтрока.Идентификатор = "НалогНаИмущество";
		Если НастройкиСистемыНалогообложения.ПрименяетсяУСН 
			И СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНалогаНаПрибыль") = Неопределено Тогда
			НоваяСтрока.Включен      = Ложь;
			НоваяСтрока.Порядок = 301;
		Иначе
			Если СписокНеиспользуемыхНастроек.НайтиПоЗначению("НалогНаИмущество") = Неопределено Тогда
				НоваяСтрока.Включен      = Истина;
				НоваяСтрока.Порядок = 200;
			Иначе
				НоваяСтрока.Включен      = Ложь;
				НоваяСтрока.Порядок = 301;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВедетсяУчетЗарплаты Тогда
		Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
			НоваяСтрока = Список.Добавить();
			НоваяСтрока.Наименование  = НСтр("ru = 'НДФЛ'");
			НоваяСтрока.Идентификатор = "НДФЛ_Агент";
			НоваяСтрока.Включен      = ЭтоЮрЛицо ИЛИ УчетЗарплаты.ИПИспользуетТрудНаемныхРаботников(Организация);
			НоваяСтрока.Порядок = 201;
		КонецЕсли;
	КонецЕсли;
	
	Если ВедетсяУчетЗарплаты Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Страховые взносы'");
		НоваяСтрока.Идентификатор = "СтраховыеВзносы";
		НоваяСтрока.Включен      = ЭтоЮрЛицо ИЛИ УчетЗарплаты.ИПИспользуетТрудНаемныхРаботников(Организация);
		НоваяСтрока.Порядок = 202;
	КонецЕсли;
	
	// Прочие налоги, не описывающие систему налогообложения
	// однако уплачиваемые организацией, при соблюдении некоторых условий
	
	Если ЭтоЮрЛицо Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Транспортный налог'");
		НоваяСтрока.Идентификатор = "ТранспортныйНалог";
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок = 302;
	КонецЕсли;
	
	Если ЭтоЮрЛицо Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Земельный налог'");
		НоваяСтрока.Идентификатор = "ЗемельныйНалог";
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок = 303;
	КонецЕсли;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Косвенные налоги (импорт)'");
	НоваяСтрока.Идентификатор = "КосвенныеНалогиТамСоюз";
	НоваяСтрока.Включен      = Ложь;
	НоваяСтрока.Порядок = 304;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Туристический налог'");
	НоваяСтрока.Идентификатор = "ТуристическийНалог";
	НоваяСтрока.Включен      = Ложь;
	НоваяСтрока.Порядок = 305;
	
	// Прочие не обслуживаемые налоги и отчеты
	
	Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможнаРеализацияАлкогольнойПродукции(НастройкиСистемыНалогообложения)
		Или СписокНедействующихНастроек.НайтиПоЗначению("ДекларированиеАлкогольнойПродукции") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Декларирование алкоголя'");
		НоваяСтрока.Идентификатор = "ДекларированиеАлкогольнойПродукции";
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок = 306;
	КонецЕсли;
	
	Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможнаРеализацияПодакцизныхТоваров(НастройкиСистемыНалогообложения)
		Или СписокНедействующихНастроек.НайтиПоЗначению("Акцизы") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Акцизы'");
		НоваяСтрока.Идентификатор = "Акцизы";
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок = 307;
	КонецЕсли;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Водный налог'");
	НоваяСтрока.Идентификатор = "ВодныйНалог";
	НоваяСтрока.Включен      = Ложь;
	НоваяСтрока.Порядок = 308;
	
	Если ЭтоЮрЛицо Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Налог на игорный бизнес'");
		НоваяСтрока.Идентификатор = "НалогНаИгорныйБизнес";
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок = 309;
	КонецЕсли;
	
	Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможенЕСХН(НастройкиСистемыНалогообложения)
		Или СписокНедействующихНастроек.НайтиПоЗначению("ЕСХН") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование       = НСтр("ru = 'ЕСХН'");
		НоваяСтрока.ПолноеНаименование = НСтр("ru = 'Единый сельскохозяйственный налог (ЕСХН)'");
		НоваяСтрока.Идентификатор      = "ЕСХН";
		НоваяСтрока.Включен            = Ложь;
		НоваяСтрока.Порядок = 310;
	КонецЕсли;
	
	Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможенНДПИ(НастройкиСистемыНалогообложения)
		Или СписокНедействующихНастроек.НайтиПоЗначению("НДПИ") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование       = НСтр("ru = 'НДПИ'");
		НоваяСтрока.ПолноеНаименование = НСтр("ru = 'Налог на добычу полезных ископаемых (НДПИ)'");
		НоваяСтрока.Идентификатор      = "НДПИ";
		НоваяСтрока.Включен            = Ложь;
		НоваяСтрока.Порядок = 311;
	КонецЕсли;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Статистика'");
	НоваяСтрока.Идентификатор = "СтатистическаяОтчетность";
	НоваяСтрока.Включен      = Ложь;
	НоваяСтрока.Порядок = 400;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Экологические платежи'");
	НоваяСтрока.Идентификатор = "ПлатаЗаНегативноеВоздействиеНаОкружающуюСреду";
	НоваяСтрока.Включен      = Ложь;
	НоваяСтрока.Порядок = 500;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Экологический сбор'");
	НоваяСтрока.Идентификатор = "ЭкологическийСбор";
	НоваяСтрока.Включен      = Ложь;
	НоваяСтрока.Порядок = 501;
	
	НоваяСтрока = Список.Добавить();
	НоваяСтрока.Наименование  = НСтр("ru = 'Прочая отчетность в Росприроднадзор'");
	НоваяСтрока.Идентификатор = "Росприроднадзор";
	НоваяСтрока.Включен      = Ложь;
	НоваяСтрока.Порядок = 502;
	
	Если РегистрыСведений.НастройкиСистемыНалогообложения.ПрименяетсяЕдинаяУпрощеннаяДекларация(НастройкиСистемыНалогообложения)
		Или СписокНедействующихНастроек.НайтиПоЗначению("ЕдинаяУпрощеннаяДекларация") <> Неопределено Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование  = НСтр("ru = 'Единая (упрощенная) декларация'");
		НоваяСтрока.Идентификатор = "ЕдинаяУпрощеннаяДекларация";
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок = 600;
	КонецЕсли;
	
	Если Не ЭтоЮрЛицо И Не НастройкиСистемыНалогообложения.ПлательщикНДФЛ Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Идентификатор = "НДФЛ_ФизЛицо";
		НоваяСтрока.Наименование  = НСтр("ru = 'НДФЛ (физ. лицо)'");
		НоваяСтрока.ПолноеНаименование = НСтр("ru = 'Налог на доходы физических лиц'");
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок      = 700;
	КонецЕсли;
	
	Если Справочники.Организации.ЭтоНекоммерческаяОрганизация(Организация) 
		И НастройкиУчетаПереопределяемый.НеобходимоСдаватьОтчетностьВМинюст(Организация) Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Идентификатор = "Минюст";
		НоваяСтрока.Наименование  = НСтр("ru = 'Министерство юстиции'");
		НоваяСтрока.ПолноеНаименование = НСтр("ru = 'Отчетность в Министерство юстиции'");
		НоваяСтрока.Включен      = Ложь;
		НоваяСтрока.Порядок      = 401;
	КонецЕсли;
	
	ПрочитатьИспользуемыеНастройки();
	
	Список.Сортировать("Включен Убыв, Порядок Возр");
	
	Для Каждого СтрокаСписка ИЗ Список Цикл
		Если НЕ СтрокаСписка.Включен Тогда
			СкрытыеОтчеты = СкрытыеОтчеты + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если СкрытыеОтчеты <> 0 Тогда
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Наименование = СтрШаблон(НСтр("ru = 'Все налоги и отчеты (еще %1)'"), СкрытыеОтчеты);
		НоваяСтрока.Служебный    = Истина;
		НоваяСтрока.Включен      = Истина;
		
		Если Разворачивать Тогда
			Элементы.Список.ОтборСтрок = Новый ФиксированнаяСтруктура("Служебный", Ложь);
		Иначе
			Элементы.Список.ОтборСтрок = Новый ФиксированнаяСтруктура("Включен", Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	ПолныйСписок = СкрытыеОтчеты = 0 И НЕ Разворачивать;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьУчетнуюПолитику()
	
	Если (ПрименениеНДСНаУСН Или ИзменениеСтавкиНДСПриУСН)
		И ДатаИзменения > ТекущаяДата Тогда
		НаДатуНДСПриУСН = ДатаИзменения;
	Иначе
		НаДатуНДСПриУСН = ТекущаяДата;
	КонецЕсли;

	МенеджерЗаписиПериодическогоРегистра("НастройкиСистемыНалогообложения", , Организация, НаДатуНДСПриУСН);

	Если ЭтоЮрЛицо Тогда
		Если НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль Тогда
			МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаНалогаНаПрибыль", , Организация);
		Иначе
			МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаНалогаНаПрибыль", , Организация, ,Ложь);
		КонецЕсли;
		МенеджерЗаписиПериодическогоРегистра("СтавкиНалогаНаИмущество", , Организация);
	Иначе
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаСтраховыхВзносовИП", , Организация);
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС", , Организация);
		Если НастройкиСистемыНалогообложения.ПлательщикНДФЛ Тогда
			МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаНДФЛ", , Организация);
		Иначе
			МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаНДФЛ", , Организация, , Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль
		ИЛИ НастройкиСистемыНалогообложения.ПлательщикНДФЛ Тогда
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаНДС", , Организация);
	Иначе
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаНДС", , Организация, НаДатуНДСПриУСН, Ложь);
	КонецЕсли;
	Если НастройкиСистемыНалогообложения.ПрименяетсяУСН Тогда
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаУСН", , Организация);
	Иначе
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаУСН", , Организация, , Ложь);
	КонецЕсли;
	
	Если ВедетсяУчетЗарплаты Тогда
		Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
			МенеджерЗаписиПериодическогоРегистра("СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев", , Организация);
			МенеджерЗаписиПериодическогоРегистра("ПрименяемыеТарифыСтраховыхВзносов", "ГоловнаяОрганизация", ГоловнаяОрганизация);
			МенеджерЗаписиРегистра("НастройкиУчетаСтраховыхВзносов", , Организация);
			МенеджерЗаписи = РегистрыСведений.РазмерФиксированныхВзносовОтНесчастныхСлучаевАУСН.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = ТекущаяДата;
			МенеджерЗаписи.Прочитать();
			ЭтотОбъект.ЗначениеВРеквизитФормы(МенеджерЗаписи, "РазмерФиксированныхВзносовОтНесчастныхСлучаевАУСН");
		КонецЕсли;
		МенеджерЗаписиРегистра("УчетнаяПолитикаПоНДФЛ", "ГоловнаяОрганизация" , ГоловнаяОрганизация);
	КонецЕсли;
	
	Если НастройкиУчетаТуристическогоНалога.ПлательщикТуристическогоНалога Тогда
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаТуристическогоНалога", , Организация);
	Иначе
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаТуристическогоНалога", , Организация, , Ложь);
	КонецЕсли;
	
	ПрочитатьНастройкиАнтикризис();
	
	ЗаполнитьСвязанныеРеквизиты();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьОтдельнуюНастройкуУчетнойПолитики(ИмяРегистра)
	
	ОтборОрганизация = Новый Структура("Организация",Организация);
	
	Если ИмяРегистра = "ПрименяемыеТарифыСтраховыхВзносов" 
		ИЛИ ИмяРегистра = "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев"
		ИЛИ ИмяРегистра = "СтраховыеВзносы" Тогда
		МенеджерЗаписиПериодическогоРегистра("ПрименяемыеТарифыСтраховыхВзносов", "ГоловнаяОрганизация", ГоловнаяОрганизация);
		МенеджерЗаписиПериодическогоРегистра("СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев",           , Организация);
		МенеджерЗаписиРегистра("НастройкиУчетаСтраховыхВзносов",                                       , Организация);
	ИначеЕсли ИмяРегистра = "УчетнаяПолитикаПоНДФЛ" ИЛИ ИмяРегистра = "НастройкиУчетаСтраховыхВзносов" Тогда
		МенеджерЗаписиРегистра("УчетнаяПолитикаПоНДФЛ", "ГоловнаяОрганизация", ГоловнаяОрганизация);
		МенеджерЗаписиРегистра("НастройкиУчетаСтраховыхВзносов",             , Организация);
	ИначеЕсли ИмяРегистра = "НастройкиУчетаТуристическогоНалога" Тогда
		МенеджерЗаписиПериодическогоРегистра("НастройкиУчетаТуристическогоНалога", , Организация, , Ложь);
	Иначе
		МенеджерЗаписиПериодическогоРегистра(ИмяРегистра, , Организация);
	КонецЕсли;
	
	Если ИмяРегистра = "НастройкиСистемыНалогообложения" Тогда
		НастройкиСистемыНалогообложенияФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	ИначеЕсли ИмяРегистра = "НастройкиУчетаНалогаНаПрибыль" Тогда
		НастройкиУчетаНалогаНаПрибыльФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	ИначеЕсли ИмяРегистра = "НастройкиУчетаУСН" Тогда
		НастройкиУчетаУСНФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	ИначеЕсли ИмяРегистра = "НастройкиУчетаНДС" Тогда
		НастройкиУчетаНДСФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	ИначеЕсли ИмяРегистра = "НастройкиУчетаНДФЛ" Тогда
		НастройкиУчетаНДФЛФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	ИначеЕсли ИмяРегистра = "СтавкиНалогаНаИмущество" Тогда
		СтавкиНалогаНаИмуществоФормы.ПодготовитьФормуНаСервере(ЭтотОбъект, "СтавкиНалогаНаИмущество");
	ИначеЕсли ИмяРегистра = "НастройкиУчетаСтраховыхВзносовИП" Тогда
		НастройкиВзносовИП_ПФР_ФФОМСИзменены = Ложь;
		НастройкиВзносовИП_ФССИзменены = Ложь;
		МенеджерЗаписиПериодическогоРегистра(
			"НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС", , Организация, ТекущаяДата);
	КонецЕсли;
	
	ЗаполнитьСвязанныеРеквизиты(ИмяРегистра);
	
КонецПроцедуры

&НаСервере
Процедура МенеджерЗаписиПериодическогоРегистра(ИмяРегистра, ИмяОтбора = "Организация", ЗначениеОтбора, ПериодЗначений = Неопределено, Создавать = Истина)
	
	Если ПериодЗначений = Неопределено Тогда
		ПериодЗначений = ТекущаяДата;
	КонецЕсли;
	
	КлючЗаписи = КлючЗаписиДействующейУчетнойПолитики(ИмяРегистра, ИмяОтбора, ЗначениеОтбора, ПериодЗначений);
	Если КлючЗаписи = Неопределено Тогда
		
		Если НЕ ПравоРедактированияНастроек Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ Создавать Тогда
			Возврат;
		КонецЕсли;
		
		Если ИмяРегистра = "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев" ИЛИ ИмяРегистра = "ПрименяемыеТарифыСтраховыхВзносов" Тогда
			УчетЗарплаты.УстановкаНастроекПоУмолчанию(ИмяРегистра, ЭтотОбъект[ИмяРегистра], Новый Структура());
		Иначе
			РегистрыСведений[ИмяРегистра].УстановкаНастроекПоУмолчанию(ЭтотОбъект[ИмяРегистра], Новый Структура());
		КонецЕсли;
		ЭтотОбъект[ИмяРегистра].Период      = НачалоГода(ТекущаяДата);
		ЭтотОбъект[ИмяРегистра][ИмяОтбора]  = ЗначениеОтбора;
		МенеджерЗаписи = РеквизитФормыВЗначение(ИмяРегистра);
		МенеджерЗаписи.Записать(Истина);
	Иначе
		МенеджерЗаписи = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
		МенеджерЗаписи[ИмяОтбора] = КлючЗаписи[ИмяОтбора];
		МенеджерЗаписи.Период     = КлючЗаписи.Период;
		МенеджерЗаписи.Прочитать();
	КонецЕсли;
	ЭтаФорма.ЗначениеВРеквизитФормы(МенеджерЗаписи, ИмяРегистра);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КлючЗаписиДействующейУчетнойПолитики(ИмяРегистра, ИмяОтбора, ЗначениеОтбора, ПериодЗначений)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",  ПериодЗначений);
	Запрос.УстановитьПараметр(ИмяОтбора, ЗначениеОтбора);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	УчетнаяПолитикаСрезПоследних.Период КАК Период
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитикаСрезПоследних";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "УчетнаяПолитика", ИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организация", ИмяОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ЗначенияКлюча = Новый Структура;
		ЗначенияКлюча.Вставить(ИмяОтбора, ЗначениеОтбора);
		ЗначенияКлюча.Вставить("Период", Выборка.Период);
		
		Возврат РегистрыСведений[ИмяРегистра].СоздатьКлючЗаписи(ЗначенияКлюча);
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура МенеджерЗаписиРегистра(ИмяРегистра, ИмяОтбора = "Организация", ЗначениеОтбора)
	
	МенеджерЗаписи = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
	МенеджерЗаписи[ИмяОтбора] = ЗначениеОтбора;
	МенеджерЗаписи.Прочитать();
	ЭтаФорма.ЗначениеВРеквизитФормы(МенеджерЗаписи, ИмяРегистра);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвязанныеРеквизиты(ИмяРаздела = Неопределено)
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "СтраховыеВзносы" ИЛИ ИмяРаздела = "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев"
		ИЛИ ИмяРаздела = "ПрименяемыеТарифыСтраховыхВзносов" Тогда
		ВидТарифаПредставление = УчетСтраховыхВзносовКлиентСервер.ОписаниеВидаТарифа(ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа);
		Если НЕ ЗначениеЗаполнено(ВидТарифаПредставление) Тогда
			ВидТарифаПредставление = НСтр("ru = 'Вид тарифа страховых взносов не указан'");
		КонецЕсли;
		Элементы.ВидТарифа.РасширеннаяПодсказка.Заголовок = ВидТарифаПредставление;
		
		// Надбавки и скидки на страхование НС и ПЗ
		Если ЗначениеЗаполнено(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Надбавка) Тогда
			ПрименяютсяНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев = 1;
		ИначеЕсли ЗначениеЗаполнено(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Скидка) Тогда
			ПрименяютсяНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев = 2;
		Иначе
			ПрименяютсяНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено Тогда
		// Ранее использовавшиеся настройки
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация",   Организация);
		Запрос.УстановитьПараметр("ТекущаяДата",   ТекущаяДата);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(ТекущаяДата));
		Запрос.УстановитьПараметр("КонецГода", КонецГода(ТекущаяДата));
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиСистемыНалогообложения.Организация КАК Организация,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикЕНВД) КАК ПлательщикЕНВД,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент) КАК ПрименяетсяУСНПатент,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСН) КАК ПрименяетсяУСН,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы) КАК ПрименяетсяУСНДоходы,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходыМинусРасходы) КАК ПрименяетсяУСНДоходыМинусРасходы,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНДС) КАК ПлательщикНДС,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНДСПрименяющийУСН) КАК ПлательщикНДСПрименяющийУСН,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль) КАК ПлательщикНалогаНаПрибыль,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНДФЛ) КАК ПлательщикНДФЛ,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяАУСН) КАК ПрименяетсяАУСН,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяНалогНаПрофессиональныйДоход) КАК ПрименяетсяНалогНаПрофессиональныйДоход
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
		|ГДЕ
		|	НастройкиСистемыНалогообложения.Период < &ТекущаяДата
		|	И НастройкиСистемыНалогообложения.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	НастройкиСистемыНалогообложения.Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиСистемыНалогообложения.Организация,
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикЕНВД),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСН),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходыМинусРасходы),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНДС),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНДСПрименяющийУСН),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНДФЛ),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяАУСН),
		|	МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяНалогНаПрофессиональныйДоход)
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
		|ГДЕ
		|	НастройкиСистемыНалогообложения.Период > &КонецГода
		|	И НастройкиСистемыНалогообложения.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	НастройкиСистемыНалогообложения.Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиСистемыНалогообложенияСрезПоследних.Организация,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикЕНВД,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНПатент,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСН,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНДоходы,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНДоходыМинусРасходы,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикНДС,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикНДСПрименяющийУСН,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикНалогаНаПрибыль,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикНДФЛ,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяАУСН,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяНалогНаПрофессиональныйДоход
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК НастройкиСистемыНалогообложенияСрезПоследних";
		
		Выборка = Запрос.Выполнить().Выбрать();
		СписокНедействующихНастроек.Очистить();
		Пока Выборка.Следующий() Цикл
			Если Выборка.ПлательщикЕНВД Тогда
				СписокНедействующихНастроек.Добавить("ПлательщикЕНВД");
			КонецЕсли;
			Если Выборка.ПрименяетсяУСНПатент Тогда
				СписокНедействующихНастроек.Добавить("ПрименяетсяУСНПатент");
			КонецЕсли;
			Если Выборка.ПрименяетсяУСН Тогда
				СписокНедействующихНастроек.Добавить("ПрименяетсяУСН");
			КонецЕсли;
			Если Выборка.ПрименяетсяАУСН Тогда
				СписокНедействующихНастроек.Добавить("ПрименяетсяАУСН");
			КонецЕсли;
			Если Выборка.ПрименяетсяУСНДоходы Тогда
				СписокНедействующихНастроек.Добавить("ПрименяетсяУСНДоходы");
			КонецЕсли;
			Если Выборка.ПрименяетсяУСНДоходыМинусРасходы Тогда
				СписокНедействующихНастроек.Добавить("ПрименяетсяУСНДоходыМинусРасходы");
			КонецЕсли;
			Если Выборка.ПлательщикНДС Тогда
				СписокНедействующихНастроек.Добавить("ПлательщикНДС");
			КонецЕсли;
			Если Выборка.ПлательщикНДСПрименяющийУСН Тогда
				СписокНедействующихНастроек.Добавить("ПлательщикНДСПрименяющийУСН");
			КонецЕсли;
			Если Выборка.ПлательщикНалогаНаПрибыль Тогда
				СписокНедействующихНастроек.Добавить("ПлательщикНалогаНаПрибыль");
			КонецЕсли;
			Если Выборка.ПлательщикНДФЛ Тогда
				СписокНедействующихНастроек.Добавить("ПлательщикНДФЛ");
			КонецЕсли;
			Если РегистрыСведений.НастройкиСистемыНалогообложения.ПлательщикФиксированныхВзносов(Выборка) Тогда
				СписокНедействующихНастроек.Добавить("ПлательщикФиксированныхВзносов");
			КонецЕсли;
			Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможнаРеализацияАлкогольнойПродукции(Выборка) Тогда
				СписокНедействующихНастроек.Добавить("ДекларированиеАлкогольнойПродукции");
			КонецЕсли;
			Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможнаРеализацияПодакцизныхТоваров(Выборка) Тогда
				СписокНедействующихНастроек.Добавить("Акцизы");
			КонецЕсли;
			Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможенЕСХН(Выборка) Тогда
				СписокНедействующихНастроек.Добавить("ЕСХН");
			КонецЕсли;
			Если РегистрыСведений.НастройкиСистемыНалогообложения.ВозможенНДПИ(Выборка) Тогда
				СписокНедействующихНастроек.Добавить("НДПИ");
			КонецЕсли;
			Если РегистрыСведений.НастройкиСистемыНалогообложения.ПрименяетсяЕдинаяУпрощеннаяДекларация(Выборка) Тогда
				СписокНедействующихНастроек.Добавить("ЕдинаяУпрощеннаяДекларация");
			КонецЕсли;
		КонецЦикла;
		
		// Отключенные по функциональным опциям настройки
		СписокНеиспользуемыхНастроек.Очистить();
		Если НЕ ВедетсяУчетИмущества Тогда
			СписокНеиспользуемыхНастроек.Добавить("НалогНаИмущество");
			СписокНеиспользуемыхНастроек.Добавить("ТранспортныйНалог");
			СписокНеиспользуемыхНастроек.Добавить("ЗемельныйНалог");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкиУчетаНДС.КодОперации0) Тогда
		ПоУмолчаниюКодОперации0 = НастройкиУчетаНДС.КодОперации0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНастройкиСписка()
	
	Модифицированность = Ложь;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Элементы.Список.ТолькоПросмотр            = Истина;
		Элементы.СписокПодчиненный.ТолькоПросмотр = Истина;
		Элементы.СтраницыНастроек.ТолькоПросмотр  = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ПравоРедактированияНастроек Тогда
		Элементы.СтраницыНастроек.ТолькоПросмотр  = Истина;
		Элементы.СписокПодчиненный.ТолькоПросмотр = Истина;
	Иначе
	
		Элементы.Список.ТолькоПросмотр            = Ложь;
		Элементы.СписокПодчиненный.ТолькоПросмотр = Ложь;
		Элементы.СтраницыНастроек.ТолькоПросмотр  = Ложь;
		
		// Так как изменяем данные разных записей в одной форме, 
		// то наложим пессимистическую объектную блокировку вручную - 
		// по специальному набору измерений, который никогда не будет записан в регистр.
		
		РезультатБлокировки = "";
		Попытка 
			ОписаниеКлюча = Новый Структура("Организация",         Организация);
			УИД = УникальныйИдентификатор;
			// В Простом интерфейсе форма является подчиненной окну Организация и при обновлении интерфейса
			// может быть переоткрыта. При этом УникальныйИдентификатор формы меняется, но при закрытии формы
			// блокировка может быть отменена не сразу, что приводит к ошибке блокировки, поэтому используем 
			// идентификатор родительской формы
			Если ИнтерфейсТаксиПростой И ЗначениеЗаполнено(ИдентификаторФормыВладельца) Тогда
				УИД = ИдентификаторФормыВладельца;
			КонецЕсли;
			ЗаблокироватьДанныеДляРедактирования(
				РегистрыСведений.НалогиОтчеты.СоздатьКлючЗаписи(ОписаниеКлюча),
				, // Данные не хранятся в ИБ
				УИД); 
		Исключение
			Элементы.СтраницыНастроек.ТолькоПросмотр = Истина;
			РезультатБлокировки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьСписокНастройкиНалоговОтчетов()
	
	ЗаполнитьСписокНастройкиНалоговОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокНастройкиНалоговОтчетов()
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияПриОткрытии");
	
	Результат = ВыполнитьПолучениеДанныхНаСервере();
	
	Если Не Результат.ЗаданиеВыполнено Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища       = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияПриОткрытии", 0.5, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	Иначе
		ЗагрузитьРезультат();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПрочитатьИспользуемыеНастройки()
	
	// Поверхностное чтение, только чтобы узнать что отображать
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НалогиОтчеты.НалогОтчет КАК Ссылка,
	|	НалогиОтчеты.НалогОтчет.Код КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НалогиОтчеты
	|ИЗ
	|	РегистрСведений.НалогиОтчеты КАК НалогиОтчеты
	|ГДЕ
	|	НалогиОтчеты.Организация = &Организация
	|	И НалогиОтчеты.НалогОтчет ССЫЛКА Справочник.ЗадачиБухгалтера
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НалогиОтчеты.НалогОтчет.Владелец,
	|	НалогиОтчеты.НалогОтчет.Владелец.Код
	|ИЗ
	|	РегистрСведений.НалогиОтчеты КАК НалогиОтчеты
	|ГДЕ
	|	НалогиОтчеты.Организация = &Организация
	|	И НалогиОтчеты.НалогОтчет ССЫЛКА Справочник.ПравилаПредставленияОтчетовУплатыНалогов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Ссылка КАК Ссылка,
	|	ЗадачиБухгалтера.Код КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Акцизы
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО (ЗадачиБухгалтера.Код = ""Акцизы"")
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Ссылка КАК Ссылка,
	|	ЗадачиБухгалтера.Код КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_МестныеНалоги
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(&ТекущаяДата, Организация = &Организация) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО ПорядокУплатыНалоговНаМестахСрезПоследних.Налог = ЗадачиБухгалтера.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Код,
	|	ЗадачиБухгалтера.Ссылка
	|ПОМЕСТИТЬ ВТ_Задачи
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	НЕ ЗадачиБухгалтера.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_НалогиОтчеты.Ссылка
	|				ИЗ
	|					ВТ_НалогиОтчеты)
	|	И НЕ ЗадачиБухгалтера.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_Акцизы.Ссылка
	|				ИЗ
	|					ВТ_Акцизы)
	|	И НЕ ЗадачиБухгалтера.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_МестныеНалоги.Ссылка
	|				ИЗ
	|					ВТ_МестныеНалоги)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВариантыПримененияТребованийЗаконодательства.Организация,
	|	ВариантыПримененияТребованийЗаконодательства.Условие
	|ПОМЕСТИТЬ ВТ_Условия
	|ИЗ
	|	РегистрСведений.ВариантыПримененияТребованийЗаконодательства КАК ВариантыПримененияТребованийЗаконодательства
	|ГДЕ
	|	ВариантыПримененияТребованийЗаконодательства.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка КАК Правило,
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Условие КАК Условие
	|ПОМЕСТИТЬ ВТ_ПравилаУсловия
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов.Условия КАК ПравилаПредставленияОтчетовУплатыНалоговУсловия
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Условие В
	|			(ВЫБРАТЬ
	|				ВТ_Условия.Условие КАК ВТ_Условия
	|			ИЗ
	|				ВТ_Условия)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Правило
	|ПОМЕСТИТЬ ВТ_Правила
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПравилаПредставленияОтчетовУплатыНалогов
	|	И ЗадачиБухгалтера.Правило.Владелец В
	|			(ВЫБРАТЬ
	|				ВТ_Задачи.Ссылка
	|			ИЗ
	|				ВТ_Задачи)
	|	И ЗадачиБухгалтера.Правило В
	|			(ВЫБРАТЬ
	|				ВТ_ПравилаУсловия.Правило КАК ВТ_ПравилаУсловия
	|			ИЗ
	|				ВТ_ПравилаУсловия)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиБухгалтера.Правило
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Задачи.Код КАК Идентификатор,
	|	ВТ_Задачи.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_СписокЗадач
	|ИЗ
	|	ВТ_Правила КАК ВТ_Правила
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Задачи КАК ВТ_Задачи
	|		ПО ВТ_Правила.Правило.Владелец = ВТ_Задачи.Ссылка
	|ГДЕ
	|	(ГОД(ВТ_Правила.Правило.КонецДействия) >= ГОД(&ТекущаяДата)
	|			ИЛИ ВТ_Правила.Правило.КонецДействия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_НалогиОтчеты.Ссылка КАК Ссылка,
	|	ВТ_НалогиОтчеты.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_СписокНалоговОтчетов
	|ИЗ
	|	ВТ_НалогиОтчеты КАК ВТ_НалогиОтчеты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Акцизы.Ссылка,
	|	ВТ_Акцизы.Идентификатор
	|ИЗ
	|	ВТ_Акцизы КАК ВТ_Акцизы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_МестныеНалоги.Ссылка,
	|	ВТ_МестныеНалоги.Идентификатор
	|ИЗ
	|	ВТ_МестныеНалоги КАК ВТ_МестныеНалоги
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_СписокЗадач.Ссылка,
	|	ВТ_СписокЗадач.Идентификатор
	|ИЗ
	|	ВТ_СписокЗадач КАК ВТ_СписокЗадач
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_СписокНалоговОтчетов.Ссылка,
	|	ВТ_СписокНалоговОтчетов.Идентификатор
	|ИЗ
	|	ВТ_СписокНалоговОтчетов КАК ВТ_СписокНалоговОтчетов";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МассивСтрок = Список.НайтиСтроки(Новый Структура("Идентификатор", Выборка.Идентификатор));
		Если МассивСтрок.Количество() <> 0 Тогда
			МассивСтрок[0].Ссылка                  = Выборка.Ссылка;
			Если НЕ МассивСтрок[0].Включен Тогда
				МассивСтрок[0].Включен = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура ПрочитатьНастройкиОрганизации()
	
	ОткрытьСтраницуДлительнойОперации();

	ОрганизацияПриИзмененииНаСервере();
	
	Если Не ПустаяСтрока(РезультатБлокировки) Тогда
		ПоказатьПредупреждение( , РезультатБлокировки);
		РезультатБлокировки = "";
	Иначе
		Интервал = ?(ЭтоМедленныйРежимРаботы ИЛИ ЭтоВебКлиент, 0.5, 0.1);
		ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьСписокНастройкиНалоговОтчетов", Интервал, Истина);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
	ПолучитьОписаниеСтавокУСНВФоне();
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьНастройкиНаТекущуюДату(ИмяРаздела)
	
	ДатаИзменения = НачалоГода(ТекущаяДата);
	ПараметрыДатыИзменения = ПредставленияДатыИзменения(
		ДатаИзменения,
		ИмяРаздела,
		НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыДатыИзменения);
	
	ПрочитатьОтдельнуюНастройкуУчетнойПолитики(ИмяРаздела);
	УправлениеФормой(ЭтотОбъект, ИмяРаздела);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиАнтикризис()
	
	МенеджерЗаписи = РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	МерыУстойчивогоРазвития = МенеджерЗаписи.МерыУстойчивогоРазвития;
	НеРаботаетВНерабочиеДни = Не МенеджерЗаписи.РаботаетВНерабочиеДни;
	ПеренестиСрокиОтчетовВФонды = МенеджерЗаписи.ПеренестиСрокиОтчетовВФонды;
	ДеятельностьОтнесенаКПострадавшимОтКоронавируса = МенеджерЗаписи.ДеятельностьОтнесенаКПострадавшимОтКоронавируса;
	
	Постановление512 = МенеджерЗаписи.ПрименяетсяПостановление512;
	Постановление776П1 = МенеджерЗаписи.ПрименяетсяПостановление776П1;
	Постановление776П2 = МенеджерЗаписи.ПрименяетсяПостановление776П2;
	
	УправлениеФормой(ЭтотОбъект, "Антикризис");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьГрафикРаботыОрганизации(Организация)
	Возврат УчетЗарплаты.ГрафикРаботыОрганизации(Организация);
КонецФункции

&НаСервереБезКонтекста
Процедура ПрочитатьВариантУчетаКурсовыхРазниц2022(Значение, Знач Организация)
	
	Если УчетДоходовРасходов.УчитыватьОтложенныеКурсовыеРазницы(Организация) Тогда
		Значение = "Независимо";
	Иначе
		Значение = "Совместно";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекущиеНастройкиСистемыНалогообложенияОрганизации(Период, Организация)
	
	СтруктураНастроекСНО = НастройкиУчета.ОписаниеСтруктурыУчетнойПолитики();
	
	НастройкиСНО = НастройкиУчета.УчетнаяПолитикаНалоговыйУчет(Период, Организация);
	
	Если ЗначениеЗаполнено(НастройкиСНО) Тогда
		ЗаполнитьЗначенияСвойств(СтруктураНастроекСНО, НастройкиСНО[0]);
	КонецЕсли; 
	
	Возврат СтруктураНастроекСНО;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПоказыватьПомощникПодбораОпераций0 (Организация)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиУчетаНДССрезПоследних.КлассифицироватьНоменклатуруПоОперациям0 КАК КлассифицироватьНоменклатуруПоОперациям0
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(, Организация = &Организация) КАК НастройкиУчетаНДССрезПоследних
	|ГДЕ
	|	НастройкиУчетаНДССрезПоследних.КлассифицироватьНоменклатуруПоОперациям0";
	
	Запрос.Параметры.Вставить("Организация", Организация);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область РаботаСКоллекциямиДетальныхНастроек

&НаСервере
Процедура ОбработатьРезультатДетальнойНастройки(ИдентификаторСтроки, РезультатДетальнойНастройки)
	
	Строка = СписокПодчиненный.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Строка.Включен Тогда
		Строка.Включен     = Истина;
	КонецЕсли;
	
	ПараметрыДетальнойНастройки = ПолучитьИзВременногоХранилища(АдресПараметрыДетальнойНастройки);
	Если Строка.Ключ = "" Тогда
		ОписаниеПараметров = ПараметрыДетальнойНастройки[Строка.Ссылка];
	Иначе
		ОписаниеПараметров = ПараметрыДетальнойНастройки[Строка.Ключ];
	КонецЕсли;
	
	// Обновим описание порядка уплаты налогов на местах
	Для ИндексПараметра = 0 По ОписаниеПараметров.ВГраница() Цикл
		Если ОписаниеПараметров[ИндексПараметра] = "ПорядокУплатыНалоговНаМестах" Тогда
			НастройкиУплатыНалога = РезультатДетальнойНастройки[ИндексПараметра];
			Если ТипЗнч(НастройкиУплатыНалога) = Тип("Структура") Тогда
				НастройкиУплатыНалога.Вставить("Описание", "");
				РегистрыСведений.ПорядокУплатыНалоговНаМестах.УстановитьОписание(НастройкиУплатыНалога);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Изменим значения во временном хранилище значений.
	ЗначенияДетальнойНастройки = ПолучитьИзВременногоХранилища(АдресЗначенияДетальнойНастройки);
	Если ЗначенияДетальнойНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Строка.Ключ = "" Тогда
		ЗначенияДетальнойНастройки.Вставить(Строка.Ссылка, РезультатДетальнойНастройки);
	Иначе
		ЗначенияДетальнойНастройки.Вставить(Строка.Ключ, РезультатДетальнойНастройки);
	КонецЕсли;
	АдресЗначенияДетальнойНастройки = ПоместитьВоВременноеХранилище(ЗначенияДетальнойНастройки, УникальныйИдентификатор);
	
	// Обновим представление детальной настройки
	Строка.ДетальнаяНастройкаПредставление = РегистрыСведений.НалогиОтчеты.ПредставлениеДетальнойНастройки(
		ОписаниеПараметров,
		РезультатДетальнойНастройки);
		
КонецПроцедуры

#КонецОбласти

#Область СохранениеРезультатовРаботы

&НаКлиенте
Процедура ВопросПередСменойОрганизации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПрочитатьНастройкиОрганизации();
		УстановитьДоступностьСписка();
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
			ИмяРаздела = Элементы.Список.ТекущиеДанные.Идентификатор;
			ЗаписатьИзмененияРазделаНаКлиенте(ИмяРаздела);
		КонецЕсли;
		
		ОповеститьОбИзмененииНастроек(Истина);
		Модифицированность = Ложь;
		ПрочитатьНастройкиОрганизации();
		УстановитьДоступностьСписка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		Закрыть();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
			ИмяРаздела = Элементы.Список.ТекущиеДанные.Идентификатор;
			ЗаписатьИзмененияРазделаНаКлиенте(ИмяРаздела);
		КонецЕсли;
		
		Модифицированность = Ложь;
		ОповеститьОбИзмененииНастроек(Истина);
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзмененияИзВладельца() Экспорт
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		ИмяРаздела = Элементы.Список.ТекущиеДанные.Идентификатор;
		ЗаписатьИзмененияРазделаНаКлиенте(ИмяРаздела);
		ОповеститьОбИзмененииНастроек(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСписокНастройкиНалоговОтчетов(Настройка)
	
	СтруктураНастройки = Новый Структура("Наименование,Включен,
		|Ссылка,ВключаетсяПользователем,Ключ,Идентификатор,Служебный,
		|ЕстьПодстроки,Порядок,БылВключен,Недоступен");
		
	ЗаполнитьЗначенияСвойств(СтруктураНастройки, Настройка);
	
	ВыполнитьЗаписьДанныхНаСервере(СтруктураНастройки);
	
	ОповеститьОбИзмененииНастроек();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеДобровольныеСтраховыеВзносы(Задача)
	
	РезультатДетальнойНастройки = Новый Массив;
	РезультатДетальнойНастройки.Добавить(?(ПеречисляютсяДобровольныеСтраховыеВзносы,1,0));
	
	ЗначенияДетальнойНастройки = ПолучитьИзВременногоХранилища(АдресЗначенияДетальнойНастройки);
	Если ЗначенияДетальнойНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗначенияДетальнойНастройки.Вставить(Задача, РезультатДетальнойНастройки);
	АдресЗначенияДетальнойНастройки = ПоместитьВоВременноеХранилище(ЗначенияДетальнойНастройки, УникальныйИдентификатор);

КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеДополнительныеСтраховыеВзносы()
	
	ОтобразитьРеквизитыЗависимыеОтТекущихНастроек(ЭтотОбъект);
	
	ТребуетсяСоздатьПоставляемыеВидыНалогов = НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудЧленовЛетныхЭкипажей
												Или НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудШахтеров
												Или НастройкиУчетаСтраховыхВзносов.ИспользуютсяРаботыСДосрочнойПенсией
												Или ПеречисляютсяДобровольныеСтраховыеВзносы
												Или НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудИностранцевСОсобымиВзносами;
	
	Если ТребуетсяСоздатьПоставляемыеВидыНалогов Тогда
		Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьПоставляемыеЭлементы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменения(ИмяРегистра)

	ПериодыОтложенногоПроведения = ПроведениеСервер.ПериодыИспользованияОтложенногоПроведения(ГоловнаяОрганизация);
	
	Если ИмяРегистра = "НастройкиСистемыНалогообложения" Тогда
		ИспользуемыеСистемыНалогообложенияПередЗаписью = РегистрыСведений.НастройкиСистемыНалогообложения.ИспользуемыеСистемыНалогообложения();
		НастройкиСистемыНалогообложенияПередЗаписью = ТекущиеНастройкиСистемыНалогообложенияОрганизации(ТекущаяДата, Организация);
	ИначеЕсли ИмяРегистра = "НастройкиУчетаНДС" Тогда
		ИспользуетсяРаздельныйУчетНДСПередЗаписью = РегистрыСведений.НастройкиУчетаНДС.ПолучитьИспользуетсяРаздельныйУчетНДС();
		Если ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
			ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов Тогда
			ДатаНачалаВеденияРаздельногоУчетаНДС = ДатаИзменения;
		КонецЕсли;
	ИначеЕсли НастройкиВзносовИП_ПФР_ФФОМСИзменены Тогда
		МенеджерЗаписи = РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС);
		МенеджерЗаписи.Период = ДатаИзменения;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
	
	Если ИмяРегистра = "СтраховыеВзносы" Тогда
		Если ДатаИзменения = ПрименяемыеТарифыСтраховыхВзносов.Период Тогда
			МенеджерЗаписи = РеквизитФормыВЗначение("ПрименяемыеТарифыСтраховыхВзносов");
			Если НЕ ЗначениеЗаполнено(МенеджерЗаписи.ДатаРегистрацииИзменений) Тогда
				МенеджерЗаписи.ДатаРегистрацииИзменений = МенеджерЗаписи.Период;
			КонецЕсли;
		Иначе
			МенеджерЗаписи = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПрименяемыеТарифыСтраховыхВзносов,,"Период");
			МенеджерЗаписи.Период = ДатаИзменения;
			МенеджерЗаписи.ДатаРегистрацииИзменений = НачалоМесяца(ДатаИзменения);
		КонецЕсли;
		МенеджерЗаписи.Записать(Истина);
		
		Если ДатаИзменения = СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период Тогда
			МенеджерЗаписи = РеквизитФормыВЗначение("СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев");
		Иначе
			МенеджерЗаписи = РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев,,"Период");
			МенеджерЗаписи.Период = ДатаИзменения;
		КонецЕсли;
		МенеджерЗаписи.Записать(Истина);
	ИначеЕсли ИмяРегистра <> "НастройкиУчетаСтраховыхВзносовИП" Или НастройкиВзносовИП_ФССИзменены Тогда
		ПараметрыФункциональнойОпции =
			Новый Структура("Дата, Организация", ЭтотОбъект[ИмяРегистра].Период, ЭтотОбъект[ИмяРегистра].Организация);
		УплачиватьДобровольныеВзносыВФССПередЗаписью = ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта(
			"УплачиватьДобровольныеВзносыВФСС",
			ПараметрыФункциональнойОпции);
		ПлательщикТуристическогоНалогаПередЗаписью = ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта(
			"ПлательщикТуристическогоНалога",
			ПараметрыФункциональнойОпции);
		ИспользуетсяПередачаИнвестиционногоВычетаПередЗаписью = ПолучитьФункциональнуюОпцию("ИспользуетсяПередачаИнвестиционногоВычета");
		ПараметрыВыбора = ПараметрыВыбораПериодаНастройки(
			ДатаИзменения,
			ИмяРегистра,
			НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
		Если ПараметрыВыбора.НачалоПериода = ЭтотОбъект[ИмяРегистра].Период Тогда
			МенеджерЗаписи = РеквизитФормыВЗначение(ИмяРегистра);
		Иначе
			МенеджерЗаписи = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЭтотОбъект[ИмяРегистра],,"Период");
			МенеджерЗаписи.Период = ПараметрыВыбора.НачалоПериода;
		КонецЕсли;
		Если ИмяРегистра = "НастройкиСистемыНалогообложения" Тогда
			МенеджерЗаписи.ДатаИзменения = ДатаИзменения;
		ИначеЕсли ИмяРегистра = "НастройкиУчетаНДС" Или ИмяРегистра = "НастройкиУчетаТуристическогоНалога" Тогда
			МенеджерЗаписи.Организация = ЭтотОбъект.Организация;
		КонецЕсли;
		Если Не ЭтоЮрЛицо Тогда
			// Запоминаем тариф, который установлен в форме настроек
			МенеджерЗаписи.ЗаписьИсторииДанных.ДополнительныеДанные.Вставить(
				"Тариф",
				НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
		КонецЕсли;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если ДатаИзменения > ТекущаяДата Тогда
		Если Не ОрганизацияВелаРаздельныйУчет 
			И (ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
			ИЛИ ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов) Тогда
			ВключилиРУБудущейДатой = Истина;
			БудущаяДатаВключенияРУ = ДатаИзменения;
		Иначе
			ВключилиРУБудущейДатой = Ложь;
		КонецЕсли;
		ПеречитатьНастройки = Не ПрименениеНДСНаУСН И Не ИзменениеСтавкиНДСПриУСН;
	Иначе
		ВключилиРУБудущейДатой = Ложь;
		ПеречитатьНастройки = Истина;
	КонецЕсли;
	
	Если ПеречитатьНастройки Тогда
		ПеречитатьНастройкиНаТекущуюДату(ИмяРегистра);
	КонецЕсли;
	
	Если ИмяРегистра = "НастройкиСистемыНалогообложения" Тогда
		ИспользуемыеСистемыНалогообложенияПослеЗаписи = РегистрыСведений.НастройкиСистемыНалогообложения.ИспользуемыеСистемыНалогообложения();
	ИначеЕсли ИмяРегистра = "НастройкиУчетаНДС" Тогда
		ИспользуетсяРаздельныйУчетНДСПослеЗаписи = РегистрыСведений.НастройкиУчетаНДС.ПолучитьИспользуетсяРаздельныйУчетНДС();
	ИначеЕсли ИмяРегистра = "НастройкиУчетаСтраховыхВзносовИП" Тогда
		УплачиватьДобровольныеВзносыВФССПослеЗаписи = ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта(
			"УплачиватьДобровольныеВзносыВФСС",
			Новый Структура("Дата, Организация", ЭтотОбъект[ИмяРегистра].Период, ЭтотОбъект[ИмяРегистра].Организация));
	ИначеЕсли ИмяРегистра = "НастройкиУчетаТуристическогоНалога" Тогда
		ПлательщикТуристическогоНалогаПослеЗаписи = ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта(
			"ПлательщикТуристическогоНалога",
			Новый Структура("Дата, Организация", ЭтотОбъект[ИмяРегистра].Период, ЭтотОбъект[ИмяРегистра].Организация));
	ИначеЕсли ИмяРегистра = "НастройкиУчетаНалогаНаПрибыль" Тогда
		ИспользуетсяПередачаИнвестиционногоВычетаПослеЗаписи = ПолучитьФункциональнуюОпцию("ИспользуетсяПередачаИнвестиционногоВычета");
	КонецЕсли;

	РезультатЗаданияПереключенияОтложенногоПроведения = ПроведениеСервер.ПроверитьОтложенноеПроведениеПослеИзмененияНастроек(
		ПериодыОтложенногоПроведения,
		УникальныйИдентификатор,
		ГоловнаяОрганизация);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСтатистику(ИмяРегистра)
	
	Если ИмяРегистра = "НастройкиУчетаНалогаНаПрибыль"
		И НастройкиУчетаНалогаНаПрибыль.ПорядокУплатыАвансов = Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.ПоФактическойПрибыли Тогда
		
		РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.ЗаписатьСтатистикуИзмененияПорядкаУплатыАвансов(
			НастройкиУчетаНалогаНаПрибыль.Организация,
			НастройкиУчетаНалогаНаПрибыль.Период);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзмененияРазделаНаКлиенте(ИмяРаздела)

	ЗаписатьИзмененияРазделаНаСервере(ИмяРаздела);
	
	ПроведениеКлиент.ОжидатьПереключенияОтложенногоПроведения(РезультатЗаданияПереключенияОтложенногоПроведения);

КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияРазделаНаСервере(ИмяРаздела)
	
	Если ИмяРаздела = "СистемаНалогообложения" Тогда
		ИмяРегистра = "НастройкиСистемыНалогообложения";
	ИначеЕсли ИмяРаздела = "НалогНаПрибыль" Тогда
		ИмяРегистра = "НастройкиУчетаНалогаНаПрибыль";
	ИначеЕсли ИмяРаздела = "НДФЛ_Предприниматель" Тогда
		ИмяРегистра = "НастройкиУчетаНДФЛ";
	ИначеЕсли ИмяРаздела = "НДС" Тогда
		ИмяРегистра = "НастройкиУчетаНДС";
	ИначеЕсли ИмяРаздела = "УСН" Тогда
		ИмяРегистра = "НастройкиУчетаУСН";
	ИначеЕсли ИмяРаздела = "СтраховыеВзносы_Предприниматель" Тогда
		ИмяРегистра = "НастройкиУчетаСтраховыхВзносовИП";
	ИначеЕсли ИмяРаздела = "СтраховыеВзносы" Тогда
		ИмяРегистра = "СтраховыеВзносы";
	ИначеЕсли ИмяРаздела = "НалогНаИмущество" Тогда
		ИмяРегистра = "СтавкиНалогаНаИмущество";
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗаписатьИзменения(ИмяРегистра);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияНепосредственно(ИмяРегистра)
	
	МенеджерЗаписи = РеквизитФормыВЗначение(ИмяРегистра);
	МенеджерЗаписи.Записать(Истина);
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияАнтикризис()
	
	МенеджерЗаписи = РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать(); // чтобы не затереть настройки, которые здесь не изменяются
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		// Если запись не существует, то поле Организация сброшено
		МенеджерЗаписи.Организация = Организация;
	КонецЕсли;	
	МенеджерЗаписи.МерыУстойчивогоРазвития = МерыУстойчивогоРазвития;
	МенеджерЗаписи.РаботаетВНерабочиеДни = Не НеРаботаетВНерабочиеДни;
	МенеджерЗаписи.ПеренестиСрокиОтчетовВФонды = ПеренестиСрокиОтчетовВФонды;
	МенеджерЗаписи.ДеятельностьОтнесенаКПострадавшимОтКоронавируса = ДеятельностьОтнесенаКПострадавшимОтКоронавируса;
	
	МенеджерЗаписи.ПрименяетсяПостановление512 = Постановление512;
	МенеджерЗаписи.ПрименяетсяПостановление776П1 = Постановление776П1;
	МенеджерЗаписи.ПрименяетсяПостановление776П2 = Постановление776П2;
	
	МенеджерЗаписи.Записать(Истина);
	
	// Параллельная настройка регистра НДФЛ
	УчетнаяПолитикаПоНДФЛ.ИгнорироватьДниС30МартаПо3Апреля = НЕ НеРаботаетВНерабочиеДни;
	УчетнаяПолитикаПоНДФЛ.ИгнорироватьДниС4По30Апреля      = НЕ НеРаботаетВНерабочиеДни;
	УчетнаяПолитикаПоНДФЛ.ИгнорироватьДниС6По8Мая          = НЕ НеРаботаетВНерабочиеДни;
	ЗаписатьИзмененияНепосредственно("УчетнаяПолитикаПоНДФЛ");
	Если ВедетсяУчетЗарплаты И УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		УчетНДФЛ.ПересмотретьСрокиУплаты(УчетнаяПолитикаПоНДФЛ.ГоловнаяОрганизация, '20200325', '20200508');
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьИзмененияДеятельностьОтнесенаКПострадавшимОтКоронавируса()
	
	ЗаписатьИзмененияАнтикризис();
	
	СоставИзмененийВручную = Новый Массив;
	
	НужноМенятьВзносыВручную = Не УчетЗарплаты.УстановитьТарифСтраховыхВзносовДляОтраслейПострадавшихОтКоронавируснойИнфекции(
		Организация,
		ДеятельностьОтнесенаКПострадавшимОтКоронавируса);
		
	Если Не НужноМенятьВзносыВручную Тогда
		ПеречитатьНастройкиНаТекущуюДату("СтраховыеВзносы");
	Иначе
		СоставИзмененийВручную.Добавить(
			ИнструкцияУстановитьВзносДляОтраслейПострадавшихОтКоронавируснойИнфекции(ДеятельностьОтнесенаКПострадавшимОтКоронавируса));
	КонецЕсли;
	
	ПериодОсвобождения = НалоговыйУчет.ПериодОсвобожденияОтНалоговПострадавшимОтКоронавируса();
	ЕстьНачислениеЗарплаты = УчетЗарплаты.ЕстьНачислениеЗарплаты(Организация, ПериодОсвобождения.Начало, ПериодОсвобождения.Конец);
	
	Если ЕстьНачислениеЗарплаты Тогда
		
		Ссылка = СтрШаблон("<a href='e1cib/list/Документ.НачислениеЗарплаты'>%1</a>", Метаданные.Документы.НачислениеЗарплаты.Синоним);
		
		ТекстСтроки = СтрШаблон(
			НСтр("ru = 'Пересчитайте сумму взносов в документах %1 за 2 квартал 2020 года'"),
			Ссылка);
		
		СоставИзмененийВручную.Добавить(СтроковыеФункции.ФорматированнаяСтрока(ТекстСтроки));
		
	КонецЕсли;
	
	НужноПовторноВыполнитьРасчетНалогов = Не УстановитьПовторноВыполнитьРасчетНалогов(Организация);
	Если НужноПовторноВыполнитьРасчетНалогов Тогда
		
		Ссылка = СтрШаблон("<a href='e1cib/command/ОбщаяКоманда.ЗакрытиеМесяца'>%1</a>", Метаданные.Обработки.ЗакрытиеМесяца.Синоним);
		
		ТекстСтроки = СтрШаблон(
			НСтр("ru = 'Повторно рассчитайте сумму налогов за 2 квартал 2020 года - см. %1'"),
			Ссылка);
		
		СоставИзмененийВручную.Добавить(СтроковыеФункции.ФорматированнаяСтрока(ТекстСтроки));
		
	КонецЕсли;
	
	ПатентыПересчитатьВручную = УстановитьОсвобождениеОтНалогаПСН(Организация, ДеятельностьОтнесенаКПострадавшимОтКоронавируса);
	Если ПатентыПересчитатьВручную.Количество() > 0 Тогда
		
		ЧастиОписанияИзменений = Новый Массив;
		
		Если ПатентыПересчитатьВручную.Количество() = 1 Тогда
			НазваниеПатентов = НСтр("ru = 'патенте'");
		Иначе
			НазваниеПатентов = НСтр("ru = 'патентах'");
		КонецЕсли;
		
		ЧастиОписанияИзменений.Добавить(СтрШаблон(НСтр("ru = 'Проверьте расчет платежей по налогу в %1 '"), НазваниеПатентов));
		
		Для Каждого ДанныеПатента Из ПатентыПересчитатьВручную Цикл
			Если ПатентыПересчитатьВручную.Индекс(ДанныеПатента) > 0 Тогда
				ЧастиОписанияИзменений.Добавить(", ");
			КонецЕсли;
			ЧастиОписанияИзменений.Добавить(
				Новый ФорматированнаяСтрока(ДанныеПатента.Наименование, , , , ПолучитьНавигационнуюСсылку(ДанныеПатента.Ссылка)));
		КонецЦикла;
		
		СоставИзмененийВручную.Добавить(Новый ФорматированнаяСтрока(ЧастиОписанияИзменений));
		
	КонецЕсли;
	
	Возврат СоставИзмененийВручную;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИнструкцияУстановитьВзносДляОтраслейПострадавшихОтКоронавируснойИнфекции(Значение)
	
	Если Значение Тогда
		Шаблон = НСтр("ru = 'В настройках <a href=''СтраховыеВзносы''>страховых взносов</a> установите с апреля 2020 года тариф ""%1"", а с июля 2020 года - тариф, который вы применяли ранее'");
	Иначе
		Шаблон = НСтр("ru = 'В настройках <a href=''СтраховыеВзносы''>страховых взносов</a> отключите применение тарифа ""%1""'");
	КонецЕсли;
	// Переход по ссылке обрабатывается в ДеятельностьОтнесенаКПострадавшимОтКоронавирусаДействияПослеВключенияОбработкаНавигационнойСсылки
	НазваниеВзноса = Строка(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(СтрШаблон(Шаблон, НазваниеВзноса));
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьВариантУчетаКурсовыхРазниц2022(Знач Значение, Знач Организация)
	
	Если Значение = "Независимо" Тогда
		ЗначениеДляЗаписи = Истина;
	ИначеЕсли Значение = "Совместно" Тогда
		ЗначениеДляЗаписи = Ложь;
	Иначе
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.НастройкиУчетаНалогаНаПрибыльПостоянные.УстановитьЗначение(
		Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыльПостоянные.Ресурсы.УчитыватьОтложенныеКурсовыеРазницы.Имя,
		ЗначениеДляЗаписи,
		Организация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьОтложитьОтрицательныеКурсовыеРазницы2022(Знач Значение, Знач Организация)
	
	РегистрыСведений.НастройкиУчетаНалогаНаПрибыльПостоянные.УстановитьЗначение(
		Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыльПостоянные.Ресурсы.ОтложитьОтрицательныеКурсовыеРазницы2022.Имя,
		Значение,
		Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеЛьготныхТарифовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ВедетсяУчетЗарплаты И УчетЗарплатыИКадровСредствамиБухгалтерии И ПравоРедактированияНастроек Тогда
		МенеджерЗаписиРегистра("НастройкиУчетаСтраховыхВзносов", , Организация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДлительныеОперации

&НаКлиенте
Процедура ОткрытьСтраницуДлительнойОперации()
	
	Если ЭтоМедленныйРежимРаботы ИЛИ ЭтоВебКлиент Тогда
		Элементы.ОбщиеСтраницыНастроек.ТекущаяСтраница = Элементы.ОбщаяСтраницаОжидание;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПолучениеДанныхНаСервере()
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Организация",   Организация);
	ПараметрыЗаполнения.Вставить("Дерево",        СформироватьДеревоНастроек());
	ПараметрыЗаполнения.Вставить("ТекущийПериод", ТекущаяДата);
	ПараметрыЗаполнения.Вставить("ТолькоОтчеты",  Истина);
	
	НаименованиеЗадания = НСтр("ru = 'Настройка списка налогов и отчетов: получение данных'");
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"РегистрыСведений.НалогиОтчеты.ПодготовитьДанные", 
			ПараметрыЗаполнения, 
			НаименованиеЗадания);
			
		АдресХранилища = Результат.АдресХранилища;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СформироватьДеревоНастроек()
	
	МассивТиповЗадач = Новый Массив;
	МассивТиповЗадач.Добавить(Тип("ПеречислениеСсылка.ВидыПодакцизныхТоваров"));
	МассивТиповЗадач.Добавить(Тип("СправочникСсылка.ЗадачиБухгалтера"));
	МассивТиповЗадач.Добавить(Тип("СправочникСсылка.РегистрацииВНалоговомОргане"));
	МассивТиповЗадач.Добавить(Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов"));
	
	МассивТиповРегистраций = Новый Массив;
	МассивТиповРегистраций.Добавить(Тип("СправочникСсылка.РегистрацииВНалоговомОргане"));

	
	ДеревоЗадач = Новый ДеревоЗначений;
	ДеревоЗадач.Колонки.Добавить("Наименование",                    ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ДеревоЗадач.Колонки.Добавить("Включен",                         Новый ОписаниеТипов("Булево")); // задача или правило применяется
	ДеревоЗадач.Колонки.Добавить("ПолученИзВнешнегоСервиса",        Новый ОписаниеТипов("Булево")); // требуется по данным внешнего сервиса
	ДеревоЗадач.Колонки.Добавить("ОтключенПользователем",           Новый ОписаниеТипов("Булево")); // принудительно отключен пользователем
	ДеревоЗадач.Колонки.Добавить("ДобавленПользователем",           Новый ОписаниеТипов("Булево")); // расчетный показатель для отображения на форме
	ДеревоЗадач.Колонки.Добавить("ДетальнаяНастройкаПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ДеревоЗадач.Колонки.Добавить("Ссылка",                          Новый ОписаниеТипов(Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОписаниеТипаУсловияПримененияТребованияЗаконодательства(), МассивТиповЗадач));
	ДеревоЗадач.Колонки.Добавить("РегистрацияВНалоговомОргане",     Новый ОписаниеТипов(МассивТиповРегистраций));
	ДеревоЗадач.Колонки.Добавить("ВключаетсяПользователем",         Новый ОписаниеТипов("Булево"));
	ДеревоЗадач.Колонки.Добавить("ЕстьДетальнаяНастройка",          Новый ОписаниеТипов("Булево"));
	ДеревоЗадач.Колонки.Добавить("НалогУплачиваетсяНаМестах",       Новый ОписаниеТипов("Булево"));
	ДеревоЗадач.Колонки.Добавить("Ключ",                            ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ДеревоЗадач.Колонки.Добавить("Идентификатор",                   ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ДеревоЗадач.Колонки.Добавить("Порядок",                         ОбщегоНазначения.ОписаниеТипаЧисло(5));
	
	Возврат ДеревоЗадач;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияПриОткрытии()
	
	Попытка
		Если НЕ ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
			Возврат;
		КонецЕсли;
		Если ТипЗнч(ФормаДлительнойОперации) = Тип("ФормаКлиентскогоПриложения") Тогда
			Если ФормаДлительнойОперации.Открыта() 
				И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
				Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
					ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
					ЗагрузитьРезультат();
				Иначе
					ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
					ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗаданияПриОткрытии", 
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
					Истина);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				ЗагрузитьРезультат();
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗаданияПриОткрытии", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияПриЗаписи()
	
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				Модифицированность = Ложь;
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗаданияПриЗаписи", 
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
					Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗагрузитьРезультат(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьРезультат();
	
	ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
	Элементы.ОбщиеСтраницыНастроек.ТекущаяСтраница = Элементы.ОбщаяСтраницаНастройки;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультат()
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ЗагрузитьПодчиненныеСтроки(Результат.Дерево);
		
		АдресПараметрыДетальнойНастройки = ПоместитьВоВременноеХранилище(Результат.ПараметрыДетальнойНастройки, УникальныйИдентификатор);
		АдресЗначенияДетальнойНастройки  = ПоместитьВоВременноеХранилище(Результат.ЗначенияДетальнойНастройки,  УникальныйИдентификатор);
		
		УстановитьДоступностьВидимостьНастроек(Истина);
		ПрочитатьДетальнуюНастройкуСтраховыхВзносов();
		НастроитьЭлементыСостоянияЗагрузкиСтатистическихОтчетов(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодчиненныеСтроки(Дерево)
	
	СписокПодчиненный.Очистить();
	
	СкрытыеОтчеты        = 0;
	
	Для Каждого Строка ИЗ Дерево.Строки Цикл
		Родитель             = Строка.Ссылка;
		ПодчиненныеСтроки    = Строка.Строки;
		Идентификатор        = Строка.Идентификатор;
		ЕстьПодстроки        = Ложь;
		Для Каждого ПодчиненнаяСтрока ИЗ ПодчиненныеСтроки Цикл
			НоваяСтрока = СписокПодчиненный.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПодчиненнаяСтрока);
			НоваяСтрока.Родитель      = Родитель;
			НоваяСтрока.Идентификатор = Идентификатор;
			НоваяСтрока.ДобавленПользователем = НоваяСтрока.Включен И Не НоваяСтрока.ПолученИзВнешнегоСервиса;
			ЕстьПодстроки             = Истина;
			Если НЕ ПодчиненнаяСтрока.Включен Тогда
				СкрытыеОтчеты = СкрытыеОтчеты + 1;
			КонецЕсли;
		КонецЦикла;
		
		МассивСтрок = Список.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор));
		Если МассивСтрок.Количество() <> 0 Тогда
			МассивСтрок[0].ЕстьПодстроки           = ЕстьПодстроки;
			МассивСтрок[0].Ссылка                  = Строка.Ссылка;
			МассивСтрок[0].Ключ                    = Строка.Ключ;
			МассивСтрок[0].ВключаетсяПользователем = Строка.ВключаетсяПользователем;
			Если НЕ МассивСтрок[0].Включен Тогда
				МассивСтрок[0].Включен = Строка.Включен;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ПолныйСписок = СкрытыеОтчеты = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПостроениеСписка()
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		
		МассивИдентификаторов = Новый Массив();
		МассивИдентификаторов.Добавить(ИдентификаторЗадания);
		ОтменитьВыполнениеЗаданий(МассивИдентификаторов);
		
	КонецЕсли;

	ИдентификаторЗадания = Неопределено;
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗаданий(Знач МассивИдентификаторовЗаданий)

	Для Каждого ИдентификаторЗадания Из МассивИдентификаторовЗаданий Цикл
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьОбновлениеТарифицируемыхУслуг()
	
	Если ТарификацияБПВызовСервера.ТарифицируемыеОпцииИзменены(АдресЗначенийТарифицируемыхОпций) Тогда
		Элементы.ГруппаОжиданиеОплатыТарифа.Видимость = Ложь;
		ОбновитьИнтерфейс();
	Иначе
		УвеличениеИнтервала = 30;
		ИнтервалПроверкиОплатыТарифа = 
			?(ЗначениеЗаполнено(ИнтервалПроверкиОплатыТарифа),
			ИнтервалПроверкиОплатыТарифа + УвеличениеИнтервала,
			60);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПроверитьОбновлениеТарифицируемыхУслуг", 
			ИнтервалПроверкиОплатыТарифа,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтрисовкаФормы

&НаСервере
Процедура УстановитьДоступностьВидимостьНастроек(ДоступностьВидимость)
	
	Если ОрганизацияНедоступна Тогда
		Элементы.ОбщиеСтраницыНастроек.ТекущаяСтраница = Элементы.ОбщаяСтраницаНедоступная;
	Иначе
		Если ДоступностьВидимость Тогда
			Элементы.ОбщиеСтраницыНастроек.ТекущаяСтраница = Элементы.ОбщаяСтраницаНастройки;
		Иначе
			Элементы.ОбщиеСтраницыНастроек.ТекущаяСтраница = Элементы.ОбщаяСтраницаПустая;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтавкаНалогообложенияПриУСН(Форма)
	
	Если ЗначениеЗаполнено(Форма.НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН) Тогда
		Возврат Форма.НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН;
	КонецЕсли;
	
	// Получим значение ставки НДС в следующем году
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Форма.Организация);
	Запрос.УстановитьПараметр("КонецГода", КонецГода(Форма.ТекущаяДата));
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН КАК СтавкаНалогообложенияПриУСН
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|ГДЕ
	|	НастройкиУчетаНДС.Период > &КонецГода
	|	И НастройкиУчетаНДС.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	НастройкиУчетаНДС.Период";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтавкаНалогообложенияПриУСН;
	КонецЕсли;

	Возврат Перечисления.ВидыСтавокНДС.ПустаяСсылка();
	
КонецФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЭтотОбъект.ТолькоПросмотр = ГоловнаяОрганизация <> Организация;
	
	ПрочитатьИОписатьНастройкиУчета();
	
	НастройкиСистемыНалогообложенияФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	
	Если Не ЭтоЮрЛицо Тогда
		Если НастройкиСистемыНалогообложения.ПлательщикНДФЛ
			Или СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНДФЛ") <> Неопределено Тогда
			НастройкиУчетаНДФЛФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
		КонецЕсли;
		НастройкиВзносовИП_ПФР_ФФОМСИзменены = Ложь;
		НастройкиВзносовИП_ФССИзменены = Ложь;
	КонецЕсли;
	
	Если НастройкиСистемыНалогообложения.ПлательщикНДС
		Или НастройкиСистемыНалогообложения.ПлательщикНДСПрименяющийУСН
		Или СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНДС") <> Неопределено
		Или СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНДСПрименяющийУСН") <> Неопределено Тогда
		ЕстьВводОстатковПоРУ = ПроверитьНаличиеДокументовВводаОстатковПоРУ();
		НастройкиУчетаНДСФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
		ЭтотОбъект.ОрганизацияВелаРаздельныйУчет = ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19 
			Или ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года
			Или ЭтотОбъект.НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов;
		ЭтотОбъект.ПлательщикНДС = ЭтотОбъект.НастройкиСистемыНалогообложения.ПлательщикНДС;
		СтавкаНалогообложенияПриУСН = СтавкаНалогообложенияПриУСН(ЭтотОбъект);
		ЭтотОбъект.СтавкаНДСПриУСН = НастройкиСистемыНалогообложенияФормы.ИндексСтавкиНДСПоЗначению(СтавкаНалогообложенияПриУСН);
	КонецЕсли;
	
	Если ЭтоЮрЛицо Тогда
		Если НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль
			Или СписокНедействующихНастроек.НайтиПоЗначению("ПлательщикНалогаНаПрибыль") <> Неопределено Тогда
			НастройкиУчетаНалогаНаПрибыльФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
	Если ((НастройкиСистемыНалогообложения.ПрименяетсяУСН Или НастройкиСистемыНалогообложения.ПрименяетсяАУСН)
		Или (СписокНедействующихНастроек.НайтиПоЗначению("ПрименяетсяУСН") <> Неопределено
			Или СписокНедействующихНастроек.НайтиПоЗначению("ПрименяетсяАУСН") <> Неопределено))
			И НастройкиУчетаУСН.Организация = Организация Тогда
		НастройкиУчетаУСНФормы.ПодготовитьФормуНаСервере(ЭтотОбъект);
	КонецЕсли;
	
	Если ЭтоЮрЛицо Тогда
		СтавкиНалогаНаИмуществоФормы.ПодготовитьФормуНаСервере(ЭтотОбъект, "СтавкиНалогаНаИмущество");
	КонецЕсли;
	
	// Раздел ЕНВД может быть доступен, даже если сейчас ЕНВД уже не применяется.
	// В этом случае показывать настройку сроков не имеет смысла.
	Элементы.ГруппаПереносСроковЕНВД.Видимость = НастройкиСистемыНалогообложения.ПлательщикЕНВД И ВозможноПродлениеСроковНалоговОтчетов;
	
	Если Элементы.ГруппаПереносСроковЕНВД.Видимость Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиУчетаВызовСервера.СрокиУплатыЕНВД(Организация));
		Элементы.УведомлениеОПродленииСроковЕНВД.Заголовок = НастройкиУчетаВызовСервера.ТекстУведомленияОПродленииСроковЕНВД(
			Организация, 
			ТекущаяДата);
	КонецЕсли;	
	
	НастроитьБаннерыИзмененияПоНалогам();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПредставленияДатыИзменения(ДатаИзменения, "НастройкиСистемыНалогообложения"));
	
	СброситьСтраницыИсторииИзменений();
	ОтобразитьРеквизитыЗависимыеОтСистемыНалогообложения();
	ОтобразитьРеквизитыЗависимыеОтПользовательскихНастроек();
	ОтобразитьРеквизитыЗависимыеОтТекущихНастроек(ЭтотОбъект);
	УстановитьДоступностьСтарыхНастроек();
	
	ИспользоватьСервисРегиональныеСтавкиНалогов =
		РегиональныеСтавкиНалоговВызовСервера.ИспользуетсяСервисРегиональныхСтавок();
	
	НастроитьПодразделАнтикризисРегиональныеНПА();
	
	НастроитьУчетКурсовыхРазниц2022();
	ПрочитатьВариантУчетаКурсовыхРазниц2022(ВариантУчетаКурсовыхРазниц2022, Организация);
	ОтложитьОтрицательныеКурсовыеРазницы2022 = УчетДоходовРасходов.ОтложитьОтрицательныеКурсовыеРазницы2022(Организация);
	
	ИмпортПодакцизныхТоваров       = ПолучитьФункциональнуюОпцию("ИмпортПодакцизныхТоваров");
	ПроизводствоПодакцизныхТоваров = ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров");
	
	Элементы.ТаблицаНастроекПоАкцизамСпособПолученияТоваров.Видимость = ИмпортПодакцизныхТоваров Или ПроизводствоПодакцизныхТоваров;
	
	ПараметрыПериодаСистемыНалогообложения =
		НастройкиСистемыНалогообложенияФормыВызовСервера.ПараметрыПериодаСистемыНалогообложения(
			Организация, НастройкиСистемыНалогообложения.СистемаНалогообложения);
	
	ПодготовитьРеквизитыФормыДляИП();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДетальнуюНастройкуСтраховыхВзносов()
	
	Условие = Неопределено;
	Задача  = Неопределено;
	
	МассивСтрок = Список.НайтиСтроки(Новый Структура("Идентификатор", "СтраховыеВзносы"));
	Если МассивСтрок.Количество() <> 0 Тогда
		Задача = МассивСтрок[0].Ссылка;
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗначенияДетальнойНастройки  = ПолучитьИзВременногоХранилища(АдресЗначенияДетальнойНастройки);
	ПараметрыДетальнойНастройки = ПолучитьИзВременногоХранилища(АдресПараметрыДетальнойНастройки);
	ПараметрыНастройки          = ПараметрыДетальнойНастройки[Задача];
	Если ПараметрыНастройки <> Неопределено Тогда
		ЗначенияНастройки = ЗначенияДетальнойНастройки[Задача];
		Для Индекс = 0 По ПараметрыНастройки.ВГраница() Цикл
			Параметр          = ПараметрыНастройки[Индекс];
			// ВариантыПримененияТребованийЗаконодательства
			ЗначениеПараметра = ЗначенияНастройки[Индекс]; // Булево или индекс
			СпособОписанияВариантов = ТипЗнч(Параметр.Варианты);
			Если СпособОписанияВариантов = Тип("ТаблицаЗначений") Тогда
				Условие = Параметр.Варианты[ЗначениеПараметра].Условие;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПеречисляютсяДобровольныеСтраховыеВзносы = ЗначениеЗаполнено(Условие);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма, ИмяРаздела = Неопределено)
	
	Элементы = Форма.Элементы;
	
	Если ИмяРаздела = Неопределено Или ИмяРаздела = "НастройкиСистемыНалогообложения" Тогда
		НастройкиСистемыНалогообложенияФормыКлиентСервер.УправлениеФормой(Форма);
		НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(Форма);
		НастройкиСистемыНалогообложения = Форма.НастройкиСистемыНалогообложения;
		ИспользоватьДатуИзменения = Форма.ПараметрыПериодаСистемыНалогообложения.ИспользуетсяДатаИзменения;
		ПереходСНачалаГода = Форма.ПараметрыПериодаСистемыНалогообложения.ПереходСНачалаГода;
		Элементы.ДатаИзмененияЧислом_НастройкиСистемыНалогообложения.Видимость = ПереходСНачалаГода;
		Элементы.ДатаИзменения_НастройкиСистемыНалогообложения.Видимость = ИспользоватьДатуИзменения И Не ПереходСНачалаГода;
		Элементы.ДатаИзмененияСтрокой_НастройкиСистемыНалогообложения.Видимость =
			Не ИспользоватьДатуИзменения И Не ПереходСНачалаГода;
			
		Если Не Форма.ЭтоЮрЛицо И Не Форма.ЗарегистрированВКачествеИП Тогда
			Элементы.ГруппаСистемаНалогообложенияСамозанятых.Видимость = Форма.СистемаНалогообложенияПредставление <> 0;
		Иначе
			Элементы.ГруппаСистемаНалогообложенияСамозанятых.Видимость = Ложь;
		КонецЕсли;
		НастройкаПодсказкиПомощникПоПереходуНаУСН(Форма);
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "НастройкиУчетаНалогаНаПрибыль" Тогда
		НастройкиУчетаНалогаНаПрибыльФормыКлиентСервер.УправлениеФормой(Форма);
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "НастройкиУчетаНДФЛ" Тогда
		НастройкиУчетаНДФЛФормыКлиентСервер.УправлениеФормой(Форма);
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "НастройкиУчетаУСН" Тогда
		НастройкиУчетаУСНФормыКлиентСервер.УправлениеФормой(Форма);
		Если ИмяРаздела = "НастройкиУчетаУСН" Тогда
			Форма.ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
				Форма.ДатаИзменения, КонецКвартала(Форма.ДатаИзменения), Истина);
		КонецЕсли;
		НастройкаПодсказкиИзмененияВСистемеНалогообложения(Форма);
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "СтавкиНалогаНаИмущество" Тогда
		СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
		Элементы.ГруппаБаннер_НалогНаИмущество.Видимость = ВидимостьБаннераИзмененияВУплатеНалога("НалогНаИмущество", Форма.Организация);
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "СтраховыеВзносы" Тогда
		Элементы.СтраницыСтраховыеВзносыИстория.Видимость                            = Форма.УчетЗарплатыИКадровСредствамиБухгалтерии;
		Элементы.Группа_ПрименяемыеТарифыСтраховыхВзносов_НастройкиУчета.Видимость   = Форма.УчетЗарплатыИКадровСредствамиБухгалтерии;
		Элементы.ГруппаСтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Видимость        = Форма.УчетЗарплатыИКадровСредствамиБухгалтерии И Не Форма.НастройкиСистемыНалогообложения.ПрименяетсяАУСН;
		ОбновитьНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев(Форма, Ложь);
		Элементы.ГруппаФиксированныеВзносыНаСтрахованиеОтНесчастныхСлучаев.Видимость = Форма.УчетЗарплатыИКадровСредствамиБухгалтерии И Форма.НастройкиСистемыНалогообложения.ПрименяетсяАУСН;
		Элементы.ГруппаНастройкиУчетаСтраховыхВзносов.Видимость                      = Форма.УчетЗарплатыИКадровСредствамиБухгалтерии И Не Форма.НастройкиСистемыНалогообложения.ПрименяетсяАУСН;
		Элементы.ГруппаУчетЗарплатыВоВнешнейПрограмме.Видимость                      = НЕ Форма.УчетЗарплатыИКадровСредствамиБухгалтерии;
		Элементы.ВидТарифа.ТолькоПросмотр                                            = Форма.НастройкиСистемыНалогообложения.ПрименяетсяАУСН;
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "Антикризис" Тогда
		Элементы.ГрафикРаботыДекорация.Видимость = Форма.УчетЗарплатыИКадровСредствамиБухгалтерии;
		
		НастроитьОсвобождениеНалогов(
			Элементы.ОсвобождениеОтУплатыНалогов,
			Элементы.ДекорацияОсвобождениеОтУплатыНалогов,
			Элементы.ДеятельностьОтнесенаКПострадавшимОтКоронавирусаДействияПослеВключения,
			Элементы.ПроверьтеРеестрМСП,
			Форма.ЭтоЮрЛицо);
			
		Элементы.ГруппаПостановление512.Видимость = Форма.НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы Или Форма.НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходыМинусРасходы;
		
		НастройкиПродленияСроков = НовыйНастройкиПродленияСроков();
		ЗаполнитьЗначенияСвойств(НастройкиПродленияСроков, Форма);
		НастроитьПодсказкиАнтикризис(
			Элементы.ДекорацияПостановление512Подсказка,
			Элементы.ДекорацияПостановление512Пояснения,
			Элементы.ДекорацияПостановление776Подсказка,
			Элементы.ДекорацияПостановление776Пояснения,
			НастройкиПродленияСроков);
			
		НастроитьВидимостьПодсказокАнтикризис(Форма.Организация, Элементы.ГруппаПостановление512Подсказка, Элементы.ГруппаПостановление776Подсказка);
			
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено ИЛИ ИмяРаздела = "ЕНВД" Тогда
		Элементы.ГруппаПереносСроковЕНВД.Видимость = Форма.НастройкиСистемыНалогообложения.ПлательщикЕНВД И Форма.ВозможноПродлениеСроковНалоговОтчетов;
		Если Элементы.ГруппаПереносСроковЕНВД.Видимость Тогда 
			ЗаполнитьЗначенияСвойств(Форма, НастройкиУчетаВызовСервера.СрокиУплатыЕНВД(Форма.Организация));
			Элементы.УведомлениеОПродленииСроковЕНВД.Заголовок = НастройкиУчетаВызовСервера.ТекстУведомленияОПродленииСроковЕНВД(
				Форма.Организация,
				Форма.ТекущаяДата);
		КонецЕсли;	
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено Или ИмяРаздела = "НастройкиУчетаСтраховыхВзносовИП" Тогда
		ИспользоватьДатуИзменения = Форма.НастройкиВзносовИП_ПФР_ФФОМСИзменены;
		Элементы.ДатаИзменения_НастройкиУчетаСтраховыхВзносовИП.Видимость = ИспользоватьДатуИзменения;
		Элементы.ДатаИзмененияЧислом_НастройкиУчетаСтраховыхВзносовИП.Видимость = Не ИспользоватьДатуИзменения;
		Элементы.УплачиватьСтраховыеВзносыПФР_ФФОМС.Видимость = Не Форма.НастройкиСистемыНалогообложения.ПрименяетсяАУСН;
		
		Если Форма.НастройкиСистемыНалогообложения.ПрименяетсяНалогНаПрофессиональныйДоход Тогда
			Элементы.УплачиватьСтраховыеВзносыПФР_ФФОМС.Заголовок = НСтр("ru = 'На пенсионное страхование'");
		Иначе
			Элементы.УплачиватьСтраховыеВзносыПФР_ФФОМС.Заголовок = НСтр("ru = 'Фиксированные взносы по тарифу:'");
		КонецЕсли;
		
		Элементы.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМСТариф.Видимость =
			Форма.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.ПлательщикСтраховыхВзносовПФР_ФФОМС
			И Не Форма.ПрименяетсяНПДНаМоментОткрытия;
		
		Если УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(Форма.ДатаИзменения) Тогда
			ЭтоСтраховойСлучайНаУплатуБольничного = Не Форма.ЭтоИП Или
				УчетСтраховыхВзносовИПКлиентСервер.ЭтоСтраховойСлучайНаУплатуБольничного(
					Форма.НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай);
			
			Элементы.НастройкиУчетаСтраховыхВзносовИПСтраховаяСумма.Видимость = ЭтоСтраховойСлучайНаУплатуБольничного;
			Элементы.ГруппаРайонныйКоэффициентИП.Видимость = Форма.ЭтоИП И Не ЭтоСтраховойСлучайНаУплатуБольничного;
			Элементы.СтраховойСлучайВСвязиСМатеринством.Видимость = Форма.ЭтоИП И Форма.ПрименяетсяНПДНаМоментОткрытия;
			Элементы.СтраховойСлучайВСвязиСМатеринством.Доступность = Форма.НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС;
			Элементы.НастройкиУчетаСтраховыхВзносовИПСтраховаяСумма.Доступность = Форма.НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС;
		Иначе
			Элементы.НастройкиУчетаСтраховыхВзносовИПСтраховаяСумма.Видимость = Ложь;
			Элементы.ГруппаРайонныйКоэффициентИП.Видимость = Истина;
			Элементы.СтраховойСлучайВСвязиСМатеринством.Видимость = Ложь;
		КонецЕсли;
		
		Элементы.РайонныйКоэффициентИП.Доступность = Форма.НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС;
		
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено Или ИмяРаздела = "ТранспортныйНалог" Тогда
		Элементы.ГруппаБаннер_ТранспортныйНалог.Видимость = ВидимостьБаннераИзмененияВУплатеНалога("ТранспортныйНалог", Форма.Организация);
	КонецЕсли;
	
	Если ИмяРаздела = Неопределено Или ИмяРаздела = "ЗемельныйНалог" Тогда
		Элементы.ГруппаБаннер_ЗемельныйНалог.Видимость = ВидимостьБаннераИзмененияВУплатеНалога("ЗемельныйНалог", Форма.Организация);
	КонецЕсли;
	
	Если ИмяРаздела = "НастройкиУчетаТуристическогоНалога" Тогда
		ДатаИзмененияТуристическогоНалога = Макс(Форма.ДатаИзменения, '20250101');
		Форма.ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			ДатаИзмененияТуристическогоНалога, КонецКвартала(ДатаИзмененияТуристическогоНалога), Истина);
		Элементы.ГруппаБаннер_ТуристическийНалог.Видимость = 
			ВидимостьБаннераИзмененияВУплатеНалога("ТуристическийНалог", Форма.Организация);
	КонецЕсли;
	
	Элементы.ГруппаДеятельностьНДС0.ТолькоПросмотр = Форма.ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьПериоды(ИмяРаздела)
	
	Элементы["Страницы" + ИмяРаздела + "История"].ТекущаяСтраница = Элементы["Страница" + ИмяРаздела + "Изменение"];
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы["Страница" + ИмяРаздела + "Изменение"].Видимость = Истина;
	КонецЕсли;
	
	УстановитьДоступностьСписка();
	
	Если ИмяРаздела = "НастройкиСистемыНалогообложения" Тогда
		Если ПараметрыПериодаСистемыНалогообложения.ПереходСНачалаГода Тогда
			ДатаИзменения = НачалоГода(ДатаИзменения);
		ИначеЕсли Не ПараметрыПериодаСистемыНалогообложения.ИспользуетсяДатаИзменения Тогда
			ДатаИзменения = НачалоМесяца(ДатаИзменения);
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект, ИмяРаздела);
	
	ИмяЭлемента = СтрШаблон("ДатаИзмененияЧислом_%1", ИмяРаздела);
	Если Элементы.Найти(ИмяЭлемента) <> Неопределено И Элементы[ИмяЭлемента].Видимость Тогда
		ДатаИзменения = Дата(ДатаИзмененияЧислом, 1, 1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьИсторию(ИмяРаздела)
	
	Элементы["Страницы" + ИмяРаздела + "История"].ТекущаяСтраница = Элементы["Страница" + ИмяРаздела + "Периоды"];
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы["Страница" + ИмяРаздела + "Изменение"].Видимость = Ложь;
	КонецЕсли;
	
	Модифицированность = Ложь;
	УстановитьДоступностьСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИзмененияИстории(Команда)
	
	ИзменениеДеятельностиНДС0 = Ложь;
	КлассифицироватьНоменклатуруПоОперациям0 = УчетНДСВызовСервера.КлассифицироватьНоменклатуруПоОперациям0(,Организация);
	
	Если СтрНайти(ТекущийЭлемент.Имя, "Отменить_") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРаздела = ИмяРегистра(ТекущийЭлемент.Имя);
	Элементы["Страницы" + ИмяРаздела + "История"].ТекущаяСтраница = Элементы["Страница" + ИмяРаздела + "Периоды"];
	
	Модифицированность = Ложь;
	УстановитьДоступностьСписка();
	
	ПеречитатьНастройкиНаТекущуюДату(ИмяРаздела);
	
	Элементы.БаннерВключениеРУ.Видимость = Ложь;
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы["Страница" + ИмяРаздела + "Изменение"].Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьСписка()
	
	СписокНедоступность         = Модифицированность;
	Элементы.Список.Доступность = НЕ СписокНедоступность;
	
КонецПроцедуры

&НаСервере
Процедура СброситьСтраницыИсторииИзменений()
	
	Элементы.СтраницыНастройкиСистемыНалогообложенияИстория.ТекущаяСтраница =
		Элементы.СтраницаНастройкиСистемыНалогообложенияПериоды;
	Элементы.СтраницыНастройкиУчетаНалогаНаПрибыльИстория.ТекущаяСтраница =
		Элементы.СтраницаНастройкиУчетаНалогаНаПрибыльПериоды;
	Элементы.СтраницыНастройкиУчетаНДФЛИстория.ТекущаяСтраница = Элементы.СтраницаНастройкиУчетаНДФЛПериоды;
	Элементы.СтраницыНастройкиУчетаНДСИстория.ТекущаяСтраница = Элементы.СтраницаНастройкиУчетаНДСПериоды;
	Элементы.СтраницыНастройкиУчетаУСНИстория.ТекущаяСтраница = Элементы.СтраницаНастройкиУчетаУСНПериоды;
	Элементы.СтраницыНастройкиУчетаСтраховыхВзносовИПИстория.ТекущаяСтраница =
		Элементы.СтраницаНастройкиУчетаСтраховыхВзносовИППериоды;
	Элементы.СтраницыСтраховыеВзносыИстория.ТекущаяСтраница = Элементы.СтраницаСтраховыеВзносыПериоды;
	Элементы.СтраницыСтавкиНалогаНаИмуществоИстория.ТекущаяСтраница = Элементы.СтраницаСтавкиНалогаНаИмуществоПериоды;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРеквизитыЗависимыеОтСистемыНалогообложения()
	
	Элементы.СтраницыСтраховыеВзносыИстория.Видимость                          = УчетЗарплатыИКадровСредствамиБухгалтерии;
	Элементы.Группа_ПрименяемыеТарифыСтраховыхВзносов_НастройкиУчета.Видимость = УчетЗарплатыИКадровСредствамиБухгалтерии;
	Элементы.ГруппаНастройкиУчетаСтраховыхВзносов.Видимость                    = УчетЗарплатыИКадровСредствамиБухгалтерии;
	Элементы.ГруппаУчетЗарплатыВоВнешнейПрограмме.Видимость                    = НЕ УчетЗарплатыИКадровСредствамиБухгалтерии;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРеквизитыЗависимыеОтПользовательскихНастроек()
	
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	
	Элементы.НалогНаИмущество_СпособыОтраженияРасходов.Видимость  = ПользовательУправляетСчетамиУчета;
	Элементы.ТранспортныйНалог_СпособыОтраженияРасходов.Видимость = ПользовательУправляетСчетамиУчета;
	Элементы.ЗемельныйНалог_СпособыОтраженияРасходов.Видимость    = ПользовательУправляетСчетамиУчета;
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ГоловнаяОрганизация");
	ЕстьОбособленныеПодразделения = НалоговыйУчет.ЕстьОбособленныеПодразделения(ГоловнаяОрганизация);
	Элементы.ГруппаПорядокПодачиДекларации.Видимость = ЕстьОбособленныеПодразделения;
	Элементы.ГруппаОпределениеДолиПрибыли.Видимость  = ЕстьОбособленныеПодразделения;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьРеквизитыЗависимыеОтТекущихНастроек(Форма)
	
	Элементы = Форма.Элементы;
	
	НастройкиУчетаСтраховыхВзносов = Форма.НастройкиУчетаСтраховыхВзносов;
	
	Элементы.ГиперссылкаДолжности.Доступность = НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудФармацевтов
		ИЛИ НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудЧленовЛетныхЭкипажей
		ИЛИ НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудШахтеров;
		
	Элементы.ПрименяютсяРезультатыСпециальнойОценкиУсловийТруда.Доступность
		= Форма.НастройкиУчетаСтраховыхВзносов.ИспользуютсяРаботыСДосрочнойПенсией;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьСтарыхНастроек()
	
	Элементы.ГруппаНастройкиУчетаНалогаНаПрибыль.ТолькоПросмотр = Ложь;
	Элементы.ГруппаНастройкиУчетаУСН.ТолькоПросмотр             = Ложь;
	Элементы.ГруппаНастройкиУчетаНДС.ТолькоПросмотр             = Ложь;
	Элементы.ГруппаНастройкиУчетаНДФЛ.ТолькоПросмотр            = Ложь;
	Элементы.ГруппаСтавкиНалогаНаИмущество.ТолькоПросмотр       = Ложь;
	Элементы.ГруппаНастройкиУчетаСтраховыхВзносовИП.ТолькоПросмотр       = Ложь;
	
	Для Каждого ЗначениеНастройки ИЗ Список Цикл
		Если ЗначениеНастройки.БылВключен Тогда
			
			Идентификатор = ЗначениеНастройки.Идентификатор;
			Если Идентификатор = "НалогНаПрибыль" Тогда
				Элементы.ГруппаНастройкиУчетаНалогаНаПрибыль.ТолькоПросмотр = Истина;
			КонецЕсли;
			Если Идентификатор = "УСН" Тогда
				Элементы.ГруппаНастройкиУчетаУСН.ТолькоПросмотр = Истина;
			КонецЕсли;
			Если Идентификатор = "НДС" Тогда
				Элементы.ГруппаНастройкиУчетаНДС.ТолькоПросмотр = Истина;
			КонецЕсли;
			Если Идентификатор = "НДФЛ_Предприниматель" Тогда
				Элементы.ГруппаНастройкиУчетаНДФЛ.ТолькоПросмотр = Истина;
			КонецЕсли;
			Если Идентификатор = "НалогНаИмущество" Тогда
				Элементы.ГруппаСтавкиНалогаНаИмущество.ТолькоПросмотр = Истина;
			КонецЕсли;
			Если Идентификатор = "СтраховыеВзносы_Предприниматель" Тогда
				Элементы.ГруппаНастройкиУчетаСтраховыхВзносовИП.ТолькоПросмотр = Истина;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОписаниеСтавокУСНВФоне()
	
	ПоказыватьДанные = ИспользоватьСервисРегиональныеСтавкиНалогов
		И (ПрименяетсяУСНДоходы ИЛИ ПрименяетсяУСНДоходыМинусРасходы);
		
	Если Не ПоказыватьДанные Тогда
		Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок =
			РегиональныеСтавкиНалоговКлиент.ТекстПодсказкиИнтернетПоддержкаНеПодключена();
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодРегиона) Тогда
		Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = НСтр("ru='Не заполнен регион регистрации организации'");
		Возврат;
	КонецЕсли;
	
	ПараметрыМетода = РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаОписаниеСтавокУСН();
	ПараметрыМетода.КодРегиона = КодРегиона;
	ПараметрыМетода.КодВидаДеятельности = КодОКВЭД2;
	ПараметрыМетода.ЭтоЮрЛицо = ЭтоЮрЛицо;
	ПараметрыМетода.ПрименяетсяУСНДоходы = ПрименяетсяУСНДоходы;
	ПараметрыМетода.ПрименяетсяУСНДоходыРасходы = ПрименяетсяУСНДоходыМинусРасходы;
	ПараметрыМетода.Период = ТекущаяДата;
	
	Обработчик = Новый ОписаниеОповещения("ОбработкаПолученияОписанияСтавокУСН", ЭтотОбъект);
	
	РегиональныеСтавкиНалоговКлиент.ПолучитьОписаниеСтавокУСНВФоне(
		ЭтотОбъект, 
		Элементы.ДекорацияСтавкиУСНВВашемРегионе, 
		Элементы.СтавкиУСНСервисДлительнаяОперация,
		ПараметрыМетода, 
		Обработчик);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПолученияОписанияСтавокУСН(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	РегиональныеСтавкиНалоговКлиент.ОбработкаПолученияОписанияСтавокНалога(
		ДлительнаяОперация, 
		Элементы.ДекорацияСтавкиУСНВВашемРегионе, 
		Элементы.СтавкиУСНСервисДлительнаяОперация);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыЛьготыПоНалогуНаИмущество()
	
	СтавкиНалогаНаИмуществоФормы.ЗаполнитьПараметрыЛьготы(ЭтотОбъект, "СтавкиНалогаНаИмущество");
		
КонецПроцедуры	

&НаСервере
Процедура УстановитьТекстПодсказкиЛьготыПоНалогуНаИмущество()
	
	СтавкиНалогаНаИмуществоФормы.УстановитьТекстПодсказкиЛьготы(ЭтотОбъект, "СтавкиНалогаНаИмущество");
			
КонецПроцедуры

&НаСервере
Процедура НастроитьУчетКурсовыхРазниц2022()
	
	Поля = Новый Массив;
	Поля.Добавить(Элементы.ВариантУчетаКурсовыхРазниц2022Список);
	Поля.Добавить(Элементы.ОтложитьОтрицательныеКурсовыеРазницы2022);
	
	// Значение реквизита сохраняется непосредственно при редактировании, поэтому его изменение не должно влиять на модифицированность формы.
	// Как следствие, необходимо обрабатывать ТолькоПросмотр формы.
	
	ВыборДоступен = Не ТолькоПросмотр
		И ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыльПостоянные);
		
	Для Каждого Поле Из Поля Цикл
		Поле.ТолькоПросмотр = Не ВыборДоступен;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев(Форма, СброситьНедоступные)
    
    Элементы = Форма.Элементы;
    
    ДоступнаНадбавка = (Форма.ПрименяютсяНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев = 1);
	ДоступнаСкидка   = (Форма.ПрименяютсяНадбавкиСкидкиНаСтрахованиеОтНесчастныхСлучаев = 2);
    
	Элементы.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевНадбавка.Доступность = ДоступнаНадбавка;
	Если СброситьНедоступные И НЕ ДоступнаНадбавка Тогда
		Форма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Надбавка = 0;
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
	Элементы.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевСкидка.Доступность = ДоступнаСкидка;
	Если СброситьНедоступные И НЕ ДоступнаСкидка Тогда
		Форма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Скидка = 0;
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Антикризис

&НаСервере
Процедура НастроитьПодразделАнтикризисРегиональныеНПА()
	
	Элементы.ПерейтиВРазделУСН.Видимость = НастройкиСистемыНалогообложения.ПрименяетсяУСН;
	Элементы.ПерейтиВРазделЕНВД.Видимость = НастройкиСистемыНалогообложения.ПлательщикЕНВД;
	
	Если Не ЭтоЮрЛицо И НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент Тогда
		Патенты = УчетПСН.ПатентыОрганизацииУплачиваемыеЗаПериод(
			Организация,
			ПериодПереносаСроков.Начало,
			ПериодПереносаСроков.Окончание);
			
		Элементы.ПерейтиВСписокПатентов.Видимость = (Патенты.Количество() > 0);
	Иначе	
		Элементы.ПерейтиВСписокПатентов.Видимость = Ложь;
	КонецЕсли;
		
	Если ЭтоЮрЛицо Тогда
		
		НалогиСНастройкамиАвансов = РегистрыСведений.ПорядокУплатыНалоговНаМестах.НалогиСНастройкамиАвансов(Организация, ТекущаяДата);
				
		Элементы.ПерейтиВНастройкиУплатыНалогаНаИмущество.Видимость = (СписокНеиспользуемыхНастроек.НайтиПоЗначению("НалогНаИмущество") = Неопределено)
			И (НалогиСНастройкамиАвансов.Найти("НалогНаИмущество")<>Неопределено);
		Элементы.ПерейтиВНастройкиУплатыТранспортногоНалога.Видимость = (СписокНеиспользуемыхНастроек.НайтиПоЗначению("ТранспортныйНалог") = Неопределено
			И НалогиСНастройкамиАвансов.Найти("ТранспортныйНалог")<>Неопределено);
		Элементы.ПерейтиВНастройкиУплатыЗемельногоНалога.Видимость = (СписокНеиспользуемыхНастроек.НайтиПоЗначению("ЗемельныйНалог") = Неопределено
			И НалогиСНастройкамиАвансов.Найти("ЗемельныйНалог")<>Неопределено);
		
	Иначе
		Элементы.ПерейтиВНастройкиУплатыНалогаНаИмущество.Видимость = Ложь;
		Элементы.ПерейтиВНастройкиУплатыТранспортногоНалога.Видимость = Ложь;
		Элементы.ПерейтиВНастройкиУплатыЗемельногоНалога.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаРегиональныеНормативныеПравовыеАкты.Видимость = 
		Элементы.ПерейтиВРазделУСН.Видимость
		ИЛИ Элементы.ПерейтиВРазделЕНВД.Видимость
		ИЛИ Элементы.ПерейтиВСписокПатентов.Видимость
		ИЛИ Элементы.ПерейтиВНастройкиУплатыНалогаНаИмущество.Видимость
		ИЛИ Элементы.ПерейтиВНастройкиУплатыТранспортногоНалога.Видимость
		ИЛИ Элементы.ПерейтиВНастройкиУплатыЗемельногоНалога.Видимость;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновнойКодОКВЭД(Знач Организация)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОсновнойКодОКВЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "КодОКВЭД2");
	КонецЕсли;
	
	Возврат ОсновнойКодОКВЭД;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОрганизацияИНН(Знач Организация)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН");
	
КонецФункции

&НаКлиенте
Процедура ОповеститьОбИзмененииНастройкиПродленияСроковНалоговОтчетов()
	
	// Используется для разных настроек, связанных с антикризисными мерами:
	// - изменение сроков
	// - освобождение от налогов
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Организация);
	
	Оповестить("Запись_НастройкиПродленияСроковНалоговОтчетов", ПараметрыОповещения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОсвобождениеНалогов(ЗаголовокГруппы, ПолеСоставМер, ПолеДействияПослеВключения, КнопкаРеестрМСП, ЭтоЮрЛицо)
	
	ЗаголовокГруппы.Заголовок = "Федеральный закон № 172-ФЗ от 08.06.2020"; // устанавливается в коде для упрощения разветвленной разработки
	
	ПолеДействияПослеВключения.Видимость = Ложь;
	
	Если ЭтоЮрЛицо Тогда
		ШаблонСоставМер     = НСтр("ru = 'Малые и средние предприятия, занятые в сферах деятельности, наиболее пострадавших от коронавирусной инфекции, освобождены от уплаты %1 за 2 квартал 2020 года.
                                    |Воспользоваться освобождением могут предприятия, включенные в Реестр МСП'");
		Ссылка              = "https://its.1c.ru/bmk/bp30/tax_exemption_org";
		ОбязателенРеестрМСП = Истина;
	Иначе
		ШаблонСоставМер     = НСтр("ru = 'Индивидуальные предприниматели, занятые в сферах деятельности, наиболее пострадавших от коронавирусной инфекции, освобождены от уплаты %1 за 2 квартал 2020 года.
                                    |Страховой взнос в ПФР в фиксированном размере за 2020 год снижен для них до 20 318 руб.'");
		Ссылка              = "https://its.1c.ru/bmk/bp30/tax_exemption_ip";
		ОбязателенРеестрМСП = Ложь;
	КонецЕсли;
	
	СсылкаHTML = СтрШаблон(НСтр("ru = '<a href = ""%1"">налогов</a>'"), СсылкаUTM(Ссылка));
	
	ПолеСоставМер.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(СтрШаблон(ШаблонСоставМер, СсылкаHTML));
	
	КнопкаРеестрМСП.Видимость = ОбязателенРеестрМСП;
	
	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СсылкаUTM(URL)
	
	ПараметрыURL = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыURL, ОбщегоНазначенияБПКлиентСервер.ИТС_ПараметрыUTM());
	Возврат ОбщегоНазначенияБПКлиентСервер.ДополнитьURLПараметрами(URL, ПараметрыURL);
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьПовторноВыполнитьРасчетНалогов(Организация)
	
	ВыполнитьПовторно = Новый Массив;
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетЕНВД);
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетЗемельногоНалога);
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетНалогаНаИмущество);
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетНалогаНаПрибыль);
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетНалогаУСН);
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетНДФЛДляИП);
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетТорговогоСбора);
	ВыполнитьПовторно.Добавить(Перечисления.ВидыРегламентныхОпераций.РасчетТранспортногоНалога);
	
	Период = НалоговыйУчет.ПериодОсвобожденияОтНалоговПострадавшимОтКоронавируса();
	
	Для Каждого ВидОперации Из ВыполнитьПовторно Цикл
		
		НомерГруппы = ЗакрытиеМесяца.ГруппаПоВидуОперации(ВидОперации);
		
		Попытка
		
			РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
				Организация,
				Период.Начало,
				НомерГруппы,
				ВидОперации,
				Истина);
				
		Исключение
			
			ЗаписьЖурналаРегистрации(
				"РегистрСведений.НеактуальныеРегламентныеОперации",
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.НеактуальныеРегламентныеОперации,
				Организация,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			Возврат Ложь;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьОсвобождениеОтНалогаПСН(Организация, ДеятельностьОтнесенаКПострадавшимОтКоронавируса)
	
	Возврат УчетПСН.ОбновитьОсвобождениеОтНалогаДляПострадавшихОтКоронавируса(Организация, ДеятельностьОтнесенаКПострадавшимОтКоронавируса);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьПодсказкиАнтикризис(Постановление512Подсказка, Постановление512Пояснения, Постановление776Подсказка, Постановление776Пояснения, НастройкиПродленияСроков)
	
	ШаблонЗаголовкаПояснения = НСтр("ru='Возможность продления сроков определяется по основному виду деятельности, указанному в Едином государственном реестре %1 по состоянию на %2'");
	
	Постановление512Пояснения.Заголовок = Новый ФорматированнаяСтрока(
		СтрШаблон(ШаблонЗаголовкаПояснения, ?(НастройкиПродленияСроков.ЭтоЮрЛицо, НСтр("ru='юридических лиц'"), НСтр("ru='индивидуальных предпринимателей'")), НСтр("ru='1 января 2022 года'")),
		Новый ФорматированнаяСтрока(Символы.ПС),
		Новый ФорматированнаяСтрока(НСтр("ru='Код основного вида деятельности можно изменить в'")),
		Новый ФорматированнаяСтрока(" "),
		Новый ФорматированнаяСтрока("настройках организации",,,,"ПерейтиВНастройкиОрганизации"));
		
	Постановление776Пояснения.Заголовок = Новый ФорматированнаяСтрока(
		СтрШаблон(ШаблонЗаголовкаПояснения, ?(НастройкиПродленияСроков.ЭтоЮрЛицо, НСтр("ru='юридических лиц'"), НСтр("ru='индивидуальных предпринимателей'")), НСтр("ru='1 апреля 2022 года'")),
		Новый ФорматированнаяСтрока(Символы.ПС),
		Новый ФорматированнаяСтрока(НСтр("ru='Код основного вида деятельности можно изменить в'")),
		Новый ФорматированнаяСтрока(" "),
		Новый ФорматированнаяСтрока("настройках организации",,,,"ПерейтиВНастройкиОрганизации"));
		
	ШрифтВыделеннойСтроки = ШрифтВыделеннойСтрокиПодсказкиАнтикризис();
	
	ШаблонВидДеятельности = НСтр("ru='Ваш основной вид деятельности %1 (%2)'");
	Если НастройкиПродленияСроков.ЕстьПравоНаПродлениеСроковУСНПостановление512 Тогда
		Постановление512Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				СтрШаблон(ШаблонВидДеятельности, НастройкиПродленияСроков.КодОКВЭД2, НастройкиПродленияСроков.НаименованиеОКВЭД2)),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='входит'"), ШрифтВыделеннойСтроки),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='в перечень видов деятельности, для которых продлены сроки уплаты налога по УСН за 2021 год и авансов по УСН за 1 квартал 2022 года'")));
		
	ИначеЕсли Не ЗначениеЗаполнено(НастройкиПродленияСроков.КодОКВЭД2) Тогда
		Постановление512Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(НСтр("ru='Заполните'"),,,,"ПерейтиВНастройкиОрганизации"),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='основной код ОКВЭД в настройках организации'")));
		
	Иначе
		Постановление512Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				СтрШаблон(ШаблонВидДеятельности, НастройкиПродленияСроков.КодОКВЭД2, НастройкиПродленияСроков.НаименованиеОКВЭД2)),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='не входит'"), ШрифтВыделеннойСтроки),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='в перечень видов деятельности, для которых продлены сроки уплаты налога по УСН за 2021 год и авансов по УСН за 1 квартал 2022 года'")));
		
	КонецЕсли;
	
	Если НастройкиПродленияСроков.ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П1 И НастройкиПродленияСроков.ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П2 Тогда
		
		Постановление776Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				СтрШаблон(ШаблонВидДеятельности, НастройкиПродленияСроков.КодОКВЭД2, НастройкиПродленияСроков.НаименованиеОКВЭД2)),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='входит'"), ШрифтВыделеннойСтроки),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='в перечень видов деятельности, для которых продлены сроки уплаты страховых взносов'")),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='за 2 и 3 квартал 2022 года'"), ШрифтВыделеннойСтроки));
		
	ИначеЕсли НастройкиПродленияСроков.ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П1 Тогда
		Постановление776Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				СтрШаблон(ШаблонВидДеятельности, НастройкиПродленияСроков.КодОКВЭД2, НастройкиПродленияСроков.НаименованиеОКВЭД2)),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='входит'"), ШрифтВыделеннойСтроки),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='в перечень видов деятельности, для которых продлены сроки уплаты страховых взносов'")),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='за 2 квартал 2022 года'"), ШрифтВыделеннойСтроки));
		
	ИначеЕсли НастройкиПродленияСроков.ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П2 Тогда
		Постановление776Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				СтрШаблон(ШаблонВидДеятельности, НастройкиПродленияСроков.КодОКВЭД2, НастройкиПродленияСроков.НаименованиеОКВЭД2)),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='входит'"), ШрифтВыделеннойСтроки),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='в перечень видов деятельности, для которых продлены сроки уплаты страховых взносов'")),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='за 3 квартал 2022 года'"), ШрифтВыделеннойСтроки));
		
	ИначеЕсли Не ЗначениеЗаполнено(НастройкиПродленияСроков.КодОКВЭД2) Тогда
		Постановление776Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(НСтр("ru='Заполните'"),,,,"ПерейтиВНастройкиОрганизации"),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='основной код ОКВЭД в настройках организации'")));
		
	Иначе
		Постановление776Подсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				СтрШаблон(ШаблонВидДеятельности, НастройкиПродленияСроков.КодОКВЭД2, НастройкиПродленияСроков.НаименованиеОКВЭД2)),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='не входит'"), ШрифтВыделеннойСтроки),
			Новый ФорматированнаяСтрока(" "),
			Новый ФорматированнаяСтрока(НСтр("ru='в перечень видов деятельности, для которых продлены сроки уплаты страховых взносов'")));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ШрифтВыделеннойСтрокиПодсказкиАнтикризис()
	
	Возврат ШрифтыСтиля.ВыделенныйЗаголовокПодтвержденияШрифт;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НовыйНастройкиПродленияСроков()
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоЮрЛицо", Ложь);
	Результат.Вставить("КодОКВЭД2", "");
	Результат.Вставить("НаименованиеОКВЭД2", "");
	Результат.Вставить("ЕстьПравоНаПродлениеСроковУСНПостановление512", Ложь);
	Результат.Вставить("ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П1", Ложь);
	Результат.Вставить("ЕстьПравоНаПродлениеСроковСтраховыхВзносовПостановление776П2", Ложь);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗакрытьПодсказкуПостановление776НаСервере()
	СохранитьНастройкуПодсказкиПостановление776(Организация, Ложь);
	НастроитьВидимостьПодсказокАнтикризис(Организация, Элементы.ГруппаПостановление512Подсказка, Элементы.ГруппаПостановление776Подсказка);
КонецПроцедуры

&НаСервере
Процедура ЗакрытьПодсказкуПостановление512НаСервере()
	СохранитьНастройкуПодсказкиПостановление512(Организация, Ложь);
	НастроитьВидимостьПодсказокАнтикризис(Организация, Элементы.ГруппаПостановление512Подсказка, Элементы.ГруппаПостановление776Подсказка);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьВидимостьПодсказокАнтикризис(Организация, ГруппаПостановление512Подсказка, ГруппаПостановление776Подсказка)
	
	ГруппаПостановление512Подсказка.Видимость = ВидимостьПодсказкиПостановление512(Организация);
	ГруппаПостановление776Подсказка.Видимость = ВидимостьПодсказкиПостановление776(Организация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкуПодсказкиПостановление512(Организация, ЗначениеНастройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ВидимостьПодсказокАнтикризисНастройкиНалоговИОтчетов",
		КлючНастройкиПодсказкиВидимостьПодсказкиПостановление512(Организация),
		ЗначениеНастройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкуПодсказкиПостановление776(Организация, ЗначениеНастройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ВидимостьПодсказокАнтикризисНастройкиНалоговИОтчетов",
		КлючНастройкиПодсказкиВидимостьПодсказкиПостановление776(Организация),
		ЗначениеНастройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидимостьПодсказкиПостановление512(Организация)
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ВидимостьПодсказокАнтикризисНастройкиНалоговИОтчетов",
		КлючНастройкиПодсказкиВидимостьПодсказкиПостановление512(Организация),
		Истина);
		
КонецФункции

&НаСервереБезКонтекста
Функция ВидимостьПодсказкиПостановление776(Организация)
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ВидимостьПодсказокАнтикризисНастройкиНалоговИОтчетов",
		КлючНастройкиПодсказкиВидимостьПодсказкиПостановление776(Организация),
		Истина);
			
КонецФункции

&НаСервереБезКонтекста
Функция КлючНастройкиПодсказкиВидимостьПодсказкиПостановление512(Организация)
	
	Возврат СтрШаблон("%1_%2", Организация.УникальныйИдентификатор(), "Постановление512");
	
КонецФункции

&НаСервереБезКонтекста
Функция КлючНастройкиПодсказкиВидимостьПодсказкиПостановление776(Организация)
	
	Возврат СтрШаблон("%1_%2", Организация.УникальныйИдентификатор(), "Постановление776");
	
КонецФункции

#КонецОбласти

#Область РаботаСПериодами

&НаКлиенте
Процедура ПериодЗавершениеВыбора(РезультатВыбора, ИмяРегистра) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		ДатаИзменения = РезультатВыбора.НачалоПериода;
		ПараметрыДатыИзменения = ПредставленияДатыИзменения(
			ДатаИзменения,
			ИмяРегистра,
			НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.Тариф);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыДатыИзменения);
		
		Если ИмяРегистра = "НастройкиСистемыНалогообложения" Тогда
			НастройкиСистемыНалогообложенияФормыКлиентСервер.ЗаполнитьСписокВыбораСистемНалогообложения(ЭтотОбъект);
			НастройкиСистемыНалогообложенияФормыКлиентСервер.УстановитьСистемуНалогообложения(ЭтотОбъект);
			НастройкиСистемыНалогообложенияФормыКлиентСервер.СброситьПризнакПлательщикЕНВД(ЭтотОбъект);
			НастройкиСистемыНалогообложенияФормыКлиентСервер.СброситьПризнакПлательщикНДСПрименяющийУСН(ЭтотОбъект);
			НастройкиСистемыНалогообложенияФормыКлиентСервер.УправлениеФормой(ЭтотОбъект);
		ИначеЕсли ИмяРегистра = "НастройкиУчетаУСН" Тогда
			ЗаполнитьЗначенияСвойств(
				ЭтотОбъект,
				НастройкиУчетаУСНФормыВызовСервера.НастройкиОрганизацииИСистемыНалогообложения(Организация, ДатаИзменения));
			НастройкиУчетаУСНФормыКлиентСервер.УправлениеФормой(ЭтотОбъект);
		ИначеЕсли ИмяРегистра = "НастройкиУчетаНДС" Тогда
			НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВыбораПериодаНастройки(Знач ДатаИзменения, ИмяРегистра, Тариф)
	
	Период = Новый Структура("НачалоПериода, КонецПериода, ВидПериода, ИмяФормы");
	
	Если ИмяРегистра = "НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС"
		И Тариф = Перечисления.ИПТарифыФиксированныхВзносов.ВоенныйПенсионер
		И НачалоГода(ДатаИзменения) = НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж() Тогда
		// Тариф для военных пенсионеров действует с конкретной даты
		ДатаИзменения = УчетСтраховыхВзносовИПКлиентСервер.ДатаНачалаДействияТарифаДляВоенныхПенсионеров();
	КонецЕсли;
	
	Если ИмяРегистра = "НастройкиУчетаТуристическогоНалога" Тогда
		ДатаИзменения = Макс(ДатаИзменения, '20250101');
	КонецЕсли;
	
	Если ИмяРегистра = "СтраховыеВзносы" Тогда
		Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Месяц;
	Иначе
		Периодичность = Метаданные.РегистрыСведений[ИмяРегистра].ПериодичностьРегистраСведений;
	КонецЕсли;
	
	Если Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Год Тогда
		Период.НачалоПериода = НачалоГода(ДатаИзменения);
		Период.КонецПериода = КонецГода(ДатаИзменения);
		Период.ВидПериода = Перечисления.ДоступныеПериодыОтчета.Год;
	ИначеЕсли Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Квартал Тогда
		Период.НачалоПериода = НачалоКвартала(ДатаИзменения);
		Период.КонецПериода = КонецКвартала(ДатаИзменения);
		Период.ВидПериода = Перечисления.ДоступныеПериодыОтчета.Квартал;
		Период.ИмяФормы = "ВыборСтандартногоПериодаКвартал";
	ИначеЕсли Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Месяц Тогда
		Период.НачалоПериода = НачалоМесяца(ДатаИзменения);
		Период.КонецПериода = КонецМесяца(ДатаИзменения);
		Период.ВидПериода = Перечисления.ДоступныеПериодыОтчета.Месяц;
		Период.ИмяФормы = "ВыборСтандартногоПериодаМесяц";
	Иначе
		Период.НачалоПериода = НачалоДня(ДатаИзменения);
		Период.КонецПериода = КонецДня(ДатаИзменения);
		Период.ВидПериода = Перечисления.ДоступныеПериодыОтчета.День;
		Период.ИмяФормы = "ВыборСтандартногоПериодаДень";
	КонецЕсли;
	
	Возврат Период;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставленияДатыИзменения(ДатаИзменения, ИмяРегистра, Тариф = Неопределено)
	
	Представления = Новый Структура;
	
	ПараметрыВыбора = ПараметрыВыбораПериодаНастройки(ДатаИзменения, ИмяРегистра, Тариф);
	
	Представления.Вставить("ДатаИзмененияСтрокой", БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		ПараметрыВыбора.НачалоПериода, ПараметрыВыбора.КонецПериода, Истина));
	
	Представления.Вставить("ДатаИзмененияЧислом", Год(ДатаИзменения));
	
	Возврат Представления;
	
КонецФункции

&НаКлиенте
Функция ИмяРегистра(ИмяЭлемента)
	
	ПозицияРазделителя = СтрНайти(ИмяЭлемента, "_");
	Если ПозицияРазделителя = 0 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не удалось определить имя регистра по имени элемента ""%1""'"),ИмяЭлемента );
	КонецЕсли;
	
	Возврат Сред(ИмяЭлемента, ПозицияРазделителя + 1);
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораОрганизации(Форма, ДоступныеОрганизации)
	
	Элементы = Форма.Элементы;
	
	ПараметрВыбораОтборПоОрганизации = Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеОрганизации);
	
	ПараметрыВыбораОрганизации = Новый Массив();
	ПараметрыВыбораОрганизации.Добавить(ПараметрВыбораОтборПоОрганизации);
	
	Элементы.Организация.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораОрганизации);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//Оформление таблицы Список
	
	// ВсеОтчеты
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Наименование");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Служебный", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Действие");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Служебный", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Истина);
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Действие");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Служебный", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//ЕНВД
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Наименование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Действие");
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ВидУчета",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"НастройкиСистемыНалогообложения.ПлательщикЕНВД", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"Список.Идентификатор", ВидСравненияКомпоновкиДанных.Равно, "ЕНВД");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"Список.БылВключен", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//Торговый сбор
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Наименование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Действие");
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ВидУчета",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"НастройкиСистемыНалогообложения.ПлательщикТорговогоСбора", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"Список.Идентификатор", ВидСравненияКомпоновкиДанных.Равно, "ТорговыйСбор");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//Патент
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Наименование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Действие");
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ВидУчета",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"Список.Идентификатор", ВидСравненияКомпоновкиДанных.Равно, "Патент");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Все настройки
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Наименование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Действие");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Выделен", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ВажнаяНадписьШрифт);
	
	//Оформление таблицы СписокПодчиненный
	
	// ДетальнаяНастройкаПредставление
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДетальнаяНастройка_СписокПодчиненный");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СписокПодчиненный.ЕстьДетальнаяНастройка", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Редактирование
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Наименование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Действие");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СписокНедоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
		
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииНастроек(ОбновитьЗадачи = Ложь)
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.НалогиОтчеты"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ПорядокУплатыАкцизов"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ПорядокУплатыНалоговНаМестах"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ВариантыПримененияТребованийЗаконодательства"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
	Оповестить("Обновить дерево отчетов", "Обновить дерево отчетов", ЭтаФорма); // На языке регламентированной отчетности
	
	Оповестить("ИзменениеУчетнойПолитики", Организация);
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	
	Если ОбновитьЗадачи Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИЗаполнитьРегулярныеЗадачи", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИОписатьНастройкиУчета()
	
	ПрочитатьУчетнуюПолитику();
	ОписатьНастройкиУчета();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьИЗаполнитьРегулярныеЗадачи()
	
	ДлительнаяОперация = ОбновитьИЗаполнитьРегулярныеЗадачи();
	
	Если ДлительнаяОперация = Неопределено Тогда
		// Фоновое задание не запущено
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Обновление списка задач бухгалтера'");
		Обработчик = Новый ОписаниеОповещения("ОбновитьИЗаполнитьРегулярныеЗадачиЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИЗаполнитьРегулярныеЗадачиЗавершение(ДлительнаяОперация, ДополнителныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
		Если ВозможноПродлениеСроковНалоговОтчетов Тогда
			Если ПрименяетсяУСНДоходы Или ПрименяетсяУСНДоходыМинусРасходы Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиУчетаУСНФормыВызовСервера.СрокиУплатыУСН(Организация, ЭтоЮрЛицо));
			КонецЕсли;
			Если НастройкиСистемыНалогообложения.ПлательщикЕНВД Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиУчетаВызовСервера.СрокиУплатыЕНВД(Организация));
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьИЗаполнитьРегулярныеЗадачи()
	
	Возврат КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(УникальныйИдентификатор, Организация, Ложь, Ложь, Истина);
	
КонецФункции

// Вызывается при получении оповещения о записи организации.
// Для новых организаций добавляет их ссылки в отбор параметра выбора, 
// чтобы они были доступны в списке на форме.
//
// Параметры:
//	Параметр - Неопределено, Структура - Параметр, полученный в ОбработкаОповещения().
//		Если имеет тип Структура и содержит ключ "ЭтоНовый", то такое оповещения будет обработано,
//		иначе проигнорировано.
//	Источник - СправочникСсылка.Организации - Ссылка на измененную организацию.
//
&НаКлиенте
Процедура ОбработкаОповещенияЗаписьОрганизации(Параметр, Источник)
	
	Если ТипЗнч(Параметр) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметр.Свойство("ЭтоНовый") 
		ИЛИ НЕ Параметр.ЭтоНовый Тогда
		Возврат;
	КонецЕсли;
	
	// При записи новой организации добавим ее в список доступных для выбора организаций на форме.
	ДоступныеОрганизации = Новый Массив;
	ДоступныеОрганизации.Добавить(Источник);
	
	Если Элементы.Организация.ПараметрыВыбора.Количество() > 0 Тогда
		// В качестве значения параметра выбора указан фиксированный массив ссылок организаций, 
		// полученный из ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю().
		// Копируем их в новый массив.
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДоступныеОрганизации, Элементы.Организация.ПараметрыВыбора[0].Значение);
	КонецЕсли;
	
	ФиксированныйМассивДоступныхОрганизаций = Новый ФиксированныйМассив(ДоступныеОрганизации);
	
	УстановитьПараметрыВыбораОрганизации(ЭтотОбъект, ДоступныеОрганизации);

КонецПроцедуры

&НаКлиенте
Процедура СтавкиНалогаНаИмуществоПриНаличии1ЛьготыЗаполнять1ЛистРаздела2ПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОтобразитьПериоды("СтавкиНалогаНаИмущество");
	
КонецПроцедуры

&НаКлиенте
Функция СписокВыбораФормСтатистики()
	
	СтрокаЗадачи = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	
	Отбор = Новый Структура("Родитель, Включен", СтрокаЗадачи.Ссылка, Ложь);
	ФормыСтатистикиНеВыбраны = СписокПодчиненный.НайтиСтроки(Отбор);
	
	СписокВыбора = Новый СписокЗначений;
	
	Для Каждого ФормаСтатистики Из ФормыСтатистикиНеВыбраны Цикл
		СписокВыбора.Добавить(ФормаСтатистики.Ссылка, ФормаСтатистики.Наименование);
	КонецЦикла;
	
	Возврат СписокВыбора;
	
КонецФункции

&НаКлиенте
Процедура СписокФормыСтатистикиПослеВыбора(МассивФормСтатистики, Параметры) Экспорт
	
	Если МассивФормСтатистики = Неопределено Или МассивФормСтатистики.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЗадачи = Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока);
	Отбор = Новый Структура("Родитель, Ссылка", СтрокаЗадачи.Ссылка, Неопределено);
	ЕстьИзменения = Ложь;
	
	Для Каждого ФормаСтатистики Из МассивФормСтатистики Цикл
		
		Отбор.Ссылка = ФормаСтатистики;
		Строки = СписокПодчиненный.НайтиСтроки(Отбор);
		Если Строки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Строки[0].Включен = Истина;
		Строки[0].ОтключенПользователем = Ложь;
		Строки[0].ДобавленПользователем = Не Строки[0].ПолученИзВнешнегоСервиса;
		
		СтрокаЗадачи.Включен = Истина;
		ЕстьИзменения = Истина;
		
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
		СохранитьСписокНастройкиНалоговОтчетов(СтрокаЗадачи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыСостоянияЗагрузкиСтатистическихОтчетов(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаБаннер_СтатистическаяОтчетность_Страницы.ТекущаяСтраница = Элементы.ГруппаБаннер_СтатистическаяОтчетность_СтраницаБаннер;
	Элементы.СписокФормыСтатистики.Доступность = Истина;
	Элементы.ВключенПользователем_СписокФормыСтатистики.Видимость = Форма.ДоступнаЗагрузкаФормИзСервиса;
	
	ИдентификаторСтатистика = ЗадачиБухгалтераКлиентСервер.КодЗадачиСтатистика();
	ДатаЗагрузкиФормСтатистики = ЗадачиБухгалтераСтатистикаВызовСервера.ДатаПоследнейЗагрузкиСпискаФормСтатистики();
	Элементы.ВосстановитьПоДаннымРосстата.Видимость = Ложь;
	Элементы.Заголовок_ДатаАктуальности.Видимость = Ложь;
	
	ТекстБаннераСтатистическаяОтчетность = Новый Массив;
	ТекстБаннераСтатистическаяОтчетностьСостояние = Новый Массив;
	
	АдресИнтерактивногоСервисаРосстата = ЗадачиБухгалтераСтатистикаВызовСервера.АдресИнтерактивногоСервисаРосстата();
	
	Если Форма.ДоступнаЗагрузкаФормИзСервиса Тогда
		
		// ТекстБаннераСтатистическаяОтчетность
		ТекстБаннераСтатистическаяОтчетность.Добавить(СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(СтрШаблон(
			НСтр("ru='Перечень форм обновляется по данным <a href = ""%1"">сайта Росстата</a>.'"),
			АдресИнтерактивногоСервисаРосстата)));
		
		ТекстБаннераСтатистическаяОтчетность.Добавить(Символы.ПС);
		ТекстБаннераСтатистическаяОтчетность.Добавить(
			НСтр("ru='Поиск выполняется по ОКПО и ИНН, указанных в реквизитах организации.'"));
		
		Если ЗначениеЗаполнено(ДатаЗагрузкиФормСтатистики) Тогда
			Элементы.Заголовок_ДатаАктуальности.Видимость = Истина;
			Элементы.Заголовок_ДатаАктуальности.Заголовок = СтрШаблон(НСтр("ru='Данные по состоянию на %1'"), Формат(ДатаЗагрузкиФормСтатистики, "ДЛФ=D;"));
		КонецЕсли;
		
		// ТекстБаннераСтатистическаяОтчетностьСостояние
		Отбор = Новый Структура("Идентификатор, ПолученИзВнешнегоСервиса", ИдентификаторСтатистика, Истина);
		ПолученыИзВнешнегоСервиса = Форма.СписокПодчиненный.НайтиСтроки(Отбор);
		
		Если ПолученыИзВнешнегоСервиса.Количество() = 0 И ЗначениеЗаполнено(ДатаЗагрузкиФормСтатистики) Тогда
			ТекстБаннераСтатистическаяОтчетностьСостояние.Добавить(
				НСтр("ru='По данным ведомства, отчетность не требуется.'"));
		ИначеЕсли ПолученыИзВнешнегоСервиса.Количество() = 0 Тогда 
			ТекстБаннераСтатистическаяОтчетностьСостояние.Добавить(
				НСтр("ru='На данный момент сведения отсутствуют, но в скором времени они будут запрошены в ведомстве.'"));
		КонецЕсли;
		
		Отбор = Новый Структура("Идентификатор, Включен, ПолученИзВнешнегоСервиса", ИдентификаторСтатистика, Истина, Ложь);
		ВключеныПользователем = Форма.СписокПодчиненный.НайтиСтроки(Отбор);
		
		Отбор = Новый Структура("Идентификатор, Включен, ПолученИзВнешнегоСервиса", ИдентификаторСтатистика, Ложь, Истина);
		ОтключеныПользователем = Форма.СписокПодчиненный.НайтиСтроки(Отбор);
	
		Если ЗначениеЗаполнено(ДатаЗагрузкиФормСтатистики)
			 И (ВключеныПользователем.Количество() > 0 Или ОтключеныПользователем.Количество() > 0) Тогда
			Если ТекстБаннераСтатистическаяОтчетностьСостояние.Количество() > 0 Тогда
				ТекстБаннераСтатистическаяОтчетностьСостояние.Добавить(Символы.ПС);
			КонецЕсли;
			ТекстБаннераСтатистическаяОтчетностьСостояние.Добавить(НСтр("ru='Часть форм была добавлена или отключена вручную.'"));
			Элементы.ВосстановитьПоДаннымРосстата.Видимость = Форма.ПравоРедактированияНастроек;
		КонецЕсли;
	
	Иначе
		
		ТекстБаннераСтатистическаяОтчетность.Добавить(СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(СтрШаблон(
			НСтр("ru='Актуальный перечень форм можно узнать на <a href = ""%1"">сайте Росстата</a>.'"),
			АдресИнтерактивногоСервисаРосстата)));
		
		Если Форма.ПричинаНедоступностиЗагрузкиСпискаСтатФорм = "НеЗаполненыДанныеИнтернетПоддержки"
			 Или Форма.ПричинаНедоступностиЗагрузкиСпискаСтатФорм = "НеверныйЛогинИлиПароль" Тогда
		
			ТекстБаннераСтатистическаяОтчетностьСостояние.Добавить(СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(
				НСтр("ru='Чтобы данные ведомства загружались автоматически, <a href = ""ПодключитьИнтернетПоддержку"">подключите Интернет-поддержку</a>.'")));
		
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ТекстБаннера_СтатистическаяОтчетность.Заголовок =
		Новый ФорматированнаяСтрока(ТекстБаннераСтатистическаяОтчетность);
	Элементы.ТекстБаннера_СтатистическаяОтчетность_Состояние.Заголовок =
		Новый ФорматированнаяСтрока(ТекстБаннераСтатистическаяОтчетностьСостояние);
	Элементы.ТекстБаннера_СтатистическаяОтчетность_Состояние.Видимость =
		(ТекстБаннераСтатистическаяОтчетностьСостояние.Количество() > 0);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗаписьДанныхНаСервере(Настройка)
	
	КалендарьБухгалтера.ЗапланироватьОбновлениеЗадачБухгалтера();
	
	РезультатЧтения = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(РезультатЧтения) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Дерево = РезультатЧтения.Дерево;
	НовоеДерево = Дерево.Скопировать();
	НовоеДерево.Строки.Очистить();
	
	СтрукутраПоиска = Новый Структура("Идентификатор", Настройка.Идентификатор);
	МассивСтрок = Дерево.Строки.НайтиСтроки(СтрукутраПоиска);
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СтараяСтрока = МассивСтрок[0];
	НоваяСтрока = НовоеДерево.Строки.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтараяСтрока);
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
	//Перезаполняем дерево
	ЗаполнитьЗначенияСвойств(СтараяСтрока, НоваяСтрока);
	
	Если Настройка.ЕстьПодстроки Тогда
		Для Каждого Строка ИЗ СтараяСтрока.Строки Цикл
			СтрукутраПоиска = Новый Структура("Ссылка", Строка.Ссылка);
			МассивСтрок = СписокПодчиненный.НайтиСтроки(СтрукутраПоиска);
			Если МассивСтрок.Количество() <> 0 Тогда
				ПодчиненнаяСтрока = НоваяСтрока.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(ПодчиненнаяСтрока, Строка);
				ЗаполнитьЗначенияСвойств(ПодчиненнаяСтрока, МассивСтрок[0]);
				//Перезаполняем дерево
				ЗаполнитьЗначенияСвойств(Строка, ПодчиненнаяСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РезультатЧтения.Вставить("Дерево", Дерево);
	ПоместитьВоВременноеХранилище(РезультатЧтения, АдресХранилища);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Организация",                 Организация);
	ПараметрыЗаполнения.Вставить("Дерево",                      НовоеДерево);
	ПараметрыЗаполнения.Вставить("ПолноеДерево",                Дерево);
	ПараметрыЗаполнения.Вставить("ЗначенияДетальнойНастройки",  ПолучитьИзВременногоХранилища(АдресЗначенияДетальнойНастройки));
	ПараметрыЗаполнения.Вставить("ПараметрыДетальнойНастройки", ПолучитьИзВременногоХранилища(АдресПараметрыДетальнойНастройки));
	
	РегистрыСведений.НалогиОтчеты.ЗаписатьДанныеВыборочно(ПараметрыЗаполнения);
	
	Если Настройка.Идентификатор = ЗадачиБухгалтераКлиентСервер.КодЗадачиСтатистика() Тогда
		НастроитьЭлементыСостоянияЗагрузкиСтатистическихОтчетов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСтавокУСН(НовыеСтавки)
	
	Если ТипЗнч(НовыеСтавки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НовыеСтавки.Свойство("НалоговыеКаникулы") Тогда
		Если НЕ ЭтоЮрЛицо Тогда
			НастройкиУчетаУСН.НалоговыеКаникулы = Истина;
			НастройкиУчетаУСН.СтавкаНалога = 0;
		КонецЕсли;
	Иначе
		НастройкиУчетаУСН.СтавкаНалога = НовыеСтавки.СтавкаНалога;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если Элементы.СтраницыНастроек.ТекущаяСтраница <> Элементы.Страница_УСН Тогда
		Для каждого СтрокаСписка Из Список Цикл
			Если СтрокаСписка.Идентификатор <> "УСН" Тогда
				Продолжить;
			КонецЕсли;
			Элементы.Список.ТекущаяСтрока = СтрокаСписка.ПолучитьИдентификатор();
			Прервать;
		КонецЦикла;
		Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_УСН;
	КонецЕсли;
	
	ОтобразитьПериоды("НастройкиУчетаУСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВНастройкиУплатыАвансов(ИдентификаторЗадачи)
	
	СтруктураПоиска = Новый Структура("Идентификатор", ИдентификаторЗадачи);
	МассивИдентификаторов = Список.НайтиСтроки(СтруктураПоиска);
	
	Если МассивИдентификаторов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	Налог = МассивИдентификаторов[0].Ссылка;
	
	ПараметрыФормы = Новый Структура("Налог, Отбор, ПоказатьУведомлениеОПродленииСроков",
		Налог,
		Новый Структура("Организация", Организация),
		Истина);
	
	ОткрытьФорму("РегистрСведений.ПорядокУплатыНалоговНаМестах.ФормаСписка",
		ПараметрыФормы,
		ЭтаФорма,
		УникальныйИдентификатор);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьЗаголовокПереченьПрямыхЗатрат()

	НастройкиУчетаНалогаНаПрибыльФормыКлиентСервер.УстановитьЗаголовокПереченьПрямыхЗатрат(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	НачатьПолучениеСпискаСтатФормИзСервиса();
	
	ИспользоватьСервисРегиональныеСтавкиНалогов =
		РегиональныеСтавкиНалоговВызовСервера.ИспользуетсяСервисРегиональныхСтавок();
	ПолучитьОписаниеСтавокУСНВФоне();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеСпискаСтатФормИзСервиса()
	
	ДоступнаЗагрузкаФормИзСервиса = ЗадачиБухгалтераСтатистикаВызовСервера.ДоступнаЗагрузкаФормИзСервиса(ПричинаНедоступностиЗагрузкиСпискаСтатФорм);
	
	Если Не ДоступнаЗагрузкаФормИзСервиса Или Не ЗадачиБухгалтераСтатистикаВызовСервера.ПроверитьМинимальныйИнтервалОбращенияКСервису() Тогда
		// Только обновляем баннер и колонки списка стат форм
		НастроитьЭлементыСостоянияЗагрузкиСтатистическихОтчетов(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	// Показываем картинку ожидания вместо баннера
	Элементы.ГруппаБаннер_СтатистическаяОтчетность_Страницы.ТекущаяСтраница = Элементы.ГруппаБаннер_СтатистическаяОтчетность_СтраницаОжидание;
	// Блокируем таблицу стат отчетов
	Элементы.СписокФормыСтатистики.Доступность = Ложь;
	
	СведенияОДлительнойОперацииПолученияСтатФорм = ЗадачиБухгалтераСтатистикаВызовСервера.СведенияОДлительнойОперацииПолученияСтатФорм();
	
	Если ЗначениеЗаполнено(СведенияОДлительнойОперацииПолученияСтатФорм.ОжидатьЗавершение) Тогда
		// Фоновое задание уже было запущено.
		// Обновим список стат. отчетов через указанный интервал
		ПодключитьОбработчикОжидания(
			"Подключаемый_ЗаполнитьСписокНастройкиНалоговОтчетов",
			СведенияОДлительнойОперацииПолученияСтатФорм.ОжидатьЗавершение,
			Истина);
	ИначеЕсли ЗначениеЗаполнено(СведенияОДлительнойОперацииПолученияСтатФорм.ДлительнаяОперация) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОповещенияОЗавершении = Новый ОписаниеОповещения("ПолучениеСпискаСтатФормИзСервисаЗавершение", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			СведенияОДлительнойОперацииПолученияСтатФорм.ДлительнаяОперация,
			ОповещенияОЗавершении,
			ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеСпискаСтатФормИзСервисаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СведенияОДлительнойОперацииПолученияСтатФорм.ДлительнаяОперация = Неопределено; // Сбросим признак выполнения.
	
	Если Результат <> Неопределено И Результат.Статус = "Выполнено" Тогда
		ЗаполнитьСписокНастройкиНалоговОтчетов();
	Иначе
		НастроитьЭлементыСостоянияЗагрузкиСтатистическихОтчетов(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СнятьБлокировкуСРегистраНалогиОтчеты(Организация, УИД)
	
	ОписаниеКлюча = Новый Структура("Организация", Организация);
	РазблокироватьДанныеДляРедактирования(
		РегистрыСведений.НалогиОтчеты.СоздатьКлючЗаписи(ОписаниеКлюча),
		УИД);
	
КонецПроцедуры

#КонецОбласти

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		НастройкиОжидания.ПолучатьРезультат = Истина;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", ?(ЗначениеЗаполнено(Организация), Организация, Неопределено));
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияНалогиИОтчеты());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область БаннерыИзмененияПоНалогам

&НаСервере
Процедура НастроитьБаннерыИзмененияПоНалогам()
	
	// Налог на имущество
	КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество();
	Элементы.ТекстБаннера_НалогНаИмущество.Заголовок = 
		УправлениеВнеоборотнымиАктивами.ТекстБаннераИзмененияВУплатеНалога(КодНалога, Организация);
	
	Элементы.ГруппаБаннер_НалогНаИмущество.Видимость = ВидимостьБаннераИзмененияВУплатеНалога(КодНалога, Организация);
	
	// Транспортный налог
	КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог();
	Элементы.ТекстБаннера_ТранспортныйНалог.Заголовок = 
		УправлениеВнеоборотнымиАктивами.ТекстБаннераИзмененияВУплатеНалога(КодНалога, Организация);
		
	Элементы.ГруппаБаннер_ТранспортныйНалог.Видимость = ВидимостьБаннераИзмененияВУплатеНалога(КодНалога, Организация);
	
	// Земельный налог
	КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог();
	Элементы.ТекстБаннера_ЗемельныйНалог.Заголовок = 
		УправлениеВнеоборотнымиАктивами.ТекстБаннераИзмененияВУплатеНалога(КодНалога, Организация);
		
	Элементы.ГруппаБаннер_ЗемельныйНалог.Видимость = ВидимостьБаннераИзмененияВУплатеНалога(КодНалога, Организация);
	
	// Туристический налог
	КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиТуристическийНалог(); 
	Элементы.ГруппаБаннер_ТуристическийНалог.Видимость = ВидимостьБаннераИзмененияВУплатеНалога(КодНалога, Организация);

	ЭлементыПодсказки = Новый Массив;
	ЭлементыПодсказки.Добавить(
		НСтр("ru = 'С 2025 года, согласно Главе 33.1 НК РФ, организации и ИП, оказывающие
		 |услуги по размещению (гостиницы, отели и иные объекты размещения постояльцев)
		 |могут быть признаны плательщиками туристического налога. Узнать, действует ли налог
		 |для вашей организации, можно на сайте местного органа власти.'"));
	ЭлементыПодсказки.Добавить(Символы.ПС);
	ЭлементыПодсказки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее в статье'"), , , , "https://buh.ru/articles/turisticheskiy-nalog-v-1s.html"));
	
	Элементы.ТекстБаннера_ТуристическийНалог.Заголовок = Новый ФорматированнаяСтрока(ЭлементыПодсказки);
	
КонецПроцедуры

&НаСервере
Процедура СкрытьБаннерИзмененияПоНалогу(Знач ИмяЭлемента)
	
	Если СтрНайти(ИмяЭлемента, "КартинкаЗакрытьБаннер_") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Налог = СтрЗаменить(ИмяЭлемента, "КартинкаЗакрытьБаннер_", "");
	
	УправлениеВнеоборотнымиАктивами.СохранитьНастройкуБаннераИзмененияВУплатеНалога(Налог, Организация, Ложь);
	Элементы["ГруппаБаннер_" + Налог].Видимость = ВидимостьБаннераИзмененияВУплатеНалога(Налог, Организация);
				
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидимостьБаннераИзмененияВУплатеНалога(Налог, Организация)
	
	Если Налог = "ТуристическийНалог" Тогда
		Возврат ТуристическийНалогСервер.ВидимостьБаннераИзмененияВУплатеНалога(Налог, Организация);
	Иначе
		Возврат УправлениеВнеоборотнымиАктивами.ВидимостьБаннераИзмененияВУплатеНалога(Налог, Организация);
	КонецЕсли;
	
КонецФункции 

#КонецОбласти

#Область НачалоВеденияРаздельногоУчетаНДС

&НаСервере
Функция ВыполнитьОбработкиПоПереходуНаРаздельныйУчет(Организация, Период)
	
	Организации         = БухгалтерскийУчетПереопределяемый.ВсяОрганизация(Организация);

	ПараметрыПроцедуры = Новый Структура();
	ПараметрыПроцедуры.Вставить("Организации", Организации);
	ПараметрыПроцедуры.Вставить("Период",      Период);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение остатков по раздельному учету'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
	"УчетНДСРаздельныйФормированиеНачальныхОстатков.СформироватьОстатки",
	ПараметрыПроцедуры, ПараметрыВыполнения);

КонецФункции

&НаКлиенте
Процедура ОткрытьВводОстатков(Команда)
	
	Если ОстаткиПоРУТолькоЧтоСформированы Тогда
		ВключатьОбособленныеПодразделения = ЕстьОбособленныеПодразделения(ГоловнаяОрганизация);
	Иначе
		ВключатьОбособленныеПодразделения = Ложь;
	КонецЕсли;
	
	ПараметрыДляОткрытияФормы = Новый Структура("Организация, ДоступенОтборПоОрганизации, ВключатьОбособленныеПодразделения",
		ЭтотОбъект.Организация, Истина, ВключатьОбособленныеПодразделения);
	ОткрытьФорму("ОбщаяФорма.ВводНачальныхОстатковДляВеденияРаздельногоУчетаНДС", ПараметрыДляОткрытияФормы);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаличиеДокументовВводаОстатковПоРУ()
	
	Возврат РегистрыСведений.ВыполнениеРегламентныхОперацийНДС.СуществуютДокументыВводаОстатковПоРУ(
		ЭтотОбъект.НастройкиУчетаНДС.Организация, ТекущаяДата);
	
КонецФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершениеПриИзменениеНастроекРаздельногоУчета(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьБаннерПереходНаПолныйИнтерфейс()
	
	Если ЭтотОбъект.НастройкиУчетаНДС.СложныйУчетНДС
	И Не ОрганизацияВелаРаздельныйУчет Тогда
		ОтобразитьПериоды("НастройкиУчетаНДС");
		Элементы.ОК_НастройкиУчетаНДС.Видимость       = Ложь;
		Элементы.Отменить_НастройкиУчетаНДС.Видимость = Ложь;
		Элементы.ГруппаДекорацииПерезапуск.Видимость  = Истина;
		Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = Ложь;
	ИначеЕсли ОрганизацияВелаРаздельныйУчет Тогда
		ОтобразитьПериоды("НастройкиУчетаНДС");
		Элементы.ОК_НастройкиУчетаНДС.Видимость       = Истина;
		Элементы.Отменить_НастройкиУчетаНДС.Видимость = Истина;
		Элементы.ГруппаДекорацииПерезапуск.Видимость  = Ложь;
	Иначе
		ОтобразитьИсторию("НастройкиУчетаНДС");
		Элементы.ОК_НастройкиУчетаНДС.Видимость       = Истина;
		Элементы.Отменить_НастройкиУчетаНДС.Видимость = Истина;
		Элементы.ГруппаДекорацииПерезапуск.Видимость  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьБаннерНеобходимоСформироватьОстатки()
	
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
	БаннерВключениеРУ = НСтр("ru = 'Для начала ведения раздельного учета НДС будут сформированы остатки.
							|Операция может занять длительное время.'");
	Если НЕ ОрганизацияВелаРаздельныйУчет Тогда
		Элементы.БаннерВключениеРУ.Видимость = ЭтотОбъект.НастройкиУчетаНДС.СложныйУчетНДС;
	КонецЕсли;
	Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнтерфейсНаСервере()
	
	ОбщегоНазначенияБП.УстановитьРежимКомандногоИнтерфейса("ИнтерфейсТакси");
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаСменыИнтерфейса"),,ТекущаяДатаСеанса());
	ХранилищеОбщихНастроек.Сохранить(ВРег("ФормированиеОстатковПоНДС"),,Истина);
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаНачалаВеденияРаздельногоУчетаНДС"),,ДатаНачалаВеденияРаздельногоУчетаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыполнитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат)
		Или Не Результат.Свойство("Статус") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		
		СохранитьИнформациюОбОшибкеВЖурналРегистрации(Результат.ПодробноеПредставлениеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = Истина;
		ЕстьВводОстатковПоРУ = Истина;
		ОстаткиПоРУТолькоЧтоСформированы = Истина;
		НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(ЭтаФорма);
		
		Если ВключилиРУБудущейДатой Тогда
			ПараметрыДляОткрытияФормы = Новый Структура;
			ПараметрыДляОткрытияФормы.Вставить("Организация",                   ЭтотОбъект.Организация);
			ПараметрыДляОткрытияФормы.Вставить("Период",                        БудущаяДатаВключенияРУ);
			ПараметрыДляОткрытияФормы.Вставить("ЕстьОбособленныеПодразделения", ЕстьОбособленныеПодразделения(ГоловнаяОрганизация));
			
			ОткрытьФорму("ОбщаяФорма.ФормаОстаткиПоРУУспешноСформированы",ПараметрыДляОткрытияФормы,
						ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Отменено" Тогда
		
		Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = Истина; 
		ЕстьВводОстатковПоРУ = Истина;
		НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(ЭтаФорма);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ФормированиеОстатковПоНДС()
	
	УстановитьСтраницуНДС();
	СформироватьОстаткиДляВеденияРаздельногоУчетаНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуНДС()
	
	СтруктураПоиска = Новый Структура("Идентификатор", "НДС");
	МассивИдентификаторов = Список.НайтиСтроки(СтруктураПоиска);
	
	Если МассивИдентификаторов.Количество() > 0 Тогда
		Элементы.Список.ТекущаяСтрока = МассивИдентификаторов[0].ПолучитьИдентификатор();
		Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.Страница_НДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОстаткиДляВеденияРаздельногоУчетаНДС()
	
	Результат = ВыполнитьОбработкиПоПереходуНаРаздельныйУчет(
		Организация,
		ДатаНачалаВеденияРаздельногоУчетаНДС);
	
	Если Результат.Статус = "Выполнено" Тогда
		
		Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = Истина;
		ЕстьВводОстатковПоРУ = Истина;
		ОстаткиПоРУТолькоЧтоСформированы = Истина;
		
	Иначе
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("КнопкаВыполнитьЗавершение", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат,
			ОповещениеОЗавершении, ПараметрыОжидания);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СохранитьИнформациюОбОшибкеВЖурналРегистрации(ТекстОшибки)
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Формирование остатков по раздельному учету НДС'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьОбособленныеПодразделения(ГоловнаяОрганизация)
	
	Возврат НалоговыйУчет.ЕстьОбособленныеПодразделения(ГоловнаяОрганизация);
	
КонецФункции


&НаСервереБезКонтекста
Функция ЕстьОстаткиПоБУ(Организация, Период)
	
	Возврат УчетНДСРаздельныйФормированиеНачальныхОстатков.ЕстьОстаткиДляФормированияПоРУ(Организация, Период);
	
КонецФункции

&НаКлиенте
Процедура ВопросВключитьРаздельныйНДСПоСпособамУчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года = Ложь;
		Если НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ОбщаяУСН")
			ИЛИ НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ПониженнаяУСН") Тогда
			НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов = Истина;
			НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19  = Ложь;
		Иначе
			НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19  = Истина;
			НастройкиУчетаНДС.РаздельныйУчетНДСБезВычетов = Ложь;
		КонецЕсли;
		ОтобразитьПериоды("НастройкиУчетаНДС");
		
	КонецЕсли;
	
	КлассифицироватьНоменклатуруПоОперациям0 = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиКодОперации0Завершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчетаНДС.КодОперации0 = РезультатВыбора.Код;
	ПоУмолчаниюКодОперации0 = НастройкиУчетаНДС.КодОперации0;
	Модифицированность = Истина;
	ОтобразитьПериоды("НастройкиУчетаНДС");
	
КонецПроцедуры

#КонецОбласти

#Область ПодсказкаПриИзмененииСистемыНалогообложения

&НаКлиенте
Процедура СформироватьПодсказкуПриИзмененииСистемыНалогообложения()
	
	МассивСтрокПодсказки          = Новый Массив;
	МассивИдентификаторовНастроек = Новый Массив;
	
	// Установили применение УСН
	Если Не НастройкиСистемыНалогообложенияПередЗаписью.ПрименяетсяУСН
		И НастройкиСистемыНалогообложения.ПрименяетсяУСН Тогда
		ШаблонПодсказки =
			НСтр("ru = 'Вы включили Упрощенную систему налогообложения. Перейдите на закладку <b>УСН</b> для выполнения детальных настроек.'");
		
		ПодсказкаТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонПодсказки);
		
		ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ПодсказкаТекст);
		
		МассивИдентификаторовНастроек.Добавить("УСН");
	КонецЕсли;
	
	// Изменили объект налогообложения УСН
	Если (НастройкиСистемыНалогообложенияПередЗаписью.ПрименяетсяУСН
			И НастройкиСистемыНалогообложения.ПрименяетсяУСН)
		И (НастройкиСистемыНалогообложенияПередЗаписью.ПрименяетсяУСНДоходы = 
			НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходыМинусРасходы) Тогда
		ПредыдущийОбъект = ?(Не НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы,
			НСтр("ru='""доходы""'"),
			НСтр("ru='""доходы минус расходы""'"));
		ТекущийОбъект    = ?(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы,
			НСтр("ru='""доходы""'"),
			НСтр("ru='""доходы минус расходы""'"));
		
		ШаблонПодсказки =
			НСтр("ru = 'Вы изменили объект налогообложения УСН с %1 на %2. Перейдите на закладку <b>УСН</b> для выполнения детальных настроек.'");
		
		ПодсказкаТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонПодсказки, ПредыдущийОбъект, ТекущийОбъект);
		
		ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ПодсказкаТекст);
		
		МассивИдентификаторовНастроек.Добавить("УСН");
	КонецЕсли;
	
	// Включили начисление НДС при УСН
	Если НастройкиСистемыНалогообложенияПередЗаписью.СистемаНалогообложения = НастройкиСистемыНалогообложения.СистемаНалогообложения
		И НастройкиСистемыНалогообложенияПередЗаписью.ПлательщикНДСПрименяющийУСН = Ложь
		И НастройкиСистемыНалогообложения.ПлательщикНДСПрименяющийУСН = Истина
		Или ПрименениеНДСНаУСН Тогда
		ШаблонПодсказки =
			СтрШаблон(НСтр("ru = 'Вы включили уплату НДС при УСН. Перейдите на закладку <b>НДС</b> для выбора ставки налогообложения (5, 7 или %1).'"),
			УчетНДСКлиентСервер.ОбщаяСтавкаНДС(НастройкиСистемыНалогообложения.ДатаИзменения));
		
		ПодсказкаТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонПодсказки);
		ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ПодсказкаТекст);
		МассивИдентификаторовНастроек.Добавить("НДС");

	КонецЕсли;
	
	// Установили применение НДФЛ (ИП)
	Если Не НастройкиСистемыНалогообложенияПередЗаписью.ПлательщикНДФЛ
		И НастройкиСистемыНалогообложения.ПлательщикНДФЛ Тогда
		ШаблонПодсказки =
			НСтр("ru = 'Вы включили Общую систему налогообложения. Перейдите на закладки <b>НДФЛ (ИП)</b> и <b>НДС</b> для выполнения детальных настроек.'");
		
		ПодсказкаТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонПодсказки);
		
		ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ПодсказкаТекст);
		
		МассивИдентификаторовНастроек.Добавить("НДФЛ_Предприниматель");
		МассивИдентификаторовНастроек.Добавить("НДС");
	КонецЕсли;
	
	// Установили применение ОСН
	Если Не НастройкиСистемыНалогообложенияПередЗаписью.ПлательщикНалогаНаПрибыль
		И НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль Тогда
		ШаблонПодсказки =
			НСтр("ru = 'Вы включили Общую систему налогообложения. Перейдите на закладки <b>Налог на прибыль</b> и <b>НДС</b> для выполнения детальных настроек.'");
		
		ПодсказкаТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонПодсказки);
		
		ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ПодсказкаТекст);
		
		МассивИдентификаторовНастроек.Добавить("НалогНаПрибыль");
		МассивИдентификаторовНастроек.Добавить("НДС");
	КонецЕсли;
	
	// Установили применение ПСН
	Если Не НастройкиСистемыНалогообложенияПередЗаписью.ПрименяетсяУСНПатент
		И НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент Тогда
		ШаблонПодсказки =
			НСтр("ru = 'Вы включили Патентную систему налогообложения. Перейдите на закладку <b>Патенты</b> и добавьте патент.'");
		
		ПодсказкаТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонПодсказки);
		
		ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ПодсказкаТекст);
		
		МассивИдентификаторовНастроек.Добавить("Патент");
	КонецЕсли;
	
	// Установили применение Торгового сбора
	Если Не НастройкиСистемыНалогообложенияПередЗаписью.ПлательщикТорговогоСбора
		И НастройкиСистемыНалогообложения.ПлательщикТорговогоСбора Тогда
		ШаблонПодсказки =
			НСтр("ru = 'Вы включили использование Торгового сбора. Перейдите на закладку <b>Торговый сбор</b> и добавьте торговую точку.'");
		
		ПодсказкаТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонПодсказки);
		
		ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ПодсказкаТекст);
		
		МассивИдентификаторовНастроек.Добавить("ТорговыйСбор");
	КонецЕсли;
	
	Если МассивИдентификаторовНастроек.Количество() > 0 Тогда
		
		Элементы.ПодсказкаИзмененияСистемыНалогообложения.Заголовок = Новый ФорматированнаяСтрока(МассивСтрокПодсказки);
		
		ВыделитьСтрокиСпискаНастройкиНалоговОтчетов(МассивИдентификаторовНастроек);
		
		ПодключитьОбработчикОжиданияВыводаПодсказкиПриИзмененииСистемыНалогообложения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТекстПодсказкиВМассив(МассивСтрокПодсказки, ТекстПодсказки)

	Если МассивСтрокПодсказки.Количество() > 0 Тогда
		МассивСтрокПодсказки.Добавить(Новый ФорматированнаяСтрока(Символы.ПС + Символы.ПС));
	КонецЕсли;
	
	МассивСтрокПодсказки.Добавить(ТекстПодсказки);

КонецПроцедуры

&НаКлиенте
Процедура ВыделитьСтрокиСпискаНастройкиНалоговОтчетов(Идентификаторы)

	Для каждого Идентификатор Из Идентификаторы Цикл
		МассивСтрокПоИдентификатору = Список.НайтиСтроки(Новый Структура("Идентификатор", СокрЛП(Идентификатор)));
		
		Если МассивСтрокПоИдентификатору.Количество() > 0 Тогда
			СтрокаПоИдентификатору = МассивСтрокПоИдентификатору[0];
			СтрокаПоИдентификатору.Выделен = Истина;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияВыводаПодсказкиПриИзмененииСистемыНалогообложения()

	// После записи изменений системы налогообложения запускается длительная операция.
	// Для ожидания выполнения операции открывается страница ожидания.
	// Необходимо дождаться выполнения длительной операции, чтобы стали видимы настройки.
	// Поэтому подсказку выводим после выполнения всех операций.
	ПодключитьОбработчикОжидания(
		"Подключаемый_ПоказатьПодсказкуПриИзмененииСистемыНалогообложения",
		ИнтервалПроверкиВозможностиВыводаЗакрытияПодсказки(),
		Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияЗакрытияПодсказкиПриИзмененииСистемыНалогообложения()

	ПодключитьОбработчикОжидания(
		"Подключаемый_ОбработатьЗакрытиеПодсказкиПриИзмененииСистемыНалогообложения",
		ИнтервалПроверкиВозможностиВыводаЗакрытияПодсказки(),
		Истина);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьПодсказкуПриИзмененииСистемыНалогообложения() Экспорт
	
	НастройкиСистемыНалогообложенияВидимы =
		Элементы.ОбщиеСтраницыНастроек.ТекущаяСтраница = Элементы.ОбщаяСтраницаНастройки;
		
	Если Не НастройкиСистемыНалогообложенияВидимы Тогда
		ПодключитьОбработчикОжиданияВыводаПодсказкиПриИзмененииСистемыНалогообложения();
		Возврат;
	КонецЕсли;

	УстановитьДоступностьЭлементовСтраницыСистемыНалогообложения(Ложь);
	
	Элементы.СтраницыНастройкиСистемыНалогообложенияИстория.ТекущаяСтраница =
		Элементы.СтраницаНастройкиСистемыНалогообложенияПодсказка;
	
	Элементы.ГруппаНастройкиСистемыНалогообложенияПодсказка.Показать();
	ТекущийЭлемент = Элементы.ЗакрытьПодсказкуНастройкиСистемыНалогообложения;
	
	ПодключитьОбработчикОжиданияЗакрытияПодсказкиПриИзмененииСистемыНалогообложения();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьЗакрытиеПодсказкиПриИзмененииСистемыНалогообложения() Экспорт

	Если Не Элементы.ГруппаНастройкиСистемыНалогообложенияПодсказка.Скрыта() Тогда
		ПодключитьОбработчикОжиданияЗакрытияПодсказкиПриИзмененииСистемыНалогообложения();
		Возврат;
	КонецЕсли;

	УстановитьДоступностьЭлементовСтраницыСистемыНалогообложения(Истина);
	
	Элементы.СтраницыНастройкиСистемыНалогообложенияИстория.ТекущаяСтраница =
		Элементы.СтраницаНастройкиСистемыНалогообложенияПериоды;
	
	ТекущийЭлемент = Элементы.Список;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовСтраницыСистемыНалогообложения(Доступен)
	
	ИменаЭлементов = ИменаЭлементовСтраницыСистемыНалогообложенияДляУстановкиДоступности();
	
	Для каждого ИмяЭлемента Из ИменаЭлементов Цикл
	
		НайденныйЭлемент = Элементы.Найти(ИмяЭлемента);
	
		Если НайденныйЭлемент <> Неопределено Тогда
			НайденныйЭлемент.Доступность = Доступен;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ИменаЭлементовСтраницыСистемыНалогообложенияДляУстановкиДоступности()

	ИменаЭлементов = Новый Массив;
	
	ИменаЭлементов.Добавить("Заголовок_СистемаНалогообложения");
	ИменаЭлементов.Добавить("ПодсказкаИзмененияВСистемеНалогообложения");
	ИменаЭлементов.Добавить("ПодсказкаОграниченияТарифа");
	ИменаЭлементов.Добавить("ЗаголовокОсновнойСистемыНалогообложения");
	ИменаЭлементов.Добавить("СистемаНалогообложенияПредставление");
	ИменаЭлементов.Добавить("ПредупреждениеСистемаНалогообложенияНПДВнимание");
	ИменаЭлементов.Добавить("ТекстОшибкиПримененияСистемыНалогообложенияНПД");
	ИменаЭлементов.Добавить("ПредупреждениеСистемаНалогообложенияВнимание");
	ИменаЭлементов.Добавить("ТекстОшибкиПримененияСистемыНалогообложения");
	ИменаЭлементов.Добавить("НастройкиСистемыНалогообложенияПрименяетсяУСНПатент");
	ИменаЭлементов.Добавить("НастройкиСистемыНалогообложенияПлательщикЕНВД");
	ИменаЭлементов.Добавить("НастройкиСистемыНалогообложенияПлательщикТорговогоСбора");
	ИменаЭлементов.Добавить("НастройкиСистемыНалогообложенияПлательщикНДСПрименяющийУСН");
	ИменаЭлементов.Добавить("ПоложенияПереходногоПериодаУСН");
	ИменаЭлементов.Добавить("ДатаПереходаНаУСН");
	ИменаЭлементов.Добавить("НастройкиСистемыНалогообложенияПлательщикЕНП");
	ИменаЭлементов.Добавить("ДекорацияЕНП");
	
	Возврат ИменаЭлементов;

КонецФункции

&НаКлиенте
Функция ИнтервалПроверкиВозможностиВыводаЗакрытияПодсказки()

	// Устанавливаем минимальный интервал, чтобы изменения на форме выполнялись без задержек.
	Возврат 0.1;

КонецФункции

#КонецОбласти

#Область Акцизы

&НаСервере
Процедура ЗаполнитьНастройкиПоАкцизам()
	Элементы.ТаблицаНастроекПоАкцизамСпособПолученияТоваров.СписокВыбора.Очистить();
	Если ИмпортПодакцизныхТоваров Тогда
		Элементы.ТаблицаНастроекПоАкцизамСпособПолученияТоваров.СписокВыбора.Добавить("Импорт", "Импорт");
	КонецЕсли;
	Если ПроизводствоПодакцизныхТоваров Тогда
		Элементы.ТаблицаНастроекПоАкцизамСпособПолученияТоваров.СписокВыбора.Добавить("Производство", "Производство");
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыПодакцизныхТоваров.Ссылка КАК Ссылка,
	|	ВидыПодакцизныхТоваров.Порядок КАК Порядок,
	|	ПРЕДСТАВЛЕНИЕ(ВидыПодакцизныхТоваров.Ссылка) КАК Наименование,
	|	ВЫБОР
	|		КОГДА ПорядокУплатыАкцизов.ВидТовара ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Включен,
	|	ЕСТЬNULL(ПорядокУплатыАкцизов.ЛьготныйСрокУплаты, ЛОЖЬ) КАК ЛьготныйСрокУплаты,
	|	ЕСТЬNULL(ПорядокУплатыАкцизов.АвансыПриЗакупкеСырья, ЛОЖЬ) КАК АвансыПриЗакупкеСырья,
	|	ЕСТЬNULL(ПорядокУплатыАкцизов.Импорт, ЛОЖЬ) КАК Импорт,
	|	ЕСТЬNULL(ПорядокУплатыАкцизов.Производство, ЛОЖЬ) КАК Производство
	|ИЗ
	|	Перечисление.ВидыПодакцизныхТоваров КАК ВидыПодакцизныхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|		ПО ВидыПодакцизныхТоваров.Ссылка = ПорядокУплатыАкцизов.ВидТовара
	|			И (ПорядокУплатыАкцизов.Организация = &Организация)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	Запрос.УстановитьПараметр("Организация", Организация);
	Результат = Запрос.Выполнить().Выгрузить();
	// Акцизы
	НастройкиУплатыАкцизов = РегистрыСведений.ПорядокУплатыАкцизов.НастройкиУплатыАкцизов();
	ТаблицаНастроекПоАкцизам.Очистить();
	Для Каждого Строка Из Результат Цикл
		НоваяСтрока = ТаблицаНастроекПоАкцизам.Добавить();
		НоваяСтрока.ВидТовара = Строка.Ссылка;
		Если ИмпортПодакцизныхТоваров Или ПроизводствоПодакцизныхТоваров Тогда
			Если Строка.Импорт Тогда
				НоваяСтрока.СпособПолученияТоваров = "Импорт";
			ИначеЕсли Строка.Производство Тогда
				НоваяСтрока.СпособПолученияТоваров = "Производство";
			КонецЕсли;
		КонецЕсли;
		СтрокаНастроек = НастройкиУплатыАкцизов.Найти(Строка.Ссылка, "ВидТовара");
		НоваяСтрока.Включен = Строка.Включен;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Если СтрокаНастроек <> Неопределено Тогда
			СтруктураОтбора = Новый Структура("ЛьготныйСрокУплаты, АвансыПриЗакупкеСырья", НоваяСтрока.ЛьготныйСрокУплаты, НоваяСтрока.АвансыПриЗакупкеСырья);
			СтрокаПредставления = СтрокаНастроек.Варианты.НайтиСтроки(СтруктураОтбора);
			Если СтрокаПредставления.Количество() > 0 Тогда
				
				НоваяСтрока.ОсобыеУсловияПоАкцизам = СтрокаПредставления[0].Представление;
			Иначе
				НоваяСтрока.ОсобыеУсловияПоАкцизам = СтрокаНастроек.ЗаголовокВыбора;
			КонецЕсли;
			НоваяСтрока.ЗаголовокВыбора = СтрокаНастроек.ЗаголовокВыбора;
		КонецЕсли;
	КонецЦикла;
	ЗадачаАкцизы = Справочники.ЗадачиБухгалтера.НайтиПоКоду("Акцизы");
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПоАкцизамВыборЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если Не РезультатЗакрытия = КодВозвратаДиалога.Отмена Тогда
		ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
		
		Если РезультатЗакрытия = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		РезультатДетальнойНастройки = РезультатЗакрытия;
		
		Строка = ТаблицаНастроекПоАкцизам.НайтиПоИдентификатору(Элементы.ТаблицаНастроекПоАкцизам.ТекущаяСтрока);
		Если Строка = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Строка.ЛьготныйСрокУплаты = РезультатДетальнойНастройки.ЛьготныйСрокУплаты;
		Строка.АвансыПриЗакупкеСырья = РезультатДетальнойНастройки.АвансыПриЗакупкеСырья;
		Строка.Включен = Истина;
		Если Строка.Импорт = Ложь И Строка.Производство = Ложь Тогда
			Если НастройкиСистемыНалогообложения.ПрименяетсяУСН И ИмпортПодакцизныхТоваров Тогда
				Строка.Импорт = Истина;
				Строка.СпособПолученияТоваров = "Импорт";
			ИначеЕсли НЕ ПроизводствоПодакцизныхТоваров И ИмпортПодакцизныхТоваров Тогда
				Строка.Импорт = Истина;
				Строка.СпособПолученияТоваров = "Импорт";
			ИначеЕсли ПроизводствоПодакцизныхТоваров Тогда
				Строка.Производство = Истина;
				Строка.СпособПолученияТоваров = "Производство";
				
			КонецЕсли;
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура("ЛьготныйСрокУплаты, АвансыПриЗакупкеСырья", Строка.ЛьготныйСрокУплаты, Строка.АвансыПриЗакупкеСырья);
		Представление = ПолучитьНастройкиУплатыАкцизов(Строка.ВидТовара, СтруктураОтбора);
		
		Строка.ОсобыеУсловияПоАкцизам = Представление;
		ДанныеДляЗаписи = Новый Структура("ЛьготныйСрокУплаты, АвансыПриЗакупкеСырья, Импорт, Производство, ВидТовара",
		Строка.ЛьготныйСрокУплаты, Строка.АвансыПриЗакупкеСырья, Строка.Импорт, Строка.Производство, Строка.ВидТовара);
		ЗаписатьНастройкиПоАкцизам(ДанныеДляЗаписи, Организация);
		ОповеститьОбИзмененииНастроек();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиПоАкцизам(Строка, Организация)
	
	НаборЗаписейПорядокУплатыАкцизов = РегистрыСведений.ПорядокУплатыАкцизов.СоздатьНаборЗаписей();
	НаборЗаписейПорядокУплатыАкцизов.Отбор.Организация.Установить(Организация);
	НаборЗаписейПорядокУплатыАкцизов.Отбор.ВидТовара.Установить(Строка.ВидТовара);
	НаборЗаписейПорядокУплатыАкцизов.Прочитать();
	Если НаборЗаписейПорядокУплатыАкцизов.Количество() = 0 Тогда
		Запись = НаборЗаписейПорядокУплатыАкцизов.Добавить();
		Запись.Организация = Организация;
		Запись.ВидТовара   = Строка.ВидТовара;
	Иначе
		Запись = НаборЗаписейПорядокУплатыАкцизов[0];
	КонецЕсли;
	Если ИмпортПодакцизныхТоваров Или ПроизводствоПодакцизныхТоваров Тогда
		Запись.Импорт = Строка.Импорт;
		Запись.Производство = Строка.Производство;
	КонецЕсли;
	Запись.ЛьготныйСрокУплаты = Строка.ЛьготныйСрокУплаты;
	Запись.АвансыПриЗакупкеСырья = Строка.АвансыПриЗакупкеСырья;
	НаборЗаписейПорядокУплатыАкцизов.Записать();
	Если ИмпортПодакцизныхТоваров Или ПроизводствоПодакцизныхТоваров Тогда
		УчетАкцизов.ЗаполнитьКлассификаторыПоАкцизам(Организация, Строка.ВидТовара, Строка.Импорт);
	КонецЕсли;
	РегистрыСведений.ЗадачиБухгалтера.ОбновитьИЗаполнитьЗадачиНачалаРаботы(Организация);
	ИзмененыРегулярныеЗадачи = РегистрыСведений.ЗадачиБухгалтера.ОбновитьИЗаполнитьРегулярныеЗадачи(
	Организация, КалендарьБухгалтера.УпреждениеЗаполненияСписка(), , Ложь);
	МобильноеПриложениеПредприниматель.ОтправитьУведомленияОЗадачах();
	Если ИзмененыРегулярныеЗадачи Тогда
		СинхронизацияСКалендаремGoogle.СинхронизироватьЗадачи(Организация);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьНастройкиУплатыАкцизов(ВидТовара, СтруктураОтбора)
	ОсобыеУсловияПоАкцизам = "";
	НастройкиУплатыАкцизов = РегистрыСведений.ПорядокУплатыАкцизов.НастройкиУплатыАкцизов();
	СтрокаНастроек = НастройкиУплатыАкцизов.Найти(ВидТовара, "ВидТовара");
	Если СтрокаНастроек <> Неопределено Тогда
		СтрокаПредставления = СтрокаНастроек.Варианты.НайтиСтроки(СтруктураОтбора);
		Если СтрокаПредставления.Количество() > 0 Тогда
			
			ОсобыеУсловияПоАкцизам = СтрокаПредставления[0].Представление;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОсобыеУсловияПоАкцизам;
КонецФункции

&НаКлиенте
Процедура ТаблицаНастроекПоАкцизамВключенПриИзменении(Элемент)
	Строка = Элементы.ТаблицаНастроекПоАкцизам.ТекущиеДанные;
	Если Строка.Включен Тогда 
		
		Если Строка.Импорт = Ложь И Строка.Производство = Ложь Тогда
			Если НастройкиСистемыНалогообложения.ПрименяетсяУСН И ИмпортПодакцизныхТоваров Тогда
				Строка.Импорт = Истина;
				Строка.СпособПолученияТоваров = "Импорт";
			ИначеЕсли НЕ ПроизводствоПодакцизныхТоваров И ИмпортПодакцизныхТоваров Тогда
				Строка.Импорт = Истина;
				Строка.СпособПолученияТоваров = "Импорт";
			ИначеЕсли ПроизводствоПодакцизныхТоваров Тогда
				Строка.Производство = Истина;
				Строка.СпособПолученияТоваров = "Производство";
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеДляЗаписи = Новый Структура("ЛьготныйСрокУплаты, АвансыПриЗакупкеСырья, Импорт, Производство, ВидТовара",
		Строка.ЛьготныйСрокУплаты, Строка.АвансыПриЗакупкеСырья, Строка.Импорт, Строка.Производство, Строка.ВидТовара);
		ЗаписатьНастройкиПоАкцизам(ДанныеДляЗаписи, Организация); 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПоАкцизамСпособПолученияТоваровПриИзменении(Элемент)
	Строка = Элементы.ТаблицаНастроекПоАкцизам.ТекущиеДанные;
	Если Строка.СпособПолученияТоваров = "Импорт" Тогда
		Строка.Импорт = Истина;
		Строка.Производство = Ложь;
	ИначеЕсли Строка.СпособПолученияТоваров = "Производство" Тогда
		Строка.Импорт = Ложь;
		Строка.Производство = Истина;
	Иначе
		Строка.Импорт = Ложь;
		Строка.Производство = Ложь;
		Строка.СпособПолученияТоваров = "";
	КонецЕсли;
	Если Строка.Импорт Или Строка.Производство Тогда
		Строка.Включен = Истина;
		ДанныеДляЗаписи = Новый Структура("ЛьготныйСрокУплаты, АвансыПриЗакупкеСырья, Импорт, Производство, ВидТовара",
		Строка.ЛьготныйСрокУплаты, Строка.АвансыПриЗакупкеСырья, Строка.Импорт, Строка.Производство, Строка.ВидТовара);
		ЗаписатьНастройкиПоАкцизам(ДанныеДляЗаписи, Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПоАкцизамВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТаблицаНастроекПоАкцизамОсобыеУсловияПоАкцизам" Тогда
		ТекущиеДанные = Элементы.ТаблицаНастроекПоАкцизам.ТекущиеДанные;
		Если Не ТекущиеДанные.ЗаголовокВыбора = "" Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ПредметНастройки",                 ТекущиеДанные.ВидТовара);
			ПараметрыФормы.Вставить("НалогОтчет",                       ЗадачаАкцизы);
			ПараметрыФормы.Вставить("АдресЗначенияДетальнойНастройки",  АдресЗначенияДетальнойНастройки);
			ПараметрыФормы.Вставить("АдресПараметрыДетальнойНастройки", АдресПараметрыДетальнойНастройки);
			ПараметрыФормы.Вставить("ЛьготныйСрокУплаты",               ТекущиеДанные.ЛьготныйСрокУплаты);
			ПараметрыФормы.Вставить("АвансыПриЗакупкеСырья",            ТекущиеДанные.АвансыПриЗакупкеСырья);
			ПараметрыФормы.Вставить("ЗаголовокВыбора",                  ТекущиеДанные.ЗаголовокВыбора);
			ПараметрыФормы.Вставить("ТолькоПросмотр",                   Не ПравоРедактированияАкцизов);
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ВыбраннаяСтрока",   Элементы.ТаблицаНастроекПоАкцизам.ТекущаяСтрока);
			
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ТаблицаНастроекПоАкцизамВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
			ОткрытьФорму("РегистрСведений.НалогиОтчеты.Форма.ДетальнаяНастройка", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПоАкцизамОсобыеУсловияПоАкцизамПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТаблицаНастроекПоАкцизам.ТекущиеДанные;
	ТекущиеДанные.ОсобыеУсловияПоАкцизам = "";
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция ДатаПереходаНаУСН(Организация)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВозможенПереходНаУСН") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыПерехода = Обработки.ПомощникПоПереходуНаУСН.ПараметрыПереходаНаУСН(Организация);
	
	Возврат ПараметрыПерехода.ДатаПереходаНаУСН;
	
КонецФункции

&НаСервере
Процедура ЗакрытьПодсказкуПомощникПоПереходуНаУСННаСервере()
	
	СохранитьНастройкуПодсказкиПомощникПоПереходуНаУСН(Организация, Ложь);
	НастройкаПодсказкиПомощникПоПереходуНаУСН(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкуПодсказкиПомощникПоПереходуНаУСН(Организация, ЗначениеНастройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ВидимостьПодсказокПомощникПоПереходуНаУСННастройкиНалоговИОтчетов",
		КлючНастройкиПодсказкиПомощникПоПереходуНаУСН(Организация),
		ЗначениеНастройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидимостьПодсказкиПомощникПоПереходуНаУСН(Организация)
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ВидимостьПодсказокПомощникПоПереходуНаУСННастройкиНалоговИОтчетов",
		КлючНастройкиПодсказкиПомощникПоПереходуНаУСН(Организация),
		Истина);
		
КонецФункции

&НаСервереБезКонтекста
Функция КлючНастройкиПодсказкиПомощникПоПереходуНаУСН(Организация)
	
	Возврат СтрШаблон("%1_%2", Организация.УникальныйИдентификатор(), "ПомощникПоПереходуНаУСН");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастройкаПодсказкиПомощникПоПереходуНаУСН(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ГруппаПомощникПоПереходуНаУСН.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(Форма.Организация) Или Не ВидимостьПодсказкиПомощникПоПереходуНаУСН(Форма.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПереходаНаУСН = ДатаПереходаНаУСН(Форма.Организация);
	
	Если Не ЗначениеЗаполнено(ДатаПереходаНаУСН) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПомощникПоПереходуНаУСН.Видимость = Истина;
	
	Если ДатаПереходаНаУСН < Форма.ТекущаяДата Тогда
		ШаблонПоясненияСтрока = НСтр("ru='С %1 года вы перешли на УСН. Рекомендуем воспользоваться '");
	Иначе
		ШаблонПоясненияСтрока = НСтр("ru='С %1 года вы переходите на УСН. Рекомендуем воспользоваться '");
	КонецЕсли;
	
	Элементы.ПодсказкаПомощникПоПереходуНаУСН.Заголовок = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(СтрШаблон(ШаблонПоясненияСтрока, Формат(ДатаПереходаНаУСН, "ДФ=гггг"))),
		Новый ФорматированнаяСтрока(НСтр("ru='Помощником по переходу на УСН'"), , , , "ПерейтиВПомощникПоПереходуНаУСН"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросВключенияРасчетов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		_ОКПродолжить(ДополнительныеПараметры.ИмяРегистра);
		УстановитьЗначенияРеквизитовБезЗакрывающихДокументов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПереходВНастройки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ТекущаяСтраница", "ГруппаРасчеты");
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыОткрытияФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияРеквизитовБезЗакрывающихДокументов()
	РежимБезЗакрывающихДокументов = РежимБезЗакрывающихДокументов();
	ВозможноИзменитьФункциональностьРасчеты =
		Обработки.ФункциональностьПрограммы.РазрешеноИспользоватьФункциональностьВДоступномИнтерфейсе("ВестиУчетРасчетовСКонтрагентами");
КонецПроцедуры
	
&НаСервереБезКонтекста
Функция РежимБезЗакрывающихДокументов()
	
	Возврат Не Константы.ВестиУчетРасчетовСКонтрагентами.Получить();
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаголовокДобровольныеВзносыПредпринимателя(ЭтоИП)
	
	Если ЭтоИП Тогда
		Возврат СтрШаблон(НСтр("ru = 'Предприниматель уплачивает взносы в фонды (за себя):'"));
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru = 'Самозанятый уплачивает взносы в фонды:'"));
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаголовокСтраховыеВзносыПредпринимателя(ЭтоИП)
	
	Если ЭтоИП Тогда
		Возврат СтрШаблон(НСтр("ru = 'Страховые взносы предпринимателя'"));
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru = 'Страховые взносы самозанятого'"));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокСтраховыхСуммСамозанятых()
	
	Элементы.НастройкиУчетаСтраховыхВзносовИПСтраховаяСумма.СписокВыбора.Очистить();
	СтраховыеСуммы = УчетСтраховыхВзносовИП.СтраховыеСуммыНаСоциальноеСтрахованиеСамозанятых(ДатаИзменения);
	Для Каждого ТекущаяСумма Из СтраховыеСуммы Цикл
		Элементы.НастройкиУчетаСтраховыхВзносовИПСтраховаяСумма.СписокВыбора.Добавить(
			ТекущаяСумма.Значение,
			ТекущаяСумма.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСтраховогоСлучаяВСвязиСМатеринством()
	
	НастройкиУчетаСтраховыхВзносовИП.СтраховаяСумма = ПредопределенноеЗначение("Перечисление.ПособияНаСоциальноеСтрахованиеСамозанятых.ПоУмолчанию");
	Если Не СтраховойСлучайВСвязиСМатеринством
		И УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(ДатаИзменения) Тогда
		
		НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай =
			ПредопределенноеЗначение("Перечисление.СлучаиСоциальногоСтрахованияСамозанятых.ВременнаяНетрудоспособность");
	Иначе
		НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай = УчетСтраховыхВзносовИПКлиентСервер.СтраховойСлучайПоУмолчанию();
	КонецЕсли;
	
	ОтобразитьПериоды("НастройкиУчетаСтраховыхВзносовИП");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыФормыДляСамозанятых()
	
	Если Не ПрименяетсяНПДНаМоментОткрытия Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокСтраховыхСуммСамозанятых();
	Если УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(ДатаИзменения) Тогда
		СтраховойСлучайВСвязиСМатеринством = Не ИндивидуальныйПредпринимательМужчина(Организация);
	Иначе
		СтраховойСлучайВСвязиСМатеринством = Истина;
	КонецЕсли;
	
	ПриИзмененииСтраховогоСлучаяВСвязиСМатеринством();
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьРеквизитыФормыДляИП()
	
	Если ЭтоЮрЛицо Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоИП = Справочники.Организации.ЭтоИП(Организация);
	Элементы.УплачиватьДобровольныеВзносыВФСС.Подсказка =
		УчетСтраховыхВзносовИПКлиентСервер.ТекстПодсказкиДобровольныеВзносыПредпринимателя(
			ЭтоИП,
			ПрименяетсяНПДНаМоментОткрытия);
	
	Элементы.СписокСтраховыхВзносовИП.Заголовок = ЗаголовокДобровольныеВзносыПредпринимателя(ЭтоИП);
	Элементы.Заголовок_СтраховыеВзносы_Предприниматель.Заголовок = ЗаголовокСтраховыеВзносыПредпринимателя(ЭтоИП);
	
	Если Не ЭтоИП Тогда
		// Для самозанятого всегда доступен только один страховой случай
		НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай = Перечисления.СлучаиСоциальногоСтрахованияСамозанятых.ВременнаяНетрудоспособность;
	ИначеЕсли Не ЗначениеЗаполнено(НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай) Тогда
		
		Если Не НастройкиУчетаСтраховыхВзносовИП.УплачиватьДобровольныеВзносыВФСС
			И ПрименяетсяНПДНаМоментОткрытия
			И ИндивидуальныйПредпринимательМужчина(Организация)
			И УчетСтраховыхВзносовИПКлиентСервер.РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(ДатаИзменения) Тогда
			// По умолчанию флаг с материнством не ставим для самозанятых ИП мужчин
			НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай = Перечисления.СлучаиСоциальногоСтрахованияСамозанятых.ВременнаяНетрудоспособность;
		Иначе
			НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай = УчетСтраховыхВзносовИПКлиентСервер.СтраховойСлучайПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	
	СтраховойСлучайВСвязиСМатеринством = Не УчетСтраховыхВзносовИПКлиентСервер.ЭтоСтраховойСлучайНаУплатуБольничного(
		НастройкиУчетаСтраховыхВзносовИП.СтраховойСлучай);
	
	Если ПрименяетсяНПДНаМоментОткрытия Тогда
		
		Если Не ЗначениеЗаполнено(НастройкиУчетаСтраховыхВзносовИП.СтраховаяСумма) Тогда
			НастройкиУчетаСтраховыхВзносовИП.СтраховаяСумма = Перечисления.ПособияНаСоциальноеСтрахованиеСамозанятых.ПоУмолчанию;
		КонецЕсли;
		
		ЗаполнитьСписокСтраховыхСуммСамозанятых();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИндивидуальныйПредпринимательМужчина(Организация)
	
	Возврат Справочники.Организации.ИндивидуальныйПредпринимательМужчина(Организация);
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	
	Элементы.Список.УстановитьДействие("ПередНачаломИзменения", "СписокПередНачаломИзменения");
	
	ДобавитьЭлементыФормыМК();
	НастроитьПоложениеЭлементовМК();
	УстановитьВидимостьЭлементовМК();
	НастроитьОтображениеЭлементовМК();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыФормыМК()
	
	НовыйЭлемент = Элементы.Добавить("СписокНастроекМК", Тип("ГруппаФормы"), Элементы.ГруппаНастроек);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	НовыйЭлемент.Заголовок = НСтр("ru='Все настройки'");
	НовыйЭлемент.ЗаголовокСвернутогоОтображения = НСтр("ru='Показать все настройки'");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовМК()
	
	Элементы.ГруппаОжиданиеОплатыТарифа.Видимость                          = Ложь;
	Элементы.БаннерСФоном.Видимость                                        = Ложь;
	Элементы.ОбщаяСтраницаОжидание.Видимость                               = Ложь;
	Элементы.КартинкаИнформация.Видимость                                  = Ложь;
	Элементы.СтраницаНастройкиСистемыНалогообложенияПодсказка.Видимость    = Ложь;
	Элементы.ГруппаПредупреждениеЕНВД.Видимость                            = Ложь;
	Элементы.ГруппаИзмененияВСистемеНалогообложения.Видимость              = Ложь;
	Элементы.ГруппаОграниченияТарифа.Видимость                             = Ложь;
	Элементы.ГруппаПомощникПоПереходуНаУСН.Видимость                       = Ложь;
	Элементы.ДекорацияПодсказкаВариантПрямыхРасходовПоПравиламБУ.Видимость = Ложь;
	Элементы.ГруппаБаннер_НалогНаИмущество.Видимость                       = Ложь;
	Элементы.ПодсказкаЛьготы.Видимость                                     = Ложь;
	Элементы.ТаблицаНастроекПоАкцизамСпособПолученияТоваров.Видимость      = Ложь;
	Элементы.ТаблицаНастроекПоАкцизамОсобыеУсловияПоАкцизам.Видимость      = Ложь;
	Элементы.СтраницаНастройкиУчетаНалогаНаПрибыльИзменение.Видимость      = Ложь;
	Элементы.СтраницаНастройкиУчетаНДФЛИзменение.Видимость                 = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеПанелиИзмененияМК()
	
	ИменаРазделов = Новый Массив;
	АкцентныеКнопки = Новый Массив;
	
	СобратьИменаРазделовРекурсивно(Элементы.СтраницыНастроек.ПодчиненныеЭлементы, ИменаРазделов);
	
	Для каждого Раздел Из ИменаРазделов Цикл
		ИмяСтраницыИзменений = "Страница" + Раздел.ИмяРаздела + "Изменение";
		
		ИмяГруппыКоманд = "Команды" + Раздел.ИмяРаздела + "ИзменениеМК";
		
		СтраницаИзменения = Элементы[ИмяСтраницыИзменений];
		
		СтраницаИзменения.Видимость = Ложь;
		
		НовыйЭлемент = Элементы.Добавить(ИмяГруппыКоманд, Тип("ГруппаФормы"), СтраницаИзменения);
		НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		НовыйЭлемент.ОтображатьЗаголовок = Ложь;
		НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
		НовыйЭлемент.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
		НовыйЭлемент.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
		НовыйЭлемент.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
		НовыйЭлемент.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Центр;
		
		ИмяГруппыИстории = "Группа" + Раздел.ИмяРаздела + "История";
		
		Элементы.Переместить(Элементы[ИмяГруппыИстории], Элементы[Раздел.ИмяКорня]);
		
		КнопкаОК = Элементы["ОК_" + Раздел.ИмяРаздела];
		КнопкаОтменить = Элементы["Отменить_" + Раздел.ИмяРаздела];
		
		Элементы.Переместить(КнопкаОК, НовыйЭлемент);
		Элементы.Переместить(КнопкаОтменить, НовыйЭлемент);
		
		КнопкаОтменить.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
		КнопкаОтменить.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
		
		АкцентныеКнопки.Добавить(КнопкаОК);
		
		СтраницаИзменения.ЦветФона = ЦветаСтиля.ИтогиФонГруппы;
	КонецЦикла;
	
	АкцентныеКнопки.Добавить(Элементы.ЗакрытьПодсказкуНастройкиСистемыНалогообложения);
	
	МобильныйКлиентБПФормаОбъекта.ОформитьАкцентнуюКнопкуФормы(АкцентныеКнопки, 20);
	
	Для каждого Кнопка Из АкцентныеКнопки Цикл
		Кнопка.РастягиватьПоГоризонтали = Ложь;
		Кнопка.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
		Кнопка.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СобратьИменаРазделовРекурсивно(ЭлементыФормы, ИменаРазделов, СтруктураРаздела = Неопределено)
	
	Для каждого Элемент Из ЭлементыФормы Цикл
		Если СтрНайти(Элемент.Имя, "Страница_") <> 0 Тогда
			СтруктураРаздела = Новый Структура("ИмяКорня, ИмяРаздела", Элемент.Имя);
		КонецЕсли;
		
		Если СтрНайти(Элемент.Имя, "ОК_") <> 0 Тогда
			ПозицияРазделителя = СтрНайти(Элемент.Имя, "_");
			СтруктураРаздела.ИмяРаздела = Сред(Элемент.Имя, ПозицияРазделителя + 1);
			ИменаРазделов.Добавить(СтруктураРаздела);
		КонецЕсли;
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			СобратьИменаРазделовРекурсивно(Элемент.ПодчиненныеЭлементы, ИменаРазделов, СтруктураРаздела);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеНедоступнымиНастройкамиМК()
	
	НедоступныеНастройки = Новый Массив;
	
	НедоступныеНастройки.Добавить(Элементы.Страница_ЕНВД);
	НедоступныеНастройки.Добавить(Элементы.Страница_НалогНаИмущество);
	НедоступныеНастройки.Добавить(Элементы.Страница_СтраховыеВзносы);
	НедоступныеНастройки.Добавить(Элементы.Страница_ТорговыйСбор);
	НедоступныеНастройки.Добавить(Элементы.Страница_ТранспортныйНалог);
	НедоступныеНастройки.Добавить(Элементы.Страница_ЗемельныйНалог);
	НедоступныеНастройки.Добавить(Элементы.Страница_СтатистическаяОтчетность);
	НедоступныеНастройки.Добавить(Элементы.Страница_Акцизы);
	НедоступныеНастройки.Добавить(Элементы.Страница_Таблица);
	
	УправлениеНедоступнымиНастройками(НедоступныеНастройки);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеНедоступнымиНастройками(КоллекцияЭлементов)
	
	Для каждого ЭлементМассива Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементМассива) <> Тип("ГруппаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементМассива.Доступность = Ложь;
		
		Если СтрНайти(ЭлементМассива.Имя, "Страница") <> 0 Тогда
			ИмяЗаголовка = СтрЗаменить(ЭлементМассива.Имя, "Страница", "Заголовок");
			ИмяБаннера = СтрЗаменить(ЭлементМассива.Имя, "Страница", "БаннерМК");
		ИначеЕсли СтрНайти(ЭлементМассива.Имя, "Группа") <> 0 Тогда
			ИмяЗаголовка = СтрЗаменить(ЭлементМассива.Имя, "Группа", "Заголовок");
			ИмяБаннера = СтрЗаменить(ЭлементМассива.Имя, "Группа", "БаннерМК");
		Иначе
			Возврат;
		КонецЕсли;
		
		СоздатьБаннерНедоступнойНастройкиМК(ИмяБаннера, ЭлементМассива, Элементы[ИмяЗаголовка]);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьБаннерНедоступнойНастройкиМК(ИмяБаннера, ЭлементРодитель, Положение = Неопределено) Экспорт
	
	НовыйЭлемент = Элементы.Добавить(ИмяБаннера, Тип("ДекорацияФормы"), ЭлементРодитель);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = НСтр("ru='Настройку этого раздела можно выполнить в версии для компьютера'");
	НовыйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
	НовыйЭлемент.Высота = 3;
	НовыйЭлемент.ЦветФона = ЦветаСтиля.ЦветБаннераБежевый;
	
	Если Положение <> Неопределено Тогда
		Элементы.Переместить(НовыйЭлемент, ЭлементРодитель, Положение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПоложениеЗаголовкаФлажкаРекурсивноМК(ЭлементыФормы)
	
	Для каждого Элемент Из ЭлементыФормы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			Если Элемент.Вид = ВидПоляФормы.ПолеФлажка Тогда
				Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
				Элемент.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			УстановитьПоложениеЗаголовкаФлажкаРекурсивноМК(Элемент.ПодчиненныеЭлементы);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидПереключателяМК(ЭлементыФормы)
	
	Для каждого Элемент Из ЭлементыФормы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			Если Элемент.Вид = ВидПоляФормы.ПолеПереключателя Тогда
				Элемент.ВидПереключателя = ВидПереключателя.Переключатель;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			УстановитьВидПереключателяМК(Элемент.ПодчиненныеЭлементы);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВертикальныйИнтервалЭлементовМК()
	
	Элементы.ГруппаЗаголовок_СистемаНалогообложения.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Авто;
	Элементы.ГруппаСпецрежим.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	Элементы.НастройкиСистемыНалогообложенияНастройка.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Авто;
	Элементы.ГруппаСистемаНалогообложенияПредставление.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Авто;
	Элементы.ГруппаПереходНаУСН.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Авто;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРастягиваниеЭлементовМК()
	
	Элементы.Список.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаНастройкиСистемыНалогообложения.РастягиватьПоВертикали = Ложь;
	Элементы.ВариантУчетаКурсовыхРазниц2022Список.РастягиватьПоВертикали = Истина;
	Элементы.СтавкаНалогаУСН.РастягиватьПоГоризонтали = Истина;	
	Элементы.СтавкаНалогаУСНПовышенная.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаПорядокПризнанияРасходовЛево.РастягиватьПоГоризонтали = Истина;
	Элементы.ОснованиеЛьготыНомерСтатьи.РастягиватьПоГоризонтали = Истина;
	Элементы.ОснованиеЛьготыПункт.РастягиватьПоГоризонтали = Истина;
	Элементы.ОснованиеЛьготыПодпункт.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаОснованиеЛьготыУСН.РастягиватьПоГоризонтали = Истина;
	Элементы.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМСТариф.РастягиватьПоГоризонтали = Истина;
	Элементы.РайонныйКоэффициентИП.РастягиватьПоГоризонтали = Истина;
	
КонецПроцедуры

&НаСервере
Процедура АдаптацияШапкиНастроекМК()
	
	Элементы.Список.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.Список.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.Список.РежимВыбора = Истина;
	Элементы.Список.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Авто;
	Элементы.Список.Ширина = 0;
	
	Элементы.ТекстИнформация.Заголовок = "";
	
КонецПроцедуры

&НаСервере
Процедура АдаптацияСтраницаНалогообложениеМК()
	
	Элементы.СтраницаНастройкиСистемыНалогообложенияИзменение.ЦветФона = ЦветаСтиля.ЦветФонаВсеПоля;
	
	Элементы.ГруппаНастройкиСистемыНалогообложенияИстория.ОтображатьЗаголовок = Истина;
	
	Элементы.ЗаголовокОсновнойСистемыНалогообложения.Заголовок = НСтр("ru='Основная система налогообложения'");
	
	Элементы.ГруппаСпецрежим.ОтображатьЗаголовок = Ложь;
	
	Элементы.ГруппаНастройкиСистемыНалогообложенияПодсказка.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ГруппаНастройкиСистемыНалогообложенияПодсказка.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Верх;
	
	Элементы.ЗаголовокПодсказкиИзмененияСистемыНалогообложения.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ЗаголовокПодсказкиИзмененияСистемыНалогообложения.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Верх;
	Элементы.ЗаголовокПодсказкиИзмененияСистемыНалогообложения.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ЗаголовокПодсказкиИзмененияСистемыНалогообложения.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Верх;
	
	Элементы.ПодсказкаИзмененияСистемыНалогообложения.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	Элементы.ПодсказкаИзмененияСистемыНалогообложения.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Верх;
	
КонецПроцедуры

&НаСервере
Процедура АдаптацияСтраницаНалогНаПрибыльМК()
	
	Элементы.МетодНачисленияАмортизации1.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	
	Элементы.ВариантУчетаКурсовыхРазниц2022Список.МногострочныйРежим = Истина;
	Элементы.ВариантУчетаКурсовыхРазниц2022Список.КнопкаВыбора = Истина;
	Элементы.ВариантУчетаКурсовыхРазниц2022Список.КнопкаВыпадающегоСписка = Истина;
	Элементы.ВариантУчетаКурсовыхРазниц2022Список.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	
	Элементы.ВариантПрямыхРасходовПроизводстваПоПравиламБУ.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	Элементы.ВариантПрямыхРасходовПроизводства.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	
КонецПроцедуры

&НаСервере
Процедура АдаптацияСтраницаУСНМК()
	
	Элементы.ГруппаПорядокПризнанияРасходовЛево.Ширина = 0;
	
	Элементы.ОснованиеЛьготыНомерСтатьи.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.ОснованиеЛьготыПункт.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.ОснованиеЛьготыПодпункт.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	
	Элементы.ГруппаСтавкиУСНВВашемРегионе.Ширина = 0;
	Элементы.ГруппаСтавкиУСНВВашемРегионе.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаСтавкиУСНВВашемРегионе.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.ГруппаСтавкаУСН.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.ГруппаНастройкиУчетаУСН.РастягиватьПоГоризонтали = Истина;
	
КонецПроцедуры

&НаСервере
Процедура АдаптацияСтраницаСтраховыеВзносыМК()
	
	Элементы.ВидТарифа.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	Элементы.ВидТарифа.КнопкаВыбора = Истина;
	Элементы.ВидТарифа.КнопкаВыпадающегоСписка = Истина;
	
КонецПроцедуры

&НаСервере
Процедура АдаптацияСтраницаСтраховыеВзносыИПМК()
	
	Элементы.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМСТариф.КнопкаВыбора = Истина;
	Элементы.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМСТариф.КнопкаВыпадающегоСписка = Истина;
	Элементы.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМСТариф.Заголовок = НСтр("ru='Тариф'");
	
	Элементы.ГруппаВзносыИППоЕдиномуТарифу.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
КонецПроцедуры

&НаСервере
Процедура АдаптацияСтраницаНДСМК()
	
	Элементы.СложныйУчетНДС.Доступность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеЭлементовМК()
	
	УправлениеНедоступнымиНастройкамиМК();
	УстановитьПоложениеЗаголовкаФлажкаРекурсивноМК(Элементы);
	НастроитьОтображениеПанелиИзмененияМК();
	УстановитьВертикальныйИнтервалЭлементовМК();
	УстановитьВидПереключателяМК(Элементы);
	УстановитьРастягиваниеЭлементовМК();
	
	АдаптацияШапкиНастроекМК();
	АдаптацияСтраницаНалогообложениеМК();
	АдаптацияСтраницаНалогНаПрибыльМК();
	АдаптацияСтраницаУСНМК();
	АдаптацияСтраницаНДСМК();
	АдаптацияСтраницаСтраховыеВзносыМК();
	АдаптацияСтраницаСтраховыеВзносыИПМК();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоложениеЭлементовМК()
	
	Элементы.Переместить(Элементы.СписокНастроекМК, Элементы.ГруппаНастроек, Элементы.СтраницыНастроек);
	Элементы.Переместить(Элементы.Список, Элементы.СписокНастроекМК);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВыбораТарифаВзносовИП_ПФР_ФФОМС(Период)
	
	ПараметрыВыбораТарифа = Новый Структура("Отбор, СтрокаПоиска", Неопределено, "");
	ПараметрыВыбораТарифа.Вставить("Период", Период);
	Возврат Перечисления.ИПТарифыФиксированныхВзносов.ПолучитьДанныеВыбора(ПараметрыВыбораТарифа);
	
КонецФункции

#КонецОбласти
