
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.АдресПараметровВХранилище) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);
	
	ДатаНачалаСобытия 			= ПараметрыРасчета.ДатаНачалаСобытия;
	Сотрудник 					= ПараметрыРасчета.Сотрудник;
	Организация 				= ПараметрыРасчета.Организация;
	ПричинаНетрудоспособности 	= ПараметрыРасчета.ПричинаНетрудоспособности;
	РайонныйКоэффициентРФНаНачалоСобытия = ПараметрыРасчета.РайонныйКоэффициентРФНаНачалоСобытия;
	
	Если ПричинаНетрудоспособности <> Перечисления.ПричиныНетрудоспособности.ПоБеременностиИРодам Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДекорацияМесяцы",
			"Высота",
			1);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПервыйГодДни",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Год1Месяц1",
			"ПоложениеЗаголовка",
			ПоложениеЗаголовкаЭлементаФормы.Нет);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВторойГодДни",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Год2Месяц1",
			"ПоложениеЗаголовка",
			ПоложениеЗаголовкаЭлементаФормы.Нет);
			
	КонецЕсли; 
	
	ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо");
	
	Год1 = ПараметрыРасчета.ПервыйГод;
	Год2 = ПараметрыРасчета.ВторойГод;
	НастроитьМаксимальноеКоличествоДнейВФевраля(ЭтаФорма);
	
	Для Каждого СтрокаСреднего Из ПараметрыРасчета.СреднийЗаработокФСС Цикл
		ЗаполнитьЗначенияСвойств(СреднийЗаработокФСС.Добавить(), СтрокаСреднего);
	КонецЦикла;
	ЗагрузитьСреднийЗаработокФСС(ПараметрыРасчета.ОтработанноеВремяДляСреднегоФСС);
	
	РассчитатьСреднийЗаработок();
	
	УстановитьПодсказкуКРасчетуСреднегоЗаработка();
	
	Если ТолькоПросмотр Тогда
		Заголовок = НСтр("ru = 'Данные для расчета среднего заработка (только просмотр)'");
	Иначе
		Заголовок = НСтр("ru = 'Ввод данных для расчета среднего заработка'"); 
	КонецЕсли;
	
	Если ТолькоПросмотр Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"OK",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Отмена",
			"Заголовок",
			НСтр("ru='Закрыть'"));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Отмена",
			"КнопкаПоУмолчанию",
			Истина);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Год1ПриИзменении(Элемент)
	
	ПриИзмененииРасчетногоГода(0);
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Регулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Год1 = Год1 + Направление;
	СтандартнаяОбработка = Ложь;
	Модифицированность = Истина;
	
	ПриИзмененииРасчетногоГода(0);
	
КонецПроцедуры

&НаКлиенте
Процедура Год2ПриИзменении(Элемент)
	
	ПриИзмененииРасчетногоГода(1);
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Регулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Год2 = Год2 + Направление;
	СтандартнаяОбработка = Ложь;
	Модифицированность = Истина;
	
	ПриИзмененииРасчетногоГода(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ГодМесяцПриИзменении(Элемент)
	
	ГодМесяцПриИзмененииНаСервере();
	
	УстановитьПодсказкуКРасчетуСреднегоЗаработка();
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц1НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц2НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц3НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц4НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц5НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц6НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц7НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц8НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц9НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц10НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц11НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц12НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц1НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц2НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц3НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц4НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц5НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц6НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц7НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц8НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц9НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц10НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц11НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц12НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьДанныеДляСреднегоЗаработка(ПериодЭлементаДанных(Элемент));
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура OK(Команда)
	
	Если НЕ Модифицированность ИЛИ ТолькоПросмотр Тогда
		Закрыть();
	Иначе
		ПодготовитьФормуКПринятиюИзменений(ВладелецФормы.УникальныйИдентификатор);
		Закрыть(АдресВХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьДанныеУчета(Команда)
	
	ДанныеЗаполнены = Ложь;
	Для НомерГода = 1 По 2 Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			Заработок = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтаФорма, "Год" + НомерГода + "Месяц" + НомерМесяца);
				
			ДнейБолезниУходаЗаДетьми = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтаФорма, "Год" + НомерГода + "ДниУхода" + НомерМесяца);
				
			Если Заработок <> 0 ИЛИ ДнейБолезниУходаЗаДетьми <> 0 Тогда
				ДанныеЗаполнены = Истина;
				Прервать;
			КонецЕсли; 
				
		КонецЦикла;
		
		Если ДанныеЗаполнены Тогда
			Прервать;
		КонецЕсли; 
				
	КонецЦикла;
	
	Если ДанныеЗаполнены Тогда
		
		ТекстВопроса = НСтр("ru='Перед заполнением введенные данные будут очищены.
			|Продолжить?'");
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьДанныеУчетаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		ПеречитатьДанныеУчетаЗавершение(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеречитатьДанныеУчетаЗавершение(Знач Результат, Знач Параметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоДаннымУчета();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуКПринятиюИзменений(ИдентификаторВладельца)
	
	АдресВХранилище = АдресПараметровВХранилище(ИдентификаторВладельца);
	
КонецПроцедуры

&НаСервере
Функция АдресПараметровВХранилище(ИдентификаторВладельца)
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Год1);
	РасчетныеГоды.Добавить(Год2);
	
	НомерГода = 1;
	ОтработанноеВремяДляСреднегоФСС = Новый Массив;
	Для каждого Год Из РасчетныеГоды Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			ДнейБолезниУходаЗаДетьми = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтаФорма, "Год" + НомерГода + "ДниУхода" + НомерМесяца);
				
			Если ДнейБолезниУходаЗаДетьми <> 0 Тогда
				
				ЗаписьОВремени = Новый Структура;
				ЗаписьОВремени.Вставить("ФизическоеЛицо", ФизическоеЛицо);
				ЗаписьОВремени.Вставить("Период", Дата(Год, НомерМесяца, 1));
				ЗаписьОВремени.Вставить("ДнейБолезниУходаЗаДетьми", ДнейБолезниУходаЗаДетьми);
				
				ОтработанноеВремяДляСреднегоФСС.Добавить(ЗаписьОВремени);
				
			КонецЕсли; 
			
		КонецЦикла;
		
		НомерГода = НомерГода + 1;
		
	КонецЦикла;
	
	Для Каждого СтрокаСреднего Из СреднийЗаработокФСС Цикл
		СтрокаСреднего.ФизическоеЛицо = ФизическоеЛицо;
	КонецЦикла;
	
	ПараметрыРасчета = ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
	СреднийЗаработок = УчетПособийСоциальногоСтрахования.СреднийЗаработокФСС(ПараметрыРасчета);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("СреднийЗаработокФСС", ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработокФСС.Выгрузить()));
	ПараметрыОповещения.Вставить("ОтработанноеВремяДляСреднегоФСС", ОтработанноеВремяДляСреднегоФСС);
	ПараметрыОповещения.Вставить("ПервыйГод", Год1);
	ПараметрыОповещения.Вставить("ВторойГод", Год2);
	ПараметрыОповещения.Вставить("СреднийДневнойЗаработок", СреднийЗаработок.Общий);
	ПараметрыОповещения.Вставить("СреднийДневнойЗаработокРК", СреднийЗаработок.РК);
	ПараметрыОповещения.Вставить("СреднийДневнойЗаработокСН", СреднийЗаработок.СН);
	ПараметрыОповещения.Вставить("МинимальныйСреднедневнойЗаработок", МинимальныйСреднедневнойЗаработок);
	
	Возврат ПоместитьВоВременноеХранилище(ПараметрыОповещения, ИдентификаторВладельца);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоДаннымУчета()
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
	
	Для НомерГода = 1 По 2 Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтаФорма, "Год" + НомерГода + "Месяц" + НомерМесяца, 0);
				
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтаФорма, "Год" + НомерГода + "ДниУхода" + НомерМесяца, 0);
				
		КонецЦикла;
		
	КонецЦикла;
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Год1);
	РасчетныеГоды.Добавить(Год2);
	
	ДанныеОЗаработке = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеОЗаработкеДляРасчетаСреднегоФСС(
		Сотрудник, Организация, ДатаНачалаСобытия, РасчетныеГоды);
		
	СреднийЗаработокФСС.Очистить();
	Для каждого СведенияОГоде Из ДанныеОЗаработке Цикл
			
		ИндексГода = РасчетныеГоды.Найти(СведенияОГоде.Ключ);
		Если ИндексГода <> Неопределено Тогда
			
			НомерГода = ИндексГода + 1;
			Для каждого СведенияМесяца Из СведенияОГоде.Значение Цикл
				
				Для Каждого СтрокаЗаработка Из СведенияМесяца.Значение.Заработок Цикл
					ЗаполнитьЗначенияСвойств(СреднийЗаработокФСС.Добавить(), СтрокаЗаработка);
				КонецЦикла;
				
				НомерМесяца = СведенияМесяца.Ключ;
				Если СведенияМесяца.Значение.ДнейБолезниУходаЗаДетьми <> 0 Тогда
					ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
						ЭтаФорма, "Год" + НомерГода + "ДниУхода" + НомерМесяца, СведенияМесяца.Значение.ДнейБолезниУходаЗаДетьми);
				КонецЕсли; 
					
			КонецЦикла;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	ЗагрузитьДанныеОЗаработкеВФорму();
	РассчитатьСреднийЗаработок();
	
	УстановитьПодсказкуКРасчетуСреднегоЗаработка();
	
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		ЭтаФорма,
		"СреднийЗаработокГруппа",
		"");
		
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРасчетаСреднегоДневногоЗаработкаФСС()
	
	ПараметрыРасчета = УчетПособийСоциальногоСтрахованияКлиентСервер.ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
	ПараметрыРасчета.ДатаНачалаСобытия = ДатаНачалаСобытия;
	ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаНачало = Дата(Год1, 1, 1) ;
	ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаОкончание = КонецДня(Дата(Год2, 12, 31));
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Год1);
	РасчетныеГоды.Добавить(Год2);

	ПараметрыРасчета.РасчетныеГоды = РасчетныеГоды;
	
	ПараметрыРасчета.ПрименяетсяФЗ_421_2014 = УчетПособийСоциальногоСтрахования.ПрименяетсяФЗ_421_2014(ФизическоеЛицо, ДатаНачалаСобытия);
	ПараметрыРасчета.ПрименяетсяФЗ_20_2023 	 = УчетПособийСоциальногоСтрахования.ПрименяетсяФЗ_20_2023(ФизическоеЛицо, ДатаНачалаСобытия);
	
	ПараметрыРасчета.ДанныеНачислений = ДанныеОЗаработкеДляРасчетаСреднегоЗаработка();
	ПараметрыРасчета.ДанныеВремени = ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка();
	ПараметрыРасчета.ДанныеСтрахователей = УчетПособийСоциальногоСтрахования.ПустаяТаблицаДанныеСтрахователейСреднийЗаработокФСС();
	ПараметрыРасчета.ИспользоватьДниБолезниУходаЗаДетьми = ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоБеременностиИРодам;
	ПараметрыРасчета.ПрименятьПредельнуюВеличину = ПричинаНетрудоспособности <> Перечисления.ПричиныНетрудоспособности.ТравмаНаПроизводстве
													И ПричинаНетрудоспособности <> Перечисления.ПричиныНетрудоспособности.Профзаболевание; 
	ПараметрыРасчета.ПорядокРасчета = УчетПособийСоциальногоСтрахованияКлиентСервер.ПорядокРасчетаСреднегоЗаработкаФСС(ДатаНачалаСобытия);
	ПараметрыРасчета.РайонныйКоэффициентРФ = РайонныйКоэффициентРФНаНачалоСобытия;
	ПараметрыРасчета.Сотрудник = Сотрудник;
		
	Возврат ПараметрыРасчета;
	
КонецФункции

&НаСервере
Функция ДанныеОЗаработкеДляРасчетаСреднегоЗаработка()
	
	ДанныеОЗаработкеДляРасчетаСреднегоЗаработка = УчетПособийСоциальногоСтрахования.ПустаяТаблицаНачисленийСреднийЗаработокФСС();
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Год1);
	РасчетныеГоды.Добавить(Год2);
	
	ДнейБолезниУходаЗаДетьми = 0;
	
	НомерГода = 1;
	Для каждого Год Из РасчетныеГоды Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			Период = Дата(Год, НомерМесяца, 1);
			
			СтрокиЗаработка = СреднийЗаработокФСС.НайтиСтроки(Новый Структура("Период", Период));
			
			Для Каждого СтрокаЗаработка Из СтрокиЗаработка Цикл
				
				СтрокаДанныхОЗаработке = ДанныеОЗаработкеДляРасчетаСреднегоЗаработка.Добавить();
				СтрокаДанныхОЗаработке.ФизическоеЛицо = ФизическоеЛицо;
				СтрокаДанныхОЗаработке.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2011;
				СтрокаДанныхОЗаработке.Период = Период;
				СтрокаДанныхОЗаработке.Сумма = СтрокаЗаработка.Сумма;
				СтрокаДанныхОЗаработке.Начисление = СтрокаЗаработка.Начисление;
				
			КонецЦикла;
			
		КонецЦикла;
		
		НомерГода = НомерГода + 1;
		
	КонецЦикла;
	
	Возврат ДанныеОЗаработкеДляРасчетаСреднегоЗаработка;
	
КонецФункции

&НаСервере
Функция ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка()
	
	ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка = УчетПособийСоциальногоСтрахования.ПустаяТаблицаОтработанноеВремяСреднийЗаработокФСС();
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Год1);
	РасчетныеГоды.Добавить(Год2);
	
	НомерГода = 1;
	Для каждого Год Из РасчетныеГоды Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			Период = Дата(Год, НомерМесяца, 1);
			
			СтрокаДанныхОбОтработанномВремени = ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка.Добавить();
			СтрокаДанныхОбОтработанномВремени.ФизическоеЛицо = ФизическоеЛицо;
			СтрокаДанныхОбОтработанномВремени.Период = Период;
			
			ДнейБолезниУходаЗаДетьми = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(ЭтаФорма, "Год" + НомерГода + "ДниУхода" + НомерМесяца);
			СтрокаДанныхОбОтработанномВремени.ДнейБолезниУходаЗаДетьми = ДнейБолезниУходаЗаДетьми;
			
		КонецЦикла;
		
		НомерГода = НомерГода + 1;
		
	КонецЦикла;
	
	Возврат ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка;
	
КонецФункции

&НаСервере
Процедура УстановитьПодсказкуКРасчетуСреднегоЗаработка()
	
	ПараметрыРасчета = ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
	ДанныеРасчетаСреднего = УчетПособийСоциальногоСтрахованияКлиентСервер.ДанныеРасчетаСреднегоЗаработкаФСС(ПараметрыРасчета);
	КалендарныхДней = УчетПособийСоциальногоСтрахованияКлиентСервер.УчитываемыхДнейВКалендарныхГодахФСС(ПараметрыРасчета, ДанныеРасчетаСреднего);
	
	Если ЗаработокСУчетомПредельныхВеличин <> 0 Тогда
		
		ТекстПодсказки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Среднедневной заработок составил'") + ": %1 / %2 = %3",
			ЗаработокСУчетомПредельныхВеличин,
			КалендарныхДней,
			Формат(?(КалендарныхДней = 0, 0, ЗаработокСУчетомПредельныхВеличин / КалендарныхДней), "ЧДЦ=2"));
			
		ТекстПодсказки = ТекстПодсказки + Символы.ПС + Символы.ПС
		
	Иначе
		ТекстПодсказки = "";
	КонецЕсли;
	
	МРОТ = ЗарплатаКадры.МинимальныйРазмерОплатыТрудаРФ(ДатаНачалаСобытия);
	ТекстПодсказки = ТекстПодсказки
		+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='На %1 МРОТ'") + ": %2",
			Формат(ДатаНачалаСобытия, "ДЛФ=DD"),
			МРОТ);
	
	Если РайонныйКоэффициентРФНаНачалоСобытия > 1 Тогда
		
		ТекстПодсказки = ТекстПодсказки + Символы.ПС
		+ НСтр("ru='Районный коэффициент'") + ": " + Формат(РайонныйКоэффициентРФНаНачалоСобытия, "ЧДЦ=2");
		
		МРОТ = МРОТ * РайонныйКоэффициентРФНаНачалоСобытия;
		
	КонецЕсли;
	
	Если КалендарныхДней = 0 Тогда
		СреднийМРОТ = 0;
	Иначе
		СреднийМРОТ = МРОТ * 24 / КалендарныхДней;
	КонецЕсли;
	
	ТекстПодсказки = ТекстПодсказки + Символы.ПС
		+ НСтр("ru='Минимальный средний заработок исходя из МРОТ составил'") + ": " + Формат(СреднийМРОТ, "ЧДЦ=2");
		
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		ЭтаФорма, "СреднийЗаработокИтог", ТекстПодсказки);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСреднийЗаработокФСС(ОтработанноеВремяДляСреднегоФСС)
	
	ЗагрузитьДанныеОЗаработкеВФорму();
	Для каждого СтрокаОтработанногоВремени Из ОтработанноеВремяДляСреднегоФСС Цикл
		
		Год = Год(СтрокаОтработанногоВремени.Период);
		Если Год = Год1 Тогда
			ИндексГода = 0;
		ИначеЕсли Год = Год2 Тогда
			ИндексГода = 1;
		Иначе
			ИндексГода = Неопределено;
		КонецЕсли;
			
		Если ИндексГода <> Неопределено Тогда
			
			НомерГода = ИндексГода + 1;
			НомерМесяца = Месяц(СтрокаОтработанногоВремени.Период);
			
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтаФорма, "Год" + НомерГода + "ДниУхода" + НомерМесяца, СтрокаОтработанногоВремени.ДнейБолезниУходаЗаДетьми);
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеОЗаработкеВФорму()
	
	ТаблицаДанных = СреднийЗаработокФСС.Выгрузить();
	ТаблицаДанных.Колонки.Добавить("СуммаРК", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("СуммаСН", Новый ОписаниеТипов("Число"));
	НачислениеРайонныйКоэффициент = ПланыВидовРасчета.Начисления.НачислениеРайонныйКоэффициент();
	НачислениеСевернаяНадбавка = ПланыВидовРасчета.Начисления.НачислениеСевернаяНадбавка();
	Для каждого СтрокаТаблицыДанных  Из ТаблицаДанных Цикл
		Если СтрокаТаблицыДанных.Начисление = НачислениеРайонныйКоэффициент Тогда
			СтрокаТаблицыДанных.СуммаРК = СтрокаТаблицыДанных.Сумма;
		ИначеЕсли СтрокаТаблицыДанных.Начисление = НачислениеСевернаяНадбавка Тогда
			СтрокаТаблицыДанных.СуммаСН = СтрокаТаблицыДанных.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	ТаблицаДанных.Свернуть("Период", "Сумма,СуммаРК,СуммаСН");
	
	Для каждого СтрокаТаблицыДанных  Из ТаблицаДанных Цикл
		
		Год = Год(СтрокаТаблицыДанных.Период);
		Если Год = Год1 Тогда
			ИндексГода = 0;
		ИначеЕсли Год = Год2 Тогда
			ИндексГода = 1;
		Иначе
			ИндексГода = Неопределено;
		КонецЕсли;
			
		Если ИндексГода <> Неопределено Тогда
			
			НомерГода = ИндексГода + 1;
			НомерМесяца = Месяц(СтрокаТаблицыДанных.Период);
			
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтаФорма, "Год" + НомерГода + "Месяц" + НомерМесяца, СтрокаТаблицыДанных.Сумма);
			
			ТекстыПодсказки = Новый Массив;
			Если СтрокаТаблицыДанных.Сумма > 0 Тогда
				ТекстыПодсказки.Добавить(НСтр("ru='Всего заработано'") + ": " + Формат(СтрокаТаблицыДанных.Сумма, "ЧДЦ=2"));
			КонецЕсли;
			Если СтрокаТаблицыДанных.СуммаРК > 0 Или СтрокаТаблицыДанных.СуммаСН > 0 Тогда
				ТекстыПодсказки.Добавить(НСтр("ru = 'В том числе:'"));
			КонецЕсли;
			Если СтрокаТаблицыДанных.СуммаРК > 0 Тогда
				ТекстыПодсказки.Добавить(НСтр("ru='- Районный коэффициент'") + ": " + Формат(СтрокаТаблицыДанных.СуммаРК, "ЧДЦ=2"));
			КонецЕсли;
			Если СтрокаТаблицыДанных.СуммаСН > 0 Тогда
				ТекстыПодсказки.Добавить(НСтр("ru='- Северная надбавка'") + ": " + Формат(СтрокаТаблицыДанных.СуммаСН, "ЧДЦ=2"));
			КонецЕсли;
			ТекстПодсказки = СтрСоединить(ТекстыПодсказки, "," + Символы.ПС);
			
			ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
				ЭтотОбъект, "Год" + НомерГода + "Месяц" + НомерМесяца, ТекстПодсказки);
				
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРасчетногоГода(ИндексГода)
	
	МаксимальноВозможныйГод = Год(ДатаНачалаСобытия) - 1;
	Если ИндексГода = 0 Тогда
		
		Если Год1 >= Год2 Тогда
			
			Год2 = Мин(МаксимальноВозможныйГод, Год1 + 1);
			
			Если Год2 = МаксимальноВозможныйГод Тогда
				Год1 = Год2 - 1;
			КонецЕсли;
			
		КонецЕсли; 
		
	Иначе
		
		Если Год2 > МаксимальноВозможныйГод Тогда
			Год2 = МаксимальноВозможныйГод;
		КонецЕсли;
		
		Если Год2 <= Год1 Тогда
			
			Если Год1 = 1 Тогда
				Год2 = 2;
			Иначе
				Год1 = Год2 - 1;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьМаксимальноеКоличествоДнейВФевраля(ЭтаФорма);
	
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		ЭтаФорма,
		"СреднийЗаработокГруппа",
		НСтр("ru='После изменения годов, за которые рассчитывается
			|средний заработок необходимо перезаполнить данные'"));
	
КонецПроцедуры

&НаСервере
Процедура ГодМесяцПриИзмененииНаСервере()
	
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСреднийЗаработок()
	
	ПараметрыРасчета = ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
	СреднийЗаработокИтог = УчетПособийСоциальногоСтрахования.СреднийДневнойЗаработокФСС(ПараметрыРасчета);
	МинимальныйСреднедневнойЗаработок = УчетПособийСоциальногоСтрахования.МинимальныйСреднедневнойЗаработокФСС(ПараметрыРасчета);
	
	ДанныеНачисленийПоГодам = Новый Соответствие;
	Для Каждого ДанныеНачисления Из ПараметрыРасчета.ДанныеНачислений Цикл
		ДанныеГода = ДанныеНачисленийПоГодам.Получить(Год(ДанныеНачисления.Период));
		Если ДанныеГода = Неопределено Тогда
			ДанныеГода = 0;
		КонецЕсли;         
		ДанныеГода = ДанныеГода + ДанныеНачисления.Сумма;
		ДанныеНачисленийПоГодам.Вставить(Год(ДанныеНачисления.Период), ДанныеГода);
	КонецЦикла;
	
	ЗаработокСУчетомПредельныхВеличин = 0;
	Для Каждого ДанныеНачисленийГода Из ДанныеНачисленийПоГодам Цикл
		ПредельнаяВеличина = ПараметрыРасчета.ПредельныеВеличиныПоГодам.Получить(ДанныеНачисленийГода.Ключ);
		Если ПредельнаяВеличина <> Неопределено И ПредельнаяВеличина < ДанныеНачисленийГода.Значение Тогда
			ЗаработокСУчетомПредельныхВеличин = ЗаработокСУчетомПредельныхВеличин + ПредельнаяВеличина;
		Иначе
			ЗаработокСУчетомПредельныхВеличин = ЗаработокСУчетомПредельныхВеличин + ДанныеНачисленийГода.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьМаксимальноеКоличествоДнейВФевраля(Форма)
	
	КоличествоДней = ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Дата(Форма.Год1,2,1));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Год1ДниУхода2",
		"МаксимальноеЗначение",
		КоличествоДней);
	
	ТекущееКоличество = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "Год1ДниУхода2");
	Если ТекущееКоличество > КоличествоДней Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
			Форма, "Год1ДниУхода2", КоличествоДней);
	КонецЕсли;
	
	КоличествоДней = ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Дата(Форма.Год2,2,1));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Год2ДниУхода2",
		"МаксимальноеЗначение",
		КоличествоДней);
	
	ТекущееКоличество = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "Год2ДниУхода2");
	Если ТекущееКоличество > КоличествоДней Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
			Форма, "Год2ДниУхода2", КоличествоДней);
	КонецЕсли;
	
	УстановитьКоличествоДнейПервыхМесяцев(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКоличествоДнейПервыхМесяцев(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Год1ДниУхода1",
		"МаксимальноеЗначение",
		31);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Год2ДниУхода1",
		"МаксимальноеЗначение",
		31);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтредактироватьДанныеДляСреднегоЗаработка(Период)
	
	ПараметрыОткрытия = Новый Структура("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыОткрытия.Вставить("ДанныеНачислений", ДанныеНачислений(Период));
	
	ДополнительныеПараметры = Новый Структура("Период", Период);
	Оповещение = Новый ОписаниеОповещения("ПриИзмененииСреднегоЗаработка", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.ВводДанныхДляРасчетаСреднегоПодробно", ПараметрыОткрытия, ЭтотОбъект,
		Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Функция ПериодЭлементаДанных(Элемент)
	
	Год = Год1;
	ПодстрокаНачала = "Год1Месяц";
	Если СтрНачинаетсяС(Элемент.Имя, "Год2") Тогда
		Год = Год2;
		ПодстрокаНачала = "Год2Месяц";
	КонецЕсли;
	
	Месяц = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрЗаменить(Элемент.Имя, ПодстрокаНачала, ""));
	
	Возврат Дата(Год, Месяц, 1);
	
КонецФункции

&НаСервере
Функция ДанныеНачислений(Период)
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(
			СреднийЗаработокФСС.Выгрузить(Новый Структура("Период", Период)));
КонецФункции

&НаКлиенте
Процедура ПриИзмененииСреднегоЗаработка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаменитьДанныеНачислений(ДополнительныеПараметры.Период, Результат);
		ЗагрузитьДанныеОЗаработкеВФорму();
		ГодМесяцПриИзмененииНаСервере();
		УстановитьПодсказкуКРасчетуСреднегоЗаработка();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьДанныеНачислений(Период, ДанныеНачислений);
	НайденныеСтроки = СреднийЗаработокФСС.НайтиСтроки(Новый Структура("Период", Период));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		СреднийЗаработокФСС.Удалить(НайденнаяСтрока);
	КонецЦикла;
	Для Каждого СтрокаНачислений Из ДанныеНачислений Цикл
		НоваяСтрока = СреднийЗаработокФСС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачислений);
		НоваяСтрока.Период = Период;
		НоваяСтрока.ФизическоеЛицо = ФизическоеЛицо;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти