
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	МоиПриложенияКонтрольнаяСумма = Параметры.МоиПриложенияКонтрольнаяСумма;
	
	ЗаполнитьДоступныеПриложения();
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("КалендарьОтчетности.ОткрытыМоиПриложения.ПоКнопке");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ПодключитьНовыеПриложенияОбработчик", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьСписокПриложений();
	Закрыть(Новый Структура("МоиПриложенияКонтрольнаяСумма", МоиПриложенияКонтрольнаяСумма));
	
КонецПроцедуры

&НаСервере
Процедура СохранитьСписокПриложений()
	
	// Метод УстановитьСостояние() требует поднятия привилегированного режима, т.к. регистр сведений
	// ОрганизацииОтчетностиОблачныхПриложений является служебным. Поднимем привилегированный режим
	// сразу на всю процедуру, чтобы не делать это в цикле.
	УстановитьПривилегированныйРежим(Истина);
	ОтключенныеОрганизацииЭтогоПриложения = Новый Массив;
	Для Каждого Строка Из ОблачныеПриложения Цикл
		
		Если Не ЗначениеЗаполнено(Строка.ПриложениеСсылка) Тогда
			Если Не Строка.Подключено Тогда
				ОтключенныеОрганизацииЭтогоПриложения.Добавить(Строка.ОрганизацияСсылка);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если Строка.Подключено = Строка.УжеПодключено Тогда
			Продолжить;
		КонецЕсли;
		
		РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.УстановитьСостояние(
			Строка.ОрганизацияСсылка,
			Строка.Подключено)
	КонецЦикла;
		
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		ТекущийПользователь,
		ОтключенныеОрганизацииЭтогоПриложения,
		КалендарьОтчетностиКлиентСервер.КлючХраненияСпискаОтключенныхОрганизаций());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого Строка Из ОблачныеПриложения Цикл
		Строка.Подключено = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьОтметки(Команда)
	
	Для Каждого Строка Из ОблачныеПриложения Цикл
		Строка.Подключено = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодключитьНовыеПриложения()
	
	ПриложенияПользователя = СообщенияОтчетностиОблачныхПриложений.ДоступныеПриложения();
	
	Если ПриложенияПользователя = Неопределено Тогда
		СообщитьОбОшибкеПолученияПриложенийПользователя();
		Возврат Неопределено;
	КонецЕсли;
	
	НоваяКонтрольнаяСумма = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ПриложенияПользователя, ХешФункция.CRC32);
	Если МоиПриложенияКонтрольнаяСумма = НоваяКонтрольнаяСумма Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыПодключения = СообщенияОтчетностиОблачныхПриложений.НовыйПараметрыПодключенияПриложений();
	ПараметрыПодключения.Пользователь = ТекущийПользователь;
	ПараметрыПодключения.ПриложенияПользователя = ПриложенияПользователя;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"СообщенияОтчетностиОблачныхПриложений.ПодключитьНовыеПриложения",
		ПараметрыПодключения);
	
	Возврат ДлительнаяОперация;
КонецФункции

&НаКлиенте
Процедура ПодключитьНовыеПриложенияОбработчик() Экспорт
	
	ДлительнаяОперация = ПодключитьНовыеПриложения();
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПодключенияПриложений", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПодключенияПриложений(РезультатФормирования, ДополнительныеПараметры) Экспорт
	
	Если РезультатФормирования <> Неопределено Тогда
		ОшибкиПодключения = ПолучитьИзВременногоХранилища(РезультатФормирования.АдресРезультата);
		
		Если ОшибкиПодключения.Количество() Тогда
			СообщитьОбОшибкеПодключения(СтрСоединить(ОшибкиПодключения, ", "));
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьДоступныеПриложения();
	
	МоиПриложенияКонтрольнаяСумма = НоваяКонтрольнаяСумма;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеПриложения()
	
	ОблачныеПриложения.Очистить();
	УстановитьПривилегированныйРежим(Истина);
	Организации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
	ОтключенныеОрганизацииЭтогоПриложения = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ТекущийПользователь,
		КалендарьОтчетностиКлиентСервер.КлючХраненияСпискаОтключенныхОрганизаций());
	УстановитьПривилегированныйРежим(Ложь);
	
	// Организации этого приложения
	ЕстьОтключенныеОрганизации = ТипЗнч(ОтключенныеОрганизацииЭтогоПриложения) = Тип("Массив");
	ОрганизацииЭтогоПриложения = Справочники.Организации.ДоступныеОрганизацииПоRLS();
	Для Каждого Организация Из ОрганизацииЭтогоПриложения Цикл
		Подключено = ?(ЕстьОтключенныеОрганизации, ОтключенныеОрганизацииЭтогоПриложения.Найти(Организация) = Неопределено, Истина);
		НоваяСтрока = ОблачныеПриложения.Добавить();
		НоваяСтрока.ПриложениеНаименование = НСтр("ru='Это приложение'");
		НоваяСтрока.ОрганизацияСсылка = Организация;
		НоваяСтрока.ОрганизацияНаименование = Организация.Наименование;
		НоваяСтрока.УжеПодключено = Подключено;
		НоваяСтрока.Подключено = Подключено;
	КонецЦикла;
	
	// Организации подключенных приложений
	Для Каждого Организация Из Организации Цикл
		НоваяСтрока = ОблачныеПриложения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Организация);
		НоваяСтрока.УжеПодключено = Организация.ВыводитьВКалендарь;
		НоваяСтрока.Подключено = Организация.ВыводитьВКалендарь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СообщитьОбОшибкеПодключения(ИмяПриложения)
	
	ШаблонСообщения = НСтр("ru = 'Не удалось подключить приложение ""%1"". Обратитесь в техническую поддержку.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ШаблонСообщения, ИмяПриложения));
	
КонецФункции

&НаСервереБезКонтекста
Функция СообщитьОбОшибкеПолученияПриложенийПользователя()
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не удалось получить список приложений.'"));
	
КонецФункции

#КонецОбласти
