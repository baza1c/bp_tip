#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// "Распаковываем" параметры
	Сотрудник = Параметры.Сотрудник;
	Организация = Параметры.Организация;
	ДатаНачалаСобытия = Параметры.ДатаНачалаСобытия;
	ДокументСсылка = Параметры.ДокументСсылка;
	ДокументВладелецДанныеАдрес = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ДокументВладелецДанныеАдрес", "");
	
	// Заполним дату приема на работу для ограничения ввода данных о среднем заработке.
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ДатаПриема");
	Если КадровыеДанные.Количество()>0 Тогда
		ДатаПриемаНаРаботуСотрудника = КадровыеДанные[0].ДатаПриема;
	Иначе
		Отказ = Истина;
	КонецЕсли; 
	
	Если Отказ Тогда
		// Проблемы с датой приема на работу.
		Возврат;
	КонецЕсли;
	
	ГодГодовыхПремий = РасчетЗарплатыДляНебольшихОрганизаций.ГодГодовыхПремий(ДатаНачалаСобытия);
	ВидыГодовыхПремий = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	ВидыПремийПроцентом = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремииПроцентом());
	
	// Заполняем период расчета среднего заработка.
	НачалоПериодаРасчета = Параметры.НачалоПериодаРасчета;
	ОкончаниеПериодаРасчета = Параметры.ОкончаниеПериодаРасчета;
	
	// Инициализируем соответствие по умолчанию.
	МесяцыРасчета = Новый ФиксированноеСоответствие(Новый Соответствие);
	
	// Заполняем период расчета среднего заработка.
	ЗаполнитьПериодРасчетаПоУмолчанию();
	// Заполним соответствие месяца и номера его колонки.
	ЗаполнитьМесяцыРасчета(ЭтаФорма);
	
	УстановитьОтображениеЭлементовУправления();
	
	УстановитьЗаголовокФормы();
	
	Если Параметры.ДанныеОНачислениях <> Неопределено Тогда
		ТаблицаНачислений = ПолучитьИзВременногоХранилища(Параметры.ДанныеОНачислениях);
	КонецЕсли;
	
	Если Параметры.ДанныеОВремени <> Неопределено Тогда
		ТаблицаВремени = ПолучитьИзВременногоХранилища(Параметры.ДанныеОВремени);
	КонецЕсли;
	
	Если Параметры.ДанныеОНачислениях = Неопределено Тогда
		// Используется режим непосредственного редактирования данных учета для расчета среднего заработка
		// в этом режиме непосредственно из учета получаем данные по всем месяцам
		// по окончании редактирования изменения записываем непосредственно в учет.
		ТаблицаНачислений	= Новый ТаблицаЗначений;
		ТаблицаВремени		= Новый ТаблицаЗначений;
		
		// Признак того, что при закрытии формы необходимо записать изменения в учет.
		ЗаписыватьИзменения = Истина;
	КонецЕсли;
	
	Если ЗаписыватьИзменения Тогда
		ПрочитатьДанныеУчетаСреднегоЗаработка();
	Иначе
		ЗагрузитьДанныеЗаработка(ТаблицаНачислений);
		ЗаполнитьФорму(ТаблицаНачислений, ТаблицаВремени);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОсновныеНачисленияПриИзменении(Элемент)
	
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

&НаКлиенте
Процедура КалендарныхДнейПриИзменении(Элемент)
	
	КалендарныхДнейПриИзмененииНаСервере(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПремияПроцентомЗаГодПриИзменении(Элемент)
	
	ПремияПриИзменении("ПремияГодоваяПроцентом");
	
КонецПроцедуры

&НаКлиенте
Процедура ПремияСуммойЗаГодПриИзменении(Элемент)
	
	ПремияПриИзменении("ПремияГодоваяФиксированнойСуммой");
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура OK(Команда) 
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьДанныеУчета(Команда)
	
	Если СреднийЗаработокИтог <> 0 Тогда
		
		ТекстВопроса = НСтр("ru='Перед заполнением введенные данные будут очищены.
			|Продолжить?'");
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьДанныеУчетаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		ПеречитатьДанныеУчетаЗавершение(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеречитатьДанныеУчетаЗавершение(Знач Результат, Знач Параметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеречитатьДанныеУчетаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ Модифицированность ИЛИ ТолькоПросмотр Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ДанныеДляРасчета = ДанныеДляРасчетаСреднего(ВладелецФормы.УникальныйИдентификатор);
	
	Модифицированность = Ложь;
	Закрыть(ДанныеДляРасчета);
	
КонецПроцедуры

&НаСервере
Функция ДанныеДляРасчетаСреднего(ИдентификаторВладельца)
	
	СтруктураДанных = Новый Структура;
	
	СтруктураДанных.Вставить("Сотрудник", Сотрудник);
	
	СтруктураДанных.Вставить("НачалоПериодаРасчета", НачалоПериодаРасчета);
	СтруктураДанных.Вставить("ОкончаниеПериодаРасчета", ОкончаниеПериодаРасчета);
	
	СтруктураДанных.Вставить("ДанныеОНачислениях", ПоместитьВоВременноеХранилище(РезультатРедактированияНачислений()));
	СтруктураДанных.Вставить("ДанныеОВремени", ПоместитьВоВременноеХранилище(РезультатРедактированияВремени()));
	
	СтруктураДанных.Вставить("СреднийЗаработок", СреднийЗаработокИтог);
	СтруктураДанных.Вставить("СреднийЗаработокРК", СреднийЗаработокИтогРК);
	СтруктураДанных.Вставить("СреднийЗаработокСН", СреднийЗаработокИтогСН);
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураДанных, ИдентификаторВладельца);
	
КонецФункции	

&НаСервере
Процедура ПеречитатьДанныеУчетаНаСервере()
	
	ПрочитатьДанныеУчетаСреднегоЗаработка();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму(ДанныеОНачислениях, ДанныеОВремени)
	
	Для НомерМесяца = 1 По МесяцыРасчета.Количество() Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
			ЭтаФорма, "КалендарныхДней" + НомерМесяца, 0);
		
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
			ЭтаФорма, "ОсновныеНачисления" + НомерМесяца, 0);
			
	КонецЦикла;
	
	ЗаполнитьНачисления();
	ЗаполнитьВремя(ДанныеОВремени);
	
	РассчитатьСреднийЗаработок();	

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисления()
	
	ДанныеОНачислениях = СреднийЗаработокРасширенный();
	Для Каждого СтрокаНачислений Из ДанныеОНачислениях Цикл
		
		ИндексМесяца = МесяцыРасчета.Найти(НачалоМесяца(СтрокаНачислений.Период));
		Если ИндексМесяца <> Неопределено Тогда
			
			НомерМесяца = ИндексМесяца + 1;
			
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтаФорма, "ОсновныеНачисления" + НомерМесяца, СтрокаНачислений.Сумма);
				
			Подсказки = Новый Массив;
			Если СтрокаНачислений.Сумма > 0 Тогда
				Подсказки.Добавить(СтрШаблон(НСтр("ru = 'Средний заработок: %1'"), Формат(СтрокаНачислений.Сумма, "ЧДЦ=2")));
			КонецЕсли;
			Если СтрокаНачислений.СуммаРК > 0 Или СтрокаНачислений.СуммаСН > 0 Тогда
				Подсказки.Добавить(НСтр("ru = 'В том числе:'"));
			КонецЕсли;
			Если СтрокаНачислений.СуммаРК > 0 Тогда
				Подсказки.Добавить(СтрШаблон(НСтр("ru = '- Районный коэффициент: %1'"), Формат(СтрокаНачислений.СуммаРК, "ЧДЦ=2")));
			КонецЕсли;
			Если СтрокаНачислений.СуммаСН > 0 Тогда
				Подсказки.Добавить(СтрШаблон(НСтр("ru = '- Севреная надбавка: %1'"), Формат(СтрокаНачислений.СуммаСН, "ЧДЦ=2")));
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ОсновныеНачисления" + НомерМесяца,
				"Подсказка",
				СтрСоединить(Подсказки, "," + Символы.ПС));
				
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Функция СреднийЗаработокРасширенный()
	
	ДанныеОНачислениях = СреднийЗаработокОбщий.Выгрузить();
	ДанныеОНачислениях.Колонки.Добавить("СуммаРК", Новый ОписаниеТипов("Число"));
	ДанныеОНачислениях.Колонки.Добавить("СуммаСН", Новый ОписаниеТипов("Число"));
	ДанныеОНачислениях.Колонки.Добавить("СуммаПремии", Новый ОписаниеТипов("Число"));
	
	НачислениеРайонныйКоэффициент = ПланыВидовРасчета.Начисления.НачислениеРайонныйКоэффициент();
	НачислениеСевернаяНадбавка = ПланыВидовРасчета.Начисления.НачислениеСевернаяНадбавка();
	Для Каждого СтрокаНачислений Из ДанныеОНачислениях Цикл
		Если ЗначениеЗаполнено(СтрокаНачислений.СоставнаяЧасть) Тогда
			СтрокаНачислений.СуммаПремии = СтрокаНачислений.Сумма;
		ИначеЕсли СтрокаНачислений.Начисление = НачислениеРайонныйКоэффициент Тогда
			СтрокаНачислений.СуммаРК = СтрокаНачислений.Сумма;
		ИначеЕсли СтрокаНачислений.Начисление = НачислениеСевернаяНадбавка Тогда
			СтрокаНачислений.СуммаСН = СтрокаНачислений.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеОНачислениях.Свернуть("Период", "Сумма,СуммаРК,СуммаСН,СуммаПремии");
	
	Возврат ДанныеОНачислениях;
КонецФункции

&НаСервере
Процедура ЗаполнитьВремя(ДанныеВремени)
	
	ПоследнийИзвестныйПериод = Неопределено;
	Для Каждого СтрокаДанныхВремени Из ДанныеВремени Цикл
		
		ИндексМесяца = МесяцыРасчета.Найти(НачалоМесяца(СтрокаДанныхВремени.Период));
		Если ИндексМесяца <> Неопределено Тогда
			
			НомерМесяца = ИндексМесяца + 1;
			
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтаФорма, "КалендарныхДней" + НомерМесяца, СтрокаДанныхВремени.ОтработаноДнейКалендарных);
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеУчетаСреднегоЗаработка(ОтборМесяцев = Неопределено)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		// Установка транзакции для расчета
		УстановитьПривилегированныйРежим(Истина);
		НачатьТранзакцию();
		Если ЗначениеЗаполнено(ДокументВладелецДанныеАдрес) Тогда
			// Получаем данные документа из временного хранилища.
			ОбъектФормы = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПолучитьИзВременногоХранилища(ДокументВладелецДанныеАдрес));
			Если КонецДня(ОбъектФормы.ДатаУвольнения) = КонецМесяца(ОбъектФормы.ДатаУвольнения) Тогда
				// Регистрируем данные документа в учете.
				ОбъектФормы.ОбменДанными.Загрузка = Истина;
				ОбъектФормы.Записать(РежимЗаписиДокумента.Запись);
				ОбъектФормы.ЗарегистрироватьНачисления(,,, Истина);
				ОбъектФормы.ЗарегистрироватьОтработанноеВремя();
			КонецЕсли;
		КонецЕсли;
		
		ДанныеДляРасчета = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(
			Сотрудник, Организация, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, Истина);
			
		ОтменитьТранзакцию();
		УстановитьПривилегированныйРежим(Ложь);
		// Отмена расчетной транзакции
		
		ЗагрузитьДанныеЗаработка(ДанныеДляРасчета.ДанныеОНачислениях);
		ЗаполнитьФорму(ДанныеДляРасчета.ДанныеОНачислениях, ДанныеДляРасчета.ДанныеОВремени);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеЗаработка(ТаблицаНачислений)
	
	СреднийЗаработокОбщий.Очистить();
	СреднийЗаработокГодовыеПремии.Очистить();
	ПремияГодоваяПроцентом = 0;
	ПремияГодоваяФиксированнойСуммой = 0; 
	
	Для Каждого СтрокаНачислений Из ТаблицаНачислений Цикл
		Если ВидыГодовыхПремий.Найти(СтрокаНачислений.СоставнаяЧасть) <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СреднийЗаработокГодовыеПремии.Добавить(), СтрокаНачислений);
			Если ВидыПремийПроцентом.Найти(СтрокаНачислений.СоставнаяЧасть) <> Неопределено Тогда
				ПремияГодоваяПроцентом = ПремияГодоваяПроцентом + СтрокаНачислений.Сумма;
			Иначе
				ПремияГодоваяФиксированнойСуммой = ПремияГодоваяФиксированнойСуммой + СтрокаНачислений.Сумма;
			КонецЕсли;
		Иначе
			ЗаполнитьЗначенияСвойств(СреднийЗаработокОбщий.Добавить(), СтрокаНачислений);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьМесяцыРасчета(Форма)
	
	МесяцыРасчета = Новый Массив;
	МесяцОбхода = НачалоМесяца(Форма.НачалоПериодаРасчета);
	Пока МесяцОбхода <= Форма.ОкончаниеПериодаРасчета Цикл
		МесяцыРасчета.Добавить(МесяцОбхода);
		МесяцОбхода = ДобавитьМесяц(МесяцОбхода, 1);
	КонецЦикла;
	Форма.МесяцыРасчета = Новый ФиксированныйМассив(МесяцыРасчета);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеЭлементовУправления(ДобавляемыеМесяцы = Неопределено)
	
	Для НомерМесяца = МесяцыРасчета.Количество() + 1 По 12 Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СреднийЗаработокГруппа" + НомерМесяца,
			"Видимость",
			Ложь);
		
	КонецЦикла;
	
	СчетчикМесяцев = 1;
	Для Каждого Месяц Из МесяцыРасчета Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЗаголовокДекорация" + СчетчикМесяцев,
			"Заголовок",
			Формат(Месяц, "ДФ='ММММ гггг'"));
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"КалендарныхДней" + СчетчикМесяцев,
			"МаксимальноеЗначение",
			ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Месяц));
			
		СчетчикМесяцев = СчетчикМесяцев + 1;

	КонецЦикла;
	
	Элементы.ГодовыеПремииГруппа.Заголовок = СтрШаблон(НСтр("ru = 'Годовые премии за %1 год'"), Формат(ГодГодовыхПремий, "ЧГ="));
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если ТолькоПросмотр Тогда
		Заголовок = НСтр("ru = 'Данные для расчета среднего заработка (только просмотр)'");
	Иначе
		Заголовок = НСтр("ru = 'Ввод данных для расчета среднего заработка'"); 
	КонецЕсли;
							
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеСтрокиДанныхОВремени()
	
	// Функция конструирует структуру, описывающую данные о времени 
	// для расчета среднего заработка за один месяц расчетного периода.
	// Поля структуры заполнены пустыми значениями соответствующих типов.
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ПоляОписания.Вставить("Период", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("ОтработаноДнейКалендарных", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("ОтработаноДнейПятидневка", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("НормаДнейПроизводственныйКалендарь", Новый ОписаниеТипов("Число"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПериодРасчетаПоУмолчанию()
	
	НачалоПериода = ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия), -12);
	ОкончаниеПериода = НачалоМесяца(ДатаНачалаСобытия) - 1;
	
	ПериодРасчета = Новый СтандартныйПериод;
	ПериодРасчета.ДатаНачала = Макс(НачалоПериода, НачалоМесяца(ДатаПриемаНаРаботуСотрудника));
	ПериодРасчета.ДатаОкончания = ОкончаниеПериода;

	НачалоПериодаРасчета = ПериодРасчета.ДатаНачала;
	ОкончаниеПериодаРасчета = ПериодРасчета.ДатаОкончания;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСреднийЗаработок()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		ДанныеНачислений = РезультатРедактированияНачислений();
		ДанныеВремени = РезультатРедактированияВремени();
		
		ТаблицыПоСотруднику = Новый Структура;
		ТаблицыПоСотруднику.Вставить("ДанныеОНачислениях", ДанныеНачислений);
		ТаблицыПоСотруднику.Вставить("ДанныеОВремени", ДанныеВремени);
		
		ПараметрыПолученияДанныхСреднего = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПараметрыПолученияДанныхСреднегоОбщего();
		ПараметрыПолученияДанныхСреднего.Вставить("ТаблицыПоСотруднику", 	ТаблицыПоСотруднику); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаПериода",  	НачалоПериодаРасчета); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаОкончанияПериода",	ОкончаниеПериодаРасчета); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаСобытия", 		ДатаНачалаСобытия);		
		
		ДанныеРасчетаСреднегоЗаработка = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеРасчетаСреднегоЗаработкаОбщего(ПараметрыПолученияДанныхСреднего);

		СреднийЗаработокИтог = ДанныеРасчетаСреднегоЗаработка.Итоги.СреднедневнойЗаработок;
		СреднийЗаработокИтогРК = ДанныеРасчетаСреднегоЗаработка.Итоги.СреднедневнойЗаработокРК;
		СреднийЗаработокИтогСН = ДанныеРасчетаСреднегоЗаработка.Итоги.СреднедневнойЗаработокСН;
		
		СреднийЗаработок = СреднийЗаработокИтог;
		
		УстановитьРасширеннуюПодсказку();
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция РезультатРедактированияНачислений(Месяц = Неопределено)
	
	Для Каждого СтрокаСреднего Из СреднийЗаработокОбщий Цикл
		СтрокаСреднего.Сотрудник = Сотрудник;
	КонецЦикла;
	
	Если Месяц = Неопределено Тогда
		ДанныеНачислений = ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработокОбщий.Выгрузить());
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеНачислений,
			ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработокГодовыеПремии.Выгрузить()));
	Иначе
		ДанныеНачислений = ОбщегоНазначения.ТаблицаЗначенийВМассив(
			СреднийЗаработокОбщий.Выгрузить(Новый Структура("Период", Месяц)));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеНачислений,
			ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработокГодовыеПремии.Выгрузить(Новый Структура("Период", Месяц))));
	КонецЕсли;
	
	Возврат ДанныеНачислений;
	
КонецФункции	

&НаСервере
Функция РезультатРедактированияВремени()
	
	ДанныеВремени = Новый Массив;
	
	КатегорииОтсутствий = Новый Массив;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		КатегорииОтсутствий = МодульРасчетЗарплатыДляНебольшихОрганизаций.КатегорииОтсутствий();
	КонецЕсли;
	
	ПроизводственныйКалендарь = РасчетЗарплатыДляНебольшихОрганизацийПовтИсп.ПроизводственныйКалендарьОрганизации(Организация);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПроизводственныйКалендарь", ПроизводственныйКалендарь);
	Запрос.УстановитьПараметр("МесяцыРасчета", МесяцыРасчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("НачисленияИсключения", ПланыВидовРасчета.Начисления.НачисленияПоОтбору(
		Новый Структура("КатегорияНачисленияИлиНеоплаченногоВремени",  КатегорииОтсутствий))); 
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеПроизводственногоКалендаря.ВидДня В (ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий), ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НормаДнейПроизводственныйКалендарь,
	|	НАЧАЛОПЕРИОДА(ДанныеПроизводственногоКалендаря.Дата, МЕСЯЦ) КАК Период
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ДанныеПроизводственногоКалендаря.Дата, МЕСЯЦ) В (&МесяцыРасчета)
	|	И ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = &ПроизводственныйКалендарь
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ДанныеПроизводственногоКалендаря.Дата, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтработанноеВремяПоСотрудникам.ОтработаноДней КАК ОтработаноДнейПятидневка,
	|	ОтработанноеВремяПоСотрудникам.ПериодДействия КАК Период
	|ИЗ
	|	РегистрНакопления.ОтработанноеВремяПоСотрудникам КАК ОтработанноеВремяПоСотрудникам
	|ГДЕ
	|	ОтработанноеВремяПоСотрудникам.Организация = &Организация
	|	И ОтработанноеВремяПоСотрудникам.Сотрудник = &Сотрудник
	|	И ОтработанноеВремяПоСотрудникам.Период В(&МесяцыРасчета)
	|	И НЕ ОтработанноеВремяПоСотрудникам.Начисление В (&НачисленияИсключения)";
	
	Результат = Запрос.ВыполнитьПакет();
	НормыДней      = Результат[0].Выгрузить();
	ОтработаноДней = Результат[1].Выгрузить();
	
	Для НомерМесяца = 1 По МесяцыРасчета.Количество() Цикл
		
		КалендарныхДней = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			ЭтотОбъект, "КалендарныхДней" + НомерМесяца);
			
		Если КалендарныхДней = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НормаДнейПроизводственныйКалендарь = 0;
		НормаДнейМесяцаРасчета = НормыДней.Найти(МесяцыРасчета[НомерМесяца - 1], "Период");
		Если НормаДнейМесяцаРасчета <> Неопределено Тогда
			НормаДнейПроизводственныйКалендарь = НормаДнейМесяцаРасчета.НормаДнейПроизводственныйКалендарь;
		КонецЕсли;
		
		ОтработаноДнейПятидневка = 0;
		ОтработаноДнейМесяцаРасчета = ОтработаноДней.Найти(МесяцыРасчета[НомерМесяца - 1], "Период");
		Если ОтработаноДнейМесяцаРасчета <> Неопределено Тогда
			ОтработаноДнейПятидневка = ОтработаноДнейМесяцаРасчета.ОтработаноДнейПятидневка;
		КонецЕсли;
		
		ОписаниеСтроки = ОписаниеСтрокиДанныхОВремени();
		ОписаниеСтроки.Сотрудник = Сотрудник;
		ОписаниеСтроки.Период = МесяцыРасчета[НомерМесяца - 1];
		ОписаниеСтроки.ОтработаноДнейКалендарных = КалендарныхДней;
		ОписаниеСтроки.НормаДнейПроизводственныйКалендарь = НормаДнейПроизводственныйКалендарь;
		ОписаниеСтроки.ОтработаноДнейПятидневка = ОтработаноДнейПятидневка;
		
		ДанныеВремени.Добавить(ОписаниеСтроки);

	КонецЦикла;
	
	Возврат ДанныеВремени;
	
КонецФункции	

&НаСервере
Процедура КалендарныхДнейПриИзмененииНаСервере(ИмяЭлемента)
	
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРасширеннуюПодсказку()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
	Иначе
		Возврат;
	КонецЕсли;
	
	КалендарныхДней = 0;
	СуммаЗаработка = 0;
	МассивРасшифровкиМесяцев = Новый Массив;
	МаксимальнаяДлинаРасшифровкиМесяца = 0;	
	МаксимальнаяДлинаРасшифровкиДней = 0;	
	
	Сумма = 0;
	СуммаРК = 0;
	СуммаСН = 0;
	ДанныеОНачислениях = СреднийЗаработокРасширенный();
	Для НомерМесяца = 1 По МесяцыРасчета.Количество() Цикл
		
		СтрокаМесяца = ДанныеОНачислениях.Найти(МесяцыРасчета[НомерМесяца - 1], "Период");
		Если СтрокаМесяца <> Неопределено Тогда
			Если СтрокаМесяца.Сумма > 0 Тогда
				Сумма = Сумма + СтрокаМесяца.Сумма;
			КонецЕсли;
			Если СтрокаМесяца.СуммаРК > 0 Тогда
				СуммаРК = СуммаРК + СтрокаМесяца.СуммаРК;
			КонецЕсли;
			Если СтрокаМесяца.СуммаСН > 0 Тогда
				СуммаСН = СуммаСН + СтрокаМесяца.СуммаСН;
			КонецЕсли;
		КонецЕсли;
		
		СуммаЗаработка = СуммаЗаработка + ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			ЭтаФорма, "ОсновныеНачисления" + НомерМесяца);
		
		КалендарныхДнейМесяца = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			ЭтаФорма, "КалендарныхДней" + НомерМесяца);
			
		Если КалендарныхДнейМесяца <> 0 Тогда
			
			Месяц = МесяцыРасчета[НомерМесяца - 1];
			УчтеноДней = МодульРасчетЗарплатыДляНебольшихОрганизаций.КоличествоУчтенныхДнейОтпуска(ДатаНачалаСобытия, Месяц, КалендарныхДнейМесяца);
			КалендарныхДней = КалендарныхДней + УчтеноДней;
			
			РасшифровкаМесяца = Новый Структура("Месяц,Дни", Формат(Месяц, "ДФ='ММММ гггг'"), Формат(УчтеноДней, "ЧДЦ=2"));
			МассивРасшифровкиМесяцев.Добавить(РасшифровкаМесяца);
			
			ДлинаМесяца = СтрДлина(РасшифровкаМесяца.Месяц);
			Если МаксимальнаяДлинаРасшифровкиМесяца < ДлинаМесяца Тогда
				МаксимальнаяДлинаРасшифровкиМесяца = ДлинаМесяца;
			КонецЕсли; 
						
			ДлинаДней = СтрДлина(РасшифровкаМесяца.Дни);
			Если МаксимальнаяДлинаРасшифровкиДней < ДлинаДней Тогда
				МаксимальнаяДлинаРасшифровкиДней = ДлинаДней;
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Если СуммаЗаработка = 0 ИЛИ КалендарныхДней = 0 Тогда
		ТекстПодсказки = НСтр("ru='Нет данных для расчета среднего заработка'");
	Иначе
		
		ТекстыПодсказки = Новый Массив;
		Если Сумма > 0 Тогда
			ТекстыПодсказки.Добавить(НСтр("ru='Всего заработано'") + ": " + Формат(Сумма, "ЧДЦ=2"));
		КонецЕсли;
		Если СуммаРК > 0 Или СуммаСН > 0 Тогда
			ТекстыПодсказки.Добавить(НСтр("ru = 'В том числе:'"));
		КонецЕсли;
		Если СуммаРК > 0 Тогда
			ТекстыПодсказки.Добавить(НСтр("ru='- Районный коэффициент'") + ": " + Формат(СуммаРК, "ЧДЦ=2"));
		КонецЕсли;
		Если СуммаСН > 0 Тогда
			ТекстыПодсказки.Добавить(НСтр("ru='- Северная надбавка'") + ": " + Формат(СуммаСН, "ЧДЦ=2"));
		КонецЕсли;
		ТекстПодсказки = СтрСоединить(ТекстыПодсказки, "," + Символы.ПС);
		
		ТекстПодсказки = ТекстПодсказки + Символы.ПС + Символы.ПС
			+ НСтр("ru='Учтено дней'") + ": " + Формат(КалендарныхДней, "ЧДЦ=2") + Символы.ПС
			+ Символы.Таб + НСтр("ru='в том числе'") + ":";
			
		ШаблонПробелов = "          ";
		Для каждого РасшифровкаМесяца Из МассивРасшифровкиМесяцев Цикл
			
			ТекстПодсказки = ТекстПодсказки + Символы.ПС + Символы.Таб
				+ РасшифровкаМесяца.Месяц + Лев(ШаблонПробелов, МаксимальнаяДлинаРасшифровкиМесяца - СтрДлина(РасшифровкаМесяца.Месяц))
				+ " - "
				+ Лев(ШаблонПробелов, МаксимальнаяДлинаРасшифровкиДней - СтрДлина(РасшифровкаМесяца.Дни)) + РасшифровкаМесяца.Дни;

		КонецЦикла;
		
		ТекстПодсказки = ТекстПодсказки + Символы.ПС + Символы.ПС
			+ НСтр("ru='Среднедневной заработок'") + ": " + Формат(СуммаЗаработка / КалендарныхДней, "ЧДЦ=2");
		
	КонецЕсли;
	
	
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		ЭтаФорма, "СреднийЗаработокИтог", ТекстПодсказки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления1НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления2НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления3НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления4НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления5НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления6НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления7НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления8НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления9НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления10НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления11НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисления12НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтредактироватьСреднийЗаработок(НомерМесяцаПоляВводаСуммыНачислений(Элемент));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтредактироватьСреднийЗаработок(НомерМесяца)
	
	Если НомерМесяца = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыОткрытия.Вставить("ДанныеНачислений", РезультатРедактированияНачислений(МесяцыРасчета[НомерМесяца - 1]));
	ПараметрыОткрытия.Вставить("Период", МесяцыРасчета[НомерМесяца - 1]);
	
	ДополнительныеПараметры = Новый Структура("Месяц", МесяцыРасчета[НомерМесяца - 1]);
	Оповещение = Новый ОписаниеОповещения("ПриИзмененииСреднегоЗаработка", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.ВводДанныхДляРасчетаСреднегоПодробно", ПараметрыОткрытия, ЭтотОбъект,
		Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры
	
&НаКлиенте
Процедура ПриИзмененииСреднегоЗаработка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность = Истина;
		ЗаменитьДанныеНачислений(ДополнительныеПараметры.Месяц, Результат);
		ЗаполнитьНачисления();
		РассчитатьСреднийЗаработок();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьДанныеНачислений(Месяц, ДанныеНачислений);
	НайденныеСтроки = СреднийЗаработокОбщий.НайтиСтроки(Новый Структура("Период", Месяц));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		СреднийЗаработокОбщий.Удалить(НайденнаяСтрока);
	КонецЦикла;
	Для Каждого СтрокаНачислений Из ДанныеНачислений Цикл
		НоваяСтрока = СреднийЗаработокОбщий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачислений);
		НоваяСтрока.Период = Месяц;
		НоваяСтрока.Сотрудник = Сотрудник;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция НомерМесяцаПоляВводаСуммыНачислений(Элемент)
	Если СтрНачинаетсяС(Элемент.Имя, "ОсновныеНачисления") Тогда
		Возврат СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрЗаменить(Элемент.Имя, "ОсновныеНачисления", ""));
	КонецЕсли;
	Возврат 0;
КонецФункции

&НаКлиенте
Процедура ПремияПриИзменении(СоставнаяЧастьИмя)
	
	СоставнаяЧасть = ПредопределенноеЗначение("Перечисление.УчетНачисленийВСреднемЗаработкеОбщий." + СоставнаяЧастьИмя);
	СтрокиПремии = СреднийЗаработокГодовыеПремии.НайтиСтроки(Новый Структура("СоставнаяЧасть", СоставнаяЧасть));
	
	Если СтрокиПремии.Количество() > 1 Тогда
		Для Каждого Строка Из СтрокиПремии Цикл
			СреднийЗаработокГодовыеПремии.Удалить(Строка);
		КонецЦикла;
		СтрокиПремии.Очистить();
	КонецЕсли;
	Если СтрокиПремии.Количество() = 0 Тогда
		ТекущаяСтрока = СреднийЗаработокГодовыеПремии.Добавить();
		ТекущаяСтрока.Период            = НачалоМесяца(Макс(ДатаПриемаНаРаботуСотрудника, НачалоПериодаРасчета));
		ТекущаяСтрока.Сотрудник         = Сотрудник;
		ТекущаяСтрока.Год               = ГодГодовыхПремий;
		ТекущаяСтрока.КоличествоМесяцев = 12;
		ТекущаяСтрока.ДатаНачалаБазовогоПериода = НачалоМесяца(Макс(ДатаПриемаНаРаботуСотрудника, Дата(ГодГодовыхПремий,1,1)));
	Иначе
		ТекущаяСтрока = СтрокиПремии[0];
	КонецЕсли;
	ТекущаяСтрока.СоставнаяЧасть = СоставнаяЧасть;
	ТекущаяСтрока.Сумма = ЭтотОбъект[СоставнаяЧастьИмя];
	
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

#КонецОбласти
