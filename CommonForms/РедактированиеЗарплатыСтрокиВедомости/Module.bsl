#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПолучитьПараметры();
	ПолучитьЗарплата();

	ПолучитьЕдинственногоСотрудника();
	
	ИспользоватьРасчетПервойПоловиныМесяца =
		УчетЗарплаты.ИспользоватьРасчетПервойПоловиныМесяца(Организация, ПериодРегистрации);
	
	ВедомостьНаВыплатуЗарплатыФормыВнутренний.РедактированиеЗарплатыСтрокиНастроитьЭлементы(ЭтотОбъект);
	ВидыДоходовИсполнительногоПроизводстваКлиентСервер.НастроитьКолонкуВидДоходаДляВыплатыЗарплаты(
		ЭтотОбъект,
		"Зарплата",
		"ЗарплатаВидДохода",
		"Зарплата.ВидДоходаИсполнительногоПроизводства"
		,
		ДатаВыплаты);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарплатаВидВыплатыНаКартыМИР", 
		"Видимость", ДляПеречисленийНаКартыМир);
	
	ЗаголовокУточнение = НСтр("ru='выплаты'");
	Если СтрНачинаетсяС(Параметры.ИмяПоля, "СоставВзысканнаяСумма") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарплатаКВыплате",
																	"Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарплатаВзысканнаяСумма",
																	"Видимость", Истина);
		ЗаголовокУточнение = НСтр("ru='удержаний'");
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Зарплата",
																	"ИзменятьСоставСтрок", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарплатаПериодВзаиморасчетов",
																	"ТолькоПросмотр", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарплатаСотрудник",
																	"ТолькоПросмотр", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарплатаПодразделение",
																	"ТолькоПросмотр", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарплатаДокументОснование",
																	"ТолькоПросмотр", Истина);
		
	КонецЕсли;
	
	Заголовок = СтрШаблон(НСтр("ru='Расшифровка %1 (%2)%3'"),
							ЗаголовокУточнение,
							Строка(ФизическоеЛицо),
							?(ТолькоПросмотр, НСтр("ru=' (только просмотр)'"), ""));
	
	ИтогоКВыплате = Зарплата.Итог("КВыплате");
	ИтогоВзыскано = Зарплата.Итог("ВзысканнаяСумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для ИндексСтроки = 0 По Зарплата.Количество() - 1 Цикл
		
		Если НЕ ЗначениеЗаполнено(Зарплата[ИндексСтроки].Сотрудник) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru='Не указан сотрудник'"),, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Зарплата", ИндексСтроки + 1, "Сотрудник"),,
				Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Зарплата[ИндексСтроки].ПериодВзаиморасчетов) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru='Не задан период взаиморасчетов'"),, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Зарплата", ИндексСтроки + 1, "ПериодВзаиморасчетовСтрокой"),,
				Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Зарплата[ИндексСтроки].КВыплате) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru='Не указана сумма к выплате'"),, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Зарплата", ИндексСтроки + 1, "КВыплате"),,
				Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗарплата

&НаКлиенте
Процедура ЗарплатаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		ЗаполнитьЗначенияСвойств(Элемент.ТекущиеДанные, ЭтотОбъект);
		Элемент.ТекущиеДанные.ПериодВзаиморасчетов                 = ПериодРегистрации;
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Зарплата, "ПериодВзаиморасчетов", "ПериодВзаиморасчетовСтрокой");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.СтатьяРасходов) Тогда
		Элемент.ТекущиеДанные.СтатьяРасходов = СтатьяРасходов;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидДоходаИсполнительногоПроизводства) Тогда
		Элемент.ТекущиеДанные.ВидДоходаИсполнительногоПроизводства = ВидДоходаИсполнительногоПроизводства;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаСотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ СотрудникСоответствуетФизическомуЛицу(ВыбранноеЗначение, ФизическоеЛицо) Тогда
		
		ВыбранноеЗначение = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Недопустимое значение'"), , 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Зарплата[%1].Сотрудник", 
				Зарплата.Индекс(Элементы.Зарплата.ТекущиеДанные)));
		
	КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаПериодВзаиморасчетовПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.Зарплата.ТекущиеДанные, "ПериодВзаиморасчетов", "ПериодВзаиморасчетовСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаПериодВзаиморасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, Элементы.Зарплата.ТекущиеДанные, "ПериодВзаиморасчетов", "ПериодВзаиморасчетовСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаПериодВзаиморасчетовРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.Зарплата.ТекущиеДанные, "ПериодВзаиморасчетов", "ПериодВзаиморасчетовСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаПериодВзаиморасчетовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаПериодВзаиморасчетовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаПриИзменении(Элемент)
	
	ИтогоКВыплате = Зарплата.Итог("КВыплате");
	ИтогоВзыскано = Зарплата.Итог("ВзысканнаяСумма");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьПараметры()
	
	Параметры.Свойство("ИдентификаторСтроки",   ИдентификаторСтроки);
	Параметры.Свойство("ФизическоеЛицо",        ФизическоеЛицо);
	Параметры.Свойство("Организация",           Организация);
	Параметры.Свойство("Подразделение",         Подразделение);
	Параметры.Свойство("ПериодРегистрации",     ПериодРегистрации);
	Параметры.Свойство("ДатаВыплаты",           ДатаВыплаты);
	Параметры.Свойство("СтатьяФинансирования",  СтатьяФинансирования);
	Параметры.Свойство("СтатьяРасходов",        СтатьяРасходов);
	Параметры.Свойство("ВидДоходаИсполнительногоПроизводства", ВидДоходаИсполнительногоПроизводства);
	Параметры.Свойство("СпособВыплаты",        СпособВыплаты);
	Параметры.Свойство("ЗарплатныйПроектДляПеречисленияНаКартыМир", ДляПеречисленийНаКартыМир);

	
	Зарплата.Сортировать("ПериодВзаиморасчетов, Сотрудник, Подразделение");
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗарплата()
	
	Зарплата.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВХранилищеЗарплатыПоСтроке));
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(
		Зарплата, 
		"ПериодВзаиморасчетов", 
		"ПериодВзаиморасчетовСтрокой");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СотрудникСоответствуетФизическомуЛицу(Сотрудник, ФизическоеЛицо)
	ФизическоеЛицоСотрудника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо");
	Возврат ФизическоеЛицоСотрудника = ФизическоеЛицо
КонецФункции

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПроверитьЗаполнение() Тогда
		Если Не ПроверитьЗаполнениеНеблокирующихРеквизитов() Тогда
			Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрытьПоказатьВопросЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'В таблице расшифровки выплат выявлены ошибки.
				|Продолжить?'"), РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Иначе
			СохранитьИЗакрытьПоказатьВопросЗавершение(КодВозвратаДиалога.Да);
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрытьПоказатьВопросЗавершение(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	РезультатыРедактирования = Новый Структура;
	РезультатыРедактирования.Вставить("Модифицированность", Модифицированность);
	РезультатыРедактирования.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
	РезультатыРедактирования.Вставить("АдресВХранилищеЗарплатыПоСтроке", АдресВХранилищеЗарплатыПоСтроке());
	
	Модифицированность = Ложь;
	Закрыть(РезультатыРедактирования)
	
КонецПроцедуры

&НаСервере
Функция АдресВХранилищеЗарплатыПоСтроке()
	
	ЗарплатаФизлица = Зарплата.Выгрузить();
	ЗарплатаФизлица.ЗаполнитьЗначения(ИдентификаторСтроки, "ИдентификаторСтроки");
	
	Возврат ПоместитьВоВременноеХранилище(ЗарплатаФизлица, УникальныйИдентификатор);
	
КонецФункции	

&НаСервере
Процедура ПолучитьЕдинственногоСотрудника()
	
	СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организация);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоМесяца(ПериодРегистрации));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецМесяца(ПериодРегистрации));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("СписокФизическихЛиц",           СписокФизическихЛиц);
	
	ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	Если ТаблицаСотрудников.Количество() <> 1 Тогда
		Элементы.ЗарплатаСотрудник.Видимость = Истина;
	Иначе
		Элементы.ЗарплатаСотрудник.Видимость = Ложь;
		Сотрудник = ТаблицаСотрудников[0].Сотрудник;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНеблокирующихРеквизитов()
	
	Отказ = Ложь;
	
	Если ИспользоватьРасчетПервойПоловиныМесяца Тогда
		Для ИндексСтроки = 0 По Зарплата.Количество() - 1 Цикл
			Если НЕ ЗначениеЗаполнено(Зарплата[ИндексСтроки].ДокументОснование) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru='Не указан документ-основание'"),, 
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Зарплата", ИндексСтроки + 1, "ДокументОснование"),,
					Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти
