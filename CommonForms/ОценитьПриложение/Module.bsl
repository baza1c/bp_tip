#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоБизнесСтарт = ОбщегоНазначенияБП.ЭтоБизнесСтарт();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если МобильныйКлиентБПКлиент.ЭтоAndroid() Тогда
		Если ЭтоБизнесСтарт Тогда
			АдресМагазина = "market://details?id=com.e1c.mcstartbusiness";
		Иначе
			АдресМагазина = "market://details?id=com.e1c.mcaccounting";
		КонецЕсли;
	ИначеЕсли МобильныйКлиентБПКлиент.ЭтоiOS() Тогда
		Если ЭтоБизнесСтарт Тогда
			АдресМагазина = "https://apps.apple.com/ru/app/id6670476914";
		Иначе
			АдресМагазина = "https://apps.apple.com/ru/app/id6753802867"
		КонецЕсли;
	КонецЕсли;
	
	Элементы.Декорация16.Заголовок = НСтр("ru = 'Мы рады, что вам понравилось. 
									|Расскажите о нас друзьям.';
									|en = 'We''re glad you enjoyed it.
									|Please tell your friends about us.'");
	
	Оценка = 0;
	НачальнаяОценка = 0;
	УправлениеФормой(ЭтаФорма, НачальнаяОценка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если НЕ ЗавершениеРаботы Тогда
		ПриЗакрытииНаСервере(Оценка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Декорация1Нажатие(Элемент)
	Оценка = 1;
	УправлениеФормой(ЭтаФорма, Оценка);
	
	ТекстПодсказки = НСтр("ru = 'Напишите нам: что вам не понравилось в приложении?';
							|en = 'Please give us your feedback: what didn''t you like?'");
	Элементы.Декорация9.Заголовок = ТекстПодсказки;
КонецПроцедуры

&НаКлиенте
Процедура Декорация2Нажатие(Элемент)
	Оценка = 2;
	УправлениеФормой(ЭтаФорма, Оценка);
	Элементы.Декорация9.Заголовок = НСтр("ru = 'Напишите нам: что вам не понравилось в приложении?';
											|en = 'Please give us your feedback: what didn''t you like?'");
КонецПроцедуры

&НаКлиенте
Процедура Декорация3Нажатие(Элемент)
	Оценка = 3;
	УправлениеФормой(ЭтаФорма, Оценка);
	Элементы.Декорация9.Заголовок = НСтр("ru = 'Напишите нам: с какими трудностями вы столкнулись?';
											|en = 'Please give us your feedback: which problems did you encounter?'");
КонецПроцедуры

&НаКлиенте
Процедура Декорация4Нажатие(Элемент)
	Оценка = 4;
	УправлениеФормой(ЭтаФорма, Оценка);
	Элементы.Декорация9.Заголовок = НСтр("ru = 'Напишите нам: что мы можем сделать, чтобы приложение стало полезнее?';
										|en = 'Please give us your feedback: what can we do to make the app more useful?'");
КонецПроцедуры

&НаКлиенте
Процедура Декорация5Нажатие(Элемент)
	Оценка = 5;
	УправлениеФормой(ЭтаФорма, Оценка);
	Элементы.Декорация16.Заголовок = НСтр("ru = 'Мы рады, что вам понравилось. 
									|Расскажите о нас друзьям.';
									|en = 'We''re glad you enjoyed it.
									|Please tell your friends about us.'");
	
	#Если ЭтоМобильныйКлиент Тогда
		ПоказатьВводОценкиПриложения();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация7Нажатие(Элемент)
	
	Закрыть();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПисьмо(Команда = Неопределено)
	
	ТекстВопроса = ТекстПисьма;
	
	МобильныйКлиентБПКлиент.НачатьОтправкуПисьмаРазработчикам(ТекстВопроса);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВМагазин(Команда)
	
	ЗапуститьПриложение(АдресМагазина);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Если НЕ ПустаяСтрока(ТекстПисьма) Тогда
		КнопкиДиалога = Новый СписокЗначений;
		КнопкиДиалога.Добавить("Закрыть", НСтр("ru = 'Закрыть форму'; en = 'Close'"));
		КнопкиДиалога.Добавить("Отправить", НСтр("ru = 'Отправить письмо'; en = 'Send message'"));
		ОписаниеЗаверешенияВопроса = Новый ОписаниеОповещения("ЗакрытьФормуЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'При закрытии формы текст письма будет потерян.'
							|en = 'The message text will be lost'");
		ПоказатьВопрос(ОписаниеЗаверешенияВопроса, "", КнопкиДиалога,,"Отправить",ТекстВопроса);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуЗавершение(КнопкаОтвет, ДопПараметры) Экспорт
	
	Если КнопкаОтвет = "Закрыть" Тогда
		Закрыть();
	ИначеЕсли КнопкаОтвет = "Отправить" Тогда
		ОтправитьПисьмо();
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма, Оценка)
	
	Элементы = Форма.Элементы;
	
	УсловиеПерейтиВМагазин = Оценка = 5;
	
	Элементы.ГруппаОценка.Видимость   = УсловиеПерейтиВМагазин;
	Элементы.ГруппаПисьмо.Видимость   = Оценка > 0 И Оценка < 5;
	Элементы.ГруппаОтложить.Видимость = Оценка = 0;
	
	Элементы.Декорация1.Картинка = ?(Оценка > 0, БиблиотекаКартинок.ЖелтаяЗвезда, БиблиотекаКартинок.СераяЗвезда);
	Элементы.Декорация2.Картинка = ?(Оценка > 1, БиблиотекаКартинок.ЖелтаяЗвезда, БиблиотекаКартинок.СераяЗвезда);
	Элементы.Декорация3.Картинка = ?(Оценка > 2, БиблиотекаКартинок.ЖелтаяЗвезда, БиблиотекаКартинок.СераяЗвезда);
	Элементы.Декорация4.Картинка = ?(Оценка > 3, БиблиотекаКартинок.ЖелтаяЗвезда, БиблиотекаКартинок.СераяЗвезда);
	Элементы.Декорация5.Картинка = ?(Оценка > 4, БиблиотекаКартинок.ЖелтаяЗвезда, БиблиотекаКартинок.СераяЗвезда);
	
	УсловиеПоказатьТекстПисьма = Оценка > 0;
	
	Элементы.ТекстПисьма.Видимость = УсловиеПоказатьТекстПисьма;
	Элементы.ОбщаяКомандаСообщитьРазработчикам.Видимость = УсловиеПоказатьТекстПисьма;
	
	Элементы.Декорация16.Видимость = Оценка > 4;
	
	ПоказатьПриглашениеВМагазин = Оценка > 4;
	Элементы.ДекорацияОценкаЗаглушка.Видимость = Не ПоказатьПриглашениеВМагазин;
	Элементы.Декорация13.Видимость = ПоказатьПриглашениеВМагазин;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере(Оценка)
	
	Если Оценка <> 0 Тогда
		РегистрыСведений.ОценкаПриложения.УстановитьОценку(Оценка);
	Иначе
		РегистрыСведений.СчетчикСобытийОценкаПриложения.НачатьОтсчетВторогоПорога();
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти


