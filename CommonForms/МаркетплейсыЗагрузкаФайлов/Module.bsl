#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Маркетплейс") Тогда
		Маркетплейс = Параметры.Маркетплейс;
	КонецЕсли;
	
	Если Параметры.Свойство("ЭтоЗагрузкаЧерезAPI") Тогда
		ЭтоЗагрузкаЧерезAPI = Параметры.ЭтоЗагрузкаЧерезAPI;
		АдресХранилища = Параметры.АдресХранилища;
		ОтобразитьРезультатЧтенияФайлов();
	КонецЕсли;
	
	ЗагрузитьНастройкиФормы();
	
	ВедетсяСкладскойУчет = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет");
	ВестиУчетРасчетовСКонтрагентами = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСКонтрагентами");
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийБухгалтерскийУчет") Тогда
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	УправлениеФормой(ЭтотОбъект);
				
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДокументы Тогда
		Если ИмяСобытия = "Запись_ОтчетКомиссионераОПродажах"
			Или ИмяСобытия = "Запись_РеализацияТоваровУслуг"
			Или ИмяСобытия = "Запись_ВозвратТоваровОтПокупателя"
			Или ИмяСобытия = "Запись_ОтчетОРозничныхПродажах" Тогда
			Если ТипЗнч(Параметр) = Тип("Структура")
				И Параметр.Свойство("РежимЗаписи") Тогда
				ОбновитьСостояниеСозданныхДокументов(Параметр.РежимЗаписи, Источник, СозданныеДокументы);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Для Каждого СтрокаДанных из ЗагружаемыеФайлыWB.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(СтрокаДанных.Организация)
			И Организация <> СтрокаДанных.Организация Тогда
			СтрокаДанных.Загружать = Ложь;
			СтрокаДанных.Комментарий = НСтр("ru='Загрузка выполняется по другой организации.'");
			Для Каждого Подстрока Из СтрокаДанных.ПолучитьЭлементы() Цикл
				Подстрока.Загружать = Ложь;
				Подстрока.Комментарий = НСтр("ru='Загрузка выполняется по другой организации.'");
			КонецЦикла;
		Иначе
			СтрокаДанных.Комментарий = "";
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаДанных из ЗагружаемыеФайлыOzon.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(СтрокаДанных.Организация)
			И Организация <> СтрокаДанных.Организация Тогда
			СтрокаДанных.Загружать = Ложь;
			СтрокаДанных.Комментарий = НСтр("ru='Загрузка выполняется по другой организации.'");
			Для Каждого Подстрока Из СтрокаДанных.ПолучитьЭлементы() Цикл
				Подстрока.Загружать = Ложь;
				Подстрока.Комментарий = НСтр("ru='Загрузка выполняется по другой организации.'");
			КонецЦикла;
		Иначе
			СтрокаДанных.Комментарий = "";
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаДанных из ЗагружаемыеФайлыМегамаркет Цикл
		Если ЗначениеЗаполнено(СтрокаДанных.Организация)
			И Организация <> СтрокаДанных.Организация Тогда
			СтрокаДанных.Загружать = Ложь;
			СтрокаДанных.Комментарий = НСтр("ru='Загрузка выполняется по другой организации.'");
		Иначе
			СтрокаДанных.Комментарий = "";
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		НайтиДокументыИБ(ЗагружаемыеФайлыWB.ПолучитьЭлементы(), ДокументыИБ, КонтрагентWB, Организация, АдресХранилища);
		НайтиДокументыИБ(ЗагружаемыеФайлыOzon.ПолучитьЭлементы(), ДокументыИБ, КонтрагентOzon, Организация, АдресХранилища, "МЕСЯЦ");
		НайтиДокументыИБМегамаркет(ЗагружаемыеФайлыМегамаркет, ДокументыИБ, Организация);
		ПолучитьНастройкиМегамаркета();
		Элементы.ФормаЗагрузить.Доступность = Истина;
	Иначе
		Элементы.ФормаЗагрузить.Доступность = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыWB.ПолучитьЭлементы(), Элементы.ЗагружаемыеФайлыWBГруппаДокумент);
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыOzon.ПолучитьЭлементы(), Элементы.ЗагружаемыеФайлыOzonГруппаДокумент);
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыМегамаркет, Элементы.ЗагружаемыеФайлыМегамаркетГруппаДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КонтрагентWBПриИзмененииНаСервере()
	НайтиДокументыИБ(ЗагружаемыеФайлыWB.ПолучитьЭлементы(), ДокументыИБ, КонтрагентWB, Организация, АдресХранилища);
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыWB.ПолучитьЭлементы(), Элементы.ЗагружаемыеФайлыWBГруппаДокумент);
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентWBПриИзменении(Элемент)
	КонтрагентWBПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КонтрагентOzonПриИзмененииНаСервере()
	НайтиДокументыИБ(ЗагружаемыеФайлыOzon.ПолучитьЭлементы(), ДокументыИБ, КонтрагентOzon, Организация, АдресХранилища, "МЕСЯЦ");
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыOzon.ПолучитьЭлементы(), Элементы.ЗагружаемыеФайлыOzonГруппаДокумент);
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентOzonПриИзменении(Элемент)
	КонтрагентOzonПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПеретащитьИзображениеНажатие(Элемент)
	
	ВыполнитьЗагрузкуФайловИнтерактивно();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПеретащитьИзображениеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПеретащитьИзображениеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьФайлВСписок(ПараметрыПеретаскивания.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Не ВыполняетсяЗагрузка Тогда
		ВыполнитьЗагрузкуФайловИнтерактивно();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если ВыполняетсяЗагрузка Тогда 
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
	Иначе 
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если Не ВыполняетсяЗагрузка Тогда
		ДобавитьФайлВСписок(ПараметрыПеретаскивания.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеФайлыWBДокументПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ЗагружаемыеФайлыWB.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки, Документ", ТекущиеДанные.КлючСтроки, ТекущиеДанные.Документ));
		Если СсылкиНаДокументы.Количество() > 0 Тогда
			ТекущиеДанные.ДокументПредставление = СсылкиНаДокументы[0].Представление;
			ТекущиеДанные.ДокументПредставление1 = СсылкиНаДокументы[0].Представление1;
			ТекущиеДанные.ДокументПредставление2 = СсылкиНаДокументы[0].Представление2;
		КонецЕсли;
	Иначе
		ТекущиеДанные.ДокументПредставление = НСтр("ru='Создать новый документ'");
		ТекущиеДанные.ДокументПредставление1 = НСтр("ru='Создать новый документ'");
		ТекущиеДанные.ДокументПредставление2 = "";
	КонецЕсли;
	Элемент.Заголовок = ТекущиеДанные.ДокументПредставление1;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеФайлыWBВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЗагружаемыеФайлыWBДокумент" Тогда
		ТекущиеДанные = Элементы.ЗагружаемыеФайлыWB.ТекущиеДанные;
		
		СписокВыбора = Элементы.ЗагружаемыеФайлыWBДокумент.СписокВыбора;
		СписокВыбора.Очистить();
		
		Если ЭтоРеестрПродажЮрлицамWB(ТекущиеДанные.ВидДокумента) Тогда
			Элементы.ЗагружаемыеФайлыWBДокумент.ТолькоПросмотр = Не ЗначениеЗаполнено(ТекущиеДанные.Документ);
		Иначе
			Элементы.ЗагружаемыеФайлыWBДокумент.ТолькоПросмотр = Ложь;
			СписокВыбора.Добавить("", НСтр("ru='Создать новый документ'"));
		КонецЕсли;
		
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", ТекущиеДанные.КлючСтроки));
		Для Каждого ДокументИБ ИЗ СсылкиНаДокументы Цикл
			СписокВыбора.Добавить(ДокументИБ.Документ, ДокументИБ.Представление);
		КонецЦикла;
	ИначеЕсли Поле.Имя = "ЗагружаемыеФайлыWBДействие" Тогда
		Если Не ВыполняетсяЗагрузка Тогда
			ВыполнитьЗагрузкуФайловИнтерактивно();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеФайлыOzonДокументПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ЗагружаемыеФайлыOzon.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки, Документ", ТекущиеДанные.КлючСтроки, ТекущиеДанные.Документ));
		Если СсылкиНаДокументы.Количество() > 0 Тогда
			ТекущиеДанные.ДокументПредставление = СсылкиНаДокументы[0].Представление;
			ТекущиеДанные.ДокументПредставление1 = СсылкиНаДокументы[0].Представление1;
			ТекущиеДанные.ДокументПредставление2 = СсылкиНаДокументы[0].Представление2;
		КонецЕсли;
	Иначе
		ТекущиеДанные.ДокументПредставление = НСтр("ru='Создать новый документ'");
		ТекущиеДанные.ДокументПредставление1 = НСтр("ru='Создать новый документ'");
		ТекущиеДанные.ДокументПредставление2 = "";
	КонецЕсли;
	Элемент.Заголовок = ТекущиеДанные.ДокументПредставление1;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеФайлыOzonВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЗагружаемыеФайлыOzonДокумент" Тогда
		ТекущиеДанные = Элементы.ЗагружаемыеФайлыOzon.ТекущиеДанные;
		
		СписокВыбора = Элементы.ЗагружаемыеФайлыOzonДокумент.СписокВыбора;
		СписокВыбора.Очистить();
		
		Если ЭтоРеестрПродажЮрлицамOzon(ТекущиеДанные.ВидДокумента) Тогда
			Элементы.ЗагружаемыеФайлыOzonДокумент.ТолькоПросмотр = Не ЗначениеЗаполнено(ТекущиеДанные.Документ);
		Иначе
			Элементы.ЗагружаемыеФайлыOzonДокумент.ТолькоПросмотр = Ложь;
			СписокВыбора.Добавить("", НСтр("ru='Создать новый документ'"));
		КонецЕсли;
		
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", ТекущиеДанные.КлючСтроки));
		Для Каждого ДокументИБ ИЗ СсылкиНаДокументы Цикл
			СписокВыбора.Добавить(ДокументИБ.Документ, ДокументИБ.Представление);
		КонецЦикла;
		
	ИначеЕсли Поле.Имя = "ЗагружаемыеФайлыOzonДействие" Тогда
		Если Не ВыполняетсяЗагрузка Тогда
			ВыполнитьЗагрузкуФайловИнтерактивно();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеФайлыМегамаркетВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЗагружаемыеФайлыМегамаркетДокумент" Тогда
		
		СписокВыбора = Элементы.ЗагружаемыеФайлыМегамаркетДокумент.СписокВыбора;
		
		СписокВыбора.Очистить();
		
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Возврат;
		КонецЕсли;
		
		СписокВыбора.Добавить("", НСтр("ru='Создать новый документ'"));
		
		ТекущиеДанные = Элементы.ЗагружаемыеФайлыМегамаркет.ТекущиеДанные;
		
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", ТекущиеДанные.КлючСтроки));
		Для Каждого ДокументИБ ИЗ СсылкиНаДокументы Цикл
			СписокВыбора.Добавить(ДокументИБ.Документ, ДокументИБ.Представление);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеФайлыМегамаркетДокументПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗагружаемыеФайлыМегамаркет.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки, Документ", ТекущиеДанные.КлючСтроки, ТекущиеДанные.Документ));
		Если СсылкиНаДокументы.Количество() > 0 Тогда
			ТекущиеДанные.ДокументПредставление = СсылкиНаДокументы[0].Представление;
			ТекущиеДанные.ДокументПредставление1 = СсылкиНаДокументы[0].Представление1;
			ТекущиеДанные.ДокументПредставление2 = СсылкиНаДокументы[0].Представление2;
		КонецЕсли;
	Иначе
		ТекущиеДанные.ДокументПредставление = НСтр("ru='Создать новый документ'");
		ТекущиеДанные.ДокументПредставление1 = НСтр("ru='Создать новый документ'");
		ТекущиеДанные.ДокументПредставление2 = "";
	КонецЕсли;
	Элемент.Заголовок = ТекущиеДанные.ДокументПредставление1;
	
КонецПроцедуры

&НаКлиенте
Процедура СозданныеДокументыПередНачаломИзменения(Элемент, Отказ)
	ТекущиеДанные = Элементы.СозданныеДокументы.ТекущиеДанные;
	Отказ = Истина;
	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Ссылка);
		Если ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", ПараметрыФормы, , , , , );
		КонецЕсли;
	Иначе
		СтрокаРодителя = СозданныеДокументы.НайтиПоИдентификатору(Элементы.СозданныеДокументы.ТекущаяСтрока);
		Если СтрокаРодителя <> Неопределено И СтрокаРодителя.ПолучитьЭлементы().Количество() > 0 Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ПродажаТоваровСоСкладаКомиссионера", Истина);
			ПараметрыФормы.Вставить("Организация", Организация);
			ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Организация));
			Если Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries") Тогда
				ПараметрыФормы.Вставить("Период", Новый СтандартныйПериод(НачалоНедели(ТекущиеДанные.Дата), КонецНедели(ТекущиеДанные.Дата)));
			Иначе
				ПараметрыФормы.Вставить("Период", Новый СтандартныйПериод(НачалоМесяца(ТекущиеДанные.Дата), КонецМесяца(ТекущиеДанные.Дата)));
			КонецЕсли;
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаСписка", ПараметрыФормы, ЭтаФорма, Новый УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Не указана организация'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"Организация");
		Возврат;
	КонецЕсли;
	
	Если Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Мегамаркет") Тогда
		Если ВедетсяСкладскойУчет И Не ЗначениеЗаполнено(Склад) Тогда
			ТекстСообщения = НСтр("ru = 'Не указан склад продаж'"); 
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Склад");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекстСообщенияЗагрузки = НСтр("ru = 'Создание документов ...'");
	ЭтотОбъект.Заголовок = НСтр("ru = 'Создание документов'");
	Элементы.ФормаЗакрыть.Заголовок = НСтр("ru = 'Прервать'");
	ПрогрессЗагрузки = 0;
	Элементы.ГруппаНастройки.Видимость = Ложь;
	Элементы.ФормаЗагрузить.Видимость = Ложь;
	Элементы.СостояниеЗагрузки.Видимость = Истина;
	Элементы.ПрогрессЗагрузки.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДокументы;
	Элементы.СозданныеДокументыПровестиДокументы.Видимость = Ложь;
			
	МассивКСопоставлению = СписокНеСопоставленнойНоменклатурыНаСервере();
	Если МассивКСопоставлению.Количество() > 0 
		И ВестиУчетРасчетовСКонтрагентами Тогда
		НачатьСопоставлениеНоменклатуры(МассивКСопоставлению, 0);
	Иначе
		НачатьСозданиеДокументов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокументы(Команда)
	
	Если ВыполняетсяЗагрузка Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СостояниеЗагрузки.Видимость = Истина;
	Элементы.СозданныеДокументыПровестиДокументы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Элементы.ФормаЗакрыть.Доступность = Ложь;
	
	ВыполняетсяЗагрузка = Истина;
	ПрогрессЗагрузки = 0;
	
	ФоновоеЗадание = ПровестиСозданныеДокументы();
	
	Если ФоновоеЗадание = Неопределено Тогда
		ТекстСообщенияЗагрузки = НСтр("ru = 'Проведение документов прервано из-за ошибки'");
		ЭтотОбъект.Заголовок = НСтр("ru = 'Ошибка при проведении документов'");
		Элементы.ФормаЗакрыть.Заголовок = НСтр("ru = 'Закрыть'");
		Возврат;
	КонецЕсли;
	
	ОбработчикПрогресса = Новый ОписаниеОповещения("ПриПрогрессеПроведенияСозданныхДокументов", ЭтотОбъект);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	НастройкиОжидания.ОповещениеОПрогрессеВыполнения = ОбработчикПрогресса;
	
	Обработчик = Новый ОписаниеОповещения("ПослеПроведенияСозданныхДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область ПомещениеФайловНаСервер

&НаКлиенте
Процедура ДобавитьФайлВСписок(СсылкиНаФайлы)
	
	Если ТипЗнч(СсылкиНаФайлы) = Тип("СсылкаНаФайл") Тогда
		СсылкиНаФайлы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкиНаФайлы);
	КонецЕсли;
	
	Для Каждого СсылкаНаФайл Из СсылкиНаФайлы Цикл
		ДобавитьФайл(СсылкаНаФайл.Имя, СсылкаНаФайл.ИдентификаторФайла, СсылкаНаФайл.Расширение);
	КонецЦикла;
	
	ВыполнитьЗагрузкуФайлов(СсылкиНаФайлы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуФайловИнтерактивно()
	
	Если Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Мегамаркет") Тогда
		ФильтрРасширенийФайлов = "(*.zip)|*.zip";
	Иначе
		ФильтрРасширенийФайлов = "(*.xlsx;*.xls;*.mxl;*.zip)|*.xlsx;*.xls;*.mxl;*.zip";
	КонецЕсли;
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов(НСтр("ru = 'Выберите файлы для загрузки'"), Истина, ФильтрРасширенийФайлов);
	ВыполнитьЗагрузкуФайлов(ПараметрыДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуФайлов(СсылкиНаФайлыИлиПараметрыДиалога)
	
	НачатьПомещениеФайловНаСервер(
		Новый ОписаниеОповещения("ПриЗавершенииПомещенияФайлов", ЭтотОбъект),
		Новый ОписаниеОповещения("ПриВыполненииПомещенияФайлов", ЭтотОбъект),
		Новый ОписаниеОповещения("ПередНачаломПомещенияФайлов", ЭтотОбъект),
		СсылкиНаФайлыИлиПараметрыДиалога,
		УникальныйИдентификатор
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередНачаломПомещенияФайлов(ПомещаемыеФайлы, ОтказОтПомещенияВсехФайлов, Контекст) Экспорт
	
	Если ПомещаемыеФайлы.Количество() = 0 Тогда
		ОтказОтПомещенияВсехФайлов = Истина;
	Иначе
		ВыполняетсяЗагрузка = Истина;
		ТекстСообщенияЗагрузки = НСтр("ru = 'Загрузка файлов ...'");
		ПерерисоватьФайлыПоСостоянию();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриВыполненииПомещенияФайлов(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ПомещеноВсего, ОтказОтПомещенияВсехФайлов, Контекст) Экспорт
	
	Если Не ЭтотОбъект.Открыта() Тогда
		ОтказОтПомещенияВсехФайлов = Истина;
	КонецЕсли;
	
	СтрокиФайлов = Файлы.НайтиСтроки(Новый Структура("Наименование, Разобран", ПомещаемыйФайл.Имя, Истина));
	Если СтрокиФайлов.Количество() > 0 Тогда
		ОтказОтПомещенияФайла = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПомещенияФайлов(ПомещенныеФайлы, Контекст) Экспорт
	
	Если ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		ОбновитьСостояниеФайлов(ПомещенныеФайлы);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("РазобратьФайлы", 0.2, Истина);
    
КонецПроцедуры

#КонецОбласти

#Область РазборФайлов

&НаКлиенте
Процедура РазобратьФайлы()
	
	ФоновоеЗадание = ЗапуститьФоновыйРазборФайлов();
	
	ОбработчикПрогресса = Новый ОписаниеОповещения("ПриПрогрессеРазбораФайлов", ЭтотОбъект);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	НастройкиОжидания.ОповещениеОПрогрессеВыполнения = ОбработчикПрогресса;
	
	Обработчик = Новый ОписаниеОповещения("ПослеРазбораФайлов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновыйРазборФайлов()
	
	ОтправляемыеФайлы = Новый Массив;
	Для Каждого Файл Из Файлы Цикл 
		
		Если Файл.Разобран
			Или Не ЗначениеЗаполнено(Файл.Адрес) Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("ИмяФайла", Файл.Наименование);
		
		ВременныйФайл = ПолучитьИмяВременногоФайла(Файл.Расширение);
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Файл.Адрес);
		ДвоичныеДанные.Записать(ВременныйФайл);
		ОписаниеФайла.Вставить("РасширениеФайла", Файл.Расширение);
		ОписаниеФайла.Вставить("ПолноеИмяФайла", Файл.Наименование);
		ОписаниеФайла.Вставить("ВременныйФайл", ВременныйФайл);
		
		ОтправляемыеФайлы.Добавить(ОписаниеФайла);
	КонецЦикла;
		
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Разбор файлов, полученных из маркетплейса'");
	Если ЗначениеЗаполнено(АдресХранилища) Тогда
		ДанныеРазобранныхФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
		АдресВременногоХранилища =  ПоместитьВоВременноеХранилище(ДанныеРазобранныхФайлов, ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ЭлектронноеВзаимодействиеБП.ПолучитьДанныеВСтруктуру",
		ОтправляемыеФайлы, АдресХранилища);
	
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаКлиенте
Процедура ПослеРазбораФайлов(Задание, Контекст) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполняетсяЗагрузка = Ложь;
	ПодключитьОбработчикОжидания("ПерерисоватьФайлыПоСостоянию", 0.2, Истина);
	
	Если Задание.Статус = "Выполнено" Тогда
		ПослеРазбораФайловНаСервере();
		ОтобразитьРезультатЧтенияФайлов();
		
		ПодключитьОбработчикОжидания("ПроверитьНаличиеФайловДляЗагрузки", 0.2, Истина);
		
	Иначе
		ПоказатьПредупреждение(, Задание.КраткоеПредставлениеОшибки);
		АдресХранилища = АдресВременногоХранилища;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеРазбораФайловНаСервере()
	
	ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(ДанныеФайлов) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРазобранныхФайлов = Неопределено;
	Если ЗначениеЗаполнено(АдресВременногоХранилища) Тогда
		ДанныеРазобранныхФайлов = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		Если ТипЗнч(ДанныеРазобранныхФайлов) <> Тип("Массив") Тогда
			ДанныеРазобранныхФайлов = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Файл Из ДанныеФайлов Цикл
		Если ДанныеРазобранныхФайлов <> Неопределено Тогда
			ДанныеРазобранныхФайлов.Добавить(Файл);
		КонецЕсли;
		
		Если Файл.Свойство("ТекстОшибки") Тогда
			ИнформацияОФайле = "";
			Если Файл.Свойство("ИмяФайла") И Файл.ПолноеИмяФайла <> Файл.ИмяФайла Тогда
				ИнформацияОФайле = " (" + Файл.ИмяФайла + ")";
			КонецЕсли;
			Файл.Вставить("Описание", Файл.ТекстОшибки + ИнформацияОФайле);
		КонецЕсли;
		
		СтрокиФайлов = Файлы.НайтиСтроки(Новый Структура("Наименование", Файл.ПолноеИмяФайла));
		Для Каждого СтрокаФайлов Из СтрокиФайлов Цикл
			Если Файл.Свойство("ТекстОшибки") Тогда
				Файл.Вставить("Описание", Файл.ТекстОшибки);
			КонецЕсли;
			Если НЕ СтрокаФайлов.Разобран Тогда
				СтрокаФайлов.Разобран = Истина;
				СтрокаФайлов.Описание = Файл.Описание;
			Иначе
				СтрокаФайлов.Описание = СтрокаФайлов.Описание + "
					|" + Файл.Описание;
			КонецЕсли;
		КонецЦикла
	КонецЦикла;
	
	Если ДанныеРазобранныхФайлов = Неопределено Тогда
		ПреобразоватьДанныеФайлов(ДанныеФайлов);
	Иначе
		ПреобразоватьДанныеФайлов(ДанныеРазобранныхФайлов);
		АдресХранилища = ПоместитьВоВременноеХранилище(ДанныеРазобранныхФайлов, ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
				
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьДанныеФайлов(ДанныеФайлов)
	
	ИнтеграцияOZON.ПреобразоватьДанныеФайлов(ДанныеФайлов);
	ИнтеграцияWildberries.ПреобразоватьДанныеФайлов(ДанныеФайлов);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРезультатЧтенияФайлов()
	Если НЕ ЗначениеЗаполнено(АдресХранилища) Тогда
		Возврат;
	КонецЕсли;
	
	ЗагружаемыеФайлыWB.ПолучитьЭлементы().Очистить();
	ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Очистить();
	ЗагружаемыеФайлыМегамаркет.Очистить();
		
	ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(ДанныеФайлов) = Тип("Массив") Тогда
		
		МассивПериодовWB = Новый Массив;
		МассивПериодовOzon = Новый Массив;
		МассивПериодовМегамаркет = Новый Массив;
		Для Каждого Файл Из ДанныеФайлов Цикл
			Если Файл.Свойство("ТекстОшибки") Тогда
				Продолжить;
			КонецЕсли;
			Если Файл.Свойство("ОшибкиПреобразования") Тогда
				ОбщегоНазначения.СообщитьПользователю(Файл.ОшибкиПреобразования);
			КонецЕсли;
			Если Файл.Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
				Если МассивПериодовWB.Найти(НачалоНедели(Файл.Дата)) = Неопределено Тогда
					НоваяСтрока = ЗагружаемыеФайлыWB.ПолучитьЭлементы().Добавить();
					НоваяСтрока.Дата = НачалоНедели(Файл.Дата);
					НоваяСтрока.ТекстЗаголовка = СтрШаблон(НСтр("ru='Период с %1 по %2'"),
						Формат(НачалоНедели(Файл.Дата), "ДФ=dd.MM.yyyy"), Формат(КонецНедели(Файл.Дата), "ДФ=dd.MM.yyyy"));
					НоваяСтрока.КлючСтроки = "-1";
					МассивПериодовWB.Добавить(НачалоНедели(Файл.Дата));
				КонецЕсли;
				НоваяСтрока = ЗагружаемыеФайлыWB.ПолучитьЭлементы().Добавить();
				Если Не ЗначениеЗаполнено(КонтрагентWB)
					И ЗначениеЗаполнено(Файл.Контрагент) Тогда
					КонтрагентWB = Файл.Контрагент;
				КонецЕсли;
			ИначеЕсли Файл.Маркетплейс = Перечисления.ВидыМаркетплейсов.OZON Тогда
				Если МассивПериодовOzon.Найти(НачалоМесяца(Файл.Дата)) = Неопределено Тогда
					НоваяСтрока = ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Добавить();
					НоваяСтрока.Дата = НачалоМесяца(Файл.Дата);
					НоваяСтрока.ТекстЗаголовка = СтрШаблон(НСтр("ru='Период с %1 по %2'"),
						Формат(НачалоМесяца(Файл.Дата), "ДФ=dd.MM.yyyy"), Формат(КонецМесяца(Файл.Дата), "ДФ=dd.MM.yyyy"));
					НоваяСтрока.КлючСтроки = "-1";
					МассивПериодовOzon.Добавить(НачалоМесяца(Файл.Дата));
				КонецЕсли;
				НоваяСтрока = ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Добавить();
				Если Не ЗначениеЗаполнено(КонтрагентOzon)
					И ЗначениеЗаполнено(Файл.Контрагент) Тогда
					КонтрагентOzon = Файл.Контрагент;
				КонецЕсли;
			ИначеЕсли Файл.Маркетплейс = Перечисления.ВидыМаркетплейсов.Мегамаркет Тогда
				Если МассивПериодовМегамаркет.Найти(НачалоМесяца(Файл.Дата)) = Неопределено Тогда
					НоваяСтрока = ЗагружаемыеФайлыМегамаркет.Добавить();
					НоваяСтрока.Дата = НачалоМесяца(Файл.Дата);
					НоваяСтрока.ТекстЗаголовка = СтрШаблон(НСтр("ru='Период с %1 по %2'"),
						Формат(НачалоМесяца(Файл.Дата), "ДФ=dd.MM.yyyy"), Формат(КонецМесяца(Файл.Дата), "ДФ=dd.MM.yyyy"));
					НоваяСтрока.КлючСтроки = "-1";
					МассивПериодовМегамаркет.Добавить(НачалоМесяца(Файл.Дата));
				КонецЕсли;
				НоваяСтрока = ЗагружаемыеФайлыМегамаркет.Добавить();
				Если Не ЗначениеЗаполнено(КонтрагентМегамаркет)
					И ЗначениеЗаполнено(Файл.Контрагент) Тогда
					КонтрагентМегамаркет = Файл.Контрагент;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Файл);
			НоваяСтрока.КлючСтроки = ДанныеФайлов.Найти(Файл);
			
			Если ЗначениеЗаполнено(НоваяСтрока.Описание) Тогда
				МассивПодстрок = СтрРазделить(НоваяСтрока.Описание, Символы.ПС, Ложь);
				Если МассивПодстрок.Количество() > 0 Тогда
					НоваяСтрока.Описание1 = МассивПодстрок[0];
				Иначе
					НоваяСтрока.Описание1 = НоваяСтрока.Описание;
				КонецЕсли;
				Если МассивПодстрок.Количество() > 1 Тогда
					НоваяСтрока.Описание2 = МассивПодстрок[1];
				КонецЕсли;
				Если МассивПодстрок.Количество() > 2 Тогда
					НоваяСтрока.Описание3 = МассивПодстрок[2];
				КонецЕсли;
			КонецЕсли;
						
			Если Не ЗначениеЗаполнено(Организация)
				И ЗначениеЗаполнено(Файл.Организация) Тогда
				Организация = Файл.Организация;
			ИначеЕсли ЗначениеЗаполнено(Файл.Организация)
				И Организация <> Файл.Организация Тогда
				
				НоваяСтрока.Загружать = Ложь;
				НоваяСтрока.Комментарий = НСтр("ru='Загрузка выполняется по другой организации.'");
			КонецЕсли;
			
			Если Файл.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon() Тогда
				ЗаполнитьПодчиненныеДокументыРеестраПродажЮрлицамOzon(НоваяСтрока, Файл, ДокументыИБ, Организация, КонтрагентOzon);
			ИначеЕсли Файл.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB() Тогда
				ЗаполнитьПодчиненныеДокументыРеестраПродажЮрлицамWB(НоваяСтрока, Файл, ДокументыИБ, Организация, КонтрагентWB);
			ИначеЕсли Файл.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzon()
				И Файл.Свойство("СкорректированНаПродажиЮрлицам")
				И Не Файл.СкорректированНаПродажиЮрлицам Тогда
				НоваяСтрока.Действие = НСтр("ru='+ Добавить: Реестр продаж юридическим лицам'");
			ИначеЕсли Файл.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBДетальный()
				И Файл.Свойство("СкорректированНаПродажиЮрлицам")
				И Не Файл.СкорректированНаПродажиЮрлицам Тогда
				НоваяСтрока.Действие = НСтр("ru='+ Добавить: Реестр продаж юридическим лицам и ИП'");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ДеревоЗагружаемыеФайлыWB = РеквизитФормыВЗначение("ЗагружаемыеФайлыWB");
	ДеревоЗагружаемыеФайлыWB.Строки.Сортировать("Дата, ВидДокумента");
	ЗначениеВРеквизитФормы(ДеревоЗагружаемыеФайлыWB, "ЗагружаемыеФайлыWB");
	ДеревоЗагружаемыеФайлыOzon = РеквизитФормыВЗначение("ЗагружаемыеФайлыOzon");
	ДеревоЗагружаемыеФайлыOzon.Строки.Сортировать("Дата, ВидДокумента");
	ЗначениеВРеквизитФормы(ДеревоЗагружаемыеФайлыOzon, "ЗагружаемыеФайлыOzon");
	ЗагружаемыеФайлыМегамаркет.Сортировать("Дата, ВидДокумента");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		НайтиДокументыИБ(ЗагружаемыеФайлыWB.ПолучитьЭлементы(), ДокументыИБ, КонтрагентWB, Организация, АдресХранилища);
		НайтиДокументыИБ(ЗагружаемыеФайлыOzon.ПолучитьЭлементы(), ДокументыИБ, КонтрагентOzon, Организация, АдресХранилища, "МЕСЯЦ");
		НайтиДокументыИБМегамаркет(ЗагружаемыеФайлыМегамаркет, ДокументыИБ, Организация);
		ПолучитьНастройкиМегамаркета();
		Элементы.ФормаЗагрузить.Доступность = Истина;
	Иначе
		Элементы.ФормаЗагрузить.Доступность = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыWB.ПолучитьЭлементы(), Элементы.ЗагружаемыеФайлыWBГруппаДокумент);
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыOzon.ПолучитьЭлементы(), Элементы.ЗагружаемыеФайлыOzonГруппаДокумент);
	УстановитьВидимостьЗамещаемогоДокумента(ЗагружаемыеФайлыМегамаркет, Элементы.ЗагружаемыеФайлыМегамаркетГруппаДокумент);
	
	Элементы.ГруппаWildberries.Заголовок = СтрШаблон(НСтр("ru='Wildberries (%1)'"), 
		Формат(ЗагружаемыеФайлыWB.ПолучитьЭлементы().Количество() - МассивПериодовWB.Количество(), "ЧН=0"));
	Элементы.ГруппаOZON.Заголовок = СтрШаблон(НСтр("ru='OZON (%1)'"), 
		Формат(ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Количество() - МассивПериодовOzon.Количество(), "ЧН=0"));
	Элементы.ГруппаМегамаркет.Заголовок = СтрШаблон(НСтр("ru='Мегамаркет (%1)'"), 
		Формат(ЗагружаемыеФайлыМегамаркет.Количество() - МассивПериодовМегамаркет.Количество(), "ЧН=0"));
		
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НайтиДокументыИБ(ТаблицаДанных, ДокументыИБ, Контрагент, Организация, АдресХранилища, Периодичность = "НЕДЕЛЯ")
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажах.Ссылка КАК Ссылка,
	|	НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ) КАК НачалоПериода,
	|	ОтчетКомиссионераОПродажах.Ссылка.ВидОперации КАК ВидОперации,
	|	ОтчетКомиссионераОПродажах.НомерВходящегоДокумента КАК НомерВходящегоДокумента
	|ПОМЕСТИТЬ ВТОтчетыКомиссионеров
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ) В (&МассивПериодов)
	|	И НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОтчетыКомиссионеров.Ссылка КАК Ссылка,
	|	ВТОтчетыКомиссионеров.НачалоПериода КАК НачалоПериода,
	|	ВТОтчетыКомиссионеров.ВидОперации КАК ВидОперации,
	|	СУММА(ОтчетКомиссионераОПродажахТовары.Сумма) КАК СуммаПродаж,
	|	0 КАК СуммаВозвратов,
	|	ВТОтчетыКомиссионеров.НомерВходящегоДокумента КАК НомерВходящегоДокумента
	|ПОМЕСТИТЬ ВТОтчетыКомиссионеровССуммами
	|ИЗ
	|	ВТОтчетыКомиссионеров КАК ВТОтчетыКомиссионеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Товары КАК ОтчетКомиссионераОПродажахТовары
	|		ПО (ОтчетКомиссионераОПродажахТовары.Ссылка = ВТОтчетыКомиссионеров.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОтчетыКомиссионеров.Ссылка,
	|	ВТОтчетыКомиссионеров.НачалоПериода,
	|	ВТОтчетыКомиссионеров.ВидОперации,
	|	ВТОтчетыКомиссионеров.НомерВходящегоДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОтчетыКомиссионеров.Ссылка,
	|	ВТОтчетыКомиссионеров.НачалоПериода,
	|	ВТОтчетыКомиссионеров.ВидОперации,
	|	0,
	|	СУММА(ОтчетКомиссионераОПродажахТоварыВозвращенные.Сумма),
	|	ВТОтчетыКомиссионеров.НомерВходящегоДокумента
	|ИЗ
	|	ВТОтчетыКомиссионеров КАК ВТОтчетыКомиссионеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.ТоварыВозвращенные КАК ОтчетКомиссионераОПродажахТоварыВозвращенные
	|		ПО (ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка = ВТОтчетыКомиссионеров.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОтчетыКомиссионеров.Ссылка,
	|	ВТОтчетыКомиссионеров.НачалоПериода,
	|	ВТОтчетыКомиссионеров.ВидОперации,
	|	ВТОтчетыКомиссионеров.НомерВходящегоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОтчетыКомиссионеров.Ссылка КАК Ссылка,
	|	ВТОтчетыКомиссионеров.НачалоПериода КАК НачалоПериода,
	|	ВТОтчетыКомиссионеров.ВидОперации КАК ВидОперации,
	|	0 КАК СуммаВыкупа,
	|	СУММА(ВТОтчетыКомиссионеров.СуммаПродаж) КАК СуммаПродаж,
	|	СУММА(ВТОтчетыКомиссионеров.СуммаВозвратов) КАК СуммаВозвратов,
	|	ВТОтчетыКомиссионеров.НомерВходящегоДокумента КАК НомерВходящегоДокумента
	|ИЗ
	|	ВТОтчетыКомиссионеровССуммами КАК ВТОтчетыКомиссионеров
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОтчетыКомиссионеров.Ссылка,
	|	ВТОтчетыКомиссионеров.НачалоПериода,
	|	ВТОтчетыКомиссионеров.ВидОперации,
	|	ВТОтчетыКомиссионеров.НомерВходящегоДокумента";
	
	ТекстЗапросаВыкуп = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, НЕДЕЛЯ),
	|	РеализацияТоваровУслуг.ВидОперации,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	0,
	|	0,
	|	""""
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Организация = &Организация
	|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, НЕДЕЛЯ) В (&МассивПериодов)
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером)
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления";
	
	Если НЕ Периодичность = "НЕДЕЛЯ" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НЕДЕЛЯ", Периодичность);
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ТекстЗапросаВыкуп;
	
	МассивПериодов = Новый Массив;
	Для Каждого СтрокаДанных из ТаблицаДанных Цикл
		Если СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа()
			ИЛИ СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonВыкуп() Тогда
			НачалоПериода = НачалоНедели(СтрокаДанных.Дата);
		Иначе
			НачалоПериода = ?(Периодичность = "НЕДЕЛЯ", НачалоНедели(СтрокаДанных.Дата), НачалоМесяца(СтрокаДанных.Дата));
		КонецЕсли;
		Если МассивПериодов.Найти(НачалоПериода) = Неопределено Тогда
			МассивПериодов.Добавить(НачалоПериода);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МассивПериодов", МассивПериодов);
	Запрос.Текст      = ТекстЗапроса;
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаДанных из ТаблицаДанных Цикл
		Если СтрокаДанных.КлючСтроки = "-1" Тогда // служебная строка
			Продолжить;
		КонецЕсли;
		Если СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon()
			Или СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB() Тогда
			СтрокаДанных.ПолучитьЭлементы().Очистить();
			ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
			Если ДанныеФайлов <> Неопределено Тогда
				Файл = ДанныеФайлов.Получить(СтрокаДанных.КлючСтроки);
				Если Файл <> Неопределено Тогда
					Если СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon() Тогда
						ЗаполнитьПодчиненныеДокументыРеестраПродажЮрлицамOzon(СтрокаДанных, Файл, ДокументыИБ, Организация, Контрагент);
					ИначеЕсли СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB() Тогда
						ЗаполнитьПодчиненныеДокументыРеестраПродажЮрлицамWB(СтрокаДанных, Файл, ДокументыИБ, Организация, Контрагент);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		СтрокаДанных.Документ = Неопределено;
		СтрокаДанных.Комментарий = "";
		СтрокаДанных.ДокументПредставление = НСтр("ru='Создать новый документ'");
		СтрокаДанных.ДокументПредставление1 = НСтр("ru='Создать новый документ'");
		СтрокаДанных.ДокументПредставление2 = "";
		
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", СтрокаДанных.КлючСтроки));
		Для Каждого ДокументИБ ИЗ СсылкиНаДокументы Цикл
			ДокументыИБ.Удалить(ДокументыИБ.Индекс(ДокументИБ));	
		КонецЦикла;
		
		НачалоПериода = ?(Периодичность = "НЕДЕЛЯ", НачалоНедели(СтрокаДанных.Дата), НачалоМесяца(СтрокаДанных.Дата));
		Если СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа()
			ИЛИ СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonВыкуп() Тогда
			НачалоПериода = НачалоНедели(СтрокаДанных.Дата);
			ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером;
		ИначеЕсли СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonСписание()
			ИЛИ СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBСписание() Тогда
			ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОСписании;
		ИначеЕсли СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonОприходование()
			ИЛИ СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBОприходование() Тогда
			ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОбОприходовании;
		ИначеЕсли СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBДетальный()
			ИЛИ СтрокаДанных.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzon() Тогда
			ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах;
		Иначе
			Продолжить;
		КонецЕсли;
		НайденныеДокументы = ТаблицаДокументов.НайтиСтроки(Новый Структура("НачалоПериода, ВидОперации", НачалоПериода, ВидОперации));
		ВключатьВыкупы     = Не ИнтеграцияWildberries.ИсключитьВыкупы(Организация, НачалоПериода);
		Если НайденныеДокументы.Количество() > 0 Тогда
			
			Для Каждого ДокументИБ из НайденныеДокументы Цикл
				
				НоваяСтрока = ДокументыИБ.Добавить();
				НоваяСтрока.Документ = ДокументИБ.Ссылка;
				НоваяСтрока.КлючСтроки = СтрокаДанных.КлючСтроки;
				
				НоваяСтрока.Представление = СтрШаблон(НСтр("ru='Заменить %1'"), ДокументИБ.Ссылка);
				
				Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером Тогда
					
					Если СтрокаДанных.СуммаВыкупа > ДокументИБ.СуммаВыкупа Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа меньше, чем сумма в файле на %1'"), Формат(СтрокаДанных.СуммаВыкупа - ДокументИБ.СуммаВыкупа, "ЧДЦ=2")); 
					ИначеЕсли СтрокаДанных.СуммаВыкупа < ДокументИБ.СуммаВыкупа Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа больше, чем сумма в файле на %1'"), Формат(ДокументИБ.СуммаВыкупа - СтрокаДанных.СуммаВыкупа, "ЧДЦ=2"));
					КонецЕсли;
					
				ИначеЕсли ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОСписании Тогда
					
					Если СтрокаДанных.СуммаСписания > ДокументИБ.СуммаПродаж Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа меньше, чем сумма в файле на %1'"), Формат(СтрокаДанных.СуммаСписания - ДокументИБ.СуммаПродаж, "ЧДЦ=2"));
					ИначеЕсли СтрокаДанных.СуммаСписания < ДокументИБ.СуммаПродаж Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа больше, чем сумма в файле на %1'"), Формат(ДокументИБ.СуммаПродаж - СтрокаДанных.СуммаСписания, "ЧДЦ=2"));
					КонецЕсли;
					
				ИначеЕсли ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОбОприходовании Тогда
					
					Если СтрокаДанных.СуммаВозвратов > ДокументИБ.СуммаВозвратов Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа меньше, чем сумма в файле на %1'"), Формат(СтрокаДанных.СуммаВозвратов - ДокументИБ.СуммаВозвратов, "ЧДЦ=2"));
					ИначеЕсли СтрокаДанных.СуммаВозвратов < ДокументИБ.СуммаВозвратов Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа больше, чем сумма в файле на %1'"), Формат(ДокументИБ.СуммаВозвратов - СтрокаДанных.СуммаВозвратов, "ЧДЦ=2"));
					КонецЕсли;
					
				ИначеЕсли ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах Тогда
					
					РазницаВСуммеДокумента = СтрокаДанных.СуммаПродаж - СтрокаДанных.СуммаВозвратов - ДокументИБ.СуммаПродаж + ДокументИБ.СуммаВозвратов;
					Если ВключатьВыкупы Тогда
						РазницаВСуммеДокумента = РазницаВСуммеДокумента + СтрокаДанных.СуммаВыкупа;
					КонецЕсли;
					
					Если РазницаВСуммеДокумента < 0 Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа больше, чем сумма в файле на %1'"), Формат(-РазницаВСуммеДокумента, "ЧДЦ=2"));
					ИначеЕсли РазницаВСуммеДокумента > 0 Тогда
						НоваяСтрока.Представление = НоваяСтрока.Представление + 
							Символы.ПС + СтрШаблон(НСтр("ru='Сумма документа меньше, чем сумма в файле на %1'"), Формат(РазницаВСуммеДокумента, "ЧДЦ=2"));
					КонецЕсли;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(НоваяСтрока.Представление) Тогда
					МассивПодстрок = СтрРазделить(НоваяСтрока.Представление, Символы.ПС, Ложь);
					Если МассивПодстрок.Количество() > 0 Тогда
						НоваяСтрока.Представление1 = МассивПодстрок[0];
					Иначе
						НоваяСтрока.Представление1 = НоваяСтрока.Представление;
					КонецЕсли;
					Если МассивПодстрок.Количество() > 1 Тогда
						НоваяСтрока.Представление2 = МассивПодстрок[1];
					КонецЕсли;
				КонецЕсли;
				
				Если СтрокаДанных.Документ = Неопределено Тогда
					
					//Проверим номер отчета
					//Если номера заполнены и не совпадаеют, документ остается в списке выбора, но по умолчанию предлагается создать новый
					Если ЗначениеЗаполнено(ДокументИБ.НомерВходящегоДокумента)
						И ЗначениеЗаполнено(СтрокаДанных.НомерВходящегоДокумента) 
						И (СокрЛП(ДокументИБ.НомерВходящегоДокумента) <> СокрЛП(СтрокаДанных.НомерВходящегоДокумента)) Тогда
						
						Продолжить;
					КонецЕсли;
				
					СтрокаДанных.Документ = НоваяСтрока.Документ;
					СтрокаДанных.ДокументПредставление = НоваяСтрока.Представление;
					СтрокаДанных.ДокументПредставление1 = НоваяСтрока.Представление1;
					СтрокаДанных.ДокументПредставление2 = НоваяСтрока.Представление2;
				Конецесли
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПРоцедуры

&НаСервереБезКонтекста
Процедура НайтиДокументыИБМегамаркет(ТаблицаДанных, ДокументыИБ, Организация)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	МассивПериодов = Новый Массив;
	Для Каждого СтрокаДанных из ТаблицаДанных Цикл
		НачалоПериода = НачалоМесяца(СтрокаДанных.Дата);
		Если МассивПериодов.Найти(НачалоПериода) = Неопределено Тогда
			МассивПериодов.Добавить(НачалоПериода);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МассивПериодов", МассивПериодов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетМаркетплейса.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ОтчетыМаркетплейсов
	|ИЗ
	|	Документ.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ОтчетМаркетплейса.Дата, МЕСЯЦ) В (&МассивПериодов)
	|	И ОтчетМаркетплейса.Маркетплейс = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.Мегамаркет)
	|	И НЕ ОтчетМаркетплейса.ПометкаУдаления
	|	И ОтчетМаркетплейса.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка,
	|	НАЧАЛОПЕРИОДА(ОтчетОРозничныхПродажах.Дата, МЕСЯЦ) КАК НачалоПериода,
	|	ОтчетОРозничныхПродажах.СуммаДокумента КАК СуммаПродаж
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	НЕ ОтчетОРозничныхПродажах.ПометкаУдаления
	|	И ОтчетОРозничныхПродажах.ОтчетМаркетплейса В
	|			(ВЫБРАТЬ
	|				ВТ_ОтчетыМаркетплейсов.Ссылка
	|			ИЗ
	|				ВТ_ОтчетыМаркетплейсов)
	|	И НАЧАЛОПЕРИОДА(ОтчетОРозничныхПродажах.Дата, МЕСЯЦ) В (&МассивПериодов)
	|	И ОтчетОРозничныхПродажах.Организация = &Организация";
	
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаДанных из ТаблицаДанных Цикл
		
		СтрокаДанных.Документ = Неопределено;
		СтрокаДанных.Комментарий = "";
		СтрокаДанных.ДокументПредставление = НСтр("ru='Создать новый документ'");
		СтрокаДанных.ДокументПредставление1 = НСтр("ru='Создать новый документ'");
		СтрокаДанных.ДокументПредставление2 = "";
		
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", СтрокаДанных.КлючСтроки));
		Для Каждого ДокументИБ ИЗ СсылкиНаДокументы Цикл
			ДокументыИБ.Удалить(ДокументыИБ.Индекс(ДокументИБ));
		КонецЦикла;
		
		НачалоПериода = НачалоМесяца(СтрокаДанных.Дата);

		НайденныеДокументы = ТаблицаДокументов.НайтиСтроки(Новый Структура("НачалоПериода", НачалоПериода));
		Если НайденныеДокументы.Количество() > 0 Тогда
			
			Для Каждого ДокументИБ из НайденныеДокументы Цикл
				
				НоваяСтрока = ДокументыИБ.Добавить();
				НоваяСтрока.Документ = ДокументИБ.Ссылка;
				НоваяСтрока.КлючСтроки = СтрокаДанных.КлючСтроки;
				
				НоваяСтрока.Представление = СтрШаблон(НСтр("ru='Заменить %1'"), ДокументИБ.Ссылка);
				
				ТекстШаблона = НСтр("ru='Сумма документа %1, чем сумма в файле на %2'");
				Если СтрокаДанных.СуммаПродаж > ДокументИБ.СуммаПродаж Тогда
					НоваяСтрока.Представление = НоваяСтрока.Представление + 
						Символы.ПС + СтрШаблон(ТекстШаблона, 
							НСтр("ru='меньше'"), Формат(СтрокаДанных.СуммаПродаж - ДокументИБ.СуммаПродаж, "ЧДЦ=2"));
				ИначеЕсли СтрокаДанных.СуммаПродаж < ДокументИБ.СуммаПродаж Тогда
					НоваяСтрока.Представление = НоваяСтрока.Представление + 
						Символы.ПС + СтрШаблон(ТекстШаблона, 
							НСтр("ru='больше'"), Формат(ДокументИБ.СуммаПродаж - СтрокаДанных.СуммаПродаж, "ЧДЦ=2"));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(НоваяСтрока.Представление) Тогда
					МассивПодстрок = СтрРазделить(НоваяСтрока.Представление, Символы.ПС, Ложь);
					Если МассивПодстрок.Количество() > 0 Тогда
						НоваяСтрока.Представление1 = МассивПодстрок[0];
					Иначе
						НоваяСтрока.Представление1 = НоваяСтрока.Представление;
					КонецЕсли;
					Если МассивПодстрок.Количество() > 1 Тогда
						НоваяСтрока.Представление2 = МассивПодстрок[1];
					КонецЕсли;
				КонецЕсли;
				
				Если СтрокаДанных.Документ = Неопределено Тогда
					СтрокаДанных.Документ = НоваяСтрока.Документ;
					СтрокаДанных.ДокументПредставление = НоваяСтрока.Представление;
					СтрокаДанных.ДокументПредставление1 = НоваяСтрока.Представление1;
					СтрокаДанных.ДокументПредставление2 = НоваяСтрока.Представление2;
				Конецесли
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПодчиненныеДокументыРеестраПродажЮрлицамOzon(СтрокаРеестра, Файл, ДокументыИБ, Организация, Контрагент)
	
	ТаблицаТоваров = Файл.ДанныеДокумента.ТаблицаТоваров;
	
	РеквизитыДокументов = ТаблицаТоваров.Скопировать(, "ТипДокумента, НомерДокумента, ДатаДокумента, НомерСФ, ДатаСФ");
	РеквизитыДокументов.Колонки.Добавить("ЭтоВозврат", Новый ОписаниеТипов("Булево"));
	Для Каждого СтрокаТаблицы Из РеквизитыДокументов Цикл
		СтрокаТаблицы.ДатаДокумента = НачалоМесяца(СтрокаТаблицы.ДатаДокумента);
		СтрокаТаблицы.ДатаСФ = НачалоМесяца(СтрокаТаблицы.ДатаСФ);
		СтрокаТаблицы.ЭтоВозврат = ЭтоДокументНаВозврат(СтрокаТаблицы.ТипДокумента);
	КонецЦикла;
	РеквизитыДокументов.Свернуть("ЭтоВозврат, НомерДокумента, ДатаДокумента, НомерСФ, ДатаСФ");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("РеквизитыДокументов", РеквизитыДокументов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеквизитыДокументов.НомерДокумента КАК НомерДокумента,
	|	РеквизитыДокументов.ДатаДокумента КАК ДатаДокумента,
	|	РеквизитыДокументов.НомерСФ КАК НомерСФ,
	|	РеквизитыДокументов.ДатаСФ КАК ДатаСФ,
	|	РеквизитыДокументов.ЭтоВозврат КАК ЭтоВозврат
	|ПОМЕСТИТЬ РеквизитыДокументов
	|ИЗ
	|	&РеквизитыДокументов КАК РеквизитыДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокументов.НомерДокумента КАК НомерДокумента,
	|	РеквизитыДокументов.ДатаДокумента КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаПродаж,
	|	0 КАК СуммаВозвратов
	|ИЗ
	|	РеквизитыДокументов КАК РеквизитыДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО РеквизитыДокументов.НомерДокумента = РеализацияТоваровУслуг.НомерВходящегоДокумента
	|			И (РеквизитыДокументов.ДатаДокумента = НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ))
	|ГДЕ
	|	РеализацияТоваровУслуг.Организация = &Организация
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера)
	|	И НЕ РеквизитыДокументов.ЭтоВозврат
	|	И РеализацияТоваровУслуг.Комиссионер = &Контрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокументов.НомерДокумента,
	|	РеквизитыДокументов.ДатаДокумента,
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	0,
	|	ВозвратТоваровОтПокупателя.СуммаДокумента
	|ИЗ
	|	РеквизитыДокументов КАК РеквизитыДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО РеквизитыДокументов.НомерДокумента = ВозвратТоваровОтПокупателя.НомерВходящегоДокумента
	|			И (РеквизитыДокументов.ДатаДокумента = НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, МЕСЯЦ))
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ТоварыНаСкладКомиссионера)
	|	И ВозвратТоваровОтПокупателя.Организация = &Организация
	|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|	И РеквизитыДокументов.ЭтоВозврат
	|	И ВозвратТоваровОтПокупателя.Комиссионер = &Контрагент";
	
	ПодчиненныеДокументыИБ = Запрос.Выполнить().Выгрузить();
	
	ИтогоСуммаПродаж = 0;
	ИтогоСуммаВозвратов = 0;
	КоличествоНайденныхДокументов = 0;
	ПодчиненныеДокументы = Файл.ДанныеДокумента.ПодчиненныеДокументы;
	Для Каждого ПодчиненныйДокумент Из ПодчиненныеДокументы Цикл
		
		НомерДокумента = ПодчиненныйДокумент.Шапка.НомерДокумента;
		ДатаДокумента = ПодчиненныйДокумент.Шапка.ДатаДокумента;
		
		ОшибкаПоискаДокумента = "";
		Если ЭтоДокументНаВозврат(ПодчиненныйДокумент.Шапка.ТипДокумента) Тогда
			СуммаПродаж = 0;
			СуммаВозвратов = ПодчиненныйДокумент.Товары.Итог("Сумма");
			ИтогоСуммаВозвратов = ИтогоСуммаВозвратов + СуммаВозвратов;
			ОписаниеСумм = СтрШаблон("Возврат: %1", Формат(СуммаВозвратов, "ЧДЦ=2"));
		Иначе
			СуммаПродаж = ПодчиненныйДокумент.Товары.Итог("Сумма");
			СуммаВозвратов = 0;
			ИтогоСуммаПродаж = ИтогоСуммаПродаж + СуммаПродаж;
			ОписаниеСумм = СтрШаблон("Продажа: %1", Формат(СуммаПродаж, "ЧДЦ=2"));
		КонецЕсли;
		
		НоваяПодстрока = СтрокаРеестра.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПодстрока, Файл);
		// загружать или нет определяется состоянием этого флага у родителя
		НоваяПодстрока.Загружать              = Ложь;
		НоваяПодстрока.КлючСтроки             = НомерДокумента;
		НоваяПодстрока.Описание1              = СтрШаблон("%1 от %2: %3",
			НомерДокумента, 
			Формат(ДатаДокумента, "ДФ=dd.MM.yyyy"),
			ПодчиненныйДокумент.Шапка.КраткоеНаименованиеПокупателя);
		НоваяПодстрока.Описание2              = ОписаниеСумм;
		НоваяПодстрока.Документ               = Неопределено;
		НоваяПодстрока.Комментарий            = "";
		НоваяПодстрока.ДокументПредставление  = НСтр("ru='Создать новый документ'");
		НоваяПодстрока.ДокументПредставление1 = НСтр("ru='Создать новый документ'");
		НоваяПодстрока.ДокументПредставление2 = ОшибкаПоискаДокумента;
		НоваяПодстрока.ЭтоПодстрока           = Истина;
		
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", НомерДокумента));
		Для Каждого ДокументИБ ИЗ СсылкиНаДокументы Цикл
			ДокументыИБ.Удалить(ДокументыИБ.Индекс(ДокументИБ));
		КонецЦикла;
			
		СтрокаДокументаИБ = ПодчиненныеДокументыИБ.Найти(ПодчиненныйДокумент.Шапка.НомерДокумента, "НомерДокумента");
		Если СтрокаДокументаИБ <> Неопределено Тогда
			
			КоличествоНайденныхДокументов = КоличествоНайденныхДокументов + 1;
			
			НоваяСтрокаДокументыИБ = ДокументыИБ.Добавить();
			НоваяСтрокаДокументыИБ.Документ      = СтрокаДокументаИБ.Документ;
			НоваяСтрокаДокументыИБ.КлючСтроки    = НомерДокумента;
			НоваяСтрокаДокументыИБ.Представление = СтрШаблон(НСтр("ru='Заменить %1'"), СтрокаДокументаИБ.Документ);
			
			ТекстШаблона = НСтр("ru='Сумма документа %1, чем сумма в файле на %2'");
			Разница = СуммаПродаж - СтрокаДокументаИБ.СуммаПродаж
				+ СуммаВозвратов - СтрокаДокументаИБ.СуммаВозвратов;
			Если Разница > 0 Тогда
				НоваяСтрокаДокументыИБ.Представление = НоваяСтрокаДокументыИБ.Представление
					+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='меньше'"), Формат(Разница, "ЧДЦ=2"));
			ИначеЕсли Разница < 0 Тогда
				НоваяСтрокаДокументыИБ.Представление = НоваяСтрокаДокументыИБ.Представление
					+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='больше'"), Формат(-Разница, "ЧДЦ=2"));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НоваяСтрокаДокументыИБ.Представление) Тогда
				МассивПодстрок = СтрРазделить(НоваяСтрокаДокументыИБ.Представление, Символы.ПС, Ложь);
				Если МассивПодстрок.Количество() > 0 Тогда
					НоваяСтрокаДокументыИБ.Представление1 = МассивПодстрок[0];
				Иначе
					НоваяСтрокаДокументыИБ.Представление1 = НоваяСтрокаДокументыИБ.Представление;
				КонецЕсли;
				Если МассивПодстрок.Количество() > 1 Тогда
					НоваяСтрокаДокументыИБ.Представление2 = МассивПодстрок[1];
				КонецЕсли;
			КонецЕсли;
			
			НоваяПодстрока.Документ = НоваяСтрокаДокументыИБ.Документ;
			НоваяПодстрока.ДокументПредставление  = НоваяСтрокаДокументыИБ.Представление;
			НоваяПодстрока.ДокументПредставление1 = НоваяСтрокаДокументыИБ.Представление1;
			НоваяПодстрока.ДокументПредставление2 = НоваяСтрокаДокументыИБ.Представление2;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаРеестра.ДокументПредставление  = НСтр("ru='Создать новые документы'");
	СтрокаРеестра.ДокументПредставление1 = НСтр("ru='Создать новые документы'");
	СтрокаРеестра.ДокументПредставление2 = "";
	Если ПодчиненныеДокументы.Количество() > 0 Тогда
		
		Если КоличествоНайденныхДокументов > 0 Тогда
			СтрокаРеестра.ДокументПредставление  = НСтр("ru='Документы программы:'");
			СтрокаРеестра.ДокументПредставление1 = НСтр("ru='Документы программы:'");
			СтрокаРеестра.ДокументПредставление2 = "";
			
			Разница = ПодчиненныеДокументы.Количество() - КоличествоНайденныхДокументов;
			Если Разница > 0 Тогда
				ТекстШаблона = НСтр("ru='Найдено на %1 меньше чем в файле'");
				ДокументСтрокой = ПолучитьСклоненияСтрокиПоЧислу("документ", Разница);
				СтрокаРеестра.ДокументПредставление  = СтрокаРеестра.ДокументПредставление
					+ Символы.ПС + СтрШаблон(ТекстШаблона, ДокументСтрокой[0]);
				СтрокаРеестра.ДокументПредставление1 = СтрокаРеестра.ДокументПредставление;
			КонецЕсли;
		КонецЕсли;
		
		ТекстШаблона = НСтр("ru='Сумма документов %1, чем сумма в файле на %2'");
		Разница = ИтогоСуммаПродаж - ПодчиненныеДокументыИБ.Итог("СуммаПродаж")
			+ ИтогоСуммаВозвратов - ПодчиненныеДокументыИБ.Итог("СуммаВозвратов");
		Если Разница > 0 И КоличествоНайденныхДокументов > 0 Тогда
			СтрокаРеестра.ДокументПредставление = СтрокаРеестра.ДокументПредставление
				+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='меньше'"), Формат(Разница, "ЧДЦ=2"));
		ИначеЕсли Разница < 0 И КоличествоНайденныхДокументов > 0 Тогда
			СтрокаРеестра.ДокументПредставление = СтрокаРеестра.ДокументПредставление
				+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='больше'"), Формат(-Разница, "ЧДЦ=2"));
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаРеестра.ДокументПредставление) Тогда
			МассивПодстрок = СтрРазделить(СтрокаРеестра.ДокументПредставление, Символы.ПС, Ложь);
			Если МассивПодстрок.Количество() > 0 Тогда
				СтрокаРеестра.ДокументПредставление1 = МассивПодстрок[0];
			Иначе
				СтрокаРеестра.ДокументПредставление1 = СтрокаРеестра.ДокументПредставление;
			КонецЕсли;
			Если МассивПодстрок.Количество() > 1 Тогда
				СтрокаРеестра.ДокументПредставление2 = МассивПодстрок[1];
				Для Сч = 2 по МассивПодстрок.ВГраница() Цикл
					СтрокаРеестра.ДокументПредставление2 = СтрокаРеестра.ДокументПредставление2
						+ Символы.ПС + МассивПодстрок[Сч];
				КонецЦИкла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПодчиненныеДокументыРеестраПродажЮрлицамWB(СтрокаРеестра, Файл, ДокументыИБ, Организация, Контрагент)
	
	ТаблицаТоваров = Файл.ДанныеДокумента.ТаблицаТоваров;
	
	РеквизитыДокументов = ТаблицаТоваров.Скопировать(, "ТипДокумента, НомерДокумента, ДатаДокумента, НомерСФ, ДатаСФ");
	РеквизитыДокументов.Колонки.Добавить("ЭтоВозврат", Новый ОписаниеТипов("Булево"));
	Для Каждого СтрокаТаблицы Из РеквизитыДокументов Цикл
		СтрокаТаблицы.ДатаДокумента = НачалоНедели(СтрокаТаблицы.ДатаДокумента);
		СтрокаТаблицы.ДатаСФ = НачалоНедели(СтрокаТаблицы.ДатаСФ);
		СтрокаТаблицы.ЭтоВозврат = ЭтоДокументНаВозврат(СтрокаТаблицы.ТипДокумента);
	КонецЦикла;
	РеквизитыДокументов.Свернуть("ЭтоВозврат, НомерДокумента, ДатаДокумента, НомерСФ, ДатаСФ");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("РеквизитыДокументов", РеквизитыДокументов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеквизитыДокументов.НомерДокумента КАК НомерДокумента,
	|	РеквизитыДокументов.ДатаДокумента КАК ДатаДокумента,
	|	РеквизитыДокументов.НомерСФ КАК НомерСФ,
	|	РеквизитыДокументов.ДатаСФ КАК ДатаСФ,
	|	РеквизитыДокументов.ЭтоВозврат КАК ЭтоВозврат
	|ПОМЕСТИТЬ РеквизитыДокументов
	|ИЗ
	|	&РеквизитыДокументов КАК РеквизитыДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокументов.НомерДокумента КАК НомерДокумента,
	|	РеквизитыДокументов.ДатаДокумента КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаПродаж,
	|	0 КАК СуммаВозвратов
	|ИЗ
	|	РеквизитыДокументов КАК РеквизитыДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО РеквизитыДокументов.НомерДокумента = РеализацияТоваровУслуг.НомерВходящегоДокумента
	|			И (РеквизитыДокументов.ДатаДокумента = НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, НЕДЕЛЯ))
	|ГДЕ
	|	РеализацияТоваровУслуг.Организация = &Организация
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера)
	|	И НЕ РеквизитыДокументов.ЭтоВозврат
	|	И РеализацияТоваровУслуг.Комиссионер = &Контрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокументов.НомерДокумента,
	|	РеквизитыДокументов.ДатаДокумента,
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	0,
	|	ВозвратТоваровОтПокупателя.СуммаДокумента
	|ИЗ
	|	РеквизитыДокументов КАК РеквизитыДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО РеквизитыДокументов.НомерДокумента = ВозвратТоваровОтПокупателя.НомерВходящегоДокумента
	|			И (РеквизитыДокументов.ДатаДокумента = НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, НЕДЕЛЯ))
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ТоварыНаСкладКомиссионера)
	|	И ВозвратТоваровОтПокупателя.Организация = &Организация
	|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|	И РеквизитыДокументов.ЭтоВозврат
	|	И ВозвратТоваровОтПокупателя.Комиссионер = &Контрагент";
	
	ПодчиненныеДокументыИБ = Запрос.Выполнить().Выгрузить();
	
	ИтогоСуммаПродаж = 0;
	ИтогоСуммаВозвратов = 0;
	КоличествоНайденныхДокументов = 0;
	ПодчиненныеДокументы = Файл.ДанныеДокумента.ПодчиненныеДокументы;
	Для Каждого ПодчиненныйДокумент Из ПодчиненныеДокументы Цикл
		
		НомерДокумента = ПодчиненныйДокумент.Шапка.НомерДокумента;
		ДатаДокумента = ПодчиненныйДокумент.Шапка.ДатаДокумента;
		
		ОшибкаПоискаДокумента = "";
		Если ЭтоДокументНаВозврат(ПодчиненныйДокумент.Шапка.ТипДокумента) Тогда
			СуммаПродаж = 0;
			СуммаВозвратов = ПодчиненныйДокумент.Товары.Итог("Сумма");
			ИтогоСуммаВозвратов = ИтогоСуммаВозвратов + СуммаВозвратов;
			ОписаниеСумм = СтрШаблон("Возврат: %1", Формат(СуммаВозвратов, "ЧДЦ=2"));
		Иначе
			СуммаПродаж = ПодчиненныйДокумент.Товары.Итог("Сумма");
			СуммаВозвратов = 0;
			ИтогоСуммаПродаж = ИтогоСуммаПродаж + СуммаПродаж;
			ОписаниеСумм = СтрШаблон("Продажа: %1", Формат(СуммаПродаж, "ЧДЦ=2"));
		КонецЕсли;
		
		НоваяПодстрока = СтрокаРеестра.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПодстрока, Файл);
		// загружать или нет определяется состоянием этого флага у родителя
		НоваяПодстрока.Загружать              = Ложь;
		НоваяПодстрока.КлючСтроки             = НомерДокумента;
		НоваяПодстрока.Описание1              = СтрШаблон("%1 от %2: %3",
			НомерДокумента, 
			Формат(ДатаДокумента, "ДФ=dd.MM.yyyy"),
			ПодчиненныйДокумент.Шапка.КраткоеНаименованиеПокупателя);
		НоваяПодстрока.Описание2              = ОписаниеСумм;
		НоваяПодстрока.Документ               = Неопределено;
		НоваяПодстрока.Комментарий            = "";
		НоваяПодстрока.ДокументПредставление  = НСтр("ru='Создать новый документ'");
		НоваяПодстрока.ДокументПредставление1 = НСтр("ru='Создать новый документ'");
		НоваяПодстрока.ДокументПредставление2 = ОшибкаПоискаДокумента;
		НоваяПодстрока.ЭтоПодстрока           = Истина;
		
		СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", НомерДокумента));
		Для Каждого ДокументИБ ИЗ СсылкиНаДокументы Цикл
			ДокументыИБ.Удалить(ДокументыИБ.Индекс(ДокументИБ));
		КонецЦикла;
			
		СтрокаДокументаИБ = ПодчиненныеДокументыИБ.Найти(ПодчиненныйДокумент.Шапка.НомерДокумента, "НомерДокумента");
		Если СтрокаДокументаИБ <> Неопределено Тогда
			
			КоличествоНайденныхДокументов = КоличествоНайденныхДокументов + 1;
			
			НоваяСтрокаДокументыИБ = ДокументыИБ.Добавить();
			НоваяСтрокаДокументыИБ.Документ      = СтрокаДокументаИБ.Документ;
			НоваяСтрокаДокументыИБ.КлючСтроки    = НомерДокумента;
			НоваяСтрокаДокументыИБ.Представление = СтрШаблон(НСтр("ru='Заменить %1'"), СтрокаДокументаИБ.Документ);
			
			ТекстШаблона = НСтр("ru='Сумма документа %1, чем сумма в файле на %2'");
			Разница = СуммаПродаж - СтрокаДокументаИБ.СуммаПродаж
				+ СуммаВозвратов - СтрокаДокументаИБ.СуммаВозвратов;
			Если Разница > 0 Тогда
				НоваяСтрокаДокументыИБ.Представление = НоваяСтрокаДокументыИБ.Представление
					+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='меньше'"), Формат(Разница, "ЧДЦ=2"));
			ИначеЕсли Разница < 0 Тогда
				НоваяСтрокаДокументыИБ.Представление = НоваяСтрокаДокументыИБ.Представление
					+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='больше'"), Формат(-Разница, "ЧДЦ=2"));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НоваяСтрокаДокументыИБ.Представление) Тогда
				МассивПодстрок = СтрРазделить(НоваяСтрокаДокументыИБ.Представление, Символы.ПС, Ложь);
				Если МассивПодстрок.Количество() > 0 Тогда
					НоваяСтрокаДокументыИБ.Представление1 = МассивПодстрок[0];
				Иначе
					НоваяСтрокаДокументыИБ.Представление1 = НоваяСтрокаДокументыИБ.Представление;
				КонецЕсли;
				Если МассивПодстрок.Количество() > 1 Тогда
					НоваяСтрокаДокументыИБ.Представление2 = МассивПодстрок[1];
				КонецЕсли;
			КонецЕсли;
			
			НоваяПодстрока.Документ = НоваяСтрокаДокументыИБ.Документ;
			НоваяПодстрока.ДокументПредставление  = НоваяСтрокаДокументыИБ.Представление;
			НоваяПодстрока.ДокументПредставление1 = НоваяСтрокаДокументыИБ.Представление1;
			НоваяПодстрока.ДокументПредставление2 = НоваяСтрокаДокументыИБ.Представление2;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаРеестра.ДокументПредставление  = НСтр("ru='Создать новые документы'");
	СтрокаРеестра.ДокументПредставление1 = НСтр("ru='Создать новые документы'");
	СтрокаРеестра.ДокументПредставление2 = "";
	Если ПодчиненныеДокументы.Количество() > 0 Тогда
		
		Если КоличествоНайденныхДокументов > 0 Тогда
			СтрокаРеестра.ДокументПредставление  = НСтр("ru='Документы программы:'");
			СтрокаРеестра.ДокументПредставление1 = НСтр("ru='Документы программы:'");
			СтрокаРеестра.ДокументПредставление2 = "";
			
			Разница = ПодчиненныеДокументы.Количество() - КоличествоНайденныхДокументов;
			Если Разница > 0 Тогда
				ТекстШаблона = НСтр("ru='Найдено на %1 меньше чем в файле'");
				ДокументСтрокой = ПолучитьСклоненияСтрокиПоЧислу("документ", Разница);
				СтрокаРеестра.ДокументПредставление  = СтрокаРеестра.ДокументПредставление
					+ Символы.ПС + СтрШаблон(ТекстШаблона, ДокументСтрокой[0]);
				СтрокаРеестра.ДокументПредставление1 = СтрокаРеестра.ДокументПредставление;
			КонецЕсли;
		КонецЕсли;
		
		ТекстШаблона = НСтр("ru='Сумма документов %1, чем сумма в файле на %2'");
		Разница = ИтогоСуммаПродаж - ПодчиненныеДокументыИБ.Итог("СуммаПродаж")
			+ ИтогоСуммаВозвратов - ПодчиненныеДокументыИБ.Итог("СуммаВозвратов");
		Если Разница > 0 И КоличествоНайденныхДокументов > 0 Тогда
			СтрокаРеестра.ДокументПредставление = СтрокаРеестра.ДокументПредставление
				+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='меньше'"), Формат(Разница, "ЧДЦ=2"));
		ИначеЕсли Разница < 0 И КоличествоНайденныхДокументов > 0 Тогда
			СтрокаРеестра.ДокументПредставление = СтрокаРеестра.ДокументПредставление
				+ Символы.ПС + СтрШаблон(ТекстШаблона, НСтр("ru='больше'"), Формат(-Разница, "ЧДЦ=2"));
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаРеестра.ДокументПредставление) Тогда
			МассивПодстрок = СтрРазделить(СтрокаРеестра.ДокументПредставление, Символы.ПС, Ложь);
			Если МассивПодстрок.Количество() > 0 Тогда
				СтрокаРеестра.ДокументПредставление1 = МассивПодстрок[0];
			Иначе
				СтрокаРеестра.ДокументПредставление1 = СтрокаРеестра.ДокументПредставление;
			КонецЕсли;
			Если МассивПодстрок.Количество() > 1 Тогда
				СтрокаРеестра.ДокументПредставление2 = МассивПодстрок[1];
				Для Сч = 2 по МассивПодстрок.ВГраница() Цикл
					СтрокаРеестра.ДокументПредставление2 = СтрокаРеестра.ДокументПредставление2
						+ Символы.ПС + МассивПодстрок[Сч];
				КонецЦИкла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СопоставлениеНоменклатуры

&НаСервере
Функция СписокНеСопоставленнойНоменклатурыНаСервере()
	
	СписокНеСопоставленнойНоменклатуры = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(АдресХранилища) Тогда
		Возврат СписокНеСопоставленнойНоменклатуры;
	КонецЕсли;
	
	ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ТаблицаНоменклатураОтчет       = Неопределено;
	ТаблицаНоменклатураУведомление = Неопределено;
	НастройкиОтчет                 = Неопределено;
	НастройкиУведомление           = Неопределено;
	
	СозданныеДокументы.ПолучитьЭлементы().Очистить();
	
	Для Каждого Строка Из ЗагружаемыеФайлыWB.ПолучитьЭлементы() Цикл
		Если Строка.Загружать Тогда
			
			СтрокаСозданиеДокументов = СозданныеДокументы.ПолучитьЭлементы().Добавить();
			СтрокаСозданиеДокументов.Описание = Строка.Описание1;
			
			ЗагружаемаяСтруктура = ДанныеФайлов.Получить(Строка.КлючСтроки);
						
			Если Не ЭтоЗагрузкаЧерезAPI И ЗагружаемаяСтруктура.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа() Тогда
				Если ТаблицаНоменклатураУведомление = Неопределено Тогда
					ТаблицаНоменклатураУведомление = ЗагружаемаяСтруктура.ТаблицаНоменклатура.СкопироватьКолонки();
				КонецЕсли;
				Для Каждого СтрокаНоменклатура Из ЗагружаемаяСтруктура.ТаблицаНоменклатура Цикл
					НоваяСтрока = ТаблицаНоменклатураУведомление.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНоменклатура);
				КонецЦикла;
				Если НастройкиУведомление = Неопределено Тогда
					НастройкиУведомление = ЗагружаемаяСтруктура.НастройкиСопоставленияНоменклатуры;
				КонецЕсли;
				
			Иначе
				Если ТаблицаНоменклатураОтчет = Неопределено Тогда
					ТаблицаНоменклатураОтчет = ЗагружаемаяСтруктура.ТаблицаНоменклатура.СкопироватьКолонки();
				КонецЕсли;
				Для Каждого СтрокаНоменклатура Из ЗагружаемаяСтруктура.ТаблицаНоменклатура Цикл
					НоваяСтрока = ТаблицаНоменклатураОтчет.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНоменклатура);
				КонецЦикла;
				Если НастройкиОтчет = Неопределено Тогда
					НастройкиОтчет = ЗагружаемаяСтруктура.НастройкиСопоставленияНоменклатуры;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТаблицаНоменклатураОтчет = Неопределено Тогда
		ДобавитьТаблицуДляСопоставленияНоменклатуры(СписокНеСопоставленнойНоменклатуры, ТаблицаНоменклатураОтчет, КонтрагентWB, НастройкиОтчет);
	КонецЕсли;
	
	Если НЕ ТаблицаНоменклатураУведомление = Неопределено Тогда
		ДобавитьТаблицуДляСопоставленияНоменклатуры(СписокНеСопоставленнойНоменклатуры, ТаблицаНоменклатураУведомление, КонтрагентWB, НастройкиУведомление);
	КонецЕсли;
	
	ТаблицаНоменклатура       = Неопределено;
	Настройки                 = Неопределено;
	
	Для Каждого Строка Из ЗагружаемыеФайлыOzon.ПолучитьЭлементы() Цикл
		Если Строка.Загружать Тогда
			
			СтрокаСозданиеДокументов = СозданныеДокументы.ПолучитьЭлементы().Добавить();
			СтрокаСозданиеДокументов.Описание = Строка.Описание1;
			
			ЗагружаемаяСтруктура = ДанныеФайлов.Получить(Строка.КлючСтроки);
			
			Если ТаблицаНоменклатура = Неопределено Тогда
				ТаблицаНоменклатура = ЗагружаемаяСтруктура.ТаблицаНоменклатура.СкопироватьКолонки();
			КонецЕсли;
			Для Каждого СтрокаНоменклатура Из ЗагружаемаяСтруктура.ТаблицаНоменклатура Цикл
				НоваяСтрока = ТаблицаНоменклатура.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНоменклатура);
			КонецЦикла;
			Если Настройки = Неопределено Тогда
				Настройки = ЗагружаемаяСтруктура.НастройкиСопоставленияНоменклатуры;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТаблицаНоменклатура = Неопределено Тогда
		ДобавитьТаблицуДляСопоставленияНоменклатуры(СписокНеСопоставленнойНоменклатуры, ТаблицаНоменклатура, КонтрагентOzon, Настройки);
	КонецЕсли;
	
	ТаблицаНоменклатура = Неопределено;
	Настройки           = Неопределено;
	
	Для Каждого Строка Из ЗагружаемыеФайлыМегамаркет Цикл
		Если Строка.Загружать Тогда
			
			СтрокаСозданиеДокументов = СозданныеДокументы.ПолучитьЭлементы().Добавить();
			СтрокаСозданиеДокументов.Описание = Строка.Описание1;
			
			ЗагружаемаяСтруктура = ДанныеФайлов.Получить(Строка.КлючСтроки);
			
			Если ТаблицаНоменклатура = Неопределено Тогда
				ТаблицаНоменклатура = ЗагружаемаяСтруктура.ТаблицаНоменклатура.СкопироватьКолонки();
			КонецЕсли;
			Для Каждого СтрокаНоменклатура Из ЗагружаемаяСтруктура.ТаблицаНоменклатура Цикл
				НоваяСтрока = ТаблицаНоменклатура.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНоменклатура);
			КонецЦикла;
			Если Настройки = Неопределено Тогда
				Настройки = ЗагружаемаяСтруктура.НастройкиСопоставленияНоменклатуры;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаНоменклатура <> Неопределено Тогда
		ДобавитьТаблицуДляСопоставленияНоменклатуры(СписокНеСопоставленнойНоменклатуры, ТаблицаНоменклатура, КонтрагентМегамаркет, Настройки);
	КонецЕсли;
	
	Возврат СписокНеСопоставленнойНоменклатуры;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УдалитьСопоставленные(ТаблицаНоменклатура, СопоставленнаяНоменклатура)
	
	КоличествоЭлементов = ТаблицаНоменклатура.Количество();
	Если КоличествоЭлементов > 0 Тогда
		Для Индекс = 1 по КоличествоЭлементов Цикл
			СтрокаНоменклатуры = ТаблицаНоменклатура[КоличествоЭлементов - Индекс];
			Если ЗначениеЗаполнено(СопоставленнаяНоменклатура.Получить(СтрокаНоменклатуры.ИД)) Тогда
				ТаблицаНоменклатура.Удалить(СтрокаНоменклатуры);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьТаблицуДляСопоставленияНоменклатуры(СписокСопоставления, ТаблицаНоменклатура, Владелец, НастройкиСопоставленияНоменклатуры)
	
	Если ТаблицаНоменклатура.Колонки.Найти("СтавкаНДС") = Неопределено Тогда
		ТаблицаНоменклатура.Свернуть("Наименование, Артикул, Штрихкод, ИД, НатуральныйИД, КодМагазина");
	Иначе
		ТаблицаНоменклатура.Свернуть("Наименование, Артикул, Штрихкод, ИД, НатуральныйИД, КодМагазина, СтавкаНДС");
	КонецЕсли;
	СопоставленнаяНоменклатура = ЭлектронноеВзаимодействиеБП.НайтиСоотвествиеНоменклатурыПоИД(ТаблицаНоменклатура, Владелец);
	УдалитьСопоставленные(ТаблицаНоменклатура, СопоставленнаяНоменклатура);
	
	МассивНеСопоставленнойНоменклатуры = Новый Массив;
	
	Для Каждого СтрокаТовара Из ТаблицаНоменклатура Цикл
		НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(Владелец,);
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, СтрокаТовара);
		НоменклатураКонтрагента.Идентификатор             = СтрокаТовара.ИД;
		НоменклатураКонтрагента.ИдентификаторНоменклатуры = СтрокаТовара.ИД;
		НоменклатураКонтрагента.ШтрихкодКомбинации        = СтрокаТовара.Штрихкод;
		НоменклатураКонтрагента.ШтрихкодыНоменклатуры     = СтрокаТовара.Штрихкод;
		Если ЗначениеЗаполнено(СтрокаТовара.НатуральныйИД) Тогда
			НоменклатураКонтрагента.ИсторияИдентификаторов.Добавить(СтрокаТовара.НатуральныйИД);
		КонецЕсли;
		МассивНеСопоставленнойНоменклатуры.Добавить(НоменклатураКонтрагента);
	КонецЦикла;
			
	ПараметрыСопоставления = Новый Структура;
	ПараметрыСопоставления.Вставить("НоменклатураКонтрагента", МассивНеСопоставленнойНоменклатуры);
	ПараметрыСопоставления.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставленияНоменклатуры);
	ПараметрыСопоставления.Вставить("Владелец", Владелец);
	Если МассивНеСопоставленнойНоменклатуры.Количество() > 0 Тогда
		СписокСопоставления.Добавить(ПараметрыСопоставления);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСопоставлениеНоменклатуры(МассивКСопоставлению, Индекс)
	
	ЭлементСопоставления = МассивКСопоставлению[Индекс]; 
	
	НастройкаИнтеграции = Новый Структура("Владелец, ВидМаркетплейса", Организация, Маркетплейс);
	
	ДополнительныеПараметры = Новый Структура("МассивКСопоставлению, Индекс, НастройкаИнтеграции",
											МассивКСопоставлению, Индекс, НастройкаИнтеграции);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("СопоставлениеНоменклатурыЗавершение",	ЭтотОбъект, ДополнительныеПараметры);
	СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры(
		ЭлементСопоставления.НоменклатураКонтрагента,
		ЭлементСопоставления.НастройкиСопоставленияНоменклатуры,
		ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеНоменклатурыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Индекс = ДополнительныеПараметры.Индекс + 1;
	МассивКСопоставлению = ДополнительныеПараметры.МассивКСопоставлению;
	Если МассивКСопоставлению.Количество() > Индекс Тогда
		НачатьСопоставлениеНоменклатуры(МассивКСопоставлению, Индекс);
	Иначе
		ЕстьНоваяНоменклатура = ЗначениеЗаполнено(РезультатЗакрытия);
		
		Если ЕстьНоваяНоменклатура Тогда
			ОбменСМаркетплейсВызовСервера.ОбработатьРезультатыСопоставления(
			РезультатЗакрытия,
			ДополнительныеПараметры.НастройкаИнтеграции);
		КонецЕсли;
			
		НачатьСозданиеДокументов();
	КонецЕсли;
	
Конецпроцедуры

#КонецОбласти

#Область СозданиеДокументов

&НаКлиенте
Процедура НачатьСозданиеДокументов()
	
	ФоновоеЗадание = ЗапуститьФоновоеСозданиеДокументов();
	
	Если ФоновоеЗадание = Неопределено Тогда
		ТекстСообщенияЗагрузки = НСтр("ru = 'Загрузка прервана из-за ошибки'");
		ЭтотОбъект.Заголовок = НСтр("ru = 'Ошибка при создании документов'");
		Элементы.ФормаЗакрыть.Заголовок = НСтр("ru = 'Закрыть'");
		Возврат;
	КонецЕсли;
	
	ОбработчикПрогресса = Новый ОписаниеОповещения("ПриПрогрессеСозданияДокументов", ЭтотОбъект);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	НастройкиОжидания.ОповещениеОПрогрессеВыполнения = ОбработчикПрогресса;
	
	Обработчик = Новый ОписаниеОповещения("ПослеСозданияДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеСозданиеДокументов()
	
	Если НЕ ЗначениеЗаполнено(АдресХранилища) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	СохранитьНастройкиФормы();
	
	СписокЗагрузки = Новый Массив;
	
	ПараметрыСозданияДокументов = Новый Структура("Организация, ГруппаДляНовыхКонтрагентов, СпособСверткиОтчетов");
	ЗаполнитьЗначенияСвойств(ПараметрыСозданияДокументов, ЭтотОбъект);
	Если ЭтоЗагрузкаЧерезAPI Тогда
		ПараметрыСозданияДокументов.Вставить("СпособЗагрузки", ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиЧерезАПИ());
	Иначе
		ПараметрыСозданияДокументов.Вставить("СпособЗагрузки", ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиЧерезФайл());
	КонецЕсли;
	
	ПодготовленоБезОшибок = Истина;
	Для Каждого Строка Из ЗагружаемыеФайлыWB.ПолучитьЭлементы() Цикл
		Если Строка.Загружать Тогда
			ЗагружаемаяСтруктура = ДанныеФайлов.Получить(Строка.КлючСтроки);
			
			Если ЗагружаемаяСтруктура.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB() Тогда
				Для Каждого ПодчиненныйДокумент Из ЗагружаемаяСтруктура.ДанныеДокумента.ПодчиненныеДокументы Цикл
					Шапка = ПодчиненныйДокумент.Шапка;
					СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", Шапка.НомерДокумента));
					Если СсылкиНаДокументы.Количество() > 0 Тогда
						Шапка.Документ = СсылкиНаДокументы[0].Документ;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если ЗначениеЗаполнено(Строка.Документ) Тогда
					ЗагружаемаяСтруктура.ДанныеДокумента.Вставить("Ссылка", Строка.Документ);
				КонецЕсли;
			КонецЕсли;
			
			Ошибки = Новый Массив;
			ПараметрыСозданияДокументов.Вставить("НачалоПериода", Строка.Дата);
			ИнтеграцияWildberries.ПодготовитьДокументыКСозданию(
				СписокЗагрузки, ЗагружаемаяСтруктура, ПараметрыСозданияДокументов, Ошибки);
			Если ЗначениеЗаполнено(Ошибки) Тогда
				СозданныеДокументы.ПолучитьЭлементы().Очистить();
				Для Каждого ОписаниеОшибки Из Ошибки Цикл
					НоваяСтрока = СозданныеДокументы.ПолучитьЭлементы().Добавить();
					НоваяСтрока.Описание = ОписаниеОшибки;
					НоваяСтрока.Картинка = 6;
					ПодготовленоБезОшибок = Ложь;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из ЗагружаемыеФайлыOzon.ПолучитьЭлементы() Цикл
		Если Строка.Загружать Тогда
			ЗагружаемаяСтруктура = ДанныеФайлов.Получить(Строка.КлючСтроки);
			
			Если ЗагружаемаяСтруктура.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon() Тогда
				Для Каждого ПодчиненныйДокумент Из ЗагружаемаяСтруктура.ДанныеДокумента.ПодчиненныеДокументы Цикл
					СсылкиНаДокументы = ДокументыИБ.НайтиСтроки(Новый Структура("КлючСтроки", ПодчиненныйДокумент.Шапка.НомерДокумента));
					Если ЗначениеЗаполнено(СсылкиНаДокументы) Тогда
						ПодчиненныйДокумент.Шапка.Документ = СсылкиНаДокументы[0].Документ;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если ЗначениеЗаполнено(Строка.Документ) Тогда
					ЗагружаемаяСтруктура.ДанныеДокумента.Вставить("Ссылка", Строка.Документ);
				КонецЕсли;
			КонецЕсли;
			
			Ошибки = Новый Массив;
			ПараметрыСозданияДокументов.Вставить("НачалоПериода", Строка.Дата);
			ИнтеграцияOzon.ПодготовитьДокументыКСозданию(
				СписокЗагрузки, ЗагружаемаяСтруктура, ПараметрыСозданияДокументов, Ошибки);
			Если ЗначениеЗаполнено(Ошибки) Тогда
				СозданныеДокументы.ПолучитьЭлементы().Очистить();
				Для Каждого ОписаниеОшибки Из Ошибки Цикл
					НоваяСтрока = СозданныеДокументы.ПолучитьЭлементы().Добавить();
					НоваяСтрока.Описание = ОписаниеОшибки;
					НоваяСтрока.Картинка = 6;
					ПодготовленоБезОшибок = Ложь;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ВидОплаты = Справочники.ВидыОплатОрганизаций.ПустаяСсылка();
	Для Каждого Строка Из ЗагружаемыеФайлыМегамаркет Цикл
		Если Строка.Загружать Тогда
			ЗагружаемаяСтруктура = ДанныеФайлов.Получить(Строка.КлючСтроки);
			ДанныеДокумента = ЗагружаемаяСтруктура.ДанныеДокумента;
			
			Если СпособСверткиОтчетов = "Сворачивать"
				И (ЗагружаемаяСтруктура.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаПриложение1Мегамаркет()
					Или ЗагружаемаяСтруктура.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетПоОтправлениямМегамаркет()) Тогда
					СворачиваемыеКолонки = "Наименование,Артикул,ИД";
					СуммируемыеКолонки = "Количество,Сумма";
					ДанныеДокумента.ТаблицаТоваров.Свернуть(СворачиваемыеКолонки, СуммируемыеКолонки);
			КонецЕсли;
			
			ОписаниеОшибки = "";
			ОтчетМаркетплейса = НайтиСоздатьОтчетМаркетплейса(Организация, 
				ДанныеДокумента.Дата, Перечисления.ВидыМаркетплейсов.Мегамаркет, ОписаниеОшибки);
			Если ОтчетМаркетплейса = Неопределено Тогда
				СозданныеДокументы.ПолучитьЭлементы().Очистить();
				НоваяСтрока = СозданныеДокументы.ПолучитьЭлементы().Добавить();
				НоваяСтрока.Описание = ОписаниеОшибки;
				НоваяСтрока.Картинка = 6;
				ПодготовленоБезОшибок = Ложь;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ВидОплаты) Тогда
				ВидОплаты = НайтиСоздатьВидОплатыМегамаркет(Организация, 
					КонтрагентМегамаркет, ДоговорКонтрагентаМегамаркет, ОписаниеОшибки);
				Если ВидОплаты = Неопределено Тогда
					СозданныеДокументы.ПолучитьЭлементы().Очистить();
					НоваяСтрока = СозданныеДокументы.ПолучитьЭлементы().Добавить();
					НоваяСтрока.Описание = ОписаниеОшибки;
					НоваяСтрока.Картинка = 6;
					ПодготовленоБезОшибок = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			СуммаОплаты = ДанныеДокумента.ТаблицаТоваров.Итог("Сумма");
			
			ЗагружаемаяСтруктура.Вставить("Организация", Организация);
			ЗагружаемаяСтруктура.Вставить("СпособЗагрузки", ПараметрыСозданияДокументов.СпособЗагрузки);
			ЗагружаемаяСтруктура.Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отчет о продажах на Мегамаркете за %1 года'"),
				НРег(Формат(ЗагружаемаяСтруктура.ДанныеДокумента.Дата, "ДФ='ММММ ггг'")));
			ДанныеДокумента.Вставить("ОтчетМаркетплейса", ОтчетМаркетплейса);
			ДанныеДокумента.Вставить("ВидОплаты", ВидОплаты);
			ДанныеДокумента.Вставить("СуммаОплаты", СуммаОплаты);
			ДанныеДокумента.Вставить("Склад", Склад);
			ДанныеДокумента.Вставить("ПервичныеДанныеМаркетплейса", ЗагружаемаяСтруктура.ДанныеДокумента.ПервичныеДанныеМаркетплейса);
			
			Если ЗначениеЗаполнено(Строка.Документ) Тогда
				ДанныеДокумента.Вставить("Ссылка", Строка.Документ);
			КонецЕсли;
			
			СписокЗагрузки.Добавить(ЗагружаемаяСтруктура);
		КонецЕсли;
	КонецЦикла;
	
	Если ПодготовленоБезОшибок Тогда
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание документов по отчетам маркетплейса'");
		АдресХранилищаДокументы = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
		
		ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
			ПараметрыВыполнения,
			"ЭлектронноеВзаимодействиеБП.СоздатьДокументыИзСтруктуры",
			СписокЗагрузки, АдресХранилищаДокументы);
		
		Возврат ФоновоеЗадание;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПослеСозданияДокументов(Задание, Контекст) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СостояниеЗагрузки.Видимость = Ложь;
	ЭтотОбъект.Заголовок = НСтр("ru = 'Созданные документы'");
	Элементы.ФормаЗакрыть.Заголовок = НСтр("ru = 'Готово'");
	Элементы.СозданныеДокументыПровестиДокументы.Видимость = Истина;
	
	ПослеСозданияДокументовНаСервере();
	
	ОповеститьОбИзмененииСозданныхДокументов();
	
	Оповестить("ИзмененыДокументыМаркетплейсов");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроведенияСозданныхДокументов(Задание, Контекст) Экспорт
	
	ВыполняетсяЗагрузка = Ложь;
	
	Элементы.СостояниеЗагрузки.Видимость = Ложь;
	Элементы.СозданныеДокументыПровестиДокументы.Отображение = ОтображениеКнопки.Текст;
	Элементы.ФормаЗакрыть.Доступность = Истина;
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПослеПроведенияСозданныхДокументовНаСервере();
	
	ОповеститьОбИзмененииСозданныхДокументов();
	
КонецПроцедуры

&НаСервере
Процедура ПослеСозданияДокументовНаСервере()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДокументы;
	СозданныеДокументы.ПолучитьЭлементы().Очистить();
	
	ДанныеДокументов = ПолучитьИзВременногоХранилища(АдресХранилищаДокументы);
	Если ТипЗнч(ДанныеДокументов) = Тип("Массив") Тогда
		
		Родители = Новый Соответствие;
		Для Каждого Документ Из ДанныеДокументов Цикл
			Если ЗначениеЗаполнено(Документ.ОписаниеРодителя) Тогда
				СтрокаРодитель = Родители.Получить(Документ.ОписаниеРодителя);
				Если СтрокаРодитель = Неопределено Тогда
					СтрокаРодитель = СозданныеДокументы.ПолучитьЭлементы().Добавить();
					СтрокаРодитель.Описание = Документ.ОписаниеРодителя;
					СтрокаРодитель.Картинка = 1;
					СтрокаРодитель.Дата = Документ.Дата;
					СтрокаРодитель.Сумма = 0;
					Родители.Вставить(Документ.ОписаниеРодителя, СтрокаРодитель);
				КонецЕсли;
			Иначе
				СтрокаРодитель = СозданныеДокументы;
			КонецЕсли;

			НоваяСтрока = СтрокаРодитель.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Документ);
			Если Не ЗначениеЗаполнено(НоваяСтрока.ТекстОшибки) Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Документ);
				НоваяСтрока.Ссылка = Документ.СозданДокумент;
				НоваяСтрока.Картинка = 1;
				Если ТипЗнч(СтрокаРодитель) = Тип("ДанныеФормыЭлементДерева") Тогда
					СтрокаРодитель.Сумма = СтрокаРодитель.Сумма + НоваяСтрока.Сумма;
				КонецЕсли;
				Если Документ.Свойство("ТекстСообщения") Тогда
					НоваяСтрока.ТекстОшибки = Документ.ТекстСообщения;
					НоваяСтрока.Картинка = 6;
				КонецЕсли;
			Иначе
				НоваяСтрока.Картинка = 6;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеСозданныхДокументов(РежимЗаписи, СсылкаНаДокумент, СтрокаДерева)
	
	Для Каждого СтрокаТаблицы Из СтрокаДерева.ПолучитьЭлементы() Цикл
		Если СтрокаТаблицы.Ссылка = СсылкаНаДокумент Тогда
			Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				СтрокаТаблицы.Картинка = 2;
				СтрокаТаблицы.ТекстОшибки = "";
				Прервать;
			Иначе
				СтрокаТаблицы.Картинка = 1;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		ОбновитьСостояниеСозданныхДокументов(РежимЗаписи, СсылкаНаДокумент, СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПровестиСозданныеДокументы()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проведение созданных документов'");
	
	ДокументыДляПроведения = Новый Массив;
	Для Каждого СтрокаДокумента Из СозданныеДокументы.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(СтрокаДокумента.Ссылка) Тогда
			ДокументыДляПроведения.Добавить(СтрокаДокумента.Ссылка);
		Иначе
			Для Каждого ПодстрокаДокумента Из СтрокаДокумента.ПолучитьЭлементы() Цикл
				Если ЗначениеЗаполнено(ПодстрокаДокумента.Ссылка) Тогда
					ДокументыДляПроведения.Добавить(ПодстрокаДокумента.Ссылка);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	АдресХранилищаДокументы = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ЭлектронноеВзаимодействиеБП.ПровестиСозданныеДокументы",
		ДокументыДляПроведения,
		АдресХранилищаДокументы);
	
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаСервере
Процедура ПослеПроведенияСозданныхДокументовНаСервере()
	
	РезультатПроведения = ПолучитьИзВременногоХранилища(АдресХранилищаДокументы);
	Если ТипЗнч(РезультатПроведения) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДокумента Из СозданныеДокументы.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(СтрокаДокумента.Ссылка) Тогда
			ТекстСообщения = РезультатПроведения.Получить(СтрокаДокумента.Ссылка);
			Если ЗначениеЗаполнено(ТекстСообщения) Тогда
				СтрокаДокумента.Картинка = 1;
				СтрокаДокумента.ТекстОшибки = ТекстСообщения;
			Иначе
				СтрокаДокумента.Картинка = 2;
			КонецЕсли;
		Иначе
			Для Каждого ПодстрокаДокумента Из СтрокаДокумента.ПолучитьЭлементы() Цикл
				ТекстСообщения = РезультатПроведения.Получить(ПодстрокаДокумента.Ссылка);
				Если ЗначениеЗаполнено(ТекстСообщения) Тогда
					ПодстрокаДокумента.Картинка = 1;
					ПодстрокаДокумента.ТекстОшибки = ТекстСообщения;
				Иначе
					ПодстрокаДокумента.Картинка = 2;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСостояниеФайлов(ПомещенныеФайлы)
	
	Для Каждого ПомещенныйФайл Из ПомещенныеФайлы Цикл
		
		Если ПомещенныйФайл.ПомещениеФайлаОтменено Тогда
			Продолжить;
		КонецЕсли;
		
		Файл = НайтиФайлПоИдентификатору(ПомещенныйФайл.СсылкаНаФайл.ИдентификаторФайла);
		
		Если Файл = Неопределено Тогда
			Файл = ДобавитьФайл(
				ПомещенныйФайл.СсылкаНаФайл.Имя,
				ПомещенныйФайл.СсылкаНаФайл.ИдентификаторФайла,
				ПомещенныйФайл.СсылкаНаФайл.Расширение
			);
		КонецЕсли;
		
		Файл.Адрес = ПомещенныйФайл.Адрес;
		
	КонецЦикла;
	
	Элементы.ГруппаФайлы.Заголовок = СтрШаблон(НСтр("ru='Файлы (%1)'"), Формат(Файлы.Количество(), "ЧН=0"));
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьФайл(Имя, ИдентификаторФайла, Расширение)
	
	НовыйФайл = Файлы.Добавить();
	НовыйФайл.Наименование = Имя;
	НовыйФайл.ИдентификаторФайла = ИдентификаторФайла;
	НовыйФайл.Расширение = Расширение;
	НовыйФайл.ИндексКартинки = РаботаСФайламиСлужебныйКлиентСервер.ИндексПиктограммыФайла(Расширение);
	
	Возврат НовыйФайл;
	
КонецФункции

&НаКлиенте
Процедура ПерерисоватьФайлыПоСостоянию()
	
	ЕстьЗагруженныеФайлы = (Файлы.Количество() > 0);
	
	Если ВыполняетсяЗагрузка Или ЕстьЗагруженныеФайлы Тогда
		Элементы.ВыборФайлов.ТекущаяСтраница = Элементы.Список;
		Элементы.ДобавитьВСписок.Видимость = Ложь;
		Элементы.ГруппаПодсказка.Видимость = Ложь;
	Иначе
		Элементы.ВыборФайлов.ТекущаяСтраница = Элементы.ПредложениеПеретащить;
		Элементы.ГруппаПодсказка.Видимость = Истина;
	КонецЕсли;
	
	Элементы.СостояниеЗагрузки.Видимость = ВыполняетсяЗагрузка;
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаличиеФайловДляЗагрузки()
	
	Если НЕ ВыполняетсяЗагрузка И Файлы.Количество() > 0 Тогда
		
		WB   = Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries");
		Ozon = Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON");
		ММ   = Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Мегамаркет");
			
		Если (WB И ЗагружаемыеФайлыWB.ПолучитьЭлементы().Количество() = 0)
			Или (Ozon И ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Количество() = 0)
			Или (ММ И ЗагружаемыеФайлыМегамаркет.Количество() = 0) Тогда
			
			ПоказатьПредупреждение(, НСтр("ru = 'Среди выбранных файлов нет подходящих для загрузки.'"));
			Элементы.ФайлыОписание.Видимость = Истина;
			Элементы.ДобавитьВСписок.Видимость = Истина;
			Элементы.ГруппаПодсказка.Видимость = Истина;

		КонецЕсли;
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Функция НайтиФайлПоИдентификатору(ИдентификаторФайла)
	
	НайденныеСтроки = Файлы.НайтиСтроки(Новый Структура("ИдентификаторФайла", ИдентификаторФайла));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат НайденныеСтроки[0];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ПриПрогрессеРазбораФайлов(Результат, Контекст) Экспорт
	
	Если Результат.Статус = "Выполняется" Тогда
		Прогресс = ПрочитатьПрогресс(Результат.ИдентификаторЗадания);
		Если Прогресс <> Неопределено Тогда
			ТекстСообщенияЗагрузки = Прогресс.Текст;
			ПрогрессЗагрузки = Прогресс.Процент;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПрогрессеСозданияДокументов(Результат, Контекст) Экспорт
	
	Если Результат.Статус = "Выполняется" Тогда
		Прогресс = ПрочитатьПрогресс(Результат.ИдентификаторЗадания);
		Если Прогресс <> Неопределено Тогда
			ТекстСообщенияЗагрузки = Прогресс.Текст;
			ПрогрессЗагрузки = Прогресс.Процент;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПрогрессеПроведенияСозданныхДокументов(Результат, Контекст) Экспорт
	
	Если Результат.Статус = "Выполняется" Тогда
		Прогресс = ПрочитатьПрогресс(Результат.ИдентификаторЗадания);
		Если Прогресс <> Неопределено Тогда
			ТекстСообщенияЗагрузки = Прогресс.Текст;
			ПрогрессЗагрузки = Прогресс.Процент;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьПрогресс(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ПрочитатьПрогресс(ИдентификаторЗадания);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Если ЗначениеЗаполнено(Форма.Маркетплейс) Тогда
		ЭтоWB   = Форма.Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries");
		ЭтоOzon = Форма.Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON");
		ЭтоММ   = Форма.Маркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Мегамаркет");
		
		Форма.Заголовок = ?(ЭтоWB, НСтр("ru='Загрузка отчетов Wildberries'"), НСтр("ru='Загрузка отчетов Ozon'"));
		
		Элементы.ДекорацияПодсказакаWB.Видимость = ЭтоWB;
		Элементы.ДекорацияПодсказкаOzon.Видимость = ЭтоOzon;
		Элементы.ДекорацияПодсказкаМегамаркет.Видимость = ЭтоММ;
		
		Элементы.ГруппаМаркетплейсы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ГруппаWildberries.Видимость = ЭтоWB;
		Элементы.ГруппаOZON.Видимость = ЭтоOzon;
		Элементы.ГруппаМегамаркет.Видимость = ЭтоММ;
		
		Элементы.ГруппаНастройкиМегамаркет.Видимость = ЭтоММ;
		Элементы.Склад.Видимость = Форма.ВедетсяСкладскойУчет И ЭтоММ;
		
		Если ЭтоWB Тогда
			Форма.Заголовок = НСтр("ru='Загрузка отчетов Wildberries'");
			Элементы.ГруппаМаркетплейсы.ТекущаяСтраница = Элементы.ГруппаWildberries;
			Элементы.ГруппаДляНовыхКонтрагентов.Видимость = Истина;
			Элементы.Добавить.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
		ИначеЕсли ЭтоOzon Тогда
			Форма.Заголовок = НСтр("ru='Загрузка отчетов Ozon'");
			Элементы.ГруппаМаркетплейсы.ТекущаяСтраница = Элементы.ГруппаOZON;
			Элементы.ГруппаДляНовыхКонтрагентов.Видимость = Истина;
			Элементы.Добавить.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
		ИначеЕсли ЭтоММ Тогда 
			Форма.Заголовок = НСтр("ru='Загрузка отчетов Мегамаркета'");
			Элементы.ГруппаМаркетплейсы.ТекущаяСтраница = Элементы.ГруппаМегамаркет;
			Элементы.ГруппаДляНовыхКонтрагентов.Видимость = Ложь;
			Элементы.Добавить.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		КонецЕсли;
		
		ЕстьРазобранныеФайлы = (ЭтоWB И Форма.ЗагружаемыеФайлыWB.ПолучитьЭлементы().Количество() > 0)
			Или (ЭтоOzon И Форма.ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Количество() > 0)
			Или (ЭтоММ И Форма.ЗагружаемыеФайлыМегамаркет.Количество() > 0);
		
	Иначе
		
		Если Элементы.ГруппаМаркетплейсы.ТекущаяСтраница <> Элементы.ГруппаOZON
			И Форма.ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Количество() > 0 Тогда
		
			Элементы.ГруппаМаркетплейсы.ТекущаяСтраница = Элементы.ГруппаOZON;
		
		ИначеЕсли Элементы.ГруппаМаркетплейсы.ТекущаяСтраница <> Элементы.ГруппаWildberries
			И Форма.ЗагружаемыеФайлыWB.ПолучитьЭлементы().Количество() > 0 Тогда
		
			Элементы.ГруппаМаркетплейсы.ТекущаяСтраница = Элементы.ГруппаWildberries;
		
		ИначеЕсли Элементы.ГруппаМаркетплейсы.ТекущаяСтраница <> Элементы.ГруппаМегамаркет
			И Форма.ЗагружаемыеФайлыМегамаркет.Количество() > 0 Тогда
		
			Элементы.ГруппаМаркетплейсы.ТекущаяСтраница = Элементы.ГруппаМегамаркет;
		
		КонецЕсли;
		
		ЕстьРазобранныеФайлы = Форма.ЗагружаемыеФайлыWB.ПолучитьЭлементы().Количество() > 0
			Или Форма.ЗагружаемыеФайлыOzon.ПолучитьЭлементы().Количество() > 0
			Или Форма.ЗагружаемыеФайлыМегамаркет.Количество() > 0;
	
	КонецЕсли;
		
	Элементы.ГруппаСтраницы.ТекущаяСтраница = ?(ЕстьРазобранныеФайлы, Элементы.ГруппаСозданиеДокументов, Элементы.ГруппаФайлы );
	Элементы.ГруппаНастройки.Видимость      = ЕстьРазобранныеФайлы;
	Элементы.ФормаЗагрузить.Видимость       = ЕстьРазобранныеФайлы;
	
	Элементы.КонтрагентOzon.Видимость = Ложь;
	Элементы.КонтрагентМегамаркет.Видимость = Ложь;
	
	Если Форма.ЭтоЗагрузкаЧерезAPI Тогда
		Элементы.Организация.Видимость = Ложь;
		СпособСверткиОтчетовСписокВыбора = Элементы.СпособСверткиОтчетов.СписокВыбора;
		ЭлементСпискаВыборка = СпособСверткиОтчетовСписокВыбора.НайтиПоЗначению("Построчно");
		Если ЭлементСпискаВыборка <> Неопределено Тогда
			ЭлементСпискаВыборка.Представление = НСтр("ru = 'Детально'");
		КонецЕсли;
		ЭлементСпискаВыборка = СпособСверткиОтчетовСписокВыбора.НайтиПоЗначению("Сворачивать");
		Если ЭлементСпискаВыборка <> Неопределено Тогда
			ЭлементСпискаВыборка.Представление = НСтр("ru = 'Сворачивать одинаковые товары'");
		КонецЕсли;
		Элементы.СпособСверткиОтчетов.МаксимальнаяШирина = 23;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Видимость колонок для отображения заголовков
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBЗагружать");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBОписание1");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBОписание2");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBОписание3");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBДокументПредставление2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыWB.ТекстЗаголовка", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBТекстЗаголовка");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыWB.ТекстЗаголовка", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBОписание3");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыWB.Описание3", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBДействие");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыWB.Действие", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonЗагружать");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonОписание1");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonОписание2");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonОписание3");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonДокументПредставление2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыOzon.ТекстЗаголовка", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBЗагружать");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыWB.ЭтоПодстрока", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonТекстЗаголовка");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыOzon.ТекстЗаголовка", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonОписание3");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыOzon.Описание3", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonДействие");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыOzon.Действие", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonЗагружать");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыOzon.ЭтоПодстрока", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетЗагружать");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетОписание1");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетОписание2");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетДокументПредставление2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыМегамаркет.ТекстЗаголовка", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетТекстЗаголовка");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыМегамаркет.ТекстЗаголовка", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СозданныеДокументыТекстОшибки");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "СозданныеДокументы.ТекстОшибки", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "Организация", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Представление документа ИБ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыWBДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыWB.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ЗагружаемыеФайлыWB.ДокументПредставление1"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыOzonДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыOzon.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ЗагружаемыеФайлыOzon.ДокументПредставление1"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеФайлыМегамаркетДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ЗагружаемыеФайлыМегамаркет.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ЗагружаемыеФайлыМегамаркет.ДокументПредставление1"));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЗамещаемогоДокумента(Знач Таблица, ЭлементФормы)
	
	Если ТипЗнч(Таблица) = Тип("ДанныеФормыДерево") Тогда
		Таблица = Таблица.ПолучитьЭлементы();
	КонецЕсли;
	
	НайденыДокументы = Ложь;
	Для Каждого Строка Из Таблица Цикл
		Если ЗначениеЗаполнено(Строка.Документ) Тогда
			НайденыДокументы = Истина;
			Прервать;
		КонецЕсли;
		Если ТипЗнч(Строка) = Тип("ДанныеФормыЭлементДерева") Тогда
			Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
				Если ЗначениеЗаполнено(Подстрока.Документ) Тогда
					НайденыДокументы = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	ЭлементФормы.Видимость = НайденыДокументы;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиСоздатьОтчетМаркетплейса(Организация, Период, ВидМаркетплейса, ОписаниеОшибки = "")

	Если Не ЗначениеЗаполнено(Организация)
		Или Не ЗначениеЗаполнено(Период)
		Или Не ЗначениеЗаполнено(ВидМаркетплейса) Тогда
		ОписаниеОшибки = НСтр("ru = 'Не удалось создать отчет маркетплейса'");
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Период));
	Запрос.УстановитьПараметр("Маркетплейс", ВидМаркетплейса);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетМаркетплейса.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|ГДЕ
	|	НЕ ОтчетМаркетплейса.ПометкаУдаления
	|	И ОтчетМаркетплейса.Организация = &Организация
	|	И ОтчетМаркетплейса.Маркетплейс = &Маркетплейс
	|	И КОНЕЦПЕРИОДА(ОтчетМаркетплейса.Дата, МЕСЯЦ) = &Дата";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	КонецЕсли;
	
	ПроверкаЗапрета = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	ДанныеДляПроверки = ПроверкаЗапрета.Добавить();
	ДанныеДляПроверки.Раздел   = "БухгалтерскийУчет";
	ДанныеДляПроверки.Объект   = Организация;
	ДанныеДляПроверки.Дата     = Период;
	ПараметрыСообщенияОЗапрете = ДатыЗапретаИзменения.ПараметрыСообщенияОЗапрете();
	УстановленЗапретИзменения  = ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ПроверкаЗапрета, ПараметрыСообщенияОЗапрете, ОписаниеОшибки);
	Если УстановленЗапретИзменения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйОтчетМаркетплейса = Документы.ОтчетМаркетплейса.СоздатьДокумент();
	НовыйОтчетМаркетплейса.Дата = КонецМесяца(Период);
	НовыйОтчетМаркетплейса.Организация = Организация;
	НовыйОтчетМаркетплейса.Маркетплейс = ВидМаркетплейса;
	
	Попытка
		НовыйОтчетМаркетплейса.Записать();
	Исключение
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр
			("ru = 'Не удалось создать Отчет маркетплейса (%1) от %2 по организации ""%3"", 
			|по причине %4'"),
				ВидМаркетплейса, Формат(Период, "ДЛФ=ДД"), Организация, 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НовыйОтчетМаркетплейса.Ссылка;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиСоздатьВидОплатыМегамаркет(Организация, Контрагент, ДоговорКонтрагента, ОписаниеОшибки)
	
	Если Не ЗначениеЗаполнено(Организация)
		Или Не ЗначениеЗаполнено(Контрагент) Тогда
		ОписаниеОшибки = НСтр("ru = 'Не удалось создать вид оплаты: Оплата через Мегамаркет'");
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ДоговорКонтрагента = НайтиСоздатьДоговорКонтрагентаМегамаркета(Организация, Контрагент);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыОплатОрганизаций.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыОплатОрганизаций КАК ВидыОплатОрганизаций
	|ГДЕ
	|	НЕ ВидыОплатОрганизаций.ПометкаУдаления
	|	И ВидыОплатОрганизаций.Организация = &Организация
	|	И ВидыОплатОрганизаций.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплат.ПодарочныйСертификатСобственный)
	|	И ВидыОплатОрганизаций.Контрагент = &Контрагент
	|	И ВидыОплатОрганизаций.ДоговорКонтрагента = &ДоговорКонтрагента";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВидОплаты = Выборка.Ссылка;
	Иначе
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Организация", Организация);
		ДанныеЗаполнения.Вставить("Контрагент", Контрагент);
		ДанныеЗаполнения.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
		ДанныеЗаполнения.Вставить("ТипОплаты", Перечисления.ТипыОплат.ПодарочныйСертификатСобственный);
		
		НовыйВидОплаты = Справочники.ВидыОплатОрганизаций.СоздатьЭлемент();
		НовыйВидОплаты.Заполнить(ДанныеЗаполнения);
		НовыйВидОплаты.Наименование = НСтр("ru = 'Оплата через Мегамаркет'");
		НовыйВидОплаты.СчетУчетаРасчетов = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами;
		Попытка
			НовыйВидОплаты.Записать();
		Исключение
			ОписаниеОшибки = НСтр("ru = 'Не удалось записать новый вид оплаты: Оплата через Мегамаркет'");
			Возврат Неопределено;
		КонецПопытки;
		ВидОплаты = НовыйВидОплаты.Ссылка;
	КонецЕсли;
	
	Возврат ВидОплаты;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиСоздатьДоговорКонтрагентаМегамаркета(Организация, Контрагент)

	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Владелец", Контрагент);
	ПараметрыДоговора.Вставить("Организация", Организация);
	ПараметрыДоговора.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	
	ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	ЕстьДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		ДоговорКонтрагента,
		ПараметрыДоговора.Владелец,
		ПараметрыДоговора.Организация,
		ПараметрыДоговора.ВидДоговора);
		
	Если НЕ ЕстьДоговорКонтрагента Тогда
		ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", ПараметрыДоговора);
		ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
	КонецЕсли;
	
	Возврат ДоговорКонтрагента;
	
КонецФункции

&НаСервере
Процедура ПолучитьНастройкиМегамаркета()
	
	Если Маркетплейс <> Перечисления.ВидыМаркетПлейсов.Мегамаркет Тогда
		Возврат;
	КонецЕсли;
	
	Склад = Неопределено;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетОРозничныхПродажах.Склад КАК Склад
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Организация = &Организация
	|	И ОтчетОРозничныхПродажах.ОтчетМаркетплейса <> ЗНАЧЕНИЕ(Документ.ОтчетМаркетплейса.ПустаяСсылка)
	|	И ОтчетОРозничныхПродажах.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтчетОРозничныхПродажах.Дата УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Склад = Выборка.Склад;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоРеестрПродажЮрлицамOzon(ВидДокумента)
	
	Возврат ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon();
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоРеестрПродажЮрлицамWB(ВидДокумента)
	
	Возврат ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB();
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоДокументНаВозврат(ТипДокумента)
	
	Возврат Врег(Лев(СокрЛП(ТипДокумента), 3)) = "УКД";
	
КонецФункции

&НаКлиенте
Функция ОповеститьОбИзмененииСозданныхДокументов()
	
	ИзмененныеТипы = Новый Массив;
	
	Для Каждого СтрокаДокумента Из СозданныеДокументы.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(СтрокаДокумента.Ссылка) Тогда
			ТипДокумента = ТипЗнч(СтрокаДокумента.Ссылка);
			Если ИзмененныеТипы.Найти(ТипДокумента) = Неопределено Тогда
				ИзмененныеТипы.Добавить(ТипДокумента);
			КонецЕсли;
		КонецЕсли;
		Для Каждого ПодстрокаДокумента Из СтрокаДокумента.ПолучитьЭлементы() Цикл
			Если ЗначениеЗаполнено(ПодстрокаДокумента.Ссылка) Тогда
				ТипДокумента = ТипЗнч(ПодстрокаДокумента.Ссылка);
				Если ИзмененныеТипы.Найти(ТипДокумента) = Неопределено Тогда
					ИзмененныеТипы.Добавить(ТипДокумента);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ИзмененныйТип Из ИзмененныеТипы Цикл
		ОповеститьОбИзменении(ИзмененныйТип);
	КонецЦикла;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	СпособСверткиОтчетов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ВладелецХранилищаНастроек(), "СпособСверткиОтчетов", "Построчно");
	ГруппаДляНовыхКонтрагентов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ВладелецХранилищаНастроек(), "ГруппаДляНовыхКонтрагентов");
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиФормы()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ВладелецХранилищаНастроек(), "СпособСверткиОтчетов", СпособСверткиОтчетов);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ВладелецХранилищаНастроек(), "ГруппаДляНовыхКонтрагентов", ГруппаДляНовыхКонтрагентов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВладелецХранилищаНастроек()
	
	Возврат ОбменСМаркетплейс.ВладелецХранилищаНастроек();
	
КонецФункции

#КонецОбласти