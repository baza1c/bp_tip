#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ВидМаркетплейса) Тогда
		ВызватьИсключение НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
	КонецЕсли; 
	
	ВидОперации = Параметры.ВидОперации;
	ВидМаркетплейса = Параметры.ВидМаркетплейса;
	ЭтоЯндексМаркет = Параметры.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет;
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Параметры, "Организация", Справочники.Организации.ОрганизацияПоУмолчанию());
		
	ОрганизацияПриИзмененииНаСервере();
	
	ПодготовитьФормуНаСервере();
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	Если ВидЗагрузки = 0 Тогда
		ПроверитьЗагрузитьПоAPI();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьНастройкаИнтеграцииМаркетплейс"
		И ЗначениеЗаполнено(Параметр) Тогда
		
		НастройкаИнтеграции = Параметр;
		
		Если ЭтоЯндексМаркет Тогда
			НастройкаИнтеграцииПриИзмененииНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	ЗначениеВидЗагрузки = ХранилищеОбщихНастроек.Загрузить(Строка(ВидМаркетплейса), "ВидЗагрузки");
	
	Если ЗначениеВидЗагрузки = Неопределено Тогда
		ЗначениеВидЗагрузки = 1;
	КонецЕсли;
	
	Настройки.Вставить("ВидЗагрузки", ЗначениеВидЗагрузки);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ЗначениеВидЗагрузки = Настройки["ВидЗагрузки"];
	
	Если ЗначениеВидЗагрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ХранилищеОбщихНастроек.Сохранить(Строка(ВидМаркетплейса), "ВидЗагрузки", ЗначениеВидЗагрузки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидЗагрузки = 0 Тогда
		НачатьЗагрузкуПоAPI();
	Иначе
		НачатьЗагрузкуИзФайла();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	НастройкаИнтеграции = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Организация, ВидМаркетплейса);
	
	Если НЕ НастройкаИнтеграции.Пустая() 
		И ЭтоЯндексМаркет Тогда
		
		НастройкаИнтеграцииПриИзмененииНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НастройкаИнтеграцииПриИзмененииНаСервере()
	Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "Контрагент");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкаИнтеграции(ОшибкаАвторизации = Ложь)
	
	ПараметрыОткрытияФормыНастройки = Новый Структура; 
	Если НастройкаИнтеграции.Пустая() Тогда
		ЗначенияЗаполнения = Новый Структура();
		ЗначенияЗаполнения.Вставить("Владелец",        Организация);
		ЗначенияЗаполнения.Вставить("ВидМаркетплейса", ВидМаркетплейса);
		ЗначенияЗаполнения.Вставить("Контрагент",      Контрагент);
		ПараметрыОткрытияФормыНастройки.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Иначе
		ПараметрыОткрытияФормыНастройки.Вставить("Ключ", НастройкаИнтеграции);
	КонецЕсли;
	
	ПараметрыОткрытияФормыНастройки.Вставить("ВидОперации", ВидОперации);
	ПараметрыОткрытияФормыНастройки.Вставить("ОшибкаАвторизации", ОшибкаАвторизации);
	ДополнительныеПараметры = Новый Структура("ОшибкаАвторизации", ОшибкаАвторизации);
	ОповещениеОЗавершении = НОвый ОписаниеОповещения("НастройкаИнтеграцииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(
		"Справочник.НастройкиИнтеграцииМаркетплейс.ФормаОбъекта", ПараметрыОткрытияФормыНастройки, 
		ЭтотОбъект,,,,ОповещениеОЗавершении,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры
	
&НаКлиенте
Процедура НастройкаИнтеграцииЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.ОшибкаАвторизации Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьЗагрузитьПоAPI();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "НастройкиИнтеграции" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуНастройкаИнтеграции();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьОбновитьНастройкуИнтеграции(НастройкаИнтеграции, ДанныеЗаполнения)
	Если НастройкаИнтеграции.Пустая() Тогда
		НастройкаИнтеграцииОбъект = Справочники.НастройкиИнтеграцииМаркетплейс.СоздатьЭлемент();
	Иначе
		НастройкаИнтеграцииОбъект = НастройкаИнтеграции.ПолучитьОбъект();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НастройкаИнтеграцииОбъект, ДанныеЗаполнения);
	
	НастройкаИнтеграцииОбъект.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет;
	НастройкаИнтеграцииОбъект.Наименование = "Яндекс.Маркет";
	НастройкаИнтеграцииОбъект.КаталогЗагружен = Истина;
	
	НастройкаИнтеграцииОбъект.Записать();
	
КонецПроцедуры


&НаКлиенте
Процедура НачатьЗагрузкуИзФайла()
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоЯндексМаркет Тогда
		ДанныеЗаполнения = Новый Структура("Владелец, Контрагент", Организация, Контрагент);
		ПроверитьОбновитьНастройкуИнтеграции(НастройкаИнтеграции, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON") Тогда
		ДоступныеРаширения = "(*.csv;*.zip)|*.csv;*.zip"
	ИНаче
		ДоступныеРаширения = "(*.xlsx;*.xls;*.mxl;*.zip)|*.xlsx;*.xls;*.mxl;*.zip";
	КонецЕсли;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов(НСтр("ru = 'Выберите файлы для загрузки'"), Истина, ДоступныеРаширения);
	ВыполнитьЗагрузкуФайлов(ПараметрыДиалога);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗагрузитьПоAPI()
	
	Если НастройкаИнтеграции.Пустая() Тогда
		Возврат;
	КонецЕсли;
		
	Доступность = Ложь;
	
	НачатьЗагрузкуПоAPI();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗагрузкуПоAPI()
	
	Если НастройкаИнтеграции.Пустая() Тогда
		ОткрытьФормуНастройкаИнтеграции();
		Возврат;
	КонецЕсли;

	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоЯндексМаркет Тогда
		ДанныеЗаполнения = Новый Структура("Владелец, Контрагент", Организация, Контрагент);
		ПроверитьОбновитьНастройкуИнтеграции(НастройкаИнтеграции, ДанныеЗаполнения);
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьНоменклатуруЗавершение", ЭтотОбъект);
	ОбменСМаркетплейсКлиент.ЗагрузитьСписокНоменклатуры(НастройкаИнтеграции, ЭтотОбъект, ОповещениеОЗавершении);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗагрузитьНоменклатуруЗавершение(РезультатОперации, ДополнительныеПараметры) Экспорт
	Перем КодОтветаСервера;
	
	Если РезультатОперации.Результат Тогда
		СопоставитьНоменклатуру(РезультатОперации.СписокТоваров);
	ИначеЕсли РезультатОперации.Свойство("КодОтветаСервера", КодОтветаСервера) 
		И (КодОтветаСервера = 401 Или КодОтветаСервера = 403) Тогда
		
		ОбменСМаркетплейсКлиент.ПредложитьНовуюАвторизацию(Организация);
	ИначеЕсли ЗначениеЗаполнено(РезультатОперации.СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОперации.СообщениеОбОшибке);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'При выполнении операции произошла ошибка. Подробности в журнале регистрации.'"));
	КонецЕсли;
	
	Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуру(СписокТоваров)
	Если НЕ ЗначениеЗаполнено(СписокТоваров) Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка завершена'"),,НСтр("ru = 'Новых товаров нет'"));
		
		Закрыть(Истина);
		Возврат;
	КонецЕсли;
	
	ПараметрыСопоставления = Новый Структура;
	ПараметрыСопоставления.Вставить("ВидМаркетплейса",     ВидМаркетплейса);
	Если ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		ПараметрыСопоставления.Вставить("НастройкаИнтеграции", НастройкаИнтеграции);
	Иначе
		НастройкаИнтеграцииСтруктура = Новый Структура;
		НастройкаИнтеграцииСтруктура.Вставить("Владелец",        Организация);
		НастройкаИнтеграцииСтруктура.Вставить("ВидМаркетплейса", ВидМаркетплейса);
		ПараметрыСопоставления.Вставить("НастройкаИнтеграции", НастройкаИнтеграцииСтруктура);
	КонецЕсли;
	ОбменСМаркетплейсКлиент.СопоставитьНоменклатуруПриЗагрузкеКаталога(СписокТоваров, ПараметрыСопоставления);
	
	Закрыть(Истина);
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	Заголовок = СтрШаблон(НСтр("ru = 'Загрузка каталога %1'"), Строка(ВидМаркетплейса));

	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаWB;
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЯндекс;
		Элементы.Контрагент.Видимость = Истина;
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОзон;
	КонецЕсли; 
КонецПроцедуры

#Область ЗагрузкаФайлов

&НаКлиенте
Процедура ВыполнитьЗагрузкуФайлов(СсылкиНаФайлыИлиПараметрыДиалога)
	
	НачатьПомещениеФайловНаСервер(
		Новый ОписаниеОповещения("ПриЗавершенииПомещенияФайлов", ЭтотОбъект),
		Новый ОписаниеОповещения("ПриВыполненииПомещенияФайлов", ЭтотОбъект),
		,
		СсылкиНаФайлыИлиПараметрыДиалога,
		УникальныйИдентификатор
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыполненииПомещенияФайлов(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ПомещеноВсего, ОтказОтПомещенияВсехФайлов, Контекст) Экспорт
	
	Если Не ЭтотОбъект.Открыта() Тогда
		ОтказОтПомещенияВсехФайлов = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПомещенияФайлов(ПомещенныеФайлы, Контекст) Экспорт
	Если ПомещенныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаНаФайл = ПомещенныеФайлы[0].СсылкаНаФайл;
	
	Файл = Новый Структура;
	Файл.Вставить("Адрес",              ПомещенныеФайлы[0].Адрес);
	Файл.Вставить("Наименование",       СсылкаНаФайл.Имя);
	Файл.Вставить("Расширение",         СсылкаНаФайл.Расширение);
	Файл.Вставить("ИдентификаторФайла", СсылкаНаФайл.ИдентификаторФайла); 
	
	ПодключитьОбработчикОжидания("РазобратьФайлы", 0.2, Истина);
КонецПроцедуры

#КонецОбласти

#Область РазборФайлов

&НаСервереБезКонтекста
Функция НачатьРазборФайловНаСервере(Файл, АдресВременногоХранилища, УникальныйИдентификатор)
	
	ОтправляемыеФайлы = Новый Массив;
	
	ОписаниеФайла = Новый Структура;
	
	ОписаниеФайла.Вставить("ИмяФайла", Файл.Наименование);
	
	ВременныйФайл = ПолучитьИмяВременногоФайла(Файл.Расширение);
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Файл.Адрес);
	ДвоичныеДанные.Записать(ВременныйФайл);
	ОписаниеФайла.Вставить("РасширениеФайла", Файл.Расширение);
	ОписаниеФайла.Вставить("ПолноеИмяФайла", Файл.Наименование);
	ОписаниеФайла.Вставить("ВременныйФайл", ВременныйФайл);
	
	ОтправляемыеФайлы.Добавить(ОписаниеФайла);
		
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Разбор файлов, полученных из маркетплейса'");
	Если ЗначениеЗаполнено(АдресВременногоХранилища) Тогда
		ДанныеРазобранныхФайлов = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		АдресВременногоХранилища =  ПоместитьВоВременноеХранилище(ДанныеРазобранныхФайлов, УникальныйИдентификатор);
	КонецЕсли;
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ЭлектронноеВзаимодействиеБП.ПолучитьДанныеВСтруктуру",
		ОтправляемыеФайлы, АдресВременногоХранилища);
	
	Возврат ФоновоеЗадание;
КонецФункции

&НаКлиенте
Процедура РазобратьФайлы()
	ФоновоеЗадание = НачатьРазборФайловНаСервере(Файл, АдресВременногоХранилища, УникальныйИдентификатор);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ПослеРазбораФайлов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокНеСопоставленнойНоменклатурыПоСтруктуре(АдресВременногоХранилища, ОрганизацияСсылка, КонтрагентСсылка, ВидМаркетплейса, УникальныйИдентификатор, СообщениеОбОшибке)
	ДанныеХранилища = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если ТипЗнч(ДанныеХранилища) <> Тип("Массив") 
		ИЛИ ДанныеХранилища.Количество() = 0
		ИЛИ ДанныеХранилища[0].Свойство("ТекстОшибки", СообщениеОбОшибке) Тогда
	
		Возврат Новый Массив;
	КонецЕсли;
	ДанныеДокумента = ДанныеХранилища[0].ДанныеДокумента;
	
	Если ЗначениеЗаполнено(КонтрагентСсылка) Тогда
		ПлательщикСтруктура = Новый Структура("Ссылка", КонтрагентСсылка);
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		ПлательщикСтруктура = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB();
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON Тогда
		ПлательщикСтруктура = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыOZON();
	Иначе
		СообщениеОбОшибке = НСтр("ru = 'Не указан контрагент маркетплейса'");
		Возврат Новый Массив;
	КонецЕсли;
	ДанныеДокумента.Вставить("Плательщик", ПлательщикСтруктура);
	ДанныеДокумента.Вставить("Организация", ОрганизацияСсылка);
	
	Возврат ЭлектронноеВзаимодействиеБП.СписокНеСопоставленнойНоменклатурыПоСтруктуре(
		ПоместитьВоВременноеХранилище(ДанныеХранилища, АдресВременногоХранилища), УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура ПослеРазбораФайлов(Задание, Контекст) Экспорт
	Перем СообщениеОбОшибке;
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Задание.Статус = "Выполнено" Тогда
		ПоказатьПредупреждение(, Задание.КраткоеПредставлениеОшибки);
		АдресВременногоХранилища = Неопределено;
		Возврат;
	КонецЕсли;
	
	СписокТоваров = СписокНеСопоставленнойНоменклатурыПоСтруктуре(
		АдресВременногоХранилища, Организация, Контрагент, ВидМаркетплейса, УникальныйИдентификатор, СообщениеОбОшибке);
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("Результат",         НЕ ЗначениеЗаполнено(СообщениеОбОшибке));
	РезультатОперации.Вставить("СписокТоваров",     СписокТоваров);
	РезультатОперации.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
	
	ЗагрузитьНоменклатуруЗавершение(РезультатОперации, Неопределено);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	НепроверяемыеРеквизиты = Новый Массив;
	Если НЕ ЭтоЯндексМаркет Тогда
		НепроверяемыеРеквизиты.Добавить("Контрагент");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти



