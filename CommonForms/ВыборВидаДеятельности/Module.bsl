
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МножественныйВыбор = Параметры.МножественныйВыбор;
	РежимВыделенияТаблиц = ?(МножественныйВыбор, РежимВыделенияТаблицы.Множественный, РежимВыделенияТаблицы.Одиночный);
	ЗапрещенныеКоды = Параметры.ЗапрещенныеКоды;
	
	Элементы.Классификатор.РежимВыделения     = РежимВыделенияТаблиц;
	Элементы.ЭлементыКатегорий.РежимВыделения = РежимВыделенияТаблиц;
	Элементы.МоиВиды.РежимВыделения = РежимВыделенияТаблиц;
	
	ПроверятьОбновления = Не ОбщегоНазначения.РазделениеВключено()
		И Не ПолучитьФункциональнуюОпцию("РаботаВАвтономномРежиме")
		И ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки()
		И РаботаСКлассификаторами.ИнтерактивнаяЗагрузкаКлассификаторовДоступна();
	
	Организация = Параметры.Организация;
	ОтображатьИзбранное = Не МножественныйВыбор И ЗначениеЗаполнено(Организация);
	
	Если ОтображатьИзбранное Тогда
		НастройкаФормы = НастроитьСписокИзбранных();
		ИсторияПросмотра = НастройкаФормы.ИсторияПросмотра;
	КонецЕсли;
		
	ЗаполнитьРазделы();
	ОбновитьДеревоКлассификатора();
	
	ТекущийКод = Параметры.ТекущийКод;
	
	УстановитьТекущиеРазделКод(Параметры.ТекущийРаздел);
	
	НастроитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПроверятьОбновления Тогда
		ПодключитьОбработчикОжидания("ПроверитьОбновленияКлассификатора", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	СтрокаПоиска = СокрЛП(СтрокаПоиска);
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
	
		ОбновитьРезультатПоиска();

		Для Каждого Стр Из Классификатор.ПолучитьЭлементы() Цикл
			Элементы.Классификатор.Развернуть(Стр.ПолучитьИдентификатор(), Истина);
		КонецЦикла;
		
		// Поиск по строке имеет смысл выполнять только в дереве классификатора.
		УстановитьТекущиеРазделКод(ИмяРазделаКлассификатор());

	Иначе
		АдресРезультатПоиска = "";
		// При сворачивании теряется позиционирование на текущей строке, поэтому заранее запомним ее.
		ТекущаяСтрока = Элементы.Классификатор.ТекущаяСтрока;
		СтрокиДерева = Классификатор.ПолучитьЭлементы();
		СвернутьСтрокиДереваРекурсивно(СтрокиДерева);
		Элементы.Классификатор.ТекущаяСтрока = ТекущаяСтрока;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ПолнаяСсылкаHTML = ДанныеСобытия.Href;
	
	Если ЗначениеЗаполнено(ПолнаяСсылкаHTML) Тогда
		СтандартнаяОбработка = Ложь;
		ОбработатьНажатиеСсылки(ПолнаяСсылкаHTML);
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеТекущейСтроки", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ПолнотекстовыйПоискВДанных.Форма.УправлениеПолнотекстовымПоискомИИзвлечениемТекстов");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРазделы

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	РазделыТекущиеДанные = Элементы.Разделы.ТекущиеДанные;
	Если ТекущийРаздел = РазделыТекущиеДанные.Наименование Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийРаздел = РазделыТекущиеДанные.Наименование;
	
	Если РазделыТекущиеДанные.ЭтоКлассификатор Тогда
		
		ТекущаяСтраница = Элементы.СтраницаКлассификатор;
		// Описание обновляется с задержкой. Если у кода изменился признак "Избранное" и при этом
		// текущая строка классификатора не изменилась, то изменение цвета "звездочки" становится заметным.
		Описание = ПустоеОписание();
		
		ИдентификаторТекущегоКода = ИдентификаторТекущегоКода(Классификатор.ПолучитьЭлементы(), ТекущийКод);
		Если Элементы.Классификатор.ТекущаяСтрока = ИдентификаторТекущегоКода Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеТекущейСтроки", 0.1, Истина);
		Иначе
			Элементы.Классификатор.ТекущаяСтрока = ИдентификаторТекущегоКода;
		КонецЕсли;
		
	ИначеЕсли РазделыТекущиеДанные.ЭтоИзбранное Тогда
		
		ТекущаяСтраница = Элементы.СтраницаМоиВиды;
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииИзбранного", 0.1, Истина);
				
		СтрокаПоиска = "";
		АдресРезультатПоиска = "";
		
	Иначе
		
		ТекущаяСтраница = Элементы.СтраницаЭлементыКатегорий;
		
		ИдентификаторСтроки = Элементы.ЭлементыКатегорий.ТекущаяСтрока;
		Элементы.ЭлементыКатегорий.ОтборСтрок = Новый ФиксированнаяСтруктура("Категория", РазделыТекущиеДанные.Наименование);
		Элементы.ЭлементыКатегорий.ТекущаяСтрока = ИдентификаторСтроки;
		
		СтрокаПоиска = "";
		АдресРезультатПоиска = "";
		
	КонецЕсли;
	
	ОбновитьКонтекстноеМеню();
	
	Если Элементы.СтраницыРазделы.ТекущаяСтраница <> ТекущаяСтраница Тогда
		Элементы.СтраницыРазделы.ТекущаяСтраница = ТекущаяСтраница;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлассификатор

&НаКлиенте
Процедура КлассификаторВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Классификатор.ТекущиеДанные;
	
	ЭтоЗапрещенныйКод = ТекущиеДанные.ДоступенДляВыбора <> ДоступенДляВыбора();
	Если ЭтоЗапрещенныйКод Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ПриВыбореСтрокиКода("Классификатор", Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Классификатор.ТекущиеДанные;
	Если ИдентификаторТекущейСтроки = Элементы.Классификатор.ТекущаяСтрока
		Или Элементы.Классификатор.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКод = ТекущиеДанные.Код;
	Если СсылкиДляВозврата.Количество() = 0 Тогда
		СсылкиДляВозврата.Добавить(ТекущиеДанные.Код, ТекущиеДанные.Наименование);
	ИначеЕсли СсылкиДляВозврата[СсылкиДляВозврата.Количество() - 1].Значение <> ТекущийКод Тогда
		СсылкиДляВозврата.Очистить();
		СсылкиДляВозврата.Добавить(ТекущиеДанные.Код, ТекущиеДанные.Наименование);
	КонецЕсли;
	
	Если ТекущиеДанные.ДоступенДляВыбора = ДоступенДляВыбора() Тогда
		Картинка = КартинкаИзбранного(ТекущиеДанные.Избранное);
		ИсторияПросмотра.Вставить(
			0,
			ТекущиеДанные.Код,
			СтрШаблон("%1 | %2", ТекущиеДанные.Код, ТекущиеДанные.Наименование),
			,
			Картинка);
		УстановитьВидимостьИстории();
	КонецЕсли;
	
	ОбновитьКонтекстноеМеню();
	
	ИдентификаторТекущейСтроки = Элементы.Классификатор.ТекущаяСтрока;

	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеТекущейСтроки", 0.1, Истина);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭлементыКатегорий

&НаКлиенте
Процедура ЭлементыКатегорийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ИмяТаблицы = "ЭлементыКатегорий";
	
	ПриВыбореСтрокиКода(ИмяТаблицы, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлементыКатегорийПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ЭлементыКатегорий.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКод = ТекущиеДанные.Код;
	
	ОбновитьКонтекстноеМеню();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМоиВиды

&НаКлиенте
Процедура МоиВидыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ИмяТаблицы = "МоиВиды";

	ПриВыбореСтрокиКода(ИмяТаблицы, Поле);

КонецПроцедуры

&НаКлиенте
Процедура МоиВидыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.МоиВиды.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКод = ТекущиеДанные.Код;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВидДеятельности(Команда)
	
	ЗавершитьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеКлассификатора(Команда)
	
	НачатьОбновлениеДанныхСписка();
	ОжидатьЗавершениеОбновленияДанныхСписка();
	
	// Если Интернет-поддержка не подключена, то загрузка не начнется, и будет взведен флаг ТребуетсяПодключениеИнтернетПоддержки.
	// В этом случае позже повторно запустим загрузку, если пользователь введет данные авторизации.
	Если ТребуетсяПодключениеИнтернетПоддержки Тогда
		
		ТекстСообщения = НСтр("ru = 'Для автоматического обновления классификатора ОКВЭД2 необходимо подключить Интернет-поддержку пользователей.'");
		
		Если ИнтернетПоддержкаПользователейКлиент.ДоступноПодключениеИнтернетПоддержки() Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1
				|Подключить Интернет-поддержку?'"), ТекстСообщения);
			ОбработчикЗавершения = Новый ОписаниеОповещения("ПриОтветеНаВопросПодключитьИнтернетПоддержку", ЭтотОбъект);
			ПоказатьВопрос(ОбработчикЗавершения, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
		Иначе
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1
				|Обратитесь к администратору.'"), ТекстСообщения);
			ПоказатьПредупреждение(ОбработчикЗавершения, ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИзбранное(Команда)
	
	Если ТекущийРаздел = ИмяРазделаКлассификатор() Тогда
		ТекущиеДанные = Элементы.Классификатор.ТекущиеДанные;
	ИначеЕсли ТекущийРаздел = ИмяРазделаМоиВиды() Тогда
		ТекущиеДанные = Элементы.МоиВиды.ТекущиеДанные;
	Иначе
		ТекущиеДанные = Элементы.ЭлементыКатегорий.ТекущиеДанные;
	КонецЕсли;

	ИзменитьИзбранноеКлиент(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьОписания(Команда)
	
	Элементы.Описание.Видимость = Не Элементы.Описание.Видимость;
	Элементы.ИсторияПросмотренных.Видимость = ОтображатьИзбранное И Элементы.Описание.Видимость;
	ИзменитьВидимостьОписанияСервер();
	Если Элементы.Описание.Видимость Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеТекущейСтроки", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПросмотренных(Команда)
	ВыбратьИсториюПросмотренных();
КонецПроцедуры

&НаКлиенте
Процедура Развернуть(Команда)
	ТекущаяСтрока = Элементы.Классификатор.ТекущаяСтрока;
	Элементы.Классификатор.Развернуть(ТекущаяСтрока, Истина);
КонецПроцедуры

&НаКлиенте
Процедура Свернуть(Команда)
	ТекущаяСтрока = Элементы.Классификатор.ТекущаяСтрока;
	СтрокиДерева = Классификатор.НайтиПоИдентификатору(ТекущаяСтрока).ПолучитьЭлементы();
	СвернутьСтрокиДереваРекурсивно(СтрокиДерева);
	Элементы.Классификатор.Свернуть(ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФорму()

	УстановитьУсловноеОформление();

	Элементы.МоиВиды.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("Избранное", 1));
	
	КлючОбъекта = КлассификаторОКВЭДБП.ИдентификаторНастройкиФормыВыборВидаДеятельности();
	КлючНастройки = "НастройкаВидимостиОписания";
	ВидимостьОписания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючОбъекта,
		КлючНастройки,
		Истина);
		
	Элементы.Описание.Видимость = ВидимостьОписания;
	УправлениеВидимостьюОписания(ЭтотОбъект);
	
	Если ИсторияПросмотра.Количество() = 0 Или Не ОтображатьИзбранное Или Не Элементы.Описание.Видимость Тогда
		Элементы.ИсторияПросмотренных.Видимость = Ложь;
	КонецЕсли;
	
	Если ПолнотекстовыйПоискСервер.СостояниеПолнотекстовогоПоиска() = "ПоискЗапрещен"
		И Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Элементы.СтрокаПоиска.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		Если Не Пользователи.ЭтоПолноправныйПользователь(, Истина) Тогда
			Элементы.СтрокаПоискаРасширеннаяПодсказка.Заголовок =
			НСтр("ru = 'Поиск работает только по точному совпадению слов.
				|Чтобы искать по частям слов, обратитесь к администратору для включения полнотекстового поиска.'")
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущиеРазделКод(Раздел)

	Если Не ЗначениеЗаполнено(ТекущийКод)
		Или Раздел = ИмяРазделаКлассификатор() Тогда
		
		УстановитьТекущиеРазделСтрокаКлассификатора();
		Возврат;
		
	КонецЕсли;

	Если Раздел = ИмяРазделаМоиВиды() Тогда
		
		// Устанавливаем только раздел, текущий код будет установлен при активизации раздела.
		УстановитьРаздел(Раздел);
		Возврат;
		
	КонецЕсли;
	
	Отбор = Новый Структура("Код", ТекущийКод);
	Если ЭтоПоискПоКатегориям() Тогда
		Отбор.Вставить("Категория", Раздел);
	КонецЕсли;
	
	РезультатыПоиска = ЭлементыКатегорий.НайтиСтроки(Отбор);
	
	// Если поиск по указанной категории неудачный, пробуем поискать в соседних разделах, прежде чем переходить в общий классификатор.
	Если РезультатыПоиска.Количество() = 0 Тогда
		
		Отбор = Новый Структура("Код", ТекущийКод);
		РезультатыПоиска = ЭлементыКатегорий.НайтиСтроки(Отбор);
		
		Если РезультатыПоиска.Количество() = 0 Тогда
			
			УстановитьТекущиеРазделСтрокаКлассификатора();
			Возврат;
			
		КонецЕсли;
			
	КонецЕсли;
				
	НайденныйЭлемент = РезультатыПоиска[0];
	Элементы.ЭлементыКатегорий.ТекущаяСтрока = НайденныйЭлемент.ПолучитьИдентификатор();
	УстановитьРаздел(НайденныйЭлемент.Категория);
	
КонецПроцедуры

&НаСервере
Функция ЭтоПоискПоКатегориям()
	Возврат ЗначениеЗаполнено(ТекущийРаздел) И ТекущийРаздел <> ИмяРазделаМоиВиды();
КонецФункции

&НаСервере
Процедура УстановитьТекущиеРазделСтрокаКлассификатора()
	
	ИмяРазделаКлассификатор = ИмяРазделаКлассификатор();
	УстановитьРаздел(ИмяРазделаКлассификатор);
		
	Если Не ЗначениеЗаполнено(ТекущийКод) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Классификатор.ТекущаяСтрока = ИдентификаторТекущегоКода(Классификатор.ПолучитьЭлементы(), ТекущийКод);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьРаздел(ИмяРаздела)

	Отбор = Новый Структура("Наименование", ИмяРаздела);
	РезультатыПоиска = Разделы.НайтиСтроки(Отбор);
	
	Если РезультатыПоиска.Количество() > 0 Тогда
		Элементы.Разделы.ТекущаяСтрока = РезультатыПоиска[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРазделы()
	
	Если ОтображатьИзбранное Тогда
		
		Раздел = Разделы.Добавить();
	
		Раздел.Наименование = ИмяРазделаМоиВиды();
		Раздел.Иконка       = БиблиотекаКартинок.ВыборОКВЭДМои;
		Раздел.ЭтоИзбранное = Истина;
		Раздел.Порядок = -1;
		
	КонецЕсли;
	
	Раздел = Разделы.Добавить();
	
	Раздел.ЭтоКлассификатор = Истина;
	Раздел.Наименование = ИмяРазделаКлассификатор();
	Раздел.Иконка       = БиблиотекаКартинок.ВыборОКВЭДВсе;
	
	ЗаполнитьПопулярныеКодыВЭД();
	
	Разделы.Сортировать("Порядок");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПопулярныеКодыВЭД()
	
	МакетыСКатегориями = Новый Массив;
	МакетыСКатегориями.Добавить(ПолучитьОбщийМакет("КатегорииОКВЭД_ИП"));
	МакетыСКатегориями.Добавить(ПолучитьОбщийМакет("КатегорииОКВЭД_ООО"));
	
	Для Каждого Макет Из МакетыСКатегориями Цикл
		
		Для Каждого ОбластьКатегория Из Макет.Области Цикл
			
			Верх = ОбластьКатегория.Верх;
			Низ  = ОбластьКатегория.Низ;
			
			НаименованиеКатегории = СокрП(Макет.Область(Верх, 2).Текст);
			
			Если Разделы.НайтиСтроки(Новый Структура("Наименование", НаименованиеКатегории)).Количество() <> 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Категория = Разделы.Добавить();
			
			Категория.Иконка       = ИконкаКатегории(ОбластьКатегория.Имя);
			Категория.Порядок      = Число(Макет.Область(Верх, 1).Текст);
			Категория.Наименование = НаименованиеКатегории;
			
			Для НомерСтр = Верх + 1 По Низ Цикл
				
				Элемент = ЭлементыКатегорий.Добавить();
				
				Элемент.Категория    = Категория.Наименование;
				Элемент.Код          = СокрП(Макет.Область(НомерСтр, 1).Текст);
				Элемент.Наименование = СокрП(Макет.Область(НомерСтр, 2).Текст);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ОтображатьИзбранное Тогда
		ЭлементыКатегорийТаблица = РеквизитФормыВЗначение("ЭлементыКатегорий");
		ЭлементыКатегорийТаблица.Индексы.Добавить("Код");
		Для Каждого ЭлементИзбранного Из ИзбранноеВключено Цикл
			СтрокаТаблицы = ЭлементыКатегорийТаблица.Найти(ЭлементИзбранного.Значение, "Код");
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.Избранное = 1;
			КонецЕсли;
		КонецЦикла;
		ЗначениеВРеквизитФормы(ЭлементыКатегорийТаблица, "ЭлементыКатегорий");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИконкаКатегории(Наименование)
	
	Иконка = Неопределено;
	
	Если Метаданные.ОбщиеКартинки.Найти("ВыборОКВЭД" + Наименование) <> Неопределено Тогда
		Иконка = БиблиотекаКартинок["ВыборОКВЭД" + Наименование];
	КонецЕсли;
	
	Возврат Иконка;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьВыбор()
	
	РезультатВыбора = Неопределено;
	
	Если Элементы.СтраницыРазделы.ТекущаяСтраница = Элементы.СтраницаЭлементыКатегорий Тогда
		ТекущийСписок = Элементы.ЭлементыКатегорий;
	ИначеЕсли Элементы.СтраницыРазделы.ТекущаяСтраница = Элементы.СтраницаМоиВиды Тогда
		ТекущийСписок = Элементы.МоиВиды;
	Иначе
		ТекущийСписок = Элементы.Классификатор;
	КонецЕсли;
	
	ТекущаяСтрока = Неопределено;
	Если МножественныйВыбор Тогда
		РезультатВыбора = Новый Массив;
		Для Каждого ВыделеннаяСтрока Из ТекущийСписок.ВыделенныеСтроки Цикл
			ТекущаяСтрока = ТекущийСписок.ДанныеСтроки(ВыделеннаяСтрока);
			Если ТекущаяСтрока <> Неопределено Тогда
				РезультатВыбора.Добавить(Новый Структура("Код, Наименование", ТекущаяСтрока.Код, ТекущаяСтрока.Наименование));
			КонецЕсли;
		КонецЦикла;
	Иначе
		ТекущаяСтрока = ТекущийСписок.ТекущиеДанные;
		// При выборе передаем также имя раздела, чтобы при следующем открытии формы спозиционироваться на нужном разделе
		Если ТекущаяСтрока <> Неопределено Тогда
				
			РезультатВыбора = Новый Структура("Код, Наименование, Раздел", ТекущаяСтрока.Код, ТекущаяСтрока.Наименование, ТекущийРаздел);
			
		КонецЕсли;
	КонецЕсли;
	
	Если РезультатВыбора <> Неопределено Тогда
		Закрыть(РезультатВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	#Область ЖирныйШрифтРаздела
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Классификатор");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Классификатор.ЭтоРаздел",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ШрифтВыборОквэдОсновнойКод);
	
	#КонецОбласти
	
	#Область КрупныйШрифтИзбранноеВсеРазделы
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Разделы");
	
	ГруппаИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаИли,
		"Разделы.ЭтоКлассификатор",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаИли,
		"Разделы.ЭтоИзбранное",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ШрифтЗаголовкаИнформационнойПанели);
	
	#КонецОбласти
	
	#Область ВыделениеНайденных
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Классификатор");
	
	ГруппаИ = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаИ,
		"СтрокаПоиска",
		ВидСравненияКомпоновкиДанных.Заполнено);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаИ,
		"Классификатор.СодержитТекстПоиска",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветУспешногоПоиска);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Классификатор");
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"СтрокаПоиска",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Классификатор.ОтображатьВНайденном",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	#КонецОбласти
	
	#Область КолонкаИзбранное
	
	// Отключение видимости для множественного выбора
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КлассификаторИзбранное");
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЭлементыКатегорийИзбранное");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ОтображатьИзбранное",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатОбновленияОтображенияДанныхФормыСервер(ДанныеФормы)
		
	РезультатОбновленияОтображения = Новый Структура;
	РезультатОбновленияОтображения.Вставить("Описание", "");
	РезультатОбновленияОтображения.Вставить("ИсторияПросмотра", Новый СписокЗначений());
	
	Организация = ДанныеФормы.Организация;

	Если ДанныеФормы.ВидимостьОписания Тогда
		ЭтоИзбранное = Неопределено;
		ДанныеТекущейСтрокиКлассификатора = ДанныеФормы.ДанныеТекущейСтрокиКлассификатора;
		Если ДанныеФормы.ОтображатьИзбранное И ДанныеТекущейСтрокиКлассификатора.ДоступенДляВыбора = ДоступенДляВыбора() Тогда
			ЭтоИзбранное = ДанныеТекущейСтрокиКлассификатора.Избранное = 1;
		КонецЕсли;
		
		ПараметрыОписания = КлассификаторОКВЭДБП.НовыйПараметрыОписания();
		ПараметрыОписания.Код = ДанныеФормы.ТекущийКод;
		ПараметрыОписания.СсылкиДляВозврата = ДанныеФормы.СсылкиДляВозврата;
		ПараметрыОписания.ЭтоИзбранное = ЭтоИзбранное;
		ПараметрыОписания.АдресРезультатаПоиска = ДанныеФормы.АдресРезультатаПоиска;
		
		РезультатОбновленияОтображения.Описание =
			КлассификаторОКВЭДБП.HTMLТекстОписания(ПараметрыОписания);
	КонецЕсли;
	
	НоваяИстория = Новый СписокЗначений();
	Для Каждого ЭлементИстории Из ДанныеФормы.ИсторияПросмотра Цикл
		Если НоваяИстория.НайтиПоЗначению(ЭлементИстории.Значение) = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(НоваяИстория.Добавить(), ЭлементИстории);
		КонецЕсли;
		Если НоваяИстория.Количество() >= МаксимальноеКоличествоВИстории() Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	НастройкаФормыВыбораКодов = КлассификаторОКВЭДБП.НастройкаФормыВыбораКодов(Организация);
	НастройкаФормыВыбораКодов.ИсторияПросмотра = НоваяИстория;
	
	КлассификаторОКВЭДБП.СохранитьНастройкуФормыВыбораКодов(НастройкаФормыВыбораКодов, Организация);
	
	РезультатОбновленияОтображения.ИсторияПросмотра = НоваяИстория;

	Возврат РезультатОбновленияОтображения;
	
КонецФункции

&НаКлиенте
Процедура ПриВыбореСтрокиКода(ИмяТаблицы, ПолеВыбора)
	
	ТекущиеДанные = Элементы[ИмяТаблицы].ТекущиеДанные;
	
	Если ПолеВыбора.Имя = ИмяТаблицы + "Избранное" Тогда
		ИзменитьИзбранноеКлиент(ТекущиеДанные);
		Возврат;
	КонецЕсли;
		
	ЗавершитьВыбор();
	
КонецПроцедуры

#Область ДеревоКлассификатора

&НаКлиенте
Процедура Подключаемый_ОбновитьДанныеТекущейСтроки()

	Если ТекущийРаздел <> ИмяРазделаКлассификатор() Тогда
		Возврат;
	КонецЕсли;
		
	ТекущиеДанные = Элементы.Классификатор.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или ЗначениеЗаполнено(СтрокаПоиска) И Не ТекущиеДанные.ОтображатьВНайденном
		Или Не Элементы.Описание.Видимость Тогда
		Описание = ПустоеОписание();
		Возврат;
	КонецЕсли;
	
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("ИсторияПросмотра", ИсторияПросмотра);
	ДанныеФормы.Вставить("ОтображатьИзбранное", ОтображатьИзбранное);
	ДанныеТекущейСтрокиКлассификатора = Новый Структура("ДоступенДляВыбора, Избранное");
	ЗаполнитьЗначенияСвойств(ДанныеТекущейСтрокиКлассификатора, ТекущиеДанные);
	ДанныеФормы.Вставить("ДанныеТекущейСтрокиКлассификатора", ДанныеТекущейСтрокиКлассификатора);
	ДанныеФормы.Вставить("ТекущийКод", ТекущийКод);
	ДанныеФормы.Вставить("СсылкиДляВозврата", СсылкиДляВозврата);
	ДанныеФормы.Вставить("ВидимостьОписания", Элементы.Описание.Видимость);
	ДанныеФормы.Вставить("Организация", Организация);
	ДанныеФормы.Вставить("АдресРезультатаПоиска", АдресРезультатПоиска);
	
	РезультатОбновленияОтображения = РезультатОбновленияОтображенияДанныхФормыСервер(ДанныеФормы);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатОбновленияОтображения);
	
КонецПроцедуры

&НаКлиенте
Функция ПустоеОписание()
	
	Возврат "<html><head></head><body></body></html>";

КонецФункции

&НаСервере
Процедура ОбновитьДеревоКлассификатора()
	
	СКД = РегистрыСведений.ДанныеОКВЭД.ПолучитьМакет("Иерархия");
	Настройки = СКД.НастройкиПоУмолчанию;
	
	МенеджерВременныхТаблиц = КлассификаторОКВЭДБП.МенеджерВременныхТаблицРазделов();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СКД, Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет,,,,, МенеджерВременныхТаблиц);
	
	ДеревоИсточник = Новый ДеревоЗначений;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДеревоИсточник);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

	ДеревоПолучатель = РеквизитФормыВЗначение("Классификатор");
	ДеревоПолучатель.Строки.Очистить();
	
	ДополнительныеДанныеДерева = НовыйДополнительныеДанныеДерева();
	Если ОтображатьИзбранное Тогда
		ДополнительныеДанныеДерева.Избранное = СоответствиеИзСпискаЗначений(ИзбранноеВключено);
	КонецЕсли;
	ДополнительныеДанныеДерева.ЗапрещенныеКоды = СоответствиеИзСпискаЗначений(ЗапрещенныеКоды);
	ДополнительныеДанныеДерева.ПредставлениеРазделов = КлассификаторОКВЭДБП.ПредставлениеРазделов();
	
	ВывестиДерево(ДеревоИсточник, ДеревоПолучатель, "", ДополнительныеДанныеДерева);
	
	ЗначениеВРеквизитФормы(ДеревоПолучатель, "Классификатор");
	
	УстановитьТекущиеРазделСтрокаКлассификатора();
	
КонецПроцедуры

&НаСервере
Функция СоответствиеИзСпискаЗначений(СписокЗначений)
	СоответствиеИзСпискаЗначений = Новый Соответствие;
	Для Каждого ЭлементСписка Из СписокЗначений Цикл
		СоответствиеИзСпискаЗначений.Вставить(ЭлементСписка.Значение, Истина);
	КонецЦикла;
	Возврат СоответствиеИзСпискаЗначений;
КонецФункции

&НаСервере
Процедура ВывестиДерево(Источник, Получатель, Знач КодРодителя, ДополнительныеДанныеДерева)
	
	Для Каждого СтрокаИсточник Из Источник.Строки Цикл
		
		Если СтрокаИсточник.Ссылка = КодРодителя Тогда
			ЗаполнитьСтроку(Получатель, СтрокаИсточник, ДополнительныеДанныеДерева);
			Продолжить;
		КонецЕсли;
		
		СтрокаПолучатель = Получатель.Строки.Добавить();
		
		Если СтрокаИсточник.Строки.Количество() > 0 Тогда
			ВывестиДерево(
				СтрокаИсточник,
				СтрокаПолучатель,
				СтрокаИсточник.Ссылка,
				ДополнительныеДанныеДерева);
		Иначе
			ЗаполнитьСтроку(СтрокаПолучатель, СтрокаИсточник, ДополнительныеДанныеДерева);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтроку(Получатель, СтрокаИсточник, ДополнительныеДанныеДерева)
	
	ЗаполнитьЗначенияСвойств(Получатель, СтрокаИсточник);
	
	Код = СтрокаИсточник.Код;
	Получатель.ПредставлениеКода = Код;
	ПредставлениеКодаРаздела = ДополнительныеДанныеДерева.ПредставлениеРазделов[Код];
	
	Если ПредставлениеКодаРаздела <> Неопределено Тогда
		Получатель.ПредставлениеКода = ПредставлениеКодаРаздела;
		Получатель.ЭтоРаздел = Истина;
	КонецЕсли;
	
	ЗапрещенДляВыбора = СтрДлина(Код) <= 4 Или ДополнительныеДанныеДерева.ЗапрещенныеКоды[Код] <> Неопределено;
	Получатель.ДоступенДляВыбора = ?(ЗапрещенДляВыбора, 1, 2);
	ЭтоИзбранное = ДополнительныеДанныеДерева.Избранное[Код] <> Неопределено;
	
	Если ЗапрещенДляВыбора Тогда
		Получатель.Избранное = -1;
	ИначеЕсли ЭтоИзбранное Тогда
		Получатель.Избранное = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьСтрокиДереваРекурсивно(СтрокиДерева)

	Для Каждого СтрокаДерева Из СтрокиДерева Цикл

		ПодчиненныеСтроки = СтрокаДерева.ПолучитьЭлементы();
		
		Если ПодчиненныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СвернутьСтрокиДереваРекурсивно(ПодчиненныеСтроки);
		
		Элементы.Классификатор.Свернуть(СтрокаДерева.ПолучитьИдентификатор());
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьРезультатПоиска()
	
	РезультатПоиска = КлассификаторОКВЭДБП.РезультатПоискаПоСтроке(СтрокаПоиска);
	СписокКодов = РезультатПоиска.СписокКодов;
	
	АдресРезультатПоиска = ПоместитьВоВременноеХранилище(РезультатПоиска.ТекстыФорматирования, УникальныйИдентификатор);
	
	ДеревоКлассификатора = РеквизитФормыВЗначение("Классификатор");
	УстановитьОтображениеНайденныхСтрок(ДеревоКлассификатора.Строки, СписокКодов, Ложь);

	ЗначениеВРеквизитФормы(ДеревоКлассификатора, "Классификатор");
	
КонецПроцедуры

&НаСервере
Функция НовыйДополнительныеДанныеДерева()
	
	ДополнительныеДанныеДерева = Новый Структура;
	
	ДополнительныеДанныеДерева.Вставить("Избранное", Новый Соответствие()); // Ключ - Код, Значение - Булево
	ДополнительныеДанныеДерева.Вставить("ЗапрещенныеКоды", Новый Соответствие()); // Ключ - Код, Значение - Булево
	ДополнительныеДанныеДерева.Вставить("ПредставлениеРазделов", Новый Соответствие); // Ключ - Код, Значение - Строка
	
	Возврат ДополнительныеДанныеДерева;
КонецФункции

&НаСервере
Процедура УстановитьОтображениеНайденныхСтрок(СтрокиДерева, СписокКодов, ОтображатьВНайденном)
	
	ЕстьОтображенныеСтроки = Ложь;
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		
		УстановитьОтображениеНайденныхСтрок(СтрокаДерева.Строки, СписокКодов, ОтображатьВНайденном);
		ОтображатьВНайденном = ОтображатьВНайденном Или СписокКодов[СтрокаДерева.Код] <> Неопределено;
		СтрокаДерева.ОтображатьВНайденном = ОтображатьВНайденном;
		СтрокаДерева.СодержитТекстПоиска = СписокКодов[СтрокаДерева.Код] <> Неопределено;
		ЕстьОтображенныеСтроки = ЕстьОтображенныеСтроки Или ОтображатьВНайденном;
		ОтображатьВНайденном = Ложь;
		
	КонецЦикла;
	
	ОтображатьВНайденном = ЕстьОтображенныеСтроки;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторТекущегоКода(ЭлементыДерева, ТекущийКод)
	
	Для Каждого СтрокаДерева Из ЭлементыДерева Цикл
		Если СтрокаДерева.Код = ТекущийКод Тогда
			Возврат СтрокаДерева.ПолучитьИдентификатор();
		КонецЕсли;
		
		ПоискВПодстроках = ИдентификаторТекущегоКода(СтрокаДерева.ПолучитьЭлементы(), ТекущийКод);
		Если ПоискВПодстроках >= 0 Тогда
			Возврат ПоискВПодстроках;
		КонецЕсли;
	КонецЦикла;
	
	Возврат -1;
	
КонецФункции

#КонецОбласти

#Область ОписаниеКлассификатора
&НаСервере
Процедура ИзменитьВидимостьОписанияСервер()
	
	КлючОбъекта = КлассификаторОКВЭДБП.ИдентификаторНастройкиФормыВыборВидаДеятельности();
	КлючНастройки = "НастройкаВидимостиОписания";
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючОбъекта,
		КлючНастройки,
		Элементы.Описание.Видимость);
	УправлениеВидимостьюОписания(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеСсылки(ПолнаяСсылкаHTML)

	ЧастиСсылкиHTML   = СтрРазделить(ПолнаяСсылкаHTML, "/", Ложь);
	СсылкаHTML        = ЧастиСсылкиHTML.Получить(ЧастиСсылкиHTML.Количество() - 1);
	
	ИдентификаторыГиперссылок = КлассификаторОКВЭДБПКлиентСервер.ИдентификаторыГиперссылок();
	
	ЭтоПрямойПереход = СтрНачинаетсяС(СсылкаHTML, ИдентификаторыГиперссылок.ПереходПоСсылке) <> 0;
	ЭтоВозврат = СтрНачинаетсяС(СсылкаHTML, ИдентификаторыГиперссылок.ВозвратПоСсылке) <> 0 ;
	ЭтоИзбранное = СтрНачинаетсяС(СсылкаHTML, ИдентификаторыГиперссылок.ИзменитьИзбранное) <> 0 ;
	
	Если ЭтоПрямойПереход Тогда
		
		ТекущийКод = СтрЗаменить(СсылкаHTML, ИдентификаторыГиперссылок.ПереходПоСсылке, "");

	ИначеЕсли ЭтоВозврат Тогда
				
		ТекущийКод = СтрЗаменить(СсылкаHTML, ИдентификаторыГиперссылок.ВозвратПоСсылке, "");
	
	ИначеЕсли ЭтоИзбранное Тогда
		
		ТекущийКод = СтрЗаменить(СсылкаHTML, ИдентификаторыГиперссылок.ИзменитьИзбранное, "");
		
	КонецЕсли;
	
	Если ТекущийКод = СсылкаHTML Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = ИдентификаторТекущегоКода(Классификатор.ПолучитьЭлементы(), ТекущийКод);
	ДанныеСтроки = Классификатор.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоИзбранное Тогда
		
		ИзменитьИзбранноеКлиент(ДанныеСтроки);
		
		Возврат;
		
	КонецЕсли;

	Если ЭтоПрямойПереход Тогда
		
		СсылкиДляВозврата.Добавить(ДанныеСтроки.Код, ДанныеСтроки.Наименование);

	ИначеЕсли ЭтоВозврат Тогда
		
		СсылкиДляВозврата.Удалить(СсылкиДляВозврата.Количество() - 1);
		
	КонецЕсли;
	
	Элементы.Классификатор.ТекущаяСтрока = ТекущаяСтрока;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюОписания(Форма)
	
	Элементы = Форма.Элементы;
	
	ОписаниеОтображено = Элементы.Описание.Видимость;
	
	ЗаголовокУправленияОписанием = НСтр("ru = 'Показать описание'");
	Если ОписаниеОтображено Тогда
		ЗаголовокУправленияОписанием = НСтр("ru = 'Скрыть описание'");
	КонецЕсли;
	
	Элементы.ИзменитьВидимостьОписания.Заголовок = ЗаголовокУправленияОписанием;
	Элементы.ИзменитьВидимостьОписания.Пометка = ОписаниеОтображено;

КонецПроцедуры

#КонецОбласти

#Область Избранное

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииИзбранного()
	ПриАктивизацииИзбранногоСервер();
КонецПроцедуры

&НаСервере
Процедура ПриАктивизацииИзбранногоСервер()
	
	ПеренестиИзбранноеИзКлассификатора();
	
	Отбор = Новый Структура("Код", ТекущийКод);
	РезультатПоиска = МоиВиды.НайтиСтроки(Отбор);
	Если РезультатПоиска.Количество() > 0 Тогда
		Элементы.МоиВиды.ТекущаяСтрока = РезультатПоиска[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НастроитьСписокИзбранных()

	НастройкаФормы = КлассификаторОКВЭДБП.НастройкаФормыВыбораКодов(Организация);
	ИзбранноеВключено = НастройкаФормы.ИзбранноеВключено;
	ИзбранноеИсключено = НастройкаФормы.ИзбранноеИсключено;
	КодыНеИсключенныеИзИзбранного =
		ОбщегоНазначенияКлиентСервер.РазностьМассивов(ИзбранноеВключено.ВыгрузитьЗначения(), ИзбранноеИсключено.ВыгрузитьЗначения());
	ИзбранноеВключено.ЗагрузитьЗначения(КодыНеИсключенныеИзИзбранного);
	
	Возврат НастройкаФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыИзбранного(Форма)
	
	ПараметрыИзбранного = Новый Структура;
	
	ПараметрыИзбранного.Вставить("Код", "");
	ПараметрыИзбранного.Вставить("Избранное", -1);
	ПараметрыИзбранного.Вставить("Организация", Форма.Организация);
	ПараметрыИзбранного.Вставить("ИзбранноеВключено", Форма.ИзбранноеВключено);
	ПараметрыИзбранного.Вставить("ИзбранноеИсключено", Форма.ИзбранноеИсключено);
	
	Возврат ПараметрыИзбранного;
	
КонецФункции

&НаСервере
Процедура ПеренестиИзбранноеИзКлассификатора()
	МоиВиды.Очистить();
	
	ДеревоКлассификатора = РеквизитФормыВЗначение("Классификатор");
	ПеренестиСтрокиДереваВТаблицуЗначений(ДеревоКлассификатора, МоиВиды);
КонецПроцедуры

&НаСервере
Процедура ПеренестиСтрокиДереваВТаблицуЗначений(Дерево, ТаблицаЗначений)
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		Если СтрокаДерева.Избранное = 1 Тогда
			НоваяСтрока = ТаблицаЗначений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДерева);
		КонецЕсли;
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			ПеренестиСтрокиДереваВТаблицуЗначений(СтрокаДерева, ТаблицаЗначений);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонтекстноеМеню()
	
	Если Не ОтображатьИзбранное Тогда
		Возврат;
	КонецЕсли;

	ВидимостьДобавить = Ложь;
	ВидимостьИсключить = Ложь;
	ВидимостьСвернутьРазвернуть = Ложь;
	
	ПрефиксЭлемента = "";
	Если ТекущийРаздел = ИмяРазделаКлассификатор() Тогда
		
		ИсточникКоманд = Элементы.Классификатор.ТекущиеДанные;
		
		Если ИсточникКоманд = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВидимостьСвернутьРазвернуть = ИсточникКоманд.ДоступенДляВыбора <> ДоступенДляВыбора();
			
		Если Элементы.КлассификаторКонтекстноеМенюСвернуть.Видимость <> ВидимостьСвернутьРазвернуть Тогда
			Элементы.КлассификаторКонтекстноеМенюСвернуть.Видимость = ВидимостьСвернутьРазвернуть;
			Элементы.КлассификаторКонтекстноеМенюРазвернуть.Видимость = ВидимостьСвернутьРазвернуть;
		КонецЕсли;
		
		ПрефиксЭлемента = "Классификатор";
		
	Иначе
		
		ИсточникКоманд = Элементы.ЭлементыКатегорий.ТекущиеДанные;
		Если ИсточникКоманд = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПрефиксЭлемента = "ЭлементыКатегорий";
		
	КонецЕсли;
	
	Если ИсточникКоманд.Избранное = 0 Тогда
		ВидимостьДобавить = Истина;
	ИначеЕсли ИсточникКоманд.Избранное = 1 Тогда
		ВидимостьИсключить = Истина;
	КонецЕсли;
	
	ИмяКонтекстноеМенюДобавить = ПрефиксЭлемента + "КонтекстноеМенюДобавитьВИзбранное";
	ИмяКонтекстноеМенюИсключить = ПрефиксЭлемента + "КонтекстноеМенюИсключитьИзИзбранного";
	Если Элементы[ИмяКонтекстноеМенюДобавить].Видимость <> ВидимостьДобавить Тогда
		Элементы[ИмяКонтекстноеМенюДобавить].Видимость = ВидимостьДобавить;
	КонецЕсли;
	Если Элементы[ИмяКонтекстноеМенюИсключить].Видимость <> ВидимостьИсключить Тогда
		Элементы[ИмяКонтекстноеМенюИсключить].Видимость = ВидимостьИсключить;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИзбранноеКлиент(ТекущиеДанные)
	
	ТекущийКод = ТекущиеДанные.Код;
	ПараметрыИзбранного = ПараметрыИзбранного(ЭтотОбъект);
	
	ПараметрыИзбранного.Код = ТекущиеДанные.Код;
	ПараметрыИзбранного.Избранное = ТекущиеДанные.Избранное;
	
	ИзменитьИзбранноеНаСервере(ПараметрыИзбранного);
	
	ИзбранноеВключено = ПараметрыИзбранного.ИзбранноеВключено;
	ИзбранноеИсключено = ПараметрыИзбранного.ИзбранноеИсключено;
	ТекущиеДанные.Избранное = ПараметрыИзбранного.Избранное;
	ЭлементИзбранного = ИсторияПросмотра.НайтиПоЗначению(ПараметрыИзбранного.Код);
	
	Если ЭлементИзбранного <> Неопределено Тогда
		ЭлементИзбранного.Картинка = КартинкаИзбранного(ТекущиеДанные.Избранное);
	КонецЕсли;
	
	Если ТекущийРаздел = ИмяРазделаКлассификатор() Тогда
		
		ОбновитьКонтекстноеМеню();
		
		// Таблица МоиВиды обновляется при активизации соответствующего раздела, поэтому изменение требуется только в категориях.
		ИзменитьИзбранноеВСвязаннойТаблице(ЭлементыКатегорий, ТекущиеДанные.Код);
		
		Если Элементы.Описание.Видимость Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеТекущейСтроки", 0.1, Истина);
		КонецЕсли;
		
	Иначе
		Если ТекущийРаздел <> ИмяРазделаМоиВиды() Тогда
			ОбновитьКонтекстноеМеню();
		Иначе
			ИзменитьИзбранноеВСвязаннойТаблице(ЭлементыКатегорий, ТекущиеДанные.Код);
		КонецЕсли;
		ТекущаяСтрока = ИдентификаторТекущегоКода(Классификатор.ПолучитьЭлементы(), ТекущийКод);
		ДанныеСтроки = Классификатор.НайтиПоИдентификатору(ТекущаяСтрока);
		ДанныеСтроки.Избранное = ПараметрыИзбранного.Избранное;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИзбранноеВСвязаннойТаблице(ТаблицаФормы, Код)
	
	Отбор = Новый Структура("Код", Код);
	
	СтрокиТаблицы = ТаблицаФормы.НайтиСтроки(Отбор);
	Если СтрокиТаблицы.Количество() > 0 Тогда
		СтрокиТаблицы[0].Избранное = Не СтрокиТаблицы[0].Избранное;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьИзбранноеНаСервере(ПараметрыИзбранного)
	
	Если Не ЗначениеЗаполнено(ПараметрыИзбранного.Код) Тогда
		Возврат;
	КонецЕсли;
	
	Действие = "";
	Если ПараметрыИзбранного.Избранное = 1 Тогда
		// Было 1, меняем на 0
		Действие = ДействиеИсключить();
		ПараметрыИзбранного.Избранное = 0;
	ИначеЕсли ПараметрыИзбранного.Избранное = 0 Тогда
		// Было 0, меняем на 1
		Действие = ДействиеВключить();
		ПараметрыИзбранного.Избранное = 1;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Действие) Тогда
		Возврат;
	КонецЕсли;
	
	СписокДобавить = ПараметрыИзбранного.ИзбранноеВключено;
	СписокСократить = ПараметрыИзбранного.ИзбранноеИсключено;
	
	Если Действие = ДействиеИсключить() Тогда
		СписокДобавить = ПараметрыИзбранного.ИзбранноеИсключено;
		СписокСократить = ПараметрыИзбранного.ИзбранноеВключено;
	КонецЕсли;
	
	Если СписокДобавить.НайтиПоЗначению(ПараметрыИзбранного.Код) = Неопределено Тогда
		СписокДобавить.Добавить(ПараметрыИзбранного.Код);
	КонецЕсли;
	
	СокращаемыйЭлемент = СписокСократить.НайтиПоЗначению(ПараметрыИзбранного.Код);
	Если СокращаемыйЭлемент <> Неопределено Тогда
		СписокСократить.Удалить(СокращаемыйЭлемент);
	КонецЕсли;
	
	НастройкаФормы = КлассификаторОКВЭДБП.НовыйНастройкиФормыВыборВидаДеятельности();
	
	НастройкаФормы.ИзбранноеВключено = ПараметрыИзбранного.ИзбранноеВключено;
	НастройкаФормы.ИзбранноеИсключено = ПараметрыИзбранного.ИзбранноеИсключено;
	
	КлассификаторОКВЭДБП.СохранитьНастройкуФормыВыбораКодов(НастройкаФормы, ПараметрыИзбранного.Организация);
	
КонецПроцедуры

&НаКлиенте
Функция КартинкаИзбранного(Избранное)
	Картинка = БиблиотекаКартинок.ВыборОКВЭДМои;
	Если Избранное = 1 Тогда
		Картинка = БиблиотекаКартинок.ЗвездаИзбранноеЖелтая;
	КонецЕсли;
	Возврат Картинка;
КонецФункции

#КонецОбласти

#Область История

&НаКлиенте
Процедура УстановитьВидимостьИстории()
	Если Элементы.Описание.Видимость И Не Элементы.ИсторияПросмотренных.Видимость И ОтображатьИзбранное Тогда
		Элементы.ИсторияПросмотренных.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьИсториюПросмотренных()
	
	Результат = Ждать ВыбратьИзМенюАсинх(ИсторияПросмотра);
	Если Результат <> Неопределено Тогда
		ВыбратьИсториюСервер(Результат.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьИсториюСервер(Код)

	Элементы.Классификатор.ТекущаяСтрока = ИдентификаторТекущегоКода(Классификатор.ПолучитьЭлементы(),Код);
	
КонецПроцедуры

#КонецОбласти

#Область ФиксированныеИменаЗначения

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРазделаКлассификатор()
	Возврат НСтр("ru = 'Все виды деятельности'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРазделаМоиВиды()
	Возврат НСтр("ru = 'Мои виды деятельности'");
КонецФункции

&НаСервереБезКонтекста
Функция ДействиеИсключить()
	Возврат "Исключить";
КонецФункции

&НаСервереБезКонтекста
Функция ДействиеВключить()
	Возврат "Включить";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДоступенДляВыбора()
	Возврат 2;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МаксимальноеКоличествоВИстории()
	Возврат 10;
КонецФункции

#КонецОбласти

#Область ОбновленииеИзСервисаКлассификаторов

&НаКлиенте
Процедура ПроверитьОбновленияКлассификатора() Экспорт
	
	ДлительнаяОперация = НачатьПроверкуОбновленияНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиОбновления", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция НачатьПроверкуОбновленияНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов()));
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатПроверкиОбновления(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияОбОбновлениях = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ДоступноОбновление = ИнформацияОбОбновлениях.ДоступныеВерсии.Количество() > 0;
	
	Элементы.ГруппаДоступноОбновление.Видимость = ДоступноОбновление;
	
	ДлительнаяОперация = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура НачатьОбновлениеДанныхСписка()
	
	ТребуетсяПодключениеИнтернетПоддержки = Ложь;
	
	// В режиме сервиса или АРМ данные обновляются менеджером сервиса, т.к. это общие данные
	Если ОбщегоНазначения.РазделениеВключено() Или ПолучитьФункциональнуюОпцию("РаботаВАвтономномРежиме") Тогда
		Возврат;
	КонецЕсли;
	
	Классификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов());
	
	ПроверкаАктуальностиДанныхСписка = РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов(Классификаторы);
	
	Если ЗначениеЗаполнено(ПроверкаАктуальностиДанныхСписка.КодОшибки) Тогда
		
		Если ПроверкаАктуальностиДанныхСписка.КодОшибки = "НеверныйЛогинИлиПароль" Тогда
			// Перед следующей попыткой загрузки запросим данные подключения к ИПП 
			ТребуетсяПодключениеИнтернетПоддержки = Истина;
		Иначе	
			ШаблонСообщения = НСтр("ru = 'При загрузке классификатора ОКВЭД2 из сервиса классификаторов возникла ошибка:
				|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПроверкаАктуальностиДанныхСписка.ИнформацияОбОшибке);
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Обновление классификатора ОКВЭД2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.ДанныеОКВЭД, ,
				ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если ПроверкаАктуальностиДанныхСписка.ДоступныеВерсии.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Загружен актуальный классификатор ОКВЭД2'"));
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация <> Неопределено Тогда
		// Не завершено предыдущее фоновое задание, новое не запускаем.
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение           = 0; // Не ждем завершения.
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление классификатора ОКВЭД2'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"КлассификаторОКВЭДБП.ЗагрузитьДанныеИзСервисаКлассификаторов",
		Новый Структура(),
		ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеОбновленияДанныхСписка()

	Если ДлительнаяОперация = Неопределено Тогда
		// Фоновое задание не запущено или обновление не требуется.
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru='Идет загрузка классификатора ОКВЭД2.'");
	
	ОповещенияОЗавершении = Новый ОписаниеОповещения("ОбновлениеДанныхСпискаЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещенияОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеДанныхСпискаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Успешно = Истина;
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус", "") = "Ошибка" Тогда
		Успешно = Ложь;
		ШаблонСообщения = НСтр("ru = 'При загрузке классификатора ОКВЭД2 из сервиса классификаторов возникла ошибка:
			|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Результат.КраткоеПредставлениеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ДлительнаяОперация = Неопределено; // Сбросим признак выполнения.
	
	Если Успешно Тогда
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДанныеОКВЭД"));
		Элементы.ГруппаДоступноОбновление.Видимость = Ложь;
		ОбновитьДеревоКлассификатора();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОтветеНаВопросПодключитьИнтернетПоддержку(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Если КодВозврата = КодВозвратаДиалога.Да Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЭтотОбъект);	
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		// Получаем классификатор и обновляем данные на форме.
		НачатьОбновлениеДанныхСписка();
		ОжидатьЗавершениеОбновленияДанныхСписка();
		
		ТребуетсяПодключениеИнтернетПоддержки = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
