
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.АдресПараметровВХранилище) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// "Распаковываем" параметры
	Если Параметры.Свойство("ТребуетсяПересчет") Тогда
		ТребуетсяПересчет = Параметры.ТребуетсяПересчет;
	КонецЕсли;
	
	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);
	ДесериализованныйОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПараметрыРасчета.СериализованныйОбъект);
	ИмяТипа = ДесериализованныйОбъект.Метаданные().Имя;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасчета, "Организация, Сотрудник, ФизическоеЛицо, МесяцНачисления");
	
	ДобавитьРеквизитФормыОбъект();
	
	ЗначениеВДанныеФормы(ДесериализованныйОбъект, ЭтотОбъект["Объект"]);
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	СписокКадровыхДанных = "ФамилияИО";
	КадровыеДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическиеЛица, СписокКадровыхДанных, МесяцНачисления);
	
	ПредставлениеСотрудника = КадровыеДанныеФизЛиц[0].ФамилияИО;
	Заголовок = СтрШаблон(НСтр("ru='Страховые взносы (%1)'"), ПредставлениеСотрудника);
	
	ЗаполнитьТаблицуВзносов();
	ПолучитьИтоги(ЭтотОбъект);
	
	УстановитьСвойстваЭлементовФормы();
	
	РассчитыватьДокументыПриРедактировании = Константы.РассчитыватьДокументыПриРедактировании.Получить()
		Или ИмяТипа = "Увольнение";
	Если НЕ РассчитыватьДокументыПриРедактировании Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПересчитать",
			"Видимость",
			ТребуетсяПересчет И НЕ КорректироватьРасчет);
	КонецЕсли;
	
	Элементы.ГруппаКнопокПросмотр.Видимость       = ТолькоПросмотр;
	Элементы.ГруппаКнопокРедактирование.Видимость = НЕ ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаОК.КнопкаПоУмолчанию      = Истина;
	КонецЕсли;
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	
	УстановитьФункциональныеОпцииФормы();
	
	УправлениеПризнакомКорректировки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИдентификаторВладельца = ВладелецФормы.УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ВопросСохранитьИзменения(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КорректироватьРасчетПриИзменении(Элемент)
	
	УправлениеПризнакомКорректировки();
	Если НЕ КорректироватьРасчет Тогда
		Если РассчитыватьДокументыПриРедактировании Тогда
			ПересчитатьДокументНаКлиенте();
		Иначе
			ТребуетсяПересчет = Истина;
			УстановитьВидимостьГруппыПересчет();
		КонецЕсли;
	Иначе
		Если НЕ РассчитыватьДокументыПриРедактировании Тогда
			ТребуетсяПересчет = Ложь;
			УстановитьВидимостьГруппыПересчет();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыВзносы

&НаКлиенте
Процедура ВзносыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НЕ Копирование И НоваяСтрока Тогда
		ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
		ДанныеТекущейСтроки.ФизическоеЛицо = ФизическоеЛицо;
		ДанныеТекущейСтроки.ДатаПолученияДохода = КонецМесяца(МесяцНачисления);
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ДанныеТекущейСтроки, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПолучитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПослеУдаления(Элемент)
	
	ПолучитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойПриИзменении(ДанныеТекущейСтроки, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойНачалоВыбора(ЭтотОбъект, ДанныеТекущейСтроки, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойРегулирование(
		Элементы.Взносы.ТекущиеДанные, 
		"ДатаПолученияДохода", 
		"ДатаПолученияДоходаСтрокой",
		Направление, 
		Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ Модифицированность ИЛИ ТолькоПросмотр Тогда
		Закрыть();
	ИначеЕсли ПроверитьЗаполнениеРеквизитов() Тогда
		ПеренестиИзмененияВОбъектФормыВладельца();
		Закрыть(АдресВХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КарточкаУчетаПоСтраховымВзносам(Команда)
	
	Если НЕ ПроверитьЗаполнениеРеквизитов() Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ВопросСохранитьИзменения(Ложь);
	Иначе
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокумент(Команда)
	
	ПересчитатьДокументНаКлиенте();
	ТребуетсяПересчет = Ложь;
	УстановитьВидимостьГруппыПересчет();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьРеквизитФормыОбъект()
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Объект", Новый ОписаниеТипов("ДокументОбъект." + ИмяТипа)));
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	УчетСтраховыхВзносов.УстановитьВидимостьКолонокТаблицыСтраховыхВзносов(ЭтотОбъект, МесяцНачисления);
	
	Если ЗарплатаКадрыПереопределяемый.ПлательщикТолькоЕНВД(Организация, МесяцНачисления) Тогда
		// если применяется только ЕНВД
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРДоПредельнойВеличины",
			"Видимость",
			Взносы.Итог("ПФРДоПредельнойВеличины") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРСПревышения",
			"Видимость",
			Взносы.Итог("ПФРСПревышения") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРПоСуммарномуТарифу",
			"Видимость",
			Взносы.Итог("ПФРПоСуммарномуТарифу") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРНакопительная",
			"Видимость",
			Взносы.Итог("ПФРНакопительная") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРСтраховая",
			"Видимость",
			Взносы.Итог("ПФРСтраховая") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыФСС",
			"Видимость",
			Взносы.Итог("ФСС") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыФФОМС",
			"Видимость",
			Взносы.Итог("ФФОМС") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыТФОМС",
			"Видимость",
			Взносы.Итог("ТФОМС") <> 0);
			
	ИначеЕсли МесяцНачисления >= УчетЕНВДКлиентСервер.ДатаОтменыЕНВД() Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРДоПредельнойВеличиныЕНВД",
			"Видимость",
			Взносы.Итог("ПФРДоПредельнойВеличиныЕНВД") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРСПревышенияЕНВД",
			"Видимость",
			Взносы.Итог("ПФРСПревышенияЕНВД") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРПоСуммарномуТарифуЕНВД",
			"Видимость",
			Взносы.Итог("ПФРПоСуммарномуТарифуЕНВД") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРНакопительнаяЕНВД",
			"Видимость",
			Взносы.Итог("ПФРНакопительнаяЕНВД") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыПФРСтраховаяЕНВД",
			"Видимость",
			Взносы.Итог("ПФРСтраховаяЕНВД") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыФССЕНВД",
			"Видимость",
			Взносы.Итог("ФССЕНВД") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыФФОМСЕНВД",
			"Видимость",
			Взносы.Итог("ФФОМСЕНВД") <> 0);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВзносыТФОМСЕНВД",
			"Видимость",
			Взносы.Итог("ТФОМСЕНВД") <> 0);
			
		Если МесяцНачисления >= УчетЗарплаты.ДатаПереходаНаЕдиныйТарифСтраховыхВзносов() Тогда
				
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ВзносыПФРПоСуммарномуТарифу",
				"Видимость",
				Взносы.Итог("ПФРПоСуммарномуТарифу") <> 0);
				
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ВзносыПФРНакопительная",
				"Видимость",
				Взносы.Итог("ПФРНакопительная") <> 0);
				
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ВзносыПФРСтраховая",
				"Видимость",
				Взносы.Итог("ПФРСтраховая") <> 0);
				
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ВзносыФСС",
				"Видимость",
				Взносы.Итог("ФСС") <> 0);
				
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ВзносыФФОМС",
				"Видимость",
				Взносы.Итог("ФФОМС") <> 0);
				
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ВзносыТФОМС",
				"Видимость",
				Взносы.Итог("ТФОМС") <> 0);
				
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеПризнакомКорректировки()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Взносы",
		"ТолькоПросмотр",
		Не КорректироватьРасчет);
		
	Если ИмяТипа <> "НачислениеЗарплаты" Тогда
		ЭтотОбъект["Объект"].КорректироватьВзносы = КорректироватьРасчет;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВзносов()
	
	Взносы.Очистить();
	
	МассивСтрок = ЭтотОбъект["Объект"].Взносы.НайтиСтроки(Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
	
	Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
		ЗаполнитьЗначенияСвойств(Взносы.Добавить(), СтрокаМассива);
		Если СтрокаМассива.ФиксРасчет Тогда
			КорректироватьРасчет = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПолучитьИтоги(Форма)
	
	ПоляВзносов = СтрРазделить(УчетСтраховыхВзносовКлиентСервер.РассчитываемыеВзносы(), ",");
	
	Для Каждого ИмяПоля ИЗ ПоляВзносов Цикл
		Форма["Итого" + ИмяПоля] = Форма.Взносы.Итог(ИмяПоля);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВзносыСотрудника()
	
	Если КорректироватьРасчет Тогда
		// Не пересчитываем
		Возврат;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
	
	МассивСтрок = ЭтотОбъект["Объект"].Взносы.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
		СтрокаМассива.ФиксРасчет = Ложь;
	КонецЦикла;
	
	Если ИмяТипа = "НачислениеЗарплаты" Тогда
		Документы[ИмяТипа].ПересчитатьВзносы(ЭтотОбъект["Объект"], ФизическоеЛицо);
	Иначе
		Документы[ИмяТипа].ПересчитатьВзносы(ЭтотОбъект["Объект"], РеквизитФормыВЗначение("Объект"));
	КонецЕсли;
	ЗаполнитьТаблицуВзносов();
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	ПолучитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокументНаКлиенте()
	
	ОбновитьВзносыСотрудника();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Взносы",
		"Доступность",
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьГруппыПересчет()
	
	Если РассчитыватьДокументыПриРедактировании Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПересчитать",
		"Видимость",
		ТребуетсяПересчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзмененияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ПроверитьЗаполнениеРеквизитов() Тогда
			ПеренестиИзмененияВОбъектФормыВладельца();
			Если ДополнительныеПараметры.Закрытие Тогда
				Закрыть(АдресВХранилище);
			Иначе
				ВладелецФормы.РедактированиеВзносовЗавершение(АдресВХранилище);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Если ДополнительныеПараметры.Закрытие Тогда
			Модифицированность = Ложь;
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ДополнительныеПараметры.Закрытие
		И НЕ Результат = КодВозвратаДиалога.Отмена Тогда
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитов()
	
	ПроверкаПройдена = Истина;
	Для Каждого СтрокаТаблицы ИЗ Взносы Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаПолученияДоходаСтрокой) Тогда
			ИндексСтроки = Взносы.Индекс(СтрокаТаблицы);
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",
				"Заполнение",
				"ДатаПолученияДоходаСтрокой",
				ИндексСтроки + 1,
				"Взносы");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Взносы[" + ИндексСтроки + "].ДатаПолученияДоходаСтрокой");
			ПроверкаПройдена = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПроверкаПройдена;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчет()
	
	КарточкаУчета = ВладелецФормы.КарточкаУчетаПоСтраховымВзносамНаСервере(ФизическоеЛицо);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.КарточкаУчетаПоСтраховымВзносам",
		"КарточкаУчетаПоСтраховымВзносамПодробнее", 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтотОбъект["Объект"].Ссылка),
		ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент, ЗаголовокФормы",
			КарточкаУчета, НСтр("ru='Карточка учета по страховым взносам'")));
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзменения(Закрытие)
	
	ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
	Оповещение = Новый ОписаниеОповещения("ВопросСохранитьИзмененияЗавершение", ЭтотОбъект, Новый Структура("Закрытие", Закрытие));
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("Организация", Организация);
	ПараметрыФО.Вставить("Период",      НачалоДня(МесяцНачисления));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры

#Область ПереносВОсновнуюФорму

&НаКлиенте
Процедура ПеренестиИзмененияВОбъектФормыВладельца()
	
	Если Модифицированность Тогда
		Оповестить("ИзмененыРезультатыРасчетаВзносы", ПоместитьИзмененныеДанныеВоВременноеХранилище(), ЭтотОбъект);
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьИзмененныеДанныеВоВременноеХранилище()
	
	ВозвращаемыеСведения = Новый Структура;
	
	ДанныеВзносов = Взносы.Выгрузить();
	ДанныеВзносов.ЗаполнитьЗначения(КорректироватьРасчет, "ФиксРасчет");
	ДанныеВзносов.ЗаполнитьЗначения(ФизическоеЛицо,       "ФизическоеЛицо");
	
	ВозвращаемыеСведения.Вставить("Взносы",               ДанныеВзносов);
	ВозвращаемыеСведения.Вставить("ФизическоеЛицо",       ФизическоеЛицо);
	ВозвращаемыеСведения.Вставить("ТребуетсяПересчет",    ТребуетсяПересчет);
	ВозвращаемыеСведения.Вставить("КорректироватьРасчет", КорректироватьРасчет);
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(ВозвращаемыеСведения, ИдентификаторВладельца);
	Возврат АдресВХранилище;
	
КонецФункции

#КонецОбласти

#КонецОбласти
