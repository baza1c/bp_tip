#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидыПремий          = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	ВидыГодовыхПремий   = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	ВидыПремийПроцентом = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремииПроцентом());
	
	УчитыватьПремии = Ложь;
	Если ЗначениеЗаполнено(Параметры.ДанныеНачислений) Тогда
		УчитыватьПремии =
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.ДанныеНачислений[0], "СоставнаяЧасть", Неопределено) <> Неопределено;
	КонецЕсли;
	Для Каждого СтрокаНачислений Из Параметры.ДанныеНачислений Цикл
		Если УчитыватьПремии И ВидыПремий.Найти(СтрокаНачислений.СоставнаяЧасть) <> Неопределено Тогда
			Если ВидыГодовыхПремий.Найти(СтрокаНачислений.СоставнаяЧасть) <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(СреднийЗаработокГодовыеПремии.Добавить(), СтрокаНачислений);
			Иначе
				ЗаполнитьЗначенияСвойств(СреднийЗаработокПремии.Добавить(), СтрокаНачислений);
				Если ВидыПремийПроцентом.Найти(СтрокаНачислений.СоставнаяЧасть) <> Неопределено Тогда
					ПремияПроцентом = ПремияПроцентом + СтрокаНачислений.Сумма;
				Иначе
					ПремияФиксированнойСуммой = ПремияФиксированнойСуммой + СтрокаНачислений.Сумма;
				КонецЕсли;
			КонецЕсли
		Иначе
			ЗаполнитьЗначенияСвойств(СреднийЗаработок.Добавить(), СтрокаНачислений);
		КонецЕсли;
	КонецЦикла;
	
	Период = Параметры.Период;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПремииГруппа",
		"Видимость",
		УчитыватьПремии);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаКнопокПросмотр",
		"Видимость",
		ТолькоПросмотр);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаКнопокРедактирование",
		"Видимость",
		Не ТолькоПросмотр);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаЗакрыть",
		"КнопкаПоУмолчанию",
		ТолькоПросмотр);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаОК",
		"КнопкаПоУмолчанию",
		Не ТолькоПросмотр);
		
	Элементы.ПремииГруппа.Заголовок = СтрШаблон(НСтр("ru = 'Премии за %1 г.'"), Формат(Параметры.Период,"ДФ='MMMM yyyy'"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПремияПроцентомПриИзменении(Элемент)
	
	ПремияПриИзменении("ПремияПроцентом");
	
КонецПроцедуры

&НаКлиенте
Процедура ПремияСуммойПриИзменении(Элемент)
	
	ПремияПриИзменении("ПремияФиксированнойСуммой");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	Закрыть(ДанныеНачислений());
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеНачислений()
	
	МассивСреднегоЗаработка = ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработок.Выгрузить());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСреднегоЗаработка,
		ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработокПремии.Выгрузить()));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСреднегоЗаработка,
		ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработокГодовыеПремии.Выгрузить()));
	
	Возврат МассивСреднегоЗаработка;
	
КонецФункции

&НаКлиенте
Процедура ПремияПриИзменении(СоставнаяЧастьИмя)
	
	СоставнаяЧасть = ПредопределенноеЗначение("Перечисление.УчетНачисленийВСреднемЗаработкеОбщий." + СоставнаяЧастьИмя);
	СтрокиПремии = СреднийЗаработокПремии.НайтиСтроки(Новый Структура("СоставнаяЧасть", СоставнаяЧасть));
	
	Если СтрокиПремии.Количество() > 1 Тогда
		Для Каждого Строка Из СтрокиПремии Цикл
			СреднийЗаработокПремии.Удалить(Строка);
		КонецЦикла;
		СтрокиПремии.Очистить();
	КонецЕсли;
	Если СтрокиПремии.Количество() = 0 Тогда
		ТекущаяСтрока = СреднийЗаработокПремии.Добавить();
		ТекущаяСтрока.Период            = Период;
		ТекущаяСтрока.КоличествоМесяцев = 1;
		ТекущаяСтрока.ДатаНачалаБазовогоПериода = Период;
	Иначе
		ТекущаяСтрока = СтрокиПремии[0];
	КонецЕсли;
	ТекущаяСтрока.СоставнаяЧасть = СоставнаяЧасть;
	ТекущаяСтрока.Сумма = ЭтотОбъект[СоставнаяЧастьИмя];
	
КонецПроцедуры


#КонецОбласти

