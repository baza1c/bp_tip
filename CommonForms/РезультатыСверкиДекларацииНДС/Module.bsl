#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользуетсяСервисСверкиРасчетов = Истина;
	ОтображатьСверку                 = Истина;

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ДекларацияПоНДС,Организация,НачалоПериода,КонецПериода");
	ДанныеДляСверкиДеклараций = ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаДанныеДляСверкиДеклараций);
	АдресСтраницРазделов      = ДанныеДляСверкиДеклараций.СтраницыРазделов;
	
	ДанныеДляСверкиДеклараций.Вставить("ЕстьРасхождения", Ложь);
	ДанныеДляСверкиДеклараций.Вставить("ЕстьНеСверенные", Ложь);
	ДанныеДляСверкиДеклараций.Вставить("ПроверкаВыполнена", Ложь);
	ДанныеДляСверкиДеклараций.Вставить("НеУдалосьВыполнитьСверку", Ложь);
	
	Заголовок = СтрШаблон(НСтр("ru = 'Результат сверки сервисом 1С:Сверка 2.0 (%1)'"), Параметры.ПредставлениеОтчета);
	
	ОбновитьДатуПолученияДанных(Ложь);
	СервисСверкиРасчетовСверкаДекларацийНДС.НастроитьОтображениеРазделов(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПолучениеНовыхРасхожденийПоОрганизации" Тогда
		Если ИдетПолучениеРасхождений
			И Параметр.Организация = Организация
			И (Параметр.КонецПериода >= НачалоПериода И Параметр.НачалоПериода <= КонецПериода)
			И ЭтотОбъект.ДатаПолученияДанныхСервисаСверкиРасчетов < Параметр.ВремяПолученияРасхождений Тогда
			ПослеЗавершенияОбмена();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область Раздел8

&НаСервере
Процедура ТекстРасхожденияРаздел8НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыСРасхождениями(ЭтотОбъект,, "Раздел8");
КонецПроцедуры

&НаКлиенте
Процедура ТекстРасхожденияРаздел8Нажатие(Элемент)
	ТекстРасхожденияРаздел8НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеОтраженоРаздел8НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетУКонтрагентаРаздел8", ,"Раздел8");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеОтраженоРаздел8Нажатие(Элемент)
	ТекстНеОтраженоРаздел8НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеСверялисьРаздел8НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНеСверилиРаздел8", ,"Раздел8");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеСверялисьРаздел8Нажатие(Элемент)
	ТекстНеСверялисьРаздел8НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстБезРасхожденийРаздел8НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаСверилиБезОшибокРаздел8", ,"Раздел8");
КонецПроцедуры

&НаКлиенте
Процедура ТекстБезРасхожденийРаздел8Нажатие(Элемент)
	ТекстБезРасхожденийРаздел8НажатиеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область Раздел8Прил1

&НаСервере
Процедура ТекстРасхожденияРаздел8_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыСРасхождениями(ЭтотОбъект,, "Раздел8_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстРасхожденияРаздел8_Прил1Нажатие(Элемент)
	ТекстРасхожденияРаздел8_Прил1НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеОтраженоРаздел8_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетУКонтрагентаРаздел8_Прил1", ,"Раздел8_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеОтраженоРаздел8_Прил1Нажатие(Элемент)
	ТекстНеОтраженоРаздел8_Прил1НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеСверялисьРаздел8_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНеСверилиРаздел8_Прил1", ,"Раздел8_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеСверялисьРаздел8_Прил1Нажатие(Элемент)
	ТекстНеСверялисьРаздел8_Прил1НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстБезРасхожденийРаздел8_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаСверилиБезОшибокРаздел8_Прил1", ,"Раздел8_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстБезРасхожденийРаздел8_Прил1Нажатие(Элемент)
	ТекстБезРасхожденийРаздел8_Прил1НажатиеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область Раздел9

&НаСервере
Процедура ТекстРасхожденияРаздел9НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыСРасхождениями(ЭтотОбъект,, "Раздел9");
КонецПроцедуры

&НаКлиенте
Процедура ТекстРасхожденияРаздел9Нажатие(Элемент)
	ТекстРасхожденияРаздел9НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеОтраженоРаздел9НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетУКонтрагентаРаздел9", ,"Раздел9");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеОтраженоРаздел9Нажатие(Элемент)
	ТекстНеОтраженоРаздел9НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНетВОрганизацииРаздел9НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетВОрганизацииРаздел9", ,"Раздел9");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНетВОрганизацииРаздел9Нажатие(Элемент)
	ТекстНетВОрганизацииРаздел9НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеСверялисьРаздел9НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНеСверилиРаздел9", ,"Раздел9");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеСверялисьРаздел9Нажатие(Элемент)
	ТекстНеСверялисьРаздел9НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстБезРасхожденийРаздел9НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаСверилиБезОшибокРаздел9", ,"Раздел9");
КонецПроцедуры

&НаКлиенте
Процедура ТекстБезРасхожденийРаздел9Нажатие(Элемент)
	ТекстБезРасхожденийРаздел9НажатиеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область Раздел9Прил1

&НаСервере
Процедура ТекстРасхожденияРаздел9_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыСРасхождениями(ЭтотОбъект,, "Раздел9_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстРасхожденияРаздел9_Прил1Нажатие(Элемент)
	ТекстРасхожденияРаздел9_Прил1НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеСверялисьРаздел9_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНеСверилиРаздел9_Прил1", ,"Раздел9_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеСверялисьРаздел9_Прил1Нажатие(Элемент)
	ТекстНеСверялисьРаздел9_Прил1НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеОтраженоРаздел9_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетУКонтрагентаРаздел9_Прил1", ,"Раздел9_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеОтраженоРаздел9_Прил1Нажатие(Элемент)
	ТекстНеОтраженоРаздел9_Прил1НажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстБезРасхожденийРаздел9_Прил1НажатиеНаСервере()
	СервисСверкиРасчетовСверкаДекларацийНДС.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаСверилиБезОшибокРаздел9_Прил1", ,"Раздел9_Прил1");
КонецПроцедуры

&НаКлиенте
Процедура ТекстБезРасхожденийРаздел9_Прил1Нажатие(Элемент)
	ТекстБезРасхожденийРаздел9_Прил1НажатиеНаСервере();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВидимостьСверки(Команда)
	
	СервисСверкиРасчетовСверкаДекларацийНДСКлиент.ИзменитьВидимостьСверки(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельСверки(Команда)
	
	СервисСверкиРасчетовСверкаДекларацийНДСКлиент.ЗакрытьПанельСверки(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбменятьсяССервисом(Команда)
	
	ИдетПолучениеРасхождений = Истина;
	УправлениеФормой(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("ОбновитьДанныеОтчета", 1, Истина);
	
КонецПроцедуры

#КонецОбласти 

#Область ПрочиеПроцедурыФункции

&НаСервере
Процедура УстановитьПризнакПолученияРасхождений()
	
	ИдетПолучениеРасхождений = Ложь;
	
	Если Не ЗначениеЗаполнено(Организация)
		ИЛИ Не ЗначениеЗаполнено(НачалоПериода)
		ИЛИ Не ЗначениеЗаполнено(КонецПериода) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим не получаются ли расхождения в других сеансах.
	ЗаблокированныеМесяца =
		РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.ЗаблокированныеМесяцаДляПолученияРасхождений(
		Организация, НачалоПериода, КонецПериода);
	
	ИдетПолучениеРасхождений = ЗаблокированныеМесяца.Количество() > 0;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДатуПолученияДанных(ОбновлятьПризнакПолученияРасхождений = Истина)
	
	ДатаПолученияДанныхСервисаСверкиРасчетов = 
		РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.ДатаАктуальностиРасхождений(
		Организация, НачалоПериода, КонецПериода);
		
	Если ОбновлятьПризнакПолученияРасхождений Тогда
		УстановитьПризнакПолученияРасхождений();
	КонецЕсли;
	
	Если Не ИдетПолучениеРасхождений Тогда // не меняем дату актуальности на форме в процессе обмена
		Если ЗначениеЗаполнено(ДатаПолученияДанныхСервисаСверкиРасчетов) Тогда
			ЧастиСообщения = Новый Массив;
			ЧастиСообщения.Добавить(НСтр("ru='Данные отчета актуальны на '"));
			ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Строка(ДатаПолученияДанныхСервисаСверкиРасчетов), Новый Шрифт(Новый Шрифт,,,Истина)));
			ИтоговоеСообщение = Новый ФорматированнаяСтрока(ЧастиСообщения);
			Элементы.НадписьДатаАктуальности.Заголовок = ИтоговоеСообщение;
		Иначе
			Элементы.НадписьДатаАктуальности.Заголовок = НСтр("ru='Данные для отчета не получались'");
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.ИдетПолучениеРасхождений Тогда 
		Элементы.Обновить.Доступность = Ложь;
		Элементы.Обновить.Заголовок   = НСтр("ru='Получение новых данных из 1С:Сверка 2.0'");
		Элементы.Обновить.Картинка    = БиблиотекаКартинок.СинхронизацияДанныхДлительнаяОперация;
	Иначе
		Элементы.Обновить.Доступность = Истина;
		Элементы.Обновить.Заголовок   = НСтр("ru='Получить новые данные и сформировать'");
		Элементы.Обновить.Картинка    = БиблиотекаКартинок.СинхронизацияДанных;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеОтчета()
	
	// Проверим не получены ли более свежие данные в другом сеансе.
	// Если получены, то не генерируем новый обмен.
	ДатаАктуальностиДО = ДатаПолученияДанныхСервисаСверкиРасчетов;
	ОбновитьДатуПолученияДанных(Ложь);
	Если ДатаАктуальностиДО < ДатаПолученияДанныхСервисаСверкиРасчетов Тогда
		ПослеЗавершенияОбмена();
		Возврат;
	КонецЕсли;
	
	ОбменССервисом = ОбменятьсяДаннымиССервисом();
	Если ОбменССервисом.Свойство("ОтказПроверкиЗаполнения")
		И ОбменССервисом.ОтказПроверкиЗаполнения Тогда
		// Необходимо заполнить обязательные для обмена поля.
		ИдетПолучениеРасхождений = Ложь;
		УправлениеФормой(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗаданияОбмена = ОбменССервисом.ИдентификаторЗадания;
	Если ОбменССервисом.Статус = "Выполнено" Тогда
		ПослеЗавершенияОбмена(ОбменССервисом);
		Возврат
	КонецЕсли;
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеЗавершенияОбмена", ЭтотОбъект);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОбменССервисом, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОбменятьсяДаннымиССервисом()
	
	ЭтоФормированиеОтчета = Ложь;
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ОтказПроверкиЗаполнения", Истина);
	КонецЕсли;
	
	НаименованиеЗадания = СтрШаблон("%1. Обмен с сервисом", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "СервисСверкиРасчетов.ОбменятьсяССервисом";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыПроцедуры = Новый Структура("ЭтоВызовИзОтчета, Организация, НачальнаяДатаПолученияРасхождений, КонечнаяДатаПолученияРасхождений",
		Истина, Организация, НачалоПериода, КонецПериода);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода, ПараметрыПроцедуры);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияОбмена(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	
	Если Не ИдетПолучениеРасхождений Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		Если Результат.Статус <> "Выполнено" Тогда
			Возврат;
		Иначе
			Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
				ИдентификаторЗаданияОбмена = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДатуПолученияДанных();
	
	Если ИдетПолучениеРасхождений Тогда
		ПодключитьОбработчикОжидания("ПроверитьЗавершенияОбмена", 60, Истина);
		Возврат;
	КонецЕсли;
	
	Элементы.Результат.ОтображениеСостояния.Видимость                      = Истина;
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.Результат.ОтображениеСостояния.Картинка                       = БиблиотекаКартинок.ДлительнаяОперация48;
	Элементы.Результат.ОтображениеСостояния.Текст                          = НСтр("ru = 'Получены новые данные из сервиса 1С:Сверка 2.0. Формируется актуальный отчет...'");
	
	ЭтоФормированиеПослеПолученияРасхождений = Истина;
	
	ЗапуститьФормированиеОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета()
	
	ОчиститьСообщения();
	ОтказПроверкиЗаполнения = Ложь;
	
	ИдетПолучениеРасхождений = Ложь;
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена"); 
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("НачалоПериода",                  ЭтотОбъект.НачалоПериода);
	СтруктураПараметров.Вставить("КонецПериода",                   ЭтотОбъект.КонецПериода);
	СтруктураПараметров.Вставить("ДанныеДляСверкиДеклараций",      ЭтотОбъект.ДанныеДляСверкиДеклараций);
	СтруктураПараметров.Вставить("Организация",                    ЭтотОбъект.Организация);
	СтруктураПараметров.Вставить("УникальныйИдентификатор",        ЭтотОбъект.УникальныйИдентификатор);
	СтруктураПараметров.Вставить("Контрагент",                     Неопределено);
	СтруктураПараметров.Вставить("ВыводитьТолькоДопЛисты",         Ложь);
	СтруктураПараметров.Вставить("ФормироватьДополнительныеЛисты", Истина);
	СтруктураПараметров.Вставить("ДанныеСверки",                   Неопределено);
	СтруктураПараметров.Вставить("КодВидаОперации",                Неопределено);
	СтруктураПараметров.Вставить("ОперацияНетВОрганизации",        Неопределено);
	СтруктураПараметров.Вставить("ДекларацияПоНДС",                ДекларацияПоНДС);
	
	СервисСверкиРасчетовСверкаДекларацийНДСКлиент.СверитьДекларациюПоНДСНаСервере(СтруктураПараметров, ЭтотОбъект);
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ЭтоФормированиеПослеПолученияРасхождений = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗавершенияОбмена() Экспорт
	
	Если ИдетПолучениеРасхождений Тогда
		ПослеЗавершенияОбмена();
	Иначе
		ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРезультатПроверки() Экспорт

	НастроитьОтображениеРазделовНаСервере();

КонецПроцедуры


&НаСервере
Процедура НастроитьОтображениеРазделовНаСервере()
	
	СервисСверкиРасчетовСверкаДекларацийНДС.НастроитьОтображениеРазделов(ЭтотОбъект);
	
КонецПроцедуры



#КонецОбласти