
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("ПараметрыОткрытия", ПараметрыОткрытия);
	
	ОсновнаяОрганизация   = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	ОсновноеПодразделение = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации");
	ОсновнойСклад         = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнойСклад");
	ОсновнаяСтавкаНДС     = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяСтавкаНДС");
	
	ПоказыватьСчетаУчетаВДокументах = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ПоказыватьСчетаУчетаВДокументах");
	Элементы.ПоказыватьСчетаУчетаВДокументах.Видимость = СчетаУчетаВДокументах.ПравоПросмотраСчетовУчета();
	
	Элементы.НастройкаСинхронизацииСКалендаремGoogle.Видимость = СинхронизацияСКалендаремGoogle.ДоступнаНастройкаСинхронизации();
	
	ЕстьПравоНастройкаПодключенияКоннект = ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.НастройкаПодключенияКоннект);
	Элементы.НастройкаКоннект.Видимость = ЕстьПравоНастройкаПодключенияКоннект;
	
	ДоступностьДО = ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом")
		И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям("Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность");
	Элементы.НастройкаАвторизацииВ1СДокументооборот.Видимость = ДоступностьДО;
	
	ИспользоватьНесколькоОрганизацийБухгалтерскийУчет = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийБухгалтерскийУчет");
	Элементы.ОсновнаяОрганизация.Видимость = ИспользоватьНесколькоОрганизацийБухгалтерскийУчет;
	
	ИспользоватьНесколькоСкладовБухгалтерскийУчет = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет");
	Элементы.ОсновнойСклад.Видимость = ИспользоватьНесколькоСкладовБухгалтерскийУчет;
	
	ИспользуетсяКабинетСотрудника = ПолучитьФункциональнуюОпцию("ИспользуетсяЭДОБЗККабинетСотрудника");
	Элементы.ПерсональнаяНастройкаПодписанияЦифровымиПодписями.Видимость = ИспользуетсяКабинетСотрудника;
	
	ЗапрашиватьПодтверждениеПриЗавершенииПрограммы = СтандартныеПодсистемыСервер.ЗапрашиватьПодтверждениеПриЗавершенииПрограммы();
	ПоказыватьПредупреждениеОбУстановленныхОбновленияхПрограммы = СтандартныеПодсистемыСервер.ПоказыватьПредупреждениеОбУстановленныхОбновленияхПрограммы();
	ЗначениеРабочейДаты = ОбщегоНазначения.РабочаяДатаПользователя();
	
	ТекущаяОрганизация                             = ОсновнаяОрганизация;
	ТекущееПодразделение                           = ОсновноеПодразделение;
	ТекущийСклад                                   = ОсновнойСклад;
	ТекущееЗначениеПоказыватьСчетаУчетаВДокументах = ПоказыватьСчетаУчетаВДокументах;
	
	Если ЗначениеЗаполнено(ЗначениеРабочейДаты) Тогда
		ИспользоватьТекущуюДатуКомпьютера = 1;
	Иначе
		ИспользоватьТекущуюДатуКомпьютера = 0;
		ЗначениеРабочейДаты = '0001-01-01';
	КонецЕсли;
	
	ТолькоПросмотр = Не ПравоДоступа("СохранениеДанныхПользователя", Метаданные);
	
	// СтандартныеПодсистемы.РаботаСФайлами
	
	ЭтоВебКлиент = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	
	Если ЭтоВебКлиент Или Не ОбщегоНазначенияКлиентСервер.ЭтоWindowsКлиент() Тогда
		Элементы.НастройкаСканирования.Видимость = Ложь;
	КонецЕсли;
	
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	УправлениеФормой(ЭтаФорма);
	
	// СтандартныеПодсистемы.Пользователи
	АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
	// Конец СтандартныеПодсистемы.Пользователи
	
	Элементы.НастройкаОповещенийСервисаСверки.Видимость = СервисСверкиРасчетовПереопределяемый.ОповещатьПользователяОРасхождениях(АвторизованныйПользователь)
		И Справочники.СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещенияВключены();
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановленАктивныйЭлемент = Ложь;
	
	Если ТипЗнч(ПараметрыОткрытия) = Тип("Структура") Тогда

		АктивныйЭлемент = Неопределено;
		ПараметрыОткрытия.Свойство("АктивныйЭлемент", АктивныйЭлемент);

		Если ТипЗнч(АктивныйЭлемент) = Тип("Строка") Тогда
			ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Найти(АктивныйЭлемент);
			УстановленАктивныйЭлемент = Истина;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	КорректныйПериод = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
	
	РабочаяДатаУказываетсяПользователем = ИспользоватьТекущуюДатуКомпьютера = 1;
	
	Если РабочаяДатаУказываетсяПользователем Тогда
		Если ЗначениеРабочейДаты < КорректныйПериод.НачалоКорректногоПериода Тогда
			ГраницаКорректности = Формат(КорректныйПериод.НачалоКорректногоПериода, "ДФ=гггг");
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Рабочая дата должна быть не ранее %1 года'"), ГраницаКорректности);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ЗначениеРабочейДаты",, Отказ);
		ИначеЕсли ЗначениеРабочейДаты > КорректныйПериод.КонецКорректногоПериода Тогда
			ГраницаКорректности = Формат(КорректныйПериод.КонецКорректногоПериода, "ДФ=гггг");
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Рабочая дата должна быть не позже %1 года'"), ГраницаКорректности);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ЗначениеРабочейДаты",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если Модифицированность Тогда
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru='Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АвторизованныйПользовательНажатие(Элемент, СтандартнаяОбработка)
	
	ПоказатьЗначение(, АвторизованныйПользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьТекущуюДатуКомпьютераПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
	Если ИспользоватьТекущуюДатуКомпьютера = 0 Тогда
		ЗначениеРабочейДаты = '0001-01-01';
	ИначеЕсли ИспользоватьТекущуюДатуКомпьютера = 1 Тогда
		ЗначениеРабочейДаты = ТекущаяДата();
	КонецЕсли;
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ОсновноеПодразделениеПриИзменении(Элемент)

	ПриИзмененииПодразделенияСервер();

КонецПроцедуры

&НаКлиенте
Процедура НастройкаСинхронизацииСКалендаремGoogle(Команда)
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуНастройкиСинхронизацииСКалендаремGoogle();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаКоннект(Команда)
	
	ИнтеграцияСКоннектКлиент.НастройкаИнтеграции(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура НастройкиЭлектроннойПодписиИШифрования(Команда)
	
	ЭлектроннаяПодписьКлиент.ОткрытьНастройкиЭлектроннойПодписиИШифрования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	Если ЗаписатьДанные() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСканирования(Команда)
	
	РаботаСФайламиКлиент.ОткрытьФормуНастройкиСканирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерсональнаяНастройкаПодписанияЦифровымиПодписями(Команда)

	МодульЭДОБЗККлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭДОБЗККлиент");
	МодульЭДОБЗККлиент.ОчиститьНастройкиПодписанияПечатныхФорм();

КонецПроцедуры

&НаКлиенте
Процедура НастройкаАвторизацииВ1СДокументооборот(Команда)
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		ИмяФормыАвторизацииДО =
			"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.АвторизацияВ1СДокументооборот";
		ОткрытьФорму(ИмяФормыАвторизацииДО,,,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаРаботыСФайлами(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ПерсональныеНастройкиРаботыСФайлами", , ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерсональнаяНастройкаСменаПодписи(Команда)
	
	МодульПодписиДокументов = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодписиДокументовКлиент");
	МодульПодписиДокументов.ОткрытьФормуНастройкиСменыПодписи(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаОповещенийСервисаСверки(Команда)
	
	КлючНастройкиОповещенийТекущегоПользователя = СервисСверкиРасчетовВызовСервера.КлючНастройкиОповещенийТекущегоПользователя();
	Если КлючНастройкиОповещенийТекущегоПользователя <> Неопределено Тогда
		ОткрытьФорму("Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.ФормаОбъекта", 
		Новый Структура("Ключ, ПерсональнаяНастройка", КлючНастройкиОповещенийТекущегоПользователя, Истина));
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкиСервер()
	
	Настройки = Новый Структура;
	Настройки.Вставить("ЗапрашиватьПодтверждениеПриЗавершенииПрограммы", ЗапрашиватьПодтверждениеПриЗавершенииПрограммы);
	Настройки.Вставить("ПоказыватьПредупреждениеОбУстановленныхОбновленияхПрограммы", ПоказыватьПредупреждениеОбУстановленныхОбновленияхПрограммы);
	ОбщегоНазначения.СохранитьПерсональныеНастройки(Настройки);
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнаяОрганизация",              ОсновнаяОрганизация);
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации", ОсновноеПодразделение);
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнойСклад",                    ОсновнойСклад);
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ПоказыватьСчетаУчетаВДокументах",  ПоказыватьСчетаУчетаВДокументах);
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнаяСтавкаНДС",                ОсновнаяСтавкаНДС);
	
	Если ИспользоватьТекущуюДатуКомпьютера = 0 Тогда
		ЗначениеРабочейДатыДляСохранения  = '0001-01-01';
	Иначе
		ЗначениеРабочейДатыДляСохранения  = ЗначениеРабочейДаты;
	КонецЕсли;
	ОбщегоНазначения.УстановитьРабочуюДатуПользователя(ЗначениеРабочейДатыДляСохранения);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПодразделенияСервер()

	Если НЕ ОсновноеПодразделение.Пустая() Тогда
		ОрганизацияПодразделения = БухгалтерскийУчетПереопределяемый.ОрганизацияПодразделения(ОсновноеПодразделение);
		Если ЗначениеЗаполнено(ОрганизацияПодразделения) 
			И ОрганизацияПодразделения <> ОсновнаяОрганизация Тогда
			ОсновнаяОрганизация = ОрганизацияПодразделения;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ЗаписатьДанные()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Настройки = Новый Структура;
	Настройки.Вставить("ЗапрашиватьПодтверждениеПриЗавершенииПрограммы", ЗапрашиватьПодтверждениеПриЗавершенииПрограммы);
	Настройки.Вставить("ПоказыватьПредупреждениеОбУстановленныхОбновленияхПрограммы", ПоказыватьПредупреждениеОбУстановленныхОбновленияхПрограммы);
	ОбщегоНазначенияКлиент.СохранитьПерсональныеНастройки(Настройки);
	
	СохранитьНастройкиСервер();
	
	Если ТекущаяОрганизация <> ОсновнаяОрганизация Тогда
		Оповестить("ИзменениеОсновнойОрганизации", ОсновнаяОрганизация);
	КонецЕсли;
	ТекущаяОрганизация = ОсновнаяОрганизация;
	
	Если ТекущееПодразделение <> ОсновноеПодразделение Тогда
		Оповестить("ИзменениеОсновногоПодразделенияОрганизации", ОсновноеПодразделение);
	КонецЕсли;
	ТекущееПодразделение = ОсновноеПодразделение;
	
	Если ТекущийСклад <> ОсновнойСклад Тогда
		Оповестить("ИзменениеОсновногоСклада", ОсновнойСклад);
	КонецЕсли;
	ТекущийСклад = ОсновнойСклад;
	
	Если ТекущееЗначениеПоказыватьСчетаУчетаВДокументах <> ПоказыватьСчетаУчетаВДокументах Тогда
		Оповестить("ИзменениеПоказыватьСчетаУчетаВДокументах", ПоказыватьСчетаУчетаВДокументах);
	КонецЕсли;
	ТекущееЗначениеПоказыватьСчетаУчетаВДокументах = ПоказыватьСчетаУчетаВДокументах;
	
	Модифицированность = Ложь;
	
	ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.ЗначениеРабочейДаты.Доступность  = Форма.ИспользоватьТекущуюДатуКомпьютера = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.АвторизованныйПользователь);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ОсновнаяОрганизация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ОсновноеПодразделение);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ОсновнойСклад);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ОсновнаяСтавкаНДС);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.НастройкиЭлектроннойПочты);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

