#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьНалогиИОтчеты = Истина;
	ЗадачиБухгалтераПереопределяемый.ПриОпределенииПоказыватьНалогиИОтчеты(ПоказыватьНалогиИОтчеты);
	
	ПоказыватьСинхронизациюСКалендаремGoogle = Ложь;
	ЗадачиБухгалтераПереопределяемый.ПриОпределенииПоказыватьСинхронизациюСКалендаремGoogle(ПоказыватьСинхронизациюСКалендаремGoogle);
	
	Если ПоказыватьНалогиИОтчеты Тогда
		ПроверитьСистемуНалогообложения();
	КонецЕсли;
	
	Если ПоказыватьСинхронизациюСКалендаремGoogle Тогда
		ДлительнаяОперацияПриОткрытии = ПроверитьСостояниеСинхронизацииСКалендаремGoogle();
	КонецЕсли;
	
	ПроверитьСостояниеУведомленийПоЭлектроннойПочте();
	
	Элементы.ГруппаНалогиИОтчеты.Видимость = ПоказыватьНалогиИОтчеты;
	Элементы.ГруппаСинхронизацияСКалендаремGoogle.Видимость = ПоказыватьСинхронизациюСКалендаремGoogle;
	
	ЗадачиБухгалтераПереопределяемый.ПриСозданииНаСервереСписокЗадачНастройка(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ДлительнаяОперацияПриОткрытии <> Неопределено Тогда
		ОжидатьЗавершенияПроверкиСостоянияСинхронизацииСКалендаремGoogle(ДлительнаяОперацияПриОткрытии);
		ДлительнаяОперацияПриОткрытии = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Организации"
		Или ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		ПроверитьСистемуНалогообложения();
	ИначеЕсли ИмяСобытия = "СписокЗадачНастройкаУведомленийПоЭлектроннойПочтеИзменение" Тогда
		ПроверитьСостояниеУведомленийПоЭлектроннойПочте();
	ИначеЕсли ИмяСобытия = "НастройкаСинхронизацииСКалендаремGoogleИзменение" Тогда
		ПроверитьСостояниеСинхронизацииСКалендаремGoogle();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НалогиИОтчеты(Команда)
	
	ЗадачиБухгалтераКлиентПереопределяемый.ОткрытьСписокНалогиИОтчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаУведомленийПоЭлектроннойПочте(Команда)
	
	ОткрытьФорму("ОбщаяФорма.СписокЗадачНастройкаУведомленийПоЭлектроннойПочте");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСинхронизацииСКалендаремGoogle(Команда)
	
	ЗадачиБухгалтераКлиентПереопределяемый.ОткрытьФормуНастройкиСинхронизацииСКалендаремGoogle();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьСистемуНалогообложения()
	
	ЗадачиБухгалтераПереопределяемый.ПроверитьСистемуНалогообложенияСписокЗадачНастройка(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСостояниеУведомленийПоЭлектроннойПочте()
	
	Если Не СписокЗадачУведомленияПоЭлектроннойПочте.КорректностьНастройки() Тогда
		Элементы.НастройкаУведомленийПоЭлектроннойПочте.Заголовок = НСтр("ru = 'Настройка не завершена'");
		Элементы.НастройкаУведомленийПоЭлектроннойПочте.ЦветТекста = ЦветаСтиля.ЦветШрифтаОшибки;
		Возврат;
	КонецЕсли;
	
	Элементы.НастройкаУведомленийПоЭлектроннойПочте.ЦветТекста = Новый Цвет();
	
	ОтправлятьУведомления = СписокЗадачУведомленияПоЭлектроннойПочте.ОтправлятьУведомления();
	ОтправлятьУведомленияСЭДО = СписокЗадачУведомленияПоЭлектроннойПочте.ОтправлятьУведомленияСЭДО();
	
	Если Не ОтправлятьУведомления И Не ОтправлятьУведомленияСЭДО Тогда
		Элементы.НастройкаУведомленийПоЭлектроннойПочте.Заголовок = НСтр("ru = 'Выключено'");
		Возврат;
	КонецЕсли;
	
	ОписаниеВидовУведомлений = "";
	УчетЗарплатыВЭтойПрограмме = Истина;
	ЗадачиБухгалтераПереопределяемый.ПроверитьУчетЗарплатыВЭтойПрограмме(УчетЗарплатыВЭтойПрограмме);
	Если УчетЗарплатыВЭтойПрограмме Тогда
		Если Не ОтправлятьУведомления Тогда
			ОписаниеВидовУведомлений = НСтр("ru = ' (только СЭДО)'");
		ИначеЕсли Не ОтправлятьУведомленияСЭДО Тогда
			ОписаниеВидовУведомлений = НСтр("ru = ' (кроме СЭДО)'");
		КонецЕсли;
	КонецЕсли;
	
	ПолучателиУведомлений = СтрРазделить(СписокЗадачУведомленияПоЭлектроннойПочте.Получатель(), ",", Ложь);
	Если ПолучателиУведомлений.Количество() = 1 Тогда
		ОписаниеПолучателей = ПолучателиУведомлений[0];
	ИначеЕсли ПолучателиУведомлений.Количество() > 1 Тогда
		ОписаниеПолучателей = СтрШаблон(НСтр("ru = '%1 и еще %2'"),
			ПолучателиУведомлений[0],
			ПолучателиУведомлений.Количество() - 1);
	КонецЕсли;
	
	Элементы.НастройкаУведомленийПоЭлектроннойПочте.Заголовок = 
		СтрШаблон(НСтр("ru = 'Включено для %1%2'"), ОписаниеПолучателей, ОписаниеВидовУведомлений);
	
КонецПроцедуры

#Область ПроверкаСостоянияСинхронизацииСКалендаремGoogle

&НаСервере
Функция ПроверитьСостояниеСинхронизацииСКалендаремGoogle()
	
	Пользователь = Пользователи.ТекущийПользователь();
	РезультатПроверки = Неопределено;
	
	ЗадачиБухгалтераПереопределяемый.ПроверитьСостояниеСинхронизацииСКалендаремGoogle(РезультатПроверки, Пользователь, ЭтотОбъект);
	
	Возврат РезультатПроверки;
	
КонецФункции

&НаСервере
Процедура ЗавершитьПроверкуСостоянияСинхронизацииСКалендаремGoogleНаСервере(ДлительнаяОперация)
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		РезультатПроверки = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Элементы.НастройкаСинхронизацииСКалендаремGoogle.Заголовок = 
			?(РезультатПроверки.СинхронизацияДоступна,
				НСтр("ru = 'Включено'"),
				НСтр("ru = 'Выключено'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершенияПроверкиСостоянияСинхронизацииСКалендаремGoogle(ДлительнаяОперация)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьПроверкуСостоянияСинхронизацииСКалендаремGoogle", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПроверкуСостоянияСинхронизацииСКалендаремGoogle(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	ЗавершитьПроверкуСостоянияСинхронизацииСКалендаремGoogleНаСервере(ДлительнаяОперация);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти