///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ИмяКартинки) Тогда
		Попытка
			Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок[Параметры.ИмяКартинки];
		Исключение
			СообщенияВСлужбуТехническойПоддержки.ЗаписатьИнформациюВЖурналРегистрации(
				НСтр("ru = 'Не удалось установить картинку при выводе сообщения об ошибке.'"));
		КонецПопытки;
	КонецЕсли;
	
	Элементы.ДекорацияСообщение.Заголовок = Параметры.ТекстСообщения;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнтернетПоддержкаПользователейКлиент.УстановитьОтображениеКнопкиКопироватьВБуфер(
		Элементы,
		СтандартныйФорматДанныхБуфераОбмена.Текст);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КопироватьВБуфер(Команда)

	КопироватьТекстВБуфер();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТехническуюИнформацию(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьДиалогВыбораКаталога",
		ЭтотОбъект);
	
	ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботы1СПредприятия(
		ОписаниеОповещения,
		НСтр("ru = 'Для продолжения необходимо установить расширение для работы с файлами.'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура КопироватьТекстВБуфер()
	
	УдалосьПоместить = Ждать ИнтернетПоддержкаПользователейКлиент.ПоместитьСтрокуВБуферОбмена(
		Строка(Элементы.ДекорацияСообщение.Заголовок));
	
	Если УдалосьПоместить Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Успешно'"),
			,
			НСтр("ru = 'Сообщение скопировано в буфер'"));
	Иначе
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Не удалось скопировать информацию.'"),);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДиалогВыбораКаталога(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = НСтр("ru = 'Укажите папку для сохранения'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыборПапкиСохраненияЗавершение",
		ЭтотОбъект);
	
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(
		ОписаниеОповещения,
		Диалог);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыборПапкиСохраненияЗавершение(РезультатВыбора, ДополнительныеФайлы) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Каталог = РезультатВыбора[0];

	Разделитель = ПолучитьРазделительПути();
	Если Прав(Каталог, 1) <> Разделитель Тогда
		Каталог = Каталог + Разделитель;
	КонецЕсли;

	ОшибкиВыгрузки = Ложь;
	
	Вложения = Параметры.ПараметрыСообщения.Вложения;
	Если ТипЗнч(Вложения) = Тип("Массив")
		И Вложения.Количество() > 0 Тогда
			
		Состояние(НСтр("ru = 'Сохранение вложений...'"));
		
		Для Каждого Вложение Из Вложения Цикл
			
			Если Вложение.ВидДанных = "Адрес" Тогда
				
				РезультатСохранения = Ждать СохранитьФайлПоАдресу(
					Каталог,
					Вложение.Данные,
					Вложение.Представление);
				Если Не РезультатСохранения Тогда
					ОшибкиВыгрузки = Истина;
				КонецЕсли;
				
			ИначеЕсли Вложение.ВидДанных = "ИмяФайла" Тогда
				
				РезультатСохранения = Ждать СкопироватьФайлВПапкуВыгрузки(
					Каталог,
					Вложение.Данные,
					Вложение.Представление);
				Если Не РезультатСохранения Тогда
					ОшибкиВыгрузки = Истина;
				КонецЕсли;
				
			Иначе
				РезультатСохранения = Ждать СохранитьТекст(
					Каталог,
					Вложение.Данные,
					Вложение.Представление);
				Если Не РезультатСохранения Тогда
					ОшибкиВыгрузки = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Получение технической информации и журнала регистрации...'"));
	ТехническаяИнформация = ТехническаяИнформация(
		Параметры.ПараметрыСообщения.ДанныеСообщения,
		Параметры.ПараметрыСообщения.ОтборЖурналаРегистрации);
	
	Если ТехническаяИнформация.ОшибкиВыгрузки Тогда
		ОшибкиВыгрузки = Истина;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Сохранение текста сообщения...'"));
	Если ЗначениеЗаполнено(ТехническаяИнформация.Сообщение) Тогда
		УИД = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
		ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ТекстСообщения_%1.json'"),
			УИД);
		РезультатСохранения = Ждать СохранитьТекст(
			Каталог,
			ТехническаяИнформация.Сообщение,
			ИмяФайла);
		Если Не РезультатСохранения Тогда
			ОшибкиВыгрузки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Сохранение технической информации...'"));
	Если ЗначениеЗаполнено(ТехническаяИнформация.ТехническаяИнформация) Тогда
		УИД = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
		ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ТехническаяИнформация_%1.json'"),
			УИД);
		РезультатСохранения = Ждать СохранитьТекст(
			Каталог,
			ТехническаяИнформация.ТехническаяИнформация,
			ИмяФайла);
		Если Не РезультатСохранения Тогда
			ОшибкиВыгрузки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Сохранение журнала регистрации...'"));
	Если ЗначениеЗаполнено(ТехническаяИнформация.Журнал) Тогда
		УИД = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
		ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ЖурналРегистрации_%1.xml'"),
			УИД);
		РезультатСохранения = Ждать СохранитьФайлПоАдресу(
			Каталог,
			ТехническаяИнформация.Журнал,
			ИмяФайла);
		Если Не РезультатСохранения Тогда
			ОшибкиВыгрузки = Истина;
		КонецЕсли;
		УдалитьИзВременногоХранилища(ТехническаяИнформация.Журнал);
	КонецЕсли;
	
	Состояние();
	
	Если ОшибкиВыгрузки Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'При сохранении информации возникли ошибки.
				|Подробности смотрите в журнале регистрации.'"));
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Успешно'"),
			,
			НСтр("ru = 'Все файлы сохранены'"));
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьПроводник(Каталог);
	
КонецПроцедуры

&НаКлиенте
Асинх Функция СохранитьФайлПоАдресу(Каталог, АдресФайла, ИмяФайла)
	
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
	Если ТипЗнч(ДанныеФайла) <> Тип("ДвоичныеДанные") Тогда
		ЗаписатьВЖурнал(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать файл %1. Ошибочный формат данных.'"),
				ИмяФайла));
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		Ждать ДанныеФайла.ЗаписатьАсинх(Каталог + ИмяФайла);
	Исключение
		ЗаписатьВЖурнал(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать файл %1. %2'"),
				ИмяФайла,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Асинх Функция СкопироватьФайлВПапкуВыгрузки(Каталог, ПутьКФайлу, ИмяФайла)
	
	Файл = Новый Файл(ПутьКФайлу);
	Результат = Ждать Файл.СуществуетАсинх();
	Если Не Результат Тогда
		ЗаписатьВЖурнал(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать файл %1. Указанный файл не существует.'"),
				ИмяФайла));
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		Результат = Ждать КопироватьФайлАсинх(
			ПутьКФайлу,
			Каталог + ИмяФайла);
	Исключение
		ЗаписатьВЖурнал(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать файл %1. %2'"),
				ИмяФайла,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Асинх Функция СохранитьТекст(Каталог, Текст, ИмяФайла)
	
	Попытка
		ДанныеФайла = ПолучитьДвоичныеДанныеИзСтроки(Текст, "UTF-8");
		Ждать ДанныеФайла.ЗаписатьАсинх(Каталог + ИмяФайла);
	Исключение
		ЗаписатьВЖурнал(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать файл %1. %2'"),
				ИмяФайла,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьВЖурнал(Знач Текст)
	
	СообщенияВСлужбуТехническойПоддержки.ЗаписатьИнформациюВЖурналРегистрации(Текст);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТехническаяИнформация(
		Знач ДанныеСообщения,
		Знач ОтборЖурналаРегистрации)
	
	Результат = Новый Структура;
	Результат.Вставить("ОшибкиВыгрузки",        Ложь);
	Результат.Вставить("ТехническаяИнформация", "");
	Результат.Вставить("Журнал",                "");
	Результат.Вставить("Сообщение",             "");
	
	ТекстСообщения = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеСообщения.Тема) Тогда
		ТекстСообщения.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Тема: %1'"),
				ДанныеСообщения.Тема));
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеСообщения.Сообщение) Тогда
		ТекстСообщения.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Сообщение: %1'"),
				ДанныеСообщения.Сообщение));
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		Результат.Сообщение = СтрСоединить(ТекстСообщения, Символы.ПС);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеСообщения.ДанныеТехническойИнформации) = Тип("Структура")
		И ТипЗнч(ДанныеСообщения.НастройкиТехническойИнформации) = Тип("Структура") Тогда
		Попытка
			СообщенияВСлужбуТехническойПоддержки.ЗаполнитьДанныеТехническойИнформации(
				ДанныеСообщения.ДанныеТехническойИнформации,
				ДанныеСообщения.НастройкиТехническойИнформации);
				
			ДанныеСообщенияВременный = ОбщегоНазначения.СкопироватьРекурсивно(
				ДанныеСообщения.ДанныеТехническойИнформации);
			ОписаниеПодсистем = СообщенияВСлужбуТехническойПоддержки.ТекстОписаниеВерсийПодсистем(
				ДанныеСообщенияВременный.ДанныеПодсистем);
			ОписаниеРасширений = СообщенияВСлужбуТехническойПоддержки.ТекстОписаниеУстановленныхРасширений(
				ДанныеСообщенияВременный.ДанныеРасширений);
			
			ДанныеСообщенияВременный.ДанныеПодсистем = ОписаниеПодсистем;
			ДанныеСообщенияВременный.ДанныеРасширений = ОписаниеРасширений;
			
			Результат.ТехническаяИнформация = ОбщегоНазначения.ЗначениеВJSON(
				ДанныеСообщенияВременный);
			
			ДанныеСообщенияВременный.Очистить();
		Исключение
			СообщенияВСлужбуТехническойПоддержки.ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось сформировать техническую информацию.
						|
						|%1'"),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
			Результат.ОшибкиВыгрузки = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если ТипЗнч(ОтборЖурналаРегистрации) = Тип("Структура") Тогда
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
		
		Попытка
			
			Если ОтборЖурналаРегистрации.ДатаНачала > ОтборЖурналаРегистрации.ДатаОкончания Тогда
				// Неправильный отбор по дате
				Возврат Результат;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОтборЖурналаРегистрации.Уровень)
				И ОтборЖурналаРегистрации.Уровень <> "Ошибка"
				И ОтборЖурналаРегистрации.Уровень <> "Предупреждение"
				И ОтборЖурналаРегистрации.Уровень <> "Информация"
				И ОтборЖурналаРегистрации.Уровень <> "Примечание" Тогда
				// неправильный отбор по уровню
				Возврат Результат;
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Истина);
			
			ВыгрузитьЖурналРегистрации(
				ИмяВременногоФайла,
				СообщенияВСлужбуТехническойПоддержки.ОтборЖурналаРегистрации(
					ОтборЖурналаРегистрации));
			
			УстановитьПривилегированныйРежим(Ложь);

			ДанныеФайла = Новый ДвоичныеДанные(ИмяВременногоФайла);
			
			Результат.Журнал = ПоместитьВоВременноеХранилище(ДанныеФайла);
			
			УдалитьФайлы(ИмяВременногоФайла);
			
		Исключение
			СообщенияВСлужбуТехническойПоддержки.ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось сформировать информацию из журнала регистрации.
						|
						|%1'"),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
			Результат.ОшибкиВыгрузки = Истина;
			УдалитьФайлы(ИмяВременногоФайла);
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции



#КонецОбласти
