
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	Период = '2025-01-01';
	
	Элементы.ДекорацияТекущаяСтавка.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сейчас в программе установлены ставки налога: %1% в федеральный бюджет, %2% в региональный.'"),
		НалоговыйУчет.НалоговаяСтавкаФедеральныйБюджет(Период, Организация, ""),
		НалоговыйУчет.НалоговаяСтавкаРегиональныйБюджет(Период, Организация, ""));
			
	Элементы.ГруппаПроверкаСтавокНалога.Видимость =
		Не РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.ЗаполненыСтавкиНалогаНаПрибыльС2025Года(Организация);
		
	ВариантУчетаОтложенногоНалога =
		НалогНаПрибыльБухгалтерскийУчет.ВариантУчетаОтложенногоНалога(Период, Организация);
	ПрименяетсяПБУ18 = ВариантУчетаОтложенногоНалога <> Перечисления.ВариантыУчетаОтложенногоНалога.Нет;
	ЭтоЗатратныйМетод = Перечисления.ВариантыУчетаОтложенногоНалога.ЭтоЗатратныйМетод(ВариантУчетаОтложенногоНалога);
	
	Элементы.ГруппаОтложенныйНалогНаПрибыль.Видимость = ПрименяетсяПБУ18;
	Элементы.ДекорацияУстановленЗатратныйМетод.Видимость = ЭтоЗатратныйМетод;
	Элементы.ГруппаУстановкаБалансовогоМетода.Видимость = ЭтоЗатратныйМетод;
	Элементы.ГруппаУведомлениеДляБалансовогоМетода.Видимость = ПрименяетсяПБУ18 И Не ЭтоЗатратныйМетод;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияПроверкаСтавокНалогаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ТекущаяЗакладка", "НалогНаПрибыль");
	ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУстановкаБалансовогоМетодаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("РегистрСведений.УчетнаяПолитика.ФормаЗаписи", ПараметрыФормыУчетнойПолитики(Организация));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПараметрыФормыУчетнойПолитики(Организация)
	
	Ключ = НастройкиУчета.КлючЗаписиДействующейУчетнойПолитики("УчетнаяПолитика", Организация);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Ключ);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("РежимНастройки", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

#КонецОбласти