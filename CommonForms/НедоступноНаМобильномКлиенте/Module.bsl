// Хранит значения, часть из которых может существовать только на клиенте.
&НаКлиенте
Перем ПараметрыОткрытияКлиент Экспорт;

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыФормыНедоступно      = Параметры.ПараметрыФормыНедоступно;
	ОписаниеНедоступногоДействия  = ПараметрыФормыНедоступно.ОписаниеНедоступногоДействия;
	ОписаниеНедоступнойФункции    = ПараметрыФормыНедоступно.ОписаниеНедоступнойФункции;
	ОткрытаВнутриОкна             = ПараметрыФормыНедоступно.ОткрытаВнутриОкна;
	ФормаДоступнаСПредупреждением = ПараметрыФормыНедоступно.ФормаДоступнаСПредупреждением;
	
	Если Не ПустаяСтрока(ОписаниеНедоступногоДействия) Тогда
		ТекстШаблонаУказание = НСтр("ru = '%1 предназначена для работы с компьютера.'");
		ТекстПредупреждения = СтрШаблон(ТекстШаблонаУказание, ОписаниеНедоступногоДействия);
	ИначеЕсли Не ПустаяСтрока(ОписаниеНедоступнойФункции) Тогда
		ТекстШаблонаУказание = НСтр("ru = '%1 можно в версии для компьютера.'");
		ТекстПредупреждения = СтрШаблон(ТекстШаблонаУказание, ОписаниеНедоступнойФункции);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Воспользоваться сервисом можно в версии для компьютера.'");
	КонецЕсли;
	
	Элементы.ОписаниеПредупреждение.Заголовок = ТекстПредупреждения;
	
	Элементы.ФормаВперед.Видимость = ФормаДоступнаСПредупреждением;
	Элементы.ДоступноСПредупреждением.Видимость = ФормаДоступнаСПредупреждением;
	Элементы.ОписаниеЗаголовок.Видимость = Не ФормаДоступнаСПредупреждением;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Параметры.ПереопределенаНаСервере И ФормаДоступнаСПредупреждением Тогда
		// Дозаполним те параметры, которые не были известны на сервере из ОбработкаПолученияФормы.
		// См. МобильныйКлиентБПКлиентСервер.НовыйПараметрыОткрытияФормыСПредупреждением()
		ПараметрыОткрытияКлиент = Параметры.ПараметрыОткрытия;
		Параметры.ПараметрыОткрытия = Неопределено;

		ПараметрыОткрытияКлиент.Владелец                    = ВладелецФормы;
		ПараметрыОткрытияКлиент.Уникальность                = КлючУникальности;
		ПараметрыОткрытияКлиент.Окно                        = Окно;
		ПараметрыОткрытияКлиент.НавигационнаяСсылка         = НавигационнаяСсылка;
		ПараметрыОткрытияКлиент.ОписаниеОповещенияОЗакрытии = ОписаниеОповещенияОЗакрытии;
		ПараметрыОткрытияКлиент.РежимОткрытияОкна           = РежимОткрытияОкна;
	КонецЕсли;
	
	#Если МобильныйКлиент Тогда
		Если Не ПустаяСтрока(ОписаниеНедоступногоДействия) Тогда
			ОписаниеСобытия = ОписаниеНедоступногоДействия;
		ИначеЕсли Не ПустаяСтрока(ОписаниеНедоступнойФункции) Тогда
			ОписаниеСобытия = ОписаниеНедоступнойФункции;
		Иначе
			ОписаниеСобытия = "Прочий сервис";
		КонецЕсли;
		
		СтатистикаИспользованияПриложения.ОтправитьСобытие("PF_form",
			СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(Символ(34)+" ", ОписаниеСобытия, "__"));
	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Назад(Команда)
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаписатьРазработчикам(Команда)
	МобильныйКлиентБПКлиент.НачатьОтправкуПисьмаРазработчикам(
		СтрШаблон(НСтр("ru = 'Функция ""%1"" будет полезна на мобильном устройстве потому что'"),
		?(Не ПустаяСтрока(ОписаниеНедоступногоДействия),
			ОписаниеНедоступногоДействия,
			ОписаниеНедоступнойФункции)));
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ОткрытьДлительнуюФорму()

	МобильныйКлиентБПКлиент.ОткрытьОсновнуюФорму(ПараметрыОткрытияКлиент, Параметры.ПараметрыОткрываемойФормы);

	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Вперед(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_ОткрытьДлительнуюФорму", 0.5, Истина);
	
	#Если МобильныйКлиент Тогда
		//Статистика переходов к неадаптированным формам
		Если Не ПустаяСтрока(ОписаниеНедоступногоДействия) Тогда
			ОписаниеСобытия = ОписаниеНедоступногоДействия;
		ИначеЕсли Не ПустаяСтрока(ОписаниеНедоступнойФункции) Тогда
			ОписаниеСобытия = ОписаниеНедоступнойФункции;
		Иначе
			ОписаниеСобытия = "Прочий сервис";
		КонецЕсли;
		
		СтатистикаИспользованияПриложения.ОтправитьСобытие("PF_form_cont",
			СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(Символ(34)+" ", ОписаниеСобытия, "__"));
	#КонецЕсли
	
КонецПроцедуры

