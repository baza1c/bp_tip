#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	ИмяПрограммы = ОбщегоНазначенияБП.ПредставлениеПрограммыВинительныйПадеж();
	
	Элементы.ДекорацияОписание2.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Элементы.ДекорацияОписание2.Заголовок,
		ИмяПрограммы);
		
	КодДоступа = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.КодДоступа();
	
	Если КодДоступа = Неопределено Тогда
		Элементы.ГруппаИнструкцияПодключение.Видимость = Ложь;
		Возврат
	КонецЕсли;
	Элементы.ГруппаОшибкаПодключения.Видимость = Ложь;
	
	Элементы.ПодключениеСотрудниковОписание.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Элементы.ПодключениеСотрудниковОписание.Заголовок,
		ИмяПрограммы);
		
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("КодДоступа", "");
	ПараметрыТекста.Вставить("Логин",      "");
	
	ИспользоватьЛогин = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ДоступноПодключениеПоЛогинуИПаролю();
	ШаблонТекста = "";
	ПараметрыТекста.КодДоступа = КодДоступа.Представление;
	
	Если ИспользоватьЛогин Тогда
		ПараметрыТекста.Логин = ?(Пользователи.ТекущийПользователь() = Пользователи.СсылкаНеуказанногоПользователя(),
			НСтр("ru = 'адрес электронной почты'"),
			СтрШаблон(НСтр("ru = 'логин <b>%1</b> и пароль'"), ИмяПользователя()));
		ШаблонТекста = НСтр(
			"ru = 'Для входа в мобильное приложение используйте
			|- код доступа <a href=''КодДоступа''>[КодДоступа]</a>
			|или
			|- свой [Логин]'");
	Иначе
		ШаблонТекста = НСтр(
			"ru = 'Для входа в мобильное приложение используйте
			|код доступа <a href=''КодДоступа''>[КодДоступа]</a>'");
	КонецЕсли;
		
	СтрокаСТегами = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста);
	Элементы.ДекорацияШаг2Заголовок.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(СтрокаСТегами);
	
	// Сразу при открытии формы направим запрос на подготовку баланса.
	РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ЗапроситьБаланс();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Сразу запустим обновление содержимого блока "Условия использования".
	НачатьОбновлениеУсловийИспользования();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияAndroidНажатие(Элемент)
	
	АдресСтраницы = "https://play.google.com/store/apps/details?id=com.e1c.ReceiptScanner";
	ПерейтиПоНавигационнойСсылке(АдресСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияiOSНажатие(Элемент)
	
	АдресСтраницы = "https://apps.apple.com/ru/app/id1513613578";
	ПерейтиПоНавигационнойСсылке(АдресСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияШаг2ЗаголовокОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "КодДоступа" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("РегистрСведений.МобильноеПриложениеСканированиеЧеков.Форма.КодДоступа", , ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключениеСотрудниковНастройкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Пользователи" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СкрыватьКомандуПереходаВПомощник", Истина);
		
		ОткрытьФорму("РегистрСведений.МобильноеПриложениеСканированиеЧеков.Форма.Пользователи", ПараметрыФормы, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУсловияИспользованияГиперссылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если РазделениеВключено Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.ГруппаОбслуживающаяОрганизация.Видимость = Не Элементы.ГруппаОбслуживающаяОрганизация.Видимость;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьБлокУсловияИспользования(ПараметрыЗаполнения) Экспорт
	
	// Баланс.
	Баланс = ПараметрыЗаполнения.Баланс;
	Элементы.ГруппаУсловияИспользованияОжиданиеБаланса.Видимость = Ложь;
	Если Баланс = Неопределено Или Баланс.Остаток = Неопределено Тогда
		Элементы.ГруппаУсловияИспользованияОшибкаЗагрузки.Видимость = Истина;
		Элементы.ДекорацияУсловияИспользования2.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаУсловияИспользованияСостояниеБаланса.Видимость = Истина;
	Элементы.ДекорацияУсловияИспользования2.Заголовок =
		СтрШаблон(НСтр("ru='Вы можете загружать до %1 чеков каждые 3 месяца без оплаты.'"), Баланс.РазмерБесплатногоТарифа);
	Элементы.ДекорацияУсловияИспользованияОстаток.Заголовок = ПредставлениеОстаткаБаланса(Баланс.Остаток);
	
	Элементы.ДекорацияУсловияИспользованияОстаток.РасширеннаяПодсказка.Заголовок = Баланс.ПредставлениеОпций;
	
	ЗаполненаДатаПереподключенияБесплатногоТарифа = ЗначениеЗаполнено(Баланс.ДатаПереподключенияБесплатногоТарифа);
	Если ЗаполненаДатаПереподключенияБесплатногоТарифа Тогда
		Элементы.ДекорацияУсловияИспользованияСрокОбновления.Заголовок =
			СтроковыеФункцииКлиент.ФорматированнаяСтрока(
				НСтр("ru='Следующие %1 чеков будут доступны после <b>%2</b>'"),
				Баланс.РазмерБесплатногоТарифа,
				Формат(НачалоДня(Баланс.ДатаПереподключенияБесплатногоТарифа) - 1, "ДЛФ=D"));
	КонецЕсли;
	Элементы.ДекорацияУсловияИспользованияСрокОбновления.Видимость = ЗаполненаДатаПереподключенияБесплатногоТарифа;
	
	Если РазделениеВключено Тогда
		// В режиме сервиса необходимо вывести информацио об обслуживающей организации.
		 НастроитьОбслуживающуюОрганизацию(ПараметрыЗаполнения.ОбслуживающаяОрганизация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОбслуживающуюОрганизацию(ОбслуживающаяОрганизация)
	
	Если ОбслуживающаяОрганизация = Неопределено Тогда
		Элементы.ДекорацияУсловияИспользованияГиперссылка.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	// Обслуживающая организация.
	Элементы.ДекорацияОбслуживающаяОрганизацияНаименование.Заголовок = ОбслуживающаяОрганизация.Наименование;
	
	ЗаполненТелефон = ЗначениеЗаполнено(ОбслуживающаяОрганизация.Телефон);
	Если ЗаполненТелефон Тогда
		Элементы.ДекорацияОбслуживающаяОрганизацияТелефон.Заголовок = ОбслуживающаяОрганизация.Телефон;
	КонецЕсли;
	Элементы.ДекорацияОбслуживающаяОрганизацияТелефон.Видимость = ЗаполненТелефон;
	
	ЗаполненаПочта = ЗначениеЗаполнено(ОбслуживающаяОрганизация.Почта);
	Если ЗаполненаПочта Тогда
		Элементы.ДекорацияОбслуживающаяОрганизацияПочта.Заголовок = ОбслуживающаяОрганизация.Почта;
	КонецЕсли;
	Элементы.ДекорацияОбслуживающаяОрганизацияПочта.Видимость = ЗаполненаПочта;
	
КонецПроцедуры

&НаКлиенте
Функция ПредставлениеОстаткаБаланса(Остаток)
	
	Если Остаток = 0 Тогда
		Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(
			Нстр("ru = 'Остаток — <b><span style=""color: ИнформационныйЦентрКрасныйЦвет"">0 чеков</span></b>'"));
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		Нстр("ru = 'Остаток — %1'"), УчетКассовыхЧековПодотчетныхЛицКлиентСервер.ТекстКоличествоЧеков(Остаток, Истина));
		
КонецФункции

// Запускает фоновый процесс заполнения блока "Условия использования" формы Помощника подключения мобильного приложения.
// После получения данных о тарификации и обслуживающей организации форма обновится.
//
&НаКлиенте
Процедура НачатьОбновлениеУсловийИспользования()
	
	Элементы.ГруппаУсловияИспользованияОжиданиеБаланса.Видимость = Истина;
	Элементы.ГруппаУсловияИспользованияСостояниеБаланса.Видимость = Ложь;
	
	// Подключим обработчик ожидания, запускающий получение сообщений с Шины. Ожидается, что времени
	// таймаута будет достаточно для подготовки сведений на стороне Шины.
	ИнтервалОпроса = 3;
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПроверкуСообщений", ИнтервалОпроса, Истина);
	
	// Подключим обработчик ожидания, который выведет полученные сведения о балансе на форму.
	ИнтервалОпроса = 5;
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьУсловияИспользования", ИнтервалОпроса, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачатьПроверкуСообщений()
	
	НачатьПроверкуСообщений();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НачатьПроверкуСообщений()
	
	РегистрыСведений.МобильноеПриложениеСканированиеЧеков.НачатьПроверкуСообщений();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьУсловияИспользования()
	
	Результат = НачатьОбновлениеУсловийИспользованияСервер(ЭтотОбъект.УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьОбновлениеУсловийИспользования", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьОбновлениеУсловийИспользованияСервер(Знач ИдентификаторФормы)
	
	Возврат УчетКассовыхЧековПодотчетныхЛиц.НачатьОбновлениеУсловийИспользования(ИдентификаторФормы);
	
КонецФункции

// Обработчик завершения длительной операции УчетКассовыхЧековПодотчетныхЛиц.ОбновитьУсловияИспользования.
// Обновляет данные об остатках тарифных опций в блоке "Условия использования" на форме
// ПомощникУстановкиМобильногоПриложенияСканированиеЧеков.
//
// Параметры:
//  Результат           - Структура - результат длительной операции, см. ДлительныеОперацииКлиент.ОжидатьЗавершение
//  ПараметрыЗавершения - Структура
//     * Форма          - УправляемаяФорма - форма помощника подключения мобильного приложения
//
&НаКлиенте
Процедура ЗавершитьОбновлениеУсловийИспользования(Результат, ПараметрыЗавершения) Экспорт // Обработчик оповещения
	
	Если Не ЗначениеЗаполнено(Результат) Или Результат.Статус <> "Выполнено" Тогда
		ОбновитьОтображениеДанных(Новый Массив);
		Возврат;
	КонецЕсли;
	
	ДанныеБлока = ПолучитьДанныеИзВременногоХранилища(Результат.АдресРезультата);
		
	Если ДанныеБлока = Неопределено Тогда
		ОбновитьОтображениеДанных(Новый Массив);
		Возврат;
	КонецЕсли;
	
	ОбновитьБлокУсловияИспользования(ДанныеБлока);
	ОбновитьОтображениеДанных(Элементы.ГруппаУсловияИспользования);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеИзВременногоХранилища(Адрес)
	
	Возврат ПолучитьИзВременногоХранилища(Адрес);
	
КонецФункции

#КонецОбласти