#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	Иначе
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ВидМаркетплейс = Параметры.ВидМаркетплейс;
	
	Если ВидМаркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		Заголовок = СтрШаблон(НСтр("ru = 'Отчеты %1'"),
			Строка(ВидМаркетплейс));
	Иначе
		Заголовок = СтрШаблон(НСтр("ru = 'Отчет о продажах %1'"),
			Строка(ВидМаркетплейс));
	КонецЕсли;
		
	ПериодичностьНеделя = (ВидМаркетплейс =  Перечисления.ВидыМаркетплейсов.Wildberries);
	ПериодичностьМесяц =  НЕ ПериодичностьНеделя;
		
	Элементы.СписокПериодовНеделя.Видимость = ПериодичностьНеделя;
	Элементы.ПредставлениеПериодаМесяц.Видимость = ПериодичностьМесяц;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ЗаполнитьНастройки();
	КонецЕсли;
	
	Элементы.Организация.Видимость = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	КлючСохраненияПоложенияОкна = ВидМаркетплейс;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru='Данная форма не может использоваться самостоятельно.
										|Форма предназначена для открытия
										|из списка документов Отчет комиссионера о продажах!'");
		ПоказатьПредупреждение(, ТекстПредупреждения, НСтр("ru='Форма загрузки из маркетплейса.'"));
		Возврат;
	КонецЕсли;
	
	Если ВидМаркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет") 
		И НЕ НастройкаИнтеграции.Пустая()
		И ОбменСМаркетплейсВызовСервера.ИнтеграцияНастроена(НастройкаИнтеграции) Тогда
		
		ПодключитьОбработчикОжидания("ПолучитьСписокСкладов", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьНастройкаИнтеграцииМаркетплейс"
		И ЗначениеЗаполнено(Параметр) Тогда
		НастройкаИнтеграции = Параметр;
		ИнтеграцияНастроена = ОбменСМаркетплейсВызовСервера.ИнтеграцияНастроена(Параметр);
		
		Если ВидМаркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет") 
			И ИнтеграцияНастроена Тогда
			
			ПолучитьСписокСкладов();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокСкладов() 
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПолучитьСписокСкладовЗавершение", ЭтотОбъект);
	
	Элементы.ДекорацияОжидание.Видимость = Истина;
	
	Результат = ПолучитьСписокСкладовНаСервере(НастройкаИнтеграции, УникальныйИдентификатор);
	Если Результат.Статус = "Выполняется" Тогда
	
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеОЗавершении);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокСкладовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Элементы.ДекорацияОжидание.Видимость = Ложь;
	
	Если НЕ Результат.Статус = "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьСписокСкладовИзСервиса(Результат.АдресРезультата, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокСкладов.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Нет доступных складов для загрузки отчетов'"));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	МагазинПоУмолчанию = Настройки["Магазин"];
	Если ЗначениеЗаполнено(МагазинПоУмолчанию) 
		И ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		
		ХранилищеОбщихНастроек.Сохранить(Строка(НастройкаИнтеграции.УникальныйИдентификатор()), "ЗагрузкаОтчетаМагазин", МагазинПоУмолчанию);
	КонецЕсли;
КонецПроцедуры 

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	Настройки["Магазин"] = ХранилищеОбщихНастроек.Загрузить(Строка(НастройкаИнтеграции.УникальныйИдентификатор()), "ЗагрузкаОтчетаМагазин");
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ВидМаркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		НепроверяемыеРеквизиты.Добавить("Магазин");
	Иначе
		Для каждого Склад Из СписокСкладов Цикл
		
			Если Склад.Значение.ИдентификаторСклада = Магазин 
				И Склад.Значение.ТипСклада = Перечисления.ТипыСкладовМаркетплейс.DBS Тогда
			
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Загрузка по складам типа DBS не поддерживается'"),,"Магазин",,Отказ);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Выберите организацию, по которой требуется загрузить отчет'"),,"Организация",,Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	НастройкаИнтеграции = Неопределено;
	ЗаполнитьНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Настроить(Команда)
	
	РедактироватьНастройку();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчет(Команда)
		
	Если Не ИнтеграцияНастроена Тогда
		РедактироватьНастройку();
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета = ПараметрыОтчета(ДатаНачала, ВидМаркетплейс, НастройкаИнтеграции, Организация, Магазин);
	
	НачатьЗагрузкуОтчета(ПараметрыОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПараметрыОтчета(НачалоПериода, ВидМаркетплейс, НастройкаИнтеграции, Организация, Магазин)
	
	Если ВидМаркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		ОкончаниеПериода = КонецНедели(НачалоПериода);
	Иначе
		ОкончаниеПериода = КонецМесяца(НачалоПериода);
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("НачалоПериода",       НачалоПериода);
	ПараметрыОтчета.Вставить("Организация",         Организация);
	ПараметрыОтчета.Вставить("ОкончаниеПериода",    ОкончаниеПериода);
	ПараметрыОтчета.Вставить("ВидМаркетплейс",      ВидМаркетплейс);
	ПараметрыОтчета.Вставить("НастройкаИнтеграции", НастройкаИнтеграции);
	ПараметрыОтчета.Вставить("ИдентификаторСклада", Магазин);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокСкладовНаСервере(НастройкаИнтеграции, УникальныйИдентификатор)
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, "ОбменСМаркетплейс.СписокСкладовМаркетплейс", НастройкаИнтеграции);
	
КонецФункции

&НаСервере
Процедура УстановитьПериод()
	
	ДатаВходящегоДокумента = ДатаПоследнегоВходящегоРозничногоОтчетаКомиссионера(НастройкаИнтеграции);
	
	Если ВидМаркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		
		Элемент = Элементы.СписокПериодовНеделя;
		// Чтобы избежать дублирования элементов, очищаем список выбора в поле формы
		Элемент.СписокВыбора.Очистить();
		
		НачалоПрошлогоПериода = НачалоНедели(ТекущаяДатаСеанса() - ИнтервалМеждуОтчетамиВБ());
		//ВБ хранит данные за последние 90 дней
		ПределЗагрузки = НачалоНедели(ТекущаяДатаСеанса() - 90*86400);
		
		Для каждого Период Из СписокПериодовВБ(НачалоПрошлогоПериода, ПределЗагрузки) Цикл
			Элемент.СписокВыбора.Добавить(Период.Значение, Период.Представление);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ДатаВходящегоДокумента) Тогда
			
			СледующийПериод = КонецНедели(ДатаВходящегоДокумента)+1;
			ДатаНачала = Мин(СледующийПериод, НачалоПрошлогоПериода);
			
		Иначе
			ДатаНачала = НачалоПрошлогоПериода;
		КонецЕсли;
		
		ДатаНачала = Макс(ПределЗагрузки, ДатаНачала);
		
		ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоНедели(ДатаНачала), КонецНедели(ДатаНачала), Истина);
		
	Иначе
		
		ДатаНачала = НачалоМесяца(НачалоМесяца(ТекущаяДатаСеанса())-1);
		
		Если ЗначениеЗаполнено(ДатаВходящегоДокумента) Тогда
			СледующийПериод = КонецМесяца(ДатаВходящегоДокумента)+1;
			ДатаНачала = Мин(ДатаНачала, СледующийПериод);
		КонецЕсли;
		
		ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоМесяца(ДатаНачала), КонецМесяца(ДатаНачала), Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройки()
	НастройкаИнтеграции = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Организация, ВидМаркетплейс);
	УстановитьПериод(); 
	ИнтеграцияНастроена = ОбменСМаркетплейс.ИнтеграцияНастроена(НастройкаИнтеграции); 
	
	Отказ = Ложь;
	Если ВидМаркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет 
		И ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		
		ЗаполнитьСписокСкладовИзНастройкиИнтеграции(Отказ);
		
		Элементы.ГруппаМагазин.Видимость = Не Отказ;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПредставлениеПериода(НачалоПериода = '00010101', КонецПериода = '00010101')
	
	ТекстПериод = "";
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда 
		Если КонецПериода >= НачалоПериода Тогда
			ТекстПериод = ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(КонецПериода), "ФП = Истина");
		Иначе
			ТекстПериод = "";
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(НачалоПериода) Тогда
		ТекстПериод = ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(Дата(3999, 11, 11)), "ФП = Истина");
		ТекстПериод = СтрЗаменить(ТекстПериод, Сред(ТекстПериод, СтрНайти(ТекстПериод, " - ")), " - ...");
	КонецЕсли;
	
	Возврат ТекстПериод;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДатаПоследнегоВходящегоРозничногоОтчетаКомиссионера(Знач НастройкаИнтеграции, Знач ДатаОтчета = Неопределено, Знач ВидМаркетплейса = Неопределено)
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "Владелец");
	Контрагент = Справочники.НастройкиИнтеграцииМаркетплейс.КонтрагентДоговорНаДату(НастройкаИнтеграции, ДатаОтчета).Контрагент;
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДатаВходящегоДокумента = Дата(1, 1, 1);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ОтчетКомиссионераОПродажах.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И ОтчетКомиссионераОПродажах.ВидОперации = &ВидОперации
	|	И ОтчетКомиссионераОПродажах.Проведен
	|	И &УсловиеПоДатаВходящегодокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтчетКомиссионераОПродажах.ДатаВходящегоДокумента УБЫВ,
	|	ОтчетКомиссионераОПродажах.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ВидОперации", Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах);
	Запрос.УстановитьПараметр("ДатаВходящегоДокумента", ДатаОтчета);
	
	Если ЗначениеЗаполнено(ДатаОтчета) тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УсловиеПоДатаВходящегодокумента",
			"ОтчетКомиссионераОПродажах.ДатаВходящегоДокумента  = &ДатаВходящегоДокумента");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УсловиеПоДатаВходящегодокумента",
			"ОтчетКомиссионераОПродажах.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1,1,1)");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатаВходящегоДокумента = Выборка.ДатаВходящегоДокумента;
	КонецЕСли;
	
	Возврат ДатаВходящегоДокумента;
	
КонецФункции

&НаКлиенте
Процедура НачатьЗагрузкуОтчета(ПараметрыОтчета)
	
	Закрыть(ПараметрыОтчета);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокПериодовВБ( НачалоСписка, ПределЗагрузки)
	
	СписокПериодов = Новый СписокЗначений;
	СписокПериодов.Очистить();
	
	ДатаНачала = НачалоНедели(НачалоСписка);
	ДатаОкончания = НачалоНедели(ПределЗагрузки);
	Пока ДатаНачала >= ДатаОкончания Цикл
		СписокПериодов.Добавить(ДатаНачала, ПолучитьПредставлениеПериода(ДатаНачала, КонецНедели(ДатаНачала)));
		ДатаНачала = НачалоНедели(ДатаНачала - ИнтервалМеждуОтчетамиВБ());
	КонецЦикла;
	
	Возврат СписокПериодов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИнтервалМеждуОтчетамиВБ()
	
	Возврат 7*86400;
	
КонецФункции

&НаКлиенте
Процедура РедактироватьНастройку()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите организацию, для которой требуется настройка'"),,"Организация");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		ОбменСМаркетплейсКлиент.ОткрытьНастройкуИнтеграции(НастройкаИнтеграции, ВидМаркетплейс, , "ЗагрузкаОтчета");
	Иначе
		ОбменСМаркетплейсКлиент.НоваяНастройкаИнтеграции(Организация, ВидМаркетплейс, , "ЗагрузкаОтчета");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	//ограничение API OZON
	ОграничениеСнизу = ДобавитьМесяц(НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()),-5);
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода",     НачалоМесяца(ДатаНачала));
	ПараметрыВыбора.Вставить("КонецПериода",      КонецМесяца(ДатаНачала));
	ПараметрыВыбора.Вставить("ОграничениеСнизу",  ОграничениеСнизу);
	ПараметрыВыбора.Вставить("ВидПериода",        ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", 
		ПараметрыВыбора, Элементы.ПредставлениеПериодаМесяц, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЗавершениеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		ДатаНачала = НачалоМесяца(РезультатВыбора.НачалоПериода);
	КонецЕсли;
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоМесяца(ДатаНачала), КонецМесяца(ДатаНачала), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаOZONОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСкладовИзНастройкиИнтеграции(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", НастройкаИнтеграции);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкладыМаркетплейс.Наименование КАК НаименованиеСклада,
	|	СкладыМаркетплейс.ИдентификаторСклада КАК ИдентификаторСклада,
	|	СкладыМаркетплейс.ИдентификаторСклада КАК ТипСклада
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс.СкладыМаркетплейс КАК СкладыМаркетплейс
	|ГДЕ
	|	СкладыМаркетплейс.Ссылка = &Ссылка
	|	И СкладыМаркетплейс.ТипСклада <> ЗНАЧЕНИЕ(Перечисление.ТипыСкладовМаркетплейс.DBS)";
	
	СписокСкладов.Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Идентификаторы = Новый Структура;
		Идентификаторы.Вставить("ИдентификаторСклада", Формат(Выборка.ИдентификаторСклада, "ЧГ="));
		Идентификаторы.Вставить("ТипСклада", Выборка.ТипСклада);
		
		СписокСкладов.Добавить(
			Идентификаторы, 
			Выборка.НаименованиеСклада);
	КонецЦикла;
		
	ЗаполнитьСписокСкладовНаФорме();
	
	Если СписокСкладов.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСкладовИзСервиса(АдресХранилища, Отказ)
	СписокСкладов.Очистить();
	
	СписокСкладовВрем = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если СписокСкладовВрем = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСписок(СписокСкладов, СписокСкладовВрем);
	
	ЗаполнитьСписокСкладовНаФорме();
	
	НастройкаИнтеграцииОбъект = НастройкаИнтеграции.ПолучитьОбъект();
	НастройкаИнтеграцииОбъект.ОбновитьСписокСкладов(СписокСкладов);
	
	Попытка
		НастройкаИнтеграцииОбъект.Записать();
	Исключение
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ОписаниеОшибки(), ВидМаркетплейс);
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСкладовНаФорме()
	Элементы.Магазин.СписокВыбора.Очистить();
	
	Для каждого НастройкиСклада Из СписокСкладов Цикл
		Идентификаторы = НастройкиСклада.Значение;
		
		ПредставлениеМагазина = СтрШаблон("%1 (%2)", НастройкиСклада.Представление, Идентификаторы.ИдентификаторСклада);
		
		Если Идентификаторы.ТипСклада = Перечисления.ТипыСкладовМаркетплейс.DBS Тогда
			ПредставлениеМагазина = Новый ФорматированнаяСтрока(ПредставлениеМагазина, ,ЦветаСтиля.НедоступныеДанныеЦвет);
		КонецЕсли;
		
		Элементы.Магазин.СписокВыбора.Добавить(
			Идентификаторы.ИдентификаторСклада, 
			ПредставлениеМагазина);
	КонецЦикла;
		
	Если Элементы.Магазин.СписокВыбора.Количество() = 1 Тогда
		Магазин = СписокСкладов[0].Значение.ИдентификаторСклада;
	ИначеЕсли НЕ ЗначениеЗаполнено(Магазин) Тогда
		Магазин = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ИдентификаторСклада");
	КонецЕсли;
		
	Если Элементы.Магазин.СписокВыбора.НайтиПоЗначению(Магазин) = Неопределено Тогда
		Магазин = Неопределено;
	КонецЕсли;
	
	Если Элементы.Магазин.СписокВыбора.Количество() > 0 Тогда
		Элементы.ГруппаМагазин.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

