////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	АдресХранилища = "";
	Если Параметры.Свойство("АдресХранилищаЗачетАвансов", АдресХранилища) Тогда
		ЗагрузитьТаблицуЗачетАвансовИзВременногоХранилища(АдресХранилища);
	КонецЕсли;

	// Сохраним в реквизитах формы ее параметры
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,
		"Дата,ДоговорКонтрагента,Контрагент,Организация,
		|ОстаткиОбороты,РежимОтбораДокументов,
		|ТипыДокументов,СпособЗачетаАвансов,
		|СчетУчетаРасчетовСКонтрагентом, СчетУчетаРасчетовПоАвансам,
		|ИспользуетсяСрокОплаты, СрокОплаты");
	
	Если Параметры.Свойство("ИспользоватьОплатуЧерезПлатежногоАгента") Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,
			"ИспользоватьОплатуЧерезПлатежногоАгента, ОплатаЧерезКомиссионера,
			|Комиссионер, ДоговорКомиссионера,
			|СчетУчетаРасчетовСКомиссионером, СчетУчетаРасчетовСКомиссионеромПоАвансам");
	КонецЕсли;
		
	// В некоторых документах счет расчетов устанавливается при проведении.
	// Если так, то скроем поле, относящиеся к расчетам.
	Если Параметры.СкрытьСчетРасчетов Тогда
		Элементы.СчетУчетаРасчетовСКонтрагентом.Видимость = Ложь;
	КонецЕсли;
	
	// В некоторых документах в качестве счета авансов используется счет расчетов.
	// Если так, то скроем поле, относящиеся к авансам.
	Если Параметры.СкрытьСчетАванса Тогда
		Элементы.СчетУчетаРасчетовПоАвансам.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаАвансы.Видимость = УчетВзаиморасчетов.ПравоУправленияЗачетомАвансов();
	
	// В некоторых документах нельзя изменить способ зачета авансов.
	// Если так, то снимем доступность у полей, относящиеся к способу зачету авансов.
	ДоступенВыборСпособаЗачетаАванса = Истина;
	Если Параметры.Свойство("ДоступенВыборСпособаЗачетаАванса")
		И Не Параметры.ДоступенВыборСпособаЗачетаАванса Тогда
		ДоступенВыборСпособаЗачетаАванса = Ложь;
	КонецЕсли;
		
	// В некоторых видах операций в документе используется только 
	// счет расчетов, а счет авансов нет. Если так, то заблокируем поля,
	// относящиеся к авансам.
	ДоступенЗачетАвансов = Истина;
	Если Параметры.Свойство("ДоступенЗачетАвансов") Тогда
		ДоступенЗачетАвансов = Параметры.ДоступенЗачетАвансов;
	КонецЕсли;

	Если ДоступенЗачетАвансов Тогда
		ЕстьСтрокиЗачетАвансов = ЗачетАвансов.Количество() > 0;
	Иначе
		СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.НеЗачитывать;
		СчетУчетаРасчетовПоАвансам = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		ЗачетАвансов.Очистить();
		Элементы.СпособЗачетаАвансов.Доступность = ОплатаЧерезКомиссионера;
		Элементы.ЗачетАвансов.Доступность = ОплатаЧерезКомиссионера;
	КонецЕсли;
	
	Элементы.СрокОплаты.Видимость = ИспользуетсяСрокОплаты;
	
	Если Не СрокиОплатыДокументов.ПравоРедактирования() Тогда
		Элементы.СрокОплаты.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Элементы.ГруппаКомиссионер.Видимость = ИспользоватьОплатуЧерезПлатежногоАгента;
	
	ВсегдаОтображатьСчетаУчета = Параметры.ВсегдаОтображатьСчетаУчета;
	
	УстановитьВидимостьСчетовУчета();
	
	УправлениеФормой(ЭтаФорма);
	ОбновитьИтоги(ЭтаФорма);

	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.ОбщаяФорма.ВыборПорядкаУчетаРасчетов",
		"ВыборПорядкаУчетаРасчетов",
		НСтр("ru='Новости: Расчеты'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы 
		И (Модифицированность ИЛИ ПеренестиВДокумент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если Модифицированность И НЕ ПеренестиВДокумент Тогда
		
		Отказ = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ВопросСохраненияДанныхЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	ИначеЕсли ПеренестиВДокумент Тогда
		
		Отказ = НЕ ПроверитьЗаполнение();
		
		Если Отказ Тогда
			Модифицированность = Истина;
			ПеренестиВДокумент = Ложь;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

	СтруктураВозврата = Новый Структура;

	Если ПеренестиВДокумент Тогда
		СтруктураВозврата.Вставить("АдресХранилищаЗачетАвансов",     АдресХранилищаЗачетАвансов);
		СтруктураВозврата.Вставить("СчетУчетаРасчетовСКонтрагентом", СчетУчетаРасчетовСКонтрагентом);
		СтруктураВозврата.Вставить("СчетУчетаРасчетовПоАвансам",     СчетУчетаРасчетовПоАвансам);
		СтруктураВозврата.Вставить("СпособЗачетаАвансов",            СпособЗачетаАвансов);
		СтруктураВозврата.Вставить("ИспользуетсяСрокОплаты",         ИспользуетсяСрокОплаты);
		СтруктураВозврата.Вставить("СрокОплаты",                     СрокОплаты);
		
		Если ИспользоватьОплатуЧерезПлатежногоАгента Тогда
			СтруктураВозврата.Вставить("ОплатаЧерезКомиссионера", ОплатаЧерезКомиссионера);
			СтруктураВозврата.Вставить("Комиссионер", Комиссионер);
			СтруктураВозврата.Вставить("ДоговорКомиссионера", ДоговорКомиссионера);
			СтруктураВозврата.Вставить("СчетУчетаРасчетовСКомиссионером", СчетУчетаРасчетовСКомиссионером);
			СтруктураВозврата.Вставить("СчетУчетаРасчетовСКомиссионеромПоАвансам", СчетУчетаРасчетовСКомиссионеромПоАвансам);
		КонецЕсли;
		
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НеТребуютПроверки = Новый Массив;
	
	Если Не СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета() Тогда
		НеТребуютПроверки.Добавить("СчетУчетаРасчетовСКонтрагентом");
		НеТребуютПроверки.Добавить("СчетУчетаРасчетовПоАвансам");
		НеТребуютПроверки.Добавить("СчетУчетаРасчетовСКомиссионером");
		НеТребуютПроверки.Добавить("СчетУчетаРасчетовСКомиссионеромПоАвансам");
	Иначе
		Если НЕ ЗначениеЗаполнено(СчетУчетаРасчетовПоАвансам) И НЕ Параметры.СкрытьСчетАванса И Не ОплатаЧерезКомиссионера Тогда
			Если СпособЗачетаАвансов <> ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.НеЗачитывать") Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Заполнение", НСтр("ru = 'Счет учета расчетов по авансам'"));
				Поле = "СчетУчетаРасчетовПоАвансам";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.СкрытьСчетРасчетов Тогда
			НеТребуютПроверки.Добавить("СчетУчетаРасчетовСКонтрагентом");
		КонецЕсли;
		
		Если Параметры.СкрытьСчетАванса Тогда
			НеТребуютПроверки.Добавить("СчетУчетаРасчетовПоАвансам");
		КонецЕсли;
		
		Если Не(ИспользоватьОплатуЧерезПлатежногоАгента И ОплатаЧерезКомиссионера) Тогда
			НеТребуютПроверки.Добавить("СчетУчетаРасчетовСКомиссионером");
			НеТребуютПроверки.Добавить("СчетУчетаРасчетовСКомиссионеромПоАвансам");
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НеТребуютПроверки);
	
	Если СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.ПоДокументу") Тогда
		
		Если ЗачетАвансов.Количество() = 0 Тогда
		
			Отказ = Истина;
			
			ТекстСообщения = НСтр("ru = 'Не введено ни одной строки с документом аванса!'");
			Поле = "ЗачетАвансов";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
		Иначе
		
			НомерСтроки = 0;
			Для Каждого СтрокаАванса Из ЗачетАвансов Цикл
			
				НомерСтроки = НомерСтроки + 1;
				Префикс = "ЗачетАвансов[%1].";
				Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Префикс, Формат(НомерСтроки - 1, "ЧН=0; ЧГ="));
				
				Если НЕ ЗначениеЗаполнено(СтрокаАванса.ДокументАванса) Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не заполнена колонка ""Документ аванса"" в строке %1 списка'"),
						НомерСтроки);
					Поле = Префикс + "ДокументАванса";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
				КонецЕсли;
			
			КонецЦикла;
		
		КонецЕсли;
	
	КонецЕсли;

	// Чтобы дважды не вызывать сервер, сразу поместим во временное хранилище 
	// таблицу ЗачетАвансов.
	Если Не Отказ Тогда
		АдресХранилищаЗачетАвансов = ПоместитьЗачетАвансовВоВременноеХранилище();
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СпособЗачетаАвансовПриИзменении(Элемент)
	
	Если СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.Автоматически")
			ИЛИ СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.НеЗачитывать") Тогда
		ЗачетАвансов.Очистить();
		ЕстьСтрокиЗачетАвансов = Ложь;
	КонецЕсли;

	ОбновитьИтоги(ЭтаФорма);
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОплатаЧерезКомиссионераПриИзменении(Элемент)
	
	СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.Автоматически");
	ЗачетАвансов.Очистить();
	ОбновитьИтоги(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	УстановитьВидимостьСчетовУчета();
	
КонецПроцедуры

&НаКлиенте
Процедура КомиссионерПриИзменении(Элемент)
	
	КомиссионерПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссионераОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ДоговорКомиссионера) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", Комиссионер);
	ЗначенияЗаполнения.Вставить("Организация", Организация);
	
	Если ТипЗнч(Элемент.ПараметрыВыбора) = Тип("ФиксированныйМассив") Тогда
		Для Каждого ЭлементОтборов Из Элемент.ПараметрыВыбора Цикл
			Если Найти(ЭлементОтборов.Имя, "Отбор.") > 0 Тогда
				ЗначенияЗаполнения.Вставить(СтрЗаменить(ЭлементОтборов.Имя, "Отбор.", ""), ЭлементОтборов.Значение);
			КонецЕсли;
		КонецЦикла
	КонецЕсли;
			
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ДоговорКомиссионера);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссионераСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(ДоговорКомиссионера),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговораКомиссионера();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссионераОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговораКомиссионера();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(
		Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссионераАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, Истина, СтандартнаяОбработка);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссионераОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ЗачетАвансов

&НаКлиенте
Процедура ЗачетАвансовПриИзменении(Элемент)
	
	ЕстьСтрокиЗачетАвансов = ЗачетАвансов.Количество() > 0;
	ОбновитьИтоги(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ЗачетАвансовПослеУдаления(Элемент)

	ЕстьСтрокиЗачетАвансов = ЗачетАвансов.Количество() > 0;
	ОбновитьИтоги(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ЗачетАвансовДокументАвансаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ЗачетАвансов.ТекущиеДанные;
	ТекущиеДанные.СуммаЗачета = 0;
	ОбновитьИтоги(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ЗачетАвансовДокументАвансаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("Дата"                 , Дата);
	Если ОплатаЧерезКомиссионера Тогда
		ПараметрыОбъекта.Вставить("ДоговорКонтрагента", ДоговорКомиссионера);
		ПараметрыОбъекта.Вставить("Контрагент"        , Комиссионер);
		ПараметрыОбъекта.Вставить("СчетУчета"         , ?(ЗначениеЗаполнено(СчетУчетаРасчетовСКомиссионеромПоАвансам), СчетУчетаРасчетовСКомиссионеромПоАвансам, СчетУчетаРасчетовСКомиссионером));
	Иначе
		ПараметрыОбъекта.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
		ПараметрыОбъекта.Вставить("Контрагент"        , Контрагент);
		ПараметрыОбъекта.Вставить("СчетУчета"         , ?(ЗначениеЗаполнено(СчетУчетаРасчетовПоАвансам), СчетУчетаРасчетовПоАвансам, СчетУчетаРасчетовСКонтрагентом));
	КонецЕсли;
	ПараметрыОбъекта.Вставить("Организация"          , Организация);
	ПараметрыОбъекта.Вставить("ОстаткиОбороты"       , ОстаткиОбороты);
	ПараметрыОбъекта.Вставить("ТипыДокументов"       , ТипыДокументов);
	ПараметрыОбъекта.Вставить("РежимОтбораДокументов", РежимОтбораДокументов);

	ПараметрыФормы = Новый Структура("ПараметрыОбъекта", ПараметрыОбъекта);
	ОткрытьФорму("Документ.ДокументРасчетовСКонтрагентом.ФормаВыбора", ПараметрыФормы, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ЗачетАвансовСуммаЗачетаПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)

	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();


	// ЗачетАвансовСуммаЗачета

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗачетАвансовСуммаЗачета");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЗачетАвансов.СуммаЗачета", ВидСравненияКомпоновкиДанных.Равно, 0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Авто>'"));


	// ЗачетАвансов

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗачетАвансов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СпособЗачетаАвансов", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СпособыЗачетаАвансов.ПоДокументу);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЕстьСтрокиЗачетАвансов", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);


	// СчетУчетаРасчетовПоАвансам

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетУчетаРасчетовПоАвансам");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СпособЗачетаАвансов", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СпособыЗачетаАвансов.НеЗачитывать);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Форма.ИтогСуммаЗачета = Форма.ЗачетАвансов.Итог("СуммаЗачета");

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТаблицуЗачетАвансовИзВременногоХранилища(АдресХранилища)

	ТаблицаЗачетАвансов = ПолучитьИзВременногоХранилища(АдресХранилища);
	ЗачетАвансов.Загрузить(ТаблицаЗачетАвансов);

КонецПроцедуры

&НаСервере
Функция ПоместитьЗачетАвансовВоВременноеХранилище()

	ТаблицаЗачетАвансов = ЗачетАвансов.Выгрузить();
	
	АдресЗачетАвансовВХранилище = ПоместитьВоВременноеХранилище(ТаблицаЗачетАвансов, УникальныйИдентификатор);

	Возврат АдресЗачетАвансовВХранилище;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	СпособЗачетаАвансов = Форма.СпособЗачетаАвансов;
	Элементы = Форма.Элементы;
	
	Элементы.СчетУчетаРасчетовПоАвансам.Доступность = Форма.ДоступенЗачетАвансов;
	
	Если СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.НеЗачитывать")
		ИЛИ СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.Автоматически") Тогда
		Элементы.ЗачетАвансов.Доступность 	= Ложь;
	Иначе
		Элементы.ЗачетАвансов.Доступность = Истина;
	КонецЕсли;
	
	Элементы.Комиссионер.Видимость = Форма.ОплатаЧерезКомиссионера;
	Элементы.ДоговорКомиссионера.Видимость = Форма.ОплатаЧерезКомиссионера;
	Элементы.СрокОплаты.Видимость = Форма.ИспользуетсяСрокОплаты
		И Не Форма.ОплатаЧерезКомиссионера;
	
	Элементы.СпособЗачетаАвансов.Доступность = Форма.ОплатаЧерезКомиссионера
		ИЛИ (Форма.ДоступенВыборСпособаЗачетаАванса И Форма.ДоступенЗачетАвансов);
	Элементы.ЗачетАвансов.Доступность = Форма.ОплатаЧерезКомиссионера
		ИЛИ (Форма.ДоступенВыборСпособаЗачетаАванса И Форма.ДоступенЗачетАвансов);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохраненияДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура КомиссионерПриИзмененииНаСервере()
	
	ПараметрыДоговораКомиссионера = ПараметрыСозданияНовогоДоговораКомиссионера();
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКомиссионера,
		Комиссионер, Организация, ПараметрыДоговораКомиссионера.ВидДоговора);
		
КонецПроцедуры

&НаСервере
Функция ПараметрыСозданияНовогоДоговораКомиссионера()
	
	ДоступныеВидыДоговоров = Новый Массив;
	ДоступныеВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	ДоступныеВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
	ДоступныеВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СТранспортнойКомпанией);
	
	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Организация", Организация);
	ПараметрыДоговора.Вставить("Владелец", Комиссионер);
	ПараметрыДоговора.Вставить("ВидДоговора", ДоступныеВидыДоговоров);
	
	Возврат ПараметрыДоговора;
	
КонецФункции

#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ВидимостьСчетУчетаРасчетовСКонтрагентом = Не Параметры.СкрытьСчетРасчетов;
	ВидимостьСчетУчетаРасчетовПоАвансам = Не Параметры.СкрытьСчетАванса;
	ВидимостьСчетУчетаРасчетовСКомиссионером = Истина;
	ВидимостьСчетУчетаРасчетовСКомиссионеромПоАвансам = Истина;
	
	Если Не ВсегдаОтображатьСчетаУчета Тогда
		ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
		
		ВидимостьСчетУчетаРасчетовСКонтрагентом = ВидимостьСчетУчетаРасчетовСКонтрагентом
			И ПользовательУправляетСчетамиУчета;
			
		ВидимостьСчетУчетаРасчетовПоАвансам = ВидимостьСчетУчетаРасчетовПоАвансам
			И ПользовательУправляетСчетамиУчета;
			
		ВидимостьСчетУчетаРасчетовСКомиссионером = ВидимостьСчетУчетаРасчетовСКомиссионером
			И ПользовательУправляетСчетамиУчета;
			
		ВидимостьСчетУчетаРасчетовСКомиссионеромПоАвансам = ВидимостьСчетУчетаРасчетовСКомиссионеромПоАвансам
			И ПользовательУправляетСчетамиУчета;
	КонецЕсли;
	
	ВидимостьСчетУчетаРасчетовПоАвансам = ВидимостьСчетУчетаРасчетовПоАвансам
		И Не Параметры.СкрытьСчетАванса
		И Не ОплатаЧерезКомиссионера;
	
	ВидимостьСчетУчетаРасчетовСКомиссионером = ВидимостьСчетУчетаРасчетовСКомиссионером
		И ОплатаЧерезКомиссионера;
		
	ВидимостьСчетУчетаРасчетовСКомиссионеромПоАвансам = ВидимостьСчетУчетаРасчетовСКомиссионеромПоАвансам
		И ОплатаЧерезКомиссионера;
		
	Элементы.СчетУчетаРасчетовСКонтрагентом.Видимость = ВидимостьСчетУчетаРасчетовСКонтрагентом;
	Элементы.СчетУчетаРасчетовПоАвансам.Видимость = ВидимостьСчетУчетаРасчетовПоАвансам;
	Элементы.СчетУчетаРасчетовСКомиссионером.Видимость = ВидимостьСчетУчетаРасчетовСКомиссионером;
	Элементы.СчетУчетаРасчетовСКомиссионеромПоАвансам.Видимость = ВидимостьСчетУчетаРасчетовСКомиссионеромПоАвансам;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаСрокОплаты);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаСчетаРасчетов);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаАвансы);
	
	ЭтотОбъект.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Авто;
	
	АдаптироватьМКСписокФормы();
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "ЗачетАвансов");
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыДобавитьСтроку = "ЗачетАвансовДобавить";
	
	Элементы.Переместить(Элементы.СпособЗачетаАвансов, ЭтотОбъект, Элементы.ГруппаАвансы);
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыКоманднаяПанель(ЭтотОбъект,
		Элементы.ЗачетАвансов, Элементы.ГруппаАвансы, КомандыПоля);
		
	Элементы.ГруппаАвансы.ОтображатьЗаголовок = Истина;
	Элементы.ЗачетАвансовСуммаЗачета.Ширина = 8;
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККолонкуТабличногоПоля(Элементы.ЗачетАвансовДокументАванса,
		Истина,
		ВажностьПриОтображении.Авто);
	Элементы.ЗачетАвансовДокументАванса.ПодсказкаВвода = НСтр("ru = 'Нажмите для выбора'");
КонецПроцедуры

#КонецОбласти

