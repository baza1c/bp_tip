#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("ТолькоПросмотр", Параметры.ТолькоПросмотр);
	ПередаваемыеПараметры.Вставить("Организация", Организация);
	ПередаваемыеПараметры.Вставить("Статформа", Параметры.Статформа);
	ПередаваемыеПараметры.Вставить("НачалоПериода", Параметры.НачалоПериода);
	ПередаваемыеПараметры.Вставить("ТоргующаяСтранаКод", Параметры.ТоргующаяСтранаКод);
	
	Если Параметры.НаправлениеПеремещения = "ЭК" Тогда
		СписокТиповДокументовЭкспорт(Организация, ТипОснования);
	Иначе
		СписокТиповДокументовИмпорт(Организация, ТипОснования);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КоличествоТиповОснований = ТипОснования.Количество();
	
	Если КоличествоТиповОснований = 0 Тогда
		
		Элементы.Декорация.Заголовок = НСтр("ru='Отсутствуют документы, на основании которых можно заполнить отчет.'");
		Элементы.ГруппаДекораций.Видимость = Истина;
		
		ТипОснования = "";
		Элементы.ТипОснования.Видимость = Ложь;
		
		Элементы.ФормаКомандаОК.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаКомандаОК.Ширина = 7;
		
		Элементы.ФормаКомандаОтмена.Видимость = Ложь;
		
		Заголовок = "";
		
		Возврат;
		
	КонецЕсли;
	
	Элементы.Декорация.Видимость = Ложь;
	
	Если КоличествоТиповОснований = 1 Тогда
		
		ОткрытьФормуВыбораОснования(ТипОснования, 0, ПередаваемыеПараметры);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТипОснованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьФормуВыбораОснования(ТипОснования, Элементы.ТипОснования.ТекущаяСтрока ,ПередаваемыеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ТипОснования.Количество() > 0 Тогда
		
		ОткрытьФормуВыбораОснования(ТипОснования, Элементы.ТипОснования.ТекущаяСтрока, ПередаваемыеПараметры);
		
	Иначе
		
		ОписаниеОповещенияОЗакрытии = Неопределено;
		Закрыть(Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ОписаниеОповещенияОЗакрытии = Неопределено;
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуВыбораОснования(Знач ТипОснования, Знач Индекс, Знач ПередаваемыеПараметры)
	
	ПараметрыФормы = Новый Структура;
	
	Если ТипОснования[Индекс].Значение = "ЗаявлениеОВвозеТоваров" Тогда
		
		ИмяФормыВыбора = "Документ.ЗаявлениеОВвозеТоваров.ФормаВыбора";
		
	ИначеЕсли ТипОснования[Индекс].Значение = "РеализацияТоваровУслуг" Тогда
		
		ПараметрыФормы.Вставить("ЭлектроннаяТорговаяПлощадка", Истина);
		ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
		
		ИмяФормыВыбора = "Документ.РеализацияТоваровУслуг.ФормаВыбора";
		
	ИначеЕсли ТипОснования[Индекс].Значение = "ОтчетКомиссионераОПродажах" Тогда
		
		ПараметрыФормы.Вставить("ВидОперации",
			ПредопределенноеЗначение("Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОПродажах"));
		ПараметрыФормы.Вставить("Покупатель");
		
		ИмяФормыВыбора = "Документ.ОтчетКомиссионераОПродажах.Форма.ФормаВыбораДляСтатформы";
		
	КонецЕсли;
	
	Для Каждого Параметр Из ПередаваемыеПараметры Цикл
		
		ПараметрыФормы.Вставить(Параметр.Ключ, Параметр.Значение);
		
	КонецЦикла;
		
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, ВладелецФормы,,,,
		ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ОписаниеОповещенияОЗакрытии = Неопределено;
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокТиповДокументовЭкспорт(Знач Организация, СписокТиповДокументов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	1 КАК ТипДанных
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетКомиссионераОПродажах.Ссылка,
	|	2
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипДанных = 1 Тогда
			СписокТиповДокументов.Добавить("РеализацияТоваровУслуг", НСтр("ru='Реализация (акты, накладные, УПД)'"));
		Иначе
			СписокТиповДокументов.Добавить("ОтчетКомиссионераОПродажах", НСтр("ru='Отчет комиссионера (агента)'"));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокТиповДокументовИмпорт(Организация, СписокТиповДокументов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаявлениеОВвозеТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаявлениеОВвозеТоваров КАК ЗаявлениеОВвозеТоваров
	|ГДЕ
	|	НЕ ЗаявлениеОВвозеТоваров.ПометкаУдаления
	|	И ЗаявлениеОВвозеТоваров.Организация = &Организация";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		СписокТиповДокументов.Добавить("ЗаявлениеОВвозеТоваров", НСтр("ru='Заявление о ввозе товаров'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти