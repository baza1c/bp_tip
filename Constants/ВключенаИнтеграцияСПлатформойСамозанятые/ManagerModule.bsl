#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьПриОбновлении() Экспорт
	
	РезультатПроверки = РезультатПроверкиАктуальности();
	
	Если РезультатПроверки.ИзменитьЗначение Тогда
		МенеджерЗначения = Константы.ВключенаИнтеграцияСПлатформойСамозанятые.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = РезультатПроверки.АктуальноеЗначение;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьЗначениеПриНеобходимости() Экспорт

	РезультатПроверки = РезультатПроверкиАктуальности();
	
	Если РезультатПроверки.ИзменитьЗначение Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ВключенаИнтеграцияСПлатформойСамозанятые.Установить(РезультатПроверки.АктуальноеЗначение);
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РезультатПроверкиАктуальности()

	Результат = Новый Структура("ИзменитьЗначение, АктуальноеЗначение", Ложь, Неопределено);
	ТекущееЗначение = Константы.ВключенаИнтеграцияСПлатформойСамозанятые.Получить();
	ПодключенныеОрганизации = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПодключенныеОрганизации();
	АктуальноеЗначение = ПодключенныеОрганизации.Количество() > 0;
	Результат.ИзменитьЗначение = ТекущееЗначение <> АктуальноеЗначение;
	Если Результат.ИзменитьЗначение Тогда
		Результат.АктуальноеЗначение = АктуальноеЗначение;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли