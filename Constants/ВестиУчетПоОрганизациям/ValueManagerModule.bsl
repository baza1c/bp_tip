#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТарификацияБП.КонстантаФункциональностиПередЗаписью(Метаданные().Имя, Значение, Отказ);
	
	Если Значение И ВариантыПриложений.ЭтоБазоваяВерсия() Тогда
		Значение = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РежимТакси = Константы.ИнтерфейсТакси.Получить();
	РежимИнтерфейсВерсии77 = Константы.ИнтерфейсВерсии77.Получить();
	РежимТаксиПростой = Константы.ИнтерфейсТаксиПростой.Получить();
	
	ИнтерфейсДопускаетУчетПоНесколькимОрганизациям = РежимТакси Или РежимТаксиПростой Или РежимИнтерфейсВерсии77;
	
	Если ИнтерфейсДопускаетУчетПоНесколькимОрганизациям И Не Значение И Справочники.Организации.КоличествоОрганизаций() > 1 Тогда
		СообщениеОбОшибке = 
			НСтр("ru = 'Невозможно отключить учет по организациям. В справочнике организаций присутствует несколько элементов.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	Если Не Значение И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		Константы.ИспользоватьНесколькоОрганизаций.Установить(Ложь);
	КонецЕсли;
	
	Константы.ВестиУчетПоОрганизациям.УстановитьЗначенияСвязанныхКонстант(Значение);
	
	УстановитьПривилегированныйРежим(Ложь); 
	
КонецПроцедуры

#КонецЕсли
