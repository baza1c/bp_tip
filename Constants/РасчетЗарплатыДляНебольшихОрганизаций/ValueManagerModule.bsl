#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ)
	
		
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;		
	
	Если Значение Тогда
		
		Если Константы.ИспользоватьНачислениеЗарплаты.Получить() Тогда
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
				МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
				
				Если НЕ МодульРасчетЗарплатыДляНебольшихОрганизаций.РасчетЗарплатыДляНебольшихОрганизацийВозможен() Тогда
					
					ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						РасчетЗарплатыДляНебольшихОрганизацийПереопределяемый.ТекстСообщенияОПревышенииМаксимальноДопустимогоКоличестваРаботающихСотрудников(),
						МодульРасчетЗарплатыДляНебольшихОрганизаций.ПорогЗапрета());
						
					ВызватьИсключение ТекстИсключения;
					
				КонецЕсли;
				
			Иначе
				ВызватьИсключение НСтр("ru = 'Включать режим расчета зарплаты для небольшой организации можно только при использовании начисления зарплаты'");
			КонецЕсли;
		КонецЕсли;
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Значение Тогда
		РасчетЗарплатыДляНебольшихОрганизацийСобытия.ПриОтключенииРасчетаЗарплатыДляНебольшихОрганизаций();
	КонецЕсли;
	
	КабинетСотрудникаБазовый.РасчетЗарплатыДляНебольшихОрганизацийПриЗаписи();
	
КонецПроцедуры


#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли