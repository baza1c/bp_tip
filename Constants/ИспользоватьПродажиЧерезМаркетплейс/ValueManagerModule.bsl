#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТарификацияБП.КонстантаФункциональностиПередЗаписью(Метаданные().Имя, Значение, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Отказ 
		ИЛИ НЕ ЭтотОбъект.Значение Тогда
		Возврат;
	КонецЕсли;
		
	//Создадим контрагентов маркетплейсы
	Маркетплейсы = Новый Массив;
	Маркетплейсы.Добавить(ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB(ТекущаяДатаСеанса()));
	Маркетплейсы.Добавить(ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыOzon());
	
	Для Каждого РеквизитыМаркетплейса Из Маркетплейсы Цикл
		
		ИНН = РеквизитыМаркетплейса.ИНН;
		КПП = РеквизитыМаркетплейса.КПП;
		Наименование = РеквизитыМаркетплейса.НаименованиеПолное;
		
		Контрагент = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ИНН, КПП);
		Если Контрагент = Неопределено Тогда
			Контрагент = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ИНН);
		КонецЕсли;
		
		Если Контрагент = Неопределено
			И ЗначениеЗаполнено(Наименование) Тогда
			КонтрагентОбъект                           = Справочники.Контрагенты.СоздатьЭлемент();
			КонтрагентОбъект.Наименование              = Наименование;
			КонтрагентОбъект.НаименованиеПолное        = Наименование;
			КонтрагентОбъект.ИНН                       = ИНН;
			КонтрагентОбъект.КПП                       = КПП;
			КонтрагентОбъект.ЮридическоеФизическоеЛицо = ?(СтрДлина(СокрЛП(ИНН)) = 12, Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо, Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
			КонтрагентОбъект.Записать();
			Контрагент = КонтрагентОбъект.Ссылка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Контрагент) И НЕ Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
			ОсновнаяОрганизация = Справочники.Организации.ОрганизацияПоУмолчанию();
			Если ЗначениеЗаполнено(ОсновнаяОрганизация) Тогда
				РеквизитыДоговора = Новый Структура;
				РеквизитыДоговора.Вставить("Владелец", Контрагент);
				РеквизитыДоговора.Вставить("Организация", ОсновнаяОрганизация);
				РеквизитыДоговора.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
				РеквизитыДоговора.Вставить("Наименование", НСтр("ru = 'Основной договор'"));
				
				ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
				ЕстьДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
					ДоговорКонтрагента,
					РеквизитыДоговора.Владелец,
					РеквизитыДоговора.Организация,
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РеквизитыДоговора.ВидДоговора));
				
				Если Не ЕстьДоговорКонтрагента Тогда
					ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", РеквизитыДоговора);
					ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли