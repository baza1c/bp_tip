#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступенПолныйСписок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ДоступенПолныйСписок", Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВыбратьИзПолногоСписка",
		"Видимость",
		ДоступенПолныйСписок);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.НачисленияФормаСпискаПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Элементы.Список.РежимВыбора = Истина;
	Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		ВАрхиве = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ТекущаяСтрока, "ВАрхиве");
		Если ВАрхиве Тогда
			// Если при открытии формы выбора передано не действующее начисление, изменим отбор в списке.
			ПоказыватьНачисленияВАрхиве = Истина;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьОтборИспользуемых(Список.Отбор, ПоказыватьНачисленияВАрхиве);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьИзПолногоСписка(Команда)
	
	ВыбратьИзПолногоСпискаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыбратьИзПолногоСпискаНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВыбратьИзПолногоСписка",
		"Видимость",
		Ложь);
		
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьЭлементыНастроек(КоллекцияЭлементов)
	
	Для Каждого ЭлементНастроек Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементНастроек) = Тип("ЭлементОтбораКомпоновкиДанных")
			Или ТипЗнч(ЭлементНастроек) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ЭлементНастроек.Использование = Ложь;
		ИначеЕсли ТипЗнч(ЭлементНастроек) = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
			ОтключитьЭлементыНастроек(ЭлементНастроек.Элементы)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборИспользуемых(ГруппаОтбора, ПоказыватьВАрхиве)
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ГруппаОтбора, "ВАрхиве");
	Если НЕ ПоказыватьВАрхиве Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "ВАрхиве", Ложь);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти