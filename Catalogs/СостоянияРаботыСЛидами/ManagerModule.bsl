#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает не помеченные на удаление состояния работы с лидами.
//
// Возвращаемое значение:
//   ТаблицаЗначений
//     *Ссылка - СправочникСсылка.СостоянияРаботыСЛидами - ссылка состояния
//     *Наименование - Строка - наименование состояния
//     *ЦветСостояния - ПеречислениеСсылка.ЦветаСостоянийРаботыСЛидами - цвет состояния
//     *Порядок - Число - порядок состояния
Функция АктуальныеСостояния() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияРаботыСЛидами.Ссылка КАК Ссылка,
	|	СостоянияРаботыСЛидами.Наименование КАК Наименование,
	|	СостоянияРаботыСЛидами.ЦветСостояния КАК ЦветСостояния,
	|	СостоянияРаботыСЛидами.РеквизитДопУпорядочивания КАК Порядок
	|ИЗ
	|	Справочник.СостоянияРаботыСЛидами КАК СостоянияРаботыСЛидами
	|ГДЕ
	|	НЕ СостоянияРаботыСЛидами.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеквизитДопУпорядочивания";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции

// Возвращает RGB цвет состояния.
//
// Параметры:
//   ЦветСостояния - ПеречислениеСсылка.ЦветаСостоянийРаботыСЛидами - цвет состояния
//
// Возвращаемое значение:
//   Цвет - RGB цвет.
Функция ЦветЭлементаСостояния(ЦветСостояния) Экспорт
	
	Если ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Лаймовый Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиЛаймовый;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Красный Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиКрасный;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Оранжевый Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиОранжевый;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Желтый Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиЖелтый;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Зеленый Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиЗеленый;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Синий Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиСиний;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Голубой Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиГолубой;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Фиолетовый Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиФиолетовый;
	ИначеЕсли ЦветСостояния = Перечисления.ЦветаСостоянийРаботыСЛидами.Розовый Тогда
		ЦветRGB = ЦветаСтиля.ЦветМеткиРозовый;
	Иначе
		ЦветRGB = ЦветаСтиля.ЦветМеткиСерый;
	КонецЕсли;
	
	Возврат ЦветRGB;
	
КонецФункции

// Возвращает первое по порядку актуальное состояние.
//
// Возвращаемое значение:
//   СправочникСсылка.СостоянияРаботыСЛидами - состояние.
Функция СостояниеПоУмолчанию() Экспорт
	
	СписокСостояний = АктуальныеСостояния();
	
	Если СписокСостояний.Количество() > 0 Тогда
		Возврат СписокСостояний[0].Ссылка;
	КонецЕсли;
	
	Возврат ПустаяСсылка();
	
КонецФункции

// Возвращает последнее по порядку актуальное состояние с переданным условием изменения.
//
// Параметры:
//   УсловиеИзменения - ПеречислениеСсылка.УсловияИзмененияСостоянияРаботыСЛидами - условие изменения.
// 
// Возвращаемое значение:
//   СправочникСсылка.СостоянияРаботыСЛидами - состояние по условию изменения,
//   Неопределено - если состояние не найдено.
Функция СостояниеПриУсловииИзменения(УсловиеИзменения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияРаботыСЛидами.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СостоянияРаботыСЛидами КАК СостоянияРаботыСЛидами
	|ГДЕ
	|	СостоянияРаботыСЛидами.УсловиеИзменения = &УсловиеИзменения
	|	И НЕ СостоянияРаботыСЛидами.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СостоянияРаботыСЛидами.РеквизитДопУпорядочивания УБЫВ";
	
	Запрос.УстановитьПараметр("УсловиеИзменения", УсловиеИзменения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура заполняет элементы справочника при включении функциональности по лидам
//
Процедура ЗаполнитьЗначенияПоУмолчанию() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено // В подчиненных узлах РИБ не выполняется, чтобы не было дублей
		Или СостоянияЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПоУмолчанию = ЗначенияПоУмолчанию();
	
	Для Каждого ОписаниеЗначения Из ЗначенияПоУмолчанию Цикл

		Попытка
			СостояниеОбъект = СоздатьЭлемент();
			СостояниеОбъект.Наименование = ОписаниеЗначения.Наименование;
			СостояниеОбъект.ЦветСостояния = ОписаниеЗначения.Цвет;
			СостояниеОбъект.Записать();
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось создать состояние работы с лидами: %1 по причине:
					|%2'"),
					ОписаниеЗначения.Наименование, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации("Данные.Добавление", УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Справочники.СостоянияРаботыСЛидами, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗначенияПоУмолчанию()
	
	СостоянияПоУмолчанию = Новый ТаблицаЗначений;
	СостоянияПоУмолчанию.Колонки.Добавить("Наименование");
	СостоянияПоУмолчанию.Колонки.Добавить("Цвет");
	
	ОписаниеСостояния = СостоянияПоУмолчанию.Добавить();
	ОписаниеСостояния.Наименование = НСтр("ru = 'Новый'");
	ОписаниеСостояния.Цвет = Перечисления.ЦветаСостоянийРаботыСЛидами.Фиолетовый;
	
	ОписаниеСостояния = СостоянияПоУмолчанию.Добавить();
	ОписаниеСостояния.Наименование = НСтр("ru = 'Переговоры'");
	ОписаниеСостояния.Цвет = Перечисления.ЦветаСостоянийРаботыСЛидами.Оранжевый;
	
	ОписаниеСостояния = СостоянияПоУмолчанию.Добавить();
	ОписаниеСостояния.Наименование = НСтр("ru = 'Решение о покупке'");
	ОписаниеСостояния.Цвет = Перечисления.ЦветаСостоянийРаботыСЛидами.Зеленый;
	
	Возврат СостоянияПоУмолчанию;
	
КонецФункции

Функция СостоянияЗаполнены()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияРаботыСЛидами.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СостоянияРаботыСЛидами КАК СостоянияРаботыСЛидами";
	
	РезультаЗапроса = Запрос.Выполнить();
	Возврат Не РезультаЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли