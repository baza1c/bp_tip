#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Находит и, при необходимости, создает элемент справочника , соответствующий переданным в параметре условиям.
// 
// Параметры:
//  ИсточникДанных - СтрокаТабличнойЧасти
// 
// Возвращаемое значение:
//  СправочникСсылка.НазначенияИспользованияНМА - Ссылка на элемент, соответствующий условиям.
//
Функция НазначениеИспользования(ИсточникДанных) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	
	// Названия параметров запроса, а именно наличие постфикса "Затрат", для реквизитов "Счет" и "Подразделение"
	// обусловлено структурой таблиц, используемых в документе "Поступление товаров и услуг".
	// Названия полей этих таблиц, в свою очередь, выбраны в соответствии с метаданными аналитики затрат табличной части "Услуги":
	// "СчетЗатрат", "ПодразделениеЗатрат", "Субконто1", "Субконто2, "Субконто3".
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НазначенияИспользованияНМА.Ссылка КАК НазначениеИспользования
	|ИЗ
	|	Справочник.НазначенияИспользованияНМА КАК НазначенияИспользованияНМА
	|ГДЕ
	|	НазначенияИспользованияНМА.Владелец = &НематериальныйАктив
	|	И НазначенияИспользованияНМА.Счет = &СчетЗатрат
	|	И НазначенияИспользованияНМА.Подразделение = &ПодразделениеЗатрат
	|	И НазначенияИспользованияНМА.Субконто1 = &Субконто1
	|	И НазначенияИспользованияНМА.Субконто2 = &Субконто2
	|	И НазначенияИспользованияНМА.Субконто3 = &Субконто3
	|	И НазначенияИспользованияНМА.ДатаНачалаСписания = &ДатаНачалаСписания
	|	И НазначенияИспользованияНМА.ДатаОкончанияСписания = &ДатаОкончанияСписания
	|	И НЕ НазначенияИспользованияНМА.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НазначенияИспользованияНМА.Ссылка";

	ОписаниеПараметровЗапроса = Запрос.НайтиПараметры();
	
	Для Каждого ОписаниеПараметра Из ОписаниеПараметровЗапроса Цикл
		Запрос.УстановитьПараметр(ОписаниеПараметра.Имя, ИсточникДанных[ОписаниеПараметра.Имя]);
	КонецЦикла;

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	Если Выборка.Следующий() Тогда
		Возврат Выборка.НазначениеИспользования;
	Иначе
		СправочникОбъект = СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(СправочникОбъект, ИсточникДанных);
		СправочникОбъект.Владелец = ИсточникДанных.НематериальныйАктив;
		СправочникОбъект.Подразделение = ИсточникДанных.ПодразделениеЗатрат;
		СправочникОбъект.Счет = ИсточникДанных.СчетЗатрат;
		СправочникОбъект.УстановитьНаименование();
		СправочникОбъект.Записать();

		Возврат СправочникОбъект.Ссылка;

	КонецЕсли;

КонецФункции

// Конструктор, создающий структуру, на основании которой заполняется элемент справочника.
// 
// Возвращаемое значение:
//  Структура - Новый назначение использования:
//   * НематериальныйАктив - СправочникСсылка.НематериальныеАктивы
//   * СчетЗатрат - ПланСчетовСсылка.Хозрасчетный
//   * ПодразделениеЗатрат - СправочникСсылка.ПодразделенияОрганизаций
//   * Субконто1 - Неопределено
//               - Характеристика.ВидыСубконтоХозрасчетные
//   * Субконто2 - Неопределено
//               - Характеристика.ВидыСубконтоХозрасчетные
//   * Субконто3 - Неопределено
//               - Характеристика.ВидыСубконтоХозрасчетные
//   * ДатаНачалаСписания - Дата
//   * ДатаОкончанияСписания - Дата
//
Функция НовыйНазначениеИспользования() Экспорт
	
	Сведения = Новый Структура;
	
	Сведения.Вставить("НематериальныйАктив", Справочники.НематериальныеАктивы.ПустаяСсылка());
	Сведения.Вставить("СчетЗатрат", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Сведения.Вставить("ПодразделениеЗатрат", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Сведения.Вставить("Субконто1", Неопределено);
	Сведения.Вставить("Субконто2", Неопределено);
	Сведения.Вставить("Субконто3", Неопределено);
	Сведения.Вставить("ДатаНачалаСписания", Дата(1, 1, 1));
	Сведения.Вставить("ДатаОкончанияСписания", Дата(1, 1, 1));
	
	Возврат Сведения;
	
КонецФункции

// Возвращает ссылку на документ приобретения права с указанным назначением использования.
//
// Параметры:
//   НазначениеИспользования - СправочникСсылка.НазначенияИспользованияНМА
//
// Возвращаемое значение:
//   ДокументСсылка.ПоступлениеТоваровУслуг
//
Функция ПриобретениеПравПоНазначениюИспользования(НазначениеИспользования) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("НазначениеИспользования", НазначениеИспользования);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпособыОтраженияРасходов.Регистратор КАК СсылкаНаДокумент
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет КАК СпособыОтраженияРасходов
	|ГДЕ
	|	СпособыОтраженияРасходов.СпособОтраженияРасходов = &НазначениеИспользования
	|	И СпособыОтраженияРасходов.Активность
	|	И СпособыОтраженияРасходов.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпособыОтраженияРасходов.МоментВремени";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СсылкаНаДокумент;	
	КонецЕсли;
	
	Возврат Документы.ПоступлениеТоваровУслуг.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#КонецЕсли