#Область ПрограммныйИнтерфейс
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	РежимОборудование = Параметры.Свойство("ИдентификаторОборудования", ИдентификаторОборудования);
	Элементы.СтраницыПредупреждение.ТекущаяСтраница = ?(РежимОборудование, Элементы.ОткрытиеСмены, Элементы.НачалоРаботы);
	
	НапомнитьПозже = Истина;
	
	Если РежимОборудование Тогда
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(СтроковыеФункции.ФорматированнаяСтрока("Драйвер <b>%1</b> устарел."+Символы.ПС, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИдентификаторОборудования, "Наименование")));
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Элементы.ПредупреждениеОборудование.Заголовок);
		
		Элементы.ПредупреждениеОборудование.Заголовок = НОвый ФорматированнаяСтрока(МассивСтрок);
	Иначе
		Элементы.СтраницыИнтерфейс.ТекущаяСтраница = 
			?(ОбщегоНазначенияБП.ЭтоПростойИнтерфейс(), Элементы.ПростойИнтерфейс, Элементы.ПолныйИнтерфейс);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекстПредупреждениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПоддержкаОборудования" Тогда
		СтандартнаяОбработка = Ложь;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПереходПоСсылкеЗавершение", ЭтотОбъект);
		ОткрытьФорму("Обработка.ПанельАдминистрированияБПО.Форма",,,,,,ОписаниеОповещения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если НЕ НапомнитьПозже Тогда
		БольшеНеНапоминатьНаСервере(ИдентификаторОборудования);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПереходПоСсылкеЗавершение(Значение, ДополнительныеПараметры) Экспорт
	БольшеНеНапоминатьНаСервере(ИдентификаторОборудования);
	
	Закрыть();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура БольшеНеНапоминатьНаСервере(ИдентификаторОборудования)
	ВерсияБиблиотеки = ОбновлениеИнформационнойБазы.ВерсияИБ("БиблиотекаПодключаемогоОборудования");
	Если ЗначениеЗаполнено(ИдентификаторОборудования) Тогда
		РегистрыСведений.ОборудованиеПоОрганизациям.УстановитьВерсиюПроверкиДрайверов(ИдентификаторОборудования, ВерсияБиблиотеки);
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		Константы.ВерсияПредупрежденияДрайвераОборудования.Установить(ВерсияБиблиотеки);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти




