#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//@skip-check doc-comment-parameter-section
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция Обновить(Организация, Быстро = Истина) Экспорт
	
	ДатаИзменения = РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.ДатаИзменения(Организация);
	
	Если Быстро И ДанныеАктуальны(ДатаИзменения) Тогда
		// Получение не требуется
		Возврат Истина;
	КонецЕсли;
	
	Если ОповещенияПлатформыСамозанятые.ПолучитьОповещения(Организация) Тогда
		ОповещенияПлатформыСамозанятые.ОбновитьОповещения(Организация);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает идентифкаторы оповещений. Порядок элементов сохраняется.
// 
// Параметры:
//  СписокОповещений - Массив из СправочникСсылка.ОповещенияПлатформыСамозанятые
// 
// Возвращаемое значение:
//  Массив из Строка - Идентифкаторы оповещений
//
Функция ИдентификаторыОповещений(СписокОповещений) Экспорт
	
	Идентификаторы = Новый Массив;
	
	//@skip-check bsl-legacy-check-string-literal
	КодыДоставленных = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокОповещений, "Код");
	Для Каждого Оповещение Из СписокОповещений Цикл
		Код = КодыДоставленных[Оповещение];
		Если Код <> Неопределено Тогда
			Идентификаторы.Добавить(Код);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Идентификаторы;
	
КонецФункции

// Читает результат запроса к платформе Самозанятые в справочник ОповещенияПлатформыСамозанятые
// 
// Параметры:
//  РезультатЗапроса - Структура - результат запроса
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.ОповещенияПлатформыСамозанятые - доставленные оповещения в порядке Даты создания
//
Функция ПрочитатьРезультатЗапроса(РезультатЗапроса) Экспорт
	
	ДоставленныеОповещения = Новый Массив;
	
	ТаблицаОповещений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗапроса, "Оповещения");
	Если Не ЗначениеЗаполнено(ТаблицаОповещений) Тогда
		Возврат ДоставленныеОповещения;
	КонецЕсли;
	
	ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗапроса, "ИНН");
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		Возврат ДоставленныеОповещения;
	КонецЕсли;
	
	Организация = Справочники.Организации.НайтиОрганизацию(ИНН);
	Если Не ЗначениеЗаполнено(Организация) Тогда
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'Не найдена организация по ИНН: %1'"),
			ИНН);
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация, , ,
			КомментарийЖурналаРегистрации);
		Возврат ДоставленныеОповещения;
	КонецЕсли;
	
	ТаблицаОповещений.Сортировать("ДатаСоздания");
	
	// Оповещений в результате запроса может оказаться много. Объединим запись в транзацию для ускорения.
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого Оповещение Из ТаблицаОповещений Цикл
			ДанныеЗаполнения = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Оповещение);
			ДанныеЗаполнения.Вставить("Владелец", Организация);
			Ссылка = ПрочитатьОповещение(ДанныеЗаполнения);
			Если ЗначениеЗаполнено(Ссылка) Тогда
				ДоставленныеОповещения.Добавить(Ссылка);
			КонецЕсли;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'Не удалось сохранить результат запроса: %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
	КонецПопытки;
	
	Возврат ДоставленныеОповещения;
	
КонецФункции

Функция Получены(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОповещенияПлатформыСамозанятые.ПометкаУдаления
		|ИЗ
		|	Справочник.ОповещенияПлатформыСамозанятые КАК ОповещенияПлатформыСамозанятые
		|ГДЕ
		|	НЕ ОповещенияПлатформыСамозанятые.ПометкаУдаления
		|	И ОповещенияПлатформыСамозанятые.Владелец = &Организация";
	
	Результат = Запрос.Выполнить().Пустой();
	
	Возврат Не Результат;
	
КонецФункции

Функция КоличествоНепрочитанных(Организация) Экспорт
	
	Реквизиты = РегистрыСведений.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые.Прочитать(Организация);
	
	Возврат Макс(Реквизиты.Количество, 0);
	
КонецФункции

Функция ДанныеАктуальны(ДатаИзменения) Экспорт
	
	Если ДатаИзменения = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДатаАктуальности = ТекущаяУниверсальнаяДата() - ОповещенияПлатформыСамозанятыеКлиентСервер.ИнтервалПроверки();
	
	Возврат ДатаАктуальности <= ДатаИзменения;
	
КонецФункции

Функция НайтиПоСтатусу(Организация, Статус, МаксимальноеКоличество = Неопределено, НаправлениеПоВозрастанию = Истина) Экспорт
	
	Возврат РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.НайтиОповещенияПоСтатусам(
		Организация, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Статус), МаксимальноеКоличество, НаправлениеПоВозрастанию);
	
КонецФункции

Функция НайтиПоСтатусам(Организация, Статусы, МаксимальноеКоличество = Неопределено, НаправлениеПоВозрастанию = Истина) Экспорт
	
	Возврат РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.НайтиОповещенияПоСтатусам(
		Организация, Статусы, МаксимальноеКоличество, НаправлениеПоВозрастанию);
	
КонецФункции

Функция ИзмененныеПозже(Организация, ДатаИзменения) Экспорт
	
	Возврат РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.ИзмененныеПозже(Организация, ДатаИзменения);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрочитатьОповещение(ДанныеЗаполнения)
	
	Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Идентификатор");
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Ссылка = НайтиПоКоду(Идентификатор, , , ДанныеЗаполнения.Владелец);
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Объект = Ссылка.ПолучитьОбъект();
	Иначе
		Объект = СоздатьЭлемент();
	КонецЕсли;
	
	Объект.Заполнить(ДанныеЗаполнения);
	
	Попытка
		Объект.Записать();
	Исключение
		КомментарийЖурналаРегистрации = СтрШаблон("ru = 'Не удалось записать оповещение: %1'",
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
		ВызватьИсключение КомментарийЖурналаРегистрации;
	КонецПопытки;
	
	Возврат Объект.Ссылка
	
КонецФункции

#КонецОбласти

#КонецЕсли