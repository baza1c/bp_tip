#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьИзСтруктуры(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Статус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Статус");
	ДатаИзменения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ДатаИзменения");
	
	Если ЗначениеЗаполнено(Статус) И ЗначениеЗаполнено(ДатаИзменения) Тогда
		Попытка
			ЗаписатьСтатус(Статус, ДатаИзменения);
		Исключение
			Отказ = Истина;
			КомментарийЖурналаРегистрации = СтрШаблон("ru = 'Не удалось записать статус оповещения: %1'",
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				КомментарийЖурналаРегистрации);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьИзСтруктуры(ДанныеЗаполнения)
	
	Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Идентификатор");
	Если Идентификатор <> Неопределено Тогда
		ДанныеЗаполнения.Вставить("Код", Идентификатор);
	КонецЕсли;
	
	Заголовок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Заголовок");
	Если ЗначениеЗаполнено(Заголовок) Тогда
		ДанныеЗаполнения.Вставить("Наименование", Заголовок);
	Иначе
		ДанныеЗаполнения.Вставить("Наименование", НСтр("ru = 'Сообщение от ФНС России'"));
	КонецЕсли;
	
	Статус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Статус");
	ДатаИзменения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "ДатаИзменения");
	ДополнительныеСвойства.Вставить("Статус", Статус);
	ДополнительныеСвойства.Вставить("ДатаИзменения", ДатаИзменения);
	
КонецПроцедуры

Процедура ЗаписатьСтатус(Статус, ДатаИзменения)
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Организация", Владелец);
	ДанныеЗаполнения.Вставить("Оповещение", Ссылка);
	ДанныеЗаполнения.Вставить("Статус", Статус);
	ДанныеЗаполнения.Вставить("ДатаИзменения", ДатаИзменения);
	
	РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.ЗаписатьСтатус(ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли