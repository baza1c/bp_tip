#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ДанныеЗаполнения = Неопределено Тогда
		ДанныеЗаполнения = Новый Структура;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("Владелец") Тогда
		Владелец = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	Иначе
		Владелец = ДанныеЗаполнения.Владелец;
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ВидМаркетплейса") Тогда
		ВидМаркетплейса = ДанныеЗаполнения.ВидМаркетплейса;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("Контрагент") 
		И ЗначениеЗаполнено(ВидМаркетплейса) Тогда
		Контрагент = ОбменСМаркетплейс.КонтрагентМаркетплейсаПоУмолчанию(ВидМаркетплейса);
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("ДоговорКонтрагента")
		И ЗначениеЗаполнено(Контрагент) Тогда
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента, Контрагент, Владелец,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером));
	КонецЕсли;
	
	Наименование = ВидМаркетплейса;
	
КонецПроцедуры

Процедура ОбновитьСписокСкладов(СписокСкладов) Экспорт
	СкладыМаркетплейс.Очистить();

	Для каждого КлючИЗначение Из СписокСкладов Цикл
		СкладМаркетплейс = СкладыМаркетплейс.Добавить();
		СкладМаркетплейс.Наименование = КлючИЗначение.Представление;
		
		ЗаполнитьЗначенияСвойств(СкладМаркетплейс, КлючИЗначение.Значение);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	ИдентификаторСклада   = Неопределено;
	ИдентификаторКабинета = Неопределено;
	НаименованиеСклада    = Неопределено;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗначениеЗаполнено(Контрагент)
		И ЗначениеЗаполнено(Владелец)
		И НЕ ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		
		ПараметрыДоговора = Новый Структура;
		ПараметрыДоговора.Вставить("ВидДоговора", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером));
		ПараметрыДоговора.Вставить("Организация", Владелец);
		ПараметрыДоговора.Вставить("Владелец",    Контрагент);
		
		Если НЕ РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента, ПараметрыДоговора.Владелец, 
			ПараметрыДоговора.Организация, ПараметрыДоговора.ВидДоговора) Тогда
			
			ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", ПараметрыДоговора);
			ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
		КонецЕсли;
	КонецЕсли;
				
КонецПроцедуры

#КонецОбласти


#КонецЕсли
