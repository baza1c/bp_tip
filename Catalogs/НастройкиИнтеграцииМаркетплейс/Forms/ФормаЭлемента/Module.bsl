
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоВыгрузкаОстатков = Параметры.ВидОперации = "ВыгрузкаОстатков";
	ЭтоЗагрузкаКаталога = Параметры.ВидОперации = "ЗагрузкаКаталога";
	
	Если Параметры.Свойство("СкрытьАвтозагрузку") Тогда
		СкрытьАвтозагрузку = Параметры.СкрытьАвтозагрузку;
	КонецЕсли;
		
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Параметры.Свойство("ВидМаркетплейса") Тогда
			Объект.ВидМаркетплейса = Параметры.ВидМаркетплейса;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.ВидМаркетплейса) Тогда
			Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
				Объект.Контрагент = ОбменСМаркетплейс.КонтрагентМаркетплейсаПоУмолчанию(Объект.ВидМаркетплейса);
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.Контрагент)
				И ЗначениеЗаполнено(Объект.Владелец)
				И НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
				РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагента, Объект.Контрагент, Объект.Владелец,
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером));
			КонецЕсли;
			Объект.Наименование = Объект.ВидМаркетплейса;
		КонецЕсли;
		Объект.ДатаНачалаАвтозагрузки = НачалоМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
		
		Элементы.ИсторияКонтрагента.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.Владелец.Видимость = Не ЗначениеЗаполнено(Объект.Владелец)
		И Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
		
	Заголовок = СтрШаблон(НСтр("ru = 'Настройки интеграции с %1'"),
		Строка(Объект.ВидМаркетплейса));
		
	Элементы.ВидМаркетплейса.Видимость = Не ЗначениеЗаполнено(Объект.ВидМаркетплейса);
	
	МассивПараметров = Новый Массив;
	НовыйПараметр = Новый ПараметрВыбора("Отбор.ВидДоговора", ПолучитьВидыДоговора());
	МассивПараметров.Добавить(НовыйПараметр);
	
	Элементы.СкладМаркетплейса.АвтоВыборНезаполненного = ЭтоВыгрузкаОстатков;
	Элементы.Контрагент.АвтоОтметкаНезаполненного = ЭтоЗагрузкаКаталога ИЛИ ЭтоВыгрузкаОстатков;
	
	Элементы.ДоговорКонтрагента.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	ЗаполнитьНастройкиАвтозагрузки();
	
	УстановитьУсловноеОформление();
	
	УправлениеФормой();
	
	Если Параметры.Ключ.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		ТокенАПИ_ВБ = УникальныйИдентификатор;
	ИначеЕсли Объект.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON Тогда
		ИдентификаторКлиента = ИнтеграцияOZON.ПрочитатьКлиентИд(Объект.Ссылка);
		КлючОзон = УникальныйИдентификатор;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьНастройкаИнтеграцииМаркетплейс", Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ВключенаФункциональностьМаркетплейс") Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Объект.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries") Тогда
		ПроверяемыеРеквизиты.Добавить("ТокенАПИ_ВБ");
	ИначеЕсли Объект.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON") Тогда
		ПроверяемыеРеквизиты.Добавить("КлючОзон");
		ПроверяемыеРеквизиты.Добавить("ИдентификаторКлиента");
	КонецЕсли;
	
	Если (ЭтоВыгрузкаОстатков ИЛИ ЭтоЗагрузкаКаталога) И Объект.Контрагент.Пустая() Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Контрагент'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Контрагент", "Объект.Контрагент", Отказ);
	КонецЕсли;
	
	Если ЭтоВыгрузкаОстатков 
		И НЕ ЗначениеЗаполнено(Объект.НаименованиеСклада) 
		И СписокСкладовЗагружен Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Склад FBS'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СкладМаркетплейса", "Объект.НаименованиеСклада", Отказ);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// для новой настройки интеграции принудительно добавим (в случае отсутствия)
	// настройку автозагрузки, по соответствующей маркетплейсу периодичности
	Если ТекущийОбъект.Ссылка.Пустая()
		И Не РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов.ПодключенаАвтозагрузкаДляМаркетплейса(Объект.ВидМаркетплейса) Тогда
		ПараметрыЗаписи.Вставить("ЗаписатьПараметрыАвтозагрузки", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ДанныеАвторизацииOZONИзменены Тогда
		ИнтеграцияOZON.СохранитьНастройкиАвторизации(ТекущийОбъект.Ссылка, КлючОзон, ИдентификаторКлиента);
		ДанныеАвторизацииOZONИзменены = Ложь;
	КонецЕсли;
	
	Если ТокенАПИ_ВБИзменен Тогда
		ИнтеграцияWildberries.СохранитьНастройкиАвторизации(ТекущийОбъект.Ссылка, ТокенАПИ_ВБ);
		ТокенАПИ_ВБИзменен = Ложь;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ЗаписатьПараметрыАвтозагрузки") Тогда
		РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов.СохранитьНастройкиПоУмолчанию(
			ТекущийОбъект.ВидМаркетплейса);
		ЗаполнитьНастройкиАвтозагрузки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если (ЭтоВыгрузкаОстатков ИЛИ ЭтоЗагрузкаКаталога) И НЕ Объект.Ссылка.Пустая() Тогда
		ЗаполнитьСписокСкладовМаркетплейс();
		ПодключитьОбработчикОжидания("ПредложитьЗаполнитьРеквизиты",0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПараметрыАвтозагрузкиОтчетовМаркетплейсовИзменены" Тогда
		ЗаполнитьНастройкиАвтозагрузки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложитьЗаполнитьРеквизиты()
	Если ЭтоВыгрузкаОстатков И НЕ ЗначениеЗаполнено(Объект.НаименованиеСклада) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Склад FBS'"),,,
			НСтр("ru = 'Для управления остатками необходимо указать склад маркетплейса'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "СкладМаркетплейса", "Объект.НаименованиеСклада");
	КонецЕсли;
	
	Если Объект.Контрагент.Пустая() Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Контрагент'"),,,
			НСтр("ru = 'Для управления остатками необходимо указать контрагента маркетплейса'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Контрагент", "Объект.Контрагент");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	ПриИзмененииКонтрагентаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПриИзмененииКонтрагентаСервер();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	ДоговорКонтрагентаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Организация", Объект.Владелец);
	ПараметрыДоговора.Вставить("Владелец", Объект.Контрагент);
	ПараметрыДоговора.Вставить("ВидДоговора", ПолучитьВидыДоговора());
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КлючОзонПриИзменении(Элемент)
	ДанныеАвторизацииOZONИзменены = Истина;
	ЗаполнитьСписокСкладовМаркетплейс();
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторКлиентаOZONПриИзменении(Элемент)
	ДанныеАвторизацииOZONИзменены = Истина;
	ЗаполнитьСписокСкладовМаркетплейс();
КонецПроцедуры

&НаКлиенте
Процедура ТокенВБПриИзменении(Элемент)
	ТокенАПИ_ВБИзменен = Истина;
	ЗаполнитьСписокСкладовМаркетплейс();
КонецПроцедуры

&НаКлиенте
Процедура ВидМаркетплейсаПриИзменении(Элемент)
	КлючАПИ = "";
	ИдентификаторКлиента = "";
	
	Если ЗначениеЗаполнено(Объект.ВидМаркетплейса) Тогда
		Объект.Наименование = Объект.ВидМаркетплейса;
	КонецЕсли;
	
	ЗаполнитьНастройкиАвтозагрузки();
	
	ДанныеАвторизацииOZONИзменены = Не Объект.Ссылка.Пустая();
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СообщитьОпроблеме(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаАвтозагрузки(Команда)
	
	ОбменСМаркетплейсКлиент.ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьНастройкуАвтозагрузки(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВключитьАвтозагрузку", Истина);
	
	ОбменСМаркетплейсКлиент.ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(ЭтотОбъект, ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СкладМаркетплейса");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СписокСкладовЗагружен", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НаименованиеСклада", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);

КонецПроцедуры 

&НаСервере
Процедура УправлениеФормой()
	ЭтоВайлдберриз = Объект.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries;
	ЭтоОЗОН = Объект.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON;
	ЭтоЯндекс = Объект.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет;
	
	Элементы.ГруппаСкладМаркетплейса.Видимость = ЭтоВыгрузкаОстатков;
	
	Элементы.ГруппаВайлдберриз.Видимость = ЭтоВайлдБерриз;
	
	Элементы.ИдентификаторКлиентаOZON.АвтоОтметкаНезаполненного = ЭтоОЗОН;
	Элементы.КлючОзон.АвтоОтметкаНезаполненного = ЭтоОЗОН;
	Элементы.ГруппаОзон.Видимость = ЭтоОЗОН;
	
	Элементы.ГруппаЯндексМаркет.Видимость = ЭтоЯндекс;
	
	Элементы.ГруппаАвтозагрузка.Видимость = Не СкрытьАвтозагрузку;
	Элементы.ГруппаСообщитьОПроблеме.Видимость = Не СкрытьАвтозагрузку;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru='Нужна интеграция с маркетплейсом'");
	ПараметрыПисьма.Текст = НСтр("ru='Укажите, с каким маркетплейсом нужна интеграция?
		|'");
	ОтправкаПочтовыхСообщенийКлиент.ДополнитьПараметрыПисьмаДляОтправкиЛегкойПочтой(ПараметрыПисьма);
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКонтрагентаСервер()
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагента, Объект.Контрагент, Объект.Владелец,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПолучитьВидыДоговора()));
		
	Справочники.НастройкиИнтеграцииМаркетплейс.УстановитьАктуальноеЗначениеИсторияКонтрагентаДоговора(
		Объект.Контрагент , Объект.ДоговорКонтрагента, Объект.ИсторияКонтрагентаДоговора);
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	Справочники.НастройкиИнтеграцииМаркетплейс.УстановитьАктуальноеЗначениеИсторияКонтрагентаДоговора(
		Объект.Контрагент , Объект.ДоговорКонтрагента, Объект.ИсторияКонтрагентаДоговора);
КонецПроцедуры

&НаКлиенте
Процедура ТокенВБРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СсылкаНаСправкуВБ = "https://seller.wildberries.ru/supplier-settings/access-to-api";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СсылкаНаСправкуВБ);
КонецПроцедуры

&НаКлиенте
Процедура КлючОзонРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СсылкаНаСправкуOZON = "https://seller.ozon.ru/app/settings/api-keys";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СсылкаНаСправкуOZON);
КонецПроцедуры

&НаСервере
Функция ПолучитьВидыДоговора()
	
	Если Объект.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		Возврат Перечисления.ВидыДоговоровКонтрагентов.СПокупателем;
	КонецЕсли;
	
	Возврат Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСписокСкладовМаркетплейс()
	Если НЕ ЭтоВыгрузкаОстатков Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() 
		ИЛИ ДанныеАвторизацииOZONИзменены 
		ИЛИ ТокенАПИ_ВБИзменен Тогда
		
		Записать();
	КонецЕсли;
	
	Элементы.СкладМаркетплейса.СписокВыбора.Очистить();
	
	СписокСкладовМаркетплейс = СписокСкладовМаркетплейс(Объект.Ссылка);
	Если СписокСкладовМаркетплейс = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СписокСкладовЗагружен = СписокСкладовМаркетплейс.Количество() > 0;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСписок(Элементы.СкладМаркетплейса.СписокВыбора, СписокСкладовМаркетплейс);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокСкладовМаркетплейс(НастройкаИнтеграции)
	Возврат ОбменСМаркетплейс.СписокСкладовМаркетплейс(НастройкаИнтеграции);
КонецФункции

&НаКлиенте
Процедура СкладМаркетплейсаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ЗначениеСклада = Элементы.СкладМаркетплейса.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСклада = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ИдентификаторСклада = ЗначениеСклада.Значение;
	Объект.НаименованиеСклада = ЗначениеСклада.Представление;
КонецПроцедуры

&НаКлиенте
Процедура ИсторияКонтрагентаНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("ТекущийКонтрагент, ТекущийДоговорКонтрагента, Организация, ИсторияКонтрагентаДоговора, ТолькоПросмотр", 
					Объект.Контрагент, Объект.ДоговорКонтрагента, Объект.Владелец, Объект.ИсторияКонтрагентаДоговора, ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.НастройкиИнтеграцииМаркетплейс.Форма.РедактированиеИсторииКонтрагента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.НастройкиИнтеграцииМаркетплейс.Форма.РедактированиеИсторииКонтрагента") Тогда
		
		УстановитьКонтрагентаПослеРедактированияИстории(ВыбранноеЗначение.ИсторияКонтрагентаДоговора);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКонтрагентаПослеРедактированияИстории(НаборЗаписей)
	
	Модифицированность = Истина;
	
	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияКонтрагентаДоговора.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьИстории = Объект.ИсторияКонтрагентаДоговора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
		КонецЦикла;
	КонецЕсли;
	
	Объект.Контрагент = НаборЗаписей[НаборЗаписей.Количество()-1].Контрагент;
	Объект.ДоговорКонтрагента = НаборЗаписей[НаборЗаписей.Количество()-1].ДоговорКонтрагента;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиАвтозагрузки()
	
	Если ЭтоВыгрузкаОстатков Или ЭтоЗагрузкаКаталога Тогда
		Элементы.ГруппаАвтозагрузка.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Справочники.НастройкиИнтеграцииМаркетплейс.ЗаполнитьНастройкиАвтозагрузки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
