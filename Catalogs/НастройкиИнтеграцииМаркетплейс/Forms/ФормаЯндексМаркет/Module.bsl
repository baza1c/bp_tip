#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	Элементы.Организация.Видимость = Не ЗначениеЗаполнено(Объект.Владелец);
			
	АдресАвторизации = ИнтеграцияЯндексМаркет.АдресАвторизации();
	
	ЭтоВыгрузкаОстатков = Параметры.ВидОперации = "ВыгрузкаОстатков";
	ЭтоЗагрузкаКаталога = Параметры.ВидОперации = "ЗагрузкаКаталога";
	ЭтоЗагрузкаОтчета   = Параметры.ВидОперации = "ЗагрузкаОтчета";
	
	Элементы.СкладМаркетплейса.Видимость    = ЭтоВыгрузкаОстатков ИЛИ ЭтоЗагрузкаКаталога;
	Элементы.ГруппаЗагрузкаОтчета.Видимость = ЭтоЗагрузкаОтчета;
	Элементы.ГруппаПлатежныйАгент.Видимость = ЭтоЗагрузкаОтчета;
	Если Параметры.ВидОперации = "" Тогда
		Элементы.СкладМаркетплейса.Видимость    = Истина;
		Элементы.ГруппаЗагрузкаОтчета.Видимость = Истина;
		Элементы.ГруппаПлатежныйАгент.Видимость = Истина;
	КонецЕсли;
	//если нет настройки для организации	
	Если Параметры.Ключ.Пустая() Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница 	= Элементы.СтраницаПодключено;
		Элементы.КнопкаГотово.КнопкаПоУмолчанию 	= Истина;
		Элементы.ТокенАПИ.АвтоОтметкаНезаполненного = Истина;
		Элементы.КомандаОтключиться.Видимость 		= Ложь;
	КонецЕсли;
	
	ЗаполнитьНастройкиАвтозагрузки();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
	ТокенАПИ = ИнтеграцияЯндексМаркет.ПолучитьТокенАПИ(Объект.Владелец);
	УстановитьВидПоляТокена(); 
	
	Если Параметры.Свойство("ОшибкаАвторизации") Тогда
		Элементы.ТокенАПИ.АвтоОтметкаНезаполненного = Параметры.ОшибкаАвторизации;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если АвторизацияПройдена Тогда
		// для новой настройки интеграции принудительно добавим (в случае отсутствия)
		// настройку автозагрузки, по соответствующей маркетплейсу периодичности
		Если ТекущийОбъект.Ссылка.Пустая()
			И Не РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов.ПодключенаАвтозагрузкаДляМаркетплейса(Объект.ВидМаркетплейса) Тогда
			ПараметрыЗаписи.Вставить("ЗаписатьПараметрыАвтозагрузки", Истина);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ТекущийОбъект.НаименованиеСклада    = Неопределено;
	ТекущийОбъект.ИдентификаторСклада   = Неопределено;
	ТекущийОбъект.ИдентификаторКабинета = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьНастройкаИнтеграцииМаркетплейс", Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ВключенаФункциональностьМаркетплейс") Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ЗаписатьПараметрыАвтозагрузки") Тогда
		РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов.СохранитьНастройкиПоУмолчанию(
			ТекущийОбъект.ВидМаркетплейса);
		ЗаполнитьНастройкиАвтозагрузки();
	КонецЕсли;
	
	Если НЕ ДанныеАвторизацииИзменены Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеАвторизацииСтруктура = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеАвторизации);
	ИнтеграцияЯндексМаркет.СохранитьНастройкиАвторизации(ТекущийОбъект.Ссылка, ДанныеАвторизацииСтруктура);
	
	ДанныеАвторизацииИзменены  = Ложь;
	АвторизацияПройдена = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если НЕ АвторизацияПройдена Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Обобщенный покупатель'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Контрагент", "Объект.Контрагент", Отказ);
	КонецЕсли;
	
	// для выгрузки остатков и загрузки каталога обязателен идентификатор кабинета
	// для загрузки отчета - склад выбирается отдельно в форме загрузки
	Если (ЭтоВыгрузкаОстатков ИЛИ ЭтоЗагрузкаКаталога)
		И Элементы.СкладМаркетплейса.СписокВыбора.Количество() > 0 
		И НЕ ЗначениеЗаполнено(Объект.ИдентификаторКабинета) Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Магазин'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СкладМаркетплейса", "Объект.НаименованиеСклада", Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПараметрыАвтозагрузкиОтчетовМаркетплейсовИзменены" Тогда
		ЗаполнитьНастройкиАвтозагрузки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДалее(Команда)
	
	Если НЕ ЗначениеЗаполнено(ВременныйКод) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите код подтверждения'"), , "ВременныйКод");
		Возврат;
	КонецЕсли;
	
	Подключиться();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	Отключиться();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаАвтозагрузки(Команда)
	
	ОбменСМаркетплейсКлиент.ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьНастройкуАвтозагрузки(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВключитьАвтозагрузку", Истина);
	
	ОбменСМаркетплейсКлиент.ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(ЭтотОбъект, ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПриИзмененииНаСервере();
КонецПроцедуры 

&НаКлиенте
Процедура ДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, Истина, СтандартнаяОбработка);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорКонтрагента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Функция ПараметрыСозданияНовогоДоговора()
	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Организация", Объект.Владелец);
	ПараметрыДоговора.Вставить("Владелец", Объект.Контрагент);
	ПараметрыДоговора.Вставить("ВидДоговора", ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
	
	Возврат ПараметрыДоговора;
КонецФункции 


&НаКлиенте
Процедура ДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Декорация4ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресАвторизации);
КонецПроцедуры

&НаКлиенте
Процедура СкладМаркетплейсаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ЗначениеСклада = Элементы.СкладМаркетплейса.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСклада = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Идентификаторы = ЗначениеСклада.Значение;
	
	Объект.ИдентификаторСклада   = СокрЛП(Идентификаторы.ИдентификаторСклада);
	Объект.ИдентификаторКабинета = СокрЛП(Идентификаторы.ИдентификаторКабинета);
	Объект.НаименованиеСклада    = ЗначениеСклада.Представление;
	
	Модифицированность = Истина;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныйАгентПриИзменении(Элемент)
	
	ПлатежныйАгентПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПлатежногоАгентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, Истина, СтандартнаяОбработка);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПлатежногоАгентаСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорПлатежногоАгента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговораПлатежногоАгента();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПлатежногоАгентаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговораПлатежногоАгента();
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПлатежногоАгентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ДанныеАвторизации   = ИнтеграцияЯндексМаркет.ПолучитьДанныеАвторизации(Объект.Ссылка);
	АвторизацияПройдена = ДанныеАвторизации <> Неопределено;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПодключено;
	Элементы.КнопкаГотово.КнопкаПоУмолчанию = Истина;

	Если АвторизацияПройдена Тогда
		ЗаполнитьСписокСкладов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокСкладов(Форма)
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Форма.ЭтоЗагрузкаОтчета Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СкладМаркетплейса.СписокВыбора.Очистить();
	
	СписокСкладовМаркетплейс = СписокСкладовМаркетплейса(Объект.Ссылка);
	Если СписокСкладовМаркетплейс = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СкладыМаркетплейс.Очистить();

	Для каждого ЭлементСписка Из СписокСкладовМаркетплейс Цикл
		СкладМаркетплейс = Объект.СкладыМаркетплейс.Добавить();
		СкладМаркетплейс.Наименование = ЭлементСписка.Представление;
		
		ЗаполнитьЗначенияСвойств(СкладМаркетплейс, ЭлементСписка.Значение);
		ЗаполнитьЗначенияСвойств(Элементы.СкладМаркетплейса.СписокВыбора.Добавить(), ЭлементСписка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УдалитьДанныеАвторизацииЯндексМаркет()
	
	Если ЗначениеЗаполнено(ТокенАПИ) Тогда
		ИнтеграцияЯндексМаркет.УдалитьТокенАПИ(Объект.Ссылка.Владелец);
	КонецЕсли;
		
	ИнтеграцияЯндексМаркет.УдалитьДанныеАвторизации(Объект.Ссылка);
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключиться()
	Если СокрЛП(СтарыйВременныйКод) = СокрЛП(ВременныйКод) Тогда
		ПослеПодключенияНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ПодключитьсяНаСервере(ВременныйКод, УникальныйИдентификатор), 
		Новый ОписаниеОповещения("ПодключитьсяЗавершение", ЭтотОбъект), 
		ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьсяЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	Результат = Новый Структура("Результат");
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	КонецЕсли;
	
	Если Результат.Результат = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'При выполнении запроса к интернет-ресурсу возникли ошибки. Подробности см. в журнале регистрации.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат.Результат Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Ошибка авторизации в сервисе. Возможно, неверный код подтверждения.'"));
		Возврат;
	КонецЕсли;
	
	ДанныеАвторизации =  Результат.ДанныеАвторизации;
	ДанныеАвторизацииИзменены = Истина;
	
	Записать();
	
	СтарыйВременныйКод = ВременныйКод;
	
	ПослеПодключенияНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияНаКлиенте()
	Прочитать();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПодключено;
	Элементы.КомандаОтключиться.Заголовок = НСтр("ru = '< Назад, к настройкам подключения'");
	Элементы.КнопкаГотово.КнопкаПоУмолчанию = Истина; 
	
	ЗаполнитьСписокСкладов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Отключиться()
	
	Если АвторизацияПройдена Тогда
		УдалитьДанныеАвторизацииЯндексМаркет();
		ОчиститьСписокСкладовНаСервере();
		АвторизацияПройдена = Ложь;
	КонецЕсли;
	
	Элементы.СкладМаркетплейса.СписокВыбора.Очистить();
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПодключитьсяНаСервере(КодПодтверждения, ИдентификаторФормы)
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "ИнтеграцияЯндексМаркет.ПолучитьТокеныПоКоду", СокрЛП(КодПодтверждения));
КонецФункции

&НаСервереБезКонтекста
Функция СписокСкладовМаркетплейса(НастройкаПодключения)
	Возврат ОбменСМаркетплейс.СписокСкладовМаркетплейс(НастройкаПодключения)
КонецФункции 

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагента, Объект.Контрагент, Объект.Владелец,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем));
КонецПроцедуры

&НаСервере
Процедура ПлатежныйАгентПриИзмененииНаСервере()
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорПлатежногоАгента, Объект.ПлатежныйАгент, Объект.Владелец,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем));
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыСозданияНовогоДоговораПлатежногоАгента()
	
	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Организация", Объект.Владелец);
	ПараметрыДоговора.Вставить("Владелец", Объект.ПлатежныйАгент);
	ПараметрыДоговора.Вставить("ВидДоговора", ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
	
	Возврат ПараметрыДоговора;
	
КонецФункции

&НаСервере
Процедура ОчиститьСписокСкладовНаСервере()
	
	Справочники.НастройкиИнтеграцииМаркетплейс.ОчиститьСписокСкладовЯндекс(Объект.Ссылка);
	
КонецПроцедуры

#Область НоваяАвторизацияЯндекс

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТокенАПИ) Тогда
		ТекстСообщения = "";
		Если Не АвторизацияВыполненаНаСервере(ТокенАПИ, ТекстСообщения) Тогда
			Отказ = Истина;
			ПоказатьПредупреждение(, ТекстСообщения);
			ТокенАПИ = "";
		Иначе
			ЗаписатьТокенАПИНаСервере();
			АвторизацияПройдена = Истина;
		КонецЕсли;
	Иначе
		Если Элементы.ТокенАПИ.АвтоОтметкаНезаполненного Тогда
			ПоказатьПредупреждение(, "Не заполнен авторизационный токен");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АвторизацияВыполненаНаСервере(ТокенАПИ, ТекстСообщения) Экспорт 
	
	Результат = Ложь;
	
	Сервер = "api.partner.market.yandex.ru";
	ИнтернетПрокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();

	Попытка	
		HTTPСоединение = Новый HTTPСоединение(Сервер, , , ,ИнтернетПрокси, 60, СоединениеOpenSSL);
	Исключение
		ТекстСообщения = НСтр("ru = 'Отсутствует соединение с сервером " + Сервер +";'");
	КонецПопытки;

	Если HTTPСоединение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Заголовки.Вставить("Api-Key", ТокенАПИ);
	АдресРесурса = "/campaigns";
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(АдресРесурса, КодировкаТекста.UTF8);

	Попытка
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос);
	Исключение
		ТекстСообщения = ОписаниеОшибки();
	КонецПопытки;
	
	Если Не HTTPОтвет = Неопределено Тогда
		Если HTTPОтвет.КодСостояния = 200 Тогда
			Результат = Истина;
		Иначе
			ТекстСообщения = "Авторизация по токену не выполнена. 
							|Возможно использован неверный токен
							|или произошла ошибка при его вводе" 
		КонецЕсли
	КонецЕсли;
	
    Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаписатьТокенАПИНаСервере()
	
	ИнтеграцияЯндексМаркет.СохранитьТокенАПИ(Объект.Владелец, ТокенАПИ);
	
	УстановитьВидПоляТокена();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидПоляТокена()
	
	Если ЗначениеЗаполнено(ТокенАПИ) Тогда
		Элементы.ТокенАПИ.РежимПароля = Истина;
	Иначе
		Элементы.ТокенАПИ.РежимПароля = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиАвтозагрузки()
	
	Если Не ЭтоЗагрузкаОтчета Тогда
		Элементы.ГруппаАвтозагрузка.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Справочники.НастройкиИнтеграцииМаркетплейс.ЗаполнитьНастройкиАвтозагрузки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

