#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НастройкаПоУмолчанию(Организация, ВидМаркетплейса) Экспорт
	Если НЕ ЗначениеЗаполнено(Организация) 
		ИЛИ НЕ ЗначениеЗаполнено(ВидМаркетплейса) Тогда
	
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	НастройкиИнтеграцииМаркетплейс.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НЕ НастройкиИнтеграцииМаркетплейс.ПометкаУдаления
	|	И НастройкиИнтеграцииМаркетплейс.Владелец = &Организация
	|	И НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = &ВидМаркетплейса";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Если НЕ ВидФормы = "ФормаОбъекта" ИЛИ НЕ Параметры.Свойство("ВидОперации") Тогда
		Возврат;
	КонецЕсли;
	
	ВидМаркетплейса = Неопределено;
	
	НастройкаИнтеграции = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ключ");
	Если ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ВидМаркетплейса");
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
		ВидМаркетплейса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.ЗначенияЗаполнения, "ВидМаркетплейса");
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") Тогда
		ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЗначениеКопирования, "ВидМаркетплейса");
	КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		ВыбраннаяФорма = "ФормаЯндексМаркет";
	Иначе
		ВыбраннаяФорма = "ФормаЭлемента";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

Процедура УстановитьАктуальноеЗначениеИсторияКонтрагентаДоговора(Контрагент, Договор, ИсторияКонтрагентаДоговора) Экспорт
	
	КоличествоЗаписей = ИсторияКонтрагентаДоговора.Количество();
	
	Если КоличествоЗаписей > 0 Тогда
		
		ИсторияКонтрагентаДоговора.Сортировать("Период");
		АктуальнаяЗаписьИстории = ИсторияКонтрагентаДоговора[КоличествоЗаписей - 1];
		АктуальнаяЗаписьИстории.Контрагент = Контрагент;
		АктуальнаяЗаписьИстории.ДоговорКонтрагента = Договор;
		
	КонецЕсли;
	
КонецПроцедуры

Функция КонтрагентДоговорНаДату(НастройкиИнтеграцииМаркетплейс, ДатаСведений) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НастройкиИнтеграцииМаркетплейс)
		ИЛИ ТипЗнч(НастройкиИнтеграцииМаркетплейс) <> Тип("СправочникСсылка.НастройкиИнтеграцииМаркетплейс") Тогда
		Возврат Новый Структура("Контрагент, ДоговорКонтрагента", 
							Справочники.Контрагенты.ПустаяСсылка(), Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаСведений) Тогда
		Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкиИнтеграцииМаркетплейс, "Контрагент, ДоговорКонтрагента");
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("НастройкиИнтеграцииМаркетплейс", НастройкиИнтеграцииМаркетплейс);
	Запрос.Параметры.Вставить("ДатаСведений", ДатаСведений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияКонтрагентаДоговора.Период) КАК Период,
	|	ИсторияКонтрагентаДоговора.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗначенияКонтрагентаДоговора
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс.ИсторияКонтрагентаДоговора КАК ИсторияКонтрагентаДоговора
	|ГДЕ
	|	ИсторияКонтрагентаДоговора.Ссылка = &НастройкиИнтеграцииМаркетплейс
	|	И ИсторияКонтрагентаДоговора.Период <= &ДатаСведений
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияКонтрагентаДоговора.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияКонтрагентаДоговора.Контрагент КАК Контрагент,
	|	ИсторияКонтрагентаДоговора.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ИЗ
	|	ЗначенияКонтрагентаДоговора КАК ЗначенияКонтрагентаДоговора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиИнтеграцииМаркетплейс.ИсторияКонтрагентаДоговора КАК ИсторияКонтрагентаДоговора
	|		ПО ЗначенияКонтрагентаДоговора.Ссылка = ИсторияКонтрагентаДоговора.Ссылка
	|			И ЗначенияКонтрагентаДоговора.Период = ИсторияКонтрагентаДоговора.Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Новый Структура("Контрагент, ДоговорКонтрагента", Выборка.Контрагент, Выборка.ДоговорКонтрагента);
	Иначе
		Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкиИнтеграцииМаркетплейс, "Контрагент, ДоговорКонтрагента");;
	КонецЕсли;
	
КонецФункции

Процедура ОчиститьСписокСкладовЯндекс(Настройка) Экспорт
	
	Если Не ЗначениеЗаполнено(Настройка) Тогда
		Возврат;
	КонецЕсли;
	
	СправочникОбъект = Настройка.ПолучитьОбъект();
	СправочникОбъект.СкладыМаркетплейс.Очистить();
	
	Попытка
		СправочникОбъект.Записать();
	Исключение
		ЗаписьЖурналаРегистрации(ОписаниеОшибки(), УровеньЖурналаРегистрации.Ошибка);
	КонецПопытки;
	
КонецПроцедуры

#Область АвтозагрузкаОтчетов

Процедура ЗаполнитьНастройкиАвтозагрузки(Форма) Экспорт
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	Форма.АвтозагрузкаВключена = НастройкиАвтозагрузки.АвтозагрузкаВключена()
		Или Форма.Объект.Ссылка.Пустая();
	
	НадписьСледующаяАвтозагрузка = "";
	Если Форма.АвтозагрузкаВключена Тогда
		НадписьСледующаяАвтозагрузка = НастройкиАвтозагрузки.ПредставлениеСледующейЗагрузкиДляМаркетплейса(
			Форма.Объект.ВидМаркетплейса);
	КонецЕсли;
	Форма.Элементы.НадписьСледующаяАвтозагрузка.Заголовок = НадписьСледующаяАвтозагрузка;
	
	Форма.Элементы.ГруппаАвтозагрузкаВключена.Видимость = Форма.АвтозагрузкаВключена;
	Форма.Элементы.ГруппаАвтозагрузкаОтключена.Видимость = Не Форма.АвтозагрузкаВключена;
	
	Форма.Элементы.НастройкаАвтозагрузки.Видимость = Не Форма.Объект.Ссылка.Пустая();
	Форма.Элементы.НастройкаАвтозагрузкиВключить.Видимость = Не Форма.Объект.Ссылка.Пустая();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли