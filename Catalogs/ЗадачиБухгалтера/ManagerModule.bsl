#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// См. РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов.
Процедура ПриДобавленииКлассификаторов(Классификаторы) Экспорт
	
	Описание = РаботаСКлассификаторами.ОписаниеКлассификатора();
	Описание.Наименование           = НСтр("ru = 'Правила представления отчетов, уплаты налогов'");
	Описание.Идентификатор          = ИдентификаторВСервисеКлассификаторов();
	Описание.ОбновлятьАвтоматически = Истина;
	Описание.ОбщиеДанные            = Истина;
	Классификаторы.Добавить(Описание);
	
КонецПроцедуры

// См. РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора.
Процедура ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан, ДополнительныеПараметры) Экспорт
	
	Если Идентификатор <> ИдентификаторВСервисеКлассификаторов() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// Обновляем только в главном узле,
		// до подчиненных узлов новые правила дойдут при очередном обмене
		Возврат;
	КонецЕсли;
	
	Попытка
		ПутьКФайлу = ПолучитьИмяВременногоФайла("xml");
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
		ДвоичныеДанные.Записать(ПутьКФайлу);
		ЗаполнитьТребованиямиНормативныхДокументов(ПутьКФайлу);
		Обработан = Истина;
	Исключение
		Обработан = Ложь;
		ЗаписьЖурналаРегистрации(
			НСтр("ru='СписокЗадач.Правила.Обновление'"),
			УровеньЖурналаРегистрации.Ошибка,
			,
			СтрШаблон(НСтр("ru='Обновление правил представления отчетов и уплаты налогов, версия %1'"), XMLСтрока(Версия)),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

// Возвращает идентификатор классификатора правил представления отчетов и уплаты налогов в сервисе классификаторов
// 
Функция ИдентификаторВСервисеКлассификаторов() Экспорт 

	Возврат "ReportingAndTaxPaymentRules";

КонецФункции

Функция СоздатьТаблицуПравил()
	
	ДлинаНаименования   = Метаданные.Справочники.ЗадачиБухгалтера.ДлинаНаименования;
	ДлинаИдентификатора = Метаданные.Справочники.ЗадачиБухгалтера.ДлинаКода;
	
	Правила = Новый ТаблицаЗначений;
	Правила.Колонки.Добавить("Идентификатор",      Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(ДлинаИдентификатора)));
	Правила.Колонки.Добавить("Представление",      Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(ДлинаНаименования)));
	Правила.Колонки.Добавить("НаименованиеПолное", Новый ОписаниеТипов("Строка"));
	Правила.Колонки.Добавить("Порядок",            Новый ОписаниеТипов("Число"));
	Правила.Колонки.Добавить("Требования",         Новый ОписаниеТипов("ТаблицаЗначений"));
	Правила.Индексы.Добавить("Идентификатор");
	
	Возврат Правила;
	
КонецФункции

Процедура ПолучитьПредопределенныеПравилаИзФайла(ПутьКФайлуПравил, Правила, ВерсияПравил)
	
	ОбъектПравил = ПрочитатьМакетПравил(ПутьКФайлуПравил);
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ОбъектПравил.ВерсияФормата, "3.0.2.0") >= 0 Тогда
		Возврат; // Неподдерживаемая версия правил
	КонецЕсли;
	ВерсияПравил = ОбъектПравил.ВерсияПравил;
	
	ТекущаяВерсияПравил = Константы.ВерсияПравилПредставленияОтчетовУплатыНалогов.Получить();
	
	Если ЗначениеЗаполнено(ТекущаяВерсияПравил) И ТекущаяВерсияПравил >= ВерсияПравил Тогда
		// В базе классификатор уже обновлен
		Возврат;
	КонецЕсли;
	
	ДобавитьПравилаВТаблицу(ОбъектПравил, Правила);
	
КонецПроцедуры

Процедура ЗаполнитьТребованиямиНормативныхДокументов(ПутьКФайлуПравил = Неопределено) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// Предопределенные элементы не следует создавать в подчиненных узлах РИБ
		Возврат;
	КонецЕсли;
	
	Правила = СоздатьТаблицуПравил();
	УстановитьПривилегированныйРежим(Истина);
	ВерсияПравил = 0;
	ПолучитьПредопределенныеПравилаИзФайла(ПутьКФайлуПравил, Правила, ВерсияПравил);
	ВерсияДополнительныхПравил = 0;
	ЗадачиБухгалтераПереопределяемый.ДополнитьПредопределенныеПравила(Правила, ВерсияДополнительныхПравил);
	
	Если Правила.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивДоступныхДействий = Новый Массив;
	ЗадачиБухгалтераПереопределяемый.ПолучитьДоступныеВидыДействийКалендаряБухгалтера(МассивДоступныхДействий);
	
	ЗагруженныеЗадачи = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиКалендаряБухгалтера.Код КАК Идентификатор,
	|	ЗадачиКалендаряБухгалтера.Наименование КАК Наименование,
	|	ЗадачиКалендаряБухгалтера.Ссылка КАК Ссылка,
	|	ЗадачиКалендаряБухгалтера.НаименованиеПолное КАК НаименованиеПолное,
	|	ЗадачиКалендаряБухгалтера.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания
	|ПОМЕСТИТЬ ЗадачиБухгалтера
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиКалендаряБухгалтера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗадачиКалендаряБухгалтера.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиКалендаряБухгалтера.Идентификатор КАК Идентификатор,
	|	ЗадачиКалендаряБухгалтера.Наименование КАК Наименование,
	|	ЗадачиКалендаряБухгалтера.Ссылка КАК Ссылка,
	|	ЗадачиКалендаряБухгалтера.НаименованиеПолное КАК НаименованиеПолное,
	|	ЗадачиКалендаряБухгалтера.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания
	|ИЗ
	|	ЗадачиБухгалтера КАК ЗадачиКалендаряБухгалтера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиКалендаряБухгалтера.Ссылка КАК Задача,
	|	Требования.Код КАК ИдентификаторТребования,
	|	Требования.Ссылка КАК Требование,
	|	Требования.ДатаИзменения КАК ДатаИзменения
	|ИЗ
	|	ЗадачиБухгалтера КАК ЗадачиКалендаряБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Требования
	|		ПО ЗадачиКалендаряБухгалтера.Ссылка = Требования.Владелец";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ЗагруженныеРанееЗадачи = РезультатЗапроса[1].Выгрузить();
	ЗагруженныеРанееЗадачи.Индексы.Добавить("Идентификатор");
	ЗагруженныеРанееТребования = РезультатЗапроса[2].Выгрузить();
	ЗагруженныеРанееТребования.Индексы.Добавить("Задача,ИдентификаторТребования");
	
	КорректныеИдентификаторы = НовыйКорректныеИдентификаторыТребованийОтчетов();
	ЗадачиБухгалтераПереопределяемый.ЗаполнитьКорректныеИдентификаторыТребованийОтчетов(КорректныеИдентификаторы);
	КорректныеИдентификаторы.Индексы.Добавить("ИдентификаторЗадачи,ИдентификаторТребования");
	
	Для Каждого НаборТребований Из Правила Цикл // Набор описывает требования к выполнению бухгалтером какой-то задачи, например "НДС"
		
		// Синхронизируем справочник ЗадачиБухгалтера
		Выборка = ЗагруженныеРанееЗадачи.НайтиСтроки(Новый Структура("Идентификатор", НаборТребований.Идентификатор));
		
		Если Выборка.Количество() = 0 Тогда
			Объект = Справочники.ЗадачиБухгалтера.СоздатьЭлемент();
			Объект.Код                       = НаборТребований.Идентификатор;
			Объект.Наименование              = НаборТребований.Представление;
			Объект.НаименованиеПолное        = НаборТребований.НаименованиеПолное;
			Объект.РеквизитДопУпорядочивания = НаборТребований.Порядок;
			Объект.Записать();
			Задача = Объект.Ссылка;
		Иначе
			ЗагруженнаяРанееЗадача = Выборка[0];
			Задача = ЗагруженнаяРанееЗадача.Ссылка;
			Если ЗагруженнаяРанееЗадача.Наименование <> НаборТребований.Представление 
				Или ЗагруженнаяРанееЗадача.НаименованиеПолное <> НаборТребований.НаименованиеПолное
				Или ЗагруженнаяРанееЗадача.РеквизитДопУпорядочивания <> НаборТребований.Порядок Тогда
				
				Объект = ЗагруженнаяРанееЗадача.Ссылка.ПолучитьОбъект();
				Объект.Наименование              = НаборТребований.Представление;
				Объект.НаименованиеПолное        = НаборТребований.НаименованиеПолное;
				Объект.РеквизитДопУпорядочивания = НаборТребований.Порядок;
				Объект.Записать();
				
			КонецЕсли;
		КонецЕсли;
		
		// Синхронизируем справочник ПравилаПредставленияОтчетовУплатыНалогов
		
		Для Каждого ОписаниеТребования Из НаборТребований.Требования Цикл
			
			НекорректноеТребование = Ложь;
			ЗадачиБухгалтераПереопределяемый.ПроверитьКорректностьЗагружаемогоТребования(НекорректноеТребование,
				КорректныеИдентификаторы, МассивДоступныхДействий, НаборТребований.Идентификатор, ОписаниеТребования.Идентификатор, ОписаниеТребования.Действие);
			Если НекорректноеТребование Тогда
				Продолжить;
			КонецЕсли;
			
			Отбор = Новый Структура;
			Отбор.Вставить("Задача",                  Задача);
			Отбор.Вставить("ИдентификаторТребования", ОписаниеТребования.Идентификатор);
			Выборка = ЗагруженныеРанееТребования.НайтиСтроки(Отбор);
			
			Объект = Неопределено;
			Если Выборка.Количество() = 0 Тогда
				Объект = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.СоздатьЭлемент();
				Объект.Владелец      = Задача;
				Объект.Код           = ОписаниеТребования.Идентификатор;
			Иначе
				ЗагруженноеРанееТребование = Выборка[0];
				Если ЗагруженноеРанееТребование.ДатаИзменения < ОписаниеТребования.ДатаИзменения Тогда
					Объект = ЗагруженноеРанееТребование.Требование.ПолучитьОбъект();
				КонецЕсли;
			КонецЕсли;
			
			Если Объект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Объект.РеквизитДопУпорядочивания = ОписаниеТребования.Порядок;
			ЗаполнитьЗначенияСвойств(Объект, ОписаниеТребования, , "Условия,ОснованияПродленияСроков");
			Объект.Условия.Очистить();
			Для Каждого Условие Из ОписаниеТребования.Условия Цикл
				Объект.Условия.Добавить().Условие = Условие;
			КонецЦикла;
			
			Объект.ОснованияПродленияСроков.Очистить();
			Для Каждого ОснованиеПродления Из ОписаниеТребования.ОснованияПродленияСроков Цикл
				Объект.ОснованияПродленияСроков.Добавить().ОснованиеПродленияСроковНалоговОтчетов = ОснованиеПродления;
			КонецЦикла;
			
			Объект.Записать();
			
		КонецЦикла;
		
	КонецЦикла;
	Константы.ВерсияПравилПредставленияОтчетовУплатыНалогов.Установить(ВерсияПравил);
	ЗадачиБухгалтераПереопределяемый.УстановитьВерсиюДополнительныхПравилПредставленияОтчетовУплатыНалогов(ВерсияДополнительныхПравил);
	
КонецПроцедуры

Функция ТребованияНормативныхДокументовXML(ПоднятьВерсию = Ложь) Экспорт
	
	Запись = Новый ЗаписьXML;
	Запись.УстановитьСтроку();
	Запись.ЗаписатьОбъявлениеXML();
	
	ПространствоИмен = Метаданные.ПакетыXDTO.ПравилаПредставленияОтчетовУплатыНалогов.ПространствоИмен;
	ТипПравила    = ФабрикаXDTO.Тип(ПространствоИмен, "ПравилаПредставленияОтчетовУплатыНалогов");
	ТипЗадача     = ФабрикаXDTO.Тип(ПространствоИмен, "Задача");
	ТипТребование = ФабрикаXDTO.Тип(ПространствоИмен, "Требование");
	ТипУсловие    = ФабрикаXDTO.Тип(ПространствоИмен, "Условие");
	
	Правила = ФабрикаXDTO.Создать(ТипПравила);
	Правила.ВерсияФормата = "3.0.1.10";
	
	ПредыдущийОбъектПравил = ПрочитатьМакетПравил();
	Правила.ВерсияПравил = ПредыдущийОбъектПравил.ВерсияПравил;
	Если ПоднятьВерсию Тогда
		Правила.ВерсияПравил = Число(Правила.ВерсияПравил) + 1;
	КонецЕсли;
	
	ПредыдущаяВерсияПравил = СоздатьТаблицуПравил();
	ДобавитьПравилаВТаблицу(ПредыдущийОбъектПравил, ПредыдущаяВерсияПравил);
	
	ВыборкаЗадачи = Справочники.ЗадачиБухгалтера.Выбрать(,,,"РеквизитДопУпорядочивания");
	Пока ВыборкаЗадачи.Следующий() Цикл
		
		Если ВыборкаЗадачи.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		
		Задача = ФабрикаXDTO.Создать(ТипЗадача);
		Задача.Идентификатор      = ВыборкаЗадачи.Код;
		Задача.Представление      = ВыборкаЗадачи.Наименование;
		
		Задача.НаименованиеПолное = ВыборкаЗадачи.НаименованиеПолное;
		Задача.Порядок            = ВыборкаЗадачи.РеквизитДопУпорядочивания;
		
		ВыборкаТребования = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.Выбрать(,ВыборкаЗадачи.Ссылка,,"РеквизитДопУпорядочивания");
		Пока ВыборкаТребования.Следующий() Цикл
			
			Если ВыборкаТребования.ПометкаУдаления Тогда
				Продолжить;
			КонецЕсли;
			
			Требование = ФабрикаXDTO.Создать(ТипТребование);
			Требование.Идентификатор = ВыборкаТребования.Код;
			Требование.Порядок       = ВыборкаТребования.РеквизитДопУпорядочивания;
			ЗаполнитьЗначенияСвойств(Требование, ВыборкаТребования);
			
			Для Каждого ВыборкаУсловия Из ВыборкаТребования.Условия Цикл 
				
				Если ЗначениеЗаполнено(ВыборкаУсловия.Условие) Тогда
					Требование.Условие.Добавить(ВыборкаУсловия.Условие);
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого ВыборкаОснованияПродления Из ВыборкаТребования.ОснованияПродленияСроков Цикл 
				
				Если ЗначениеЗаполнено(ВыборкаОснованияПродления.ОснованиеПродленияСроковНалоговОтчетов) Тогда
					Требование.ОснованиеПродленияСроков.Добавить(ВыборкаОснованияПродления.ОснованиеПродленияСроковНалоговОтчетов);
				КонецЕсли;
				
			КонецЦикла;
			
			// Если какие-либо параметры требования отличаются от предыдущей версии,
			// то обновим номер версии
			Если ВерсияТребованияОтличается(ПредыдущаяВерсияПравил, ВыборкаЗадачи.Код, Требование) Тогда
				Требование.ДатаИзменения = НачалоДня(ТекущаяДатаСеанса());
			КонецЕсли;
			
			Задача.Требование.Добавить(Требование);
			
		КонецЦикла;
		
		Правила.Задача.Добавить(Задача);
		
	КонецЦикла;
	
	ФабрикаXDTO.ЗаписатьXML(
		Запись, 
		Правила,
		"ПравилаПредставленияОтчетовУплатыНалогов",
		ПространствоИмен,
		,
		НазначениеТипаXML.Явное);
	
	Возврат Запись.Закрыть();
	
КонецФункции

Функция ПредставлениеЗадачи(Задача) Экспорт
	
	Представление = СокрЛП(Задача);
	Представление = ВРег(Лев(Представление, 1)) + Сред(Представление, 2);
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
//
// Возвращаемое значение: Массив(Строка) - массив имен реквизитов, образующих
//  естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив();
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

Функция НовыйОписаниеТребования() Экспорт
	
	ОписаниеСправочника = Метаданные.Справочники.ПравилаПредставленияОтчетовУплатыНалогов;
	ОписаниеРеквизитов  = ОписаниеСправочника.Реквизиты;
	ТипИдентификатора   = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(ОписаниеСправочника.ДлинаКода));
	ТипНаименования     = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(ОписаниеСправочника.ДлинаНаименования));
	
	Требования = Новый ТаблицаЗначений;
	Требования.Колонки.Добавить("Идентификатор",                 ТипИдентификатора); // Строковой идентификатор требования (уникален в пределах задачи)
	Требования.Колонки.Добавить("Наименование",                  ТипНаименования);
	Требования.Колонки.Добавить("Порядок",                       Новый ОписаниеТипов("Число"));
	Требования.Колонки.Добавить("ДатаИзменения",                 ОписаниеРеквизитов.ДатаИзменения.Тип);
	Требования.Колонки.Добавить("ЕстьИнформацияНаИТС",           ОписаниеРеквизитов.ЕстьИнформацияНаИТС.Тип);
	// Условия, в которых следует применять это требование
	Требования.Колонки.Добавить("НачалоДействия",                ОписаниеРеквизитов.НачалоДействия.Тип); // Дата, начиная с которой применяются требования, которые предусматривают это требование
	Требования.Колонки.Добавить("КонецДействия",                 ОписаниеРеквизитов.КонецДействия.Тип); // Дата, по которую применяются требования, которые предусматривают это требование
	Требования.Колонки.Добавить("Условия",                       Новый ОписаниеТипов("Массив")); // Условия, при выполнении которых применяется требование. Выбираются из классификатора КлассификаторУсловий()
	// Способ выполнения требования
	Требования.Колонки.Добавить("Описание",                      ОписаниеРеквизитов.Описание.Тип); // Особая часть представления для пользователя
	Требования.Колонки.Добавить("Действие",                      ОписаниеРеквизитов.Действие.Тип); // Отчет, УплатаНалога
	Требования.Колонки.Добавить("ФинансовыйПериод",              ОписаниеРеквизитов.ФинансовыйПериод.Тип); // Налоговый период (для задач, не связанных с налогами - отчетный период)
	Требования.Колонки.Добавить("РасширенныйПервыйНалоговыйПериод",
                                                                 ОписаниеРеквизитов.РасширенныйПервыйНалоговыйПериод.Тип); // Условие, при котором налоговый (отчетный) период может быть расширен
	Требования.Колонки.Добавить("Периодичность",                 ОписаниеРеквизитов.Периодичность.Тип); // Периодичность выполнения действия в рамках отчетного периода
	Требования.Колонки.Добавить("ОграничениеПериода",            ОписаниеРеквизитов.ОграничениеПериода.Тип); // Периоды (в рамках финансового), в которые действие не следует выполнять
	Требования.Колонки.Добавить("СрокМесяцев",                   ОписаниеРеквизитов.СрокМесяцев.Тип); // Срок выполнения действия после окончания периода, к которому оно относится.
	Требования.Колонки.Добавить("ОснованияПродленияСроков",      Новый ОписаниеТипов("Массив")); // Основания для продления сроков выполнения действия, Массив из Строка, см. общий модуль ОснованияПродленияСроковНалоговОтчетов
	Требования.Колонки.Добавить("СрокДней",                      ОписаниеРеквизитов.СрокДней.Тип); // Срок выполнения действия после окончания периода, к которому оно относится. 
	Требования.Колонки.Добавить("ПеренестиНаРабочийДень",        ОписаниеРеквизитов.ПеренестиНаРабочийДень.Тип); // -1 - на предыдущий, +1 - на следующий
	
	Требования.Колонки.Добавить("БазовыйПериод",                 ОписаниеРеквизитов.БазовыйПериод.Тип);
	Требования.Колонки.Добавить("ОтставаниеБазовогоПериода",     ОписаниеРеквизитов.ОтставаниеБазовогоПериода.Тип);
	
	Требования.Колонки.Добавить("ИсключатьПериодИзПредставленияЗадачи", ОписаниеРеквизитов.ИсключатьПериодИзПредставленияЗадачи.Тип);
	Требования.Колонки.Добавить("СчитатьСрокВРабочихДнях", ОписаниеРеквизитов.СчитатьСрокВРабочихДнях.Тип);
	
	Требования.Колонки.Добавить("НачальныйСрокМесяцев",             ОписаниеРеквизитов.НачальныйСрокМесяцев.Тип); // Срок, не ранее которого требуется выполнять действие.
	Требования.Колонки.Добавить("НачальныйСрокДней",                ОписаниеРеквизитов.НачальныйСрокДней.Тип); // Срок, не ранее которого требуется выполнять действие.
	Требования.Колонки.Добавить("ПеренестиСрокНачалаНаРабочийДень", ОписаниеРеквизитов.ПеренестиСрокНачалаНаРабочийДень.Тип); // -1 - на предыдущий, +1 - на следующий
	
	Требования.Колонки.Добавить("ВыполняетсяЕдинымПомощником", ОписаниеРеквизитов.ВыполняетсяЕдинымПомощником.Тип);
	Требования.Колонки.Добавить("ИсключатьИмяРодительскойЗадачиИзПредставленияЗадачи", ОписаниеРеквизитов.ИсключатьИмяРодительскойЗадачиИзПредставленияЗадачи.Тип);
	
	Требования.Индексы.Добавить("Идентификатор");
	
	Возврат Требования;
	
КонецФункции

// Устанавливает порядок следования задач: приоритет задач по страховым взносам должен быть ниже задач по АУСН
//
Процедура УстановитьРеквизитыДопУпорядочивания_СтраховыеВзносыАУСН() Экспорт
	
	УстановитьРеквизитДопУпорядочивания("АУСН", 12);
	УстановитьРеквизитДопУпорядочивания("СтраховыеВзносы_Предприниматель", 13);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЧтениеФайла // Чтение файла с требованиями нормативных документов

Функция ПрочитатьМакетПравил(ПутьКФайлуПравил = Неопределено)
	
	Чтение = Новый ЧтениеXML;
	Если ПутьКФайлуПравил = Неопределено Тогда
		Макет = Справочники.ЗадачиБухгалтера.ПолучитьМакет("ТребованияНормативныхДокументов");
		Чтение.УстановитьСтроку(Макет.ПолучитьТекст());
	Иначе
		Чтение.ОткрытьФайл(ПутьКФайлуПравил);
	КонецЕсли;
	
	Возврат ФабрикаXDTO.ПрочитатьXML(Чтение);
	
КонецФункции

Процедура ДобавитьПравилаВТаблицу(ОбъектXDTO, Правила) Экспорт
	
	// Читаем тело файла правил
	Для Каждого Задача Из ОбъектXDTO.Задача Цикл
		
		НаборПравил = Правила.Добавить();
		ЗаполнитьЗначенияСвойств(НаборПравил, Задача);
		
		НаборПравил.Требования = НовыйОписаниеТребования();
		
		Для Каждого Требование Из Задача.Требование Цикл
			
			НовоеТребование = НаборПравил.Требования.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеТребование, Требование);
			
			Для Каждого Условие Из Требование.Условие Цикл
				
				НовоеТребование.Условия.Добавить(Условие);
				
			КонецЦикла;
			
			Для Каждого ОснованиеПродленияСроковНалоговОтчетов Из Требование.ОснованиеПродленияСроков Цикл
				
				НовоеТребование.ОснованияПродленияСроков.Добавить(ОснованиеПродленияСроковНалоговОтчетов);
				
			КонецЦикла;
				
		КонецЦикла;
				
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

// Требование - ЗначениеXDTO
Функция ВерсияТребованияОтличается(ПредыдущаяВерсияПравил, ИдентификаторЗадачи, Требование)
	
	Если ПредыдущаяВерсияПравил = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПредыдущаяВерсияНабораТребований = ПредыдущаяВерсияПравил.Найти(ИдентификаторЗадачи, "Идентификатор");
	Если ПредыдущаяВерсияНабораТребований = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПредыдущаяВерсияТребования = ПредыдущаяВерсияНабораТребований.Требования.Найти(Требование.Идентификатор, "Идентификатор");
	Если ПредыдущаяВерсияТребования = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Сравним версии требований
	// Требование - ЗначениеXDTO, ПредыдущаяВерсияТребования - строка таблицы значений
	// Для сравнения версий приведем их к одинаковому формату хранения
	
	НоваяВерсияЗначенияПолей      = Новый Массив;
	ПредыдущаяВерсияЗначенияПолей = Новый Массив;
	
	Поля = ПредыдущаяВерсияНабораТребований.Требования.Колонки;
	Для Каждого ОписаниеПоля Из Поля Цикл
		
		ПредыдущаяВерсияЗначениеПоля = ПредыдущаяВерсияТребования[ОписаниеПоля.Имя];
		
		Если ОписаниеПоля.Имя = "Условия" Тогда
			
			// В XDTO это СписокXDTO. И он отличается именем.
			ЗначенияСписка = Новый Массив;
			Для ИндексЭлемента = 0 По Требование.Условие.Количество() - 1 Цикл
				ЗначенияСписка.Добавить(Требование.Условие.Получить(ИндексЭлемента));
			КонецЦикла;
			НоваяВерсияЗначениеПоля = ЗначенияСписка;
			
		ИначеЕсли ОписаниеПоля.Имя = "ОснованияПродленияСроков" Тогда
			
			// В XDTO это СписокXDTO. И он отличается именем.
			ЗначенияСписка = Новый Массив;
			Для ИндексЭлемента = 0 По Требование.ОснованиеПродленияСроков.Количество() - 1 Цикл
				ЗначенияСписка.Добавить(Требование.ОснованиеПродленияСроков.Получить(ИндексЭлемента));
			КонецЦикла;
			НоваяВерсияЗначениеПоля = ЗначенияСписка;
		Иначе
			НоваяВерсияЗначениеПоля = Требование[ОписаниеПоля.Имя];
		КонецЕсли;
		
		НоваяВерсияЗначенияПолей.Добавить(НоваяВерсияЗначениеПоля);
		ПредыдущаяВерсияЗначенияПолей.Добавить(ПредыдущаяВерсияЗначениеПоля);
		
	КонецЦикла;
	
	Возврат Не ОбщегоНазначения.ДанныеСовпадают(НоваяВерсияЗначенияПолей, ПредыдущаяВерсияЗначенияПолей);
	
КонецФункции

// Устанавливает порядок следования задач - зависит от реквизита "РеквизитДопУпорядочивания"
//
// Параметры:
//   КодЭлемента - Строка - Код справочника
//   Порядок     - Число - Реквизит упорядочивания
//
Процедура УстановитьРеквизитДопУпорядочивания(КодЭлемента, Порядок)
	
	ЭлементСсылка = Справочники.ЗадачиБухгалтера.НайтиПоКоду(КодЭлемента);
	Если Не ЗначениеЗаполнено(ЭлементСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	СправочникОбъект = ЭлементСсылка.ПолучитьОбъект();
	СправочникОбъект.РеквизитДопУпорядочивания = Порядок;
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
	
КонецПроцедуры

Функция НовыйКорректныеИдентификаторыТребованийОтчетов()
	
	Результат = Новый ТаблицаЗначений;
	ТипСтрока = Новый ОписаниеТипов("Строка");
	
	Результат.Колонки.Добавить("ИдентификаторЗадачи", ТипСтрока);
	Результат.Колонки.Добавить("ИдентификаторТребования", ТипСтрока);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли
