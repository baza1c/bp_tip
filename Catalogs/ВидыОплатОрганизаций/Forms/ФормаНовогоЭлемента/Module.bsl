
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.ЗначенияЗаполнения.Свойство("Наименование") Тогда
		Объект.Наименование = Параметры.ЗначенияЗаполнения.Наименование;
	КонецЕсли;
	
	Элементы.Контрагент.Видимость         = ПравоДоступа("Просмотр", Метаданные.Справочники.Контрагенты);
	Элементы.ГруппаДоговор.Видимость      = ПравоДоступа("Добавление", Метаданные.Справочники.ДоговорыКонтрагентов);
	
	СформироватьПодсказкуСозданияВидаОплаты();
	
	УправлениеФормой();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ЗначениеЗаполнено(ДоговорНомер) И ЗначениеЗаполнено(ДоговорДата) Тогда
		СоздатьНовыйДоговор();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	КоличествоСтрокКомиссияБанка = ТекущийОбъект.КомиссияБанка.Количество();
	Если КоличествоСтрокКомиссияБанка = 0 Тогда
		ТекущийОбъект.КомиссияБанка.Добавить().ПроцентБанковскойКомиссии = ТекущийОбъект.ПроцентБанковскойКомиссии;
	ИначеЕсли КоличествоСтрокКомиссияБанка = 1 Тогда
		ТекущийОбъект.ПроцентБанковскойКомиссии = ТекущийОбъект.КомиссияБанка[0].ПроцентБанковскойКомиссии;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ОповеститьОВыборе(Объект.Ссылка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	КонтрагентПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)

	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)

	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)

	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)

	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	Записать();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)

	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеРеквизиты(Команда)

	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Наименование", Объект.Наименование);
	СтруктураЗаполнения.Вставить("Организация", Объект.Организация);
	СтруктураЗаполнения.Вставить("Контрагент", Объект.Контрагент);
	СтруктураЗаполнения.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", СтруктураЗаполнения);
	ПараметрыФормы.Вставить("ДанныеМодифицированы", Модифицированность);
	
	ОткрытьФорму("Справочник.ВидыОплатОрганизаций.ФормаОбъекта", ПараметрыФормы, ВладелецФормы);

	Модифицированность = Ложь;

	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ГруппаДоговор.Доступность = ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Контрагент);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПодсказкуСозданияВидаОплаты()
	
	ТекстПодсказки = НСтр("ru = 'Вид оплаты создается для каждого соглашения с банком-эквайером.
		|Изменить настройки соглашения с банком-эквайером в дальнейшем можно будет в разделе %1.'");
	
	Если ОбщегоНазначенияБП.ЭтоПростойИнтерфейс() Тогда
		РасположениеНастроек = НСтр("ru = 'Деньги - Настройки - Виды оплат'");
	Иначе
		РасположениеНастроек = НСтр("ru = 'Справочники - Виды оплат'");
	КонецЕсли;
	
	ТекстПодсказки = СтрШаблон(ТекстПодсказки, РасположениеНастроек);
	
	Элементы.ГруппаРеквизитыРасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	
	ДоступныеВидыДоговоров = Справочники.ВидыОплатОрганизаций.ВидыДоговоровПоТипуОплаты(Объект.ТипОплаты);
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		Объект.ДоговорКонтрагента, Объект.Контрагент, Объект.Организация, ДоступныеВидыДоговоров);
		
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьНовыйДоговор()
	
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ЗначенияЗаполнения", ПараметрыДоговора);
	
	Объект.ДоговорКонтрагента =
		РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыСозданияНовогоДоговора()
	
	ДоступныеВидыДоговоров = Справочники.ВидыОплатОрганизаций.ВидыДоговоровПоТипуОплаты(Объект.ТипОплаты);
	
	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Организация", Объект.Организация);
	ПараметрыДоговора.Вставить("Владелец",    Объект.Контрагент);
	ПараметрыДоговора.Вставить("Дата",        ДоговорДата);
	ПараметрыДоговора.Вставить("Номер",       ДоговорНомер);
	ПараметрыДоговора.Вставить("Наименование",
		ПечатьДоговоровКлиентСервер.РеквизитыДоговораСтрокой(ДоговорНомер, ДоговорДата));
	ПараметрыДоговора.Вставить("ВидДоговора",
		?(ДоступныеВидыДоговоров.Количество() > 0, ДоступныеВидыДоговоров[0], Неопределено));
	
	Возврат ПараметрыДоговора;
	
КонецФункции

#КонецОбласти