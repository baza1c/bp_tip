
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаголовокОрганизация = НСтр("ru = 'Организация:'");
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьУчитыватьНДСПоКомиссии();
		
		ПодготовитьФормуНаСервере();
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Наименование") Тогда
			Объект.Наименование = Параметры.ЗначенияЗаполнения.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеМодифицированы = Параметры.ДанныеМодифицированы;
	
	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Передадим объекту информацию о том, что запись идет из формы.
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЗаписьИзФормыЭлемента", Истина);
	
	Если ЗависитОтСуммыОпераций = 0 Или ЗависитОтСуммыОпераций = 3 Тогда
		ТекущийОбъект.ПроцентБанковскойКомиссии = 0;
		ТекущийОбъект.КомиссияУдерживаетсяОтдельно = ЗависитОтСуммыОпераций = 3;
	Иначе
		ТекущийОбъект.КомиссияУдерживаетсяОтдельно = Ложь;
	КонецЕсли;
	
	ТекущийОбъект.УчитыватьНДСПоКомиссии = ОтражениеВРасходах = 1;
	
	КоличествоСтрокКомиссияБанка = ТекущийОбъект.КомиссияБанка.Количество();
	Если КоличествоСтрокКомиссияБанка = 0 Тогда
		ТекущийОбъект.КомиссияБанка.Добавить().ПроцентБанковскойКомиссии = ТекущийОбъект.ПроцентБанковскойКомиссии;
	ИначеЕсли КоличествоСтрокКомиссияБанка = 1 Тогда
		ТекущийОбъект.ПроцентБанковскойКомиссии = ТекущийОбъект.КомиссияБанка[0].ПроцентБанковскойКомиссии;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
	ДействияСОтложеннымПроведением =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущийОбъект.ДополнительныеСвойства, "ДействияСОтложеннымПроведением");
	
	Если ДействияСОтложеннымПроведением = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Запускаем фоновое задание по переключению режима отложенного проведения
	РезультатЗаданияПереключенияОтложенногоПроведения = ПроведениеСервер.НачатьПереключениеОтложенногоПроведения(
		ДействияСОтложеннымПроведением.Включить,
		ДействияСОтложеннымПроведением.Отключить,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если РезультатЗаданияПереключенияОтложенногоПроведения <> Неопределено Тогда
		ПроведениеКлиент.ОжидатьПереключенияОтложенногоПроведения(РезультатЗаданияПереключенияОтложенногоПроведения);
	КонецЕсли;
	
	Оповестить("Запись_ВидОплаты", , ЭтотОбъект)
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ИспользоватьНумерациюДоговоровСПокупателями" Тогда
		
		УстановитьПараметрыВыбораДоговора();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если ДанныеМодифицированы Тогда
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОплатыПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ТипОплаты) Тогда
		ТипОплатыПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = ЗначенияЗаполненияИзПараметровВыбора(Элементы.Контрагент);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Объект.Контрагент);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = ЗначенияЗаполненияИзПараметровВыбора(Элементы.ДоговорКонтрагента);
	
	ЗначенияЗаполнения.Вставить("Владелец",    Объект.Контрагент);
	ЗначенияЗаполнения.Вставить("Организация", Объект.Организация);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Объект.ДоговорКонтрагента);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(
		Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, ПредлагатьНовыйДоговор, СтандартнаяОбработка, ПредлагатьНовыйДоговорСНумерацией);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	Если ПредлагатьНовыйДоговор И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ПредлагатьНовыйДоговор = Ложь;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорКонтрагента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключаемоеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтбора = Новый Структура("Организация, ТипОборудования", Объект.Организация, ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал"));
	
	ПараметрыФормыВыбора = Новый Структура("Отбор, Заголовок", СтруктураОтбора, НСтр("ru='Выберите эвайринговый терминал'", "ru") );
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.ФормаВыбораИзСписка", ПараметрыФормыВыбора, Элементы.ПодключаемоеОборудование,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ПроцентБанковскойКомиссииПриИзменении(Элемент)
	
	Если Объект.ПроцентБанковскойКомиссии = 0 Тогда
		Объект.КомиссияБанка.Очистить();
	Иначе
		Объект.КомиссияБанка.Очистить();
		Объект.КомиссияБанка.Добавить().ПроцентБанковскойКомиссии = Объект.ПроцентБанковскойКомиссии;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗависитОтСуммыОперацийПриИзменении(Элемент)
	
	Объект.КомиссияБанка.Очистить();
	Если ЗависитОтСуммыОпераций = 0 Или ЗависитОтСуммыОпераций = 3 Тогда
		Объект.ПроцентБанковскойКомиссии = 0;
	КонецЕсли;
	
	Объект.КомиссияБанка.Добавить().ПроцентБанковскойКомиссии = Объект.ПроцентБанковскойКомиссии;
	Если ЗависитОтСуммыОпераций = 2 Тогда
		ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыКомиссияБанка

&НаКлиенте
Процедура КомиссияБанкаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомиссияБанкаПослеУдаления(Элемент)
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомиссияБанкаПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ТекущиеДанные.НомерСтроки = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомиссияБанкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	// Видимость по умолчанию
	ВидимостьРеквизитовБезналичнойОплаты = Истина;
	ВидимостьКомиссииБанка               = Истина;
	ВидимостьПодключаемоеОборудование    = Ложь;
	
	ЗаголовокСчетаРасчетов = НСтр("ru = 'Счет расчетов:'");
	
	Если Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплат.Наличные") Тогда
		ВидимостьРеквизитовБезналичнойОплаты = Ложь;
	ИначеЕсли Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплат.ПлатежнаяКарта")
		Или Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплат.СБП") Тогда
		
		Элементы.Контрагент.ПодсказкаВвода = "Банк-эквайер";
		ВидимостьПодключаемоеОборудование = (Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплат.ПлатежнаяКарта"));
	ИначеЕсли Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплат.БанковскийКредит") Тогда
		Элементы.Контрагент.ПодсказкаВвода = "Банк-кредитор";
	ИначеЕсли Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплат.ПодарочныйСертификатСторонний") Тогда
		Элементы.Контрагент.ПодсказкаВвода = "Эмитент подарочных сертификатов";
		ВидимостьКомиссииБанка = Ложь;
	ИначеЕсли Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплат.ПодарочныйСертификатСобственный") Тогда
		Элементы.Контрагент.ПодсказкаВвода = "Обобщенный покупатель, например: ""Розничный покупатель"" или ""Частное лицо""";
		ВидимостьКомиссииБанка = Ложь;
		ЗаголовокСчетаРасчетов = НСтр("ru = 'Счет авансов:'");
	КонецЕсли;
	
	ТекДата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Если Год(ТекДата) < 2026 Тогда
		ТекДата = '20260101';
	КонецЕсли;
	
	ВыделятьНДССКомиссииБанка = УчетнаяПолитика.ВыделятьНДССКомиссииБанка(Объект.Организация, ТекДата);
	
	Элементы.БезналичнаяОплата.Видимость        = ВидимостьРеквизитовБезналичнойОплаты;
	Элементы.ПодключаемоеОборудование.Видимость = ВидимостьПодключаемоеОборудование;
	Элементы.БанковскаяКомиссия.Видимость       = ВидимостьКомиссииБанка;
	Элементы.ГруппаОтражениеВРасходах.Видимость = ВидимостьКомиссииБанка И ВыделятьНДССКомиссииБанка;
	
	ЗаголовокСчетРасчетов = ЗаголовокСчетаРасчетов;
	
	Элементы.ДоговорКонтрагента.Доступность     = ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Контрагент);
	
	Элементы.БанковскаяКомиссияТарифы.Видимость = ЗависитОтСуммыОпераций = 2;
	Элементы.БанковскаяКомиссияОдноЗначение.Доступность = ЗависитОтСуммыОпераций = 1;
	
	Элементы.ГруппаОтражениеВРасходах.Доступность = ЗависитОтСуммыОпераций <> 3;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Если Параметры.Свойство("ТипОплатыДоступныеЗначения") Тогда
		Элементы.ТипОплаты.СписокВыбора.ЗагрузитьЗначения(Параметры.ТипОплатыДоступныеЗначения);
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура") Тогда
		
		Если Параметры.ЗначенияЗаполнения.Свойство("ТипОплаты") Тогда
			
			Элементы.ТипОплаты.СписокВыбора.Очистить();
			
			Если ТипЗнч(Параметры.ЗначенияЗаполнения.ТипОплаты) = Тип("ФиксированныйМассив") Тогда
			
				Для ИндексОтбора = 0 по Параметры.ЗначенияЗаполнения.ТипОплаты.Количество() - 1 Цикл
					
					Если Параметры.ЗначенияЗаполнения.ТипОплаты[ИндексОтбора] = Перечисления.ТипыОплат.ПлатежнаяКарта Тогда
						Элементы.ТипОплаты.СписокВыбора.Вставить(0, Параметры.ЗначенияЗаполнения.ТипОплаты[ИндексОтбора]);
					Иначе
						Элементы.ТипОплаты.СписокВыбора.Добавить(Параметры.ЗначенияЗаполнения.ТипОплаты[ИндексОтбора]);
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				Элементы.ТипОплаты.СписокВыбора.Добавить(Параметры.ЗначенияЗаполнения.ТипОплаты);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.ТипОплаты.СписокВыбора.Количество() = 0 Тогда
		
		Элементы.ТипОплаты.СписокВыбора.Добавить(Перечисления.ТипыОплат.ПлатежнаяКарта);
		Элементы.ТипОплаты.СписокВыбора.Добавить(Перечисления.ТипыОплат.СБП);
		Элементы.ТипОплаты.СписокВыбора.Добавить(Перечисления.ТипыОплат.БанковскийКредит);
		
		Если ПолучитьфункциональнуюОпцию("ИспользуютсяПодарочныеСертификаты") Тогда
			
			Элементы.ТипОплаты.СписокВыбора.Добавить(Перечисления.ТипыОплат.ПодарочныйСертификатСобственный);
			
			Если ПолучитьфункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов") Тогда
				Элементы.ТипОплаты.СписокВыбора.Добавить(Перечисления.ТипыОплат.ПодарочныйСертификатСторонний);
			КонецЕсли;
			
		ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользоватьПродажиЧерезМаркетплейс") Тогда
			
			Элементы.ТипОплаты.СписокВыбора.Добавить(Перечисления.ТипыОплат.ПодарочныйСертификатСобственный);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.Контрагент.Видимость         = ПравоДоступа("Просмотр", Метаданные.Справочники.Контрагенты);
	Элементы.ДоговорКонтрагента.Видимость = ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКонтрагентов);
	
	УстановитьПараметрыВыбораКонтрагента();
	УстановитьПараметрыВыбораДоговора();
	Если Объект.КомиссияУдерживаетсяОтдельно Тогда
		ЗависитОтСуммыОпераций = 3;
	ИначеЕсли Объект.КомиссияБанка.Количество() > 1 Тогда
		ЗависитОтСуммыОпераций = 2;
	ИначеЕсли ЗначениеЗаполнено(Объект.ПроцентБанковскойКомиссии) Тогда
		ЗависитОтСуммыОпераций = 1;
	Иначе
		ЗависитОтСуммыОпераций = 0;
	КонецЕсли;
	
	ОтражениеВРасходах = ?(Объект.УчитыватьНДСПоКомиссии, 1, 0);
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	
	ДоступныеВидыДоговоров = Справочники.ВидыОплатОрганизаций.ВидыДоговоровПоТипуОплаты(Объект.ТипОплаты);
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагента,
		Объект.Контрагент, Объект.Организация, ДоступныеВидыДоговоров);
		
	ПредлагатьНовыйДоговор = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента)
		И ДоступныеВидыДоговоров.Найти(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем) <> Неопределено
		И РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент) Тогда
		
		ПредлагатьНовыйДоговор = Истина;
		
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьУчитыватьНДСПоКомиссии();
	ОтражениеВРасходах = ?(Объект.УчитыватьНДСПоКомиссии, 1, 0);
	
	ДоступныеВидыДоговоров = Справочники.ВидыОплатОрганизаций.ВидыДоговоровПоТипуОплаты(Объект.ТипОплаты);
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагента,
		Объект.Контрагент, Объект.Организация, ДоступныеВидыДоговоров);
	
	ПредлагатьНовыйДоговор = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) 
		И ДоступныеВидыДоговоров.Найти(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем) <> Неопределено
		И РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент) Тогда
		
		ПредлагатьНовыйДоговор = Истина;
		
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ТипОплатыПриИзмененииНаСервере()
	
	УстановитьПараметрыВыбораКонтрагента();
	УстановитьПараметрыВыбораДоговора();
	
	Если ЗначениеЗаполнено(Объект.Контрагент)
		И Объект.ТипОплаты = Перечисления.ТипыОплат.ПодарочныйСертификатСобственный Тогда
		
		// для собственных сертификатов контрагент может быть только физлицом
		ВидТекущегоКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.Контрагент, "ЮридическоеФизическоеЛицо");
		
		Если ВидТекущегоКонтрагента <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			Объект.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	ДоступныеВидыДоговоров = Справочники.ВидыОплатОрганизаций.ВидыДоговоровПоТипуОплаты(Объект.ТипОплаты);
	ВидТекущегоДоговора    = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "ВидДоговора");
	Если ДоступныеВидыДоговоров.Найти(ВидТекущегоДоговора) = Неопределено Тогда
		Объект.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагента,
		Объект.Контрагент, Объект.Организация, ДоступныеВидыДоговоров);
	
	УстановитьСчетУчетаРасчетовПоУмолчанию();
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораКонтрагента()
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.Справочники.Контрагенты) Тогда
		Возврат;
	КонецЕсли; 
	
	Если Объект.ТипОплаты = Перечисления.ТипыОплат.ПодарочныйСертификатСобственный Тогда
		// Собственный сертификат может быть выдан только обобщенному физическому лицу.
		ДоступныеВидыКонтрагента = 
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	
		ПараметрВыбораКонтрагента = Новый ПараметрВыбора(
			"Отбор.ЮридическоеФизическоеЛицо", Новый ФиксированныйМассив(ДоступныеВидыКонтрагента));
	
		НовыйМассивПараметров = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрВыбораКонтрагента);
		Элементы.Контрагент.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассивПараметров);
	Иначе
		// Очистим параметры выбора.
		Элементы.Контрагент.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораДоговора()
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКонтрагентов) Тогда
		Возврат;
	КонецЕсли; 
	
	ДоступныеВидыДоговоров    = Справочники.ВидыОплатОрганизаций.ВидыДоговоровПоТипуОплаты(Объект.ТипОплаты);
	ПараметрВыбораВидДоговора = Новый ПараметрВыбора("Отбор.ВидДоговора", Новый ФиксированныйМассив(ДоступныеВидыДоговоров));
	
	ПараметрВыбораВалютный = Новый ПараметрВыбора("Отбор.Валютный", Ложь);
	
	НовыйМассивПараметров = Новый Массив;
	НовыйМассивПараметров.Добавить(ПараметрВыбораВидДоговора);
	НовыйМассивПараметров.Добавить(ПараметрВыбораВалютный);
	
	Элементы.ДоговорКонтрагента.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассивПараметров);
	
	ПредлагатьНовыйДоговор = ДоступныеВидыДоговоров.Найти(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем) <> Неопределено
		И РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
	
	Если ДоступныеВидыДоговоров.Количество() > 0 Тогда
		ПредлагатьНовыйДоговорСНумерацией = РаботаСДоговорамиКонтрагентовБП.ИспользуетсяНумерацияПоВидуДоговора(
			ДоступныеВидыДоговоров[0]);
	Иначе
		ПредлагатьНовыйДоговорСНумерацией = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСчетУчетаРасчетовПоУмолчанию()
	
	Объект.СчетУчетаРасчетов = Справочники.ВидыОплатОрганизаций.СчетУчетаРасчетовПоУмолчанию(Объект.ТипОплаты);
	
КонецПроцедуры

&НаКлиенте
Функция ЗначенияЗаполненияИзПараметровВыбора(Элемент)
	
	ЗначенияЗаполнения = Новый Структура;
	
	ПараметрыВыбораПоля = Новый Массив(Элемент.ПараметрыВыбора);
	Для каждого ПараметрВыбора Из ПараметрыВыбораПоля Цикл
		Если СтрНайти(ПараметрВыбора.Имя, "Отбор.") > 0 Тогда
			ИмяОтбора = СтрЗаменить(ПараметрВыбора.Имя, "Отбор.", "");
			ЗначенияЗаполнения.Вставить(ИмяОтбора, ПараметрВыбора.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначенияЗаполнения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(Форма)
	
	Объект = Форма.Объект;
	
	Объект.КомиссияБанка.Сортировать("СуммаОперацийОт");
	КоличествоСтрок = Объект.КомиссияБанка.Количество();
	
	ГраницаСуммыОпераций = 0;
	Для сч = 1 По КоличествоСтрок Цикл
		СтрокаТаблицы = Объект.КомиссияБанка[КоличествоСтрок - сч];
		Если КоличествоСтрок = 1 Тогда
			ШаблонСтроки = НСтр("ru = 'Комиссия %1% (не зависит от суммы операций)'");
			СтрокаТаблицы.УсловияБанка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтроки, СтрокаТаблицы.ПроцентБанковскойКомиссии);
			Прервать;
		КонецЕсли;
		
		Если СтрокаТаблицы.НомерСтроки = КоличествоСтрок Тогда
			ШаблонСтроки = НСтр("ru = 'Свыше %1 руб. - комиссия %2%'");
			СтрокаТаблицы.УсловияБанка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтроки, СтрокаТаблицы.СуммаОперацийОт, СтрокаТаблицы.ПроцентБанковскойКомиссии);
		ИначеЕсли СтрокаТаблицы.НомерСтроки = 1 Тогда
			ШаблонСтроки = НСтр("ru = 'До %1 руб. включительно - комиссия %2%'");
			СтрокаТаблицы.УсловияБанка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтроки, ГраницаСуммыОпераций, СтрокаТаблицы.ПроцентБанковскойКомиссии);
		Иначе
			ШаблонСтроки = НСтр("ru = 'От %1 до %2 руб. включительно - комиссия %3%'");
			СтрокаТаблицы.УсловияБанка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтроки, СтрокаТаблицы.СуммаОперацийОт, ГраницаСуммыОпераций, СтрокаТаблицы.ПроцентБанковскойКомиссии);
		КонецЕсли;
		
		ГраницаСуммыОпераций = СтрокаТаблицы.СуммаОперацийОт;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив();
	ЭлементыСчетов.Добавить("ЗаголовокСчетРасчетов");
	ЭлементыСчетов.Добавить("СчетУчетаРасчетов");
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

#Область УсловноеОформление

&НаСервере
Процедура УстановитьУсловноеОформление() Экспорт
	
	УсловноеОформление.Элементы.Очистить();
	
	УстановитьУсловноеОформлениеКомиссияБанка();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеКомиссияБанка() Экспорт
	
	//Объект.КомиссияБанка.СуммаОперацийОт
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КомиссияБанкаСуммаОпераций");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.КомиссияБанка.НомерСтроки",
		ВидСравненияКомпоновкиДанных.Равно,
		1);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.КомиссияБанка.СуммаОперацийОт",
		ВидСравненияКомпоновкиДанных.Равно,
		0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "0");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//Объект.КомиссияБанка.СуммаОперацийОт
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КомиссияБанкаСуммаОпераций");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.КомиссияБанка.НомерСтроки",
		ВидСравненияКомпоновкиДанных.Больше,
		1);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.КомиссияБанка.СуммаОперацийОт",
		ВидСравненияКомпоновкиДанных.Равно,
		0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//Объект.КомиссияБанка.От
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "0");
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КомиссияБанкаТекстОт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.КомиссияБанка.НомерСтроки",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "от");
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ПараметрыСозданияНовогоДоговора()
	
	ДоступныеВидыДоговоров = Справочники.ВидыОплатОрганизаций.ВидыДоговоровПоТипуОплаты(Объект.ТипОплаты);
	
	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Организация", Объект.Организация);
	ПараметрыДоговора.Вставить("Владелец", Объект.Контрагент);
	ПараметрыДоговора.Вставить("ВидДоговора", ?(ДоступныеВидыДоговоров.Количество() > 0,
		ДоступныеВидыДоговоров[0], Неопределено));
	ПараметрыДоговора.Вставить("Дата", ОбщегоНазначения.ТекущаяДатаПользователя());
	
	Возврат ПараметрыДоговора;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьУчитыватьНДСПоКомиссии()
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ТекДата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Если Год(ТекДата) < 2026 Тогда
		ТекДата = '20260101';
	КонецЕсли;
	
	Объект.УчитыватьНДСПоКомиссии = УчетнаяПолитика.ВыделятьНДССКомиссииБанка(Объект.Организация, ТекДата);
	
КонецПроцедуры

#КонецОбласти
