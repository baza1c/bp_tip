
#Область ОписаниеПеременных

&НаКлиенте
Перем ОткрытыеФормы Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Сотрудник);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	СклонениеПредставленийОбъектов.ПриСозданииНаСервере(ЭтотОбъект, ФизическоеЛицо.ФИО, "ФизическоеЛицо");
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	СотрудникиФормы.СотрудникиПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	РазместитьКонтактнуюИнформацию();
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	ПредупреждениеНерезидентАУСН = УправлениеПредупреждениямиАУСНВызовСервера.ТекстПредупрежденияНерезидент(); 
		
	СервисЭлектронныхТрудовыхКнижек.Сотрудник_ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

	УстановитьФункциональныеОпцииФормы(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_ПослеОткрытияФормы", 0.1, Истина);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "ИзмененЗаголовокФормыСотрудника" Тогда
		
		ПросклонятьФИО();
		УправлениеФормой(ЭтотОбъект);

	КонецЕсли;

	СотрудникиКлиент.СотрудникиОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	Если ИмяСобытия = "ОтредактированаИстория" И ФизическоеЛицоСсылка = Источник Тогда
		Если Параметр.ИмяРегистра = "ДокументыФизическихЛиц" Тогда
			Если ДокументыФизическихЛицНаборЗаписейПрочитан Тогда
				СотрудникиКлиентБазовый.ОбработкаОповещенияОтредактированаИсторияДокументыФизическихЛиц(
					ЭтотОбъект,
					ФизическоеЛицоСсылка,
					ИмяСобытия,
					Параметр,
					Источник);
				СотрудникиКлиентСервер.ОбработатьОтображениеСерияДокументаФизическогоЛица(
					ДокументыФизическихЛиц.ВидДокумента,
					ДокументыФизическихЛиц.Серия,
					Элементы.ДокументыФизическихЛицСерия, ЭтотОбъект);
				СотрудникиКлиентСервер.ОбработатьОтображениеНомерДокументаФизическогоЛица(
					ДокументыФизическихЛиц.ВидДокумента,
					ДокументыФизическихЛиц.Номер,
					Элементы.ДокументыФизическихЛицНомер,
					ЭтотОбъект);
				СерияНомерПаспорта = ПредставлениеДокументаУдостоверяющегоЛичность(ДокументыФизическихЛиц, ПаспортРФ);
				ОтобразитьПаспортныеДанные(ЭтотОбъект);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеДанныхМестаРаботы" И Параметр.Сотрудник = СотрудникСсылка Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	Если ИмяСобытия = "ИзмененыВычеты" И Параметр = ФизическоеЛицоСсылка Тогда
		ОбновитьПредставлениеНалогаНаДоходы();
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДоговорРаботыУслуги" Тогда
		ОбновитьОписаниеДоговораПодряда();
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_НастройкиПрямыхВыплатФСССотрудников" Тогда
		Если Параметр.ГоловнаяОрганизация = Сотрудник.ГоловнаяОрганизация
			И Параметр.ФизическоеЛицо = ФизическоеЛицоСсылка Тогда
			ПолучитьПредставлениеПрямыхВыплатФСС();
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД" Тогда
		Если Источник <> Неопределено Тогда
			Если Параметр.Организация = ТекущаяОрганизация
				И Параметр.Сотрудники.Найти(СотрудникСсылка) <> Неопределено Тогда
				ПолучитьСтатусЗаписиЭлектроннойТрудовойКнижки();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	СотрудникиФормы.СотрудникиПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Сотрудник);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	ПрочитатьКонтактнуюИнформацию(ТекущийОбъект);
	
	СтатусЗаписиЭлектроннойТрудовойКнижки();
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьИЗакрытьНаКлиенте", ЭтотОбъект);
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы, ТекстПредупреждения);
	Иначе
		СотрудникиКлиент.ПроверитьНеобходимостьЗаписи(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	ЗакрытьПослеЗаписи = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СотрудникиКлиент.СотрудникиПриЗакрытии(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ Отказ И НЕ ПараметрыЗаписи.Свойство("ПроверкаПередЗаписьюВыполнена") Тогда 
		ЗаписатьНаКлиенте(Ложь, , Отказ);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Контактная информация
	Если КонтактнаяИнформацияФизическогоЛица <> Неопределено Тогда
		УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(КонтактнаяИнформацияФизическогоЛица, ФизическоеЛицо);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Контактная информация
	
	СотрудникиФормы.СотрудникиПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	Если НЕ ЭтотОбъект.ИспользоватьКадровыйУчет Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			МодульРасчетЗарплатыДляНебольшихОрганизаций.ЗапомнитьТекущееКоличествоСотрудников(ЭтотОбъект, ТекущаяОрганизация);
		КонецЕсли;
	КонецЕсли;
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СотрудникиФормы.СотрудникиПриЗаписиНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	ПараметрыСклонения = ПараметрыСклонения(ФизическоеЛицо);
	СклонениеПредставленийОбъектов.ПриЗаписиФормыОбъектаСклонения(ЭтотОбъект, СокрЛП(ФизическоеЛицо.ФИО), ФизическоеЛицоСсылка, ПараметрыСклонения);
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
	Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		Если ДокументыФизическихЛиц.Период = '00010101' Тогда
			СотрудникиКлиентСервер.ОбновитьПолеУдостоверениеЛичностиПериод(ЭтотОбъект);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДокументыФизическихЛиц.ДатаВыдачи)
			ИЛИ ЗначениеЗаполнено(ДокументыФизическихЛиц.Серия)
			ИЛИ ЗначениеЗаполнено(ДокументыФизическихЛиц.Номер)
			ИЛИ ЗначениеЗаполнено(ДокументыФизическихЛиц.КемВыдан) Тогда
			
			ДополнительныеСвойства = Новый Структура;
			Если СозданиеНового Тогда
				ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			КонецЕсли;
		
			РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФормеПоСтруктуре(
				ЭтотОбъект,
				"ДокументыФизическихЛиц",
				Новый Структура("Физлицо", ФизическоеЛицоСсылка),
				,
				ДополнительныеСвойства);
				
			КонецЕсли;
		
		ЗаписатьМестоВыплатыЗарплатыСотрудника();
		ЗаписатьОсновнойБанковскийСчет();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	СотрудникиФормы.СотрудникиПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
	Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		УчетЗарплаты.ПроверитьПоддержкуСовместительства(ТекущийВидЗанятости);
		УчетЗарплаты.ОбновитьИЗаполнитьРегулярныеЗадачи(УникальныйИдентификатор, ТекущийОбъект.ГоловнаяОрганизация);
	КонецЕсли;
	
	СинхронизацияДанныхЗарплатаКадры.ЗапуститьОтложеннуюОбработкуЗаполненияДанныхПоФизическимЛицам(ТекущийОбъект);
	СинхронизацияДанныхЗарплатаКадры.ЗапуститьОтложеннуюОбработкуЗаполненияДанныхПоСотрудникам(ТекущийОбъект);
	
	СтатусЗаписиЭлектроннойТрудовойКнижки();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	СотрудникиКлиент.СотрудникиПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
	Если НЕ ПредупреждениеПоказано Тогда
		Если НЕ ЭтотОбъект.ИспользоватьКадровыйУчет Тогда
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций") Тогда
				МодульРасчетЗарплатыДляНебольшихОрганизацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизацийКлиент");
				ПредупреждениеПоказано = МодульРасчетЗарплатыДляНебольшихОрганизацийКлиент.ПоказатьПредупреждениеОбОграниченияхРежимаРасчетаЗарплатыДляНебольшихОрганизаций(
					ЭтотОбъект, ТекущаяОрганизация);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ПоказатьПредупреждениеОбОграниченияхРежимаРасчетаЗарплатыДляНебольшихОрганизацийЗавершение();
	КонецЕсли;
		
	Если Не ПредупреждениеПоказано Тогда
		ПоказатьПредупреждениеОбОграниченияхРежимаРасчетаЗарплатыДляНебольшихОрганизацийЗавершение();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ТекстСообщения = "";
	Если НЕ ЗначениеЗаполнено(ФИОФизическихЛиц.Фамилия) И НЕ ЗначениеЗаполнено(ФИОФизическихЛиц.Имя) Тогда
		ТекстСообщения = НСтр("ru='Не заполнены фамилия, имя сотрудника'")
	ИначеЕсли НЕ ЗначениеЗаполнено(ФИОФизическихЛиц.Фамилия) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена фамилия сотрудника'")
	ИначеЕсли НЕ ЗначениеЗаполнено(ФИОФизическихЛиц.Имя) Тогда
		ТекстСообщения = НСтр("ru='Не заполнено имя сотрудника'")
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ФИО", "ФизическоеЛицо", Отказ);
	КонецЕсли;
	
	Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		
		Если ЗначениеЗаполнено(ДокументыФизическихЛиц.ВидДокумента)
			ИЛИ НЕ Элементы.ДокументыФизическихЛицВидДокумента.Видимость Тогда
			Если ЗначениеЗаполнено(ДокументыФизическихЛиц.Серия)
				ИЛИ ЗначениеЗаполнено(ДокументыФизическихЛиц.Номер)
				ИЛИ ЗначениеЗаполнено(ДокументыФизическихЛиц.КемВыдан)
				ИЛИ ЗначениеЗаполнено(ДокументыФизическихЛиц.ДатаВыдачи) Тогда
			
				Если НЕ ЗначениеЗаполнено(ДокументыФизическихЛиц.Серия) ИЛИ НЕ ЗначениеЗаполнено(ДокументыФизическихЛиц.Номер) Тогда
					Если (НЕ ЗначениеЗаполнено(ДокументыФизическихЛиц.ВидДокумента)
						ИЛИ ДокументыФизическихЛиц.ВидДокумента = ПаспортРФ)
						И ГражданинРФ Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							НСтр("ru='Не указаны серия и номер паспорта'"),
							,
							"СерияНомерПаспорта",
							,
							Отказ);
					Иначе
						Если НЕ ЗначениеЗаполнено(ДокументыФизическихЛиц.Серия) 
							И ДокументыФизическихЛиц.ВидДокумента = ПаспортРФ Тогда
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
								НСтр("ru='Не указана серия документа, удостоверяющего личность'"),
								,
								"ДокументыФизическихЛиц.Серия",
								,
								Отказ);
						КонецЕсли;
						Если НЕ ЗначениеЗаполнено(ДокументыФизическихЛиц.Номер) Тогда
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
								НСтр("ru='Не указан номер документа, удостоверяющего личность'"),
								,
								"ДокументыФизическихЛиц.Номер",
								,
								Отказ);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ДокументыФизическихЛиц.ДатаВыдачи) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтрШаблон(НСтр("ru='Не указана дата выдачи %1'"),
						?(ДокументыФизическихЛиц.ВидДокумента = ПаспортРФ,
						НСтр("ru='паспорта'"),
						НСтр("ru='документа, удостоверяющего личность'"))),
					,
					"ДокументыФизическихЛиц.ДатаВыдачи",
					,
					Отказ);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	
		Если МестаВыплатыЗарплатыСотрудников.Вид = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет Тогда
			Если НЕ ЗначениеЗаполнено(ФизическоеЛицо.ОсновнойБанковскийСчет) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru='Не заполнен банковский счет'"),
					,
					"ОсновнойБанковскийСчетПредставление",
					,
					Отказ);
			КонецЕсли;
		ИначеЕсли МестаВыплатыЗарплатыСотрудников.Вид = Перечисления.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект Тогда
			Если НЕ ЛицевойСчетЗаполнен Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru='Не заполнены параметры зачисления по зарплатному проекту'"),
					,
					"ЛицевойСчетСотрудникаПредставление",
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		// СтандартныеПодсистемы.Свойства
		УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, Сотрудник);
		// Конец СтандартныеПодсистемы.Свойства
		
		СотрудникиФормы.СотрудникиОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

////////////////////////////////////////////////////////////////////////////////
// При изменении данных физлица / сотрудника.

&НаКлиенте
Процедура ДатаПриемаПриИзменении(Элемент)
	
	ПроверитьОрганизациюИПодразделениеСотрудника();
	ПроверитьВидЗанятостиСотрудника();
	СотрудникиКлиентСервер.УстановитьИнфоНадпись(ЭтотОбъект, ОбщегоНазначенияКлиент.ДатаСеанса());
	УстановитьФункциональныеОпцииФормы(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФизлицоИННПриИзменении(Элемент)
	
	СотрудникиКлиент.СотрудникиИННПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ФизлицоСтраховойНомерПФРПриИзменении(Элемент)
	
	СотрудникиКлиент.СотрудникиСтраховойНомерПФРПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаУвольненияПриИзменении(Элемент)
	
	СотрудникиКлиентСервер.УстановитьИнфоНадпись(ЭтотОбъект, ОбщегоНазначенияКлиент.ДатаСеанса());
	Если НЕ ЗначениеЗаполнено(ДатаУвольнения) Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяОрганизацияПриИзменении(Элемент)
	
	ТекущаяОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗанятостиПриИзменении(Элемент)
	
	СотрудникиКлиент.ПроверитьКонфликтыВидаЗанятостиССуществующимиСотрудниками(
		Сотрудник.Ссылка,
		Сотрудник.ФизическоеЛицо,
		ТекущаяОрганизация,
		ТекущийВидЗанятости,
		ДатаПриема);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСтавокПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "Другая" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("КоличествоСтавок", КоличествоСтавок);
		Оповещение = Новый ОписаниеОповещения("ВводКоличестваСтавокЗавершение", ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.ВводСтавкиСотрудника",
			ПараметрыОткрытия,
			ЭтотОбъект,
			УникальныйИдентификатор,
			,
			,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		КоличествоСтавокПредыдущее    = КоличествоСтавок;
		КоличествоСтавок       = ВыбранноеЗначение;
		КоличествоСтавокПредставление = Окр(КоличествоСтавок, 5);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяТарифнаяСтавкаПриИзменении(Элемент)
	
	СотрудникиКлиентСервер.УстановитьИнфоНадпись(ЭтотОбъект, ОбщегоНазначенияКлиент.ДатаСеанса());
	
КонецПроцедуры

&НаКлиенте
Процедура ВАрхивеПриИзменении(Элемент)
	
	СотрудникиКлиентСервер.УстановитьИнфоНадпись(ЭтотОбъект, ОбщегоНазначенияКлиент.ДатаСеанса());
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнитьПредставлениеПриИзменении(Элемент)
	
	СотрудникиКлиент.ДополнитьПредставлениеСотрудникаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеНаименованияПриИзменении(Элемент)
	
	СотрудникиКлиент.СформироватьНаименованиеСотрудника(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФизлицоПолПриИзменении(Элемент)
	
	СотрудникиКлиент.ЗаблокироватьФизическоеЛицоПриРедактировании(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаАвансаПриИзменении(Элемент)
	
	Если ТекущийСпособРасчетаАванса = ПредопределенноеЗначение("Перечисление.СпособыРасчетаАванса.ПроцентомОтТарифа") Тогда
		ТекущийАванс = РазмерАвансаВПроцентахПоУмолчанию;
	Иначе
		ТекущийАванс = 0;
	КонецЕсли; 
	
	РасчетЗарплатыКлиентСервер.УстановитьПоказРазмераАванса(ЭтотОбъект, "ТекущийСпособРасчетаАванса");
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникИзменилФИОНажатие(Элемент)

	СотрудникиКлиент.СотрудникИзменилФИОНажатие(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ФИОПриИзменении(Элемент) Экспорт
	
	СотрудникиКлиент.ПриИзмененииФИОСотрудника(ЭтотОбъект);
	ПросклонятьФИО();
	
КонецПроцедуры

&НаКлиенте
Процедура ФИООбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	ФизическиеЛицаЗарплатаКадрыКлиент.ФИООбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ФИОАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ФизическиеЛицаЗарплатаКадрыКлиент.ФИОАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ФизлицоДатаРожденияПриИзменении(Элемент)
	
	СотрудникиКлиент.ЗаблокироватьФизическоеЛицоПриРедактировании(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаДоступаПриИзменении(Элемент)
	
	СотрудникиКлиент.ЗаблокироватьФизическоеЛицоПриРедактировании(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СерияНомерПаспортаПриИзменении(Элемент)
	
	МассивПодстрок = СтрРазделить(СерияНомерПаспорта, " ", Истина);
	
	Если МассивПодстрок.Количество() = 3 Тогда
		ДокументыФизическихЛиц.Номер = МассивПодстрок[2];
		МассивПодстрок.Удалить(2);
	Иначе
		ДокументыФизическихЛиц.Номер = "";
	КонецЕсли;
	
	ДокументыФизическихЛиц.Серия = СтрСоединить(МассивПодстрок, " ");
	
	Если ДокументыФизическихЛиц.ВидДокумента <> ПаспортРФ Тогда
		ДокументыФизическихЛиц.ВидДокумента = ПаспортРФ;
	КонецЕсли;
	
	СотрудникиКлиент.ДокументыФизическихЛицВидДокументаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицВидДокументаПриИзменении(Элемент)
	
	СотрудникиКлиент.ДокументыФизическихЛицВидДокументаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицСерияПриИзменении(Элемент)
	
	СерияНомерПаспорта = ПредставлениеДокументаУдостоверяющегоЛичность(ДокументыФизическихЛиц, ПаспортРФ);
	СотрудникиКлиент.ДокументыФизическихЛицСерияПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицНомерПриИзменении(Элемент)
	
	СерияНомерПаспорта = ПредставлениеДокументаУдостоверяющегоЛичность(ДокументыФизическихЛиц, ПаспортРФ);
	СотрудникиКлиент.ДокументыФизическихЛицНомерПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СотрудникиКлиент.ДокументыФизическихЛицВидДокументаНачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицДатаВыдачиПриИзменении(Элемент)
	
	СотрудникиКлиентСервер.ОбновитьПолеУдостоверениеЛичностиПериод(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицКемВыданПриИзменении(Элемент)
	
	СотрудникиКлиентСервер.ОбновитьПолеУдостоверениеЛичностиПериод(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицКодПодразделенияПриИзменении(Элемент)
	
	СотрудникиКлиентСервер.ОбновитьПолеУдостоверениеЛичностиПериод(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияПолеАдресПоПропискеФизическиеЛицаНажатие(Элемент, СтандартнаяОбработка)
	
	Если НЕ СотрудникиКлиент.ЗаблокироватьФизическоеЛицоПриРедактировании(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	Если ЗначениеЗаполнено(Сотрудник.Наименование) Тогда
		ПараметрыОткрытия.Вставить("Заголовок", СтрШаблон(НСтр("ru='%1: Адрес по прописке'"), Сотрудник.Наименование));
	Иначе
		ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru=' Адрес по прописке'"));
	КонецЕсли;
	
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, Модифицированность, СтандартнаяОбработка, ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура МестаВыплатыПриИзменении(Элемент)
	
	ОтобразитьМестоВыплаты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗаписьЭлектроннойТрудовойКнижкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗаписиТрудовойКнижки.Количество() = 0 Тогда
		Возврат
	ИначеЕсли ЗаписиТрудовойКнижки.Количество() = 1 Тогда
		ПоказатьЗначение(, ЗаписиТрудовойКнижки[0].ЗаписьЭлектроннойТрудовойКнижки)
	Иначе
		СписокЗаписей = Новый СписокЗначений;
		Для Каждого СтрокаТаблицы Из ЗаписиТрудовойКнижки Цикл
			СписокЗаписей.Добавить(СтрокаТаблицы.ЗаписьЭлектроннойТрудовойКнижки);
		КонецЦикла;
		
		Оповещение = Новый ОписаниеОповещения("НадписьЗаписьЭлектроннойТрудовойКнижкиПоказатьОповещение", ЭтотОбъект);
		ПоказатьВыборИзМеню(Оповещение, СписокЗаписей, Элемент);
	КонецЕсли
		
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяДолжностьОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	УчетЗарплатыКлиент.ДолжностьОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка);
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Должности") Тогда
		ТекущаяДолжность = ВыбранноеЗначение;
		Элемент.ОбновитьТекстРедактирования();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяДолжностьАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ПустаяСтрока(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбораДолжности(ПараметрыПолученияДанных, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяДолжностьОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если Не ПустаяСтрока(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбораДолжности(ПараметрыПолученияДанных, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ОткрытьФормуСотрудника(Команда)

	СотрудникиКлиент.ОткрытьФормуСотрудника(ЭтотОбъект, Команда);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДругиеМестаРаботы(Команда)
	
	СотрудникиКлиент.ОткрытьФормуСпискаМестРаботыФизическогоЛица(ФизическоеЛицоСсылка, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОформитьНаОсновании(Команда)
	
	СотрудникиКлиент.ОформитьНаОсновании(ЭтотОбъект, СотрудникСсылка, Команда.Имя);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
			Команда,
			ЭтотОбъект,
			Сотрудник);
	КонецЕсли;
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура ОформитьПриемНаРаботу(Команда)
	
	СотрудникиКлиент.ОформитьНаОсновании(ЭтотОбъект, СотрудникСсылка, "Документы.ПриемНаРаботу");
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьДоговорРаботыУслуги(Команда)
	
	СотрудникиКлиент.ОформитьНаОсновании(ЭтотОбъект, СотрудникСсылка, "Документы.ДоговорРаботыУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьПриемНаРаботуУпрощенный(Команда)
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеПриемаНаРаботуЗавершение", ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура("ТекущаяОрганизация, ТекущееПодразделение,
		|ТекущийВидЗанятости, ТекущийСпособРасчетаАванса, ТекущийАванс, КоличествоСтавок,
		|АвансРедактируется, РазмерАвансаВПроцентахПоУмолчанию,
		|ИспользоватьНесколькоОрганизаций, УчетЗарплатыИКадровСредствамиБухгалтерии, ПрименятьСевернуюНадбавку");
	
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ЭтотОбъект);
	
	ПараметрыОткрытия.Вставить("АдресДополнительныхПараметров", ПодготовитьДополнительныеПараметрыОткрытия());

	ОткрытьФорму("Справочник.Сотрудники.Форма.ПриемНаРаботуУпрощенный",
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ЗакрытьПослеЗаписи = Истина;
	ЗаписатьНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	ЗаписатьНаКлиенте(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОклад(Команда)
	
	ЗначенияЗаполнения = Новый Структура("ИзменитьНачисления", Истина);
	ПараметрыОткрытияФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	СотрудникиКлиент.ОформитьНаОсновании(ЭтотОбъект, СотрудникСсылка, "Документы.КадровыйПеревод", ПараметрыОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДоговор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ДоговорРаботыУслуги.Ссылка);
	ОткрытьФорму("Документ.ДоговорРаботыУслуги.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ФИОФизическихЛицИстория(Команда)
	
	СотрудникиКлиент.СотрудникиОткрытьФормуРедактированияИстории("ФИОФизическихЛиц", ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КадровыеДокументы(Команда)
	
	СотрудникиКлиент.ОткрытьДополнительнуюФорму(
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("ЖурналДокументов.КадровыеДокументы.ФормаСписка"), ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛичныеДанные(Команда)
	
	ОписаниеДополнительнойФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.ЛичныеДанные");
	ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, ЭтотОбъект,,"ЛичныеДанные", НСтр("ru = 'Личные данные и контактная информация'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектронныеТрудовыеКнижки_ПаспортныеДанные(Команда)
	
	ОписаниеДополнительнойФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.ЛичныеДанные");
	ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, ЭтотОбъект,,"УдостоверениеЛичности", НСтр("ru = 'Паспортные данные'"));
	
КонецПроцедуры

&НаКлиенте
Процедура Склонения(Команда)
	
	ПараметрыСклонения = ПараметрыСклонения(ФизическоеЛицо);
	СклонениеПредставленийОбъектовКлиент.ПоказатьСклонение(ЭтотОбъект, СокрЛП(ФизическоеЛицо.ФИО), ПараметрыСклонения);
	
КонецПроцедуры

&НаКлиенте
Процедура Уволить(Команда)
	
	Если ИспользоватьКадровыйУчет Тогда
		СотрудникиКлиент.ОформитьНаОсновании(ЭтотОбъект, СотрудникСсылка, "Документы.Увольнение");
	Иначе
		Оповещение = Новый ОписаниеОповещения("ВводДатыУвольненияЗавершение", ЭтотОбъект);
		ПоказатьВводДаты(Оповещение, ТекущаяДата(), НСтр("ru = 'Дата увольнения'"), ЧастиДаты.Дата) 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицевойСчетСотрудникаПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеДополнительнойФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.ВыплатыУчетЗатрат");
	ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, ЭтотОбъект,,"ВыплатаЗарплаты", НСтр("ru = 'Карта зарплатного проекта'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПрямыеВыплатыФССПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Сотрудник.ФизическоеЛицо.Пустая() И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацияСотрудника = ТекущаяОрганизация;
	Если Не ЗначениеЗаполнено(ОрганизацияСотрудника) И ЗначениеЗаполнено(ДоговорРаботыУслуги.Ссылка) Тогда // Договорник
		ОрганизацияСотрудника = Сотрудник.ГоловнаяОрганизация;
	КонецЕсли;
		
	УчетПособийСоциальногоСтрахованияКлиент.НастроитьСпособПрямыхВыплатФизическогоЛица(
		ОрганизацияСотрудника,
		Сотрудник.ФизическоеЛицо,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалогНаДоходыПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьДополнительнуюФорму(
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.НалогНаДоходы"), ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура УсловияСтрахованияПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьДополнительнуюФорму(
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.ФизическиеЛица.Форма.Страхование"), ЭтотОбъект, , "УсловияСтрахования");
		
КонецПроцедуры

&НаКлиенте
Процедура ДругаяКонтактнаяИнформация(Команда)
	
	ОписаниеДополнительнойФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.ЛичныеДанные");
	ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, ЭтотОбъект,,"КонтактнаяИнформация", НСтр("ru = 'Вся контактная информация'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектронныеТрудовыеКнижки_КонтактнаяИнформация(Команда)
	
	ОписаниеДополнительнойФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.ЛичныеДанные");
	ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, ЭтотОбъект,,"КонтактнаяИнформация", НСтр("ru = 'Контактная информация'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеДополнительнойФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.ЛичныеДанные");
	ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, ЭтотОбъект,,"Гражданство", НСтр("ru = 'Гражданство'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицИстория(Команда)
	
	СотрудникиКлиент.ОткрытьФормуРедактированияИстории("ДокументыФизическихЛиц", ФизическоеЛицоСсылка, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура БухучетЗарплатыСотрудниковПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеДополнительнойФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.Сотрудники.Форма.ВыплатыУчетЗатрат");
	ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, ЭтотОбъект, , "УчетЗатрат", НСтр("ru = 'Учет расходов'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ФаксимилеПодписи(Команда)
	
	Если НЕ СотрудникиКлиент.ЗаблокироватьФизическоеЛицоПриРедактировании(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ЗаписатьЭлемент", Истина);
	
	Если СозданиеНового И Параметры.Ключ.Пустая() Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
				|Указать подпись возможно только после записи данных.
				|Записать данные?'");
				
		Оповещение = Новый ОписаниеОповещения("КомандаФаксимилеПодписиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);	
		
	Иначе
		
		ДополнительныеПараметры.ЗаписатьЭлемент = Ложь;
		КомандаФаксимилеПодписиЗавершение(Неопределено, ДополнительныеПараметры);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаФаксимилеПодписиЗавершение(Знач Ответ, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Ответ) И Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗаписатьЭлемент Тогда
		Отказ = Ложь;
		ЗаписатьНаКлиенте(Ложь,,Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ФизическоеЛицо, ФаксимилеПодписи", ФизическоеЛицоСсылка, ФизическоеЛицо.ФаксимилеПодписи);
	ОткрытьФорму("Справочник.ФизическиеЛица.Форма.ФормаФаксимилеПодписи",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьЗаписьЭлектроннойТрудовойКнижки(Команда)
	
	Если Сотрудник.ПометкаУдаления Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Нельзя оформить запись электронной трудовой книжки на основании элемента, помеченного на удаление.'"));
		Возврат;
	Иначе
		Если Модифицированность Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Записать");
			Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
			
			Оповещение = Новый ОписаниеОповещения("ВопросПередРегистрациейСледуетЗаписатьЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед регистрацией записи электронной трудовой книжки элемент следует записать'"), Кнопки,, КодВозвратаДиалога.Да);
		Иначе
			СоздатьЗаписьЭлектроннойТрудовойКнижки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИзменитьДокумент(Команда)
	
	ДопуститьИзменениеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ВоинскийУчет(Команда)
	
	СотрудникиКлиент.ОткрытьДополнительнуюФорму(
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.ФизическиеЛица.Форма.ВоинскийУчет"), ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбразованиеКвалификация(Команда)
	
	ДополнительныеПараметры = Новый Структура("ЗаписатьЭлемент", Истина);
	
	Если СозданиеНового И НЕ ЗначениеЗаполнено(Сотрудник.ФизическоеЛицо) Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
				|Переход к сведениям об образовании, квалификации возможен только после записи данных.
				|Данные будут записаны.'");
				
		Оповещение = Новый ОписаниеОповещения("ОбразованиеКвалификацияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
		
	Иначе 
		
		ДополнительныеПараметры.ЗаписатьЭлемент = Ложь;
		ОбразованиеКвалификацияЗавершение(Неопределено, ДополнительныеПараметры);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбразованиеКвалификацияЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗаписатьЭлемент И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	СотрудникиКлиент.ОткрытьДополнительнуюФорму(
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("ОбщаяФорма.ФизическиеЛицаОбразованиеКвалификация"), ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Семья(Команда)
	
	СотрудникиКлиент.ОткрытьДополнительнуюФорму(
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("ОбщаяФорма.ФизическиеЛицаСемья"), ЭтотОбъект, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Сотрудник);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Сотрудник, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Сотрудник);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыСклонения(ФизическоеЛицо)
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	ПараметрыСклонения.ЭтоФИО = Истина;
	ПараметрыСклонения.Пол    = ?(
		ЗначениеЗаполнено(ФизическоеЛицо.Пол),
		?(ФизическоеЛицо.Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской"), 1, 2),
		Неопределено);
	Возврат ПараметрыСклонения;
КонецФункции
// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, РеквизитФормыВЗначение("Сотрудник"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Сотрудник);
	
КонецПроцедуры
// Конец ТехнологияСервиса.ИнформационныйЦентр

// СтандартныеПодсистемы.КонтрольВеденияУчета

&НаКлиенте
Процедура Подключаемый_ОткрытьОтчетПоПроблемам(ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка)
	
	КонтрольВеденияУчетаКлиентБЗК.ОткрытьОтчетПоПроблемамОбъекта(ЭтотОбъект, Сотрудник.Ссылка, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

&НаСервере
Процедура ПрочитатьДанныеСвязанныеСФизлицом() Экспорт

	СотрудникиФормы.ПрочитатьДанныеСвязанныеССотрудником(ЭтотОбъект);
	КоличествоСтавокПредставление = Окр(КоличествоСтавок, 5);

КонецПроцедуры

&НаСервере
Процедура ОбработкаИзмененияДанныхОРабочемМестеНаСервере(ПараметрСотрудник) Экспорт

	СотрудникиФормы.ОбработкаИзмененияДанныхОРабочемМесте(ЭтотОбъект, ПараметрСотрудник, "ДругиеРабочиеМеста");

КонецПроцедуры

&НаСервере
Процедура ТекущаяОрганизацияПриИзмененииНаСервере() Экспорт
	
	СотрудникиФормы.ПриИзмененииОрганизации(ЭтотОбъект);
	
	УстановитьФункциональныеОпцииФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФизическогоЛицаНаСервере() Экспорт
	
	СотрудникиФормы.ОбновитьДанныеФизическогоЛица(ЭтотОбъект);
	ОписаниеВспомогательныхРеквизитовПриЧтенииНаСервере();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаборЗаписейПериодическихСведений(ИмяРегистра, ВедущийОбъект) Экспорт
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтотОбъект, ИмяРегистра, ВедущийОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДополнительныеПараметрыОткрытия()
	
	ДополнительныеПараметрыОткрытия = Новый Структура();
	ДополнительныеПараметрыОткрытия.Вставить("Сотрудник", РеквизитФормыВЗначение("Сотрудник", Тип("СправочникОбъект.Сотрудники")));
	ДополнительныеПараметрыОткрытия.Вставить("ФизическоеЛицо", РеквизитФормыВЗначение("ФизическоеЛицо", Тип("СправочникОбъект.ФизическиеЛица")));
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(ДополнительныеПараметрыОткрытия, УникальныйИдентификатор);
	
	Возврат АдресВременногоХранилища;
	
КонецФункции


#Область ЗаписьЭлемента

&НаКлиенте
Процедура ЗаписатьНаКлиенте(ЗакрытьПослеЗаписи, ОповещениеЗавершения = Неопределено, Отказ = Ложь) Экспорт 
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ЗаписьЭлементаСправочникаСотрудники");
	
	СотрудникиКлиент.СохранитьДанныеФорм(ЭтотОбъект, Отказ, ЗакрытьПослеЗаписи);
	Если НЕ ПроверяютсяОднофамильцы Тогда
		ПараметрыЗаписи = Новый Структура;
		СотрудникиКлиент.СотрудникиПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи, ОповещениеЗавершения, ЗакрытьПослеЗаписи);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


////////////////////////////////////////////////////////////////////////////////
// Работа с дополнительными формами.

&НаСервере
Функция АдресДанныхДополнительнойФормыНаСервере(ОписаниеДополнительнойФормы) Экспорт
	Возврат СотрудникиФормы.АдресДанныхДополнительнойФормы(ОписаниеДополнительнойФормы, ЭтотОбъект);
КонецФункции

&НаСервере
Процедура ПрочитатьДанныеИзХранилищаВФормуНаСервере(Параметр) Экспорт
	
	СотрудникиФормы.ПрочитатьДанныеИзХранилищаВФорму(
		ЭтотОбъект,
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы(Параметр.ИмяФормы),
		Параметр.АдресВХранилище);
		
	ОбновитьОписанияРеквизитов(Параметр.ИмяФормы);
	УстановитьПараметрыИностранногоГражданина();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеДополнительнойФормы(ИмяФормы, Отказ, ТекущийОбъект) Экспорт
	
	СотрудникиФормы.СохранитьДанныеДополнительнойФормы(ЭтотОбъект, ИмяФормы, Отказ, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредупреждениеОбОграниченияхРежимаРасчетаЗарплатыДляНебольшихОрганизацийЗавершение(ОписаниеОповещения = Неопределено) Экспорт
	
	Если ЗакрытьПослеЗаписи И Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаКлиенте
Процедура Подключаемый_ПросклонятьПредставлениеПоВсемПадежам() 
	
	ПросклонятьФИО();
	
КонецПроцедуры

&НаКлиенте
Процедура ПросклонятьФИО()
	
	ПараметрыСклонения = ПараметрыСклонения(ФизическоеЛицо);
	СклонениеПредставленийОбъектовКлиент.ПросклонятьПредставление(ЭтотОбъект, СокрЛП(ФизическоеЛицо.ФИО), ПараметрыСклонения);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаКлиенте
Процедура УстановитьРежимТолькоПросмотраЛичныхДанных() Экспорт
	
	СотрудникиКлиентСервер.УстановитьРежимТолькоПросмотраЛичныхДанныхВФормеСотрудника(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЦветСтиляЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
	ОписаниеВспомогательныхРеквизитовПриЧтенииНаСервере();
	УчетЗарплаты.УправлениеДоступностьюКадровыхРеквизитов(ЭтотОбъект, НЕ МероприятиеПереданоПФР, Истина);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	УчетЗарплатыИКадровСредствамиБухгалтерии    = Форма.УчетЗарплатыИКадровСредствамиБухгалтерии;
	РассчитыватьЗарплатуНебольшихОрганизаций    = Форма.РасчетЗарплатыДляНебольшихОрганизаций;
	ИспользоватьКадровыйУчет                    = Форма.ИспользоватьКадровыйУчет;
	ИспользоватьНачисленияПоДоговорам           = Форма.ИспользоватьНачисленияПоДоговорам;
	МожноРедактировать                          = Форма.МожноРедактировать;
	ИспользуетсяСервисЭлектронныхТрудовыхКнижек = Форма.ИспользуетсяСервисЭлектронныхТрудовыхКнижек;
	ИспользоватьКадровыйУчетПриВнешнейЗарплате  = Форма.ИспользоватьКадровыйУчетПриВнешнейЗарплате;
	
	Принят     = ЗначениеЗаполнено(Форма.ДатаПриема);
	Уволен     = ЗначениеЗаполнено(Форма.ДатаУвольнения);
	Договорник = ЗначениеЗаполнено(Форма.ДоговорРаботыУслуги.Ссылка);
	
	Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаОформлениеСотрудника",
			"Видимость",
			(НЕ Договорник ИЛИ Не Принят) И Форма.РежимВыбора = Ложь);
			
		// Кадровые данные видны только в том случае, когда сотрудник оформлен 
		// по трудовому или гражданско-правовому договору.
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаРабота",
			"Заголовок",
			?(ИспользоватьНачисленияПоДоговорам,
				НСтр("ru = 'Договоры'"),
				НСтр("ru = 'Договор'")));
			
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОформитьДокумент",
			"Видимость",
			Форма.РежимВыбора = Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СведенияОДоговорахПодряда",
			"Видимость",
			Договорник);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СведенияОРабочихМестах",
			"Видимость",
			Принят
			ИЛИ (НЕ ИспользоватьКадровыйУчет 
				И НЕ ИспользоватьНачисленияПоДоговорам));
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОформитьДоговорРаботыУслуги",
			"Видимость",
			НЕ Договорник
			И РассчитыватьЗарплатуНебольшихОрганизаций
			И ИспользоватьНачисленияПоДоговорам
			И МожноРедактировать);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаВыплатаЗарплаты",
			"Видимость",
			(Принят
			ИЛИ (НЕ Форма.ИспользоватьКадровыйУчет
				И НЕ (РассчитыватьЗарплатуНебольшихОрганизаций И ИспользоватьНачисленияПоДоговорам)))
			ИЛИ Договорник);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаНалогиИВзносы",
			"Видимость",
			(Принят
			ИЛИ (НЕ Форма.ИспользоватьКадровыйУчет
				И НЕ (РассчитыватьЗарплатуНебольшихОрганизаций И ИспользоватьНачисленияПоДоговорам)))
			ИЛИ Договорник);
			
		// Подробности кадрового учета
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОформитьПриемНаРаботуУпрощенный",
			"Видимость",
			НЕ Принят
			И РассчитыватьЗарплатуНебольшихОрганизаций
			И ИспользоватьНачисленияПоДоговорам
			И НЕ ИспользоватьКадровыйУчет);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаУвольнение",
			"Доступность",
			Принят И НЕ Форма.Параметры.Ключ.Пустая());
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"КнопкаНовогоОформитьПриемНаРаботу",
			"Видимость",
			НЕ Принят
			И МожноРедактировать);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатаПриема",
			"Видимость",
			Принят ИЛИ НЕ ИспользоватьКадровыйУчет);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаУвольнение",
			"Видимость",
			Принят ИЛИ НЕ ИспользоватьКадровыйУчет);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Уволить",
			"Видимость",
			НЕ ИспользоватьКадровыйУчет);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Уволить",
			"Доступность",
			Принят И НЕ Уволен И НЕ Форма.Параметры.Ключ.Пустая());
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИзменитьОклад",
			"Доступность",
			Принят И НЕ Уволен);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИзменитьОклад",
			"Видимость",
			НЕ Уволен
			И МожноРедактировать);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИзменитьДоговор",
			"Видимость",
			МожноРедактировать);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатаУвольнения",
			"Видимость",
			Уволен);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВАрхиве",
			"Видимость",
			Уволен);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаБухгалтерскийУчетЗарплаты",
			"Видимость",
			Форма.ПользовательУправляетСчетамиУчета
			И (Принят ИЛИ Договорник));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаАвансСотрудника",
			"Видимость",
			Форма.АвансРедактируется);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПроцентСевернойНадбавки",
			"Видимость",
			Форма.ПрименятьСевернуюНадбавку);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЛичныеДанные",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаВыплатаЗарплаты",
			"ТолькоПросмотр",
			((НЕ Принят ИЛИ Уволен) И ИспользоватьКадровыйУчет)
				И НЕ Договорник);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОсновнойБанковскийСчетПредставление",
			"Доступность",
			Форма.МожноРедактироватьБанковскийСчет
				Или Не ПустаяСтрока(Форма.ОсновнойБанковскийСчетНаименование));
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПрямыеВыплатыФССПредставление",
			"Видимость",
			Принят ИЛИ Договорник);
			
		ОтобразитьМестоВыплаты(Форма);
		ОтобразитьПаспортныеДанные(Форма);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЗаписьЭлектроннойТрудовойКнижкиПросмотр",
			"Видимость",
			НЕ ИспользоватьКадровыйУчет
			И (НЕ Уволен ИЛИ Форма.ДатаУвольнения >= Форма.ДатаНачалаУчетаЭТК));
			
		УправлениеФормойКадровогоМероприятия(Форма);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаОформлениеСотрудника",
			"Видимость",
			ИспользуетсяСервисЭлектронныхТрудовыхКнижек);
		
		Элементы.ГруппаРабота.Заголовок = НСтр("ru = 'Кадровые данные'");
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ФизлицоДатаРождения",
			"АвтоОтметкаНезаполненного",
			ИспользуетсяСервисЭлектронныхТрудовыхКнижек);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатыПриемаУвольнения",
			"Видимость",
			ИспользуетсяСервисЭлектронныхТрудовыхКнижек
				Или ИспользоватьКадровыйУчетПриВнешнейЗарплате);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаВыплатаЗарплаты",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаНалогиИВзносы",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаБухгалтерскийУчетЗарплаты",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОформитьДокумент",
			"Видимость",
			ИспользуетсяСервисЭлектронныхТрудовыхКнижек);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПаспортныеДанные",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПроцентСевернойНадбавки",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЛичныеДанные",
			"Видимость",
			Не ИспользуетсяСервисЭлектронныхТрудовыхКнижек);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПрямыеВыплатыФССПредставление",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СведенияОДоговорахПодряда",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЗаписьЭлектроннойТрудовойКнижкиПросмотр",
			"Видимость",
			Ложь);
		
	КонецЕсли;
	
	Если ИспользоватьКадровыйУчетПриВнешнейЗарплате Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СведенияОРабочихМестах",
			"Видимость",
			Принят);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатыПриемаУвольнения",
			"ТолькоПросмотр",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатаУвольнения",
			"Видимость",
			Уволен);
		
	КонецЕсли;
	
	Если ИспользуетсяСервисЭлектронныхТрудовыхКнижек Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ФизлицоДатаРождения",
			"ОтображениеПодсказки",
			ОтображениеПодсказки.Нет);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатыПриемаУвольнения",
			"ТолькоПросмотр",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатаПриема",
			"ПропускатьПриВводе",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ТекущееПодразделение",
			"ПропускатьПриВводе",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ТекущаяДолжность",
			"ПропускатьПриВводе",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ТекущийВидЗанятости",
			"ПропускатьПриВводе",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"КнопкаНовогоОформитьПриемНаРаботу",
			"Видимость",
			НЕ Принят
			И МожноРедактировать);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИзменитьОклад",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатаУвольнения",
			"Видимость",
			Уволен);
		
	КонецЕсли;
	
	Элементы.ГруппаПредупреждениеНерезидентАУСН.Видимость =
		Форма.СтатусФизическихЛицКакНалогоплательщиковНДФЛ.Статус = Форма.Нерезидент;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьПаспортныеДанные(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Элементы.ДокументыФизическихЛицВидДокумента.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ВидДокумента = Форма.ДокументыФизическихЛиц.ВидДокумента;
	ЭтоПаспортРФ = (ЗначениеЗаполнено(ВидДокумента) И ВидДокумента = Форма.ПаспортРФ)
		ИЛИ (НЕ ЗначениеЗаполнено(ВидДокумента) И Форма.ГражданинРФ);
		
	Элементы.ДокументыФизическихЛицВидДокумента.Видимость = НЕ ЭтоПаспортРФ ИЛИ НЕ Форма.ГражданинРФ;
	Элементы.ГруппаСерияНомерРаздельно.Видимость          = НЕ ЭтоПаспортРФ ИЛИ НЕ Форма.ГражданинРФ;
	Элементы.СерияНомерПаспорта.Видимость                 = ЭтоПаспортРФ И Форма.ГражданинРФ;
	
	Элементы.ДокументыФизическихЛицСерия.АвтоОтметкаНезаполненного = ЭтоПаспортРФ;
	
	СотрудникиКлиентСервер.УстановитьВидимостьСтраныВыдачиДокумента(Форма);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеОткрытияФормы()
	
	СотрудникиКлиент.СотрудникиПриОткрытии(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрытьНаКлиенте(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаписатьНаКлиенте(Истина);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаСервере
Процедура ОбновитьОписанияРеквизитов(ИмяФормы = Неопределено)
	
	Если ИмяФормы = Неопределено ИЛИ ИмяФормы = "Справочник.Сотрудники.Форма.НалогНаДоходы" Тогда
		ОбновитьПредставлениеНалогаНаДоходы();
	КонецЕсли;
	
	Если ИмяФормы = Неопределено ИЛИ ИмяФормы = "Справочник.ФизическиеЛица.Форма.Страхование" Тогда
		ОбновитьПредставлениеУсловийСтрахования();
	КонецЕсли;
	
	Если ИмяФормы = Неопределено ИЛИ ИмяФормы = "Справочник.Сотрудники.Форма.ЛичныеДанные" Тогда
		ОбновитьПредставлениеГражданства();
		ОбновитьПредставлениеАдресаПоПрописке();
	КонецЕсли;
	
	Если ИмяФормы = Неопределено ИЛИ ИмяФормы = "Справочник.Сотрудники.Форма.ВыплатыУчетЗатрат" Тогда
		ОбновитьПредставлениеВыплатыЗарплаты();
		ОбновитьПредставлениеУчетаРасходов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеГражданства(ИсточникДанных = Неопределено)
	
	Если ИсточникДанных = Неопределено
		ИЛИ ИсточникДанных.Количество() = 0 Тогда
		Источник = ГражданствоФизическихЛиц;
	Иначе
		Источник = ИсточникДанных[0];
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Источник.Период) Тогда
		ГражданствоПредставление = Строка(СтранаРоссия);
		ГражданинРФ = Истина;
	Иначе
		Если НЕ ЗначениеЗаполнено(Источник.Страна) Тогда
			ГражданствоПредставление = НСтр("ru = 'Лицо без гражданства'");
		Иначе
			ГражданствоПредставление = Строка(Источник.Страна);
			Если ЗначениеЗаполнено(Источник.ИНН) Тогда
				ГражданствоПредставление = СтрШаблон(НСтр("ru = '%1 (ИНН: %2)'"),ГражданствоПредставление, Источник.ИНН);
			КонецЕсли;
		КонецЕсли;
		ГражданинРФ = Источник.Страна = СтранаРоссия;
	КонецЕсли;
	
	ОтобразитьПаспортныеДанные(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеНалогаНаДоходы(ИсточникДанных = Неопределено, Знач СтруктураВычетов = Неопределено)
	
	Если ИсточникДанных = Неопределено
		ИЛИ ИсточникДанных.Количество() = 0 Тогда
		Источник = СтатусФизическихЛицКакНалогоплательщиковНДФЛ;
	Иначе
		Источник = ИсточникДанных[0];
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Источник.Период)
		ИЛИ НЕ ЗначениеЗаполнено(Источник.Статус)
		ИЛИ Источник.Статус = Резидент Тогда
		
		Если СтруктураВычетов = Неопределено Тогда
			СтруктураВычетов = ПредоставленныеВычетыПоНДФЛ();
		КонецЕсли;
		
		ВычетыПредставление = НСтр("ru = 'Предоставляются %1 вычеты'");
		
		ОписаниеВычетов = НСтр("ru = ''");
		Если СтруктураВычетов.ЕстьСтандартыеВычеты
			И СтруктураВычетов.ЕстьИмущественныеВычеты Тогда
			ОписаниеВычетов = НСтр("ru = 'стандартные и имущественные'");
		ИначеЕсли СтруктураВычетов.ЕстьСтандартыеВычеты Тогда
			ОписаниеВычетов = НСтр("ru = 'стандартные'");
		ИначеЕсли СтруктураВычетов.ЕстьИмущественныеВычеты Тогда
			ОписаниеВычетов = НСтр("ru = 'имущественные'");
		КонецЕсли;
		
		Если СтруктураВычетов.ЕстьСтандартыеВычеты
			ИЛИ СтруктураВычетов.ЕстьИмущественныеВычеты Тогда
			ВычетыПредставление = СтрШаблон(ВычетыПредставление, ОписаниеВычетов);
		Иначе
			ВычетыПредставление = НСтр("ru = 'Вычеты не предоставляются'");
		КонецЕсли;
		
		НалогНаДоходыПредставление = СтрШаблон(НСтр("ru = '%1, ставка 13%% (%2)'"), ВычетыПредставление, НРег(Строка(Резидент)));
	ИначеЕсли Источник.Статус = Нерезидент Тогда
		НалогНаДоходыПредставление = СтрШаблон(НСтр("ru = 'Ставка 30%% (%1)'"), НРег(Строка(Источник.Статус)));
	Иначе
		Статус = Строка(Источник.Статус);
		НалогНаДоходыПредставление = СтрШаблон(НСтр("ru = 'Ставка 13%% (%1%2)'"), НРег(Лев(Статус,1)), Сред(Статус,2));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеУсловийСтрахования(ИсточникДанных = Неопределено, ИсточникДанныхДоп = Неопределено)
	
	Если ИсточникДанных = Неопределено
		ИЛИ ИсточникДанных.Количество() = 0 Тогда
		Источник = СтатусыЗастрахованныхФизическихЛиц;
	Иначе
		Источник = ИсточникДанных[0];
	КонецЕсли;
	
	Если ИсточникДанныхДоп = Неопределено
		ИЛИ ИсточникДанныхДоп.Количество() = 0 Тогда
		ИсточникДоп = СведенияОбИнвалидностиФизическихЛиц;
	Иначе
		ИсточникДоп = ИсточникДанныхДоп[0];
	КонецЕсли;
	
	СтатусПредставление = НСтр("ru = 'Стандартные'");
	Если Источник.ВидЗастрахованногоЛица <> СтатусГражданинаРФ Тогда
		СтатусПредставление = Строка(Источник.ВидЗастрахованногоЛица);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Источник.Период)
		ИЛИ НЕ ЗначениеЗаполнено(Источник.ВидЗастрахованногоЛица)
		ИЛИ Источник.ВидЗастрахованногоЛица = СтатусГражданинаРФ Тогда
		
		СтатусПредставление = Строка(СтатусГражданинаРФ);
		
		Если Год(ТекущаяДатаСеанса()) < 2018 Тогда
			Если ФизическоеЛицо.ПостоянноПроживалВКрыму18Марта2014Года Тогда
				СтатусПредставление = СтрШаблон(НСтр("ru = '%1, проживал в Крыму 18 марта 2014 г.'"), СтатусПредставление);
			КонецЕсли;
		КонецЕсли;
		
		Если ИсточникДоп.Инвалидность Тогда
			СтатусПредставление = СтрШаблон(НСтр("ru = '%1, установлена инвалидность с %2'"),
				СтатусПредставление,
				Формат(?(ЗначениеЗаполнено(ИсточникДоп.ДатаВыдачи),
					ИсточникДоп.ДатаВыдачи, 
					ИсточникДоп.ДатаРегистрацииИзменений),
					"ДФ=dd.MM.yyyy"));
			Если ЗначениеЗаполнено(СведенияОбИнвалидностиФизическихЛиц.ДействуетДо) Тогда
				СтатусПредставление = СтрШаблон(НСтр("ru = '%1 по %2'"),
					СтатусПредставление,
					Формат(ИсточникДоп.ДействуетДо, "ДФ=dd.MM.yyyy"));
			КонецЕсли;
		Иначе
			СтатусПредставление = СтрШаблон(НСтр("ru = '%1, инвалидность не установлена'"), СтатусПредставление);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФизическоеЛицо.ЛьготаПриНачисленииПособий) Тогда
			Если ФизическоеЛицо.ЛьготаПриНачисленииПособий = Перечисления.ОснованияФинансированияПособияФедеральнымБюджетом.ЗачетНестраховыхПериодов Тогда
				СтатусПредставление = СтрШаблон(НСтр("ru = '%1, учитываются периоды военной службы'"), СтатусПредставление);
			Иначе
				СтатусПредставление = СтрШаблон(НСтр("ru = '%1, подвергся воздействию радиации (%2)'"), СтатусПредставление, ФизическоеЛицо.ЛьготаПриНачисленииПособий);
			КонецЕсли;
		КонецЕсли;
		
		УсловияСтрахованияПредставление = СтатусПредставление;
		
	Иначе
		УсловияСтрахованияПредставление = Строка(Источник.ВидЗастрахованногоЛица);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеАдресаПоПрописке(ИсточникДанных = Неопределено)
	
	Если ИсточникДанных = Неопределено Тогда
		ВидИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица");
		МассивДанных = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(Новый Структура("Вид", ВидИнформации));
		Источник = ?(МассивДанных.Количество()<> 0, МассивДанных[0], Неопределено);
	Иначе
		Источник = ИсточникДанных;
	КонецЕсли;
	КонтактнаяИнформацияПолеАдресПоПропискеФизическиеЛица = ?(Источник = Неопределено, "", Источник.Представление);
	
	Если ПустаяСтрока(КонтактнаяИнформацияПолеАдресПоПропискеФизическиеЛица) Тогда
		Если МожноРедактировать Тогда
			КонтактнаяИнформацияПолеАдресПоПропискеФизическиеЛица = НСтр("ru = 'Заполнить'");
		Иначе
			КонтактнаяИнформацияПолеАдресПоПропискеФизическиеЛица = НСтр("ru = '<Не заполнен>'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеВыплатыЗарплаты(ИсточникДанных = Неопределено)
	
	Если ИсточникДанных = Неопределено
		ИЛИ ИсточникДанных.Количество() = 0 Тогда
		Источник = ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам;
	Иначе
		Источник = ИсточникДанных[0];
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Источник.ЗарплатныйПроект)
			И НЕ ЗначениеЗаполнено(Источник.НомерЛицевогоСчета) Тогда
		ЛицевойСчетСотрудникаПредставление = НСтр("ru = 'Заполнить параметры зачисления'");
		Элементы.ЛицевойСчетСотрудникаПредставление.ЦветТекста = ЦветСтиляПоясняющийОшибкуТекст;
		ЛицевойСчетЗаполнен = Ложь;
	Иначе
		ЛицевойСчетСотрудникаПредставление = СтрШаблон(НСтр("ru = '%1 (%2)'"),
			Источник.ЗарплатныйПроект,
			Источник.СпособЗачисления);
		Элементы.ЛицевойСчетСотрудникаПредставление.ЦветТекста = ЦветСтиляЦветГиперссылки;
		ЛицевойСчетЗаполнен = Истина;
	КонецЕсли;
	
	ОсновнойБанковскийСчетНаименование = "";
	Если Не ЗначениеЗаполнено(ФизическоеЛицо.ОсновнойБанковскийСчет) Тогда
		ОсновнойБанковскийСчетПредставление = НСтр("ru = 'Заполнить банковский счет'");
		Элементы.ОсновнойБанковскийСчетПредставление.ЦветТекста = ЦветСтиляПоясняющийОшибкуТекст;
	Иначе
		ОсновнойБанковскийСчетНаименование = СокрЛП(ОсновнойБанковскийСчет.Наименование);
		Если ПустаяСтрока(ОсновнойБанковскийСчетНаименование) Тогда
			ОсновнойБанковскийСчетПредставление = НСтр("ru = '<Не заполнен>'");
		Иначе
			ОсновнойБанковскийСчетПредставление = Строка(ОсновнойБанковскийСчет.Наименование);
		КонецЕсли;
		Элементы.ОсновнойБанковскийСчетПредставление.ЦветТекста = ЦветСтиляЦветГиперссылки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеДоговораПодряда(ИсточникДанных = Неопределено)
	
	Если ИсточникДанных = Неопределено
		ИЛИ ИсточникДанных.Количество() = 0 Тогда
		Если НЕ МожноРедактировать Тогда
			Возврат;
		КонецЕсли;
		Источник = Документы.ДоговорРаботыУслуги.СоздатьДокумент();
	Иначе
		Источник = ИсточникДанных[0].ПолучитьОбъект();
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Источник, "ДоговорРаботыУслуги");
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПредставлениеПрямыхВыплатФСС()
	
	ПрямыеВыплатыФССПредставление = НСтр("ru = '<Авто>'");
	
	ОрганизацияСотрудника = ТекущаяОрганизация;
	Если Не ЗначениеЗаполнено(ОрганизацияСотрудника) И ЗначениеЗаполнено(ДоговорРаботыУслуги.Ссылка) Тогда // Договорник
		ОрганизацияСотрудника = Сотрудник.ГоловнаяОрганизация;
	КонецЕсли;
	
	Способы = СпособыПрямыхВыплатФСС.НастройкиСотрудников(
		ОрганизацияСотрудника,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицоСсылка),
		ТекущаяДатаСеанса());
	СтрокаРезультат = Способы.ТаблицаРезультат[0];
	
	Если НЕ СтрокаРезультат.ОпределятьПоОрганизации Тогда
		ТипыСпособов = СпособыПрямыхВыплатФСС.ТипыСпособовПрямыхВыплат();
		ТипСпособа = ТипыСпособов.Найти(ТипЗнч(СтрокаРезультат.Значение), "Тип");
		Если ТипСпособа <> Неопределено Тогда
			ПрямыеВыплатыФССПредставление = ТипСпособа.Заголовок;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеУчетаРасходов(ИсточникДанных = Неопределено)
	
	Если НЕ ПользовательУправляетСчетамиУчета Тогда
		Возврат;
	КонецЕсли;
	
	Если ИсточникДанных = Неопределено
		ИЛИ ИсточникДанных.Количество() = 0 Тогда
		Источник = БухучетЗарплатыСотрудников;
	Иначе
		Источник = ИсточникДанных[0];
	КонецЕсли;
	
	УчитыватьЕНВД = ОбщегоНазначения.ТекущаяДатаПользователя() < УчетЕНВДКлиентСервер.ДатаОтменыЕНВД();
	
	Если НЕ ЗначениеЗаполнено(Источник.Период)
		ИЛИ (НЕ ЗначениеЗаполнено(Источник.СпособОтраженияЗарплатыВБухучете)
			И (НЕ ЗначениеЗаполнено(Источник.ОтношениеКЕНВД)
				ИЛИ ЗначениеЗаполнено(Источник.ОтношениеКЕНВД) И НЕ УчитыватьЕНВД)) Тогда
		БухучетЗарплатыСотрудниковПредставление = НСтр("ru = '<Авто>'");
	Иначе
		Если ЗначениеЗаполнено(Источник.СпособОтраженияЗарплатыВБухучете) Тогда
			БухучетЗарплатыСотрудниковПредставление = Строка(Источник.СпособОтраженияЗарплатыВБухучете);
		Иначе
			БухучетЗарплатыСотрудниковПредставление = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Источник.ОтношениеКЕНВД) И УчитыватьЕНВД Тогда
			ШаблонЕНВД = ?(ЗначениеЗаполнено(БухучетЗарплатыСотрудниковПредставление), НСтр("ru = ' ;%1'"),НСтр("ru = '%1'"));
			ОтношениеКЕНВД = СтрШаблон(ШаблонЕНВД ,Источник.ОтношениеКЕНВД);
		Иначе
			ОтношениеКЕНВД = "";
		КонецЕсли;
		БухучетЗарплатыСотрудниковПредставление = СтрШаблон(НСтр("ru = '%1%2'"),БухучетЗарплатыСотрудниковПредставление, ОтношениеКЕНВД);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеДокументаУдостоверяющегоЛичность(ДокументыФизическихЛиц, ПаспортРФ)
	
	Если ДокументыФизическихЛиц.ВидДокумента = ПаспортРФ Тогда
		СерияНомер = СтрШаблон(НСтр("ru='%1 %2'"), ДокументыФизическихЛиц.Серия, ДокументыФизическихЛиц.Номер);
	Иначе
		СерияНомер = "";
	КонецЕсли;
	
	Возврат СерияНомер;
	
КонецФункции

&НаСервере
Процедура ПрочитатьКонтактнуюИнформацию(ТекущийОбъект)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ФизическоеЛицо);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаСервере
Процедура РазместитьКонтактнуюИнформацию()
	
	ПараметрыРазмещенияКонтактнойИнформации = УправлениеКонтактнойИнформацией.ПараметрыКонтактнойИнформации();
	ПараметрыРазмещенияКонтактнойИнформации.РазмещеныНаФорме = СтатическиеРеквизиты();
	ПараметрыРазмещенияКонтактнойИнформации.СкрываемыеВиды = СкрываемыеВиды();
	ПараметрыРазмещенияКонтактнойИнформации.РазрешитьДобавлениеПолей = Ложь;
	
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, ФизическоеЛицо, ПараметрыРазмещенияКонтактнойИнформации);
	
КонецПроцедуры

&НаСервере
Функция СтатическиеРеквизиты()
	
	РазмещеныНаФорме = Новый Соответствие();
	
	РазмещеныНаФорме.Вставить(Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица, Истина);
	
	Возврат РазмещеныНаФорме;

КонецФункции

&НаСервере
Функция СкрываемыеВиды()
	
	СкрываемыеВиды = Новый Массив;
	
	СкрываемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.АдресДляИнформированияФизическиеЛица);
	СкрываемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.АдресЗаПределамиРФФизическиеЛица);
	СкрываемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
	СкрываемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица);
	СкрываемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица);
	СкрываемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица);
	СкрываемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
	
	Возврат СкрываемыеВиды;

КонецФункции

&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьКонтактнуюИнформацию(Результат);
КонецПроцедуры

&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(Результат)
	
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, ФизическоеЛицо, Результат);
	ЗаписатьКонтактнуюИнформацию();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонтактнуюИнформацию()
	
	ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица");
	
	КонтактнаяИнформацияМассив = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(Новый Структура("Вид", ВидКонтактнойИнформации));
	Если КонтактнаяИнформацияМассив.Количество() <> 0 Тогда
		ФизическоеЛицо.КонтактнаяИнформация.Удалить(ФизическоеЛицо.КонтактнаяИнформация.Индекс(КонтактнаяИнформацияМассив[0]));
	КонецЕсли;
	
	КонтактнаяИнформацияМассив = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("Вид",ВидКонтактнойИнформации));
	Если КонтактнаяИнформацияМассив.Количество() <> 0 Тогда
		СтрокаАдреса = ФизическоеЛицо.КонтактнаяИнформация.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаАдреса, КонтактнаяИнформацияМассив[0]);
		СтрокаАдреса.Вид = ВидКонтактнойИнформации;
	КонецЕсли;
	
	УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(ФизическоеЛицо,
		КонтактнаяИнформацияМассив[0],
		ВидКонтактнойИнформации,
		Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	КонтактнаяИнформацияМассив = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("Вид",ВидКонтактнойИнформации));
	Если КонтактнаяИнформацияМассив.Количество() <> 0 Тогда
		ОбновитьПредставлениеАдресаПоПрописке(КонтактнаяИнформацияМассив[0]);
		ЗаполнитьЗависимыеАдреса(КонтактнаяИнформацияМассив[0], ВидКонтактнойИнформации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьМестоВыплаты(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.МестаВыплатыЗарплатыСотрудников.Вид = ПредопределенноеЗначение("Перечисление.ВидыМестВыплатыЗарплаты.БанковскийСчет") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЛицевойСчетСотрудникаПредставление",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОсновнойБанковскийСчетПредставление",
			"Видимость",
			Истина);
		
	ИначеЕсли Форма.МестаВыплатыЗарплатыСотрудников.Вид = ПредопределенноеЗначение("Перечисление.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЛицевойСчетСотрудникаПредставление",
			"Видимость",
			Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОсновнойБанковскийСчетПредставление",
			"Видимость",
			Ложь);
		
	Иначе
		
		Если НЕ ЗначениеЗаполнено(Форма.МестаВыплатыЗарплатыСотрудников.Вид) Тогда
			Форма.МестаВыплатыЗарплатыСотрудников.Вид = ПредопределенноеЗначение("Перечисление.ВидыМестВыплатыЗарплаты.Касса");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЛицевойСчетСотрудникаПредставление",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОсновнойБанковскийСчетПредставление",
			"Видимость",
			Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьМестоВыплатыЗарплатыСотрудника()
	
	МестаВыплатыЗарплатыСотрудниковНаборЗаписей = РегистрыСведений.МестаВыплатыЗарплатыСотрудников.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МестаВыплатыЗарплатыСотрудниковНаборЗаписей, МестаВыплатыЗарплатыСотрудников);
	МестаВыплатыЗарплатыСотрудниковНаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьОсновнойБанковскийСчет();
	
	Если МестаВыплатыЗарплатыСотрудников.Вид = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет Тогда
		ОсновнойБанковскийСчетОбъект = РеквизитФормыВЗначение("ОсновнойБанковскийСчет", Тип("СправочникОбъект.БанковскиеСчета"));
		Если НЕ ЗначениеЗаполнено(ОсновнойБанковскийСчетОбъект.Ссылка) Тогда
			ОсновнойБанковскийСчетОбъект.УстановитьСсылкуНового(ОсновнойБанковскийСчетСсылка);
		КонецЕсли;
		ОсновнойБанковскийСчетОбъект.Записать();
		ЗначениеВРеквизитФормы(ОсновнойБанковскийСчетОбъект, "ОсновнойБанковскийСчет");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПредоставленныеВычетыПоНДФЛ()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ЭтотОбъект.ФизическоеЛицоСсылка);
	Запрос.УстановитьПараметр("Организация",    ЭтотОбъект.ТекущаяОрганизация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
	|ГДЕ
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыФизическихЛицНДФЛ КАК СтандартныеВычетыФизическихЛицНДФЛ
	|ГДЕ
	|	СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрНакопления.ИмущественныеВычетыНДФЛ КАК ИмущественныеВычетыНДФЛ
	|ГДЕ
	|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
	|	И ИмущественныеВычетыНДФЛ.Организация = &Организация";
	
	Результат = Запрос.ВыполнитьПакет();
	СтандартыеВычеты    = Результат[0].Выгрузить();
	ИмущественныеВычеты = Результат[1].Выгрузить();
	
	СтруктураВычетов = Новый Структура;
	СтруктураВычетов.Вставить("ЕстьСтандартыеВычеты",    СтандартыеВычеты.Количество()>0);
	СтруктураВычетов.Вставить("ЕстьИмущественныеВычеты", ИмущественныеВычеты.Количество()>0);
	
	Возврат СтруктураВычетов;
	
КонецФункции

&НаСервере
Процедура ОписаниеВспомогательныхРеквизитовПриЧтенииНаСервере()
	
	УчетЗарплатыИКадровСредствамиБухгалтерии    = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	ИспользоватьКадровыйУчет                    = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	ИспользоватьНесколькоОрганизаций            = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийЗарплатаКадрыБазовая");
	ПоддержкаСовместительства                   = ПолучитьФункциональнуюОпцию("ПоддержкаСовместительства");
	ИспользоватьНачисленияПоДоговорам           = ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам");
	ПрименятьСевернуюНадбавку                   = ПолучитьФункциональнуюОпцию("ПрименятьСевернуюНадбавку", Новый Структура("Организация", ТекущаяОрганизация));
	ИспользуетсяСервисЭлектронныхТрудовыхКнижек = ПолучитьФункциональнуюОпцию("ИнтерфейсЭлектронныхТрудовыхКнижек");
	ИспользоватьКадровыйУчетПриВнешнейЗарплате  = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчетПриВнешнейЗарплате");
	ИспользоватьКадровуюИсторию                 = УчетЗарплаты.ИспользоватьКадровуюИсторию();
	ДатаНачалаУчетаЭТК                          = ЭлектронныеТрудовыеКнижки.ДатаНачалаУчета();
	
	ЭтотОбъект.РасчетЗарплатыДляНебольшихОрганизаций = ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций");
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Сотрудники);
	МожноРедактироватьБанковскийСчет = ПравоДоступа("Изменение", Метаданные.Справочники.БанковскиеСчета);
	
	Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		
		ПользовательУправляетСчетамиУчета =
			СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
		ПаспортРФ =
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ");
		Резидент =
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент");
		Нерезидент =
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент");
		СтатусГражданинаРФ =
			Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ;
		СтатусИностранногоГражданина =
			Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПроживающиеИностранцы;
		СтранаРоссия =
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтраныМира.Россия");
		ИностранныйПаспорт =
			Справочники.ВидыДокументовФизическихЛиц.НайтиПоРеквизиту("КодМВД", "10");
			
		ОрганизацияСотрудника = ?(ЗначениеЗаполнено(ТекущаяОрганизация), ТекущаяОрганизация, Сотрудник.ГоловнаяОрганизация);

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ФизическоеЛицо",     ФизическоеЛицоСсылка);
		Запрос.УстановитьПараметр("Сотрудник",          СотрудникСсылка);
		Запрос.УстановитьПараметр("Организация",        ОрганизацияСотрудника);
		Запрос.УстановитьПараметр("Период",             ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("СтранаРоссия",       СтранаРоссия);
		Запрос.УстановитьПараметр("Резидент",           Резидент);
		Запрос.УстановитьПараметр("Нерезидент",         Нерезидент);
		Запрос.УстановитьПараметр("СтатусГражданинаРФ", СтатусГражданинаРФ);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ГражданствоФизическихЛицСрезПоследних.Период КАК Период,
		|	ГражданствоФизическихЛицСрезПоследних.Страна КАК Страна,
		|	ГражданствоФизическихЛицСрезПоследних.ИНН КАК ИНН
		|ИЗ
		|	РегистрСведений.ГражданствоФизическихЛиц.СрезПоследних(&Период, ФизическоеЛицо = &ФизическоеЛицо) КАК ГражданствоФизическихЛицСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних.Период КАК Период,
		|	СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ.СрезПоследних(&Период, ФизическоеЛицо = &ФизическоеЛицо) КАК СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
		|ГДЕ
		|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.СтандартныеВычетыФизическихЛицНДФЛ КАК СтандартныеВычетыФизическихЛицНДФЛ
		|ГДЕ
		|	СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	РегистрНакопления.ИмущественныеВычетыНДФЛ КАК ИмущественныеВычетыНДФЛ
		|ГДЕ
		|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
		|	И ИмущественныеВычетыНДФЛ.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыЗастрахованныхФизическихЛицСрезПоследних.Период КАК Период,
		|	СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица
		|ИЗ
		|	РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&Период, ФизическоеЛицо = &ФизическоеЛицо) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СведенияОбИнвалидностиФизическихЛицСрезПоследних.Период КАК Период,
		|	СведенияОбИнвалидностиФизическихЛицСрезПоследних.Инвалидность КАК Инвалидность,
		|	СведенияОбИнвалидностиФизическихЛицСрезПоследних.ДействуетДо КАК ДействуетДо,
		|	СведенияОбИнвалидностиФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
		|	СведенияОбИнвалидностиФизическихЛицСрезПоследних.ДатаРегистрацииИзменений КАК ДатаРегистрацииИзменений
		|ИЗ
		|	РегистрСведений.СведенияОбИнвалидностиФизическихЛиц.СрезПоследних(&Период, ФизическоеЛицо = &ФизическоеЛицо) КАК СведенияОбИнвалидностиФизическихЛицСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БухучетЗарплатыСотрудниковСрезПоследних.Период КАК Период,
		|	БухучетЗарплатыСотрудниковСрезПоследних.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетЗарплатыСотрудниковСрезПоследних.ОтношениеКЕНВД КАК ОтношениеКЕНВД
		|ИЗ
		|	РегистрСведений.БухучетЗарплатыСотрудников.СрезПоследних(&Период, Сотрудник = &Сотрудник) КАК БухучетЗарплатыСотрудниковСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ДатаОткрытияЛицевогоСчета КАК ДатаОткрытияЛицевогоСчета,
		|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ЗарплатныйПроект КАК ЗарплатныйПроект,
		|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.СпособЗачисления КАК СпособЗачисления
		|ИЗ
		|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам
		|ГДЕ
		|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.Организация = &Организация
		|	И ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ФизическоеЛицо = &ФизическоеЛицо
		|	И ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ДатаОткрытияЛицевогоСчета <= &Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОткрытияЛицевогоСчета УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БанковскиеСчета.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ПО ФизическиеЛица.ОсновнойБанковскийСчет = БанковскиеСчета.Ссылка
		|ГДЕ
		|	ФизическиеЛица.Ссылка = &ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БанковскиеСчета.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.Владелец = &ФизическоеЛицо
		|	И НЕ БанковскиеСчета.ПометкаУдаления";
		
		Результат = Запрос.ВыполнитьПакет();
		
		ОбновитьПредставлениеГражданства(Результат[0].Выгрузить());
		
		ОбновитьПредставлениеНалогаНаДоходы(
			Результат[1].Выгрузить(),
			Новый Структура("ЕстьСтандартыеВычеты, ЕстьИмущественныеВычеты",
				НЕ Результат[2].Пустой(), НЕ Результат[3].Пустой()));
				
		ОбновитьПредставлениеУсловийСтрахования(Результат[4].Выгрузить(), Результат[5].Выгрузить());
		
		ОбновитьПредставлениеУчетаРасходов(Результат[6].Выгрузить());

		ОпределитьПорядокРасчетаАванса();
		ПрочитатьЗаписьОМестеВыплатыЗарплатыСотрудниковДляРедактированияВФорме();
		
		ОсновнойБанковскийСчетСуществует = Не Результат[8].Пустой();
		
		СуществующиеБанковскиеСчетаСотрудника = Новый ФиксированныйМассив(Новый Массив);
		Если Не Результат[9].Пустой() Тогда
			СуществующиеБанковскиеСчетаСотрудника =
				Новый ФиксированныйМассив(Результат[9].Выгрузить().ВыгрузитьКолонку("Ссылка"));
		КонецЕсли;
		
		Если ОсновнойБанковскийСчетСуществует Тогда
			ОсновнойБанковскийСчетСсылка = ФизическоеЛицо.ОсновнойБанковскийСчет;
			ЗначениеВРеквизитФормы(ОсновнойБанковскийСчетСсылка.ПолучитьОбъект(), "ОсновнойБанковскийСчет");
		Иначе
			Если МожноРедактироватьБанковскийСчет Тогда
				ОсновнойБанковскийСчетСсылка = Справочники.БанковскиеСчета.ПолучитьСсылку();
				ОсновнойБанковскийСчетОбъект = Справочники.БанковскиеСчета.СоздатьЭлемент();
				ОсновнойБанковскийСчетОбъект.Владелец = ФизическоеЛицоСсылка;
				ЗначениеВРеквизитФормы(ОсновнойБанковскийСчетОбъект, "ОсновнойБанковскийСчет");
			КонецЕсли;
		КонецЕсли;
		Если Не ЗаблокироватьБанковскийСчет(ОсновнойБанковскийСчетСсылка, ОсновнойБанковскийСчет.ВерсияДанных, УникальныйИдентификатор) Тогда
			МожноРедактироватьБанковскийСчет = Ложь;
		КонецЕсли;
		
		ОбновитьПредставлениеВыплатыЗарплаты(Результат[7].Выгрузить());
		
		ПолучитьДоговорПодряда();
		
		ПолучитьПредставлениеПрямыхВыплатФСС();
		
		УчетЗарплаты.СформироватьСписокСтавок(Элементы.КоличествоСтавокПредставление);
		КоличествоСтавокПредставление = Окр(КоличествоСтавок, 5);
		
	КонецЕсли;
	
	СерияНомерПаспорта = ПредставлениеДокументаУдостоверяющегоЛичность(ДокументыФизическихЛиц, ПаспортРФ);
	ОбновитьПредставлениеАдресаПоПрописке();
	
КонецПроцедуры

Процедура ПолучитьДоговорПодряда()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", СотрудникСсылка);
	Запрос.УстановитьПараметр("Период",    ТекущаяДатаСеанса());
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПериодыДействияДоговоровГражданскоПравовогоХарактера.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера КАК ПериодыДействияДоговоровГражданскоПравовогоХарактера
	|ГДЕ
	|	ПериодыДействияДоговоровГражданскоПравовогоХарактера.Сотрудник = &Сотрудник
	|	И ПериодыДействияДоговоровГражданскоПравовогоХарактера.Регистратор.Дата <= &Период
	|	И ПериодыДействияДоговоровГражданскоПравовогоХарактера.ДатаНачала <= &Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка УБЫВ";
	
	Результат = Запрос.Выполнить().Выгрузить();
	ОбновитьПредставлениеДоговораПодряда(Результат.ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОписаниеДоговораПодряда()
	
	ПолучитьДоговорПодряда();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьПорядокРасчетаАванса()
	
	АвансРедактируется = ПолучитьФункциональнуюОпцию("ИндивидуальныйАванс", Новый Структура("Организация", ТекущаяОрганизация));
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЗаписьОМестеВыплатыЗарплатыСотрудниковДляРедактированияВФорме() Экспорт
	
	МенеджерЗаписи = РегистрыСведений.МестаВыплатыЗарплатыСотрудников.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Сотрудник           = СотрудникСсылка;
	МенеджерЗаписи.ФизическоеЛицо      = ФизическоеЛицоСсылка;
	МенеджерЗаписи.Прочитать();
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Сотрудник           = СотрудникСсылка;
		МенеджерЗаписи.ФизическоеЛицо      = ФизическоеЛицоСсылка;
		МенеджерЗаписи.Вид                 = Перечисления.ВидыМестВыплатыЗарплаты.Касса;
	КонецЕсли;
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "МестаВыплатыЗарплатыСотрудников");
	
КонецПроцедуры

&НаКлиенте
Процедура ВводДатыУвольненияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ДатаУвольнения = Результат;
		Записать();
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗависимыеАдреса(АдресПоПрописке, ВидКонтактнойИнформации)
	
	ЗависимостиВидовАдресов = СотрудникиКлиентСервер.ЗависимостиВидовАдресов();
	
	Если ЗависимостиВидовАдресов <> Неопределено И ЗначениеЗаполнено(АдресПоПрописке.Представление) Тогда
		
		ЗависимыеВидыКонтактнойИнформации = ЗависимостиВидовАдресов[ВидКонтактнойИнформации];
		Если ЗависимыеВидыКонтактнойИнформации <> Неопределено Тогда
			Для Каждого ЗависимыйВидКонтактнойИнформации Из ЗависимыеВидыКонтактнойИнформации Цикл
				КонтактнаяИнформацияМассив = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(Новый Структура("Вид", ЗависимыйВидКонтактнойИнформации));
				Если КонтактнаяИнформацияМассив.Количество() = 0 Тогда
					СтрокаАдреса = ФизическоеЛицо.КонтактнаяИнформация.Добавить();
				Иначе
					СтрокаАдреса = КонтактнаяИнформацияМассив[0];
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(СтрокаАдреса.Представление) Тогда
					ЗаполнитьЗначенияСвойств(СтрокаАдреса, АдресПоПрописке);
					СтрокаАдреса.Вид = ЗависимыйВидКонтактнойИнформации;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыИностранногоГражданина()
	
	Если НЕ ГражданинРФ Тогда
		Если ДокументыФизическихЛиц.ВидДокумента = ПаспортРФ
			ИЛИ НЕ ЗначениеЗаполнено(ДокументыФизическихЛиц.ВидДокумента) Тогда
			ДокументыФизическихЛиц.ВидДокумента = ИностранныйПаспорт;
		КонецЕсли;
	Иначе
		Если ДокументыФизическихЛиц.ВидДокумента = ИностранныйПаспорт Тогда
			ДокументыФизическихЛиц.ВидДокумента = ПаспортРФ;
		КонецЕсли;
	КонецЕсли;
	
	СотрудникиКлиентСервер.УстановитьВидимостьСтраныВыдачиДокумента(ЭтотОбъект);
	
	Если НЕ ГражданинРФ Тогда
		Если СтатусыЗастрахованныхФизическихЛиц.ВидЗастрахованногоЛица = СтатусГражданинаРФ
			ИЛИ НЕ ЗначениеЗаполнено(СтатусыЗастрахованныхФизическихЛиц.Период) Тогда
			РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(
				ЭтотОбъект, "СтатусыЗастрахованныхФизическихЛиц", ФизическоеЛицоСсылка);
			РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(
				ЭтотОбъект, "СведенияОбИнвалидностиФизическихЛиц", ФизическоеЛицоСсылка);
			Если СтатусыЗастрахованныхФизическихЛиц.ВидЗастрахованногоЛица = СтатусГражданинаРФ Тогда
				СтатусыЗастрахованныхФизическихЛиц.ВидЗастрахованногоЛица = СтатусИностранногоГражданина;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтатусыЗастрахованныхФизическихЛиц.Период) Тогда
				СтатусыЗастрахованныхФизическихЛиц.Период = ?(ЗначениеЗаполнено(ДатаПриема), ДатаПриема, ТекущаяДатаСеанса());
			КонецЕсли;
			УстановитьПризнакРедактированияДанныхВДополнительнойФорме("Справочник.ФизическиеЛица.Форма.Страхование", ЭтотОбъект);
			ОбновитьПредставлениеУсловийСтрахования();
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(СтатусыЗастрахованныхФизическихЛиц.Период) Тогда
			РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(
				ЭтотОбъект, "СтатусыЗастрахованныхФизическихЛиц", ФизическоеЛицоСсылка);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СведенияОбИнвалидностиФизическихЛиц.Период) Тогда
			РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(
				ЭтотОбъект, "СведенияОбИнвалидностиФизическихЛиц", ФизическоеЛицоСсылка);
		КонецЕсли;
		Если СтатусыЗастрахованныхФизическихЛиц.ВидЗастрахованногоЛица <> СтатусГражданинаРФ Тогда
			СтатусыЗастрахованныхФизическихЛиц.ВидЗастрахованногоЛица = СтатусГражданинаРФ;
		КонецЕсли;
		УстановитьПризнакРедактированияДанныхВДополнительнойФорме("Справочник.ФизическиеЛица.Форма.Страхование", ЭтотОбъект);
		ОбновитьПредставлениеУсловийСтрахования();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойБанковскийСчетПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СотрудникиКлиент.ЗаблокироватьФизическоеЛицоПриРедактировании(ЭтотОбъект);
	
	Оповещение = Новый ОписаниеОповещения("БанковскийСчетРедактированиеЗавершение", ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо.ОсновнойБанковскийСчет)
		И СуществующиеБанковскиеСчетаСотрудника.Количество() = 0 Тогда
		СоздатьНовыйБанковскийСчет();
	Иначе
		Если МожноРедактироватьБанковскийСчет Тогда
			ВыбратьБанковскийСчет(Элемент);
		Иначе
			РедактироватьБанковскийСчет();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйБанковскийСчет()
	
	Оповещение = Новый ОписаниеОповещения("БанковскийСчетРедактированиеЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Объект", ОсновнойБанковскийСчет);
	ПараметрыФормы.Вставить("ОписаниеДанных", ФизическоеЛицо.ФИО);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "Сотрудник");
	ОткрытьФорму(
		"Справочник.Сотрудники.Форма.БанковскийСчет",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьБанковскийСчет()
	
	Если Не РазблокироватьБанковскийСчет(ОсновнойБанковскийСчетСсылка, УникальныйИдентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("БанковскийСчетРедактированиеЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ОсновнойБанковскийСчетСсылка);
	ПараметрыФормы.Вставить("ОписаниеДанных", ФизическоеЛицо.ФИО);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "Сотрудник");
	ОткрытьФорму(
		"Справочник.Сотрудники.Форма.БанковскийСчет",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьБанковскийСчетИзСписка() Экспорт
	
	Оповещение = Новый ОписаниеОповещения("БанковскийСчетВыборЗавершение", ЭтотОбъект);
	
	СтруктураОтбора = Новый Структура("Владелец", ФизическоеЛицоСсылка);
	ПараметрыФормы  = Новый Структура("Отбор, КлючНазначенияИспользования", СтруктураОтбора, "Сотрудник");
	ОткрытьФорму(
		"Справочник.БанковскиеСчета.Форма.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьБанковскийСчет(Элемент)
	
	Если ЗначениеЗаполнено(ФизическоеЛицо.ОсновнойБанковскийСчет) И ПустаяСтрока(ОсновнойБанковскийСчет.Наименование) Тогда
		ВыбратьБанковскийСчетИзСписка();
	Иначе
		
		Оповещение = Новый ОписаниеОповещения("БанковскийСчетВыборИзМенюЗаврешение", ЭтотОбъект);
		
		СписокМеню = Новый СписокЗначений;
		СписокМеню.Добавить(1, НСтр("ru = 'Редактировать текущий счет'"));
		СписокМеню.Добавить(2, НСтр("ru = 'Выбрать другой счет из списка'"));
		
		ПоказатьВыборИзМеню(Оповещение, СписокМеню, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетРедактированиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ОсновнойБанковскийСчет, Результат);
		ФизическоеЛицо.ОсновнойБанковскийСчет = ОсновнойБанковскийСчетСсылка;
		ОбновитьПредставлениеВыплатыЗарплаты();
		Модифицированность = Истина;
	КонецЕсли;
	ЗаблокироватьБанковскийСчет(ОсновнойБанковскийСчетСсылка, ОсновнойБанковскийСчет.ВерсияДанных, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	БанковскийСчетВыборЗавершениеНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура БанковскийСчетВыборЗавершениеНаСервере(Результат)
	
	Если Результат <> Неопределено Тогда
		ОсновнойБанковскийСчетСсылка = Результат;
		ФизическоеЛицо.ОсновнойБанковскийСчет = ОсновнойБанковскийСчетСсылка;
		ОсновнойБанковскийСчетОбъект = ОсновнойБанковскийСчетСсылка.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ОсновнойБанковскийСчетОбъект, "ОсновнойБанковскийСчет");
		ОбновитьПредставлениеВыплатыЗарплаты();
		Модифицированность = Истина;
	КонецЕсли;
	ЗаблокироватьБанковскийСчет(ОсновнойБанковскийСчетСсылка, ОсновнойБанковскийСчет.ВерсияДанных, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетВыборИзМенюЗаврешение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Значение = 1 Тогда
			РедактироватьБанковскийСчет();
		ИначеЕсли Результат.Значение = 2 Тогда
			ВыбратьБанковскийСчетИзСписка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаблокироватьБанковскийСчет(Ссылка, ВерсияДанных, УникальныйИдентификатор)
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Ссылка, ВерсияДанных, УникальныйИдентификатор);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаСервереБезКонтекста
Функция РазблокироватьБанковскийСчет(Ссылка, УникальныйИдентификатор)
	
	Попытка
		РазблокироватьДанныеДляРедактирования(Ссылка, УникальныйИдентификатор);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьОрганизациюИПодразделениеСотрудника()
	
	Если НЕ ИспользоватьКадровыйУчет И НЕ ИспользоватьНесколькоОрганизаций Тогда
		Если НЕ ЗначениеЗаполнено(Сотрудник.ГоловнаяОрганизация)
			ИЛИ НЕ ЗначениеЗаполнено(ТекущаяОрганизация)
			ИЛИ НЕ ЗначениеЗаполнено(ТекущееПодразделение) Тогда
			ЗаполнитьОрганизациюПодразделение();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОрганизациюПодразделение()
	
	ЗначенияПоУмолчанию = Новый Структура("Организация, Подразделение");
	ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияПоУмолчанию);
	
	Если НЕ ЗначениеЗаполнено(Сотрудник.ГоловнаяОрганизация)
		ИЛИ НЕ ЗначениеЗаполнено(ТекущаяОрганизация) Тогда
		ТекущаяОрганизация = ЗначенияПоУмолчанию.Организация;
		Сотрудник.ГоловнаяОрганизация = ТекущаяОрганизация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущееПодразделение) Тогда
		ТекущееПодразделение = ЗначенияПоУмолчанию.Подразделение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВидЗанятостиСотрудника()
	
	Если НЕ ИспользоватьКадровыйУчет И НЕ ИспользоватьНесколькоОрганизаций Тогда
		Если НЕ ЗначениеЗаполнено(ТекущийВидЗанятости) Тогда
			ТекущийВидЗанятости = ПредопределенноеЗначение("Перечисление.ВидыЗанятости.ОсновноеМестоРаботы");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВводКоличестваСтавокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность            = Истина;
		КоличествоСтавокПредыдущее    = КоличествоСтавок;
		КоличествоСтавок              = Результат;
		КоличествоСтавокПредставление = Окр(КоличествоСтавок, 5);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьДолжностиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ТекущаяДолжность = Результат;
		Элементы.ТекущаяДолжность.ОбновитьТекстРедактирования();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаблокироватьОбъектВФормеВладельце() Экспорт
	
	ОбъектЗаблокирован = Истина;
	
	Если Не СотрудникиКлиент.ЗаблокироватьСотрудникаПриРедактировании(ЭтотОбъект) Тогда
		
		СотрудникиКлиент.УстановитьРежимТолькоПросмотраДополнительнойФормы(ЭтотОбъект);
		ЭтотОбъект.Модифицированность = Ложь;
		
		ОбъектЗаблокирован = Ложь;
		
	КонецЕсли;

	
	Если ОбъектЗаблокирован
		И Не СотрудникиКлиент.ЗаблокироватьФизическоеЛицоПриРедактировании(ЭтотОбъект, , Истина) Тогда
		
		СотрудникиКлиент.УстановитьРежимТолькоПросмотраДополнительнойФормы(ЭтотОбъект);
		ЭтотОбъект.Модифицированность = Ложь;
		
		ОбъектЗаблокирован = Ложь;
		
	КонецЕсли;
	
	Возврат ОбъектЗаблокирован;
	
КонецФункции

&НаКлиенте
Процедура ВопросПередРегистрациейСледуетЗаписатьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Записать();
	УправлениеФормой(ЭтотОбъект);
	
	Если НЕ Модифицированность Тогда
		СоздатьЗаписьЭлектроннойТрудовойКнижки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеКадровыхДанных(Отказ)
	
	Если НЕ ЗначениеЗаполнено(ТекущаяОрганизация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Организация"" не заполнено'"),
			,
			"ТекущаяОрганизация",
			,
			Отказ);
		ЗаполнятьПодразделения = Ложь;
	Иначе
		ЗаполнятьПодразделения = Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяОрганизация, "НеЗаполнятьПодразделенияВМероприятияхТрудовойДеятельности");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФизическоеЛицо.ДатаРождения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Дата рождения"" не заполнено'"),
			,
			"ФизическоеЛицо.ДатаРождения",
			,
			Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФизическоеЛицо.СтраховойНомерПФР) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""СНИЛС"" не заполнено'"),
			,
			"ФизическоеЛицо.СтраховойНомерПФР",
			,
			Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаПриема) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Дата приема"" не заполнено'"),
			,
			"ДатаПриема",
			,
			Отказ);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ТекущееПодразделение)
		 И ЗаполнятьПодразделения Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Подразделение"" не заполнено'"),
			,
			"ТекущееПодразделение",
			,
			Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущаяДолжность) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Должность"" не заполнено'"),
			,
			"ТекущаяДолжность",
			,
			Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущийВидЗанятости) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Поле ""Вид занятости"" не заполнено'"),
			,
			"ТекущийВидЗанятости",
			,
			Отказ);
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура СоздатьЗаписьЭлектроннойТрудовойКнижки()
	
	Отказ = Ложь;
	ПроверитьЗаполнениеКадровыхДанных(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЗаписьЭлектроннойТрудовойКнижкиЗавершение", ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура("ФизическоеЛицоСсылка, ТекущаяОрганизация, ТекущееПодразделение,
		|ТекущаяДолжность, ТекущийВидЗанятости, ДатаПриема, ДатаУвольнения, ИспользоватьНесколькоОрганизаций");
	
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ЭтотОбъект);
	ПараметрыОткрытия.Вставить("Прием",      НЕ ЗначениеЗаполнено(ДатаУвольнения)
		И НЕ ТребуетсяЗарегистрироватьКадровыйПеревод);
	ПараметрыОткрытия.Вставить("Увольнение", ЗначениеЗаполнено(ДатаУвольнения));
	ПараметрыОткрытия.Вставить("Перевод",    ЗначениеЗаполнено(ДатаПриема)
		И НЕ ЗначениеЗаполнено(ДатаУвольнения)
		И ТребуетсяЗарегистрироватьКадровыйПеревод);
	
	ПараметрыОткрытия.Вставить("АдресДополнительныхПараметров", ПодготовитьДополнительныеПараметрыОткрытия());

	ОткрытьФорму("Справочник.Сотрудники.Форма.РегистрацияТрудовойДеятельности",
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаписьЭлектроннойТрудовойКнижкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Сотрудник",   ФизическоеЛицоСсылка);
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", Новый Структура("Организация", ТекущаяОрганизация));
	
	ОткрытьФорму("Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗаписьЭлектроннойТрудовойКнижкиПоказатьОповещение(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПоказатьЗначение(, ВыбранныйЭлемент.Значение)
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтатусЗаписиЭлектроннойТрудовойКнижки()
	
	СтатусЗаписиЭлектроннойТрудовойКнижки();
	УчетЗарплаты.УправлениеДоступностьюКадровыхРеквизитов(ЭтотОбъект, Не МероприятиеПереданоПФР, Истина);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СтатусЗаписиЭлектроннойТрудовойКнижки()
	
	УчетЗарплаты.СтатусЗаписиЭлектроннойТрудовойКнижкиСотрудника(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормойКадровогоМероприятия(Форма)
	
	Если НЕ Форма.УчетЗарплатыИКадровСредствамиБухгалтерии
		И НЕ Форма.ИспользоватьКадровыйУчет Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	ЕстьЗаписиТрудовойКнижки = Форма.ЗаписиТрудовойКнижки.Количество() > 0;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаписьЭлектроннойТрудовойКнижкиСсылка",
		"Видимость",
		ЕстьЗаписиТрудовойКнижки);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаписьЭлектроннойТрудовойКнижкиКнопка",
		"Видимость",
		НЕ ЕстьЗаписиТрудовойКнижки);
	
КонецПроцедуры

&НаСервере
Процедура ДопуститьИзменениеДокумента()
	
	УчетЗарплаты.УправлениеДоступностьюКадровыхРеквизитов(ЭтотОбъект, Истина, Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФункциональныеОпцииФормы(Форма)
	
	ПараметрыОпций = Новый Структура;
	ПараметрыОпций.Вставить("Организация", Форма.ТекущаяОрганизация);
	ПараметрыОпций.Вставить("Период", Форма.ДатаПриема);
	Форма.УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыОпций);
	
КонецПроцедуры

&НаСервере
Функция ДанныеВыбораДолжности(Параметры, ОкончаниеВводаТекста = Ложь)
	
	Возврат УчетЗарплаты.ДанныеВыбораДолжности(Параметры, ОкончаниеВводаТекста);
	
КонецФункции

&НаКлиенте
Процедура ПослеВыбораДолжностиИзКлассификатора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ТекущаяДолжность = УчетЗарплатыВызовСервера.ПодобратьДолжностьПоПараметрам(Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДополнительнымиФормами

&НаКлиенте
Процедура ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, Форма, ОписаниеОповещения = Неопределено, КлючНазначенияИспользования = "", ОписаниеДанных= "") Экспорт
	
	ПараметрыФормы = СотрудникиКлиент.ПараметрыДополнительнойФормы(ОписаниеДополнительнойФормы, Форма, Истина);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючНазначенияИспользования);
	ПараметрыФормы.Вставить("ОписаниеДанных",              ОписаниеДанных);
	ОткрытьФорму(
		ОписаниеДополнительнойФормы.ИмяФормы,
		ПараметрыФормы,
		Форма,
		Истина,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПризнакРедактированияДанныхВДополнительнойФорме(ИмяДополнительнойФормы, Форма) Экспорт
	
	ПрочитанныеДанные = Новый Соответствие;
	
	Если ТипЗнч(Форма.ПрочитанныеДанныеФорм) = Тип("ФиксированноеСоответствие") Тогда
		
		Для каждого ЭлементСтруктуры Из Форма.ПрочитанныеДанныеФорм Цикл
			ПрочитанныеДанные.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	ПрочитанныеДанные.Вставить(ИмяДополнительнойФормы, Истина);
	
	Форма.ПрочитанныеДанныеФорм = Новый ФиксированноеСоответствие(ПрочитанныеДанные);
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеПриемаНаРаботуЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатРедактирования)
		И ТипЗнч(РезультатРедактирования) = Тип("Структура") Тогда
		
		КоличествоСтавокПредыдущее = КоличествоСтавок;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатРедактирования);
		ЗаполнитьЗначенияСвойств(Сотрудник, РезультатРедактирования);
		Если НЕ ЗначениеЗаполнено(Сотрудник.ГоловнаяОрганизация) Тогда
			Сотрудник.ГоловнаяОрганизация = ТекущаяОрганизация;
		КонецЕсли;
		КоличествоСтавокПредставление = Окр(КоличествоСтавок, 5);
		СотрудникиКлиентСервер.УстановитьИнфоНадпись(ЭтотОбъект, ОбщегоНазначенияКлиент.ДатаСеанса());
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПредупреждениеНерезидентАУСН);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаОсновныеДанные);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ИнформационныеСсылки);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаОформлениеСотрудника);
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти