#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьРеквизитыПоТекстуЗаполнения(Параметры.ТекстЗаполнения);
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьВидимостьСчетовУчета();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если (ЗначениеЗаполнено(Объект.ВидНалога) Или Объект.Предопределенный)
			И Не ВидНалогаТребуетУточненияПоКодуВидаДоходов Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("КодБК");
		
	ИначеЕсли Не ПустаяСтрока(КодБК) Тогда
		
		Если СтрДлина(КодБК) <> 20 Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", НСтр("ru = 'КБК'"),,,
				НСтр("ru = 'КБК должен состоять из 20 знаков'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "КодБК",, Отказ);
		КонецЕсли;
		
		Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КодБК) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", НСтр("ru = 'КБК'"),,,
				НСтр("ru = 'КБК должен содержать только цифры'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "КодБК",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ПустаяСтрока(НазначениеПлатежаИзМакета) Тогда
		Объект.НазначениеПлатежаИзменено = НазначениеПлатежаИзМакета <> Объект.НазначениеПлатежа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	РазблокироватьДанныеФормыДляРедактирования();
	
	ПараметрОповещения = Новый Структура("Ссылка", Объект.Ссылка);
	Оповестить("ИзмененВидНалогаИПлатежаВБюджет", ПараметрОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВидНалога) Тогда
		Объект.ВидНалога = Неопределено;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("НаименованиеНачалоВыбораЗавершение", ЭтотОбъект);
	
	СписокВыбора = Новый СписокЗначений;
	Для Каждого КлючИЗначение Из ПредопределенныеВидыНалогов Цикл
		Если КлючИЗначение.Значение <> ПредопределенноеЗначение("Перечисление.ВидыНалогов.ПрочиеНалогиИСборы") Тогда
			СписокВыбора.Добавить(КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СписокВыбора.НайтиПоЗначению(Объект.ВидНалога);
	
	ПоказатьВыборИзСписка(ОписаниеОповещенияОЗакрытии, СписокВыбора, Элемент, СписокВыбора.НайтиПоЗначению(Объект.ВидНалога));
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбораЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		Объект.ВидНалога = ВыбранноеЗначение.Значение;
		Объект.СчетУчета = "";
		ЗаполнитьРеквизитыПоВидуНалога(Истина);
		Если Не ЗначениеЗаполнено(Объект.ВидНалога) 
			Или ТипЗнч(ОписаниеПоставляемыхДанных) = Тип("СписокЗначений") Тогда
			// В классификаторе может и не быть всех видов налогов или есть несколько значений
			Объект.Наименование = Строка(ВыбранноеЗначение.Значение);
		КонецЕсли;
		Если ТипЗнч(ОписаниеПоставляемыхДанных) = Тип("СписокЗначений") Тогда
			КодБК = "";
		КонецЕсли;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодБКПриИзменении(Элемент)
	
	КодБК = ОбщегоНазначенияБПКлиентСервер.ОставитьВСтрокеТолькоЦифры(КодБК);
	
	Объект.КодБК = КодБК;
	
	ЗаполнитьРеквизитыПоКБК(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПлатежаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВидНалога) Тогда
		Объект.НазначениеПлатежаИзменено = Истина;
		Элементы.НазначениеПлатежа.ПредупреждениеПриРедактировании = "";
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.КодБК) Тогда
		ЗаполнитьРеквизитыПоСчету(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ЗначениеЗаполнено(Объект.ВидНалога) Тогда
		Объект.ВидНалога = Неопределено;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КонструкторКБК(Команда)
	
	Если ЗначениеЗаполнено(Объект.ВидНалога) И Не ПустаяСтрока(ПредупреждениеПриРедактировании) Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВопросКонструкторКБКЗавершение", ЭтотОбъект);
		
		ТекстВопроса = ПредупреждениеПриРедактировании + "." + Символы.ПС +
			НСтр("ru = 'Продолжить редактирование?'");
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе
		
		ОткрытьФорму("Справочник.ВидыНалоговИПлатежейВБюджет.Форма.КонструкторКБК", Новый Структура("КБК", Объект.КодБК), Элементы.КодБК);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросКонструкторКБКЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.ВидНалога = Неопределено;
		ОписаниеПоставляемыхДанных = Неопределено;
		УправлениеФормой(ЭтотОбъект);
		
		ОткрытьФорму("Справочник.ВидыНалоговИПлатежейВБюджет.Форма.КонструкторКБК", Новый Структура("КБК", Объект.КодБК), Элементы.КодБК);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзКлассификатора(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ВопросОбновитьИзКлассификатораЗавершение", ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru = 'Реквизиты элемента будут заменены значениями из классификатора.
		|Все ручные изменения будут потеряны. Продолжить?'");
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОбновитьИзКлассификатораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ЗаблокироватьДанныеФормыДляРедактирования();
		
		ЗаполнитьРеквизитыПоВидуНалога(Ложь);
		
		Если ТипЗнч(ОписаниеПоставляемыхДанных) <> Тип("СписокЗначений") Тогда
			Записать();
			ОповеститьОбИзменении(Объект.Ссылка);
			УправлениеФормой(ЭтотОбъект);
		Иначе
			КодБК = "";
			Объект.КодБК = КодБК;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	КодБК = Объект.КодБК;
	
	ВидНалогаТребуетУточненияПоКодуВидаДоходов =
		Справочники.ВидыНалоговИПлатежейВБюджет.ВидНалогаТребуетУточненияПоКодуВидаДоходов(Объект.ВидНалога);
	
	Элементы.ФормаОбновитьИзКлассификатора.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Обновить из классификатора %1'"),
		Формат(Справочники.ВидыНалоговИПлатежейВБюджет.ДатаАктуальностиКлассификатора(), "ДФ=yyyy"));
	
	ПредопределенныеВидыНалогов = РасчетыСБюджетом.ВидыНалогов();
	
	Элементы.КодБК.АвтоОтметкаНезаполненного = Объект.ВидНалога <> Перечисления.ВидыНалогов.ПрочиеНалогиИСборы
		И Объект.ВидНалога <> Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж;
	
	Если ЗначениеЗаполнено(Объект.ВидНалога) И (Не ВидНалогаТребуетУточненияПоКодуВидаДоходов
		Или (ВидНалогаТребуетУточненияПоКодуВидаДоходов И ПустаяСтрока(КодБК))) Тогда
		
		ЭтоНовыйЭлемент = Объект.Ссылка.Пустая();
		
		Элементы.КонструкторКБК.Видимость  = ЭтоНовыйЭлемент;
		Элементы.Наименование.КнопкаВыбора = ЭтоНовыйЭлемент;
		Если НЕ ЭтоНовыйЭлемент Тогда
			Элементы.ГруппаНаименованиеИКод.ТолькоПросмотр = Истина;
			Элементы.ГруппаКБКиКонструктор.ТолькоПросмотр  = Истина;
			Элементы.ГруппаСчетУчета.ТолькоПросмотр        = Истина;
		КонецЕсли;
		
		Если Объект.ВидНалога = Перечисления.ВидыНалогов.ПрочиеНалогиИСборы Тогда
			Элементы.НазначениеПлатежа.ТолькоПросмотр = Истина;
		КонецЕсли;
	Иначе
		Элементы.ГруппаКодБК.ЦветФона     = ЦветаСтиля.ФонУправляющегоПоля;
		Элементы.КонструкторКБК.Видимость = Истина;
	КонецЕсли;
	
	СтруктураДанных = НайтиПоставляемыеДанные(Объект.ВидНалога, Объект.КодБК, Неопределено, Объект.НазначениеПлатежаИзменено, Ложь);
	Если СтруктураДанных <> Неопределено Тогда
		НаименованиеКБК = СтруктураДанных.НаименованиеПолное;
		НазначениеПлатежаИзМакета = СтруктураДанных.НазначениеПлатежа;
	КонецЕсли;
	
	ПредупреждениеПриРедактировании = НСтр("ru = 'Автоматическое обновление реквизитов будет отключено'");
	
	Элементы.КодБК.ПредупреждениеПриРедактировании        = ПредупреждениеПриРедактировании;
	Элементы.Наименование.ПредупреждениеПриРедактировании = ПредупреждениеПриРедактировании;
	Элементы.СчетУчета.ПредупреждениеПриРедактировании    = ПредупреждениеПриРедактировании;
	
	Если НЕ Объект.НазначениеПлатежаИзменено Тогда
		Элементы.НазначениеПлатежа.ПредупреждениеПриРедактировании =
			НСтр("ru = 'Автоматическое обновление реквизита ""Назначение платежа"" будет отключено'");
	КонецЕсли;
	
	УстановитьТекстДополнительногоПараметра(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЭтоПоставляемыйЭлемент = ЗначениеЗаполнено(Объект.ВидНалога);
	ТребуетсяДозаполнение = Форма.ВидНалогаТребуетУточненияПоКодуВидаДоходов И ПустаяСтрока(Форма.КодБК);
	
	Элементы.ФормаОбновитьИзКлассификатора.Доступность = ЭтоПоставляемыйЭлемент И Не ТребуетсяДозаполнение;
	
	ВариантОтображениеПредупрежденияКодаБК = ?(ЭтоПоставляемыйЭлемент И Не ТребуетсяДозаполнение,
		ОтображениеПредупрежденияПриРедактировании.Отображать,
		ОтображениеПредупрежденияПриРедактировании.НеОтображать);
	
	ВариантОтображениеПредупреждения = ?(ЭтоПоставляемыйЭлемент,
		ОтображениеПредупрежденияПриРедактировании.Отображать,
		ОтображениеПредупрежденияПриРедактировании.НеОтображать);
	
	Элементы.КодБК.ОтображениеПредупрежденияПриРедактировании              = ВариантОтображениеПредупрежденияКодаБК;
	Элементы.Наименование.ОтображениеПредупрежденияПриРедактировании       = ВариантОтображениеПредупреждения;
	
	Элементы.СчетУчета.ОтображениеПредупрежденияПриРедактировании          = ВариантОтображениеПредупреждения;
	
	Если Объект.НазначениеПлатежаИзменено Тогда
		Элементы.НазначениеПлатежа.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	Иначе
		Элементы.НазначениеПлатежа.ОтображениеПредупрежденияПриРедактировании = ВариантОтображениеПредупреждения;
	КонецЕсли;
	
	Элементы.ГруппаДопПараметр.Видимость = НЕ ПустаяСтрока(Форма.ДополнительныйПараметр);
	
	УправлениеСпискомВыбораКБК(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстДополнительногоПараметра(Знач КБК, Знач ВидНалога)
	
	ДополнительныйПараметр = Новый Структура("Наименование, Подсказка", "", "");
	
	ПараметрНеЗадан = Истина;
	
	Если ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяПенсионнымФондом(КБК)
		Или ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяСФР(КБК) Тогда
		Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоФиксированныеВзносы(ВидНалога) Тогда
			// Данный параметр поддерживаем только для "поставляемых" налогов
			ДополнительныйПараметр.Наименование = НСтр("ru = '<СФР> или <ПФР_ИП> или <ФСС_ИП>'");
			ДополнительныйПараметр.Подсказка = НСтр("ru = '- текст, вместо которого в назначение платежа будет подставлен регистрационный номер ""за себя""'");
			ПараметрНеЗадан = Ложь;
		Иначе
			ДополнительныйПараметр.Наименование = НСтр("ru = '<СФР> или <ПФР> или <ФСС>'");
			ДополнительныйПараметр.Подсказка = НСтр("ru = '- текст, вместо которого в назначение платежа будет подставлен регистрационный номер'");
			ПараметрНеЗадан = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрНеЗадан И ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяФСС(КБК)
		Или ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяСФР(КБК) Тогда
		Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоФиксированныеВзносы(ВидНалога) Тогда
			// Данный параметр поддерживаем только для "поставляемых" налогов
			ДополнительныйПараметр.Наименование = НСтр("ru = '<СФР> или <ФСС_ИП> или <ПФР_ИП>'");
			ДополнительныйПараметр.Подсказка = НСтр("ru = '- текст, вместо которого в назначение платежа будет подставлен регистрационный номер ""за себя""'");
			ПараметрНеЗадан = Ложь;
		Иначе
			ДополнительныйПараметр.Наименование = НСтр("ru = '<СФР> или <ФСС> или <ПФР>'");
			ДополнительныйПараметр.Подсказка = НСтр("ru = '- текст, вместо которого в назначение платежа будет подставлен регистрационный номер'");
			ПараметрНеЗадан = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоВзносыНаПенсионноеСтрахование(ВидНалога)
		Или ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоВзносыНаСоциальноеСтрахование(ВидНалога) Тогда
		ПараметрНеЗадан = Ложь; // иначе для взносов, которые администрируются ФНС появится подсказка с параметром <Период>
	КонецЕсли;
	
	Если ПараметрНеЗадан И ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(КБК)
		И Не ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоЕдиныйНалоговыйПлатеж(ВидНалога) Тогда
		ДополнительныйПараметр.Наименование = НСтр("ru = '<Период>'");
		ДополнительныйПараметр.Подсказка = НСтр("ru = '- текст, вместо которого в назначение платежа будет подставлен налоговый период платежа'");
	КонецЕсли;
	
	Возврат ДополнительныйПараметр;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив();
	ЭлементыСчетов.Добавить("СчетУчета");
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоТекстуЗаполнения(Знач ТекстЗаполнения)
	
	Если СтрДлина(ТекстЗаполнения) > 13 Тогда
		
		ТекстЗаполнения = СтрЗаменить(ТекстЗаполнения, " ", "");
		
		Если СтрДлина(ТекстЗаполнения) > 13 И СтрДлина(ТекстЗаполнения) <= 20
			И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекстЗаполнения) Тогда
			
			// Строка поиска похожа на КБК
			
			Объект.Наименование = "";
			Объект.КодБК        = ТекстЗаполнения;
			
			ЗаполнитьРеквизитыПоКБК(ЭтотОбъект);
			
			ТекстЗаполнения = Неопределено;
			
		КонецЕсли;
		
	ИначеЕсли СтрДлина(ТекстЗаполнения) > 2 И СтрДлина(ТекстЗаполнения) <= 8 Тогда
		
		ТекстЗаполнения = СтрЗаменить(ТекстЗаполнения, " ", "");
		
		Если СтрДлина(ТекстЗаполнения) > 2 И СтрДлина(ТекстЗаполнения) <= 8
			И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(ТекстЗаполнения, ".", "")) Тогда
			
			// Строка поиска похожа на СчетУчета
			
			СчетУчета = СчетУчетаПоТекстуЗаполнения(ТекстЗаполнения);
			Если ЗначениеЗаполнено(СчетУчета) Тогда
				
				Объект.Наименование = "";
				Объект.СчетУчета    = СчетУчетаПоТекстуЗаполнения(ТекстЗаполнения);
				
				ЗаполнитьРеквизитыПоСчету(ЭтотОбъект);
				
				ТекстЗаполнения = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоВидуНалога(ПолучатьВсеЗначение)
	
	ВидНалогаТребуетУточненияПоКодуВидаДоходов =
		Справочники.ВидыНалоговИПлатежейВБюджет.ВидНалогаТребуетУточненияПоКодуВидаДоходов(Объект.ВидНалога);
	
	ЗначениеКБК = Неопределено;
	Если ВидНалогаТребуетУточненияПоКодуВидаДоходов И Не ПолучатьВсеЗначение Тогда
		ЗначениеКБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(Объект.КодБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(Объект.КодБК);
	КонецЕсли;
	
	ОписаниеПоставляемыхДанных = НайтиПоставляемыеДанные(Объект.ВидНалога, ЗначениеКБК, Неопределено, , ПолучатьВсеЗначение);
	
	Если ОписаниеПоставляемыхДанных = Неопределено Тогда
		Объект.ВидНалога = Неопределено;
	ИначеЕсли ТипЗнч(ОписаниеПоставляемыхДанных) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Объект, ОписаниеПоставляемыхДанных);
		КодБК           = ОписаниеПоставляемыхДанных.КодБК;
		НаименованиеКБК = ОписаниеПоставляемыхДанных.НаименованиеПолное;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеСпискомВыбораКБК(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.КодБК.СписокВыбора.Очистить();
	КнопкаВыпадающегоСписка = Ложь;
	ПодсказкаВвода          = "";
	Если ТипЗнч(Форма.ОписаниеПоставляемыхДанных) = Тип("СписокЗначений") Тогда
		Для Каждого СтрокаСписка Из Форма.ОписаниеПоставляемыхДанных Цикл
			Элементы.КодБК.СписокВыбора.Добавить(СтрокаСписка.Значение.КодБК, СтрокаСписка.Представление);
		КонецЦикла;
		КнопкаВыпадающегоСписка = Истина;
		ПодсказкаВвода = НСтр("ru = 'Выберите значение'");
	КонецЕсли;
	Элементы.КодБК.КнопкаВыпадающегоСписка = КнопкаВыпадающегоСписка;
	Элементы.КодБК.ПодсказкаВвода = ПодсказкаВвода;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыПоКБК(Форма)
	
	Объект = Форма.Объект;
	
	СтруктураДанных = НайтиПоставляемыеДанные(Неопределено, Объект.КодБК, Неопределено);
	
	Если СтруктураДанных = Неопределено Тогда
		
		Объект.ВидНалога = Неопределено;
		Форма.НаименованиеКБК = "";
		
	Иначе
		
		НаименованиеПодвидаДохода = "";
		Если ПлатежиВБюджетКлиентСервер.ЭтоКБКШтраф(Объект.КодБК) Тогда
			НаименованиеПодвидаДохода = НСтр("ru = '(штраф)'");
		ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКПени(Объект.КодБК) Тогда
			НаименованиеПодвидаДохода = НСтр("ru = '(пени)'");
		ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКПроценты(Объект.КодБК) Тогда
			НаименованиеПодвидаДохода = НСтр("ru = '(проценты)'");
		ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКПениПроценты(Объект.КодБК) Тогда
			НаименованиеПодвидаДохода = НСтр("ru = '(пени, проценты)'");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураДанных.ВидНалога) Тогда
			
			ЗаполнитьЗначенияСвойств(Объект, СтруктураДанных);
			
		Иначе
			
			Если НЕ ПустаяСтрока(СтруктураДанных.Наименование) Тогда
				Объект.Наименование = СокрЛП(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1 %2'"),
					СтруктураДанных.Наименование,
					НаименованиеПодвидаДохода));
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Объект.СчетУчета) Тогда
				Объект.СчетУчета = СтруктураДанных.СчетУчета;
			КонецЕсли;
			
			Если ПустаяСтрока(Объект.НазначениеПлатежа) Тогда
				Объект.НазначениеПлатежа = СтруктураДанных.НазначениеПлатежа;
			КонецЕсли;
			
		КонецЕсли;
		
		Форма.НаименованиеКБК = СокрЛП(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 %2'"),
			СтруктураДанных.НаименованиеПолное,
			НаименованиеПодвидаДохода));
		
	КонецЕсли;
	
	УстановитьТекстДополнительногоПараметра(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстДополнительногоПараметра(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ДополнительныйПараметр = ТекстДополнительногоПараметра(Объект.КодБК, Объект.ВидНалога);
	Форма.ДополнительныйПараметр = ДополнительныйПараметр.Наименование;
	Элементы.НадписьДополнительныйПараметр.РасширеннаяПодсказка.Заголовок = ДополнительныйПараметр.Подсказка;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыПоСчету(Форма)
	
	Объект = Форма.Объект;
	
	СтруктураДанных = НайтиПоставляемыеДанные(Неопределено, Неопределено, Объект.СчетУчета);
	
	Если СтруктураДанных = Неопределено Тогда
		Объект.ВидНалога = Неопределено;
	Иначе
		Если ЗначениеЗаполнено(СтруктураДанных.ВидНалога) Тогда
			ЗаполнитьЗначенияСвойств(Объект, СтруктураДанных);
		Иначе
			Если ПустаяСтрока(Объект.Наименование) Тогда
				Объект.Наименование = СтруктураДанных.Наименование;
			КонецЕсли;
			Если ПустаяСтрока(Объект.НазначениеПлатежа) Тогда
				Объект.НазначениеПлатежа = СтруктураДанных.НазначениеПлатежа;
			КонецЕсли;
		КонецЕсли;
		Форма.КодБК           = СтруктураДанных.КодБК;
		Форма.НаименованиеКБК = СтруктураДанных.НаименованиеПолное;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиПоставляемыеДанные(Знач ВидНалога = Неопределено, Знач КБК = Неопределено, Знач СчетУчета = Неопределено, НазначениеПлатежаИзменено = Ложь, ПолучатьВсеЗначение = Ложь)
	
	Если ВидНалога = Перечисления.ВидыНалогов.ПрочиеНалогиИСборы Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеПоставляемыхДанных = Справочники.ВидыНалоговИПлатежейВБюджет.НайтиПоставляемыеДанные(ВидНалога, КБК, СчетУчета,, НазначениеПлатежаИзменено, ПолучатьВсеЗначение);
	Если ОписаниеПоставляемыхДанных <> Неопределено Тогда
		Если ТипЗнч(ОписаниеПоставляемыхДанных) = Тип("Структура") Тогда
			УстановитьВидНалога(ОписаниеПоставляемыхДанных, ВидНалога);
		Иначе
			Для Каждого СтрокаСписка Из ОписаниеПоставляемыхДанных Цикл
				УстановитьВидНалога(СтрокаСписка.Значение, ВидНалога);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОписаниеПоставляемыхДанных;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьВидНалога(ОписаниеПоставляемыхДанных, ВидНалога)
	
	Если ВидНалога = Неопределено
		И ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеПоставляемыхДанных, "ВидНалога")) Тогда
		ОписаниеПоставляемыхДанных.ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СчетУчетаПоТекстуЗаполнения(ТекстЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШаблонКода", ТекстЗаполнения + "%");
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Хозрасчетный.Ссылка
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Код ПОДОБНО &ШаблонКода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Хозрасчетный.Ссылка
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.КодБыстрогоВыбора ПОДОБНО &ШаблонКода";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СчетУчета = Выборка.Ссылка;
	Иначе
		СчетУчета = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли;
	
	Возврат СчетУчета;
	
КонецФункции

#КонецОбласти
