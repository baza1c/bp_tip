
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоВозвратНалога = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.Отбор, "ВозвратУплатыНалога", Ложь);
	ПрименяетсяОсобыйПорядокУплатыНалога =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.Отбор, "ПрименяетсяОсобыйПорядокУплатыНалога", Неопределено);
	ТолькоДопустимые =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.Отбор, "ТолькоДопустимые", Ложь);
	Период = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.Отбор, "Период");
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя())
	КонецЕсли;
	
	Если ЭтоВозвратНалога Тогда
		Параметры.Отбор.Удалить("ВозвратУплатыНалога");
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ВидНалога",
			ЕдиныйНалоговыйСчетПовтИсп.ВидыВозвращаемыхНалогов(),
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если ПрименяетсяОсобыйПорядокУплатыНалога = Истина Тогда
		Параметры.Отбор.Удалить("ПрименяетсяОсобыйПорядокУплатыНалога");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ВидНалога",
			ЕдиныйНалоговыйСчетПовтИсп.МассивДоступныхВидовНалогов(Период),
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ИначеЕсли ПрименяетсяОсобыйПорядокУплатыНалога = Ложь Тогда
		Параметры.Отбор.Удалить("ПрименяетсяОсобыйПорядокУплатыНалога");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ВидНалога",
			ЕдиныйНалоговыйСчетПовтИсп.МассивНедоступныхВидовНалогов(ТолькоДопустимые),
			ВидСравненияКомпоновкиДанных.НеВСписке,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	УстановитьУсловноеОформление();
	УстановитьОтборПоСчетамУчета(Параметры.ОграничениеПоСчетамУчета);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	Элементы.ФормаВыбратьИзПолногоСписка.Видимость = ПлатежиВБюджетКлиентСервер.ДействуетПереходныйПериодЕНП(Период);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьИзПолногоСписка(Команда)
	
	ВыбратьИзПолногоСпискаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// СчетУчета
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетУчета");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ПользовательУправляетСчетамиУчета", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьИзПолногоСпискаНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВыбратьИзПолногоСписка",
		"Видимость",
		Ложь);
		
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьЭлементыНастроек(КоллекцияЭлементов)
	
	Для Каждого ЭлементНастроек Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементНастроек) = Тип("ЭлементОтбораКомпоновкиДанных")
			Или ТипЗнч(ЭлементНастроек) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ЭлементНастроек.Использование = Ложь;
		ИначеЕсли ТипЗнч(ЭлементНастроек) = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
			ОтключитьЭлементыНастроек(ЭлементНастроек.Элементы)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСчетамУчета(СчетаУчета)
	
	Если Не ЗначениеЗаполнено(СчетаУчета) Тогда
		Возврат;
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(Список, "СчетУчета", СчетаУчета,
		ВидСравненияКомпоновкиДанных.ВСписке);
	
КонецПроцедуры

#КонецОбласти