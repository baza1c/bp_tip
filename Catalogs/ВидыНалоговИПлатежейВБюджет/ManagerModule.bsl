#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает дату вступления в силу актуального классификатора кодов бюджетной классификации
//
Функция ДатаАктуальностиКлассификатора() Экспорт
	
	Возврат '20260101';
	
КонецФункции

// Возвращает дату начала года второго макета классификатора кодов бюджетной классификации
//
Функция ДатаВторогоКлассификатора() Экспорт
	
	// Обязательно должна быть больше или меньше ДатаАктуальностиКлассификатора()
	
	Возврат '20250101';
	
КонецФункции

// Проверяет актуальность реквизитов при подготовке платежных документов
//
// Параметры:
//   Налог - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   Период - Дата - дата платежного документа
//
// Возвращаемое значение:
//  Булево
//
Функция РеквизитыАктуальны(Налог, Знач Период = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Налог) ИЛИ
		Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	Если НачалоДня(Период) < ДатаАктуальностиКлассификатора() Тогда
		// Платеж относится к периоду действия старого классификатора
		Возврат Истина;
	КонецЕсли;
	
	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, ДатаАктуальности");
	Если ЗначениеЗаполнено(ЗначениеРеквизитов.ВидНалога) И ЗначениеЗаполнено(ЗначениеРеквизитов.ДатаАктуальности) Тогда
		// Проверка актуальности выполняется только для поставляемых элементов.
		
		// Некоторые пенсионные страховые взносы 2022 года не требуются для текущих платежей и уведомлений,
		// но используются при вводе старых учетных документов. Актуальность их КБК не проверяем.
		СтраховыеВзносы2022 = ПлатежиВБюджетКлиентСерверПереопределяемый.ВзносыНаПенсионноеСтрахованиеДо2023();
		
		РеквизитыАктуальны = НачалоДня(ЗначениеРеквизитов.ДатаАктуальности) >= ДатаАктуальностиКлассификатора()
			Или СтраховыеВзносы2022.Найти(ЗначениеРеквизитов.ВидНалога) <> Неопределено;
	Иначе
		РеквизитыАктуальны = Истина;
	КонецЕсли;
	
	Возврат РеквизитыАктуальны;
	
КонецФункции

// Ищет налог по виду и создает поставляемый элемент при необходимости.
// Если налог не найден и не должен быть создан по классификатору,
// то возвращается предопределенный элемент ПрочиеНалогиИСборы.
//
// Параметры:
//   ВидНалога - ПеречислениеСсылка.ВидыНалогов - вид налога
//   СоздаватьНовый - Булево - Истина, если нужно создать налог, когда он не найден
//   Период - Дата - период действия налога для создания из классификатора
//   КодВидаДоходов - Строка - уточнение кода вида налога для случая когда несколько КБК
//
// Возвращаемое значение:
//   СправочникСсылка.ВидыНалоговИПлатежейВБюджет - найденный или созданный налог.
//
Функция НалогПоВиду(ВидНалога, СоздаватьНовый = Истина, Период = Неопределено, КодВидаДоходов = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ВидНалога) Тогда
		Возврат Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
	КонецЕсли;
	
	Ссылка = Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
	
	РезультатЗапроса = ЗапросПоВидуНалога(ВидНалога, КодВидаДоходов);
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Ссылка = Выборка.Ссылка;
	КонецЕсли;
	
	Если Ссылка.Пустая() И СоздаватьНовый Тогда
		ОтборВидНалога = Новый Структура("ВидНалога", ВидНалога);
		Если ЗначениеЗаполнено(Период) Тогда
			НачалоПериода = НачалоГода(Период);
		Иначе
			НачалоПериода = Период;
		КонецЕсли;
		
		ТаблицаПоставляемыхДанных = УчетДенежныхСредствПовтИсп.ТаблицаПоставляемыхДанныхПоВидуНалога(
			НачалоПериода, ВидНалога);
		
		МассивПоставляемыхДанных = ТаблицаПоставляемыхДанных.НайтиСтроки(ОтборВидНалога);
		Для Каждого СтрокаПоставляемыхДанных Из МассивПоставляемыхДанных Цикл
			СоздатьПоставляемыйЭлемент(СтрокаПоставляемыхДанных, СоздаватьНовый);
		КонецЦикла;
		
		Ссылка = НалогПоВиду(ВидНалога, Ложь, , КодВидаДоходов);
	КонецЕсли;
	
	Если Ссылка.Пустая() Тогда
		Ссылка = ПрочиеНалогиИСборы;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

// Возвращает налоги по списку видов.
//
// Параметры:
//   ВидыНалога - Массив из ПеречислениеСсылка.ВидыНалогов - виды налогов
//
// Возвращаемое значение:
//   Массив из СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//
Функция НалогиПоВидам(ВидыНалога) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ВидыНалога", ВидыНалога);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Налог
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	ВидыНалоговИПлатежейВБюджет.ВидНалога В(&ВидыНалога)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Налог";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Налог);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Ищет налог по КБК и создает поставляемый элемент при необходимости
//
Функция НалогПоКБК(Знач КодБК, СоздаватьНовый = Истина, СтатусПлательщика = Неопределено) Экспорт
	
	Если ПустаяСтрока(КодБК) Тогда
		Возврат ПрочиеНалогиИСборы;
	КонецЕсли;
	
	// Ищем по всем значениям КодПодвидаДоходов
	ГраницыАдминистратораКБК  = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодГлавногоАдминистратора");
	ГраницыПеременнойЧастиКБК = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	ШаблонКБК = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("_", ГраницыАдминистратораКБК.Длина)
		+ Сред(КодБК, ГраницыАдминистратораКБК.Начало + ГраницыАдминистратораКБК.Длина);
	ШаблонКБК = Лев(ШаблонКБК, ГраницыПеременнойЧастиКБК.Начало - 1)
		+ СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("_", ГраницыПеременнойЧастиКБК.Длина)
		+ Сред(ШаблонКБК, ГраницыПеременнойЧастиКБК.Начало + ГраницыПеременнойЧастиКБК.Длина);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ШаблонКБК", ШаблонКБК);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Ссылка,
	|	ВидыНалоговИПлатежейВБюджет.КодБК КАК КодБК,
	|	ВидыНалоговИПлатежейВБюджет.ВидНалога КАК ВидНалога
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
	|	И ВидыНалоговИПлатежейВБюджет.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы)
	|	И ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодБК,
	|	ВидыНалоговИПлатежейВБюджет.Предопределенный,
	|	ВидыНалоговИПлатежейВБюджет.СчетУчета.Код,
	|	Ссылка";
	
	Ссылка = Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
	
	// Выберем лучший
	МаксимальныйПриоритет = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Приоритет = 1; // За соответствие шаблону
		
		Если Выборка.КодБК = КодБК Тогда
			Приоритет = Приоритет + 2;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ВидНалога) Тогда
			// Предопределенный налог должен иметь больший приоритет.
			Приоритет = Приоритет + 4;
		КонецЕсли;
		
		Если Приоритет > МаксимальныйПриоритет Тогда
			МаксимальныйПриоритет = Приоритет;
			Ссылка = Выборка.Ссылка;
		ИначеЕсли Приоритет = МаксимальныйПриоритет Тогда
			Ссылка = НалогПоСтатусуПлательщика(Ссылка, Выборка.Ссылка, СтатусПлательщика);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Ссылка.Пустая() И СоздаватьНовый Тогда
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("КодБК", КодБК));
		Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
			Ссылка = СоздатьПоставляемыйЭлемент(ТаблицаПоставляемыхДанных[0], СоздаватьНовый);
		КонецЕсли;
	КонецЕсли;
	
	Если Ссылка.Пустая() Тогда
		Ссылка = ПрочиеНалогиИСборы;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

// Возвращает КБК налога
//
// Параметры:
//   Налог                      - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//   Период                     - Дата - период действия классификатора КБК
//
// Возвращаемое значение:
//   Строка
//
Функция КБК(Налог, Знач ВидНалоговогоОбязательства = Неопределено, Знач Период = Неопределено, ПериодПлатежа = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, КодБК, ДатаАктуальности");
	ВидНалога         = ЗначениеРеквизитов.ВидНалога;
	КодБК             = ЗначениеРеквизитов.КодБК;
	ДатаАктуальности  = ЗначениеРеквизитов.ДатаАктуальности;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда
		
		Если Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж Тогда
			
			// Если это ЕНП, то нет смысла проверять дату актуальности и при несовпадении искать его в макетах - он не меняется.
			КодБК = ЕдиныйНалоговыйСчет.КБКЕНП();
			
		ИначеЕсли НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности < ДатаАктуальностиКлассификатора()
			ИЛИ НачалоДня(Период) < ДатаАктуальностиКлассификатора() И ДатаАктуальности >= ДатаАктуальностиКлассификатора() Тогда
			
			Если НачалоДня(Период) < ДатаАктуальностиКлассификатора() И НачалоДня(Период) < ДатаВторогоКлассификатора()
				Или КонецГода(Период) > КонецГода(ДатаАктуальностиКлассификатора())
					И КонецГода(Период) >  КонецГода(ДатаВторогоКлассификатора()) Тогда
				// возвращаем текущий КБК, т.к. период выходит за границы имеющихся макетов
			Иначе
				СтруктураОтбора = Новый Структура();
				СтруктураОтбора.Вставить("ВидНалога", ВидНалога);
				Если ВидНалогаТребуетУточненияПоКодуВидаДоходов(ВидНалога) Тогда
					СтруктураОтбора.Вставить("КодБК", КодБК);
					ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, СтруктураОтбора);
				Иначе
					НачалоПериода = НачалоГода(Период);
					ТаблицаПоставляемыхДанных = УчетДенежныхСредствПовтИсп.ТаблицаПоставляемыхДанныхПоВидуНалога(
						НачалоПериода, ВидНалога);
				КонецЕсли;
				
				Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
					КодБК = ТаблицаПоставляемыхДанных[0].КодБК;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КодБК) = ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
		КодБК = КБКПоВидуНалоговогоОбязательства(КодБК, ВидНалога, ВидНалоговогоОбязательства, Период, ПериодПлатежа);
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКНалогиВзносы(КодБК) И ВидНалоговогоОбязательства <> Перечисления.ВидыПлатежейВГосБюджет.Налог Тогда
		ШаблонКБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КодБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(КодБК, Истина);
		КодБК = КБКПоВидуНалоговогоОбязательства(ШаблонКБК, ВидНалога, ВидНалоговогоОбязательства, Период, ПериодПлатежа);
	ИначеЕсли ЗначениеЗаполнено(ПериодПлатежа) И Год(ПериодПлатежа) < Год(Период) Тогда
		КодБК = КБКПоВидуНалоговогоОбязательства(КодБК, ВидНалога, ВидНалоговогоОбязательства, Период, ПериодПлатежа);
	КонецЕсли;
	
	Возврат КодБК;
	
КонецФункции

// Возвращает КБК с учетом вида налогового обязательства
//
// Параметры:
//   КБК                        - Строка
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//
// Возвращаемое значение:
//   Строка
//
Функция КБКПоВидуНалоговогоОбязательства(КБК, ВидНалога, Знач ВидНалоговогоОбязательства = Неопределено, Знач Период = Неопределено, Знач ПериодПлатежа = Неопределено) Экспорт
	
	Если ПустаяСтрока(КБК) Тогда
		Возврат КБК;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК) <> ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
			Возврат КБК; // Сохраняем заданный код подвида доходов
		Иначе
			ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	КодГлавногоАдминистратора    = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КБК);
	КодВидаДоходов               = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК);
	КодПодвидаДоходов            = КодПодвидаДоходов(КБК, ВидНалога, ВидНалоговогоОбязательства, Период, ПериодПлатежа);
	КодОперацииСектораУправления = ПлатежиВБюджетКлиентСервер.КодОперацииСектораУправления(КБК);
	
	Возврат КодГлавногоАдминистратора + КодВидаДоходов + КодПодвидаДоходов + КодОперацииСектораУправления;
	
КонецФункции

Функция КодПодвидаДоходов(КБК, ВидНалога, ВидНалоговогоОбязательства, Знач Период, Знач ПериодПлатежа = Неопределено)
	
	Если Не ЗначениеЗаполнено(ПериодПлатежа) Тогда
		ПериодПлатежа = Период;
	КонецЕсли;
	
	ЭтоНалог    = ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	ЭтоПени     = Перечисления.ВидыПлатежейВГосБюджет.ЭтоПени(ВидНалоговогоОбязательства);
	ЭтоПроценты = Перечисления.ВидыПлатежейВГосБюджет.ЭтоПроценты(ВидНалоговогоОбязательства);
	ЭтоШтраф    = Перечисления.ВидыПлатежейВГосБюджет.ЭтоШтраф(ВидНалоговогоОбязательства);
	
	ЭтоВзносыСвышеПредела = ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
	
	ЭтоТаможенныйПлатеж = ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяТаможеннымиОрганами(КБК);
	КодПодвидаДоходаПоУмолчанию = "0000";
	ТекущийКодПодвидаДохода = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
	ЭтоОсобыйКодПодвидаДохода = СтрНачинаетсяС(ТекущийКодПодвидаДохода, "0")
		И Не СтрЗаканчиваетсяНа(ТекущийКодПодвидаДохода, "00");
	
	СоответствиеСтраховыхДо2023 =
		ПлатежиВБюджетКлиентСерверПереопределяемый.СоответствиеКБКСтраховыхВзносов2022ГодаВ2023Году();
	КБКПриведенный = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК);
	ВидСтраховогоВзносаДо2023 = СоответствиеСтраховыхДо2023[КБКПриведенный];
	ЭтоСтарыйСтраховойВзносДо2023 = ЗначениеЗаполнено(ВидСтраховогоВзносаДо2023);
	
	Если ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС
		Или ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(Период) // дата платежа больше 2023 года
			И Не ЭтоСтарыйСтраховойВзносДо2023 Тогда
			Если ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "1001";
			ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "1002";
			КонецЕсли;
		ИначеЕсли ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(ПериодПлатежа) Тогда
			КодПодвидаДоходов = "1013";
			Если ЭтоПени ИЛИ ЭтоПроценты И ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "2013";
			ИначеЕсли ЭтоПроценты И ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "2213";
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3013";
			КонецЕсли;
		Иначе
			КодПодвидаДоходов = "1011";
			Если ЭтоПени Или ЭтоПроценты Тогда
				КодПодвидаДоходов = "2011";
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3011";
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_Добровольные Тогда
		
		КодПодвидаДоходов = "12";
		
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФСС Тогда
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(ПериодПлатежа) Тогда
			Если ПлатежиВБюджетКлиентСервер.ДействуетФиксированныеВзносыФСС2026(Период) И КодПодвидаДоходов <> "1020" Тогда
				КодПодвидаДоходов = "1010";
			Иначе
				КодПодвидаДоходов = "1";
			КонецЕсли;
		Иначе
			КодПодвидаДоходов = "6";
		КонецЕсли;
		
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносыЕдиныйТариф Тогда
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ОПС_ИностранныеРаботники
		Или ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ОМС_ИностранныеРаботники
		Или ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ОСС_ИностранныеРаботники Тогда
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф
		И ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(ПериодПлатежа) Тогда
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела Тогда
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ВредныеУсловия
		ИЛИ ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ТяжелыеУсловия Тогда
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(Период) Тогда
			КодПодвидаДоходаДоИзменения = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
			НеЗависитОтСпецОценки = СтрЗаканчиваетсяНа(КодПодвидаДоходаДоИзменения, "10");
			Если ЭтоНалог Тогда
				КодПодвидаДоходов = "102";
			ИначеЕсли ЭтоПени Тогда
				Если НеЗависитОтСпецОценки Тогда
					КодПодвидаДоходов = "211";
				Иначе
					КодПодвидаДоходов = "210";
				КонецЕсли;
			ИначеЕсли ЭтоПроценты Тогда
				Если НеЗависитОтСпецОценки Тогда
					КодПодвидаДоходов = "221";
				Иначе
					КодПодвидаДоходов = "220";
				КонецЕсли;
			ИначеЕсли ЭтоШтраф Тогда
				Если НеЗависитОтСпецОценки Тогда
					КодПодвидаДоходов = "301";
				Иначе
					КодПодвидаДоходов = "300";
				КонецЕсли
			Иначе
				КодПодвидаДоходов = "101";
			КонецЕсли;
		Иначе
			КодПодвидаДоходов = "101";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ЛетныеЭкипажи
		Или ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_Шахтеры Тогда
		Если ЭтоНалог Тогда
			КодПодвидаДоходов = "1";
		ИначеЕсли ЭтоПени Или ЭтоПроценты Тогда
			КодПодвидаДоходов = "21";
		ИначеЕсли ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.Госпошлина_ГосрегистрацияОрганизаций Тогда
		
		Если ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ГосрегистрацияОрганизацийЧерезМФЦ Тогда
			КодПодвидаДоходов = "8";
		ИначеЕсли ЭтоПени ИЛИ ЭтоПроценты Тогда
			КодПодвидаДоходов = "2";
			Если ПлатежиВБюджетКлиентСервер.ПениПроцентыРаздельно(КБК, Период) Тогда
				КодПодвидаДоходов = ?(ЭтоПени, "21", "22");
			КонецЕсли;
		ИначеЕсли ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
		
	ИначеЕсли ЭтоТаможенныйПлатеж И ЭтоАвансовыйПлатежТаможня(КБК) Тогда
		
		Если АвансовыйПлатежТаможня2022(Период)
			И ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаАвансовыйПлатежТаможня() Тогда
			КодПодвидаДоходов = "1000";
		Иначе
			КодПодвидаДоходов = КодПодвидаДоходаПоУмолчанию;
		КонецЕсли;
		
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоЕдиныйНалоговыйПлатеж(КБК) Тогда
		
		КодПодвидаДоходов = КодПодвидаДоходаПоУмолчанию;
		
	ИначеЕсли ЭтоПениШтрафыИсключения(КБК) И ТекущийКодПодвидаДохода = КодПодвидаДоходаПоУмолчанию Тогда
		
		КодПодвидаДоходов = ТекущийКодПодвидаДохода;
		
	ИначеЕсли ЭтоПроцентыПоПлатежу(КБК) Тогда
		
		КодПодвидаДоходов = "2";
		
	Иначе
		
		Если Не ЗначениеЗаполнено(ВидНалога) И КодПодвидаДоходаПоУмолчанию = ТекущийКодПодвидаДохода Тогда
			КодПодвидаДоходов = ТекущийКодПодвидаДохода;
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(ПериодПлатежа)
			И (ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_СтраховаяЧасть
			ИЛИ ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть) Тогда
			
			Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(Период) // дата платежа больше 2023 года
				И Не ЭтоСтарыйСтраховойВзносДо2023 Тогда
				Если ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_СтраховаяЧасть Тогда
					КодПодвидаДоходов = "1001";
				ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть Тогда
					КодПодвидаДоходов = "1005";
				КонецЕсли;
			Иначе
				Если ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть
					И (ЭтоНалог ИЛИ ЭтоВзносыСвышеПредела) Тогда
					КодПодвидаДоходов = "111";
				ИначеЕсли ЭтоНалог Тогда
					КодПодвидаДоходов = "101";
				ИначеЕсли ЭтоПени Тогда
					КодПодвидаДоходов = "211";
				ИначеЕсли ЭтоПроценты Тогда
					КодПодвидаДоходов = "221";
				ИначеЕсли ЭтоШтраф Тогда
					КодПодвидаДоходов = "301";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС Тогда
			Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(ПериодПлатежа) Тогда
				Если ТекущийКодПодвидаДохода = "1101" Тогда
					КодПодвидаДоходов = ТекущийКодПодвидаДохода;
				Иначе
					КодПодвидаДоходов = "1001";
				КонецЕсли;
			ИначеЕсли ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(ПериодПлатежа) Тогда
				Если ЭтоНалог Тогда
					КодПодвидаДоходов = "101";
				ИначеЕсли ЭтоПени Тогда
					КодПодвидаДоходов = "211";
				ИначеЕсли ЭтоПроценты Тогда
					КодПодвидаДоходов = "221";
				ИначеЕсли ЭтоШтраф Тогда
					КодПодвидаДоходов = "301";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ЭтоОсобыйКодПодвидаДохода Тогда
			
			КодПодвидаДоходов = ТекущийКодПодвидаДохода;
			
		Иначе
			
			Если ЭтоНалог И ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть Тогда
				КодПодвидаДоходов = "11";
			ИначеЕсли ЭтоВзносыСвышеПредела И ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть Тогда
				КодПодвидаДоходов = "12";
			ИначеЕсли ЭтоПени ИЛИ ЭтоПроценты Тогда
				КодПодвидаДоходов = "2";
				Если ПлатежиВБюджетКлиентСервер.ПениПроцентыРаздельно(КБК, Период) Тогда
					КодПодвидаДоходов = ?(ЭтоПени, "21", "22");
				КонецЕсли;
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоНДФЛНалоговогоАгента(ВидНалога) Тогда
		
		Если (ЭтоНалог Или ЭтоШтраф)
			И СтрНачинаетсяС(ТекущийКодПодвидаДохода, Лев(КодПодвидаДоходов, 1) + "0")
			И СтрЗаканчиваетсяНа(ТекущийКодПодвидаДохода, "10") Тогда
			КодПодвидаДоходов = ТекущийКодПодвидаДохода;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодПодвидаДоходов) Тогда
		КодПодвидаДоходов = "1"; // по умолчанию
	КонецЕсли;
	
	РасположениеЭлемента = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	Для Счетчик = СтрДлина(КодПодвидаДоходов) + 1 По РасположениеЭлемента.Длина Цикл
		КодПодвидаДоходов = КодПодвидаДоходов + "0";
	КонецЦикла;
	
	Возврат КодПодвидаДоходов;
	
КонецФункции

Функция АвансовыйПлатежТаможня2022(Период) Экспорт
	
	Возврат Период <> Неопределено
		И Период >= '20220101';
	
КонецФункции

// Возвращает вид налогового обязательства по КБК
//
// Параметры:
//   КБК - Строка
//   Период - Дата - период уплаты
//   ВидНалога - ПеречислениеСсылка.ВидыНалогов - вид налога, если известен
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//
Функция ВидНалоговогоОбязательстваПоКБК(КБК, Знач Период = Неопределено, ВидНалога = Неопределено) Экспорт
	
	Если ПустаяСтрока(КБК)
		Или ВидНалога = Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж Тогда
		Возврат Перечисления.ВидыПлатежейВГосБюджет.Налог;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	Если ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела Тогда
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
		Возврат ВидНалоговогоОбязательства;
	КонецЕсли;
	
	КодПодвидаДоходов = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
	
	Если Лев(КодПодвидаДоходов, 1) = "3" Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Штраф;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 2) = "22"
		И ПлатежиВБюджетКлиентСервер.ПениПроцентыРаздельно(КБК, Период) Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Проценты;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 1) = "2" Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ПениСам;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 1) = "1" Тогда
		
		Если ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть
			И ПлатежиВБюджетКлиентСервер.СтраховыеВзносыРаздельно(Период) Тогда
			
			Если Лев(КодПодвидаДоходов, 2) = "12" Тогда
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
			Иначе
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			КонецЕсли;
			
		ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ВредныеУсловия
			Или ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ТяжелыеУсловия Тогда
			
			Если СтрЗаканчиваетсяНа(КодПодвидаДоходов, "10") Тогда
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки;
			Иначе
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			КонецЕсли;
			
		Иначе
			ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		КонецЕсли;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 1) = "8"
		И ВидНалога = Перечисления.ВидыНалогов.Госпошлина_ГосрегистрацияОрганизаций Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ГосрегистрацияОрганизацийЧерезМФЦ;
		
	Иначе
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		
	КонецЕсли;
	
	Возврат ВидНалоговогоОбязательства;
	
КонецФункции

// Формирует назначение платежа для уплаты налогов
//
// Параметры:
//   Налог                      - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//   Организация                - СправочникСсылка.Организации
//
// Возвращаемое значение:
//  Строка - назначение платежа
//
Функция НазначениеПлатежа(Налог, ВидНалоговогоОбязательства = Неопределено, Организация = Неопределено, Знач Период = Неопределено,
	ПредставлениеНалоговогоПериода = "", ШаблонНазначения = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	НазначениеПлатежа = "";
	Если ЗначениеЗаполнено(Налог) И Налог <> ПрочиеНалогиИСборы Тогда
		ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, НазначениеПлатежа, ДатаАктуальности");
		ВидНалога         = ЗначениеРеквизитов.ВидНалога;
		НазначениеПлатежа = ЗначениеРеквизитов.НазначениеПлатежа;
		ДатаАктуальности  = ЗначениеРеквизитов.ДатаАктуальности;
	КонецЕсли;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда
		
		Если НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности < ДатаАктуальностиКлассификатора()
			ИЛИ НачалоДня(Период) < ДатаАктуальностиКлассификатора() И ДатаАктуальности >= ДатаАктуальностиКлассификатора() Тогда
			
			НачалоПериода = НачалоГода(Период);
			ТаблицаПоставляемыхДанных = УчетДенежныхСредствПовтИсп.ТаблицаПоставляемыхДанныхПоВидуНалога(
				НачалоПериода, ВидНалога);
			Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
				НазначениеПлатежа = ТаблицаПоставляемыхДанных[0].НазначениеПлатежа;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ШаблонНазначения) Тогда
		ШаблонНазначения = НазначениеПлатежа;
	Иначе
		Параметр = "";
		ПозицияПараметра = СтрНайти(НазначениеПлатежа, "<Период");
		Если ПозицияПараметра = 0 Тогда
			ПозицияПараметра = СтрНайти(НазначениеПлатежа, "<ПФР");
		КонецЕсли;
		Если ПозицияПараметра = 0 Тогда
			ПозицияПараметра = СтрНайти(НазначениеПлатежа, "<ФСС");
		КонецЕсли;
		Если ПозицияПараметра = 0 Тогда
			ПозицияПараметра = СтрНайти(НазначениеПлатежа, "<СФР");
		КонецЕсли;
		
		Если ПозицияПараметра > 0 Тогда
			Параметр = Сред(НазначениеПлатежа, ПозицияПараметра);
		КонецЕсли;
		
		НазначениеПлатежа = СокрЛП(ШаблонНазначения + " " + ?(СтрНайти(НазначениеПлатежа, Параметр) = 0, Параметр, ""));
	КонецЕсли;
	
	// Заменяем при необходимости в назначении платежа "Налог" на "Штраф по налогу", "Процент по налогу" и т.п
	Если ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		Если (Перечисления.ВидыПлатежейВГосБюджет.ЭтоПени(ВидНалоговогоОбязательства)
			Или Перечисления.ВидыПлатежейВГосБюджет.ЭтоПроценты(ВидНалоговогоОбязательства)
			Или Перечисления.ВидыПлатежейВГосБюджет.ЭтоШтраф(ВидНалоговогоОбязательства)
			Или Перечисления.ВидыПлатежейВГосБюджет.ЭтоВзносыСвышеПредела(ВидНалоговогоОбязательства)) Тогда
			
			ПредставлениеНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ПредставлениеНалоговогоОбязательства(Налог, ВидНалоговогоОбязательства);
			ПредставлениеПоУмолчанию = Перечисления.ВидыПлатежейВГосБюджет.ПредставлениеНалоговогоОбязательства(Налог, Перечисления.ВидыПлатежейВГосБюджет.Налог);
			
			Если ПредставлениеНалоговогоОбязательства <> ПредставлениеПоУмолчанию Тогда
				ДлинаПредставленияПоУмолчанию = СтрДлина(ПредставлениеПоУмолчанию);
				Если ВРег(Лев(НазначениеПлатежа, ДлинаПредставленияПоУмолчанию)) = ВРег(ПредставлениеПоУмолчанию) Тогда
					НазначениеПлатежа = ПредставлениеНалоговогоОбязательства + Сред(НазначениеПлатежа, ДлинаПредставленияПоУмолчанию + 1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ПредставлениеНалоговогоПериода) И НЕ Перечисления.ВидыПлатежейВГосБюджет.ЭтоШтраф(ВидНалоговогоОбязательства) Тогда
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", ПредставлениеНалоговогоПериода);
	Иначе
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", "");
	КонецЕсли;
	
	// При уплате страховых взносов, как правило, требуется указывать регистрационный номер
	ВидГосударственногоОргана = ВидГосударственногоОргана(Налог);
	Если ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР
		ИЛИ ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганФСС
		ИЛИ ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоВзносыНаПенсионноеСтрахование(ВидНалога)
		ИЛИ ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоВзносыНаСоциальноеСтрахование(ВидНалога) Тогда
		
		КлючиРеквизитов = "РегистрационныйНомерПФР, ИПРегистрационныйНомерПФР, РегистрационныйНомерФСС, ИПРегистрационныйНомерФСС, РегистрационныйНомерСФР";
		РеквизитыОрганизации = ?(ЗначениеЗаполнено(Организация), ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, КлючиРеквизитов), Новый Структура(КлючиРеквизитов));
		
		Если ЗначениеЗаполнено(РеквизитыОрганизации.РегистрационныйНомерСФР) Тогда
			НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР>", РеквизитыОрганизации.РегистрационныйНомерСФР);
		КонецЕсли;
		
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР>",    РеквизитыОрганизации.РегистрационныйНомерПФР);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР_ИП>", РеквизитыОрганизации.ИПРегистрационныйНомерПФР);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС>",    РеквизитыОрганизации.РегистрационныйНомерФСС);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС_ИП>", РеквизитыОрганизации.ИПРегистрационныйНомерФСС);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<СФР>",    РеквизитыОрганизации.РегистрационныйНомерСФР);
	КонецЕсли;
	
	НазначениеПлатежа = СокрЛП(СтрЗаменить(НазначениеПлатежа, "  ", " "));
	
	Возврат НазначениеПлатежа;
	
КонецФункции

// Возвращает Счет учета для налога
//
// Параметры:
//   Налог  - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   Период - Дата
//
// Возвращаемое значение:
//  ПланСчетовСсылка.Хозрасчетный
//
Функция СчетУчета(Налог, Знач Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		Возврат ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, СчетУчета, ДатаАктуальности");
	ВидНалога        = ЗначениеРеквизитов.ВидНалога;
	СчетУчета        = ЗначениеРеквизитов.СчетУчета;
	ДатаАктуальности = ЗначениеРеквизитов.ДатаАктуальности;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда
		
		Если НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности < ДатаАктуальностиКлассификатора()
			ИЛИ НачалоДня(Период) < ДатаАктуальностиКлассификатора() И ДатаАктуальности >= ДатаАктуальностиКлассификатора() Тогда
			
			СчетУчета = РасчетыСБюджетом.СчетУчета(ВидНалога, Период);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СчетУчета
	
КонецФункции

// Возвращает уровень бюджета по налогу
//
Функция УровеньБюджета(Налог) Экспорт
	
	Если ЗначениеЗаполнено(Налог) Тогда
		ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "ВидНалога");
	Иначе
		ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Возврат РасчетыСБюджетом.УровеньБюджета(ВидНалога);
	
КонецФункции

// Возвращает администратора налога по КБК
//
// Параметры:
//   Налог - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыГосударственныхОрганов
//
Функция ВидГосударственногоОргана(Налог, Период = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Налог) Тогда
		КБК = КБК(Налог,, Период);
	Иначе
		КБК = "";
	КонецЕсли;
	
	Возврат ПлатежиВБюджетПереопределяемый.ВидГосударственногоОргана(КБК);
	
КонецФункции

// Создает поставляемые элементы справочника в зависимости от включеных функциональных опций
//
Функция СоздатьПоставляемыеЭлементы() Экспорт
	
	НеобходимыеВидыНалогов = РасчетыСБюджетом.НеобходимыеВидыНалогов();
	
	СуществующиеНалоги = НайтиНалоги(НеобходимыеВидыНалогов);
	
	Если НеобходимыеВидыНалогов.Количество() > 0 Тогда
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("ВидНалога", НеобходимыеВидыНалогов));
		ТаблицаПоставляемыхДанных.Индексы.Добавить("ВидНалога");
	КонецЕсли;
	
	Для Каждого ВидНалога Из НеобходимыеВидыНалогов Цикл
		ОтборВидНалога = Новый Структура("ВидНалога", ВидНалога);
		МассивСуществующихДанных = СуществующиеНалоги.НайтиСтроки(ОтборВидНалога);
		МассивПоставляемыхДанных = ТаблицаПоставляемыхДанных.НайтиСтроки(ОтборВидНалога);
		Если МассивСуществующихДанных.Количество() < МассивПоставляемыхДанных.Количество() Тогда
			Для Каждого СтрокаПоставляемыхДанных Из МассивПоставляемыхДанных Цикл
				СоздатьПоставляемыйЭлемент(СтрокаПоставляемыхДанных);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

// Обновляет поставляемые данные справочника
//
// Параметры:
//   Период - Дата - период действия классификатора КБК
//
Процедура ОбновитьПоставляемыеДанныеИзКлассификатора(Период = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СуществующиеНалоги = НайтиНалоги();
	
	ВидыНалогов = ОбщегоНазначения.ВыгрузитьКолонку(СуществующиеНалоги, "ВидНалога", Истина);
	
	ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога", ВидыНалогов));
	ТаблицаПоставляемыхДанных.Индексы.Добавить("ВидНалога");
	
	НачатьТранзакцию();
	
	Для Каждого СуществующийНалог Из СуществующиеНалоги Цикл
		
		ОтборВидНалога = Новый Структура("ВидНалога", СуществующийНалог.ВидНалога);
		СтрокиДанныхКлассификатора = ТаблицаПоставляемыхДанных.НайтиСтроки(ОтборВидНалога);
		
		Для Каждого СтрокаДанныхКлассификатора Из СтрокиДанныхКлассификатора Цикл
			
			Если ВидНалогаТребуетУточненияПоКодуВидаДоходов(СтрокаДанныхКлассификатора.ВидНалога) Тогда
				Если ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СуществующийНалог.КодБК) <>
						ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СтрокаДанныхКлассификатора.КодБК)
						И Не (ЭтоПересечениеКБКВидаНалогаНДФЛ(СуществующийНалог.ВидНалога, СуществующийНалог.КодБК)
							И ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СтрокаДанныхКлассификатора.КодБК) = ОтдельныйКодВидаДоходаНДФЛПредпринимателя15()) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Объект = СуществующийНалог.Ссылка.ПолучитьОбъект();
			
			Если Объект.НазначениеПлатежаИзменено Тогда
				ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора,, "НазначениеПлатежа");
			Иначе
				ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
			КонецЕсли;
			
			Объект.СчетУчета = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога, Период);
			Если ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ДатаАктуальности) Тогда
				Объект.ДатаАктуальности = СтрокаДанныхКлассификатора.ДатаАктуальности;
			Иначе
				Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
			КонецЕсли;
			
			Объект.КодБК = КБКПоВидуНалоговогоОбязательства(
				СтрокаДанныхКлассификатора.КодБК, СтрокаДанныхКлассификатора.ВидНалога, , ДатаАктуальностиКлассификатора());
			Объект.Записать();
			
			Прервать;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Функция НаименованиеГлавногоАдминистратора(КБК) Экспорт
	
	Если НЕ ПлатежиВБюджетКлиентСервер.КБКЗадан(КБК) Тогда
		Возврат "";
	КонецЕсли;
	
	ИсточникДанных = Справочники.ВидыНалоговИПлатежейВБюджет.ПолучитьМакет("АдминистраторыДоходовБюджетов");
	ОбластьПоиска  = ИсточникДанных.Область(2, 1, ИсточникДанных.ВысотаТаблицы, 1);
	
	ОбластьДанных  = ИсточникДанных.НайтиТекст(
		ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КБК),
		,
		ОбластьПоиска);
	
	Если ОбластьДанных = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ЗначениеИзИсточникаДанных(ИсточникДанных, ОбластьДанных.Верх, 2);
	
КонецФункции

Функция ДанныеВыбораПоКБК(КодБК) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШаблонКБК", Лев(КодБК, 13) + "%");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка,
	|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК";
	
	Если ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяПенсионнымФондом(КодБК) ИЛИ
		ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяФСС(КодБК) Тогда
		Запрос.УстановитьПараметр("ШаблонКБК182",
			ПлатежиВБюджетКлиентСервер.КодАдминистрированияНалоговымиОрганами()
				+ ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КодБК) + "%");
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|"
		+ "ВЫБРАТЬ
		|	ВидыНалоговИПлатежейВБюджет.Ссылка,
		|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
		|ИЗ
		|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
		|ГДЕ
		|	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК182
		|	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления";
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(КодБК)
		ИЛИ ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяФСС(КодБК)
		ИЛИ ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяСФР(КодБК) Тогда
		Запрос.УстановитьПараметр("ШаблонКБК392",
			ПлатежиВБюджетКлиентСервер.КодАдминистрированияПенсионнымФондом()
				+ ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КодБК) + "%");
		Запрос.УстановитьПараметр("ШаблонКБК393",
			ПлатежиВБюджетКлиентСервер.КодАдминистрированияФСС()
				+ ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КодБК) + "%");
		Запрос.УстановитьПараметр("ШаблонКБК797",
			ПлатежиВБюджетКлиентСервер.КодАдминистрированияСФР()
				+ ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КодБК) + "%");
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|"
		 + "ВЫБРАТЬ
		|	ВидыНалоговИПлатежейВБюджет.Ссылка,
		|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
		|ИЗ
		|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
		|ГДЕ
		|	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК392
		|	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВидыНалоговИПлатежейВБюджет.Ссылка,
		|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
		|ИЗ
		|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
		|ГДЕ
		|	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК393
		|	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВидыНалоговИПлатежейВБюджет.Ссылка,
		|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
		|ИЗ
		|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
		|ГДЕ
		|	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК797
		|	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(
			Новый Структура("Значение, ПометкаУдаления",
			Выборка.Ссылка, Выборка.ПометкаУдаления));
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Проверяет по виду организации корректность указанного вида налога
//
Функция ОрганизацияМожетУплачиватьНалог(Налог, Организация) Экспорт
	
	Результат = Истина;
	
	Если Не ЗначениеЗаполнено(Налог) Или Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "ВидНалога");
	Если Не ЗначениеЗаполнено(ВидНалога) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВидыНалоговФизическихЛиц  = РасчетыСБюджетом.ВидыНалоговФизическихЛиц();
	ВидыНалоговЮридическихЛиц = РасчетыСБюджетом.ВидыНалоговЮридическихЛиц();
	
	НалогУплачиваетсяТолькоФизическимиЛицами  = (ВидыНалоговФизическихЛиц.Найти(ВидНалога) <> Неопределено);
	НалогУплачиваетсяТолькоЮридическимиЛицами = (ВидыНалоговЮридическихЛиц.Найти(ВидНалога) <> Неопределено);
	
	// Проверку выполняем на вхождение в оба массива, чтобы налог не оказался запрещенным для всех организаций
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		Результат = Не НалогУплачиваетсяТолькоФизическимиЛицами Или НалогУплачиваетсяТолькоЮридическимиЛицами;
	Иначе
		Результат = Не НалогУплачиваетсяТолькоЮридическимиЛицами Или НалогУплачиваетсяТолькоФизическимиЛицами;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет по виду налога, требуется ли при его выборе дополнительно уточнить код вида дохода
//
// Параметры:
//   ВидНалога - ПеречислениеСсылка.ВидыНалогов
//
// Возвращаемое значение:
//   Булево - Истина, если требуется уточнение, Ложь - уточнение не требуется
//
Функция ВидНалогаТребуетУточненияПоКодуВидаДоходов(ВидНалога) Экспорт
	
	Возврат ЭтоНДФЛНалоговогоАгента(ВидНалога)
		Или ЭтоНДФЛПредпринимателя(ВидНалога);
	
КонецФункции

// Определяет коды видов доходов бюджета, соответствующие НДФЛ, удерживаемого из оплаты труда работников.
// Получить соответствующие элементы справочника ВидыНалоговИПлатежейВБюджет можно с помощью НалогПоВиду()
// 
// Возвращаемое значение:
//  Массив Из Строка - коды видов доходов бюджета.
//
Функция КодыВидовДоходаНДФЛ_ОплатаТруда() Экспорт
	
	Коды = Новый Массив;
	
	ДополнитьКодыВидаДоходаНДФЛАгент(Коды);
	ДополнитьКодыВидаДоходаНДФЛАгентРКиСН(Коды);
	
	Возврат Коды;
	
КонецФункции

// Определяет коды видов доходов бюджета, соответствующие НДФЛ, удерживаемого при выплате дивидендов.
// Получить соответствующие элементы справочника ВидыНалоговИПлатежейВБюджет можно с помощью НалогПоВиду()
// 
// Возвращаемое значение:
//  Массив Из Строка - коды видов доходов бюджета.
//
Функция КодыВидовДоходаНДФЛ_Дивиденды() Экспорт
	
	Коды = Новый Массив;
	
	ДополнитьКодыВидаДоходаНДФЛАгентДивиденды(Коды);
	
	Возврат Коды;
	
КонецФункции

#Область СлужебныйПрограммныйИнтерфейсДляОбслуживанияНДФЛ

#Область АгентскийНДФЛ

Функция КодыВидовДоходаНалоговогоАгента(ВидНалога, УчитыватьНастройки = Ложь) Экспорт
	
	МассивКодовВидаДоходов = Новый Массив;
	
	Если ЭтоНДФЛНалоговогоАгента(ВидНалога) Тогда
		
		ПлатежиВБюджетПереопределяемый.УчитыватьНастройкиПриСозданииЭлементов(УчитыватьНастройки, ВидНалога);
		
		ДополнитьКодыВидаДоходаНДФЛАгент(МассивКодовВидаДоходов);
		
		Если Не УчитыватьНастройки Или (УчитыватьНастройки И РасчетыСБюджетом.ВедетсяУчетДивидендов())Тогда
			ДополнитьКодыВидаДоходаНДФЛАгентДивиденды(МассивКодовВидаДоходов);
		КонецЕсли;
		
		Если Не УчитыватьНастройки Или (УчитыватьНастройки И РасчетыСБюджетом.ВедетсяУчетРКиСН()) Тогда
			ДополнитьКодыВидаДоходаНДФЛАгентРКиСН(МассивКодовВидаДоходов);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивКодовВидаДоходов;

КонецФункции

Функция ЭтоНДФЛНалоговогоАгента(ВидНалога) Экспорт
	
	ВидыАгентскогоНДФЛ = ЕдиныйНалоговыйСчетПовтИсп.ВидыАгентскогоНДФЛ();
	ЭтоНДФЛНалоговогоАгента = ВидыАгентскогоНДФЛ.Найти(ВидНалога) <> Неопределено;
	
	Возврат ЭтоНДФЛНалоговогоАгента;
	
КонецФункции

Функция ЭтоНДФЛПредпринимателя(ВидНалога) Экспорт
	
	ВидыПредпринимательскогоНДФЛ = ЕдиныйНалоговыйСчетПовтИсп.ВидыПредпринимательскогоНДФЛ();
	ЭтоНДФЛПредпринимателя = ВидыПредпринимательскогоНДФЛ.Найти(ВидНалога) <> Неопределено;
	
	Возврат ЭтоНДФЛПредпринимателя;
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгент13(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгент13();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгент15(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгент15();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгент18(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгент18();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгент20(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгент20();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгент22(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгент22();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгентДивиденды13(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгентДивиденды13();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгентДивиденды15(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгентДивиденды15();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгентРКиСН13(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгентРКиСН13();
	
КонецФункции

Функция ЭтоНДФЛНалоговыйАгентРКиСН15(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛАгентРКиСН15();
	
КонецФункции

Функция КодВидаДоходаНДФЛАгент13() Экспорт
	
	Возврат "1010201001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгент15() Экспорт
	
	Возврат "1010208001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгент18() Экспорт
	
	Возврат "1010215001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгент20() Экспорт
	
	Возврат "1010216001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгент22() Экспорт
	
	Возврат "1010217001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгентДивиденды13() Экспорт
	
	Возврат "1010213001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгентДивиденды15() Экспорт
	
	Возврат "1010214001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгентРКиСН13() Экспорт
	
	Возврат "1010221001";
	
КонецФункции

Функция КодВидаДоходаНДФЛАгентРКиСН15() Экспорт
	
	Возврат "1010223001";
	
КонецФункции

#КонецОбласти

#Область ПредпринимательскийНДФЛ

Функция КодыВидовДоходаПредпринимателя(ВидНалога, УчитыватьНастройки = Ложь, Период = Неопределено) Экспорт
	
	МассивКодовВидаДоходов = Новый Массив;
	
	Если ЭтоНДФЛПредпринимателя(ВидНалога) Тогда
		Если Не УчитыватьНастройки Или (УчитыватьНастройки И РасчетыСБюджетом.ВедетсяУчетПредпринимателя()) Тогда
			МассивКодовВидаДоходов.Добавить(КодВидаДоходаНДФЛПредпринимателя13());
			МассивКодовВидаДоходов.Добавить(КодВидаДоходаНДФЛПредпринимателя15(Период));
			МассивКодовВидаДоходов.Добавить(КодВидаДоходаНДФЛПредпринимателя18());
			МассивКодовВидаДоходов.Добавить(КодВидаДоходаНДФЛПредпринимателя20());
			МассивКодовВидаДоходов.Добавить(КодВидаДоходаНДФЛПредпринимателя22());
		КонецЕсли;
	КонецЕсли;
	
	Возврат МассивКодовВидаДоходов;

КонецФункции

Функция ЭтоНДФЛПредпринимателя13(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛПредпринимателя13();
	
КонецФункции

Функция ЭтоНДФЛПредпринимателя15(КБК, Период = Неопределено) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛПредпринимателя15(Период);
	
КонецФункции

Функция ЭтоНДФЛПредпринимателя18(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛПредпринимателя18();
	
КонецФункции

Функция ЭтоНДФЛПредпринимателя20(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛПредпринимателя20();
	
КонецФункции

Функция ЭтоНДФЛПредпринимателя22(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаНДФЛПредпринимателя22();
	
КонецФункции

Функция КодВидаДоходаНДФЛПредпринимателя13() Экспорт
	
	Возврат "1010202001";
	
КонецФункции

Функция КодВидаДоходаНДФЛПредпринимателя15(Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	Если Период < '20250101' Тогда // до 2025 г. КБК ИП и агентский были едины
		Возврат КодВидаДоходаНДФЛАгент15();
	Иначе
		Возврат ОтдельныйКодВидаДоходаНДФЛПредпринимателя15();
	КонецЕсли;
	
КонецФункции

Функция ОтдельныйКодВидаДоходаНДФЛПредпринимателя15() Экспорт
	
	Возврат "1010202101";
	
КонецФункции

Функция КодВидаДоходаНДФЛПредпринимателя18() Экспорт
	
	Возврат "1010202201";
	
КонецФункции

Функция КодВидаДоходаНДФЛПредпринимателя20() Экспорт
	
	Возврат "1010202301";
	
КонецФункции

Функция КодВидаДоходаНДФЛПредпринимателя22() Экспорт
	
	Возврат "1010202401";
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Ищет КБК в макете (указанной версии) по переданному Коду
//
// Параметры:
//
//	- Код - Строка
//	- ВерсияКлассификатора - Число - Значение по умолчанию: 0
//	- КБКИсходный - Строка
//
Функция НайтиКодДоходаБюджета(Код, ВерсияКлассификатора = 0, КБКИсходный = "", ОпределитьКОСГУ = Ложь) Экспорт
	
	// Функция оставлена для совместимости.
	// Следует использовать ОписаниеКБК()
	// В качестве Код и КБКИсходный передается шаблон кода (17 знаков)
	
	ВидДохода = Лев(Код, 10);
	КОСГУ    = Сред(Код, 15);
	
	КодДоходаБюджета = Новый Структура();
	КодДоходаБюджета.Вставить("ВидДохода",           ВидДохода);
	КодДоходаБюджета.Вставить("КОСГУ",               КОСГУ);
	КодДоходаБюджета.Вставить("Наименование",        "");
	КодДоходаБюджета.Вставить("КраткоеНаименование", "");
	КодДоходаБюджета.Вставить("ПодвидДохода",        "");
	
	Период = ДатаАктуальностиКлассификатора();
	Если ЗначениеЗаполнено(ВерсияКлассификатора) Тогда
		Период = Дата(ВерсияКлассификатора, 1, 1);
	КонецЕсли;
	
	Если Период < ДатаАктуальностиКлассификатора() Тогда
		Период = НачалоГода(ДатаАктуальностиКлассификатора() - 1);
	КонецЕсли;
	
	ОписаниеКБК = Неопределено;
	Если ЗначениеЗаполнено(КБКИсходный) Тогда
		ОписаниеКБК = ОписаниеКБК("000" + СокрЛП(КБКИсходный), Период, ОпределитьКОСГУ);
	КонецЕсли;
	
	Если ОписаниеКБК = Неопределено Тогда
		ОписаниеКБК = ОписаниеКБК("000" + СокрЛП(Код), Период, ОпределитьКОСГУ);
	КонецЕсли;
	
	Если ОписаниеКБК = Неопределено Тогда
		Возврат КодДоходаБюджета;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(КодДоходаБюджета, ОписаниеКБК);
	
	НайденныйШаблон = ОписаниеКБК.НайденныйШаблон;
	
	КодДоходаБюджета.ВидДохода = Лев(НайденныйШаблон,  10);
	КодДоходаБюджета.КОСГУ     = Сред(НайденныйШаблон, 15);
	
	ВыбраныйПодвидДохода = Сред(НайденныйШаблон, 11, 4);
	Если НЕ ПустаяСтрока(ВыбраныйПодвидДохода) И ВыбраныйПодвидДохода <> "0000"
		И СтрДлина(ВыбраныйПодвидДохода) = 4 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ВыбраныйПодвидДохода) Тогда
		КодДоходаБюджета.ПодвидДохода = ВыбраныйПодвидДохода;
	КонецЕсли;
	
	Возврат КодДоходаБюджета;
	
КонецФункции

Функция ОписаниеКБК(КБК, Период, ОпределитьКОСГУ = Ложь) Экспорт
	
	Если НЕ ПлатежиВБюджетКлиентСервер.КБКЗадан(КБК) Тогда
		ДлинаКБК = СтрДлина(КБК);
		Если НЕ (ОпределитьКОСГУ И (ДлинаКБК = 13 ИЛИ ДлинаКБК = 17)) Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ИмяМакета = ИмяМакетаКБК(Период);
	
	ИсточникДанных = Справочники.ВидыНалоговИПлатежейВБюджет.ПолучитьМакет(ИмяМакета);
	ОбластьПоиска  = ИсточникДанных.Область(2, 1, ИсточникДанных.ВысотаТаблицы, 1);
	
	ШаблонКБК      = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК, Ложь);
	
	ОбластьДанных = Неопределено;
	Если ШаблонКБК <> "" Тогда
		ОбластьДанных = ИсточникДанных.НайтиТекст(ШаблонКБК,, ОбластьПоиска);
		Если ОбластьДанных = Неопределено Тогда
			ШаблонКБК     = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК, Истина);
			ОбластьДанных = ИсточникДанных.НайтиТекст(ШаблонКБК,, ОбластьПоиска);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбластьДанных = Неопределено И ОпределитьКОСГУ Тогда
		ШаблонКБКБезКОСГУ = ПлатежиВБюджетКлиентСервер.ЭлементКБК(КБК, "КодВидаДоходов");
		ОбластьДанных     = ИсточникДанных.НайтиТекст(ШаблонКБКБезКОСГУ,, ОбластьПоиска);
		
		Если ОбластьДанных <> Неопределено Тогда
			ШаблонКБК = ШаблонКБКБезКОСГУ + ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов();
		КонецЕсли;
	КонецЕсли;
	
	Если ОбластьДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НомерСтроки                     = ОбластьДанных.Верх;
	НомерКолонкиКод                 = 1;
	НомерКолонкиНаименование        = 2;
	НомерКолонкиКраткоеНаименование = 3;
	
	Описание = Новый Структура();
	
	Если ОпределитьКОСГУ Тогда
		НайденныйКБК = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКод);
		Описание.Вставить("КОСГУ",           Прав(НайденныйКБК, 3));
		Описание.Вставить("НайденныйШаблон", НайденныйКБК); // ШаблонКБК + Описание.КОСГУ);
	Иначе
		Описание.Вставить("НайденныйШаблон", ШаблонКБК);
	КонецЕсли;
	
	Описание.Вставить("Наименование",        ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименование));
	Описание.Вставить("КраткоеНаименование", ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКраткоеНаименование));
	
	Возврат Описание;
	
КонецФункции

Функция СчетУчетаРасчетовПоКБК(КБК) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодБК_1_13", Лев(КБК, 13));
	Запрос.УстановитьПараметр("КОСГУ", ПлатежиВБюджетКлиентСервер.ЭлементКБК(КБК, "КодОперацииСектораУправления"));
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ВидыНалогов.СчетУчета КАК Счет
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалогов
	|ГДЕ
	|	ПОДСТРОКА(ВидыНалогов.КодБК, 1, 13) = &КодБК_1_13
	|	И ПОДСТРОКА(ВидыНалогов.КодБК, 18, 3) = &КОСГУ
	|	И ВидыНалогов.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Счет;
	Иначе
		Возврат ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// Возвращает имя макета КБК, у которого не указан год.
// Это макет КБК текущего, или прошлого года.
//
// Возвращаемое значение:
//   Строка - имя макета
//
Функция ИмяМакетаКБКБезГода() Экспорт
	
	Возврат "КлассификацияДоходовБюджетов";
	
КонецФункции

// Возвращает имя макета КБК, у которого указан год.
// Это макет КБК текущего, или нового года.
//
// Возвращаемое значение:
//   Строка - имя макета
//
Функция ИмяМакетаКБКНовый() Экспорт
	
	ИмяМакетаНовый   = "";
	ИмяМакетаБезГода = ИмяМакетаКБКБезГода();
	Для Каждого ИмяМакета Из Метаданные.Справочники.ВидыНалоговИПлатежейВБюджет.Макеты Цикл
		Если Не СтрНачинаетсяС(ИмяМакета.Имя, ИмяМакетаБезГода) Или ИмяМакета.Имя = ИмяМакетаБезГода Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяМакетаНовый = ИмяМакета.Имя;
		Прервать;
	КонецЦикла;
	
	Если ПустаяСтрока(ИмяМакетаНовый) Тогда
		// что-то пошло не так
		ИмяМакетаНовый = ИмяМакетаБезГода;
	КонецЕсли;
	
	Возврат ИмяМакетаНовый;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДатаАктуальностиКлассификатораПользователя(Знач Период = Неопределено)
	
	ДатаАктуальностиКлассификатора = ДатаАктуальностиКлассификатора();
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаПользователя;
	КонецЕсли;
	
	ДатаАктуальностиВПрошломГоду = НачалоГода(Период) < ДатаАктуальностиКлассификатора
		Или Год(ТекущаяДатаПользователя) < Год(ДатаАктуальностиКлассификатора);
	Если ДатаАктуальностиВПрошломГоду Тогда
		ДатаАктуальностиКлассификатора = НачалоГода(НачалоГода(ДатаАктуальностиКлассификатора) - 1);
	КонецЕсли;
	
	Возврат ДатаАктуальностиКлассификатора;
	
КонецФункции

Функция ИмяМакетаКБК(Знач Период = Неопределено)
	
	ДатаАктуальностиКлассификатора = ДатаАктуальностиКлассификатора();
	ДатаАктуальностиПользователя   = ДатаАктуальностиКлассификатораПользователя(Период);
	
	Если ДатаАктуальностиПользователя < ДатаАктуальностиКлассификатора Тогда
		Возврат ИмяМакетаКБКБезГода();
	Иначе
		Возврат ИмяМакетаКБКНовый();
	КонецЕсли;
	
КонецФункции

Функция НайтиПоставляемыеДанные(ВидНалога = Неопределено, КБК = Неопределено, СчетУчета = Неопределено, Период = Неопределено, НазначениеПлатежаИзменено = Ложь, ПолучатьВсеЗначение = Ложь) Экспорт
	
	КодБК = "";
	Если Не ПустаяСтрока(КБК) Тогда
		КодБК = Лев(СокрЛ(КБК), 13) + ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() + Сред(СокрЛП(КБК), 18);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидНалога) И ПустаяСтрока(КодБК) И ЗначениеЗаполнено(СчетУчета) Тогда
		ВидНалога = РасчетыСБюджетом.ВидНалогаПоСчетуУчета(СчетУчета, , Период);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Период) Тогда
		НачалоПериода = НачалоГода(Период);
	Иначе
		НачалоПериода = Период;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидНалога) Тогда
		
		Если ПустаяСтрока(КодБК) Тогда
			ТаблицаПоставляемыхДанных = УчетДенежныхСредствПовтИсп.ТаблицаПоставляемыхДанныхПоВидуНалога(
				НачалоПериода, ВидНалога);
		Иначе
			СтруктураОтбора = Новый Структура();
			СтруктураОтбора.Вставить("ВидНалога", ВидНалога);
			СтруктураОтбора.Вставить("КодБК", КодБК);
			ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, СтруктураОтбора);
		КонецЕсли;
		
	ИначеЕсли Не ПустаяСтрока(КодБК) Тогда
		
		ТаблицаПоставляемыхДанных = УчетДенежныхСредствПовтИсп.ТаблицаПоставляемыхДанныхПоКБК(НачалоПериода, КодБК);
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ОписаниеПоставляемыхДанных = Неопределено;
	
	Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
		
		ОписаниеПоставляемыхДанных = Новый СписокЗначений;
		
		Для Каждого СтрокаТаблицы Из ТаблицаПоставляемыхДанных Цикл
			
			СтруктураДанных = НоваяСтруктураПоставляемыхДанных();
			ЗаполнитьЗначенияСвойств(СтруктураДанных, СтрокаТаблицы);
			
			Если ЗначениеЗаполнено(СтруктураДанных.ВидНалога) Тогда
				
				Если ЗначениеЗаполнено(СчетУчета) Тогда
					СтруктураДанных.СчетУчета = СчетУчета;
				Иначе
					СтруктураДанных.СчетУчета = РасчетыСБюджетом.СчетУчета(СтруктураДанных.ВидНалога, Период);
				КонецЕсли;
				Если ЗначениеЗаполнено(Период) И Период < ДатаАктуальностиКлассификатора() Тогда
					СтруктураДанных.ДатаАктуальности = Период;
				ИначеЕсли Не ЗначениеЗаполнено(СтруктураДанных.ДатаАктуальности) Тогда
					СтруктураДанных.ДатаАктуальности = ДатаАктуальностиКлассификатора();
				КонецЕсли;
				
				СтруктураДанных.НазначениеПлатежаИзменено = НазначениеПлатежаИзменено;
				ПодменитьРеквизитыПриСовпаденииКБК(СтруктураДанных);
				
			КонецЕсли;
			
			Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(СтруктураДанных.КодБК) = ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
				СтруктураДанных.КодБК = КБКПоВидуНалоговогоОбязательства(СтруктураДанных.КодБК, СтруктураДанных.ВидНалога, , СтруктураДанных.ДатаАктуальности);
			КонецЕсли;
			
			Если ПолучатьВсеЗначение И ТаблицаПоставляемыхДанных.Количество() > 1 Тогда
				ОписаниеПоставляемыхДанных.Добавить(СтруктураДанных, СтрШаблон(НСтр("ru = '%1 - %2'"), СтруктураДанных.КодБК, СтруктураДанных.Наименование));
			Иначе
				ОписаниеПоставляемыхДанных = СтруктураДанных;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ОписаниеПоставляемыхДанных;
	
КонецФункции

Процедура ПодменитьРеквизитыПриСовпаденииКБК(Объект) Экспорт
	
	Если ДатаАктуальностиКлассификатора() < ПлатежиВБюджетКлиентСервер.НачалоДействияПриказа90н() Тогда
		
		Если ЗначениеЗаполнено(Объект.ВидНалога) И Объект.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
			// До 2016 года фиксированные взносы в ФФОМС уплачиваются по тому же КБК, что и взносы за работников.
			// По умолчанию реквизиты содержат наименование и назначение для взносов с ФОТ.
			Объект.Наименование      = НСтр("ru = 'Фиксированные взносы в ФФОМС'");
			Если НЕ Объект.НазначениеПлатежаИзменено Тогда
				Объект.НазначениеПлатежа = НСтр("ru = 'Страховые взносы на ОМС, зачисляемые в бюджет ФФОМС'");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьПоставляемыйЭлемент(СтрокаДанныхКлассификатора, ПропускатьПроверку = Ложь)
	
	Если СтрокаДанныхКлассификатора = Неопределено Или Не ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ВидНалога) Тогда
		Возврат Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
	КонецЕсли;
	
	ПоставляемыйЭлемент = Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
	
	// Для исключения дублей проверяем наличие элемента в справочнике
	КодВидаДоходов            = "";
	КонтрольныйКодВидаДоходов = "";
	Если ВидНалогаТребуетУточненияПоКодуВидаДоходов(СтрокаДанныхКлассификатора.ВидНалога) Тогда
		КодВидаДоходов = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СтрокаДанныхКлассификатора.КодБК);
		Если Не ПропускатьПроверку Тогда
			КодыВидовДоходов = КодыВидовДоходовВидаНалогаТребующегоУточнения(СтрокаДанныхКлассификатора.ВидНалога,
				Истина, Макс(ДатаАктуальностиКлассификатора(), ДатаВторогоКлассификатора()));
			Если КодыВидовДоходов.Найти(КодВидаДоходов) = Неопределено Тогда
				// Не требуется
				Возврат ПоставляемыйЭлемент;
			КонецЕсли;
			КонтрольныйКодВидаДоходов = КонтрольныйКодВидаДоходов(СтрокаДанныхКлассификатора.КодБК);
		КонецЕсли;
	КонецЕсли;
	
	РезультатЗапроса = ЗапросПоВидуНалога(СтрокаДанныхКлассификатора.ВидНалога, КодВидаДоходов);
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ПоставляемыйЭлемент = Выборка.Ссылка;
	КонецЕсли;
	
	Если ПоставляемыйЭлемент.Пустая() И ЗначениеЗаполнено(КонтрольныйКодВидаДоходов) Тогда
		РезультатЗапроса = ЗапросПоВидуНалога(СтрокаДанныхКлассификатора.ВидНалога, КонтрольныйКодВидаДоходов);
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ПоставляемыйЭлемент = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если ПоставляемыйЭлемент.Пустая() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Если ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ДатаАктуальности) Тогда
			ДатаАктуальности = СтрокаДанныхКлассификатора.ДатаАктуальности;
		Иначе
			ДатаАктуальности = ДатаАктуальностиКлассификатора();
		КонецЕсли;
		
		Объект = Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
		Объект.УстановитьНовыйКод("0");
		Объект.СчетУчета        = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога);
		Объект.ДатаАктуальности = ДатаАктуальности;
		Объект.КодБК = КБКПоВидуНалоговогоОбязательства(
			СтрокаДанныхКлассификатора.КодБК, СтрокаДанныхКлассификатора.ВидНалога, , ДатаАктуальности);
		Объект.Записать();
		
		ПоставляемыйЭлемент = Объект.Ссылка;
		
	КонецЕсли;
	
	Возврат ПоставляемыйЭлемент;
	
КонецФункции

Функция НайтиНалоги(ВидыНалогов = Неопределено)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Ссылка,
		|	ВидыНалоговИПлатежейВБюджет.ВидНалога КАК ВидНалога,
		|	ВидыНалоговИПлатежейВБюджет.КодБК КАК КодБК
		|ИЗ
		|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
		|ГДЕ
		|	&УсловиеПоВидамНалогов
		|	И ВидыНалоговИПлатежейВБюджет.ВидНалога <> ЗНАЧЕНИЕ(Перечисление.ВидыНалогов.ПрочиеНалогиИСборы)
		|	И ВидыНалоговИПлатежейВБюджет.ВидНалога <> ЗНАЧЕНИЕ(Перечисление.ВидыНалогов.ПустаяСсылка)";
		
	Если ВидыНалогов <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоВидамНалогов", "ВидыНалоговИПлатежейВБюджет.ВидНалога В(&ВидыНалогов)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоВидамНалогов", "ИСТИНА");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыНалогов", ВидыНалогов);
	Запрос.Текст = ТекстЗапроса;
	
	Налоги = Запрос.Выполнить().Выгрузить();
	
	Возврат Налоги;
	
КонецФункции

Функция НоваяСтруктураПоставляемыхДанных()
	
	ТаблицаПоставляемыхДанных = НоваяТаблицаПоставляемыхДанных();
	
	СтруктураПоставляемыхДанных = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаПоставляемыхДанных.Добавить());
	СтруктураПоставляемыхДанных.Вставить("СчетУчета",        ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	СтруктураПоставляемыхДанных.Вставить("ДатаАктуальности", Дата(1, 1, 1));
	СтруктураПоставляемыхДанных.Вставить("НазначениеПлатежаИзменено", Ложь);
	
	Возврат СтруктураПоставляемыхДанных;
	
КонецФункции

Функция НоваяТаблицаПоставляемыхДанных()
	
	ТаблицаПоставляемыхДанных = Новый ТаблицаЗначений;
	ТаблицаПоставляемыхДанных.Колонки.Добавить("ВидНалога",           Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНалогов"));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("КодБК",               ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("Наименование",        ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("НаименованиеПолное",  ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("НазначениеПлатежа",   ОбщегоНазначения.ОписаниеТипаСтрока(210));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("ДатаАктуальности",    ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	
	Возврат ТаблицаПоставляемыхДанных;
	
КонецФункции

Функция ПрочитатьПоставляемыеДанныеКлассификатора(Период = Неопределено, Отбор = Неопределено, ТолькоСИдентификаторами = Ложь) Экспорт
	Перем ВидНалога;
	Перем ОтборПоВидуНалога, ОтборПоКБК, ОтборПоШаблонуКБК;
	
	УстановленОтбор = (Отбор <> Неопределено);
	
	Если УстановленОтбор И ТипЗнч(Отбор) = Тип("Структура") Тогда
		Отбор.Свойство("ВидНалога", ОтборПоВидуНалога);
		Отбор.Свойство("КодБК",     ОтборПоКБК);
		Если ОтборПоКБК <> Неопределено Тогда
			Если ЗначениеЗаполнено(Период) Тогда
				МаксимальнаяДата = Макс(НачалоГода(Период), ДатаАктуальностиКлассификатора(), ДатаВторогоКлассификатора());
				Если ЭтоНДФЛПредпринимателя15(ОтборПоКБК, МаксимальнаяДата) И Не ЭтоНДФЛПредпринимателя15(ОтборПоКБК, Период) Тогда
					ОтборПоКБК = СтрЗаменить(ОтборПоКБК, КодВидаДоходаНДФЛПредпринимателя15(МаксимальнаяДата), КодВидаДоходаНДФЛПредпринимателя15(Период));
				КонецЕсли;
			КонецЕсли;
			ОтборПоКБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(ОтборПоКБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(ОтборПоКБК, Истина);
			ОтборПоШаблонуКБК = ПлатежиВБюджетКлиентСервер.ПустойКодГлавногоАдминистратора() + ПлатежиВБюджетКлиентСервер.ШаблонКБК(ОтборПоКБК, Истина);
		КонецЕсли;
	КонецЕсли;
	
	ТипОтбораПоВидуНалога = ТипЗнч(ОтборПоВидуНалога);
	
	УстановленМножественныйОтбор = (ТипОтбораПоВидуНалога = Тип("Массив"));
	Если УстановленМножественныйОтбор Тогда
		ОтборПоВидуНалога = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(ОтборПоВидуНалога);
	КонецЕсли;
	
	ТаблицаПоставляемыхДанных = НоваяТаблицаПоставляемыхДанных();
	
	НомерКолонкиКБК                 = 1; // Код дохода бюджета
	НомерКолонкиНаименованиеПолное  = 2; // Полное наименование
	НомерКолонкиНаименование        = 3; // Краткое наименование
	НомерКолонкиКодАдминистратора   = 4; // Код главного администратора доходов бюджета
	НомерКолонкиНазначениеПлатежа   = 5; // Шаблон назначения платежа
	НомерКолонкиИдентификатор       = 6; // Идентификатор
	
	ГраницаРазделаПоддерживаемыхКБК = "118";
	
	ИмяМакета = ИмяМакетаКБК(Период);
	ДатаАктуальности = ДатаАктуальностиКлассификатораПользователя(Период);
	ИсточникДанных = Справочники.ВидыНалоговИПлатежейВБюджет.ПолучитьМакет(ИмяМакета);
	
	ПредопределенныеВидовНалогов = РасчетыСБюджетом.ВидыНалогов();
	
	ПустойКодГлавногоАдминистратора = ПлатежиВБюджетКлиентСервер.ПустойКодГлавногоАдминистратора();
	
	Для НомерСтроки = 2 По ИсточникДанных.ВысотаТаблицы Цикл
		
		КБК = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКБК);
		Если ПустаяСтрока(КБК) Тогда
			// Конец таблицы
			Прервать;
		ИначеЕсли Лев(КБК, 3) = ГраницаРазделаПоддерживаемыхКБК Тогда
			// Читаем не весь макет, только данные раздела с поддерживаемыми налогами и взносами.
			Прервать;
		КонецЕсли;
		
		КодАдминистратора = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКодАдминистратора);
		
		КодБК = ?(Не ПустаяСтрока(КодАдминистратора), КодАдминистратора + КБК, ПустойКодГлавногоАдминистратора + КБК);
		
		Если УстановленОтбор И ОтборПоКБК <> Неопределено Тогда
			Если Не ПустаяСтрока(КодАдминистратора) Тогда
				Если ОтборПоКБК <> КодБК Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Если ОтборПоШаблонуКБК <> КодБК Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаИдентификаторов = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиИдентификатор);
		Если УстановленОтбор И ОтборПоВидуНалога <> Неопределено И ПустаяСтрока(СтрокаИдентификаторов) Тогда
			// Непоставляемые данные
			Продолжить;
		КонецЕсли;
		
		Если Не ПустаяСтрока(СтрокаИдентификаторов) Тогда
			МассивИдентификаторов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаИдентификаторов);
		Иначе
			МассивИдентификаторов = Новый Массив;
			МассивИдентификаторов.Добавить("");
		КонецЕсли;
		
		НаименованиеПолное    = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименованиеПолное);
		Наименование          = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименование);
		НазначениеПлатежа     = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНазначениеПлатежа);
		
		Для Индекс = 0 По МассивИдентификаторов.ВГраница() Цикл
			
			Идентификатор       = СокрЛП(МассивИдентификаторов[Индекс]);
			
			Если ПустаяСтрока(Идентификатор) Или Не ПредопределенныеВидовНалогов.Свойство(Идентификатор, ВидНалога) Тогда
				Если ТолькоСИдентификаторами Тогда
					// Читаем не весь макет, только данные раздела с поддерживаемыми налогами и взносами.
					Если Лев(КБК, 3) = ГраницаРазделаПоддерживаемыхКБК Тогда
						Прервать;
					Иначе
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка();
			КонецЕсли;
			
			Если УстановленОтбор И ОтборПоВидуНалога <> Неопределено Тогда
				Если ТипОтбораПоВидуНалога = Тип("Массив") Тогда
					Если ОтборПоВидуНалога.Найти(ВидНалога) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли ОтборПоВидуНалога <> ВидНалога Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЭтоПересечениеКБКВидаНалогаНДФЛ(ВидНалога, КодБК) Тогда
				// Исключение для КБК, по которому есть 2 разных налога.
				Наименование = НСтр("ru = 'НДФЛ индивидуального предпринимателя (15%)'");
			ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть Тогда
				Наименование = НСтр("ru = 'Фиксированные взносы в ПФР (до 2023 года)'");
				НазначениеПлатежа = НСтр("ru = 'Страховые взносы в фиксированном размере'");
			ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
				Наименование = НСтр("ru = 'Фиксированные взносы в ФФОМС (до 2023 года)'");
			КонецЕсли;
			
			НоваяСтрока = ТаблицаПоставляемыхДанных.Добавить();
			НоваяСтрока.ВидНалога           = ВидНалога;
			НоваяСтрока.КодБК               = КодБК;
			НоваяСтрока.НаименованиеПолное  = НаименованиеПолное;
			НоваяСтрока.Наименование        = Наименование;
			НоваяСтрока.НазначениеПлатежа   = НазначениеПлатежа;
			НоваяСтрока.ДатаАктуальности    = ДатаАктуальности;
			
			Если УстановленОтбор Тогда
				Если ВидНалогаТребуетУточненияПоКодуВидаДоходов(ВидНалога) Тогда
					Продолжить;
				КонецЕсли;
				Если УстановленМножественныйОтбор Тогда
					МассивПоставляемыхВидовНалога = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПоставляемыхДанных, "ВидНалога", Истина);
					Если ОтборПоВидуНалога <> Неопределено И МассивПоставляемыхВидовНалога.Количество() = ОтборПоВидуНалога.Количество() Тогда
						// Все значения отбора уже найдены
						Возврат ТаблицаПоставляемыхДанных;
					КонецЕсли;
				Иначе
					// Возвращаем первую найденную запись
					Возврат ТаблицаПоставляемыхДанных;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаПоставляемыхДанных;
	
КонецФункции

Функция ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонки)
	
	ОбластьДанных = ИсточникДанных.Область(НомерСтроки, НомерКолонки);
	Если ОбластьДанных = Неопределено Тогда
		Возврат "";
	Иначе
		Возврат ОбластьДанных.Текст;
	КонецЕсли;
	
КонецФункции

Функция ЭтоАвансовыйПлатежТаможня(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаАвансовыйПлатежТаможня()
		Или ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаАвансовыйПлатежТаможняЕАЭС();
	
КонецФункции

Функция ЭтоПроцентыПоПлатежу(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = КодВидаДоходаПроцентыПоПлатежу();
	
КонецФункции

Функция ЭтоПениШтрафыИсключения(КБК) Экспорт
	
	КодыИсключения = КодыВидаДоходаПениШтрафыИсключения();
	ВидДохода = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК);
	
	Результат = КодыИсключения.Найти(ВидДохода) <> Неопределено;
	
	Возврат Результат;
	
КонецФункции

Функция КодВидаДоходаАвансовыйПлатежТаможня() Экспорт
	
	// Авансовые платежи в счет будущих таможенных и иных платежей
	Возврат "1100900001";
	
КонецФункции

Функция КодВидаДоходаАвансовыйПлатежТаможняЕАЭС() Экспорт
	
	// Авансовые платежи в счет будущих таможенных платежей
	Возврат "0106130101";
	
КонецФункции

Функция КодВидаДоходаПроцентыПоПлатежу() Экспорт
	
	// Суммы процентов, установленных Налоговым кодексом Российской Федерации
	Возврат "1162000001";
	
КонецФункции

Функция КодыВидаДоходаПениШтрафыИсключения() Экспорт
	
	Коды = Новый Массив;
	
	// Штрафы по страховым взносам на обязательное социальное страхование на случай временной нетрудоспособности
	// и в связи с материнством за расчетные периоды, истекшие до 1 января 2023 года
	Коды.Добавить("1162102006");
	
	// Административные штрафы, установленные главой 15 Кодекса Российской Федерации об административных правонарушениях,
	// за административные правонарушения в области финансов, налогов и сборов, страхования, рынка ценных бумаг
	// (за исключением штрафов, указанных в пункте 6 статьи 46 Бюджетного кодекса Российской Федерации),
	// выявленные должностными лицами Фонда пенсионного и социального страхования Российской Федерации
	Коды.Добавить("1160123006");
	
	// Иные штрафы, неустойки, пени, уплаченные в соответствии с законом или договором в случае неисполнения
	// или ненадлежащего исполнения обязательств перед Фондом пенсионного и социального страхования Российской Федерации
	Коды.Добавить("1160709006");
	
	// Суммы пеней, установленных Налоговым кодексом Российской Федерации, распределяемые в соответствии
	// с подпунктом 1 пункта 11 статьи 46 Бюджетного кодекса Российской Федерации, в части
	// обязательного социального страхования на случай временной нетрудоспособности и в связи с материнством
	Коды.Добавить("1161902001");
	
	Возврат Коды;
	
КонецФункции

Функция КодыВидовДоходовВидаНалогаТребующегоУточнения(ВидНалога, УчитыватьНастройки = Ложь, Период = Неопределено)
	
	МассивКодовВидаДоходов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКодовВидаДоходов,
		КодыВидовДоходаНалоговогоАгента(ВидНалога, УчитыватьНастройки));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКодовВидаДоходов,
		КодыВидовДоходаПредпринимателя(ВидНалога, УчитыватьНастройки, Период));
	
	Возврат МассивКодовВидаДоходов;
	
КонецФункции

Функция ЗапросПоВидуНалога(ВидНалога, КодВидаДоходов = "")
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидНалога", ВидНалога);
	Запрос.УстановитьПараметр("КодВидаДоходов", "___" + КодВидаДоходов + "_______");
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Ссылка,
	|	ВидыНалоговИПлатежейВБюджет.КодБК КАК КодБК
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	ВидыНалоговИПлатежейВБюджет.ВидНалога = &ВидНалога
	|	И &УсловиеСоответствияВидуДохода";
	
	Если ЗначениеЗаполнено(КодВидаДоходов) Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоответствияВидуДохода", "ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &КодВидаДоходов");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоответствияВидуДохода", "Истина");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции

// Возвращает вид налога в зависимости от статуса налогоплательщика:
//   если налогоплательщик является ИП, тогда возвращается вид налога по счету учета для ИП.
//
// Параметры:
//   ВидНалогаТекущий      - СправочникСсылка.ВидыНалоговИПлатежейВБюджет - Вид налога текущий
//   ВидНалогаСравниваемый - СправочникСсылка.ВидыНалоговИПлатежейВБюджет - Вид налога для сравнения
//   СтатусПлательщика     - Строка - Статус налогоплательщика
//
// Возвращаемое значение:
//   СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//
Функция НалогПоСтатусуПлательщика(ВидНалогаТекущий, ВидНалогаСравниваемый, СтатусПлательщика)
	
	СчетУчета_ИП = ПланыСчетов.Хозрасчетный.НДФЛ_ИП;
	СчетУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНалогаСравниваемый, "СчетУчета");
	
	Если (СтатусПлательщика = ПлатежиВБюджетКлиентСервер.СтатусПлательщикаНалоговИСборовИП()
		Или СтатусПлательщика = ПлатежиВБюджетКлиентСервер.СтатусНалогоплательщикаФизЛицо())
		И БухгалтерскийУчетПовтИсп.СчетВИерархии(СчетУчета, СчетУчета_ИП) Тогда
		
		Возврат ВидНалогаСравниваемый;
		
	КонецЕсли;
	
	Возврат ВидНалогаТекущий;
	
КонецФункции

Функция ЭтоПересечениеКБКВидаНалогаНДФЛ(ВидНалога, КодБК)
	
	Возврат ВидНалога = Перечисления.ВидыНалогов.НДФЛ_ИП
		И ЭтоНДФЛНалоговыйАгент15(КодБК);
	
КонецФункции

Функция КонтрольныйКодВидаДоходов(КодБК)
	
	// Для случая, если у вида налога сменился код вида дохода
	КодВидаДоходов = "";
	Если (ЭтоНДФЛПредпринимателя15(КодБК, ДатаВторогоКлассификатора())
		Или ЭтоНДФЛПредпринимателя15(КодБК, ДатаАктуальностиКлассификатора())) Тогда
		КодВидаДоходов = КодВидаДоходаНДФЛАгент15();
	КонецЕсли;
	
	Возврат КодВидаДоходов;
	
КонецФункции

Процедура ДополнитьКодыВидаДоходаНДФЛАгент(Коды)
	
	Коды.Добавить(КодВидаДоходаНДФЛАгент13());
	Коды.Добавить(КодВидаДоходаНДФЛАгент15());
	Коды.Добавить(КодВидаДоходаНДФЛАгент18());
	Коды.Добавить(КодВидаДоходаНДФЛАгент20());
	Коды.Добавить(КодВидаДоходаНДФЛАгент22());
	
КонецПроцедуры

Процедура ДополнитьКодыВидаДоходаНДФЛАгентРКиСН(Коды)
	
	Коды.Добавить(КодВидаДоходаНДФЛАгентРКиСН13());
	Коды.Добавить(КодВидаДоходаНДФЛАгентРКиСН15());
	
КонецПроцедуры

Процедура ДополнитьКодыВидаДоходаНДФЛАгентДивиденды(Коды)
	
	Коды.Добавить(КодВидаДоходаНДФЛАгентДивиденды13());
	Коды.Добавить(КодВидаДоходаНДФЛАгентДивиденды15());
	
КонецПроцедуры

#Область ОбработчикиОбновления

// Заполняет реквизиты предопределенных элементов "ПрочиеНалогиИСборы" и "ЕдиныйНалоговыйПлатеж"
// и создает поставляемые элементы справочника в зависимости от включеных функциональных опций
//
Процедура ЗаполнитьПоставляемыеДанные() Экспорт
	
	// Предопределенный элемент заполняем во всех узлах РИБ
	
	// "Прочие налоги и сборы"
	СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы");
	Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
		Объект = СсылкаПредопределенного.ПолучитьОбъект();
	Иначе
		Объект = Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьЭлемент();
		Объект.ИмяПредопределенныхДанных = "ПрочиеНалогиИСборы";
		Объект.УстановитьНовыйКод("0");
	КонецЕсли;
	Объект.ВидНалога    = Перечисления.ВидыНалогов.ПрочиеНалогиИСборы;
	Объект.Наименование = НСтр("ru = 'Прочие налоги и сборы'");
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Ложь);
	
	// "Единый налоговый платеж"
	СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж");
	Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
		Объект = СсылкаПредопределенного.ПолучитьОбъект();
	Иначе
		Объект = Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьЭлемент();
		Объект.ИмяПредопределенныхДанных = "ЕдиныйНалоговыйПлатеж";
		Объект.УстановитьНовыйКод("0");
	КонецЕсли;
	Объект.ВидНалога    = Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж;
	Объект.Наименование = НСтр("ru = 'Единый налоговый платеж'");
	Объект.СчетУчета    = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога);
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Ложь);
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
		
		// Поставляемые элементы создаем в главном узле и регистрируем изменения
		
		ВидыНалогов = Новый Массив;
		КоллекцияМетаданных = Перечисления.ВидыНалогов.Получить(0).Метаданные().ЗначенияПеречисления;
		Для Индекс = 0 По КоллекцияМетаданных.Количество() - 1 Цикл
			ЗначениеПеречисления = Перечисления.ВидыНалогов.Получить(Индекс);
			Если ЗначениеПеречисления <> Перечисления.ВидыНалогов.ПрочиеНалогиИСборы Тогда
				ВидыНалогов.Добавить(ЗначениеПеречисления);
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("ВидНалога", ВидыНалогов));
		ТаблицаПоставляемыхДанных.Индексы.Добавить("ВидНалога");
		ТаблицаПоставляемыхДанных.Индексы.Добавить("КодБК");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Ссылка,
			|	ВидыНалоговИПлатежейВБюджет.ВидНалога КАК ВидНалога,
			|	ВидыНалоговИПлатежейВБюджет.КодБК КАК КодБК,
			|	ВидыНалоговИПлатежейВБюджет.СчетУчета КАК СчетУчета,
			|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления КАК ПометкаУдаления
			|ИЗ
			|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет";
		
		СуществующиеНалоги = Запрос.Выполнить().Выгрузить();
		СуществующиеНалоги.Индексы.Добавить("ВидНалога");
		
		// Обновляем реквизиты существующих налогов, если обновление ИБ выполняется повторно
		
		Для каждого СтрокаДанных Из СуществующиеНалоги Цикл
			
			Если ЗначениеЗаполнено(СтрокаДанных.ВидНалога) Тогда
				
				ВидНалогаТребуетУточненияПоКодуВидаДоходов = ВидНалогаТребуетУточненияПоКодуВидаДоходов(СтрокаДанных.ВидНалога);
				КодВидаДоходовСтрокиДанных                 = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СтрокаДанных.КодБК);
				
				ОтборВидНалога = Новый Структура("ВидНалога", СтрокаДанных.ВидНалога);
				СтрокиДанныхКлассификатора = ТаблицаПоставляемыхДанных.НайтиСтроки(ОтборВидНалога);
				
				Для Каждого СтрокаДанныхКлассификатора Из СтрокиДанныхКлассификатора Цикл
					
					Если ВидНалогаТребуетУточненияПоКодуВидаДоходов Тогда
						КодВидаДоходовСтрокиДанныхКлассификатора = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СтрокаДанныхКлассификатора.КодБК);
						Если КодВидаДоходовСтрокиДанных <> КодВидаДоходовСтрокиДанныхКлассификатора Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					Объект = СтрокаДанных.Ссылка.ПолучитьОбъект();
					ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
					
					Объект.СчетУчета = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога);
					Если ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ДатаАктуальности) Тогда
						Объект.ДатаАктуальности = СтрокаДанныхКлассификатора.ДатаАктуальности;
					Иначе
						Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
					КонецЕсли;
					
					Объект.КодБК = КБКПоВидуНалоговогоОбязательства(
						СтрокаДанныхКлассификатора.КодБК, СтрокаДанныхКлассификатора.ВидНалога,, ДатаАктуальностиКлассификатора());
					
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Истина, Истина);
					
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
		// По возможности ставим на поддержку пользовательские данные
		
		ПользовательскиеДанные = СуществующиеНалоги.НайтиСтроки(Новый Структура("ВидНалога, ПометкаУдаления", Перечисления.ВидыНалогов.ПустаяСсылка(), Ложь));
		Для каждого СтрокаДанных Из ПользовательскиеДанные Цикл
			
			СтрокаДанныхКлассификатора = Неопределено;
			Если НЕ ПустаяСтрока(СтрокаДанных.КодБК) Тогда
				Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(СтрокаДанных.КодБК) = ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
					СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(СтрокаДанных.КодБК, "КодБК");
				ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКНалогиВзносы(СтрокаДанных.КодБК) Тогда
					ШаблонКБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(СтрокаДанных.КодБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(СтрокаДанных.КодБК, Истина);
					СтрокаДанныхКлассификатора = ТаблицаПоставляемыхДанных.Найти(ШаблонКБК, "КодБК");
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаДанныхКлассификатора <> Неопределено
				И ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ВидНалога) Тогда
				
				ВидНалогаТребуетУточненияПоКодуВидаДоходов = ВидНалогаТребуетУточненияПоКодуВидаДоходов(СтрокаДанныхКлассификатора.ВидНалога);
				КодВидаДоходовСтрокиДанныхКлассификатора   = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СтрокаДанныхКлассификатора.КодБК);
			
				СуществующиеНалогиПоВиду = СуществующиеНалоги.НайтиСтроки(Новый Структура("ВидНалога", СтрокаДанныхКлассификатора.ВидНалога));
				
				Для Каждого СуществующийНалог Из СуществующиеНалогиПоВиду Цикл
					
					// Ставим на поддержку только один элемент
					Если ВидНалогаТребуетУточненияПоКодуВидаДоходов(СтрокаДанныхКлассификатора.ВидНалога) Тогда
						КодВидаДоходовСтрокиДанных = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СуществующийНалог.КодБК);
						Если КодВидаДоходовСтрокиДанных <> КодВидаДоходовСтрокиДанныхКлассификатора
							Или НалогПоВиду(СтрокаДанныхКлассификатора.ВидНалога, Ложь, , КодВидаДоходовСтрокиДанныхКлассификатора) <> ПрочиеНалогиИСборы Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					СчетУчета = РасчетыСБюджетом.СчетУчета(СтрокаДанныхКлассификатора.ВидНалога);
					Если ЗначениеЗаполнено(СтрокаДанных.СчетУчета) И СтрокаДанных.СчетУчета <> СчетУчета Тогда
						// В классификаторе есть соответствующая строка, но пользователь решил учитывать данные на другом счете учета
						Продолжить;
					КонецЕсли;
					
					Объект = СтрокаДанных.Ссылка.ПолучитьОбъект();
					ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
					
					Объект.СчетУчета = СчетУчета;
					Если ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ДатаАктуальности) Тогда
						Объект.ДатаАктуальности = СтрокаДанныхКлассификатора.ДатаАктуальности;
					Иначе
						Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
					КонецЕсли;
					
					Объект.КодБК = КБКПоВидуНалоговогоОбязательства(
						СтрокаДанныхКлассификатора.КодБК, СтрокаДанныхКлассификатора.ВидНалога,, ДатаАктуальностиКлассификатора());
					
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Истина, Истина);
					
					// Обновление данных таблицы СуществующиеНалоги для исключения дублирования элементов
					ЗаполнитьЗначенияСвойств(СтрокаДанных, Объект);
					
				КонецЦикла;
			
			КонецЕсли;
			
		КонецЦикла;
		
		// Создаем необходимые налоги
		
		НеобходимыеВидыНалогов = РасчетыСБюджетом.НеобходимыеВидыНалогов();
		
		Для Каждого ВидНалога Из НеобходимыеВидыНалогов Цикл
			
			ОтборВидНалога = Новый Структура("ВидНалога", ВидНалога);
			СуществующиеНалогиПоВиду = СуществующиеНалоги.НайтиСтроки(ОтборВидНалога);
			
			ВидНалогаТребуетУточненияПоКодуВидаДоходов = ВидНалогаТребуетУточненияПоКодуВидаДоходов(ВидНалога);
			Если ВидНалогаТребуетУточненияПоКодуВидаДоходов Тогда
				КодыВидовДоходов = КодыВидовДоходовВидаНалогаТребующегоУточнения(ВидНалога, Истина);
				Для Индекс = 0 По СуществующиеНалогиПоВиду.ВГраница() Цикл
					КодВидаДоходовСтрокиДанных = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СуществующиеНалогиПоВиду[Индекс].КодБК);
					ИндексЭлемента = КодыВидовДоходов.Найти(КодВидаДоходовСтрокиДанных);
					Если ИндексЭлемента <> Неопределено Тогда
						КодыВидовДоходов.Удалить(ИндексЭлемента);
					КонецЕсли;
				КонецЦикла;
				СоздатьНалоги = КодыВидовДоходов.Количество() > 0;
			Иначе
				СоздатьНалоги = СуществующиеНалогиПоВиду.Количество() = 0;
			КонецЕсли;
			
			Если СоздатьНалоги Тогда
			
				СтрокиДанныхКлассификатора = ТаблицаПоставляемыхДанных.НайтиСтроки(ОтборВидНалога);
				
				Для Каждого СтрокаДанныхКлассификатора Из СтрокиДанныхКлассификатора Цикл
					
					Если ВидНалогаТребуетУточненияПоКодуВидаДоходов Тогда
						КодВидаДоходовСтрокиДанныхКлассификатора = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(СтрокаДанныхКлассификатора.КодБК);
						Если НалогПоВиду(СтрокаДанныхКлассификатора.ВидНалога, Ложь, , КодВидаДоходовСтрокиДанныхКлассификатора) <> ПрочиеНалогиИСборы Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					Объект = Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьЭлемент();
					ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
					Объект.УстановитьНовыйКод("0");
					Объект.СчетУчета = РасчетыСБюджетом.СчетУчета(Объект.ВидНалога);
					Если ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ДатаАктуальности) Тогда
						Объект.ДатаАктуальности = СтрокаДанныхКлассификатора.ДатаАктуальности
					Иначе
						Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
					КонецЕсли;
					
					Объект.КодБК = КБКПоВидуНалоговогоОбязательства(
						СтрокаДанныхКлассификатора.КодБК, СтрокаДанныхКлассификатора.ВидНалога,, ДатаАктуальностиКлассификатора());
					
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект, Истина, Истина);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает код для поставляемого элемента вида налога Единый налоговый платеж
//
Процедура УстановитьНовыйКодЕдиныйНалоговыйПлатеж() Экспорт
	
	ВидНалога = Справочники.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж;
	
	Если ЗначениеЗаполнено(ВидНалога) Тогда
		
		Попытка
			ВидНалогаОбъект = ВидНалога.ПолучитьОбъект();
			ВидНалогаОбъект.УстановитьНовыйКод();
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ВидНалогаОбъект);
		Исключение
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,
				Метаданные.Справочники.ВидыНалоговИПлатежейВБюджет, ,
				ОписаниеОшибки());
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// Заменяет в банковских документах 2023 года взнос, с видом налога ФиксированныеВзносы_ПФР_СтраховаяЧасть,
// на взнос, с видом налога ФиксированныеВзносы_СвышеПредела.
//
Процедура ЗаменитьФиксированныеВзносыСверхДохода2023БанкСписание(Параметры) Экспорт
	
	ПрочиеНИС = Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы;
	УстаревшийВзнос = НалогПоВиду(Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть, Ложь);
	НовыйВзнос = НалогПоВиду(Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела, Ложь);
	Если УстаревшийВзнос = ПрочиеНИС Или НовыйВзнос = ПрочиеНИС Тогда
		// Обработка не требуется, т.к. заменяемые взносы отсутствуют в ИБ
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ПлатежиВБюджетКлиентСерверПереопределяемый.ПериодИзмененияСтраховыхВзносов());
	Запрос.УстановитьПараметр("УстаревшийВзнос", УстаревшийВзнос);
	Запрос.УстановитьПараметр("ВидНалоговогоОбязательства", ВидНалоговогоОбязательства);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	СписаниеСРасчетногоСчета.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Дата >= &Период
	|	И СписаниеСРасчетногоСчета.Налог = &УстаревшийВзнос
	|	И СписаниеСРасчетногоСчета.ВидНалоговогоОбязательства = &ВидНалоговогоОбязательства
	|	И НЕ СписаниеСРасчетногоСчета.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
		НачатьТранзакцию();
		Попытка
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.СписаниеСРасчетногоСчета");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.Налог = НовыйВзнос;
			
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обработать документ: %1 по причине:
				|%2'"),
				Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.СписаниеСРасчетногоСчета,
				Выборка.Ссылка,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре Справочники.ВидыНалоговИПлатежейВБюджет.ЗаменитьФиксированныеВзносыСверхДохода2023БанкСписание() не удалось обработать документы:
				|в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.СписаниеСРасчетногоСчета,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Справочники.ВидыНалоговИПлатежейВБюджет.ЗаменитьФиксированныеВзносыСверхДохода2023БанкСписание обработала очередную порцию авансовых отчетов: %1 документов'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

// Заменяет в документах "Операция по единому налоговому счету" и Заявление о зачете в счет предстоящей обязанности
// взнос, с видом налога ФиксированныеВзносы_ПФР_СтраховаяЧасть,
// на взнос, с видом налога ФиксированныеВзносы_СвышеПредела.
//
Процедура ЗаменитьФиксированныеВзносыСверхДохода2023ОперацияПоЕНС(Параметры) Экспорт
	
	ПрочиеНИС = Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы;
	УстаревшийВзнос = НалогПоВиду(Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть, Ложь);
	НовыйВзнос = НалогПоВиду(Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела, Ложь);
	Если УстаревшийВзнос = ПрочиеНИС Или НовыйВзнос = ПрочиеНИС Тогда
		// Обработка не требуется, т.к. заменяемые взносы отсутствуют в ИБ
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
	КБКНовыйВзнос = КБК(НовыйВзнос, ВидНалоговогоОбязательства);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УстаревшийВзнос", УстаревшийВзнос);
	Запрос.УстановитьПараметр("КБКНовыйВзнос", КБКНовыйВзнос);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ОперацияПоЕдиномуНалоговомуСчетуНалоги.Ссылка КАК Ссылка,
	|	ОперацияПоЕдиномуНалоговомуСчетуНалоги.Ссылка.Проведен КАК ДокументПроведен
	|ИЗ
	|	Документ.ОперацияПоЕдиномуНалоговомуСчету.Налоги КАК ОперацияПоЕдиномуНалоговомуСчетуНалоги
	|ГДЕ
	|	ОперацияПоЕдиномуНалоговомуСчетуНалоги.Налог = &УстаревшийВзнос
	|	И ОперацияПоЕдиномуНалоговомуСчетуНалоги.КодБК = &КБКНовыйВзнос
	|	И НЕ ОперацияПоЕдиномуНалоговомуСчетуНалоги.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ЗаявлениеОЗачетеВСчетПредстоящейОбязанностиНалоги.Ссылка,
	|	ЗаявлениеОЗачетеВСчетПредстоящейОбязанностиНалоги.Ссылка.Проведен
	|ИЗ
	|	Документ.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Налоги КАК ЗаявлениеОЗачетеВСчетПредстоящейОбязанностиНалоги
	|ГДЕ
	|	ЗаявлениеОЗачетеВСчетПредстоящейОбязанностиНалоги.Налог = &УстаревшийВзнос
	|	И ЗаявлениеОЗачетеВСчетПредстоящейОбязанностиНалоги.КодБК = &КБКНовыйВзнос
	|	И НЕ ЗаявлениеОЗачетеВСчетПредстоящейОбязанностиНалоги.Ссылка.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
		НачатьТранзакцию();
		Попытка
			Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.ОперацияПоЕдиномуНалоговомуСчету") Тогда
				ВидДокумента = "Документ.ОперацияПоЕдиномуНалоговомуСчету";
				МетаданныеДокумента = Метаданные.Документы.ОперацияПоЕдиномуНалоговомуСчету;
			ИначеЕсли ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
				ВидДокумента = "Документ.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности";
				МетаданныеДокумента = Метаданные.Документы.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности;
			Иначе
				// необрабатываемая ситуация
				Продолжить;
			КонецЕсли;
			
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ВидДокумента);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Для Каждого Налоги Из ДокументОбъект.Налоги Цикл
				Если Налоги.Налог = УстаревшийВзнос Тогда
					Налоги.Налог = НовыйВзнос;
				КонецЕсли;
			КонецЦикла;
			
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			Если Выборка.ДокументПроведен Тогда
				НаборЗаписей = РегистрыНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Ссылка);
				НаборЗаписей.Прочитать();
				Для Каждого Запись Из НаборЗаписей Цикл
					Если Запись.Налог = УстаревшийВзнос Тогда
						Запись.Налог = НовыйВзнос;
					КонецЕсли;
				КонецЦикла;
				
				Если НаборЗаписей.Количество() > 0 Тогда
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				КонецЕсли;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обработать документ: %1 по причине:
				|%2'"),
				Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеДокумента,
				Выборка.Ссылка,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре Справочники.ВидыНалоговИПлатежейВБюджет.ЗаменитьФиксированныеВзносыСверхДохода2023ОперацияПоЕНС() не удалось обработать документы:
				|в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Справочники.ВидыНалоговИПлатежейВБюджет.ЗаменитьФиксированныеВзносыСверхДохода2023ОперацияПоЕНС обработала очередную порцию авансовых отчетов: %1 документов'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

// Заменяет в документах "Операция по ЕНС" вид налога и КБК у страховых взносов до 2023 года.
//
Процедура ЗаменитьКБКСтраховых2023ОперацияПоЕНС(Параметры) Экспорт
	
	ПрочиеНИС = Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы;
	
	ЗаменяемыеВзносы = Новый Соответствие;
	
	СтраховыеВзносы_ПФР = НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_СтраховаяЧасть, Ложь);
	Если СтраховыеВзносы_ПФР <> ПрочиеНИС Тогда
		ЗаменяемыеВзносы.Вставить(СтраховыеВзносы_ПФР, КБК(СтраховыеВзносы_ПФР));
	КонецЕсли;
	
	СтраховыеВзносы_ФФОМС = НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС, Ложь);
	Если СтраховыеВзносы_ФФОМС <> ПрочиеНИС Тогда
		ЗаменяемыеВзносы.Вставить(СтраховыеВзносы_ФФОМС, КБК(СтраховыеВзносы_ФФОМС));
	КонецЕсли;
	
	ФиксированныеВзносы_ФФОМС = НалогПоВиду(Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС, Ложь);
	Если ФиксированныеВзносы_ФФОМС <> ПрочиеНИС Тогда
		ЗаменяемыеВзносы.Вставить(ФиксированныеВзносы_ФФОМС,  КБК(ФиксированныеВзносы_ФФОМС));
	КонецЕсли;
	
	ФиксированныеВзносы_ПФР = НалогПоВиду(Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть, Ложь);
	СвышеПредела = НалогПоВиду(Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела, Ложь);
	Если ФиксированныеВзносы_ПФР <> ПрочиеНИС Тогда
		КБКФиксированныеВзносы_ПФР = КБК(ФиксированныеВзносы_ПФР);
		ЗаменяемыеВзносы.Вставить(ФиксированныеВзносы_ПФР, КБКФиксированныеВзносы_ПФР);
		Если СвышеПредела <> ПрочиеНИС Тогда
			ЗаменяемыеВзносы.Вставить(СвышеПредела, КБКФиксированныеВзносы_ПФР);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаменяемыеВзносы.Количество() = 0 Тогда
		// Обработка не требуется, т.к. обновляемые взносы отсутствуют в ИБ
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Начало2023Года = '20230101';
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Начало2023Года", Начало2023Года);
	ШаблонТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ОперацияПоЕдиномуНалоговомуСчетуНалоги.Ссылка КАК Ссылка,
	|	ОперацияПоЕдиномуНалоговомуСчетуНалоги.Ссылка.Проведен КАК ДокументПроведен
	|ИЗ
	|	Документ.ОперацияПоЕдиномуНалоговомуСчету.Налоги КАК ОперацияПоЕдиномуНалоговомуСчетуНалоги
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ОперацияПоЕдиномуНалоговомуСчетуНалоги.Ссылка.Дата, ГОД) = &Начало2023Года
	|	И НАЧАЛОПЕРИОДА(ОперацияПоЕдиномуНалоговомуСчетуНалоги.СрокУплаты, ГОД) <= &Начало2023Года
	|	И ОперацияПоЕдиномуНалоговомуСчетуНалоги.Налог = &СтраховойВзнос
	|	И ОперацияПоЕдиномуНалоговомуСчетуНалоги.КодБК <> &КБКСтраховогоВзноса
	|	И НЕ ОперацияПоЕдиномуНалоговомуСчетуНалоги.Ссылка.ПометкаУдаления";
	
	Счетчик = 0;
	МассивЗапроса = Новый Массив;
	Для Каждого КлючИЗначение Из ЗаменяемыеВзносы Цикл
		ПараметрСтраховойВзнос = "СтраховойВзнос";
		ПараметрКБКВзноса = "КБКСтраховогоВзноса";
		
		Запрос.УстановитьПараметр(ПараметрСтраховойВзнос + Счетчик, КлючИЗначение.Ключ);
		Запрос.УстановитьПараметр(ПараметрКБКВзноса + Счетчик, КлючИЗначение.Значение);
		
		ТекстЗапроса = СтрЗаменить(ШаблонТекстЗапроса, ПараметрСтраховойВзнос, ПараметрСтраховойВзнос + Счетчик);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПараметрКБКВзноса, ПараметрКБКВзноса + Счетчик);
		
		МассивЗапроса.Добавить(ТекстЗапроса);
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	ТекстОбъединить = "
	|ОБЪЕДИНИТЬ
	|";
	
	Запрос.Текст = СтрСоединить(МассивЗапроса, ТекстОбъединить);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
		НачатьТранзакцию();
		Попытка
			ВидДокумента = "Документ.ОперацияПоЕдиномуНалоговомуСчету";
			МетаданныеДокумента = Метаданные.Документы.ОперацияПоЕдиномуНалоговомуСчету;
			
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ВидДокумента);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Для Каждого Налоги Из ДокументОбъект.Налоги Цикл
				Если ЗаменяемыеВзносы[Налоги.Налог] <> Неопределено Тогда
					Если НачалоГода(Налоги.СрокУплаты) > Начало2023Года Тогда
						Продолжить;
					КонецЕсли;
					
					Если Налоги.Налог = СвышеПредела Тогда
						Налоги.Налог = ФиксированныеВзносы_ПФР;
					КонецЕсли;
					
					Налоги.КодБК = ЗаменяемыеВзносы[Налоги.Налог];
				КонецЕсли;
			КонецЦикла;
			
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			Если Выборка.ДокументПроведен Тогда
				НаборЗаписей = РегистрыНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Ссылка);
				НаборЗаписей.Прочитать();
				Для Каждого Запись Из НаборЗаписей Цикл
					Если ЗаменяемыеВзносы[Запись.Налог] <> Неопределено Тогда
						Если НачалоГода(Запись.СрокУплаты) > Начало2023Года Тогда
							Продолжить;
						КонецЕсли;
						
						Если Запись.Налог = СвышеПредела Тогда
							Запись.Налог = ФиксированныеВзносы_ПФР;
						КонецЕсли;
						
						Запись.КодБК = ЗаменяемыеВзносы[Запись.Налог];
					КонецЕсли;
				КонецЦикла;
				
				Если НаборЗаписей.Количество() > 0 Тогда
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				КонецЕсли;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обработать документ: %1 по причине:
				|%2'"),
				Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеДокумента,
				Выборка.Ссылка,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре Справочники.ВидыНалоговИПлатежейВБюджет.ЗаменитьКБКСтраховых2023ОперацияПоЕНС() не удалось обработать документы:
				|в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			МетаданныеДокумента,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Справочники.ВидыНалоговИПлатежейВБюджет.ЗаменитьКБКСтраховых2023ОперацияПоЕНС() обработала очередную порцию авансовых отчетов: %1 документов'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет реквизит "Вид налога" для всех записей по НДФЛ агента и ИП единым значением
//
Процедура ЗаполнитьВидНалогаНДФЛ() Экспорт
	
	ПарыЗамен = Новый Соответствие();
	ПарыЗамен.Вставить(
		Перечисления.ВидыНалогов.НДФЛ_ДоходыСвышеПредельнойВеличины,
		Перечисления.ВидыНалогов.НДФЛ);
	ПарыЗамен.Вставить(
		Перечисления.ВидыНалогов.НДФЛ_Дивиденды,
		Перечисления.ВидыНалогов.НДФЛ);
	ПарыЗамен.Вставить(
		Перечисления.ВидыНалогов.НДФЛ_Дивиденды_ДоходыСвышеПредельнойВеличины,
		Перечисления.ВидыНалогов.НДФЛ);
	ПарыЗамен.Вставить(
		Перечисления.ВидыНалогов.НДФЛ_ИП_НалоговаяБазаСвышеПредельнойВеличины,
		Перечисления.ВидыНалогов.НДФЛ_ИП);
		
	ПараметрыЗаменыСсылок = ОбщегоНазначения.ПараметрыЗаменыСсылок();
	ПараметрыЗаменыСсылок.ВключатьБизнесЛогику    = Ложь;
	ПараметрыЗаменыСсылок.ПривилегированнаяЗапись = Истина;
	
	Попытка
		ОшибкиЗамены = ОбщегоНазначения.ЗаменитьСсылки(ПарыЗамен, ПараметрыЗаменыСсылок);
		Для Каждого Ошибка Из ОшибкиЗамены Цикл
			// Если не удалось обработать какой-либо объект
			ТекстОшибки = ?(Ошибка.ИнформацияОбОшибке = Неопределено, Ошибка.ТекстОшибки, Ошибка.ИнформацияОбОшибке);
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Обновление информационной базы.
				|Не удалось заменить ссылки на вид налога %1 в объекте: %2 по причине:
				|%3'"), Ошибка.Ссылка, Ошибка.ПредставлениеОбъектаОшибки, ТекстОшибки);
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Перечисления.ВидыНалогов,
				Ошибка.ОбъектОшибки,
				ТекстСообщения);
		КонецЦикла;
	Исключение
		// Если не удалось обработать какой-либо объект
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Обновление информационной базы.
			|Не удалось заменить ссылки на вид налога по причине:
			|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			Перечисления.ВидыНалогов,
			,
			ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

Процедура ИсправитьВидНалогаНДФЛ() Экспорт
	
	ИсправляемыйЭлемент = НалогПоВиду(Перечисления.ВидыНалогов.НДФЛ, Ложь, , "1010218001");
	Если Не ЗначениеЗаполнено(ИсправляемыйЭлемент) Или ИсправляемыйЭлемент = ПрочиеНалогиИСборы Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ВидНалогаОбъект = ИсправляемыйЭлемент.ПолучитьОбъект();
		ВидНалогаОбъект.ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка();
		ВидНалогаОбъект.Наименование = НСтр("ru = 'НДФЛ с подарков, исчисленный налоговым агентом (15%)'");
		ВидНалогаОбъект.НазначениеПлатежа = НСтр("ru = 'Налог на доходы физических лиц <Период>'");
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ВидНалогаОбъект);
	Исключение
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Справочники.ВидыНалоговИПлатежейВБюджет, ,
			ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
