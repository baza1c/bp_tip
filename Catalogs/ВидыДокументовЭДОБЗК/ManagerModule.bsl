#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ДанныеВидыДокументовЭДОБЗКДляПубликации(Ссылки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыДокументовЭДОБЗК.Ссылка КАК Ссылка,
		|	ВидыДокументовЭДОБЗК.Наименование КАК Наименование,
		|	ВидыДокументовЭДОБЗК.ВариантПодписания КАК ВариантПодписания,
		|	ВидыДокументовЭДОБЗК.ТипЭлектроннойПодписи КАК ТипЭлектроннойПодписи
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|ГДЕ
		|	НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа
		|	И ВидыДокументовЭДОБЗК.Ссылка В(&Ссылки)";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Процедура выполняет первоначальное заполнение статей ТК - оснований увольнения.
Процедура НачальноеЗаполнение() Экспорт
	
	ЗагрузитьКлассификатор();
	
КонецПроцедуры

Процедура ЗагрузитьКлассификатор() Экспорт
	Классификатор = ЭлементыКлассификатора();
	ДанныеМакетаКлассификатора = ТаблицаИзМакетаКлассификатора();
	МассивРодителей = Новый Массив();
	Для Каждого СтрокаКлассификатора Из ДанныеМакетаКлассификатора Цикл
		ЭтоНовыйЭлемент = Ложь;
		Если МассивРодителей.Количество() > СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаКлассификатора.Level) Тогда
			МассивРодителей.Удалить(МассивРодителей.Количество() - 1);
		КонецЕсли;
		ЭтоГруппаКлассификатора = ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора);
		Элемент = Неопределено;
		Если ЗначениеЗаполнено(МассивРодителей) Тогда
			Родитель = МассивРодителей[МассивРодителей.Количество() - 1];
		Иначе
			Родитель = Неопределено;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаКлассификатора.Id) Тогда
			Попытка
				Элемент = ПредопределенноеЗначение("Справочник.ВидыДокументовЭДОБЗК." + СтрокаКлассификатора.Id);
			Исключение
				ЭтоНовыйЭлемент = Истина;
				ОбъектСсылки = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектСсылки);
				Элемент = ОбъектСсылки.Ссылка;
			КонецПопытки;
		Иначе
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Наименование", СтрокаКлассификатора.Name);
			СтруктураПоиска.Вставить("ЭтоГруппа", ЭтоГруппаКлассификатора);
			НайденныеСтроки = Классификатор.НайтиСтроки(СтруктураПоиска);
			Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
				ЭтоНовыйЭлемент = Истина;
				ОбъектСсылки = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектСсылки);
				Элемент = ОбъектСсылки.Ссылка;
			Иначе
				Элемент = НайденныеСтроки[0].Ссылка;
			КонецЕсли;
		КонецЕсли;
		Если ЭтоНовыйЭлемент Тогда
			НоваяСтрокаКлассификатора = Классификатор.Добавить();
			НоваяСтрокаКлассификатора.Наименование = СтрокаКлассификатора.Name;
			НоваяСтрокаКлассификатора.ЭтоГруппа = ЭтоГруппаКлассификатора;
			НоваяСтрокаКлассификатора.Ссылка = Элемент;
		КонецЕсли;
		Если ЭтоГруппаКлассификатора Тогда
			МассивРодителей.Добавить(Элемент);
		КонецЕсли;
		УстановитьВидДокументаПечатнымФормам(Элемент, СтрокаКлассификатора.PrintedFormIds);
		УстановитьВидДокументаШаблонуДокумента(Элемент, СтрокаКлассификатора.TemplateIds);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьВидДокументаПечатнымФормам(ВидДокумента, ИдентификаторыПечатныхФорм)
	Если Не ЗначениеЗаполнено(ИдентификаторыПечатныхФорм) Тогда
		Возврат;
	КонецЕсли;
	Идентификаторы = СтрРазделить(ИдентификаторыПечатныхФорм, ", ", Ложь);
	Для Каждого ИдентификаторПечатнойФормы Из Идентификаторы Цикл
		ИдентификаторПечатнойФормы = СокрЛП(ИдентификаторПечатнойФормы);
		НаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторПечатнойФормы.Установить(ИдентификаторПечатнойФормы);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Если Не ЗначениеЗаполнено(НаборЗаписей[0].ВидДокумента) Тогда
				НаборЗаписей[0].ВидДокумента = ВидДокумента;
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьВидДокументаШаблонуДокумента(ВидДокумента, ИдентификаторыШаблонов)
	Если Не ЗначениеЗаполнено(ИдентификаторыШаблонов) Тогда
		Возврат;
	КонецЕсли;
	Идентификаторы = СтрРазделить(ИдентификаторыШаблонов, ", ", Ложь);
	Для Каждого Идентификатор Из Идентификаторы Цикл
		Попытка
			ШаблонДокументаСсылка = ПредопределенноеЗначение("Справочник.ШаблоныДокументов." + Идентификатор);
		Исключение
			Продолжить;;
		КонецПопытки;
		ШаблонДокументаОбъект = ШаблонДокументаСсылка.ПолучитьОбъект();
		Если Не ЗначениеЗаполнено(ШаблонДокументаОбъект.ВидДокумента) Тогда
			ШаблонДокументаОбъект.ВидДокумента = ВидДокумента;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ШаблонДокументаОбъект);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПредопределенныйВидДокумента(ИмяПредопределенныхДанных) Экспорт
	
	ПредопределенныйЭлемент = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыДокументовЭДОБЗК." + ИмяПредопределенныхДанных);
	Если ПредопределенныйЭлемент <> Неопределено Тогда
		Возврат ПредопределенныйЭлемент;
	КонецЕсли;
	
	Если Метаданные.Справочники.ВидыДокументовЭДОБЗК.ПолучитьИменаПредопределенных().Найти(ИмяПредопределенныхДанных) Тогда
		Попытка
			ПредопределенныйЭлемент = ПредопределенноеЗначение("Справочник.ВидыДокументовЭДОБЗК." + ИмяПредопределенныхДанных);
		Исключение
			ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
			ОбъектСсылки.Заполнить(Неопределено);
			ОбъектСсылки.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных;
			ОбъектСсылки.Наименование = ИмяПредопределенныхДанных;
			ОбъектСсылки.Записать();
			ПредопределенныйЭлемент = ОбъектСсылки.Ссылка;
		КонецПопытки;
	КонецЕсли;
	
	Возврат ПредопределенныйЭлемент;
	
КонецФункции

Функция ВидДокументаПоПричинеОтсутствия(ПричинаОтсутствия) Экспорт
	Если ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Опоздание Тогда
		Возврат ПредопределенныйВидДокумента("СообщениеОбОпоздании");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Болезнь Тогда
		Возврат ПредопределенныйВидДокумента("СообщениеОБолезни");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпуск");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаУчебныйОтпуск");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпускЗаСвойСчет");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпускПоБеременностиИРодам");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоУходуЗаРебенком Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпускПоУходуЗаРебенком");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отгул Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтгул");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаКомандировку");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела Тогда
		Возврат ПредопределенныйВидДокумента("СообщениеОбОтсутствии");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаДниУходаЗаДетьмиИнвалидами");
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ВидПоКодуКадровогоДокумент(КодКадровогоДокумента) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодКадровогоДокумента", КодКадровогоДокумента);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВидыДокументовЭДОБЗК.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|ГДЕ
		|	ВидыДокументовЭДОБЗК.КодКадровогоДокумента = &КодКадровогоДокумента
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыДокументовЭДОБЗК.Ссылка,
		|	ВидыДокументовЭДОБЗК.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Если ДобавитьВидыКадровыхДокументовПоКодам(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодКадровогоДокумента)) > 0 Тогда
		Возврат ВидПоКодуКадровогоДокумент(КодКадровогоДокумента);
	КонецЕсли;
	
	Возврат ПредопределенныйВидДокумента("ПрочиеКЭДО");
	
КонецФункции

Функция ДобавитьВидыКадровыхДокументовПоКодам(КодыКадровыхДокументов) Экспорт
	
	ДобавленныеРодители = Новый Соответствие;
	ДобавленныеЭлементы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодыКадровыхДокументов", КодыКадровыхДокументов);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КодыДокументовКадровыхМероприятий.Ссылка КАК КодКадровогоДокумента,
		|	КодыДокументовКадровыхМероприятий.НаименованиеПолное КАК Наименование,
		|	КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное КАК НаименованиеРодителя,
		|	ЕСТЬNULL(ВидыДокументовЭДОБЗКРодители.Ссылка, НЕОПРЕДЕЛЕНО) КАК Родитель
		|ИЗ
		|	Справочник.КодыДокументовКадровыхМероприятий КАК КодыДокументовКадровыхМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|		ПО (НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа)
		|			И КодыДокументовКадровыхМероприятий.Ссылка = ВидыДокументовЭДОБЗК.КодКадровогоДокумента
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.НаименованиеПолное КАК СТРОКА(150))) = ВидыДокументовЭДОБЗК.Наименование)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗКРодители
		|		ПО (ВидыДокументовЭДОБЗКРодители.ЭтоГруппа)
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное КАК СТРОКА(150))) = (ВЫРАЗИТЬ(ВидыДокументовЭДОБЗКРодители.Наименование КАК СТРОКА(150))))
		|ГДЕ
		|	КодыДокументовКадровыхМероприятий.Ссылка В(&КодыКадровыхДокументов)
		|	И НЕ КодыДокументовКадровыхМероприятий.ЭтоГруппа
		|	И ВидыДокументовЭДОБЗК.Ссылка ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	КодыДокументовКадровыхМероприятий.Ссылка,
		|	КодыДокументовКадровыхМероприятий.НаименованиеПолное,
		|	КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное,
		|	ЕСТЬNULL(ВидыДокументовЭДОБЗКРодители.Ссылка, НЕОПРЕДЕЛЕНО)
		|ИЗ
		|	Справочник.КодыДокументовКадровыхМероприятий КАК КодыДокументовКадровыхМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|		ПО (НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа)
		|			И КодыДокументовКадровыхМероприятий.Ссылка = ВидыДокументовЭДОБЗК.КодКадровогоДокумента
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.НаименованиеПолное КАК СТРОКА(150))) = ВидыДокументовЭДОБЗК.Наименование)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗКРодители
		|		ПО (ВидыДокументовЭДОБЗКРодители.ЭтоГруппа)
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное КАК СТРОКА(150))) = ВидыДокументовЭДОБЗКРодители.Наименование)
		|ГДЕ
		|	КодыДокументовКадровыхМероприятий.Родитель В(&КодыКадровыхДокументов)
		|	И НЕ КодыДокументовКадровыхМероприятий.ЭтоГруппа
		|	И ВидыДокументовЭДОБЗК.Ссылка ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ДобавленныеЭлементы.Найти(Выборка.КодКадровогоДокумента) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Родитель = Выборка.Родитель;
		Если Не ЗначениеЗаполнено(Родитель) Тогда
			
			Если ЗначениеЗаполнено(Выборка.НаименованиеРодителя) Тогда
				Родитель = ДобавленныеРодители.Получить(Выборка.НаименованиеРодителя);
				Если Родитель = Неопределено Тогда
					РодительОбъект = Справочники.ВидыДокументовЭДОБЗК.СоздатьГруппу();
					РодительОбъект.Наименование = Выборка.НаименованиеРодителя;
					РодительОбъект.Записать();
					Родитель = РодительОбъект.Ссылка;
					ДобавленныеРодители.Вставить(Выборка.НаименованиеРодителя, Родитель);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		ЭлементОбъект = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
		ЭлементОбъект.Заполнить(Неопределено);
		ЭлементОбъект.Родитель = Родитель;
		ЭлементОбъект.Наименование = Выборка.Наименование;
		ЭлементОбъект.ДокументСвязанныйСРаботой = Истина;
		ЭлементОбъект.КодКадровогоДокумента = Выборка.КодКадровогоДокумента;
		ЭлементОбъект.Записать();
		
		ДобавленныеЭлементы.Добавить(Выборка.КодКадровогоДокумента);
		
	КонецЦикла;
	
	Возврат ДобавленныеЭлементы.Количество();
	
КонецФункции

Функция ВидПоИдентификаторуШаблона(ИдентификаторШаблона) Экспорт
	Возврат ВидПоИдентификаторуОбъекта(ИдентификаторШаблона, "TemplateIds")
КонецФункции

Функция ВидПоИдентификаторуПечатнойФормы(ИдентификаторПечатнойФормы) Экспорт
	Возврат ВидПоИдентификаторуОбъекта(ИдентификаторПечатнойФормы, "PrintedFormIds")
КонецФункции

Функция ВидПоИдентификаторуОбъекта(ИдентификаторОбъекта, ПолеИдентификатора)

	ДанныеМакетаКлассификатора = ТаблицаИзМакетаКлассификатора();
	МассивРодителей = Новый Массив();
	Для Каждого СтрокаКлассификатора Из ДанныеМакетаКлассификатора Цикл
		Если МассивРодителей.Количество() > СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаКлассификатора.Level) Тогда
			МассивРодителей.Удалить(МассивРодителей.Количество() - 1);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаКлассификатора[ПолеИдентификатора]) Тогда
			Идентификаторы = СтрРазделить(СтрокаКлассификатора[ПолеИдентификатора], ", ", Ложь);
			Если Идентификаторы.Найти(ИдентификаторОбъекта) <> Неопределено Тогда
				Родитель = РодительПоСтрокамИдентификатора(МассивРодителей);
				Возврат ЭлементКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Родитель);
			КонецЕсли;
		КонецЕсли;
		Если ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора) Тогда
			МассивРодителей.Добавить(СтрокаКлассификатора);
		КонецЕсли;
	КонецЦикла;
	Возврат ПредопределенныйВидДокумента("Прочие");
КонецФункции

Функция ЭлементКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Родитель)
	Элемент = Неопределено;
	Если ЗначениеЗаполнено(СтрокаКлассификатора.Id) Тогда
		Попытка
			Элемент = ПредопределенноеЗначение("Справочник.ВидыДокументовЭДОБЗК." + СтрокаКлассификатора.Id);
		Исключение
			Элемент = Неопределено
		КонецПопытки;
	Иначе
		Классификатор = ЭлементыКлассификатора();
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Наименование", СтрокаКлассификатора.Name);
		СтруктураПоиска.Вставить("ЭтоГруппа", Ложь);
		НайденныеСтроки = Классификатор.НайтиСтроки(СтруктураПоиска);
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Элемент = НайденныеСтроки[0].Ссылка;
		КонецЕсли;
	КонецЕсли;
	Если Элемент = Неопределено Тогда
		ЭлементОбъект = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
		ЭлементОбъект.Записать();
		Элемент = ЭлементОбъект.Ссылка;
	КонецЕсли;
	Возврат Элемент;
КонецФункции

Функция ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель)
	
	ЭтоГруппаКлассификатора = ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора);
	Если ЗначениеЗаполнено(СтрокаКлассификатора.Id) Тогда
		ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
		ОбъектСсылки.ИмяПредопределенныхДанных = СтрокаКлассификатора.Id;
	Иначе
		Если ЭтоГруппаКлассификатора Тогда
			ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьГруппу();
		Иначе
			ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
		КонецЕсли;
	КонецЕсли;
	ОбъектСсылки.Заполнить(Неопределено);
	ОбъектСсылки.Наименование = СтрокаКлассификатора.Name;
	ОбъектСсылки.Родитель = Родитель;
	Если Не ЭтоГруппаКлассификатора Тогда
		Если ЗначениеЗаполнено(СтрокаКлассификатора.Code) Тогда
			ОбъектСсылки.КодКадровогоДокумента = ЭДОБЗКПовтИсп.СсылкаНаКодДокументаКадровогоМероприятия(СтрокаКлассификатора.Code);
		Иначе
			ОбъектСсылки.КодКадровогоДокумента = Неопределено;
		КонецЕсли;
		ОбъектСсылки.ДокументСвязанныйСРаботой = ЗначениеЗаполнено(ОбъектСсылки.КодКадровогоДокумента);
	КонецЕсли;
	Возврат ОбъектСсылки;
	
КонецФункции

Функция РодительПоСтрокамИдентификатора(МассивРодителей);
	Родитель = Неопределено;
	Если ЗначениеЗаполнено(МассивРодителей) Тогда
		Классификатор = ЭлементыКлассификатора();
		Родитель = ГруппаКлассификатораПоДаннымСтрокиМакета(МассивРодителей[МассивРодителей.Количество() - 1], Классификатор);
		Если Родитель = Неопределено Тогда
			Для Каждого СтрокаКлассификатора Из МассивРодителей Цикл
				Родитель = ГруппаКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Классификатор);
				Если Родитель = Неопределено Тогда
					ЭлементОбъект = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
					ЭлементОбъект.Записать();
					Родитель = ЭлементОбъект.Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Возврат Родитель;
КонецФункции

Функция ГруппаКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Классификатор)
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Наименование", СтрокаКлассификатора.Name);
	СтруктураПоиска.Вставить("ЭтоГруппа", Истина);
	НайденныеСтроки = Классификатор.НайтиСтроки(СтруктураПоиска);
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Возврат НайденныеСтроки[0].Ссылка;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ЭлементыКлассификатора()
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДанных.Ссылка КАК Ссылка,
		|	ТаблицаДанных.Наименование КАК Наименование,
		|	ТаблицаДанных.ЭтоГруппа КАК ЭтоГруппа,
		|	ТаблицаДанных.ДокументСвязанныйСРаботой КАК ДокументСвязанныйСРаботой,
		|	ТаблицаДанных.КодКадровогоДокумента КАК КодКадровогоДокумента
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ТаблицаДанных
		|ГДЕ
		|	НЕ ТаблицаДанных.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ТаблицаИзМакетаКлассификатора()
	Возврат ОбщегоНазначения.ПрочитатьXMLВТаблицу(
		ПолучитьМакет("Классификатор").ПолучитьТекст()).Данные;
КонецФункции

Функция ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора)
	Возврат СтрокаКлассификатора.IsGroup = "True";
КонецФункции

Функция КлючВариантаОтчета(ИдентификаторСсылки)
	Возврат "ВидыДокументовЭДОБЗК" + ИдентификаторСсылки;
КонецФункции

Функция НастройкиНовогоВариантаОтчета(НаименованиеДокумента = "")
	
	СхемаКомпоновки = Отчеты.ОтчетыПоСотрудникам.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = СхемаКомпоновки.ВариантыНастроек["ШаблонВариантаДокумента"].Настройки;
	
	ПараметрСхемы = СхемаКомпоновки.Параметры.Добавить();
	ПараметрСхемы.Имя = "НаименованиеДокумента";
	ПараметрСхемы.Заголовок = НСтр("ru = 'Наименование документа'");
	ПараметрСхемы.ТипЗначения = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(500));
	ПараметрСхемы.Значение = НаименованиеДокумента;
	ПараметрСхемы.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	
	ПараметрСхемы = СхемаКомпоновки.Параметры.Добавить();
	ПараметрСхемы.Имя = "НомерДокумента";
	ПараметрСхемы.Заголовок = НСтр("ru = 'Номер документа'");
	ПараметрСхемы.ТипЗначения = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(15));
	ПараметрСхемы.Значение = "";
	ПараметрСхемы.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	
	ОбновитьВычисляемыеПоля(СхемаКомпоновки, Настройки);
	
	ПараметрДата = Настройки.ПараметрыДанных.Элементы.Добавить();
	ПараметрДата.Параметр = Новый ПараметрКомпоновкиДанных("НомерДокумента");
	ПараметрДата.Использование = Истина;
	
	ВыбранноеПоле = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ПараметрыДанных.НаименованиеДокумента");
	
	ВыбранноеПоле = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ПараметрыДанных.НомерДокумента");
	
	НастройкиВариантаОтчета = ЗарплатаКадрыОтчеты.НоваяСтруктураНастроекПечатнойФормы();
	НастройкиВариантаОтчета.Настройки = Настройки;
	НастройкиВариантаОтчета.СхемаКомпоновкиДанных = СхемаКомпоновки;
	
	Возврат НастройкиВариантаОтчета
	
КонецФункции

Функция ОбновитьНастройкиВариантаОтчета(ВариантОтчета) Экспорт
	
	РезультатОбновления = Ложь;
	Если ЗначениеЗаполнено(ВариантОтчета) Тогда
		
		ДанныеВарианта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВариантОтчета, "Отчет,КлючВарианта");
		НастройкиПечатнойФормы = ЗарплатаКадрыОтчеты.НастройкиПечатнойФормы(ДанныеВарианта.Отчет, ДанныеВарианта.КлючВарианта);
		
		РезультатОбновления = ОбновитьВычисляемыеПоля(НастройкиПечатнойФормы.СхемаКомпоновкиДанных, НастройкиПечатнойФормы.Настройки);
		
		Если РезультатОбновления Тогда
			ЗарплатаКадрыОтчеты.ЗапомнитьНастройкиВариантаОтчета(ВариантОтчета, НастройкиПечатнойФормы);
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОбновления;
	
КонецФункции

Функция ОбновитьВычисляемыеПоля(СхемаКомпоновки, Настройки)
	
	РезультатОбновления = Ложь;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Организация.Руководитель.ФИОПолные",
		НСтр("ru = 'Руководитель, ФИО'"));
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Организация.Руководитель.ФамилияИО",
		НСтр("ru = 'Руководитель, Фамилия и инициалы'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Организация.Руководитель.РасшифровкаПодписи",
		НСтр("ru = 'Руководитель, расшифровка подписи'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Организация.Руководитель.ФИОПолныеРодительный",
		НСтр("ru = 'Руководитель, ФИО (родительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ФИОПолныеДательный",
		НСтр("ru = 'Руководитель, ФИО (дательный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ФИОПолныеВинительный",
		НСтр("ru = 'Руководитель, ФИО (винительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ФИОПолныеТворительный",
		НСтр("ru = 'Руководитель, ФИО (творительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ФИОПолныеПредложный",
		НСтр("ru = 'Руководитель, ФИО (предложный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.Должность",
		НСтр("ru = 'Руководитель, должность'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Организация.Руководитель.ДолжностьРодительный",
		НСтр("ru = 'Руководитель, должность (родительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ДолжностьДательный",
		НСтр("ru = 'Руководитель, должность (дательный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ДолжностьВинительный",
		НСтр("ru = 'Руководитель, должность (винительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ДолжностьТворительный",
		НСтр("ru = 'Руководитель, должность (творительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ДолжностьПредложный",
		НСтр("ru = 'Руководитель, должность (предложный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Организация.Руководитель.ОснованиеПодписи",
		НСтр("ru = 'Руководитель, основание подписи'")) Или РезультатОбновления;
	
	// Подразделение
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Подразделение.Руководитель.ФИОПолные",
		НСтр("ru = 'Руководитель, ФИО'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Подразделение.Руководитель.ФамилияИО",
		НСтр("ru = 'Руководитель, Фамилия и инициалы'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Подразделение.Руководитель.РасшифровкаПодписи",
		НСтр("ru = 'Руководитель, расшифровка подписи'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Подразделение.Руководитель.ФИОПолныеРодительный",
		НСтр("ru = 'Руководитель, ФИО (родительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ФИОПолныеДательный",
		НСтр("ru = 'Руководитель, ФИО (дательный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ФИОПолныеВинительный",
		НСтр("ru = 'Руководитель, ФИО (винительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ФИОПолныеТворительный",
		НСтр("ru = 'Руководитель, ФИО (творительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ФИОПолныеПредложный",
		НСтр("ru = 'Руководитель, ФИО (предложный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.Должность",
		НСтр("ru = 'Руководитель, должность'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки,
		"РабочееМесто.Подразделение.Руководитель.ДолжностьРодительный",
		НСтр("ru = 'Руководитель, должность (родительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ДолжностьДательный",
		НСтр("ru = 'Руководитель, должность (дательный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ДолжностьВинительный",
		НСтр("ru = 'Руководитель, должность (винительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ДолжностьТворительный",
		НСтр("ru = 'Руководитель, должность (творительный)'")) Или РезультатОбновления;
	
	РезультатОбновления = ДобавитьВычисляемоеПоле(СхемаКомпоновки, ,
		"РабочееМесто.Подразделение.Руководитель.ДолжностьПредложный",
		НСтр("ru = 'Руководитель, должность (предложный)'")) Или РезультатОбновления;
	
	Возврат РезультатОбновления;
	
КонецФункции

Функция НовыйВариантОтчета(ВидДокумента)
	
	ИдентификаторСсылки = ВидДокумента.УникальныйИдентификатор();
	
	ИдентификаторОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Отчеты.ОтчетыПоСотрудникам);
	
	КлючВариантаОтчета = КлючВариантаОтчета(ИдентификаторСсылки);
	
	НастройкиВариантаОтчета = НастройкиНовогоВариантаОтчета(Строка(ВидДокумента));

	ПредопределенныйВариант = Неопределено;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", ИдентификаторОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", "ШаблонВариантаДокумента");
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПредопределенныеВариантыОтчетов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПредопределенныеВариантыОтчетов КАК ПредопределенныеВариантыОтчетов
		|ГДЕ
		|	ПредопределенныеВариантыОтчетов.Отчет = &Отчет
		|	И ПредопределенныеВариантыОтчетов.КлючВарианта = &КлючВарианта
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПредопределенныеВариантыОтчетов.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПредопределенныйВариант = Выборка.Ссылка;
	КонецЕсли;
	
	НовыйВариантОтчета = Справочники.ВариантыОтчетов.СоздатьЭлемент();
	НовыйВариантОтчета.Отчет = ИдентификаторОтчета;
	НовыйВариантОтчета.КлючВарианта = КлючВариантаОтчета;
	НовыйВариантОтчета.Наименование = ВидДокумента;
	НовыйВариантОтчета.Описание = НСтр("ru = 'Содержит настройки для подготовки шаблона вида документа в 1С:Кабинет сотрудника (для служебного использования)'");
	НовыйВариантОтчета.ТолькоДляАвтора = Истина;
	НовыйВариантОтчета.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний;
	НовыйВариантОтчета.ПредопределенныйВариант = ПредопределенныйВариант;
	НовыйВариантОтчета.Назначение = Перечисления.НазначенияВариантовОтчетов.ДляКомпьютеровИПланшетов;
	УстановитьПривилегированныйРежим(Истина);
	НовыйВариантОтчета.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗарплатаКадрыОтчеты.ЗапомнитьНастройкиВариантаОтчета(НовыйВариантОтчета.Ссылка, НастройкиВариантаОтчета);
	
	Возврат НовыйВариантОтчета.Ссылка;
	
КонецФункции

Функция ДобавитьВычисляемоеПоле(СхемаКомпоновки, Настройки = Неопределено, ПутьКДанным, Заголовок)
	
	РезультатДобавления = Ложь;
	
	Если СхемаКомпоновки.ВычисляемыеПоля.Найти(ПутьКДанным) = Неопределено Тогда
		РезультатДобавления = Истина;
		ВычисляемоеПоле = СхемаКомпоновки.ВычисляемыеПоля.Добавить();
		ВычисляемоеПоле.Выражение = "";
		ВычисляемоеПоле.ПутьКДанным = ПутьКДанным;
		ВычисляемоеПоле.Заголовок = Заголовок;
	КонецЕсли;
	
	Если Настройки <> Неопределено Тогда
		ПолеВыбрано = Ложь;
		Для Каждого ЭлементНастройки Из Настройки.Выбор.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ВыбранноеПолеКомпоновкиДанных")
				И ЭлементНастройки.Поле = Новый ПолеКомпоновкиДанных(ПутьКДанным) Тогда
				
				ПолеВыбрано = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не ПолеВыбрано Тогда
			РезультатДобавления = Истина;
			ВыбранноеПоле = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(ПутьКДанным);
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатДобавления;

КонецФункции

Функция ВариантОтчетаВидаДокумента(ВидДокумента)
	
	КлючВариантаОтчета = КлючВариантаОтчета(ВидДокумента.УникальныйИдентификатор());
	ИдентификаторОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Отчеты.ОтчетыПоСотрудникам);
	
	Возврат ВариантыОтчетов.ВариантОтчета(ИдентификаторОтчета, КлючВариантаОтчета);
	
КонецФункции

Функция ВариантОтчетаМакетаВидаДокумента(ВидДокумента, СоздатьЕслиОтсутствует) Экспорт
	
	ВариантОтчета = ВариантОтчетаВидаДокумента(ВидДокумента);
	Если СоздатьЕслиОтсутствует И Не ЗначениеЗаполнено(ВариантОтчета) Тогда
		ВариантОтчета = НовыйВариантОтчета(ВидДокумента);
	КонецЕсли;
	
	Возврат ВариантОтчета;
	
КонецФункции

Функция ВариантОтчетаМакетаВидаДокументаДляРедактора(ВидДокумента) Экспорт
	
	ВариантОтчета = ВариантОтчетаВидаДокумента(ВидДокумента);
	Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
		ВариантОтчета = НовыйВариантОтчета(ВидДокумента);
	Иначе
		ОбновитьНастройкиВариантаОтчета(ВариантОтчета);
	КонецЕсли;
	
	Возврат ВариантОтчета;
	
КонецФункции

Функция СоответствиеПадежей()
	
	СоответствиеПадежей = Новый Соответствие;
	
	СоответствиеПадежей.Вставить(1, "Именительный");
	СоответствиеПадежей.Вставить(2, "Родительный");
	СоответствиеПадежей.Вставить(3, "Дательный");
	СоответствиеПадежей.Вставить(4, "Винительный");
	СоответствиеПадежей.Вставить(5, "Творительный");
	СоответствиеПадежей.Вставить(6, "Предложный");
	
	Возврат СоответствиеПадежей;
	
КонецФункции

Функция СклоненияФИО(ФИО, ФизическоеЛицо, Пол)
	
	ПолЧислом = ?(Пол = Перечисления.ПолФизическогоЛица.Мужской, 1, 2);
	
	Склонения = Новый Структура;
	СоответствиеПадежей = СоответствиеПадежей();
	Для каждого КлючиЗначение Из СоответствиеПадежей Цикл
		Склонения.Вставить(КлючиЗначение.Значение, СклонениеПредставленийОбъектов.ПросклонятьФИО(ФИО, КлючиЗначение.Ключ,, ПолЧислом));
	КонецЦикла;
	
	Возврат Склонения;
	
КонецФункции

Функция СклоненияПредставленияОбъекта(Представление, Ссылка)
	
	Склонения = Новый Структура;
	СоответствиеПадежей = СоответствиеПадежей();
	Для каждого КлючиЗначение Из СоответствиеПадежей Цикл
		Склонения.Вставить(КлючиЗначение.Значение, СклонениеПредставленийОбъектов.ПросклонятьПредставление(Представление, КлючиЗначение.Ключ));
	КонецЦикла;
	
	Возврат Склонения;
	
КонецФункции

Функция ОбразецДокумента(ВидДокумента, Сотрудник) Экспорт
	ТабличныеДокументыСотрудниковОрганизаций = СформироватьДокументы(
		ВидДокумента,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник),
		ВариантОтчетаМакетаВидаДокумента(ВидДокумента, Истина),
		"",
		ТекущаяДатаСеанса());
	Для Каждого ТабличныеДокументыСотрудниковОрганизации Из ТабличныеДокументыСотрудниковОрганизаций Цикл
		Для Каждого ТабличныеДокументыСотрудников Из ТабличныеДокументыСотрудниковОрганизаций Цикл
			Для Каждого ТабличныйДокументСотрудника Из ТабличныеДокументыСотрудников.Значение Цикл
				Образец = ТабличныйДокументСотрудника.Значение;
				Образец.ОтображатьЗаголовки = Ложь;
				Образец.ОтображатьСетку = Ложь;
				Образец.Защита = Истина;
				Образец.ТолькоПросмотр = Истина;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	Возврат Образец;
КонецФункции

Функция СформироватьДокументы(ВидДокумента, ВыбранныеСотрудники, ВариантОтчета, НомерДокумента, ДатаДокумента) Экспорт
	
	Результаты = Новый Соответствие;
	
	ДанныеВарианта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВариантОтчета, "Отчет,КлючВарианта");
	
	НастройкиПечатнойФормы = ЗарплатаКадрыОтчеты.НастройкиПечатнойФормы(ДанныеВарианта.Отчет, ДанныеВарианта.КлючВарианта);
	
	МакетДокумента = НастройкиПечатнойФормы.Макет;
	Если МакетДокумента = Неопределено Тогда
		МакетДокумента = УправлениеПечатью.МакетПечатнойФормы("Справочник.ВидыДокументовЭДОБЗК.ПФ_MXL_ШаблонВидаДокументаЭДО");
	КонецЕсли;
	
	ДополнительныеПоляПредставлений = КадровыйУчет.ПоляПредставленийКадровыхДанныхСотрудников();
	СоответствиеДополнительныхПолейПредставлениям = Новый Структура;
	СоответствиеДополнительныхПолейПредставлениям.Вставить("Представления_КадровыеДанныеСотрудников", ДополнительныеПоляПредставлений);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(
		Новый Структура("СхемаКомпоновкиДанных", НастройкиПечатнойФормы.СхемаКомпоновкиДанных), СоответствиеДополнительныхПолейПредставлениям);
	
	РеквизитыВидаДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокумента, "НаСписокСотрудников,СодержимоеДокумента");
	
	ПараметрНастроек = НастройкиПечатнойФормы.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НомерДокумента"));
	Если ПараметрНастроек <> Неопределено Тогда
		Если ЗначениеЗаполнено(НомерДокумента) Тогда
			ПараметрНастроек.Значение = СтрШаблон(" №%1", НомерДокумента);
		Иначе
			ПараметрНастроек.Значение = "";
		КонецЕсли;
	КонецЕсли;
	
	ПараметрНастроек = НастройкиПечатнойФормы.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ПараметрНастроек <> Неопределено Тогда
		ПараметрНастроек.Значение = ДатаДокумента;
	КонецЕсли;
	
	ЭлементОтбора = НастройкиПечатнойФормы.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РабочееМесто.Сотрудник");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	
	Если РеквизитыВидаДокументов.НаСписокСотрудников Тогда
		ЭлементОтбора.ПравоеЗначение = ВыбранныеСотрудники[0];
	Иначе
		ЭлементОтбора.ПравоеЗначение = ВыбранныеСотрудники;
	КонецЕсли;
	
	КомпоновщикНастроекКД = Новый КомпоновщикНастроекКомпоновкиДанных;
	ЗарплатаКадрыОтчеты.ИнициализироватьИЗагрузитьНастройкиВКомпоновщикКД(КомпоновщикНастроекКД, НастройкиПечатнойФормы);
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(
		НастройкиПечатнойФормы.СхемаКомпоновкиДанных, КомпоновщикНастроекКД.ПолучитьНастройки());
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТаблицаРезультата = Новый ДеревоЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультата);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ИмяПоляПодразделение = "РабочееМестоПодразделение";
	Подразделения = Новый Массив;
	Для Каждого СтрокаОрганизации Из ТаблицаРезультата.Строки Цикл
		Подразделение = Неопределено;
		Если Не ИнтеграцияУправлениеПерсоналом.ПубликоватьСтруктуруЮридическихЛиц() Тогда
			ИмяПоляПодразделение = "РабочееМестоМестоВСтруктуреПредприятия";
		КонецЕсли;
		
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаОрганизации, ИмяПоляПодразделение) Тогда
			Прервать;
		КонецЕсли;
		Для Каждого СтрокаСотрудника Из СтрокаОрганизации.Строки Цикл
			Если Подразделения.Найти(СтрокаСотрудника[ИмяПоляПодразделение]) = Неопределено Тогда
				Подразделения.Добавить(СтрокаСотрудника[ИмяПоляПодразделение]);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ДополнительныеПараметрыЗаполненияРуководителейПодразделений = Новый Соответствие;
	Если ЗначениеЗаполнено(Подразделения) Тогда
		ДополнительныеПараметрыЗаполненияРуководителейПодразделений =
			ДанныеРуководителейПодразделений(Подразделения, ДатаДокумента);
	КонецЕсли;
	
	Для Каждого СтрокаОрганизации Из ТаблицаРезультата.Строки Цикл
		
		Организация = СтрокаОрганизации.РабочееМестоОрганизация;
		ДополнительныеПараметрыЗаполнения = Новый Структура;
		
		ОтветственныеЛица = ЗарплатаКадры.ОтветственныеЛицаОрганизации(
			СтрокаОрганизации.РабочееМестоОрганизация, "Руководитель,ДолжностьРуководителя,ОснованиеПодписиРуководителя", СтрокаОрганизации.ПараметрыДанныхПериод);
		
		Если ЗначениеЗаполнено(ОтветственныеЛица.Руководитель) Тогда
			ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководитель", ОтветственныеЛица.Руководитель);
			КадровыеДанныеРуководителя = КадровыйУчет.КадровыеДанныеФизическогоЛица(Истина, ОтветственныеЛица.Руководитель,
				"ФИОПолные,ФамилияИО,ИОФамилия,Пол", СтрокаОрганизации.ПараметрыДанныхПериод);
			Если КадровыеДанныеРуководителя <> НЕопределено Тогда
				ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководительФИОПолные", КадровыеДанныеРуководителя.ФИОПолные);
				ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководительФамилияИО", КадровыеДанныеРуководителя.ФамилияИО);
				ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководительРасшифровкаПодписи", КадровыеДанныеРуководителя.ИОФамилия);
				СклоненияРуководителя = СклоненияФИО(КадровыеДанныеРуководителя.ФИОПолные, ОтветственныеЛица.Руководитель, КадровыеДанныеРуководителя.Пол);
				Для Каждого СклонениеФИО Из СклоненияРуководителя Цикл
					Если СклонениеФИО.Ключ = "Именительный" Тогда
						Продолжить;
					КонецЕсли;
					ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководительФИОПолные" + СклонениеФИО.Ключ, СклонениеФИО.Значение);
				КонецЦикла;
			КонецЕсли;
			Если ЗначениеЗаполнено(ОтветственныеЛица.ОснованиеПодписиРуководителя) Тогда
				ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководительОснованиеПодписи", ОтветственныеЛица.ОснованиеПодписиРуководителя);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтветственныеЛица.ДолжностьРуководителя) Тогда
			ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководительДолжность", ОтветственныеЛица.ДолжностьРуководителя);
			СклоненияОбъекта = СклоненияПредставленияОбъекта(Строка(ОтветственныеЛица.ДолжностьРуководителя), ОтветственныеЛица.ДолжностьРуководителя);
			Для Каждого СклонениеОбъекта Из СклоненияОбъекта Цикл
				Если СклонениеОбъекта.Ключ = "Именительный" Тогда
					Продолжить;
				КонецЕсли;
				ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоОрганизацияРуководительДолжность" + СклонениеОбъекта.Ключ, СклонениеОбъекта.Значение);
			КонецЦикла;
		КонецЕсли;
		
		ДокументыОрганизации = Новый Соответствие;
		Результаты.Вставить(Организация, ДокументыОрганизации);
		Для Каждого СтрокаСотрудника Из СтрокаОрганизации.Строки Цикл
			ДополнительныеПараметрыЗаполнения.Вставить("ПараметрыДанныхПериод", Формат(СтрокаСотрудника.ПараметрыДанныхПериод, "ДЛФ=D; ДП=' '"));
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаСотрудника, "РабочееМестоДатаДоговораКонтракта") Тогда
				ДополнительныеПараметрыЗаполнения.Вставить("РабочееМестоДатаДоговораКонтракта", Формат(СтрокаСотрудника.РабочееМестоДатаДоговораКонтракта, "ДЛФ=D; ДП=' '"));
			КонецЕсли;
			ДокументРезультат = Новый ТабличныйДокумент;
			Область = МакетДокумента.ПолучитьОбласть(1,1, МакетДокумента.ВысотаТаблицы, МакетДокумента.ШиринаТаблицы);
			Область.Параметры.Заполнить(СтрокаСотрудника);
			Область.Параметры.Заполнить(ДополнительныеПараметрыЗаполнения);
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаСотрудника, ИмяПоляПодразделение)
				И ЗначениеЗаполнено(СтрокаСотрудника[ИмяПоляПодразделение]) Тогда
				
				ДополнительныеПараметрыЗаполненияПодразделения =
					ДополнительныеПараметрыЗаполненияРуководителейПодразделений.Получить(СтрокаСотрудника[ИмяПоляПодразделение]);
				Если ДополнительныеПараметрыЗаполненияПодразделения <> Неопределено Тогда
					Область.Параметры.Заполнить(ДополнительныеПараметрыЗаполненияПодразделения);
				КонецЕсли;
			КонецЕсли;
			ДокументРезультат.Вывести(Область);
			ДанныеДокумента = Новый Структура;
			ДанныеДокумента.Вставить("НаименованиеДокумента", СтрокаСотрудника.ПараметрыДанныхНаименованиеДокумента);
			ДанныеДокумента.Вставить("НомерДокумента", СтрокаСотрудника.ПараметрыДанныхНомерДокумента);
			ДанныеДокумента.Вставить("ДатаДокумента", СтрокаСотрудника.ПараметрыДанныхПериод);
			ДанныеДокумента.Вставить("Организация", СтрокаСотрудника.РабочееМестоОрганизация);
			ДанныеДокумента.Вставить("НаСписокСотрудников", РеквизитыВидаДокументов.НаСписокСотрудников);
			Если РеквизитыВидаДокументов.НаСписокСотрудников Тогда
				ДанныеДокумента.Вставить("ФизическоеЛицо", КадровыйУчет.ФизическиеЛицаСотрудников(ВыбранныеСотрудники));
			Иначе
				ДанныеДокумента.Вставить("ФизическоеЛицо", СтрокаСотрудника.ЛичныеДанныеФизическоеЛицо);
			КонецЕсли;
			ДанныеДокумента.Вставить("СодержимоеДокумента", РеквизитыВидаДокументов.СодержимоеДокумента);
			ДанныеДокумента.Вставить("ФИОФамилияИИнициалы", СтрокаСотрудника.ЛичныеДанныеФИОФамилияИИнициалы);
			ДокументыОрганизации.Вставить(ДанныеДокумента, ДокументРезультат);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результаты;
	
КонецФункции

Процедура ЗаполнитьЗначенияЗаполненияДокументов(ПараметрыОбновления) Экспорт
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ВидыДокументовЭДОБЗК.Ссылка КАК Ссылка,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументЭДОБЗК.Ссылка) КАК КоличествоДокументов,
		|	ДокументЭДОБЗК.УдалитьВариантПодписания КАК ВариантПодписания,
		|	ДокументЭДОБЗК.СодержимоеДокумента КАК СодержимоеДокумента
		|ПОМЕСТИТЬ ВТСтатистика
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|		ПО ДокументЭДОБЗК.ВидДокумента = ВидыДокументовЭДОБЗК.Ссылка
		|ГДЕ
		|	НЕ ВидыДокументовЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа
		|	И (ВидыДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка)
		|			ИЛИ ВидыДокументовЭДОБЗК.СодержимоеДокумента = ЗНАЧЕНИЕ(Перечисление.СодержимоеДокументов.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыДокументовЭДОБЗК.Ссылка,
		|	ДокументЭДОБЗК.УдалитьВариантПодписания,
		|	ДокументЭДОБЗК.СодержимоеДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Статистика.Ссылка КАК Ссылка,
		|	МАКСИМУМ(Статистика.КоличествоДокументов) КАК КоличествоДокументов
		|ПОМЕСТИТЬ ВТМаксимальныеКоличествоСочетаний
		|ИЗ
		|	ВТСтатистика КАК Статистика
		|
		|СГРУППИРОВАТЬ ПО
		|	Статистика.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Статистика.Ссылка КАК Ссылка,
		|	МАКСИМУМ(Статистика.ВариантПодписания) КАК ВариантПодписания,
		|	МАКСИМУМ(Статистика.СодержимоеДокумента) КАК СодержимоеДокумента
		|ИЗ
		|	ВТСтатистика КАК Статистика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМаксимальныеКоличествоСочетаний КАК МаксимальныеКоличествоСочетаний
		|		ПО Статистика.Ссылка = МаксимальныеКоличествоСочетаний.Ссылка
		|			И Статистика.КоличествоДокументов = МаксимальныеКоличествоСочетаний.КоличествоДокументов
		|
		|СГРУППИРОВАТЬ ПО
		|	Статистика.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "Справочник.ВидыДокументовЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			
			Продолжить;
		КонецЕсли;
		
		ОбъектСправочника = Выборка.Ссылка.ПолучитьОбъект();
		
		Если Не ЗначениеЗаполнено(ОбъектСправочника.ВариантПодписания) Тогда
			ОбъектСправочника.ВариантПодписания = Выборка.ВариантПодписания;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ОбъектСправочника.СодержимоеДокумента) Тогда
			ОбъектСправочника.СодержимоеДокумента = Выборка.СодержимоеДокумента;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектСправочника);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВариантПодписанияИТипПодписи(ПараметрыОбновления) Экспорт
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ВидыДокументовЭДОБЗК.Ссылка КАК Ссылка,
		|	ВидыДокументовЭДОБЗК.ВариантПодписания КАК ВариантПодписания
		|ПОМЕСТИТЬ ВТВидыДокументов
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|ГДЕ
		|	НЕ ВидыДокументовЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И ВидыДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка)
		|	И НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВидыДокументовЭДОБЗК.Ссылка,
		|	ВидыДокументовЭДОБЗК.ВариантПодписания
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|ГДЕ
		|	НЕ ВидыДокументовЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И ВидыДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.Требуется)
		|	И ВидыДокументовЭДОБЗК.ТипЭлектроннойПодписи = ЗНАЧЕНИЕ(Перечисление.ТипыЭлектронныхПодписейЭДОБЗК.ПустаяСсылка)
		|	И НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыДокументов.Ссылка КАК Ссылка,
		|	НастройкиПечатныхФормБЗК.УдалитьВариантПодписания КАК ВариантПодписания
		|ПОМЕСТИТЬ ВТВариантыПодписанияПечатныхФорм
		|ИЗ
		|	ВТВидыДокументов КАК ВидыДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПечатныхФормБЗК КАК НастройкиПечатныхФормБЗК
		|		ПО ВидыДокументов.Ссылка = НастройкиПечатныхФормБЗК.ВидДокумента
		|			И (НастройкиПечатныхФормБЗК.УдалитьВариантПодписания <> ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка))
		|ГДЕ
		|	ВидыДокументов.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыДокументов.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ДокументЭДОБЗК.УдалитьВариантПодписания) КАК ВариантПодписания
		|ПОМЕСТИТЬ ВТВариантыПодписанияДокументовЭДОБЗК
		|ИЗ
		|	ВТВидыДокументов КАК ВидыДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВариантыПодписанияПечатныхФорм КАК ВариантыПодписанияПечатныхФорм
		|		ПО ВидыДокументов.Ссылка = ВариантыПодписанияПечатныхФорм.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ПО ВидыДокументов.Ссылка = ДокументЭДОБЗК.ВидДокумента
		|			И (ДокументЭДОБЗК.УдалитьВариантПодписания <> ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка))
		|			И (ДокументЭДОБЗК.Дата В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(Документ.ДокументЭДОБЗК.Дата)
		|				ИЗ
		|					Документ.ДокументЭДОБЗК
		|				ГДЕ
		|					ДокументЭДОБЗК.ВидДокумента = ВидыДокументов.Ссылка))
		|ГДЕ
		|	ВариантыПодписанияПечатныхФорм.Ссылка ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыДокументов.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыДокументов.Ссылка КАК Ссылка,
		|	ЕСТЬNULL(ВариантыПодписанияПечатныхФорм.ВариантПодписания, ЕСТЬNULL(ВариантыПодписанияДокументовЭДОБЗК.ВариантПодписания, ВидыДокументов.ВариантПодписания)) КАК ВариантПодписания
		|ИЗ
		|	ВТВидыДокументов КАК ВидыДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВариантыПодписанияПечатныхФорм КАК ВариантыПодписанияПечатныхФорм
		|		ПО ВидыДокументов.Ссылка = ВариантыПодписанияПечатныхФорм.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВариантыПодписанияДокументовЭДОБЗК КАК ВариантыПодписанияДокументовЭДОБЗК
		|		ПО ВидыДокументов.Ссылка = ВариантыПодписанияДокументовЭДОБЗК.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "Справочник.ВидыДокументовЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			
			Продолжить;
		КонецЕсли;
		
		ОбъектСправочника = Выборка.Ссылка.ПолучитьОбъект();
		
		Если Не ЗначениеЗаполнено(ОбъектСправочника.ВариантПодписания) Тогда
			Если ЗначениеЗаполнено(Выборка.ВариантПодписания) Тогда
				ОбъектСправочника.ВариантПодписания = Выборка.ВариантПодписания;
			Иначе
				ОбъектСправочника.ВариантПодписания = Перечисления.ВариантыПодписанияДокументовЭДОБЗК.Требуется;
			КонецЕсли;
		КонецЕсли;
		
		Если ОбъектСправочника.ВариантПодписания = Перечисления.ВариантыПодписанияДокументовЭДОБЗК.Требуется Тогда
			ОбъектСправочника.ТипЭлектроннойПодписи = Перечисления.ТипыЭлектронныхПодписейЭДОБЗК.УНЭП;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектСправочника);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеРуководителейПодразделений(Подразделения, ДатаСведений)
	
	ДанныеРуководителей = Новый Соответствие;
	
	СведенияРукводителей = ЭДОБЗК.ДанныеРукводителейПодразделений(Подразделения, ДатаСведений);
	
	Руководители = ОбщегоНазначения.ВыгрузитьКолонку(СведенияРукводителей.ПодразделенияРуководители, "Значение", Истина);
	КадровыеДанныеРуководителей = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, Руководители, "ФИОПолные,ФамилияИО,ИОФамилия,Пол", ДатаСведений);
	
	Для Каждого Подразделение Из Подразделения Цикл
		Руководитель = СведенияРукводителей.ПодразделенияРуководители.Получить(Подразделение);
		Если ЗначениеЗаполнено(Руководитель) Тогда
			
			ДанныеРуководителя = КадровыеДанныеРуководителей.Найти(Руководитель, "ФизическоеЛицо");
			
			Если ДанныеРуководителя <> Неопределено Тогда
				ДополнительныеПараметрыЗаполненияПодразделения = Новый Структура;
				ДополнительныеПараметрыЗаполненияПодразделения.Вставить("РабочееМестоПодразделениеРуководитель", ДанныеРуководителя.ФизическоеЛицо);
				ДополнительныеПараметрыЗаполненияПодразделения.Вставить("РабочееМестоПодразделениеРуководительФИОПолные", ДанныеРуководителя.ФИОПолные);
				ДополнительныеПараметрыЗаполненияПодразделения.Вставить("РабочееМестоПодразделениеРуководительФамилияИО", ДанныеРуководителя.ФамилияИО);
				ДополнительныеПараметрыЗаполненияПодразделения.Вставить("РабочееМестоПодразделениеРуководительРасшифровкаПодписи", ДанныеРуководителя.ИОФамилия);
				СклоненияРуководителя = СклоненияФИО(ДанныеРуководителя.ФИОПолные, ДанныеРуководителя.ФизическоеЛицо, ДанныеРуководителя.Пол);
				Для Каждого СклонениеФИО Из СклоненияРуководителя Цикл
					Если СклонениеФИО.Ключ = "Именительный" Тогда
						Продолжить;
					КонецЕсли;
					ДополнительныеПараметрыЗаполненияПодразделения.Вставить("РабочееМестоПодразделениеРуководительФИОПолные" + СклонениеФИО.Ключ, СклонениеФИО.Значение);
				КонецЦикла;
			КонецЕсли;
			
			ДолжностьРуководителя = СведенияРукводителей.ДанныеРуководителей.Получить(Подразделение);
			Если ДолжностьРуководителя <> Неопределено Тогда
				ДополнительныеПараметрыЗаполненияПодразделения.Вставить("РабочееМестоПодразделениеРуководительДолжность", ДолжностьРуководителя);
				СклоненияОбъекта = СклоненияПредставленияОбъекта(Строка(ДолжностьРуководителя), ДолжностьРуководителя);
				Для Каждого СклонениеОбъекта Из СклоненияОбъекта Цикл
					Если СклонениеОбъекта.Ключ = "Именительный" Тогда
						Продолжить;
					КонецЕсли;
					ДополнительныеПараметрыЗаполненияПодразделения.Вставить("РабочееМестоПодразделениеРуководительДолжность" + СклонениеОбъекта.Ключ, СклонениеОбъекта.Значение);
				КонецЦикла;
			КонецЕсли;
			ДанныеРуководителей.Вставить(Подразделение, ДополнительныеПараметрыЗаполненияПодразделения);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеРуководителей
	
КонецФункции

#КонецОбласти

#КонецЕсли