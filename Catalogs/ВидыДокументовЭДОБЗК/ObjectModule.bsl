#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	МассивНеПроверяемыхРеквизитов = Новый Массив;
	Если Не ДокументСвязанныйСРаботой Тогда
		МассивНеПроверяемыхРеквизитов.Добавить("КодКадровогоДокумента");
	КонецЕсли;
	
	Если Не ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("3.3")
		Или ВариантПодписания <> Перечисления.ВариантыПодписанияДокументовЭДОБЗК.Требуется Тогда
		
		МассивНеПроверяемыхРеквизитов.Добавить("ТипЭлектроннойПодписи");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивНеПроверяемыхРеквизитов) Тогда
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНеПроверяемыхРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		Если ВариантПодписания <> Перечисления.ВариантыПодписанияДокументовЭДОБЗК.Требуется Тогда
			ТипЭлектроннойПодписи = Перечисления.ТипыЭлектронныхПодписейЭДОБЗК.ПустаяСсылка();
		ИначеЕсли Не ЗначениеЗаполнено(ТипЭлектроннойПодписи) Тогда
			ТипЭлектроннойПодписи = Перечисления.ТипыЭлектронныхПодписейЭДОБЗК.УНЭП;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли