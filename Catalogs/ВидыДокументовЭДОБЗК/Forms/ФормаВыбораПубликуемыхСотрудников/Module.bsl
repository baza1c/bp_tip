#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭДОБЗК.УстановитьЗапросДинамическогоСпискаФормыВыбораПубликуемыхСотрудников(ЭтотОбъект);
	
	ДатаДокумента = Параметры.ДатаДокумента;
	Если Не ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	РанееВыбранные = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.РанееВыбранные) Тогда
		РанееВыбранные = ПолучитьИзВременногоХранилища(Параметры.РанееВыбранные);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Сотрудники, "РанееВыбранные", РанееВыбранные, Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Сотрудники, "ДатаДокумента", ДатаДокумента, Истина);
	
	ВидДокумента = Параметры.ВидДокумента;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Сотрудники, "ВидДокумента", ВидДокумента, Истина);
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Сотрудники, "Организация", Параметры.Организация);
		Заголовок = СтрШаблон(НСтр("ru = 'Сотрудники %1, имеющие кабинеты'"), Параметры.Организация);
	Иначе
		ЗаполняемыеЗначения = Новый Структура("Организация");
		ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗаполняемыеЗначения, ТекущаяДатаСеанса());
		Если Не ЗначениеЗаполнено(ЗаполняемыеЗначения.Организация) Тогда
			ЗаполняемыеЗначения.Организация = ЗарплатаКадры.ПерваяДоступнаяОрганизация();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Сотрудники, "Организация", ЗаполняемыеЗначения.Организация, ВидСравненияКомпоновкиДанных.Равно, ,
			ЗначениеЗаполнено(ЗаполняемыеЗначения.Организация), РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Сотрудники, "Подразделение", , ВидСравненияКомпоновкиДанных.ВИерархии, ,
		Ложь, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Сотрудники, "ДолжностьПоШтатномуРасписанию", , ВидСравненияКомпоновкиДанных.ВСписке, ,
		Ложь, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	ДатаПоследнегоДокумента = ДобавитьМесяц(ДатаДокумента, -1);
	УстановитьДатуПоследнегоДокумента(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаПоследнегоДокумента",
		"Заголовок",
		СтрШаблон(НСтр("ru = 'Не получавшие ""%1"" с'"), ВидДокумента));
		
	Если Параметры.Свойство("МножественныйВыбор")
		И Параметры.МножественныйВыбор = Ложь Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Сотрудники",
			"РежимВыделения",
			РежимВыделенияТаблицы.Одиночный);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СотрудникиВыбратьВсех",
			"Видимость",
			Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПоследнегоДокументаПриИзменении(Элемент)
	
	УстановитьДатуПоследнегоДокумента(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыИмяСотрудники

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Сотрудники.ТекущаяСтрока <> Неопределено Тогда
		ВыделенныеСотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Элементы.Сотрудники.ДанныеСтроки(Элементы.Сотрудники.ТекущаяСтрока).Сотрудник);
		Закрыть(ВсеДанныеСотрудников(ВыделенныеСотрудники))
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ЗначениеЗаполнено(Элементы.Сотрудники.ВыделенныеСтроки) Тогда
		ВыделенныеСотрудники = Новый Массив;
		Для Каждого КлючДанных Из Элементы.Сотрудники.ВыделенныеСтроки Цикл
			ВыделенныеСотрудники.Добавить(Элементы.Сотрудники.ДанныеСтроки(КлючДанных).Сотрудник);
		КонецЦикла;
		Закрыть(ВсеДанныеСотрудников(ВыделенныеСотрудники));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсех(Команда)
	Закрыть(ВсеДанныеСотрудников());
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВсеДанныеСотрудников(ВыбранныеСотрудники = Неопределено)
	
	ВсеДанные = Новый Массив;
	СхемаСписка = Элементы.Сотрудники.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	НастройкиСхемы = Элементы.Сотрудники.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	Если ВыбранныеСотрудники <> Неопределено Тогда
		ЭлементОтбора = НастройкиСхемы.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Сотрудник");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ПравоеЗначение = ВыбранныеСотрудники;
	КонецЕсли;
	ЭлементОтбора = НастройкиСхемы.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РанееВыбран");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Ложь;
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаСписка, НастройкиСхемы, , , Ложь);
	
	ДанныеСписка = Новый ТаблицаЗначений;
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеСписка);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Для Каждого СтрокаДанныхСписка Из ДанныеСписка Цикл
		ДанныеСотрудника = Новый Структура("Сотрудник,Организация,Подразделение,Должность");
		ЗаполнитьЗначенияСвойств(ДанныеСотрудника, СтрокаДанныхСписка);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаДанныхСписка, "Филиал") Тогда
			ДанныеСотрудника.Организация = СтрокаДанныхСписка.Филиал;
		КонецЕсли;
		ВсеДанные.Добавить(ДанныеСотрудника);
	КонецЦикла;
	
	Возврат ВсеДанные;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДатуПоследнегоДокумента(УправляемаяФорма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		УправляемаяФорма.Сотрудники, "ДатаПоследнегоДокумента", УправляемаяФорма.ДатаПоследнегоДокумента, Истина);
	
КонецПроцедуры

#КонецОбласти
