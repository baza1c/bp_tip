#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПриПолученииДанныхНаСервере();
	КонецЕсли;
	
	УстановитьОтображениеПодсказкиКодаДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Объект.Предопределенный Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОсновныеДанныеГруппа",
			"ТолькоПросмотр",
			Истина);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаПодготовитьМакет",
		"Доступность",
		ПравоДоступа("Изменение", Метаданные.Справочники.ВидыДокументовЭДОБЗК));
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументСвязанныйСРаботойПриИзменении(Элемент)
	УстановитьОтображениеЭлементовФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КодКадровогоДокументаПриИзменении(Элемент)
	
	УстановитьОтображениеПодсказкиКодаДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПодписанияПриИзменении(Элемент)
	Если Объект.ВариантПодписания = ПредопределенноеЗначение("Перечисление.ВариантыПодписанияДокументовЭДОБЗК.Требуется")
		И Не ЗначениеЗаполнено(Объект.ТипЭлектроннойПодписи) Тогда
		
		Объект.ТипЭлектроннойПодписи = ПредопределенноеЗначение("Перечисление.ТипыЭлектронныхПодписейЭДОБЗК.УНЭП");
	КонецЕсли;
	УстановитьОтображениеЭлементовФормы(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодготовитьМакет(Команда)
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Данные вида документов ЭДО еще не записаны.
			|Записать и продолжить?'");
		Оповещение = Новый ОписаниеОповещения("ОбработатьОтветОНеобходимостиЗаписиДляПодготовкиМакета", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ОбработатьОтветОНеобходимостиЗаписиДляПодготовкиМакета(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветОНеобходимостиЗаписиДляПодготовкиМакета(Ответ, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Если (Параметры.Ключ.Пустая() Или Модифицированность) И Не Записать() Тогда
			Возврат;
		КонецЕсли;
		
		ОткрытьФормуПодготовкиМакета();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРассылку(Команда)
	
	Если Не ЗначениеЗаполнено(ВариантОтчетаМакетаВидаДокумента(Объект.Ссылка)) Тогда
		ТекстПредупреждения = НСтр("ru = 'Не подготовлен макет. Формирование образца невозможно.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая()
		Или Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Перед выполнением рассылки, объект необходимо записать.
			|Продолжить?'");
		Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаОНеобходимостиЗаписи", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		ОткрытьФормуВыполненияРассылки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаОНеобходимостиЗаписи(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если Записать() Тогда
			ОткрытьФормуВыполненияРассылки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОбразец(Команда)
	
	Если Не ЗначениеЗаполнено(ВариантОтчетаМакетаВидаДокумента(Объект.Ссылка)) Тогда
		ТекстПредупреждения = НСтр("ru = 'Не подготовлен макет. Формирование образца невозможно.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Для подготовки образца необходимо выбрать сотрудника.'");
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("ВыбратьСотрудника", НСтр("ru = 'Выбрать сотрудника'"));
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Отмена);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОВыбореСотрудников", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, КнопкиВопроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОВыбореСотрудников(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = "ВыбратьСотрудника" Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ДатаДокумента", ОбщегоНазначенияКлиент.ДатаСеанса());
		ПараметрыОткрытия.Вставить("ВидДокумента", Объект.Ссылка);
		ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
		
		Оповещение = Новый ОписаниеОповещения("ОбработатьВыборСотрудника", ЭтотОбъект);
		ОткрытьФорму("Справочник.ВидыДокументовЭДОБЗК.Форма.ФормаВыбораПубликуемыхСотрудников",ПараметрыОткрытия, ЭтотОбъект, Ложь, , , Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборСотрудника(ВыбранныеСотрудники, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(ВыбранныеСотрудники) Тогда
		ОбразецДокументаВида = ОбразецДокументаНаСервере(Объект.Ссылка, ВыбранныеСотрудники[0].Сотрудник);
		Если ОбразецДокументаВида <> Неопределено Тогда
			ОбразецДокументаВида.Показать(СтрШаблон(НСтр("ru = 'Образец документа: %1'"), Объект.Ссылка));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеЭлементовФормы(УправляемаяФорма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"КодКадровогоДокумента",
		"Доступность",
		УправляемаяФорма.Объект.ДокументСвязанныйСРаботой);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ТипЭлектроннойПодписи",
		"Видимость",
		УправляемаяФорма.ИспользуетсяВерсияDTO_3_3
			И УправляемаяФорма.Объект.ВариантПодписания = ПредопределенноеЗначение("Перечисление.ВариантыПодписанияДокументовЭДОБЗК.Требуется"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ВариантПодписания",
		"АвтоМаксимальнаяШирина",
		Не УправляемаяФорма.ИспользуетсяВерсияDTO_3_3);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеПодсказкиКодаДокумента()
	
	НаименованиеКода = "";
	Если Объект.ДокументСвязанныйСРаботой Тогда
		СвойстваКода = ЭДОБЗКПовтИсп.СвойстваКодаКадровогоДокумента(
			Объект.КодКадровогоДокумента);
		Если СвойстваКода <> Неопределено Тогда
			НаименованиеКода = СвойстваКода.НаименованиеПолное;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КодКадровогоДокумента",
		"Подсказка",
		НаименованиеКода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыполненияРассылки()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыОткрытия.Вставить("ВидДокумента", Объект.Ссылка);
	
	ОткрытьФорму("Справочник.ВидыДокументовЭДОБЗК.Форма.ФормаРассылкиДокументов", ПараметрыОткрытия,
		ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВариантОтчетаМакетаВидаДокумента(ВидДокумента)
	Возврат Справочники.ВидыДокументовЭДОБЗК.ВариантОтчетаМакетаВидаДокументаДляРедактора(ВидДокумента);
КонецФункции

&НаСервереБезКонтекста
Функция ВариантОтчетаМакетаВидаДокументаДляРедактора(ВариантОтчета)
	Возврат Справочники.ВидыДокументовЭДОБЗК.ВариантОтчетаМакетаВидаДокументаДляРедактора(ВариантОтчета);
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуПодготовкиМакета()
	
	Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
		ВариантОтчета = ВариантОтчетаМакетаВидаДокументаДляРедактора(Объект.Ссылка);
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыОткрытия.Вставить("Заголовок", Объект.Наименование);
	ПараметрыОткрытия.Вставить("ВариантОтчета", ВариантОтчета);
	ПараметрыОткрытия.Вставить("НеОтображатьКомандуПредварительныйПросмотр", Истина);
	ПараметрыОткрытия.Вставить("ПутьКМакетуПечатнойФормы", "Справочник.ВидыДокументовЭДОБЗК.ПФ_MXL_ШаблонВидаДокументаЭДО");
	
	ДополнительныеПараметры = Новый Структура("ВариантОтчета", ВариантОтчета);
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуПодготовкиМакетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.НастраиваемыеПечатныеФормыЗарплатаКадры.Форма.ФормаНастройкиПечатнойФормы",
		ПараметрыОткрытия, ЭтотОбъект, Истина, , , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодготовкиМакетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено
		И Результат.Результат Тогда
		
		Объект.ПодготовленМакетДокумента = Истина;
		Записать();
		ОбновитьМакетДокументаНастроекВариантовОтчета(ДополнительныеПараметры.ВариантОтчета, Результат.МакетПечатнойФормы)
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьМакетДокументаНастроекВариантовОтчета(ВариантОтчета, МакетДокумента)
	ЗарплатаКадрыОтчеты.ОбновитьМакетДокументаНастроекВариантовОтчета(ВариантОтчета, МакетДокумента);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбразецДокументаНаСервере(ВидДокумента, Сотрудник)
	Возврат Справочники.ВидыДокументовЭДОБЗК.ОбразецДокумента(ВидДокумента, Сотрудник);
КонецФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ИспользуетсяВерсияDTO_3_3 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("3.3");
	УстановитьОтображениеЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
