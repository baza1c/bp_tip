#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаДокумента = ТекущаяДатаСеанса();
	ВидДокумента = Параметры.ВидДокумента;
	
	Если Параметры.Свойство("Заголовок") Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	Иначе
		Заголовок = Строка(ВидДокумента);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВидДокумента",
		"Видимость",
		Не ЗначениеЗаполнено(ВидДокумента));
	
	ЗначенияДляЗаполнения  = Новый Структура("Организация", "Организация");
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗарплатаКадры.ПерваяДоступнаяОрганизация();
	КонецЕсли;
	
	ВидДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветОЗакрытииБезСохранения(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	ВидДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Сотрудники.Очистить();
	УстановитьОтображениеЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыИмяСотрудники

&НаКлиенте
Процедура СотрудникиПриИзменении(Элемент)
	УстановитьОтображениеЭлементовФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если Элементы.Сотрудники.ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(Элементы.Сотрудники.ТекущиеДанные.Сотрудник) Тогда
		
			ДанныеСотрудника = КадровыйУчетВызовСервера.КадровыеДанныеСотрудника(
				Элементы.Сотрудники.ТекущиеДанные.Сотрудник, "Организация,Подразделение,Должность,ИмеетКабинетСотрудника", ОбщегоНазначенияКлиент.ДатаСеанса());
			Если ДанныеСотрудника <> Неопределено Тогда
				Элементы.Сотрудники.ТекущиеДанные.Организация = ДанныеСотрудника.Организация;
				Элементы.Сотрудники.ТекущиеДанные.Подразделение = ДанныеСотрудника.Подразделение;
				Элементы.Сотрудники.ТекущиеДанные.Должность = ДанныеСотрудника.Должность;
				Если Не ДанныеСотрудника.ИмеетКабинетСотрудника Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = '%1 не имеет кабинета сотрудника'"), ДанныеСотрудника.Сотрудник);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщения, , "Сотрудник", "Сотрудники");
				КонецЕсли;
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле Сотрудник обязательно для заполнения'"), , "СотрудникиСотрудник", , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ДатаДокумента", ДатаДокумента);
	ПараметрыОткрытия.Вставить("РанееВыбранные", АдресСпискаРанееВыбранныхСотрудников());
	ПараметрыОткрытия.Вставить("ВидДокумента", ВидДокумента);
	ПараметрыОткрытия.Вставить("Организация", Организация);
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораСотрудников", ЭтотОбъект);
	ОткрытьФорму("Справочник.ВидыДокументовЭДОБЗК.Форма.ФормаВыбораПубликуемыхСотрудников", ПараметрыОткрытия, ЭтотОбъект,
		Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСотрудников(ВыбранныеСотрудники, ДополнительныеПараметры) Экспорт
	Если ВыбранныеСотрудники <> Неопределено Тогда
		ОбработкаВыбораСотрудниковНаСервере(ВыбранныеСотрудники);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОбразец(Команда)
	
	Если Элементы.Сотрудники.ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для подготовки образца необходимо выбрать сотрудника'"));
	Иначе
		ОбразецДокументаВида = ОбразецДокументаНаСервере(ВидДокумента, Элементы.Сотрудники.ТекущиеДанные.Сотрудник);
		Если ОбразецДокументаВида <> Неопределено Тогда
			ОбразецДокумента = ОбразецДокументаВида;
			ОбразецДокумента.Показать(СтрШаблон(НСтр("ru = 'Образец документа: %1'"), ВидДокумента));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)
	
	СформироватьИПередатьДокументыНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументы(Команда)
	
	СформироватьИПередатьДокументыНаКлиенте(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьИПередатьДокументыНаКлиенте(ПодписатьИОтправить)
	
	ЭлектронныеДокументы = ПодготовитьДокументыНаСервере(ПодписатьИОтправить);
	Если ЗначениеЗаполнено(ЭлектронныеДокументы) Тогда
		Если ПодписатьИОтправить Тогда
			Если ТребуетсяПодписьОрганизации Тогда
				ДанныеФайловНаПодпись = ЭДОБЗКВызовСервера.ДанныеФайловНаПодпись(ЭлектронныеДокументы, УникальныйИдентификатор);
				Оповещение = Новый ОписаниеОповещения("ПослеПодписанияДокументов", ЭтотОбъект,
					Новый Структура("КоличествоДокументов", ЭлектронныеДокументы.Количество()));
				ЭДОБЗККлиент.ПодписатьФайлы(ДанныеФайловНаПодпись, Оповещение, ЭтотОбъект,
					ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников"));
			Иначе
				ЗавершениеПодготовкиДокументов(ЭлектронныеДокументы.Количество(), Истина);
			КонецЕсли;
		Иначе
			ЗавершениеПодготовкиДокументов(ЭлектронныеДокументы.Количество(), Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодписанияДокументов(Результат, ДополнительныеПараметры) Экспорт
	ЗавершениеПодготовкиДокументов(ДополнительныеПараметры.КоличествоДокументов, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПодготовкиДокументов(КоличествоДокументов, СохранениеИПодписание)
	
	Если СохранениеИПодписание Тогда
		Если ТребуетсяПодписьОрганизации Тогда
			ТекстСообщения = СтрокаСЧислом(";Подготовлен и подписан %1 документ.;;Подготовлено и подписано %1 документа.;Подготовлено и подписано %1 документов.;Подготовлено и подписано %1 документа.",
				КоличествоДокументов, ВидЧисловогоЗначения.Количественное, "L=ru");
		Иначе
			ТекстСообщения = СтрокаСЧислом(";Подготовлен и отправлен %1 документ.;;Подготовлено и отправлено %1 документа.;Подготовлено и отправлено %1 документов.;Подготовлено и отправлено %1 документа.",
				КоличествоДокументов, ВидЧисловогоЗначения.Количественное, "L=ru");
		КонецЕсли;
	Иначе
		ТекстСообщения = СтрокаСЧислом(";Подготовлен %1 документ.;;Подготовлено %1 документа.;Подготовлено %1 документов.;Подготовлено %1 документа.",
			КоличествоДокументов, ВидЧисловогоЗначения.Количественное, "L=ru");
	КонецЕсли;
	Состояние(ТекстСообщения);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидДокумента", ВидДокумента);
	ПараметрыОткрытия.Вставить("Ответственный", ПользователиКлиент.ТекущийПользователь());
	ПараметрыОткрытия.Вставить("Дата", НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса()));
	
	ОткрытьФорму("Документ.ДокументЭДОБЗК.ФормаСписка", ПараметрыОткрытия, , Истина);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораСотрудниковНаСервере(ВыбранныеСотрудники)
	Для Каждого ДанныеСотрудника Из ВыбранныеСотрудники Цикл
		Если Не ЗначениеЗаполнено(Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ДанныеСотрудника.Сотрудник))) Тогда
			ЗаполнитьЗначенияСвойств(Сотрудники.Добавить(), ДанныеСотрудника);
		КонецЕсли;
	КонецЦикла;
	УстановитьОтображениеЭлементовФормы(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Функция АдресСпискаРанееВыбранныхСотрудников()
	Возврат ПоместитьВоВременноеХранилище(Сотрудники.Выгрузить().ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор)
КонецФункции

&НаСервереБезКонтекста
Функция ОбразецДокументаНаСервере(ВидДокумента, Сотрудник)
	Возврат Справочники.ВидыДокументовЭДОБЗК.ОбразецДокумента(ВидДокумента, Сотрудник);
КонецФункции

&НаСервереБезКонтекста
Функция СформироватьДокументыНаСервере(ВидДокумента, ВыбранныеСотрудники, ВариантОтчета, НомерДокумента, ДатаДокумента)
	
	Возврат Справочники.ВидыДокументовЭДОБЗК.СформироватьДокументы(
		ВидДокумента, ВыбранныеСотрудники, ВариантОтчета, НомерДокумента, ДатаДокумента)
	
КонецФункции

&НаСервере
Функция ПодготовитьДокументыНаСервере(ОтправитьВКабинетыСотрудников)
	
	ИмяВременногоКаталога = РаботаСФайламиБЗК.ВременныйКаталог();
	
	ВыбранныеСотрудники = Сотрудники.Выгрузить().ВыгрузитьКолонку("Сотрудник");
	ТабличныеДокументыСотрудников = СформироватьДокументыНаСервере(
		ВидДокумента, ВыбранныеСотрудники, ВариантОтчета, НомерДокумента, ДатаДокумента);
	
	ДанныеДокументовОрганизаций = Новый Соответствие;
	Для Каждого ТабличныеДокументыСотрудниковОрганизации Из ТабличныеДокументыСотрудников Цикл
		ДанныеДокументовОрганизации = Новый Соответствие;
		ДанныеДокументовОрганизаций.Вставить(ТабличныеДокументыСотрудниковОрганизации.Ключ, ДанныеДокументовОрганизации);
		Для Каждого ТабличныйДокументСотрудника Из ТабличныеДокументыСотрудниковОрганизации.Значение Цикл
			ДанныеДобавленногоДокумента = ДокументЭДОСотрудника(ТабличныйДокументСотрудника.Значение, ТабличныйДокументСотрудника.Ключ, ИмяВременногоКаталога);
			ДанныеДокументовОрганизации.Вставить(ДанныеДобавленногоДокумента.ДокументЭДО, ДанныеДобавленногоДокумента.ЭлектронныйДокумент);
		КонецЦикла;
		ЭДОБЗК.ЗарегистрироватьДокументыЭДОБЗККПересчетуСостояний(
			ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДокументовОрганизации, "Ключ"));
		Если ОтправитьВКабинетыСотрудников Тогда
			Если ТребуетсяПодписьОрганизации Тогда
				ЭДОБЗКВызовСервера.ЗапланироватьДействияСПечатнымиФормами(
					ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДокументовОрганизации, "Значение"),
					Перечисления.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников,
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Пользователи.ТекущийПользователь()));
			Иначе
				ЭДОБЗКВызовСервера.ЗапланироватьДействияСПечатнымиФормами(
					ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДокументовОрганизации, "Значение"),
					Перечисления.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	УдалитьФайлы(ИмяВременногоКаталога);
	
	Возврат ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДокументовОрганизации, "Значение")
	
КонецФункции

&НаСервере
Функция ДокументЭДОСотрудника(ТабличныйДокумент, КлючиОтчета, ИмяВременногоКаталога)
	
	ДокументЭДОБЗКСсылка = Документы.ДокументЭДОБЗК.ПолучитьСсылку();
	
	ИмяФайла = КлючиОтчета.НаименованиеДокумента;
	Если ЗначениеЗаполнено(КлючиОтчета.НомерДокумента) Тогда
		ИмяФайла = СтрШаблон("%1 №2", ИмяФайла, КлючиОтчета.НомерДокумента);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючиОтчета.ДатаДокумента) Тогда
		ИмяФайла = СтрШаблон(НСтр("ru = '%1 от %2'"), ИмяФайла, Формат(КлючиОтчета.ДатаДокумента, "ДФ=dd.MM.yyyy"));
	КонецЕсли;
	
	Если Не КлючиОтчета.НаСписокСотрудников И ЗначениеЗаполнено(КлючиОтчета.ФИОФамилияИИнициалы) Тогда
		ИмяФайла = СтрШаблон("%1 (%2)", ИмяФайла, КлючиОтчета.ФИОФамилияИИнициалы);
	КонецЕсли;
	
	ИмяФайлаСРасширением = ЭДОБЗКВызовСервера.ИмяФайлаЭДОБЗК(ИмяФайла + ".pdf");
	ПолноеИмяФайла = РаботаСФайламиБЗК.УникальноеИмяФайла(
		ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременногоКаталога) + ИмяФайлаСРасширением);
	
	ФайлЭДОБЗК = Новый Файл(ПолноеИмяФайла);

	Поток = Новый ПотокВПамяти();
	ТабличныйДокумент.Записать(Поток, ЭДОБЗКВызовСервера.ТипФайлаЭлектронногоДокумента());
	ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();
	АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	
	ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыФайла.ВладелецФайлов     = ДокументЭДОБЗКСсылка;
	ПараметрыФайла.ИмяБезРасширения   = ФайлЭДОБЗК.ИмяБезРасширения;
	ПараметрыФайла.РасширениеБезТочки = СтрЗаменить(ФайлЭДОБЗК.Расширение, ".", "");
	УстановитьПривилегированныйРежим(Истина);
	ФайлДокумента = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресХранилища);
	
	ДанныеПечатнойФормы = ЭДОБЗК.ДанныеПечатнойФормы();
	ДанныеПечатнойФормы.Владелец                   = ДокументЭДОБЗКСсылка;
	ДанныеПечатнойФормы.Организация                = КлючиОтчета.Организация;
	ДанныеПечатнойФормы.ФизическоеЛицо             = КлючиОтчета.ФизическоеЛицо;
	ДанныеПечатнойФормы.ПечатнаяФорма              = ТабличныйДокумент;
	ДанныеПечатнойФормы.ИдентификаторПечатнойФормы = Строка(ВидДокумента);
	ДанныеПечатнойФормы.Название                   = ИмяФайла;
	ДанныеПечатнойФормы.ИмяФайла                   = ИмяФайла;
	ДанныеПечатнойФормы.ФайлОбъекта                = ФайлДокумента;
	ДанныеПечатнойФормы.Номер                      = КлючиОтчета.НомерДокумента;
	ДанныеПечатнойФормы.Дата                       = КлючиОтчета.ДатаДокумента;
	РегистрыСведений.ПодписанныеПечатныеФормы.ДобавитьПечатнуюФорму(ДанныеПечатнойФормы, ТабличныйДокумент, ТекущаяДатаСеанса());
	
	ОписаниеДокумента = Документы.ДокументЭДОБЗК.СвойстваДокумента(
		ФайлДокумента,
		КлючиОтчета.Организация,
		КлючиОтчета.ФизическоеЛицо,
		ДокументЭДОБЗКСсылка,
		,
		КлючиОтчета.СодержимоеДокумента,
		ВидДокумента);
	ОписаниеДокумента.НаСписокСотрудников		= КлючиОтчета.НаСписокСотрудников;
	ОписаниеДокумента.ДополнительныеСвойства	= Новый Структура("СсылкаНового", ДокументЭДОБЗКСсылка);
	ОписаниеДокумента.Дата 						= ТекущаяДатаСеанса();
	
	ДокументЭДОБЗКОбъект = Документы.ДокументЭДОБЗК.НовыйДокумент(ОписаниеДокумента);
	ДокументЭДОБЗКОбъект.Внешний = Истина;
	ДокументЭДОБЗКОбъект.ЭлектронныйДокумент = ФайлДокумента;
	ДокументЭДОБЗКОбъект.Записать();
	
	Возврат Новый Структура("ДокументЭДО,ЭлектронныйДокумент", ДокументЭДОБЗКСсылка, ФайлДокумента);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеЭлементовФормы(УправляемаяФорма) 
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"Организация",
		"Доступность",
		ЗначениеЗаполнено(УправляемаяФорма.ВидДокумента));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ДатаДокумента",
		"Доступность",
		ЗначениеЗаполнено(УправляемаяФорма.ВидДокумента));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"Сотрудники",
		"Доступность",
		ЗначениеЗаполнено(УправляемаяФорма.ВидДокумента)
			И ЗначениеЗаполнено(УправляемаяФорма.Организация));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ФормаПоказатьОбразец",
		"Доступность",
		ЗначениеЗаполнено(УправляемаяФорма.Сотрудники));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ФормаПодписатьИОтправить",
		"Доступность",
		ЗначениеЗаполнено(УправляемаяФорма.Сотрудники));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ФормаСформироватьИПередатьДокументы",
		"Доступность",
		ЗначениеЗаполнено(УправляемаяФорма.Сотрудники));
	
КонецПроцедуры

&НаСервере
Процедура ВидДокументаПриИзмененииНаСервере()
	
	ЭДОБЗККлиентСервер.УстановитьПодсказкуОКодеВидаДокумента(
		ЭтотОбъект, "ВидДокумента", ВидДокумента);
	
	ВариантОтчета = Справочники.ВидыДокументовЭДОБЗК.ВариантОтчетаМакетаВидаДокумента(ВидДокумента, Истина);
	ДанныеВарианта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВариантОтчета, "Отчет,КлючВарианта");
	
	ФормаПодписатьИОтправитьЗаголовок = НСтр("ru = 'Подписать и отправить'");
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		ТребуетсяПодписьОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДокумента, "ТребуетсяПодписьОрганизации");
		Если Не ТребуетсяПодписьОрганизации Тогда
			ФормаПодписатьИОтправитьЗаголовок = НСтр("ru = 'Сформировать и отправить'");
		КонецЕсли;
	Иначе
		ТребуетсяПодписьОрганизации = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаПодписатьИОтправить",
		"Заголовок",
		ФормаПодписатьИОтправитьЗаголовок);
	
	УстановитьОтображениеЭлементовФормы(ЭтотОбъект);
	
	Сотрудники.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолучения.Организация = Организация;
	ПараметрыПолучения.НачалоПериода = НачалоДня(ТекущаяДатаСеанса());
	ПараметрыПолучения.ОкончаниеПериода = КонецДня(ПараметрыПолучения.НачалоПериода);
	ПараметрыПолучения.КадровыеДанные = "Организация,Подразделение,Должность";
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
		ПараметрыПолучения.Отборы, "ИмеетКабинетСотрудника", "=", Истина);
	
	СотрудникиОрганизации = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолучения);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидДокумента", ВидДокумента);
	Запрос.УстановитьПараметр("ДатаПоследнегоДокумента", ДобавитьМесяц(ДатаДокумента, -1));
	Запрос.УстановитьПараметр("ФизическиеЛица", СотрудникиОрганизации.ВыгрузитьКолонку("ФизическоеЛицо"));
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ДокументЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	Документ.ДокументЭДОБЗК.ВнешниеПодписанты КАК ДокументЭДОБЗК
		|ГДЕ
		|	ДокументЭДОБЗК.Ссылка.ВидДокумента = &ВидДокумента
		|	И ДокументЭДОБЗК.Ссылка.Дата >= &ДатаПоследнегоДокумента
		|	И НЕ ДокументЭДОБЗК.Ссылка.ПометкаУдаления
		|	И ДокументЭДОБЗК.ФизическоеЛицо В(&ФизическиеЛица)";
	ФизическиеЛицаСДокументами = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	
	ДанныеВыбранныхСотрудников = Новый Массив;
	Для Каждого ДанныеСотрудника Из СотрудникиОрганизации Цикл
		Если ФизическиеЛицаСДокументами.Найти(ДанныеСотрудника.ФизическоеЛицо) = Неопределено Тогда
			ДанныеВыбранныхСотрудников.Добавить(ДанныеСотрудника);
		КонецЕсли;
	КонецЦикла;
	
	ОбработкаВыбораСотрудниковНаСервере(ДанныеВыбранныхСотрудников);
	
КонецПроцедуры

#КонецОбласти
