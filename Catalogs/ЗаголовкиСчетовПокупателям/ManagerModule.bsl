#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает заголовок счета по умолчанию
//
// Возвращаемое значение:
//  СправочникСсылка.ЗаголовкиСчетовПокупателям
//
Функция ЗаголовокСчетаПоУмолчанию() Экспорт
	
	ЗаголовокСчета = ПечатьТорговыхДокументовКлиентСервер.ЗаголовокСчетаНаОплатуПоУмолчанию();
	
	ЭлементЗаголовкаСчета = НайтиПоНаименованию(ЗаголовокСчета, Истина);
	Если ЗначениеЗаполнено(ЭлементЗаголовкаСчета) Тогда
		Возврат ЭлементЗаголовкаСчета;
	КонецЕсли;
	
	Возврат ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеВыбораЗаголовка(Параметры, ОкончаниеВводаТекста = Ложь) Экспорт

	ДанныеВыбора = ПолучитьДанныеВыбора(Параметры);
	Если ДанныеВыбора.Количество() = 0 Тогда
		СтрокаПоиска = СокрЛП(Параметры.СтрокаПоиска);
		ДлинаСтроки  = СтрДлина(СтрокаПоиска);
		ПредставлениеНового = СтрШаблон(НСтр("ru='Создать: %1'"), СтрокаПоиска);
		ДанныеВыбора.Добавить(СтрокаПоиска, ПредставлениеНового);
		Если ОкончаниеВводаТекста Тогда
			// Добавляем вторую строку, чтобы в этом режиме не срабатывал автовыбор единственной строки
			ДанныеВыбора.Добавить("");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура СозданиеТиповыхЗаголовковСчетов() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено // В подчиненных узлах РИБ не выполняется, чтобы не было дублей
		Или ЗаголовкиЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовкиСчетов = ТиповыеЗаголовкиСчетов();
	
	Для Каждого ПредставлениеЗаголовка Из ЗаголовкиСчетов Цикл
		Попытка
			ЗаголовокСчетаОбъект = СоздатьЭлемент();
			ЗаголовокСчетаОбъект.Наименование = ПредставлениеЗаголовка;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЗаголовокСчетаОбъект);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось создать заголовок счета покупателю ""%1"" по причине:
					|%2'"),
					ПредставлениеЗаголовка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.ЗаголовкиСчетовПокупателям, ,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнениеЗаголовкаПоУмолчаниюВОрганизациях() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокПоУмолчанию = ЗаголовокСчетаПоУмолчанию();
	Если Не ЗначениеЗаполнено(ЗаголовокПоУмолчанию) Тогда
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			НСтр("ru = 'Не удалось выполнить процедуру ЗаполнениеЗаголовкаПоУмолчаниюВОрганизациях
			|из-за того, что не созданы типовые заголовки счетов покупателям'"));
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЗаголовокСчетаПоУмолчанию = ЗНАЧЕНИЕ(Справочник.ЗаголовкиСчетовПокупателям.ПустаяСсылка)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Попытка
			ОрганизацияОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ОрганизацияОбъект.ЗаголовокСчетаПоУмолчанию = ЗаголовокПоУмолчанию;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОрганизацияОбъект);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось заполнить заголовок счета покупателю в организации по причине:
					|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка, , Выборка.Ссылка, ТекстСообщения);
		КонецПопытки
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнениеЗаголовкаПоУмолчаниюВСчетахПокупателям(Параметры) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокПоУмолчанию") Тогда
		ЗаголовокПоУмолчанию = Параметры.ЗаголовокПоУмолчанию;
	Иначе
		ЗаголовокПоУмолчанию = ЗаголовокСчетаПоУмолчанию();
		Параметры.Вставить("ЗаголовокПоУмолчанию", ЗаголовокПоУмолчанию);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЗаголовокПоУмолчанию) Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			НСтр("ru = 'Не удалось выполнить процедуру ЗаполнениеЗаголовкаПоУмолчаниюВСчетахПокупателям
			|из-за того, что не созданы типовые заголовки счетов покупателям'"));
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СчетНаОплатуПокупателю.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.ЗаголовокСчета = ЗНАЧЕНИЕ(Справочник.ЗаголовкиСчетовПокупателям.ПустаяСсылка)";
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Документ.СчетНаОплатуПокупателю");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		НачатьТранзакцию();
		Попытка
			ПричинаИсключения = "Блокировка";
			Блокировка.Заблокировать();
			СчетНаОплатуОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если СчетНаОплатуОбъект = Неопределено Или ЗначениеЗаполнено(СчетНаОплатуОбъект.ЗаголовокСчета) Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			СчетНаОплатуОбъект.ЗаголовокСчета = ЗаголовокПоУмолчанию;
			ПричинаИсключения = "Запись";
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СчетНаОплатуОбъект);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось заполнить заголовок счета покупателю по причине:
				|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка, ,
				Выборка.Ссылка, ТекстСообщения);
			Если ПричинаИсключения <> "Блокировка" Тогда
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
		КонецПопытки
	КонецЦикла;
	
	Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
	Иначе
		Параметры.ОбработкаЗавершена = Ложь;
		Если ОбъектовОбработано = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедуре ЗаполнениеЗаголовкаПоУмолчаниюВСчетахПокупателям
				|не удалось заполнить заголовок счета покупателю в %1 документах'"), 
				ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
		Иначе
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация, , ,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаполнениеЗаголовкаПоУмолчаниюВСчетахПокупателям
				|обработала очередную порцию документов Счет покупателю: %1'"), 
				ОбъектовОбработано));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция ТиповыеЗаголовкиСчетов()
	
	СписокЗаголовков = Новый Массив;
	СписокЗаголовков.Добавить(ПечатьТорговыхДокументовКлиентСервер.ЗаголовокСчетаНаОплатуПоУмолчанию());
	СписокЗаголовков.Добавить(НСтр("ru = 'Счет-оферта'"));
	СписокЗаголовков.Добавить(НСтр("ru = 'Счет-договор'"));
	Возврат СписокЗаголовков;
	
КонецФункции

Функция ЗаголовкиЗаполнены()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаголовкиСчетовПокупателям.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗаголовкиСчетовПокупателям КАК ЗаголовкиСчетовПокупателям";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли