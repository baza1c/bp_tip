#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	УстановитьЗначенияРасшифровок(
		Объект.Владелец,
		ПредставлениеЕдИзмерения,
		Объект.СрокПолезногоИспользования,
		РашифровкаСрокПолезногоИспользования);
		
	Если Объект.Ссылка.Пустая() Тогда
		Объект.СрокПолезногоИспользования = ПолучитьСрокПолезногоИспользования(Объект.Владелец);
	КонецЕсли;			
		
	ПорядокИспользования = ?(Объект.СрокПолезногоИспользования > 0, 1, 0);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Объект.СрокПолезногоИспользования = ПолучитьСрокПолезногоИспользования(Объект.Владелец);
	
	УстановитьЗначенияРасшифровок(
		Объект.Владелец,
		ПредставлениеЕдИзмерения,
		Объект.СрокПолезногоИспользования,
		РашифровкаСрокПолезногоИспользования);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПогашенияСтоимостиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокПолезногоИспользованияПриИзменении(Элемент)
	
	УстановитьЗначенияРасшифровок(
		Объект.Владелец,
		ПредставлениеЕдИзмерения,
		Объект.СрокПолезногоИспользования,
		РашифровкаСрокПолезногоИспользования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокИспользованияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	Если ПорядокИспользования = 0 Тогда
		Объект.СрокПолезногоИспользования = 0;
		УстановитьЗначенияРасшифровок(
			Объект.Владелец,
			ПредставлениеЕдИзмерения,
			Объект.СрокПолезногоИспользования,
			РашифровкаСрокПолезногоИспользования);
	ИначеЕсли Объект.СрокПолезногоИспользования = 0 Тогда
		Объект.СрокПолезногоИспользования = ПолучитьСрокПолезногоИспользования(Объект.Владелец);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьЗначенияРасшифровок(Номенклатура, ПредставлениеЕдИзмерения, СрокИспользования, РашифровкаСрокИспользования)

	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ПредставлениеЕдИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения.Наименование");
	Иначе
		ПредставлениеЕдИзмерения = ""
	КонецЕсли; 

	РашифровкаСрокИспользования = УправлениеВнеоборотнымиАктивамиКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользования);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	ПропорциональноОбъемуПродукцииРабот = Форма.Объект.СпособПогашенияСтоимости
		= ПредопределенноеЗначение("Перечисление.СпособыПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот");
		
	Форма.Элементы.ГруппаСрокИспользованияИРасшифровка.Видимость = (Форма.ПорядокИспользования = 1);
	Форма.Элементы.ОбщийОбъемПродукцииРабот.Видимость = ПропорциональноОбъемуПродукцииРабот;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСрокПолезногоИспользования(Номенклатура)
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат РегистрыСведений.СрокиИспользованияНоменклатуры.Получить(
			Новый Структура("Номенклатура", Номенклатура)).СрокИспользования;
	
КонецФункции

#КонецОбласти