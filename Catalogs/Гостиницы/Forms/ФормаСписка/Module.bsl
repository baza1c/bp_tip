
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Отбор.Организация) Тогда
		Организация = Параметры.Отбор.Организация;
	КонецЕсли;
	
	ОсновнаяГостиница	= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяГостиница");
    Список.Параметры.УстановитьЗначениеПараметра("ОсновнаяГостиница", ОсновнаяГостиница);
	
	Элементы.ФормаИспользоватьОсновным.Видимость = ПравоДоступа("СохранениеДанныхПользователя", Метаданные);
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеФормойКлиент();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Ссылка = ОсновнаяГостиница Тогда
		ОсновнаяГостиница = Неопределено;
	Иначе
		ОсновнаяГостиница = ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	УстановитьОсновнуюГостиницу(ОсновнаяГостиница);	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновнаяГостиница", ОсновнаяГостиница);

	УправлениеФормойКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	УправлениеФормойКлиент();
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьОсновнуюГостиницу(ОсновнаяГостиница)
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнаяГостиница",	ОсновнаяГостиница);

КонецПроцедуры   

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список); 
	ДоступнаУстановкаОсновным = ТекущиеДанные <> Неопределено И Не ТекущиеДанные.ЭтоГруппа;
		
	Если ДоступнаУстановкаОсновным Тогда	
		Если ТекущиеДанные.Свойство("Ссылка") Тогда
			Элементы.ФормаИспользоватьОсновным.Пометка = ТекущиеДанные.Ссылка = ОсновнаяГостиница;
		КонецЕсли;
		
		Если ТекущиеДанные.Свойство("ПометкаУдаления") И ТекущиеДанные.ПометкаУдаления Тогда
			Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		Иначе
			Элементы.ФормаИспользоватьОсновным.Доступность = Истина;
		КонецЕсли;
	Иначе
		Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

