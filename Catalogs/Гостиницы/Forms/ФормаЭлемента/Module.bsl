
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() И  Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда  
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		КонецЕсли;
		УстановитьГоловнуюОрганизацию(ЭтотОбъект);
	КонецЕсли;
	
	Элементы.ГруппаТурналог.Видимость = ПолучитьФункциональнуюОпцию("УплачиваетсяТуристическийНалог");

	НастроитьНалоговыйОрган(ЭтотОбъект);
	ЗаполнитьДанныеОНалоговомОргане();

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
	// СтандартныеПодсистемы.КонтактнаяИнформация
	РазмещеныНаФорме = Новый Соответствие;
	РазмещеныНаФорме.Вставить(Справочники.ВидыКонтактнойИнформации.АдресГостиницы, Истина);
	
	ПараметрыРазмещенияКонтактнойИнформации = УправлениеКонтактнойИнформацией.ПараметрыКонтактнойИнформации();
	ПараметрыРазмещенияКонтактнойИнформации.ИмяЭлементаДляРазмещения = "ГруппаКонтактнаяИнформация";
	ПараметрыРазмещенияКонтактнойИнформации.РазмещеныНаФорме = РазмещеныНаФорме;
	ПараметрыРазмещенияКонтактнойИнформации.РазрешитьДобавлениеПолей = Ложь;
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, ПараметрыРазмещенияКонтактнойИнформации);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьГоловнуюОрганизацию(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации Тогда
		ТекущийОбъект.НалоговыйОрган = Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
		ТекущийОбъект.КодПоОКТМО = "";
	КонецЕсли;
	
	// Обработчик подсистемы "Контактная информация"
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
	
		Если Не ЗначениеЗаполнено(Объект.НалоговыйОрган) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				,
				,
				НСтр("ru = 'Налоговый орган'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				"Объект.НалоговыйОрган",
				,
				Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.КодПоОКТМО) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				,
				,
				НСтр("ru = 'Код по ОКТМО'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				"Объект.КодПоОКТМО",
				,
				Отказ);
		КонецЕсли;
				
	КонецЕсли;

	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Гостиницы.Форма.РедактированиеИсторииСтавок") Тогда
		
		УстановитьСтавкуПослеРедактированияИстории(ВыбранноеЗначение.ИсторияСтавок);
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзмененаРегистрацияВНалоговомОргане" 
		Или ИмяСобытия = "Запись_Организации" Тогда
		ЗаполнитьДанныеОНалоговомОргане();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПостановкаНаУчетВНалоговомОрганеПриИзменении(Элемент)
	
	НастроитьНалоговыйОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганПриИзменении(Элемент)
	
	НалоговыйОрганПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяСтавкаПриИзменении(Элемент)
	НалоговаяСтавкаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИсторияСтавокНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("ТекущаяСтавка, ИсторияСтавок, ТолькоПросмотр", 
					Объект.СтавкаТуристическогоНалога, Объект.ИсторияСтавок, ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.Гостиницы.Форма.РедактированиеИсторииСтавок", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "НалоговыйОрганСсылка" Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", Объект.Организация);
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОКТМОСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОКТМОСсылка" Тогда
		
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
		
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Ключ", НалоговыйОрган);
			
			ФормаНалоговогоОргана = ОткрытьФорму("Справочник.РегистрацииВНалоговомОргане.ФормаОбъекта", ПараметрыФормы);
			
			ЭлементКодПоОКТМО = ФормаНалоговогоОргана.Элементы.Найти("КодПоОКТМО");
			Если ЭлементКодПоОКТМО <> Неопределено Тогда
				ФормаНалоговогоОргана.ТекущийЭлемент = ЭлементКодПоОКТМО;
			КонецЕсли;
			
			СтандартнаяОбработка = Ложь;
			
		Иначе
			
			ПараметрыФормы = Новый Структура("Ключ", Объект.Организация);
			ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НалоговыйОрганПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.НалоговыйОрган) Тогда 
		РеквизитыНО = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.НалоговыйОрган, "КодПоОКТМО, КодПоОКАТО");
		Объект.КодПоОКТМО = РеквизитыНО.КодПоОКТМО;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьСтавкуПослеРедактированияИстории(НаборЗаписей)
	
	Модифицированность = Истина;
	
	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияСтавок.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьИстории = Объект.ИсторияСтавок.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
		КонецЦикла;
	КонецЕсли;
	
	Объект.СтавкаТуристическогоНалога = НаборЗаписей[НаборЗаписей.Количество()-1].Ставка;
	
КонецПроцедуры

// СтандартныеПодсистемы.КонтактнаяИнформация

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	УправлениеКонтактнойИнформациейКлиент.НачатьИзменение(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент,, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьОчистку(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьКонтактнуюИнформацию(Результат);
КонецПроцедуры

&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(Результат)
	
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтактнаяИнформация

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьНалоговыйОрган(Форма)
	
	Объект = Форма.Объект;
	ПоМестуНахожденияОрганизации = Объект.ПостановкаНаУчетВНалоговомОргане = 
		ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации");
		
	Форма.Элементы.ГруппаНалоговыйОрганПоМестуНахожденияОрганизации.Видимость = ПоМестуНахожденияОрганизации;
	Форма.Элементы.ГруппаДругойНалоговыйОрган.Видимость = НЕ ПоМестуНахожденияОрганизации;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОНалоговомОргане()
	
	НалоговыйОрганПредставление = НСтр("ru = '<не указан>'");
	ОКТМОПредставление = НСтр("ru = '<не указан>'");
	
	НалоговыйОрганГиперссылка = "";
	ОКТМОГиперссылка = "";
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
	
		НалоговыйОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "РегистрацияВНалоговомОргане");
		ОКТМО = "";
		
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
			
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НалоговыйОрган, "Код, КПП, КодПоОКТМО");
			
			Если ЗначениеЗаполнено(Реквизиты.КПП) Тогда
				НалоговыйОрганПредставление = СтрШаблон(НСтр("ru = 'КПП %1 в инспекции %2'"), Реквизиты.КПП, Реквизиты.Код);
			Иначе
				НалоговыйОрганПредставление = СтрШаблон(НСтр("ru = 'В инспекции %1'"), Реквизиты.Код);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Реквизиты.КодПоОКТМО) Тогда
				ОКТМО = Реквизиты.КодПоОКТМО;
				ОКТМОПредставление = СокрЛП(Реквизиты.КодПоОКТМО);
			Иначе
				ОКТМОГиперссылка = "ОКТМОСсылка";
			КонецЕсли;
			
		Иначе
			
			НалоговыйОрганГиперссылка = "НалоговыйОрганСсылка";
			
		КонецЕсли;
		
	КонецЕсли;
	
	НалоговыйОрганСсылка = Новый ФорматированнаяСтрока(НалоговыйОрганПредставление,,,, НалоговыйОрганГиперссылка);
	ОКТМОСсылка = Новый ФорматированнаяСтрока(ОКТМОПредставление,,,, ОКТМОГиперссылка);
		
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
		
	УстановитьГоловнуюОрганизацию(ЭтотОбъект);
	ЗаполнитьДанныеОНалоговомОргане();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьГоловнуюОрганизацию(Форма)
	
	Форма.ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Форма.Объект.Организация);
		
КонецПроцедуры

&НаСервере
Процедура НалоговаяСтавкаПриИзмененииНаСервере()
	Справочники.Гостиницы.УстановитьАктуальноеЗначениеИсторииСтавок(Объект.СтавкаТуристическогоНалога , Объект.ИсторияСтавок);
КонецПроцедуры

#КонецОбласти



