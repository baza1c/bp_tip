#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает значение ставки туристического налога на дату
//
// Параметры
//  Гостиница - <СправочникСсылка.Гостиницы> - Гостиница
//  ДатаСведений   - <Дата> - Дата получения значения
//
// Возвращаемое значение:
//   <Число>			– ставка туристического налогаи
//
Функция СтавкаНалогаНаДату(Гостиница, ДатаСведений) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Гостиница)
		ИЛИ ТипЗнч(Гостиница) <> Тип("СправочникСсылка.Гостиницы") Тогда
		Возврат 0;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаСведений) Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Гостиница, "СтавкаТуристическогоНалога");
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Гостиница", Гостиница);
	Запрос.Параметры.Вставить("ДатаСведений", ДатаСведений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияСтавок.Период) КАК Период,
	|	ИсторияСтавок.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗначенияСтавок
	|ИЗ
	|	Справочник.Гостиницы.ИсторияСтавок КАК ИсторияСтавок
	|ГДЕ
	|	ИсторияСтавок.Ссылка = &Гостиница
	|	И ИсторияСтавок.Период <= &ДатаСведений
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияСтавок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияСтавокНалога.Ставка КАК СтавкаТуристическогоНалога
	|ИЗ
	|	ЗначенияСтавок КАК ЗначенияСтавок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Гостиницы.ИсторияСтавок КАК ИсторияСтавокНалога
	|		ПО ЗначенияСтавок.Ссылка = ИсторияСтавокНалога.Ссылка
	|			И ЗначенияСтавок.Период = ИсторияСтавокНалога.Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтавкаТуристическогоНалога;
	Иначе
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Гостиница, "СтавкаТуристическогоНалога");
	КонецЕсли;
	
КонецФункции

Процедура УстановитьАктуальноеЗначениеИсторииСтавок(Ставка, ИсторияСтавок) Экспорт
	
	КоличествоЗаписей = ИсторияСтавок.Количество();
	
	Если КоличествоЗаписей > 0 Тогда
		
		ДатаТекущегоМесяца = НачалоМесяца(ТекущаяДатаСеанса());
		ИсторияСтавок.Сортировать("Период");   
		АктуальнаяЗаписьИстории = ИсторияСтавок[КоличествоЗаписей - 1];
		АктуальнаяЗаписьИстории.Ставка = Ставка;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация) ИЛИ ЭтоГруппа";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли