
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Список.Параметры.УстановитьЗначениеПараметра(
		"ПредставлениеОбособленногоПодразделения", НСтр("ru='Обособленное подразделение'"));
	
	ОткрытИзПлатежки = Параметры.Свойство("ОткрытИзПлатежки");
	
	ПоказыватьВзаиморасчеты = ОбщегоНазначенияБП.ЭтоПростойИнтерфейс()
		И Константы.ОтображатьДополнительнуюИнформациюВСпискеКонтрагентов.Отображать()
		И Не ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	ОтображатьСтатусыДокументов = ПолучитьФункциональнуюОпцию("ОтображатьДополнительныеКолонкиВСписках");
	Элементы.ЭДО.Видимость = ОтображатьСтатусыДокументов;
	Если ОтображатьСтатусыДокументов Тогда
		
		// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
		ПроверкаКонтрагентов.ПриСозданииНаСервереСписокКонтрагентов(Список);
		// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

	КонецЕсли;
	
	ВестиУчетРасчетовСКонтрагентами = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСКонтрагентами");
	
	РаботаСМетками.ПриСозданииНаСервере(ЭтотОбъект, Истина);
	
	УстановитьУсловноеОформление();
	
	Элементы.ГруппаСвойстваКонтрагента.Видимость = ПоказыватьВзаиморасчеты;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Справочник.Контрагенты",
		"ФормаВыбора",
		НСтр("ru='Новости: Контрагент'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	Поля = Новый Массив;
	Поля.Добавить("МеткиСтрокой");
	
	Список.УстановитьОграниченияИспользованияВГруппировке(Поля);
	Список.УстановитьОграниченияИспользованияВОтборе(Поля);
	Список.УстановитьОграниченияИспользованияВПорядке(Поля);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	Если РаботаСМеткамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбработкаОповещенияФормыСМеткамиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	РаботаСМетками.ПриЗагрузкеДанныхИзНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область Метки

&НаКлиенте
Процедура ИспользоватьОтборМеткиПриИзменении(Элемент)
	
	РаботаСМеткамиКлиент.ИспользоватьОтборМеткиПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиПриИзменении(Элемент)
	
	ОтборМеткиПриИзмененииНаСервере();
	
	РаботаСМеткамиКлиент.ОтборМеткиПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиСоздание(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОткрытиеМножественногоЗначения(Элемент, Идентификатор, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОткрытиеМножественногоЗначения(ЭтотОбъект, Элемент, Идентификатор, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ПоказыватьВзаиморасчеты Тогда
		ПодключитьОбработчикОжидания("ОбновитьСвойстваТекущегоКонтрагента", 0.2, Истина);
	КонецЕсли;
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	РаботаСМетками.ПриПолученииДанныхНаСервере(Настройки, Строки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Если ОткрытИзПлатежки И Элемент.ТекущиеДанные.Вид <> "" Тогда
		Отказ = Истина;
		ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущаяСтрока);
		ПараметрыФормы.Вставить("ОткрытИзПлатежки"); 
		
		ЗначенияЗаполнения = Новый Структура();
		ДополнитьЗначенияЗаполненияОтборомПоГосоргану(ЗначенияЗаполнения);
		Если ЗначениеЗаполнено(ЗначенияЗаполнения) Тогда
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		КонецЕсли;
		
		ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ОткрытИзПлатежки И НЕ Группа Тогда
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура("ОткрытИзПлатежки");
		ЗначенияЗаполнения = Новый Структура("Родитель", Родитель);
		ДополнитьЗначенияЗаполненияОтборомПоГосоргану(ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		Если Копирование И ЗначениеЗаполнено(Элемент.ТекущаяСтрока) Тогда
			ПараметрыФормы.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
		КонецЕсли;
		
		ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДополнитьЗначенияЗаполненияОтборомПоГосоргану(ЗначенияЗаполнения)
	
	УстановленОтборПоГосоргану = УстановленОтборПоГосоргану();
	Если УстановленОтборПоГосоргану Тогда
		ЗначенияЗаполнения.Вставить("ГосударственныйОрган", Истина);
		ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо", ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция УстановленОтборПоГосоргану()
	
	ПолеКомпоновкиГосорган = Новый ПолеКомпоновкиДанных("ГосударственныйОрган");
	
	УстановленОтборПоГосоргану = Ложь;
	
	Для Каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиГосорган
			И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			И ЭлементОтбора.ПравоеЗначение = Истина Тогда
			УстановленОтборПоГосоргану = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат УстановленОтборПоГосоргану;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// ИНН некорректный
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИНН");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ИННВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);

	// КПП некорректный
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КППВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);

	Если Не ОтображатьСтатусыДокументов Тогда
		Возврат;
	КонецЕсли;
	
	// ИНН дублируется
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИНН");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ИННВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ЕстьДубли", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ПроверкаКонтрагентовКонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);
	
	// КПП дублируется
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КППВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ЕстьДубли", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ПроверкаКонтрагентовКонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);

	// Контрагента нет в реестре
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИНН");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ПроверкаКонтрагентовКонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ПроверкаКонтрагентовСостояние", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.СостоянияСуществованияКонтрагента.НеДействуетИлиИзмененКПП);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветКонтрагентаОтсутствущегоВРеестре);

	// Контрагент прекратил деятельность
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ПроверкаКонтрагентовКонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ПроверкаКонтрагентовСостояние", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СостоянияСуществованияКонтрагента.НеДействуетИлиИзмененКПП);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветКонтрагентаПрекратившегоДеятельность);
	
	// Метки
	РаботаСМетками.УстановитьУсловноеОформление(ЭтотОбъект);

КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервереБезКонтекста
Функция СвойстваКонтрагента(Знач Контрагент, Знач ВестиУчетРасчетовСКонтрагентами)
	
	Результат = Новый Структура;
	Результат.Вставить("Телефон", "Не указан");
	Результат.Вставить("ЗадолженностьПоставщику", "Нет");
	Результат.Вставить("ЗадолженностьПокупателя", "Нет");
	Результат.Вставить("КонтрагентНеоплаченныеСчета", "Нет");
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Контрагенты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Контрагент);
	ДополнительнаяИнформация = Справочники.Контрагенты.ДополнительнаяИнформацияДляФормыСписка(Контрагенты,
		ВестиУчетРасчетовСКонтрагентами);
	Если ДополнительнаяИнформация.Количество() > 0 Тогда
		
		Информация = ДополнительнаяИнформация[0];
		
		Если ЗначениеЗаполнено(Информация.Телефон) Тогда
			Результат.Телефон = Информация.Телефон;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Информация.ЗадолженностьПоставщику) Тогда
			Результат.ЗадолженностьПоставщику = СтрШаблон("%1 %2",
				Формат(Информация.ЗадолженностьПоставщику, "ЧДЦ=0; ЧН=0;"),
				ВалютаРегламентированногоУчета);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Информация.ЗадолженностьПокупателя) Тогда
			Результат.ЗадолженностьПокупателя = СтрШаблон("%1 %2",
				Формат(Информация.ЗадолженностьПокупателя, "ЧДЦ=0; ЧН=0;"),
				ВалютаРегламентированногоУчета);
		КонецЕсли;
		
		НеоплаченныеСчета = ПредставлениеНеоплаченногоСчета(Информация, ВалютаРегламентированногоУчета);
		Если ЗначениеЗаполнено(НеоплаченныеСчета) Тогда
			Результат.КонтрагентНеоплаченныеСчета = НеоплаченныеСчета;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеНеоплаченногоСчета(СвойстваКонтрагента, Валюта = Неопределено)
	
	Количество = СвойстваКонтрагента.НеоплаченныеСчетаПокупателемКоличество;
	Сумма = Формат(СвойстваКонтрагента.НеоплаченныеСчетаПокупателемСумма, "ЧДЦ=0");
	
	Если Количество = 0 Тогда
		Представление = "";
	Иначе
		Если ЗначениеЗаполнено(Валюта) Тогда
			Представление = СтрШаблон(НСтр("ru='%1 на %2 %3'"), Количество, Сумма, Валюта);
		Иначе
			Представление = СтрШаблон(НСтр("ru='%1 на %2'"), Количество, Сумма);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСвойстваТекущегоКонтрагента()
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.ЭтоГруппа Тогда
		
		КонтрагентТелефон           = "";
		КонтрагентЗадолженностьПоставщику          = "";
		КонтрагентЗадолженностьПокупателя         = "";
		КонтрагентНеоплаченныеСчета = "";
		
	ИначеЕсли Элементы.ГруппаСвойстваКонтрагента.Видимость Тогда
		
		СвойстваКонтрагента = СвойстваКонтрагента(ТекущиеДанные.Ссылка, ВестиУчетРасчетовСКонтрагентами);
		
		КонтрагентТелефон = СвойстваКонтрагента.Телефон;
		КонтрагентЗадолженностьПоставщику = СвойстваКонтрагента.ЗадолженностьПоставщику;
		КонтрагентЗадолженностьПокупателя = СвойстваКонтрагента.ЗадолженностьПокупателя;
		КонтрагентНеоплаченныеСчета = СвойстваКонтрагента.КонтрагентНеоплаченныеСчета;
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтборМеткиПриИзмененииНаСервере()
	
	РаботаСМетками.МеткиПриИзмененииНаСервере(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Функция ОбработкаОповещенияФормыСМеткамиНаСервере()
	
	РаботаСМетками.ОбработкаОповещенияНаСервере(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКИтогиСпискаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКИтогиСпискаФормы()
	
	Элементы.ГруппаСвойстваКонтрагента.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти