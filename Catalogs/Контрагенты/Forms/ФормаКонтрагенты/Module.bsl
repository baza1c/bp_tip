#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		
		ВариантОтображенияДополнительнойИнформации = Константы.ОтображатьДополнительнуюИнформациюВСпискеКонтрагентов.Получить();
		
		ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ОтборОрганизация = ОсновнаяОрганизация;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра(
		"ПредставлениеОбособленногоПодразделения", НСтр("ru='Обособленное подразделение'"));
		
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереСписокКонтрагентов(Список);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Контрагенты);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	ЕстьПравоИзменениеВзаимодействие = ПравоДоступа("Редактирование", Метаданные.Документы.ПрочееВзаимодействие);
	Элементы.ГруппаКомандВзаимодействия.Видимость = ЕстьПравоИзменениеВзаимодействие;
	
	УстановитьВосстановленныеОтборы();
	
	НастроитьОперацииПодменюОтчеты();

	НастроитьОперацииПодменюДокументы();
	
	ВестиУчетРасчетовСКонтрагентами = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСКонтрагентами");
	
	ПоказыватьВзаиморасчетыВКолонках = ПоказыватьВзаиморасчетыВКолонках(ВариантОтображенияДополнительнойИнформации);
	Организация = ?(ПоказыватьВзаиморасчетыВКолонках И ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено);
	
	РаботаСМетками.ПриСозданииНаСервере(ЭтотОбъект, Истина);
	
	НастройкиСписка = Новый Структура;
	НастройкиСписка.Вставить("ПоказыватьВзаиморасчетыВКолонках", ПоказыватьВзаиморасчетыВКолонках);
	НастройкиСписка.Вставить("ВестиУчетРасчетовСКонтрагентами", ВестиУчетРасчетовСКонтрагентами);
	НастройкиСписка.Вставить("Организация", Организация);
	Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("НастройкиСписка", НастройкиСписка);
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Справочник.Контрагенты",
		"ФормаСпискаПростойИнтерфейс",
		НСтр("ru='Новости: Контрагенты'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Элементы.Подменю1СПАРКРиски.Видимость = СПАРКРиски.ИспользованиеРазрешено();
	Элементы.СправочникКонтрагентыПерейтиВСПАРК.Видимость = СПАРКРиски.ИспользованиеРазрешено("ПереходВВебВерсиюСПАРК;");
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ЭлектронноеВзаимодействиеБП.КомандыЭДО_ФормаСпискаПриСоздании(ЭтотОбъект);
	Элементы.ЭДО.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД");
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	УправлениеПанельюПодсказки.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если КлючНазначенияИспользования = "ЛидыКонтрагенты" Тогда
		Элементы.ИзменитьВидимостьПодсказок.Видимость = Ложь;
		Элементы.ПанельПодсказки.Видимость = Ложь;
	КонецЕсли;
	
	Поля = Новый Массив;
	Поля.Добавить("Телефон");
	Поля.Добавить("НеоплаченныеСчета");
	Поля.Добавить("ЗадолженностьПокупателя");
	Поля.Добавить("ЗадолженностьПоставщику");
	Поля.Добавить("МеткиСтрокой");
	
	Список.УстановитьОграниченияИспользованияВГруппировке(Поля);
	Список.УстановитьОграниченияИспользованияВОтборе(Поля);
	Список.УстановитьОграниченияИспользованияВПорядке(Поля);
	
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы(МожноРедактировать);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ЭлектронноеВзаимодействиеБПКлиент.КомандыЭДО_ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОсновнаяОрганизация = Параметр;
		ПоказыватьВзаиморасчеты = ПоказыватьВзаиморасчеты(ВариантОтображенияДополнительнойИнформации);
		Если ПоказыватьВзаиморасчеты И ОсновнаяОрганизация <> ОтборОрганизация Тогда
			ОтборОрганизация = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
			ОбновитьОтборПоВзаиморасчетам();
		КонецЕсли;
	КонецЕсли;
	
	СобытияТребующиеОбновленияСписка = Новый Массив;
	СобытияТребующиеОбновленияСписка.Добавить("Запись_ПоступлениеТоваровУслуг");
	СобытияТребующиеОбновленияСписка.Добавить("Запись_РеализацияТоваровУслуг");
	СобытияТребующиеОбновленияСписка.Добавить("Запись_СчетПокупателю");
	СобытияТребующиеОбновленияСписка.Добавить("Запись_ПриходныйКассовыйОрдер");
	СобытияТребующиеОбновленияСписка.Добавить("Запись_РасходныйКассовыйОрдер");
	СобытияТребующиеОбновленияСписка.Добавить("Запись_ПоступлениеНаРасчетныйСчет");
	СобытияТребующиеОбновленияСписка.Добавить("Запись_СписаниеСРасчетногоСчета");
	СобытияТребующиеОбновленияСписка.Добавить("Запись_НачислениеПеней");
	
	Если ПоказыватьВзаиморасчетыВПодвале(ВариантОтображенияДополнительнойИнформации)
		И СобытияТребующиеОбновленияСписка.Найти(ИмяСобытия) <> Неопределено Тогда
		ОбновитьСвойстваТекущегоКонтрагента();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	УправлениеПанельюПодсказкиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	Если РаботаСМеткамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбработкаОповещенияФормыСМеткамиНаСервере();
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ЭлектронноеВзаимодействиеБПКлиент.КомандыЭДО_ФормаСпискаОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "ЖурналДокументов.ЖурналОпераций.Форма.ВыборТипаДокумента" Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка")
			Или ПустаяСтрока(ВыбранноеЗначение) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = ПараметрыФормыДокумента(Тип("ДокументСсылка." + ВыбранноеЗначение));
		ОткрытьФорму("Документ." + ВыбранноеЗначение + ".ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(ОсновнаяОрганизация) И ОтборОрганизация <> ОсновнаяОрганизация Тогда
		ОтборОрганизация = ОсновнаяОрганизация;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	КонецЕсли;
	
	Если Не ФункциональностьПрограммы.РазделЛидыДоступен() Тогда
		ОтборОтветственныйИспользование = Ложь;
	КонецЕсли;
	
	УстановитьВосстановленныеОтборы();
	
	РаботаСМетками.ПриЗагрузкеДанныхИзНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьОтчетОбщаяКоманда(Команда)

	ПараметрыОтчета = Новый Структура;
	
	Если Команда.Имя = "ОтчетАнализНеоплаченныхСчетовПокупателями" Тогда
		ИмяФормыОтчета = "Отчет.АнализНеоплаченныхСчетовПокупателям.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетАнализНеоплаченныхСчетовПоставщиков" Тогда
		ИмяФормыОтчета = "Отчет.АнализНеоплаченныхСчетовПоставщиков.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетДинамикаЗадолженностиПокупателей" Тогда
		ИмяФормыОтчета = "Отчет.ДинамикаЗадолженностиПокупателей.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетДинамикаЗадолженностиПоставщикам" Тогда
		ИмяФормыОтчета = "Отчет.ДинамикаЗадолженностиПоставщикам.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетЗадолженностьПокупателей" Тогда
		
		ИмяФормыОтчета = "Отчет.ЗадолженностьПокупателей.ФормаОбъекта";
		ПараметрыОтчета.Вставить("КлючВарианта", "ЗадолженностьПокупателей");
		Уникальность = "Отчет.ЗадолженностьПокупателей/КлючВарианта." + ПараметрыОтчета.КлючВарианта;
	
		ПараметрыОтчета.Вставить("КлючПараметровПечати",        Уникальность);
		ПараметрыОтчета.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
		
	ИначеЕсли Команда.Имя = "ОтчетЗадолженностьПокупателейПоСрокамДолга" Тогда
		ИмяФормыОтчета = "Отчет.ЗадолженностьПокупателейПоСрокамДолга.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетЗадолженностьПоставщикамПоСрокамДолга" Тогда
		ИмяФормыОтчета = "Отчет.ЗадолженностьПоставщикамПоСрокамДолга.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетЗадолженностьПоставщикам" Тогда
		
		ИмяФормыОтчета = "Отчет.ЗадолженностьПоставщикам.ФормаОбъекта";
		ПараметрыОтчета.Вставить("КлючВарианта", "ЗадолженностьПоставщикам");
		Уникальность = "Отчет.ЗадолженностьПоставщикам/КлючВарианта." + ПараметрыОтчета.КлючВарианта;
	
		ПараметрыОтчета.Вставить("КлючПараметровПечати",        Уникальность);
		ПараметрыОтчета.Вставить("КлючСохраненияПоложенияОкна", Уникальность);

	ИначеЕсли Команда.Имя = "ОтчетОборотныеСредства" Тогда
		ИмяФормыОтчета = "Отчет.ОборотныеСредства.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетРасчетыСПокупателямиИПоставщиками" Тогда
		ИмяФормыОтчета = "Отчет.РасчетыСПокупателямиИПоставщиками.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетПродажиПоКонтрагентамПоОплате" Тогда
		
		ПараметрыОтчета.Вставить("КлючВарианта", "ПродажиПоКонтрагентамПоОплате");
		Уникальность = "Отчет.Продажи/КлючВарианта." + ПараметрыОтчета.КлючВарианта;
	
		ПараметрыОтчета.Вставить("КлючПараметровПечати",        Уникальность);
		ПараметрыОтчета.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
		
		ИмяФормыОтчета = "Отчет.Продажи.Форма";
		
	ИначеЕсли Команда.Имя = "ОтчетыПродажиПоКонтрагентам" Тогда
		
		ПараметрыОтчета.Вставить("КлючВарианта", "ПродажиПоКонтрагентам");
		Уникальность = "Отчет.Продажи/КлючВарианта." + ПараметрыОтчета.КлючВарианта;
	
		ПараметрыОтчета.Вставить("КлючПараметровПечати",        Уникальность);
		ПараметрыОтчета.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
	
		ИмяФормыОтчета = "Отчет.Продажи.Форма";
	
	ИначеЕсли Команда.Имя = "ОтчетыСравнениеПродажПоКонтрагентам" Тогда
		
		ПараметрыОтчета.Вставить("КлючВарианта", "СравнениеПродажПоКонтрагентам");
		Уникальность = СтрШаблон("Отчет.СравнениеПродаж/КлючВарианта.%1", ПараметрыОтчета.КлючВарианта);
		
		ПараметрыОтчета.Вставить("КлючПараметровПечати",        Уникальность);
		ПараметрыОтчета.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
		
		ИмяФормыОтчета = "Отчет.СравнениеПродаж.Форма";
		
	ИначеЕсли Команда.Имя = "ОтчетСверкаДокументовСКонтрагентами" Тогда
		ПараметрыОтчета.Вставить("ОтложенноеФормирование", Истина);
		ИмяФормыОтчета = "Отчет.СверкаРасчетовСКонтрагентами.ФормаОбъекта";
	ИначеЕсли Команда.Имя = "ОтчетПродажиПоОтветственным" Тогда
		
		ПараметрыОтчета.Вставить("КлючВарианта", "ПродажиПоОтветственным");
		Уникальность = "Отчет.Продажи/КлючВарианта." + ПараметрыОтчета.КлючВарианта;
	
		ПараметрыОтчета.Вставить("КлючПараметровПечати",        Уникальность);
		ПараметрыОтчета.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
	
		ИмяФормыОтчета = "Отчет.Продажи.Форма";
		
	КонецЕсли;
	
	ФормаОтчета = ПолучитьФорму(ИмяФормыОтчета, ПараметрыОтчета, ЭтотОбъект);
	ФормаОтчета.ПодключитьОбработчикОжидания("Подключаемый_СформироватьПриОткрытии", 
		БухгалтерскиеОтчетыКлиент.ИнтервалЗапускаФормированияОтчетаПриОткрытии(), 
		Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ФормаОтчета.Элементы.Результат, "ФормированиеОтчета");
	ФормаОтчета.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументСчетПокупателю(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.СчетНаОплатуПокупателю"));
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументАкт(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.РеализацияТоваровУслуг"),
		ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Услуги"));
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументНакладная(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.РеализацияТоваровУслуг"),
		ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары"));
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументСчетПоставщика(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
	ОткрытьФорму("Документ.СчетНаОплатуПоставщика.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументАктПоступление(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.ПоступлениеТоваровУслуг"),
		ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги"));
	ОткрытьФорму("Документ.ПоступлениеТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументНакладнаяПоступление(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.ПоступлениеТоваровУслуг"),
		ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Товары"));
	ОткрытьФорму("Документ.ПоступлениеТоваровУслуг.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументАктСверкиВзаиморасчетов(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.АктСверкиВзаиморасчетов"));
	ОткрытьФорму("Документ.АктСверкиВзаиморасчетов.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументНачислениеПеней(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.НачислениеПеней"));
	ОткрытьФорму("Документ.НачислениеПеней.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДругойДокумент(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ДокументыПоКонтрагенту");
	
	ОткрытьФорму("ЖурналДокументов.ЖурналОпераций.Форма.ВыборТипаДокумента", ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ИндексыСПАРК(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ИндексыСПАРКРиски.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СобытияМониторингаСПАРК(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.СобытияМониторингаСПАРКРиски.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументКоммерческоеПредложение(Команда)
	
	СтруктураПараметров = ПараметрыФормыДокумента(Тип("ДокументСсылка.КоммерческоеПредложение"));
	ОткрытьФорму("Документ.КоммерческоеПредложение.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗвонок(Команда)

	СоздатьНовоеВзаимодействие("ТелефонныйЗвонок");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПисьмо(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЭлектронноеПисьмоПродолжение", ЭтотОбъект, );
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВстречу(Команда)
	
	СоздатьНовоеВзаимодействие("Встреча");
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПрочееВзаимодействие(Команда)
	
		СоздатьНовоеВзаимодействие("ПрочееВзаимодействие");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	
	ОтборОтветственныйИспользование = ЗначениеЗаполнено(ОтборОтветственный);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Ответственный");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Ответственный");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОбновитьОтборПоВзаиморасчетам();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОбновитьОтборПоВзаиморасчетам();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНеоплаченныеСчетаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяСтрока = Элементы.Список.ТекущаяСтрока;
	СформироватьОтчетПоНеоплаченнымСчетамПокупателем(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентЗадолженностьПокупателяНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяСтрока = Элементы.Список.ТекущаяСтрока;
	СформироватьОтчетПоЗадолженностиПокупателей(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентЗадолженностьПоставщикуНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяСтрока = Элементы.Список.ТекущаяСтрока;
	СформироватьОтчетПоЗадолженностиПоставщикам(ВыбраннаяСтрока);
	
КонецПроцедуры

#Область Панель_Подсказки

&НаКлиенте
Процедура НавигацияВПрограммеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.НавигацияВПрограммеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяФункциональностьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.ДополнительнаяФункциональностьОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылка,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе4ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область Метки

&НаКлиенте
Процедура ИспользоватьОтборМеткиПриИзменении(Элемент)
	
	РаботаСМеткамиКлиент.ИспользоватьОтборМеткиПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиПриИзменении(Элемент)
	
	ОтборМеткиПриИзмененииНаСервере();
	
	РаботаСМеткамиКлиент.ОтборМеткиПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиСоздание(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОткрытиеМножественногоЗначения(Элемент, Идентификатор, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОткрытиеМножественногоЗначения(ЭтотОбъект, Элемент, Идентификатор, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если Поле.Имя = "НеоплаченныеСчета"
		И Не ПустаяСтрока(ТекущиеДанные.НеоплаченныеСчета) Тогда
		
		СтандартнаяОбработка = Ложь;
		СформироватьОтчетПоНеоплаченнымСчетамПокупателем(ВыбраннаяСтрока);
		
	ИначеЕсли Поле.Имя = "ЗадолженностьПоставщику"
		И ТекущиеДанные.ЗадолженностьПоставщику <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		СформироватьОтчетПоЗадолженностиПоставщикам(ВыбраннаяСтрока);

	ИначеЕсли Поле.Имя = "ЗадолженностьПокупателя"
		И ТекущиеДанные.ЗадолженностьПокупателя <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		СформироватьОтчетПоЗадолженностиПокупателей(ВыбраннаяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ПоказыватьВзаиморасчетыВПодвале(ВариантОтображенияДополнительнойИнформации) Тогда
		ПодключитьОбработчикОжидания("ОбновитьСвойстваТекущегоКонтрагента", 0.2, Истина);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриАктивизацииСтроки_ФормаСписка(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Группа Тогда
		КлючеваяОперация = "СозданиеФормыКонтрагенты";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Если НЕ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		КлючеваяОперация = "ОткрытиеФормыКонтрагенты";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	НастройкиСписка = Настройки.ДополнительныеСвойства.НастройкиСписка;
	
	Если НастройкиСписка.ПоказыватьВзаиморасчетыВКолонках
		И Строки.Количество() > 0 Тогда
		
		ТаблицаВзаиморасчетов = Справочники.Контрагенты.ДополнительнаяИнформацияДляФормыСписка(
			Строки.ПолучитьКлючи(), НастройкиСписка.ВестиУчетРасчетовСКонтрагентами, НастройкиСписка.Организация);
		
		Для Каждого СтрокаВзаиморасчетов Из ТаблицаВзаиморасчетов Цикл
			Строка = Строки.Получить(СтрокаВзаиморасчетов.Контрагент);
			ЗаполнитьЗначенияСвойств(Строка.Данные, СтрокаВзаиморасчетов);
			Если Строка.Данные.Свойство("НеоплаченныеСчета")
				И СтрокаВзаиморасчетов.НеоплаченныеСчетаПокупателемКоличество <> 0 Тогда
				Строка.Данные.НеоплаченныеСчета = ПредставлениеНеоплаченногоСчета(СтрокаВзаиморасчетов);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РаботаСМетками.ПриПолученииДанныхНаСервере(Настройки, Строки);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьЭлектронноеПисьмоПродолжение(ПроверкаВыполнена, ДополнительныеПараметры) Экспорт
	
	Если ПроверкаВыполнена = Истина Тогда
		СоздатьНовоеВзаимодействие("ЭлектронноеПисьмоИсходящее");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеВзаимодействие(ТипОбъекта)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Контрагент = ТекущиеДанные.Ссылка;
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Контакт", Контрагент);	
	ДанныеУчастника.Вставить("КакСвязаться", "");
	ДанныеУчастника.Вставить("Представление", Строка(Контрагент));	
		
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ДанныеУчастника", ДанныеУчастника);

	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие(ТипОбъекта, ПараметрыСоздания, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ПоказыватьВзаиморасчетыВКолонках = ПоказыватьВзаиморасчетыВКолонках(Форма.ВариантОтображенияДополнительнойИнформации);
	
	ДоступнаЗадолженностьПоставщику = Форма.ВестиУчетРасчетовСКонтрагентами И Форма.ДоступенОтчетЗадолженностьПоставщику;
	ДоступнаЗадолженностьПокупателя = Форма.ВестиУчетРасчетовСКонтрагентами И Форма.ДоступенОтчетЗадолженностьПокупателя;
	ДоступныНеоплаченныеСчета = Форма.ДоступенОтчетАнализНеоплаченныхСчетов;
	
	Элементы.ЗадолженностьПоставщику.Видимость = ПоказыватьВзаиморасчетыВКолонках И ДоступнаЗадолженностьПоставщику;
	Элементы.ЗадолженностьПокупателя.Видимость = ПоказыватьВзаиморасчетыВКолонках И ДоступнаЗадолженностьПокупателя;
	Элементы.НеоплаченныеСчета.Видимость       = ПоказыватьВзаиморасчетыВКолонках И ДоступныНеоплаченныеСчета;
	Элементы.Телефон.Видимость                 = ПоказыватьВзаиморасчетыВКолонках;
	
	Элементы.ГруппаОтборПоОрганизации.Видимость = ПоказыватьВзаиморасчеты(Форма.ВариантОтображенияДополнительнойИнформации)
		И (ДоступнаЗадолженностьПоставщику Или ДоступнаЗадолженностьПокупателя Или ДоступныНеоплаченныеСчета);
	
	КолонкиСписка = Элементы.Список.ПодчиненныеЭлементы;
	КолонкиСписка.ЗадолженностьПокупателя.ГиперссылкаЯчейки = Форма.ДоступенОтчетЗадолженностьПокупателя;
	КолонкиСписка.ЗадолженностьПоставщику.ГиперссылкаЯчейки = Форма.ДоступенОтчетЗадолженностьПоставщику;
	КолонкиСписка.НеоплаченныеСчета.ГиперссылкаЯчейки       = Форма.ДоступенОтчетАнализНеоплаченныхСчетов;
	
	ЦветТекстаПоля = ЦветТекстаПоля();
	ЦветГиперссылки = ЦветГиперссылки();
	
	КолонкиСписка.ЗадолженностьПокупателя.ЦветТекста = ?(Форма.ДоступенОтчетЗадолженностьПокупателя,
		ЦветГиперссылки, ЦветТекстаПоля);
	КолонкиСписка.ЗадолженностьПоставщику.ЦветТекста = ?(Форма.ДоступенОтчетЗадолженностьПоставщику,
		ЦветГиперссылки, ЦветТекстаПоля);
	КолонкиСписка.НеоплаченныеСчета.ЦветТекста = ?(Форма.ДоступенОтчетАнализНеоплаченныхСчетов,
		ЦветГиперссылки, ЦветТекстаПоля);
	
	Элементы.ГруппаСвойстваКонтрагента.Видимость = ПоказыватьВзаиморасчетыВПодвале(Форма.ВариантОтображенияДополнительнойИнформации);
	Элементы.КонтрагентЗадолженностьПокупателя.Видимость = ДоступнаЗадолженностьПокупателя;
	Элементы.КонтрагентЗадолженностьПоставщику.Видимость = ДоступнаЗадолженностьПоставщику;
	Элементы.КонтрагентНеоплаченныеСчета.Видимость       = ДоступныНеоплаченныеСчета;
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервереБезКонтекста
Функция СвойстваКонтрагента(Знач Контрагент, Знач ВестиУчетРасчетовСКонтрагентами, Знач Организация)
	
	Результат = Новый Структура;
	Результат.Вставить("Телефон", "Не указан");
	Результат.Вставить("ЗадолженностьПоставщику", "Нет");
	Результат.Вставить("ЗадолженностьПокупателя", "Нет");
	Результат.Вставить("КонтрагентНеоплаченныеСчета", "Нет");
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Контрагенты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Контрагент);
	ДополнительнаяИнформация = Справочники.Контрагенты.ДополнительнаяИнформацияДляФормыСписка(Контрагенты,
		ВестиУчетРасчетовСКонтрагентами, Организация);
	Если ДополнительнаяИнформация.Количество() > 0 Тогда
		
		Информация = ДополнительнаяИнформация[0];
		
		Если ЗначениеЗаполнено(Информация.Телефон) Тогда
			Результат.Телефон = Информация.Телефон;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Информация.ЗадолженностьПоставщику) Тогда
			Результат.ЗадолженностьПоставщику = СтрШаблон("%1 %2",
				Формат(Информация.ЗадолженностьПоставщику, "ЧДЦ=0; ЧН=0;"),
				ВалютаРегламентированногоУчета);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Информация.ЗадолженностьПокупателя) Тогда
			Результат.ЗадолженностьПокупателя = СтрШаблон("%1 %2",
				Формат(Информация.ЗадолженностьПокупателя, "ЧДЦ=0; ЧН=0;"),
				ВалютаРегламентированногоУчета);
		КонецЕсли;
		
		НеоплаченныеСчета = ПредставлениеНеоплаченногоСчета(Информация, ВалютаРегламентированногоУчета);
		Если ЗначениеЗаполнено(НеоплаченныеСчета) Тогда
			Результат.КонтрагентНеоплаченныеСчета = НеоплаченныеСчета;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();


	// ИНН некорректный

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИНН");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ИННВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);


	// ИНН дублируется

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИНН");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ИННВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ЕстьДубли", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);


	// КПП некорректный

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КППВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);


	// КПП дублируется

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КППВведенКорректно", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ЕстьДубли", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой);


	// Контрагента нет в реестре

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИНН");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Состояние", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.СостоянияСуществованияКонтрагента.НеДействуетИлиИзмененКПП);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветКонтрагентаОтсутствущегоВРеестре);


	// Контрагент прекратил деятельность

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.КонтрагентНеСуществует", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Состояние", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СостоянияСуществованияКонтрагента.НеДействуетИлиИзмененКПП);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветКонтрагентаПрекратившегоДеятельность);
	
	
	// Метки
	
	РаботаСМетками.УстановитьУсловноеОформление(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСвойстваТекущегоКонтрагента()
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.ЭтоГруппа Тогда
		
		КонтрагентТелефон           = "";
		КонтрагентЗадолженностьПоставщику = "";
		КонтрагентЗадолженностьПокупателя = "";
		КонтрагентНеоплаченныеСчета = "";
		
	Иначе
		
		Организация = ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено);
		
		СвойстваКонтрагента = СвойстваКонтрагента(ТекущиеДанные.Ссылка, ВестиУчетРасчетовСКонтрагентами, Организация);
		
		КонтрагентТелефон = СвойстваКонтрагента.Телефон;
		КонтрагентЗадолженностьПоставщику = СвойстваКонтрагента.ЗадолженностьПоставщику;
		КонтрагентЗадолженностьПокупателя = СвойстваКонтрагента.ЗадолженностьПокупателя;
		КонтрагентНеоплаченныеСчета = СвойстваКонтрагента.КонтрагентНеоплаченныеСчета;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеНеоплаченногоСчета(СвойстваКонтрагента, Валюта = Неопределено)
	
	Количество = СвойстваКонтрагента.НеоплаченныеСчетаПокупателемКоличество;
	Сумма = Формат(СвойстваКонтрагента.НеоплаченныеСчетаПокупателемСумма, "ЧДЦ=0");
	
	Если Количество = 0 Тогда
		Представление = "";
	Иначе
		Если ЗначениеЗаполнено(Валюта) Тогда
			Представление = СтрШаблон(НСтр("ru='%1 на %2 %3'"), Количество, Сумма, Валюта);
		Иначе
			Представление = СтрШаблон(НСтр("ru='%1 на %2'"), Количество, Сумма);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

&НаСервере
Процедура ИзменитьВидимостьПодсказокНаСервере()
	
	УправлениеПанельюПодсказки.ИзменитьВидимостьПодсказок(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВедетсяУчетПоДоговорам()
	
	Возврат ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетПоЗадолженностиПокупателей(Контрагент)
	
	ИмяОтчета = "ЗадолженностьПокупателей";

	Если ВедетсяУчетПоДоговорам() Тогда
		КлючВарианта = "ЗадолженностьПокупателейПоДоговорам";
	Иначе
		КлючВарианта = "ЗадолженностьПокупателей";
	КонецЕсли;

	СформироватьОтчетПоВзаиморасчетам(ИмяОтчета, КлючВарианта, Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетПоЗадолженностиПоставщикам(Контрагент)
	
	ИмяОтчета = "ЗадолженностьПоставщикам";
	
	Если ВедетсяУчетПоДоговорам() Тогда
		КлючВарианта = "ЗадолженностьПоставщикамПоДоговорам";
	Иначе
		КлючВарианта = "ЗадолженностьПоставщикам";
	КонецЕсли;

	СформироватьОтчетПоВзаиморасчетам(ИмяОтчета, КлючВарианта, Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетПоНеоплаченнымСчетамПокупателем(Контрагент)

	ИмяОтчета = "АнализНеоплаченныхСчетовПокупателям";

	КлючВарианта = "НеоплаченныеСчетаПокупателям";
	СформироватьОтчетПоВзаиморасчетам(ИмяОтчета, КлючВарианта, Контрагент);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыДляОтчетаНаСервере(ИмяОтчета, КлючВарианта, Контрагент, Организация)
	
	Возврат Справочники.Контрагенты.ПараметрыДляОтчета(ИмяОтчета, КлючВарианта, Контрагент, Организация);
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетПоВзаиморасчетам(ИмяОтчета, КлючВарианта, Контрагент)
	
	Организация = ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено);
	ПараметрыОтчета = ПараметрыДляОтчетаНаСервере(ИмяОтчета, КлючВарианта, Контрагент, Организация);
	
	ИмяФормыОтчета = СтрШаблон("Отчет.%1.Форма.ФормаОтчета", ИмяОтчета);
	
	ОткрытьФорму(ИмяФормыОтчета, ПараметрыОтчета, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыФормыДокумента(ТипДокумента, ВидОперации = Неопределено)
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	
	ПараметрыФормы = Новый Структура;
	
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	Если ЗначениеЗаполнено(ВидОперации) Тогда
		ЗначенияЗаполнения.Вставить("ВидОперации", ВидОперации);
	КонецЕсли;
	
	Если ОтборОрганизацияИспользование Тогда
		ЗначенияЗаполнения.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если ТекущиеДанные <> Неопределено И Не ТекущиеДанные.ЭтоГруппа Тогда
		Если ТипДокумента = Тип("ДокументСсылка.КорректировкаДолга") Тогда
			ЗначенияЗаполнения.Вставить("КонтрагентДебитор", ТекущиеДанные.Ссылка);
			ЗначенияЗаполнения.Вставить("КонтрагентКредитор", ТекущиеДанные.Ссылка);
		Иначе
			ЗначенияЗаполнения.Вставить("Контрагент", ТекущиеДанные.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервере
Процедура НастроитьОперацииПодменюДокументы()
	
	ТипыДокументов = Новый Массив;
	ДокументыЖурнала = ЖурналыДокументов.ЖурналОпераций.СоставДокументов("ДокументыПоКонтрагенту");
	Для Каждого ОбъектМетаданных Из ДокументыЖурнала Цикл
		Если ПравоДоступа("Изменение", ОбъектМетаданных) Тогда
			ТипыДокументов.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	ЕстьПравоИзменениеПоступлениеТоваровУслуг = ТипыДокументов.Найти(Метаданные.Документы.ПоступлениеТоваровУслуг) <> Неопределено;
	ЕстьПравоИзменениеРеализацияТоваровУслуг  = ТипыДокументов.Найти(Метаданные.Документы.РеализацияТоваровУслуг) <> Неопределено;
	ЕстьПравоИзменениеСчетНаОплату            = ТипыДокументов.Найти(Метаданные.Документы.СчетНаОплатуПокупателю) <> Неопределено;
	ЕстьПравоИзменениеСчетПоставщика          = ТипыДокументов.Найти(Метаданные.Документы.СчетНаОплатуПоставщика) <> Неопределено;
	ЕстьПравоИзменениеАктСверкиВзаиморасчетов = ТипыДокументов.Найти(Метаданные.Документы.АктСверкиВзаиморасчетов) <> Неопределено;
	ЕстьПравоИзменениеНачислениеПеней         = ТипыДокументов.Найти(Метаданные.Документы.НачислениеПеней) <> Неопределено;
	ЕстьПравоИзменениеКоммерческоеПредложение = ТипыДокументов.Найти(Метаданные.Документы.КоммерческоеПредложение) <> Неопределено;
	
	Элементы.СоздатьДокументКоммерческоеПредложение.Видимость = ЕстьПравоИзменениеКоммерческоеПредложение;
	Элементы.СоздатьДокументСчетПокупателю.Видимость          = ЕстьПравоИзменениеСчетНаОплату;
	Элементы.СоздатьДокументАкт.Видимость                     = ЕстьПравоИзменениеРеализацияТоваровУслуг;
	Элементы.СоздатьДокументНакладная.Видимость               = ЕстьПравоИзменениеРеализацияТоваровУслуг;
	Элементы.СоздатьДокументСчетПоставщика.Видимость          = ЕстьПравоИзменениеСчетПоставщика;  
	
	ДоступныеЗначенияОперацийПоступления = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.СписокДоступныхЗначений();

	ОперацияПоступлениеУслуг = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги;
	ОперацияПоступлениеУслугДоступна = 
		ДоступныеЗначенияОперацийПоступления.НайтиПоЗначению(ОперацияПоступлениеУслуг) <> Неопределено;
		
	Элементы.СоздатьДокументАктПоступление.Видимость          =
		ЕстьПравоИзменениеПоступлениеТоваровУслуг И ОперацияПоступлениеУслугДоступна;
		
	ОперацияПоступлениеТоваров = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары;
	ОперацияПоступлениеТоваровДоступна = 
		ДоступныеЗначенияОперацийПоступления.НайтиПоЗначению(ОперацияПоступлениеТоваров) <> Неопределено;

	Элементы.СоздатьДокументНакладнаяПоступление.Видимость    =
		ЕстьПравоИзменениеПоступлениеТоваровУслуг И ОперацияПоступлениеТоваровДоступна;
		
	Элементы.СоздатьДокументАктСверкиВзаиморасчетов.Видимость = ЕстьПравоИзменениеАктСверкиВзаиморасчетов;
	Элементы.СоздатьДокументНачислениеПеней.Видимость         = ЕстьПравоИзменениеНачислениеПеней;
	Элементы.СоздатьДругойДокумент.Видимость                  = ТипыДокументов.Количество() > 0;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтчетДоступен(ИмяОтчета)
	
	Возврат ПравоДоступа("Использование", Метаданные.Отчеты[ИмяОтчета]);

КонецФункции

&НаСервереБезКонтекста
Функция ЦветГиперссылки()
	
	Возврат ЦветаСтиля.ЦветГиперссылки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЦветТекстаПоля()
	
	Возврат ЦветаСтиля.ЦветТекстаПоля;
	
КонецФункции

&НаСервере
Процедура НастроитьОперацииПодменюОтчеты()
	
	ЕстьПравоОтчетПродажи = ОтчетДоступен("Продажи");
	Элементы.ОтчетыПродажиПоКонтрагентам.Видимость = ЕстьПравоОтчетПродажи;
	Элементы.ОтчетПродажиПоКонтрагентамПоОплате.Видимость = ЕстьПравоОтчетПродажи;
	Элементы.ОтчетПродажиПоОтветственным.Видимость = ЕстьПравоОтчетПродажи;

	ЕстьПравоОтчетСравнениеПродаж = ОтчетДоступен("СравнениеПродаж");
	Элементы.ОтчетыСравнениеПродажПоКонтрагентам.Видимость = ЕстьПравоОтчетСравнениеПродаж;
	
	ЕстьПравоОтчетРасчетыСПокупателямиИПоставщиками = ОтчетДоступен("РасчетыСПокупателямиИПоставщиками");
	Элементы.ОтчетРасчетыСПокупателямиИПоставщиками.Видимость = ЕстьПравоОтчетРасчетыСПокупателямиИПоставщиками;
	
	ЕстьПравоОтчетДинамикаЗадолженностиПокупателей = ОтчетДоступен("ДинамикаЗадолженностиПокупателей");
	Элементы.ОтчетДинамикаЗадолженностиПокупателей.Видимость = ЕстьПравоОтчетДинамикаЗадолженностиПокупателей;
	
	ДоступенОтчетЗадолженностьПокупателя = ОтчетДоступен("ЗадолженностьПокупателей");
	Элементы.ОтчетЗадолженностьПокупателей.Видимость = ДоступенОтчетЗадолженностьПокупателя;
	
	ЕстьПравоОтчетЗадолженностьПокупателейПоСрокамДолга = ОтчетДоступен("ЗадолженностьПокупателейПоСрокамДолга");
	Элементы.ОтчетЗадолженностьПокупателейПоСрокамДолга.Видимость = ЕстьПравоОтчетЗадолженностьПокупателейПоСрокамДолга;
	
	ДоступенОтчетАнализНеоплаченныхСчетов = ОтчетДоступен("АнализНеоплаченныхСчетовПокупателям");
	Элементы.ОтчетАнализНеоплаченныхСчетовПокупателям.Видимость = ДоступенОтчетАнализНеоплаченныхСчетов;
	
	ЕстьПравоОтчетДинамикаЗадолженностиПоставщикам = ОтчетДоступен("ДинамикаЗадолженностиПоставщикам");
	Элементы.ОтчетДинамикаЗадолженностиПоставщикам.Видимость = ЕстьПравоОтчетДинамикаЗадолженностиПоставщикам;
	
	ДоступенОтчетЗадолженностьПоставщику = ОтчетДоступен("ЗадолженностьПоставщикам");
	Элементы.ОтчетЗадолженностьПоставщикам.Видимость = ДоступенОтчетЗадолженностьПоставщику;
	
	ЕстьПравоОтчетЗадолженностьПоставщикамПоСрокамДолга = ОтчетДоступен("ЗадолженностьПоставщикамПоСрокамДолга");
	Элементы.ОтчетЗадолженностьПоставщикамПоСрокамДолга.Видимость = ЕстьПравоОтчетЗадолженностьПоставщикамПоСрокамДолга;
	
	ЕстьПравоОтчетАнализНеоплаченныхСчетовПоставщиков = ОтчетДоступен("АнализНеоплаченныхСчетовПоставщиков");
	Элементы.ОтчетАнализНеоплаченныхСчетовПоставщиков.Видимость = ЕстьПравоОтчетАнализНеоплаченныхСчетовПоставщиков;
		
	ЕстьПравоОтчетОборотныеСредства = ОтчетДоступен("ОборотныеСредства");
	Элементы.ОтчетОборотныеСредства.Видимость = ЕстьПравоОтчетОборотныеСредства;

КонецПроцедуры

&НаСервере
Процедура ОтборМеткиПриИзмененииНаСервере()
	
	РаботаСМетками.МеткиПриИзмененииНаСервере(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Функция ОбработкаОповещенияФормыСМеткамиНаСервере()
	
	РаботаСМетками.ОбработкаОповещенияНаСервере(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Ответственный");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтборПоВзаиморасчетам()
	
	Если ПоказыватьВзаиморасчетыВПодвале(ВариантОтображенияДополнительнойИнформации) Тогда
		ОбновитьСвойстваТекущегоКонтрагента();
	КонецЕсли;
	
	Если ПоказыватьВзаиморасчетыВКолонках(ВариантОтображенияДополнительнойИнформации) Тогда
		Организация = ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено);
		Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.НастройкиСписка.Вставить("Организация", Организация);
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПоказыватьВзаиморасчеты(ВариантОтображенияДополнительнойИнформации)
	
	Возврат ПоказыватьВзаиморасчетыВКолонках(ВариантОтображенияДополнительнойИнформации)
		Или ПоказыватьВзаиморасчетыВПодвале(ВариантОтображенияДополнительнойИнформации);
	
КонецФункции
	
&НаКлиентеНаСервереБезКонтекста
Функция ПоказыватьВзаиморасчетыВКолонках(ВариантОтображенияДополнительнойИнформации)
	
	Возврат ВариантОтображенияДополнительнойИнформации = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияДополнительнойИнформацииВСписках.ВКолонках");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоказыватьВзаиморасчетыВПодвале(ВариантОтображенияДополнительнойИнформации)
	
	Возврат ВариантОтображенияДополнительнойИнформации = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияДополнительнойИнформацииВСписках.ВПодвале");
	
КонецФункции

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", Неопределено);
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияКонтрагенты());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЭДО

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы(МожноРедактировать)
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы(МожноРедактировать);
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы(МожноРедактировать)
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.КомандаСоздать = "Создать";
	КомандыПоля.РазрешеноИзменение = МожноРедактировать;
	КомандыПоля.ДоступноУправлениеОтбором = ПолучитьФункциональнуюОпцию("ИспользоватьЛидыПолныйИнтерфейс")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьЛидыПростойИнтерфейс");
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКПерваяКомандаПанели(ЭтотОбъект, Элементы.ГруппаОтчеты);
	Если Элементы.Найти("СоздатьГруппу") <> Неопределено Тогда
		МобильныйКлиентБПФормаСписка.ПеренестиЭлементВМККомандыФормы(ЭтотОбъект, Элементы.СоздатьГруппу);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьОтбор(Команда)
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойОтбораМК(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОтбораМК(ЭтотОбъект, "Список", "");
	
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораБыстрыеОтборы(ЭтотОбъект,
		Элементы.ОтборОтветственный,
		НСтр("ru = 'Ответственный'"),
		"Все ответственные");
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораБыстрыеОтборы(ЭтотОбъект,
		Элементы.ОтборМетки,
		НСтр("ru = 'Метки'"),
		НСтр("ru = 'Любая метка'"));
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.ГруппаДополнительныеОтборы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.ГруппаДокументы,
		Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.ГруппаКомандВзаимодействия,
		Элементы.Список.КонтекстноеМеню);
	
	ОбщаяКомандаПроверкаКонтрагента = Элементы.Найти("ОбработкаПроверкаКонтрагентаПроверкаКонтрагента");
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		ОбщаяКомандаПроверкаКонтрагента,
		Элементы.Список.КонтекстноеМеню);

КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормыСправочника(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Наименование);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.ЭДО);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.ИНН, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.КПП, 1);
	Если Элементы.ГруппаМетки.Видимость Тогда
		МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.МеткиСтрокой, 2);
		МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Ответственный, 2);
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
	Элементы.ГруппаДокументы.Заголовок = НСтр("ru = 'Создать на основании'");
	
КонецПроцедуры

#КонецОбласти
