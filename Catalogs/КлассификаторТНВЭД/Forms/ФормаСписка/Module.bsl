
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.КлассификаторТНВЭД) Тогда
		Элементы.ПодборИзТНВЭД.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПослеДобавленияТНВЭД" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Текст = НСтр("ru = 'Есть возможность подобрать товарную номенклатуру ВЭД из классификатора.
		|Подобрать?'");
	
	ДополнительныеПараметры = Новый Структура;
	Если Копирование Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Код", Элемент.ТекущиеДанные.Код);
		ЗначенияЗаполнения.Вставить("Наименование", Элемент.ТекущиеДанные.Наименование);
		ДополнительныеПараметры.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВопросПодобратьТНВЭДЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборИзТНВЭД(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПодборИзКлассификатораЗавершение", ЭтотОбъект);
	
	НастройкиРежима = Новый Структура;
	НастройкиРежима.Вставить("Владелец", ЭтотОбъект);
	НастройкиРежима.Вставить("РежимВыбораЭлемента", Истина);
	НастройкиРежима.Вставить("ОбработчикЗавершения", ОповещениеОЗакрытии);
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		НастройкиРежима.Вставить("КодВыбранногоЭлемента", Элементы.Список.ТекущиеДанные.Код);
	КонецЕсли;
	
	ОблачныеКлассификаторыКлиент.ПодобратьИзСервисаЭлементыТНВЭД(НастройкиРежима);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПодобратьТНВЭДЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПодборИзТНВЭД(Неопределено);
	Иначе
		ОткрытьФорму("Справочник.КлассификаторТНВЭД.Форма.ФормаЭлемента", ДополнительныеПараметры, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборИзКлассификатораЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовыйЭлемент = ДобавитьЭлементКлассификатора(РезультатВыбора);
	
	ОповеститьОбИзменении(НовыйЭлемент);
	
	Элементы.Список.ТекущаяСтрока = НовыйЭлемент;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ДобавитьЭлементКлассификатора(СвойстваЭлемента)
	
	Если НЕ ЗначениеЗаполнено(СвойстваЭлемента.Код) Тогда
		Возврат Справочники.КлассификаторТНВЭД.ПустаяСсылка();
	КонецЕсли;
	
	ЭлементТНВЭД = Справочники.КлассификаторТНВЭД.СоздатьОбновитьЭлементКлассификатора(
		СвойстваЭлемента);
	
	Возврат ЭлементТНВЭД;
	
КонецФункции

#КонецОбласти