#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура СоздатьЭлементыСпецодежда() Экспорт 
	
	Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Спецодежда")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Спецодежда";
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаНаСкладе;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатации;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
						
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Спецоснастка")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Спецоснастка";
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаНаСкладе;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатации;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Инвентарь и хозяйственные принадлежности")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Инвентарь и хозяйственные принадлежности";
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежности;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку;
		Запись.СчетУчетаЗабалансовый                = ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку;
		
	КонецЕсли;
	
	Если Набор.Количество() > 0 Тогда
		Набор.Записать(Ложь);
	КонецЕсли;
	
	СоздатьЭлементыМалоценка();
	
КонецПроцедуры

Процедура СоздатьЭлементыВозвратнаяТара() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Возвратная тара")) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Возвратная тара";
		ВидНоменклатуры.ПолучаемОтПоставщика = Истина;
		ВидНоменклатуры.СобственныеНужды     = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ТараПодТоваромИПорожняя;
		
		Набор.Записать(Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура СоздатьЭлементыОС() Экспорт 
	
	Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Оборудование (объекты основных средств)")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Оборудование (объекты основных средств)";
		ВидНоменклатуры.ПолучаемОтПоставщика = Истина;
		ВидНоменклатуры.Производим           = Истина;
		ВидНоменклатуры.СобственныеНужды     = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ПриобретениеКомпонентовОсновныхСредств;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСприПриобретенииОсновныхСредств;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Оборудование к установке")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Оборудование к установке";
		ВидНоменклатуры.ПолучаемОтПоставщика = Истина;
		ВидНоменклатуры.Производим           = Истина;
		ВидНоменклатуры.СобственныеНужды     = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСприПриобретенииОсновныхСредств;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		
	КонецЕсли;
	
	Если Набор.Количество() > 0 Тогда
		Набор.Записать(Ложь);
	КонецЕсли;
	
	СоздатьЭлементыМалоценка();
	
КонецПроцедуры

Процедура СоздатьЭлементыКомиссияНаПродажу() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Товары на комиссии")) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Товары на комиссии";
		ВидНоменклатуры.ПолучаемОтПоставщика = Истина;
		ВидНоменклатуры.НаПродажу            = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ТоварыНаСкладе;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию;
		
		Набор.Записать(Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура СоздатьЭлементыКомиссияНаЗакупку() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Товары на ответственном хранении")) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Товары на ответственном хранении";
		ВидНоменклатуры.ПолучаемОтПоставщика = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение;
		
		Набор.Записать(Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура СоздатьЭлементыПроизводство() Экспорт 
	
	Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Полуфабрикаты")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Полуфабрикаты";
		ВидНоменклатуры.Производим           = Истина;
		ВидНоменклатуры.ВПроизводство        = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.Полуфабрикаты;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная;
		Запись.СчетУчетаЗабалансовый                = ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку;
		Запись.СчетУчетаПередачиЗабалансовый        = ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработкуВПроизводстве;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Продукция")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Продукция";
		ВидНоменклатуры.Производим           = Истина;
		ВидНоменклатуры.НаПродажу            = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ГотоваяПродукция;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Продукция из материалов заказчика")) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Продукция из материалов заказчика";
		ВидНоменклатуры.Производим           = Истина;
		ВидНоменклатуры.НаПродажу            = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД;
		
	КонецЕсли;
	
	Если Набор.Количество() > 0 Тогда
		Набор.Записать(Ложь);
    КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЭлементыУслугаЕНВД() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Услуги ЕНВД")) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Услуги ЕНВД";
		ВидНоменклатуры.НаПродажу    = Истина;
		ВидНоменклатуры.Услуга       = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ВыручкаЕНВД;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.СебестоимостьПродажЕНВД;
		
		Набор.Записать(Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура СоздатьЭлементыПродукция() Экспорт 
	
	НаименованиеПоУмолчанию = НСтр("ru = 'Продукция'");
	ВидНоменклатурыПродукция = Справочники.ВидыНоменклатуры.НайтиПоНаименованию(НаименованиеПоУмолчанию, Истина);
	
	Если ЗначениеЗаполнено(ВидНоменклатурыПродукция) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Наименование", НаименованиеПоУмолчанию);
		
		ВидНоменклатуры = Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Заполнить(ДанныеЗаполнения);
		ВидНоменклатуры.Производим           = Истина;
		ВидНоменклатуры.НаПродажу            = Истина;
		ВидНоменклатуры.Записать();
		
		Набор = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		Запись.СчетУчета = ПланыСчетов.Хозрасчетный.ГотоваяПродукция;
		Запись.СчетУчетаНДСПоРеализации = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетУчетаПередачи = ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная;
		
		Набор.Записать(Ложь);
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура СоздатьЭлементыМатериал() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Материалы")) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Материалы";
		ВидНоменклатуры.ПолучаемОтПоставщика = Истина;
		ВидНоменклатуры.ВПроизводство        = Истина;
		ВидНоменклатуры.СобственныеНужды     = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.СырьеИМатериалы;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку;
		Запись.СчетУчетаЗабалансовый                = ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку;
		
		Набор.Записать(Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура СоздатьЭлементыМалоценка() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Малоценное оборудование и запасы")) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = "Малоценное оборудование и запасы";
		ВидНоменклатуры.ПолучаемОтПоставщика     = Истина;
		ВидНоменклатуры.ВЭксплуатациюСотрудникам = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ПриобретениеМалоценногоОборудованияИЗапасов;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		
		Набор.Записать(Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры

Функция НайтиСоздатьЭлементыМалоценка() Экспорт 
	
	НаименованиеМалоценка = НСтр("ru='Малоценное оборудование и запасы'");
	
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию(НаименованиеМалоценка)) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = НаименованиеМалоценка;
		ВидНоменклатуры.ПолучаемОтПоставщика     = Истина;
		ВидНоменклатуры.ВЭксплуатациюСотрудникам = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ПриобретениеМалоценногоОборудованияИЗапасов;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		
		Набор.Записать(Ложь);
		
		Возврат ВидНоменклатуры.Ссылка;
		
	Иначе
		
		Возврат Справочники.ВидыНоменклатуры.НайтиПоНаименованию(НаименованиеМалоценка);
		
	КонецЕсли; 
	
КонецФункции

Функция НайтиСоздатьЭлементыТовар() Экспорт 
	
	НаименованиеПоУмолчанию = НСтр("ru = 'Товары'");
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию(НаименованиеПоУмолчанию, Истина)) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = НаименованиеПоУмолчанию;
		ВидНоменклатуры.ПолучаемОтПоставщика     = Истина;
		ВидНоменклатуры.Комплектуем              = Истина;
		ВидНоменклатуры.НаПродажу                = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ТоварыНаСкладах;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.ПокупныеТоварыОтгруженные;
		
		Набор.Записать(Ложь);
		
		Возврат ВидНоменклатуры.Ссылка;
		
	Иначе
		
		Возврат Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Товары");
		
	КонецЕсли; 
	
КонецФункции

Функция НайтиСоздатьЭлементыУслуга() Экспорт 
	
	НаименованиеПоУмолчанию = НСтр("ru = 'Услуги'");
	Если НЕ ЗначениеЗаполнено(Справочники.ВидыНоменклатуры.НайтиПоНаименованию(НаименованиеПоУмолчанию, Истина)) Тогда
		
		Набор  = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
		
		УстановитьПривилегированныйРежим(Истина);
		
		ВидНоменклатуры	= Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.Наименование = НаименованиеПоУмолчанию;
		ВидНоменклатуры.ПолучаемОтПоставщика     = Истина;
		ВидНоменклатуры.Производим               = Истина;
		ВидНоменклатуры.НаПродажу                = Истина;
		ВидНоменклатуры.Услуга = Истина;
		ВидНоменклатуры.Записать();
		
		Запись = Набор.Добавить();
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымУслугам;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД;
		
		Набор.Записать(Ложь);
		
		Возврат ВидНоменклатуры.Ссылка;
		
	Иначе
		
		Возврат Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Услуги");
		
	КонецЕсли; 
	
КонецФункции

Функция НайтиСоздатьЭлементТопливо() Экспорт 
	
	НаименованиеПоУмолчанию = НСтр("ru = 'Топливо'");
	ЭлементСсылка = Справочники.ВидыНоменклатуры.НайтиПоНаименованию(НаименованиеПоУмолчанию, Истина);
	
	Если ЗначениеЗаполнено(ЭлементСсылка) Тогда
		Возврат ЭлементСсылка;
	КонецЕсли;
	
	Набор = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВидНоменклатуры = Справочники.ВидыНоменклатуры.СоздатьЭлемент();
	ВидНоменклатуры.Наименование = НаименованиеПоУмолчанию;
	ВидНоменклатуры.Услуга = Ложь;
	ВидНоменклатуры.ПолучаемОтПоставщика     = Истина;
	ВидНоменклатуры.СобственныеНужды         = Истина;
	ВидНоменклатуры.ВПроизводство            = Истина;
	ВидНоменклатуры.Записать();
	
	Запись = Набор.Добавить();
	Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
	
	Запись.СчетУчета = ПланыСчетов.Хозрасчетный.Топливо;
	
	Набор.Записать(Ложь);
	
	Возврат ВидНоменклатуры.Ссылка;
	
КонецФункции

Функция ЭлементВидНоменклатурыПоУмолчанию(Услуга) Экспорт
	
	НеобходимоСоздатьЭлемент = Ложь;
	
	Если Услуга Тогда
		НаименованиеПоУмолчанию = НСтр("ru = 'Услуги'");
	Иначе
		НаименованиеПоУмолчанию = НСтр("ru = 'Товары'");
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 2
		|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатурыПоУмолчанию,
		|	ВЫБОР
		|		КОГДА ВидыНоменклатуры.Наименование = &НаименованиеЭлементаПоУмолчанию
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоЭлементПоУмолчаниюДляОбмена
		|ИЗ
		|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|ГДЕ
		|	ВидыНоменклатуры.Услуга = &Услуга
		|	И НЕ ВидыНоменклатуры.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоЭлементПоУмолчаниюДляОбмена УБЫВ");
		
	Запрос.УстановитьПараметр("Услуга",                          Услуга);
	Запрос.УстановитьПараметр("НаименованиеЭлементаПоУмолчанию", НаименованиеПоУмолчанию);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Выборка.Количество() = 1 Или Выборка.ЭтоЭлементПоУмолчаниюДляОбмена Тогда
			ВидНоменклатурыПоУмолчанию = Выборка.ВидНоменклатурыПоУмолчанию;
		Иначе
			НеобходимоСоздатьЭлемент = Истина;
		КонецЕсли;
	Иначе
		НеобходимоСоздатьЭлемент = Истина;
	КонецЕсли;
	
	// Создание элемента для обмена с заданными параметрами
	Если НеобходимоСоздатьЭлемент Тогда
		ВидНоменклатурыПоУмолчанию = ?(Услуга, НайтиСоздатьЭлементыУслуга(), НайтиСоздатьЭлементыТовар());
	КонецЕсли;
	
	Возврат ВидНоменклатурыПоУмолчанию.Ссылка;
	
КонецФункции

Процедура ОбновитьВидыНоменклатурыМалоценка() Экспорт
	
	Если Не (ПолучитьФункциональнуюОпцию("ВедетсяУчетСпецодеждыСпецоснастки")
		ИЛИ ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредствПростойИнтерфейс")
		ИЛИ ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств")) Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЭлементыМалоценка();
	
КонецПроцедуры

Процедура ОтметитьУстаревшиеВидыНоменклатуры2022() Экспорт
	
	ВидыНоменклатуры = Новый ТаблицаЗначений;
	ВидыНоменклатуры.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ВидыНоменклатуры.Колонки.Добавить("СчетУчета", 	  Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ВидыНоменклатуры.Колонки.Добавить("СчетУчетаПередачи", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	Спецодежда = ВидыНоменклатуры.Добавить();
	Спецодежда.Наименование      = "Спецодежда";
	Спецодежда.СчетУчета         = ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаНаСкладе;
	Спецодежда.СчетУчетаПередачи = ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатации;
	
	Спецоснастка = ВидыНоменклатуры.Добавить();
	Спецоснастка.Наименование      = "Спецоснастка";
	Спецоснастка.СчетУчета         = ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаНаСкладе;
	Спецоснастка.СчетУчетаПередачи = ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатации;
	
	Инвентарь = ВидыНоменклатуры.Добавить();
	Инвентарь.Наименование      = "Инвентарь и хозяйственные принадлежности";
	Инвентарь.СчетУчета         = ПланыСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежности;
	Инвентарь.СчетУчетаПередачи = ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыНоменклатуры", ВидыНоменклатуры);
	запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыНоменклатуры.Наименование КАК Наименование,
	|	ВидыНоменклатуры.СчетУчета КАК СчетУчета,
	|	ВидыНоменклатуры.СчетУчетаПередачи КАК СчетУчетаПередачи
	|ПОМЕСТИТЬ СвойстваВидовНоменклатуры
	|ИЗ
	|	&ВидыНоменклатуры КАК ВидыНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
	|	СвойстваВидовНоменклатуры.СчетУчета КАК СчетУчета,
	|	СвойстваВидовНоменклатуры.СчетУчетаПередачи КАК СчетУчетаПередачи
	|ПОМЕСТИТЬ ВидыПоНаименованию
	|ИЗ
	|	СвойстваВидовНоменклатуры КАК СвойстваВидовНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО СвойстваВидовНоменклатуры.Наименование = ВидыНоменклатуры.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыПоНаименованию.ВидНоменклатуры КАК ВидНоменклатуры,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыПоНаименованию.СчетУчета = СчетаУчетаНоменклатуры.СчетУчета
	|					И ВидыПоНаименованию.СчетУчетаПередачи = СчетаУчетаНоменклатуры.СчетУчетаПередачи
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СчетаСовпадают,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыПоНаименованию.СчетУчета <> СчетаУчетаНоменклатуры.СчетУчета
	|					ИЛИ ВидыПоНаименованию.СчетУчетаПередачи <> СчетаУчетаНоменклатуры.СчетУчетаПередачи
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СчетаНеСовпадают
	|ИЗ
	|	ВидыПоНаименованию КАК ВидыПоНаименованию
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаНоменклатуры КАК СчетаУчетаНоменклатуры
	|		ПО ВидыПоНаименованию.ВидНоменклатуры = СчетаУчетаНоменклатуры.ВидНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	ВидыПоНаименованию.ВидНоменклатуры";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строки Из Результат Цикл
		Если ЗначениеЗаполнено(Строки.ВидНоменклатуры)
			И Строки.СчетаСовпадают > 0
			И Строки.СчетаНеСовпадают = 0 Тогда
			Попытка
				ВидНоменклатурыОбъект = Строки.ВидНоменклатуры.ПолучитьОбъект();
				ВидНоменклатурыОбъект.Наименование = ВидНоменклатурыОбъект.Наименование + НСтр("ru = ' (применяется до 2022 года)'");
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ВидНоменклатурыОбъект);
			Исключение
				ШаблонСообщения = НСтр("ru = 'Не удалось обработать вид номенклатуры
		                               |%1'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.Справочники.ВидыНоменклатуры,
					Строки.ВидНоменклатуры,
					ТекстСообщения);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура НайтиСоздатьПодакцизныйВидНоменклатуры(Организация, ВидТовара, Импорт) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоРеквизиту("ВидПодакцизногоТовара", ВидТовара);
	
	Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		
		ВидНоменклатуры = Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		ВидНоменклатуры.ВидПодакцизногоТовара = ВидТовара;
		ВидНоменклатуры.Наименование          = ВидТовара;
		ВидНоменклатуры.ПодакцизныйТовар      = Истина;
		ВидНоменклатуры.НаПродажу             = Истина;
		Если Импорт Тогда
			ВидНоменклатуры.ПолучаемОтПоставщика = Истина;
		Иначе
			ВидНоменклатуры.Производим           = Истина;
		КонецЕсли;
		ВидНоменклатуры.Записать();
		
	КонецЕсли;
	
	Набор = РегистрыСведений.СчетаУчетаНоменклатуры.СоздатьНаборЗаписей();
	Набор.Отбор.Организация.Установить(Организация);
	Набор.Отбор.ВидНоменклатуры.Установить(ВидНоменклатуры.Ссылка);
	Набор.Отбор.Номенклатура.Установить(Справочники.Номенклатура.ПустаяСсылка());
	Набор.Прочитать();
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
		Запись.Организация     = Организация;
		Запись.ВидНоменклатуры = ВидНоменклатуры.Ссылка;
		Запись.Номенклатура    = Справочники.Номенклатура.ПустаяСсылка();
	Иначе
		Запись = Набор[0];
	КонецЕсли;
	ПустойСчет = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	Если Импорт Тогда
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ТоварыНаСкладах;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПланыСчетов.Хозрасчетный.НДСуплачиваемыйТаможеннымОрганам;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.ПокупныеТоварыОтгруженные;
		Запись.СчетДоходовОтРеализации              = ПустойСчет;
		Запись.СчетРасходовОтРеализации             = ПустойСчет;
		Запись.СчетУчетаАкцизаПоРеализации          = ПустойСчет;
		Запись.СчетУчетаАкцизаПоИмпорту             = ПланыСчетов.Хозрасчетный.АкцизыПоОплаченнымМатериальнымЦенностям;
		
	Иначе
		Запись.СчетУчета                            = ПланыСчетов.Хозрасчетный.ГотоваяПродукция;
		Запись.СчетУчетаНДСПоПриобретеннымЦенностям = ПустойСчет;
		Запись.СчетУчетаНДСУплаченногоНаТаможне     = ПустойСчет;
		Запись.СчетУчетаНДСПоРеализации             = ПланыСчетов.Хозрасчетный.Продажи_НДС;
		Запись.СчетУчетаПередачи                    = ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная;
		Запись.СчетДоходовОтРеализации              = ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД;
		Запись.СчетРасходовОтРеализации             = ПланыСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД;
		Запись.СчетУчетаАкцизаПоРеализации          = Планысчетов.Хозрасчетный.Продажи_Акцизы;
		Запись.СчетУчетаАкцизаПоИмпорту             = ПустойСчет;
		
	КонецЕсли;
	
	Набор.Записать();
		
КонецПроцедуры

#Область Обновление

Процедура ЗаполнитьПризнакиИспользования() Экспорт
	
	УсловияОтбораТара = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбораТара.СчетаИсключения = Новый Массив();
	УсловияОтбораТара.СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.ТараПодТоваромИПорожняя);
	
	УсловияОтбораМалоценка = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбораМалоценка.СчетаИсключения = Новый Массив();
	УсловияОтбораМалоценка.СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаНаСкладе);
	УсловияОтбораМалоценка.СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.МалоценноеОборудованиеИЗапасы);
		
	СчетаПолучаемОтПоставщика = Новый Массив;
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеПринятоеДляМонтажа);
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.Материалы);
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.Товары);
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение);
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку);
	СчетаПолучаемОтПоставщика.Добавить(ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссию);
	СчетаПолучаемОтПоставщика = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаПолучаемОтПоставщика);
	
	СчетаПроизводим = Новый Массив;
	СчетаПроизводим.Добавить(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
	СчетаПроизводим.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);
	СчетаПроизводим.Добавить(ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья);
	СчетаПроизводим.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);
	СчетаПроизводим.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	СчетаПроизводим = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаПроизводим);
	
	СчетаКомплектуем = Новый Массив;
	СчетаКомплектуем.Добавить(ПланыСчетов.Хозрасчетный.Товары);
	СчетаКомплектуем = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаКомплектуем, УсловияОтбораТара); 
	
	СчетаНаПродажу = Новый Массив;
	СчетаНаПродажу.Добавить(ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья);
	СчетаНаПродажу.Добавить(ПланыСчетов.Хозрасчетный.Товары);
	СчетаНаПродажу.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	СчетаНаПродажу.Добавить(ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссию);
	СчетаНаПродажу = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаНаПродажу, УсловияОтбораТара);
	
	СчетаВПроизводство = Новый Массив;
	СчетаВПроизводство.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);
	СчетаВПроизводство.Добавить(ПланыСчетов.Хозрасчетный.Материалы);
	СчетаВПроизводство.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку);
	СчетаВПроизводство = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаВПроизводство, УсловияОтбораМалоценка);
	
	СчетаСобственныеНужды = Новый Массив;
	СчетаСобственныеНужды.Добавить(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
	СчетаСобственныеНужды.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеПринятоеДляМонтажа);
	СчетаСобственныеНужды.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);
	СчетаСобственныеНужды.Добавить(ПланыСчетов.Хозрасчетный.Материалы);
	СчетаСобственныеНужды.Добавить(ПланыСчетов.Хозрасчетный.ТараПодТоваромИПорожняя);
	СчетаСобственныеНужды = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаСобственныеНужды, УсловияОтбораМалоценка);
	
	СчетаВЭксплуатациюСотрудникам = Новый Массив;
	СчетаВЭксплуатациюСотрудникам.Добавить(ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаНаСкладе);
	СчетаВЭксплуатациюСотрудникам.Добавить(ПланыСчетов.Хозрасчетный.МалоценноеОборудованиеИЗапасы);
	СчетаВЭксплуатациюСотрудникам = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаВЭксплуатациюСотрудникам);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетаПолучаемОтПоставщика",     СчетаПолучаемОтПоставщика);
	Запрос.УстановитьПараметр("СчетаПроизводим",               СчетаПроизводим);
	Запрос.УстановитьПараметр("СчетаКомплектуем",              СчетаКомплектуем);
	Запрос.УстановитьПараметр("СчетаНаПродажу",                СчетаНаПродажу);
	Запрос.УстановитьПараметр("СчетаВПроизводство",            СчетаВПроизводство);
	Запрос.УстановитьПараметр("СчетаСобственныеНужды",         СчетаСобственныеНужды);
	Запрос.УстановитьПараметр("СчетаВЭксплуатациюСотрудникам", СчетаВЭксплуатациюСотрудникам);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета ЕСТЬ NULL И НЕ ВидыНоменклатуры.Услуга
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СчетаНастроены,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета В (&СчетаПолучаемОтПоставщика)
	|				ИЛИ ВидыНоменклатуры.Услуга
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПолучаемОтПоставщика,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета В (&СчетаПроизводим)
	|				ИЛИ ВидыНоменклатуры.Услуга
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Производим,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета В (&СчетаКомплектуем)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Комплектуем,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета В (&СчетаНаПродажу)
	|				ИЛИ ВидыНоменклатуры.Услуга
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаПродажу,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета В (&СчетаВПроизводство)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВПроизводство,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета В (&СчетаСобственныеНужды)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СобственныеНужды,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНоменклатуры.СчетУчета В (&СчетаВЭксплуатациюСотрудникам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВЭксплуатациюСотрудникам
	|ПОМЕСТИТЬ НазначенияВидовНоменклатуры
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаНоменклатуры КАК СчетаУчетаНоменклатуры
	|		ПО СчетаУчетаНоменклатуры.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НазначенияВидовНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.СчетаНастроены) КАК СчетаНастроены,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.ПолучаемОтПоставщика) КАК ПолучаемОтПоставщика,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.Производим) КАК Производим,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.Комплектуем) КАК Комплектуем,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.НаПродажу) КАК НаПродажу,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.ВПроизводство) КАК ВПроизводство,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.СобственныеНужды) КАК СобственныеНужды,
	|	МАКСИМУМ(НазначенияВидовНоменклатуры.ВЭксплуатациюСотрудникам) КАК ВЭксплуатациюСотрудникам
	|ИЗ
	|	НазначенияВидовНоменклатуры КАК НазначенияВидовНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	НазначенияВидовНоменклатуры.ВидНоменклатуры";
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Результат.ВидНоменклатуры) Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			СправочникОбъект = Результат.ВидНоменклатуры.ПолучитьОбъект();
			
			// Объект удален или обработан другим сеансом
			Если СправочникОбъект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если Результат.СчетаНастроены Тогда
				ЗаполнитьЗначенияСвойств(СправочникОбъект, Результат, "ПолучаемОтПоставщика, Производим, Комплектуем, НаПродажу, ВПроизводство, СобственныеНужды, ВЭксплуатациюСотрудникам");
			Иначе
				СправочникОбъект.ПолучаемОтПоставщика     = Истина;
				СправочникОбъект.Производим               = Истина;
				СправочникОбъект.Комплектуем              = Истина;
				СправочникОбъект.НаПродажу                = Истина;
				СправочникОбъект.ВПроизводство            = Истина;
				СправочникОбъект.СобственныеНужды         = Истина;
				СправочникОбъект.ВЭксплуатациюСотрудникам = Истина;
			КонецЕсли;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обработать вид номенклатуры по причине:
				|%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Справочники.ВидыНоменклатуры,
				Результат.ВидНоменклатуры,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли