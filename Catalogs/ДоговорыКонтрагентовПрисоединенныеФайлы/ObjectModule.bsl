#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьОбновитьДоговорВладелец();

КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьОбновитьДоговорВладелец();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьОбновитьДоговорВладелец()

	// С помощью средств БСП пользователь может удалить данные файла или пометить его на удаление.
	// Проверяем такую ситуацию, чтобы очистить ссылку на текущий файл в договоре-владельце.

	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;

	Если Не ПометкаУдаления
		И Не ДополнительныеСвойства.Свойство("УдалениеДанных") Тогда
		// Текущий файл не помечен на удаление, проверка не требуется.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВладелецФайла) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверяем до блокировки, чтобы не писать лишних ошибок в журнал регистрации
	СсылкиНаФайл = СсылкиНаФайл();
	Если СсылкиНаФайл.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(ВладелецФайла);
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ДоговорыКонтрагентов");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ВладелецФайла);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		// Получаем файлы договора повторно (после блокировки)
		СсылкиНаФайл = СсылкиНаФайл();
		Если СсылкиНаФайл.Количество() = 0 Тогда
			РазблокироватьДанныеДляРедактирования(ВладелецФайла);
			Возврат;
		КонецЕсли;
		
		ДоговорОбъект = ВладелецФайла.ПолучитьОбъект();
		Если ДоговорОбъект = Неопределено Тогда
			Возврат;
		КонецЕсли;
	
		Для Каждого ДанныеФайла Из СсылкиНаФайл Цикл
			Если ДанныеФайла.НомерСтрокиСоглашения = -1 Тогда
				ДоговорОбъект.ФайлДоговора = Неопределено;
				ДоговорОбъект.ЭлектронныйФормат = Ложь;
			Иначе
				СтрокаСоглашения = ДоговорОбъект.ДополнительныеСоглашения[ДанныеФайла.НомерСтрокиСоглашения-1];
				СтрокаСоглашения.ФайлСоглашения = Неопределено;
			КонецЕсли;
		КонецЦикла;
		
		ДоговорОбъект.Записать();
		РазблокироватьДанныеДляРедактирования(ВладелецФайла);
		
	Исключение
		// Регистрируем в журнал предупреждение, а не ошибку, так как, возможно, что пользователь сейчас редактирует этот договор
		// и он заблокирован формой. Тогда форма сама поймает оповещение от БСП и очистит ссылку на файл договора.
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'При обновлении договора-владельца файла произошла ошибка: %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Обновление договора-владельца файла'"),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.Справочники.ДоговорыКонтрагентов,
			ВладелецФайла,
			ТекстСообщения);
		
	КонецПопытки;

КонецПроцедуры

Функция СсылкиНаФайл()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.ФайлДоговора КАК Файл,
	|	-1 КАК НомерСтрокиСоглашения
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Договор
	|	И ДоговорыКонтрагентов.ФайлДоговора = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентовДополнительныеСоглашения.ФайлСоглашения,
	|	ДоговорыКонтрагентовДополнительныеСоглашения.НомерСтроки
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов.ДополнительныеСоглашения КАК ДоговорыКонтрагентовДополнительныеСоглашения
	|ГДЕ
	|	ДоговорыКонтрагентовДополнительныеСоглашения.Ссылка = &Договор
	|	И ДоговорыКонтрагентовДополнительныеСоглашения.ФайлСоглашения = &Ссылка";
	
	Запрос.УстановитьПараметр("Договор", ВладелецФайла);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ТаблицаФайлов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаФайлов;
	
КонецФункции

#КонецОбласти

#КонецЕсли