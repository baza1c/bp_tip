
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = СтрШаблон(НСтр("ru = 'Настройка видов зачислений (%1)'"), Параметры.ЗарплатныйПроект);
	
	АдресТаблицыНастройкиВидовЗачислений = Параметры.АдресТаблицыНастройкиВидовЗачислений;
	
	ТаблицаЗначений = ПолучитьИзВременногоХранилища(АдресТаблицыНастройкиВидовЗачислений);
	ВидыЗачисленийПоСпособамВыплат.Загрузить(ТаблицаЗначений);
	Если ВидыЗачисленийПоСпособамВыплат.Количество() = 0 Тогда
		ЗаполнитьСпособамиВыплатНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СохранитьВидыЗачисленийПоСпособамВыплатНаСервере();
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
		
	Модифицированность = Ложь;
	Закрыть(КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпособамиВыплат(Команда)
	
	Если ВидыЗачисленийПоСпособамВыплат.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросЗаполнитьСпособамиВыплатЗавершение", ЭтотОбъект, Неопределено);
		ТекстВопроса = НСтр("ru = 'Табличная часть будет очищена. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		Модифицированность = Истина;
		ЗаполнитьСпособамиВыплатНаСервере();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросЗаполнитьСпособамиВыплатЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьСпособамиВыплатНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпособамиВыплатНаСервере()
	
	ВидыЗачисленийПоСпособамВыплат.Очистить();
	ВыборкаСпособовВыплат = Справочники.СпособыВыплатыЗарплаты.Выбрать();
	Пока ВыборкаСпособовВыплат.Следующий() Цикл
		Если ВыборкаСпособовВыплат.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		ВидЗачисленияПоСпособуВыплат = ВидыЗачисленийПоСпособамВыплат.Добавить();
		ВидЗачисленияПоСпособуВыплат.СпособВыплат = ВыборкаСпособовВыплат.Ссылка;
	КонецЦикла
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВидыЗачисленийПоСпособамВыплатНаСервере()
	
	ПоместитьВоВременноеХранилище(ВидыЗачисленийПоСпособамВыплат.Выгрузить(, "СпособВыплат,ВидЗачисления"), АдресТаблицыНастройкиВидовЗачислений);
	
КонецПроцедуры

#КонецОбласти
