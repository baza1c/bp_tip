#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.ПрименитьПараметрыФормыНастроек(ЭтотОбъект, Счет);
	
	// В редакторе формы содержится эталонное описание счета.
	// Содержимое формы заполняется программно счетами из списка Справочники.НастройкиУчетаЗатрат.СчетаСписанияЗатратНаИсследования(),
	// некоторые свойства элементов формы берутся из эталонного описания формы.
	// Подсказки к счетам содержатся в методе ПодсказкиКСчету().
	ЗаполнитьСодержимоеФормы(РедактированиеНастроек.Настройки);
	
	ПрименитьНастройки(РедактированиеНастроек);
	
	ИндексВыбранногоСчетаСтрокой = XMLСтрока(СписокСчетов.Индекс(СписокСчетов.НайтиПоЗначению(СчетСписания)));
	УстановитьВидимостьАналитики(ИндексВыбранногоСчетаСтрокой);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	РезультатФормы = Неопределено;
	
	Если Модифицированность Тогда
		РезультатФормы = УстановитьВыполненныеНастройки();
	КонецЕсли;
	
	Закрыть(РезультатФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_СчетСписанияПриИзменении(Элемент)
	ИндексЭлемента = СтрЗаменить(Элемент.Имя, ИмяЭлементаСчетСписания(), "");
	УстановитьВидимостьАналитики(ИндексЭлемента)
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьАналитики(ИндексВыбранногоЭлемента)

	Для Индекс = 0 По СписокСчетов.Количество() - 1 Цикл
		Видимость = Ложь;
		ИндексСтрокой = XMLСтрока(Индекс); 
		Если ИндексВыбранногоЭлемента = ИндексСтрокой Тогда
			ОписаниеАналитики = НастраиваемаяАналитика.Получить(СписокСчетов[Индекс].Значение);
			Если ЗначениеЗаполнено(ОписаниеАналитики) Тогда
				Если ОписаниеАналитики.Отображать Тогда
					Видимость = Истина;
				Иначе
					// Например, в случае, если используется одна номенклатурная группа, подставляем основную номенклатурную группу.
					АналитикаСписания = ОписаниеАналитики.ЗначениеПоУмолчанию;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Элементы[ИмяЭлементаАналитикаСписания() + ИндексСтрокой].Видимость = Видимость;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройки(РедактированиеНастроек)
	
	СписаниеЗатратНаИсследования = РедактированиеНастроек.Настройки.Закрытие[Счет].СписаниеЗатратНаИсследования;
	
	СчетСписания = СписаниеЗатратНаИсследования.Счет;
	Если Справочники.НастройкиУчетаЗатрат.СчетаСписанияЗатратНаИсследования().Найти(СчетСписания) = Неопределено Тогда
		СчетСписания = Справочники.НастройкиУчетаЗатрат.СчетСписанияЗатратНаИсследованияПоУмолчанию();
	КонецЕсли;
	
	АналитикаСписания = СписаниеЗатратНаИсследования.ЗначениеАналитики;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСодержимоеФормы(Настройки)
	
	ЭлементСчетСписанияЭталон = Элементы.СчетСписания;
	АналитикаКЗаполнению = Новый Соответствие();
	
	РазрезыРаспределения = РаспределениеРасходов.РазрезыРаспределенияСчетовУчета(Настройки.СчетаУчета);
	НеиспользуемаяАналитика = ПланыСчетов.Хозрасчетный.НеиспользуемаяАналитика();
	
	// Запомним настраиваемые счета.
	СписокСчетов.ЗагрузитьЗначения(Справочники.НастройкиУчетаЗатрат.СчетаСписанияЗатратНаИсследования());
	
	НомерГруппыЭлементов = 0;
	ПодсказкиКСчету = ПодсказкиКСчету();
	Для Каждого СчетСписания Из СписокСчетов.ВыгрузитьЗначения() Цикл
		
		НомерГруппыСтрокой = XMLСтрока(НомерГруппыЭлементов);
		
		// Переключатель с названием счета
		НовыйЭлементСчетСписания = Элементы.Добавить(ИмяЭлементаСчетСписания() + НомерГруппыСтрокой, Тип("ПолеФормы"), Элементы.ГруппаВыборСчета);
		ЗаполнитьЗначенияСвойств(НовыйЭлементСчетСписания, ЭлементСчетСписанияЭталон, "Вид, ПутьКДанным, ПоложениеЗаголовка");
		НовыйЭлементСчетСписания.СписокВыбора.Добавить(СчетСписания, ПланыСчетов.Хозрасчетный.ПолноеПредставлениеСчета(СчетСписания));
		НовыйЭлементСчетСписания.УстановитьДействие("ПриИзменении", "Подключаемый_СчетСписанияПриИзменении");
		
		// Объединяющая группа
		НовыйЭлементГруппаСчетСписанияПодсказка = Элементы.Добавить("ГруппаСчетСписанияПодсказка_" + НомерГруппыСтрокой, Тип("ГруппаФормы"), Элементы.ГруппаВыборСчета);
		НовыйЭлементГруппаСчетСписанияПодсказка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ЗаполнитьЗначенияСвойств(НовыйЭлементГруппаСчетСписанияПодсказка, Элементы.ГруппаСчетСписанияПодсказка, "Группировка, ОтображатьЗаголовок");
		
		// Вертикальный отступ
		НовыйЭлементСчетСписанияОтступ = Элементы.Добавить("СчетСписанияОтступ_" + НомерГруппыСтрокой, Тип("ДекорацияФормы"), НовыйЭлементГруппаСчетСписанияПодсказка);
		ЗаполнитьЗначенияСвойств(НовыйЭлементСчетСписанияОтступ, Элементы.СчетСписанияОтступ, "Ширина, Высота, Вид");
		
		// Объединяющая группа: Аналитика + Подсказка
		НовыйЭлементГруппаОписание = Элементы.Добавить("ГруппаОписание_" + НомерГруппыСтрокой, Тип("ГруппаФормы"), НовыйЭлементГруппаСчетСписанияПодсказка);
		НовыйЭлементГруппаОписание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ЗаполнитьЗначенияСвойств(НовыйЭлементГруппаОписание, Элементы.ГруппаОписание, "Группировка, ОтображатьЗаголовок");
		
		// Аналитика
		// Определим, какое субконто требуется заполнить. В любом случае добавим элемент на форму, установим необходимый тип,
		// запомним его в реквизите формы, чтобы в дальнейшем управлять видимостью и в конце передать вид субконто в настройки.
		НовыйЭлементАналитикаСписания = Элементы.Добавить(
			ИмяЭлементаАналитикаСписания() + НомерГруппыСтрокой,
			Тип("ПолеФормы"),
			НовыйЭлементГруппаОписание);
		ЗаполнитьЗначенияСвойств(НовыйЭлементАналитикаСписания, Элементы.АналитикаСписания, "Вид, ПутьКДанным");
		НовыйЭлементАналитикаСписания.Видимость = Ложь;
			
		ОписаниеАналитикиСписания = НовыйОписаниеАналитики();
		ЗаполнитьОписаниеАналитики(ОписаниеАналитикиСписания, РазрезыРаспределения, НеиспользуемаяАналитика);
		
		Если ЗначениеЗаполнено(ОписаниеАналитикиСписания.ВидСубконто) Тогда
			
			НовыйЭлементАналитикаСписания.Заголовок = ОписаниеАналитикиСписания.ВидСубконто.ПолноеНаименование();
			НовыйЭлементАналитикаСписания.ОграничениеТипа = ОписаниеАналитикиСписания.ТипЗначения;
			
			// Сохраняем вид субконто в реквизите формы.
			АналитикаКЗаполнению.Вставить(СчетСписания, ОписаниеАналитикиСписания);
			
		КонецЕсли;	

		// Подсказка
		ПодсказкаКСчету = ПодсказкиКСчету[СчетСписания];
		Если ЗначениеЗаполнено(ПодсказкаКСчету) Тогда
			НовыйЭлементСчетСписанияПодсказка = Элементы.Добавить(
				"СчетСписанияПодсказка_" + НомерГруппыСтрокой,
				Тип("ДекорацияФормы"),
				НовыйЭлементГруппаОписание);
			ЗаполнитьЗначенияСвойств(НовыйЭлементСчетСписанияПодсказка, Элементы.СчетСписанияПодсказка, "Вид, ЦветТекста, АвтомаксимальнаяШирина");
			НовыйЭлементСчетСписанияПодсказка.Заголовок = ПодсказкаКСчету;
		КонецЕсли;

		НомерГруппыЭлементов = НомерГруппыЭлементов + 1;
	КонецЦикла;
	
	Элементы.ГруппаЭталон.Видимость = Ложь;
	
	УстановитьЗаголовокВариантУчета(Элементы.ЗаголовокГруппыПереключателей, Счет);
	
	НастраиваемаяАналитика = Новый ФиксированноеСоответствие(АналитикаКЗаполнению);
	
КонецПроцедуры

&НаСервере
Функция ИмяЭлементаАналитикаСписания()
	// Логика работы формы связана с именами элементов, соответствующих аналитикам списания,
	// поэтому выносим текстовую составляющую наименования элементов в отдельный метод.
	// см. ЗаполнитьСодержимоеФормы(), УстановитьВидимостьАналитики()
	Возврат "АналитикаСписания_";
КонецФункции

&НаСервере
Функция ИмяЭлементаСчетСписания()
	// Логика работы формы связана с именами элементов, соответствующих счетам списания,
	// поэтому выносим текстовую составляющую наименования элементов в отдельный метод.
	// см. ЗаполнитьСодержимоеФормы(), Подключаемый_СчетСписанияПриИзменении()
	Возврат "СчетСписания_";
КонецФункции

&НаСервере
Процедура ЗаполнитьОписаниеАналитики(ОписаниеАналитикиСписания, РазрезыРаспределения, НеиспользуемаяАналитика)

	АналитикаРаспределенияСчета = Новый Соответствие;
	ЗаполнитьАналитикуРаспределенияСчета(АналитикаРаспределенияСчета, СчетСписания, РазрезыРаспределения);

	Если ЗначениеЗаполнено(АналитикаРаспределенияСчета) Тогда

		Для Каждого ОписаниеРазрезаРаспределения Из АналитикаРаспределенияСчета Цикл
	
			ОписаниеАналитикиСписания.ВидСубконто = ОписаниеРазрезаРаспределения.Ключ;
			ОписаниеАналитикиСписания.ТипЗначения = ОписаниеРазрезаРаспределения.Значение.ТипЗначения;
			
			// Если используется только одна номенклатурная группа, подставляем значение по умолчанию и не отображаем ее.
			Если НеиспользуемаяАналитика.Найти(ОписаниеРазрезаРаспределения.Ключ) <> Неопределено Тогда
				ОписаниеАналитикиСписания.ЗначениеПоУмолчанию =
					БухгалтерскийУчетКлиентСервер.ПредопределенныеЗначенияСубконтоПоУмолчанию()[ОписаниеАналитикиСписания.ВидСубконто];
				ОписаниеАналитикиСписания.Отображать = Ложь;
			КонецЕсли;
			// Ограничимся настройкой только самой первой аналитики.
			Возврат;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАналитикуРаспределенияСчета(АналитикаРаспределения, СчетСписания, РазрезыРаспределения)
	
	НедоступныеРазрезыРаспределения = УчетЗатрат.РазрезыРаспределенияНедоступныеНаФинансовыйРезультат();
	
	АналитикаСчета = БухгалтерскийУчет.АналитикаСчета(СчетСписания);
	
	Если Не ЗначениеЗаполнено(АналитикаСчета) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОписаниеРазрезаРаспределения Из РазрезыРаспределения Цикл

		Если НедоступныеРазрезыРаспределения.Найти(ОписаниеРазрезаРаспределения.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если АналитикаСчета[ОписаниеРазрезаРаспределения.Ключ] = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		АналитикаРаспределения.Вставить(ОписаниеРазрезаРаспределения.Ключ, ОписаниеРазрезаРаспределения);

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НовыйОписаниеАналитики()
	ОписаниеАналитики = Новый Структура();
	ОписаниеАналитики.Вставить("ВидСубконто", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
	ОписаниеАналитики.Вставить("ТипЗначения", ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	ОписаниеАналитики.Вставить("Отображать", Истина);
	ОписаниеАналитики.Вставить("ЗначениеПоУмолчанию", Неопределено);
	Возврат ОписаниеАналитики;
КонецФункции

&НаСервере
Функция ПодсказкиКСчету()
	
	ПодсказкиКСчету = Новый Соответствие();
	ТекстОписания =
		НСтр("ru = 'Выберите этот вариант, если исследования направлены на создание и улучшение
		|выпускаемой продукции, оказываемых услуг и выполняемых работ'");
	ПодсказкиКСчету.Вставить(ПланыСчетов.Хозрасчетный.СебестоимостьПродаж, ТекстОписания);
	
	ТекстОписания =
		НСтр("ru = 'Выберите этот вариант, если результаты исследований будут использованы
		|в коммерческой деятельности: при продаже товаров, доставке, хранении и рекламном продвижении продукции'");
	ПодсказкиКСчету.Вставить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажу, ТекстОписания);
	
	ТекстОписания =
		НСтр("ru = 'Выберите этот вариант, если исследования направлены на повышение эффективности бизнес-процессов,
		|улучшение работы организации в целом'");
	ПодсказкиКСчету.Вставить(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходы, ТекстОписания);
	
	ТекстОписания =
		НСтр("ru = 'Выберите этот вариант, если исследования проводятся для иных целей'");
	ПодсказкиКСчету.Вставить(ПланыСчетов.Хозрасчетный.ПрочиеРасходы, ТекстОписания);
	
	Возврат ПодсказкиКСчету;
		
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьЗаголовокВариантУчета(ЗаголовокПереключателей, Счет)
	
	ЗаголовокПереключателей.Заголовок = СтрШаблон(
		НСтр("ru = 'Учтенные на счете %1 затраты на выполнение научно-исследовательских,
		|опытно-конструкторских и технологических работ, относящиеся к стадии исследований,
		|ежемесячно списываются на счет:'"),
		ПланыСчетов.Хозрасчетный.ПолноеПредставлениеСчета(Счет));
	
КонецПроцедуры

&НаСервере
Функция УстановитьВыполненныеНастройки()
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.НачатьУстановкуВыполненныхНастроек(АдресРедактированиеНастроек);
	
	СписаниеЗатратНаИсследования = РедактированиеНастроек.Настройки.Закрытие[Счет].СписаниеЗатратНаИсследования;
	СписаниеЗатратНаИсследования.Счет = СчетСписания;
	СписаниеЗатратНаИсследования.ЗначениеАналитики = Неопределено;
	СписаниеЗатратНаИсследования.ВидАналитики = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(НастраиваемаяАналитика.Получить(СчетСписания)) Тогда
		СписаниеЗатратНаИсследования.ЗначениеАналитики = АналитикаСписания;
		СписаниеЗатратНаИсследования.ВидАналитики = НастраиваемаяАналитика[СчетСписания].ВидСубконто;
	КонецЕсли;
	
	Возврат Справочники.НастройкиУчетаЗатрат.ЗавершитьУстановкуВыполненныхНастроек(
		АдресРедактированиеНастроек,
		РедактированиеНастроек);
	
КонецФункции

#КонецОбласти
