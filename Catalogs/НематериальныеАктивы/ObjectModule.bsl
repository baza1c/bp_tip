#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ВключенВРеестрРоссийскогоПО Тогда
		Если Не ЗначениеЗаполнено(ДатаРеестрРоссийскогоПО) Или Не ОбщегоНазначенияКлиентСервер.ЭтоЧисло(НомерРеестрРоссийскогоПО) Тогда
			ТекстСообщения =
				НСтр("ru = 'Должны быть заполнены номер и дата реестровой записи, а номер должен состоять только из цифр'");
			ТекстОшибкиЗаполнения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", НСтр("ru = 'Реестровая запись'"),,, ТекстСообщения);
				
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибкиЗаполнения, , "ЗаписьВРеестреРоссийскогоПОНадпись", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ВсеОрганизацииПлательщикиНДФЛ = БухгалтерскийУчетПовтИсп.ВсеОрганизацииПлательщикиНДФЛ();
	ЕстьОрганизацииПлательщикиНДФЛ = БухгалтерскийУчетПовтИсп.ЕстьОрганизацииПлательщикиНДФЛ();
	
	Если (ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс
		Или ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Расходы)
		И (ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования
			Или ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		Или ВсеОрганизацииПлательщикиНДФЛ
			И (ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования
				Или ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		Или БухгалтерскийУчетВызовСервераПовтИсп.ПользователюДоступныСуммыНалогНаПрибыль() = "НеИспользовать"
			И Не ЕстьОрганизацииПлательщикиНДФЛ
			И ОтражениеБУ <> Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидНематериальногоАктива");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Реквизит ВидОбъектаУчета оставлен для совместимости, планируется удаление в будущих версиях. 
	Если ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.РасходыНаНИОКР Тогда
		ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР;
	Иначе
		ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив;
	КонецЕсли;
	
	// Значения реквизитов ОтражениеБУ и ОтражениеНУ при отключенной функциональной опции "Ведется учет НМА"
	// не должны принимать значение "Нематериальный актив" и "Расходы на НИОКР".
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА") Тогда
		
		Если Не ЗначениеЗаполнено(ОтражениеБУ)
			Или ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив Тогда
			ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Расходы;
		КонецЕсли;
		
		ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования;

	КонецЕсли;
	
	Если Не ЭтоИсключительноеПраво Тогда
		ДокументРегистрацииНаименование = "";
		ДокументРегистрацииДата = Дата(1, 1, 1);
		ДокументРегистрацииНомер = "";
	КонецЕсли;
	
	Если Не ВключенВРеестрРоссийскогоПО Тогда
		НомерРеестрРоссийскогоПО = "";
		ДатаРеестрРоссийскогоПО = Дата(1, 1, 1);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ВключенВРеестрРоссийскогоПО
		И Не ЭтоИсключительноеПраво
		И ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования
		И ПравоДоступа("Редактирование", Справочники.СтатьиЗатрат.ПустаяСсылка().Метаданные()) Тогда
		Справочники.СтатьиЗатрат.ПредопределенныйЭлемент("ПриобретениеКомпьютерныхЛицензийСПовышающимКоэффициентом");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли