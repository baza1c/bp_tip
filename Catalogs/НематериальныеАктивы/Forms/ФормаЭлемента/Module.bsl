
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ФормироватьНаименованиеПолноеАвтоматически = УстановитьФлагФормироватьНаименованиеПолноеАвтоматически(
		Объект.НаименованиеПолное, Объект.Наименование);
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	
	Если Параметры.Ключ.Пустая() Тогда
	
		УстановитьНастройкиИнформационнойБазы();
	
	КонецЕсли;
	
	Если Не ИспользуетсяОтражениеНУ Тогда
		Элементы.ОтражениеНУ.Видимость = Ложь;
		Элементы.ГруппаПорядокУчета.ОтображатьЗаголовок = Ложь;
		Элементы.ОтражениеБУ.Заголовок = НСтр("ru = 'Порядок учета'");
	ИначеЕсли Не ИспользуетсяОтражениеБУ Тогда
		Элементы.ОтражениеБУ.Видимость = Ложь;
		Элементы.ГруппаПорядокУчета.ОтображатьЗаголовок = Ложь;
		Элементы.ОтражениеНУ.Заголовок = НСтр("ru = 'Порядок учета'");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА") Тогда
			
		Если Параметры.Ключ.Пустая() Тогда
			Объект.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс;
			Объект.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования;
		КонецЕсли;
	ИначеЕсли Не ИспользуетсяОтражениеБУ И Параметры.Ключ.Пустая() Тогда
		// Поле ОтражениеБУ в этом случае не отображается на форме, но после изменений настроек (например, при введении новой организации на УСН или ОСНО)
		// это поле может стать видимым и будет срабатывать проверка на заполненность "ВидНематериальногоАктива". Чтобы в созданных ранее НМА
		// эта проверка не срабатывала, устанавливаем вариант отражения в БУ, не требующий заполненности вида нематериального актива.
		Объект.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс;
	Иначе
		Элементы.ОтражениеБУ.СписокВыбора.Добавить(Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив);
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
			И ЗначениеЗаполнено(Параметры.ОтражениеБУ)
			И ЗначениеЗаполнено(Параметры.ОтражениеНУ) Тогда
			
			ЗаполнитьЗначенияСвойств(Объект, Параметры, "ОтражениеБУ, ОтражениеНУ");
			
			Если ФормироватьНаименованиеПолноеАвтоматически Тогда
				Объект.НаименованиеПолное = Объект.Наименование;
			КонецЕсли;
			
		КонецЕсли;
		
		УправлениеФормой();
		
	КонецЕсли;
	
	ИменаКлючейНастройки = ИменаКлючейНастройки();
	
	Элементы.ГруппаБаннер.Видимость =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИменаКлючейНастройки.КлючОбъекта, ИменаКлючейНастройки.КлючНастроек) <> Ложь;
		
	НастройкиУчетаМСФО.НастроитьФормуОбъектаУчета(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьПолеНаименованияКода("ОКВЭД");
	ЗаполнитьПолеНаименованияКода("ОКВЭД2");
	ЗаполнитьПолеНаименованияКода("ОКОФ");
	
	УстановитьНастройкиИнформационнойБазы();
	УправлениеФормой();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если СпособУказанияСрока = "ДоДаты" Тогда
		ТекущийОбъект.СрокДействияПраваКоличествоМесяцев = 0;
	ИначеЕсли СпособУказанияСрока = "КоличествоМесяцев" Тогда
		ТекущийОбъект.СрокДействияПраваДата = Дата(1, 1, 1);
	Иначе
		ТекущийОбъект.СрокДействияПраваДата = Дата(1, 1, 1);
		ТекущийОбъект.СрокДействияПраваКоличествоМесяцев = 0;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ДокументРегистрацииНадпись = НадписьДокументРегистрации(
		Объект.ДокументРегистрацииНаименование,
		Объект.ДокументРегистрацииНомер,
		Объект.ДокументРегистрацииДата);
		
	ЗаписьВРеестреРоссийскогоПОНадпись = НадписьДокументРегистрации(
		"",
		Объект.НомерРеестрРоссийскогоПО,
		Объект.ДатаРеестрРоссийскогоПО);
		
КонецПроцедуры
	
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Используется для оповещения документа ИнвентаризацияНМА
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("НематериальныйАктив", Объект.Ссылка);
	ПараметрыОповещения.Вставить("НачалоДействияПрава", Объект.НачалоДействияПрава);
	ПараметрыОповещения.Вставить("ОтветственноеЛицо", Объект.ОтветственноеЛицо);
	ПараметрыОповещения.Вставить("ОтражениеБУ", Объект.ОтражениеБУ);
	Оповестить("Запись_НематериальныйАктив", ПараметрыОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	СформироватьНаименованиеПолноеАвтоматически();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)

	ФормироватьНаименованиеПолноеАвтоматически = УстановитьФлагФормироватьНаименованиеПолноеАвтоматически(
		Элементы.НаименованиеПолное, Элементы.Наименование);

КонецПроцедуры

&НаКлиенте
Процедура СпособУказанияСрокаПриИзменении(Элемент)
	УстановитьВидимостьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОКВЭДКодПриИзменении(Элемент)
	ЗаполнитьПолеНаименованияКода("ОКВЭД");
КонецПроцедуры

&НаКлиенте
Процедура ОКВЭД2КодПриИзменении(Элемент)
	ЗаполнитьПолеНаименованияКода("ОКВЭД2");
КонецПроцедуры

&НаКлиенте
Процедура КодОКОФПриИзменении(Элемент)
	ЗаполнитьПолеНаименованияКода("ОКОФ");
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеБУПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеНУПриИзменении(Элемент)

	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьБаннер(Элемент)
	ИменаКлючейНастройки = ИменаКлючейНастройки();
	ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить(ИменаКлючейНастройки.КлючОбъекта, ИменаКлючейНастройки.КлючНастроек, Ложь);
	Элементы.ГруппаБаннер.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДокументРегистрацииНадписьНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РеквизитыДокументаРегистрации = УчетНМАКлиентСервер.НовыйРеквизитыДокументаРегистрации();

	ПараметрыФормы = Новый Структура(РеквизитыДокументаРегистрации);
	ПараметрыФормы.Наименование = Объект.ДокументРегистрацииНаименование;
	ПараметрыФормы.Номер = Объект.ДокументРегистрацииНомер;
	ПараметрыФормы.Дата = Объект.ДокументРегистрацииДата;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючДокументРегистрации());
	
	ЗаполнитьИнформациюОРегистрации(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоИсключительноеПравоПриИзменении(Элемент)
	УстановитьВидимостьИнформацииОРегистрации(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВключенВРеестрРоссийскогоПОПриИзменении(Элемент)
	УстановитьВидимостьИнформацииОРегистрации(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьВРеестреРоссийскогоПОНадписьНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	РеквизитыДокументаРегистрации = УчетНМАКлиентСервер.НовыйРеквизитыДокументаРегистрации();

	ПараметрыФормы = Новый Структура(РеквизитыДокументаРегистрации);
	ПараметрыФормы.Наименование = НаименованиеДокументаРеестроваяЗапись();
	ПараметрыФормы.Номер = Объект.НомерРеестрРоссийскогоПО;
	ПараметрыФормы.Дата = Объект.ДатаРеестрРоссийскогоПО;
	
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "РеестрРоссийскогоПО");
	
	ЗаполнитьИнформациюОРегистрации(ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьИнформациюОРегистрации(ПараметрыФормы)
	
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводИнформацииОРегистрацииЗавершение", ЭтотОбъект, ПараметрыФормы.КлючНазначенияИспользования);
	ОткрытьФорму("Справочник.НематериальныеАктивы.Форма.ИнформацияОРегистрации", ПараметрыФормы, , , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Функция КлючДокументРегистрации()
	Возврат "ДокументРегистрации";
КонецФункции

&НаКлиенте
Процедура ВводИнформацииОРегистрацииЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	РеквизитыДокумента = Новый Структура(УчетНМАКлиентСервер.НовыйРеквизитыДокументаРегистрации());
	
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, РезультатЗакрытия);
	
	Если ДополнительныеПараметры = КлючДокументРегистрации() Тогда
	
		Объект.ДокументРегистрацииНаименование = РеквизитыДокумента.Наименование;
		Объект.ДокументРегистрацииНомер = РеквизитыДокумента.Номер;
		Объект.ДокументРегистрацииДата = РеквизитыДокумента.Дата;
		
		ДокументРегистрацииНадпись = НадписьДокументРегистрации(
			Объект.ДокументРегистрацииНаименование,
			Объект.ДокументРегистрацииНомер,
			Объект.ДокументРегистрацииДата);
			
	Иначе
		
		Объект.НомерРеестрРоссийскогоПО = РеквизитыДокумента.Номер;
		Объект.ДатаРеестрРоссийскогоПО = РеквизитыДокумента.Дата;
				
		ЗаписьВРеестреРоссийскогоПОНадпись = НадписьДокументРегистрации(
			"",
			Объект.НомерРеестрРоссийскогоПО,
			Объект.ДатаРеестрРоссийскогоПО);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НаименованиеДокументаРеестроваяЗапись()
	Возврат НСтр("ru = 'Реестровая запись'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НадписьДокументРегистрации(Наименование, Номер, Дата)
	
	НадписьДокументРегистрации = ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначенияЗаполнить();
	
	Если Не ЗначениеЗаполнено(Номер)
		Или Не ЗначениеЗаполнено(Дата) Тогда
		Возврат НадписьДокументРегистрации;
	КонецЕсли;
					
	ШаблонНадписи = НСтр("ru = '%1 №%2 от %3'");
						
	НадписьДокументРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонНадписи,
		Наименование,
		Номер,
		Формат(Дата, "ДЛФ=D"));

	Возврат СокрЛП(НадписьДокументРегистрации);

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИменаКлючейНастройки()

	ИменаКлючейНастройки = Новый Структура;
	ИменаКлючейНастройки.Вставить("КлючОбъекта", "НематериальныеАктивы");
	ИменаКлючейНастройки.Вставить("КлючНастроек", "ВидимостьБаннераРезультатыИнтеллектуальнойДеятельности");
	Возврат ИменаКлючейНастройки;
	
КонецФункции

&НаСервере
Процедура УправлениеФормой()
	
	Если ЗначениеЗаполнено(Объект.СрокДействияПраваДата) Тогда
		СпособУказанияСрока = "ДоДаты";
	ИначеЕсли ЗначениеЗаполнено(Объект.СрокДействияПраваКоличествоМесяцев) Тогда
		СпособУказанияСрока = "КоличествоМесяцев";
	Иначе
		СпособУказанияСрока = "НеОграничен";
	КонецЕсли;
	
	ДокументРегистрацииНадпись = НадписьДокументРегистрации(
		Объект.ДокументРегистрацииНаименование,
		Объект.ДокументРегистрацииНомер,
		Объект.ДокументРегистрацииДата);
		
	ЗаписьВРеестреРоссийскогоПОНадпись = НадписьДокументРегистрации(
		"",
		Объект.НомерРеестрРоссийскогоПО,
		Объект.ДатаРеестрРоссийскогоПО);
		
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()

	Элементы.СрокДействияПраваДата.Видимость              = (СпособУказанияСрока = "ДоДаты");
	Элементы.СрокДействияПраваКоличествоМесяцев.Видимость = (СпособУказанияСрока = "КоличествоМесяцев");
	Элементы.НадписьМесяцы.Видимость                 = (СпособУказанияСрока = "КоличествоМесяцев");
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА") Тогда
		Элементы.ГруппаСведенияОНематериальномАктиве.Видимость = Ложь;
	Иначе
		
		ВидимостьСведенийОНематериальномАктивеБУ =
			ИспользуетсяОтражениеБУ И Объект.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив;
			
		ВидимостьСведенийОНематериальномАктивеНУ =
			ИспользуетсяОтражениеНУ И (Объект.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив
				Или Объект.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.РасходыНаНИОКР);
				
		Элементы.ГруппаСведенияОНематериальномАктиве.Видимость =
			ВидимостьСведенийОНематериальномАктивеБУ Или ВидимостьСведенийОНематериальномАктивеНУ;
			
		ВидимостьРеквизитовНУ = ВидимостьСведенийОНематериальномАктивеНУ
			И Объект.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив;
		
		Элементы.ВидНМАПоПБУ14.Видимость = ВидимостьРеквизитовНУ;
		Элементы.АмортизационнаяГруппа.Видимость = ВидимостьРеквизитовНУ;
	КонецЕсли;
	УстановитьВидимостьИнформацииОРегистрации(ЭтотОбъект);
КонецПроцедуры

// Проверяет, совпадало ли ранее полное наименование с наименованием,
// и присваивает соответствующее значение переменной ФормироватьНаименованиеПолноеАвтоматически.
//
&НаКлиентеНаСервереБезКонтекста
Функция УстановитьФлагФормироватьНаименованиеПолноеАвтоматически(НаименованиеПолное, Наименование)

	Возврат (ПустаяСтрока(НаименованиеПолное) Или НаименованиеПолное = Наименование);

КонецФункции

// Процедура проверяет, необходимо ли формировать полное наименование автоматически или нет,
// и, если необходимо, формирует его.
//
&НаКлиенте
Процедура СформироватьНаименованиеПолноеАвтоматически()

	Если ФормироватьНаименованиеПолноеАвтоматически Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПолеНаименованияКода(ИмяКлассификатора)

	Классификатор = Объект["Код" + ИмяКлассификатора];
	
	ЭтотОбъект[ИмяКлассификатора + "Наименование"] = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Классификатор, "Наименование");

КонецПроцедуры

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()

	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_НастройкаМСФОПриИзменении(Элемент)
	
	НастройкиУчетаМСФОКлиент.ПриИзмененииПоляФормы(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьИнформацииОРегистрации(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ДокументРегистрацииНадпись.Видимость = Форма.Объект.ЭтоИсключительноеПраво;
	Элементы.ЗаписьВРеестреРоссийскогоПОНадпись.Видимость = Форма.Объект.ВключенВРеестрРоссийскогоПО;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиИнформационнойБазы()
	
	ВсеОрганизацииПлательщикиНДФЛ = БухгалтерскийУчетПовтИсп.ВсеОрганизацииПлательщикиНДФЛ();
	ЕстьОрганизацииПлательщикиНДФЛ = БухгалтерскийУчетПовтИсп.ЕстьОрганизацииПлательщикиНДФЛ();
	
	ИспользуетсяОтражениеБУ = Истина;
	ИспользуетсяОтражениеНУ = Истина;
	Если (БухгалтерскийУчетВызовСервераПовтИсп.ПользователюДоступныСуммыНалогНаПрибыль() = "НеИспользовать"
		Или Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА")) И Не ЕстьОрганизацииПлательщикиНДФЛ Тогда
		ИспользуетсяОтражениеНУ = Ложь;
	ИначеЕсли ВсеОрганизацииПлательщикиНДФЛ Тогда
		ИспользуетсяОтражениеБУ = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти