
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Наименование, Дата, Номер");
	
	Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = ТолькоПросмотр;
	
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если (Не ЗначениеЗаполнено(Наименование) Или КлючНазначенияИспользования = "РеестрРоссийскогоПО")
		И Не ЗначениеЗаполнено(Дата)
		И Не ЗначениеЗаполнено(Номер) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Наименование");
		МассивНепроверяемыхРеквизитов.Добавить("Дата");
		МассивНепроверяемыхРеквизитов.Добавить("Номер");
	
	КонецЕсли; 
	
	Если КлючНазначенияИспользования = "РеестрРоссийскогоПО" И Не ЭтоНомерРеестраРоссийскогоПО(Номер) Тогда
		
		ТекстСообщения =
			НСтр("ru = 'Номер реестровой записи должен быть целым положительным числом.'");
		ТекстОшибкиЗаполнения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru = '№'"), , , ТекстСообщения);
			
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибкиЗаполнения, , "Номер", , Отказ);
		
		МассивНепроверяемыхРеквизитов.Добавить("Номер");
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	Номер = СтрЗаменить(Номер, " ", "");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОДокументе = Новый Структура;
	СведенияОДокументе.Вставить("Наименование", Наименование);
	СведенияОДокументе.Вставить("Дата", Дата);
	СведенияОДокументе.Вставить("Номер", Номер);
	
	Закрыть(СведенияОДокументе);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФорму()
	Если КлючНазначенияИспользования = "РеестрРоссийскогоПО" Тогда
		Элементы.Наименование.Видимость = Ложь;
		Заголовок = НСтр("ru = 'Реестровая запись'");
	Иначе
		Заголовок = НСтр("ru = 'Документ о регистрации'");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЭтоНомерРеестраРоссийскогоПО(ПроверяемоеЗначение)

	ОписаниеЧисла = Новый ОписаниеТипов("Число");
	НомерЧислом = ОписаниеЧисла.ПривестиЗначение(ПроверяемоеЗначение);
	
	Возврат НомерЧислом > 0 И Цел(НомерЧислом) = НомерЧислом;

КонецФункции

#КонецОбласти
