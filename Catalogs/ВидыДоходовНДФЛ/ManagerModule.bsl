#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

// Возвращает реквизиты справочника, которые образуют естественный ключ для элементов справочника.
//
// Возвращаемое значение:
//  Массив из Строка - имена реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

// Конец ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

#КонецОбласти

// АПК:134-выкл: унаследовано 
// АПК:142-выкл: унаследовано 

// Процедура прописывает реквизиты элемента, определяющие поведение данных, помеченных этим кодом, в учете и отчетности по НДФЛ.
// 
// Параметры:
//		СсылкаИлиИмяПредопределенныхДанных - Строка, СправочникСсылка.ВидыДоходовНДФЛ - элемент для описания.
//  	СтавкаНалогообложения - ПеречислениеСсылка.НДФЛСтавкиНалогообложенияРезидента.
//  	Код - Строка.
//  	Наименование - Строка.
//		ВычетРассчитываетсяАвтоматически - Булево.
//  	ИмеетЕдинственныйВычет - Булево.
//  	ВычетПоУмолчанию - СправочникСсылка.ВидыВычетовНДФЛ.
//  	НеОблагаетсяУНалоговогоАгента - Булево.
//  	СоответствуетОплатеТруда - Булево.
//  	КодПрименяемыйВНалоговойОтчетностиС2010Года - Строка.
//  	КодПрименяемыйВНалоговойОтчетностиС2021Года - Строка.
//  	КодПрименяемыйВНалоговойОтчетностиС2025Года - Строка.
//  	
Процедура ОписатьКодДоходаНДФЛ(СсылкаИлиИмяПредопределенныхДанных = Неопределено, СтавкаНалогообложения = Неопределено, Код = "", Наименование = "", ВычетРассчитываетсяАвтоматически = Ложь, ИмеетЕдинственныйВычет = Ложь, ВычетПоУмолчанию = Неопределено, НеОблагаетсяУНалоговогоАгента = Ложь, СоответствуетОплатеТруда = Ложь, КодПрименяемыйВНалоговойОтчетностиС2010Года = "", КодПрименяемыйВНалоговойОтчетностиС2021Года = "", КодПрименяемыйВНалоговойОтчетностиС2025Года = "") Экспорт 

	Если ТипЗнч(СсылкаИлиИмяПредопределенныхДанных) = Тип("Строка") И Не ПустаяСтрока(СсылкаИлиИмяПредопределенныхДанных) Тогда
		
		СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыДоходовНДФЛ." + СсылкаИлиИмяПредопределенныхДанных);
		Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
			Элемент = СсылкаПредопределенного.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ВидыДоходовНДФЛ.СоздатьЭлемент();
			Элемент.ИмяПредопределенныхДанных = СсылкаИлиИмяПредопределенныхДанных;
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(СсылкаИлиИмяПредопределенныхДанных) Тогда
			СсылкаНаЭлемент = СсылкаИлиИмяПредопределенныхДанных;
		Иначе
			СсылкаНаЭлемент = Справочники.ВидыДоходовНДФЛ.НайтиПоКоду(Код);
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
			Элемент = СсылкаНаЭлемент.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ВидыДоходовНДФЛ.СоздатьЭлемент();
		КонецЕсли;
		
	КонецЕсли;

	Если ЗначениеЗаполнено(Код) Тогда
		Элемент.Код = Код;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Наименование) Тогда
		Элемент.Наименование = Наименование;
	КонецЕсли;

	Элемент.КодПрименяемыйВНалоговойОтчетностиС2010Года	= ?(Не ЗначениеЗаполнено(КодПрименяемыйВНалоговойОтчетностиС2010Года), Элемент.Код, КодПрименяемыйВНалоговойОтчетностиС2010Года);
	Элемент.КодПрименяемыйВНалоговойОтчетностиС2021Года	= ?(Не ЗначениеЗаполнено(КодПрименяемыйВНалоговойОтчетностиС2021Года), Элемент.Код, КодПрименяемыйВНалоговойОтчетностиС2021Года);
	Элемент.КодПрименяемыйВНалоговойОтчетностиС2025Года	= ?(Не ЗначениеЗаполнено(КодПрименяемыйВНалоговойОтчетностиС2025Года), Элемент.КодПрименяемыйВНалоговойОтчетностиС2021Года, КодПрименяемыйВНалоговойОтчетностиС2025Года);
	Элемент.СтавкаНалогообложенияРезидента		= ?(Не ЗначениеЗаполнено(СтавкаНалогообложения), Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13, СтавкаНалогообложения);
	Элемент.ВычетПоУмолчанию					= ВычетПоУмолчанию;
	Элемент.ВычетРассчитываетсяАвтоматически	= ВычетРассчитываетсяАвтоматически;
	Элемент.ИмеетЕдинственныйВычет				= ИмеетЕдинственныйВычет;
	Элемент.НеОблагаетсяУНалоговогоАгента		= НеОблагаетсяУНалоговогоАгента;
	Элемент.СоответствуетОплатеТруда			= СоответствуетОплатеТруда;
	
	Элемент.ОбменДанными.Загрузка = Истина;
	Элемент.Записать();

КонецПроцедуры

// АПК:134-вкл 
// АПК:142-вкл 

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	ДанныеВыбораБЗК.ЗаполнитьДляКлассификатораСПорядкомПоКоду(
		Справочники.ВидыДоходовНДФЛ,
		ДанныеВыбора, Параметры, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура НачальноеЗаполнение() Экспорт
	
	ЗаполнитьКодыДоходовНДФЛ();
	СоздатьКодыДоходовНДФЛ();
	СоздатьКодыДоходовНДФЛДекабрь2011();
	ОбновлениеДоходовВычетовНДФЛНеПредопределенныхЯнварь2012();
	ЗаполнитьКодыДоходов2014();
	ЗаполнитьКодыДоходов2015();
	ЗаполнитьКодыДоходов2016();
	ЗаполнитьКодыДоходов2017();
	ЗаполнитьКодыДоходов2018();
	ЗаполнитьКодыДоходов2021();
	ОписатьКодыПодарков();	
	ОписатьДоходы3020и3023();
	ОписатьДоходы2023Года();
	ОписатьДоходы2024Года();
	ОписатьРКиСН();
	ПроставитьКод2025Года();
	ОписатьДоходы2025Года();
	ОписатьДоходы2026Года();
	ПереименоватьКодыДоходов2026Года();
	
	УчетЗарплаты.ЗаполнитьКодыДоходовНДФЛНепредопределенных();
	
КонецПроцедуры

// Выполняет заполнение справочника "ВидыДоходовНДФЛ" не предопределенными элементами.
//
Процедура СоздатьКодыДоходовНДФЛ() Экспорт
	
	Ставка09 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09;
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	Ставка35 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35;
	
	// Добавление непредопределенных кодов доходов.
	// АПК:1036-выкл: сокращения 
	ОписатьКодДоходаНДФЛ(, Ставка09, "1110", НСтр("ru = 'Проценты по облигациям с ипотечным покрытием, эмитированным до 01.01.2007'"));
	ОписатьКодДоходаНДФЛ(, Ставка09, "1120", НСтр("ru = 'Доходы учредителей доверительного управления ипотечным покрытием, полученные на основании приобретения ипотечных сертификатов, выданных до 01.01.2007'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1200", НСтр("ru = 'Доходы, полученные в виде иных страховых выплат по договорам страхования'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1201", НСтр("ru = 'Доходы, полученные в виде страховых выплат по договорам страхования в виде оплаты стоимости санаторно-курортных путевок'")); 
	ОписатьКодДоходаНДФЛ(, Ставка13, "1202", НСтр("ru = 'Доходы, полученные в виде страховых выплат по договорам добровольного страхования жизни (за исключением добровольного пенсионного страхования)'")); 
	ОписатьКодДоходаНДФЛ(, Ставка13, "1203", НСтр("ru = 'Доходы, полученные в виде страховых выплат по договорам добровольного имущественного страхования (включая страхование гражданской ответственности)'")); 
	ОписатьКодДоходаНДФЛ(, Ставка13, "1212", НСтр("ru = 'Доходы в виде денежных (выкупных) сумм, выплачиваемых по договорам страхования при досрочном расторжении договоров страхования'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1213", НСтр("ru = 'Доходы в виде денежных (выкупных) сумм при расторжении договора за вычетом сумм страховых взносов, уплаченных по договору добр. пенс. страхования'")); 
	ОписатьКодДоходаНДФЛ(, Ставка13, "1220", НСтр("ru = 'Доходы в виде денежных (выкупных) сумм, выплачиваемые по договорам негосударственного пенсионного обеспечения'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1240", НСтр("ru = 'Суммы пенсий, выплачиваемых по договорам негосударственного пенсионного обеспечения'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1301", НСтр("ru = 'Доходы, полученные от отчуждения авторских или иных смежных прав'")); 
	ОписатьКодДоходаНДФЛ(, Ставка13, "1400", НСтр("ru = 'Доходы, полученные от сдачи в аренду или иного использования имущества, за исключением доходов с кодами 1401, 1402 и 2400'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1540", НСтр("ru = 'Доходы, полученные от реализации долей участия в уставном капитале организаций'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1550", НСтр("ru = 'Доходы, полученные налогоплательщиком при уступке прав требования по договору участия в долевом строительстве (договору инвестирования и др.)'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2001", НСтр("ru = 'Вознаграждение директоров и иные аналогичные выплаты, получаемые членами органа управления организации (совета директоров или иного подобного органа)'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2012", НСтр("ru = 'Суммы отпускных выплат'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2300", НСтр("ru = 'Пособия по временной нетрудоспособности'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2400", НСтр("ru = 'Доходы от использования любых транспортных средств в связи с перевозками, включая штрафы; трубопроводов, ЛЭП, линий связи, компьютерных сетей'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2630", НСтр("ru = 'Материальная выгода, полученная от приобретения товаров, работ, услуг у физ. лиц, организаций, явл. взаимозависимыми по отношению к налогоплательщику'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2640", НСтр("ru = 'Материальная выгода, полученная от приобретения ценных бумаг'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2710", НСтр("ru = 'Материальная помощь (за исключением материальной помощи, учитываемой с кодами 2760 и 2761)'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2750", НСтр("ru = 'Стоимость призов в денежной и натуральной форме (за исключением стоимости призов, учитываемой с кодами 2730 и 2740)'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2780", НСтр("ru = 'Возмещение (оплата) стоимости приобретенных налогоплательщиком медикаментов в случаях, не подпадающих под действие п. 28 ст. 217 НК РФ'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2800", НСтр("ru = 'Проценты (дисконт), полученные при оплате предъявленного к платежу векселя'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2900", НСтр("ru = 'Доходы, полученные от операций с иностранной валютой'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "3010", НСтр("ru = 'Доходы в виде выигрышей, полученных от участия в азартных играх, проводимых в букмекерской конторе и тотализаторе'"));
	// АПК:1036-вкл 
	
КонецПроцедуры 

Функция ДоходыПоСтавке13(НалоговыйПериод) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ВидыДоходовНДФЛ.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.КодДоходаПоУмолчанию)
	|			ТОГДА 1
	|		КОГДА ВидыДоходовНДФЛ.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код2010)
	|			ТОГДА 3
	|		КОГДА ВидыДоходовНДФЛ.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код2760)
	|			ТОГДА 4
	|		КОГДА ВидыДоходовНДФЛ.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код2762)
	|			ТОГДА 4
	|		КОГДА ВидыДоходовНДФЛ.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код2720c2021)
	|			ТОГДА 5
	|		КОГДА ВидыДоходовНДФЛ.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код2720)
	|			ТОГДА 6
	|		КОГДА ВидыДоходовНДФЛ.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код4800)
	|			ТОГДА 6
	|		ИНАЧЕ 99
	|	КОНЕЦ КАК Порядок,
	|	ВидыДоходовНДФЛ.Код КАК Код
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Код";
	
	Доходы = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Доходы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Если НалоговыйПериод >= 2015 Тогда
		Доходы.Добавить(Справочники.ВидыДоходовНДФЛ.Код1010);
	КонецЕсли;
	
	Возврат Доходы;
	
КонецФункции

Функция ДоходыПоСтавке30(НалоговыйПериод) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка,
	|	ВидыДоходовНДФЛ.Код КАК Код
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код1010)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код";
	
	Доходы = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Доходы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Доходы;
	
КонецФункции

Функция ДоходыПоСтавке09(НалоговыйПериод) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка,
	|	ВидыДоходовНДФЛ.Код КАК Код
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код1010)
	|	И ВидыДоходовНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код";
	
	Доходы = Новый Массив;
	Если НалоговыйПериод < 2015 Тогда
		Доходы.Добавить(Справочники.ВидыДоходовНДФЛ.Код1010);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Доходы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Доходы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступноСвойствоСоответствуетОплатеТруда() Экспорт
	
	ВидыДоходов = Новый Массив;
	ВидыДоходов.Добавить(Справочники.ВидыДоходовНДФЛ.Код2002);
	
	Возврат ВидыДоходов;
	
КонецФункции

Функция ВидыДоходовСоответствующиеОплатеТруда() Экспорт 
	
	ВидыДоходов = Новый Массив;
	ВидыДоходов.Добавить(Справочники.ВидыДоходовНДФЛ.КодДоходаПоУмолчанию);
	ВидыДоходов.Добавить(Справочники.ВидыДоходовНДФЛ.Код2530);
	
	Возврат ВидыДоходов;
	
КонецФункции

// Выполняет заполнение справочника "ВидыДоходовНДФЛ".
//
Процедура ЗаполнитьКодыДоходовНДФЛ()
	
	Ставка09 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09;
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	Ставка35 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35;
	ВычетыНДФЛ = Справочники.ВидыВычетовНДФЛ;
	
	
	// Заполнение реквизитов предопределенных элементов.
	// АПК:1036-выкл: сокращения 
	ОписатьКодДоходаНДФЛ("Код1010", 			Ставка09, "1010", НСтр("ru = 'Дивиденды'"), Ложь, Истина, ВычетыНДФЛ.Код601);
	ОписатьКодДоходаНДФЛ("Код1211", 			Ставка13, "1211", НСтр("ru = 'Доходы, полученные в виде сумм страховых взносов по договорам страхования, если указанные суммы вносятся за физических лиц из средств работодателей'"), Ложь, Истина, ВычетыНДФЛ.Код607);
	ОписатьКодДоходаНДФЛ("Код1300", 			Ставка13, "1300", НСтр("ru = 'Доходы, полученные налогоплательщиком от использования в РФ авторских или иных смежных прав (кроме авторских вознаграждений)'"));
	ОписатьКодДоходаНДФЛ("КодДоходаПоУмолчанию",Ставка13, "2000", НСтр("ru = 'Вознаграждение за выполнение трудовых или иных обязанностей; денежное содержание и иные налогооблагаемые выплаты военнослужащим и приравненным к ним'"), , , , , Истина);
	ОписатьКодДоходаНДФЛ("Код2010", 			Ставка13, "2010", НСтр("ru = 'Выплаты по договорам гражданско-правового характера (за исключением авторских вознаграждений)'"), Ложь, Ложь, ВычетыНДФЛ.Код403);
	ОписатьКодДоходаНДФЛ("Код2201", 			Ставка13, "2201", НСтр("ru = 'Авторские вознаграждения (вознаграждения) за создание литературных произведений, в том числе для театра, кино, эстрады и цирка'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2202", 			Ставка13, "2202", НСтр("ru = 'Авторские вознаграждения (вознаграждения) за создание художественно-графических произведений, фоторабот для печати, произведений архитектуры и дизайна'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2203", 			Ставка13, "2203", НСтр("ru = 'Авторские вознаграждения (вознаграждения) за создание произведений скульптуры, живописи, театрально- и кинодекорационного искусства и графики'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2204", 			Ставка13, "2204", НСтр("ru = 'Авторские вознаграждения (вознаграждения) за создание аудиовизуальных произведений (видео-, теле- и кинофильмов)'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2205", 			Ставка13, "2205", НСтр("ru = 'Авторские за создание музыкальных произведений: музыкально-сценических, симфонических, хоровых, камерных, для духового оркестра, для фильмов и театра'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2206", 			Ставка13, "2206", НСтр("ru = 'Авторские вознаграждения (вознаграждения) за создание других музыкальных произведений, в том числе подготовленных к опубликованию'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2207", 			Ставка13, "2207", НСтр("ru = 'Авторские вознаграждения (вознаграждения) за исполнение произведений литературы и искусства'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2208", 			Ставка13, "2208", НСтр("ru = 'Авторские вознаграждения (вознаграждения) за создание научных трудов и разработок'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2209", 			Ставка13, "2209", НСтр("ru = 'Авторские вознаграждения за открытия, изобретения, промышленные образцы'"), Истина, Ложь, ВычетыНДФЛ.Код405);
	ОписатьКодДоходаНДФЛ("Код2210", 			Ставка13, "2210", НСтр("ru = 'Вознаграждение, выплачиваемое наследникам (правопреемникам) авторов произведений науки, литературы, искусства, открытий, изобретений и пром. образцов'"));
	ОписатьКодДоходаНДФЛ("Код2510", 			Ставка13, "2510", НСтр("ru = 'Оплата за налогоплательщика товаров, работ, услуг или имущественных прав, в том числе коммунальных услуг, питания, отдыха, обучения в его интересах'"));
	ОписатьКодДоходаНДФЛ("Код2520", 			Ставка13, "2520", НСтр("ru = 'Доход, полученный налогоплательщиком в натуральной форме, в виде полной или частичной оплаты товаров, выполненных в его интересах работ, услуг.'"));
	ОписатьКодДоходаНДФЛ("Код2530", 			Ставка13, "2530", НСтр("ru = 'Оплата труда в натуральной форме'"), , , , , Истина);
	ОписатьКодДоходаНДФЛ("Код2610",				Ставка35, "2610", НСтр("ru = 'Материальная выгода, полученная от экономии на процентах за пользование заемными (кредитными) средствами'"));
	ОписатьКодДоходаНДФЛ("Код2730", 			Ставка13, "2730", НСтр("ru = 'Стоимость призов в денежной и натуральной форме на конкурсах и соревнованиях, проводимых в соотв. с решениями Правительства РФ и др. органов власти'"), Истина, Истина, ВычетыНДФЛ.Код502);
	ОписатьКодДоходаНДФЛ("Код2740", 			Ставка35, "2740", НСтр("ru = 'Стоимость выигрышей и призов, получаемых в проводимых конкурсах, играх и других мероприятиях в целях рекламы товаров, работ и услуг'"), Истина, Истина, ВычетыНДФЛ.Код505);
	ОписатьКодДоходаНДФЛ("Код2760", 			Ставка13, "2760", НСтр("ru = 'Материальная помощь, оказываемая работодателями своим работникам, а также бывшим своим работникам, уволившимся в связи с выходом на пенсию'"), Истина, Истина, ВычетыНДФЛ.Код503);
	ОписатьКодДоходаНДФЛ("Код2761", 			Ставка13, "2761", НСтр("ru = 'Материальная помощь, оказываемая инвалидам общественными организациями инвалидов'"), Истина, Истина, ВычетыНДФЛ.Код506);
	ОписатьКодДоходаНДФЛ("Код2762",				Ставка13, "2762", НСтр("ru = 'Материальная помощь, оказываемая работодателями своим работникам при рождении (усыновлении) ребенка'"), Истина, Истина, ВычетыНДФЛ.Код508);
	ОписатьКодДоходаНДФЛ("Код2770", 			Ставка13, "2770", НСтр("ru = 'Возмещение (оплата) работодателями своим работникам и членам их семьи, бывшим своим работникам-пенсионерам, а также инвалидам стоимости медикаментов'"), Истина, Истина, ВычетыНДФЛ.Код504);
	ОписатьКодДоходаНДФЛ("Код2790", 			Ставка13, "2790", НСтр("ru = 'Сумма помощи (в денежной и натуральной форме), стоимость подарков, полученных ветеранами, инвалидами Великой Отечественной войны и приравненных к ним'"), Истина, Истина, ВычетыНДФЛ.Код507);
	ОписатьКодДоходаНДФЛ("Код4800", 			Ставка13, "4800", НСтр("ru = 'Иные доходы'"), Ложь, Истина, ВычетыНДФЛ.Код620);
	// АПК:1036-вкл 
	
КонецПроцедуры 

Процедура СоздатьКодыДоходовНДФЛДекабрь2011() 
	
	Ставка35 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35;
	
	// АПК:1036-выкл: сокращения 
	ОписатьКодДоходаНДФЛ(, Ставка35, "3022", НСтр("ru = 'Доходы в виде платы за использование денежных средств членов кр.потреб.кооператива или %% за исп-е с-х кр.потреб.кооперативом займов (с 01.01.2011)'"));
	// АПК:1036-вкл 

КонецПроцедуры	

Процедура ОбновлениеДоходовВычетовНДФЛНеПредопределенныхЯнварь2012()
	
	// АПК:1327-выкл используется при начальном заполнении ИБ.
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоходыНДФЛ.Ссылка
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ДоходыНДФЛ
	|ГДЕ
	|	ДоходыНДФЛ.Код = ""1541""
	|	И НЕ ДоходыНДФЛ.Предопределенный
	|	И НЕ ДоходыНДФЛ.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "-";
		Объект.ПометкаУдаления = Истина;
		Объект.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
		Объект.Записать();
	КонецЕсли;
	
	// АПК:1327-вкл
	
КонецПроцедуры

Процедура ЗаполнитьКодыДоходов2014()
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	ВычетыНДФЛ = Справочники.ВидыВычетовНДФЛ;	
	
	ОписатьКодДоходаНДФЛ("Код1530", Ставка13, "1530", НСтр("ru = 'Доходы, полученные по операциям с ценными бумагами, обращающимися на организованном рынке ценных бумаг'"), Ложь, Ложь, ВычетыНДФЛ.Код201);
	ОписатьКодДоходаНДФЛ("Код1531", Ставка13, "1531", НСтр("ru = 'Доходы, полученные от операций с ценными бумагами, не обращающимися  на  организованном  рынке  ценных бумаг'"), Ложь, Ложь, ВычетыНДФЛ.Код202);
	ОписатьКодДоходаНДФЛ("Код1536", Ставка13, "1536", НСтр("ru = 'Доход полученные по операциям купли-продажи ценных бумаг, не обращающихся на организованном рынке ценных бумаг, но отвечавших его требованиям'"), Ложь, Ложь, ВычетыНДФЛ.Код203);
	ОписатьКодДоходаНДФЛ("Код1537", Ставка13, "1537", НСтр("ru = 'Доходы в виде процентов по займу, полученные по совокупности операций РЕПО'"), Ложь, Истина,   ВычетыНДФЛ.Код211);
	ОписатьКодДоходаНДФЛ("Код1538", Ставка13, "1538", НСтр("ru = 'Доходы в виде процентов, полученные в налоговом периоде по совокупности договоров займа'"), Ложь, Истина, ВычетыНДФЛ.Код215);
	ОписатьКодДоходаНДФЛ("Код1539", Ставка13, "1539", НСтр("ru = 'Доходы по операциям, связанным с открытием короткой позиции, являющимся объектом операций РЕПО'"), Ложь, Ложь, ВычетыНДФЛ.Код213);
	ОписатьКодДоходаНДФЛ("Код1541", Ставка13, "1541", НСтр("ru = 'Доходы, получаемые в результате обмена ценных бумаг, переданных по первой части РЕПО'"), Ложь, Истина, ВычетыНДФЛ.Код620);
	
КонецПроцедуры	

Процедура ЗаполнитьКодыДоходов2015()
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	
	// АПК:1036-выкл: сокращения 
	ОписатьКодДоходаНДФЛ(, Ставка13, "1215", НСтр("ru = 'Доходы в виде денежных (выкупных) сумм, выплачиваемые по договорам негосударственного пенсионного обеспечения'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1219", НСтр("ru = 'Суммы страховых взносов, в отношении которых был предоставлен социальный налоговый вычет, в случае расторжения договора добровольного пенс.страхования'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1220", НСтр("ru = 'Суммы страховых взносов, в отношении которых был предоставлен социальный налоговый вычет, учитыв. в случае расторжения негос. пенсионного обеспечения'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1542", НСтр("ru = 'Доходы в виде действительной стоимости доли в уставном капитале организации, выплачиваемые при выходе участника из организации'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1550", НСтр("ru = 'Доходы, полученные при уступке прав требования по договору участия в долевом строительстве'"));
	// АПК:1036-вкл 
	
КонецПроцедуры	

Процедура ЗаполнитьКодыДоходов2016()
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	ВычетыНДФЛ = Справочники.ВидыВычетовНДФЛ;	
	
	// АПК:1036-выкл: сокращения 
	ОписатьКодДоходаНДФЛ("Код1532", Ставка13, "1532", НСтр("ru = 'Доходы по опер. с произв.фин-выми инстр-ми, обр-ся на орг.рынке и базисным активом кот.явл. цен.бум., фонд.индексы, иные произв.фин-вые инстр-ты'"), Ложь, Ложь, ВычетыНДФЛ.Код205);
	ОписатьКодДоходаНДФЛ("Код1533", Ставка13, "1533", НСтр("ru = 'Доходы по операциям с производными финансовыми инструментами, не обращающимися на организованном рынке'"), Ложь, Ложь, ВычетыНДФЛ.Код220);
	ОписатьКодДоходаНДФЛ("Код1535", Ставка13, "1535", НСтр("ru = 'Доходы по опер. с произв.фин-выми инстр-ми, обр-ся на орг.рынке и базисным активом кот. не явл. цен.бум., фонд.индексы, иные произв.фин-вые инстр-ты'"), Ложь, Ложь, ВычетыНДФЛ.Код207);
	
	ОписатьКодДоходаНДФЛ("Код1543", Ставка13, "1543", НСтр("ru = '(не используется с 2016 года) Доходы по операциям, учитываемым на индивидуальном инвестиционном счете'"), Ложь, Ложь, ВычетыНДФЛ.ПустаяСсылка(),,,, "-");
	
	ОписатьКодДоходаНДФЛ("Код1544", Ставка13, "1544", НСтр("ru = 'Доходы по операциям с ценными бумагами, обращающимися на организованном рынке ценных бумаг, учитываемым на индивидуальном инвестиционном счете'"), Ложь, Ложь, ВычетыНДФЛ.Код221);
	ОписатьКодДоходаНДФЛ("Код1545", Ставка13, "1545", НСтр("ru = 'Доходы по операциям с ценными бумагами, не обращающимися на организованном рынке ценных бумаг, учитываемым на индивидуальном инвестиционном счете'"), Ложь, Ложь, ВычетыНДФЛ.Код226);
	ОписатьКодДоходаНДФЛ("Код1546", Ставка13, "1546", НСтр("ru = 'Доходы по опер. с произв.фин-выми инстр-ми, обр-ся на орг.рынке и базисным активом кот.явл. цен.бум., фонд.индексы и пр., учит. на инд. инвест.счете'"), Ложь, Ложь, ВычетыНДФЛ.Код228);
	ОписатьКодДоходаНДФЛ("Код1547", Ставка13, "1547", НСтр("ru = 'Доходы по операциям с производными финансовыми инструментами, не обращающимися на орг. рынке ценных бумаг, учитываемым на индивидуальном инвест.счете'"), Ложь, Ложь, ВычетыНДФЛ.Код235);
	ОписатьКодДоходаНДФЛ("Код1548", Ставка13, "1548", НСтр("ru = 'Доходы по опер. с произв.фин-выми инстр., обр-ся на орг.рынке и базисным активом кот. не явл. цен.бум., фонд.индексы и пр., учит. на инд.инвест.счете'"), Ложь, Ложь, ВычетыНДФЛ.Код229);
	ОписатьКодДоходаНДФЛ("Код1549", Ставка13, "1549", НСтр("ru = 'Доходы по опер. с ценными бум., не обр-ся на орг.рынке ц.бумаг, кот. при приобретении отвечали треб-ям для обр-хся ц.бум., учит. на инд. инвест.счете'"), Ложь, Ложь, ВычетыНДФЛ.Код227);
	
	ОписатьКодДоходаНДФЛ("Код1551", Ставка13, "1551", НСтр("ru = 'Доходы в виде процентов по займу, полученные по совокупности операций РЕПО, учитываемых на индивидуальном инвестиционном счете'"), Ложь, Ложь, ВычетыНДФЛ.Код230);
	ОписатьКодДоходаНДФЛ("Код1552", Ставка13, "1552", НСтр("ru = 'Доходы в виде процентов, полученных в налоговом периоде по совокупности договоров займа, учитываемых на индивидуальном инвестиционном счете'"), Ложь,Ложь, ВычетыНДФЛ.Код232);
	ОписатьКодДоходаНДФЛ("Код1553", Ставка13, "1553", НСтр("ru = 'Доходы по операциям, связанным с открытием короткой позиции, являющимся объектом операций РЕПО, учитываемых на индивидуальном инвестиционном счете'"), Ложь,Ложь, ВычетыНДФЛ.Код231);
	ОписатьКодДоходаНДФЛ("Код1554", Ставка13, "1554", НСтр("ru = 'Доходы, получаемые в результате обмена ценных бумаг, переданных по первой части РЕПО, учитываемых на индивидуальном инвестиционном счете'"));
	
	ОписатьКодДоходаНДФЛ("Код2002", Ставка13, "2002", НСтр("ru = 'Суммы премий за производственные результаты, предусмотренные зак-вом РФ, трудовыми (коллективными) договорами кроме доходов с кодом 2003'"));
	ОписатьКодДоходаНДФЛ("Код2003", Ставка13, "2003", НСтр("ru = 'Суммы вознаграждений, выплачиваемых за счет средств прибыли организации, средств специального назначения или целевых поступлений'"));
	
	ОписатьКодДоходаНДФЛ(, Ставка13, "2641", НСтр("ru = 'Материальная выгода, полученная от приобретения производных финансовых инструментов'"));
	ОписатьКодДоходаНДФЛ("Код2791", Ставка13, "2791", НСтр("ru = '(не используется с 2016 года) Доходы, полученные работниками в натуральной форме в качестве оплаты труда от с/х товаропроизводителей, упл-х ЕСХН, и крестьянских х-в'"), Ложь, Ложь, ВычетыНДФЛ.ПустаяСсылка(),,,, "-");
	ОписатьКодДоходаНДФЛ(, Ставка13, "3010", НСтр("ru = 'Доходы в виде выигрышей, полученных в букмекерской конторе и тотализаторе'"));
	// АПК:1036-вкл 
	
КонецПроцедуры	

Процедура ЗаполнитьКодыДоходов2017()
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	
	ОписатьКодДоходаНДФЛ("Код2013", Ставка13, "2013", НСтр("ru = 'Сумма компенсации за неиспользованный отпуск'"));
	
КонецПроцедуры

Процедура ЗаполнитьКодыДоходов2018()
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	
	ОписатьКодДоходаНДФЛ("Код2014", Ставка13, "2014", НСтр("ru = 'Сумма выплаты в виде выходного пособия, среднего месячного заработка, превышающей 3-х кратный размер среднего заработка'"));
	ОписатьКодДоходаНДФЛ( , Ставка13, "2301", НСтр("ru = 'Суммы штрафов и неустойки, выплачиваемые на основании решения суда за несоблюдение удовлетворения требований потребителей'"));
	ОписатьКодДоходаНДФЛ( , Ставка13, "2611", НСтр("ru = 'Суммы прекращенных обязательств по уплате задолженности в связи с признанием такой задолженности в установленном порядке безнадежной к взысканию'"));
	
КонецПроцедуры

Процедура ИсправитьОписаниеКода2210() Экспорт
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.ВидыДоходовНДФЛ.Код2210, "НеОблагаетсяУНалоговогоАгента") Тогда
		// АПК:1036-выкл: сокращения 
		ОписатьКодДоходаНДФЛ("Код2210", Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13, "2210", НСтр("ru = 'Вознаграждение, выплачиваемое наследникам (правопреемникам) авторов произведений науки, литературы, искусства, открытий, изобретений и пром. образцов'"));
		// АПК:1036-вкл 
	КонецЕсли;
	
КонецПроцедуры 

Процедура ИсправитьОписаниеКода3023() Экспорт
	
	ИсправитьСтавкуДоходаНа35Процентов("3023");	
	
КонецПроцедуры 

Процедура ЗаполнитьКодыДоходов2021() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	
	ОписатьКодДоходаНДФЛ(, Ставка13, "1401", НСтр("ru = 'Доходы, полученные от сдачи в аренду или иного использования жилого недвижимого имущества'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1402", НСтр("ru = 'Доходы, полученные от сдачи в аренду или иного использования недвижимого имущества, за исключением доходов с кодом 1401'"));
	ОписатьКодДоходаНДФЛ("Код2004", Ставка13, "2004", НСтр("ru = 'Суммы выплат ежемесячного денежного вознаграждения за классное руководство педагогическим работникам за счет средств федерального бюджета'"),,,,, Истина);
	ОписатьДоход2015(?(ЗначениеЗаполнено(Справочники.ВидыДоходовНДФЛ.НайтиПоКоду("2018")), Неопределено, Справочники.ВидыДоходовНДФЛ.НайтиПоКоду("2017")));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2611", НСтр("ru = 'Суммы прекращенных обязательств по уплате задолженности в связи с признанием такой задолженности в установленном порядке безнадежной к взысканию'"));
	ОписатьКодДоходаНДФЛ("Код2763",	Ставка13, "2763", НСтр("ru = 'Материальная помощь, оказываемая образовательной организацией студентам (курсантам), аспирантам, адъюнктам, ординаторам и ассистентам-стажерам'"), Истина, Истина, Справочники.ВидыВычетовНДФЛ.Код511, , , "2763");
	ОписатьКодДоходаНДФЛ(, Ставка13, "3010", НСтр("ru = 'Доходы в виде выигрышей, полученных от участия в азартных играх, проводимых в букмекерской конторе и тотализаторе'"));
	ОписатьКодДоходаНДФЛ("Код3011", Ставка13, "3011", НСтр("ru = 'Доходы в виде выигрышей, полученных от участия в лотерее'"));
	
КонецПроцедуры

Процедура ВосстановитьКоды1400и2400() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	
	ОписатьКодДоходаНДФЛ(, Ставка13, "1400", НСтр("ru = 'Доходы, полученные от сдачи в аренду или иного использования имущества, за исключением доходов с кодами 1401, 1402 и 2400'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "2400", НСтр("ru = 'Доходы от использования любых транспортных средств в связи с перевозками, включая штрафы; трубопроводов, ЛЭП, линий связи, компьютерных сетей'"));
	
КонецПроцедуры

Процедура ПереименоватьКодыДоходовДекабрь2021() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	
	ОписатьКодДоходаНДФЛ(, Ставка13, "1401", НСтр("ru = 'Доходы, полученные от сдачи в аренду или иного использования жилого недвижимого имущества'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1402", НСтр("ru = 'Доходы, полученные от сдачи в аренду или иного использования недвижимого имущества, за исключением доходов с кодом 1401'"));
	ОписатьДоход2015(Справочники.ВидыДоходовНДФЛ.НайтиПоКоду("2017"));
	
КонецПроцедуры

Процедура ОписатьКодыПодарков() Экспорт

	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	ВычетыНДФЛ = Справочники.ВидыВычетовНДФЛ;	
	ОписатьКодДоходаНДФЛ("Код2720", 	 Ставка13, "2721", НСтр("ru = 'Стоимость имущества, полученного в порядке дарения, за исключением доходов с кодом 2720'"), Истина, Истина, ВычетыНДФЛ.Код501, , , "2720");
	ОписатьКодДоходаНДФЛ("Код2720c2021", Ставка13, "2720", НСтр("ru = 'Стоимость имущества, полученного в порядке дарения, налоговая база по которому определяется в соответствии с пунктом 6 статьи 210'"), Истина, Истина, ВычетыНДФЛ.Код501, , , "-");

КонецПроцедуры

Процедура ПроставитьКод2021Года() Экспорт
	
	// АПК:1327-выкл используется при начальном заполнении ИБ.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка КАК Ссылка,
	|	ВидыДоходовНДФЛ.Код КАК Код
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.КодПрименяемыйВНалоговойОтчетностиС2021Года = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года;
		Если Выборка.Ссылка = Справочники.ВидыДоходовНДФЛ.Код2720 Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "2720";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "2721";
		КонецЕсли;
		Если Выборка.Ссылка = Справочники.ВидыДоходовНДФЛ.Код2720c2021 Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "-";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "2720";
		КонецЕсли;
		Если Выборка.Ссылка = Справочники.ВидыДоходовНДФЛ.Код2791 Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "2791";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "-";
		КонецЕсли;
		Если Выборка.Ссылка = Справочники.ВидыДоходовНДФЛ.Код1543 Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "1543";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "-";
		КонецЕсли;
		Если Выборка.Код = "3020_2021" Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "-";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "3020";
		КонецЕсли;
		Если Выборка.Код = "3020" Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "3020";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "-";
		КонецЕсли;
		Если Выборка.Код = "3023_2021" Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "-";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "3023";
		КонецЕсли;
		Если Выборка.Код = "3023" Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2010Года = "3023";
			Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года = "-";
		КонецЕсли;
		Объект.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
		Объект.Записать();
		
	КонецЦикла;
	
	// АПК:1327-вкл
	
КонецПроцедуры

Процедура ОписатьДоходы3020и3023() Экспорт

	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	Ставка35 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35;
	ОписатьКодДоходаНДФЛ(, Ставка13, "3020_2021", НСтр("ru = 'Доходы в виде процентов, получаемых по вкладам в банках'"),,,, Истина,, "-", "3020");
	ОписатьКодДоходаНДФЛ(, Ставка35, "3020", НСтр("ru = 'Доходы в виде процентов, получаемых по вкладам в банках'"),,,,,,,"-");
	ОписатьКодДоходаНДФЛ(, Ставка13, "3023_2021", НСтр("ru = 'Сумма дохода, получаемого по обращающимся облигациям, номинированным в рублях и эмитированным после 1 января 2017 года'"),,,,,, "-", "3023");
	ОписатьКодДоходаНДФЛ(, Ставка35, "3023", НСтр("ru = 'Сумма дохода, получаемого по обращающимся облигациям, номинированным в рублях и эмитированным после 1 января 2017 года'"),,,,,,,"-");

КонецПроцедуры

Процедура ОписатьДоход2015(Ссылка = Неопределено) Экспорт
	
	ОписатьКодДоходаНДФЛ(Ссылка, Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13, "2015", НСтр("ru = 'Суточные, превышающие 700 руб. за каждый день в командировке на территории РФ и превышающие 2500 руб. за каждый день в командировке за пределами РФ'"),,,,,,"4800","2015");

КонецПроцедуры

Процедура ОписатьДоходы2023Года() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	ОписатьКодДоходаНДФЛ(, Ставка13, "1011", НСтр("ru = 'Проценты (за исключением доходов с кодами 1110, 2800 и 3020), вкл. дисконт, полученный по долговому обяз-ву любого вида за искл. доходов с кодом 3023'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "1013", НСтр("ru = 'Проценты, полученные по долговому обязательству любого вида, за исключением указанных по кодам 1011 и 3023'"),,,,,,"-");
	ОписатьКодДоходаНДФЛ(, Ставка13, "2016", НСтр("ru = 'Безотчетные суммы в целях возмещения дополнительных расходов, превышающие 700 рублей за каждый день в командировке'"),,,,,,"-");
	ОписатьКодДоходаНДФЛ(, Ставка13, "3100", НСтр("ru = 'Доходы, полученные по договору об осуществлении опеки или попечительства на возмездных условиях (договору о приемной семье, договору о патронатной семье)'"),,,,,,"-");
	ОписатьКодДоходаНДФЛ(, Ставка13, "3110", НСтр("ru = 'Доходы в виде компенсаций от государственных органов и общественных объединений за время исполнения государственных или иных обязанностей'"),,,,,,"-");

КонецПроцедуры

Процедура ОписатьДоходы2024Года() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	Ставка09 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09;
	ОписатьКодДоходаНДФЛ(,			Ставка13, "2017", НСтр("ru = 'Суточные или полевое довольствие работникам, превышающие 700 рублей за каждый день на территории РФ, превышающие 2500 рублей за каждый день за пределами РФ'"),,,,,,"-");
	ОписатьКодДоходаНДФЛ(,			Ставка13, "2018", НСтр("ru = 'Возмещение расходов дистанционного работника в сумме, превышающей 35 рублей за каждый день выполнения трудовой функции дистанционно'"),,,,,,"-");
	ОписатьКодДоходаНДФЛ("Код1010",	Ставка09, "1010", НСтр("ru = 'Дивиденды'"), Ложь, Истина);
	ОписатьКодДоходаНДФЛ(,			Ставка13, "3023_2021", НСтр("ru = 'Сумма дохода, получаемого по обращающимся облигациям, номинированным в рублях и эмитированным после 1 января 2017 года'"),,, Справочники.ВидыВычетовНДФЛ.Код253,,, "-", "3023");

КонецПроцедуры

Процедура ОписатьРКиСН() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	ОписатьКодДоходаНДФЛ("ДовольствиеСВО"			,Ставка13, "ДСВО", НСтр("ru = 'Денежное довольствие и иные выплаты, непосредственно связанные с участием в специальной военной операции'"), , , , , Истина,"2000","2000","2005");
	ОписатьКодДоходаНДФЛ("РайонныеСеверныеНадбавки"	,Ставка13, "РКСН", НСтр("ru = 'Суммы районных коэффициентов к зар.плате за работу в районах Крайнего Севера, приравненных к ним местностях, и процентных надбавок к заработной плате'"), , , , , Истина,"2000","2000","2006");
	
КонецПроцедуры

Процедура ПроставитьКод2025Года() Экспорт
	
	// АПК:1327-выкл используется при начальном заполнении ИБ.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка КАК Ссылка,
	|	ВидыДоходовНДФЛ.Код КАК Код
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.КодПрименяемыйВНалоговойОтчетностиС2025Года = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.КодПрименяемыйВНалоговойОтчетностиС2025Года = Объект.КодПрименяемыйВНалоговойОтчетностиС2021Года;
		Если Выборка.Ссылка = Справочники.ВидыДоходовНДФЛ.ДовольствиеСВО Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2025Года = "2005";
		КонецЕсли;
		Если Выборка.Ссылка = Справочники.ВидыДоходовНДФЛ.РайонныеСеверныеНадбавки Тогда
			Объект.КодПрименяемыйВНалоговойОтчетностиС2025Года = "2006";
		КонецЕсли;
		Объект.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
		Объект.Записать();
		
	КонецЦикла;
	
	// АПК:1327-вкл
	
КонецПроцедуры

Процедура ОписатьДоходы2025Года() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	ОписатьКодДоходаНДФЛ("Код2010", Ставка13, "2010", НСтр("ru = 'Выплаты по договорам гражданско-правового характера (за исключением авторских вознаграждений)'"), , , Справочники.ВидыВычетовНДФЛ.Код403);
	ОписатьКодДоходаНДФЛ(,			Ставка13, "1241", НСтр("ru = 'Выплаты по договорам долгосрочных сбережений, заключенным физическими лицами с имеющими соответствующую лицензию российскими НПФ'"),,,,,,"-","-", "1241");
	ОписатьКодДоходаНДФЛ(,			Ставка13, "1242", НСтр("ru = 'Денежные (выкупные) суммы за вычетом сумм платежей (взносов), которые подлежат выплате в соответствии с правилами и условиями договоров, заключенных с российскими НПФ...'"),,,,,,"-","-", "1242");
	ОписатьКодДоходаНДФЛ("Код2792",	Ставка13, "", НСтр("ru = 'Доходы от реализации (погашения) долей участия в уставном капитале российских организаций, а также некоторых акций'"),,, Справочники.ВидыВычетовНДФЛ.Код513,,, "-", "-", "2792");
	ОписатьКодДоходаНДФЛ("Код2793",	Ставка13, "", НСтр("ru = 'Доходы от реализации (погашения) акций, облигаций российских организаций, инвестиционных паев при особых условиях'"),,, Справочники.ВидыВычетовНДФЛ.Код514,,, "-", "-", "2793");
	ОписатьКодДоходаНДФЛ("Код2540",	Ставка13, "", НСтр("ru = 'Доход в натуральной форме, безвозмездно или с частичной оплатой в виде ЦФА и (или) цифровых правах, вкл. одновременно ЦФА и утилитарные цифровые права'"),,,,,, "-", "-", "2540");
	ОписатьКодДоходаНДФЛ("Код2650",	Ставка13, "", НСтр("ru = 'Материальная выгода, полученная от приобретения ЦФА и (или) цифровых прав, включающих одновременно ЦФА и утилитарные цифровые права'"),,,,,, "-", "-", "2650");
	ОписатьКодДоходаНДФЛ("Код2651",	Ставка13, "", НСтр("ru = 'Мат.выгода, полученная от приобретения ценных бумаг, полученных в результате их мены на ЦФА и (или) цифр.права, вкл. и ЦФА и утилитарные цифр.права'"),,,,,, "-", "-", "2651");
	ОписатьКодДоходаНДФЛ("Код3030",	Ставка13, "", НСтр("ru = 'Доходы в виде выплат, не связанных с выкупом ЦФА и/или цифр.права, включающих и ЦФА и утилитарное цифр.право (за исключением доходов с кодом 3031)'"),,,,,, "-", "-", "3030");
	ОписатьКодДоходаНДФЛ("Код3031",	Ставка13, "", НСтр("ru = 'Доходы в виде выплат, не связанных с выкупом ЦФА, в случае, если решением о выпуске этого ЦФА предусмотрена выплата дохода в сумме дивидендов'"),,,,,, "-", "-", "3031");
	ОписатьКодДоходаНДФЛ("Код3032",	Ставка13, "", НСтр("ru = 'Доходы в виде выплат в денежной форме, связанных с выкупом ЦФА и (или) цифрового права, включающего одновременно ЦФА и утилитарное цифровое право'"),,,,,, "-", "-", "3032");
	ОписатьКодДоходаНДФЛ("Код3033",	Ставка13, "", НСтр("ru = 'Доходы от отчуждения ЦФА и/или цифр.права, включающего и ЦФА и утилитарное цифр.право, в том числе в результате мены'"),,,,,, "-", "-", "3033");
                                                                                                              
КонецПроцедуры

Процедура ИсправитьСтавкуДоходаНа35Процентов(Код, ВычетРассчитываетсяАвтоматически = Ложь, ИмеетЕдинственныйВычет = Ложь, ВычетПоУмолчанию = Неопределено)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.Код = ""ХХ""
	|	И ВидыДоходовНДФЛ.СтавкаНалогообложенияРезидента <> ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "ХХ", Код);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОписатьКодДоходаНДФЛ(Выборка.Ссылка, Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35, , , ВычетРассчитываетсяАвтоматически, ИмеетЕдинственныйВычет, ВычетПоУмолчанию);
	КонецЕсли;	

КонецПроцедуры

Процедура ПереименоватьКодыДоходов2026Года() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	
	// АПК:1036-вкл 
	ОписатьКодДоходаНДФЛ(, Ставка13, "1215", НСтр("ru = '(не используется с 2026 года) Доходы в виде денежных (выкупных) сумм, выплачиваемые по договорам негосударственного пенсионного обеспечения'"));
	ОписатьКодДоходаНДФЛ(, Ставка13, "3020_2021", НСтр("ru = '(не используется с 2026 года) Доходы в виде процентов, получаемых по вкладам в банках'"),,,, Истина,, "-", "3020");
	ОписатьКодДоходаНДФЛ(, Ставка13, "1241", НСтр("ru = 'Выплаты по договорам долгосрочных сбережений, закл. с имеющими соотв. лицензию росс-ми НПФ, за искл. выплат, ук-ых в абз.8 и 9 п.1 ст.213.1 НК РФ'"),,,,,,"-","-", "1241");
	// АПК:1036-выкл: сокращения 
	
КонецПроцедуры

Процедура ОписатьДоходы2026Года() Экспорт
	
	Ставка13 = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
	// АПК:1036-вкл 
	ОписатьКодДоходаНДФЛ("Код2007", Ставка13, "2007", НСтр("ru = 'Единовременное денежное поощрение, выплачиваемое при награждении гос.наградами РФ за особые заслуги в зоне проведения специальной военной операции'"), , , , , Истина,"-","-","2007");
	
	ОписатьКодДоходаНДФЛ("Код1221", Ставка13, "1221", НСтр("ru = 'Суммы пенс. взносов, явл. социальным вычетом пп. 1 п. 1 ст. 219.2 НК РФ, учит-ым в случае расторжения договора негосударственного пенс. обеспечения'"), , , , , ,"-","-","1221");
	ОписатьКодДоходаНДФЛ("Код1222", Ставка13, "1222", НСтр("ru = 'Суммы сберегательных взносов, явл. социальным вычетом пп. 2 п. 1 ст. 219.2 НК РФ, учитываемым в случае расторжения договора долгосрочных сбережений'"), , , , , ,"-","-","1222");
	ОписатьКодДоходаНДФЛ("Код1243", Ставка13, "1243", НСтр("ru = 'Суммы выплат по договору ДС с имеющ. лицензию НПФ, вкл. суммы взносов по договору, превыш. уплаченные сбер. взносы, в пределах 30 млн руб.'"), , , , , ,"-","-","1243");
	// АПК:1036-выкл: сокращения 
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли

