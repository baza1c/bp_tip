#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"Дата, Номер, Комментарий, Подписано, ФайлСоглашения, НомерСтроки, ДоговорКонтрагента");
	
	Если ТолькоПросмотр Тогда
		Элементы.КомандаОчиститьФайл.Видимость = Ложь;
		Элементы.ГруппаФайл.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьПредставлениеФайлаСоглашения(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
	ИначеЕсли Модифицированность И Не ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ПеренестиВДокумент И (Модифицированность Или ЭтоНовоеСоглашение()) Тогда
		
		СтруктураРезультат = Новый Структура("Дата, Номер, Комментарий, Подписано, ФайлСоглашения, НомерСтроки");
		
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, ЭтотОбъект);
		
		ОповеститьОВыборе(СтруктураРезультат);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ДоговорыКонтрагентовПрисоединенныеФайлы") Тогда
		ФайлСоглашения = ВыбранноеЗначение;
		ОбработатьИзменениеФайлаСоглашения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДоговорыКонтрагентов_СозданиеФайлаДополнительногоСоглашения"
		И Параметр.ДоговорКонтрагента = ДоговорКонтрагента Тогда
		ОбработатьОповещениеСозданиеФайлаДополнительногоСоглашения(Параметр.ФайлДоговора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФайлСоглашенияРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьФайлСоглашения" Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ФайлСоглашения) И ФайлСозданИзШаблонаДоговора(ФайлСоглашения) Тогда
			ОткрытьФормуРедактированияТекстаСоглашения();
		Иначе
			ОткрытьФайлСоглашения();
		КонецЕсли;
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОчиститьФайлСоглашения" Тогда
		СтандартнаяОбработка = Ложь;
		ОчиститьФайлСоглашения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	ПеренестиВДокумент = Истина;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)

	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура КомандаПечать(Команда)
	
	ПечатьДоговоровКлиент.ВыполнитьОткрытиеТекстаДополнительногоСоглашения(
		ДоговорКонтрагента, ЭтотОбъект, РеквизитыСоглашенияДляПечати());
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаФайл(Команда)
	
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить(ВариантФайлСДиска(),         НСтр("ru = 'Файл с диска...'"));
	СписокВариантов.Добавить(ВариантПрисоединенныйФайл(), НСтр("ru = 'Присоединенный файл...'"));
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ВывестиВариантыВыбораФайлаЗавершение", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОповещениеОВыборе, СписокВариантов, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОчиститьФайл(Команда)
	
	ОчиститьФайлСоглашения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ЭтоНовоеСоглашение()
	
	Возврат НомерСтроки = Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция ФайлСозданИзШаблонаДоговора(ФайлСоглашения)
	
	ПараметрыДанныхФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыДанныхФайла.ПолучатьСсылкуНаДвоичныеДанные = Ложь;
	ПараметрыДанныхФайла.ВызыватьИсключение = Ложь;
	ДанныеФайлаСоглашения = РаботаСФайлами.ДанныеФайла(ФайлСоглашения, ПараметрыДанныхФайла);
	РасширениеФайла = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеФайлаСоглашения, "Расширение");
	Возврат НРег(РасширениеФайла) = "htm" Или НРег(РасширениеФайла) = "html";
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуРедактированияТекстаСоглашения()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ФайлДоговора", ФайлСоглашения);
	ПараметрыФормы.Вставить("РеквизитыДополнительногоСоглашения", РеквизитыСоглашенияДляПечати(ФайлСоглашения));
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаРедактированияТекстаДоговора", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлСоглашения()
	
	ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ФайлСоглашения, УникальныйИдентификатор);
	
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФайлСоглашения()
	
	ФайлСоглашения = Неопределено;
	ОбработатьИзменениеФайлаСоглашения();
	
КонецПроцедуры

&НаКлиенте
Функция РеквизитыСоглашенияДляПечати(Файл = Неопределено)
	
	РеквизитыСоглашения = ПечатьДоговоровКлиентСервер.РеквизитыДополнительногоСоглашения();
	РеквизитыСоглашения.НомерСоглашения = Номер;
	РеквизитыСоглашения.ДатаСоглашения = Дата;
	РеквизитыСоглашения.СоглашениеПодписано = Подписано;
	РеквизитыСоглашения.ФайлСоглашения = Файл;
	РеквизитыСоглашения.КомментарийСоглашения = Комментарий;
	
	Возврат РеквизитыСоглашения;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьОповещениеСозданиеФайлаДополнительногоСоглашения(ФайлДоговора)
	
	ФайлСоглашения = ФайлДоговора;
	ОбработатьИзменениеФайлаСоглашения();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ВыбранФайлСоглашения = ЗначениеЗаполнено(Форма.ФайлСоглашения);
	
	Элементы.ГруппаНовый.Видимость = Не ВыбранФайлСоглашения;
	Элементы.ГруппаСуществующий.Видимость = ВыбранФайлСоглашения;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиВариантыВыбораФайлаЗавершение(ВыбранныйВариант, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйВариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйВариант.Значение = ВариантФайлСДиска() Тогда
		
		ОповещениеОВыборе = Новый ОписаниеОповещения("ВыборФайлаСДискаЗавершение", ЭтотОбъект);
		
		РаботаСФайламиКлиент.ДобавитьФайл(ОповещениеОВыборе, ДоговорКонтрагента, ЭтотОбъект, 2);
	
	ИначеЕсли ВыбранныйВариант.Значение = ВариантПрисоединенныйФайл() Тогда
		
		ОтборФайлов = Новый Структура;
		ОтборФайлов.Вставить("ВладелецФайла", ДоговорКонтрагента);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", ОтборФайлов);
		
		ОткрытьФорму("Справочник.ДоговорыКонтрагентовПрисоединенныеФайлы.Форма.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаСДискаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Результат.ФайлСсылка) Тогда
		Если ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
			ПоказатьПредупреждение(, Результат.ТекстОшибки);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ФайлСоглашения = Результат.ФайлСсылка;
	ОбработатьИзменениеФайлаСоглашения();
	
КонецПроцедуры

&НаКлиенте
Функция ВариантФайлСДиска()
	
	Возврат "ФайлСДиска";
	
КонецФункции

&НаКлиенте
Функция ВариантПрисоединенныйФайл()
	
	Возврат "ПрисоединенныйФайл";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеФайлаСоглашения(Форма)
	
	Если Не ЗначениеЗаполнено(Форма.ФайлСоглашения) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	ПредставлениеСоглашения = Новый Массив;
	ПредставлениеСоглашения.Добавить(Новый ФорматированнаяСтрока(
		Строка(Форма.ФайлСоглашения), , , , "ОткрытьФайлСоглашения"));
	
	Элементы.ФайлСоглашенияРасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ПредставлениеСоглашения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеФайлаСоглашения()
	
	Модифицированность = Истина;
	
	ЗаполнитьПредставлениеФайлаСоглашения(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Комментарий);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.ГруппаНовый);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.ГруппаСуществующий);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаПодписано);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ДекорацияОтступ);
	
	Элементы.Подписано.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСлева;
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
