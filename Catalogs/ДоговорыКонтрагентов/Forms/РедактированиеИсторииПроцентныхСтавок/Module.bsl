
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Не Параметры.Свойство("ИсторияПроцентныхСтавок")
	 ИЛИ Не Параметры.Свойство("ТекущаяПроцентнаяСтавка")Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ТипПроцентнойСтавки", ТипПроцентнойСтавки);
	
	ИсторияПроцентныхСтавок.Загрузить(Параметры.ИсторияПроцентныхСтавок.Выгрузить());
	Если ИсторияПроцентныхСтавок.Количество() = 0 Тогда
		НоваяСтрока = ИсторияПроцентныхСтавок.Добавить();
		НоваяСтрока.ПроцентнаяСтавка = Параметры.ТекущаяПроцентнаяСтавка;
	КонецЕсли;
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.ДоговорыКонтрагентов) Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли; 
	
	Если ТолькоПросмотр Тогда
		
		Элементы.ФормаКомандаОтмена.КнопкаПоУмолчанию = Истина;
		
	КонецЕсли;
	
	УстановитьПризнакПервоначальногоЗначения(ИсторияПроцентныхСтавок);
	
	Если ИсторияПроцентныхСтавок.Количество() > 0 Тогда
		Элементы.ИсторияПроцентныхСтавок.ТекущаяСтрока = ИсторияПроцентныхСтавок[ИсторияПроцентныхСтавок.Количество()-1].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыИсторияПроцентныхСтавок

&НаКлиенте
Процедура ИсторияПроцентныхСтавокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Сутки = 60 * 60 * 24;
	
	Если НоваяСтрока Тогда
		
		Если Элемент.ТекущиеДанные <> Неопределено Тогда
			
			НовыйПериод = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
			
			// Получим максимальный период в таблице
			ПоследнийПериод = '00010101000000';
			Для Каждого ЗаписьИстории ИЗ ИсторияПроцентныхСтавок Цикл
				Если ЗаписьИстории.Период > ПоследнийПериод Тогда
					ПоследнийПериод = ЗаписьИстории.Период;
				КонецЕсли;
			КонецЦикла;
			
			Если НовыйПериод <= ПоследнийПериод Тогда
				НовыйПериод = НачалоДня(ПоследнийПериод + Сутки);
			КонецЕсли;
			
			Элемент.ТекущиеДанные.Период = НовыйПериод;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПроцентныхСтавокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПризнакПервоначальногоЗначения(ИсторияПроцентныхСтавок);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПроцентныхСтавокПередУдалением(Элемент, Отказ)
	
	Если ИсторияПроцентныхСтавок.Индекс(Элемент.ТекущиеДанные) = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеИстории(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ИсторияПроцентныхСтавок", ИсторияПроцентныхСтавок);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПризнакПервоначальногоЗначения(ТаблицаИстории)
	
	ТаблицаИстории.Сортировать("Период");
	// Первая строка должна быть первоначальным значением
	Если ТаблицаИстории.Количество() > 0 Тогда
		ПерваяСтрока = ТаблицаИстории[0];
		ПерваяСтрока.ПервоначальноеЗначение = Истина;
		ПерваяСтрока.Период = '00010101';
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеИстории(Отказ)
	
	ПериодыСтрок = Новый Массив;
	ТипСтавкиЦБ = ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ");
	
	Для Каждого Запись Из ИсторияПроцентныхСтавок Цикл
		
		ИндексСтроки = ИсторияПроцентныхСтавок.Индекс(Запись);
		ПрефиксСообщенияСтроки = "ИсторияПроцентныхСтавок["+Формат(ИндексСтроки, "ЧЦ=; ЧДЦ=; ЧГ=")+"].";
		
		Если Не ЗначениеЗаполнено(Запись.Период) И ИндексСтроки > 0 Тогда
			СообщениеОбОшибке = НСтр("ru = 'Нужно указать дату начала действия'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки+"Период", , Отказ);
		КонецЕсли;
		
		Если ПериодыСтрок.Найти(Запись.Период) = Неопределено Тогда
			Если ЗначениеЗаполнено(Запись.Период) Тогда
				ПериодыСтрок.Добавить(Запись.Период);
			КонецЕсли;
		Иначе
			// Строка с такой датой уже была раньше, это ошибка.
			// Не может быть 2 строки с одинаковой датой.
			СообщениеОбОшибке = НСтр("ru = 'Уже есть запись с указанной датой сведений'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки+"Период", , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись.ПроцентнаяСтавка) И ТипПроцентнойСтавки <> ТипСтавкиЦБ Тогда
			СообщениеОбОшибке = НСтр("ru = 'Поле ""Ставка"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки+"ПроцентнаяСтавка",  , Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти