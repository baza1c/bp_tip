
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	СрокОплатыПокупателей = Константы.СрокОплатыПокупателей.Получить();
	СрокОплатыПоставщикам = Константы.СрокОплатыПоставщикам.Получить();
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ДоговорСРезидентомТаможенногоСоюза = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Объект.Владелец);
	
	Параметры.Свойство("ДанныеМодифицированы", ДанныеМодифицированы);
	
	Если Параметры.ПараметрыВыбора.Свойство("Организация") И ЗначениеЗаполнено(Параметры.ПараметрыВыбора.Организация) Тогда
		Параметры.ПараметрыВыбора.Организация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(
			Параметры.ПараметрыВыбора.Организация);
	КонецЕсли;

	Если НЕ Параметры.Ключ.Пустая() Тогда
		Справочники.ДоговорыКонтрагентов.УстановитьДоступныеВидыДоговора(Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	ОграничитьВыборРеквизитов(Параметры.ЗначенияЗаполнения);
	
	СуммаВключаетНДС = Объект.СуммаВключаетНДС;
	Сумма = Объект.Сумма - ?(Объект.СуммаВключаетНДС, 0, Объект.СуммаНДС);
	
	ВалютаДоговора = Объект.ВалютаВзаиморасчетов;
	ОплатаВРублях = Объект.РасчетыВУсловныхЕдиницах;

	ЗаполнитьСписокСпособовРасчетаКомиссионногоВознаграждения(ЭтотОбъект);
	ЗаполнитьСписокВидовСрокаОплаты();
	ЗаполнитьИОтобразитьСистемуНалогообложенияКонтрагента();
	
	НаименованиеПоУмолчанию  = ПечатьДоговоровКлиентСервер.НаименованиеПоУмолчаниюБезРеквизитов();
	РеквизитыДоговораСтрокой = ПечатьДоговоровКлиентСервер.РеквизитыДоговораСтрокой(Объект.Номер, Объект.Дата);
	Элементы.НаименованиеДляСчетаФактурыНалоговогоАгента.Видимость = ПолучитьФункциональнуюОпцию("ИсполняютсяОбязанностиНалоговогоАгентаПоНДС")
		Или БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	
	РаздельныйУчетНДС = УчетНДСРаздельный.ЕстьУчетнаяПолитикаСРаздельнымУчетомНДС();
	
	ГосударственныйКонтрактЕИС = ЭлектронноеАктированиеЕИС.НайтиГосударственныйКонтрактПоВладельцу(Объект.Ссылка);
	
	ПредусмотреноОбеспечениеОбязательств = ЗначениеЗаполнено(Объект.ВидОбеспеченияОбязательствИПлатежей);
	ОбеспечениеПредоставил = Объект.ОбеспечениеПредоставил;
	ОбеспечениеПолучил     = Объект.ОбеспечениеПолучил;
	ОбеспечениеВыданоЗа    = Объект.ОбеспечениеВыданоЗа;
	УстановитьСпискиЭлементовОбеспеченияОбязательств();
	УстановитьВидСрокаОплаты();
	УстановитьКнопкуВыбораРуководителяКонтрагента();

	УстановитьФункциональныеОпцииФормы();
	
	УстановитьНумерациюДоговоровСПокупателями();
	
	УстановитьЗаголовокПодсказкиНомера();
	
	Если Параметры.Ключ.Пустая() Тогда
		ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
		Если ИспользоватьНумерациюДоговоровСПокупателями И Не ЗначениеЗаполнено(Объект.Номер) Тогда
			УстановитьДатуДоговораСПокупателемПоУмолчанию(ЭтотОбъект);
			УстановитьНомерДоговораСПокупателем();
		КонецЕсли;
		ЗаполнитьСтавкуНДСПоУмолчанию(ЭтотОбъект);
		Если Параметры.ЗначенияЗаполнения.Свойство("Наименование") Тогда
			Объект.Наименование = Параметры.ЗначенияЗаполнения.Наименование;
		Иначе
			СформироватьНаименованиеДоговора(ЭтотОбъект);
		КонецЕсли;
		Элементы.ГруппаРасчеты.Показать();
	КонецЕсли; 
	
	УстановитьПараметрыВыбораСтавкиНДС();
	
	Если ИспользоватьНумерациюДоговоровСПокупателями Тогда
		ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
	КонецЕсли;

	Если Не ТарификацияБП.РазрешенВалютныйУчет() Тогда
		ВалютаНедоступна = Истина;
		Элементы.ВалютаДоговора.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет");
	КонецЕсли;
	
	Элементы.ГруппаВалютныйКонтроль.Видимость =
		ПолучитьФункциональнуюОпцию("УчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров");

	УправлениеФормой(ЭтотОбъект);
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Справочник.ДоговорыКонтрагентов",
		"ФормаЭлемента",
		НСтр("ru='Новости: Договор'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
	// На форме размещены 4 реквизита, чьи представления склоняются по падежам.
	// Основным режимом работы со склонениями Руководитель и ДолжностьРуководителя является их изменение в форме элемента
	// соответствующего справочника. Но возможен переход к их склонениям и из текущей формы.
	// Работа с реквизитами РуководительКонтрагента и ДолжностьРуководителяКонтрагента ведётся с данной форме
	// с использованием подсистемы БСП "СклонениеПредставленийОбъектов". Данные склонений реквизитов хранятся в реквизитах
	// СклоненияРуководительКонтрагента и СклоненияДолжностьРуководителяКонтрагента. Имя реквизита формы, с которым сейчас
	// ведётся работа с использованием подсистемы БСП, хранится в переменной ТекущийРеквизитСклонения. Данные из текущего
	// реквизита синхронизируются с реквизитом формы Склонения, относящимся к подсистеме БСП.
	
	СклонениеПредставленийОбъектов.ПриСозданииНаСервере(ЭтотОбъект, Объект.РуководительКонтрагента); // создание реквизитов формы
	
	Попытка
	
		ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
		Если ЗначениеЗаполнено(Объект.ДолжностьРуководителяКонтрагента) Тогда

			ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента = ПросклонятьПредставлениеПоВсемПадежам(
				Объект.ДолжностьРуководителяКонтрагента, ПараметрыСклонения);

		Иначе
			ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента = СклонениеПредставленийОбъектовКлиентСервер.СтруктураСклонения();
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.РуководительКонтрагента) Тогда

			Если ЭтотОбъект.Склонения = Неопределено Тогда // не было заполнено при создании реквизитов формы
				
				ПараметрыСклонения.ЭтоФИО = Истина;
				ПараметрыСклонения.Пол = ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(Объект.ПолРуководителяКонтрагента);
				ЭтотОбъект.СклоненияРуководительКонтрагента = ПросклонятьПредставлениеПоВсемПадежам(
					Объект.РуководительКонтрагента, ПараметрыСклонения);

			Иначе // было заполнено стандартным образом

				ЭтотОбъект.СклоненияРуководительКонтрагента = ЭтотОбъект.Склонения;
				
			КонецЕсли;

		Иначе
			ЭтотОбъект.СклоненияРуководительКонтрагента = СклонениеПредставленийОбъектовКлиентСервер.СтруктураСклонения();
		КонецЕсли;
		
		ТекущийРеквизитСклонения = "СклоненияРуководительКонтрагента";
		ЭтотОбъект.Склонения = ЭтотОбъект[ТекущийРеквизитСклонения];
	
	Исключение
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось произвести склонение ФИО и/или должности руководителя контрагента в договоре: %1 по причине:
			|%2'"),
			Объект.Ссылка,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Склонение ФИО и/или должности руководителя контрагента'"),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ДоговорыКонтрагентов,
			Объект.Ссылка,
			ТекстСообщения);
		
	КонецПопытки;
	
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСправочника();
	ПараметрыПриСозданииНаСервере.Форма                  = ЭтотОбъект;
	ПараметрыПриСозданииНаСервере.СправочникСсылка       = Объект.Ссылка;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд  = Элементы.КомандыЭДО;
	ПараметрыПриСозданииНаСервере.КонтроллерСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыПриСозданииНаСервере.ГруппаСостояниеЭДО     = Элементы.ГруппаСостояниеЭДО;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСправочника(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	НастройкиУчетаМСФО.НастроитьФормуОбъектаУчета(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	НастроитьПоложениеПодсказкиНалогообложенияКонтрагента();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриОткрытии = ОбменСКонтрагентамиКлиент.ПараметрыПриОткрытии();
	ПараметрыПриОткрытии.Форма = ЭтотОбъект;
	ПараметрыПриОткрытии.ЕстьОбработчикОбновитьКомандыЭДО = Истина;
	
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ПараметрыПриОткрытии);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	Если ДанныеМодифицированы Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ДоговорыКонтрагентов_СозданиеФайлаДоговора" 
		И Параметр.ДоговорКонтрагента = Объект.Ссылка Тогда 
		// Для текущего договора был добавлен присоединенный файл с текстом договора.
		// Сохраним ссылку на него в текущем объекте. 
		ЭлектронныйФормат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "ЭлектронныйФормат", Ложь);
		ОбработкаОповещенияСозданиеФайлаДоговора(Параметр.ФайлДоговора, ЭлектронныйФормат);
	ИначеЕсли ИмяСобытия = "Запись_Файл" Тогда
		Если Не Модифицированность Тогда
			Прочитать();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_ИспользоватьНумерациюДоговоровСПокупателями" Тогда
		УстановитьНумерациюДоговоровСПокупателями();
		Если ИспользоватьНумерациюДоговоровСПокупателями Тогда
			Если Не ЗначениеЗаполнено(Объект.Номер) Тогда
				УстановитьДатуДоговораСПокупателемПоУмолчанию(ЭтотОбъект);
				УстановитьНомерДоговораСПокупателем();
				СформироватьНаименованиеДоговора(ЭтотОбъект);
				Модифицированность = Истина;
			КонецЕсли;
			ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
		КонецЕсли;
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоПолучен()
		И Источник = ЭтотОбъект Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДоговора(
			Параметр.ПлательщикНПД, Параметр.НаДату, Параметр.ДатаРегистрации);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоОшибка()
		И Источник = ЭтотОбъект Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДоговора(
			Ложь, Неопределено, , Параметр);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ПростойИнтерфейс_ФункциональностьИзменение"
		Или ИмяСобытия = "Запись_НаборКонстант" И Источник = "ВестиУчетПоДоговорам" Тогда
		НастроитьПоложениеПодсказкиНалогообложенияКонтрагента();
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыОповещенияЭДО                        = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСправочника();
	ПараметрыОповещенияЭДО.Форма                  = ЭтотОбъект;
	ПараметрыОповещенияЭДО.СправочникСсылка       = Объект.Ссылка;
	ПараметрыОповещенияЭДО.КонтроллерСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыОповещенияЭДО.ГруппаСостояниеЭДО     = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСправочника(ИмяСобытия, Параметр, Источник,
		ПараметрыОповещенияЭДО);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	ПриЗаписиНаСервереСклоненийРеквизитов(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
	ПриЗаписиНаСервереГосконтрактЕИС(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЗаполнитьДобавленныеКолонкиДополнительныхСоглашений();
	ОтменитьДобавлениеСоглашенияПриИзмененииРеквизита();
	
	Оповестить(РаботаСДоговорамиКонтрагентовБПКлиент.ИмяСобытияЗаписьДоговора(), ПараметрыЗаписи, Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ЗакрытьПослеЗаписи")
		И ПараметрыЗаписи.ЗакрытьПослеЗаписи Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	РазблокироватьДанныеФормыДляРедактирования();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	ЗаполнитьДобавленныеКолонкиДополнительныхСоглашений();
	
	ПрочитатьСтатусСамозанятого();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПередЗаписьюДоговораСНеуникальнымНомером(Отказ, ПараметрыЗаписи);
	ПередЗаписьюДоговораСИзмененнымТипомСтавки(Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.ДоговорыКонтрагентов.Форма.РедактированиеИсторииПроцентныхСтавок") Тогда
		УстановитьПроцентнуюСтавкуПослеРедактированияИстории(ВыбранноеЗначение.ИсторияПроцентныхСтавок);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.ДоговорыКонтрагентов.Форма.ФормаДополнительногоСоглашения" Тогда
		ОбработкаВыбораДополнительногоСоглашения(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	
	НомерСозданАвтоматически = Ложь;
	Если ИспользоватьНумерациюДоговоровСПокупателями Тогда
		ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	СформироватьНаименованиеДоговора(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ЗаполнитьСтавкуНДСПоУмолчанию(ЭтотОбъект);
	УстановитьПараметрыВыбораСтавкиНДС();
	
	Если ИспользоватьНумерациюДоговоровСПокупателями Тогда
		Если НомерСозданАвтоматически Или Не ЗначениеЗаполнено(Объект.Номер) Тогда
			УстановитьНомерДоговораСПокупателем();
		КонецЕсли;
		ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	СформироватьНаименованиеДоговора(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда

		ВладелецПриИзмененииНаСервере();
		// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
		ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
		ОповещениеОРезультатеСклонения = Новый ОписаниеОповещения(
			"ДолжностьРуководителяКонтрагентаПриИзмененииЗавершение", ЭтотОбъект);
		СклонениеПредставленийОбъектовКлиент.НачатьСклонение(
			ЭтотОбъект, Объект.ДолжностьРуководителяКонтрагента, ПараметрыСклонения, Истина, ОповещениеОРезультатеСклонения);

		ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
		ПараметрыСклонения.ЭтоФИО = Истина;
		ПараметрыСклонения.Пол = ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(Объект.ПолРуководителяКонтрагента);
		ОповещениеОРезультатеСклонения = Новый ОписаниеОповещения(
			"РуководительКонтрагентаПриИзмененииЗавершение", ЭтотОбъект);
		СклонениеПредставленийОбъектовКлиент.НачатьСклонение(
			ЭтотОбъект, Объект.РуководительКонтрагента, ПараметрыСклонения, Истина, ОповещениеОРезультатеСклонения);
		// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
		
		ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидДоговора)
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее")) Тогда
		Объект.ТипЦен = Неопределено;
		Объект.СпособРасчетаКомиссионногоВознаграждения = Неопределено;
		Объект.ПроцентКомиссионногоВознаграждения = 0;
		Объект.УстановленСрокОплаты = Ложь;
		ВидСрокаОплаты = ИмяНеназначенногоСрокаОплаты();
		Объект.СрокОплаты = 0;
		Объект.ДатаОплаты = Дата(1, 1, 1);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.ВидДоговора)
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией")) Тогда
		Объект.РасчетыВУсловныхЕдиницах = Ложь;
		ОплатаВРублях = 0;
	КонецЕсли;
	
	ЭтоДоговорПриобретения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));
	
	Если НЕ ЭтоДоговорПриобретения Тогда
		Объект.УчетАгентскогоНДС       = Ложь;
		Объект.НДСПоСтавкам4и2         = Ложь;
		Объект.ВидАгентскогоДоговора   = Неопределено;
		Если Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
			Объект.КодРаздел7ДекларацииНДС   = Неопределено;
			Раздел7ДекларацииНДСНаименование = "";
		КонецЕсли;
	Иначе
		Объект.КодРаздел7ДекларацииНДС   = Неопределено;
		Раздел7ДекларацииНДСНаименование = "";
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")
			И Не ИсполняютсяОбязанностиНалоговогоАгентаПоНДС Тогда
			
			Объект.УчетАгентскогоНДС     = Ложь;
			Объект.ВидАгентскогоДоговора = Неопределено;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.УчетАгентскогоНДС
			И (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")) Тогда
		Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Нерезидент");
	КонецЕсли;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку") Тогда
		Если НЕ УсловиеОплатаВВалютеПередано Тогда
			Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
			ВалютаДоговора = ВалютаРегламентированногоУчета;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией") Тогда
		Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
		ВалютаДоговора = ВалютаРегламентированногоУчета;
	КонецЕсли;
	
	ЗаполнитьСписокСпособовРасчетаКомиссионногоВознаграждения(ЭтотОбъект);
	Если ЗначениеЗаполнено(Объект.СпособРасчетаКомиссионногоВознаграждения) Тогда
		Если Элементы.СпособРасчетаКомиссионногоВознаграждения.СписокВыбора.НайтиПоЗначению(Объект.СпособРасчетаКомиссионногоВознаграждения) = Неопределено Тогда
			Объект.СпособРасчетаКомиссионногоВознаграждения = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
		
		Объект.ПлатежныйАгент = Ложь;
		
		ПлатежныйАгентПриИзменении(Элементы.ПлатежныйАгент);
	КонецЕсли;
	
	ЭтоДоговорКомиссии     = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));
		
	Если ЭтоДоговорКомиссии 
		ИЛИ НЕ ЗначениеЗаполнено(Объект.ВидДоговора) Тогда
		Объект.ГосударственныйКонтракт = Неопределено;
	КонецЕсли;
	
	Если Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку") Тогда
		Объект.СчетаФактурыОтИмениОрганизации = Ложь;
	КонецЕсли;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ЗаемПолученный")
		И Не ЗначениеЗаполнено(Объект.ТипПроцентнойСтавки) Тогда
		Объект.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.ФиксированнаяСтавка");
	КонецЕсли;
	
	УстановитьНумерациюДоговоровСПокупателями();
	Если ИспользоватьНумерациюДоговоровСПокупателями Тогда
		Если НомерСозданАвтоматически Или Не ЗначениеЗаполнено(Объект.Номер) Тогда
			УстановитьДатуДоговораСПокупателемПоУмолчанию(ЭтотОбъект);
			УстановитьНомерДоговораСПокупателем();
			СформироватьНаименованиеДоговора(ЭтотОбъект);
		КонецЕсли;
		ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
	КонецЕсли;
	
	СпособЗаполненияСтавкиНДСПоУмолчанию();
	ЗаполнитьСтавкуНДСПоУмолчанию(ЭтотОбъект);
	УстановитьПараметрыВыбораСтавкиНДС();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаВПриИзменении(Элемент)
	
	Объект.РасчетыВУсловныхЕдиницах = ОплатаВРублях;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСрокаОплатыПриИзменении(Элемент)
	
	Объект.УстановленСрокОплаты = ВидСрокаОплаты <> ИмяНеназначенногоСрокаОплаты();
	Если Объект.УстановленСрокОплаты Тогда
		СтандартныйСрокОплаты = 0;
		Если (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")) Тогда
			СтандартныйСрокОплаты = СрокОплатыПокупателей;
		ИначеЕсли (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
				ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку")) Тогда
			СтандартныйСрокОплаты = СрокОплатыПоставщикам;
		КонецЕсли;
		Если ВидСрокаОплаты = ИмяСрокаОплатыВДнях() Тогда
			Объект.СрокОплаты = СтандартныйСрокОплаты;
			Объект.ДатаОплаты = Дата(1, 1, 1);
		Иначе
			ДлинаСуток = 86400;
			Объект.ДатаОплаты = ОбщегоНазначенияКлиент.ДатаСеанса() + СтандартныйСрокОплаты * ДлинаСуток;
			Объект.СрокОплаты = 0;
		КонецЕсли;
	Иначе
		Объект.ДатаОплаты = Дата(1, 1, 1);
		Объект.СрокОплаты = 0;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УчетАгентскогоНДСПриИзменении(Элемент)
	
	Если Объект.УчетАгентскогоНДС Тогда
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом") 
			ИЛИ ЭтоДоговорИностранца Тогда
			Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Нерезидент");
		Иначе
			Объект.ВидАгентскогоДоговора = ?(
				ИсполняютсяОбязанностиНалоговогоАгентаПоНДС,
				ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Аренда"),
				ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров"));
		КонецЕсли;
	Иначе
		Объект.ВидАгентскогоДоговора = Неопределено;
	КонецЕсли;
	
	СпособЗаполненияСтавкиНДСПоУмолчанию();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектронныеУслугиПриИзменении(Элемент)
	
	// Очистим поля связанные с настройкой по налоговому агенту.
	Объект.УчетАгентскогоНДС = Ложь;
	Объект.ВидАгентскогоДоговора = Неопределено;
	Объект.НаименованиеДляСчетаФактурыНаАванс = ""; // используется для счетов-фактур налогового агента
	
	Если ЭлектронныеУслугиИностранца Тогда
		ПорядокУплатыНДСПоЭлектроннымУслугам = 1; // НДС платит иностранец
		Объект.ЭлектронныеУслуги = Истина;
	Иначе
		Объект.ЭлектронныеУслуги = Ложь;
	КонецЕсли;
	
	СпособЗаполненияСтавкиНДСПоУмолчанию();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУплатыНДСПоЭлектроннымУслугамПриИзменении(Элемент)
	
	Объект.ЭлектронныеУслуги = ПорядокУплатыНДСПоЭлектроннымУслугам = 1; // НДС платит иностранец
	Если ПорядокУплатыНДСПоЭлектроннымУслугам = 2 Тогда // НДС платит организация как налоговый агент
		Объект.УчетАгентскогоНДС = Истина;
		Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги");
	Иначе
		// Очистим поля связанные с настройкой по налоговому агенту.
		Объект.УчетАгентскогоНДС = Ложь;
		Объект.ВидАгентскогоДоговора = Неопределено;
		Объект.НаименованиеДляСчетаФактурыНаАванс = ""; // используется для счетов-фактур налогового агента
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УчетАгентскогоНДСПокупателемПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокРегистрацииСчетовФактурНаАвансПоДоговоруПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидАгентскогоДоговораПриИзменении(Элемент)
	
	Если Объект.ВидАгентскогоДоговора = 
		ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров") Тогда 
		
		Если Объект.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета
			И НЕ Объект.РасчетыВУсловныхЕдиницах Тогда
			Объект.РасчетыВУсловныхЕдиницах = Истина;
			ОплатаВРублях = 1; 
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаФактурыОтИмениОрганизацииПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РуководительКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Список = ПолучитьСписокКонтактныхЛиц(Объект.Владелец);
	
	ОповещениеВыбора = Новый ОписаниеОповещения("РуководительКонтрагентаНачалоВыбораЗавершение", ЭтотОбъект);
	ПоказатьВыборИзСписка(ОповещениеВыбора, Список, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура РуководительКонтрагентаПриИзменении(Элемент)
	
	ПриИзмененииПредставленияРуководителяКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяКонтрагентаПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	ОповещениеОРезультатеСклонения = Новый ОписаниеОповещения(
		"ДолжностьРуководителяКонтрагентаПриИзмененииЗавершение", ЭтотОбъект);
	СклонениеПредставленийОбъектовКлиент.НачатьСклонение(
		ЭтотОбъект, Объект.ДолжностьРуководителяКонтрагента, ПараметрыСклонения, Истина, ОповещениеОРезультатеСклонения);
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлДоговораОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ФайлДоговора) Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ФайлДоговора", Объект.ФайлДоговора);
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаРедактированияТекстаДоговора", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлДоговораСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) ИЛИ Модифицированность Тогда
		// Выполняем запись договора перед открытием формы редактирования файла с текстом договора,
		// аналогично тому как происходит при нажатии на кнопку Печать.
		Записать();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	Если ЗначениеЗаполнено(Объект.ФайлДоговора) Тогда
		// Открываем существующий файл договора вместо создания нового.
		ПараметрыФормы.Вставить("ФайлДоговора", Объект.ФайлДоговора);
	Иначе
		ПараметрыФормы.Вставить("ОбъектПечати", Объект.Ссылка);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаРедактированияТекстаДоговора", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЭтоГосударственныйКонтрактПриИзменении(Элемент)
	
	Если НЕ ЭтоГосударственныйКонтракт Тогда
		Объект.ГосударственныйКонтракт = Неопределено;
		ГосударственныйКонтрактЕИС = Неопределено;
	КонецЕсли;
	
	Элементы.ГосударственныйКонтракт.Доступность = ЭтоГосударственныйКонтракт;
	Элементы.ГосударственныйКонтрактЕИС.Доступность = ЭтоГосударственныйКонтракт;
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакАгентаПриИзменении(Элемент)
	ОчиститьПризнакиАгента();
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныйАгентПриИзменении(Элемент)
	
	ОчиститьПризнакиАгента();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НастройкаМСФОПриИзменении(Элемент)
	
	НастройкиУчетаМСФОКлиент.ПриИзмененииПоляФормы(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредусмотреноОбеспечениеОбязательствПриИзменении(Элемент)
	
	Если НЕ ПредусмотреноОбеспечениеОбязательств Тогда
		Объект.ВидОбеспеченияОбязательствИПлатежей = Неопределено;
		Объект.ОбеспечениеПредоставил              = Неопределено;
		Объект.ОбеспечениеПолучил                  = Неопределено;
		Объект.ОбеспечениеВыданоЗа                 = Неопределено;
		
		ОбеспечениеПредоставил = "";
		ОбеспечениеПолучил     = "";
		ОбеспечениеВыданоЗа    = "";
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбеспечениеСторонаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		
		ДополнительныеПараметры = Новый Структура("ИмяЭлемента", Элемент.Имя);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОткрытьФормуВыбораКонтрагентаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", , ЭтотОбъект, , , , ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		Объект[Элемент.Имя] = ВыбранноеЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОплатыПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПояснениеПроверкиДублейОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьДубли" Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьДублиДоговораСПокупателем();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДоговораПриИзменении(Элемент)
	
	Объект.ВалютаВзаиморасчетов = ВалютаДоговора;
	ВалютаВзаиморасчетовПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДоговораСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ЗагрузкаКурсовВалют.Форма.ПодборВалютИзКлассификатора", , Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	РассчитатьСтавкуНДС();
	РассчитатьСумму();
	УправлениеФормой(ЭтотОбъект);
	ПроверитьДобавитьСоглашениеПриИзмененииРеквизита(ИмяРеквизитаСумма());
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаВключаетНДСПриИзменении(Элемент)
	
	Объект.СуммаВключаетНДС = СуммаВключаетНДС;
	
	ПроцентНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(Объект.СтавкаНДС);
	
	Сумма = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
		Сумма,
		Не Объект.СуммаВключаетНДС,
		Объект.СуммаВключаетНДС,
		ПроцентНДС);
	
	РассчитатьСтавкуНДС(ПроцентНДС);
	РассчитатьСумму();
	УправлениеФормой(ЭтотОбъект);
	ПроверитьДобавитьСоглашениеПриИзмененииРеквизита(ИмяРеквизитаСумма());
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	РассчитатьСтавкуНДС();
	РассчитатьСумму();
	УправлениеФормой(ЭтотОбъект);
	ПроверитьДобавитьСоглашениеПриИзмененииРеквизита(ИмяРеквизитаСумма());
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаНДСПриИзменении(Элемент)
	
	РассчитатьСумму();
	УправлениеФормой(ЭтотОбъект);
	ПроверитьДобавитьСоглашениеПриИзмененииРеквизита(ИмяРеквизитаСумма());
	
КонецПроцедуры

&НаКлиенте
Процедура УникальныйНомерВалютногоКонтроляПриИзменении(Элемент)
	
	КодОшибкиПроверкиКонтракта = КодОшибкиПроверкиНомераКонтракта(Объект.ДатаПостановкиНаВалютныйКонтроль,
		Объект.УникальныйНомерВалютногоКонтроля);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалогообложениеКонтрагентаПриИзменении(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого");
	ЭтоПлательщикНДС = ПрименятьНДСВДоговоре(Объект.Организация, ТекущаяДатаПользователя, Объект.НалогообложениеКонтрагента);
	ЗаполнитьСтавкуНДСПоУмолчанию(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
		НастроитьПоложениеПодсказкиНалогообложенияКонтрагента(Истина);
	КонецЕсли;
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПостановкиНаВалютныйКонтрольПриИзменении(Элемент)
	
	КодОшибкиПроверкиКонтракта = КодОшибкиПроверкиНомераКонтракта(Объект.ДатаПостановкиНаВалютныйКонтроль,
		Объект.УникальныйНомерВалютногоКонтроля);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалогообложениеКонтрагентаРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки =
		ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ИмяСобытияВызовСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		ИННКонтрагента = ИННКонтрагентаНаСервере(Объект.Владелец);
		ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(Объект.Дата);
		Если ИнтеграцияСтатусСамозанятогоКлиент.НачатьПолучениеСтатусаСамозанятого(
			ЭтотОбъект, ЭтотОбъект, ИННКонтрагента, ДатаПолученияСтатуса) Тогда
			Элемент.Заголовок = ИнтеграцияСтатусСамозанятогоФормыБПКлиент.ЗаголовокПодсказкиПолучениеСтатусаВПроцессе();
		КонецЕсли;
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки =
		РаботаСДоговорамиКонтрагентовБПКлиентСервер.ПерейтиКОпцииУчетПоДоговорам() Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыОткрываемойФормы = Новый Структура;
		ПараметрыОткрываемойФормы.Вставить("ТекущаяСтраница", "ГруппаУчетПоДоговорам");
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма.ФормаФункциональностьПрограммы", ПараметрыОткрываемойФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорЗакрытПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоходаИностранногоКонтрагентаПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТипПроцентнойСтавкиПриИзменении(Элемент)
	
	Если Объект.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.БезНачисленияПроцентов") Тогда
		Объект.ПроцентнаяСтавка = 0;
	Иначе
		ЧислоЗаписейВИстории = Объект.ИсторияПроцентныхСтавок.Количество();
		Если ЧислоЗаписейВИстории > 0 Тогда
			Объект.ПроцентнаяСтавка = Объект.ИсторияПроцентныхСтавок[ЧислоЗаписейВИстории - 1].ПроцентнаяСтавка;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ИзмененТипПроцентнойСтавки = Истина;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентнаяСтавкаПриИзменении(Элемент)
	
	АктуализироватьПроцентнуюСтавкуВИстории();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПроцентныхСтавокНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущаяПроцентнаяСтавка", Объект.ПроцентнаяСтавка);
	ПараметрыФормы.Вставить("ИсторияПроцентныхСтавок", Объект.ИсторияПроцентныхСтавок);
	ПараметрыФормы.Вставить("ТипПроцентнойСтавки", Объект.ТипПроцентнойСтавки);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.РедактированиеИсторииПроцентныхСтавок", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РуководительКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ФИОАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокДействияПриИзменении(Элемент)
	
	ПроверитьДобавитьСоглашениеПриИзмененииРеквизита(ИмяРеквизитаСрокДействия());
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДобавитьДополнительноеСоглашениеСуммаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ДобавитьДополнительноеСоглашение" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФормуДополнительногоСоглашения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДобавитьДополнительноеСоглашениеСрокДействияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ДобавитьДополнительноеСоглашение" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФормуДополнительногоСоглашения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтсрочкуПоСтавкеЦБПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеСоглашения

&НаКлиенте
Процедура ДополнительныеСоглашенияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеСоглашенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуДополнительногоСоглашения(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеСоглашенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ОткрытьФормуДополнительногоСоглашения();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеСоглашенияПередНачаломИзменения(Элемент, Отказ)
	
	Если Элементы.ДополнительныеСоглашения.ТекущийЭлемент.Имя <> "ДополнительныеСоглашенияПодписано" Тогда
		Отказ = Истина;
		
		ОткрытьФормуДополнительногоСоглашения(Элементы.ДополнительныеСоглашения.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеСоглашенияПриАктивизацииЯчейки(Элемент)
	
	Если Элементы.ДополнительныеСоглашения.ТекущийЭлемент.Имя <> "ДополнительныеСоглашенияПодписано" Тогда
		Элементы.ДополнительныеСоглашения.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	ВалютаРегламентированногоУчета = Форма.ВалютаРегламентированногоУчета;

	ЭтоДоговорКомиссии     = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));
		
	ЭтоДоговорСКомиссионером = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером");
	
	ЭтоДоговорПриобретения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"));
		
	ЭтоДоговорРеализации   = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СТранспортнойКомпанией"));;
	
	ЭтоДоговорФакторинга = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией"));
	
	ЭтоДоговорСПоставщиком            = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"));
	ЭтоДоговорСКомиссионеромНаЗакупку = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"));
	
	ЭтоДоговорЗайма = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ЗаемПолученный"));
	ЭтоБеспроцентныйЗаем = Объект.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.БезНачисленияПроцентов");
	
	ЭтоПрочийДоговор = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее");
	
	ЭтоПлательщикНДС = Форма.ЭтоПлательщикНДС;
	
	ВидимостьКомиссионногоВознаграждения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером")
												И Форма.ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров)
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));
	
	//Группа "Расчеты"
	
	Элементы.ОплатаВРублях.Видимость = (ЭтоДоговорРеализации И Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")
			ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		И (Объект.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета)
		И ЗначениеЗаполнено(Объект.ВалютаВзаиморасчетов)
		И Объект.ВидАгентскогоДоговора <> ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров");
		
	ЗначениеВыбора = Элементы.ОплатаВРублях.СписокВыбора.НайтиПоЗначению(0);
	ЗначениеВыбора.Представление = Объект.ВалютаВзаиморасчетов;
	
	ЗначениеВыбора = Элементы.ОплатаВРублях.СписокВыбора.НайтиПоЗначению(1);
	ЗначениеВыбора.Представление = ВалютаРегламентированногоУчета;
	
	ТекстЗаголовкаРасчеты = "";
	Если ЗначениеЗаполнено(Объект.Сумма) Тогда
		ТекстЗаголовкаРасчеты = Формат(Объект.Сумма, "ЧДЦ=2");
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Сумма) Или Объект.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета Тогда
		Если Не ПустаяСтрока(ТекстЗаголовкаРасчеты) Тогда
			ТекстЗаголовкаРасчеты = СтрШаблон(НСтр("ru= '%1 '"), ТекстЗаголовкаРасчеты);
		КонецЕсли;
		ТекстЗаголовкаРасчеты = СтрШаблон(НСтр("ru= '%1%2'"), ТекстЗаголовкаРасчеты, Объект.ВалютаВзаиморасчетов);
	КонецЕсли;
	Если Объект.РасчетыВУсловныхЕдиницах Тогда
		Если Не ПустаяСтрока(ТекстЗаголовкаРасчеты) Тогда
			ТекстЗаголовкаРасчеты = СтрШаблон(НСтр("ru= '%1 '"), ТекстЗаголовкаРасчеты);
		КонецЕсли;
		ТекстЗаголовкаРасчеты = СтрШаблон(НСтр("ru= '%1(оплата в %2)'"), ТекстЗаголовкаРасчеты, ВалютаРегламентированногоУчета);
	КонецЕсли;
	Если ЭтоДоговорЗайма Тогда
		Если Объект.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.БезНачисленияПроцентов") Тогда
			ПараметрыЗайма = Объект.ТипПроцентнойСтавки;
		Иначе
			ТекстЗаголовкаПроцентнаяСтавка = Формат(Объект.ПроцентнаяСтавка, "ЧДЦ=2; ЧН=0,00%; ЧФ=Ч%");
			Если Объект.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ") Тогда
				ШаблонЗаголовкаРасчеты = НСтр("ru = '%1 + %2'");
				Если Объект.ИспользоватьОтсрочкуПоСтавкеЦБ Тогда
					ТекстЗаголовкаПроцентнаяСтавка = СтрШаблон(НСтр("ru = '%1 %2'"), ТекстЗаголовкаПроцентнаяСтавка,
						НСтр("ru = '(применяется отсрочка)'"));
				КонецЕсли;
			Иначе
				ШаблонЗаголовкаРасчеты = НСтр("ru = '%1 %2'");
			КонецЕсли;
			ПараметрыЗайма = СтрШаблон(ШаблонЗаголовкаРасчеты, Объект.ТипПроцентнойСтавки, ТекстЗаголовкаПроцентнаяСтавка);
		КонецЕсли;
		Если ПустаяСтрока(ТекстЗаголовкаРасчеты) Тогда
			ТекстЗаголовкаРасчеты = СтрШаблон(НСтр("ru= '%1'"), ПараметрыЗайма);
		Иначе
			ТекстЗаголовкаРасчеты = СтрШаблон(НСтр("ru= '%1, %2'"), ПараметрыЗайма, ТекстЗаголовкаРасчеты);
		КонецЕсли;
	КонецЕсли;
	Если ПустаяСтрока(ТекстЗаголовкаРасчеты) Тогда
		ТекстЗаголовкаРасчеты = НСтр("ru= 'Расчеты'");
	Иначе
		ТекстЗаголовкаРасчеты = СтрШаблон(НСтр("ru= 'Расчеты: %1'"), ТекстЗаголовкаРасчеты);
	КонецЕсли;
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьЗаголовокСворачиваемойГруппы(Форма, "ГруппаРасчеты", ТекстЗаголовкаРасчеты);
	
	Элементы.СрокОплаты.Заголовок = СформироватьЗаголовокСрокаОплаты(Объект.СрокОплаты);
	
	Если Элементы.ОплатаВРублях.Видимость Тогда
		Элементы.ОплатаВРублях.ТолькоПросмотр = Форма.УсловиеОплатаВВалютеПередано;
	КонецЕсли;
	
	Если Элементы.ВалютаДоговора.Видимость Тогда
		Элементы.ВалютаДоговора.ТолькоПросмотр = Форма.ВалютаНедоступна Или ЭтоДоговорФакторинга;
		Элементы.ВалютаДоговора.КнопкаСоздания = Не (Форма.ВалютаНедоступна Или ЭтоДоговорФакторинга);
	КонецЕсли;
	
	Элементы.ГруппаНалогообложениеКонтрагента.Видимость = Истина;
	Элементы.ВидВзаиморасчетов.Видимость = Истина;
	Элементы.ГруппаПроцентнаяСтавка.Видимость = Ложь;
	Элементы.ТипЦен.Видимость = ЕстьПравоЧтенияТипыЦенНоменклатуры();
	
	Элементы.ГруппаСрокОплаты.Видимость = Не ЭтоПрочийДоговор И Не ЭтоДоговорЗайма;
	
	Элементы.СрокОплаты.Видимость = Форма.ВидСрокаОплаты = ИмяСрокаОплатыВДнях();
	Элементы.ДатаОплаты.Видимость = Форма.ВидСрокаОплаты = ИмяСрокаОплатыДатой();

	Элементы.ГруппаБухгалтерскийУчет.Видимость = Форма.ОтражатьАвансыВыданныеКакАктивы 
		И (ЭтоДоговорСПоставщиком ИЛИ ЭтоДоговорСКомиссионеромНаЗакупку);
	
	// Группа "Комиссионное вознаграждение"
	Элементы.ГруппаКомиссионноеВознаграждение.Видимость = ВидимостьКомиссионногоВознаграждения;
	
	// Группа "НДС"
	Элементы.ГруппаЗаполнениеДокументов.Видимость  = Истина;
	Элементы.СпособВыставленияДокументов.Видимость = Не ЭтоДоговорКомиссии;
	ЭтоДоговорССамозанятым = Объект.НалогообложениеКонтрагента = ПредопределенноеЗначение(
		"Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход");
	Элементы.ГруппаНДС.Видимость = Не ЭтоДоговорССамозанятым И Не ЭтоДоговорЗайма;
	
	Если ЭтоДоговорРеализации Тогда
		Элементы.ГруппаНалоговыйАгентОрганизация.Видимость = Ложь;
		Элементы.ГруппаЭлектронныеУслуги.Видимость         = Ложь;
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
			Элементы.ГруппаНалоговыйАгентПокупатель.Видимость = Истина;
		Иначе
			Элементы.ГруппаНалоговыйАгентПокупатель.Видимость = Ложь;
		КонецЕсли;
		Элементы.ГруппаНДССАвансов.Видимость = ЭтоПлательщикНДС;
		Элементы.СпособЗаполненияСтавкиНДС.Видимость   = ЭтоДоговорСКомиссионером;
		Элементы.НДСПоСтавкам4и2.Видимость             = ЭтоДоговорСКомиссионером;
		Элементы.СпособВыставленияДокументов.Заголовок = НСтр("ru='Документ реализации'");
			
		Если Объект.УчетАгентскогоНДСПокупателем Тогда 
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Покупатель выступает в качестве налогового агента по уплате НДС'");
		ИначеЕсли ЭтоПлательщикНДС Тогда
			Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку")
				И Объект.СчетаФактурыОтИмениОрганизации Тогда 
				ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Счета-фактуры перевыставляются от имени организации'");
			ИначеЕсли ЗначениеЗаполнено(Объект.ПорядокРегистрацииСчетовФактурНаАвансПоДоговору) Тогда
				ТекстЗаголовкаНДС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'НДС: %1'"), Строка(Объект.ПорядокРегистрацииСчетовФактурНаАвансПоДоговору));
			Иначе 
				ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Регистрировать счета-фактуры на аванс в порядке, соответствующем учетной политике'");
			КонецЕсли;
		Иначе
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС'");
		КонецЕсли;
		
		Элементы.КодДекларацияРаздел7.Видимость = 
			ЭтоПлательщикНДС 
			И Форма.РаздельныйУчетНДС 
			И НЕ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");
		Элементы.ГруппаПеревыставлениеСФ.Видимость = 
			Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");
		
	ИначеЕсли ЭтоДоговорЗайма Тогда
		Элементы.ГруппаПроцентнаяСтавка.Видимость           = Истина;
		Элементы.ГруппаНалогообложениеКонтрагента.Видимость = Ложь;
		Элементы.ТипЦен.Видимость                           = Ложь;
		Элементы.ВидВзаиморасчетов.Видимость                = Ложь;
		Элементы.ИсторияПроцентныхСтавок.Видимость          = Не ЭтоБеспроцентныйЗаем;
		Если Объект.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ") Тогда
			Элементы.ДекорацияПлюсКСтавкеЦБ.Видимость = Истина;
			Элементы.ТипПроцентнойСтавки.Ширина = 18;
			Элементы.ГруппаПроцентнаяСтавкаОтсрочкаПоСтавкеЦБ.Видимость = Истина;
		Иначе
			Элементы.ДекорацияПлюсКСтавкеЦБ.Видимость = Ложь;
			Элементы.ТипПроцентнойСтавки.Ширина = 20;
			Элементы.ГруппаПроцентнаяСтавкаОтсрочкаПоСтавкеЦБ.Видимость = Ложь;
		КонецЕсли;
		Элементы.ПроцентнаяСтавка.Видимость = Объект.ТипПроцентнойСтавки <> ПредопределенноеЗначение("Перечисление.ТипыПроцентныхСтавок.БезНачисленияПроцентов");
	ИначеЕсли ЭтоПрочийДоговор Тогда
		Элементы.ГруппаНалоговыйАгентОрганизация.Видимость = Ложь;
		Элементы.ГруппаЭлектронныеУслуги.Видимость         = Ложь;
		Элементы.ГруппаНалоговыйАгентПокупатель.Видимость  = Ложь;
		Элементы.ГруппаНДССАвансов.Видимость               = Ложь;
		
		Элементы.ГруппаЗаполнениеДокументов.Видимость      = Ложь;
		
		Элементы.КодДекларацияРаздел7.Видимость            = Ложь;
		Элементы.ГруппаПеревыставлениеСФ.Видимость         = Ложь;
	Иначе
		Элементы.ГруппаНалоговыйАгентОрганизация.Видимость = НЕ ЭтоДоговорФакторинга И Форма.ИсполняютсяОбязанностиНалоговогоАгентаПоНДС;
		Элементы.ГруппаЭлектронныеУслуги.Видимость         = Форма.ЭтоДоговорИностранца И Форма.ВедетсяУчетУслугИностранныхИнтернетКомпаний;
		Элементы.ГруппаНалоговыйАгентПокупатель.Видимость  = Ложь;
		Элементы.ГруппаНДССАвансов.Видимость               = Ложь;
		Элементы.КодДекларацияРаздел7.Видимость            = Ложь;
		
		Элементы.НДСПоСтавкам4и2.Видимость                 = Истина;
		Элементы.СпособЗаполненияСтавкиНДС.Видимость       = Истина;
		Элементы.СпособВыставленияДокументов.Заголовок     = НСтр("ru='Документ поступления'");
		
		// Установим параметры выбора вида агентского договора в зависимости от страны регистрации контрагента.
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.ИностранныйКонтрагент", Форма.ЭтоДоговорИностранца);
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(ПараметрВыбора);
		Элементы.ВидАгентскогоДоговора.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
		Элементы.ГруппаНалоговыйАгентОрганизация.Доступность = ЭтоДоговорПриобретения 
			И Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку");
			
		// Установим "виртуальные" флажки и переключатели по электронным услугам.
		Если Объект.ЭлектронныеУслуги 
			ИЛИ Объект.УчетАгентскогоНДС 
			И Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги") Тогда 
			Форма.ЭлектронныеУслугиИностранца = Истина;
			Если Объект.ЭлектронныеУслуги Тогда 
				Форма.ПорядокУплатыНДСПоЭлектроннымУслугам = 1;
			Иначе
				Форма.ПорядокУплатыНДСПоЭлектроннымУслугам = 2;
			КонецЕсли;
		Иначе
			Форма.ЭлектронныеУслугиИностранца = Ложь;
		КонецЕсли;
		
		Элементы.УчетАгентскогоНДС.Видимость = НЕ Форма.ЭлектронныеУслугиИностранца;
		Элементы.ВидАгентскогоДоговора.Видимость = НЕ Форма.ЭлектронныеУслугиИностранца;
		Элементы.ГруппаПорядокУплатыНДСПоЭлектроннымУслугам.Видимость = 
			Форма.ЭлектронныеУслугиИностранца И Форма.ИсполняютсяОбязанностиНалоговогоАгентаПоНДС;
		
		ДоступенВыборВидаАгентскогоДоговора = ЭтоДоговорПриобретения
			И Объект.УчетАгентскогоНДС
			И НЕ ЭтоДоговорКомиссии
			И Форма.ИсполняютсяОбязанностиНалоговогоАгентаПоНДС
			И НЕ Форма.ЭтоДоговорИностранца;
		Элементы.ВидАгентскогоДоговора.Доступность = Объект.УчетАгентскогоНДС;
		Элементы.ВидАгентскогоДоговора.ТолькоПросмотр = Не ДоступенВыборВидаАгентскогоДоговора;
		Элементы.ВидАгентскогоДоговора.КнопкаВыпадающегоСписка = ДоступенВыборВидаАгентскогоДоговора;
		
		Элементы.НаименованиеДляСчетаФактурыНалоговогоАгента.Доступность = Объект.УчетАгентскогоНДС;
		
		Если Объект.ЭлектронныеУслуги Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Приобретение электронных услуг'");
		ИначеЕсли Объект.УчетАгентскогоНДС Тогда
			ТекстЗаголовкаНДС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'НДС: Налоговый агент по уплате НДС - %1'"), Строка(Объект.ВидАгентскогоДоговора));
		ИначеЕсли Форма.ДоговорСРезидентомТаможенногоСоюза И НЕ Объект.УчетАгентскогоНДС Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: Организация не выступает в качестве налогового агента по уплате НДС'");
		ИначеЕсли Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.ИзКарточкиНоменклатуры") Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: ставка в документах из карточки номенклатуры'");
		ИначеЕсли Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.БезНДС") Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: ставка в документах ""без НДС""'");
		ИначеЕсли Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.Автоматически") Тогда
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС: ставка в документах определяется автоматически'");
		Иначе
			ТекстЗаголовкаНДС = НСтр("ru = 'НДС'");
		КонецЕсли;
		Элементы.ГруппаПеревыставлениеСФ.Видимость = Ложь;
		
	КонецЕсли;
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьЗаголовокСворачиваемойГруппы(Форма, "ГруппаНДС", ТекстЗаголовкаНДС);
	
	//Группа "Государственный контракт"
	Если ЭтоДоговорКомиссии Или ЭтоДоговорЗайма Тогда 
		Элементы.ГруппаГосударственныйКонтракт.Видимость = Ложь;
	Иначе
		Элементы.ГруппаГосударственныйКонтракт.Видимость = Форма.ПоказыватьГосконтракты;
		Форма.ЭтоГосударственныйКонтракт = ЗначениеЗаполнено(Объект.ГосударственныйКонтракт) ИЛИ ЗначениеЗаполнено(Форма.ГосударственныйКонтрактЕИС);
		Элементы.ГосударственныйКонтракт.Доступность = Форма.ЭтоГосударственныйКонтракт;
		Элементы.ГосударственныйКонтрактЕИС.Доступность = Форма.ЭтоГосударственныйКонтракт;
	КонецЕсли;
	
	//Группа "Платежный агент"
	Элементы.ГруппаПлатежныйАгент.Видимость               = ВидимостьКомиссионногоВознаграждения;
	
	Элементы.ПризнакАгента.Доступность                    = Объект.ПлатежныйАгент;
	
	ЭтоПлатежныйАгент  = Объект.ПлатежныйАгент 
		И (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйАгент") 
			ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйСубагент")));
	ЭтоБанковскийАгент = Объект.ПлатежныйАгент 
		И (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент") 
			ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйСубагент")));
	
	Элементы.ТелефонПоставщика.Видимость                = ЭтоПлатежныйАгент ИЛИ ЭтоБанковскийАгент;
	Элементы.НаименованиеОператораПеревода.Видимость    = ЭтоБанковскийАгент;
	Элементы.ИННОператораПеревода.Видимость             = ЭтоБанковскийАгент;
	Элементы.АдресОператораПеревода.Видимость           = ЭтоБанковскийАгент;
	Элементы.ТелефонОператораПеревода.Видимость         = ЭтоБанковскийАгент;
	Элементы.ТелефонОператораПоПриемуПлатежей.Видимость = ЭтоПлатежныйАгент;
	Элементы.ТелефонПлатежногоАгента.Видимость          = ЭтоПлатежныйАгент ИЛИ ЭтоБанковскийАгент;
	Элементы.ОперацияПлатежногоАгента.Видимость         = ЭтоБанковскийАгент;
	
	Элементы.СпособЗаполненияСтавкиНДС.Доступность = 
		НЕ (ЭтоДоговорРеализации ИЛИ Объект.УчетАгентскогоНДС ИЛИ Форма.ДоговорСРезидентомТаможенногоСоюза)
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером");
	
	Элементы.НДСПоСтавкам4и2.Доступность = ?(Объект.СпособЗаполненияСтавкиНДС =
		 ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.ИзКарточкиНоменклатуры"), Истина, Ложь);
	
	// Группа "Обеспечения обязательств"
	Элементы.ВидОбеспеченияОбязательствИПлатежей.Видимость = Форма.ПредусмотреноОбеспечениеОбязательств;
	Элементы.ОбеспечениеПредоставил.Видимость              = Форма.ПредусмотреноОбеспечениеОбязательств;
	Элементы.ОбеспечениеПолучил.Видимость                  = Форма.ПредусмотреноОбеспечениеОбязательств;
	Элементы.ОбеспечениеВыданоЗа.Видимость                 = Форма.ПредусмотреноОбеспечениеОбязательств;
	
	Элементы.ГруппаПроверкаДублей.Видимость = Форма.ИспользоватьНумерациюДоговоровСПокупателями
		И ЗначениеЗаполнено(Форма.ПояснениеПроверкиДублей);
	
	Элементы.ГруппаСуммаНДС.Видимость = Не ЭтоДоговорССамозанятым И Не ЭтоДоговорЗайма;
	Элементы.СуммаВключаетНДС.Видимость = Не ЭтоДоговорССамозанятым И Не ЭтоДоговорЗайма;
	Элементы.Всего.Видимость = Не ЭтоДоговорССамозанятым И Не ЭтоДоговорЗайма;
	Элементы.СуммаНДС.Видимость = Не (Объект.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС")
		Или Объект.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0"));
	
	Если Форма.КодОшибкиПроверкиКонтракта <> 0 Тогда 
		Элементы.ОшибочныйНомерОтступ.Видимость = Истина;
		Элементы.ОшибочныйНомерУНК.Видимость = Истина;
		Элементы.ОшибочныйНомерУНК.Заголовок = ПояснениеКОшибкеВНомереУНК(Форма.КодОшибкиПроверкиКонтракта);
	Иначе
		Элементы.ОшибочныйНомерОтступ.Видимость = Ложь;
		Элементы.ОшибочныйНомерУНК.Видимость = Ложь;
	КонецЕсли;
		
	Элементы.ВидДоходаИностранногоКонтрагента.Видимость = Форма.ЭтоДоговорИностранца;
	
	Если Элементы.ВидДоходаИностранногоКонтрагента.Видимость Тогда
		Элементы.ОшибочныйНомерОтступ.Ширина = 24;
	Иначе
		Элементы.ОшибочныйНомерОтступ.Ширина = 14;
	КонецЕсли;
	
	ВидимостьПояснениеКВидуДохода = Форма.ЭтоДоговорИностранца И Форма.ТекущаяДатаПользователя < Дата(2025,4,1)
		И (Объект.ВидДоходаИностранногоКонтрагента = 
		ПредопределенноеЗначение("Перечисление.ВидыДоходовИностранныхОрганизаций.ДоходыУслугиВзаимозависимым")
		ИЛИ Объект.ВидДоходаИностранногоКонтрагента = 
		ПредопределенноеЗначение("Перечисление.ВидыДоходовИностранныхОрганизаций.ДоходПриравненныйДивидендам"));
	
	Элементы.ГруппаПояснениеКВидуДохода.Видимость = ВидимостьПояснениеКВидуДохода;
	
	СтатусСамозанятого = Форма.СтатусСамозанятого;
	Если СтатусСамозанятого <> Неопределено Тогда
		ОформлениеСтатусаНПД =
			ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ДанныеДляОформленияСтатусаСамозанятогоНаФорме(
				СтатусСамозанятого.ПлательщикНПД, СтатусСамозанятого.Описание, СтатусСамозанятого.ТекстОшибки);
		Элементы.ГруппаНалогообложениеКонтрагента.ЦветФона = ОформлениеСтатусаНПД.ЦветФона;
		Элементы.НалогообложениеКонтрагентаРасширеннаяПодсказка.Заголовок = ОформлениеСтатусаНПД.ЗаголовокПодсказки;
	Иначе
		Элементы.ГруппаНалогообложениеКонтрагента.ЦветФона = Новый Цвет;
		Элементы.НалогообложениеКонтрагентаРасширеннаяПодсказка.Заголовок = "";
	КонецЕсли;
	
	Если ЭтоДоговорССамозанятым И Не РаботаСДоговорамиКонтрагентовБПВызовСервера.ВестиУчетПоДоговорам() Тогда
		Элементы.ГруппаНалогообложениеКонтрагента.ЦветФона = Новый Цвет;
		Элементы.НалогообложениеКонтрагентаРасширеннаяПодсказка.Заголовок =
			РаботаСДоговорамиКонтрагентовБПКлиентСервер.ЗаголовокПодсказкиСамозанятыйНеВключеныДоговоры();
	КонецЕсли;
	
	//Группа "Дополнительные соглашения"
	КоличествоСоглашений = Объект.ДополнительныеСоглашения.Количество();
	Если ЗначениеЗаполнено(КоличествоСоглашений) Тогда
		ПоследнееСоглашение = Объект.ДополнительныеСоглашения[0];
		СтрокиПредставления = Новый Массив;
		Если ЗначениеЗаполнено(ПоследнееСоглашение.Номер) Тогда
			СтрокиПредставления.Добавить(СтрШаблон(НСтр("ru = '№%1'"), ПоследнееСоглашение.Номер));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПоследнееСоглашение.Дата) Тогда
			Если СтрокиПредставления.Количество() > 0 Тогда
				СтрокиПредставления.Добавить(НСтр("ru = 'от'"));
			КонецЕсли;
			СтрокиПредставления.Добавить(Формат(ПоследнееСоглашение.Дата, "ДФ=dd.MM.yyyy"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПоследнееСоглашение.Комментарий) Тогда
			СтрокиКомментария = СтрРазделить(ПоследнееСоглашение.Комментарий, Символы.ПС);
			СтрокиПредставления.Добавить(СтрШаблон(НСтр("ru = '(%1)'"), СтрокиКомментария[0]));
		КонецЕсли;
		Если СтрокиПредставления.Количество() > 0 Тогда
			ПредставлениеСоглашений = СтрСоединить(СтрокиПредставления, " ");
			Если КоличествоСоглашений > 1 Тогда
				ПредставлениеСоглашений = СтрШаблон(НСтр("ru = '%1 и еще %2'"), ПредставлениеСоглашений, КоличествоСоглашений - 1);
			КонецЕсли;
			ЗаголовокСоглашений = СтрШаблон(НСтр("ru = 'Дополнительные соглашения: %1'"), ПредставлениеСоглашений);
		Иначе
			ЗаголовокСоглашений = СтрШаблон(НСтр("ru = 'Дополнительные соглашения: %1'"), КоличествоСоглашений);
		КонецЕсли;
	Иначе
		ЗаголовокСоглашений = НСтр("ru = 'Дополнительные соглашения'");
	КонецЕсли;
	ОбщегоНазначенияБПКлиентСервер.УстановитьЗаголовокСворачиваемойГруппы(Форма,
		"ГруппаДополнительныеСоглашения",
		ЗаголовокСоглашений);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокСпособовРасчетаКомиссионногоВознаграждения(Форма)

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;

	ЭтоКомиссияПоЗакупке = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");

	СписокСпособов = ОбщегоНазначенияБПКлиентСервер.СформироватьСписокСпособовРасчетаКомиссионногоВознаграждения(ЭтоКомиссияПоЗакупке);
	СписокВыбора = Элементы.СпособРасчетаКомиссионногоВознаграждения.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокСпособов Цикл
		СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНаименованиеДоговора(Форма)
	
	Объект = Форма.Объект;
	
	ТекстНаименования = Объект.Наименование;
	
	НовыеРеквизитыДоговораСтрокой = ПечатьДоговоровКлиентСервер.РеквизитыДоговораСтрокой(Объект.Номер, Объект.Дата);
	
	Если ПустаяСтрока(ТекстНаименования)
		Или ТекстНаименования = Форма.НаименованиеПоУмолчанию // См. ПечатьДоговоровКлиентСервер.НаименованиеПоУмолчаниюБезРеквизитов()
		Или ТекстНаименования = Форма.РеквизитыДоговораСтрокой Тогда
		ТекстНаименования = НовыеРеквизитыДоговораСтрокой;
	ИначеЕсли СтрНайти(ТекстНаименования, Форма.РеквизитыДоговораСтрокой) > 0 
		И СтрНайти(ТекстНаименования, НовыеРеквизитыДоговораСтрокой) = 0 Тогда
		ТекстНаименования = СтрЗаменить(ТекстНаименования, Форма.РеквизитыДоговораСтрокой, НовыеРеквизитыДоговораСтрокой);
	КонецЕсли;
	
	Форма.РеквизитыДоговораСтрокой = НовыеРеквизитыДоговораСтрокой;
	
	Объект.Наименование = ТекстНаименования;
	
КонецПроцедуры

&НаСервере
Процедура ОграничитьВыборРеквизитов(ЗначенияЗаполнения)

	Если Не Параметры.ОткрытИзОбщейФормы И ЗначенияЗаполнения.Свойство("Организация") Тогда
		Если ТипЗнч(ЗначенияЗаполнения.Организация) = Тип("СправочникСсылка.Организации") Тогда
			Элементы.Организация.ТолькоПросмотр = ЗначениеЗаполнено(ЗначенияЗаполнения.Организация);
		КонецЕсли;
	КонецЕсли;

	Если Не Параметры.ОткрытИзОбщейФормы И ЗначенияЗаполнения.Свойство("Владелец") Тогда
		Если ТипЗнч(ЗначенияЗаполнения.Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
			Элементы.Владелец.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначенияЗаполнения.Свойство("ВалютаВзаиморасчетов") Тогда
		Если ТипЗнч(ЗначенияЗаполнения.ВалютаВзаиморасчетов) = Тип("СправочникСсылка.Валюты") Тогда
			ВалютаНедоступна = Истина;
		КонецЕсли;
	КонецЕсли;
	
	//Проверим, передавались ли ограничения по валюте
	Если Параметры.ЗначенияЗаполнения.Свойство("Валютный") Тогда
		
		МассивРазрешенныхВалют = 
			Справочники.ДоговорыКонтрагентов.ПодготовитьСписокРазрешенныхВалют(
				ВалютаРегламентированногоУчета, Параметры.ЗначенияЗаполнения.Валютный);
		
		Элементы.ВалютаДоговора.ПараметрыВыбора = Новый ФиксированныйМассив(МассивРазрешенныхВалют);
		
		Если Не Параметры.ЗначенияЗаполнения.Валютный Тогда
			ВалютаНедоступна = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.ЗначенияЗаполнения.Свойство("ОплатаВВалюте") Тогда
		УсловиеОплатаВВалютеПередано = Истина;
	КонецЕсли;
	
	Если ЗначенияЗаполнения.Свойство("ВидДоговора") Тогда
		МассивВидовДоговоров = Новый Массив;
		Если ТипЗнч(ЗначенияЗаполнения.ВидДоговора) = Тип("Массив") Тогда
			МассивВидовДоговоров = ЗначенияЗаполнения.ВидДоговора;
		ИначеЕсли ТипЗнч(ЗначенияЗаполнения.ВидДоговора) = Тип("ФиксированныйМассив") Тогда
			МассивВидовДоговоров = Новый Массив(ЗначенияЗаполнения.ВидДоговора);
		КонецЕсли;
		ОдинВидДоговора = МассивВидовДоговоров.Количество() <= 1;
		Если ОдинВидДоговора Тогда
			Если Не Параметры.ОткрытИзОбщейФормы Тогда
				Элементы.ВидДоговора.ТолькоПросмотр = Истина;
			КонецЕсли;
		Иначе
			Элементы.ВидДоговора.РежимВыбораИзСписка = Истина;
			Элементы.ВидДоговора.СписокВыбора.ЗагрузитьЗначения(МассивВидовДоговоров);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияСозданиеФайлаДоговора(ФайлДоговора, ЭлектронныйФормат)
	
	Если ЗначениеЗаполнено(Объект.ФайлДоговора) Тогда
		// В договоре уже указан какой-то файл договора, не меняем его.
		Возврат;
	КонецЕсли;

	Объект.ФайлДоговора = ФайлДоговора;
	
	Объект.ЭлектронныйФормат = ЭлектронныйФормат;
	
	Если Не Модифицированность Тогда
		// Пользователь договор не менял, записываем его самостоятельно.
		Записать();
	КонецЕсли;
	
	// Сообщим форме редактирования текста договора, что мы запомнили ссылку на файл, 
	// и сам объект справочника ДоговорыКонтрагентов изменять не требуется,
	// чтобы при интерактивной работе пользователя не возникало сообщений об изменении
	// объекта в другом сеансе из-за записи изменений элемента справочника.
	Оповестить("ДоговорыКонтрагентов_СозданиеФайлаОбработаноОсновнойФормойДоговора", Объект.Ссылка);

КонецПроцедуры

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	
	Справочники.ДоговорыКонтрагентов.ЗаполнитьРуководителяКонтрагента(Объект);
	УстановитьКнопкуВыбораРуководителяКонтрагента();
	УстановитьСпискиЭлементовОбеспеченияОбязательств();
	ЗаполнитьИОтобразитьСистемуНалогообложенияКонтрагента(Истина);
	// Очистим признаки налогового агента и электронных услуг.
	Объект.УчетАгентскогоНДС     = Ложь;
	Объект.ВидАгентскогоДоговора = Неопределено;
	Объект.ЭлектронныеУслуги     = Ложь;
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если ИспользоватьНумерациюДоговоровСПокупателями Тогда
		Если НомерСозданАвтоматически Или Не ЗначениеЗаполнено(Объект.Номер) Тогда
			УстановитьДатуДоговораСПокупателемПоУмолчанию(ЭтотОбъект);
			УстановитьНомерДоговораСПокупателем();
			СформироватьНаименованиеДоговора(ЭтотОбъект);
		КонецЕсли;
		ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
	КонецЕсли;
	
	Справочники.ДоговорыКонтрагентов.ЗаполнитьРуководителяОрганизации(Объект);
	УстановитьСпискиЭлементовОбеспеченияОбязательств();
	
	УстановитьФункциональныеОпцииФормы();
	УстановитьПараметрыВыбораСтавкиНДС();
	ЗаполнитьСтавкуНДСПоУмолчанию(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКнопкуВыбораРуководителяКонтрагента()
	
	Элементы.РуководительКонтрагента.КнопкаВыбора = (ПолучитьСписокКонтактныхЛиц(Объект.Владелец).Количество() > 0);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ЭтоПлательщикНДС = ПрименятьНДСВДоговоре(Объект.Организация, ТекущаяДатаПользователя, Объект.НалогообложениеКонтрагента);
	ИсполняютсяОбязанностиНалоговогоАгентаПоНДС = ПолучитьФункциональнуюОпцию("ИсполняютсяОбязанностиНалоговогоАгентаПоНДС");
	ОбратноеНачислениеНДС                       = ПолучитьФункциональнуюОпцию("ОбратноеНачислениеНДС");
	ПоказыватьГосконтракты                      = ПолучитьФункциональнуюОпцию("РасширенныйФункционал") ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяГособоронзаказ");
	ВедетсяУчетУслугИностранныхИнтернетКомпаний = ПолучитьФункциональнуюОпцию("ВедетсяУчетУслугИностранныхИнтернетКомпаний");
	ОтражатьАвансыВыданныеКакАктивы             = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета() 
														И ОтражатьАвансыВыданныеКакАктивы(Объект.Организация);
														
	ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров = ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров");

	
	// Для отображения релевантных настроек НДС необходимо различать договоры с российскими и иностранными организациями.
	СтранаРегистрацииКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "СтранаРегистрации");
	ЭтоДоговорИностранца = ЗначениеЗаполнено(СтранаРегистрацииКонтрагента)
		И СтранаРегистрацииКонтрагента <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокКонтактныхЛиц(Знач Контрагент)
	
	Возврат Справочники.КонтактныеЛица.СписокКонтактныхЛиц(Контрагент, Ложь);

КонецФункции

&НаКлиенте
Процедура РуководительКонтрагентаНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивФИОДолжность = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(РезультатВыбора.Представление, ", ");
	Если МассивФИОДолжность.Количество() > 0 Тогда
		Объект.РуководительКонтрагента = МассивФИОДолжность[0];
	КонецЕсли;
	Если МассивФИОДолжность.Количество() > 1 Тогда
		Объект.ДолжностьРуководителяКонтрагента = МассивФИОДолжность[1];
	КонецЕсли;
	ПриИзмененииПредставленияРуководителяКонтрагента();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РуководительКонтрагентаПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		Возврат;
	ИначеЕсли Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектСоСклоняемымиРеквизитами = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"); // любой договор
	
	ЭтотОбъект.СклоненияРуководительКонтрагента = Новый ФиксированнаяСтруктура(ПолучитьИзВременногоХранилища(Результат.АдресРезультата));
	ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента = Истина;
	Если ТекущийРеквизитСклонения = "СклоненияРуководительКонтрагента" Тогда
		ЭтотОбъект.Склонения = ЭтотОбъект.Склонения;
		ЭтотОбъект.ИзмененоПредставление = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяКонтрагентаПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		Возврат;
	ИначеЕсли Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектСоСклоняемымиРеквизитами = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"); // любой договор
	
	ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента = Новый ФиксированнаяСтруктура(ПолучитьИзВременногоХранилища(Результат.АдресРезультата));
	ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента = Истина;
	Если ТекущийРеквизитСклонения = "СклоненияДолжностьРуководителяКонтрагента" Тогда
		ЭтотОбъект.Склонения = ЭтотОбъект.Склонения;
		ЭтотОбъект.ИзмененоПредставление = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВалютаВзаиморасчетовПриИзмененииНаСервере()
	
	Если Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
		Объект.РасчетыВУсловныхЕдиницах = Ложь;
		ОплатаВРублях = 0;
	Иначе
		
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем")
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
			ИЛИ ДокументОформляетсяВУсловныхЕдиницах() Тогда
			Объект.РасчетыВУсловныхЕдиницах = Истина;
			ОплатаВРублях = 1;
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ДокументОформляетсяВУсловныхЕдиницах()
	
	// Для этих видов договоров оплата в У.Е. не поддерживается.
	МассивВидовДоговоровБезУЕ = Новый Массив;
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.Прочее);
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	МассивВидовДоговоровБезУЕ.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	Если МассивВидовДоговоровБезУЕ.Найти(Объект.ВидДоговора) <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат УсловиеОплатаВВалютеПередано И НЕ Объект.ОплатаВВалюте;
	
КонецФункции

&НаКлиенте
Процедура СпособЗаполненияСтавкиНДСПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияСтавкиНДСПоУмолчанию()
	
	ЗаполнятьСтавкуНДСБезНДС = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем")
		ИЛИ Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку");
	
	Если ЗаполнятьСтавкуНДСБезНДС Тогда
		Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.БезНДС");
	Иначе
		Объект.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.Автоматически");
	КонецЕсли;
	
	Если Объект.НДСПоСтавкам4и2 и ЗаполнятьСтавкуНДСБезНДС Тогда
		Объект.НДСПоСтавкам4и2 = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПризнакиАгента()
	Если НЕ Объект.ПлатежныйАгент Тогда
		Объект.ПризнакАгента = Неопределено;
		Объект.ТелефонПоставщика             = "";
		Объект.ТелефонПлатежногоАгента       = "";
	КонецЕсли; 
	
	ЭтоПлатежныйАгент  = (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйАгент")) ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйСубагент"));
	ЭтоБанковскийАгент = (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент")) ИЛИ (Объект.ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйСубагент"));
	
	Если НЕ ЭтоБанковскийАгент Тогда
		Объект.НаименованиеОператораПеревода = "";
		Объект.ИННОператораПеревода          = "";
		Объект.АдресОператораПеревода        = "";
		Объект.ТелефонОператораПеревода      = "";
	КонецЕсли;
	
	Если НЕ ЭтоПлатежныйАгент Тогда
		Объект.ТелефонОператораПоПриемуПлатежей = "";
		Объект.ОперацияПлатежногоАгента         = "";
	КонецЕсли; 
КонецПроцедуры 

&НаСервере
Процедура УстановитьСпискиЭлементовОбеспеченияОбязательств()
	
	СписокВыбора = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		СписокВыбора.Добавить(Объект.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		СписокВыбора.Добавить(Объект.Владелец);
	КонецЕсли;
	
	СписокВыбора.Добавить(НСтр("ru='Выбрать...'"));
	
	Элементы.ОбеспечениеПредоставил.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	Элементы.ОбеспечениеПолучил.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	Элементы.ОбеспечениеВыданоЗа.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораКонтрагентаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтотОбъект[ДополнительныеПараметры.ИмяЭлемента] = Результат;
		Объект[ДополнительныеПараметры.ИмяЭлемента] = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВидовСрокаОплаты()
	
	Элементы.ВидСрокаОплаты.СписокВыбора.Добавить(
		ИмяНеназначенногоСрокаОплаты(), НСтр("ru = 'не установлен'"));
	Элементы.ВидСрокаОплаты.СписокВыбора.Добавить(
		ИмяСрокаОплатыВДнях(), НСтр("ru = 'в течение'"));
	Элементы.ВидСрокаОплаты.СписокВыбора.Добавить(
		ИмяСрокаОплатыДатой(), НСтр("ru = 'не позднее'"));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидСрокаОплаты()
	
	Если Не Объект.УстановленСрокОплаты Тогда
		ВидСрокаОплаты = ИмяНеназначенногоСрокаОплаты();
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаОплаты) Тогда
		ВидСрокаОплаты = ИмяСрокаОплатыДатой();
	Иначе
		ВидСрокаОплаты = ИмяСрокаОплатыВДнях();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяНеназначенногоСрокаОплаты()
	
	Возврат "НеУстановлен";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСрокаОплатыВДнях()
	
	Возврат "ВДнях";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСрокаОплатыДатой()
	
	Возврат "Датой";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьЗаголовокСрокаОплаты(КоличествоДней)
	
	Возврат ОбщегоНазначенияБПКлиентСервер.ФормаМножественногоЧисла(
		НСтр("ru = 'дня'"), НСтр("ru = 'дней'"), НСтр("ru = 'дней'"), КоличествоДней);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтражатьАвансыВыданныеКакАктивы(Организация)
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УчетнаяПолитика.СпособОтраженияАвансовВыданных КАК СпособОтраженияАвансовВыданных
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.Организация = &Организация
	|	И УчетнаяПолитика.СпособОтраженияАвансовВыданных = ЗНАЧЕНИЕ(Перечисление.СпособОтраженияАвансовВыданных.РазделятьПоВидамАктивов)";

	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

&НаСервере
Процедура УстановитьНумерациюДоговоровСПокупателями()
	
	ИспользоватьНумерациюДоговоровСПокупателями = РаботаСДоговорамиКонтрагентовБП.ИспользуетсяНумерацияПоВидуДоговора(
		Объект.ВидДоговора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНомерДоговораСПокупателем()
	
	ДоговорОбъект = РеквизитФормыВЗначение("Объект");
	
	Справочники.ДоговорыКонтрагентов.УстановитьНомерДоговораСПокупателем(ДоговорОбъект);
	
	ЗначениеВРеквизитФормы(ДоговорОбъект, "Объект");
	
	НомерСозданАвтоматически = Истина;
	
	ТекущийЭлемент = Элементы.Номер;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДублиДоговораСПокупателем(Знач Номер, Знач Дата, Знач Организация, Знач Ссылка)
	
	Возврат Справочники.ДоговорыКонтрагентов.ДублиДоговораСПокупателем(Номер, Дата, Организация, Ссылка);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьДублиДоговораСПокупателем(Форма)
	
	Если Форма.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ПояснениеПроверкиДублей = "";
	
	Объект = Форма.Объект;
	
	Дубли = ДублиДоговораСПокупателем(Объект.Номер, Объект.Дата, Объект.Организация, Объект.Ссылка);
	
	КоличествоЭлементовДублей = Дубли.Количество();
	Если КоличествоЭлементовДублей > 0 Тогда
		Форма.ПояснениеПроверкиДублей = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Договор с таким номером уже есть'"), , Новый Цвет(255, 0, 0), , "ПоказатьДубли");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДатуДоговораСПокупателемПоУмолчанию(Форма)
	
	Объект = Форма.Объект;
	Если Не ЗначениеЗаполнено(Объект.Дата) И Объект.Ссылка.Пустая() Тогда
		Объект.Дата = Форма.ТекущаяДатаПользователя;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыСпискаДублей(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДублиДоговораСПокупателем()
	
	ВидыДоговоров = Новый СписокЗначений;
	ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
	ЗначениеОтбора = Новый Структура;
	ЗначениеОтбора.Вставить("Ссылка", ДублиДоговораСПокупателем(Объект.Номер, Объект.Дата, Объект.Организация, Объект.Ссылка));
	ПараметрыФормы = Новый Структура("Отбор", ЗначениеОтбора);
	
	ОповещениеЗакрытия = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСпискаДублей", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаСписка", 
		ПараметрыФормы, ЭтотОбъект, , , , ОповещениеЗакрытия, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюДоговораСНеуникальнымНомером(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ПропуститьПроверку") 
		И ПараметрыЗаписи.ПропуститьПроверку Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьНумерациюДоговоровСПокупателями Тогда
		ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
		
		Если ЗначениеЗаполнено(ПояснениеПроверкиДублей) Тогда
			Отказ = Истина;
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
			
			ОбработчикОповещения = Новый ОписаниеОповещения("ВопросЗаписатьДоговорСНеуникальнымНомеромЗавершение",
				ЭтотОбъект, ДополнительныеПараметры);
			
			ТекстВопроса = СтрШаблон(НСтр("ru = '%1. Записать?'"), ПояснениеПроверкиДублей);
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Записать'"));
			Кнопки.Добавить(КодВозвратаДиалога.Нет);
			
			ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Нет);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаписатьДоговорСНеуникальнымНомеромЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ПараметрыЗаписи = ДополнительныеПараметры.ПараметрыЗаписи;
		ПараметрыЗаписи.Вставить("ПропуститьПроверку", Истина);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокПодсказкиНомера()
	
	Если ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой") Тогда
		ИмяРаздела = "Настройки - Система - Параметры учета - Нумерация договоров";
	Иначе
		ИмяРаздела = "Администрирование - Параметры учета - Нумерация договоров";
	КонецЕсли;
	
	СсылкаНаРаздел = "e1cib/command/Обработка.ПанельАдминистрированияБП.Команда.НумерацияДоговоров";
	
	ДоступнаНастройка = ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьНумерациюДоговоровСПокупателями);
	
	Элементы.НомерРасширеннаяПодсказка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = '%1 нумерацию в разделе <a href=""%2"">%3</a>'"),
		?(ДоступнаНастройка, "Настройте", "Проверьте"), СсылкаНаРаздел, ИмяРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСтавкуНДС(Знач ПроцентНДС = Неопределено)
	
	Если ПроцентНДС = Неопределено Тогда
		ПроцентНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(Объект.СтавкаНДС);
	КонецЕсли;
	
	Объект.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
		Сумма,
		Объект.СуммаВключаетНДС,
		ПроцентНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСумму()
	
	Объект.Сумма = Сумма + ?(Объект.СуммаВключаетНДС, 0, Объект.СуммаНДС);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьПравоРедактирования()
	Возврат ПравоДоступа("Редактирование", Метаданные.Справочники.ДоговорыКонтрагентов);
КонецФункции

&НаСервере
Функция КодОшибкиПроверкиНомераКонтракта(Знач ДатаПостановкиКонтрактаНаУчет, Знач УникальныйНомер)
	
	Если НЕ ЗначениеЗаполнено(ДатаПостановкиКонтрактаНаУчет)
		И НЕ ЗначениеЗаполнено(УникальныйНомер) Тогда
		// Пользователь еще ничего не ввел.
		Возврат 0;
	ИначеЕсли ЗначениеЗаполнено(ДатаПостановкиКонтрактаНаУчет)
		И НЕ ЗначениеЗаполнено(УникальныйНомер) Тогда
		// Нужно заполнить уникальный номер валютного контракта, т.к. заполнена дата постановки контракта на валютный учет.
		Возврат 1;
	ИначеЕсли НЕ ЗначениеЗаполнено(ДатаПостановкиКонтрактаНаУчет)
		И ЗначениеЗаполнено(УникальныйНомер) Тогда
		// Нужно заполнить дату постановки контракта на валютный учет, т.к. заполен уникальный номер валютного контракта.
		Возврат 2;
	Иначе
		
		МассивЧастейНомера = СтрРазделить(УникальныйНомер, "/");
		
		Если МассивЧастейНомера.Количество() <> 5 Тогда
			// Ошибочное количество элементов.
			Возврат 8;
		КонецЕсли;

		ПерваяЧасть = МассивЧастейНомера[0];
		
		Если Не СтрПодобнаПоРегулярномуВыражению(ПерваяЧасть, "[0-9]{8}") Тогда
		 	// Ошибочная длина первой части.
			Возврат 3;
		КонецЕсли;
		
		ВтораяЧасть = МассивЧастейНомера[1];
		
		Если Не СтрПодобнаПоРегулярномуВыражению(ВтораяЧасть, "[0-9]{4}") Тогда
		 	// Ошибочная длина второй части.
			Возврат 4;
		КонецЕсли;
		
		ТретьяЧасть = МассивЧастейНомера[2];
		
		Если Не СтрПодобнаПоРегулярномуВыражению(ТретьяЧасть, "[0-9]{4}")
			И Не СтрПодобнаПоРегулярномуВыражению(ТретьяЧасть, "GU[0-9]{2}") Тогда
		 	// Ошибочная длина третьей части.
			Возврат 5;
		КонецЕсли;
		
		ЧетвертаяЧасть = МассивЧастейНомера[3];
		
		Если Не СтрПодобнаПоРегулярномуВыражению(ЧетвертаяЧасть, "[0-9]{1}")  Тогда
		 	// Ошибочная длина четвертой части.
			Возврат 6;
		КонецЕсли;
		
		ПятаяЧасть = МассивЧастейНомера[4];
		
		Если Не СтрПодобнаПоРегулярномуВыражению(ПятаяЧасть, "[0-9]{1}") Тогда
		 	// Ошибочная длина пятой части.
			Возврат 7;
		КонецЕсли;

	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПояснениеКОшибкеВНомереУНК(КодОшибкиПроверкиКонтракта) 
	
	Если КодОшибкиПроверкиКонтракта = 1 Тогда
		Возврат НСтр("ru='Заполните уникальный номер валютного контракта'");
	ИначеЕсли КодОшибкиПроверкиКонтракта = 2 Тогда
		Возврат НСтр("ru='Заполните дату постановки контракта на валютный учет'");
	Иначе
		
		КодОшибкиПроверкиКонтрактаЗаголовок = НСтр("ru='Уникальный номер валютного контракта не соответствует шаблону:
			|""ХХХХХХХХ/ХХХХ/ХХХХ/Х/Х"" или ""ХХХХХХХХ/ХХХХ/GUХХ/Х/Х"", 
			|где X - число от 0 до 9, ""GU"" - символы.
			|'");
		
		Если КодОшибкиПроверкиКонтракта = 3 Тогда
			Возврат КодОшибкиПроверкиКонтрактаЗаголовок + НСтр("ru='Первый блок должен состоять из 8 цифр'");
		ИначеЕсли КодОшибкиПроверкиКонтракта = 4 Тогда 
			Возврат КодОшибкиПроверкиКонтрактаЗаголовок + НСтр("ru='Второй блок должен состоять из 4 цифр'");
		ИначеЕсли КодОшибкиПроверкиКонтракта = 5 Тогда
			Возврат КодОшибкиПроверкиКонтрактаЗаголовок + НСтр("ru='Третий блок должен соответствовать формату'");
		ИначеЕсли КодОшибкиПроверкиКонтракта = 6 Тогда
			Возврат КодОшибкиПроверкиКонтрактаЗаголовок + НСтр("ru='Четвертый блок должен состоять из 1 цифры'");
		ИначеЕсли КодОшибкиПроверкиКонтракта = 7 Тогда
			Возврат КодОшибкиПроверкиКонтрактаЗаголовок + НСтр("ru='Пятый блок должен состоять из 1 цифры'");
		ИначеЕсли КодОшибкиПроверкиКонтракта = 8 Тогда
			Возврат КодОшибкиПроверкиКонтрактаЗаголовок + НСтр("ru='Номер должен состоять из пяти блоков, разделенных дробью ""/""'");
		КонецЕсли;
	КонецЕсли;

	Возврат "";
	
КонецФункции

&НаСервере
Процедура ЗаполнитьИОтобразитьСистемуНалогообложенияКонтрагента(ЗаполнитьИзИстории = Ложь)
	
	НалогообложениеСписок = Элементы.НалогообложениеКонтрагента.СписокВыбора;
	НалогообложениеСписок.Очистить();
	
	ДоступныеСистемы = Перечисления.СистемыНалогообложения.ДанныеВыбора(Объект.Владелец);
	
	Для Каждого Элемент Из ДоступныеСистемы Цикл
		НалогообложениеСписок.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;
	
	Если ЗаполнитьИзИстории И ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Владелец) Тогда
		НайденноеНалогообложение = Справочники.ДоговорыКонтрагентов.НалогообложениеКонтрагента(
			Объект.Организация, Объект.Владелец);
		Если НалогообложениеСписок.НайтиПоЗначению(НайденноеНалогообложение) <> Неопределено Тогда
			Объект.НалогообложениеКонтрагента = НайденноеНалогообложение;
		Иначе
			Объект.НалогообложениеКонтрагента = Перечисления.СистемыНалогообложения.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСтавкуНДСПоУмолчанию(Форма)
	
	Объект = Форма.Объект;
	
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
		Объект.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПриРеализации(
			Форма.ТекущаяДатаПользователя,
			Объект.Организация);
	Иначе
		Объект.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПользователя(
			Форма.ТекущаяДатаПользователя,
			Форма.ЭтоПлательщикНДС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачатьПолучениеСтатусаСамозанятого()
	
	Если Объект.ДоговорЗакрыт Тогда
		СтатусСамозанятого = Неопределено;
		УправлениеФормой(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ЭтоСамозанятый = Объект.НалогообложениеКонтрагента =
		ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход");
	
	ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(Объект.Дата);
	Если ЭтоСамозанятый Тогда
		СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБПКлиент.СтатусСамозанятогоДляДоговора(
			ЭтотОбъект, Объект.Владелец, ДатаПолученияСтатуса);
	Иначе
		СохраненныйСтатус = Неопределено;
	КонецЕсли;
	Если СохраненныйСтатус <> Неопределено Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДоговора(
			СохраненныйСтатус.ПлательщикНПД, ДатаПолученияСтатуса, СохраненныйСтатус.ДатаРегистрации);
	Иначе
		СтатусСамозанятого = Неопределено;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСтатусСамозанятого()
	
	Если Объект.НалогообложениеКонтрагента = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход
		И Не Объект.ДоговорЗакрыт Тогда
		ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБП.ДатаПолученияСтатуса(Объект.Дата);
		СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБП.СохраненныйСтатус(
			Объект.Владелец, ДатаПолученияСтатуса);
		Если СохраненныйСтатус <> Неопределено Тогда
			СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДоговора(
				СохраненныйСтатус.ПлательщикНПД, ДатаПолученияСтатуса, СохраненныйСтатус.ДатаРегистрации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораСтавкиНДС()

	Если Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда
		УчетНДС.УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "СтавкаНДС", "СтавкиНДСДоговора", ТекущаяДатаПользователя);
	Иначе
		ПараметрВыбора = Новый ПараметрВыбора("СтавкиНДСДоговора", Истина);
		МассивПараметровВыбора = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрВыбора);
		Элементы.СтавкаНДС.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрименятьНДСВДоговоре(Организация, Дата, НалогообложениеКонтрагента)
	
	Возврат (УчетнаяПолитика.ПлательщикНДС(Организация, Дата)
		Или УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, Дата))
		И Не НалогообложениеКонтрагента = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход;
	
КонецФункции

&НаКлиенте
Процедура ПередЗаписьюДоговораСИзмененнымТипомСтавки(Отказ, ПараметрыЗаписи)

	Если ИзмененТипПроцентнойСтавки И ЕстьДокументыПоДоговору(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, НСтр("ru='В договоре был изменен тип процентной ставки.
			|Есть введенные документы по договору, для корректного перерасчета требуется"
			+ " вручную выполнить регламентные операции ""Начисление процентов по займам"" за весь период.'"));
	КонецЕсли;	
	
	ИзмененТипПроцентнойСтавки = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьПравоЧтенияТипыЦенНоменклатуры()
	Возврат ПравоДоступа("Чтение", Метаданные.Справочники.ТипыЦенНоменклатуры);
КонецФункции

&НаКлиенте
Процедура АктуализироватьПроцентнуюСтавкуВИстории()
	
	ТаблицаФормы = Объект.ИсторияПроцентныхСтавок;
	КоличествоЗаписей = ТаблицаФормы.Количество();
	
	Если КоличествоЗаписей > 0 Тогда
		
		ТаблицаФормы.Сортировать("Период");
		АктуальнаяЗаписьИстории = ТаблицаФормы[КоличествоЗаписей - 1];
		АктуальнаяЗаписьИстории.ПроцентнаяСтавка = Объект.ПроцентнаяСтавка;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьДокументыПоДоговору(Договор)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументыПоДоговоруКонтрагента.Ссылка КАК ДокументПоДоговору
		|ИЗ
		|	КритерийОтбора.ДокументыПоДоговоруКонтрагента(&Договор) КАК ДокументыПоДоговоруКонтрагента";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	
	Возврат Не Запрос.Выполнить().Пустой();
		
КонецФункции

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиДополнительныхСоглашений()
	
	Для Каждого СтрокаТаблицы Из Объект.ДополнительныеСоглашения Цикл
		СтрокаТаблицы.ЕстьФайлы = ?(ЗначениеЗаполнено(СтрокаТаблицы.ФайлСоглашения), 0, 1);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДополнительногоСоглашения(ВыбранноеЗначение = Неопределено)
	
	// Заполним комментарий к дополнительному соглашению, поскольку после записи он будет очищен
	КомментарийДополнительногоСоглашенияПередЗаписью = КомментарийДополнительногоСоглашения;
	
	Если Не ТолькоПросмотр Тогда
		Если (Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность)
			И Не Записать() Тогда
			// Выполняем запись договора перед добавлением дополнительного соглашения, поскольку
			// печатная форма соглашения заполняется по реквизитам ссылки; присоединение файла
			// также предполагает наличие ссылки
			Возврат;
		КонецЕсли;
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("ДоговорКонтрагента", Объект.Ссылка);
	ПараметрыФормы.Вставить("НомерСтроки", ВыбранноеЗначение);
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		ДанныеСтроки = Объект.ДополнительныеСоглашения.НайтиПоИдентификатору(ВыбранноеЗначение);
		ПараметрыФормы.Вставить("Номер", ДанныеСтроки.Номер);
		ПараметрыФормы.Вставить("Дата", ДанныеСтроки.Дата);
		ПараметрыФормы.Вставить("Подписано", ДанныеСтроки.Подписано);
		ПараметрыФормы.Вставить("Комментарий", ДанныеСтроки.Комментарий);
		ПараметрыФормы.Вставить("ФайлСоглашения", ДанныеСтроки.ФайлСоглашения);
	Иначе
		ПараметрыФормы.Вставить("Номер", СледующийНомерСоглашения());
		ПараметрыФормы.Вставить("Дата", ТекущаяДатаПользователя);
		ПараметрыФормы.Вставить("Комментарий", КомментарийДополнительногоСоглашенияПередЗаписью);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаДополнительногоСоглашения", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция СледующийНомерСоглашения()
	
	ДоговорОбъект = РеквизитФормыВЗначение("Объект");
	НомерСоглашения = Справочники.ДоговорыКонтрагентов.СледующийНомерДополнительногоСоглашения(ДоговорОбъект);
	
	Возврат НомерСоглашения;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораДополнительногоСоглашения(ЗначенияДополнительногоСоглашения)
	
	Если ЗначенияДополнительногоСоглашения.НомерСтроки = Неопределено Тогда
		СтрокаСоглашения = Объект.ДополнительныеСоглашения.Вставить(0);
		ЗаполнитьЗначенияСвойств(СтрокаСоглашения, ЗначенияДополнительногоСоглашения);
	Иначе
		СтрокаСоглашения = Объект.ДополнительныеСоглашения.НайтиПоИдентификатору(ЗначенияДополнительногоСоглашения.НомерСтроки);
		ЗаполнитьЗначенияСвойств(СтрокаСоглашения, ЗначенияДополнительногоСоглашения);
	КонецЕсли;
	
	СтрокаСоглашения.ЕстьФайлы = ?(ЗначениеЗаполнено(СтрокаСоглашения.ФайлСоглашения), 0, 1);
	
	Элементы.ДополнительныеСоглашения.ТекущаяСтрока = СтрокаСоглашения.ПолучитьИдентификатор();
	
	Если Элементы.ГруппаДополнительныеСоглашения.Скрыта() Тогда
		Элементы.ГруппаДополнительныеСоглашения.Показать();
	КонецЕсли;
	
	ОтменитьДобавлениеСоглашенияПриИзмененииРеквизита();
	
	УправлениеФормой(ЭтотОбъект);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДобавитьСоглашениеПриИзмененииРеквизита(ИзменяемыйРеквизит)
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеОбъекта = Объект[ИзменяемыйРеквизит];
	Если Не ЗначениеЗаполнено(ЗначениеОбъекта) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеСсылки = ЗначениеРеквизитаДляДобавленияСоглашения(Объект.Ссылка, ИзменяемыйРеквизит);
	
	ИзменилосьЗначение = ЗначениеЗаполнено(ЗначениеСсылки)
		И ЗначениеОбъекта <> ЗначениеСсылки;
	
	ИзмениласьСумма = ИзменилосьЗначение И ИзменяемыйРеквизит = ИмяРеквизитаСумма();
	ИзменилсяСрокДействия = ИзменилосьЗначение И ИзменяемыйРеквизит = ИмяРеквизитаСрокДействия();
		
	Если ИзмениласьСумма Тогда
		КомментарийДополнительногоСоглашения = СтрШаблон(НСтр("ru = 'Изменение суммы с %1 на %2'"),
			ЗначениеСсылки,
			ЗначениеОбъекта);
		
	ИначеЕсли ИзменилсяСрокДействия Тогда
		КомментарийДополнительногоСоглашения = СтрШаблон(НСтр("ru = 'Изменение срока действия с %1 на %2'"),
			Формат(ЗначениеСсылки, "ДФ=dd.MM.yyyy"),
			Формат(ЗначениеОбъекта, "ДФ=dd.MM.yyyy"));
		
	Иначе
		КомментарийДополнительногоСоглашения = "";
	КонецЕсли;
	
	Элементы.ГруппаДобавитьСоглашениеСумма.Видимость = ИзмениласьСумма;
	Элементы.ГруппаДобавитьСоглашениеСрокДействия.Видимость = ИзменилсяСрокДействия;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьДобавлениеСоглашенияПриИзмененииРеквизита()
	
	КомментарийДополнительногоСоглашения = "";
	Элементы.ГруппаДобавитьСоглашениеСумма.Видимость = Ложь;
	Элементы.ГруппаДобавитьСоглашениеСрокДействия.Видимость = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеРеквизитаДляДобавленияСоглашения(ДоговорСсылка, ИмяРеквизита)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорСсылка, ИмяРеквизита);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРеквизитаСумма()
	
	Возврат "Сумма";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРеквизитаСрокДействия()
	
	Возврат "СрокДействия";
	
КонецФункции

#КонецОбласти

#Область ПроцедурыПодсистемыСвойств

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()

	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, РеквизитФормыВЗначение("Объект"));

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ПредусмотреноОбеспечениеОбязательств Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ВидОбеспеченияОбязательствИПлатежей) Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
				НСтр("ru='Вид обеспечения'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ВидОбеспеченияОбязательствИПлатежей", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаСервереБезКонтекста
Функция РеквизитыСклоненияРуководителя(Руководитель)
	
	РеквизитыСклонения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Руководитель, "ФИО,Пол");
	
	РеквизитыСклонения.Пол = ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(РеквизитыСклонения.Пол);
	
	РеквизитыСклонения.Вставить("ТолькоПросмотр", Не ПравоНаРедактирование(Истина));
	
	Возврат РеквизитыСклонения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПравоНаРедактирование(ЭтоФИО)
	
	ПроверяемыйСправочник = ?(ЭтоФИО, Метаданные.Справочники.ФизическиеЛица, Метаданные.Справочники.Должности);
	Возврат ПравоДоступа("Редактирование", ПроверяемыйСправочник);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьВРегистрСклонения(ОбъектСоСклоняемымиРеквизитами, Представление, СтруктураСклонения)
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	ХешПредставления = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(Представление);
	НаборЗаписейСклонения = РегистрыСведений.СклоненияПредставленийОбъектов.СоздатьНаборЗаписей();
	
	НаборЗаписейСклонения.Отбор.ХешПредставления.Установить(ХешПредставления);
	Если Не ЗначениеЗаполнено(ОбъектСоСклоняемымиРеквизитами) Тогда
		// Может быть несколько склонений представлений, относимых к одному пустому значению.
		НаборЗаписейСклонения.Отбор.Объект.Установить(ОбъектСоСклоняемымиРеквизитами);
	КонецЕсли;
	
	НоваяСтрока = НаборЗаписейСклонения.Добавить();
	НоваяСтрока.Объект           = ОбъектСоСклоняемымиРеквизитами;
	НоваяСтрока.ХешПредставления = ХешПредставления;
	Для каждого Падеж Из СтруктураСклонения Цикл
		НоваяСтрока[Падеж.Ключ + "Падеж"] = Падеж.Значение;
	КонецЦикла; 
	НаборЗаписейСклонения.Записать();
	
КонецПроцедуры

// Завершение процедуры ПоказатьСклонение.
// Вызывается для реквизитов, чьи склонения привязаны к объектам вне договора: ФИО и должности руководителя организации.
//
&НаКлиенте
Процедура ОткрытьФормуСклоненияЗавершение(РезультатРедактирования, ДополнительныеПараметры) Экспорт

	Если РезультатРедактирования <> Неопределено
	   И РезультатРедактирования.Именительный = ДополнительныеПараметры.Представление
	   И Не ДополнительныеПараметры.Свойство("ЗапретРедактирования") Тогда
	   
		СтруктураСклонения = Новый ФиксированнаяСтруктура(РезультатРедактирования);
		Если ТекущийРеквизитСклонения = "СклоненияДолжностьРуководителяКонтрагента"
		 Или ТекущийРеквизитСклонения = "СклоненияРуководительКонтрагента" Тогда // только получаем результат
		 
			ЭтотОбъект[ТекущийРеквизитСклонения] = СтруктураСклонения;
			ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = Истина;
			Модифицированность = Истина;
		 
	 	Иначе // получаем результат и фиксируем в регистре

			ЗаписатьВРегистрСклонения(
				Объект[ТекущийРеквизитСклонения],
				ДополнительныеПараметры.Представление,
				СтруктураСклонения);
				
		КонецЕсли;
		
	КонецЕсли;	
	
	// Закончили редактирование объекта склонения внешнего по отношению к данной форме.
	// Возвращаемся к редактированию склонений реквизита формы.
	ТекущийРеквизитСклонения         = "СклоненияРуководительКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПредставленияРуководителяКонтрагента()
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	
	// Пытаемся определить пол.
	ПредполагаемыйПол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.ПустаяСсылка");
	ОрганизацииФормыКлиентСервер.ОпределитьПолПоОтчеству(ПредполагаемыйПол, СокрП(Объект.РуководительКонтрагента));
	Если ЗначениеЗаполнено(ПредполагаемыйПол) Тогда
		Объект.ПолРуководителяКонтрагента = ПредполагаемыйПол;
	КонецЕсли;
	
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	ПараметрыСклонения.ЭтоФИО = Истина;
	ПараметрыСклонения.Пол = ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(Объект.ПолРуководителяКонтрагента);
	ОповещениеОРезультатеСклонения = Новый ОписаниеОповещения(
		"РуководительКонтрагентаПриИзмененииЗавершение", ЭтотОбъект);
	СклонениеПредставленийОбъектовКлиент.НачатьСклонение(
		ЭтотОбъект, Объект.РуководительКонтрагента, ПараметрыСклонения, Истина, ОповещениеОРезультатеСклонения);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервереСклоненийРеквизитов(ТекущийОбъект)
	
	Попытка
	
		ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
		ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
		ОбъектСоСклоняемымиРеквизитами = Справочники.ДоговорыКонтрагентов.ПустаяСсылка(); // любой договор
		
		Если ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента
		   И ТипЗнч(ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента) = Тип("ФиксированнаяСтруктура") Тогда

			ЗаписатьВРегистрСклонения(
				ОбъектСоСклоняемымиРеквизитами,
				ТекущийОбъект.ДолжностьРуководителяКонтрагента,
				ЭтотОбъект.СклоненияДолжностьРуководителяКонтрагента);
			ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента = Ложь;

		КонецЕсли;
		
		Если ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента
		   И ТипЗнч(ЭтотОбъект.СклоненияРуководительКонтрагента) = Тип("ФиксированнаяСтруктура") Тогда

			ЗаписатьВРегистрСклонения(
				ОбъектСоСклоняемымиРеквизитами,
				ТекущийОбъект.РуководительКонтрагента,
				ЭтотОбъект.СклоненияРуководительКонтрагента);
			ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента = Ложь;

		КонецЕсли;

		ЭтотОбъект.Склонения = ЭтотОбъект[ТекущийРеквизитСклонения];
		ЭтотОбъект.ИзмененоПредставление = Ложь;
	
	Исключение
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось произвести склонение ФИО и/или должности руководителя контрагента в договоре: %1 по причине:
			|%2'"),
			Объект.Ссылка,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Склонение ФИО и/или должности руководителя контрагента'"),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ДоговорыКонтрагентов,
			Объект.Ссылка,
			ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервереГосконтрактЕИС(ТекущийОбъект)
	
	Попытка
		
		СтарыйГосударственныйКонтрактЕИС = ЭлектронноеАктированиеЕИС.НайтиГосударственныйКонтрактПоВладельцу(ТекущийОбъект.Ссылка);
		
		Если ЗначениеЗаполнено(ГосударственныйКонтрактЕИС)
			ИЛИ ЗначениеЗаполнено(СтарыйГосударственныйКонтрактЕИС) Тогда
			
			Если ЗначениеЗаполнено(СтарыйГосударственныйКонтрактЕИС)
				И СтарыйГосударственныйКонтрактЕИС <> ГосударственныйКонтрактЕИС Тогда
				
				ОбъектСтарыйГосконтрактЕИС = СтарыйГосударственныйКонтрактЕИС.ПолучитьОбъект();
				ОбъектСтарыйГосконтрактЕИС.ВладелецКонтракта = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
				ОбъектСтарыйГосконтрактЕИС.Записать();
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ГосударственныйКонтрактЕИС)
				И ГосударственныйКонтрактЕИС <> СтарыйГосударственныйКонтрактЕИС Тогда
				
				ОбъектГосконтрактЕИС = ГосударственныйКонтрактЕИС.ПолучитьОбъект();
				ОбъектГосконтрактЕИС.ВладелецКонтракта = ТекущийОбъект.Ссылка;
				ОбъектГосконтрактЕИС.Записать();
			КонецЕсли;
			
		КонецЕсли;
	
	Исключение
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось связать договор: %1 с госконтрактом ЕИС: %2 по причине:
			|%3'"),
			Объект.Ссылка,
			ГосударственныйКонтрактЕИС,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Склонение ФИО и/или должности руководителя контрагента'"),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ДоговорыКонтрагентов,
			Объект.Ссылка,
			ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

// Обработчик команды "Склонения" формы объекта склонения.
//
// Параметры:
//  Представление   - Строка - Строка для склонения.
//  ЗапретРедактирования - Булево - форма склонений открывается в режиме ТолькоПросмотр.
//  ПараметрыСклонения - Структура - см. СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения() 
//
&НаКлиенте
Процедура ПоказатьСклонение(Представление, ЗапретРедактирования, ПараметрыСклонения)
	
	СтруктураСклонения = ПросклонятьПредставлениеПоВсемПадежам(Представление, ПараметрыСклонения);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Представление", Представление);
	Если ЗапретРедактирования Тогда
		ДополнительныеПараметры.Вставить("ЗапретРедактирования", Истина);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуСклоненияЗавершение", ЭтотОбъект, ДополнительныеПараметры);

	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Склонения",             СтруктураСклонения);
	СтруктураПараметров.Вставить("Представление",         Представление);
	СтруктураПараметров.Вставить("ПараметрыСклонения",    ПараметрыСклонения);
	СтруктураПараметров.Вставить("ИзмененоПредставление", Ложь);
	
	Если ЗапретРедактирования Тогда
		СтруктураПараметров.Вставить("ТолькоПросмотр", Истина);
	КонецЕсли;
		
	ОткрытьФорму("ОбщаяФорма.Склонения", СтруктураПараметров, ЭтотОбъект, , , , Оповещение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПросклонятьПредставлениеПоВсемПадежам(Знач Представление, Знач ПараметрыСклонения)
	
	Возврат СклонениеПредставленийОбъектов.ДанныеСклонения(Представление, ПараметрыСклонения, Истина);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

// СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаКлиенте
Процедура ПоказатьСклоненияРуководительКонтрагента(Команда)
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	
	ТекущийРеквизитСклонения         = "СклоненияРуководительКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеРуководительКонтрагента;
	Если ЭтотОбъект.ИзмененоПредставление Тогда
		ЗаписатьВРегистрСклонения(
			ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"),
			Объект.РуководительКонтрагента,
			ЭтотОбъект.Склонения);
	КонецЕсли;
	
	ЗапретРедактирования = Не ПравоНаРедактирование(Ложь);
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	ПараметрыСклонения.ЭтоФИО = Истина;
	ПараметрыСклонения.Пол = ПечатьДоговоровКлиентСервер.ПорядковыйНомерПола(Объект.ПолРуководителяКонтрагента);
	ПоказатьСклонение(Объект.РуководительКонтрагента, ЗапретРедактирования, ПараметрыСклонения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСклоненияДолжностьРуководителяКонтрагента(Команда)
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	
	ТекущийРеквизитСклонения         = "СклоненияДолжностьРуководителяКонтрагента";
	ЭтотОбъект.Склонения             = ЭтотОбъект[ТекущийРеквизитСклонения];
	ЭтотОбъект.ИзмененоПредставление = ЭтотОбъект.ИзмененоПредставлениеДолжностьРуководителяКонтрагента;
	Если ЭтотОбъект.ИзмененоПредставление Тогда
		ЗаписатьВРегистрСклонения(
			ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"),
			Объект.ДолжностьРуководителяКонтрагента,
			ЭтотОбъект.Склонения);
	КонецЕсли;
	
	ЗапретРедактирования = Не ПравоНаРедактирование(Ложь);
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	ПоказатьСклонение(Объект.ДолжностьРуководителяКонтрагента, ЗапретРедактирования, ПараметрыСклонения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСклоненияРуководитель(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Руководитель) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	ТекущийРеквизитСклонения = "Руководитель";
	
	РеквизитыСклонения = РеквизитыСклоненияРуководителя(Объект.Руководитель);
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	ПараметрыСклонения.ЭтоФИО = Истина;
	ПараметрыСклонения.Пол    = РеквизитыСклонения.Пол;
	
	ПоказатьСклонение(РеквизитыСклонения.ФИО, РеквизитыСклонения.ТолькоПросмотр, ПараметрыСклонения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСклоненияДолжностьРуководителя(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ДолжностьРуководителя) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект[ТекущийРеквизитСклонения] = ЭтотОбъект.Склонения;
	ЭтотОбъект["ИзмененоПредставление" + Сред(ТекущийРеквизитСклонения, 10)] = ЭтотОбъект.ИзмененоПредставление;
	ТекущийРеквизитСклонения = "ДолжностьРуководителя";
	
	ПредставлениеОбъекта = Строка(Объект.ДолжностьРуководителя);
	ЗапретРедактирования = Не ПравоНаРедактирование(Ложь);
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();

	ПоказатьСклонение(ПредставлениеОбъекта, ЗапретРедактирования, ПараметрыСклонения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьАвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьОкончаниеВводаТекста(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГосударственныйКонтрактЕИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураПараметров  = Новый Структура;
	Отборы               = Новый Структура;
	МассивВладельцев     = Новый СписокЗначений;
	МассивВладельцев.Добавить(Объект.Ссылка);
	МассивВладельцев.Добавить(ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
		
	Отборы.Вставить("ВладелецКонтракта", МассивВладельцев);
	Отборы.Вставить("Организация", Объект.Организация);
	Отборы.Вставить("Контрагент", Объект.Владелец);
	
	СтруктураПараметров.Вставить("Ключ", ГосударственныйКонтрактЕИС);
	СтруктураПараметров.Вставить("Отбор", Отборы);
	СтруктураПараметров.Вставить("Организация", Объект.Организация);
	
	ОткрытьФорму("Справочник.ГосударственныеКонтрактыЕИС.ФормаВыбора", СтруктураПараметров, Элемент);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура("ЗакрытьПослеЗаписи", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНомерДоговора(Команда)
	
	УстановитьНомерДоговораСПокупателем();
	СформироватьНаименованиеДоговора(ЭтотОбъект);
	ПроверитьДублиДоговораСПокупателем(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПечатьДоговоровКлиент.ВыполнитьОткрытиеТекстаДоговора(Объект.Ссылка, ЭтотОбъект, Ложь, Истина);
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет И ДополнительныеПараметры.ЕстьФайлДоговора Тогда
		
		СообщенияОбОшибках = ПечатьДоговоровВызовСервера.СообщенияЗаполненияОбязательныхРеквизитов(Объект.Ссылка);
		Если СообщенияОбОшибках.Количество() <> 0 Тогда
							
			ПечатьДоговоровКлиент.ОткрытьФормуНавигацииПоОшибкамВыгрузки(СообщенияОбОшибках);
			Возврат;
			
		КонецЕсли;

		ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(ДополнительныеПараметры.КомандаЭДО, ЭтотОбъект, Объект);
		
	КонецЕсли;
		
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЕстьРабочийЭДД = ПечатьДоговоровВызовСервера.ЕстьЭлектронныйДоговорныйДокумент(Объект.Ссылка);
	
	ЯвляетсяКомандойФормированияЭД = СтрНайти(Команда.Имя, "СформироватьПодписатьОтправитьЭД") <> 0 Или
		СтрНайти(Команда.Имя, "ОткрытьАктуальныйЭД") <> 0;
		
	// Для ограниченных прав
	Если Не ЕстьПравоРедактирования() Тогда
			
		Если ЕстьРабочийЭДД Или Не ЯвляетсяКомандойФормированияЭД Тогда     
			ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
		ИначеЕсли Не ЕстьРабочийЭДД Тогда  
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нет прав для формирования электронного документа.'"));
		КонецЕсли;
	
		Возврат;
		
	КонецЕсли;
		
	// Нет сохраненного ЭД и его необходимо создать
	Если НЕ ЕстьРабочийЭДД И ЯвляетсяКомандойФормированияЭД Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) ИЛИ Модифицированность Тогда
			// Выполняем запись договора перед формированием электронного документа
			Записать();
		КонецЕсли; 
		
		ЕстьФайлДоговора = ЗначениеЗаполнено(Объект.ФайлДоговора);
		Если ЕстьФайлДоговора И Объект.ЭлектронныйФормат Тогда
			
			СообщенияОбОшибках = ПечатьДоговоровВызовСервера.СообщенияЗаполненияОбязательныхРеквизитов(Объект.Ссылка);
			
			Если СообщенияОбОшибках.Количество() <> 0 Тогда
							
				ПечатьДоговоровКлиент.ОткрытьФормуНавигацииПоОшибкамВыгрузки(СообщенияОбОшибках);
				Возврат;
				
			КонецЕсли;

			ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
				
		Иначе
				
			ДополнительныеПараметры = Новый Структура("ЕстьФайлДоговора, КомандаЭДО", ЕстьФайлДоговора, Команда);
			
			Оповещение = Новый ОписаниеОповещения("ВопросЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПараметрВопроса = ?(ЕстьФайлДоговора, НСтр("ru = 'содержит только'"), НСтр("ru = 'не содержит'"));

			ТекстВопроса = НСтр("ru = 'Сохраненный файл %1 текст.
								|Сформировать договор для выгрузки в электронном формате?'");
			ТекстВопроса = СтрШаблон(ТекстВопроса, ПараметрВопроса);
			
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
				
		КонецЕсли;
					
	Иначе
		
		ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
		
	КонецЕсли;
				
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура ДекорацияСостояниеЭДОНажатие(Элемент)
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаСправочника(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыМК = "ФормаКомандаЗаписатьИЗакрыть,ФормаЗаписать";
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект, КомандыМК);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Этотобъект.КоманднаяПанель.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ПодменюПечать.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	Элементы.ГруппаШапкаЛевая.Объединенная = Истина;
	Элементы.ГруппаШапкаПравая.Объединенная = Истина;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаШапкаПравая);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаШапкаЛевая);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаРасчеты);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаБухгалтерскийУчет);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаНДС);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаГосударственныйКонтракт);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаКомиссионноеВознаграждение);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПлатежныйАгент);
	Элементы.ГруппаПодписи.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПодписи);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаОбеспеченияОбязательствИПлатежей);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаДополнительнаяИнформация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаДополнительныеСоглашения);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.ГруппаДополнительныеРеквизиты);
	
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.Родитель);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.ГруппаСостояниеЭДО);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.Комментарий);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.Подписан);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ДоговорЗакрыт);
	
	МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(ЭтотОбъект, "ГруппаНомерДатаДополнительныеСоглашения", 
		ГруппировкаКолонок.ВЯчейке, Элементы.ДополнительныеСоглашения);
	
	Элементы.Переместить(Элементы.ДополнительныеСоглашенияНомер, Элементы.ГруппаНомерДатаДополнительныеСоглашения);
	Элементы.Переместить(Элементы.ДополнительныеСоглашенияДата, Элементы.ГруппаНомерДатаДополнительныеСоглашения);
	Элементы.Переместить(Элементы.ДополнительныеСоглашенияЕстьФайлы, Элементы.ГруппаНомерДатаДополнительныеСоглашения);
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыДобавитьСтроку = "ДополнительныеСоглашенияДобавить";
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаДополнительныеСоглашения;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.ВажныеКолонки = "ДополнительныеСоглашенияНомер,ДополнительныеСоглашенияДата,ДополнительныеСоглашенияЕстьФайлы";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
		Элементы.ДополнительныеСоглашения, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.ДополнительныеСоглашенияНомер.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	
	Элементы.ДополнительныеСоглашенияПодписано.Вид = ВидПоляФормы.ПолеВвода;
	Элементы.ДополнительныеСоглашенияПодписано.Формат = "БЛ='Не подписано'; БИ=Подписано";
	
	Элементы.ДополнительныеСоглашения.Ширина = 0;
	Элементы.ДополнительныеСоглашения.РастягиватьПоГоризонтали = Истина;
	
	Для каждого Элемент Из Элементы.ДополнительныеСоглашения.ПодчиненныеЭлементы Цикл
		Если Элемент.Вид = ВидПоляФормы.ПолеВвода Тогда
			Элемент.Ширина = 0;
			Элемент.РастягиватьПоГоризонтали = Истина;
		КонецЕсли;
	КонецЦикла;
	
	//Защитим группы от сворачивания по важности
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементыВГруппаМКФормаПодвал(Элементы, ПодчиненныеЭлементы);
	
	Элементы.ДоговорЗакрыт.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСлева;
	Элементы.Подписан.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСлева;
	
	ИсключеноИзАдаптации = "ГруппаСумма,СуммаВключаетНДС,ГруппаСуммаНДС,Всего"
	 + ",ОплатаВРублях,ТипЦен,ВидДоходаИностранногоКонтрагента,ГруппаВалютныйКонтроль"
	 + ",ГруппаПлатежныйАгент,ГруппаОбеспеченияОбязательствИПлатежей";
	МобильныйКлиентБПФормаОбъекта.УстановитьИсключенияИзАдаптации(ЭтотОбъект, ИсключеноИзАдаптации);
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИННКонтрагентаНаСервере(Контрагент)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ИНН");
	
КонецФункции

&НаСервере
Процедура НастроитьПоложениеПодсказкиНалогообложенияКонтрагента(ОбновитьВидимостьЭлемента = Ложь)
	
	// Для обхода ситуации, когда веб-браузеры не обновляют изображение, из-за того что картинка закэширована
	Если ОбновитьВидимостьЭлемента Тогда
		Элементы.НалогообложениеКонтрагента.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	Если Не РаботаСДоговорамиКонтрагентовБПВызовСервера.ВестиУчетПоДоговорам() Тогда
		Элементы.НалогообложениеКонтрагента.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе
		Элементы.НалогообложениеКонтрагента.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

#Область ИсторияПроцентныхСтавок

&НаСервере
Процедура УстановитьПроцентнуюСтавкуПослеРедактированияИстории(НаборЗаписей)
	
	Модифицированность = Истина;
	
	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияПроцентныхСтавок.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьИстории = Объект.ИсторияПроцентныхСтавок.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
			ЗаписьИстории.УдалитьПериодСкорректирован = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Объект.ПроцентнаяСтавка = НаборЗаписей[НаборЗаписей.Количество()-1].ПроцентнаяСтавка;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
