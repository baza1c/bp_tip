#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция проверяет, существуют ли ссылки номенклатуру в движениях регистров накопления.
// Если есть - нельзя менять признак "Услуга"
//
// Параметры:
//  СуществуютСсылки - булево, переменная, в которой сохраняется результат работы функции, чтобы
//                     при последующих вызовах заново не считать функцию.
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|ГДЕ
	|	ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|	И ХозрасчетныйСубконто.Значение = &Номенклатура"; 
	
	СтатусВозврата = НЕ Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СтатусВозврата;
	
КонецФункции

// Функция проверяет, существуют ли ссылки номенклатуру в справочнике "КонтрольныеЗнакиГИСМ".
// Если есть - нельзя менять признак "ПродукцияМаркируемаяДляГИСМ"
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылкиНаКонтрольныеЗнакиГИСМ()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтрольныеЗнакиГИСМ.Владелец
	|ИЗ
	|	Справочник.КонтрольныеЗнакиГИСМ КАК КонтрольныеЗнакиГИСМ
	|ГДЕ
	|	КонтрольныеЗнакиГИСМ.Владелец = &Номенклатура"; 
	
	СтатусВозврата = НЕ Запрос.Выполнить().Пустой();
	
	Возврат СтатусВозврата;
	
КонецФункции

// Функция проверяет, существуют ли ссылки номенклатуру в регистре "СоответствиеНоменклатурыВЕТИС".
// Если есть - нельзя менять признак "ПодконтрольнаяПродукцияВЕТИС".
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылкиНаПодконтрольнуюПродукциюВЕТИС()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СоответствиеНоменклатурыВЕТИС.Номенклатура
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|ГДЕ
	|	СоответствиеНоменклатурыВЕТИС.Номенклатура = &Номенклатура"; 
	
	СтатусВозврата = НЕ Запрос.Выполнить().Пустой();
	
	Возврат СтатусВозврата;
	
КонецФункции

Процедура ОбработатьИсториюРеквизитовПередЗаписью()

	// Если запись в истории одна, то считается, что изменений нет,
	// нужно определять значение из данных объекта, а таблицу истории очистить.
	// Если записей в истории более 1, последняя запись всегда должна соответствовать данным объекта,
	// а первая запись должна иметь пустую дату.
	
	Если ИсторияВидаСтавкиНДС.Количество() = 1 Тогда
		ИсторияВидаСтавкиНДС.Очистить();
	ИначеЕсли ИсторияВидаСтавкиНДС.Количество() > 1 Тогда
		Справочники.Номенклатура.УстановитьАктуальноеЗначениеИсторииВидаСтавкиНДС(ВидСтавкиНДС, ИсторияВидаСтавкиНДС);
		ИсторияВидаСтавкиНДС[0].Период = '00010101';
		ВсеПериоды = ОбщегоНазначения.ВыгрузитьКолонку(ИсторияВидаСтавкиНДС, "Период", Истина);
		Если ВсеПериоды.Количество() <> ИсторияВидаСтавкиНДС.Количество() Тогда
			ТекстСообщения = НСтр("ru='В истории ставок НДС есть записи с одинаковым периодом.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьИсториюРеквизитовПередЗаписью();
	
	ДополнительныеСвойства.Вставить("ОчиститьСопоставлениеНомеклатуры",  Ложь);
	
	Если НЕ ЭтоГруппа 
		И НЕ ЭтоНовый() Тогда
		
		Если ВАрхиве И ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеНоменклатуры") Тогда
			ДополнительныеСвойства.Вставить("ОчиститьСопоставлениеНомеклатуры", НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ВАрхиве"));
		КонецЕсли;
		
		РеквизитыНоменклатурыИзИБ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"Услуга, ВидМаркировки");
		
		Если Услуга <> РеквизитыНоменклатурыИзИБ.Услуга 
			И СуществуютСсылки() Тогда
		
			ТекстСообщения = НСтр("ru = 'Номенклатура ""%1"" участвует в товародвижении.
				|Признак услуги не может быть изменен!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СокрЛП(Наименование));
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", 
				НСтр("ru = 'Услуга'"),,, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, 
				"Услуга", "Объект", Отказ);
			
		КонецЕсли;
		
		Если (РеквизитыНоменклатурыИзИБ.ВидМаркировки = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха)
			И (РеквизитыНоменклатурыИзИБ.ВидМаркировки <> ВидМаркировки)
			И СуществуютСсылкиНаКонтрольныеЗнакиГИСМ() Тогда
		
			ТекстСообщения = НСтр("ru = 'Для номенклатуры ""%1"" введены контрольные знаки ГИСМ.
				|Признак ""Меховое изделие"" не может быть изменен!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СокрЛП(Наименование));
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", 
				НСтр("ru = 'Меховое изделие'"),,, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, 
				"ПродукцияМаркируемаяДляГИСМ", "Объект", Отказ);
			
		КонецЕсли;

		Если (РеквизитыНоменклатурыИзИБ.ВидМаркировки = Перечисления.ВидыПродукцииИС.ПодконтрольнаяПродукцияВЕТИС)
			И (РеквизитыНоменклатурыИзИБ.ВидМаркировки <> ВидМаркировки)
			И СуществуютСсылкиНаПодконтрольнуюПродукциюВЕТИС() Тогда
		
			ТекстСообщения = НСтр("ru = 'Номенклатура ""%1"" участвует в обмене с ФГИС ""Меркурий"".
				|Признак ""Ветеринарный контроль"" не может быть изменен!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СокрЛП(Наименование));
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", 
				НСтр("ru = 'Ветеринарный контроль'"),,, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, 
				"ПодконтрольнаяПродукцияВЕТИС", "Объект", Отказ);
			
		КонецЕсли;
		
	ИначеЕсли ЭтоНовый()
		И ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Малоценное оборудование и запасы") Тогда
		
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("Номенклатура.МалоценноеОборудование.Создание");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодВидаПодакцизногоТовара) И Не ЭтоГруппа Тогда
		// Коэффициент пересчета должен быть заполнен - но не заполнен
		Если Не ЗначениеЗаполнено(КоэффициентПересчета)
			И (ЗначениеЗаполнено(ЕдиницаИзмеренияДляРасчетаАкциза)
			И ЕдиницаИзмеренияДляРасчетаАкциза <> Неопределено
			И ЕдиницаИзмеренияДляРасчетаАкциза <> ЕдиницаИзмерения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Заполнение",
			НСтр("ru = 'коэффициент пересчета'"),,, ТекстСообщения);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, 
			"КоэффициентПересчета", "Объект", Отказ);
			// Коэффициент пересчета по умолчанию = 1
			// например, совпадают единицы измерений по товару и для расчета акциза
		ИначеЕсли Не ЗначениеЗаполнено(КоэффициентПересчета)
			И (ЕдиницаИзмеренияДляРасчетаАкциза = ЕдиницаИзмерения
			Или НЕ ЗначениеЗаполнено(ЕдиницаИзмеренияДляРасчетаАкциза)) Тогда
			КоэффициентПересчета = 1;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПризнакПредметаРасчета) Тогда
			Если Не ВидМаркировки = Перечисления.ВидыПродукцииИС.Табак
				Или Не ВидМаркировки = Перечисления.ВидыПродукцииИС.АльтернативныйТабак
				Или Не ВидМаркировки = Перечисления.ВидыПродукцииИС.НикотиносодержащаяПродукция
				Или Не ВидМаркировки = Перечисления.ВидыПродукцииИС.Пиво
				Или Не ВидМаркировки = Перечисления.ВидыПродукцииИС.БезалкогольноеПиво
				Или Не ВидМаркировки = Перечисления.ВидыПродукцииИС.СоковаяПродукция Тогда
					ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПодакцизныйТовар;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Не ЭтоГруппа Тогда
		КоэффициентПересчета = 0;
		ЕдиницаИзмеренияДляРасчетаАкциза = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ЭтоГруппа Тогда
		ОсновнаяСпецификацияНоменклатуры = "";
		ВАрхиве = Ложь;
		ИсторияПрослеживаемогоТовара.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Наименование") Тогда
			Наименование = ДанныеЗаполнения.Наименование;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("ВидНоменклатуры") Тогда
			ВидНоменклатуры = ДанныеЗаполнения.ВидНоменклатуры;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Родитель") И Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
			РеквизитыГруппы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.Родитель, "ВидНоменклатуры");
			ВидНоменклатуры = РеквизитыГруппы.ВидНоменклатуры;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		ОтборУслуга = (ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Услуга"));
		
		Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
			
			УслугаВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНоменклатуры, "Услуга");
			
			Если ОтборУслуга И УслугаВидНоменклатуры <> ДанныеЗаполнения.Услуга Тогда
				ВидНоменклатуры = Справочники.ВидыНоменклатуры.ПустаяСсылка();
				Услуга = ДанныеЗаполнения.Услуга;
			Иначе
				Услуга = УслугаВидНоменклатуры;
			КонецЕсли;
			
		КонецЕсли;
		
		//Если вид номенклатуры не заполнен (в т.ч. если вид номенклатуры очищен, как не соотвествующий отбору)
		Если НЕ ЗначениеЗаполнено(ВидНоменклатуры) Тогда
			
			Запрос = Новый Запрос();
			Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 2
			|	ВидыНоменклатуры.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
			|ГДЕ
			|	НЕ ВидыНоменклатуры.ПометкаУдаления";
			
			Если ОтборУслуга Тогда
				Текст = Текст + "
								|	И ВидыНоменклатуры.Услуга = &Услуга";
				Запрос.УстановитьПараметр("Услуга", ДанныеЗаполнения.Услуга);
			ИначеЕсли РаботаСНоменклатуройБП.УслугаПриоритетныйВидНоменклатуры() Тогда
				Текст = Текст + "
								|	И ВидыНоменклатуры.Услуга";
			КонецЕсли;
			Запрос.Текст = Текст;
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
				ВидНоменклатуры = Выборка.Ссылка;
				УслугаВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНоменклатуры, "Услуга");
				Услуга = УслугаВидНоменклатуры;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ЕдиницаИзмерения") Тогда
				ЕдиницаИзмерения = ДанныеЗаполнения.ЕдиницаИзмерения;
			Иначе
				ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ПолучитьЕдиницуИзмеренияПоУмолчанию();
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
			НоменклатурнаяГруппа = БухгалтерскийУчетВызовСервераПовтИсп.ОсновнаяНоменклатурнаяГруппа();
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("СтавкаНДС") Тогда
			Если Не ЗначениеЗаполнено(ВидСтавкиНДС) Тогда
				ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.ВидСтавки(ДанныеЗаполнения.СтавкаНДС);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтранаПроисхождения) Тогда
			СтранаПроисхождения = Справочники.Номенклатура.ОсновнаяСтранаПроисхожденияТоваров();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЭтоГруппа Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидНоменклатуры");
	КонецЕсли;
	
	Если ЭтоГруппа ИЛИ Услуга Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмерения");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ВАрхиве Тогда
		УстановитьПривилегированныйРежим(Истина);
		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеНоменклатуры") Тогда
			Константы.ИспользоватьСкрытиеНоменклатуры.Установить(Истина);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ДополнительныеСвойства.ОчиститьСопоставлениеНомеклатуры Тогда
			ОчиститьСопоставленияНоменклатураВАрхиве();
		КонецЕсли;
	
	КонецЕсли;
	
	
	Если ОбщегоНазначенияБПВызовСервера.ЕстьПравоЗаписыватьНастройки() Тогда
		Если Услуга Тогда
			ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнойВидНоменклатурыУслуга", ВидНоменклатуры);
		Иначе
			ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнойВидНоменклатуры", ВидНоменклатуры);
		КонецЕсли;
	КонецЕсли;
	
	Если Услуга Тогда
		Справочники.Номенклатура.ПроверитьЗначениеОпцииИспользоватьПериодичностьУслуг(ПериодичностьУслуги);
	Иначе
		Если ВидМаркировки = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
			Справочники.КонтрольныеЗнакиГИСМ.ПроверитьЗначениеОпцииИспользоватьКонтрольныеЗнакиГИСМ();
		КонецЕсли;
		
		Если СредствоИндивидуальнойЗащиты Тогда
			Справочники.КлассификаторВидовНоменклатурыЧекаККТ.ПроверитьЗначениеОпцииИспользоватьПодконтрольныеТоварыККТ();
		КонецЕсли; 
	КонецЕсли;
	
	Штрихкоды = "";
	Если ДополнительныеСвойства.Свойство("Штрихкоды", Штрихкоды) Тогда
		ЗаписатьШтрихкоды(Штрихкоды);
		КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДляПроверки = Новый Структура;
	РеквизитыДляПроверки.Вставить("СоответствиеНоменклатурыЕГАИС",               "Номенклатура");
	РеквизитыДляПроверки.Вставить("КодыТоваровПодключаемогоОборудованияOffline", "Номенклатура");
	РеквизитыДляПроверки.Вставить("ШтрихкодыНоменклатуры",                       "Номенклатура");
	РеквизитыДляПроверки.Вставить("СопоставлениеНоменклатурыОФД",                "Номенклатура");
	РеквизитыДляПроверки.Вставить("НоменклатураМаркетплейсов",                   "Номенклатура");
	РеквизитыДляПроверки.Вставить("НоменклатураYclients",                        "Номенклатура");
	РеквизитыДляПроверки.Вставить("НоменклатураИнтернетМагазина",                "Номенклатура");
	
	Запросы = Новый Массив;
	Для каждого Регистр Из РеквизитыДляПроверки Цикл
		МетаданныеРегистра = Метаданные.РегистрыСведений[Регистр.Ключ];
		
		СписокИзмерений = Новый Массив;
		Для каждого Измерение Из МетаданныеРегистра.Измерения Цикл
			СписокИзмерений.Добавить(СтрШаблон("%1.%2 КАК %2", Регистр.Ключ, Измерение.Имя));
		КонецЦикла;
		
		Запросы.Добавить(
			СтрШаблон(
				"ВЫБРАТЬ
				|	%1 КАК ИмяРегистра,
				|	%2
				|ИЗ
				|	%3
				|ГДЕ
				|	%4",
				СтрШаблон("""%1""", Регистр.Ключ),
				СтрСоединить(СписокИзмерений, ","+Символы.ПС+Символы.Таб),
				СтрШаблон("РегистрСведений.%1 КАК %1", Регистр.Ключ),
				СтрШаблон("%1.%2 = &Номенклатура", Регистр.Ключ, Регистр.Значение)));
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(Запросы, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("Номенклатура", Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
	
		РезультатыЗапросов = Запрос.ВыполнитьПакет();
		Для каждого РезультатЗапроса Из РезультатыЗапросов Цикл
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				МенеджерЗаписи = РегистрыСведений[Выборка.ИмяРегистра].СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
				МенеджерЗаписи.Удалить();
			КонецЦикла;
		КонецЦикла;
		
		Справочники.Номенклатура.ОчиститьСопоставлениеНоменклатурыБЭД(Ссылка);
				
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
	
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

Процедура ЗаписатьШтрихкоды(Штрихкоды)

	МассивШтрихкодов = СтрРазделить(Штрихкоды, ",", Ложь);
	Для каждого Штрихкод Из МассивШтрихкодов Цикл
		МенеджерЗаписи              = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Номенклатура = Ссылка;
		МенеджерЗаписи.Штрихкод     = Штрихкод;
		
		МенеджерЗаписи.Записать();
	КонецЦикла;

КонецПроцедуры

Процедура ОчиститьСопоставленияНоменклатураВАрхиве()
	УстановитьПривилегированныйРежим(Истина);
	
	Справочники.Номенклатура.ОчиститьСопоставлениеНоменклатурыБЭД(Ссылка);
	Справочники.Номенклатура.ОчиститьСопоставлениеНоменклатурыОФД(Ссылка);
	Справочники.Номенклатура.ОчиститьСопоставлениеНоменклатурыМаркетплейсы(Ссылка);
	Справочники.Номенклатура.ОчиститьСопоставлениеНоменклатурыНоменклатурыИнтернетМагазина(Ссылка);
	
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

#КонецЕсли
