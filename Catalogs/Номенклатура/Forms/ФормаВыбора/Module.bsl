#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Номенклатура: '") + Параметры.Заголовок;
		
	КонецЕсли;
	
	Если Параметры.Свойство("НаименованиеПолное") И НЕ ПустаяСтрока(Параметры.НаименованиеПолное) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НаименованиеПолное", Параметры.НаименованиеПолное);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	(ВЫРАЗИТЬ(Номенклатура.НаименованиеПолное КАК СТРОКА(80))) = &НаименованиеПолное
		|	И НЕ Номенклатура.ВАрхиве";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Элементы.Список.ТекущаяСтрока = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ДопустимыеВидыПродукции") Тогда
	
		ГруппаОтбора = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
			Список.Отбор.Элементы, "ДопустимыеВидыПродукции",
			ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
		Для каждого ЭлементОтбора Из Параметры.ДопустимыеВидыПродукции Цикл
			ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
				ГруппаОтбора, ЭлементОтбора, Истина,
				Истина, ВидСравненияКомпоновкиДанных.Равно);
		КонецЦикла; 
	КонецЕсли;
	
	Если Параметры.Свойство("СписокНоменклатуры") Тогда
		
		ГруппаОтбора = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
			Список.Отбор.Элементы, "ОтборПоСпискуНоменклатуры",
			ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
				ГруппаОтбора, "Ссылка", Параметры.СписокНоменклатуры,
				Истина, ВидСравненияКомпоновкиДанных.ВСписке);
				
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеНоменклатуры") Тогда
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ВАрхиве", Ложь, Истина);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Справочник.Номенклатура",
		"ФормаВыбора",
		НСтр("ru='Новости: Номенклатура'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	Если ИмяСобытия = "ВключеноИспользованиеСкрытияНоменклатуры" Тогда
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ВАрхиве", Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСкрытые(Команда)
	ПоказыватьСкрытые = НЕ ПоказыватьСкрытые;
	Элементы.ФормаПоказыватьСкрытые.Пометка = ПоказыватьСкрытые;
	ПодключитьОбработчикОжидания("Подключаемый_ПоказыватьСкрытые", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ВАрхиве", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНоменклатурыВАрхиве);
	
	ВедетсяУчетНДСНаУСН = РегистрыСведений.НастройкиУчетаНДС.ВедетсяУчетНДСНаУСН();
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Для Каждого ВидСтавки Из Перечисления.ВидыСтавокНДС Цикл
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидСтавкиНДС");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.ВидСтавкиНДС", ВидСравненияКомпоновкиДанных.Равно, ВидСтавки);

		Если ВедетсяУчетНДСНаУСН Тогда 
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст",
				УчетНДСКлиентСервер.ТекстовоеПредставлениеСтавкиНДС(ВидСтавки, ТекущаяДатаПользователя));
		Иначе
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст",
				Строка(Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавки, ТекущаяДатаПользователя)));
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказыватьСкрытые()
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ВАрхиве", Ложь, НЕ ПоказыватьСкрытые);
КонецПроцедуры


// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти