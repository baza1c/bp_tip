#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КонецПериода = ОбщегоНазначения.ТекущаяДатаПользователя();
	НачалоПериода = ДобавитьМесяц(КонецПериода, -1);
	СоставСписка = 1;
	УстановитьПараметрыСписка();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоставСпискаПриИзменении(Элемент)
	
	УстановитьСоставВыборки();
	Элементы.СписокДвижений.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)

	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатВыбора, "НачалоПериода,КонецПериода");
	УстановитьПараметрыПериода();
	Элементы.СписокДвижений.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДвиженийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "Документ" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Документ);
	ИначеЕсли Поле.Имя = "Контрагент" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Контрагент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	УстановитьПараметрыПериода();
	
	Элементы.СписокДвижений.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	УстановитьПараметрыПериода();
	
	Элементы.СписокДвижений.Обновить();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыСписка()
	
	СписокДвижений.Параметры.УстановитьЗначениеПараметра("Номенклатура", Параметры.Номенклатура);
	СписокДвижений.Параметры.УстановитьЗначениеПараметра("СубконтоНоменклатура",
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	УстановитьСоставВыборки();

	УстановитьПараметрыПериода();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСоставВыборки()
	
	СписокДвижений.Параметры.УстановитьЗначениеПараметра("ПоказатьВсе", СоставСписка = 1);
	СписокДвижений.Параметры.УстановитьЗначениеПараметра("Покупка", СоставСписка = 2);
	СписокДвижений.Параметры.УстановитьЗначениеПараметра("Продажа", СоставСписка = 3);

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыПериода()
	
	СписокДвижений.Параметры.УстановитьЗначениеПараметра("НачалоПериода", НачалоПериода);
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		СписокДвижений.Параметры.УстановитьЗначениеПараметра("КонецПериода", КонецДня(КонецПериода));
	Иначе
		СписокДвижений.Параметры.УстановитьЗначениеПараметра("КонецПериода", КонецПериода);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьИсториюДвиженийНоменклатуры" Тогда
		Элементы.СписокДвижений.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Элементы.ГруппаПериод);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Элементы.СоставСписка);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Элементы.ГруппаДлительнаяОперация);
	Элементы.СоставСписка.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.СписокДвижений);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.РазрешеноИзменение = Ложь;
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормы(ЭтотОбъект, "СписокДвижений", 2);
	
	Элементы.Дата.РастягиватьПоГоризонтали = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.Дата);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СуммаДокумента(ЭтотОбъект, Элементы.Сумма);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Документ);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Контрагент, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Количество, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Цена, 2);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "СписокДвижений");
	
	Элементы.Количество.Формат = "ЧФ='Количество: Ч'";
	Элементы.Цена.Формат = "ЧФ='Цена: Ч'";
	Элементы.Сумма.Формат = "ЧФ='Сумма: Ч'";
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
