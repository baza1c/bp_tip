#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Форма спецификации открывается в разных режимах. Это зависит от места откуда была открыта форма спецификации:
	// - если форма была открыта из номенклатуры (по гиперссылке в карточке номенклатуры или из списка спецификаций номенклатуры),
	//   то форма откроется в режиме Блокировка владельца, чтобы было понятно, к какой продукции она относится.
	// - если форма была открыта из общего списка спецификации, то режим открытия будет Независимый. 
	//   Продукция, к которой она относится, отображается на форме.
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ФормироватьНаименованиеПолноеАвтоматически = УстановитьФлагФормироватьНаименованиеПолноеАвтоматически(Объект.НаименованиеПолное,Объект.Наименование);
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	
	Если Параметры.Ключ.Пустая() И ЗначениеЗаполнено(Параметры.ТекстЗаполнения) И ПустаяСтрока(Объект.НаименованиеПолное) Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	
	ИспользоватьСкрытиеНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеНоменклатуры");
	
	Если Параметры.Ключ.Пустая() 
		И Параметры.Свойство("ЗначенияЗаполнения") Тогда
		
		Штрихкоды = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.ЗначенияЗаполнения, "Штрихкоды");
		
		ПроверитьНоменклатуруВАрхиве(ЭтотОбъект);
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая()
		И Параметры.Свойство("АлкогольнаяПродукцияЕГАИС")
		И ЗначениеЗаполнено(Параметры.АлкогольнаяПродукцияЕГАИС) Тогда
		
		Объект.Наименование       = Параметры.НаименованиеНоменклатурыЕГАИС;
		Объект.НаименованиеПолное = Параметры.ПолноеНаименованиеНоменклатурыЕГАИС;
		
		Если ЗначениеЗаполнено(Параметры.ИмпортерЕГАИС) Тогда
			ИмпортерЕГАИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ИмпортерЕГАИС, "Контрагент");
			Если ТипЗнч(ИмпортерЕГАИС) = Тип("СправочникСсылка.Контрагенты") Тогда
				Объект.Импортер = ИмпортерЕГАИС;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(Параметры.ПроизводительЕГАИС) Тогда
			ПроизводительЕГАИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ПроизводительЕГАИС, "Контрагент");
			Если ТипЗнч(ПроизводительЕГАИС) = Тип("СправочникСсылка.Контрагенты") Тогда
				Объект.Производитель = ПроизводительЕГАИС;
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьСведенияОбАлкогольнойПродукции = Истина;
		Объект.ВидМаркировки = Перечисления.ВидыПродукцииИС.Алкогольная;
		Если ЗначениеЗаполнено(Параметры.ВидАлкогольнойПродукцииЕГАИС) Тогда
			ВидАлкогольнойПродукцииЕГАИС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ВидАлкогольнойПродукцииЕГАИС, "Код, Наименование, Маркируемый");
			КодВидаАлкогольнойПродукции = ВидАлкогольнойПродукцииЕГАИС.Код;
			НаименованиеВидаАлкогольнойПродукции = ВидАлкогольнойПродукцииЕГАИС.Наименование;
		КонецЕсли;
			
		КоэффПересчетаДал = Параметры.ОбъемЕГАИС;
				
	КонецЕсли;
	
	ОсновнаяСтавкаНДС = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяСтавкаНДС");
	Если Параметры.Ключ.Пустая() Тогда
		Объект.ВидСтавкиНДС = ?(ЗначениеЗаполнено(ОсновнаяСтавкаНДС),
		Перечисления.ВидыСтавокНДС.ВидСтавки(ОсновнаяСтавкаНДС),
		Объект.ВидСтавкиНДС);
	КонецЕсли;
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Услуга") Тогда
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Услуга", Параметры.ЗначенияЗаполнения.Услуга);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ВидНоменклатуры.ПараметрыВыбора = НовыеПараметры;
		
	КонецЕсли;
	
	Штрихкоды = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Штрихкоды");
	
	Если Объект.Услуга Тогда
		Объект.СтатьяЗатрат = Неопределено;
		ОсновнойВидНоменклатуры = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
			"ОсновнойВидНоменклатурыУслуга");
		Если Не ЗначениеЗаполнено(ОсновнойВидНоменклатуры) Тогда
			ОсновнойВидНоменклатуры = Справочники.ВидыНоменклатуры.ЭлементВидНоменклатурыПоУмолчанию(Истина);
		КонецЕсли;
	Иначе
		ОсновнойВидНоменклатуры = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
			"ОсновнойВидНоменклатуры");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
		ВидНоменклатурыПриИзмененииНаСервере();
	КонецЕсли;
	
	Элементы.СтатьяЗатрат.Видимость = ПравоДоступа("Чтение", Метаданные.Справочники.СтатьиЗатрат);
	
	ИспользуетсяПростойИнтерфейс = ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс();
	
	ИспользуютсяНесколькоСкладов = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет")
		И ВедетсяСкладскойУчет();
		
	ИнтеграцияИСМПБП.ЗаполнитьСписокВыбораВидовПродукцииИС(Элементы.ВидМаркировки.СписокВыбора);
	ЕстьДоступныеВидыМаркировки          = Элементы.ВидМаркировки.СписокВыбора.Количество() > 1;
	ВедетсяУчетПрослеживаемыхТоваров     = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров");
	ИспользоватьКомплектациюНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьКомплектациюНоменклатуры");
		
	ПрочитатьСрокиИспользованияСпецодежды();
	ПрочитатьСведенияОбАлкогольнойПродукции();
	
	РаздельныйУчетНДС = УчетНДСРаздельный.ЕстьУчетнаяПолитикаСРаздельнымУчетомНДС();
	КлассификацияНоменклатурПоОперациям0 = УчетНДС.ЕстьКлассификацияНоменклатурПоОперациям0();
	ВИсторииЕстьСтавкаБезНДС = Справочники.Номенклатура.ВИсторииЕстьСтавкаБезНДС(Объект.Ссылка);
	ЕстьИсторияВидовСтавокНДС = Справочники.Номенклатура.ЕстьИсторияВидовСтавокНДС(Объект.Ссылка);
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	ВидСтавкиНДСНаДату = Справочники.Номенклатура.ВидСтавкиНДСНаДату(Объект.Ссылка, ТекущаяДатаПользователя);

	ЗаполнитьАктуальнуюСтавкуНДС(ВидСтавкиНДСНаДату);
	
	Элементы.СкрыватьВСписках.Видимость = НЕ ПомеченныеНаУдалениеСервер.СкрыватьПомеченныеНаУдаление() 
		И Пользователи.РолиДоступны("СкрытиеНоменклатуры");
	
	ЗаполнитьПолеНаименованияКода("ОКВЭД");
	ЗаполнитьПолеНаименованияКода("ОКВЭД2");
	ЗаполнитьПолеНаименованияКода("ТНВЭД");
	ЗаполнитьПолеНаименованияКода("ОКП");
	ЗаполнитьПолеНаименованияКода("ОКПД2");
	ЗаполнитьПолеНаименованияКода("НоменклатурнойКлассификацииККТ");
	
	РегистрыСведений.СведенияОбАлкогольнойПродукции.СписокВидовПродукции(Элементы.КодВидаАлкогольнойПродукции.СписокВыбора);
	УстановитьУчетПодконтрольнойПродукции();
	
	Элементы.ГруппаЭлектронныеСертификаты.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуСертификатамиНСПК");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьФискальныйРегистратор") 
		ИЛИ НЕ Объект.ПризнакПредметаРасчета.Пустая() Тогда
		
		Элементы.ПризнакПредметаРасчета.Видимость = Истина;
		ПодготовитьСписокСведенияОПредметеРасчета();
	КонецЕсли;
	
	ПолучитьСвойстваВидаНоменклатуры();
	УправлениеФормой(ЭтаФорма);
	
	ЕстьУчетПодакцизныхТоваров = ПолучитьФункциональнуюОпцию("ИмпортПодакцизныхТоваров")
		Или ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров");
	
	Если ЕстьУчетПодакцизныхТоваров Тогда
		Если ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
			ВидТовара = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидНоменклатуры, "ВидПодакцизногоТовара");
			Если Объект.Ссылка.Пустая() Тогда
				УчетАкцизов.ЗаполнитьКодВидаПодакцизногоТовараПоУмолчанию(ВидТовара, Объект.КодВидаПодакцизногоТовара);
				Если ЗначениеЗаполнено(Объект.КодВидаПодакцизногоТовара) Тогда
					УчетАкцизовВызовСервера.ПолучитьЕдиницуИзмеренияПоСтавкеАкциза(Объект.КодВидаПодакцизногоТовара, Объект.ЕдиницаИзмеренияДляРасчетаАкциза);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		НастроитьВидимостьГруппыАкцизы();
		НастроитьВидимостьКоэффициентаПересчетаПоАкцизам();
	Иначе
		НастроитьВидимостьГруппыАкцизы();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Справочник.Номенклатура",
		"ФормаЭлемента",
		НСтр("ru='Новости: Номенклатура'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ЗаполнитьПодсказкуПечатиАртикула();
	
	Если Объект.Ссылка.Пустая() Тогда
		ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
		ВалютаЦены         = ВалютаРегламентированногоУчета;
		ВалютыЦеныБезВалют = ВалютаРегламентированногоУчета;
		
		ОтобразитьОсновнуюСпецификацию();
		
	КонецЕсли;
	
	НастройкиУчетаМСФО.НастроитьФормуОбъектаУчета(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	Перечисления.СтавкиНДС.ЗаполнитьСписокВыбораСтавокНДС(ЭтотОбъект, "СтавкаНДС");
	
	УточнитьПредставлениеСтавкиНДСПриУСН();
	
	// ЭлектронноеВзаимодействие.СопоставлениеНоменклатурыКонтрагентов
	СопоставлениеНоменклатурыКонтрагентов.ПриСозданииФормыНоменклатуры(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.СопоставлениеНоменклатурыКонтрагентов
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	НастроитьЭлементыУправленияРаботаСНоменклатурой();
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	//БгосИС 1.1.29.3
	Если Не Параметры.Ключ.Пустая() Тогда
		ВывестиОписаниеМаркируемогоТовараНаСервере();
	КонецЕсли;
	// 
	
	// Типы оборудования которые надо подключать при открытии формы
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	ТипыОборудования.СканерШтрихкода = НЕ ТолькоПросмотр;
	ТипыОборудования.ПринтерЭтикеток = Истина;
	
	МенеджерОборудования.ПриСозданииНаСервере(ЭтаФорма, ТипыОборудования);
	ЗаполнитьАктуальныеЦены();
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАктуальнуюСтавкуНДС(ВидСтавки)
	
	Если РегистрыСведений.НастройкиУчетаНДС.ВедетсяУчетНДСНаУСН() Тогда
		АктуальнаяСтавкаНДС = УчетНДСКлиентСервер.ТекстовоеПредставлениеСтавкиНДС(ВидСтавки, ТекущаяДатаПользователя);
	Иначе
		АктуальнаяСтавкаНДС = Строка(Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавки, ТекущаяДатаПользователя));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	СкрыватьВСписках = ОБъект.ВАрхиве;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ОтобразитьОсновнуюСпецификацию();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатурой.ПерезаполнитьВстроенныеРеквизиты(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
			
	ЗаписатьИзменениеЦеныВРегистр(ТекущийОбъект);	
	ЗаписатьШтрикходыНоменклатуры(ТекущийОбъект);
	ЗаписатьСрокИспользованияНоменклатуры(ТекущийОбъект);
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатурой.ПриЗаписиНаСервереФормаНоменклатуры(ЭтотОбъект, ТекущийОбъект, Отказ);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	Если СкрыватьВСписках <> ТекущийОбъект.ВАрхиве Тогда
		ТекущийОбъект.ВАрхиве = СкрыватьВСписках;
	КонецЕсли;
	
	Если Не ТекущийОбъект.ЭтоНовый() Тогда
		НаименованиеДоИзменения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.Ссылка, "Наименование");
		ПараметрыЗаписи.Вставить("НаименованиеДоИзменения", НаименованиеДоИзменения);
		// Для передачи в ИсправитьНаименованиеСпецификации() - см. ПослеЗаписиНаСервере()
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект);
	
	Если Не ПустаяСтрока(ИдентификаторСервиса) Тогда
		ДанныеОбъекта = Новый Структура("Идентификатор", ИдентификаторСервиса);
		ПодготовитьПараметрыЗаполненияФормыНоменклатуры(ДанныеОбъекта);
	КонецЕсли;
	
	Если ИспользуетсяПростойИнтерфейс
		И Не Параметры.Ключ.Пустая()
		И Не Объект.Услуга Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСведенияОбОстатке", 0.1, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;

	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	Если ИмяСобытия = "ФормаНастройкаЦеныПродажиИзменена" или ИмяСобытия = "Запись_НаборКонстант" Тогда
		
		ЗаполнитьАктуальныеЦены();
		
	ИначеЕсли ИмяСобытия = "Запись_ВидНоменклатуры" И Параметр = Объект.ВидНоменклатуры Тогда
		
		ВидНоменклатурыПриИзмененииНаСервере();
		УправлениеФормой(ЭтаФорма);
		
	ИначеЕсли ИмяСобытия = "НазначенаПоКнопкеОсновнаяСпецификацияВСпискеСпецификаций" Тогда
		
		// Спецификация может быть назначена основной из списка спецификаций, открытого в подчиненной форме.
		// Информация об основной спецификации хранится в справочнике Номенклатура.
		// Однако, непосредственно из списка спецификаций менять элемент номенклатуры не следует,
		// так как это может привести к выдаче пользователю непонятного ему сообщения 
		// "Данные были изменены другим пользователем".
		// Поэтому список посылает оповещение форме номенклатуры, которая записывает сама себя.
		Если Параметр.Номенклатура = Объект.Ссылка Тогда
			УстановитьОсновнуюСпецификацию(Параметр.ОсновнаяСпецификация);
			ОповеститьОбИзменении(Параметр.ОсновнаяСпецификация);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗаписанаСпецификацияНоменклатуры" Тогда
		
		Если Параметр.Свойство("НоменклатураПредыдущийВладелец") Тогда
			
			Если Параметр.НоменклатураВладелец = Объект.Ссылка Или Параметр.НоменклатураПредыдущийВладелец  = Объект.Ссылка Тогда
				
				Если Не Модифицированность Тогда 
					Прочитать();
				Иначе
					
					// Записать изменения уже нельзя.
					// Предотвратим возможность выполнить явно бесполезные действия,
					// но оставим возможность скопировать введенные данные в буфер обмена.
					
					Если Параметр.НоменклатураПредыдущийВладелец  = Объект.Ссылка Тогда
						Объект.ОсновнаяСпецификацияНоменклатуры = Неопределено;
					Иначе
						Объект.ОсновнаяСпецификацияНоменклатуры = Параметр.ИзмененнаяСпецификация;
					КонецЕсли;
					
					ОтобразитьОсновнуюСпецификацию();
					
				КонецЕсли;
				
			КонецЕсли;
				
		ИначеЕсли Параметр.НоменклатураВладелец = Объект.Ссылка Тогда
			
			// Вызывается, в двух случаях
			// 1. редактируем новую (первую) спецификацию - и записали ее
			// 2. изменили состав спецификации
			
			Если Не ЗначениеЗаполнено(Объект.ОсновнаяСпецификацияНоменклатуры) Тогда
				// записали новую
				УстановитьОсновнуюСпецификацию(Параметр.ИзмененнаяСпецификация);
				ОповеститьОбИзменении(Параметр.ИзмененнаяСпецификация);
			ИначеЕсли Объект.ОсновнаяСпецификацияНоменклатуры = Параметр.ИзмененнаяСпецификация Тогда
				// изменили состав существующей
				ОсновнаяСпецификацияПредставление = Параметр.СпецификацияПредставление;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если ИмяСобытия = РаботаСНоменклатуройКлиент.ОписаниеОповещенийПодсистемы().ЗагрузкаНоменклатуры
		ИЛИ ИмяСобытия = РаботаСНоменклатуройКлиент.ОписаниеОповещенийПодсистемы().СопоставлениеНоменклатуры Тогда
		
		ПерезаполнитьВстроенныеРеквизиты();
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() И Элементы.ГруппаЗаполнениеПоДаннымСервиса.Видимость Тогда
		Если ИмяСобытия = "ScanData" Тогда
			ДанныеСоСканера = МенеджерОборудованияКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			Если ДанныеСоСканера.Количество() > 0 Тогда
				НайтиЗаполнитьПоШтрихкоду(ДанныеСоСканера[0]);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	//БгосИС 1.1.29.3
	Если ЗначениеЗаполнено(Объект.Ссылка) И ИмяСобытия = "ИзменениеСпискаАлкогольнойПродукции" Тогда
		ВывестиОписаниеМаркируемогоТовараНаСервере();	
	КонецЕсли;
	//
КонецПроцедуры

&НаСервере
Процедура УстановитьОсновнуюСпецификацию(ИзмененнаяСпецификация)
	
	Объект.ОсновнаяСпецификацияНоменклатуры = ИзмененнаяСпецификация;
	Записать();
	ОсновнаяСпецификацияПредставление = УправлениеПроизводством.ПредставлениеОсновнойСпецификации(ИзмененнаяСпецификация);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ЕстьПрослеживаемыйТоварВИстории = Ложь;
	Если ЕстьИсторияРеквизитовПрослеживаемости Тогда
		ПрослеживаемыйТоварВИстории = Объект.ИсторияПрослеживаемогоТовара.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
		ЕстьПрослеживаемыйТоварВИстории = ПрослеживаемыйТоварВИстории.Количество() > 0;
	КонецЕсли;
	
	Если (Объект.ПрослеживаемыйТовар Или ЕстьПрослеживаемыйТоварВИстории)
		И НЕ ЗначениеЗаполнено(Объект.КодТНВЭД) Тогда
	
		СообщениеОбОшибке = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,,НСтр("ru = 'ТН ВЭД'"));
		ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке, , "КодТНВЭД", "Объект", Отказ);
	КонецЕсли; 
	
	Если Объект.ПрослеживаемыйТовар 
		И ЗначениеЗаполнено(Объект.КодТНВЭД)
		И Объект.ВесПоСертификатуТовара = 0 Тогда
		
		ЕдиницаПрослеживаемости = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.КодТНВЭД, "ЕдиницаИзмерения");
		КодОКЕИ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаПрослеживаемости, "Код");
		
		Если ЕдиницаПрослеживаемости <> Объект.ЕдиницаИзмерения И КодОКЕИ = "166" Тогда
			
			СообщениеОбОшибке = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,,НСтр("ru = 'Вес из сертификата'"));
			ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке, , "ВесПоСертификатуТовара", "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьУчетПодакцизныхТоваров И ВидТовара = Перечисления.ВидыПодакцизныхТоваров.АвтомобилиИМотоциклы Тогда
		Если Не ЗначениеЗаполнено(Объект.ЕдиницаИзмеренияДляРасчетаАкциза) Тогда
			СообщениеОбОшибке = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,,НСтр("ru = 'Акциз рассчитывается в '"));
			ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке, , "ЕдиницаИзмеренияДляРасчетаАкциза", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		Оповестить("ВводНовогоЭлементаСправочникаНоменклатура", ВладелецФормы, Объект.Ссылка);
		
		Если ОписаниеОповещенияОЗакрытии <> Неопределено
			И ТипЗнч(ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры) = Тип("Структура") Тогда
			ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Вставить("Номенклатура", Объект.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Выполняем вне транзакции записи, чтобы незначительная проблема
	// (невозможность уточнить наименование спецификации, которое и так еще не видно пользователю)
	// не эскалировала более серьезную (невозможность записать номенклатуру)
	Справочники.СпецификацииНоменклатуры.ИсправитьНаименованиеСпецификации(
		ТекущийОбъект.ОсновнаяСпецификацияНоменклатуры,
		ТекущийОбъект.Ссылка,
		ТекущийОбъект.Наименование,
		ПараметрыЗаписи);
		
	Если ЭтоАлкогольнаяПродукция(Объект.ВидМаркировки) Тогда
		
		СведенияОбАлкогольнойПродукции = РегистрыСведений.СведенияОбАлкогольнойПродукции.СоздатьМенеджерЗаписи();
		СведенияОбАлкогольнойПродукции.Номенклатура        = ТекущийОбъект.Ссылка;
		СведенияОбАлкогольнойПродукции.ВидЛицензии         = ВидЛицензииАлкогольнойПродукции;
		СведенияОбАлкогольнойПродукции.КодВида169          = КодВидаАлкогольнойПродукции;
		СведенияОбАлкогольнойПродукции.НаименованиеВида169 = НаименованиеВидаАлкогольнойПродукции;
		СведенияОбАлкогольнойПродукции.КоэффПересчетаДал   = КоэффПересчетаДал;
		СведенияОбАлкогольнойПродукции.Записать();
		
		ЗаполнитьСведенияОбАлкогольнойПродукции = Ложь;
		
	ИначеЕсли ЗначениеЗаполнено(КодВидаАлкогольнойПродукции) Тогда
		//Очистим запись регистра СведенияОбАлкогольнойПродукции
		СведенияОбАлкогольнойПродукции = РегистрыСведений.СведенияОбАлкогольнойПродукции.СоздатьНаборЗаписей();
		СведенияОбАлкогольнойПродукции.Отбор.Номенклатура.Установить(ТекущийОбъект.Ссылка);
		СведенияОбАлкогольнойПродукции.Записать(Истина);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатуройКлиент.ПослеЗаписиФормаНоменклатуры(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЗаполнениеПоДаннымСервиса",
		"Видимость", Ложь);
	
	Если НЕ ИспользоватьСкрытиеНоменклатуры 
		И Объект.ВАрхиве Тогда
		// Была включена функциональность скрытой номенклатуры
		Оповестить("ВключеноИспользованиеСкрытияНоменклатуры");
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОписаниеОповещенияОЗакрытии <> Неопределено 
		И ТипЗнч(ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры) = Тип("Структура") Тогда
			ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Вставить("НоменклатураСсылка", Объект.Ссылка);
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатуройКлиент.ПередЗакрытиемФормыНоменклатуры(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодичностьУслугиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Если ФормироватьНаименованиеПолноеАвтоматически Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	
	ПроверитьНоменклатуруВАрхиве(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Список = Новый СписокЗначений();
	Список.Добавить(Объект.Наименование);

	// Выбор из списка и обработка выбора.
	Оповещение = Новый ОписаниеОповещения("НаименованиеПолноеНачалоВыбораЗавершение", ЭтотОбъект);
	ПоказатьВыборИзСписка(Оповещение, Список, Элементы.НаименованиеПолное);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)
	
	ФормироватьНаименованиеПолноеАвтоматически = УстановитьФлагФормироватьНаименованиеПолноеАвтоматически(
		Элементы.НаименованиеПолное,
		Элементы.Наименование);
		
	ПроверитьНоменклатуруВАрхиве(ЭтотОбъект);
		
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТНВЭДКодПриИзменении(Элемент)
	
	ЗаполнитьПолеНаименованияКода("ТНВЭД");
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОКПКодПриИзменении(Элемент)
	
	ЗаполнитьПолеНаименованияКода("ОКП");
	
КонецПроцедуры

&НаКлиенте
Процедура ОКПД2КодПриИзменении(Элемент)
	
	ЗаполнитьПолеНаименованияКода("ОКПД2");
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатурнойКлассификацииКодПриИзменении(Элемент)
	ЗаполнитьПолеНаименованияКода("НоменклатурнойКлассификацииККТ");
КонецПроцедуры

&НаКлиенте
Процедура ОКВЭДКодПриИзменении(Элемент)
	
	ЗаполнитьПолеНаименованияКода("ОКВЭД");
	
КонецПроцедуры

&НаКлиенте
Процедура ОКВЭД2КодПриИзменении(Элемент)
	
	ЗаполнитьПолеНаименованияКода("ОКВЭД2");
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	Если (НЕ ЗначениеЗаполнено(Объект.ВидНоменклатуры))
		И (ЗначениеЗаполнено(Объект.Родитель)) Тогда
		
		РодительПриИзмененииНаСервере();
		УправлениеФормой(ЭтаФорма);
		
	ИначеЕсли (ЗначениеЗаполнено(Объект.ВидНоменклатуры))
		И (ЗначениеЗаполнено(Объект.Родитель)) Тогда 
		
		ВидНоменклатурыГруппы = ПолучитьВидНоменклатуры(Объект.Родитель);
		
		Если ЗначениеЗаполнено(ВидНоменклатурыГруппы) И
			Объект.ВидНоменклатуры <> ВидНоменклатурыГруппы Тогда		
		
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для группы ""%1"" основной вид номенклатуры ""%2"".'")
				+ Символы.ПС
				+ НСтр("ru = 'Заменить текущий вид номенклатуры ""%3"" на ""%2""?'"), 
				Объект.Родитель, ВидНоменклатурыГруппы, Объект.ВидНоменклатуры);
			
			Оповещение = Новый ОписаниеОповещения("ВопросИзменитьВидНоменклатурыЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросИзменитьВидНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		РодительПриИзмененииНаСервере();
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВидНоменклатуры(ГруппаНоменклатуры)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГруппаНоменклатуры, "ВидНоменклатуры");
	
КонецФункции

&НаСервере
Процедура РодительПриИзмененииНаСервере()
	
	Объект.ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Родитель, "ВидНоменклатуры");
	ВидНоменклатурыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	Если Объект.ВидСтавкиНДС <> ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.БезНДС")
		И Не ВИсторииЕстьСтавкаБезНДС Тогда
		Объект.КодРаздел7ДекларацииНДС = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОсновнаяСтавкаНДС) И ЗначениеЗаполнено(Объект.ВидСтавкиНДС) Тогда
		ПараметрыОсновнаяСтавкаНДС = Новый Структура;
		ПараметрыОсновнаяСтавкаНДС.Вставить("Настройка",   "ОсновнаяСтавкаНДС");
		ПараметрыОсновнаяСтавкаНДС.Вставить("ВидСтавкиНДС", Объект.ВидСтавкиНДС);
		ПараметрыОсновнаяСтавкаНДС.Вставить("Дата",         ТекущаяДатаПользователя);
		ПараметрыНастройки = Новый Структура("ПараметрыНастройки", ПараметрыОсновнаяСтавкаНДС);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПодсказкаОНастройкеОсновнаяСтавкаНДСЗавершение", ЭтотОбъект);
		
		ОткрытьФорму("ОбщаяФорма.ФормаПодсказкаОНастройке", ПараметрыНастройки, ЭтотОбъект,,,, ОписаниеОповещения);
		
	КонецЕсли;

	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НастройкаМСФОПриИзменении(Элемент)
	
	НастройкиУчетаМСФОКлиент.ПриИзмененииПоляФормы(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВесИзСертификатаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаШтрихкодНаименованиеПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(Объект.Наименование) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыФормыПоДаннымСервиса(Неопределено);
	ОтключитьЗаполнениеПоДаннымСервиса = Истина;
	ПодключитьОбработчикОжидания("Подключаемый_ВключитьЗаполнениеПоДаннымСервиса", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПрослеживаемогоТовараНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("ПрослеживаемыйТовар, ПрослеживаемыйКомплект, ИсторияПрослеживаемогоТовара, ТолькоПросмотр",
		Объект.ПрослеживаемыйТовар, 
		Объект.ПрослеживаемыйКомплект,
		Объект.ИсторияПрослеживаемогоТовара, ТолькоПросмотр);
	
	ОткрытьФорму("ОбщаяФорма.ФормаИсторияПрослеживемогоТовара",
		ПараметрыФормы, ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура Операции0ПриИзменении(Элемент)
	МногострочныйРежимОперации0(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Операции0НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповещениеОВыборе = Новый ОписаниеОповещения("ПослеВыбораОперации0",ЭтотОбъект,Новый Структура);
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Операции0", Объект.Операции0);
	ОткрытьФорму("Справочник.Операции0.Форма.ФормаВыбора",ПараметрыФормы,ЭтотОбъект,Истина,,,ОповещениеОВыборе);
КонецПроцедуры

&НаКлиенте
Процедура ПризнакПредметаРасчетаКодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ЗаполнитьСведенияОПредметеРасчета(ЭтотОбъект, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПризнакПредметаРасчетаКодПриИзменении(Элемент)
	Для каждого КлючИЗначение Из Элемент.СписокВыбора Цикл
		ВыбранноеЗначение = КлючИЗначение.Значение;
		Если ЗначениеЗаполнено(ВыбранноеЗначение) 
			И ПризнакПредметаРасчетаКод = Строка(ВыбранноеЗначение.Код) Тогда
			
			Прервать;
		КонецЕсли;
		ВыбранноеЗначение = Неопределено;
	КонецЦикла;
	
	ЗаполнитьСведенияОПредметеРасчета(ЭтотОбъект, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура КодВидаПодакцизногоТовараНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыОтбора = Новый Структура("ВидТовара, ОтбиратьПоДате", 
		ВидТовара, Истина);
	
	ОткрытьФорму("Справочник.КодыВидовПодакцизныхТоваров.ФормаВыбора", ПараметрыОтбора, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияДляРасчетаАкцизаПриИзменении(Элемент)
	
	Объект.КоэффициентПересчета = 0;
	НастроитьВидимостьКоэффициентаПересчетаПоАкцизам();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияДляРасчетаАкцизаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МассивОтбора = Новый Массив();
	МассивОтбора.Добавить("214"); //кВт/ч
	МассивОтбора.Добавить("251"); // л.с.
	ЗначениеОтбора = Новый Структура("Код", МассивОтбора);
	ПараметрыОтбора = Новый Структура("Отбор", ЗначениеОтбора);
	
	ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.ФормаВыбора", ПараметрыОтбора, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура КоэффициентПересчетаПриИзменении(Элемент)
	Элементы.КоэффициентПересчета.ОтметкаНезаполненного = Элементы.ГруппаКоэффициента.Видимость
		И Не ЗначениеЗаполнено(Объект.КоэффициентПересчета);
КонецПроцедуры
	
	&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
		
	Если ЕстьУчетПодакцизныхТоваров Тогда
		Объект.КоэффициентПересчета = 0;
		НастроитьВидимостьКоэффициентаПересчетаПоАкцизам();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПриИзменении(Элемент)
	Если НЕ СкрыватьВСписках 
		ИЛИ Объект.Ссылка.Пустая() Тогда
		
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьОстатки", 0.5, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиВКарточкуСкрытойНоменклатуры" Тогда
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", Новый Структура("Ключ", НоменклатураВАрхиве));
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВсеЦеныНажатие(Элемент)
	
	ПараметрыОткрытия =  Новый Структура;
	ПараметрыОткрытия.Вставить("Номенклатура", Объект.Ссылка);
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеЦеныЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.Номенклатура.Форма.ВсеЦены", ПараметрыОткрытия,,,,,ОписаниеОповещения);
			
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастроитьЦеныМКНажатие(Элемент)
	ОткрытьФормуНастройкиЦен();
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяСпецификацияПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УдалосьЗаписатьОбъект = Истина;
	
	Если (Объект.Ссылка.Пустая() Или Модифицированность) Тогда
		УдалосьЗаписатьОбъект = Записать();
	КонецЕсли;
	
	Если УдалосьЗаписатьОбъект Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Владелец", Объект.Ссылка);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ",                          Объект.ОсновнаяСпецификацияНоменклатуры);
		ПараметрыФормы.Вставить("ОткрытоИзКарточкиНоменклатуры", Истина);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения",            ЗначенияЗаполнения);
		
		ОткрытьФорму("Справочник.СпецификацииНоменклатуры.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаЦеныПродажиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "e1cib/data/Обработка.ПанельАдминистрированияБП.Форма.ФормаНастройкиЦеныПродажи" Тогда
		
		СтандартнаяОбработка = Ложь; // Форму будем открывать с параметром
		
		ОткрытьФормуНастройкиЦен();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПродажиИзНоменклатурыПриИзменении(Элемент)
	ЦенаПродажиМодифицирована = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВалютаЦеныПродажиСВалютамиПриИзменении(Элемент)
	ЦенаПродажиМодифицирована = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИсторияСтавкиНДСНажатие(Элемент)
		
	ПараметрыФормы = Новый Структура("ТекущийВидСтавкиНДС, ИсторияВидаСтавкиНДС, ТолькоПросмотр", 
		Объект.ВидСтавкиНДС, Объект.ИсторияВидаСтавкиНДС, ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.РедактированиеИсторииВидаСтавкиНДС",
		ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовуюЦену(Команда)
	
	ТипЦен = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СозданиеЦеныПоказатьВопрос(ТипЦен);
	Иначе
		СозданиеЦеныОткрытьФормуЗаписи(ТипЦен);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
		
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Номенклатура.Форма.РедактированиеИсторииВидаСтавкиНДС") Тогда
		
		УстановитьВидСтавкиНДСПослеРедактированияИстории(ВыбранноеЗначение.ИсторияВидаСтавкиНДС);
		
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ФормаИсторияПрослеживемогоТовара") Тогда
		УстановитьПрослеживаемостьПослеРедактированияИстории(ВыбранноеЗначение.ИсторияПрослеживаемогоТовара);
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.КодыВидовПодакцизныхТоваров.ФормаВыбора") Тогда
		Объект.КодВидаПодакцизногоТовара = ВыбранноеЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидСтавкиНДСПослеРедактированияИстории(НаборЗаписей)
	
	Модифицированность = Истина;
	ВИсторииЕстьСтавкаБезНДС = Ложь;
	ЕстьИсторияВидовСтавокНДС = Ложь;
	
	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияВидаСтавкиНДС.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		ЕстьИсторияВидовСтавокНДС = Истина;
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьИстории = Объект.ИсторияВидаСтавкиНДС.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
			Если ЗаписьНабора.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.БезНДС Тогда
				ВИсторииЕстьСтавкаБезНДС = Истина;
			КонецЕсли;
			Если ЗаписьНабора.Период <= ТекущаяДатаПользователя Тогда
				АктуальныйВидСтавкиНДС = ЗаписьНабора.ВидСтавкиНДС;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	Объект.ВидСтавкиНДС = НаборЗаписей[НаборЗаписей.Количество()-1].ВидСтавкиНДС;
	
	Если Не ЗначениеЗаполнено(АктуальныйВидСтавкиНДС) Тогда
		АктуальныйВидСтавкиНДС = Объект.ВидСтавкиНДС;
	КонецЕсли;
	
	ЗаполнитьАктуальнуюСтавкуНДС(АктуальныйВидСтавкиНДС);

	Справочники.Номенклатура.УстановитьАктуальноеЗначениеИсторииВидаСтавкиНДС(
		Объект.ВидСтавкиНДС , Объект.ИсторияВидаСтавкиНДС);
	УточнитьПредставлениеСтавкиНДСПриУСН();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокИспользованияСпецодеждыПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	Если ПорядокИспользованияСпецодежды = 0 Тогда
		СрокПолезногоИспользования = 0;
	ИначеЕсли СрокПолезногоИспользования = 0 Тогда
		СрокПолезногоИспользования = ПолучитьСрокПолезногоИспользования(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоДаннымСервиса(Команда)
	
	ЗаполнитьРеквизитыФормыПоДаннымСервиса(Команда);
	
КонецПроцедуры

#Область ЭлектронноеВзаимодействиеРаботаСНоменклатурой

// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
&НаКлиенте
Процедура Подключаемый_НажатиеРаботаСНоменклатурой(Элемент, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеГиперссылки(ЭтотОбъект, Элемент, СтандартнаяОбработка,
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаНавигационнойСсылкиРаботаСНоменклатурой(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеГиперссылки(ЭтотОбъект, Элемент, СтандартнаяОбработка,
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачалоВыбораРаботаСНоменклатурой(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.ВыбратьОбъектСервиса(ЭтотОбъект, Элемент, СтандартнаяОбработка, Ложь, 
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОчисткаРаботаСНоменклатурой(Элемент, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеОчиститьНоменклатуру(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеРежимОбновленияРаботаСНоменклатурой(Элемент)
	
	РаботаСНоменклатуройКлиент.НажатиеРежимОбновления(ЭтотОбъект, ПодготовитьДанныеФормы(), 
		Новый ОписаниеОповещения("ЗакрытиеФормыЗаполненияОбъекта", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьРаботаСНоменклатурой(Элемент, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.ОткрытьКарточкуОбъектаСервиса(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИзменениеТекстаРедактированияРаботаСНоменклатурой(Элемент, Текст, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.ИзменениеТекстаСобратьСтрокуПоиска(ЭтотОбъект, Текст, Элемент, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРаботаСНоменклатурой(Элемент)
	
	РаботаСНоменклатуройКлиент.ПриИзмененииСобратьСтрокуПоиска(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеЗаписиРаботаСНоменклатурой()
	
	РаботаСНоменклатуройКлиент.ПослеЗаписиФормаНоменклатурыПродолжение(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыВыбораОбъектаСервиса(ДанныеОбъекта, ДополнительныеПараметры) Экспорт 
	
	ДополнительныеПараметры.Вставить("НеОткрыватьФормуЗаполненияНоменклатуры", Объект.Ссылка.Пустая());
	
	РаботаСНоменклатуройКлиент.ОбработкаОповещенияЗакрытиеФормыВыбора(
		ДанныеОбъекта, ДополнительныеПараметры, ПодготовитьДанныеФормы(),
			Новый ОписаниеОповещения("ЗакрытиеФормыЗаполненияОбъекта", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыЗаполненияОбъекта(ПараметрыЗакрытияФормы, ДополнительныеПараметры) Экспорт 
	
	ЗаполнитьРеквизитыФормы(ПараметрыЗакрытияФормы, ДополнительныеПараметры);
	
	НастроитьФормуПриДлительнойОперации(ЭтотОбъект, Ложь);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеФормы()
	
	Возврат РаботаСНоменклатурой.ПодготовитьДанныеДляИнтерактивногоЗаполнения(ЭтотОбъект);
			
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(ПараметрыЗакрытияФормы, ДополнительныеПараметры)
	
	РаботаСНоменклатурой.ЗаполнитьРеквизитыФормы(ЭтотОбъект, ПараметрыЗакрытияФормы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыОписанияМаркируемогоТовара(Ссылка, ВидМаркировки)
	
	ПараметрыОписанияМаркируемогоТовара 												= ИнтеграцияИСКлиентСервер.ПараметрыФормированияДанныхПредставленияНастройкиНоменклатуры();
	ПараметрыОписанияМаркируемогоТовара.СсылкаНаОбъект 									= Ссылка;
	ПараметрыОписанияМаркируемогоТовара.ВидПродукции 									= ВидМаркировки;
	ПараметрыОписанияМаркируемогоТовара.ДопустимаНастройкаЛогистическойЕдиницы 			= Истина;
	ПараметрыОписанияМаркируемогоТовара.ПредставлениеВНесколькоСтрок 					= Ложь;
	ПараметрыОписанияМаркируемогоТовара.ОтступПриВыводеНастройкиВидаУпаковки 			= Ложь;
	ПараметрыОписанияМаркируемогоТовара.ВыводитьГиперссылкуНастройкиВидовУпаковокПоGTIN = Ложь;

	Возврат ПараметрыОписанияМаркируемогоТовара;
	
КонецФункции

&НаКлиенте
Процедура ОписаниеМаркируемогоТовараОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормИСКлиентПереопределяемый.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	 		
КонецПроцедуры

&НаКлиенте
Процедура СвойстваМаркируемогоТовараЗавершение(Параметры, ДополнительныеПараметры) Экспорт 
	
	ПараметрыОписанияМаркируемогоТовара = ПараметрыОписанияМаркируемогоТовара(Объект.Ссылка, Объект.ВидМаркировки);
	Если Не ЗначениеЗаполнено(ПараметрыОписанияМаркируемогоТовара.ВидПродукции) Тогда
		ПараметрыОписанияМаркируемогоТовара.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная");
	КонецЕсли;

	ОписаниеМаркируемогоТовара = ИнтеграцияИСВызовСервера.ПредставлениеНастройкиПараметровНоменклатуры(ПараметрыОписанияМаркируемогоТовара);
	
КонецПроцедуры

&НаСервере
Процедура ДополнительныеРеквизитыМаркировкиВидимость()
	
	Элементы.ОписаниеМаркируемогоТовара.Видимость = ЗначениеЗаполнено(ОписаниеМаркируемогоТовара);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокСведенийОПредметеРасчета()
	СписокСведений = Новый Соответствие;
	МетаданныеПеречисления = Метаданные.Перечисления.ПризнакиПредметаРасчета;

	Для Индекс = 0 По Перечисления.ПризнакиПредметаРасчета.Количество()-1 Цикл
		ПризнакПредметаРасчета   = Перечисления.ПризнакиПредметаРасчета.Получить(Индекс);
		
		КодППР           = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.КодПризнакаПредметаРасчета(ПризнакПредметаРасчета);
		НаименованиеППР  = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.НаименованиеПризнакаПредметаРасчета(КодППР);
		ПредставлениеППР = НРег(МетаданныеПеречисления.ЗначенияПеречисления[Индекс].Комментарий);
		
		СписокСведений.Вставить(
			ПризнакПредметаРасчета, 
			Новый Структура("Код,Наименование,Представление, ПризнакПредметаРасчета", КодППР, НаименованиеППР,  ПредставлениеППР, ПризнакПредметаРасчета));
	КонецЦикла;
	
	Возврат СписокСведений;
КонецФункции

// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьСписокСведенияОПредметеРасчета()
	СписокСведенийОПредметахРасчета = СписокСведенийОПредметеРасчета();

	Элементы.ПризнакПредметаРасчетаКод.СписокВыбора.Очистить();
	Элементы.ПризнакПредметаРасчетаКод.СписокВыбора.Добавить("", НСтр("ru = '<Авто>'"));
	Для Индекс = 0 По Перечисления.ПризнакиПредметаРасчета.Количество()-1 Цикл
		СведенияОПредметеРасчета = СписокСведенийОПредметахРасчета[Перечисления.ПризнакиПредметаРасчета.Получить(Индекс)];
		
		Элементы.ПризнакПредметаРасчетаКод.СписокВыбора.Добавить(
			СведенияОПредметеРасчета,
			СтрШаблон("%1 - %2 (%3)", СведенияОПредметеРасчета.Код, СведенияОПредметеРасчета.Наименование, СведенияОПредметеРасчета.Представление));
	КонецЦикла;
		
	Если НЕ Объект.ПризнакПредметаРасчета.Пустая() Тогда
		ЗаполнитьСведенияОПредметеРасчета(ЭтотОбъект, СписокСведенийОПредметахРасчета[Объект.ПризнакПредметаРасчета])
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСведенияОПредметеРасчета(Форма, СведенияОПредметеРасчета)
	Объект = Форма.Объект;
	Если НЕ ЗначениеЗаполнено(СведенияОПредметеРасчета) Тогда
		Объект.ПризнакПредметаРасчета = Неопределено;
		Форма.ПризнакПредметаРасчетаКод = "";
		Форма.ПризнакПредметаРасчетаНаименование = "";
		Возврат;
	КонецЕсли;
	
	Объект.ПризнакПредметаРасчета             = СведенияОПредметеРасчета.ПризнакПредметаРасчета;
	Форма.ПризнакПредметаРасчетаКод           = СтрШаблон("%1 - %2", СведенияОПредметеРасчета.Код, СведенияОПредметеРасчета.Наименование);
	Форма.ПризнакПредметаРасчетаНаименование  = СведенияОПредметеРасчета.Представление;
КонецПроцедуры 

&НаСервере
Процедура УстановитьУчетПодконтрольнойПродукции()

	ПараметрыОписанияМаркируемогоТовара = ПараметрыОписанияМаркируемогоТовара(Объект.Ссылка, Объект.ВидМаркировки);
	ОписаниеМаркируемогоТовара = ИнтеграцияИСВызовСервера.ПредставлениеНастройкиПараметровНоменклатуры(ПараметрыОписанияМаркируемогоТовара);
	ДополнительныеРеквизитыМаркировкиВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменениеЦеныВРегистр(ТекущийОбъект)
	
	ПравоНаИзменениеРегистраЦен = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ЦеныНоменклатурыДокументов); 	
	
	Если ПравоНаИзменениеРегистраЦен Тогда
		
		РедактироватьВКарточкеНоменклатуры = 
			(Константы.НастройкаЗаполненияЦеныПродажи.Получить() = Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура);
		
		Если РедактироватьВКарточкеНоменклатуры и ЦенаПродажиМодифицирована Тогда
			
			МенеджерЗаписи 						= РегистрыСведений.ЦеныНоменклатурыДокументов.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Номенклатура 		= ТекущийОбъект.Ссылка;
			МенеджерЗаписи.СпособЗаполненияЦены = Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам;
			МенеджерЗаписи.Валюта				= ВалютаЦены;
			МенеджерЗаписи.Цена 				= ЦенаПродажи;
			МенеджерЗаписи.ЦенаВключаетНДС      = Истина;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьШтрикходыНоменклатуры(ТекущийОбъект)
	Если НЕ ЗначениеЗаполнено(Штрихкоды) Тогда
		Возврат;
	КонецЕсли;

	МассивШтрихкодов = СтрРазделить(Штрихкоды, ",", Ложь);
	Для каждого Штрихкод Из МассивШтрихкодов Цикл
		МенеджерЗаписи              = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Номенклатура = ТекущийОбъект.Ссылка;
		МенеджерЗаписи.Штрихкод     = Штрихкод;
		
		МенеджерЗаписи.Записать();
	КонецЦикла;
КонецПроцедуры 

&НаСервере
Процедура ЗаписатьСрокИспользованияНоменклатуры(ТекущийОбъект)
	
	Если СрокПолезногоИспользования = ПолучитьСрокПолезногоИспользования(ТекущийОбъект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СрокиИспользованияНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи                   = РегистрыСведений.СрокиИспользованияНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура      = ТекущийОбъект.Ссылка;
	МенеджерЗаписи.СрокИспользования = СрокПолезногоИспользования;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры 

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();


	// ЕдиницаИзмерения

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЕдиницаИзмерения");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуга", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЕдиницаИзмерения", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
КонецПроцедуры

// Процедура устанавливает доступность реквизитов формы.
//
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.Импортер.Видимость                      = Не Объект.Услуга;
	Элементы.НомерГТД.Видимость                      = Не Объект.Услуга;
	Элементы.СтранаПроисхождения.Видимость           = Не Объект.Услуга;
	Элементы.СтатьяЗатрат.Видимость                  = Не Объект.Услуга И (Форма.ВПроизводство ИЛИ Форма.СобственныеНужды ИЛИ Форма.ВЭксплуатациюСотрудникам);
	Элементы.ТНВЭД.Видимость                         = Не Объект.Услуга;
	Элементы.ОКП.Видимость                           = Не Объект.Услуга;
	Элементы.КодРаздел7ДекларацииНДС.Видимость       = Форма.РаздельныйУчетНДС
			И (Объект.ВидСтавкиНДС = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.БезНДС")
		Или Форма.ВИсторииЕстьСтавкаБезНДС);
	Элементы.Операции0.Видимость = Форма.КлассификацияНоменклатурПоОперациям0;
	
	Элементы.ГруппаСпецодежда.Видимость              = Не Объект.Услуга И Форма.ВЭксплуатациюСотрудникам;
	Элементы.ВидМаркировки.Видимость                 = Не Объект.Услуга И Форма.ЕстьДоступныеВидыМаркировки;
	Элементы.ГруппаЭлектронныеСертификаты.Видимость  = Форма.НаПродажу;
	Элементы.Производитель.Видимость                 = Не Объект.Услуга; 
	Элементы.НоменклатурнаяГруппа.Видимость          = Форма.НаПродажу;
	Элементы.ГруппаАлкогольнаяПродукция.Видимость    = Форма.НаПродажу;
	Элементы.ОсновнаяСпецификацияПредставление.Видимость = Форма.Производим ИЛИ (Форма.ИспользоватьКомплектациюНоменклатуры И Форма.Комплектуем);
	
	Элементы.ГруппаПрослеживаемость.Видимость         = Не Объект.Услуга И Форма.ВедетсяУчетПрослеживаемыхТоваров 
															И НЕ ЗначениеЗаполнено(Объект.ВидМаркировки);
	Элементы.ГруппаПрослеживаемостьКомплект.Видимость = Форма.ВедетсяУчетПрослеживаемыхТоваров И Форма.ИспользоватьКомплектациюНоменклатуры 
															И (Форма.Комплектуем ИЛИ Объект.Услуга)
															И НЕ ЗначениеЗаполнено(Объект.ВидМаркировки);
	
	ИмпортныйТовар                                   = ЗначениеЗаполнено(Объект.СтранаПроисхождения) И Объект.СтранаПроисхождения <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	Элементы.ГруппаИмпортныйТовар.Видимость          = ИмпортныйТовар;
	
	Если ЗначениеЗаполнено(Объект.КодТНВЭД) Тогда
		Элементы.ВесИзСертификата.Видимость = Истина;
	Иначе
		Элементы.ВесИзСертификата.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ВесИзСертификата.АвтоОтметкаНезаполненного = Объект.ВесПоСертификатуТовара = 0
			И Объект.ПрослеживаемыйТовар
			И ЗначениеЗаполнено(Объект.КодТНВЭД);
	
	МногострочныйРежимОперации0(Форма);
	
	Элементы.СтавкаНДС.Видимость = Не Форма.ЕстьИсторияВидовСтавокНДС;
	Элементы.АктуальнаяСтавкаНДС.Видимость = Форма.ЕстьИсторияВидовСтавокНДС;

	Элементы.КодВидаНоменклатурнойКлассификации.Видимость = Не Объект.Услуга И Форма.НаПродажу;
	
	// В требовании-накладной допускается указывать только статьи с видом "МатериальныеРасходы"
	ПараметрыВыбораСтатьиЗатрат = Новый Массив;
	Если Не Объект.Услуга Тогда
		МатериальныеРасходы = ПредопределенноеЗначение("Перечисление.ВидыРасходовНУ.МатериальныеРасходы");
		ПараметрыВыбораСтатьиЗатрат.Добавить(Новый ПараметрВыбора("Отбор.ВидРасходовНУ", МатериальныеРасходы));
		// При изменении флага статья затрат очищается
	КонецЕсли;
	
	Элементы.ГруппаПериодичность.Видимость = Объект.Услуга И Форма.НаПродажу;
	Элементы.УслугаРазмещения.Видимость = Объект.Услуга;
	Если ЗначениеЗаполнено(Объект.ПериодичностьУслуги) И (ЗначениеЗаполнено(Объект.НаименованиеПолное) ИЛИ ЗначениеЗаполнено(Объект.Наименование)) Тогда
		НаименованиеНоменклатуры = ?(ПустаяСтрока(Объект.НаименованиеПолное), Объект.Наименование, Объект.НаименованиеПолное);
		ТекстПодсказки = СтрШаблон(НСтр("ru='Пример: %1'"), РаботаСНоменклатуройКлиентСерверБП.СодержаниеУслуги(
			НаименованиеНоменклатуры,
			Объект.ПериодичностьУслуги,
			ТекущаяДата()));
		Элементы.ПериодичностьУслуги.Подсказка = ТекстПодсказки;
		Элементы.ПериодичностьУслуги.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе
		Элементы.ПериодичностьУслуги.Подсказка = "";
		Элементы.ПериодичностьУслуги.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	ШаблонПодсказки = НСтр("ru='Содержание услуги в документе будет дополнено выбранной периодичностью.
		|Например, ""%1""'");
		
	ПериодичностьДляПримера = ?(ЗначениеЗаполнено(Объект.ПериодичностьУслуги),
		Объект.ПериодичностьУслуги,
		ПредопределенноеЗначение("Перечисление.Периодичность.Месяц"));
	
	Если ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		НаименованиеДляПримера = Объект.НаименованиеПолное;
	ИначеЕсли ЗначениеЗаполнено(Объект.Наименование) Тогда
		НаименованиеДляПримера = Объект.Наименование;
	Иначе
		НаименованиеДляПримера = НСтр("ru='Оказание услуг'");
	КонецЕсли;
	
	Если Форма.ИспользуетсяПростойИнтерфейс Тогда
		
		Элементы.ГруппаОстаток.Видимость = Не Объект.Услуга;
		
		Если Форма.ОстатокПолучен Тогда
			ОстатковНет = Форма.ОбщийОстаток = 0;
			Элементы.ДекорацияОстатковНет.Видимость = ОстатковНет;
			Элементы.ГруппаОстатокВсего.Видимость   = Не ОстатковНет;
			Элементы.ГруппаДетали.Видимость         = Не ОстатковНет;
		КонецЕсли;
		
		Элементы.ГруппаЗаголовокНесколькоСкладов.Видимость = Форма.ИспользуютсяНесколькоСкладов;
		Элементы.ГруппаЗаголовокОдинСклад.Видимость = Не Форма.ИспользуютсяНесколькоСкладов;
		
	КонецЕсли;
	
	Элементы.СрокПолезногоИспользования.Видимость = (Форма.ПорядокИспользованияСпецодежды = 1 И Не Объект.Услуга);
		
	Элементы.ГруппаПериодичность.Подсказка = СтрШаблон(ШаблонПодсказки, РаботаСНоменклатуройКлиентСерверБП.СодержаниеУслуги(
		НаименованиеДляПримера,
		ПериодичностьДляПримера,
		ТекущаяДата()));
	
	Элементы.СтатьяЗатрат.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораСтатьиЗатрат);
	
	Элементы.ГруппаАлкогольнаяПродукция.Видимость = ЭтоАлкогольнаяПродукция(Объект.ВидМаркировки);
	
КонецПроцедуры

// и присваивает соответствующее значение переменной ФормироватьНаименованиеПолноеАвтоматически.
//
// Параметры:
//  Нет.
//
&НаКлиентеНаСервереБезКонтекста
Функция УстановитьФлагФормироватьНаименованиеПолноеАвтоматически(НаименованиеПолное,Наименование)
   
   Возврат (ПустаяСтрока(НаименованиеПолное) ИЛИ НаименованиеПолное = Наименование);
   
КонецФункции // УстановитьФлагФормироватьНаименованиеПолноеАвтоматически()

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма, РеквизитФормыВЗначение("Объект"));

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.НаименованиеПолное = Результат.Значение;
		Модифицированность = Истина;
		ФормироватьНаименованиеПолноеАвтоматически = Истина;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПолеНаименованияКода(ИмяКлассификатора)

	Классификатор = Объект["Код" + ИмяКлассификатора];
	ЭтаФорма[ИмяКлассификатора+"Наименование"] = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Классификатор, "Наименование");

КонецПроцедуры

&НаСервере
Процедура ОтобразитьОсновнуюСпецификацию()
	
	Если Не ЗначениеЗаполнено(Объект.ОсновнаяСпецификацияНоменклатуры) Тогда
		Элементы.ОсновнаяСпецификацияПредставление.Видимость = ПравоДоступа("Редактирование", Метаданные.Справочники.СпецификацииНоменклатуры);
		ОсновнаяСпецификацияПредставление = ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначенияЗаполнить();
	Иначе
		ОсновнаяСпецификацияПредставление = УправлениеПроизводством.ПредставлениеОсновнойСпецификации(Объект.ОсновнаяСпецификацияНоменклатуры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодсказкуПечатиАртикула()
	
	Если ПравоДоступа("Редактирование", Метаданные.Константы.ДополнительнаяКолонкаПечатныхФормДокументов) Тогда
		ЭлементыСтроки = Новый Массив;
		ЭлементыСтроки.Добавить(НСтр("ru = 'Как добавить артикул в печатную форму?'"));
		ЭлементыСтроки.Добавить(Символы.ПС);
		Если ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой") Тогда
			ЭлементыСтроки.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Настройки - Печать артикулов'"),
			,
			,
			,
			УправлениеПечатьюБПКлиентСервер.ГиперссылкаФормыНастройкиПечатиАртикулов()));
		Иначе
			ЭлементыСтроки.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Администрирование - Параметры учета - Печать артикулов'"),
			,
			,
			,
			УправлениеПечатьюБПКлиентСервер.ГиперссылкаФормыНастройкиПечатиАртикулов()));
		КонецЕсли;
		Элементы.Артикул.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ЭлементыСтроки);
		Элементы.Артикул.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.Артикул.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСрокиИспользованияСпецодежды()
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СрокиИспользованияНоменклатуры) Тогда
		Элементы.ГруппаСпецодежда.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СрокиИспользованияНоменклатуры) Тогда
		Элементы.ПорядокИспользованияСпецодежды.ТолькоПросмотр = Истина;
		Элементы.СрокПолезногоИспользования.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	СрокПолезногоИспользования = ПолучитьСрокПолезногоИспользования(Объект.Ссылка);
	ПорядокИспользованияСпецодежды = СрокПолезногоИспользования > 0;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСрокПолезногоИспользования(Номенклатура)
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СрокиИспользованияНоменклатуры) Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат РегистрыСведений.СрокиИспользованияНоменклатуры.Получить(
			Новый Структура("Номенклатура", Номенклатура)).СрокИспользования;
	
КонецФункции

&НаСервере
Процедура ПрочитатьСведенияОбАлкогольнойПродукции()
	
	Если ЗаполнитьСведенияОбАлкогольнойПродукции
		ИЛИ НЕ ЭтоАлкогольнаяПродукция(Объект.ВидМаркировки) Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОбАлкогольнойПродукции = ПолучитьСведенияОбАлкогольнойПродукции(Объект.Ссылка);
	
	Если СведенияОбАлкогольнойПродукции.Количество() > 0 Тогда
		
		ВидЛицензииАлкогольнойПродукции      = СведенияОбАлкогольнойПродукции.ВидЛицензии;
		КодВидаАлкогольнойПродукции          = СведенияОбАлкогольнойПродукции.КодВида169;
		НаименованиеВидаАлкогольнойПродукции = СведенияОбАлкогольнойПродукции.НаименованиеВида169;
		КоэффПересчетаДал                    = СведенияОбАлкогольнойПродукции.КоэффПересчетаДал;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСведенияОбАлкогольнойПродукции(Номенклатура)
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СведенияОбАлкогольнойПродукции) Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	Возврат РегистрыСведений.СведенияОбАлкогольнойПродукции.Получить(
			Новый Структура("Номенклатура", Номенклатура));
	
КонецФункции

#Область ЭлектронноеВзаимодействиеРаботаСНоменклатурой

&НаСервере
Процедура НастроитьЭлементыУправленияРаботаСНоменклатурой()
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьПодсказкуСервиса();
		РаботаСНоменклатуройБП.ЗаполнитьРеквизитыФормыДляРаботыСервиса(ЭтотОбъект);
	Иначе 
		Элементы.ГруппаЗаполнениеПоДаннымСервиса.Видимость = Ложь;
	КонецЕсли;
	
	ДобавитьРеквизитыИЭлементыФормыРаботаСНоменклатурой();
	
	Параметры.Свойство("Штрихкод", ПолеПоискаШтрихкодНаименование);
	Параметры.Свойство("ИдентификаторСервиса", ИдентификаторСервиса);
	Если Не ПустаяСтрока(ИдентификаторСервиса) Тогда
		НастроитьФормуПриДлительнойОперации(ЭтотОбъект, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитыИЭлементыФормыРаботаСНоменклатурой()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыбиратьРежимОбновления", ИспользоватьАвтоматическоеОбновлениеНоменклатуры());
	
	РаботаСНоменклатурой.ПриСозданииНаСервереФормаНоменклатуры(ЭтотОбъект, Объект.Ссылка,
		Элементы.ГруппаРаботаСНоменклатурой, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодсказкуСервиса()
	
	ГиперссылкаНаПромоСайтНоменклатура = РаботаСНоменклатурой.ГиперссылкаНаПромоСайтНоменклатура();
	ГиперссылкаНаОфертуСервисов = РаботаСНоменклатурой.ГиперссылкаНаОфертуСервисов();
	
	ТекстПодсказки = СтрШаблон("<span style=""color: ПоясняющийТекст"">%1 </span> <a href = ""%2"">%3</a>.
		|<span style=""color: ПоясняющийТекст"">%4 </span> <a href = ""%5"">%6</a>.",
		НСтр("ru='Автоматическое заполнение реквизитов по данным каталога'"),
		ГиперссылкаНаПромоСайтНоменклатура,
		НСтр("ru='1С:Номенклатура'"),
		НСтр("ru='Использование сервиса 1С:Номенклатура означает согласие с'"),
		ГиперссылкаНаОфертуСервисов,
		НСтр("ru='Условиями использования сервиса'"));
	
	ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(ТекстПодсказки);
	
	Элементы.ЗаполнитьРеквизитыПоДаннымСервисаРасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьВстроенныеРеквизиты()
	РаботаСНоменклатурой.ПерезаполнитьВстроенныеРеквизиты(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуПользователейНажатиеПродолжение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьПоискДанныхСервиса();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьФормуПриДлительнойОперации(Форма, Знач ДлительнаяОперацияАктивна)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "КартинкаОжидание",
		"Видимость", ДлительнаяОперацияАктивна);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ЗаполнитьРеквизитыПоДаннымСервиса",
		"Доступность", Не ДлительнаяОперацияАктивна);
	
	Если Форма.ТолькоПросмотр <> ДлительнаяОперацияАктивна Тогда
		Форма.ТолькоПросмотр = ДлительнаяОперацияАктивна;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеДлительнойОперации(ДлительнаяОперация, ОповещениеОЗавершении)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоискДанныхСервиса()
	
	ДлительнаяОперация = ДлительнаяОперацияРезультатПоискаНоменклатурыСервиса();
		
	Если ТипЗнч(ДлительнаяОперация) = Тип("Структура") Тогда
		НастроитьФормуПриДлительнойОперации(ЭтотОбъект, Истина);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьПоискДанныхСервисаЗавершение", ЭтотОбъект);
		
		ОжидатьЗавершениеДлительнойОперации(ДлительнаяОперация, ОповещениеОЗавершении);
	Иначе 
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось подключить сервис 1С:Номенклатура'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДлительнаяОперацияРезультатПоискаНоменклатурыСервиса()
	
	Если Не СервисАктивен Тогда
		СервисАктивен = РаботаСНоменклатуройБП.СервисУспешноАктивирован();
		Если СервисАктивен Тогда
			ДобавитьРеквизитыИЭлементыФормыРаботаСНоменклатурой();
		Иначе 
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = 
		НСтр("ru = '1С:Номенклатура. Поиск номенклатуры по строке.'");
		
	ПараметрыФункции = РаботаСНоменклатурой.ПараметрыПоискаНоменклатурыСервиса();
	ПараметрыФункции.СтрокаПоиска = ПолеПоискаШтрихкодНаименование;
	ПараметрыФункции.КоличествоЗаписей = 2;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"РаботаСНоменклатурой.РезультатПоискаНоменклатурыСервиса", ПараметрыФункции);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПоискДанныхСервисаЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт 
	
	Перем РезультатПоиска;
	
	Статус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус");
	Если Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	ИначеЕсли Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда 
		РезультатПоиска = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
	НайденОдинРезультат = (ТипЗнч(РезультатПоиска) = Тип("Массив") И РезультатПоиска.Количество() = 1);
	ДлительнаяОперацияЗавершена = Истина;
	
	Если НайденОдинРезультат Тогда
		НоменклатураПоИдентификатору = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатПоиска[0], "Номенклатура");
		Если ЗначениеЗаполнено(НоменклатураПоИдентификатору) Тогда
			ПоказатьСообщениеОбОшибкеЗаполненияФормыПоДаннымСервиса(НоменклатураПоИдентификатору, 2);
		Иначе 
			ПодготовитьПараметрыЗаполненияФормыНоменклатуры(РезультатПоиска[0]);
			ДлительнаяОперацияЗавершена = Ложь;
		КонецЕсли;
	ИначеЕсли Результат.Сообщения <> Неопределено И Результат.Сообщения.Количество() > 0 Тогда 
		Для каждого Сообщение Из Результат.Сообщения Цикл
			Сообщение.Сообщить();
		КонецЦикла;
	Иначе 
		Оповещение = Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура);
		СтандартнаяОбработка = Ложь;
		РаботаСНоменклатуройКлиент.ВыбратьОбъектСервиса(ЭтотОбъект, Элементы.ПолеПоискаШтрихкодНаименование,
			СтандартнаяОбработка, Ложь, Оповещение);
	КонецЕсли;
	
	Если ДлительнаяОперацияЗавершена Тогда
		НастроитьФормуПриДлительнойОперации(ЭтотОбъект, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользоватьАвтоматическоеОбновлениеНоменклатуры()
	
	Результат = Ложь;
	
	ИмяЗадания = "ОбновлениеНоменклатурыРаботаСНоменклатурой";
	Отбор = Новый Структура("Метаданные", ИмяЗадания);
	УстановитьПривилегированныйРежим(Истина);
	Найденные = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);
	Если Найденные.Количество() > 0 Тогда
		Результат = Найденные[0].Использование;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьРеквизитыФормыПоДаннымСервиса(Команда)
	
	Если ОтключитьЗаполнениеПоДаннымСервиса Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	Если ПустаяСтрока(ПолеПоискаШтрихкодНаименование) Тогда
		Если Команда <> Неопределено Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Введите штрихкод или наименование товара'"));
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	НоменклатураПоИдентификатору = РаботаСНоменклатуройВызовСервераБП.НоменклатураПоШтрихкоду(
		ПолеПоискаШтрихкодНаименование);
	Если Не НоменклатураПоИдентификатору.Пустая() Тогда
		ПоказатьСообщениеОбОшибкеЗаполненияФормыПоДаннымСервиса(НоменклатураПоИдентификатору);
		Возврат;
	КонецЕсли;
	
	Если Не СервисДоступен Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Нарушение прав доступа'"));
		Возврат;
	КонецЕсли;
	
	Если ИнтернетПоддержкаПодключена Тогда
		ВыполнитьПоискДанныхСервиса();
	Иначе
		Оповещение = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержкуПользователейНажатиеПродолжение", ЭтотОбъект);
		РаботаСНоменклатуройКлиентБП.ПодключитьИнтернетПоддержкуПользователей(Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиЗаполнитьПоШтрихкоду(ДанныеСоСканера)
	
	ПолеПоискаШтрихкодНаименование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСоСканера, "Штрихкод");
	
	Оповещение = Новый ОписаниеОповещения("НайтиЗаполнитьПоШтрихкодуЗавершение", ЭтотОбъект);
	Контекст = Новый Структура("ОповещениеОРезультате", Оповещение);
	Контекст.Вставить("ТекстВопроса",
		НСтр("ru = 'Номенклатура с таким штрих-кодом не найдена. Проискать в сервисе 1С:Номенклатура?'"));
	РаботаСНоменклатуройКлиентБП.ПоискНоменклатурыПоШтрихкодуПослеВводаСтроки(ДанныеСоСканера, Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиЗаполнитьПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РезультатШтрихкод      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Штрихкод");
	РезультатИдентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Идентификатор", "");
	РезультатНоменклатура  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Номенклатура",
		ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	
	Если Не РезультатНоменклатура.Пустая() Тогда
		ПоказатьСообщениеОбОшибкеЗаполненияФормыПоДаннымСервиса(РезультатНоменклатура);
	ИначеЕсли Не ПустаяСтрока(РезультатИдентификатор) Тогда 
		НастроитьФормуПриДлительнойОперации(ЭтотОбъект, Истина);
		
		ДанныеОбъекта = Новый Структура("Идентификатор", РезультатИдентификатор);
		ПодготовитьПараметрыЗаполненияФормыНоменклатуры(ДанныеОбъекта);
	Иначе 
		ПоказатьПредупреждение(, СтрШаблон(НСтр("ru = 'По штрихкоду %1 номенклатура не найдена.'"), РезультатШтрихкод));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьПараметрыЗаполненияФормыНоменклатуры(ДанныеОбъекта)
	
	ПараметрыОбработчика = Новый Структура("ВыводитьОкноОжидания", Ложь);
	
	Оповещение = Новый ОписаниеОповещения("ЗакрытиеФормыЗаполненияОбъекта", ЭтотОбъект);
	РаботаСНоменклатуройКлиент.ПодготовитьПараметрыЗаполненияФормыНоменклатуры(ЭтотОбъект,
		ДанныеОбъекта, Оповещение, ПараметрыОбработчика);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВключитьЗаполнениеПоДаннымСервиса()
	
	ОтключитьЗаполнениеПоДаннымСервиса = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщениеОбОшибкеЗаполненияФормыПоДаннымСервиса(НоменклатураСсылка, ТипОшибки = 1)
	
	Если ТипОшибки = 1 Тогда
		ТекстСообщения = НСтр("ru = 'Штрихкод уже используется для другой номенклатуры'");
	Иначе 
		ТекстСообщения = НСтр("ru = 'Карточка 1С:Номенклатуры уже используется для другой номенклатуры'");
	КонецЕсли;
	
	ФорматированныйТекст = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
	"%1:
	|<a href = %2>%3</a>.", 
	ТекстСообщения, ПолучитьНавигационнуюСсылку(НоменклатураСсылка), Строка(НоменклатураСсылка));
	
	ПоказатьПредупреждение(, ФорматированныйТекст);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСведенияОбОстатке()
	
	ОбновитьСведенияОбОстаткеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОбОстаткеНаСервере()
	ДанныеПоОстаткам = Справочники.Номенклатура.ДанныеПоОстаткам(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка), ИспользуютсяНесколькоСкладов);
		
	ОбщийОстаток = ДанныеПоОстаткам.ОбщийОстаток;

	СтрокиОписанияОстатка = Новый Массив;
	СтрокиОписанияОстатка.Добавить(Формат(ОбщийОстаток, "ЧГ=0"));
	СтрокиОписанияОстатка.Добавить(Строка(Объект.ЕдиницаИзмерения));
	ОбщийОстатокСтрокой = СтрСоединить(СтрокиОписанияОстатка, " ");
	
	Если ИспользуютсяНесколькоСкладов Тогда
		НоваяДекорация = Элементы.ДекорацияОбщийОстатокНесколькоСкладов;
	Иначе
		НоваяДекорация = Элементы.ДекорацияОбщийОстатокОдинСклад;
	КонецЕсли;
	
	НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(ОбщийОстатокСтрокой, ШрифтыСтиля.ШрифтТекстаБаннера);
		
	Если ИспользуютсяНесколькоСкладов 
		И ДанныеПоОстаткам.ОстаткиПоСкладам.Количество() > 0 Тогда
		ВывестиОстаткиПоСкладам(ДанныеПоОстаткам.ОстаткиПоСкладам);
	КонецЕсли;
	
	ОстатокПолучен = Истина;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиОстаткиПоСкладам(ОстаткиПоСкладам)
	
	Для Каждого ОстатокПоСкладу Из ОстаткиПоСкладам Цикл
		Индекс = ОстаткиПоСкладам.Индекс(ОстатокПоСкладу);
		СтрокиОписанияОстатка = Новый Массив;
		СтрокиОписанияОстатка.Добавить(Формат(ОстатокПоСкладу.Количество, "ЧГ=0"));
		СтрокиОписанияОстатка.Добавить(Строка(ОстатокПоСкладу.ЕдиницаИзмерения));
		Остаток = СтрСоединить(СтрокиОписанияОстатка, " ");
		
		НазваниеСклада = Новый ФорматированнаяСтрока(Строка(ОстатокПоСкладу.Склад), ШрифтыСтиля.ОбычныйШрифтТекста);
		
		Группа = Элементы.Найти(СтрШаблон("ГруппаОстатки%1", Индекс));
		
		Если Группа = Неопределено Тогда
			ДобавитьГруппуОстатокНаСкладе(Индекс);
		Иначе
			Группа.Видимость = Истина;
		КонецЕсли;
		
		ДекорацияСклад = Элементы.Найти(СтрШаблон("Склад%1", Индекс));
		ДекорацияОстаток = Элементы.Найти(СтрШаблон("Остаток%1", Индекс));
		
		ДекорацияСклад.Заголовок = НазваниеСклада;
		ДекорацияОстаток.Заголовок = Остаток;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьГруппуОстатокНаСкладе(Индекс)
	
	Группа = Элементы.Найти(СтрШаблон("ГруппаОстатки%1", Индекс));
	
	Если Группа <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Группа = Элементы.Добавить(СтрШаблон("ГруппаОстатки%1", Индекс),
		Тип("ГруппаФормы"), Элементы.ГруппаДетали);
	
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Заголовок = СтрШаблон(НСтр("ru='Группа остатки %1'"), Индекс);
	
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	НоваяДекорация = Элементы.Добавить(СтрШаблон("Склад%1", Индекс),
		Тип("ДекорацияФормы"), Группа);
	
	НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
	НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
	НоваяДекорация.МаксимальнаяШирина = 20;
	НоваяДекорация.РастягиватьПоГоризонтали = Истина;
	НоваяДекорация.Высота = 1;
	НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
	
	НоваяДекорация = Элементы.Добавить(СтрШаблон("Остаток%1", Индекс),
		Тип("ДекорацияФормы"), Группа);
	
	НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
	НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
	НоваяДекорация.МаксимальнаяШирина = 10;
	НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
	НоваяДекорация.Высота = 1;
	НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;

КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаОНастройкеОсновнаяСтавкаНДСЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") И ЗначениеЗаполнено(РезультатВыполнения) Тогда
		
		УстановитьОсновнуюСтавкуНДС(РезультатВыполнения.ВидСтавкиНДС, ТекущаяДатаПользователя);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьОсновнуюСтавкуНДС(ВидСтавкиНДС, ДатаПользователя)
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнаяСтавкаНДС",
		Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавкиНДС, ДатаПользователя));

КонецПроцедуры


#КонецОбласти

&НаСервереБезКонтекста
Функция ВедетсяСкладскойУчет()
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ОбщегоНазначенияБП.ОпределитьПараметрыУчета().СкладскойУчет <> 0;
КонецФункции 

&НаСервере
Процедура УстановитьПрослеживаемостьПослеРедактированияИстории(НаборЗаписей)
	
	Модифицированность = Истина;
	ЕстьИсторияРеквизитовПрослеживаемости = Ложь;
	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияПрослеживаемогоТовара.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
				ЗаписьИстории = Объект.ИсторияПрослеживаемогоТовара.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
				Если ЗаписьНабора.Период <= ТекущаяДатаПользователя Тогда
					АктуальныеРеквизитыПрослеживаемости = Новый Структура("ПрослеживаемыйТовар, ПрослеживаемыйКомплект", 
					ЗаписьНабора.ПрослеживаемыйТовар, ЗаписьНабора.ПрослеживаемыйКомплект);
				КонецЕсли;
		КонецЦикла;
		
		ЕстьИсторияРеквизитовПрослеживаемости = Истина;
		
	КонецЕсли;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		Объект.ПрослеживаемыйТовар = НаборЗаписей[НаборЗаписей.Количество()-1].ПрослеживаемыйТовар;
		Объект.ПрослеживаемыйКомплект = НаборЗаписей[НаборЗаписей.Количество()-1].ПрослеживаемыйКомплект;
	КонецЕсли;
	
	Если ЕстьИсторияРеквизитовПрослеживаемости Тогда
		Объект.ПрослеживаемыйТовар    = АктуальныеРеквизитыПрослеживаемости.ПрослеживаемыйТовар;
		Объект.ПрослеживаемыйКомплект = АктуальныеРеквизитыПрослеживаемости.ПрослеживаемыйКомплект;
	КонецЕсли;
	
	Элементы.ВесИзСертификата.АвтоОтметкаНезаполненного = Объект.ВесПоСертификатуТовара = 0
			И Объект.ПрослеживаемыйТовар
			И ЗначениеЗаполнено(Объект.КодТНВЭД);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МногострочныйРежимОперации0 (Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	МинимальнаяДлинаСтрокиМногострочногоРежима = 53;
		
	Если СтрДлина(Строка(Объект.Операции0)) > МинимальнаяДлинаСтрокиМногострочногоРежима Тогда
		Элементы.Операции0.МногострочныйРежим = Истина;
	Иначе
		Элементы.Операции0.МногострочныйРежим = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОперации0(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Объект.Операции0 = Результат;
		МногострочныйРежимОперации0(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункцииБСП


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	ВидНоменклатурыПриИзмененииНаСервере();
	УправлениеФормой(ЭтаФорма);
	НастроитьВидимостьГруппыАкцизы();
	
КонецПроцедуры

&НаСервере
Процедура ВидНоменклатурыПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
		Объект.ВидНоменклатуры = ОсновнойВидНоменклатуры;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
		Объект.Услуга = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидНоменклатуры, "Услуга");
	Иначе
		Объект.Услуга = Ложь;
	КонецЕсли;
	
	ПолучитьСвойстваВидаНоменклатуры();
	
	Если Объект.Услуга Тогда
		Объект.СтатьяЗатрат = Неопределено;
		Объект.КодОКП = Неопределено;
		Объект.КодТНВЭД = Неопределено;
		ОКПНаименование = "";
		ТНВЭДНаименование = "";
		ВидНоменклатурнойКлассификацииНаименование = "";
		
		Объект.ВидМаркировки = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПустаяСсылка");
	Иначе
		Объект.ПериодичностьУслуги = Неопределено;
		Объект.УслугаРазмещения    = Неопределено;
	КонецЕсли;
	
	Если ЕстьУчетПодакцизныхТоваров И ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
		ВидТовара = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидНоменклатуры, "ВидПодакцизногоТовара");
		Объект.КодВидаПодакцизногоТовара = ПредопределенноеЗначение("Справочник.КодыВидовПодакцизныхТоваров.ПустаяСсылка");
	Иначе
		ВидТовара = ПредопределенноеЗначение("Перечисление.ВидыПодакцизныхТоваров.ПустаяСсылка");
	КонецЕсли;
	
	Если ЕстьУчетПодакцизныхТоваров И ЗначениеЗаполнено(ВидТовара) Тогда
		Объект.КоэффициентПересчета = 0;
		УчетАкцизов.ЗаполнитьКодВидаПодакцизногоТовараПоУмолчанию(ВидТовара, Объект.КодВидаПодакцизногоТовара);
		Если ЗначениеЗаполнено(Объект.КодВидаПодакцизногоТовара) Тогда
			УчетАкцизовВызовСервера.ПолучитьЕдиницуИзмеренияПоСтавкеАкциза(Объект.КодВидаПодакцизногоТовара, Объект.ЕдиницаИзмеренияДляРасчетаАкциза);
		КонецЕсли;
		НастроитьВидимостьКоэффициентаПересчетаПоАкцизам();
	КонецЕсли;
	
	ЗаполнитьАктуальныеЦены();
	
КонецПроцедуры

&НаСервере
Процедура ВывестиОписаниеМаркируемогоТовараНаСервере()
	
	ПараметрыОписанияМаркируемогоТовара = ПараметрыОписанияМаркируемогоТовара(Объект.Ссылка, Объект.ВидМаркировки);
	ОписаниеМаркируемогоТовара = ИнтеграцияИСВызовСервера.ПредставлениеНастройкиПараметровНоменклатуры(ПараметрыОписанияМаркируемогоТовара);
	
	ДополнительныеРеквизитыМаркировкиВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСвойстваВидаНоменклатуры()
	
	СписокРеквизитов = "ПолучаемОтПоставщика, Производим, Комплектуем, НаПродажу, ВПроизводство, СобственныеНужды, ВЭксплуатациюСотрудникам";
	РеквизитыВидаНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидНоменклатуры, СписокРеквизитов);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РеквизитыВидаНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаПодакцизногоТовараПриИзменении(Элемент)
	Объект.КоэффициентПересчета = 0;
	Если ЗначениеЗаполнено(Объект.КодВидаПодакцизногоТовара) Тогда
		УчетАкцизовВызовСервера.ПолучитьЕдиницуИзмеренияПоСтавкеАкциза(Объект.КодВидаПодакцизногоТовара, Объект.ЕдиницаИзмеренияДляРасчетаАкциза);
	КонецЕсли;
	
	НастроитьВидимостьКоэффициентаПересчетаПоАкцизам();
КонецПроцедуры

#КонецОбласти

Процедура НастроитьВидимостьГруппыАкцизы()
	
	Если ЗначениеЗаполнено(ВидТовара) И ЕстьУчетПодакцизныхТоваров Тогда
		Элементы.ГруппаАкцизы.Видимость = Истина;
	Иначе
		Элементы.ГруппаАкцизы.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьКоэффициентаПересчетаПоАкцизам()
	
	Если ЗначениеЗаполнено(Объект.КодВидаПодакцизногоТовара) Тогда
		Если ВидТовара = ПредопределенноеЗначение("Перечисление.ВидыПодакцизныхТоваров.АвтомобилиИМотоциклы") Тогда
			Элементы.ЕдиницаИзмеренияДляРасчетаАкциза.Видимость = Истина;
			Элементы.ГруппаКоэффициента.Видимость = Истина;
			Элементы.КоэффициентПересчета.Заголовок = НСтр("ru = 'Количество '") + Объект.ЕдиницаИзмеренияДляРасчетаАкциза;
			Элементы.НадписьКоэффициент.Заголовок = "";
		Иначе
			Элементы.ЕдиницаИзмеренияДляРасчетаАкциза.Видимость = Ложь;
			Если (ЗначениеЗаполнено(Объект.КоэффициентПересчета)
				Или ЗначениеЗаполнено(Объект.ЕдиницаИзмеренияДляРасчетаАкциза))
				И Объект.ЕдиницаИзмеренияДляРасчетаАкциза <> Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка()
				И Объект.ЕдиницаИзмеренияДляРасчетаАкциза <> Объект.ЕдиницаИзмерения Тогда
				Элементы.ГруппаКоэффициента.Видимость = Истина;
				Элементы.КоэффициентПересчета.Заголовок = НСтр("ru = 'В 1 '") + Объект.ЕдиницаИзмерения + ".";
				Элементы.НадписьКоэффициент.Заголовок = "" + Объект.ЕдиницаИзмеренияДляРасчетаАкциза + ".";
				
			Иначе
				Элементы.ГруппаКоэффициента.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Элементы.ГруппаКоэффициента.Видимость               = Ложь;
		Элементы.ЕдиницаИзмеренияДляРасчетаАкциза.Видимость = Ложь;
	КонецЕсли;
	Элементы.КоэффициентПересчета.ОтметкаНезаполненного = Элементы.ГруппаКоэффициента.Видимость
		И Не ЗначениеЗаполнено(Объект.КоэффициентПересчета);
	
КонецПроцедуры

&НаСервере
Процедура УточнитьПредставлениеСтавкиНДСПриУСН()
	
	Если НЕ РегистрыСведений.НастройкиУчетаНДС.ВедетсяУчетНДСНаУСН() Тогда
		Возврат;
	КонецЕСли;
	
	Элементы.СтавкаНДС.Ширина = 15;
	Элементы.СтавкаНДС.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
	Для каждого ЭлементСписка Из Элементы.СтавкаНДС.СписокВыбора Цикл 
		ЭлементСписка.Представление = УчетНДСКлиентСервер.ТекстовоеПредставлениеСтавкиНДС(
			ЭлементСписка.Значение, ТекущаяДатаПользователя);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьНоменклатуруВАрхиве(Форма)
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если НЕ Объект.Ссылка.Пустая() 
		ИЛИ (НЕ ЗначениеЗаполнено(ОБъект.Наименование) 
		И НЕ ЗначениеЗаполнено(Объект.НаименованиеПолное))
		ИЛИ НЕ Форма.ИспользоватьСкрытиеНоменклатуры Тогда
		
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		Отбор.Вставить("НаименованиеПолное", Объект.НаименованиеПолное);
	Иначе
		Отбор.Вставить("Наименование", Объект.Наименование);
	КонецЕсли;
	Отбор.Вставить("ВидНоменклатуры", Объект.ВидНоменклатуры);

	Форма.НоменклатураВАрхиве = РаботаСНоменклатуройВызовСервераБП.НоменклатураВАрхиве(Отбор);
	Элементы.Наименование.ОтображениеПодсказки = ?(НЕ ЗначениеЗаполнено(Форма.НоменклатураВАрхиве), ОтображениеПодсказки.Нет, ОтображениеПодсказки.ОтображатьСнизу);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОстаткиНоменклатурыНаСервере(НоменклатураСсылка)
	Результат = Новый Структура;
	
	ОстаткиТоваров = Справочники.Номенклатура.ДанныеПоОстаткам(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НоменклатураСсылка), Ложь);
		
	Возврат ОстаткиТоваров.ОбщийОстаток;
КонецФункции

&НаКлиенте
асинх Процедура Подключаемый_ПроверитьОстатки() Экспорт
	Остаток = ОстаткиНоменклатурыНаСервере(Объект.Ссылка);
	Если Остаток = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = '
		|По данным учета, в наличии имеется %1 %2.
		|
		|Все равно скрыть?'");
		
	ТекстВопроса = СтрШаблон(ТекстВопроса, Формат(Остаток, "ЧЦ=12.5"), Объект.ЕдиницаИзмерения);
	
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 15);
	СкрыватьВСписках = Ответ = КодВозвратаДиалога.Да;
КонецПроцедуры 

&НаКлиенте
Процедура ВидМаркировкиПриИзменении(Элемент)
	
	ПараметрыОписанияМаркируемогоТовара = ПараметрыОписанияМаркируемогоТовара(Объект.Ссылка, Объект.ВидМаркировки);
	ОписаниеМаркируемогоТовара = ИнтеграцияИСВызовСервера.ПредставлениеНастройкиПараметровНоменклатуры(ПараметрыОписанияМаркируемогоТовара);
    ПрочитатьСведенияОбАлкогольнойПродукции();
	ДополнительныеРеквизитыМаркировкиВидимость();
	Если Объект.ПрослеживаемыйТовар Тогда
		Объект.ПрослеживаемыйТовар = Ложь;
	КонецЕсли;
	Если Объект.ПрослеживаемыйКомплект Тогда
		Объект.ПрослеживаемыйКомплект = Ложь;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоАлкогольнаяПродукция(ВидМаркировки)

	Возврат (ВидМаркировки = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")
		ИЛИ ВидМаркировки = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов")
		ИЛИ ВидМаркировки = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
		ИЛИ ВидМаркировки = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках"));
		
КонецФункции

&НаКлиенте
Процедура КодВидаАлкогольнойПродукцииПриИзменении(Элемент)
	
	ЭлементСписка = Элемент.СписокВыбора.НайтиПоЗначению(КодВидаАлкогольнойПродукции);
	Если ЭлементСписка <> Неопределено Тогда
		НаименованиеВидаАлкогольнойПродукции = СтрЗаменить(ЭлементСписка.Представление, КодВидаАлкогольнойПродукции + " - ", "");
	Иначе
		НаименованиеВидаАлкогольнойПродукции = "";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтранаПроисхожденияПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

#Область РаботаСЦенами

&НаСервере
Процедура ЗаполнитьАктуальныеЦены()
	
	Если (Не ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.ЦеныНоменклатурыДокументов))
		ИЛИ (Не ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.ЦеныНоменклатуры)) Тогда
		Возврат;
	КонецЕсли;
	
	АктуальныеЦены.Очистить();
	АктуальныеЦеныУдалитьЭлементы();
	КоличествоЦенНаФорме = 10;
	
	ОсновнойТипЦеныПродажи = Неопределено;
	ОсновнойТипЦеныЗакупки = Неопределено;
	ТипПлановыхЦен         = Неопределено;
	
	ЕстьЦеныПоУмолчанию    = Ложь;
	ЕстьЦены               = Ложь;
	
	НастройкаЦеныПродажи  = Константы.НастройкаЗаполненияЦеныПродажи.Получить();
	Если ПравоДоступа("Просмотр", Метаданные.Константы.НастройкаЗаполненияЦеныПокупки) Тогда
		НастройкаЦеныПокупки = Константы.НастройкаЗаполненияЦеныПокупки.Получить();
		Если НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен Тогда
			ОсновнойТипЦеныЗакупки = Константы.ОсновнойТипЦеныПокупки.Получить();
		КонецЕсли;
	Иначе
		НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен;
	КонецЕсли;
		
	ИспользоватьТипыЦенНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры");
	
	ЦенаПокупки = 0;
	ЦенаПродажи = 0;
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаЦеныПокупки              = ВалютаРегламентированногоУчета;
	ВалютаЦены                     = ВалютаРегламентированногоУчета;
	
	Если НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен Тогда
		ОсновнойТипЦеныПродажи = Константы.ОсновнойТипЦеныПродажи.Получить();
	КонецЕсли;
	Если ПравоДоступа("Просмотр", Метаданные.Константы.ТипЦенПлановойСебестоимостиНоменклатуры) Тогда
		ТипПлановыхЦен = Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить();
	КонецЕсли;
	
	//Обработаем ситуацию, когда один и тот же тип цен указан в нескольких настройках
	//Один тип цен должен отображаться только один раз
	Если ОсновнойТипЦеныЗакупки = ТипПлановыхЦен 
		ИЛИ ОсновнойТипЦеныЗакупки = ОсновнойТипЦеныПродажи Тогда
		ОсновнойТипЦеныЗакупки = Неопределено;
	ИначеЕсли ТипПлановыхЦен = ОсновнойТипЦеныПродажи Тогда
		ТипПлановыхЦен = Неопределено;
	КонецЕсли;
	
	ПолучитьЦеныНоменклатурыДокументов(ЦенаПокупки, ЦенаПродажи, ВалютаЦеныПокупки, ВалютаЦены,
		НастройкаЦеныПокупки, НастройкаЦеныПродажи);
		
	Если НЕ НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен И НаПродажу Тогда
		
		ЕстьЦеныПоУмолчанию = Истина;
		ВывестиЦенуПродажиПоУмолчанию(ЦенаПродажи, ВалютаЦены, НастройкаЦеныПродажи);
		
	КонецЕсли;
	
	Если НЕ НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен И ПолучаемОтПоставщика Тогда
		
		ЕстьЦеныПоУмолчанию = Истина;
		ВывестиЦенуПокупкиПоУмолчанию(ЦенаПокупки, ВалютаЦеныПокупки, НастройкаЦеныПокупки);
		
	КонецЕсли;
	
	Если ИспользоватьТипыЦенНоменклатуры Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Номенклатура", Объект.Ссылка);
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
		|	ЦеныНоменклатурыСрезПоследних.Валюта КАК Валюта,
		|	ЦеныНоменклатурыСрезПоследних.ТипЦен КАК ТипЦен,
		|	ЦеныНоменклатурыСрезПоследних.Регистратор КАК Регистратор,
		|	ЦеныНоменклатурыСрезПоследних.Период КАК Период,
		|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ЦеныНоменклатурыСрезПоследних.ТипЦен) КАК ТипЦенУИД
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ТекущаяДата, Номенклатура = &Номенклатура) КАК ЦеныНоменклатурыСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
		
		Цены = Запрос.Выполнить().Выгрузить();
		
		ПозицияОсновнойЦены = 0;
		ОбработатьОсновныеЦены(Цены, ОсновнойТипЦеныПродажи, ПозицияОсновнойЦены, НаПродажу);
		ОбработатьОсновныеЦены(Цены, ОсновнойТипЦеныЗакупки, ПозицияОсновнойЦены, ПолучаемОтПоставщика);
		ОбработатьОсновныеЦены(Цены, ТипПлановыхЦен, ПозицияОсновнойЦены, Производим);
		
		СоставСтроки = Новый Массив;
		Цвет         = ЦветаСтиля.ЦветТекстаФормы;
		Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
			Штифт        = Новый Шрифт();
		Иначе
			Штифт        = Новый Шрифт(,10);
		КонецЕсли;
		Итератор         = 0;
		ФорматнаяСтрока  = "";
		
		Для каждого Цена Из Цены Цикл
			
			НоваяСтрока = АктуальныеЦены.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Цена);
			
			ГруппаЭлементов = Элементы.АктуальныеЦены;
			СоставСтроки.Очистить();
			
			Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
				ПредставлениеЦены = Строка(Цена.ТипЦен);
				СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЦены, Штифт, Цвет));
				Если ОсновнойТипЦеныПродажи = Цена.ТипЦен Тогда
					ГруппаЭлементов = ПолучитьСоздатьГруппаЦенПоУмолчанию("ГруппаЦенаПродажиВКарточке");
					КоличествоЦенНаФорме = КоличествоЦенНаФорме + 1;
					ЕстьЦеныПоУмолчанию = Истина;
				ИначеЕсли ОсновнойТипЦеныЗакупки = Цена.ТипЦен Тогда
					ГруппаЭлементов = ПолучитьСоздатьГруппаЦенПоУмолчанию("ГруппаЦенаПокупкиВКарточке");
					КоличествоЦенНаФорме = КоличествоЦенНаФорме + 1;
					ЕстьЦеныПоУмолчанию = Истина;
				ИначеЕсли ТипПлановыхЦен = Цена.ТипЦен Тогда
					ГруппаЭлементов = ПолучитьСоздатьГруппаЦенПоУмолчанию("ГруппаЦенаПлановаяВКарточке");
					КоличествоЦенНаФорме = КоличествоЦенНаФорме + 1;
					ЕстьЦеныПоУмолчанию = Истина;
				Иначе
					ЕстьЦены = Истина;
				КонецЕсли;
				
			Иначе
				Если ОсновнойТипЦеныПродажи = Цена.ТипЦен Тогда
					ПредставлениеЦены = СтрШаблон(НСтр("ru = 'Продажа (тип цен %1)'"), Строка(Цена.ТипЦен));
					ГруппаЭлементов = Элементы.АктуальныеЦеныПоУмолчанию;
					КоличествоЦенНаФорме = КоличествоЦенНаФорме + 1;
					СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЦены, Штифт, Цвет));
					ЕстьЦеныПоУмолчанию = Истина;
				ИначеЕсли ОсновнойТипЦеныЗакупки = Цена.ТипЦен Тогда
					ПредставлениеЦены = СтрШаблон(НСтр("ru = 'Покупка (тип цен %1)'"), Строка(Цена.ТипЦен));
					ГруппаЭлементов = Элементы.АктуальныеЦеныПоУмолчанию;
					КоличествоЦенНаФорме = КоличествоЦенНаФорме + 1;
					СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЦены, Штифт, Цвет));
					ЕстьЦеныПоУмолчанию = Истина;
				ИначеЕсли ТипПлановыхЦен = Цена.ТипЦен Тогда
					ПредставлениеЦены = СтрШаблон(НСтр("ru = 'Плановая (тип цен %1)'"), Строка(Цена.ТипЦен));
					ГруппаЭлементов = Элементы.АктуальныеЦеныПоУмолчанию;
					КоличествоЦенНаФорме = КоличествоЦенНаФорме + 1;
					СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЦены, Штифт, Цвет));
					ЕстьЦеныПоУмолчанию = Истина;
				Иначе
					ПредставлениеЦены = Строка(Цена.ТипЦен);
					СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЦены, Штифт, Цвет));
					ЕстьЦены = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Группа = Элементы.Добавить("ГруппаАктуальныеЦены" + ИтераторСтрокой(Итератор), Тип("ГруппаФормы"), ГруппаЭлементов);
			Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
			Группа.ОтображатьЗаголовок = Ложь;
			Группа.Объединенная = Истина;
			Группа.СквозноеВыравнивание = СквозноеВыравнивание.Авто;
			Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
			Группа.ВажностьПриОтображении = ВажностьПриОтображении.Авто;
			Группа.Ширина = 40;
			Группа.ОтображатьОтступСлева = Ложь;
			
			НоваяДекорация = Элементы.Добавить("АктуальнаяЦенаТипЦен" + ИтераторСтрокой(Итератор), Тип("ДекорацияФормы"), Группа);
			НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
			НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(СоставСтроки, Штифт);
			НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
			НоваяДекорация.МаксимальнаяШирина = 22;
			НоваяДекорация.РастягиватьПоГоризонтали = Истина;
			НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
			
			СоставСтроки.Очистить();
			ФорматнаяСтрока	= "ЧДЦ=2; ЧН=0,00";
			ФорматированноеЧисло = Формат(Цена.Цена, ФорматнаяСтрока);
			СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ФорматированноеЧисло,, ЦветаСтиля.ЦветГиперссылки,, Строка(Цена.ТипЦенУИД)));
			
			НоваяДекорация = Элементы.Добавить("АктуальнаяЦенаЗначениеЦены" + ИтераторСтрокой(Итератор), Тип("ДекорацияФормы"), Группа);
			НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
			
			НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(СоставСтроки, Штифт);
			НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
			НоваяДекорация.МаксимальнаяШирина = 10;
			НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
			НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
			НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
			НоваяДекорация.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_ЗаголовокЗначенияЦенОбработкаНавигационнойСсылки");
			
			СоставСтроки.Очистить();
			ФорматированноеЧисло = Формат(Цена.Цена, ФорматнаяСтрока);
			СоставСтроки.Добавить(Новый ФорматированнаяСтрока(" " + Цена.Валюта, ));
			
			НоваяДекорация = Элементы.Добавить("АктуальнаяЦенаВалютаЦены" + ИтераторСтрокой(Итератор), Тип("ДекорацияФормы"), Группа);
			НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
			НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(СоставСтроки, Штифт);
			НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
			НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
			НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
			
			Итератор = Итератор + 1;
			
			Если Итератор = КоличествоЦенНаФорме Тогда
				Прервать
			КонецЕсли;
			
		КонецЦикла;
		
		Если КоличествоЦенНаФорме < Цены.Количество() Тогда
			Элементы.ДекорацияВсеЦены.Заголовок = СтрШаблон(НСтр("ru = 'Все цены (%1)'"), Строка(Цены.Количество()));
		Иначе
			Элементы.ДекорацияВсеЦены.Заголовок = "";
		КонецЕсли;
	
	КонецЕсли;
	
	Если ЕстьЦеныПоУмолчанию Тогда
		Элементы.ГруппаЦены.Заголовок = НСтр("ru = 'Прочие цены'");
	Иначе
		Элементы.ГруппаЦены.Заголовок = НСтр("ru = 'Цены'");
	КонецЕсли;
	
	Элементы.АктуальныеЦеныПодвал.Видимость       = ИспользоватьТипыЦенНоменклатуры;
	Элементы.ДекорацияЦеныПоУмолчанию.Видимость   = ЕстьЦеныПоУмолчанию;
	Элементы.ДекорацияЦеныНеУстановлены.Видимость = ИспользоватьТипыЦенНоменклатуры И (НЕ ЕстьЦеныПоУмолчанию) И (НЕ ЕстьЦены);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЦеныНоменклатурыДокументов(ЦенаПокупки, ЦенаПродажи, 
				ВалютаЦеныПокупки, ВалютаЦены, НастройкаЦеныПокупки, НастройкаЦеныПродажи)
	
	//Проверим, нужно ли получить цены из регистра Цены номенклатуры документов
	Если НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура
		ИЛИ НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.ПредыдущийДокумент
		ИЛИ НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.ПредыдущийДокумент Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Номенклатура", Объект.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыДокументов.Цена КАК Цена,
		|	ЦеныНоменклатурыДокументов.Валюта КАК Валюта,
		|	ЦеныНоменклатурыДокументов.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ЦеныНоменклатурыДокументов.Номенклатура.ВидСтавкиНДС КАК ВидСтавкиНДС,
		|	ЦеныНоменклатурыДокументов.СпособЗаполненияЦены КАК СпособЗаполненияЦены
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатурыДокументов КАК ЦеныНоменклатурыДокументов
		|ГДЕ
		|	ЦеныНоменклатурыДокументов.Номенклатура = &Номенклатура";
		
		ВыборкаДанных = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаДанных.Следующий() Цикл
			
			Если ВыборкаДанных.СпособЗаполненияЦены  = Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам 
				И (НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура
				ИЛИ НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.ПредыдущийДокумент) Тогда
				
				Если Не ВыборкаДанных.ЦенаВключаетНДС  Тогда
					// В форме номенклатуры цену всегда показываем окончательную для покупателя - т.е. с НДС
					СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ВыборкаДанных.ВидСтавкиНДС, ОбщегоНазначения.ТекущаяДатаПользователя());
					ЗначениеСтавкиНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДС, Ложь);
					ЦенаПродажи = ВыборкаДанных.Цена * (1 + ЗначениеСтавкиНДС / 100);
				Иначе
					ЦенаПродажи = ВыборкаДанных.Цена;
				КонецЕсли;
				ВалютаЦены = ВыборкаДанных.Валюта;
			ИначеЕсли ВыборкаДанных.СпособЗаполненияЦены  = Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам
				И НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.ПредыдущийДокумент Тогда
				
				ЦенаПокупки = ВыборкаДанных.Цена;
				ВалютаЦеныПокупки = ВыборкаДанных.Валюта;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиЦенуПродажиПоУмолчанию(ЦенаПродажи, ВалютаЦены, НастройкаЦеныПродажи)
		
	СоставСтроки = Новый Массив;
	Цвет         = ЦветаСтиля.ЦветТекстаФормы;
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Штифт        = Новый Шрифт();
	Иначе
		Штифт        = Новый Шрифт(,10);
	КонецЕсли;
	
	Группа = ПолучитьСоздатьГруппаЦенПоУмолчанию("ГруппаЦенаПродажиВКарточке");
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Если НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.НеЗаполнять Тогда
			ПредставлениеЦены = Строка(НСтр("ru = '<не заполнять>'"));
		ИначеЕсли НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура Тогда
			ПредставлениеЦены = Строка(НСтр("ru = 'Цена из карточки'"));
		Иначе
			ПредставлениеЦены = Строка(НСтр("ru = 'Последняя цена'"));
		КонецЕсли;
	Иначе
		Если НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.НеЗаполнять Тогда
			ПредставлениеЦены = Строка(НСтр("ru = 'Продажа (не заполнять)'"));
		ИначеЕсли НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура Тогда
			ПредставлениеЦены = Строка(НСтр("ru = 'Продажа (цена из карточки)'"));
		Иначе
			ПредставлениеЦены = Строка(НСтр("ru = 'Продажа (последняя цена)'"));
		КонецЕсли;
	КонецЕсли;
	
	СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЦены, Штифт, Цвет));
	
	НоваяДекорация = Элементы.Добавить("ДекорацияЦенаПродажиВКарточке", Тип("ДекорацияФормы"), Группа);
	НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
	НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(СоставСтроки, Штифт);
	НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
	НоваяДекорация.МаксимальнаяШирина = 22;
	НоваяДекорация.РастягиватьПоГоризонтали = Истина;
	НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
	
	Если НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.НеЗаполнять
		ИЛИ НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.ПредыдущийДокумент Тогда
		НоваяДекорация = Элементы.Добавить("ЗначениеЦеныПродажиВКарточке", Тип("ДекорацияФормы"), Группа);
		НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
		НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
		НоваяДекорация.МаксимальнаяШирина = 10;
		НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
		НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		НоваяДекорация.Заголовок = Формат(ЦенаПродажи, "ЧДЦ=2; ЧН=0,00");
	Иначе
		НоваяДекорация = Элементы.Добавить("ЗначениеЦеныПродажиВКарточке", Тип("ПолеФормы"), Группа);
		НоваяДекорация.Вид = ВидПоляФормы.ПолеВвода;
		НоваяДекорация.ПутьКДанным = "ЦенаПродажи";
		НоваяДекорация.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
		НоваяДекорация.МаксимальнаяШирина = 10;
		НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
		НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
		НоваяДекорация.УстановитьДействие("ПриИзменении", "ЦенаПродажиИзНоменклатурыПриИзменении");
	КонецЕсли;
	
	СоставСтроки.Очистить();
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет") 
		И НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура Тогда
		СоставСтроки.Добавить(Новый ФорматированнаяСтрока(" " + ВалютаЦены, ));
		НоваяДекорация = Элементы.Добавить("ВалютаЦеныПродажиВКарточке", Тип("ПолеФормы"), Группа);
		НоваяДекорация.Вид = ВидПоляФормы.ПолеВвода;
		НоваяДекорация.ПутьКДанным = "ВалютаЦены";
		НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
		НоваяДекорация.МаксимальнаяШирина = 8;
		НоваяДекорация.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НоваяДекорация.УстановитьДействие("ПриИзменении", "ВалютаЦеныПродажиСВалютамиПриИзменении");
		Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
			НоваяДекорация.МаксимальнаяШирина = 1;
			НоваяДекорация.Ширина = 1;
			НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
			НоваяДекорация.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
		КонецЕсли;
	Иначе
		СоставСтроки.Добавить(Новый ФорматированнаяСтрока(" " + ВалютаЦены, ));
		НоваяДекорация = Элементы.Добавить("ВалютаЦеныПродажиВКарточке", Тип("ДекорацияФормы"), Группа);
		НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
		НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(СоставСтроки, Штифт);
	КонецЕсли;
	
	НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
	НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
		
КонецПроцедуры

&НаСервере
Процедура ВывестиЦенуПокупкиПоУмолчанию(ЦенаПокупки, ВалютаЦеныПокупки, НастройкаЦеныПокупки)

	СоставСтроки = Новый Массив;
	Цвет         = ЦветаСтиля.ЦветТекстаФормы;
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Штифт        = Новый Шрифт();
	Иначе
		Штифт        = Новый Шрифт(,10);
	КонецЕсли;
	
	Группа = ПолучитьСоздатьГруппаЦенПоУмолчанию("ГруппаЦенаПокупкиВКарточке");
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Если НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.НеЗаполнять Тогда
			ПредставлениеЦены = НСтр("ru = '<не заполнять>'");
		Иначе
			ПредставлениеЦены = НСтр("ru = 'Последняя цена'");
		КонецЕсли;
	Иначе
		Если НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.НеЗаполнять Тогда
			ПредставлениеЦены = НСтр("ru = 'Покупка (не заполнять)'");
		Иначе
			ПредставлениеЦены = НСтр("ru = 'Покупка (последняя цена)'");
		КонецЕсли;
	КонецЕсли;
	
	СоставСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЦены, Штифт, Цвет));
		
	НоваяДекорация = Элементы.Добавить("ДекорацияЦенаПокупкиВКарточке", Тип("ДекорацияФормы"), Группа);
	НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
	НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(СоставСтроки, Штифт);
	НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
	НоваяДекорация.МаксимальнаяШирина = 22;
	НоваяДекорация.РастягиватьПоГоризонтали = Истина;
	НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
	
	НоваяДекорация = Элементы.Добавить("ЗначениеЦеныПокупкиВКарточке", Тип("ДекорацияФормы"), Группа);
	НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
	НоваяДекорация.АвтоМаксимальнаяШирина = Ложь;
	НоваяДекорация.МаксимальнаяШирина = 10;
	НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
	НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	НоваяДекорация.Заголовок = Формат(ЦенаПокупки, "ЧДЦ=2; ЧН=0,00");
	
	СоставСтроки.Очистить();
	СоставСтроки.Добавить(Новый ФорматированнаяСтрока(" " + ВалютаЦеныПокупки, ));
	НоваяДекорация = Элементы.Добавить("ВалютаЦеныПокупкиВКарточке", Тип("ДекорацияФормы"), Группа);
	НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
	НоваяДекорация.Заголовок = Новый ФорматированнаяСтрока(СоставСтроки, Штифт);
	
	НоваяДекорация.РастягиватьПоГоризонтали = Ложь;
	НоваяДекорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	НоваяДекорация.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
		
КонецПроцедуры

&НаСервере
Процедура ОбработатьОсновныеЦены(Цены, ОсновнойТипЦены, ПозицияОсновнойЦены, ПризнакЦены)
	
	Если ЗначениеЗаполнено(ОсновнойТипЦены) Тогда
		СтрокаОсновнойЦены = Цены.Найти(ОсновнойТипЦены, "ТипЦен");
		Если ЗначениеЗаполнено(СтрокаОсновнойЦены) Тогда
			СдвигСтроки = Макс(Цены.Индекс(СтрокаОсновнойЦены) - ПозицияОсновнойЦены, 0);
			Если СдвигСтроки > 0 Тогда
				Цены.Сдвинуть(Цены.Индекс(СтрокаОсновнойЦены), -СдвигСтроки);
			КонецЕсли;
			ПозицияОсновнойЦены = ПозицияОсновнойЦены + 1;
		ИначеЕсли ПризнакЦены Тогда
			НоваяСтрока = Цены.Вставить(ПозицияОсновнойЦены);
			ПозицияОсновнойЦены = ПозицияОсновнойЦены + 1;
			НоваяСтрока.Номенклатура = Объект.Ссылка;
			НоваяСтрока.Цена   = 0;
			НоваяСтрока.ТипЦен = ОсновнойТипЦены;
			НоваяСтрока.Валюта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнойТипЦены, "ВалютаЦены");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаголовокЗначенияЦенОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТипЦен = ТипЦенПоИдентификатору(НавигационнаяСсылкаФорматированнойСтроки);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СозданиеЦеныПоказатьВопрос(ТипЦен);
	Иначе
		СозданиеЦеныОткрытьФормуЗаписи(ТипЦен);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаголовокИсторииЦеныОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТипЦен = ТипЦенПоИдентификатору(НавигационнаяСсылкаФорматированнойСтроки);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыОткрытия =  Новый Структура("Период, Номенклатура, ТипЦен, Валюта, Регистратор, Цена");
		ПараметрыОткрытия.Номенклатура = Объект.Ссылка;
		Если ЗначениеЗаполнено(ТипЦен) Тогда
			Строки = АктуальныеЦены.НайтиСтроки(Новый Структура("ТипЦен", ТипЦен));
			Если Строки.Количество() = 0 Тогда
				Возврат;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, Строки[0]);
			ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеЦеныЗавершение", ЭтотОбъект);
			ОткрытьФорму("Справочник.Номенклатура.Форма.ИсторияЦены", ПараметрыОткрытия,,,,,ОписаниеОповещения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеЦеныЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьАктуальныеЦены();
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеЦеныПоказатьВопрос(ТипЦен)
	
	ТекстВопроса = НСтр("ru='Перед установкой цены необходимо записать объект. Записать?'");
	
	ПоказатьВопрос(Новый ОписаниеОповещения("СозданиеЦеныОбработатьОтвет", ЭтотОбъект, ТипЦен), ТекстВопроса,
		РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеЦеныОбработатьОтвет(Результат, ТипЦен) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Записать();
	Иначе
		Возврат;
	КонецЕсли;
	
	СозданиеЦеныОткрытьФормуЗаписи(ТипЦен);
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеЦеныОткрытьФормуЗаписи(ТипЦен)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыОткрытия =  Новый Структура("Период, Номенклатура, ТипЦен, Валюта, Регистратор, Цена");
		ПараметрыОткрытия.Номенклатура = Объект.Ссылка;
		Если ЗначениеЗаполнено(ТипЦен) Тогда
			Строки = АктуальныеЦены.НайтиСтроки(Новый Структура("ТипЦен", ТипЦен));
			Если Строки.Количество() = 0 Тогда
				Возврат;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, Строки[0]);
			ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеЦеныЗавершение", ЭтотОбъект);
			ОткрытьФорму("Справочник.Номенклатура.Форма.ИсторияЦены", ПараметрыОткрытия,,,,,ОписаниеОповещения);
		Иначе
			ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеЦеныЗавершение", ЭтотОбъект);
			ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЦены", ПараметрыОткрытия,,,,,ОписаниеОповещения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АктуальныеЦеныУдалитьЭлементы()
	
	ЭлементыКУдалению = Новый Массив;
	
	Для каждого Элемент Из Элементы.АктуальныеЦены.ПодчиненныеЭлементы Цикл
		Если Элемент = Элементы.ДекорацияЦеныНеУстановлены Тогда
			Продолжить;
		КонецЕсли;
		ЭлементыКУдалению.Добавить(Элемент);
	КонецЦикла; 
	
	Для каждого Элемент Из Элементы.АктуальныеЦеныПоУмолчанию.ПодчиненныеЭлементы Цикл
		ЭлементыКУдалению.Добавить(Элемент);
	КонецЦикла;

	
	Для каждого Элемент Из ЭлементыКУдалению Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ТипЦенПоИдентификатору(ИдентификаторТипаЦен)
	
	Возврат Справочники.ТипыЦенНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторТипаЦен));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИтераторСтрокой(Итератор)
	
	Возврат Формат(Итератор, "ЧГ=0");
	
КонецФункции

&НаСервере
Функция ПолучитьСоздатьГруппаЦенПоУмолчанию(ИмяГруппы)
	
	Группа = Элементы.Найти(ИмяГруппы);
	Если Группа <> Неопределено Тогда
		Возврат Группа;
	КонецЕсли;
	
	Группа = Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), Элементы.АктуальныеЦеныПоУмолчанию);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Объединенная = Истина;
	Группа.СквозноеВыравнивание = СквозноеВыравнивание.Авто;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Группа.ВажностьПриОтображении = ВажностьПриОтображении.Авто;
	Группа.Ширина = 40;
	Группа.ОтображатьОтступСлева = Ложь;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Если СтрНайти(НРег(ИмяГруппы), "ценапокупки") Тогда
			Группа.Заголовок = НСтр("ru = 'Цена покупки (по умолчанию)'");
		ИначеЕсли СтрНайти(НРег(ИмяГруппы), "ценапродажи") Тогда
			Группа.Заголовок = НСтр("ru = 'Цена продажи (по умолчанию)'");
		ИначеЕсли СтрНайти(НРег(ИмяГруппы), "ценаплановая") Тогда
			Группа.Заголовок = НСтр("ru = 'Цена плановая'");
		Иначе
			ВызватьИсключение "Заголовок группы не определен";
		КонецЕсли;
		Группа.ОтображатьЗаголовок = Истина;
	КонецЕсли;
	
	Возврат Группа;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуНастройкиЦен()
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытаИзКарточкиНоменклатуры", Истина);
	
	ОткрытьФорму("Обработка.ПанельАдминистрированияБП.Форма.ФормаНастройкиЦеныПродажи", ПараметрыФормы);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Этотобъект.КоманднаяПанель.ПодчиненныеЭлементы);
	ПодменюПечатиДинамическое = Элементы.Найти("ПодменюПечатьОбычное");
	Если ПодменюпечатиДинамическое <> Неопределено Тогда
		МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, ПодменюпечатиДинамическое.ПодчиненныеЭлементы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	ГруппаОстаток = Элементы.Найти("ГруппаОстаток");
	ГруппаЦеныОбщая = Элементы.Найти("ГруппаЦеныОбщая");
	
	Элементы.ПолеПоискаШтрихкодНаименование.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	
	Элементы.ГруппаЗаполнениеПоДаннымСервисаПолеВвода.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.ПолеПоискаШтрихкодНаименование.ПодсказкаВвода = НСтр("ru = 'Введите штрихкод или наименование'");
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаЗаполнениеПоДаннымСервиса);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаЛеваяКолонка);
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(
		Элементы,
		Элементы.ГруппаВидНоменклатуры,
		Элементы.ГруппаЛеваяКолонка,
		Элементы.ВидМаркировки);
		
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(
		Элементы,
		ГруппаОстаток,
		Элементы.ГруппаЛеваяКолонка,
		Элементы.ГруппаОписание);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(
		Элементы,
		ГруппаЦеныОбщая,
		Элементы.ГруппаЛеваяКолонка,
		Элементы.ГруппаОписание);
	
	Если ГруппаОстаток <> Неопределено Тогда
		ГруппаОстаток.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
		ГруппаОстаток.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;
		ГруппаОстаток.ОтображатьЗаголовок = Истина;
		ГруппаОстаток.Показать();
		ГруппаОстаток.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
		
		Элементы.ГруппаОстатокВсего.РастягиватьПоГоризонтали = Истина;
		
		Элементы.ДекорацияОстатковНет.Высота = 0;
		Элементы.ДекорацияОстатковНет.РастягиватьПоГоризонтали = Истина;
		Элементы.ДекорацияОстатковНет.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		
	КонецЕсли;

	Если ГруппаЦеныОбщая <> Неопределено Тогда
		
		Элементы.НастроитьЦеныМК.Видимость = Истина;
		
		ГруппаЦеныОбщая.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
		ГруппаЦеныОбщая.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;
		ГруппаЦеныОбщая.ОтображатьЗаголовок = Истина;
		ГруппаЦеныОбщая.Скрыть();
		
		Элементы.ГруппаЦеныПоУмолчанию.Отображение = ОтображениеОбычнойГруппы.Нет;
		Элементы.ГруппаЦены.Отображение = ОтображениеОбычнойГруппы.Нет;
		Элементы.АктуальныеЦеныПоУмолчанию.Отображение = ОтображениеОбычнойГруппы.Нет;
		Элементы.АктуальныеЦеныПоУмолчанию.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	КонецЕсли;
	
	Элементы.Артикул.РастягиватьПоГоризонтали = Истина;
	Элементы.СтавкаНДС.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаКлассификация.РастягиватьПоГоризонтали = Истина;
	Элементы.Артикул.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВидНоменклатуры.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВидМаркировки.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ПериодичностьУслуги.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ГруппаЕдиница.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	Элементы.СтавкаНДС.Заголовок = НСтр("ru = 'НДС'");
	Элементы.СтавкаНДС.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	Элементы.ОКПД2.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаОКПД2Подсказка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ПрослеживаемыйТовар",
		"ПоложениеЗаголовка",
		ПоложениеЗаголовкаЭлементаФормы.Авто);
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.Родитель);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.Комментарий);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"СкрыватьВСписках",
		"ПоложениеЗаголовка",
		ПоложениеЗаголовкаЭлементаФормы.Авто);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.СкрыватьВСписках);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ДекорацияСтрелка);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ДекорацияРазделитель);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ДекорацияЦеныПоУмолчанию);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаПраваяКолонка);
	
	Исключения = "ПолеПоискаШтрихкодНаименование,Операции0"
	+ ",ГруппаВидНоменклатуры,ВидМаркировки,ПериодичностьУслуги"
	+ ",Артикул,СтавкаНДС,АктуальнаяСтавкаНДС,КодРаздел7ДекларацииНДС";
	МобильныйКлиентБПФормаОбъекта.УстановитьИсключенияИзАдаптации(ЭтотОбъект, Исключения);
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
