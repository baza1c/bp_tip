
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Номенклатура", Номенклатура);
	Параметры.Свойство("ТипЦен", ТипЦен);
	Параметры.Свойство("Период", Период); 
	Параметры.Свойство("Регистратор", УстановкаЦенНоменклатуры);
	
	Заголовок = СтрШаблон("История цены %1, тип цен %2", Номенклатура, ТипЦен);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ИсторияЦены, "Номенклатура", Номенклатура,,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ИсторияЦены, "ТипЦен", ТипЦен,,,Истина);
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы.УстановкаЦенНоменклатуры) Тогда
		Элементы.ИсторияЦеныРегистратор.Видимость = Ложь;
		ЭтаФорма.Доступность = Ложь;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НоваяЦена(Команда)
	
	ПараметрыОткрытия =  Новый Структура("Период, Номенклатура, ТипЦен, Валюта, Регистратор, Цена");
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ЭтаФорма, , "Период");
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеЦеныЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЦены", ПараметрыОткрытия,,,,,ОписаниеОповещения);
		
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	ТекущиеДанные = Элементы.ИсторияЦены.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтрокиТаблицы = Новый Структура("Номенклатура, Период, Регистратор, Валюта, ТипЦен,Цена");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	УдалитьНаСервере(ДанныеСтрокиТаблицы);
	
	Элементы.ИсторияЦены.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИсторияЦеныПериод");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИсторияЦеныЦена");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИсторияЦеныВалюта");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИсторияЦеныРегистратор");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ИсторияЦены.Период", ВидСравненияКомпоновкиДанных.Равно, Период);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина));
	
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();

КонецПроцедуры

&НаКлиенте
Процедура ИзменениеЦеныЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Элементы.ИсторияЦены.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНаСервере(ДанныеЦены)
	
	Если ЗначениеЗаполнено(ДанныеЦены.Регистратор) Тогда
		Попытка
			
			ДокументОбъект = ДанныеЦены.Регистратор.ПолучитьОбъект();
			Строка = ДокументОбъект.Товары.Найти(Номенклатура, "Номенклатура");
			Если ЗначениеЗаполнено(Строка) Тогда
				ДокументОбъект.Товары.Удалить(Строка);
			Иначе
				Возврат;
			КонецЕсли;
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось удалить цену, установленную с %1. Возможно, открыт документ установки цен.'"),
				ДанныеЦены.Период);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецПопытки
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

