
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИсторияВидаСтавкиНДС.Загрузить(Параметры.ИсторияВидаСтавкиНДС.Выгрузить());
	Если ИсторияВидаСтавкиНДС.Количество() = 0 Тогда
		ИсторияВидаСтавкиНДС.Добавить().ВидСтавкиНДС = Параметры.ТекущийВидСтавкиНДС;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Редактирование", Метаданные.Справочники.Номенклатура) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если ТолькоПросмотр Тогда
		Элементы.ФормаКомандаОтмена.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	УстановитьПризнакПервоначальногоЗначения(ИсторияВидаСтавкиНДС);
	
	Если ИсторияВидаСтавкиНДС.Количество() > 0 Тогда
		Элементы.ИсторияВидаСтавкиНДС.ТекущаяСтрока = ИсторияВидаСтавкиНДС[ИсторияВидаСтавкиНДС.Количество() - 1].ПолучитьИдентификатор();
	КонецЕсли;
	
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	Перечисления.СтавкиНДС.ЗаполнитьСписокВыбораСтавокНДС(ЭтотОбъект, "ИсторияВидСтавки");
	
	УстановитьУсловноеОформление();
	УточнитьПредставлениеСтавкиНДСПриУСН();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыИсторияКПП

&НаКлиенте
Процедура ИсторияВидаСтавкиНДСПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		Если Элемент.ТекущиеДанные <> Неопределено Тогда
			
			НовыйПериод = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
			
			// Получим максимальный период в таблице
			ПоследнийПериод = '00010101000000';
			Для Каждого ЗаписьИстории ИЗ ИсторияВидаСтавкиНДС Цикл
				Если ЗаписьИстории.Период > ПоследнийПериод Тогда
					ПоследнийПериод = ЗаписьИстории.Период;
				КонецЕсли;
			КонецЦикла;
			
			Если НовыйПериод <= ПоследнийПериод Тогда
				НовыйПериод = НачалоДня(ПоследнийПериод + 60*60*24);
			КонецЕсли;
			
			Элемент.ТекущиеДанные.Период = НовыйПериод;
			
			ЭтотОбъект.ТекущийЭлемент = Элементы.ИсторияВидСтавки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияВидаСтавкиНДСПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПризнакПервоначальногоЗначения(ИсторияВидаСтавкиНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияВидаСтавкиНДСППередУдалением(Элемент, Отказ)
	
	Если ИсторияВидаСтавкиНДС.Индекс(Элемент.ТекущиеДанные) = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеИстории(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ИсторияВидаСтавкиНДС", ИсторияВидаСтавкиНДС);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПризнакПервоначальногоЗначения(ТаблицаИстории)
	
	ТаблицаИстории.Сортировать("Период");
	// Первая строка должна быть первоначальным значением
	Если ТаблицаИстории.Количество() > 0 Тогда
		ПерваяСтрока = ТаблицаИстории[0];
		ПерваяСтрока.ПервоначальноеЗначение = Истина;
		ПерваяСтрока.Период = '00010101';
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеИстории(Отказ)
	
	ПериодыСтрок = Новый Массив;
	
	Для Каждого Запись Из ИсторияВидаСтавкиНДС Цикл
		
		ИндексСтроки = ИсторияВидаСтавкиНДС.Индекс(Запись);
		ПрефиксСообщенияСтроки = "ИсторияВидаСтавкиНДС["+ Формат(ИндексСтроки, "ЧЦ=; ЧДЦ=; ЧГ=") + "].";
		
		Если НЕ ЗначениеЗаполнено(Запись.Период) И ИндексСтроки > 0 Тогда
			СообщениеОбОшибке = НСтр("ru = 'Нужно указать дату начала действия'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки + "Период", , Отказ);
		КонецЕсли;
		
		Если ПериодыСтрок.Найти(Запись.Период) = Неопределено Тогда
			Если ЗначениеЗаполнено(Запись.Период) Тогда
				ПериодыСтрок.Добавить(Запись.Период);
			КонецЕсли;
		Иначе
			// Строка с такой датой уже была раньше, это ошибка.
			// Не может быть 2 строки с одинаковой датой.
			СообщениеОбОшибке = НСтр("ru = 'Уже есть запись с указанной датой сведений'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки + "Период", , Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.ВидСтавкиНДС) Тогда
			СообщениеОбОшибке = НСтр("ru = 'Поле ""Ставка НДС"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки + "КПП",  , Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	// ИсторияНаименованийПериод
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИсторияПериод");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ИсторияВидаСтавкиНДС.ПервоначальноеЗначение", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Начальное значение'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ИсторияНаименованийПериод
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИсторияПериод");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ИсторияВидаСтавкиНДС.ПервоначальноеЗначение", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ИсторияВидаСтавкиНДС.Период", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// ИсторияНаименованийВидСтавкиНДС
	
	ВедетсяУчетНДСНаУСН = РегистрыСведений.НастройкиУчетаНДС.ВедетсяУчетНДСНаУСН();
	
	Для Каждого ВидСтавки Из Перечисления.ВидыСтавокНДС Цикл
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИсторияВидСтавки");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ИсторияВидаСтавкиНДС.ВидСтавкиНДС", ВидСравненияКомпоновкиДанных.Равно, ВидСтавки);

		Если ВедетсяУчетНДСНаУСН Тогда 
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст",
				УчетНДСКлиентСервер.ТекстовоеПредставлениеСтавкиНДС(ВидСтавки, ТекущаяДатаПользователя));
		Иначе
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст",
				Строка(Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавки, ТекущаяДатаПользователя)));
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры
	
Процедура УточнитьПредставлениеСтавкиНДСПриУСН()
	
	Если НЕ РегистрыСведений.НастройкиУчетаНДС.ВедетсяУчетНДСНаУСН() Тогда
		Возврат;
	КонецЕСли;
		
	Для каждого ЭлементСписка Из Элементы.ИсторияВидСтавки.СписокВыбора Цикл 
		ЭлементСписка.Представление = УчетНДСКлиентСервер.ТекстовоеПредставлениеСтавкиНДС(
			ЭлементСписка.Значение, ТекущаяДатаПользователя);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти