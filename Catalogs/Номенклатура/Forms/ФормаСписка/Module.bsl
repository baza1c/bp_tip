#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если Параметры.Свойство("ВидОтображенияТаблицы") Тогда
		// В некоторых случаях нужно открывать список номенклатуры с отбором по списку элементов.
		// В этом случае иерархический список выглядит плохо - в нем выводятся все группы справочника, даже если в них нет элементов.
		// Поэтому для таких случаев можно переопределить отображение списка: вместо иерархического использовать плоскую таблицу.
		Элементы.Список.Отображение = Параметры.ВидОтображенияТаблицы;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Номенклатура);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	Элементы.ФормаГруппаМаркетплейсы.Видимость = ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.МаркетплейсыЗагрузкаКаталога);
	
	УстановитьУсловноеОформление();
	
	ОбщегоНазначенияБП.УстановитьВидимостьКолонокДополнительнойИнформации(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Справочник.Номенклатура",
		"ФормаСписка",
		НСтр("ru='Новости: Номенклатура'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеНоменклатуры") Тогда
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ВАрхиве", Ложь, Истина);
	КонецЕсли;
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатурой.ПриСозданииНаСервереФормаСпискаНоменклатуры(
		ЭтотОбъект,
		Элементы.ГруппаКомандыРаботаСНоменклатурой,
		Элементы.Список,,
		Новый Структура("ДобавитьКомандыСопоставления", Ложь));
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	РаботаСНоменклатуройБП.ЗаполнитьРеквизитыФормыДляРаботыСервиса(ЭтотОбъект);
	
	
	// Проверим принципиальное наличие оборудования этого типа в ИБ перед попыткой его подключения
	ИспользоватьПодключаемоеОборудование = МенеджерОборудованияБП.ИспользуетсяОборудование("СканерШтрихкода");
	
	// РекламныйСервис
	РекламныйСервис.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РекламныйСервис
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	Если ИспользоватьПодключаемоеОборудование Тогда
		ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
		
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
			Неопределено,
			ЭтотОбъект,
			ПоддерживаемыеТипыПодключаемогоОборудования);
	КонецЕсли;
	
	// РекламныйСервис
	РекламныйСервисКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец РекламныйСервис
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если ИмяСобытия = РаботаСНоменклатуройКлиент.ОписаниеОповещенийПодсистемы().ЗагрузкаНоменклатуры Тогда
		Если Параметр.СозданныеОбъекты.Количество() > 0 Тогда
			Элементы.Список.ТекущаяСтрока = Параметр.СозданныеОбъекты[0].Номенклатура;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = РаботаСНоменклатуройКлиент.ОписаниеОповещенийПодсистемы().СопоставлениеНоменклатуры Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	Если ИмяСобытия = "ВключеноИспользованиеСкрытияНоменклатуры" Тогда
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ВАрхиве", Ложь, Истина);
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если  ИмяСобытия = "ScanData" Тогда
			ДанныеСоСканера = МенеджерОборудованияКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			Если ДанныеСоСканера.Количество() > 0 Тогда
				НайтиПоШтрихкоду(ДанныеСоСканера[0]);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуРаботаСНоменклатурой(Команда)
	РаботаСНоменклатуройКлиент.ВыполнитьПодключаемуюКоманду(ЭтотОбъект, Команда);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыборРаботаСНоменклатурой(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	РаботаСНоменклатуройКлиент.ВыборВТаблицеФормы(ЭтотОбъект, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
КонецПроцедуры
// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	НайтиПоШтрихкоду();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаКаталогаWIldberries(Команда)
	ЗагрузитьКаталогМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаКаталогаOzon(Команда)
	ЗагрузитьКаталогМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаКаталогаЯндексМаркет(Команда)
	ЗагрузитьКаталогМаркетплейса(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСкрытые(Команда)
	ПоказыватьСкрытые = НЕ ПоказыватьСкрытые;
	Элементы.ФормаПоказыватьСкрытые.Пометка = ПоказыватьСкрытые;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокКонтекстноеМенюСкрыть", "Видимость", НЕ ПоказыватьСкрытые);
	ПодключитьОбработчикОжидания("Подключаемый_ПоказыватьСкрытые", 0.1, Истина);
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Группа Тогда
		КлючеваяОперация = "СозданиеФормыНоменклатура";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТекущиеДанные.ЭтоГруппа Тогда
		КлючеваяОперация = "ОткрытиеФормыНоменклатура";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ВАрхиве", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНоменклатурыВАрхиве);
	
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	ВедетсяУчетНДСНаУСН = РегистрыСведений.НастройкиУчетаНДС.ВедетсяУчетНДСНаУСН();
	
	Для Каждого ВидСтавки Из Перечисления.ВидыСтавокНДС Цикл
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидСтавкиНДС");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.ВидСтавкиНДС", ВидСравненияКомпоновкиДанных.Равно, ВидСтавки);
		
		Если ВедетсяУчетНДСНаУСН Тогда
			ЭлементУО.Оформление.УстановитьЗначениеПараметра(
				"Текст",
				УчетНДСКлиентСервер.ТекстовоеПредставлениеСтавкиНДС(ВидСтавки, ТекущаяДатаПользователя));
		Иначе
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст",
				Строка(Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавки, ТекущаяДатаПользователя)));
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиПоШтрихкоду(ДополнительныеПараметры = Неопределено)
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	ДополнительныеПараметры.Вставить("ТекстВопроса", НСтр("ru = 'Хотите поискать в сервисе 1С:Номенклатура?'"));
	
	Оповещение = Новый ОписаниеОповещения("НайтиПоШтрихкодуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РаботаСНоменклатуройКлиентБП.НайтиПоШтрихкоду(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Перем РезультатШтрихкод, РезультатИдентификатор, РезультатНоменклатура;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Результат.Свойство("Штрихкод", РезультатШтрихкод);
		Результат.Свойство("Идентификатор", РезультатИдентификатор);
		Результат.Свойство("Номенклатура", РезультатНоменклатура);
		
		Результат.Свойство("СервисАктивен", СервисАктивен);
		Результат.Свойство("ИнтернетПоддержкаПодключена", ИнтернетПоддержкаПодключена);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РезультатНоменклатура) Тогда
		Элементы.Список.ТекущаяСтрока = РезультатНоменклатура;
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", Новый Структура("Ключ", РезультатНоменклатура));
	ИначеЕсли ЗначениеЗаполнено(РезультатИдентификатор) Тогда 
		ПараметрыСоздания = Новый Структура;
		ПараметрыСоздания.Вставить("ИдентификаторСервиса", РезультатИдентификатор);
		ПараметрыСоздания.Вставить("Штрихкод", РезультатШтрихкод);
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", ПараметрыСоздания);
	Иначе 
		ПоказатьПредупреждение(, СтрШаблон(НСтр("ru = 'По штрихкоду %1 номенклатура не найдена.'"), РезультатШтрихкод));
	КонецЕсли;
	
КонецПроцедуры

#Область ЗагрузкаМаркетплейс

&НаКлиенте
Процедура ЗагрузитьКаталогМаркетплейса(ВидМаркетплейса)
	НастройкаИнтеграции = НастройкаИнтеграцииПоУмолчанию(ВидМаркетплейса);
	Если НЕ ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		ОткрытьФормуЗагрузкаКаталога(ВидМаркетплейса);
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВидМаркетплейса",     ВидМаркетплейса);
	ПараметрыЗагрузки.Вставить("НастройкаИнтеграции", НастройкаИнтеграции);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьНоменклатуруЗавершение", ЭтотОбъект, ПараметрыЗагрузки);
	ОбменСМаркетплейсКлиент.ЗагрузитьСписокНоменклатуры(НастройкаИнтеграции, ЭтотОбъект, ОповещениеОЗавершении);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНоменклатуруЗавершение(РезультатОперации, ПараметрыЗагрузки) Экспорт
	Если НЕ РезультатОперации.Результат Тогда
		ОткрытьФормуЗагрузкаКаталога(ПараметрыЗагрузки.ВидМаркетплейса);
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РезультатОперации.СписокТоваров) Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка завершена'"),,НСтр("ru = 'Новых товаров нет'"));
	Иначе
		ОбменСМаркетплейсКлиент.СопоставитьНоменклатуруПриЗагрузкеКаталога(РезультатОперации.СписокТоваров, ПараметрыЗагрузки);
	КонецЕсли;
КонецПроцедуры    

&НаКлиенте
Процедура ОткрытьФормуЗагрузкаКаталога(ВидМаркетплейса)
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	ПараметрыОткрытияФормы.Вставить("ВидОперации",     "ЗагрузкаКаталога");
	ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаКаталога", ПараметрыОткрытияФормы,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкаИнтеграцииПоУмолчанию(ВидМаркетплейса)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийБухгалтерскийУчет") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
	Возврат Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(ОрганизацияПоУмолчанию, ВидМаркетплейса);
КонецФункции

#КонецОбласти

#Область РекламныйСервис

&НаКлиенте
Процедура Подключаемый_ЗаполнитьРекламныйНоситель()
	РекламныйСервисКлиент.ЗаполнитьРекламныйНоситель(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьКнопкуЗакрытьРекламу()
	РекламныйСервисКлиент.ПоказатьКнопкуЗакрытьРекламу(ЭтотОбъект);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_МакетРекламныйСервисПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	РекламныйСервисКлиент.МакетРекламныйСервисНажатие(ЭтотОбъект, ДанныеСобытия, СтандартнаяОбработка);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_КомандаЗакрытьРекламу()
	РекламныйСервисКлиент.КомандаЗакрытьРекламу(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ПоказыватьСкрытые()
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ВАрхиве", Ложь, НЕ ПоказыватьСкрытые);
КонецПроцедуры


#КонецОбласти