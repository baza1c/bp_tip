#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Номенклатура", Номенклатура);
	Параметры.Свойство("ТипЦен", ТипЦен);
	Параметры.Свойство("Валюта", Валюта);
	Параметры.Свойство("Период", Период); 
	Параметры.Свойство("Цена",   Цена);
	Параметры.Свойство("Регистратор", УстановкаЦенНоменклатуры);
	
	Элементы.Номенклатура.Видимость = НЕ ЗначениеЗаполнено(Номенклатура);
	Заголовок = ?(ЗначениеЗаполнено(Номенклатура), Номенклатура, НСтр("ru = 'Установка цены'"));
	
	КонецПериода  = "<не ограничено>";
	
	Элементы.ТипЦен.Доступность = НЕ ЗначениеЗаполнено(ТипЦен);
		
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ТипЦенПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ТипЦенПриИзмененииНаСервере()
	
	//проверим конец периода
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЦеныНоменклатуры.Период КАК Период
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|ГДЕ
	|	ЦеныНоменклатуры.ТипЦен = &ТипЦен
	|	И ЦеныНоменклатуры.Номенклатура = &Номенклатура
	|	И ЦеныНоменклатуры.Период > &ДатаКон
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЦеныНоменклатуры.Период ВОЗР";
	
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Период));
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		КонецПериода  = Формат(Результат.Период, "ДЛФ=D;");
	Иначе
		КонецПериода  = "<не ограничено>";
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТипЦенПриИзменении(Элемент)
	ТипЦенПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ЗаписатьНаСервере(Отказ = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УстановкаЦенНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.УстановкаЦенНоменклатуры КАК УстановкаЦенНоменклатуры
	|ГДЕ
	|	УстановкаЦенНоменклатуры.ТипЦен = &ТипЦен
	|	И УстановкаЦенНоменклатуры.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|	И НЕ УстановкаЦенНоменклатуры.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	УстановкаЦенНоменклатуры.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	УстановкаЦенНоменклатуры.Дата УБЫВ";
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Период));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Период));
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		
		Попытка
		
			ДокументОбъект = Результат.Ссылка.ПолучитьОбъект();
			Строка = ДокументОбъект.Товары.Найти(Номенклатура, "Номенклатура");
			Если ЗначениеЗаполнено(Строка) Тогда
				Строка.Цена = Цена;
				Строка.Валюта = Валюта;
			Иначе
				ТЧТовары = ДокументОбъект.Товары;
				Строка = ТЧТовары.Добавить();
				Строка.Номенклатура = Номенклатура;
				Строка.Цена = Цена;
				Строка.Валюта = Валюта;
			КонецЕсли;
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось изменить документ %1.'"), Результат.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Результат.Ссылка, , , Отказ);

			Отказ = Истина;
		КонецПопытки;
			
	Иначе
		ДокументОбъект = Документы.УстановкаЦенНоменклатуры.СоздатьДокумент();
		ЗаполнениеДокументов.Заполнить(ДокументОбъект);
		ДокументОбъект.Дата = Период;
		ДокументОбъект.ТипЦен = ТипЦен;
		ТЧТовары = ДокументОбъект.Товары;
		Строка = ТЧТовары.Добавить();
		Строка.Номенклатура = Номенклатура;
		Строка.Цена = Цена;
		Строка.Валюта = Валюта;
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось записать документ %1.'"), ДокументОбъект.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка, , , Отказ);

			Отказ = Истина;
		КонецПопытки;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	Если ПроверитьЗаполнение() Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Отказ = Ложь;
	ЗаписатьНаСервере(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	Закрыть();
	ТекстОповещения = СтрШаблон(НСтр("ru = 'Новая цена - %1 %2'"), Цена, Валюта);
	ПоказатьОповещениеПользователя(ТекстОповещения,,,,СтатусОповещенияПользователя.Информация,);
КонецПроцедуры

#КонецОбласти



