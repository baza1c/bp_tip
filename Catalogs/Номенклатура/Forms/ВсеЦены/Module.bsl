
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Номенклатура", Номенклатура);
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Элементы.Номенклатура.ТолькоПросмотр = Истина;
		ЗаполнитьАктуальныеЦены();
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НоваяЦена(Команда)
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ПараметрыОткрытия =  Новый Структура("Период, Номенклатура, ТипЦен, Валюта, Регистратор, Цена");
		ПараметрыОткрытия.Номенклатура = Номенклатура;
		ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеЦеныЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЦены", ПараметрыОткрытия,,,,,ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ЗаполнитьАктуальныеЦены();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура АктуальныеЦеныВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "АктуальныеЦеныЦена" Тогда
		ТекущиеДанные = Элементы.АктуальныеЦены.ТекущиеДанные;
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ТекущиеДанные.ТипЦен) Тогда
			ПараметрыОткрытия =  Новый Структура("Период, Номенклатура, ТипЦен, Валюта, Регистратор, Цена");
			ПараметрыОткрытия.Номенклатура = Номенклатура;
			ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ТекущиеДанные);
			ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеЦеныЗавершение", ЭтотОбъект);
			ОткрытьФорму("Справочник.Номенклатура.Форма.ИсторияЦены", ПараметрыОткрытия,,,,,ОписаниеОповещения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьАктуальныеЦены()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	|	ЦеныНоменклатурыСрезПоследних.Валюта КАК Валюта,
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен КАК ТипЦен,
	|	ЦеныНоменклатурыСрезПоследних.Регистратор КАК Регистратор,
	|	ЦеныНоменклатурыСрезПоследних.Период КАК Период
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ТекущаяДата, Номенклатура = &Номенклатура) КАК ЦеныНоменклатурыСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	АктуальныеЦены.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеЦеныЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьАктуальныеЦены();
	
КонецПроцедуры

#КонецОбласти



