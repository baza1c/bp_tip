#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Отбор.Вставить("ВАрхиве", Ложь);
КонецПроцедуры

#КонецОбласти

#Область ПрограмныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

Процедура ПроверитьЗначениеОпцииИспользоватьПериодичностьУслуг(ПериодичностьУслуг) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПериодичностьУслуг")
		И ЗначениеЗаполнено(ПериодичностьУслуг) Тогда
	
		УстановитьПривилегированныйРежим(Истина);
		Константы.ИспользоватьПериодичностьУслуг.Установить(Истина);
		УстановитьПривилегированныйРежим(Ложь);
	
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоМаркируемаяАлкогольнаяПродукция(Номенклатура) Экспорт
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СоответствиеНоменклатурыЕГАИС) 
		ИЛИ НЕ ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		
		Возврат Ложь;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &Номенклатура
	|	И СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.ВидПродукции.Маркируемый";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

Функция ЭтоАлкогольнаяПродукция(Номенклатура) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпрНоменклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ
	|	(СпрНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Алкогольная)
	|			ИЛИ СпрНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов)
	|			ИЛИ СпрНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)
	|			ИЛИ СпрНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках))";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ОсновнаяСтранаПроисхожденияТоваров() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Константы.ОсновнаяСтранаПроисхожденияТоваров.Получить();
	
КонецФункции

Процедура УстановитьАктуальноеЗначениеИсторииВидаСтавкиНДС(ВидСтавкиНДС, ИсторияВидаСтавкиНДС) Экспорт
	
	КоличествоЗаписей = ИсторияВидаСтавкиНДС.Количество();
	
	Если КоличествоЗаписей > 0 Тогда
		
		ИсторияВидаСтавкиНДС.Сортировать("Период");
		АктуальнаяЗаписьИстории = ИсторияВидаСтавкиНДС[КоличествоЗаписей - 1];
		АктуальнаяЗаписьИстории.ВидСтавкиНДС = ВидСтавкиНДС;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьЭтикеток) Тогда
		// Этикетка	
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Ценники";
		КомандаПечати.Представление = НСтр("ru = 'Этикетка'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Этикетка'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиЭтикеток";
		КомандаПечати.СписокФорм    = "ФормаЭлемента";
		КомандаПечати.Порядок       = 10;
		
		// Ценник
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Ценники";
		КомандаПечати.Представление = НСтр("ru = 'Ценник'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Ценник'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиЦенников";
		КомандаПечати.СписокФорм    = "ФормаСписка, ФормаЭлемента";
		КомандаПечати.Порядок       = 20;
	КонецЕсли;
	
	// Спецификация	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Спецификация";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьСпецификации";
	КомандаПечати.Представление = НСтр("ru = 'Спецификация'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Спецификация сырья и материалов'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиСпецификации";
	КомандаПечати.ФункциональныеОпции = "ВедетсяПроизводственнаяДеятельность,ИспользоватьКомплектациюНоменклатуры";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаЭлемента";
	КомандаПечати.Порядок       = 30;
		
КонецПроцедуры

// Получает ставку НДС по номенклатуре
//
// Параметры:
//       Номенклатура - Справочник.Номенклатура - ставку НДС которой необходимо получить
//       Период - Дата - дата получения Ставки НДС.
// Возвращаемое значение:
//       СтавкаНДС - Перечисление.СтавкаНДС - Ставка НДС
Функция СтавкаНДС(Номенклатура, Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДата();
	КонецЕсли;
	
	ВидСтавкиНДС = ВидСтавкиНДСНаДату(Номенклатура, Период);
	СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавкиНДС, Период);

	Возврат СтавкаНДС;
	
КонецФункции

// Получает вид ставки НДС по номенклатуре и периоду
//
// Параметры:
//      Номенклатура - Справочник.Номенклатура - вид ставки НДС которой необходимо получить
//      ДатаСведений - Дата - дата получения вида ставки НДС.
// Возвращаемое значение:
//      ВидСтавкиНДС - Перечисление.ВидыСтавокНДС
Функция ВидСтавкиНДСНаДату(Номенклатура, ДатаСведений) Экспорт
	
	Если Не ЗначениеЗаполнено(Номенклатура)
		Или ТипЗнч(Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат Перечисления.ВидыСтавокНДС.ПустаяСсылка();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаСведений) Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидСтавкиНДС");
	КонецЕсли;
	
	СписокНоменклатуры = Новый Массив();
	СписокНоменклатуры.Добавить(Номенклатура);
	
	СоответствиеСведенийОВидахСтавкиНоменклатуры = ВидыСтавокНаДатуИзИстории(СписокНоменклатуры, ДатаСведений);
	СведенияОВидеСтавкиНДСИзИстории = СоответствиеСведенийОВидахСтавкиНоменклатуры.Получить(Номенклатура);
	
	Если СведенияОВидеСтавкиНДСИзИстории = Неопределено Тогда
		ВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидСтавкиНДС");
	Иначе
		ВидСтавкиНДС = СведенияОВидеСтавкиНДСИзИстории;
	КонецЕсли;
	
	Возврат ВидСтавкиНДС;
	
КонецФункции

// Получает виды ставок НДС из истории для массива номенклатуры на заданную дату
// Параметры:
//      СписокНоменклатуры - Массив - ссылки на объекты вида Справочник.Номенклатура.
//                    Если массив пуст, то результатом будет пустое соответствие.
//      ДатаСведений - Дата - дата получения вида ставки НДС.
// Возвращаемое значение:
//  Соответствие - список объектов Справочник.Номенклатура и значений видов ставок:
//   * Ключ - Справочник.Номенклатура - ссылка на объект;
//   * Значение - ПеречислениеСсылка.ВидыСтавокНДС - вид ставки НДС на дату сведений.
// Для номенклатуры с пустой историей вида ставки НДС ключ будет отсутствовать.
// 
Функция ВидыСтавокНаДатуИзИстории(СписокНоменклатуры, ДатаСведений) Экспорт

	ЗначенияРеквизитов = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(ДатаСведений) Тогда
		Возврат ЗначенияРеквизитов;
	КонецЕсли;
	
	Если СписокНоменклатуры.Количество() = 0 Тогда
		Возврат ЗначенияРеквизитов;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Номенклатура", СписокНоменклатуры);
	Запрос.Параметры.Вставить("ДатаСведений", ДатаСведений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияВидаСтавкиНДС.Период) КАК Период,
	|	ИсторияВидаСтавкиНДС.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗначенияВидаСтавкиНДС
	|ИЗ
	|	Справочник.Номенклатура.ИсторияВидаСтавкиНДС КАК ИсторияВидаСтавкиНДС
	|ГДЕ
	|	ИсторияВидаСтавкиНДС.Ссылка В (&Номенклатура)
	|	И ИсторияВидаСтавкиНДС.Период <= &ДатаСведений
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияВидаСтавкиНДС.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияВидаСтавкиНДС.Ссылка КАК Номенклатура,
	|	ИсторияВидаСтавкиНДС.ВидСтавкиНДС КАК ВидСтавкиНДС
	|ИЗ
	|	ЗначенияВидаСтавкиНДС КАК ЗначенияВидаСтавкиНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ИсторияВидаСтавкиНДС КАК ИсторияВидаСтавкиНДС
	|		ПО ЗначенияВидаСтавкиНДС.Ссылка = ИсторияВидаСтавкиНДС.Ссылка
	|			И ЗначенияВидаСтавкиНДС.Период = ИсторияВидаСтавкиНДС.Период";

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗначенияРеквизитов[Выборка.Номенклатура] = Выборка.ВидСтавкиНДС;
	КонецЦикла;
	
	Возврат ЗначенияРеквизитов;

КонецФункции

// Получает признаки "Прослеживаемый товар" и "Прослеживаемый комплект" по номенклатуре и периоду
// если есть история прослеживаемого товара - возвращаются значения из истории
// если история отсутсвует, тогда возвращается значения реквизитов "Прослеживаемый товар" и "Прослеживаемый комплект"
//
// Параметры:
//      Номенклатура - Справочник.Номенклатура - признаки прослеживаемости которой необходимо получить
//      ДатаСведений - Дата - дата получения признаков прослеживаеимости
// Возвращаемое значение:
//      Структура:
//               Прослеживаемый товар - Булево
//               Прослеживаемый комплект - Булево
Функция ПризнакПрослеживаемостиНаДату(Номенклатура, ДатаСведений) Экспорт
	
	РеквизитыПрослеживаемости = Новый Структура("ПрослеживаемыйТовар, ПрослеживаемыйКомплект", Ложь, Ложь);

	Если Не ЗначениеЗаполнено(Номенклатура)
		Или ТипЗнч(Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат РеквизитыПрослеживаемости;
	КонецЕсли;
	
		
	Если Не ЗначениеЗаполнено(ДатаСведений) Тогда
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "ПрослеживаемыйТовар, ПрослеживаемыйКомплект");
		РеквизитыПрослеживаемости.ПрослеживаемыйТовар = РеквизитыНоменклатуры.ПрослеживаемыйТовар;
		РеквизитыПрослеживаемости.ПрослеживаемыйКомплект = РеквизитыНоменклатуры.ПрослеживаемыйКомплект;

		Возврат РеквизитыПрослеживаемости;
	КонецЕсли;
	
		
	СписокНоменклатуры = Новый Массив();
	СписокНоменклатуры.Добавить(Номенклатура);
	
	СоответствиеСведенийПризнакаПрослеживаемостиНоменклатуры = ПризнакПрослеживаемостиНаДатуИзИстории(СписокНоменклатуры, ДатаСведений);
	
	СведенияПризнакаПрослеживаемостиИзИстории = СоответствиеСведенийПризнакаПрослеживаемостиНоменклатуры.Получить(Номенклатура);
	
	Если СведенияПризнакаПрослеживаемостиИзИстории = Неопределено Тогда
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "ПрослеживаемыйТовар, ПрослеживаемыйКомплект");
		РеквизитыПрослеживаемости.ПрослеживаемыйТовар = РеквизитыНоменклатуры.ПрослеживаемыйТовар;
		РеквизитыПрослеживаемости.ПрослеживаемыйКомплект = РеквизитыНоменклатуры.ПрослеживаемыйКомплект;
	Иначе
		
		РеквизитыПрослеживаемости.ПрослеживаемыйТовар = СведенияПризнакаПрослеживаемостиИзИстории.ПрослеживаемыйТовар;
		РеквизитыПрослеживаемости.ПрослеживаемыйКомплект = СведенияПризнакаПрослеживаемостиИзИстории.ПрослеживаемыйКомплект;

	КонецЕсли;
	
	Возврат РеквизитыПрослеживаемости;
	
КонецФункции

// Получает признаки прослеживаемости из истории для массива номенклатуры на заданную дату
// Параметры:
//      СписокНоменклатуры - Массив - ссылки на объекты вида Справочник.Номенклатура.
//                    Если массив пуст, то результатом будет пустое соответствие.
//      ДатаСведений - Дата - дата получения признаков прослеживаемости.
// Возвращаемое значение:
//  Соответствие - список объектов Справочник.Номенклатура и значений признаков прослеживаемости:
//   * Ключ - Справочник.Номенклатура - ссылка на объект;
//   * Значение - Структура - признаки прослеживаемости на дату
//               Прослеживаемый товар - Булево
//               Прослеживаемый комплект - Булево
// Для номенклатуры с пустой историей прослеживаемости ключ будет отсутствовать.
// 
Функция ПризнакПрослеживаемостиНаДатуИзИстории(СписокНоменклатуры, ДатаСведений) Экспорт

	ЗначенияРеквизитов = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(ДатаСведений) Тогда
		Возврат ЗначенияРеквизитов;
	КонецЕсли;
	
	Если СписокНоменклатуры.Количество() = 0 Тогда
		Возврат ЗначенияРеквизитов;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Номенклатура", СписокНоменклатуры);
	Запрос.Параметры.Вставить("ДатаСведений", ДатаСведений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(НоменклатураИсторияПрослеживаемогоТовара.Период) КАК Период,
	|	НоменклатураИсторияПрослеживаемогоТовара.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗначенияПрослеживаемости
	|ИЗ
	|	Справочник.Номенклатура.ИсторияПрослеживаемогоТовара КАК НоменклатураИсторияПрослеживаемогоТовара
	|ГДЕ
	|	НоменклатураИсторияПрослеживаемогоТовара.Ссылка В(&Номенклатура)
	|	И НоменклатураИсторияПрослеживаемогоТовара.Период <= &ДатаСведений
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураИсторияПрослеживаемогоТовара.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураИсторияПрослеживаемогоТовара.Ссылка КАК Номенклатура,
	|	НоменклатураИсторияПрослеживаемогоТовара.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	НоменклатураИсторияПрослеживаемогоТовара.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ИЗ
	|	ЗначенияПрослеживаемости КАК ЗначенияПрослеживаемости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ИсторияПрослеживаемогоТовара КАК НоменклатураИсторияПрослеживаемогоТовара
	|		ПО ЗначенияПрослеживаемости.Ссылка = НоменклатураИсторияПрослеживаемогоТовара.Ссылка
	|			И ЗначенияПрослеживаемости.Период = НоменклатураИсторияПрослеживаемогоТовара.Период";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Результат = Новый Структура("ПрослеживаемыйТовар, ПрослеживаемыйКомплект");
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		ЗначенияРеквизитов[Выборка.Номенклатура] = Результат;
	КонецЦикла;
	
	Возврат ЗначенияРеквизитов;

КонецФункции

// Определяет есть ли в истории видов ставок НДС значение вида ставки "Без НДС"
//
// Параметры:
//      Номенклатура - Справочник.Номенклатура - номенклатура, историю видов ставок которой необходимо проверить
// Возвращаемое значение:
//      Булево - Истина, если в истории видов ставок НДС значение вида ставки "Без НДС", Ложь - если нет.
Функция ВИсторииЕстьСтавкаБезНДС(Номенклатура) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураИсторияВидаСтавкиНДС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура.ИсторияВидаСтавкиНДС КАК НоменклатураИсторияВидаСтавкиНДС
	|ГДЕ
	|	НоменклатураИсторияВидаСтавкиНДС.Ссылка = &Номенклатура
	|	И НоменклатураИсторияВидаСтавкиНДС.ВидСтавкиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.БезНДС)";
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

// Определяет есть ли записи в истории видов ставок НДС
//
// Параметры:
//      Номенклатура - Справочник.Номенклатура - номенклатура, историю видов ставок которой необходимо проверить
// Возвращаемое значение:
//      Булево - Истина, если в истории видов ставок НДС есть записи, Ложь - если нет.
Функция ЕстьИсторияВидовСтавокНДС(Номенклатура) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураИсторияВидаСтавкиНДС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура.ИсторияВидаСтавкиНДС КАК НоменклатураИсторияВидаСтавкиНДС
	|ГДЕ
	|	НоменклатураИсторияВидаСтавкиНДС.Ссылка = &Номенклатура";
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

// Сопоставляет содержмое загружаемого файла с элементами справочника "Номенклатура".
// Переопределяет процедуру СопоставитьЗагружаемыеДанные модуля менеджера обработки ЗагрузкаДанныхВТабличнуюЧасть.
// Вызывается только в прикладной загрузке (см. ЗагрузкаДанныхИзВнешнихФайловКлиент.НовыйПараметрыЗагрузкиВТабЧасть).
//
// Параметры:
//  ЗагружаемыеДанные - ТаблицаЗначений - содержит колонки:
//	- СтрокаИзФайла - Строка.
//	- СсылкаНайденая - СправочникСсылка.Номенклатура - сюда помещаются ссылки на найденные элементы.
//  ИмяРеквизитаДляСопоставления - Строка - имя реквизита справочника "Номенклатура".
//  ПараметрыПрикладнойЗагрузки - см. ЗагрузкаДанныхИзВнешнихФайловКлиент.НовыйПараметрыПрикладнойЗагрузки.
//
Процедура СопоставитьЗагружаемыеДанныеИзФайла(ЗагружаемыеДанные, ИмяРеквизитаДляСопоставления, ПараметрыПрикладнойЗагрузки) Экспорт
	
	СтрокиИзФайла = ЗагружаемыеДанные.Скопировать(, "СтрокаИзФайла");
	ОбщегоНазначенияБПВызовСервера.ПронумероватьТаблицу(СтрокиИзФайла); 
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(
	"ВЫБРАТЬ
	|	СтрокиИзФайла.СтрокаИзФайла КАК СтрокаИзФайла,
	|	СтрокиИзФайла.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&СтрокиИзФайла КАК СтрокиИзФайла
	|ГДЕ
	|	СтрокиИзФайла.СтрокаИзФайла <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.СтрокаИзФайла КАК СтрокаИзФайла,
	|	ИсходныеДанные.НомерСтроки КАК НомерСтроки,
	|	НоменклатураДляПоиска.Ссылка КАК СсылкаНайденая
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураДляПоиска
	|		ПО ИсходныеДанные.СтрокаИзФайла = НоменклатураДляПоиска.Наименование
	|ГДЕ
	|	(НЕ НоменклатураДляПоиска.ЭтоГруппа)
	|	И (НЕ НоменклатураДляПоиска.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
	
	УсловиеСопоставления = СтрШаблон("ИсходныеДанные.СтрокаИзФайла = НоменклатураДляПоиска.%1", ИмяРеквизитаДляСопоставления);
	СхемаЗапроса.ПакетЗапросов[1].Операторы[0].Источники[0].Соединения[0].Условие = Новый ВыражениеСхемыЗапроса(УсловиеСопоставления);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("СтрокиИзФайла", СтрокиИзФайла);
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
		ЗагружаемыеДанные[Выборка.НомерСтроки - 1].СсылкаНайденая = Выборка.СсылкаНайденая;
	КонецЦикла;
	
КонецПроцедуры

// Получает актуальные остатки номенклатуры на складах
//
// Параметры:
//      СписокНоменклатуры - Массив 
//                            * СправочникСсылка.Номенклатура - номенклатура, остатки которой необходимо получить
//      ИспользуютсяНесколькоСкладов - Булево - признак того, что учет ведется по нескольким складам
// Возвращаемое значение:                        
//      Структура - данные по остаткам на складах
//          * ОбщийОстаток     - Число
//          * ОстаткиПоСкладам - ТаблицаЗначений:
//                        ** Склад            - СправочникСсылка.Склады - склад, по которому получен остаток
//                        ** Количество       - Число - остаток номенклатуры.
//                        ** ЕдиницаИзмерения - Строка - представление единицы измерения.
//
Функция ДанныеПоОстаткам(СписокНоменклатуры, ИспользуютсяНесколькоСкладов) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОбщийОстаток", 0);
	
	ОстаткиПоСкладам = Новый ТаблицаЗначений;
	ОстаткиПоСкладам.Колонки.Добавить("Номенклатура");
	ОстаткиПоСкладам.Колонки.Добавить("Склад");
	ОстаткиПоСкладам.Колонки.Добавить("Количество");
	ОстаткиПоСкладам.Колонки.Добавить("ЕдиницаИзмерения");
	
	Результат.Вставить("ОстаткиПоСкладам", ОстаткиПоСкладам);
	
	Запрос = Новый Запрос;
	
	Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяССылка)
	|			ТОГДА &Склад
	|		ИНАЧЕ &НеУказанСклад
	|	КОНЕЦ КАК Склад,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Номенклатура).ЕдиницаИзмерения) КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(, Счет В (&СчетаУчетаТоваров), &ВидыСубконто, Субконто1 В (&СписокНоменклатуры)) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1,
	|	&НеУказанСклад,
	|	ХозрасчетныйОстатки.КоличествоОстаток,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Номенклатура).ЕдиницаИзмерения)
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(, Счет В (&СчетаУчетаТоваровБезСкладов), &ВидыСубконтоНоменклатура, Субконто1 В (&СписокНоменклатуры)) КАК ХозрасчетныйОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Количество УБЫВ
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	Номенклатура";
	
	ВидыСубконтоНоменклатура = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		
	Запрос.УстановитьПараметр("ВидыСубконтоНоменклатура", ВидыСубконтоНоменклатура);
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Если ИспользуютсяНесколькоСкладов Тогда
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
		Текст = СтрЗаменить(Текст, "&Склад", "ХозрасчетныйОстатки.Субконто2");
	Иначе
		Текст = СтрЗаменить(Текст, "&Склад", НСтр("ru='""<Склад не указан>""'"));
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	
	СчетаУчетаТоваров = БухгалтерскиеОтчеты.СчетаУчетаТоваров();
	Запрос.УстановитьПараметр("СчетаУчетаТоваров", СчетаУчетаТоваров);
	Запрос.УстановитьПараметр("СчетаУчетаТоваровБезСкладов", СчетаУчетаТоваровБезСкладов(СчетаУчетаТоваров));
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.УстановитьПараметр("НеУказанСклад", НСтр("ru='<Склад не указан>'"));
	
	Запрос.Текст = Текст;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Результат.ОбщийОстаток = ВыборкаНоменклатура.Количество;
		
		Если Не ИспользуютсяНесколькоСкладов Тогда
			Прервать;
		КонецЕсли;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОстаткиПоСкладам.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НоменклатураВИерархии(ГруппаНоменклатуры, СписокУсловий = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|ГДЕ
	|	СправочникНоменклатура.Ссылка В ИЕРАРХИИ(&ГруппаНоменклатуры)";
	
	Если СписокУсловий <> Неопределено Тогда
		Для каждого СтрокаУсловие Из СписокУсловий Цикл
			Запрос.УстановитьПараметр(СтрокаУсловие.Ключ, СтрокаУсловие.Значение);
			Запрос.Текст = Запрос.Текст + СтрШаблон(" И СправочникНоменклатура.%1 = &%1", СтрокаУсловие.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ВыгрузитьКолонку(Запрос.Выполнить().Выгрузить(), "Ссылка");
КонецФункции

Процедура ИзменитьПризнакВАрхиве(СписокСсылок, СообщениеОбОшибке) Экспорт
	НачатьТранзакцию();
	
	Для каждого Ссылка Из СписокСсылок Цикл
		Объект = Ссылка.ПолучитьОбъект();
		Объект.ВАрхиве = НЕ Объект.ВАрхиве;
		Попытка
			Объект.Записать();
		Исключение
			ДанныеОбОшибке = ИнформацияОбОшибке();
			СообщениеОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ДанныеОбОшибке);
			ОтменитьТранзакцию();
			Возврат;
		КонецПопытки;
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
КонецПроцедуры


Функция СопоставлениеНоменклатурыКонтрагентов(НоменклатураСсылка) Экспорт
	Отбор = Новый Структура;
	НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(НоменклатураСсылка);
		
	Отбор.Вставить("НоменклатураИБ", ОБщегоНазначенияКлиентСервер.ЗначениеВМассиве(НоменклатураИБ));
	Возврат СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Отбор, Истина);
КонецФункции

Процедура ОчиститьСопоставлениеНоменклатурыБЭД(НоменклатураСсылка) Экспорт
	СопоставлениеНоменклатуры = СопоставлениеНоменклатурыКонтрагентов(НоменклатураСсылка);
	Для каждого ЭлементСопоставления Из СопоставлениеНоменклатуры Цикл
		СопоставлениеНоменклатурыКонтрагентов.УдалитьСоответствиеНоменклатуры(ЭлементСопоставления.НоменклатураКонтрагента);
	КонецЦикла;
КонецПроцедуры

Функция СопоставлениеНоменклатурыМаркетплейсов(НоменклатураСсылка) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Номенклатура", НоменклатураСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	НоменклатураМаркетплейсов.Маркетплейс КАК Маркетплейс,
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|ГДЕ
	|	НоменклатураМаркетплейсов.Номенклатура = &Номенклатура";
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Процедура ОчиститьСопоставлениеНоменклатурыМаркетплейсы(Ссылка) Экспорт
	СопоставлениеНоменклатуры  = СопоставлениеНоменклатурыМаркетплейсов(Ссылка);
	Для каждого СтрокаСопоставления Из СопоставлениеНоменклатуры Цикл
		МенеджерЗаписи = РегистрыСведений.НоменклатураМаркетплейсов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаСопоставления);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
КонецПроцедуры

Процедура ОчиститьСопоставлениеНоменклатурыОФД(Ссылка) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Номенклатура", ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СопоставлениеНоменклатурыОФД.НоменклатураНаименование КАК НоменклатураНаименование,
	|	СопоставлениеНоменклатурыОФД.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.СопоставлениеНоменклатурыОФД КАК СопоставлениеНоменклатурыОФД
	|ГДЕ
	|	СопоставлениеНоменклатурыОФД.Номенклатура = &Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.СопоставлениеНоменклатурыОФД.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
КонецПроцедуры

Процедура ОчиститьСопоставлениеНоменклатурыНоменклатурыИнтернетМагазина(Ссылка) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Номенклатура", ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураИнтернетМагазина.ИдентификаторНоменклатуры КАК ИдентификаторНоменклатуры
	|ИЗ
	|	РегистрСведений.НоменклатураИнтернетМагазина КАК НоменклатураИнтернетМагазина
	|ГДЕ
	|	НоменклатураИнтернетМагазина.Номенклатура = &Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.НоменклатураИнтернетМагазина.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
КонецПроцедуры

#Область ОплатаСервиса

Процедура УстановитьЦену(Номенклатура, Цена) Экспорт
	
	// Цены хранятся в карточке номенклатуры, поэтому эта функциональность должна быть включена.
	НастройкаЗаполненияЦеныПродажи = Константы.НастройкаЗаполненияЦеныПродажи.Получить();
	Если НастройкаЗаполненияЦеныПродажи <> Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура Тогда
		Константы.НастройкаЗаполненияЦеныПродажи.Установить(Перечисления.НастройкаЗаполненияЦеныПродажи.Номенклатура);
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	МенеджерЗаписи                      = РегистрыСведений.ЦеныНоменклатурыДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура         = Номенклатура;
	МенеджерЗаписи.СпособЗаполненияЦены = Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам;
	МенеджерЗаписи.Валюта               = ВалютаРегламентированногоУчета;
	МенеджерЗаписи.Цена                 = Цена;
	МенеджерЗаписи.ЦенаВключаетНДС      = Истина;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьВидыНоменклатуры() Экспорт
	
	//Проверим необходимость обновления
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)";
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СоответствиеИменВидовНоменклатуры", СоответствиеИменВидовНоменклатуры()); 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоответствиеИменВидовНоменклатуры.Наименование,
	|	СоответствиеИменВидовНоменклатуры.Счет
	|ПОМЕСТИТЬ ВТСоответствиеИменВидовНоменклатуры
	|ИЗ
	|	&СоответствиеИменВидовНоменклатуры КАК СоответствиеИменВидовНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаУчетаНоменклатуры.Номенклатура.Ссылка КАК НоменклатураСсылка,
	|	СчетаУчетаНоменклатуры.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	СчетаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ЕСТЬNULL(СчетаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.Услуга, НЕ ЕСТЬNULL(СчетаУчетаНоменклатуры.СчетУчета.Количественный, ЛОЖЬ)) КАК Услуга
	|ПОМЕСТИТЬ ВТГруппыНоменклатуры
	|ИЗ
	|	РегистрСведений.СчетаУчетаНоменклатуры КАК СчетаУчетаНоменклатуры
	|ГДЕ
	|	СчетаУчетаНоменклатуры.Номенклатура.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	Номенклатура.Наименование,
	|	Номенклатура.ВидНоменклатуры,
	|	ЕСТЬNULL(Номенклатура.ВидНоменклатуры.Услуга, ЛОЖЬ)
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа
	|	И Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕСТЬNULL(ИменаВидовНоменклатуры.Наименование, ВЫБОР
	|			КОГДА ЕСТЬNULL(СчетаУчетаНоменклатуры.СчетУчета.Количественный, ЛОЖЬ)
	|				ТОГДА СчетаУчетаНоменклатуры.СчетУчета.Наименование
	|			ИНАЧЕ ""Товары""
	|		КОНЕЦ) КАК ИмяВидаНоменклатуры,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(СчетаУчетаНоменклатуры.СчетУчета.Количественный, ИСТИНА)
	|			ТОГДА СчетаУчетаНоменклатуры.СчетУчета.Наименование
	|		ИНАЧЕ ""Услуги""
	|	КОНЕЦ КАК ИмяВидаНоменклатурыУслуга,
	|	НЕ ЕСТЬNULL(СчетаУчетаНоменклатуры.СчетУчета.Количественный, ИСТИНА) КАК Услуга
	|ПОМЕСТИТЬ ВТГруппаПустыхЭлементов
	|ИЗ
	|	РегистрСведений.СчетаУчетаНоменклатуры КАК СчетаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеИменВидовНоменклатуры КАК ИменаВидовНоменклатуры
	|		ПО СчетаУчетаНоменклатуры.СчетУчета = ИменаВидовНоменклатуры.Счет
	|ГДЕ
	|	СчетаУчетаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетаУчетаНоменклатуры.Организация,
	|	СчетаУчетаНоменклатуры.Склад,
	|	СчетаУчетаНоменклатуры.ТипСклада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТГруппыНоменклатуры.НоменклатураСсылка,
	|	ВТГруппыНоменклатуры.НоменклатураНаименование,
	|	ВТГруппыНоменклатуры.НоменклатураНаименование КАК НоменклатураНаименованиеУслуга,
	|	ВЫБОР
	|		КОГДА ВТГруппыНоменклатуры.Услуга
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ВТГруппыНоменклатуры.ВидНоменклатуры
	|	КОНЕЦ КАК ВидНоменклатуры,
	|	ВЫБОР
	|		КОГДА ВТГруппыНоменклатуры.Услуга
	|			ТОГДА ВТГруппыНоменклатуры.ВидНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ВидНоменклатурыУслуга,
	|	ВТГруппыНоменклатуры.Услуга КАК Услуга
	|ПОМЕСТИТЬ ВТВидыНоменклатуры
	|ИЗ
	|	ВТГруппыНоменклатуры КАК ВТГруппыНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ВТГруппаПустыхЭлементов.ИмяВидаНоменклатуры,
	|	ВТГруппаПустыхЭлементов.ИмяВидаНоменклатурыУслуга,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка),
	|	ВТГруппаПустыхЭлементов.Услуга
	|ИЗ
	|	ВТГруппаПустыхЭлементов КАК ВТГруппаПустыхЭлементов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыНоменклатуры.НоменклатураСсылка КАК НоменклатураСсылка,
	|	МАКСИМУМ(ВТВидыНоменклатуры.НоменклатураНаименование) КАК НоменклатураНаименование,
	|	МАКСИМУМ(ВТВидыНоменклатуры.НоменклатураНаименованиеУслуга) КАК НоменклатураНаименованиеУслуга,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТВидыНоменклатуры.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|					И НЕ ВидыНоменклатуры.Ссылка ЕСТЬ NULL 
	|				ТОГДА ВидыНоменклатуры.Ссылка
	|			ИНАЧЕ ВТВидыНоменклатуры.ВидНоменклатуры
	|		КОНЕЦ) КАК ВидНоменклатуры,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТВидыНоменклатуры.ВидНоменклатурыУслуга = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|					И НЕ ВидыНоменклатурыУслуга.Ссылка ЕСТЬ NULL 
	|				ТОГДА ВидыНоменклатурыУслуга.Ссылка
	|			ИНАЧЕ ВТВидыНоменклатуры.ВидНоменклатурыУслуга
	|		КОНЕЦ) КАК ВидНоменклатурыУслуга,
	|	МИНИМУМ(ВТВидыНоменклатуры.Услуга) КАК Услуга
	|ИЗ
	|	ВТВидыНоменклатуры КАК ВТВидыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО ВТВидыНоменклатуры.НоменклатураНаименование = ВидыНоменклатуры.Наименование
	|			И (НЕ ВидыНоменклатуры.Услуга)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатурыУслуга
	|		ПО ВТВидыНоменклатуры.НоменклатураНаименованиеУслуга = ВидыНоменклатурыУслуга.Наименование
	|			И (ВидыНоменклатурыУслуга.Услуга)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВидыНоменклатуры.НоменклатураСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураСсылка";
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	ТаблицаРезультата.Индексы.Добавить("НоменклатураСсылка");
	ТаблицаРезультата.Индексы.Добавить("ВидНоменклатуры");
	ТаблицаРезультата.Индексы.Добавить("ВидНоменклатурыУслуга");
	
	Если ТаблицаРезультата.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицыПоУмолчанию = ТаблицаРезультата[0];
	ПовторноеВыполнение = Ложь;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицыПоУмолчанию.ВидНоменклатуры) Тогда
		ЭлементСправочника = Справочники.ВидыНоменклатуры.СоздатьЭлемент();				
		ЭлементСправочника.Наименование = СтрокаТаблицыПоУмолчанию.НоменклатураНаименование;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементСправочника);
		СтрокаТаблицыПоУмолчанию.ВидНоменклатуры = ЭлементСправочника.Ссылка;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицыПоУмолчанию.ВидНоменклатурыУслуга) Тогда
		ЭлементСправочника = Справочники.ВидыНоменклатуры.СоздатьЭлемент();				
		ЭлементСправочника.Наименование = СтрокаТаблицыПоУмолчанию.НоменклатураНаименованиеУслуга;
		ЭлементСправочника.Услуга = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементСправочника);
		СтрокаТаблицыПоУмолчанию.ВидНоменклатурыУслуга = ЭлементСправочника.Ссылка;
	КонецЕсли;
	
	Для НомерСтроки = 1 По ТаблицаРезультата.Количество()-1 Цикл
		СтрокаРезультата = ТаблицаРезультата[НомерСтроки];
		Если ЗначениеЗаполнено(СтрокаРезультата.ВидНоменклатуры) ИЛИ 
			ЗначениеЗаполнено(СтрокаРезультата.ВидНоменклатурыУслуга) Тогда
			//Вид номенклатуры уже создан и используется для товаров
			ПовторноеВыполнение = Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаРезультата.ВидНоменклатуры) Тогда
			Если СтрокаРезультата.Услуга 
				ИЛИ СтрокаТаблицыПоУмолчанию.НоменклатураНаименование = СтрокаРезультата.НоменклатураНаименование Тогда
				СтрокаРезультата.ВидНоменклатуры = СтрокаТаблицыПоУмолчанию.ВидНоменклатуры;
			Иначе
				ЭлементСправочника = Справочники.ВидыНоменклатуры.СоздатьЭлемент();				
				ЭлементСправочника.Наименование = СтрокаРезультата.НоменклатураНаименование;
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементСправочника);
				СтрокаРезультата.ВидНоменклатуры = ЭлементСправочника.Ссылка;
			КонецЕсли;
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(СтрокаРезультата.ВидНоменклатурыУслуга) Тогда
			Если (НЕ СтрокаРезультата.Услуга) 
				ИЛИ СтрокаТаблицыПоУмолчанию.НоменклатураНаименованиеУслуга = СтрокаРезультата.НоменклатураНаименованиеУслуга Тогда
				СтрокаРезультата.ВидНоменклатурыУслуга = СтрокаТаблицыПоУмолчанию.ВидНоменклатурыУслуга;
			Иначе
				ЭлементСправочника = Справочники.ВидыНоменклатуры.СоздатьЭлемент();				
				ЭлементСправочника.Наименование = СтрокаРезультата.НоменклатураНаименованиеУслуга;
				ЭлементСправочника.Услуга = Истина;
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементСправочника);
				СтрокаРезультата.ВидНоменклатурыУслуга = ЭлементСправочника.Ссылка;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ВыборкаНоменклатуры = Справочники.Номенклатура.ВыбратьИерархически();
	
	ТекущееПравило	= СтрокаТаблицыПоУмолчанию;
	ТекущийУровень 	= -1;
	
	Пока ВыборкаНоменклатуры.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаНоменклатуры.ВидНоменклатуры) Тогда
			Продолжить;
		КонецЕсли;
		Если ВыборкаНоменклатуры.ЭтоГруппа Тогда
			//Если это группа, проверим, есть ли для нее правило.
			//Если своего правила нет, действует правило вышестоящей группы
			ПравилоДляГруппы = ТаблицаРезультата.Найти(ВыборкаНоменклатуры.Ссылка, "НоменклатураСсылка");
			Если ПравилоДляГруппы = Неопределено Тогда
				//Проверим, что действующее правило определено для группы более высокого уровня
				//При повторной обработке часть элементов могут быть пропущены, поэтому доверять можно только правилу владельца
				Если (НЕ ВыборкаНоменклатуры.УровеньВВыборке() > ТекущийУровень)
					ИЛИ ПовторноеВыполнение 
					ИЛИ (НЕ ЗначениеЗаполнено(ТекущееПравило)) Тогда
					Если ЗначениеЗаполнено(ВыборкаНоменклатуры.Родитель) Тогда
						ТекущийВид 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаНоменклатуры.Родитель, "ВидНоменклатуры");
						ТекущийУровень 	= ВыборкаНоменклатуры.УровеньВВыборке()-1;
						ТекущееПравило	= ТаблицаРезультата.Найти(ТекущийВид, "ВидНоменклатуры, ВидНоменклатурыУслуга");
					Иначе
						ТекущийУровень 	= -1;
						ТекущееПравило	= СтрокаТаблицыПоУмолчанию;
					КонецЕсли;
				КонецЕсли;
			Иначе
				ТекущийУровень 	= ВыборкаНоменклатуры.УровеньВВыборке();
				ТекущееПравило 	= ПравилоДляГруппы;
			КонецЕсли;
		Иначе //Проверим соотвествие признака "Услуга" в номенклатуре и виде номенклатуры
			
			//Проверим, что действующее правило определено для группы более высокого уровня
			//При повторной обработке часть элементов могут быть пропущены, поэтому доверять можно только правилу владельца
			Если (НЕ ВыборкаНоменклатуры.УровеньВВыборке() > ТекущийУровень)
				ИЛИ ПовторноеВыполнение 
				ИЛИ (НЕ ЗначениеЗаполнено(ТекущееПравило)) Тогда
				Если ЗначениеЗаполнено(ВыборкаНоменклатуры.Родитель) Тогда
					ТекущийВид 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаНоменклатуры.Родитель, "ВидНоменклатуры");
					ТекущийУровень 	= ВыборкаНоменклатуры.УровеньВВыборке()-1;
					ТекущееПравило	= ТаблицаРезультата.Найти(ТекущийВид, "ВидНоменклатуры, ВидНоменклатурыУслуга");					
				Иначе
					ТекущийУровень 	= -1;
					ТекущееПравило	= СтрокаТаблицыПоУмолчанию;
				КонецЕсли;										
			КонецЕсли;
				
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекущееПравило) Тогда
			Продолжить;
		КонецЕсли;
		
		//Обработаем элемент номенклатуры
		ЭлементНоменклатуры = ВыборкаНоменклатуры.ПолучитьОбъект();
		Если ВыборкаНоменклатуры.ЭтоГруппа Тогда
			ЭлементНоменклатуры.ВидНоменклатуры = ?(ТекущееПравило.Услуга, ТекущееПравило.ВидНоменклатурыУслуга, ТекущееПравило.ВидНоменклатуры);
		Иначе
			ЭлементНоменклатуры.ВидНоменклатуры = ?(ВыборкаНоменклатуры.Услуга, ТекущееПравило.ВидНоменклатурыУслуга, ТекущееПравило.ВидНоменклатуры);
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементНоменклатуры, Истина);
				
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВидыСтавокНДС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.УдалитьСтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ВидСтавкиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.ПустаяСсылка)
	|	И Номенклатура.УдалитьСтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
	|	И НЕ Номенклатура.ЭтоГруппа";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоменклатураОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НоменклатураОбъект.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.ВидСтавки(Выборка.СтавкаНДС);
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НоменклатураОбъект, Ложь);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВидМаркировки(ПараметрыОбработчика) Экспорт
	
	ТекстЗапроса = ТекстЗапросаВидМаркировки();
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Условие", Истина);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Ложь;
	ОбработканоЗаписей = 0;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.Номенклатура");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		ПредставлениеСсылки = Выборка.ПредставлениеСсылки;
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка.Заблокировать();
			
			НоменклатураОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если НоменклатураОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ОбъектБылИзмененДругимСеансом = ЗначениеЗаполнено(НоменклатураОбъект.ВидМаркировки)
				И ЗначениеЗаполнено(НоменклатураОбъект.СтранаПроисхождения);
				
			Если ОбъектБылИзмененДругимСеансом Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(НоменклатураОбъект.ВидМаркировки) 
				И ЗначениеЗаполнено(Выборка.ВидМаркировки) Тогда
				НоменклатураОбъект.ВидМаркировки = Выборка.ВидМаркировки;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(НоменклатураОбъект.СтранаПроисхождения) Тогда
				НоменклатураОбъект.СтранаПроисхождения = Справочники.СтраныМира.Россия;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НоменклатураОбъект);
			ОбработканоЗаписей = ОбработканоЗаписей + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение = СтрШаблон(
				НСтр("ru = 'Ошибка при выполнении обработчика Справочники.Номенклатура.ЗаполнитьВидМаркировки.
				|%1'"),
				ПредставлениеОшибки);
			
			ОбновлениеИнформационнойБазы.ЗаписатьОшибкуВЖурналРегистрации(
				Выборка.Ссылка,
				ПредставлениеСсылки,
				Сообщение);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработканоЗаписей = 0 Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаВидМаркировки()
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СпрНоменклатура.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СпрНоменклатура.Ссылка) КАК ПредставлениеСсылки,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
	|		КОГДА СпрНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Обувь)
	|		КОГДА СпрНоменклатура.ЛегкаяПромышленность
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность)
	|		КОГДА СпрНоменклатура.МолочнаяПродукцияПодконтрольнаяВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		КОГДА СпрНоменклатура.МолочнаяПродукцияБезВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС)
	|		КОГДА СпрНоменклатура.Шины
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Шины)
	|		КОГДА СпрНоменклатура.АльтернативныйТабак
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак)
	|		КОГДА СпрНоменклатура.УпакованнаяВода
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.УпакованнаяВода)
	|		КОГДА СпрНоменклатура.Фотоаппараты
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Фотоаппараты)
	|		КОГДА СпрНоменклатура.Духи
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Духи)
	|		КОГДА СпрНоменклатура.Велосипеды
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Велосипеды)
	|		КОГДА СпрНоменклатура.Антисептики
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Антисептики)
	|		КОГДА СпрНоменклатура.БАДы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БАДы)
	|		КОГДА СпрНоменклатура.НикотиносодержащаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция)
	|		КОГДА СпрНоменклатура.Пиво
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)
	|		КОГДА СпрНоменклатура.КреслаКоляски
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КреслаКоляски)
	|		КОГДА СпрНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха)
	|		КОГДА СпрНоменклатура.БезалкогольноеПиво
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво)
	|		КОГДА СпрНоменклатура.СоковаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.СоковаяПродукция)
	|		КОГДА СпрНоменклатура.Зерно
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно)
	|		КОГДА СпрНоменклатура.ПродуктыПереработкиЗерна
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)
	|		КОГДА СпрНоменклатура.ЗерноВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ЗерноВЕТИС)
	|		КОГДА СпрНоменклатура.ПродуктыПереработкиЗернаВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗернаВЕТИС)
	|		КОГДА СпрНоменклатура.МорепродуктыПодконтрольныеВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС)
	|		КОГДА СпрНоменклатура.ПодконтрольнаяПродукцияСАТУРН
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПодконтрольнаяПродукцияСАТУРН)
	|		КОГДА СпрНоменклатура.КормаДляЖивотныхПодконтрольныеВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		КОГДА СпрНоменклатура.МясоПодконтрольноеВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МясоПодконтрольноеВЕТИС)
	|		КОГДА СпрНоменклатура.ВетеринарныеПрепараты
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ВетеринарныеПрепараты)
	|		КОГДА СпрНоменклатура.ИгрыИИгрушкиДляДетей
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ИгрыИИгрушкиДляДетей)
	|		КОГДА СпрНоменклатура.РадиоэлектроннаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.РадиоэлектроннаяПродукция)
	|		КОГДА СпрНоменклатура.ТитановаяМеталлопродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ТитановаяМеталлопродукция)
	|		КОГДА СпрНоменклатура.КонсервированнаяПродукцияПодконтрольнаяВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		КОГДА СпрНоменклатура.РастительныеМасла
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.РастительныеМасла)
	|		КОГДА СпрНоменклатура.ОптоволокноИОптоволоконнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ОптоволокноИОптоволоконнаяПродукция)
	|		КОГДА СпрНоменклатура.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия)
	|		КОГДА СпрНоменклатура.КормаДляЖивотныхБезВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС)
	|		КОГДА СпрНоменклатура.КонсервированнаяПродукцияБезВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияБезВЕТИС)
	|		КОГДА СпрНоменклатура.ПечатнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПечатнаяПродукция)
	|		КОГДА СпрНоменклатура.СтроительныеМатериалы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.СтроительныеМатериалы)
	|		КОГДА СпрНоменклатура.ОтопительныеПриборы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ОтопительныеПриборы)
	|		КОГДА СпрНоменклатура.Бакалея
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Бакалея)
	|		КОГДА СпрНоменклатура.АлкогольнаяПродукцияДо9Процентов
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов)
	|		КОГДА СпрНоменклатура.ТелефоныИНоутбуки
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ТелефоныИНоутбуки)
	|		КОГДА СпрНоменклатура.ПиротехническиеИзделияИСредстваПожарнойБезопасности
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПиротехническиеИзделияИСредстваПожарнойБезопасности)
	|		КОГДА СпрНоменклатура.КабельнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КабельнаяПродукция)
	|		КОГДА СпрНоменклатура.МоторныеМасла
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МоторныеМасла)
	|		КОГДА СпрНоменклатура.БезалкогольныеНапитки
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольныеНапитки)
	|		КОГДА СпрНоменклатура.ПивоВПотребительскихУпаковках
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках)
	|		КОГДА СпрНоменклатура.ТехническиеСредстваРеабилитации
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ТехническиеСредстваРеабилитации)
	|		КОГДА СпрНоменклатура.МедицинскиеИзделия
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МедицинскиеИзделия)
	|		КОГДА СпрНоменклатура.МедицинскиеИзделия20
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МедицинскиеИзделия20)
	|		КОГДА СпрНоменклатура.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС)
	|		КОГДА СпрНоменклатура.КормаДляЖивотныхВлажныеБезВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС)
	|		КОГДА СпрНоменклатура.ЛегкаяПромышленность2025
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность2025)
	|		КОГДА СпрНоменклатура.ПодконтрольнаяПродукцияВЕТИС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПодконтрольнаяПродукцияВЕТИС)
	|		КОГДА НЕ СведенияОбАлкогольнойПродукции.Номенклатура ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Алкогольная)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ КАК ВидМаркировки
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
	|		ПО (СведенияОбАлкогольнойПродукции.Номенклатура = СпрНоменклатура.Ссылка)
	|ГДЕ
	|	НЕ СпрНоменклатура.ЭтоГруппа
	|	И СпрНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	И НЕ СпрНоменклатура.Услуга
	|	И &Условие
	|	И (СпрНоменклатура.ТабачнаяПродукция
	|			ИЛИ СпрНоменклатура.ОбувнаяПродукция
	|			ИЛИ СпрНоменклатура.ЛегкаяПромышленность
	|			ИЛИ СпрНоменклатура.МолочнаяПродукцияПодконтрольнаяВЕТИС
	|			ИЛИ СпрНоменклатура.МолочнаяПродукцияБезВЕТИС
	|			ИЛИ СпрНоменклатура.Шины
	|			ИЛИ СпрНоменклатура.АльтернативныйТабак
	|			ИЛИ СпрНоменклатура.УпакованнаяВода
	|			ИЛИ СпрНоменклатура.Фотоаппараты
	|			ИЛИ СпрНоменклатура.Духи
	|			ИЛИ СпрНоменклатура.Велосипеды
	|			ИЛИ СпрНоменклатура.Антисептики
	|			ИЛИ СпрНоменклатура.БАДы
	|			ИЛИ СпрНоменклатура.НикотиносодержащаяПродукция
	|			ИЛИ СпрНоменклатура.Пиво
	|			ИЛИ СпрНоменклатура.КреслаКоляски
	|			ИЛИ СпрНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|			ИЛИ СпрНоменклатура.БезалкогольноеПиво
	|			ИЛИ СпрНоменклатура.СоковаяПродукция
	|			ИЛИ СпрНоменклатура.Зерно
	|			ИЛИ СпрНоменклатура.ПродуктыПереработкиЗерна
	|			ИЛИ СпрНоменклатура.ЗерноВЕТИС
	|			ИЛИ СпрНоменклатура.ПродуктыПереработкиЗернаВЕТИС
	|			ИЛИ СпрНоменклатура.МорепродуктыПодконтрольныеВЕТИС
	|			ИЛИ СпрНоменклатура.ПодконтрольнаяПродукцияСАТУРН
	|			ИЛИ СпрНоменклатура.КормаДляЖивотныхПодконтрольныеВЕТИС
	|			ИЛИ СпрНоменклатура.МясоПодконтрольноеВЕТИС
	|			ИЛИ СпрНоменклатура.ВетеринарныеПрепараты
	|			ИЛИ СпрНоменклатура.ИгрыИИгрушкиДляДетей
	|			ИЛИ СпрНоменклатура.РадиоэлектроннаяПродукция
	|			ИЛИ СпрНоменклатура.ТитановаяМеталлопродукция
	|			ИЛИ СпрНоменклатура.КонсервированнаяПродукцияПодконтрольнаяВЕТИС
	|			ИЛИ СпрНоменклатура.РастительныеМасла
	|			ИЛИ СпрНоменклатура.ОптоволокноИОптоволоконнаяПродукция
	|			ИЛИ СпрНоменклатура.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия
	|			ИЛИ СпрНоменклатура.КормаДляЖивотныхБезВЕТИС
	|			ИЛИ СпрНоменклатура.КонсервированнаяПродукцияБезВЕТИС
	|			ИЛИ СпрНоменклатура.ПечатнаяПродукция
	|			ИЛИ СпрНоменклатура.СтроительныеМатериалы
	|			ИЛИ СпрНоменклатура.ОтопительныеПриборы
	|			ИЛИ СпрНоменклатура.Бакалея
	|			ИЛИ СпрНоменклатура.АлкогольнаяПродукцияДо9Процентов
	|			ИЛИ СпрНоменклатура.ТелефоныИНоутбуки
	|			ИЛИ СпрНоменклатура.ПиротехническиеИзделияИСредстваПожарнойБезопасности
	|			ИЛИ СпрНоменклатура.КабельнаяПродукция
	|			ИЛИ СпрНоменклатура.МоторныеМасла
	|			ИЛИ СпрНоменклатура.БезалкогольныеНапитки
	|			ИЛИ СпрНоменклатура.ПивоВПотребительскихУпаковках
	|			ИЛИ СпрНоменклатура.ТехническиеСредстваРеабилитации
	|			ИЛИ СпрНоменклатура.МедицинскиеИзделия
	|			ИЛИ СпрНоменклатура.МедицинскиеИзделия20
	|			ИЛИ СпрНоменклатура.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС
	|			ИЛИ СпрНоменклатура.КормаДляЖивотныхВлажныеБезВЕТИС
	|			ИЛИ СпрНоменклатура.ЛегкаяПромышленность2025
	|			ИЛИ СпрНоменклатура.ПодконтрольнаяПродукцияВЕТИС
	|			ИЛИ НЕ СведенияОбАлкогольнойПродукции.Номенклатура ЕСТЬ NULL)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ВидМаркировкиЗаполнен(МетаданныеИОтбор) Экспорт 
	
	ЭлементСправочника = МетаданныеИОтбор.Отбор;
	
	Если Не ЗначениеЗаполнено(ЭлементСправочника) Или МетаданныеИОтбор.ЭтоНовый Тогда
		Возврат Истина;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВидМаркировки();
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "ПЕРВЫЕ 1");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "СпрНоменклатура.Ссылка = &ЭлементСправочника");
	Запрос.УстановитьПараметр("ЭлементСправочника", ЭлементСправочника);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура ЗаполнитьСтрануПроисхождения(ПараметрыОбработчика) Экспорт
	
	ТекстЗапроса = ТекстЗапросаПустаяСтранаПроисхождения();
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Условие", Истина);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Ложь;
	ОбработканоЗаписей = 0;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.Номенклатура");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		ПредставлениеСсылки = Выборка.ПредставлениеСсылки;
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка.Заблокировать();
			
			НоменклатураОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если НоменклатураОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ОбъектБылИзмененДругимСеансом = ЗначениеЗаполнено(НоменклатураОбъект.СтранаПроисхождения);
				
			Если ОбъектБылИзмененДругимСеансом Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(НоменклатураОбъект.СтранаПроисхождения) Тогда
				НоменклатураОбъект.СтранаПроисхождения = Справочники.СтраныМира.Россия;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НоменклатураОбъект);
			ОбработканоЗаписей = ОбработканоЗаписей + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение = СтрШаблон(
				НСтр("ru = 'Ошибка при выполнении обработчика Справочники.Номенклатура.ЗаполнитьСтрануПроисхождения.
				|%1'"),
				ПредставлениеОшибки);
			
			ОбновлениеИнформационнойБазы.ЗаписатьОшибкуВЖурналРегистрации(
				Выборка.Ссылка,
				ПредставлениеСсылки,
				Сообщение);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработканоЗаписей = 0 Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаПустаяСтранаПроисхождения()
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	Номенклатура.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Номенклатура.Ссылка) КАК ПредставлениеСсылки
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа
	|	И Номенклатура.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|	И НЕ Номенклатура.Услуга
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура.Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьПризнакПрослеживаемостиПриПереходеС20() Экспорт
	
	// Устанавливаем признак в номенклатуре для позиций, указанных в проведенных ПТУ и РТУ с заполненным РНПТ.
	Запрос = Новый запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ НоменклатураКОбработке
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.УдалитьРНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ПоступлениеТоваровУслугТовары.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугОборудование.Номенклатура
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|ГДЕ
	|	ПоступлениеТоваровУслугОборудование.Ссылка.Проведен
	|	И ПоступлениеТоваровУслугОборудование.УдалитьРНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Номенклатура
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка.Проведен
	|	И РеализацияТоваровУслугТовары.УдалитьРНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НоменклатураКОбработке.Номенклатура КАК Ссылка
	|ИЗ
	|	НоменклатураКОбработке КАК НоменклатураКОбработке
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО НоменклатураКОбработке.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	НоменклатураСправочник.ПрослеживаемыйТовар = ЛОЖЬ";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// Если есть прослеживаемые товары, то установим константу "ВестиУчетПрослеживаемыхТоваров".
	Если НЕ Константы.ВестиУчетПрослеживаемыхТоваров.Получить() Тогда
		Константы.ВестиУчетПрослеживаемыхТоваров.Установить(Истина);
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоменклатураОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НоменклатураОбъект.ПрослеживаемыйТовар = Истина;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НоменклатураОбъект, Ложь);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКонстантуСтранаПроисхожденияТоваров() Экспорт
	
	МенеджерКонстанты = Константы.ОсновнаяСтранаПроисхожденияТоваров.СоздатьМенеджерЗначения();
	МенеджерКонстанты.Значение = Справочники.СтраныМира.Россия;
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	
КонецПроцедуры

Процедура ЗаполнитьНастройкаЗаполненияЦеныПокупки() Экспорт
	
	МенеджерКонстанты = Константы.НастройкаЗаполненияЦеныПокупки.СоздатьМенеджерЗначения();
	МенеджерКонстанты.Значение = Перечисления.НастройкаЗаполненияЦеныПродажи.ПредыдущийДокумент;
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает параметры, необходимые для формирования отчета с расшифровкой по переданной номенклатуре.
//
// Параметры:
//  ИмяОтчета - Строка - имя отчета, для которого формируются параметры.
//  КлючВарианта - Строка - имя ключа варианта отчета
//  Номенклатура - СправочникСсылка.Номенклатура - значение отбора для формируемого отчета
//  Организация - СправочникСсылка.Организации - значение отбора для формируемого отчета
//              - Неопределено - не задачать отбор по организации
//  Склад - СправочникСсылка.Склады - значение отбора для формируемого отчета
//        - Неопределено - не задачать отбор по складу
//
// Возвращаемые значения:
//  Структура
//
Функция ПараметрыДляОтчета(ИмяОтчета, КлючВарианта, Номенклатура, Организация = Неопределено, Склад = Неопределено) Экспорт
	
	ТекущаяДатаНаСервере = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	
	ПользовательскиеНастройкиКомпоновкиДанных = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("НачалоПериода" , ТекущаяДатаНаСервере);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КонецПериода" , ТекущаяДатаНаСервере);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КлючВарианта", КлючВарианта);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Организация" , Организация);
	КонецЕсли;
	
	Отбор = ПользовательскиеНастройкиКомпоновкиДанных.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
	
	ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Номенклатура");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Номенклатура;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Склад");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Склад;
	КонецЕсли;
	
	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить(ИмяОтчета, ПользовательскиеНастройкиКомпоновкиДанных);
	
	УсловияОтбора = Новый Структура();
	УсловияОтбора.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	ОбщиеНастройки = Новый Структура();
	ОбщиеНастройки.Вставить("Объект", УсловияОтбора);
	ОбщиеНастройки.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбщиеНастройки, Новый УникальныйИдентификатор);
	
	ЗаполнятьТиповыеНастройки = Новый Структура;
	ЗаполнятьТиповыеНастройки.Вставить("Отбор", Ложь);
	ЗаполнятьТиповыеНастройки.Вставить("Группировка", Истина);
	ЗаполнятьТиповыеНастройки.Вставить("ВыводимыеДанные", Истина);
	ЗаполнятьТиповыеНастройки.Вставить("Показатели", Истина);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ВидРасшифровки", 1);
	ПараметрыОтчета.Вставить("АдресНастроек", АдресХранилища);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ИДРасшифровки", ИмяОтчета);
	ПараметрыОтчета.Вставить("РежимРасшифровки", Истина);
	ПараметрыОтчета.Вставить("ЗаполняемыеНастройки", ЗаполнятьТиповыеНастройки);
	ПараметрыОтчета.Вставить("КлючВарианта", КлючВарианта);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоответствиеИменВидовНоменклатуры()
	
	ТаблицаСоответствий = Новый ТаблицаЗначений;
	ТаблицаСоответствий.Колонки.Добавить("Наименование" , ВариантыОтчетов.ОписаниеТиповСтрока(200));
	ТаблицаСоответствий.Колонки.Добавить("Счет" , Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Товары";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ТоварыНаСкладах;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Материалы";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.СырьеИМатериалы;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Спецодежда";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаНаСкладе;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Инвентарь и хозяйственные принадлежности";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежности;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Товары на комиссии";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ТоварыНаСкладе;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Оборудование (объекты основных средств)";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ПриобретениеКомпонентовОсновныхСредств;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Оборудование к установке";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Полуфабрикаты";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.Полуфабрикаты;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Продукция";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ГотоваяПродукция;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Продукция из материалов заказчика";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Возвратная тара";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ТараПодТоваромИПорожняя;
	
	НоваяСтрока 				= ТаблицаСоответствий.Добавить();
	НоваяСтрока.Наименование 	= "Товары на ответственном хранении";
	НоваяСтрока.Счет 			= ПланыСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение;
	
	Возврат ТаблицаСоответствий;
	
КонецФункции

Функция СчетаУчетаТоваровБезСкладов(СчетаУчетаТоваров)

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Счет
	|ПОМЕСТИТЬ ВидыСубконто
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.Ссылка В(&СчетаУчетаТоваров)
	|	И ХозрасчетныйВидыСубконто.ВидСубконто = &ВидСубконтоСклады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	НЕ Хозрасчетный.Ссылка В
	|				(ВЫБРАТЬ
	|					ВидыСубконто.Счет КАК Счет
	|				ИЗ
	|					ВидыСубконто КАК ВидыСубконто)
	|	И Хозрасчетный.Ссылка В(&СчетаУчетаТоваров)";
	
	Запрос.УстановитьПараметр("СчетаУчетаТоваров", СчетаУчетаТоваров);
	Запрос.УстановитьПараметр("ВидСубконтоСклады", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("Счет");
	
КонецФункции

#КонецОбласти

#КонецЕсли

