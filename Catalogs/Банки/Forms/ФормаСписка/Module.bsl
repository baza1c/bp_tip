
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Банки);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	Элементы.ФормаПодобратьИзКлассификатора.Видимость = МожноРедактировать;
	Элементы.ФормаОбновитьИзКлассификатора.Видимость  = МожноРедактировать;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПослеДобавления" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Группа Тогда
		
		Текст = НСтр("ru = 'Есть возможность подобрать банк из классификатора.
		|Подобрать?'");
		
		ТекДанные = Элементы.Список.ТекущиеДанные;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Родитель", Родитель);
		Если Копирование Тогда
			ДополнительныеПараметры.Вставить("Наименование", ТекДанные.Наименование);
			ДополнительныеПараметры.Вставить("Код", ТекДанные.Код);
			ДополнительныеПараметры.Вставить("КоррСчет", ТекДанные.КоррСчет);
			ДополнительныеПараметры.Вставить("Город", ТекДанные.Город);
			ДополнительныеПараметры.Вставить("Адрес", ТекДанные.Адрес);
			ДополнительныеПараметры.Вставить("Телефоны", ТекДанные.Телефоны);
			ДополнительныеПараметры.Вставить("СВИФТБИК", ТекДанные.СВИФТБИК);
			ДополнительныеПараметры.Вставить("Страна", ТекДанные.Страна);
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ВопросЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, МножественныйВыбор", Ложь, Истина);
		ОткрытьФорму("Справочник.КлассификаторБанков.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	Иначе
		ПараметрыФормы = Новый Структура("Основание", ДополнительныеПараметры);
		ОткрытьФорму("Справочник.Банки.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзКлассификатора(Команда)
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, МножественныйВыбор", Ложь, Истина);
	ОткрытьФорму("Справочник.КлассификаторБанков.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзКлассификатора(Команда)
	
	ОписаниеЗаданияОбновленияКлассификатора = ОбновитьИзКлассификатораНаСервере();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанныеНаКлиенте", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Заголовок = НСтр("ru = 'Обновление банков из классификатора'");
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется обновление.'");
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ОписаниеЗаданияОбновленияКлассификатора, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанныеНаКлиенте(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДанных.Свойство("УспешноОбновлены") Тогда
		
		ТекстОповещения = НСтр("ru = 'Банки обновлены из классификатора'");
		ПоказатьОповещениеПользователя("Обновление",, ТекстОповещения);
		
	КонецЕсли;
	
	Оповестить("ОбновитьПослеДобавления");
	
КонецПроцедуры

&НаСервере
Функция ОбновитьИзКлассификатораНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление банков из классификатора'");
	ПараметрыВыполнения.ЗапуститьНеВФоне = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ОписаниеЗаданияОбновленияКлассификатора = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "Справочники.Банки.ОбновитьБанкиИзКлассификатора");
	
	Возврат ОписаниеЗаданияОбновленияКлассификатора;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Список.УсловноеОформление.Элементы.Очистить();
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РучноеИзменение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 3;
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
КонецПроцедуры

#КонецОбласти
