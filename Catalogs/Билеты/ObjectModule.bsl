#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		// Заполнение переданными данными
		ЗаполнениеДокументов.ЗаполнитьПоСтруктуре(ЭтотОбъект, ДанныеЗаполнения, Метаданные());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаПокупки) Тогда
		ДатаПокупки = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;	
	
	Наименование = ПредставлениеБилета();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Код = "";
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
		
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли; 
	
	Наименование = ПредставлениеБилета();
	
	Если Не ЭтоНовый() Тогда
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Сотрудник");
		ДополнительныеСвойства.Вставить("СотрудникДоЗаписи", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Сотрудник"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	СотрудникДоЗаписи = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "СотрудникДоЗаписи", Неопределено);
	Если Не Отказ И ЗначениеЗаполнено(Сотрудник) И СотрудникДоЗаписи <> Неопределено И Сотрудник <> СотрудникДоЗаписи Тогда
	
		// После выбора сотрудника в билете нужно перепровести документы "Операция с билетом",
		// чтобы выбранный сотрудник попал в проводки. Данная ситуация возможна только по билетам,
		// загруженным из внешней системы.
		
		// Операция обмена приходит из внешней системы двумя отдельными операциями: 
		//    - ЗаменаВозврат (в реквизите Билет - прежний билет)
		//    - ЗаменаПокупка (в реквизите Билет - новый билет)
		// Каждая из этих операций делает проводки только по билету, указанному в поле Билет. Поле БилетЗамена - для информации.
		// Таким образом, все операции по билету для всех видов операций находим по реквизиту Билет.
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОперацияСБилетом.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ОперацияСБилетом КАК ОперацияСБилетом
		|ГДЕ
		|	ОперацияСБилетом.Билет = &Билет";
		
		Запрос.УстановитьПараметр("Билет", Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Попытка
				Объект = Выборка.Ссылка.ПолучитьОбъект();
				Объект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ВызватьИсключение;
			КонецПопытки;
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает представление билета
// 
// Возвращаемое значение:
//   - строка
//
// Представление билета включает следующие поля (если заполнены, через запятую):
//	- Дата отправления
//	- Имя сотрудника (в формате Петров П.П.)
//	- Маршрут
//	- Номер билета (код)
Функция ПредставлениеБилета()

	// Представление билета включает следующие поля (если заполнены, через запятую):
	//	- Дата отправления
	//	- Имя сотрудника (в формате Петров П.П.)
	//	- Маршрут
	//	- Номер билета (код)
	ЧастиПредставления = Новый Массив;
	
	ЧастиПредставления.Добавить(Формат(ДатаОтправления, "ДЛФ=D"));
		
	Если ЗначениеЗаполнено(Сотрудник.ФИО) Тогда
		ЧастиПредставления.Добавить(ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Сотрудник.ФИО));
	КонецЕсли; 
	
	Если НЕ ПустаяСтрока(Маршрут) Тогда
		ЧастиПредставления.Добавить(Маршрут);
	КонецЕсли; 
	
	ЧастиПредставления.Добавить(Код);
	
	Представление = СтрСоединить(ЧастиПредставления, " ");
		
	Возврат Представление;

КонецФункции

#КонецОбласти

#КонецЕсли
