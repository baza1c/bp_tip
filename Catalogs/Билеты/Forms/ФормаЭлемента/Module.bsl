
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметры.ДополнительныеПараметры);
	КонецЕсли;
	
	ДанныеСотрудникаИзSmartway = ДанныеСотрудникаИзSmartway(Объект.СотрудникСтрокой);
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СотрудникАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Сотрудник) И ПустаяСтрока(Текст)
		И Не ПустаяСтрока(Объект.СотрудникСтрокой) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыПолученияДанных.СтрокаПоиска = ДанныеСотрудникаИзSmartway.НаименованиеСотрудника;
		ДанныеВыбора = ДанныеВыбораСотрудника(ДанныеСотрудникаИзSmartway);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Элемент.ТекстРедактирования) И ЗначениеЗаполнено(Объект.СотрудникСтрокой) Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("СправочникСсылка.ФизическиеЛица") Тогда
			СтандартнаяОбработка = Ложь;
			Объект.Сотрудник = СотрудникНаСервере(ДанныеСотрудникаИзSmartway);
			Модифицированность = Истина;
			УправлениеФормой();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	Если Не ЗначениеЗаполнено(Объект.Сотрудник) И ЗначениеЗаполнено(Объект.СотрудникСтрокой) Тогда
		Элементы.ГруппаБаннерСотрудник.Видимость = Истина;
		Элементы.БаннерСотрудник.Заголовок = СтрШаблон(НСтр("ru='Сотрудник при загрузке из Smartway не найден: %1.
			|Данные по нему сохранены, будут заполнены при создании сотрудника.'"), ДанныеСотрудникаИзSmartway.НаименованиеСотрудника);
	Иначе
		Элементы.ГруппаБаннерСотрудник.Видимость = Ложь;
		Элементы.БаннерСотрудник.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеСотрудникаИзSmartway(СотрудникСтрокой)
	
	Если Не ЗначениеЗаполнено(СотрудникСтрокой) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеПассажира = ОбщегоНазначения.ЗначениеИзСтрокиXML(СотрудникСтрокой);
	ДанныеСотрудникаИзSmartway = РасширениеEnterpriseDataКомандировки.ДанныеСотрудника(ОписаниеПассажира);
	ФамилияИмяОтчество = Новый Структура(
		"Фамилия, Имя, Отчество", ДанныеСотрудникаИзSmartway.Фамилия, ДанныеСотрудникаИзSmartway.Имя, ДанныеСотрудникаИзSmartway.Отчество);
	ДанныеСотрудникаИзSmartway.Вставить("НаименованиеСотрудника", КадровыйУчетКлиентСервер.ПолноеНаименованиеСотрудника(
		ФамилияИмяОтчество.Фамилия, ФамилияИмяОтчество.Имя, ФамилияИмяОтчество.Отчество, ""));
		
	Возврат ДанныеСотрудникаИзSmartway;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеВыбораСотрудника(ДанныеСотрудникаИзSmartway)
	
	ШрифтКоманды = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста);
	ШрифтПодстроки = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , Истина);
	ЦветПодстроки = ЦветаСтиля.РезультатУспехЦвет;
	
	ДанныеВыбора = ДанныеВыбораПоискПохожих(ДанныеСотрудникаИзSmartway, ШрифтПодстроки, ЦветПодстроки);
	
	Если Не ЗначениеЗаполнено(ДанныеВыбора) Тогда
		
		Представление = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(НСтр("ru='Создать:'") + " """, ШрифтКоманды),
			Новый ФорматированнаяСтрока(СокрЛП(ДанныеСотрудникаИзSmartway.НаименованиеСотрудника), ШрифтПодстроки, ЦветПодстроки),
			Новый ФорматированнаяСтрока("""", ШрифтКоманды));
		
		ДанныеВыбора.Добавить(Представление, Представление);
	КонецЕсли;
	
	Возврат ДанныеВыбора;

КонецФункции

&НаСервереБезКонтекста
Функция ДанныеВыбораПоискПохожих(ДанныеСотрудникаИзSmartway, ШрифтПодСтроки, ЦветПодстроки)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ПараметрыПоискаПохожих = СотрудникиКлиентСервер.ПараметрыПоискаПохожих();
	ЗаполнитьЗначенияСвойств(ПараметрыПоискаПохожих, ДанныеСотрудникаИзSmartway);
	ФизическиеЛицаПоИмени = СотрудникиФормы.РезультатПоискаФизическихЛиц(ПараметрыПоискаПохожих);
	
	НайденыЛюдиПоИмени = Не ФизическиеЛицаПоИмени.ФизическоеЛицоУникально;
	Если НайденыЛюдиПоИмени Тогда
		
		// свойство ДанныеФизическихЛицДоступны определено только если люди найдены
		Если Не ФизическиеЛицаПоИмени.ДанныеФизическихЛицДоступны Тогда
			Возврат ДанныеВыбора;
		КонецЕсли;
		
		ДатаРождения = ДанныеСотрудникаИзSmartway.ДатаРождения;
		Для Каждого ОписаниеФизическогоЛица Из ФизическиеЛицаПоИмени.ДанныеФизическихЛиц Цикл
			
			Если ЗначениеЗаполнено(ОписаниеФизическогоЛица.ДатаРождения) 
				И ЗначениеЗаполнено(ДатаРождения)
				И ОписаниеФизическогоЛица.ДатаРождения <> ДатаРождения Тогда
				Продолжить;
			КонецЕсли;
			
			ФизическоеЛицо = ОписаниеФизическогоЛица.ФизическоеЛицо;
			РеквизитыФизЛица = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФизическоеЛицо, "Код, Наименование");
			Представление = Новый ФорматированнаяСтрока(
				РеквизитыФизЛица.Наименование, ШрифтПодстроки, ЦветПодстроки);
					
			Представление = Новый ФорматированнаяСтрока(
				Представление,
				" (" + РеквизитыФизЛица.Код + ")");
			ДанныеВыбора.Добавить(ФизическоеЛицо, Представление);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

&НаСервереБезКонтекста
Функция СотрудникНаСервере(ДанныеСотрудникаИзSmartway)
	
	ЭтоНовый = Ложь;
	
	Ссылка = УчетЗарплаты.ФизическоеЛицо(
		ДанныеСотрудникаИзSmartway.Фамилия,
		ДанныеСотрудникаИзSmartway.Имя,
		ДанныеСотрудникаИзSmartway.Отчество,
		ДанныеСотрудникаИзSmartway.Пол,
		ДанныеСотрудникаИзSmartway.ДатаРождения,
		ДанныеСотрудникаИзSmartway.НомераДокументов,
		ДанныеСотрудникаИзSmartway.УникальныйИдентификатор,
		ЭтоНовый);
	
	Возврат Ссылка;
	
КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти