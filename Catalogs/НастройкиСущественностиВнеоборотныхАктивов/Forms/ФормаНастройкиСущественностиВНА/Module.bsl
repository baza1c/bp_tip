#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	АдресРедактированияНастройки = Параметры.АдресРедактированияНастройки;
	РедактированиеНастроек = ПолучитьИзВременногоХранилища(Параметры.АдресРедактированияНастройки);
	Настройки = РедактированиеНастроек.Настройки; // см. Справочники.НастройкиСущественностиВнеоборотныхАктивов.НовыйРедактированиеНастроекСущественныхВНА

	Если РедактированиеНастроек.ТолькоПросмотр Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;

	Элементы.ФормаОтмена.КнопкаПоУмолчанию = ТолькоПросмотр;

	СпособОпределенияСущественныхАктивов = Настройки.СпособУказанияЛимита;

	СуммаЛимита = Настройки.ЛимитСтоимости;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	РезультатЗакрытия = Неопределено;

	Если Модифицированность Тогда
		РезультатЗакрытия = ПрименитьНастройки(АдресРедактированияНастройки, СпособОпределенияСущественныхАктивов, СуммаЛимита);
	КонецЕсли;

	Закрыть(РезультатЗакрытия);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПрименитьНастройки(АдресРедактированияНастройки, Способ, Лимит)

	РезультатЗакрытия = ПолучитьИзВременногоХранилища(АдресРедактированияНастройки); // см. Справочники.НастройкиСущественностиВнеоборотныхАктивов.НовыйРедактированиеНастроекСущественныхВНА
	Настройки = РезультатЗакрытия.Настройки;
	Настройки.СпособУказанияЛимита = Способ;
	Настройки.ЛимитСтоимости = Лимит;
	РезультатЗакрытия.Модифицированность = Истина;

	Справочники.НастройкиСущественностиВнеоборотныхАктивов.ЗаполнитьКраткоеОписание(РезультатЗакрытия);

	ПоместитьВоВременноеХранилище(РезультатЗакрытия, АдресРедактированияНастройки);

	Возврат РезультатЗакрытия;

КонецФункции

#КонецОбласти