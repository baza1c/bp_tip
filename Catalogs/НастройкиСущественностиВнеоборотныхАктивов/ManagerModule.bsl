
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Готовит объект, позволяющий редактировать настройки лимита существенности внеобротных активов.
//
// Параметры:
//  РедактируемаяЗапись - РегистрСведенийМенеджерЗаписи.УчетнаяПолитика - редактируемая запись учетной политики.
// 
// Возвращаемое значение:
//  Структура - см. НовыйРедактированиеНастроекСущественныхВНА
//
Функция РедактированиеЗаписиУчетнойПолитики(РедактируемаяЗапись) Экспорт
	
	Настройки = НовыйНастройкиСущественностиВНА();
	
	ЗаполнитьНастройки(Настройки, РедактируемаяЗапись);
	РедактированиеНастроек = НовыйРедактированиеНастроекСущественныхВНА(Настройки);
	
	РедактированиеНастроек.Ссылка = РедактируемаяЗапись.НастройкиСущественностиВнеоборотныхАктивов;
	ЗаполнитьКраткоеОписание(РедактированиеНастроек);
	
	Возврат РедактированиеНастроек;
	
КонецФункции

// Конструктор настроек существенности внеоборотных активов.
// 
// Возвращаемое значение:
//  Структура:
// * Контекст - Структура:
// ** Организация - СправочникСсылка.Организации
// ** ПериодДействия - Дата
// * СпособУказанияЛимита - ПеречислениеСсылка.СпособыУказанияЛимитаСущественностиВНА
// * ЛимитСтоимости - Число
//
Функция НовыйНастройкиСущественностиВНА() Экспорт
	
	// Эти настройки будут действовать по умолчанию для существующих записей учетной политики.
	// Исходя из этих настроек формируются проводки документов.
	// Если впоследствии настройки по умолчанию будут изменены, может потребоваться создать обработчик обновления,
	// который заполнит этими значениями настройки существующих записей учетной политики.
	
	Настройки = Новый Структура;
	
	Настройки.Вставить("Контекст", НовыйКонтекстНастроекСущественностиВНА());
	Настройки.Вставить("СпособУказанияЛимита", Перечисления.СпособыУказанияЛимитаСущественностиВНА.ПоСтоимости);
	Настройки.Вставить("ЛимитСтоимости", ЛимитСущественностиПоУмолчанию());
	
	Возврат Настройки;
	
КонецФункции

// Настройки существенности внеоборотных активов.
// 
// Параметры:
//  КонтекстНастройки - Структура - КлючИЗначение:
//    * Организация - СправочникСсылка.Организации
//    * Период - Дата
//
// Возвращаемое значение:
//  Структура - см. Справочники.НастройкиСущественностиВнеоборотныхАктивов.НовыйНастройкиСущественностиВНА
//
Функция НастройкиСущественностиВНА(КонтекстНастройки) Экспорт
	
	Настройки = БухгалтерскийУчетПовтИсп.НастройкиСущественностиВНА(
		УчетнаяПолитика.ПериодКеша(КонтекстНастройки.Период),
		КонтекстНастройки.Организация);
	
	Возврат Настройки;
	
КонецФункции

// Конструктор коллекции, предназначенной для редактирования настроек существенности внеоборотных активов.
// Расширяет настройки сведениями, необходимыми для записи их в информационную базу.
//
// Параметры:
//  Настройки - Структура - см. НовыйНастройкиСущественностиВНА
//            - Неопределено
// 
// Возвращаемое значение:
//  Структура - см. РегистрыСведений.УчетнаяПолитика.НовыйРедактированиеСвязанныхЗаписейУчетнойПолитики
//
Функция НовыйРедактированиеНастроекСущественныхВНА(Знач Настройки = Неопределено) Экспорт
	
	Если Настройки = Неопределено Тогда
		Настройки = НовыйНастройкиСущественностиВНА();
	КонецЕсли;
	
	РедактированиеНастроек = РегистрыСведений.УчетнаяПолитика.НовыйРедактированиеСвязанныхЗаписейУчетнойПолитики(Настройки);
	// Проверяется неинтерактивное право, так как прикладным пользователям ограничено интерактивное редактирование объекта

	РедактированиеНастроек.ТолькоПросмотр = Не ПравоДоступа("Изменение", Метаданные.Справочники.НастройкиСущественностиВнеоборотныхАктивов)
														  Или БухгалтерскийУчетПереопределяемый.ОбособленноеПодразделение(Настройки.Контекст.Организация);
	РедактированиеНастроек.Видимость = ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА")
		И (Не ЗначениеЗаполнено(Настройки.Контекст.ПериодДействия) Или Настройки.Контекст.ПериодДействия > УчетНМА.НачалоОбязательногоПримененияФСБУ14());
	
	Возврат РедактированиеНастроек;
	
КонецФункции

// Возвращает текст настроек существенности внеоборотных активов для отображения на форме записи учетной политики.
// 
// Параметры:
//  РедактированиеНастроек - Структура - см. НовыйРедактированиеНастроекСущественныхВНА
//
Процедура ЗаполнитьКраткоеОписание(РедактированиеНастроек) Экспорт
	
	Настройки = РедактированиеНастроек.Настройки;
	
	ТекстСпособаУказания = НСтр("ru = 'без исключения'");
	Если Настройки.СпособУказанияЛимита = Перечисления.СпособыУказанияЛимитаСущественностиВНА.ПоСтоимости И Настройки.ЛимитСтоимости > 0 Тогда
		ТекстСпособаУказания = НСтр("ru = 'стоимостью более %1 за единицу'");
		ФорматСуммыЛимитСтоимости = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 %2'"), Формат(Настройки.ЛимитСтоимости, "ЧДЦ=0; ЧН=0"), Константы.ВалютаРегламентированногоУчета.Получить());
		ТекстСпособаУказания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСпособаУказания, ФорматСуммыЛимитСтоимости);
	КонецЕсли;
	
	КраткоеОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Нематериальными активами, информация о которых существенна для бухгалтерской отчетности, являются все активы <a href=""СущественныеНМА"">%1</a>.'"),
			ТекстСпособаУказания);
			
	РедактированиеНастроек.КраткоеОписание = СтроковыеФункции.ФорматированнаяСтрока(КраткоеОписание);
	
КонецПроцедуры

// Заполняет настройки существенности внеоборотных активов значениями из справочника.
// 
// Параметры:
//  Настройки - см. НовыйНастройкиСущественностиВНА
//  Ссылка - СправочникСсылка.НастройкиСущественностиВнеоборотныхАктивов
//
Процедура ПрочитатьСсылку(Настройки, Ссылка) Экспорт
	
	Запрос = Новый Запрос;
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = ТекстЗапросаНастройкаЛимита();

	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Настройки, Выборка);
	Иначе
		Настройки.СпособУказанияЛимита = Перечисления.СпособыУказанияЛимитаСущественностиВНА.НеПрименяется;
		Настройки.ЛимитСтоимости = 0;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет, что переданные данные соответствуют по структуре настройке существенности внеоборотных активов
//
// Параметры:
//  ПроверяемоеЗначение - см. НовыйНастройкиСущественностиВНА
// 
// Возвращаемое значение:
//  Булево - Истина, если переданное значение - настройки существенности внеоборотных активов
//
Функция ЭтоНастройкаСущественностиВНА(Знач ПроверяемоеЗначение) Экспорт
	
	Если ТипЗнч(ПроверяемоеЗначение) <> Тип("Структура") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого ЭлементБазовыхНастроек Из НовыйНастройкиСущественностиВНА() Цикл
		Если Не ПроверяемоеЗначение.Свойство(ЭлементБазовыхНастроек.Ключ) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область КешируемыеЗначения

// Заполняет возвращаемый параметр НастройкиСущественности настройками существенности внеоборотных активов,
// установленными в учетной политике.
// 
// Параметры:
//  НастройкиСущественности - см. НовыйНастройкиСущественностиВНА
//  Период - Дата
//  Организация - СправочникСсылка.Организации
//
Процедура ЗаполнитьКешПараметровНастройкиСущественностиВНА(НастройкиСущественности, Период, Организация) Экспорт
	
	НастройкиСущественности = НовыйНастройкиСущественностиВНА();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ссылка = УчетнаяПолитика.НастройкиСущественностиВНА(Период, Организация);
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = ТекстЗапросаНастройкаЛимита();
	
	ВыборкаСпособУказания = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаСпособУказания.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(НастройкиСущественности, ВыборкаСпособУказания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаНастройкаЛимита()
	
	ТекстЗапроса =
	 "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Настройки.СпособУказанияЛимита КАК СпособУказанияЛимита,
	|	Настройки.ЛимитСтоимости КАК ЛимитСтоимости
	|ИЗ
	|	Справочник.НастройкиСущественностиВнеоборотныхАктивов КАК Настройки
	|ГДЕ
	|	Настройки.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Лимит существенности по умолчанию.
// 
// Возвращаемое значение:
//  Число
//
Функция ЛимитСущественностиПоУмолчанию()
	Возврат УчетОС.ПредельнаяСтоимостьАмортизируемогоИмуществаНУ();
КонецФункции

Функция НовыйКонтекстНастроекСущественностиВНА()
	
	Контекст = Новый Структура;
	Контекст.Вставить("Организация", Справочники.Организации.ПустаяСсылка()); // организация, учетная политика которой используется
	Контекст.Вставить("ПериодДействия",   '0001-01-01');
	
	Возврат Контекст;
	
КонецФункции

Процедура ЗаполнитьНастройки(Настройки, РедактируемаяЗапись)
	
	Настройки.Контекст.Организация = РедактируемаяЗапись.Организация;
	Настройки.Контекст.ПериодДействия = РегистрыСведений.УчетнаяПолитика.ПериодДействия(
			РедактируемаяЗапись.Период,
			РедактируемаяЗапись.Организация);
	
	Если ЗначениеЗаполнено(РедактируемаяЗапись.НастройкиСущественностиВнеоборотныхАктивов) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Настройки.Ссылка КАК Ссылка,
			|	Настройки.СпособУказанияЛимита КАК СпособУказанияЛимита,
			|	Настройки.ЛимитСтоимости КАК ЛимитСтоимости
			|ИЗ
			|	Справочник.НастройкиСущественностиВнеоборотныхАктивов КАК Настройки
			|ГДЕ
			|	Настройки.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", РедактируемаяЗапись.НастройкиСущественностиВнеоборотныхАктивов);
		Результат = Запрос.Выполнить();
		
		ВыборкаСправочник = Результат.Выбрать();
		
		Если ВыборкаСправочник.Следующий() Тогда
			
			ЗаполнитьЗначенияСвойств(Настройки, ВыборкаСправочник);

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
