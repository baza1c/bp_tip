#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьКлассификатор();
	ТекстПоискаДолжности = Параметры.ЗначениеПоиска;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ТекстПоискаДолжности) Тогда
		НайтиИПозиционировать(ТекущаяСтрокаДолжностей, ТекстПоискаДолжности, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлассификаторОКПДТРДолжности

&НаКлиенте
Процедура КлассификаторДолжностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьИЗакрыть();
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторДолжностиПриАктивизацииПоля(Элемент)
	ТекущаяСтрокаДолжностей = Элементы.КлассификаторДолжности.ТекущаяСтрока;
	ИдентификаторСтрокиПоиска = ТекущаяСтрокаДолжностей;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИскатьДолжность(Команда)
	НайтиИПозиционировать(ТекущаяСтрокаДолжностей, ТекстПоискаДолжности);
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ИдентификаторСтрокиПоиска <> Неопределено Тогда
		ВыбратьИЗакрыть();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана строка классификатора.'"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Загружает классификатор из макета в дерево значений.
//
&НаСервере
Процедура ЗаполнитьКлассификатор()
	
	Классификатор = УчетЗарплаты.КлассификаторДолжностей(Истина);
	
	Для Каждого СтрокаКлассификатора Из Классификатор Цикл
		ЗаполнитьЗначенияСвойств(КлассификаторДолжности.ПолучитьЭлементы().Добавить(),
			СтрокаКлассификатора);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиИПозиционировать(ТекущаяСтрокаКлассификатора, ТекстПоиска, ИскатьСНачала = Ложь)
	
	Результат = Ложь;
	
	ПолеПоиска = "Name";
	
	ИдентификаторСтрокиПоиска = ТекущаяСтрокаКлассификатора;
	
	// если не найдено от текущей строки, то поиск происходит с начала дерева
	// аналогично при принудительном сбросе на начало дерева
	Если Не ОбщегоНазначенияБЗККлиентСервер.СледующийИдентификаторСтрокиДереваПоЗначениюПоля(
			ИдентификаторСтрокиПоиска, КлассификаторДолжности, ПолеПоиска, ТекстПоиска)
		Или ИскатьСНачала Тогда
		ИдентификаторСтрокиПоиска = ОбщегоНазначенияБЗККлиентСервер.ИдентификаторСтрокиДереваПоЗначениюПоля(
			КлассификаторДолжности, ПолеПоиска, ТекстПоиска, ИскатьСНачала);
	КонецЕсли;
	
	Если ИдентификаторСтрокиПоиска <> Неопределено Тогда
		ТекущаяСтрокаКлассификатора = ИдентификаторСтрокиПоиска;
		Результат = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "КлассификаторДолжности", 
		"ТекущаяСтрока", ТекущаяСтрокаКлассификатора);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	
	Результат = Новый Структура;
	Результат.Вставить("Наименование", СокрЛП(Элементы.КлассификаторДолжности.ТекущиеДанные.Name));
	Результат.Вставить("ОКЗКод", Элементы.КлассификаторДолжности.ТекущиеДанные.OKZ);
	Результат.Вставить("Порядок", Элементы.КлассификаторДолжности.ТекущиеДанные.Order);
	
	Закрыть(Результат);

КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаДолжностиИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) <= 3 Тогда
		Возврат;
	КонецЕсли;
	
	НайтиИПозиционировать(ТекущаяСтрокаДолжностей, Текст, Истина);
	
КонецПроцедуры

#КонецОбласти