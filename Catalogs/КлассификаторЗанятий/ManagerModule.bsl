
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

// Возвращает реквизиты справочника, которые образуют естественный ключ для элементов справочника.
//
// Возвращаемое значение:
//  Массив из Строка - имена реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

// Конец ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий


Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	УчетЗарплатыВызовСервера.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	УчетЗарплатыВызовСервера.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.СтрокаПоиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПозицияРазделителя = СтрНайти(Параметры.СтрокаПоиска,".");
	Если ПозицияРазделителя > 0 Тогда
		Если СтрДлина(Параметры.СтрокаПоиска) = ПозицияРазделителя Тогда
			Возврат;
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Код", Лев(Параметры.СтрокаПоиска, ПозицияРазделителя - 1));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КлассификаторЗанятий.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КлассификаторЗанятий КАК КлассификаторЗанятий
		|ГДЕ
		|	КлассификаторЗанятий.Код = &Код
		|	И НЕ КлассификаторЗанятий.ЭтоГруппа
		|	И НЕ КлассификаторЗанятий.ПометкаУдаления";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Не Выборка.Следующий() Тогда
			Возврат;
		Иначе
			СтандартнаяОбработка = Ложь;
			ЭлементСправочника = Выборка.Ссылка;
			Если ДанныеВыбора = Неопределено Тогда
				ДанныеВыбора = Новый СписокЗначений;
			КонецЕсли;
			ДанныеВыбора.Добавить(ЭлементСправочника);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Начальное заполнение и обновление информационной базы.
Процедура НачальноеЗаполнение() Экспорт
	Обновить(ПолучитьМакет("ПредопределенныеЗначения").ПолучитьТекст(), Ложь);
КонецПроцедуры

// Обновляет данные регистра.
Процедура Обновить(ТекстXML, ПолучатьДанныеИзСервиса = Истина) Экспорт
	ЗарплатаКадры.ОбновитьКлассификатор(ТекстXML, ПолучатьДанныеИзСервиса);
КонецПроцедуры

Процедура ЗагрузитьКлассификатор(ТаблицаСоСсылками) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСоСсылками", ТаблицаСоСсылками);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеКлассификатора.Код КАК Код,
		|	ДанныеКлассификатора.КонтрольноеЧисло КАК КонтрольноеЧисло,
		|	ДанныеКлассификатора.НаименованиеГруппЗанятий КАК НаименованиеГруппЗанятий
		|ПОМЕСТИТЬ ВТДанныеКлассификатора
		|ИЗ
		|	&ТаблицаСоСсылками КАК ДанныеКлассификатора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеСправочника.Ссылка КАК Ссылка,
		|	ДанныеКлассификатора.Код КАК Код,
		|	ДанныеКлассификатора.КонтрольноеЧисло КАК КонтрольноеЧисло,
		|	ДанныеКлассификатора.НаименованиеГруппЗанятий КАК НаименованиеГруппЗанятий
		|ИЗ
		|	ВТДанныеКлассификатора КАК ДанныеКлассификатора
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторЗанятий КАК ДанныеСправочника
		|		ПО (ДанныеКлассификатора.Код = ЕСТЬNULL(ДанныеСправочника.Код, НЕОПРЕДЕЛЕНО))";
	
	Родители = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			ОбъектСправочника = Выборка.Ссылка.ПолучитьОбъект();
		Иначе
			
			Если СтрДлина(Выборка.Код) < 4 Тогда
				ОбъектСправочника = СоздатьГруппу();
			Иначе
				ОбъектСправочника = СоздатьЭлемент();
			КонецЕсли;
			
			ОбъектСправочника.Родитель = Родители.Получить(Лев(Выборка.Код, СтрДлина(Выборка.Код) - 1));
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ОбъектСправочника, Выборка, , "Ссылка");
		ОбъектСправочника.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектСправочника);
		
		Если СтрДлина(ОбъектСправочника.Код) < 4 Тогда
			Родители.Вставить(ОбъектСправочника.Код, ОбъектСправочника.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли