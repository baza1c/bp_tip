#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Параметры.ЗначенияЗаполнения.Свойство("ОКЗКод")
			И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ОКЗКод) Тогда
			
			Объект.КодПоОКЗ = Справочники.КлассификаторЗанятий.НайтиПоКоду(Параметры.ЗначенияЗаполнения.ОКЗКод);
		КонецЕсли;
		
		УстановитьОтображениеЭлементов(ЭтотОбъект);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КодПрофессиональнойДеятельности",
		"Видимость",
		ЗначениеЗаполнено(Объект.КодПрофессиональнойДеятельности));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КодПрофессиональнойДеятельности1",
		"Видимость",
		Не ЗначениеЗаполнено(Объект.КодПрофессиональнойДеятельности));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Группа2025",
		"Видимость",
		ОбщегоНазначения.ТекущаяДатаПользователя() < ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026());
		
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Группа2026",
		"Видимость",
		ОбщегоНазначения.ТекущаяДатаПользователя() >= ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026());
		
	УстановитьРасшифровкуОКЗ();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьОтображениеЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьОтображениеЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы
		И ОписаниеОповещенияОЗакрытии <> Неопределено
		И Не Модифицированность
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, Объект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодПоОКЗПриИзменении(Элемент)
	
	УстановитьРасшифровкуОКЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	УстановитьОтображениеЭлементов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеЭлементов(УправляемаяФорма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ПредупрежедниеОДлинеОписания",
		"Видимость",
		СтрДлина(УправляемаяФорма.Объект.Описание) > 255);
	
	Если Не ПустаяСтрока(УправляемаяФорма.Объект.ИмяПредопределенныхДанных) Тогда
		
		УправляемаяФорма.ТолькоПросмотр = Истина;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"КодПоОКЗ",
			"Видимость",
			Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"КодПоОКЗ2026",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"Описание",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"КодПрофессиональнойДеятельности",
			"Маска",
			"NNNNNNNNNN");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРасшифровкуОКЗ()
	
	Элементы.ГруппаКодПоОКЗ.РасширеннаяПодсказка.Заголовок = Объект.КодПоОКЗ.НаименованиеГруппЗанятий;
	
КонецПроцедуры

#КонецОбласти
