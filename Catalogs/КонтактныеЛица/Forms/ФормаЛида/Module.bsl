
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	РазмещеныНаФорме = Новый Соответствие;
	РазмещеныНаФорме.Вставить(Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйКонтактныеЛица, Истина);
	РазмещеныНаФорме.Вставить(Справочники.ВидыКонтактнойИнформации.EmailКонтактныеЛица, Истина);
	
	ПараметрыРазмещенияКонтактнойИнформации = УправлениеКонтактнойИнформацией.ПараметрыКонтактнойИнформации();
	ПараметрыРазмещенияКонтактнойИнформации.ИмяЭлементаДляРазмещения = "ГруппаКомпоновкиКонтактнойИнформации";
	ПараметрыРазмещенияКонтактнойИнформации.РазмещеныНаФорме = РазмещеныНаФорме;
	
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, ПараметрыРазмещенияКонтактнойИнформации);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация

	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	РаботаСМетками.ПриСозданииНаСервере(ЭтотОбъект);
	
	ЕстьПравоИзменениеКонтрагента = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.Контрагенты);
	Элементы.ФормаГруппаПеревестиВКонтрагента.Видимость = ЕстьПравоИзменениеКонтрагента;
	
	ЕстьПравоИзменениеСчетНаОплату = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.СчетНаОплатуПокупателю);
	Элементы.ФормаКомандаСоздатьСчет.Видимость = ЕстьПравоИзменениеСчетНаОплату;
	
	Если ТолькоПросмотр Тогда
		Элементы.ФормаКомандаОтметитьКакНекачественный.Видимость = Ложь;
		Элементы.ФормаКомандаСнятьОтметкуНекачественный.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораСостояния();
	
	Если Параметры.Ключ.Пустая() Тогда
		УстановитьСостояниеПоУмолчанию();
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(Объект.Наименование);
	ЗаполнитьЗначенияСвойств(Объект, ФИО);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы "Контактная информация"
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, Отказ);
	
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЛидЗавершен = ЗначениеЗаполнено(ТекущийОбъект.ОбъектВладелец) Или ТекущийОбъект.НекачественныйЛид;
	Если Не ЛидЗавершен И Состояние <> ТекущееСостояние Тогда
		УстановитьСостояниеРаботыСЛидом(ТекущийОбъект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ЗначениеЗаполнено(Объект.ОбъектВладелец) Тогда
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Контрагент", Объект.ОбъектВладелец);
		
		Оповестить("Запись_КонтактныеЛица", СтруктураПараметров);
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Ответственный", Объект.Ответственный);
	Оповестить("Запись_Лид", СтруктураПараметров);
	
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьФорму" И Параметр = Объект.Ссылка Тогда
		Прочитать();
		УстановитьЗаголовокФормы();
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_СостоянияРаботыСЛидами" Тогда
		ЗаполнитьСписокВыбораСостояния();
	ИначеЕсли ИмяСобытия = "ИзмененоСостояниеЛидов" Тогда
		// Состояние изменено в форме списка
		Если Не Объект.Ссылка.Пустая() И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
			ОбработатьОповещениеОбИзмененииСостояния();
		КонецЕсли;
	КонецЕсли;
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
	Если РаботаСМеткамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбработкаОповещенияФормыСМеткамиНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Состояние = ПолучитьСостояниеРаботыСЛидом(Объект.Ссылка);
	ТекущееСостояние = Состояние;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ТипЗнч(НовыйОбъект) = Тип("СправочникСсылка.Контрагенты") Тогда
		Если Не ПереведенВКонтрагента(ЭтотОбъект) Тогда
			ПеревестиВКонтрагента(НовыйОбъект);
		КонецЕсли;
	ИначеЕсли ТипЗнч(НовыйОбъект) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		Если Не ПереведенВКонтрагента(ЭтотОбъект) Тогда
			КонтрагентСчета = КонтрагентСчетаПокупателю(НовыйОбъект);
			ПеревестиВКонтрагента(КонтрагентСчета);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Контрагенты") Тогда
		Если Не ПереведенВКонтрагента(ЭтотОбъект) Тогда
			ПеревестиВКонтрагента(ВыбранноеЗначение);
		КонецЕсли;
		ПараметрыФормы = Новый Структура("ТекущаяСтрока", ВыбранноеЗначение);
		Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
			ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаКонтрагенты", ПараметрыФормы, ВладелецФормы, , ВладелецФормы.Окно);
		Иначе
			ОткрытьФорму("Справочник.Контрагенты.ФормаСписка", ПараметрыФормы);
		КонецЕсли;
		Закрыть();
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		Если Не ПереведенВКонтрагента(ЭтотОбъект) Тогда
			КонтрагентСчета = КонтрагентСчетаПокупателю(ВыбранноеЗначение);
			ПеревестиВКонтрагента(КонтрагентСчета);
		КонецЕсли;
		ПараметрыФормы = Новый Структура("ТекущаяСтрока", ВыбранноеЗначение);
		Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
			ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаСписка", ПараметрыФормы, ВладелецФормы, , ВладелецФормы.Окно);
		Иначе
			ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаСписка", ПараметрыФормы);
		КонецЕсли;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ФИОАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораКонтрагенты");
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

#Область Метки

&НаКлиенте
Процедура МеткиОбъектаПриИзменении(Элемент)
	
	МеткиОбъектаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МеткиОбъектаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МеткиОбъектаСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиСоздание(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МеткиОбъектаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МеткиОбъектаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МеткиОбъектаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МеткиОбъектаОткрытиеМножественногоЗначения(Элемент, Идентификатор, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОткрытиеМножественногоЗначения(ЭтотОбъект, Элемент, Идентификатор, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПеревестиВКонтрагента(Команда)
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Ответственный", Объект.Ответственный);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
	
	Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
		ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаНовогоЭлемента", ПараметрыФормы, ЭтотОбъект);
	Иначе
		ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСоздатьСчет(Команда)
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ОткрыватьФормуНовогоКонтрагента", Истина);
	ПараметрыФормы.Вставить("Ответственный", Объект.Ответственный);
	
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтметитьКакНекачественный(Команда)
	
	УстановитьОтметкуНекачественныйЛид(Истина);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСнятьОтметкуНекачественный(Команда)
	
	УстановитьОтметкуНекачественныйЛид(Ложь);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ФормаГруппаРезультатОбработки.Видимость = Не ЗавершенаРаботаСЛидом(Форма);
	Элементы.ФормаКомандаСнятьОтметкуНекачественный.Видимость = ЭтоНекачественныйЛид(Форма);
	
	Элементы.Контрагент.Видимость = ПереведенВКонтрагента(Форма);
	Элементы.Состояние.Видимость = Не ЗавершенаРаботаСЛидом(Форма);

КонецПроцедуры

#Область ПроцедурыПодсистемыКонтактнаяИнформация

// СтандартныеПодсистемы.КонтактнаяИнформация

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	УправлениеКонтактнойИнформациейКлиент.НачатьИзменение(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьОчистку(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда.Имя);
КонецПроцедуры

&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(Результат)
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьКонтактнуюИнформацию(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтактнаяИнформация

#КонецОбласти

#Область ПроцедурыПодсистемыСвойств

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, РеквизитФормыВЗначение("Объект"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаКлиентеНаСервереБезКонтекста
Функция ЗавершенаРаботаСЛидом(Форма)
	
	Возврат ЭтоНекачественныйЛид(Форма) Или ПереведенВКонтрагента(Форма);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНекачественныйЛид(Форма)
	
	Объект = Форма.Объект;
	
	Возврат Объект.НекачественныйЛид;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПереведенВКонтрагента(Форма)
	
	Объект = Форма.Объект;
	
	Возврат ЗначениеЗаполнено(Объект.ОбъектВладелец);
	
КонецФункции

&НаКлиенте
Процедура УстановитьЗаголовокФормы()

	АвтоЗаголовок = Ложь;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ПереведенВКонтрагента(ЭтотОбъект) Тогда
			ТекстСтатусЛида = НСтр("ru='Лид: переведен в контрагента'");
		ИначеЕсли ЭтоНекачественныйЛид(ЭтотОбъект) Тогда
			ТекстСтатусЛида = НСтр("ru='Лид: некачественный'");
		Иначе
			ТекстСтатусЛида = НСтр("ru='Лид: в работе'");
		КонецЕсли;
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru=' %1 (%2)'"), Объект.Наименование, ТекстСтатусЛида);
	Иначе
		Заголовок = НСтр("ru = 'Лид (создание)'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КонтрагентСчетаПокупателю(СчетПокупателю)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетПокупателю, "Контрагент");

КонецФункции

&НаКлиенте
Процедура ПеревестиВКонтрагента(Контрагент)
	
	Объект.ОбъектВладелец = Контрагент;
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметкуНекачественныйЛид(Отметка)
	
	Объект.НекачественныйЛид = Отметка;
	Записать();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеРаботыСЛидом(Ссылка)
	
	РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.УстановитьСостояниеРаботыСЛидом(Ссылка, Состояние);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеРаботыСЛидом(Знач Ссылка)
	
	Возврат РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.ПолучитьСостояниеРаботыСЛидом(Ссылка).Состояние;
	
КонецФункции

&НаСервере
Процедура ОбработатьОповещениеОбИзмененииСостояния()
	
	Состояние = ПолучитьСостояниеРаботыСЛидом(Объект.Ссылка);
	ТекущееСостояние = Состояние;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеПоУмолчанию()
	
	Состояние = Справочники.СостоянияРаботыСЛидами.СостояниеПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораСостояния()
	
	СписокВыбора = Элементы.Состояние.СписокВыбора;
	СписокВыбора.Очистить();
	
	АктуальныеСостояния = Справочники.СостоянияРаботыСЛидами.АктуальныеСостояния();
	
	Для Каждого СтрокаСостояния Из АктуальныеСостояния Цикл
		СписокВыбора.Добавить(СтрокаСостояния.Ссылка, СтрокаСостояния.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Метки
	РаботаСМетками.УстановитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ОбработкаОповещенияФормыСМеткамиНаСервере()
	
	РаботаСМетками.ОбработкаОповещенияНаСервере(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура МеткиОбъектаПриИзмененииНаСервере()
	
	РаботаСМетками.МеткиПриИзмененииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыМК = "ФормаКомандаЗаписатьИЗакрыть,ФормаЗаписать";
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект, КомандыМК);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ФормаГруппаРезультатОбработки.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВМККомандыФормы(Элементы, Элементы.ФормаКомандаСнятьОтметкуНекачественный);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Код);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаШапкаЛевая);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Состояние);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Контрагент);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.Отступ);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

