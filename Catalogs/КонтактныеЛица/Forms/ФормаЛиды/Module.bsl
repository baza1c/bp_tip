
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.КонтактныеЛица);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	Элементы.ГруппаЗавершениеРаботыСЛидомКанбан.Видимость = МожноРедактировать;
	Элементы.ГруппаСписокКанбан.Видимость = МожноРедактировать;
	
	ЕстьПравоИзменениеВзаимодействие = ПравоДоступа("Редактирование", Метаданные.Документы.ПрочееВзаимодействие);
	Элементы.ГруппаКомандВзаимодействия.Видимость = ЕстьПравоИзменениеВзаимодействие;
	Элементы.ГруппаКомандВзаимодействияКанбан.Видимость = ЕстьПравоИзменениеВзаимодействие;
	Элементы.ГруппаСоздатьВзаимодействие.Видимость = ЕстьПравоИзменениеВзаимодействие;
	
	ЕстьПравоИзменениеКоммерческоеПредложение = ПравоДоступа("Изменение", Метаданные.Документы.КоммерческоеПредложение);
	Элементы.СоздатьДокументКоммерческоеПредложение.Видимость = ЕстьПравоИзменениеКоммерческоеПредложение;
	
	УстановитьВосстановленныеОтборы();
	
	ЭтоПростойИнтерфейс = ОбщегоНазначенияБП.ЭтоПростойИнтерфейс();
	Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ЭтоПростойИнтерфейс", ЭтоПростойИнтерфейс);
	
	ОбщегоНазначенияБП.УстановитьВидимостьКолонокДополнительнойИнформации(ЭтотОбъект);
	
	РаботаСМетками.ПриСозданииНаСервере(ЭтотОбъект, Истина);
	
	УстановитьУсловноеОформление();
	
	НастроитьОтборСтатусЛидов();
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	УправлениеПанельюПодсказки.ПриСозданииНаСервере(ЭтотОбъект);
	
	Поля = Новый Массив;
	Поля.Добавить("Телефон");
	Поля.Добавить("Email");
	Поля.Добавить("МеткиСтрокой");
	
	Список.УстановитьОграниченияИспользованияВГруппировке(Поля);
	Список.УстановитьОграниченияИспользованияВОтборе(Поля);
	Список.УстановитьОграниченияИспользованияВПорядке(Поля);
	
	УправлениеФормой(ЭтотОбъект);
	
	НастроитьКанбанОтложенно = Истина;
	ЗаполнитьКанбанОтложенно = Истина;
	
	РамкаЭлементаКанбан = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная, 1);
	РамкаВыделенияКанбан = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная, 3);
	
	МобильныйКлиентАдаптацияФормы(МожноРедактировать);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СостоянияРаботыСЛидами" Тогда
		НастроитьКанбанОтложенно = Истина;
		ПроверитьЗаполнитьКанбанНаКлиенте();
		
	ИначеЕсли ИмяСобытия = "Запись_Лид" Тогда
		Если ОтборОтветственныйИспользование Тогда
			ОтветственныйПоЛиду = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Ответственный");
			ОбработатьБезусловно = ОтветственныйПоЛиду = ОтборОтветственный;
		Иначе
			ОбработатьБезусловно = Истина;
		КонецЕсли;
		Если ОбработатьБезусловно Тогда
			ПроверитьЗаполнитьКанбанНаКлиенте();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзмененоСостояниеЛидов" Тогда
		Для Каждого ЛидИзменен Из Параметр Цикл
			Если Канбан.Элементы.Найти(ЛидИзменен) <> Неопределено Тогда
				ПроверитьЗаполнитьКанбанНаКлиенте();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
	УправлениеПанельюПодсказкиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	Если РаботаСМеткамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбработкаОповещенияФормыСМеткамиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ТипЗнч(НовыйОбъект) = Тип("СправочникСсылка.Контрагенты") Тогда
		ЛидИзменен = ПеревестиВКонтрагентаНаСервере(НовыйОбъект);
		ОповеститьОбИзмененииЛида(ЛидИзменен);
	ИначеЕсли ТипЗнч(НовыйОбъект) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		ЛидИзменен = ПеревестиВКонтрагентаСчета(НовыйОбъект);
		ОповеститьОбИзмененииЛида(ЛидИзменен);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Контрагенты") Тогда
		ЛидИзменен = ПеревестиВКонтрагентаНаСервере(ВыбранноеЗначение);
		ОповеститьОбИзмененииЛида(ЛидИзменен);
		ПараметрыФормы = Новый Структура("ТекущаяСтрока", ВыбранноеЗначение);
		Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
			ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаКонтрагенты", ПараметрыФормы, ЭтотОбъект, , ЭтотОбъект.Окно);
		Иначе
			ОткрытьФорму("Справочник.Контрагенты.ФормаСписка", ПараметрыФормы);
		КонецЕсли;

	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		ЛидИзменен = ПеревестиВКонтрагентаСчета(ВыбранноеЗначение);
		ОповеститьОбИзмененииЛида(ЛидИзменен);
		ПараметрыФормы = Новый Структура("ТекущаяСтрока", ВыбранноеЗначение);
		Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
			ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаСписка", ПараметрыФормы, ЭтотОбъект, , ЭтотОбъект.Окно);
		Иначе
			ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаСписка", ПараметрыФормы);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Не ФункциональностьПрограммы.РазделЛидыДоступен() Тогда
		ОтборОтветственныйИспользование = Ложь;
	КонецЕсли;
	
	УстановитьВосстановленныеОтборы();
	
	РаботаСМетками.ПриЗагрузкеДанныхИзНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	
	ОтборОтветственныйИспользование = ЗначениеЗаполнено(ОтборОтветственный);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Ответственный");
	
	ПроверитьЗаполнитьКанбанНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Ответственный");
	
	ПроверитьЗаполнитьКанбанНаКлиенте();
	
КонецПроцедуры

#Область Панель_Подсказки

&НаКлиенте
Процедура НавигацияВПрограммеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.НавигацияВПрограммеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяФункциональностьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.ДополнительнаяФункциональностьОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылка,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область Метки

&НаКлиенте
Процедура ИспользоватьОтборМеткиПриИзменении(Элемент)
	
	РаботаСМеткамиКлиент.ИспользоватьОтборМеткиПриИзменении(ЭтотОбъект, Элемент);
	
	ПроверитьЗаполнитьКанбанНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиПриИзменении(Элемент)
	
	РаботаСМеткамиКлиент.ОтборМеткиПриИзменении(ЭтотОбъект, Элемент);
	
	ОтборМеткиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиСоздание(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМеткиОткрытиеМножественногоЗначения(Элемент, Идентификатор, СтандартнаяОбработка)
	
	РаботаСМеткамиКлиент.МеткиОткрытиеМножественногоЗначения(ЭтотОбъект, Элемент, Идентификатор, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Ключ", ВыбраннаяСтрока);
	ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаЛида", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Не ЭтоПростойИнтерфейс Тогда
		ПодключитьОбработчикОжидания("ОбновитьСвойстваТекущегоЛида", 0.2, Истина);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Если Настройки.ДополнительныеСвойства.Свойство("ЭтоПростойИнтерфейс") Тогда
		ЭтоПростойИнтерфейс = Настройки.ДополнительныеСвойства.ЭтоПростойИнтерфейс;
	Иначе
		ЭтоПростойИнтерфейс = Ложь;
	КонецЕсли;
	
	Если ЭтоПростойИнтерфейс И Строки.Количество() > 0 Тогда
		ТаблицаКонтактов = Справочники.КонтактныеЛица.КонтактнаяИнформацияЛиц(Строки.ПолучитьКлючи());
		
		Для Каждого СтрокаКонтактов Из ТаблицаКонтактов Цикл
			Строка = Строки.Получить(СтрокаКонтактов.КонтактноеЛицо);
			ЗаполнитьЗначенияСвойств(Строка.Данные, СтрокаКонтактов);
		КонецЦикла;
	КонецЕсли;
	
	РаботаСМетками.ПриПолученииДанныхНаСервере(Настройки, Строки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	СоздатьЛида();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура("Ключ", Элементы.Список.ТекущаяСтрока);
	ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаЛида", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКанбан

&НаКлиенте
Процедура КанбанПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанВыбор(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьЛидаКанбан();
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементы.Количество() = 0 Тогда
		ОтменаРедактирования = Истина;
		Возврат;
	КонецЕсли;
	
	ЛидыДляИзменения = Новый Массив;
	Для Каждого ЭлементКанбан Из ВыделенныеЭлементы Цикл
		Если ЗначениеЗаполнено(ЭлементКанбан.Значение)
			И ТипЗнч(ЭлементКанбан.Значение) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
			ЛидыДляИзменения.Добавить(ЭлементКанбан.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ЛидыДляИзменения.Количество() = 0 Тогда
		ОтменаРедактирования = Истина;
		Возврат;
	КонецЕсли;
	
	ПервыйЭлемент = ВыделенныеЭлементы[0];
	ПоследнийЭлемент = ВыделенныеЭлементы[ВыделенныеЭлементы.ВГраница()];
	
	НовоеСостояние = ПоследнийЭлемент.ЗначенияИзмерений[ИзмерениеКанбан()];
	
	СмещаемыйЛид = Неопределено;
	
	Для Каждого ЭлементКанбан Из Канбан.Элементы Цикл
		СостояниеЭлемента = ЭлементКанбан.ЗначенияИзмерений[ИзмерениеКанбан()];
		Если ЛидыДляИзменения.Найти(ЭлементКанбан.Значение) <> Неопределено
			Или СостояниеЭлемента <> НовоеСостояние Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементКанбан.Начало = ПервыйЭлемент.Начало
			Или ЭлементКанбан.Конец = ПервыйЭлемент.Конец
			Или (ЭлементКанбан.Начало < ПоследнийЭлемент.Начало
				И ЭлементКанбан.Конец > ПоследнийЭлемент.Начало) Тогда
			СмещаемыйЛид = ЭлементКанбан.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ИзмененоСостояниеЛидов = ИзменитьСостояниеПоЛидамКанбан(ЛидыДляИзменения, НовоеСостояние, СмещаемыйЛид);
	Если ИзмененоСостояниеЛидов Тогда
		Оповестить("ИзмененоСостояниеЛидов", ЛидыДляИзменения);
	Иначе
		ОтменаРедактирования = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьЛидаКанбан();
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеЭлемента = ВыделенныеЭлементы[0].Значение;
	
	Если ЗначениеЗаполнено(ЗначениеЭлемента) И ТипЗнч(ЗначениеЭлемента) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Лид", ЗначениеЭлемента);
		Оповещение = Новый ОписаниеОповещения("ВопросПометитьНаУдалениеКанбанЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ТекстВопроса = НСтр("ru = 'Пометить ""%1"" на удаление?'");
		ТекстВопроса = СтрШаблон(ТекстВопроса, Строка(ЗначениеЭлемента));
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДоступнаПрокруткаНазад = Элементы.ПерейтиНаПредыдущуюСтраницуКанбан.Доступность;
	ДоступнаПрокруткаВперед = Элементы.ПерейтиНаСледующуюСтраницуКанбан.Доступность;
	
	Если ДоступнаПрокруткаНазад И ТекущиеПериодыОтображения[0].Начало < Канбан.ТекущиеПериодыОтображения[0].Начало Тогда
		ЗаполнитьКанбан(ПрокруткаКанбанНазад());
	ИначеЕсли ДоступнаПрокруткаВперед И ТекущиеПериодыОтображения[0].Начало > Канбан.ТекущиеПериодыОтображения[0].Конец Тогда
		ЗаполнитьКанбан(ПрокруткаКанбанВперед());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанОбработкаФормированияКоманд(Элемент, ПараметрыКоманд, Команды, КомандаПоУмолчанию)
	
	Для Каждого ЗначениеКанбан Из ВыделенныеЭлементыКанбан Цикл
		ЭлементКанбан = Канбан.Элементы.Найти(ЗначениеКанбан.Значение);
		Если ЭлементКанбан <> Неопределено Тогда
			ЭлементКанбан.Рамка = РамкаЭлементаКанбан;
			ЭлементКанбан.ЦветРамки = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаКанбан");
		КонецЕсли;
	КонецЦикла;
	
	ВыделенныеЭлементыКанбан.Очистить();
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	
	Для Каждого ЭлементКанбан Из ВыделенныеЭлементы Цикл
		ЭлементКанбан.Рамка = РамкаВыделенияКанбан;
		ЭлементКанбан.ЦветРамки = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветВыделенияЭлементаКанбан");
		ВыделенныеЭлементыКанбан.Добавить(ЭлементКанбан.Значение);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументКоммерческоеПредложение(Команда)
	
	Если ВидФормыКанбан(СписокКанбан) Тогда
		СтруктураПараметров = ПараметрыФормыДокументаКанбан();
	Иначе
		СтруктураПараметров = ПараметрыФормыДокумента();
	КонецЕсли;
	ОткрытьФорму("Документ.КоммерческоеПредложение.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗвонок(Команда)

	СоздатьНовоеВзаимодействие("ТелефонныйЗвонок");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПисьмо(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЭлектронноеПисьмоПродолжение", ЭтотОбъект, );
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВстречу(Команда)
	
	СоздатьНовоеВзаимодействие("Встреча");
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПрочееВзаимодействие(Команда)
	
	СоздатьНовоеВзаимодействие("ПрочееВзаимодействие");
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСписок(Команда)
	
	СписокКанбан = 0;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьВРаботе(Команда)
	
	ОтборСтатусЛидов = СтатусЛидовВРаботе();
	НастроитьОтборСтатусЛидов();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьВсе(Команда)
	
	ОтборСтатусЛидов = СтатусЛидовВсе();
	НастроитьОтборСтатусЛидов();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#Область ОбработчикиКомандФормыКанбан

&НаКлиенте
Процедура СоздатьКанбан(Команда)
	
	СоздатьЛида();

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьКанбан(Команда)
	
	СписокКанбан = 1;
	
	Если ЗаполнитьКанбанОтложенно Тогда
		ЗаполнитьКанбанВФоне();
	Иначе
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьКакНекачественныйКанбан(Команда)
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЛидыДляИзменения = Новый Массив;
	Для Каждого ЭлементКанбан Из ВыделенныеЭлементы Цикл
		ЛидыДляИзменения.Добавить(ЭлементКанбан.Значение);
	КонецЦикла;
	
	УстановитьОтметкуНекачественныйЛид(ЛидыДляИзменения);
	
	Если ЛидыДляИзменения.Количество() = 1 Тогда
		ОповеститьОбИзмененииЛида(ЛидыДляИзменения[0]);
	Иначе
		ОповеститьОбИзменении(Тип("СправочникСсылка.КонтактныеЛица"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиВКонтрагентаКанбан(Команда)
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыделенныеЭлементы.Количество() > 1 Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Выберите одного лида, чтобы перевести в контрагента.'"));
		Возврат;
	КонецЕсли;
	
	ЛидДляИзменения = ВыделенныеЭлементы[0].Значение;
	
	АдресДляЗавершенияРаботыЛидов = ПоместитьВоВременноеХранилище(ЛидДляИзменения, УникальныйИдентификатор);
	
	Если ОтборОтветственныйИспользование Тогда
		Ответственный = ОтборОтветственный;
	Иначе
		Ответственный = ОтветственныйПоЛиду(ЛидДляИзменения);
	КонецЕсли;
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Ответственный", Ответственный);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
	
	Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
		ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаНовогоЭлемента", ПараметрыФормы, ЭтотОбъект);
	Иначе
		ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСчетКанбан(Команда)
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыделенныеЭлементы.Количество() > 1 Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Выберите одного лида, чтобы перевести в контрагента и создать счет.'"));
		Возврат;
	КонецЕсли;
	
	ЛидДляИзменения = ВыделенныеЭлементы[0].Значение;
	
	АдресДляЗавершенияРаботыЛидов = ПоместитьВоВременноеХранилище(ЛидДляИзменения, УникальныйИдентификатор);
	
	Если ОтборОтветственныйИспользование Тогда
		Ответственный = ОтборОтветственный;
	Иначе
		Ответственный = ОтветственныйПоЛиду(ЛидДляИзменения);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ОткрыватьФормуНовогоКонтрагента", Истина);
	ПараметрыФормы.Вставить("Ответственный", Ответственный);
	
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаПервуюСтраницуКанбан(Команда)
	
	ЗаполнитьКанбан();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаПредыдущуюСтраницуКанбан(Команда)
	
	ЗаполнитьКанбан(ПрокруткаКанбанНазад());
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСледующуюСтраницуКанбан(Команда)
	
	ЗаполнитьКанбан(ПрокруткаКанбанВперед());
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаПоследнююСтраницуКанбан(Команда)
	
	ЗаполнитьКанбан(ПрокруткаКанбанВКонец());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКанбан(Команда)
	
	НастроитьКанбанОтложенно = Истина;
	ПроверитьЗаполнитьКанбанНаКлиенте();	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьЭлектронноеПисьмоПродолжение(ПроверкаВыполнена, ДополнительныеПараметры) Экспорт
	
	Если ПроверкаВыполнена = Истина Тогда
		СоздатьНовоеВзаимодействие("ЭлектронноеПисьмоИсходящее");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеВзаимодействие(ТипОбъекта)

	Если ВидФормыКанбан(СписокКанбан) Тогда
		ПараметрыСоздания = ПараметрыФормыВзаимодействияКанбан();
	Иначе
		ПараметрыСоздания = ПараметрыФормыВзаимодействия();
	КонецЕсли;
	
	Если ПараметрыСоздания = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие(ТипОбъекта, ПараметрыСоздания, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.Телефон.Видимость = Форма.ЭтоПростойИнтерфейс;
	Элементы.Email.Видимость = Форма.ЭтоПростойИнтерфейс;

	Элементы.ГруппаСвойстваЛида.Видимость = Не Форма.ЭтоПростойИнтерфейс;
	
	Элементы.ГруппаСписок.Видимость = ВидФормыСписок(Форма.СписокКанбан);
	Элементы.ГруппаКанбан.Видимость = ВидФормыКанбан(Форма.СписокКанбан);
	
	Элементы.ГруппаВРаботеВсе.Видимость = ВидФормыСписок(Форма.СписокКанбан);
	
	Если Форма.МожноРедактировать Тогда
		Элементы.ФормаСоздать.Видимость = ВидФормыСписок(Форма.СписокКанбан);
	КонецЕсли;
	Элементы.ГруппаСтандартныеКоманды.Видимость = ВидФормыСписок(Форма.СписокКанбан);
	
	Элементы.ФормаСоздатьКанбан.Видимость = ВидФормыКанбан(Форма.СписокКанбан);
	Элементы.ФормаОбновитьКанбан.Видимость = ВидФормыКанбан(Форма.СписокКанбан);
	
	Элементы.ПереключитьСписок.Пометка = ВидФормыСписок(Форма.СписокКанбан);
	Элементы.ПереключитьКанбан.Пометка = ВидФормыКанбан(Форма.СписокКанбан);
	
	Элементы.ПереключитьВРаботе.Пометка = Форма.ОтборСтатусЛидов = СтатусЛидовВРаботе();
	Элементы.ПереключитьВсе.Пометка = Форма.ОтборСтатусЛидов = СтатусЛидовВсе();
	
	Элементы.СтрокаПоиска.Видимость = ВидФормыСписок(Форма.СписокКанбан);
	Элементы.УправлениеПоиском.Видимость = ВидФормыСписок(Форма.СписокКанбан);
	
	Элементы.ГруппаЗавершениеРаботыСЛидомКанбан.Видимость = ВидФормыКанбан(Форма.СписокКанбан);

КонецПроцедуры

&НаСервере
Процедура НастроитьОтборСтатусЛидов()
	
	ИспользованиеОтбора = ОтборСтатусЛидов = СтатусЛидовВРаботе();
	ОбластьОтбора = Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	ИмяОтбора = НСтр("ru='Статус лидов'");
	
	Если ИспользованиеОтбора Тогда
		ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
			ОбластьОтбора.Элементы,
			ИмяОтбора, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаИ, "Контрагент", , ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаИ, "НекачественныйЛид", Ложь, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			ОбластьОтбора,
			,
			ИмяОтбора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	Список.УсловноеОформление.Элементы.Очистить();

	// Вся строка серым, если некачественный лид
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"НекачественныйЛид", ВидСравненияКомпоновкиДанных.Равно, Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ДосьеВторостепеннаяНадписьЦвет);
	
	// Вся строка зеленым, если лид переведен в контрагента
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор,
		"Контрагент", ВидСравненияКомпоновкиДанных.Заполнено);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ЗеленыйЛес);
	
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, "Состояние");
	ГруппаОтбораИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		Элемент.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИли,
		"НекачественныйЛид", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИли,
		"Контрагент", ВидСравненияКомпоновкиДанных.Заполнено);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Работа завершена'"));
	
	// Метки
	РаботаСМетками.УстановитьУсловноеОформление(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСвойстваТекущегоЛида()
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	
	Если ТекущиеДанные = Неопределено Тогда
		
		ЛидТелефон = "";
		ЛидEmail = "";
		
	Иначе
		
		СвойстваЛида = СвойстваЛида(ТекущиеДанные.Ссылка);
		
		ЛидТелефон = СвойстваЛида.Телефон;
		ЛидEmail = СвойстваЛида.Email;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СвойстваЛида(Знач Лид)
	
	Результат = Новый Структура;
	
	Результат.Вставить("Телефон",
		УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Лид,
			Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйКонтактныеЛица));
	
	Результат.Вставить("Email",
		УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Лид,
			Справочники.ВидыКонтактнойИнформации.EmailКонтактныеЛица));
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ИзменитьВидимостьПодсказокНаСервере()
	
	УправлениеПанельюПодсказки.ИзменитьВидимостьПодсказок(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьЭлементПодсказкиНаФорме(ИмяЭлементаПодсказки) Экспорт
	
	СнятьВыделениеЭлементаПодсказкиНаФорме();
	
	Если ИмяЭлементаПодсказки = "ЗагрузитьИзФайла" Тогда
		
		ВыделенныйЭлементПодсказки = "ЗагрузитьИзФайла";
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = УправлениеПанельюПодсказкиКлиентСервер.ЦветФонаВыделенияЭлементаПодсказки();
		ПодключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме", 1.3, Истина);
		
	ИначеЕсли ИмяЭлементаПодсказки = "СоздатьДокументКоммерческоеПредложение" Тогда

		ВыделенныйЭлементПодсказки = "СоздатьДокументКоммерческоеПредложение";
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = УправлениеПанельюПодсказкиКлиентСервер.ЦветФонаВыделенияЭлементаПодсказки();
		ПодключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме", 1.3, Истина);
		
	ИначеЕсли ИмяЭлементаПодсказки = "ПереключитьКанбан" Тогда
		
		ВыделенныйЭлементПодсказки = "ПереключитьКанбан";
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = УправлениеПанельюПодсказкиКлиентСервер.ЦветФонаВыделенияЭлементаПодсказки();
		ПодключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме", 1.3, Истина);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделениеЭлементаПодсказкиНаФорме() Экспорт
	
	ОтключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме");
	
	Если ВыделенныйЭлементПодсказки = "ЗагрузитьИзФайла"
		Или ВыделенныйЭлементПодсказки = "СоздатьДокументКоммерческоеПредложение"
		Или ВыделенныйЭлементПодсказки = "ПереключитьКанбан" Тогда
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветЭлементаКанбан");
	КонецЕсли;
	
	ВыделенныйЭлементПодсказки = "";
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыФормыДокумента()
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	
	ПараметрыФормы = Новый Структура;
	
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	
	Если ТекущиеДанные <> Неопределено Тогда
		ЗначенияЗаполнения.Вставить("КонтактноеЛицо", ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Ответственный");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтветственныйПоЛиду(Лид)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Лид, "Ответственный");
	
КонецФункции

&НаКлиенте
Процедура СоздатьЛида()
	
	ЗначенияЗаполнения = Новый Структура("Лид", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаЛида", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтатусЛидовВРаботе()
	
	Возврат 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтатусЛидовВсе()
	
	Возврат 1;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВидФормыСписок(СписокКанбан)
	
	Возврат СписокКанбан = 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОтветственныйОтбора(Форма)
	
	Если Форма.ОтборОтветственныйИспользование Тогда
		Ответственный = Форма.ОтборОтветственный;
	Иначе
		Ответственный = Неопределено;
	КонецЕсли;
	
	Возврат Ответственный;
	
КонецФункции

&НаСервере
Функция МеткиОтбора()
	
	Если ПараметрыФормыСМетками.ИспользоватьМетки И ИспользоватьОтборМетки Тогда
		ОтобранныеМетки = ОбщегоНазначения.ВыгрузитьКолонку(ОтборМетки, "Свойство");
	Иначе
		ОтобранныеМетки = Неопределено;
	КонецЕсли;
	
	Возврат ОтобранныеМетки;

КонецФункции

&НаКлиенте
Процедура ОповеститьОбИзмененииЛида(Лид)
	
	Если Лид <> Неопределено Тогда
		ОповеститьОбИзменении(Лид);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыФормыВзаимодействия()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КонтактноеЛицо = ТекущиеДанные.Ссылка;
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Контакт", КонтактноеЛицо);	
	ДанныеУчастника.Вставить("КакСвязаться", "");
	ДанныеУчастника.Вставить("Представление", Строка(КонтактноеЛицо));	
		
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ДанныеУчастника", ДанныеУчастника);

	Возврат ПараметрыСоздания;
	
КонецФункции

#Область СлужебныеПроцедурыИФункцииКанбан

&НаКлиентеНаСервереБезКонтекста
Функция ВидФормыКанбан(СписокКанбан)
	
	Возврат СписокКанбан = 1;
	
КонецФункции

&НаКлиенте
Функция ПараметрыФормыДокументаКанбан()
	
	ПараметрыФормы = Новый Структура;
	
	ЗначенияЗаполнения = Новый Структура;
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() > 0 Тогда
		ЗначенияЗаполнения.Вставить("КонтактноеЛицо", ВыделенныеЭлементы[0].Значение);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Функция ПараметрыФормыВзаимодействияКанбан()
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КонтактноеЛицо = ВыделенныеЭлементы[0].Значение;
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Контакт", КонтактноеЛицо);	
	ДанныеУчастника.Вставить("КакСвязаться", "");
	ДанныеУчастника.Вставить("Представление", Строка(КонтактноеЛицо));	
		
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ДанныеУчастника", ДанныеУчастника);
	
	Возврат ПараметрыСоздания;
	
КонецФункции

&НаСервере
Процедура НастроитьКанбан()
	
	ЗаполнитьСостоянияРаботыСЛидами();
	
	ИзмеренияКанбан = Канбан.Измерения;
	ИзмеренияКанбан.Очистить();
	
	НовоеИзмерениеКанбан          = ИзмеренияКанбан.Добавить(ИзмерениеКанбан());
	НовоеИзмерениеЗаголовкаКанбан = ИзмеренияКанбан.Добавить(ИзмерениеЗаголовкаКанбан());
	
	ШрифтНевидимогоЭлемента = Новый Шрифт( , , , , , , 30);
	ШрифтЭлемента = Новый Шрифт(, , Истина);
	
	Для Каждого Состояние Из СостоянияРаботыСЛидами Цикл
		ЦветЭлемента = Справочники.СостоянияРаботыСЛидами.ЦветЭлементаСостояния(Состояние.ЦветСостояния);
		
		НовоеСостояние = НовоеИзмерениеКанбан.Элементы.Добавить(Состояние.Ссылка);
		
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				Состояние.Наименование,
				ШрифтНевидимогоЭлемента,
				ЦветЭлемента));
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				Состояние.Наименование,
				ШрифтЭлемента,
				ЦветаСтиля.ЦветРамкиЭпиценра,
				ЦветЭлемента));
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				Состояние.Наименование,
				ШрифтНевидимогоЭлемента,
				ЦветЭлемента));
		НовоеСостояние.Текст = Новый ФорматированнаяСтрока(МассивСтрок);
		НовоеСостояние.ЦветФона = ЦветЭлемента;
	КонецЦикла;
	
	НовыйЗаголовок = НовоеИзмерениеЗаголовкаКанбан.Элементы.Добавить(ЗначениеЗаголовкаКанбан());
	НовыйЗаголовок.Текст = " ";
	НовыйЗаголовок.ЦветФона = ЦветаСтиля.ЦветФонаКанбан;
	
	НастроитьКанбанОтложенно = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнитьКанбанНаКлиенте()
	
	Если ВидФормыКанбан(СписокКанбан) Тогда
		Если Не ФоновоеЗаданиеАктивно(ИдентификаторЗадания) Тогда
			ЗаполнитьКанбан();
		КонецЕсли;
	Иначе
		ЗаполнитьКанбанОтложенно = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнитьКанбанНаСервере()
	
	Если ВидФормыКанбан(СписокКанбан) Тогда
		Если Не ФоновоеЗаданиеАктивно(ИдентификаторЗадания) Тогда
			ЗаполнитьКанбан();
		КонецЕсли;
	Иначе
		ЗаполнитьКанбанОтложенно = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКанбан(ПрокруткаКанбан = Неопределено)
	
	Если ЗначениеЗаполнено(АдресРезультата) Тогда
		СостоянияЛидов = ПолучитьИзВременногоХранилища(АдресРезультата).Получить();
	Иначе
		Если НастроитьКанбанОтложенно Тогда
			НастроитьКанбан();
		КонецЕсли;
	
		ПараметрыКанбан = Справочники.КонтактныеЛица.НовыеПараметрыКанбан();
		ПараметрыКанбан.СостоянияКанбан = СостоянияРаботыСЛидами.Выгрузить();
		ПараметрыКанбан.Ответственный = ОтветственныйОтбора(ЭтотОбъект);
		ПараметрыКанбан.Прокрутка = ПрокруткаКанбан;
		ПараметрыКанбан.КоличествоЭлементов = КоличествоЭлементовКанбан(ВысотаСтраницыКанбан, ПараметрыФормыСМетками.ИспользоватьМетки);
		ПараметрыКанбан.ТекущаяСтраницаКанбан = ТекущаяСтраницаКанбан;
		ПараметрыКанбан.КоличествоСтраницКанбан = КоличествоСтраницКанбан;
		ПараметрыКанбан.ИспользоватьМетки = ПараметрыФормыСМетками.ИспользоватьМетки;
		ПараметрыКанбан.ИмяВладельцаМеток = ПараметрыФормыСМетками.ИмяВладельца;
		ПараметрыКанбан.ОтобранныеМетки = МеткиОтбора();
		
		СостоянияЛидов = Справочники.КонтактныеЛица.ДанныеКанбан(ПараметрыКанбан);
	КонецЕсли;
	
	ВысотаЭлементаКанбан = ВысотаЭлементаКанбан(ПараметрыФормыСМетками.ИспользоватьМетки);
	
	ЭлементыКанбан = Канбан.Элементы;
	ЭлементыКанбан.Очистить();
	
	НачалоОтображенияКанбан = НачалоДня(ТекущаяДатаСеанса());
	КонецОтображенияКанбан = НачалоОтображенияКанбан + ВысотаСтраницыКанбан;
	
	Канбан.ТекущиеПериодыОтображения.Очистить();
	Канбан.ТекущиеПериодыОтображения.Добавить(НачалоОтображенияКанбан, КонецОтображенияКанбан);
	
	Для Каждого Состояние Из СостоянияРаботыСЛидами Цикл
		ТекущиеСостоянияЛидов = СостоянияЛидов.НайтиСтроки(Новый Структура("Состояние", Состояние.Ссылка));
		Если ТекущиеСостоянияЛидов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначенияИзмеренийКанбан = Новый Соответствие;
		ЗначенияИзмеренийКанбан.Вставить(ИзмерениеКанбан(), Состояние.Ссылка);
		ЗначенияИзмеренийКанбан.Вставить(ИзмерениеЗаголовкаКанбан(), ЗначениеЗаголовкаКанбан());
		
		Отступ = 0;
		ИндексЛида = 0;
		ВсегоЛидов = ТекущиеСостоянияЛидов.ВГраница();
		
		Для ИндексЛида = 0 По ВсегоЛидов Цикл
			Лид = ТекущиеСостоянияЛидов[ИндексЛида];
			
			НовыйЭлемент = ЭлементыКанбан.Добавить(
				НачалоОтображенияКанбан + Отступ,
				НачалоОтображенияКанбан + Отступ + ВысотаЭлементаКанбан);
			НовыйЭлемент.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмеренийКанбан);
			НовыйЭлемент.Текст = Лид.Представление;
			НовыйЭлемент.Значение = Лид.Ссылка;
			НовыйЭлемент.Рамка = РамкаЭлементаКанбан;
			НовыйЭлемент.ЦветРамки = ЦветаСтиля.ЦветФонаКанбан;
			НовыйЭлемент.ЦветФона = ЦветаСтиля.ЦветЭлементаКанбан;
			НовыйЭлемент.РежимРазрешенияРедактирования = РежимРазрешенияРедактированияЭлементаПланировщика.ЗапретитьРастягивание;
			
			Отступ = Отступ + ВысотаЭлементаКанбан;
			
			Если ИндексЛида = 0 Тогда
				Состояние.ПорядокПервогоЭлементаКанбан = Лид.ПорядокКанбан;
			КонецЕсли;
			Состояние.ПорядокПоследнегоЭлементаКанбан = Лид.ПорядокКанбан;
		КонецЦикла;
		
	КонецЦикла;
	
	Если СостоянияЛидов.Количество() > 0 Тогда
		Если ПрокруткаКанбан = ПрокруткаКанбанНазад() Тогда
			ТекущаяСтраницаКанбан = ТекущаяСтраницаКанбан - 1;
		ИначеЕсли ПрокруткаКанбан = ПрокруткаКанбанВперед() Тогда
			ТекущаяСтраницаКанбан = ТекущаяСтраницаКанбан + 1;
		ИначеЕсли ПрокруткаКанбан = ПрокруткаКанбанВКонец()Тогда
			ТекущаяСтраницаКанбан = КоличествоСтраницКанбан - 1;
		Иначе
			ТекущаяСтраницаКанбан = 0;
		КонецЕсли;
	Иначе
		ТекущаяСтраницаКанбан = 0;
	КонецЕсли;
	
	Элементы.ПерейтиНаПервуюСтраницуКанбан.Доступность = ТекущаяСтраницаКанбан > 0;
	Элементы.ПерейтиНаПредыдущуюСтраницуКанбан.Доступность = ТекущаяСтраницаКанбан > 0;
	Элементы.ПерейтиНаСледующуюСтраницуКанбан.Доступность = ТекущаяСтраницаКанбан < КоличествоСтраницКанбан - 1;
	Элементы.ПерейтиНаПоследнююСтраницуКанбан.Доступность = ТекущаяСтраницаКанбан < КоличествоСтраницКанбан - 1;
	
	ПереключитьВРежимОжиданияКанбан(Ложь);
	
	ЗаполнитьКанбанОтложенно = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКанбанВФоне()
	
	Если Не ФоновоеЗаданиеАктивно(ИдентификаторЗадания) Тогда
		ПодключитьОбработчикОжидания("НачатьЗаполнениеКанбан", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаполнениеКанбан()
	
	Если ВысотаСтраницыКанбан = 0 Тогда
		ЗаполнитьВысотуСтраницыКанбан();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	Результат = ВыполнитьЗаполнениеКанбанВФоне();

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьЗаполнениеКанбан", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ФоновоеЗаданиеАктивно(Знач ИдентификаторЗадания)
	
	ФоновоеЗаданиеКанбан = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	
	Если ФоновоеЗаданиеКанбан <> Неопределено И ФоновоеЗаданиеКанбан.Состояние = СостояниеФоновогоЗадания.Активно Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ВыполнитьЗаполнениеКанбанВФоне()
	
	ПереключитьВРежимОжиданияКанбан(Истина);

	НаименованиеЗадания = НСтр("ru = 'Заполнение канбан лидов'");
	
	Если НастроитьКанбанОтложенно Тогда
		НастроитьКанбан();
	КонецЕсли;
	
	ПараметрыЗадания = Справочники.КонтактныеЛица.НовыеПараметрыКанбан();
	ПараметрыЗадания.СостоянияКанбан = СостоянияРаботыСЛидами.Выгрузить();
	ПараметрыЗадания.Ответственный = ОтветственныйОтбора(ЭтотОбъект);
	ПараметрыЗадания.КоличествоЭлементов = КоличествоЭлементовКанбан(ВысотаСтраницыКанбан, ПараметрыФормыСМетками.ИспользоватьМетки);
	ПараметрыЗадания.ТекущаяСтраницаКанбан = ТекущаяСтраницаКанбан;
	ПараметрыЗадания.КоличествоСтраницКанбан = КоличествоСтраницКанбан;
	ПараметрыЗадания.ИспользоватьМетки = ПараметрыФормыСМетками.ИспользоватьМетки;
	ПараметрыЗадания.ИмяВладельцаМеток = ПараметрыФормыСМетками.ИмяВладельца;
	ПараметрыЗадания.ОтобранныеМетки = МеткиОтбора();
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Справочники.КонтактныеЛица.СформироватьДанныеКанбан", 
		ПараметрыЗадания, ПараметрыВыполненияВФоне);
	
КонецФункции

&НаСервере
Процедура ПереключитьВРежимОжиданияКанбан(ВключитьОжидание)
	
	Элементы.ОжиданиеКанбан.Видимость = ВключитьОжидание;
	Элементы.Канбан.Видимость = Не ВключитьОжидание;
	Элементы.ГруппаПодвалКанбан.Видимость = Не ВключитьОжидание;
	Элементы.ГруппаКоманднаяПанель.ТолькоПросмотр = ВключитьОжидание;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьЗаполнениеКанбан(Результат, ПроверитьАктуальность) Экспорт
	
	Если Результат = Неопределено Тогда // Задание было отменено.
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	АдресРезультата = Результат.АдресРезультата;
	ЗаполнитьКанбан();
	АдресРезультата = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСостоянияРаботыСЛидами()
	
	Ответственный = ОтветственныйОтбора(ЭтотОбъект);
	
	СостоянияКанбан = Справочники.КонтактныеЛица.СостоянияКанбан(Ответственный);
	
	АктуальныеСостояния = Справочники.СостоянияРаботыСЛидами.АктуальныеСостояния();
	Для Каждого Состояние Из АктуальныеСостояния Цикл
		Если СостоянияКанбан.Найти(Состояние.Ссылка, "Ссылка") = Неопределено Тогда
			НовоеСостояние = СостоянияКанбан.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеСостояние, Состояние);
		КонецЕсли;
	КонецЦикла;
	
	СостоянияКанбан.Сортировать("Порядок");
	
	СостоянияРаботыСЛидами.Загрузить(СостоянияКанбан);
	
	ЗаполнитьКоличествоСтраницКанбан();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИзмерениеКанбан()
	
	Возврат "СостоянияРаботыСЛидами";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмерениеЗаголовкаКанбан()
	
	Возврат "ЗаголовокСостоянияРаботыСЛидами";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеЗаголовкаКанбан()
	
	Возврат "Заголовок";
	
КонецФункции

&НаСервере
Функция ИзменитьСостояниеПоЛидамКанбан(ЛидыДляИзменения, НовоеСостояние, СмещаемыйЛид)
	
	Если ЗначениеЗаполнено(СмещаемыйЛид) Тогда
		ПорядокКанбан = РегистрыСведений.ПорядокРаботыСЛидами.ПорядокКанбанПоЛиду(СмещаемыйЛид);
	Иначе
		ПорядокКанбан = 0;
	КонецЕсли;
	
	ПериодИзменения = ТекущаяДатаСеанса();
	
	ИзмененныеЛиды = РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.УстановитьСостояниеРаботыСЛидами(
		ЛидыДляИзменения, НовоеСостояние, ПериодИзменения);
	
	ИзмененоСостояниеЛидов = ИзмененныеЛиды.Количество() > 0;
	
	ЛидыДляИзмененияПорядка = Новый Массив;
	Если ПорядокКанбан > 0 Тогда
		Для Каждого Лид Из ЛидыДляИзменения Цикл
			Если ИзмененныеЛиды.Найти(Лид) = Неопределено Тогда
				ЛидыДляИзмененияПорядка.Добавить(Лид);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИзмененПорядокКанбан = Ложь;
	Если ЛидыДляИзмененияПорядка.Количество() > 0 Тогда
		РегистрыСведений.ПорядокРаботыСЛидами.УстановитьРучнойПорядокРаботыСЛидами(
			ЛидыДляИзмененияПорядка, НовоеСостояние, ПорядокКанбан);
		ИзмененПорядокКанбан = Истина;
	КонецЕсли;
	
	Возврат ИзмененоСостояниеЛидов Или ИзмененПорядокКанбан;
	
КонецФункции

&НаСервере
Процедура УстановитьОтметкуНекачественныйЛид(ЛидыДляИзменения)
	
	Для Каждого Лид Из ЛидыДляИзменения Цикл
		ЛидОбъект = Лид.ПолучитьОбъект();
		ЛидОбъект.НекачественныйЛид = Истина;
		ЛидОбъект.Записать();
	КонецЦикла;
	
	ПроверитьЗаполнитьКанбанНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ПеревестиВКонтрагентаНаСервере(Контрагент)
	
	ЛидДляИзменения = ПолучитьИзВременногоХранилища(АдресДляЗавершенияРаботыЛидов);
	
	Если ТипЗнч(ЛидДляИзменения) <> Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЛидОбъект = ЛидДляИзменения.ПолучитьОбъект();
	ЛидОбъект.ОбъектВладелец = Контрагент;
	ЛидОбъект.Записать();
	
	УдалитьИзВременногоХранилища(АдресДляЗавершенияРаботыЛидов);
	
	ПроверитьЗаполнитьКанбанНаСервере();
	
	Возврат ЛидДляИзменения;
	
КонецФункции

&НаСервере
Функция ПеревестиВКонтрагентаСчета(СчетПокупателю)
	
	ЛидДляИзменения = ПолучитьИзВременногоХранилища(АдресДляЗавершенияРаботыЛидов);
	
	Если ТипЗнч(ЛидДляИзменения) <> Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетПокупателю, "Контрагент");
	
	ЛидОбъект = ЛидДляИзменения.ПолучитьОбъект();
	ЛидОбъект.ОбъектВладелец = Контрагент;
	ЛидОбъект.Записать();
	
	УдалитьИзВременногоХранилища(АдресДляЗавершенияРаботыЛидов);
	
	ПроверитьЗаполнитьКанбанНаСервере();
	
	Возврат ЛидДляИзменения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПрокруткаКанбанНазад()
	
	Возврат "Назад";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПрокруткаКанбанВперед()
	
	Возврат "Вперед";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПрокруткаКанбанВКонец()
	
	Возврат "ВКонец";
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьВысотуСтраницыКанбан()
	
	ИнформацияОбЭкранеКанбан = ОбщегоНазначенияКлиентСервер.ПараметрыЭкранаУстройства();
	
	Если ИнформацияОбЭкранеКанбан.Ширина = 1280
		И ИнформацияОбЭкранеКанбан.Высота = 768
		И ИнформацияОбЭкранеКанбан.DPI = 96 Тогда
		ВысотаСтраницыКанбан = 20;
	Иначе
		ВысотаСтраницыКанбан = 40;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ВысотаСтраницыКанбан = ВысотаСтраницыКанбан - 3;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КоличествоЭлементовКанбан(ВысотаСтраницыКанбан, ИспользоватьМетки)
	
	Возврат Цел(ВысотаСтраницыКанбан / ВысотаЭлементаКанбан(ИспользоватьМетки));
	
КонецФункции

&НаСервереБезКонтекста
Функция ВысотаЭлементаКанбан(ИспользоватьМетки)

	Возврат Справочники.КонтактныеЛица.ВысотаЭлементаКанбан(ИспользоватьМетки);

КонецФункции

&НаСервере
Процедура ЗаполнитьКоличествоСтраницКанбан()
	
	КоличествоСтраницКанбан = 0;
	
	КоличествоЭлементовКанбанНаСтранице = КоличествоЭлементовКанбан(ВысотаСтраницыКанбан, ПараметрыФормыСМетками.ИспользоватьМетки);
	
	Для Каждого Состояние из СостоянияРаботыСЛидами Цикл
		КоличествоСтраницСостояния = Цел(Состояние.КоличествоЭлементовКанбан / КоличествоЭлементовКанбанНаСтранице);
		Если Состояние.КоличествоЭлементовКанбан / КоличествоЭлементовКанбанНаСтранице > КоличествоСтраницСостояния Тогда
			КоличествоСтраницСостояния = КоличествоСтраницСостояния + 1;
		КонецЕсли;
		
		КоличествоСтраницКанбан = Макс(КоличествоСтраницСостояния, КоличествоСтраницКанбан);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЛидаКанбан()
	
	ВыделенныеЭлементы = Элементы.Канбан.ВыделенныеЭлементы;
	
	Если ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеЭлемента = ВыделенныеЭлементы[0].Значение;
	
	Если ЗначениеЗаполнено(ЗначениеЭлемента) И ТипЗнч(ЗначениеЭлемента) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		ПараметрыФормы = Новый Структура("Ключ", ЗначениеЭлемента);
		ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаЛида", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВопросПометитьНаУдалениеКанбанЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПометитьНаУдалениеЛидаКанбан(ДополнительныеПараметры.Лид);
		ОповеститьОбИзмененииЛида(ДополнительныеПараметры.Лид);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПометитьНаУдалениеЛидаКанбан(ЛидДляИзменения)
	
	ЛидОбъект = ЛидДляИзменения.ПолучитьОбъект();
	ЛидОбъект.УстановитьПометкуУдаления(Истина);
	
	ПроверитьЗаполнитьКанбанНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаСервере
Процедура ОтборМеткиПриИзмененииНаСервере()
	
	РаботаСМетками.МеткиПриИзмененииНаСервере(ЭтотОбъект);
	
	ПроверитьЗаполнитьКанбанНаСервере();

КонецПроцедуры

&НаСервере
Функция ОбработкаОповещенияФормыСМеткамиНаСервере()
	
	РаботаСМетками.ОбработкаОповещенияНаСервере(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы(МожноРедактировать)
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы(МожноРедактировать);
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы(МожноРедактировать)
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.КомандаСоздать = "ФормаСоздать";
	КомандыПоля.РазрешеноИзменение = МожноРедактировать;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьОтбор(Команда)
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойОтбораМК(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОтбораМК(ЭтотОбъект, "Список", "");
	
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораБыстрыеОтборы(ЭтотОбъект,
		Элементы.ОтборОтветственный,
		НСтр("ru = 'Ответственный'"),
		"Все ответственные");
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораБыстрыеОтборы(ЭтотОбъект,
		Элементы.ОтборМетки,
		НСтр("ru = 'Метки'"),
		НСтр("ru = 'Любая метка'"));
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.ГруппаДополнительныеОтборы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.СоздатьДокументКоммерческоеПредложение,
		Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.ГруппаКомандВзаимодействия,
		Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.КомандыЗавершениеРаботыСЛидомКанбан,
		Элементы.Список.КонтекстноеМеню);

КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормыСправочника(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Наименование);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Телефон, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Email, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.МеткиСтрокой, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Ответственный, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Контрагент, 3);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти

