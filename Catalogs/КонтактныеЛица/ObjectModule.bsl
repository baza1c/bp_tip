#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		Строки = Новый Массив;
		Если Не ПустаяСтрока(Фамилия) Тогда
			Строки.Добавить(Фамилия);
		КонецЕсли;
		Если Не ПустаяСтрока(Имя) Тогда
			Строки.Добавить(Имя);
		КонецЕсли;
		Если Не ПустаяСтрока(Отчество) Тогда
			Строки.Добавить(Отчество);
		КонецЕсли;
		ФИО = СтрСоединить(Строки, " ");
		Наименование = Справочники.КонтактныеЛица.НаименованиеКонтактногоЛица(ФИО, Должность);
	КонецЕсли;

	Если ВидКонтактногоЛица = Перечисления.ВидыКонтактныхЛиц.ЛичныйКонтакт Тогда
		ПользовательЛичногоКонтакта = ОбъектВладелец;
	Иначе
		ПользовательЛичногоКонтакта = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
	Если Лид И Не ЭтоНовый() Тогда
		ЛидЗавершенДоИзменения = Не Справочники.КонтактныеЛица.ЭтоЛид(Ссылка, Истина);
		ЛидЗавершен = ЗначениеЗаполнено(ОбъектВладелец) Или НекачественныйЛид;
		
		Если ЛидЗавершенДоИзменения <> ЛидЗавершен Тогда
			ДополнительныеСвойства.Вставить("ЛидЗавершен", ЛидЗавершен);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЛидЗавершенИзменен = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ЛидЗавершен");
	ЛидЗавершен = ЛидЗавершенИзменен = Истина;
	ЛидВозвращенВРаботу = ЛидЗавершен = Ложь;
	Если ЛидЗавершен Тогда
		РегистрыСведений.ПорядокРаботыСЛидами.ОчиститьПорядокКанбанПоЛиду(Ссылка);
	ИначеЕсли ЛидВозвращенВРаботу Тогда
		ТекущееСостояние = РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.ПолучитьСостояниеРаботыСЛидом(Ссылка);
		Если ЗначениеЗаполнено(ТекущееСостояние.Состояние) Тогда
			РегистрыСведений.ПорядокРаботыСЛидами.УстановитьПорядокКанбанПоЛиду(
				Ссылка,
				ТекущееСостояние.Состояние,
				ТекущееСостояние.Период);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если Лид И ТипЗнч(ОбъектВладелец) <> Тип("СправочникСсылка.Контрагенты") Тогда
			ОбъектВладелец = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
		
		ТипВладельца = ТипЗнч(ОбъектВладелец);
		
		Если ТипВладельца = Тип("СправочникСсылка.Контрагенты") Тогда
			ВидКонтактногоЛица = Перечисления.ВидыКонтактныхЛиц.КонтактноеЛицоКонтрагента;
		ИначеЕсли ТипВладельца = Тип("СправочникСсылка.Пользователи") Тогда
			ВидКонтактногоЛица = Перечисления.ВидыКонтактныхЛиц.ЛичныйКонтакт;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ТелефонЗначениеJSON")
			И ЗначениеЗаполнено(ДанныеЗаполнения.ТелефонЗначениеJSON) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
				ЭтотОбъект, ДанныеЗаполнения.ТелефонЗначениеJSON, Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйКонтактныеЛица);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("АдресЭлектроннойПочтыЗначениеJSON")
			И ЗначениеЗаполнено(ДанныеЗаполнения.АдресЭлектроннойПочтыЗначениеJSON) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
				ЭтотОбъект, ДанныеЗаполнения.АдресЭлектроннойПочтыЗначениеJSON, Справочники.ВидыКонтактнойИнформации.EmailКонтактныеЛица);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Лид Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Лид Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Наименование");
	
		Если ПустаяСтрока(Наименование) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "ЗАПОЛНЕНИЕ", НСтр("ru = 'Наименование'"));
			Поле = "Наименование";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры


Процедура ПриКопировании(ОбъектКопирования)
	
	Если Лид Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	Иначе
		Ответственный = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли