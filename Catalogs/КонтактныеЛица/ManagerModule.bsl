#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьКоличествоПодчиненныхЭлементовПоВладельцу(Владелец) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактныеЛица.Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.ОбъектВладелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Владелец);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество();

КонецФункции

Функция КонтактноеЛицоПоУмолчанию(Владелец) Экспорт

	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОсновноеКонтактноеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ОсновноеКонтактноеЛицо");
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("ОсновноеКонтактноеЛицо", ОсновноеКонтактноеЛицо);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	КонтактныеЛица.Ссылка,
	|	ВЫБОР
	|		КОГДА КонтактныеЛица.Ссылка = &ОсновноеКонтактноеЛицо
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК ПорядокСортировки
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.ОбъектВладелец = &Владелец
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокСортировки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.КонтактныеЛица.ПустаяСсылка();

КонецФункции

Функция СписокКонтактныхЛиц(Контрагент, ФИОКратко = Истина) Экспорт
	
	Список = Новый СписокЗначений();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.Текст = "ВЫБРАТЬ
		|	КонтактныеЛица.*
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|ГДЕ
		|	КонтактныеЛица.ОбъектВладелец = &Контрагент";
		
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		
		Представление = "";
		Если ЗначениеЗаполнено(РезультатЗапроса.Фамилия) Тогда

			Представление = ОбщегоНазначенияБПВызовСервера.ПолучитьФамилиюИмяОтчество(
				РезультатЗапроса.Фамилия, 
				РезультатЗапроса.Имя,
				РезультатЗапроса.Отчество,
				ФИОКратко);
				
			Если ЗначениеЗаполнено(РезультатЗапроса.Должность) Тогда
				Представление = Представление + ", " + РезультатЗапроса.Должность;
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(РезультатЗапроса.Наименование) Тогда
			Представление = РезультатЗапроса.Наименование;
			Если ЗначениеЗаполнено(РезультатЗапроса.Должность) Тогда
				Представление = Представление + ", " + РезультатЗапроса.Должность;
			КонецЕсли;
		КонецЕсли;
			
		Если ЗначениеЗаполнено(Представление) Тогда 
			Список.Добавить(РезультатЗапроса.Ссылка, Представление);
		КонецЕсли;

	КонецЦикла;
	
	Возврат Список;

КонецФункции

Функция НаименованиеКонтактногоЛица(ФИО, Должность) Экспорт
	Возврат ФИО + ?(ЗначениеЗаполнено(Должность), ", " + Должность, "");
КонецФункции

// Ищет элемент справочника "КонтактныеЛица" по реквизитам ОбъектВладелец и Наименование.
//
// Параметры:
//   Контрагент - СправочникСсылка.Контрагенты - ссылка на ОбъектВладелец контактного лица
//   Наименование - Строка - наименование контактного лица.
//
// Возвращаемое значение:
//   СправочникСсылка.КонтактныеЛица - ссылка на найденный элемент справочника или Неопределено, если элемент не найден.
//
Функция НайтиКонтактноеЛицо(Контрагент, Наименование) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтактныеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.Наименование = &Наименование
	|	И КонтактныеЛица.ОбъектВладелец = &ОбъектВладелец";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("ОбъектВладелец", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
			
	Возврат Результат;
	
КонецФункции

// Создает элемент справочника "КонтактныеЛица".
//
// Параметры:
//   ПараметрыСоздания - Структура - Параметры создания контактного лица:
//       * ЗначенияЗаполнения - Структура - Параметры заполнения нового объекта.
//       * КонтактнаяИнформация - Массив - Массив структур для заполнения контактной информации объекта.
//
// Возвращаемое значение:
//   СправочникСсылка.КонтактныеЛица - ссылка на созданный элемент справочника.
//
Функция СоздатьКонтактноеЛицо(ПараметрыСоздания) Экспорт
	
	НовыйОбъект = Справочники.КонтактныеЛица.СоздатьЭлемент();
	
	Если ПараметрыСоздания.Свойство("ЗначенияЗаполнения") Тогда
		НовыйОбъект.Заполнить(ПараметрыСоздания.ЗначенияЗаполнения);
	КонецЕсли;
	
	Если ПараметрыСоздания.Свойство("КонтактнаяИнформация") Тогда
		Для Каждого СтрокаКИ Из ПараметрыСоздания.КонтактнаяИнформация Цикл
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйОбъект, СтрокаКИ.Представление, СтрокаКИ.Вид);
		КонецЦикла;
	КонецЕсли;
	
	НовыйОбъект.Записать();
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

// Записывает контрагента в качестве владельца контактного лица.
//
// Параметры:
//   КонтактноеЛицо - СправочникСсылка.КонтактныеЛица - контактное лицо для установки владельца
//   Контрагент - СправочникСсылка.Контрагенты - контрагент, который будет установлен в качестве владельца
//
Процедура УстановитьВладельцаКонтактногоЛица(КонтактноеЛицо, Контрагент) Экспорт
	
	Если Не ЗначениеЗаполнено(КонтактноеЛицо)
		Или Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	КонтрагентКонтакта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтактноеЛицо, "ОбъектВладелец");
	Если КонтрагентКонтакта <> Контрагент Тогда
		КонтактноеЛицоОбъект = КонтактноеЛицо.ПолучитьОбъект();
		
		Попытка
			КонтактноеЛицоОбъект.Заблокировать();
			КонтактноеЛицоОбъект.ОбъектВладелец = Контрагент;
			КонтактноеЛицоОбъект.Записать();
			КонтактноеЛицоОбъект.Разблокировать();
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'При записи владельца контактного лица произошла ошибка: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись владельца контактного лица'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Справочники.КонтактныеЛица,
				КонтактноеЛицо,
				ТекстСообщения);
				
			ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось записать владельца контактного лица. Возможно, уже открыта форма контактного лица.'"),
				КонтактноеЛицо);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияПользователю, КонтактноеЛицо);
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(ОбъектВладелец)
	|ИЛИ ЧтениеСпискаРазрешено(ОбъектВладелец ТОЛЬКО Справочник.Контрагенты)
	|	
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(ОбъектВладелец)
	|ИЛИ ИзменениеСпискаРазрешено(ОбъектВладелец ТОЛЬКО Справочник.Контрагенты)
	|";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиОбновления

Функция ЗаполнитьВидыКонтактнойИнформации() Экспорт
	
	ПредопределенныеВиды = Новый Массив;
	ПредопределенныеВиды.Добавить("АдресДляИнформированияКонтактныеЛица");
	ПредопределенныеВиды.Добавить("ТелефонМобильныйКонтактныеЛица");
	ПредопределенныеВиды.Добавить("ТелефонРабочийКонтактныеЛица");
	ПредопределенныеВиды.Добавить("EmailКонтактныеЛица");
	ПредопределенныеВиды.Добавить("ДругаяИнформацияКонтактныеЛица");
	
	Для Каждого ПредопределенныйВидКИ Из ПредопределенныеВиды Цикл
		
		ПараметрыВида = ОписаниеПредопределенногоВида(ПредопределенныйВидКИ);
		Если ЗначениеЗаполнено(ПараметрыВида) Тогда
			УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Процедура УстановитьПризнакОтображатьВсегдаВидовКонтактнойИнформации() Экспорт
	
	НеобязательныеПредопределенныеВиды = Новый Массив;
	НеобязательныеПредопределенныеВиды.Добавить("АдресДляИнформированияКонтактныеЛица");
	НеобязательныеПредопределенныеВиды.Добавить("ТелефонРабочийКонтактныеЛица");
	НеобязательныеПредопределенныеВиды.Добавить("ДругаяИнформацияКонтактныеЛица");
	Для Каждого НеобязательныйВид Из НеобязательныеПредопределенныеВиды Цикл
		
		ПараметрыВида = ОписаниеПредопределенногоВида(НеобязательныйВид);
		Если ЗначениеЗаполнено(ПараметрыВида) Тогда
			УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(ПараметрыВида);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Проверяет является ли контактное лицо лидом, который не переведен в контрагента
//
// Параметры:
//   КонтактноеЛицо - СправочникСсылка.КонтактныеЛица - контактное лицо для проверки
//   ПроверкаНаНекачественный - Булево - проверка, что лид не отмечен как некачественный
//
// Возвращаемое значение:
//   Булево - истина если контактное лицо является лидом
//
Функция ЭтоЛид(КонтактноеЛицо, ПроверкаНаНекачественный = Ложь) Экспорт
	
	РезультатПроверки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтактныеЛица.Лид КАК Лид,
	|	КонтактныеЛица.ОбъектВладелец КАК ОбъектВладелец,
	|	КонтактныеЛица.НекачественныйЛид КАК НекачественныйЛид
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.Ссылка = &КонтактноеЛицо";
	Запрос.УстановитьПараметр("КонтактноеЛицо", КонтактноеЛицо);
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		РезультатПроверки = Выборка.Лид И Не ЗначениеЗаполнено(Выборка.ОбъектВладелец);
		Если ПроверкаНаНекачественный Тогда
			РезультатПроверки = РезультатПроверки И Не Выборка.НекачественныйЛид;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта"
		И ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ключ")) Тогда
		
		РеквизитыКонтакта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Ключ, "Лид, ОбъектВладелец");
		
		Если РеквизитыКонтакта.Лид И Не ЗначениеЗаполнено(РеквизитыКонтакта.ОбъектВладелец)
			И ФункциональностьПрограммы.РазделЛидыДоступен() Тогда
			// Контакт не переведен в контрагента, открываем форму лида
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаЛида";
		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаВыбора" И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Лид", Ложь) Тогда
		
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "ФормаВыбораЛидов";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

Функция ОписаниеПредопределенногоВида(ИмяВида)
	
	СсылкаНаВид = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации." + ИмяВида);
	Если Не ЗначениеЗаполнено(СсылкаНаВид) Тогда
		// Предопределенного элемента по какой-то причине нет, поэтому ничего не делаем, чтобы обработчик не упал.
		Возврат Неопределено;
	КонецЕсли;
	
	Если ИмяВида = "АдресДляИнформированияКонтактныеЛица" Тогда
		ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(
			Перечисления.ТипыКонтактнойИнформации.Адрес);
		ПараметрыВида.Вид                               = СсылкаНаВид;
		ПараметрыВида.Наименование = НСтр("ru = 'Адрес для информирования'");
		ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
		ПараметрыВида.ВидРедактирования                 = "ПолеВводаИДиалог";
		ПараметрыВида.ОбязательноеЗаполнение            = Ложь;
		ПараметрыВида.Порядок                           = 1;
		ПараметрыВида.РазрешитьВводНесколькихЗначений   = Ложь;
		ПараметрыВида.ОтображатьВсегда                  = Ложь;
		Возврат ПараметрыВида;
	КонецЕсли;
	
	Если ИмяВида = "ТелефонМобильныйКонтактныеЛица" Тогда
		ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(
			Перечисления.ТипыКонтактнойИнформации.Телефон);
		ПараметрыВида.Вид                               = СсылкаНаВид;
		ПараметрыВида.Наименование = НСтр("ru = 'Телефон мобильный'");
		ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
		ПараметрыВида.ВидРедактирования                 = "ПолеВводаИДиалог";
		ПараметрыВида.ОбязательноеЗаполнение            = Ложь;
		ПараметрыВида.Порядок                           = 2;
		ПараметрыВида.РазрешитьВводНесколькихЗначений   = Истина;
		Возврат ПараметрыВида;
	КонецЕсли;
	
	Если ИмяВида = "ТелефонРабочийКонтактныеЛица" Тогда
		ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(
			Перечисления.ТипыКонтактнойИнформации.Телефон);
		ПараметрыВида.Вид                               = СсылкаНаВид;
		ПараметрыВида.Наименование = НСтр("ru = 'Телефон рабочий'");
		ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
		ПараметрыВида.ВидРедактирования                 = "ПолеВводаИДиалог";
		ПараметрыВида.ОбязательноеЗаполнение            = Ложь;
		ПараметрыВида.Порядок                           = 3;
		ПараметрыВида.РазрешитьВводНесколькихЗначений   = Истина;
		ПараметрыВида.ОтображатьВсегда                  = Ложь;
		Возврат ПараметрыВида;
	КонецЕсли;
	
	Если ИмяВида = "EmailКонтактныеЛица" Тогда
		ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(
			Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
		ПараметрыВида.Вид                               = СсылкаНаВид;
		ПараметрыВида.Наименование = НСтр("ru = 'Email'");
		ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
		ПараметрыВида.ВидРедактирования                 = "ПолеВвода";
		ПараметрыВида.ОбязательноеЗаполнение            = Ложь;
		ПараметрыВида.Порядок                           = 4;
		ПараметрыВида.РазрешитьВводНесколькихЗначений   = Истина;
		Возврат ПараметрыВида;
	КонецЕсли;
	
	Если ИмяВида = "ДругаяИнформацияКонтактныеЛица" Тогда
		ПараметрыВида = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(
			Перечисления.ТипыКонтактнойИнформации.Другое);
		ПараметрыВида.Вид                               = СсылкаНаВид;
		ПараметрыВида.Наименование = НСтр("ru = 'Другое (любая другая контактная информация)'");
		ПараметрыВида.МожноИзменятьСпособРедактирования = Истина;
		ПараметрыВида.ВидРедактирования                 = "ПолеВвода";
		ПараметрыВида.ОбязательноеЗаполнение            = Ложь;
		ПараметрыВида.Порядок                           = 5;
		ПараметрыВида.РазрешитьВводНесколькихЗначений   = Истина;
		ПараметрыВида.ОтображатьВсегда                  = Ложь;
		Возврат ПараметрыВида;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Получает ссылку на элемент справочника с переданными свойствами.
// Если элемент с таким набором свойств отсутствует в справочнике,
// он автоматически создается.
//
// Параметры:
//  - Владелец - Ссылка - ссылка на элемент-владелец
//  - ДанныеЗаполнения  - структура свойств, которыми должен обладать
//                        элемент справочника. Элементы структуры:
//    - Должность - Строка - должность контактного лица
//    - Фамилия   - Строка - фамилия контактного лица
//    - Имя       - Строка - имя контактного лица
//    - Отчество  - Строка - отчество контактного лица
//
// Возвращаемое значение:
//  - Ссылка - ссылка на элемент справочника
//
Функция ПолучитьЭлемент(Владелец, ДанныеЗаполнения) Экспорт
	
	Должность = ДанныеЗаполнения.Должность;
	Фамилия   = ДанныеЗаполнения.Фамилия;
	Имя       = ДанныеЗаполнения.Имя;
	Отчество  = ДанныеЗаполнения.Отчество;
	
	Результат = ПустаяСсылка();
	
	// Поиск существующего контактного лица
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец",  Владелец);
	Запрос.УстановитьПараметр("Должность", Должность);
	Запрос.УстановитьПараметр("Фамилия",   Фамилия);
	Запрос.УстановитьПараметр("Имя",       Имя);
	Запрос.УстановитьПараметр("Отчество",  Отчество);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактныеЛица.Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.ОбъектВладелец = &Владелец
	|	И КонтактныеЛица.Фамилия = &Фамилия
	|	И КонтактныеЛица.Имя = &Имя
	|	И КонтактныеЛица.Отчество = &Отчество
	|	И КонтактныеЛица.Должность = &Должность";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Если Результат.Пустая() Тогда
		НовоеКонтактноеЛицо = СоздатьЭлемент();
		НовоеКонтактноеЛицо.ВидКонтактногоЛица = Перечисления.ВидыКонтактныхЛиц.КонтактноеЛицоКонтрагента;
		НовоеКонтактноеЛицо.ОбъектВладелец     = Владелец;
		НовоеКонтактноеЛицо.Должность          = Должность;
		НовоеКонтактноеЛицо.Фамилия            = Фамилия;
		НовоеКонтактноеЛицо.Имя                = Имя;
		НовоеКонтактноеЛицо.Отчество           = Отчество;
		НовоеКонтактноеЛицо.Наименование       = НовоеКонтактноеЛицо.Фамилия +
			" " + НовоеКонтактноеЛицо.Имя +
			" " + НовоеКонтактноеЛицо.Отчество +
			", " + НовоеКонтактноеЛицо.Должность;
		НовоеКонтактноеЛицо.Записать();
		
		Результат = НовоеКонтактноеЛицо.Ссылка;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция КонтактнаяИнформацияЛиц(КонтактныеЛица) Экспорт
	
	Если Не ЗначениеЗаполнено(КонтактныеЛица) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка КАК КонтактноеЛицо,
	|	МАКСИМУМ(КонтактнаяИнформация.Телефон) КАК Телефон,
	|	МАКСИМУМ(КонтактнаяИнформация.Email) КАК Email
	|ИЗ
	|	(ВЫБРАТЬ
	|		КонтактныеЛицаКонтактнаяИнформация.Ссылка КАК Ссылка,
	|		КонтактныеЛицаКонтактнаяИнформация.Представление КАК Телефон,
	|		NULL КАК Email
	|	ИЗ
	|		Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	|	ГДЕ
	|		КонтактныеЛицаКонтактнаяИнформация.Ссылка В(&МассивКонтактныхЛиц)
	|		И КонтактныеЛицаКонтактнаяИнформация.ВидДляСписка В (ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонМобильныйКонтактныеЛица), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонРабочийКонтактныеЛица))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КонтактныеЛицаКонтактнаяИнформация.Ссылка,
	|		NULL,
	|		КонтактныеЛицаКонтактнаяИнформация.Представление
	|	ИЗ
	|		Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	|	ГДЕ
	|		КонтактныеЛицаКонтактнаяИнформация.Ссылка В(&МассивКонтактныхЛиц)
	|		И КонтактныеЛицаКонтактнаяИнформация.ВидДляСписка = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКонтактныеЛица)) КАК КонтактнаяИнформация
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтактнаяИнформация.Ссылка";
	
	Запрос.УстановитьПараметр("МассивКонтактныхЛиц", КонтактныеЛица);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#Область СлужебныеПроцедурыИФункцииКанбан

Функция НовыеПараметрыКанбан() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СостоянияКанбан", Новый ТаблицаЗначений);
	Результат.Вставить("Ответственный", Неопределено);
	Результат.Вставить("Прокрутка", Неопределено);
	Результат.Вставить("КоличествоЭлементов", 0);
	Результат.Вставить("ТекущаяСтраницаКанбан", 0);
	Результат.Вставить("КоличествоСтраницКанбан", 0);
	Результат.Вставить("ИспользоватьМетки", Ложь);
	Результат.Вставить("ИмяВладельцаМеток", "");
	Результат.Вставить("ОтобранныеМетки", Неопределено);
	
	Возврат Результат;
	
КонецФункции

Процедура СформироватьДанныеКанбан(Параметры, АдресРезультата) Экспорт
	
	ДанныеКанбан = ДанныеКанбан(Параметры);
	
	Хранилище = Новый ХранилищеЗначения(ДанныеКанбан);
	ПоместитьВоВременноеХранилище(Хранилище, АдресРезультата);
	
КонецПроцедуры

Функция ДанныеКанбан(Параметры) Экспорт
	
	ДанныеКанбан = Новый ТаблицаЗначений;
	ДанныеКанбан.Колонки.Добавить("Ссылка");
	ДанныеКанбан.Колонки.Добавить("Представление");
	ДанныеКанбан.Колонки.Добавить("ПорядокКанбан");
	ДанныеКанбан.Колонки.Добавить("Состояние");
	
	КоличествоСостояний = Параметры.СостоянияКанбан.Количество();
	
	Если КоличествоСостояний = 0 Тогда
		Возврат ДанныеКанбан;
	КонецЕсли;
	
	ШаблонТекстЗапроса = 
	"ВЫБРАТЬ
	|	КонтактныеЛица.Ссылка КАК Ссылка,
	|	КонтактныеЛица.Наименование КАК Наименование,
	|	ПорядокРаботыСЛидами.Состояние КАК Состояние,
	|	ПорядокРаботыСЛидами.ДатаИзменения КАК ДатаИзменения,
	|	ПорядокРаботыСЛидами.ПорядокКанбан КАК ПорядокКанбан
	|ИЗ
	|	РегистрСведений.ПорядокРаботыСЛидами КАК ПорядокРаботыСЛидами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
	|		ПО ПорядокРаботыСЛидами.Лид = КонтактныеЛица.Ссылка
	|ГДЕ
	|	НЕ КонтактныеЛица.ПометкаУдаления";
	
	Запрос = Новый Запрос;
	СхемаЗапроса = Новый СхемаЗапроса;
	
	ТекстыЗапросов = Новый Массив;
	
	Ответственный = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ответственный");
	Если Ответственный <> Неопределено Тогда
		Запрос.УстановитьПараметр("Ответственный", Ответственный);
	КонецЕсли;
	
	ОтобранныеМетки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ОтобранныеМетки");
	Если ОтобранныеМетки <> Неопределено Тогда
		Запрос.УстановитьПараметр("ОтобранныеМетки", ОтобранныеМетки);
	КонецЕсли;
	
	Прокрутка = Параметры.Прокрутка;
	Если Прокрутка = ПрокруткаКанбанНазад()
		И Параметры.ТекущаяСтраницаКанбан - 1 = 0 Тогда
		Прокрутка = Неопределено;
	КонецЕсли;
	
	ИндексСостояния = 0;
	
	Для ИндексСостояния = 0 По КоличествоСостояний - 1 Цикл
		Состояние = Параметры.СостоянияКанбан[ИндексСостояния];
		
		СхемаЗапроса.УстановитьТекстЗапроса(ШаблонТекстЗапроса);
		
		// Вычисляем количество элементов на странице
		Если Прокрутка = ПрокруткаКанбанВКонец() Тогда
			КоличествоСтраницСостояния = Цел(Состояние.КоличествоЭлементовКанбан / Параметры.КоличествоЭлементов);
			Если Состояние.КоличествоЭлементовКанбан / Параметры.КоличествоЭлементов > КоличествоСтраницСостояния Тогда
				КоличествоСтраницСостояния = КоличествоСтраницСостояния + 1;
			КонецЕсли;
			Если КоличествоСтраницСостояния >= Параметры.КоличествоСтраницКанбан Тогда
				ВыведеноДо = (КоличествоСтраницСостояния - 1) * Параметры.КоличествоЭлементов;
				ОсталосьВывести = Состояние.КоличествоЭлементовКанбан - ВыведеноДо;
				Если ОсталосьВывести > 0 Тогда
					КоличествоЭлементов = Мин(ОсталосьВывести, Параметры.КоличествоЭлементов);
				Иначе
					Продолжить;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].КоличествоПолучаемыхЗаписей = КоличествоЭлементов;
		ИначеЕсли Прокрутка = ПрокруткаКанбанВперед() Тогда
			ВыведеноДо = (Параметры.ТекущаяСтраницаКанбан + 1) * Параметры.КоличествоЭлементов;
			ОсталосьВывести = Состояние.КоличествоЭлементовКанбан - ВыведеноДо;
			Если ОсталосьВывести > 0 Тогда
				КоличествоЭлементов = Мин(ОсталосьВывести, Параметры.КоличествоЭлементов);
			Иначе
				Продолжить;
			КонецЕсли;
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].КоличествоПолучаемыхЗаписей = КоличествоЭлементов;
			УсловиеЗапроса = СтрШаблон("ПорядокРаботыСЛидами.ПорядокКанбан < &ПорядокКанбан%1", Формат(ИндексСостояния, "ЧГ="));
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(УсловиеЗапроса);
			Запрос.УстановитьПараметр(СтрШаблон("ПорядокКанбан%1", Формат(ИндексСостояния, "ЧГ=")), Состояние.ПорядокПоследнегоЭлементаКанбан);
		ИначеЕсли Прокрутка = ПрокруткаКанбанНазад() Тогда
			ВыведеноДо = (Параметры.ТекущаяСтраницаКанбан - 1) * Параметры.КоличествоЭлементов;
			ОсталосьВывести = Состояние.КоличествоЭлементовКанбан - ВыведеноДо;
			Если ОсталосьВывести > 0 Тогда
				КоличествоЭлементов = Мин(ОсталосьВывести, Параметры.КоличествоЭлементов);
				// Если возвращаемся на страницу, где были заполнены оставшиеся элементы,
				// то не накладываем условие по порядку канбан
				КоличествоСтраницСостояния = Цел(Состояние.КоличествоЭлементовКанбан / Параметры.КоличествоЭлементов);
				Если Состояние.КоличествоЭлементовКанбан / Параметры.КоличествоЭлементов > КоличествоСтраницСостояния Тогда
					КоличествоСтраницСостояния = КоличествоСтраницСостояния + 1;
				КонецЕсли;
				Если Параметры.ТекущаяСтраницаКанбан <> КоличествоСтраницСостояния Тогда
					УсловиеЗапроса = СтрШаблон("ПорядокРаботыСЛидами.ПорядокКанбан > &ПорядокКанбан%1", Формат(ИндексСостояния, "ЧГ="));
					СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(УсловиеЗапроса);
					Запрос.УстановитьПараметр(СтрШаблон("ПорядокКанбан%1", Формат(ИндексСостояния, "ЧГ=")), Состояние.ПорядокПервогоЭлементаКанбан);
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].КоличествоПолучаемыхЗаписей = КоличествоЭлементов;
		Иначе
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].КоличествоПолучаемыхЗаписей = Параметры.КоличествоЭлементов;
		КонецЕсли;
		
		УсловиеОтбора = СтрШаблон("ПорядокРаботыСЛидами.Состояние = &Состояние%1", Формат(ИндексСостояния, "ЧГ="));
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(УсловиеОтбора);
		Запрос.УстановитьПараметр(СтрШаблон("Состояние%1", Формат(ИндексСостояния, "ЧГ=")), Состояние.Ссылка);
		
		Если Ответственный <> Неопределено Тогда
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("КонтактныеЛица.Ответственный = &Ответственный");
		КонецЕсли;
		
		Если ОтобранныеМетки <> Неопределено Тогда
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("КонтактныеЛица.ДополнительныеРеквизиты.Свойство В(&ОтобранныеМетки)");
		КонецЕсли;
		
		ТекстЗапросаСостояния = СхемаЗапроса.ПолучитьТекстЗапроса();
		Если ТекстыЗапросов.Количество() > 0 Тогда
			ТекстЗапросаСостояния = "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|" + ТекстЗапросаСостояния;
		КонецЕсли;
		
		ТекстыЗапросов.Добавить(ТекстЗапросаСостояния);
		
	КонецЦикла;
	
	СхемаЗапроса.УстановитьТекстЗапроса(СтрСоединить(ТекстыЗапросов, Символы.ПС));
	Если Прокрутка = ПрокруткаКанбанНазад()
		Или Прокрутка = ПрокруткаКанбанВКонец() Тогда
		СхемаЗапроса.ПакетЗапросов[0].Порядок.Добавить("ПорядокКанбан").Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
	Иначе
		СхемаЗапроса.ПакетЗапросов[0].Порядок.Добавить("ПорядокКанбан").Направление = НаправлениеПорядкаСхемыЗапроса.ПоУбыванию;
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	СостоянияЛидов = Запрос.Выполнить().Выгрузить();
	
	Если Прокрутка = ПрокруткаКанбанНазад()
		Или Прокрутка = ПрокруткаКанбанВКонец() Тогда
		// Элементы упорядочиваем в порядке убывания, если это не сделано в запросе
		СостоянияЛидов.Сортировать("ПорядокКанбан Убыв");
	КонецЕсли;
	
	ЛидыСостояний = ОбщегоНазначения.ВыгрузитьКолонку(СостоянияЛидов, "Ссылка");
	ТаблицаКонтактов = КонтактнаяИнформацияЛиц(ЛидыСостояний);
	Если Параметры.ИспользоватьМетки Тогда
		ТаблицаМеток = РаботаСМетками.МеткиВладельцев(ЛидыСостояний, Параметры.ИмяВладельцаМеток);
	КонецЕсли;
	
	ШрифтНаименования = Новый Шрифт(, , Истина);
	ШрифтВторостепенногоЭлемента = Новый Шрифт( , , , , , , 80);
	
	ВысотаЭлементаКанбан = ВысотаЭлементаКанбан(Параметры.ИспользоватьМетки);
	
	Для Каждого Лид Из СостоянияЛидов Цикл
		
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				Лид.Наименование,
				ШрифтНаименования));
		
		ВысотаЭлемента = 2;
		
		КонтактыЛида = ТаблицаКонтактов.НайтиСтроки(Новый Структура("КонтактноеЛицо", Лид.Ссылка));
		Если КонтактыЛида.Количество() > 0 Тогда
			Если ЗначениеЗаполнено(КонтактыЛида[0].Телефон) Тогда
				МассивСтрок.Добавить(Символы.ПС);
				МассивСтрок.Добавить(КонтактыЛида[0].Телефон);
				ВысотаЭлемента = ВысотаЭлемента + 1;
			КонецЕсли;
			Если ЗначениеЗаполнено(КонтактыЛида[0].Email) Тогда
				МассивСтрок.Добавить(Символы.ПС);
				МассивСтрок.Добавить(КонтактыЛида[0].Email);
				ВысотаЭлемента = ВысотаЭлемента + 1;
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.ИспользоватьМетки Тогда
			МеткиЛида = ТаблицаМеток.НайтиСтроки(Новый Структура("Владелец", Лид.Ссылка));
			Если МеткиЛида.Количество() > 0 Тогда
				МассивСтрок.Добавить(Символы.ПС);
				ПерваяМетка = Истина;
				Для Каждого СвойстваМетки Из МеткиЛида Цикл
					Если Не ПерваяМетка Тогда
						МассивСтрок.Добавить(", ");
					КонецЕсли;
					МассивСтрок.Добавить(
						Новый ФорматированнаяСтрока(
							СвойстваМетки.НаименованиеМетки,
							ШрифтВторостепенногоЭлемента,
							,
							РаботаСМетками.ЭлементСтиляПоЦвету(СвойстваМетки.ЦветМетки)));
					ПерваяМетка = Ложь;
				КонецЦикла;
				ВысотаЭлемента = ВысотаЭлемента + 1;
			КонецЕсли;
		КонецЕсли;
		
		ДатаИзмененияСтрокой = ОтносительнаяДатаИзмененияСостояния(Лид.ДатаИзменения);
		Если ЗначениеЗаполнено(ДатаИзмененияСтрокой) Тогда
			МассивСтрок.Добавить(Символы.ПС);
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					ДатаИзмененияСтрокой,
					ШрифтВторостепенногоЭлемента,
					ЦветаСтиля.ДосьеВторостепеннаяНадписьЦвет));
			ВысотаЭлемента = ВысотаЭлемента + 1;
		КонецЕсли;
		
		Пока ВысотаЭлемента < ВысотаЭлементаКанбан Цикл
			МассивСтрок.Добавить(Символы.ПС);
			МассивСтрок.Добавить(" ");
			ВысотаЭлемента = ВысотаЭлемента + 1;
		КонецЦикла;
		
		ЭлементКанбан = ДанныеКанбан.Добавить();
		ЭлементКанбан.Ссылка = Лид.Ссылка;
		ЭлементКанбан.Представление = Новый ФорматированнаяСтрока(МассивСтрок);
		ЭлементКанбан.ПорядокКанбан = Лид.ПорядокКанбан;
		ЭлементКанбан.Состояние = Лид.Состояние;
	КонецЦикла;
	
	Возврат ДанныеКанбан;
	
КонецФункции

Функция СостоянияКанбан(Ответственный = Неопределено) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПорядокРаботыСЛидами.Состояние КАК Ссылка,
	|	СостоянияРаботыСЛидами.Наименование КАК Наименование,
	|	СостоянияРаботыСЛидами.ЦветСостояния КАК ЦветСостояния,
	|	СостоянияРаботыСЛидами.РеквизитДопУпорядочивания КАК Порядок,
	|	КОЛИЧЕСТВО(КонтактныеЛица.Ссылка) КАК КоличествоЭлементовКанбан
	|ИЗ
	|	РегистрСведений.ПорядокРаботыСЛидами КАК ПорядокРаботыСЛидами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
	|		ПО ПорядокРаботыСЛидами.Лид = КонтактныеЛица.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СостоянияРаботыСЛидами КАК СостоянияРаботыСЛидами
	|		ПО ПорядокРаботыСЛидами.Состояние = СостоянияРаботыСЛидами.Ссылка
	|ГДЕ
	|	НЕ КонтактныеЛица.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ПорядокРаботыСЛидами.Состояние,
	|	СостоянияРаботыСЛидами.Наименование,
	|	СостоянияРаботыСЛидами.ЦветСостояния,
	|	СостоянияРаботыСЛидами.РеквизитДопУпорядочивания";
	
	Запрос = Новый Запрос;
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Если Ответственный <> Неопределено Тогда
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("КонтактныеЛица.Ответственный = &Ответственный");
		Запрос.УстановитьПараметр("Ответственный", Ответственный);
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ОтносительнаяДатаИзмененияСостояния(ДатаСостояния)
	
	Результат = "";
	
	Если Не ЗначениеЗаполнено(ДатаСостояния) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ДатаТекущая = ТекущаяДатаСеанса();
	
	Интервал = ДатаТекущая - ДатаСостояния;
	
	Если Интервал < 0 Тогда
		Результат = Формат(ДатаСостояния, "ДФ=dd.MM.yyyy");
	ИначеЕсли Интервал < 60 * 5 Тогда
		Результат = НСтр("ru = 'Сейчас'");
	ИначеЕсли Интервал < 60 * 15 Тогда
		Результат = НСтр("ru = '5 минут назад'");
	ИначеЕсли Интервал < 60 * 30 Тогда
		Результат = НСтр("ru = '15 минут назад'");
	ИначеЕсли Интервал < 60 * 60 * 1 Тогда
		Результат = НСтр("ru = '30 минут назад'");
	ИначеЕсли Интервал < 60 * 60 * 2 Тогда
		Результат = НСтр("ru = '1 час назад'");
	ИначеЕсли Интервал < 60 * 60 * 3 Тогда
		Результат = НСтр("ru = '2 часа назад'");
	Иначе
		СекундВДне = 24 * 60 * 60;
		РазницаВДнях = Цел((НачалоДня(ДатаТекущая) - НачалоДня(ДатаСостояния))/ СекундВДне);
		
		Если РазницаВДнях = 0 Тогда
			Результат = СтрШаблон(НСтр("ru = 'Сегодня, %1'"), Формат(ДатаСостояния, "ДФ=HH:mm"));
		ИначеЕсли РазницаВДнях = 1 Тогда
			Результат = СтрШаблон(НСтр("ru = 'Вчера, %1'"), Формат(ДатаСостояния, "ДФ=HH:mm"));
		ИначеЕсли РазницаВДнях = 2 Тогда
			Результат = СтрШаблон(НСтр("ru = 'Позавчера, %1'"), Формат(ДатаСостояния, "ДФ=HH:mm"));
		Иначе
			Результат = Формат(ДатаСостояния, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПрокруткаКанбанНазад()
	
	Возврат "Назад";
	
КонецФункции

Функция ПрокруткаКанбанВперед()
	
	Возврат "Вперед";
	
КонецФункции

Функция ПрокруткаКанбанВКонец()
	
	Возврат "ВКонец";
	
КонецФункции

Функция ВысотаЭлементаКанбан(ИспользоватьМетки = Ложь) Экспорт
	
	Если ИспользоватьМетки Тогда
		Возврат 6;
	Иначе
		Возврат 5;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
