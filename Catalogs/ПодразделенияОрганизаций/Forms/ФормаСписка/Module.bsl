////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	ОсновноеПодразделение = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации");
	
	Если ОтборОрганизация.Пустая() Тогда
		ОтборОрганизация = ОсновнаяОрганизация;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		ЕстьОтборПоВладельцу = Истина;
		ОтборОрганизация = Параметры.Отбор.Владелец;
	Иначе
		ЕстьОтборПоВладельцу = Ложь;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	КонецЕсли;
	
	Элементы.ГруппаОтборОрганизация.Видимость = Не ЕстьОтборПоВладельцу;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновноеПодразделение", ОсновноеПодразделение);
	
	ОрганизацияЯвляетсяЮрЛицом = НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборОрганизация, "ЮридическоеФизическоеЛицо") 
		= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.ПодразделенияОрганизаций);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	ЭлементыФормБП.НастроитьОтборСпискаПриСозданииНаСервере(ЭтотОбъект, "Расформировано");
	
	УстановитьУсловноеОформление();
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборПоОрганизации();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = ЭтаФорма Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" И НЕ ЕстьОтборПоВладельцу Тогда
		
		ОтборОрганизация = Параметр;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		УстановитьОтборПоОрганизации();
		
		ОсновнаяОрганизация = Параметр;
		
		ОрганизацияЯвляетсяЮрЛицом	= ОрганизацияЯвляетсяЮрЛицом(ОтборОрганизация);
	
		УправлениеФормойКлиент();
		
	ИначеЕсли ИмяСобытия = "ИзменениеОсновногоПодразделенияОрганизации" Тогда
		
		ОсновноеПодразделение = Параметр;
		Список.Параметры.УстановитьЗначениеПараметра("ОсновноеПодразделение", ОсновноеПодразделение);
		
		УправлениеФормойКлиент();
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	УстановитьОтборПоОрганизации();

КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизацияИспользование = Истина;
	КонецЕсли;
	УстановитьОтборПоОрганизации();
	
	ОрганизацияЯвляетсяЮрЛицом	= ОрганизацияЯвляетсяЮрЛицом(ОтборОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Ссылка = ОсновноеПодразделение Тогда
		ОсновноеПодразделение = Неопределено;
	Иначе
		ОсновноеПодразделение = ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	ВремОсновнаяОрганизация	= ОсновнаяОрганизация;
	
	УстановитьОсновноеПодразделение(ОсновнаяОрганизация, ОсновноеПодразделение);
	
	Если НЕ ВремОсновнаяОрганизация	= ОсновнаяОрганизация Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Установлена основная организация:'"),
			ПолучитьНавигационнуюСсылку(ОсновнаяОрганизация), ОсновнаяОрганизация);
		Оповестить("ИзменениеОсновнойОрганизации", ОсновнаяОрганизация);
	КонецЕсли;
	
	Оповестить("ИзменениеОсновногоПодразделенияОрганизации", ОсновноеПодразделение);
	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновноеПодразделение", ОсновноеПодразделение);
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьВсе(Команда)
	
	ОтобратьПодразделения(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьДействующие(Команда)
	
	ОтобратьПодразделения(Команда.Имя);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УстановитьОтборПоОрганизации()
	
	Если ЕстьОтборПоВладельцу Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтборОрганизацияИспользование Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
			"Владелец",
			ОтборОрганизация,
			,
			,
			ОтборОрганизацияИспользование);
	Иначе
		СписокОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Владелец", СписокОрганизаций, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// КПП
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КПП");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОрганизацияЯвляетсяЮрЛицом", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	ТекущиеДанные = ОбщегоНазначенияБПКлиент.ТекущиеДанныеДинамическогоСписка(Элементы.Список);
	Если ТекущиеДанные <> Неопределено Тогда
		Элементы.ФормаИспользоватьОсновным.Пометка = ТекущиеДанные.Ссылка = ОсновноеПодразделение;
	КонецЕсли;
	Элементы.ФормаИспользоватьОсновным.Доступность = ТекущиеДанные <> Неопределено И Не ТекущиеДанные.ВладелецВАрхиве;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьОсновноеПодразделение(ОсновнаяОрганизация, ОсновноеПодразделение)

	Если ЗначениеЗаполнено(ОсновноеПодразделение) Тогда
		
		ОсновнаяОрганизация				= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ОрганизацияПодразделения		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновноеПодразделение, "Владелец");
		
		Если НЕ ОсновнаяОрганизация = ОрганизацияПодразделения Тогда
			ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнаяОрганизация",	ОрганизацияПодразделения);
			ОсновнаяОрганизация	= ОрганизацияПодразделения;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации",	ОсновноеПодразделение);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПодразделения(ИмяКоманды)
	
	Если ИмяКоманды = АктивныйОтборСтрок Тогда
		Возврат;
	Иначе
		АктивныйОтборСтрок = ИмяКоманды;
	КонецЕсли;
	ОтобратьПодразделенияНаСервере(ИмяКоманды);
	
КонецПроцедуры

&НаСервере
Процедура ОтобратьПодразделенияНаСервере(ИмяКоманды)
	
	ЭлементыФормБП.ОтобратьСтрокиНаСервере(ЭтотОбъект, ИмяКоманды,,,"Расформировано");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

&НаСервереБезКонтекста
Функция ОрганизацияЯвляетсяЮрЛицом(Организация)
	
	Возврат НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") 
		= ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

