#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		ОтборОрганизация = Параметры.Отбор.Организация;
		ЭлементОтбораОрганизация = ОтборыСписковКлиентСервер.ЭлементОтбораСпискаПоИмени(Список, "Организация");
		Если ЭлементОтбораОрганизация <> Неопределено Тогда
			ЭлементОтбораОрганизация.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли;
		Элементы.ОтборОрганизация.Видимость = Истина;
	Иначе
		ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтотОбъект);
		Элементы.ОтборОрганизация.Видимость = Ложь;
	КонецЕсли;
	
	РежимВыбора = (Параметры.РежимВыбора = Истина);	
	Элементы.Список.РежимВыбора = РежимВыбора;
	Элементы.Список.МножественныйВыбор = (Параметры.МножественныйВыбор = Истина);
	Элементы.СоздатьРегистрация.Видимость = Не РежимВыбора;
	Элементы.СоздатьСнятиеСРегистрации.Видимость = Не РежимВыбора;
	
	РежимСверки = Ложь;
	Если Параметры.Свойство("РежимСверки") Тогда
		РежимСверки = Параметры.РежимСверки;
	КонецЕсли;
	
	Элементы.Выбрать.Видимость = РежимВыбора;
	Элементы.ОбъектаНетВСписке.Видимость = РежимВыбора И РежимСверки;
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	ПравоРедактированияРегистраций = ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы,
					"СоздатьРегистрация",
					"Видимость",
					ПравоРедактированияРегистраций);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы,
					"СоздатьСнятиеСРегистрации",
					"Видимость",
					ПравоРедактированияРегистраций);
					
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаИнформацияОС" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьРегистрация(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		
		ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
		ЗначенияЗаполнения.Вставить("ВидЗаписи", ПредопределенноеЗначение("Перечисление.ВидЗаписиОРегистрации.Регистрация"));
		ЗначенияЗаполнения.Вставить("ОсновноеСредство", ТекущиеДанные.Ссылка);
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("РегистрСведений.РегистрацияТранспортныхСредств.ФормаЗаписи", СтруктураПараметров, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСнятиеСРегистрации(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		
		ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
		ЗначенияЗаполнения.Вставить("ВидЗаписи", ПредопределенноеЗначение("Перечисление.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета"));
		ЗначенияЗаполнения.Вставить("ОсновноеСредство", ТекущиеДанные.Ссылка);
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("РегистрСведений.РегистрацияТранспортныхСредств.ФормаЗаписи", СтруктураПараметров, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		ВыбранноеЗначение = Новый Структура;
		ВыбранноеЗначение.Вставить("ОсновноеСредство", ТекущиеДанные.Ссылка);
		ВыбранноеЗначение.Вставить("Наименование", ТекущиеДанные.Наименование);
		ВыбранноеЗначение.Вставить("РегистрационныйЗнак", ТекущиеДанные.РегистрационныйЗнак);
		ВыбранноеЗначение.Вставить("ВидТС", ТекущиеДанные.ВидТС);
		ВыбранноеЗначение.Вставить("Поставлено", ТекущиеДанные.Поставлено);
		ВыбранноеЗначение.Вставить("Снято", ТекущиеДанные.Снято);
		ОповеститьОВыборе(ВыбранноеЗначение);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбъектаНетВСписке(Команда)
	ОповеститьОВыборе(Новый Структура);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Значение = Неопределено Или Не РежимСверки Тогда
		// Стандартная обработка
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ВыбранноеЗначение = Новый Структура;
	ВыбранноеЗначение.Вставить("ОсновноеСредство", ТекущиеДанные.Ссылка);
	ВыбранноеЗначение.Вставить("Наименование", ТекущиеДанные.Наименование);
	ВыбранноеЗначение.Вставить("РегистрационныйЗнак", ТекущиеДанные.РегистрационныйЗнак);
	ВыбранноеЗначение.Вставить("ВидТС", ТекущиеДанные.ВидТС);
	ВыбранноеЗначение.Вставить("Поставлено", ТекущиеДанные.Поставлено);
	ВыбранноеЗначение.Вставить("Снято", ТекущиеДанные.Снято);
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Элементы.ГруппаШапка);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	//Выбор доступен всегда
	КомандыПоля.РазрешеноИзменение = Ложь;
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКПерваяКомандаПанели(ЭтотОбъект,
		Элементы.ОбъектаНетВСписке);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()

	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.ОбъектаНетВСписке,
		Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.СоздатьРегистрация,
		Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.СоздатьСнятиеСРегистрации,
		Элементы.Список.КонтекстноеМеню);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормыСправочника(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.РегистрационныйЗнак);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Наименование);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Поставлено, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Снято, 2);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
	Элементы.Поставлено.Формат = "ДФ='''Поставлено:'' dd.MM.yyyy'";
	Элементы.Снято.Формат = "ДФ='''Снято:'' dd.MM.yyyy'";
	
КонецПроцедуры

#КонецОбласти
