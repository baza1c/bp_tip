#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область БСП

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает список реквизитов, которые не нужно редактировать
// с помощью обработки группового изменения объектов
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("УдалитьАдресМестонахождения");
	Возврат МассивРеквизитов;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ПроставитьТипОСПриОбновлении() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойТип", Перечисления.ТипыОС.ПустаяСсылка());
	Запрос.Текст =  "ВЫБРАТЬ
		|	ОсновныеСредства.Ссылка КАК Ссылка,
		|	ОсновныеСредства.ГруппаОС КАК ГруппаОС
		|ИЗ
		|	Справочник.ОсновныеСредства КАК ОсновныеСредства
		|ГДЕ
		|	ОсновныеСредства.ТипОС = &ПустойТип
		|	И НЕ ОсновныеСредства.Предопределенный
		|	И НЕ ОсновныеСредства.ЭтоГруппа";
	
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из Результат Цикл
		
		ОСОбъект = Строка.Ссылка.ПолучитьОбъект();
		
		Если Строка.ГруппаОС = Перечисления.ГруппыОС.КапитальныеВложенияВАрендованноеИмущество Тогда 
			ОСОбъект.ТипОС = Перечисления.ТипыОС.КапитальноеВложение;
		Иначе
			ОСОбъект.ТипОС = Перечисления.ТипыОС.ОбъектОС;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОСОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПризнакНедвижимоеИмущество() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НедвижимоеИмущество = Новый Массив;
	НедвижимоеИмущество.Добавить(Перечисления.ГруппыОС.Здания);
	НедвижимоеИмущество.Добавить(Перечисления.ГруппыОС.Сооружения);
	НедвижимоеИмущество.Добавить(Перечисления.ГруппыОС.МноголетниеНасаждения);
	НедвижимоеИмущество.Добавить(Перечисления.ГруппыОС.ЗемельныеУчастки);
	НедвижимоеИмущество.Добавить(Перечисления.ГруппыОС.ОбъектыПриродопользования);
	НедвижимоеИмущество.Добавить(Перечисления.ГруппыОС.ПрочееИмуществоТребующееГосударственнойРегистрации);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НедвижимоеИмущество", НедвижимоеИмущество);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК Ссылка,
	|	ОсновныеСредства.ГруппаОС КАК ГруппаОС
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.ГруппаОС В(&НедвижимоеИмущество)
	|	И НЕ ОсновныеСредства.НедвижимоеИмущество
	|	И НЕ ОсновныеСредства.ЭтоГруппа";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из Результат Цикл
		
		Объект = Строка.Ссылка.ПолучитьОбъект();
		Объект.НедвижимоеИмущество = Истина;
		
		Если Строка.ГруппаОС = Перечисления.ГруппыОС.ПрочееИмуществоТребующееГосударственнойРегистрации Тогда
			Объект.ГруппаОС = Перечисления.ГруппыОС.ДругиеВидыОсновныхСредств;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПустуюЕдиницуУчета(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ОсновныеСредства.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ПустаяСсылка)
	|	И НЕ ОсновныеСредства.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
		НачатьТранзакцию();
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ОсновныеСредства");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		Блокировка.Заблокировать();
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если СправочникОбъект = Неопределено ИЛИ ЗначениеЗаполнено(СправочникОбъект.ЕдиницаУчета) Тогда
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект.ЕдиницаУчета = Перечисления.ЕдиницыУчетаОС.ИнвентарныйОбъект;
		
		Попытка
			// Запись обработанного объекта.
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо объект, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре Справочники.ОсновныеСредства.ЗаполнитьПустуюЕдиницуУчета() не удалось обработать элемент по причине:
					|%1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Справочники.ОсновныеСредства, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки; 
		
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре Справочники.ОсновныеСредства.ЗаполнитьПустуюЕдиницуУчета() не удалось обработать все элементы: в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Справочники.ОсновныеСредства, ,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Справочники.ОсновныеСредства.ЗаполнитьПустуюЕдиницуУчета() обработала очередную порцию: %1 элементов'"), 
					ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанныхИзФайла

// Переопределяет при загрузке данных в табличную часть алгоритм сопоставления содержимого загружаемого файла с элементами справочника "Основные средства".
// Используется только при прикладной загрузке данных. Т.е. в случае, когда сопоставление выполняется не только по реквизитам справочника.
// При стандартной загрузке сопоставление данных выполняет процедура СопоставитьЗагружаемыеДанные() в
// модуле менеджера обработки ЗагрузкаДанныхВТабличнуюЧасть.
//
// Параметры:
//  ЗагружаемыеДанные - ТаблицаЗначений - Загружаемые данные из файла.
//  ИмяРеквизитаДляСопоставления - Строка - Имя реквизита, по которому необходимо выполнять сопоставление данных.
//  Параметры - Структура - Параметры прикладной загрузки (см. ЗагрузкаДанныхИзВнешнихФайловКлиент.НовыйПараметрыПрикладнойЗагрузки()).
//
Процедура СопоставитьЗагружаемыеДанныеИзФайла(ЗагружаемыеДанные, ИмяРеквизитаДляСопоставления, Параметры) Экспорт
	
	ДанныеКолонки = ЗагружаемыеДанные.Скопировать(, "СтрокаИзФайла");
	
	ДанныеКолонки.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	НомерСтроки = 0;
	
	Для Каждого СтрокаДанных Из ДанныеКолонки Цикл
		НомерСтроки = НомерСтроки + 1;
		СтрокаДанных.НомерСтроки = НомерСтроки;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ДанныеКолонки", ДанныеКолонки);
	
	// Инвентарный номер не является реквизитом справочника.
	// Сопоставляем по данным из регистра сведений "Первоначальные сведения ОС".
	Если ВРЕГ(ИмяРеквизитаДляСопоставления) = "ИНВЕНТАРНЫЙНОМЕР" Тогда
		
		Запрос.Параметры.Вставить("Период", Параметры.Период);
		Запрос.Параметры.Вставить("Организация", Параметры.Организация);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеКолонки.СтрокаИзФайла КАК СтрокаИзФайла,
		|	ДанныеКолонки.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	&ДанныеКолонки КАК ДанныеКолонки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПервоначальныеСведенияОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПервоначальныеСведенияОС.ОсновноеСредство КАК Ссылка
		|ПОМЕСТИТЬ ДанныеБухгалтерскогоУчета
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И ИнвентарныйНомер В
		|					(ВЫБРАТЬ
		|						ИсходныеДанные.СтрокаИзФайла
		|					ИЗ
		|						ИсходныеДанные)) КАК ПервоначальныеСведенияОС
		|
		|ИНДЕКСИРОВАТЬ ПО ПервоначальныеСведенияОС.ИнвентарныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсходныеДанные.СтрокаИзФайла КАК СтрокаИзФайла,
		|	ИсходныеДанные.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ИсходныеДанные.СтрокаИзФайла = """"
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|		ИНАЧЕ ЕСТЬNULL(ДанныеБухгалтерскогоУчета.Ссылка, ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))
		|	КОНЕЦ КАК СсылкаНайденая
		|ИЗ
		|	ИсходныеДанные КАК ИсходныеДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеБухгалтерскогоУчета КАК ДанныеБухгалтерскогоУчета
		|		ПО ИсходныеДанные.СтрокаИзФайла = ДанныеБухгалтерскогоУчета.ИнвентарныйНомер";
		
	Иначе
		// Выполняем поиск по реквизитам справочника.
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеКолонки.СтрокаИзФайла КАК СтрокаИзФайла,
		|	ДанныеКолонки.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	&ДанныеКолонки КАК ДанныеКолонки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&РеквизитДляПоиска КАК РеквизитДляСопоставления,
		|	ОсновныеСредства.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДанныеСправочника
		|ИЗ
		|	Справочник.ОсновныеСредства КАК ОсновныеСредства
		|ГДЕ
		|	&РеквизитДляПоиска В
		|			(ВЫБРАТЬ
		|				ИсходныеДанные.СтрокаИзФайла
		|			ИЗ
		|				ИсходныеДанные)
		|
		|ИНДЕКСИРОВАТЬ ПО &РеквизитДляПоиска
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсходныеДанные.СтрокаИзФайла КАК СтрокаИзФайла,
		|	ИсходныеДанные.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ИсходныеДанные.СтрокаИзФайла = """"
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|		ИНАЧЕ ЕСТЬNULL(ДанныеСправочника.Ссылка, ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))
		|	КОНЕЦ КАК СсылкаНайденая
		|ИЗ
		|	ИсходныеДанные КАК ИсходныеДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеСправочника КАК ДанныеСправочника
		|		ПО ИсходныеДанные.СтрокаИзФайла = ДанныеСправочника.РеквизитДляСопоставления";
		
	КонецЕсли;
	
	// Если данных меньше 1000 записей, то уберем индексацию временной таблицы.
	Если ЗагружаемыеДанные.Количество() < 1000 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИНДЕКСИРОВАТЬ ПО &РеквизитДляПоиска", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИНДЕКСИРОВАТЬ ПО ПервоначальныеСведенияОС.ИнвентарныйНомер", "");
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РеквизитДляПоиска", "ОсновныеСредства." + ИмяРеквизитаДляСопоставления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗагружаемыеДанные[Выборка.НомерСтроки - 1].СсылкаНайденая = Выборка.СсылкаНайденая;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Инвентарная карточка ОС (ОС-6)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС6";
	КомандаПечати.Представление = НСтр("ru = 'Инвентарная карточка ОС (ОС-6)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиКарточкиОС";
	
КонецПроцедуры

// Функция формирует табличный документ с печатной формой инвентарной карточки ОС (форма ОС-6)
// Утверждена постановлением Госкомстата России от 21.01.2003 № 7
// Возвращаемое значение:
//  Табличный документ - печатная форма инвентарной карточки ОС
Функция ПечатьОС6_2003(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб			= Истина;
	ТабДок.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДок.ПолеСнизу			= 0;
	ТабДок.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ОсновныеСредства_ОС6";
	СисИнфо = Новый СистемнаяИнформация;
	Если ПустаяСтрока(СисИнфо.ИнформацияПрограммыПросмотра) Тогда 
		ТабДок.ПолеСверху          = 0;
	Иначе
		ТабДок.ПолеСверху          = 10;
	КонецЕсли;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.ОсновныеСредства.ПФ_MXL_ОС6");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ШапкаРазделов1и2  = Макет.ПолучитьОбласть("ШапкаРазделов1и2");
	ШапкаРаздела3     = Макет.ПолучитьОбласть("ШапкаРаздела3");
	СтрокиРаздела3    = Макет.ПолучитьОбласть("СтрокиРаздела3");
	ШапкаРаздела4     = Макет.ПолучитьОбласть("ШапкаРаздела4");
	СтрокаРаздела4    = Макет.ПолучитьОбласть("СтрокаРаздела4");
	ПодвалСтраницы1   = Макет.ПолучитьОбласть("ПодвалСтраницы1");
	ШапкаРазделов5и6  = Макет.ПолучитьОбласть("ШапкаРазделов5и6");
	СтрокаРазделов5и6 = Макет.ПолучитьОбласть("СтрокаРазделов5и6");
	ШапкаРаздела7_1   = Макет.ПолучитьОбласть("ШапкаРаздела7_1");
	ШапкаРаздела7_2   = Макет.ПолучитьОбласть("ШапкаРаздела7_2");
	ПодвалСтраницы2   = Макет.ПолучитьОбласть("ПодвалСтраницы2");
	
	// Карточка выводится на дату заданную в форме элемента, при печати из других мест используем рабочую дату пользователя.
	Если ПараметрыПечати.Свойство("ДатаСведений") Тогда
		ДатаСведений = ПараметрыПечати.ДатаСведений;
	Иначе
		ДатаСведений = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	ТаблицаОбъектов = Новый ТаблицаЗначений;
	ТаблицаОбъектов.Колонки.Добавить("ОС", Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ТаблицаОбъектов.Колонки.Добавить("Номер", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	НомерСтроки = 1;
	Для каждого ОС Из МассивОбъектов Цикл
		СтрокаТаблицы = ТаблицаОбъектов.Добавить();
		СтрокаТаблицы.ОС = ОС;
		СтрокаТаблицы.Номер = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОбъектов", ТаблицаОбъектов);
	Запрос.УстановитьПараметр("ДатаСведений", ДатаСведений);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОС.ОС КАК ОС,
	|	ТаблицаОС.Номер КАК Порядок
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	&ТаблицаОбъектов КАК ТаблицаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СведенияПоОрганизации.ОсновноеСредство КАК ОС,
	|	СведенияПоОрганизации.Организация КАК Организация
	|ПОМЕСТИТЬ СведенияПоОрганизации
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(&ДатаСведений, ОсновноеСредство В (&МассивОбъектов)) КАК СведенияПоОрганизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновныеСредства.Ссылка КАК Ссылка,
	|	ОсновныеСредства.ЕдиницаУчета КАК ЕдиницаУчета,
	|	ОсновныеСредства.Код КАК Код,
	|	ОсновныеСредства.Наименование КАК Наименование,
	|	ОсновныеСредства.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ОсновныеСредства.ГруппаОС КАК ГруппаОС,
	|	ОсновныеСредства.ДатаВыпуска КАК ДатаВыпуска,
	|	ОсновныеСредства.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ОсновныеСредства.Изготовитель КАК Изготовитель,
	|	ОсновныеСредства.КодПоОКОФ КАК КодПоОКОФСсылка,
	|	ОсновныеСредства.КодПоОКОФ.Код КАК КодПоОКОФ,
	|	ОсновныеСредства.НаименованиеПолное КАК НаименованиеПолное,
	|	ОсновныеСредства.НомерПаспорта КАК НомерПаспорта,
	|	ОсновныеСредства.ШифрПоЕНАОФ КАК ШифрПоЕНАОФ,
	|	ЕСТЬNULL(СведенияПоОрганизации.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(СведенияПоОрганизации.Организация.КодПоОКПО, """") КАК КодПоОКПО,
	|	ТаблицаОС.Порядок КАК Порядок
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ПО ТаблицаОС.ОС = ОсновныеСредства.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПоОрганизации КАК СведенияПоОрганизации
	|		ПО ТаблицаОС.ОС = СведенияПоОрганизации.ОС
	|ГДЕ
	|	ОсновныеСредства.ЭтоГруппа = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	РезультатЗапрос = Запрос.Выполнить();
	
	Если РезультатЗапрос.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Печатная форма № ОС-6 может быть сформирована только для элементов справочника.'"));
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаОбъектов = РезультатЗапрос.Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаОбъектов.Следующий() Цикл
		
		ТекстСообщения = "";
		Если ВыборкаОбъектов.ЕдиницаУчета = Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
			ТекстШаблона = НСтр("ru = 'Печатная форма № ОС-6 не формируется для групповых объектов основных средств.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстШаблона, Строка(ВыборкаОбъектов.Ссылка));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ВыборкаОбъектов.Ссылка);
			Продолжить;
		КонецЕсли;
		
		//Последние сведения об ОС
		ЗапросСведенияОС = Новый Запрос();
		ЗапросСведенияОС.УстановитьПараметр("Организация", ВыборкаОбъектов.Организация);
		ЗапросСведенияОС.УстановитьПараметр("ДатаСведений", ДатаСведений);
		ЗапросСведенияОС.УстановитьПараметр("ОсновноеСредство", ВыборкаОбъектов.Ссылка);
		ЗапросСведенияОС.УстановитьПараметр("СубконтоОС", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
		ЗапросСведенияОС.Текст = 
		"ВЫБРАТЬ
		|	ПервоначальныеСведенияОС.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПервоначальныеСведенияОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ЕСТЬNULL(ПараметрыАмортизацииОС.СтоимостьДляВычисленияАмортизации, 0) КАК СтоимостьДляВычисленияАмортизации,
		|	ЕСТЬNULL(ПараметрыАмортизацииОС.ОбъемПродукцииРаботДляВычисленияАмортизации, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ЕСТЬNULL(ПараметрыАмортизацииОС.КоэффициентАмортизации, 1) КАК КоэффициентАмортизации,
		|	ЕСТЬNULL(ПараметрыАмортизацииОС.КоэффициентУскорения, 1) КАК КоэффициентУскорения,
		|	ЕСТЬNULL(ПараметрыАмортизацииОС.Период, &ДатаСведений) КАК Период,
		|	ЕСТЬNULL(ПараметрыАмортизацииОС.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
		|	СчетаБухгалтерскогоУчета.СчетУчета КАК СчетУчета,
		|	СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|	МестонахождениеОС.Местонахождение КАК Местонахождение,
		|	МестонахождениеОС.МОЛ КАК МОЛ,
		|	МестонахождениеОС.Местонахождение.Наименование КАК МестонахождениеНаименование
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|			&ДатаСведений,
		|			Организация = &Организация
		|				И ОсновноеСредство = &ОсновноеСредство) КАК ПервоначальныеСведенияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
		|				&ДатаСведений,
		|				Организация = &Организация
		|					И ОсновноеСредство = &ОсновноеСредство) КАК ПараметрыАмортизацииОС
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ПараметрыАмортизацииОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|				&ДатаСведений,
		|				Организация = &Организация
		|					И ОсновноеСредство = &ОсновноеСредство) КАК СчетаБухгалтерскогоУчета
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = СчетаБухгалтерскогоУчета.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
		|				&ДатаСведений,
		|				Организация = &Организация
		|					И ОсновноеСредство = &ОсновноеСредство) КАК МестонахождениеОС
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство";
		ТекущиеСведенияОС = ЗапросСведенияОС.Выполнить().Выбрать();
		ТекущиеСведенияОС.Следующий();
		
		Если НЕ ПервыйДокумент Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
			
		СчетУчетаСтоимостиБУ = ТекущиеСведенияОС.СчетУчета;
		СрокИспользования   = ТекущиеСведенияОС.СрокПолезногоИспользования;
		
		Отбор  = Новый Структура( "ОсновноеСредство", ВыборкаОбъектов.Ссылка);
		Подразделение  = ТекущиеСведенияОС.МестонахождениеНаименование;
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаОбъектов.Организация, ДатаСведений);
		
		Шапка.Параметры.Организация       = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
		Шапка.Параметры.КодПоОКПО         = ВыборкаОбъектов.КодПоОКПО;
		Шапка.Параметры.Подразделение     = Подразделение;
		Шапка.Параметры.НаименованиеОС     = ?(НЕ ЗначениеЗаполнено(ВыборкаОбъектов.НаименованиеПолное),
			ВыборкаОбъектов.Наименование, ВыборкаОбъектов.НаименованиеПолное);
		
		Шапка.Параметры.НомерДок          = ТекущиеСведенияОС.ИнвентарныйНомер;
		Шапка.Параметры.ДатаДок           = Формат(ДатаСведений,"ДФ=dd.MM.yyyy");
		Шапка.Параметры.МестоНахождениеОС = Подразделение;
		Шапка.Параметры.ИзготовительОС    = ВыборкаОбъектов.Изготовитель;
		Шапка.Параметры.КодПоОКОФ         = ВыборкаОбъектов.КодПоОКОФ;
		Шапка.Параметры.НомерГруппы       = ВыборкаОбъектов.АмортизационнаяГруппа;
		Шапка.Параметры.НомерПаспорта     = ВыборкаОбъектов.НомерПаспорта;
		Шапка.Параметры.ЗаводскойНомер    = ВыборкаОбъектов.ЗаводскойНомер;
		Шапка.Параметры.ИнвентарныйНомер  = ТекущиеСведенияОС.ИнвентарныйНомер;
		Шапка.Параметры.СубСчет           = Строка(СчетУчетаСтоимостиБУ);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостоянияОСОрганизаций.Состояние,
		|	СостоянияОСОрганизаций.ДатаСостояния,
		|	СобытияОСОрганизаций.НазваниеДокумента,
		|	СобытияОСОрганизаций.НомерДокумента,
		|	СобытияОСОрганизаций.Событие,
		|	СостоянияОСОрганизаций.Регистратор
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
		|		ПО СобытияОСОрганизаций.Период = СостоянияОСОрганизаций.ДатаСостояния
		|			И СобытияОСОрганизаций.Регистратор = СостоянияОСОрганизаций.Регистратор
		|ГДЕ
		|	СобытияОСОрганизаций.Организация = &Организация
		|	И СостоянияОСОрганизаций.Организация = &Организация
		|	И СобытияОСОрганизаций.ОсновноеСредство = &ОсновноеСредство
		|	И СостоянияОСОрганизаций.ОсновноеСредство = &ОсновноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	NULL,
		|	СобытияОСОрганизацийСрезПоследних.Период,
		|	СобытияОСОрганизацийСрезПоследних.НазваниеДокумента,
		|	СобытияОСОрганизацийСрезПоследних.НомерДокумента,
		|	СобытияОСОрганизацийСрезПоследних.Событие,
		|	СобытияОСОрганизацийСрезПоследних.Регистратор
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций.СрезПоследних(
		|			&ДатаСведений,
		|			ОсновноеСредство = &ОсновноеСредство
		|				И Организация = &Организация
		|				И Событие.ВидСобытияОС В (&МодернизацияИКапРемонт)) КАК СобытияОСОрганизацийСрезПоследних";
		
		Запрос.УстановитьПараметр("Организация", ВыборкаОбъектов.Организация);
		Запрос.УстановитьПараметр("ОсновноеСредство", ВыборкаОбъектов.Ссылка);
		Запрос.УстановитьПараметр("ДатаСведений", ДатаСведений);
		
		ВидыСобытий = Новый СписокЗначений;
		ВидыСобытий.Добавить(Перечисления.ВидыСобытийОС.РаспределениеНДС);
		ВидыСобытий.Добавить(Перечисления.ВидыСобытийОС.Модернизация);
		ВидыСобытий.Добавить(Перечисления.ВидыСобытийОС.Достройка);
		ВидыСобытий.Добавить(Перечисления.ВидыСобытийОС.Дооборудование);
		ВидыСобытий.Добавить(Перечисления.ВидыСобытийОС.КапитальныйРемонт);
		
		Запрос.УстановитьПараметр("МодернизацияИКапРемонт", ВидыСобытий);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ДатаПринятия     = '00010101';
		ДокументПринятия = "";
		
		ДатаВвода     = '00010101';
		ДокументВвода = "";
		ДокументВводаНомер = "";
		
		ДатаСписания       = '00010101';
		ДокументСписания   = "";
		РегистраторСписания = Неопределено;
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.Состояние   = Перечисления.СостоянияОС.ПринятоКУчету Тогда
				 ДатаПринятия        = Выборка.ДатаСостояния;
				 ДокументПринятия    = Выборка.НазваниеДокумента;
			ИначеЕсли Выборка.Состояние = Перечисления.СостоянияОС.СнятоСУчета Тогда
				 ДатаСписания        = Выборка.ДатаСостояния;
				 ДокументСписания    = Выборка.НазваниеДокумента;
				 РегистраторСписания = Выборка.Регистратор;
			Иначе
				ДатаПоследнейМодернизации     = Выборка.ДатаСостояния;
				ДокументПоследнейМодернизации = Выборка.НазваниеДокумента;
			КонецЕсли; 
		КонецЦикла;

		Шапка.Параметры.ДатаВвода    = ДатаПринятия;
		Шапка.Параметры.ДатаСписания = ДатаСписания;

		ТабДок.Вывести(Шапка);

		// Балансовая стоимость ОС на момент поступления и первоначально принятый срок полезного использования
		ШапкаРазделов1и2.Параметры.ПервоначальнаяСтоимость    = ТекущиеСведенияОС.ПервоначальнаяСтоимость;
		ШапкаРазделов1и2.Параметры.СрокПолезногоИспользования = СрокИспользования;

		ТабДок.Вывести(ШапкаРазделов1и2);
		ТабДок.Вывести(ШапкаРаздела3);
		ТабДок.Вывести(СтрокиРаздела3);

		// Сведения о приемке, внутренних перемещениях и выбытии	
		ТабДок.Вывести(ШапкаРаздела4);

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Период",           ДатаСведений);
		Запрос.УстановитьПараметр("Организация",      ВыборкаОбъектов.Организация);
		Запрос.УстановитьПараметр("ОсновноеСредство", ВыборкаОбъектов.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СобытияОСОрганизаций.НазваниеДокумента КАК НазваниеДокумента,
		|	СобытияОСОрганизаций.НомерДокумента КАК НомерДокумента,
		|	ВЫБОР
		|		КОГДА СобытияОСОрганизаций.Событие.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.ПринятиеКУчету)
		|			ТОГДА ЕСТЬNULL(СостоянияОСОрганизаций.ДатаСостояния, СобытияОСОрганизаций.Период)
		|		ИНАЧЕ СобытияОСОрганизаций.Период
		|	КОНЕЦ КАК Период,
		|	ВЫБОР
		|		КОГДА СобытияОСОрганизаций.Регистратор ССЫЛКА Документ.ПередачаОС
		|				И ВЫРАЗИТЬ(СобытияОСОрганизаций.Регистратор КАК Документ.ПередачаОС).ДокПодготовкаКПередачеОС <> ЗНАЧЕНИЕ(Документ.ПодготовкаКПередачеОС.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(СобытияОСОрганизаций.Регистратор КАК Документ.ПередачаОС).ДокПодготовкаКПередачеОС.Дата
		|		ИНАЧЕ СобытияОСОрганизаций.Период
		|	КОНЕЦ КАК ПериодОстатков,
		|	СобытияОСОрганизаций.Событие КАК Событие,
		|	СобытияОСОрганизаций.Событие.ВидСобытияОС КАК ВидСобытияОС,
		|	СобытияОСОрганизаций.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ СобытияОС
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
		|		ПО СобытияОСОрганизаций.ОсновноеСредство = СостоянияОСОрганизаций.ОсновноеСредство
		|			И СобытияОСОрганизаций.Организация = СостоянияОСОрганизаций.Организация
		|			И (СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
		|ГДЕ
		|	СобытияОСОрганизаций.ОсновноеСредство = &ОсновноеСредство
		|	И СобытияОСОрганизаций.Период < &Период
		|	И СобытияОСОрганизаций.Организация = &Организация
		|	И СобытияОСОрганизаций.Событие.ВидСобытияОС В (ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.ПринятиеКУчету), ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.ПринятиеКУчетуСВводомВЭксплуатацию), ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.ВнутреннееПеремещение), ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Списание), ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Передача), ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.ПереводВМалоценноеОборудование))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МестонахождениеОСБухгалтерскийУчет.Период КАК Период,
		|	МестонахождениеОСБухгалтерскийУчет.МОЛ КАК МОЛ,
		|	МестонахождениеОСБухгалтерскийУчет.Местонахождение КАК Местонахождение
		|ПОМЕСТИТЬ МестонахождениеОСБухгалтерскийУчет
		|ИЗ
		|	РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК МестонахождениеОСБухгалтерскийУчет
		|ГДЕ
		|	МестонахождениеОСБухгалтерскийУчет.ОсновноеСредство = &ОсновноеСредство
		|	И МестонахождениеОСБухгалтерскийУчет.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СобытияОС.Период КАК Период,
		|	МАКСИМУМ(МестонахождениеОСБухгалтерскийУчет.Период) КАК ПоследнийПериод
		|ПОМЕСТИТЬ МестонахождениеОСБухгалтерскийУчетСрезПоследнихПериодов
		|ИЗ
		|	СобытияОС КАК СобытияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСБухгалтерскийУчет КАК МестонахождениеОСБухгалтерскийУчет
		|		ПО СобытияОС.Период >= МестонахождениеОСБухгалтерскийУчет.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СобытияОС.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МестонахождениеОСБухгалтерскийУчетСрезПоследнихПериодов.Период КАК Период,
		|	МестонахождениеОСБухгалтерскийУчет.МОЛ КАК МОЛ,
		|	МестонахождениеОСБухгалтерскийУчет.Местонахождение КАК Местонахождение
		|ПОМЕСТИТЬ МестонахождениеОСБухгалтерскийУчетСрезПоследних
		|ИЗ
		|	МестонахождениеОСБухгалтерскийУчетСрезПоследнихПериодов КАК МестонахождениеОСБухгалтерскийУчетСрезПоследнихПериодов
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСБухгалтерскийУчет КАК МестонахождениеОСБухгалтерскийУчет
		|		ПО МестонахождениеОСБухгалтерскийУчетСрезПоследнихПериодов.ПоследнийПериод = МестонахождениеОСБухгалтерскийУчет.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетаБухгалтерскогоУчетаОС.Период КАК Период,
		|	СчетаБухгалтерскогоУчетаОС.СчетУчета КАК СчетУчета,
		|	СчетаБухгалтерскогоУчетаОС.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации
		|ПОМЕСТИТЬ СчетаБухгалтерскогоУчетаОС
		|ИЗ
		|	РегистрСведений.СчетаБухгалтерскогоУчетаОС КАК СчетаБухгалтерскогоУчетаОС
		|ГДЕ
		|	СчетаБухгалтерскогоУчетаОС.ОсновноеСредство = &ОсновноеСредство
		|	И СчетаБухгалтерскогоУчетаОС.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СобытияОС.ПериодОстатков КАК ПериодОстатков,
		|	МАКСИМУМ(СчетаБухгалтерскогоУчетаОС.Период) КАК ПоследнийПериод
		|ПОМЕСТИТЬ СчетаБухгалтерскогоУчетаОССрезПоследнихПериодов
		|ИЗ
		|	СобытияОС КАК СобытияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаБухгалтерскогоУчетаОС КАК СчетаБухгалтерскогоУчетаОС
		|		ПО СобытияОС.ПериодОстатков >= СчетаБухгалтерскогоУчетаОС.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СобытияОС.ПериодОстатков
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетаБухгалтерскогоУчетаОССрезПоследнихПериодов.ПериодОстатков КАК ПериодОстатков,
		|	СчетаБухгалтерскогоУчетаОС.СчетУчета КАК СчетУчета,
		|	СчетаБухгалтерскогоУчетаОС.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации
		|ПОМЕСТИТЬ СчетаБухгалтерскогоУчетаОССрезПоследних
		|ИЗ
		|	СчетаБухгалтерскогоУчетаОССрезПоследнихПериодов КАК СчетаБухгалтерскогоУчетаОССрезПоследнихПериодов
		|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаБухгалтерскогоУчетаОС КАК СчетаБухгалтерскогоУчетаОС
		|		ПО СчетаБухгалтерскогоУчетаОССрезПоследнихПериодов.ПоследнийПериод = СчетаБухгалтерскогоУчетаОС.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.Период КАК Период,
		|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаКонечныйОстатокКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт
		|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОбороты
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			,
		|			&Период,
		|			Регистратор,
		|			,
		|			Счет В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета
		|				ИЗ
		|					СчетаБухгалтерскогоУчетаОССрезПоследних
		|			
		|				ОБЪЕДИНИТЬ
		|			
		|				ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СчетаБухгалтерскогоУчетаОССрезПоследних.СчетНачисленияАмортизации
		|				ИЗ
		|					СчетаБухгалтерскогоУчетаОССрезПоследних),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация
		|				И Субконто1 = &ОсновноеСредство) КАК ХозрасчетныйОстаткиИОбороты
		|ГДЕ
		|	НЕ ХозрасчетныйОстаткиИОбороты.Регистратор = НЕОПРЕДЕЛЕНО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СобытияОС.ПериодОстатков КАК ПериодОстатков,
		|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
		|	МАКСИМУМ(ХозрасчетныйОстаткиИОбороты.Период) КАК ПоследнийПериод
		|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОборотыСрезПоследнихПериодов
		|ИЗ
		|	СобытияОС КАК СобытияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК ХозрасчетныйОстаткиИОбороты
		|		ПО СобытияОС.ПериодОстатков >= ХозрасчетныйОстаткиИОбороты.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СобытияОС.ПериодОстатков,
		|	ХозрасчетныйОстаткиИОбороты.Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОборотыСрезПоследнихПериодов.ПериодОстатков КАК ПериодОстатков,
		|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаКонечныйОстатокКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт
		|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОборотыСрезПоследних
		|ИЗ
		|	ХозрасчетныйОстаткиИОборотыСрезПоследнихПериодов КАК ХозрасчетныйОстаткиИОборотыСрезПоследнихПериодов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК ХозрасчетныйОстаткиИОбороты
		|		ПО ХозрасчетныйОстаткиИОборотыСрезПоследнихПериодов.ПоследнийПериод = ХозрасчетныйОстаткиИОбороты.Период
		|			И ХозрасчетныйОстаткиИОборотыСрезПоследнихПериодов.Счет = ХозрасчетныйОстаткиИОбороты.Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СобытияОС.НазваниеДокумента КАК НазваниеДокумента,
		|	СобытияОС.НомерДокумента КАК НомерДокумента,
		|	СобытияОС.Период КАК Период,
		|	СобытияОС.Событие КАК Событие,
		|	СобытияОС.Регистратор КАК Регистратор,
		|	МестонахождениеОСБухгалтерскийУчетСрезПоследних.МОЛ КАК МОЛ,
		|	МестонахождениеОСБухгалтерскийУчетСрезПоследних.Местонахождение КАК Местонахождение,
		|	СтоимостьОС.СуммаОборотКт КАК СтоимостьОборот,
		|	АмортизацияОС.СуммаОборотДт КАК АмортизацияОборот,
		|	ВЫБОР
		|		КОГДА СобытияОС.ВидСобытияОС В (ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Списание), ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Передача), ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.ПереводВМалоценноеОборудование))
		|			ТОГДА ЕСТЬNULL(СтоимостьОС.СуммаОборотКт, 0) - ЕСТЬNULL(АмортизацияОС.СуммаОборотДт, 0)
		|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СуммаКонечныйОстатокДт, 0) - ЕСТЬNULL(АмортизацияОС.СуммаКонечныйОстатокКт, 0)
		|	КОНЕЦ КАК ОстаточнаяСтоимость
		|ИЗ
		|	СобытияОС КАК СобытияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСБухгалтерскийУчетСрезПоследних КАК МестонахождениеОСБухгалтерскийУчетСрезПоследних
		|		ПО СобытияОС.Период = МестонахождениеОСБухгалтерскийУчетСрезПоследних.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаБухгалтерскогоУчетаОССрезПоследних КАК СчетаБухгалтерскогоУчетаОССрезПоследних
		|		ПО СобытияОС.ПериодОстатков = СчетаБухгалтерскогоУчетаОССрезПоследних.ПериодОстатков
		|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОборотыСрезПоследних КАК СтоимостьОС
		|		ПО (СчетаБухгалтерскогоУчетаОССрезПоследних.ПериодОстатков = СтоимостьОС.ПериодОстатков)
		|			И (СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета = СтоимостьОС.Счет)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОборотыСрезПоследних КАК АмортизацияОС
		|		ПО (СчетаБухгалтерскогоУчетаОССрезПоследних.ПериодОстатков = АмортизацияОС.ПериодОстатков)
		|			И (СчетаБухгалтерскогоУчетаОССрезПоследних.СчетНачисленияАмортизации = АмортизацияОС.Счет)";
		ВыборкаЗаписей = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаЗаписей.Следующий() Цикл
			
			ТекущаяОперация = ВыборкаЗаписей.Событие;
			
			СтрокаРаздела4.Параметры.ДатаНомерДокумента = ВыборкаЗаписей.НазваниеДокумента + " № "+ВыборкаЗаписей.НомерДокумента+" от "+Формат(ВыборкаЗаписей.Период,"ДФ=dd.MM.yyyy");
			СтрокаРаздела4.Параметры.ВидОперации        = ТекущаяОперация;
			СтрокаРаздела4.Параметры.ФИОМОЛДвижения     = ВыборкаЗаписей.МОЛ;
			СтрокаРаздела4.Параметры.Подразделение      = ВыборкаЗаписей.Местонахождение;
				
			Если ВыборкаЗаписей.СтоимостьОборот = NULL И ВыборкаЗаписей.АмортизацияОборот = NULL Тогда // ввод начальных остатков
				
				СтрокаРаздела4.Параметры.ОстаточнаяСтоимость = ТекущиеСведенияОС.ПервоначальнаяСтоимость;
				
			Иначе
				
				СтрокаРаздела4.Параметры.ОстаточнаяСтоимость = ВыборкаЗаписей.ОстаточнаяСтоимость;
				
			КонецЕсли;
			
			ТабДок.Вывести(СтрокаРаздела4);
			
		КонецЦикла;
		
		ТабДок.Вывести(ПодвалСтраницы1);
		
		// модернизация ос и ремонт
		ТаблицаМодернизаций = Новый ТаблицаЗначений;
		ТаблицаМодернизаций.Колонки.Добавить("ВидОперации");
		ТаблицаМодернизаций.Колонки.Добавить("Название");
		ТаблицаМодернизаций.Колонки.Добавить("Дата");
		ТаблицаМодернизаций.Колонки.Добавить("Номер");
		ТаблицаМодернизаций.Колонки.Добавить("Сумма");
		
		ТаблицаРемонтов = Новый ТаблицаЗначений;
		ТаблицаРемонтов.Колонки.Добавить("ВидОперации");
		ТаблицаРемонтов.Колонки.Добавить("Название");
		ТаблицаРемонтов.Колонки.Добавить("Дата");
		ТаблицаРемонтов.Колонки.Добавить("Номер");
		ТаблицаРемонтов.Колонки.Добавить("Сумма");
		
		ТабДок.Вывести(ШапкаРазделов5и6);
		
		СписокМодернизаций = Новый Массив;
		СписокМодернизаций.Добавить(Перечисления.ВидыСобытийОС.РаспределениеНДС);
		СписокМодернизаций.Добавить(Перечисления.ВидыСобытийОС.Модернизация);
		СписокМодернизаций.Добавить(Перечисления.ВидыСобытийОС.Достройка);
		СписокМодернизаций.Добавить(Перечисления.ВидыСобытийОС.Реконструкция);
		СписокМодернизаций.Добавить(Перечисления.ВидыСобытийОС.Дооборудование);
		СписокМодернизаций.Добавить(Перечисления.ВидыСобытийОС.ЧастичнаяЛиквидация);
		
		СписокРемонтов = Новый Массив;
		СписокРемонтов.Добавить(Перечисления.ВидыСобытийОС.СреднийРемонт);
		СписокРемонтов.Добавить(Перечисления.ВидыСобытийОС.ТекущийРемонт);
		СписокРемонтов.Добавить(Перечисления.ВидыСобытийОС.КапитальныйРемонт);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", ВыборкаОбъектов.Организация);
		Запрос.УстановитьПараметр("ОсновноеСредство", ВыборкаОбъектов.Ссылка);
		Запрос.УстановитьПараметр("УсловиеМодернизаций",СписокМодернизаций);
		Запрос.УстановитьПараметр("УсловиеРемонтов", СписокРемонтов);
		Запрос.УстановитьПараметр("ВидСобытияОС", Перечисления.ВидыСобытийОС.ПринятиеКУчету);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА СобытияОСОрганизаций.Событие.ВидСобытияОС В (&УсловиеМодернизаций)
		|			ТОГДА СобытияОСОрганизаций.СуммаЗатратБУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаЗатратБУМодернизаций,
		|	ВЫБОР
		|		КОГДА СобытияОСОрганизаций.Событие.ВидСобытияОС В (&УсловиеРемонтов)
		|			ТОГДА СобытияОСОрганизаций.СуммаЗатратБУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаЗатратБУРемонтов,
		|	СобытияОСОрганизаций.Регистратор КАК Регистратор,
		|	СобытияОСОрганизаций.Период КАК Период,
		|	СобытияОСОрганизаций.Событие КАК Операция,
		|	СобытияОСОрганизаций.Событие.ВидСобытияОС КАК ВидСобытияОС,
		|	СобытияОСОрганизаций.НомерДокумента КАК НомерДокумента,
		|	ВЫРАЗИТЬ(СобытияОСОрганизаций.НазваниеДокумента КАК СТРОКА(200)) КАК НазваниеДокумента,
		|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, СобытияОСОрганизаций.Период) КАК ДатаРегистратора
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО СобытияОСОрганизаций.Регистратор = ДанныеПервичныхДокументов.Документ
		|ГДЕ
		|	СобытияОСОрганизаций.Организация = &Организация
		|	И СобытияОСОрганизаций.Событие.ВидСобытияОС <> &ВидСобытияОС
		|	И СобытияОСОрганизаций.ОсновноеСредство = &ОсновноеСредство
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	Регистратор";
		
		Результат = Запрос.Выполнить();
		
		СпособВыборки = ОбходРезультатаЗапроса.Прямой;
		ВыборкаРегистраторов = Результат.Выбрать(СпособВыборки);
		Пока ВыборкаРегистраторов.Следующий() Цикл
			
			Если ТипЗнч(ВыборкаРегистраторов.Регистратор) = Тип("ДокументСсылка.ВводНачальныхОстатков")
				И ВыборкаРегистраторов.ВидСобытияОС = Перечисления.ВидыСобытийОС.ПринятиеКУчетуСВводомВЭксплуатацию Тогда
				
				ЗапросСведенияОС.УстановитьПараметр("ДатаСведений", ВыборкаРегистраторов.ДатаРегистратора);
				ВыборкаСведенияОС = ЗапросСведенияОС.Выполнить().Выбрать();
				Если ВыборкаСведенияОС.Следующий() И
					ВыборкаСведенияОС.СтоимостьДляВычисленияАмортизации > ВыборкаСведенияОС.ПервоначальнаяСтоимость Тогда
				
					СтрокаТаблицыРемонтов = ТаблицаМодернизаций.Добавить();
					День = 86400;
					СтрокаТаблицыРемонтов.ВидОперации =
						Нстр(СтрШаблон("ru='Изменения до %1'", Формат(ВыборкаРегистраторов.ДатаРегистратора + День, "ДЛФ=D")));
					СтрокаТаблицыРемонтов.Название = Нстр("ru='Ввод остатков'");
					СтрокаТаблицыРемонтов.Номер = ВыборкаРегистраторов.НомерДокумента;
					СтрокаТаблицыРемонтов.Дата = ВыборкаРегистраторов.Период;
					СтрокаТаблицыРемонтов.Сумма = ВыборкаСведенияОС.СтоимостьДляВычисленияАмортизации - ВыборкаСведенияОС.ПервоначальнаяСтоимость;
				КонецЕсли;
			КонецЕсли;
			
			СуммаМодернизаций = ?(ВыборкаРегистраторов.СуммаЗатратБУМодернизаций = NULL, 0, ВыборкаРегистраторов.СуммаЗатратБУМодернизаций);
			СуммаРемонтов = ?(ВыборкаРегистраторов.СуммаЗатратБУРемонтов = NULL, 0, ВыборкаРегистраторов.СуммаЗатратБУРемонтов);
			
			Если СуммаМодернизаций <> 0 Тогда
				СтрокаТаблицыМодернизаций = ТаблицаМодернизаций.Добавить();
				СтрокаТаблицыМодернизаций.ВидОперации = ВыборкаРегистраторов.Операция;
				СтрокаТаблицыМодернизаций.Название    = ВыборкаРегистраторов.НазваниеДокумента;
				СтрокаТаблицыМодернизаций.Номер       = ВыборкаРегистраторов.НомерДокумента;
				СтрокаТаблицыМодернизаций.Дата        = ВыборкаРегистраторов.Период;
				СтрокаТаблицыМодернизаций.Сумма       = СуммаМодернизаций;
			КонецЕсли;
			
			Если СуммаРемонтов <> 0 Тогда
				СтрокаТаблицыРемонтов = ТаблицаРемонтов.Добавить();
				СтрокаТаблицыРемонтов.ВидОперации      = ВыборкаРегистраторов.Операция;
				СтрокаТаблицыРемонтов.Название         = ВыборкаРегистраторов.НазваниеДокумента;
				СтрокаТаблицыРемонтов.Номер            = ВыборкаРегистраторов.НомерДокумента;
				СтрокаТаблицыРемонтов.Дата             = ВыборкаРегистраторов.Период;
				СтрокаТаблицыРемонтов.Сумма            = СуммаРемонтов;
			КонецЕсли;
		КонецЦикла;
		
		КоличествоСтрок = Макс(ТаблицаМодернизаций.Количество(),ТаблицаРемонтов.Количество(),1); 
		Для СчетСтрок = 1 По КоличествоСтрок Цикл
			Если СчетСтрок <= ТаблицаМодернизаций.Количество() Тогда
				СтрокаТаблицы = ТаблицаМодернизаций.Получить(СчетСтрок - 1);
				СтрокаРазделов5и6.Параметры.Модернизация          = СтрокаТаблицы.ВидОперации;
				СтрокаРазделов5и6.Параметры.ДокМодернизации       = СтрокаТаблицы.Название;
				СтрокаРазделов5и6.Параметры.ДокМодернизацииДата   = СтрокаТаблицы.Дата;
				СтрокаРазделов5и6.Параметры.ДокМодернизацииНомер  = СтрокаТаблицы.Номер;
				СтрокаРазделов5и6.Параметры.ЗатратыНаМодернизацию = СтрокаТаблицы.Сумма; 
			Иначе
				СтрокаРазделов5и6.Параметры.Модернизация          = "";
				СтрокаРазделов5и6.Параметры.ДокМодернизации       = "";
				СтрокаРазделов5и6.Параметры.ДокМодернизацииДата   = "";
				СтрокаРазделов5и6.Параметры.ДокМодернизацииНомер  = "";
				СтрокаРазделов5и6.Параметры.ЗатратыНаМодернизацию = ""; 
			КонецЕсли;
			
			Если СчетСтрок <= ТаблицаРемонтов.Количество() Тогда
				СтрокаТаблицыРемонтов = ТаблицаРемонтов.Получить(СчетСтрок - 1);
				СтрокаРазделов5и6.Параметры.Ремонт          = СтрокаТаблицыРемонтов.ВидОперации;
				СтрокаРазделов5и6.Параметры.ДокРемонта      = СтрокаТаблицыРемонтов.Название;
				СтрокаРазделов5и6.Параметры.ДокРемонтаДата  = СтрокаТаблицыРемонтов.Дата;
				СтрокаРазделов5и6.Параметры.ДокРемонтаНомер = СтрокаТаблицыРемонтов.Номер;
				СтрокаРазделов5и6.Параметры.ЗатратыНаРемонт = СтрокаТаблицыРемонтов.Сумма;
			Иначе
				СтрокаРазделов5и6.Параметры.Ремонт          = "";
				СтрокаРазделов5и6.Параметры.ДокРемонта      = "";
				СтрокаРазделов5и6.Параметры.ДокРемонтаДата  = "";
				СтрокаРазделов5и6.Параметры.ДокРемонтаНомер = "";
				СтрокаРазделов5и6.Параметры.ЗатратыНаРемонт = "";
			КонецЕсли;
			
			ТабДок.Вывести(СтрокаРазделов5и6);
		КонецЦикла;
		
		ТабДок.Вывести(ШапкаРаздела7_1);
		ТабДок.Вывести(ШапкаРаздела7_2);
		ТабДок.Вывести(ПодвалСтраницы2);
		
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаОбъектов.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС6") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ОС6",
			НСтр("ru = 'Инвентарная карточка ОС (ОС-6)'"),
			ПечатьОС6_2003(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			, // Картинка
			"Справочник.ОсновныеСредства.ПФ_MXL_ОС6");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Получает признаки прослеживаемости из истории для массива ОС на заданную дату
// Параметры:
//      СписокОС - Массив - ссылки на объекты вида Справочник.ОсновныеСредства.
//                    Если массив пуст, то результатом будет пустое соответствие.
//      ДатаСведений - Дата - дата получения признака прослеживаемости.
// Возвращаемое значение:
//  Соответствие - список объектов Справочник.ОсновныеСредства и значений признаков прослеживаемости:
//   * Ключ - Справочник.ОсновныеСредства - ссылка на объект;
//   * Значение - Прослеживаемый товар - Булево - признак прослеживаеимости на дату
// Для основных средств с пустой историей прослеживаемости ключ будет отсутствовать.
// 
Функция ПризнакПрослеживаемостиНаДатуИзИстории(СписокОС, ДатаСведений) Экспорт

	ЗначенияРеквизитов = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(ДатаСведений) Тогда
		Возврат ЗначенияРеквизитов;
	КонецЕсли;
	
	Если СписокОС.Количество() = 0 Тогда
		Возврат ЗначенияРеквизитов;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("ОС", СписокОС);
	Запрос.Параметры.Вставить("ДатаСведений", ДатаСведений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ОсновныеСредстваИсторияПрослеживаемогоТовара.Период) КАК Период,
	|	ОсновныеСредстваИсторияПрослеживаемогоТовара.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗначенияПрослеживаемости
	|ИЗ
	|	Справочник.ОсновныеСредства.ИсторияПрослеживаемогоТовара КАК ОсновныеСредстваИсторияПрослеживаемогоТовара
	|ГДЕ
	|	ОсновныеСредстваИсторияПрослеживаемогоТовара.Ссылка В(&ОС)
	|	И ОсновныеСредстваИсторияПрослеживаемогоТовара.Период <= &ДатаСведений
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновныеСредстваИсторияПрослеживаемогоТовара.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСредстваИсторияПрослеживаемогоТовара.Ссылка КАК ОС,
	|	ОсновныеСредстваИсторияПрослеживаемогоТовара.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
	|ИЗ
	|	ЗначенияПрослеживаемости КАК ЗначенияПрослеживаемости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства.ИсторияПрослеживаемогоТовара КАК ОсновныеСредстваИсторияПрослеживаемогоТовара
	|		ПО ЗначенияПрослеживаемости.Ссылка = ОсновныеСредстваИсторияПрослеживаемогоТовара.Ссылка
	|			И ЗначенияПрослеживаемости.Период = ОсновныеСредстваИсторияПрослеживаемогоТовара.Период";

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Новый Структура("ПрослеживаемыйТовар");
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		ЗначенияРеквизитов[Выборка.ОС] = Результат;

	КонецЦикла;
	
	Возврат ЗначенияРеквизитов;

КонецФункции

// Получает признак "Прослеживаемый товар" по основному средству и периоду
// если есть история прослеживаемого товара - возвращается значение из истории
// если история отсутсвует, тогда возвращается значение реквизита "Прослеживаемый товар"
//
// Параметры:
//      ОС - Справочник.ОсновныеСредства - признаки прослеживаемости которой необходимо получить
//      ДатаСведений - Дата - дата получения признака прослеживаеимости
// Возвращаемое значение:
//      Прослеживаемый товар - Булево 
Функция ПризнакПрослеживаемостиНаДату(ОС, ДатаСведений) Экспорт
	
	Если Не ЗначениеЗаполнено(ОС)
		Или ТипЗнч(ОС) <> Тип("СправочникСсылка.ОсновныеСредства") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	
	Если Не ЗначениеЗаполнено(ДатаСведений) Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОС, "ПрослеживаемыйТовар");
	КонецЕсли;
	
	СписокОС = Новый Массив();
	СписокОС.Добавить(ОС);
	
	СоответствиеСведенийПризнакаПрослеживаемостиОС = ПризнакПрослеживаемостиНаДатуИзИстории(СписокОС, ДатаСведений);

	СведенияПризнакаПрослеживаемостиИзИстории = СоответствиеСведенийПризнакаПрослеживаемостиОС.Получить(ОС);
	
	Если СведенияПризнакаПрослеживаемостиИзИстории = Неопределено Тогда
		ПризнакПрослеживаемости = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОС, "ПрослеживаемыйТовар");;
	Иначе
		ПризнакПрослеживаемости = СведенияПризнакаПрослеживаемостиИзИстории.ПрослеживаемыйТовар;
	КонецЕсли;
	
	Возврат ПризнакПрослеживаемости;
	
КонецФункции

#КонецОбласти

#КонецЕсли