&НаКлиенте
Перем УстановкаОсновногоБанковскогоСчетаВыполнена;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		ОтборОрганизация = Параметры.Отбор.Владелец;
		Параметры.Отбор.Удалить("Владелец");
	ИначеЕсли НЕ Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		ОтборОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	Элементы.ОтборОрганизация.Видимость = ОтборОрганизацияИспользование;
	
	Если ОтборОрганизацияИспользование Тогда
		ОсновнойБанковскийСчет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборОрганизация, "ОсновнойБанковскийСчет");
	КонецЕсли;
	
	ТипВладельца = ТипЗнч(ОтборОрганизация);
	Элементы.СтатусСчета.Видимость = Ложь;
	Если ОтборОрганизацияИспользование Тогда
		ДоступноСоздание = ПравоДоступа("Редактирование", Метаданные.Справочники.БанковскиеСчета);
		
		Если ТипВладельца = Тип("СправочникСсылка.Контрагенты") Тогда
			Элементы.ОтборОрганизация.Заголовок = НСтр("ru = 'Контрагент'");
			ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.Контрагенты);
			
			// Переключение на подменю создать
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Создать", "Видимость", Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСоздать", "Видимость", ДоступноСоздание);
		ИначеЕсли ТипВладельца = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			Элементы.ОтборОрганизация.Заголовок = НСтр("ru = 'Физическое лицо'");
			ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.ФизическиеЛица);
			Если ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "КлючНазначенияИспользования", "")) Тогда
				Элементы.ФормаИспользоватьОсновным.Видимость = Ложь;
			КонецЕсли;
		ИначеЕсли ТипВладельца = Тип("СправочникСсылка.Организации") Тогда
			Элементы.ОтборОрганизация.Видимость = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
			Элементы.ОтборОрганизация.Заголовок = НСтр("ru = 'Организация'");
			ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.Организации);
			
			ЭтоИнтерфейсИнтеграцииСБанком  = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
			Элементы.Основной.Видимость    = Не ЭтоИнтерфейсИнтеграцииСБанком;
			Элементы.СтатусСчета.Видимость = Справочники.НастройкиИнтеграцииСБанками.ИнтеграцияВИнформационнойБазеВключена();
			Элементы.СчетБанк.Видимость    = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
			
			// Переключение на подменю создать
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Создать", "Видимость", Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСоздать", "Видимость", ДоступноСоздание);
			
			БИКБанковСИнформациейОНастройкеПрямогоОбмена =
				Справочники.БанковскиеСчета.БИКБанковСИнформациейОНастройкеПрямогоОбмена(ОтборОрганизация);
			
			Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("БИКБанковСИнформациейОНастройкеПрямогоОбмена",
				БИКБанковСИнформациейОНастройкеПрямогоОбмена);
			Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ДоступнаНастройкаПрямогоОбмена",
				ПравоДоступа("Редактирование", Метаданные.Справочники.НастройкиОбменСБанками));
			
			Элементы.ГруппаДиректБанк.Видимость = БИКБанковСИнформациейОНастройкеПрямогоОбмена.Количество() > 0;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПараметрыСписка(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборПоОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УстановкаОсновногоБанковскогоСчетаВыполнена" Тогда
		
		УстановкаОсновногоБанковскогоСчетаВыполнена = Истина;
		
	ИначеЕсли ИмяСобытия = "УстановкаОсновногоБанковскогоСчета" Тогда
		
		Если ЗначениеЗаполнено(ОтборОрганизация) И ОтборОрганизация = Параметр.КонтрагентОрганизация Тогда
			ОсновнойБанковскийСчет = Параметр.ОсновнойБанковскийСчет;
			УстановитьПараметрыСписка(ЭтотОбъект);
			УправлениеФормойКлиент();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "УстановкаОсновногоБанковскогоСчетаПриЗаписи" Тогда
		
		Если ЗначениеЗаполнено(ОтборОрганизация) И ОтборОрганизация = Параметр.КонтрагентОрганизация
			И УстановкаОсновногоБанковскогоСчетаВыполнена = Истина Тогда
			ОсновнойБанковскийСчет = Параметр.ОсновнойБанковскийСчет;
			УстановитьПараметрыСписка(ЭтотОбъект);
			УправлениеФормойКлиент();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" Тогда
		Если Параметр.Свойство("ОсновнойБанковскийСчет")
			И ЗначениеЗаполнено(ОтборОрганизация) И ОтборОрганизация = Параметр.Владелец Тогда
			ОсновнойБанковскийСчет = Параметр.ОсновнойБанковскийСчет;
		КонецЕсли;
		
		// Проверка состояния настройки прямого обмена для банка счета
		Если Параметр.Свойство("Владелец") И Параметр.Свойство("Банк") Тогда
			БанкСНастройкойПрямогоОбменаНаСервере(Параметр.Владелец, Параметр.Банк);
		КонецЕсли;
		
		Элементы.Список.ТекущаяСтрока = Параметр.Ссылка;
		УстановитьПараметрыСписка(ЭтотОбъект);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено
		Или Не Элементы.Список.ТекущиеДанные.Свойство("Ссылка")
		Или Не Элементы.Список.ТекущиеДанные.Свойство("Владелец") Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.Ссылка = ОсновнойБанковскийСчет Тогда
		ОсновнойБанковскийСчет = ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка");
	Иначе
		ОсновнойБанковскийСчет = Элементы.Список.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	Если ФизическомуЛицуЗаписываетсяСчетИПНаСервере(ОтборОрганизация, Элементы.Список.ТекущиеДанные.НомерСчета) Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВопросЗаписатьФизическомуЛицуСчетИПЗавершение", ЭтотОбъект, Новый Структура);
		
		ТекстВопроса = НСтр("ru = 'Номер основного счета контрагента был определен, как счет индивидуального предпринимателя.
						|
						|Изменить вид контрагента на ""Индивидуальный предприниматель""?'");
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПараметрыСписка(ЭтотОбъект);
	
	УстановкаОсновногоБанковскогоСчетаВыполнена = Ложь;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("КонтрагентОрганизация",  Элементы.Список.ТекущиеДанные.Владелец);
	СтруктураПараметров.Вставить("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
	
	Оповестить("УстановкаОсновногоБанковскогоСчета", СтруктураПараметров);
	
	// Если форма владельца закрыта, то запишем основной банковский счет самостоятельно.
	Если Не УстановкаОсновногоБанковскогоСчетаВыполнена Тогда
		УстановитьОсновнойБанковскийСчет(СтруктураПараметров);
	КонецЕсли;
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЦифровойСчет(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", ОтборОрганизация);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЦифровойСчет", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаЭлементаЦифровойСчет", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБанковскийСчет(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", ОтборОрганизация);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаЭлемента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьОсновнойБанковскийСчет(СтруктураПараметров)
	
	Справочники.БанковскиеСчета.УстановитьОсновнойБанковскийСчет(
		СтруктураПараметров.КонтрагентОрганизация,
		СтруктураПараметров.ОсновнойБанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	Если ОсновнойБанковскийСчет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ДоступноИспользоватьОсновным И ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Свойство("Ссылка") Тогда
			Элементы.ФормаИспользоватьОсновным.Пометка = ТекущиеДанные.Ссылка = ОсновнойБанковскийСчет;
		КонецЕсли;
		
		Если ТекущиеДанные.Свойство("ПометкаУдаления") И ТекущиеДанные.ПометкаУдаления Тогда
			Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		ИначеЕсли ТекущиеДанные.Свойство("ДатаЗакрытия") И ЗначениеЗаполнено(ТекущиеДанные.ДатаЗакрытия) Тогда
			Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		ИначеЕсли ТекущиеДанные.Свойство("ЦифровойСчет") И ТекущиеДанные.ЦифровойСчет Тогда
			Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		Иначе
			Элементы.ФормаИспользоватьОсновным.Доступность = Истина;
		КонецЕсли;
	Иначе
		Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоОрганизации()
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ЭтотОбъект.Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
			"Владелец",
			ОтборОрганизация,
			Неопределено,
			,
			ОтборОрганизацияИспользование);
	ИначеЕсли ЗначениеЗаполнено(ТипВладельца) И ТипВладельца <> Тип("Неопределено") Тогда
		Если ТипВладельца = Тип("СправочникСсылка.Организации") Тогда
			СписокОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, "Владелец", СписокОрганизаций, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
				ЭтотОбъект.Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, "ТипВладельца", ТипВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураОтборовСписка()
	
	СтруктураОтборов = Новый Структура;
	Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		Если НЕ ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			СтруктураОтборов.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение);
		ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
			Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СписокЗначений") Тогда
				СтруктураОтборов.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение.ВыгрузитьЗначения());
			Иначе
				СтруктураОтборов.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураОтборов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтатусыСчетов()
	
	СтатусыСчетов = Новый Структура;
	СтатусыСчетов.Вставить("СчетВРежимеИнтеграции", 0);
	СтатусыСчетов.Вставить("СчетБезИнтеграции", 1);
	
	Возврат СтатусыСчетов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыСписка(Форма)
	
	Список = Форма.Список;
	
	СтатусыСчетов = СтатусыСчетов();
	Список.Параметры.УстановитьЗначениеПараметра("СтатусСчетВРежимеИнтеграции", СтатусыСчетов.СчетВРежимеИнтеграции);
	Список.Параметры.УстановитьЗначениеПараметра("СтатусСчетБезИнтеграции", СтатусыСчетов.СчетБезИнтеграции);
	
	Если ЗначениеЗаполнено(Форма.ОсновнойБанковскийСчет) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", Форма.ОсновнойБанковскийСчет);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Список.УсловноеОформление.Элементы.Очистить();
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаЗакрытия");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	// Владелец
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Владелец");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизацияИспользование", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизация", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФизическомуЛицуЗаписываетсяСчетИПНаСервере(Знач Контрагент, Знач НомерСчета)
	
	Возврат Справочники.Контрагенты.ФизическомуЛицуЗаписываетсяСчетИП(Контрагент, НомерСчета);
	
КонецФункции

&НаСервере
Процедура БанкСНастройкойПрямогоОбменаНаСервере(ВладелецСчета, Банк)
	
	СвойстваНастроек = Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	
	Если НЕ ЗначениеЗаполнено(Банк) ИЛИ НЕ ЗначениеЗаполнено(ВладелецСчета)
		ИЛИ ТипЗнч(ОтборОрганизация) <> Тип("СправочникСсылка.Организации") ИЛИ ВладелецСчета <> ОтборОрганизация
		ИЛИ НЕ СвойстваНастроек.Свойство("БИКБанковСИнформациейОНастройкеПрямогоОбмена") Тогда
		Возврат;
	КонецЕсли;
	
	БИКБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Код");
	БИКБанковСИнформациейОНастройкеПрямогоОбмена = СвойстваНастроек.БИКБанковСИнформациейОНастройкеПрямогоОбмена;
	
	Если БИКБанковСИнформациейОНастройкеПрямогоОбмена.Получить(БИКБанка) = Неопределено Тогда	
		
		ИнформацияОНастройкеПрямогоОбмена
			= Справочники.БанковскиеСчета.ИнформацияОНастройкеПрямогоОбменаСБанком(ВладелецСчета, Банк);
		
		Если ИнформацияОНастройкеПрямогоОбмена.Рублевый ИЛИ ИнформацияОНастройкеПрямогоОбмена.Валютный Тогда
			
			БИКБанковСИнформациейОНастройкеПрямогоОбмена.Вставить(БИКБанка, ИнформацияОНастройкеПрямогоОбмена);
			Элементы.ГруппаДиректБанк.Видимость = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ЦифровойСчет = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", СтруктураОтборовСписка());
	ПараметрыФормы.ЗначенияЗаполнения.Вставить("Владелец", ОтборОрганизация);
	
	Если Копирование Тогда
		
		Если Не Элементы.Список.ТекущиеДанные.Свойство("Ссылка") Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Список.ТекущиеДанные.Ссылка);
		ЦифровойСчет = Элементы.Список.ТекущиеДанные.ЦифровойСчет;
		
	КонецЕсли;
	
	Если ЦифровойСчет Тогда
		
		ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаЭлементаЦифровойСчет",
			ПараметрыФормы, ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли Копирование Или ТипЗнч(ОтборОрганизация) <> Тип("СправочникСсылка.Организации") Тогда
		
		ОткрытьФорму("Справочник.БанковскиеСчета.ФормаОбъекта",
			ПараметрыФормы, ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыполнитьОткрытиеФормыВыбораЗавершение", ЭтотОбъект, ПараметрыФормы);
		
		ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаВыбораВидаСчета",
			ПараметрыФормы,
			,
			,
			,
			,
			ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьВидКонтрагентаНаИП(Владелец)
	
	Попытка
		
		ВладелецОбъект = Владелец.ПолучитьОбъект();
		ВладелецОбъект.ИндивидуальныйПредприниматель = Истина;
		ВладелецОбъект.Записать();
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
		
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВопросЗаписатьФизическомуЛицуСчетИПЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если НЕ ИзменитьВидКонтрагентаНаИП(ОтборОрганизация) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Для контрагента %1 не удалось изменить вид контрагента на ""Индивидуальный предприниматель"".'"),
							ОтборОрганизация);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПараметрыСписка(ЭтотОбъект);
	
	УстановкаОсновногоБанковскогоСчетаВыполнена = Ложь;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("КонтрагентОрганизация",  Элементы.Список.ТекущиеДанные.Владелец);
	СтруктураПараметров.Вставить("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
	
	Оповестить("УстановкаОсновногоБанковскогоСчета", СтруктураПараметров);
	
	// Если форма владельца закрыта, то запишем основной банковский счет самостоятельно.
	Если Не УстановкаОсновногоБанковскогоСчетаВыполнена Тогда
		УстановитьОсновнойБанковскийСчет(СтруктураПараметров);
	КонецЕсли;
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОткрытиеФормыВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатЗакрытия.ЦифровойСчет Тогда
		
		ДополнительныеПараметры.Вставить("ЦифровойСчет", Истина);
		ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаЭлементаЦифровойСчет",
			ДополнительныеПараметры, ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		ОткрытьФорму("Справочник.БанковскиеСчета.ФормаОбъекта",
			ДополнительныеПараметры, ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	СписокПуст = Элементы.Список.ТекущиеДанные = Неопределено;
	
	Если СписокПуст Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Элемент.ТекущаяСтрока);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", СтруктураОтборовСписка());
	
	ОткрытьФорму("Справочник.БанковскиеСчета.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Свойство("Ссылка") И ЗначениеЗаполнено(ТекущиеДанные.Ссылка)
			И ТекущиеДанные.Свойство("Основной") И ТекущиеДанные.Основной
			И (ТекущиеДанные.Свойство("ПометкаУдаления") И ТекущиеДанные.ПометкаУдаления
				Или ТекущиеДанные.Свойство("ДатаЗакрытия") И ЗначениеЗаполнено(ТекущиеДанные.ДатаЗакрытия)) Тогда
			ПараметрОповещения = Новый Структура("Ссылка, Владелец, ОсновнойБанковскийСчет",
				ТекущиеДанные.Ссылка,
				ТекущиеДанные.Владелец,
				ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка"));
			Оповестить("ИзмененБанковскийСчет", ПараметрОповещения);
		КонецЕсли;
	КонецЕсли;
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ДиректБанкСтатус = Неопределено;
	
	Если Элементы["ДиректБанкСтатус"] = Поле И Элементы.Список.ТекущиеДанные <> Неопределено
		И Элементы.Список.ТекущиеДанные.Свойство("ДиректБанкСтатус", ДиректБанкСтатус)
		И ДиректБанкСтатус = НСтр("ru = 'Подключить'") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ДоступнаНастройкаПрямогоОбмена")
			И Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ДоступнаНастройкаПрямогоОбмена Тогда
			
			ТекущиеДанные = Элементы.Список.ТекущиеДанные;
			ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(ТекущиеДанные.Владелец,
				ТекущиеДанные.Банк, ТекущиеДанные.НомерСчета);
		Иначе
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://portal.1c.ru/applications/1C-Direct-bank");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	БИКБанковСИнформациейОНастройкеПрямогоОбмена = Неопределено;
	
	Если НЕ Настройки.ДополнительныеСвойства.Свойство("БИКБанковСИнформациейОНастройкеПрямогоОбмена", 
		БИКБанковСИнформациейОНастройкеПрямогоОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	МассивКлючей = Строки.ПолучитьКлючи();
	
	Если МассивКлючей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаСписка = Строки[МассивКлючей[0]];
	Если НЕ СтрокаСписка.Данные.Свойство("ДиректБанкСтатус") ИЛИ
		НЕ СтрокаСписка.Данные.Свойство("ДиректБанкКартинка") Тогда
		Возврат;
	КонецЕсли;
	
	Банки = Новый Массив;
	Для Каждого ЭлементСписка Из Строки Цикл
		ТекущиеДанные = ЭлементСписка.Значение.Данные;
		Банки.Добавить(ТекущиеДанные.Банк);
	КонецЦикла;
	
	РеквизитыБанков = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Банки, "РучноеИзменение");
	
	ПроверятьИнтеграциюСБанком = СтрокаСписка.Данные.Свойство("СтатусСчета");
	
	Для каждого ЭлементСписка Из Строки Цикл
		ТекущиеДанные = ЭлементСписка.Значение.Данные;
		СтатусСчетБезИнтеграции = Истина;
		
		Если ПроверятьИнтеграциюСБанком Тогда
			СтатусСчетБезИнтеграции = ЗначениеЗаполнено(ТекущиеДанные.СтатусСчета);
		КонецЕсли;
		
		ЭлементСписка.Значение.Оформление["ДиректБанкКартинка"].УстановитьЗначениеПараметра("Видимость", Истина);
		ЭлементСписка.Значение.Оформление["ДиректБанкСтатус"].УстановитьЗначениеПараметра("Видимость", Ложь);
		
		ДеятельностьБанкаПрекращена = Ложь;
		РеквизитыБанка = РеквизитыБанков[ТекущиеДанные.Банк];
		Если РеквизитыБанка <> Неопределено Тогда
			ДеятельностьБанкаПрекращена = БанковскиеСчетаСервер.ДеятельностьБанкаПрекращена(РеквизитыБанка.РучноеИзменение);
		КонецЕсли;
		
		Если СтатусСчетБезИнтеграции И НЕ ЗначениеЗаполнено(ТекущиеДанные.ДатаЗакрытия)
			И НЕ ТекущиеДанные.ПометкаУдаления
			И НЕ ДеятельностьБанкаПрекращена Тогда
			
			ИнформацияОНастройкахПрямогоОбмена = БИКБанковСИнформациейОНастройкеПрямогоОбмена.Получить(ТекущиеДанные.БИК);
			ЕстьНастройка = Ложь;
			
			Если ИнформацияОНастройкахПрямогоОбмена <> Неопределено
				И (ТекущиеДанные.Валютный И ИнформацияОНастройкахПрямогоОбмена.Валютный
					ИЛИ НЕ ТекущиеДанные.Валютный И ИнформацияОНастройкахПрямогоОбмена.Рублевый) Тогда
				
				ЕстьНастройка = ИнформацияОНастройкахПрямогоОбмена.Настройка;
				
				Если НЕ ЕстьНастройка Тогда
					ТекущиеДанные.ДиректБанкСтатус = НСтр("ru = 'Подключить'");
				КонецЕсли;
				
			КонецЕсли;
			
			ТекущиеДанные.ДиректБанкКартинка = ЕстьНастройка;
			ЭлементСписка.Значение.Оформление["ДиректБанкКартинка"].УстановитьЗначениеПараметра("Видимость",
				ЕстьНастройка);
			ЭлементСписка.Значение.Оформление["ДиректБанкСтатус"].УстановитьЗначениеПараметра("Видимость",
				НЕ ЕстьНастройка);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	Элементы.Список.ПоведениеПриСжатииПоГоризонтали = ПоведениеТаблицыПриСжатииПоГоризонтали.ПереноситьЭлементыПоВажности;
	Элементы.Наименование.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	Элементы.СтатусСчета.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаДиректБанк);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаКоманднаяПанель.ПодчиненныеЭлементы);
	
КонецПроцедуры

#КонецОбласти
