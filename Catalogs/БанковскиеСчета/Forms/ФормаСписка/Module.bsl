#Область ОписаниеПеременных

&НаКлиенте
Перем УстановкаОсновногоБанковскогоСчетаВыполнена;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		
		Владелец = Параметры.Отбор.Владелец;
		
	КонецЕсли;
	
	БанковскиеСчетаОрганизаций = Параметры.БанковскиеСчетаОрганизаций;
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	Если БанковскиеСчетаОрганизаций Тогда
		ОсновнаяОрганизация = Справочники.Организации.ОрганизацияПоУмолчанию();
		ОтборОрганизация = ОсновнаяОрганизация;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		УстановитьОтборПоОрганизации(ЭтотОбъект);
	КонецЕсли;
	
	Элементы.ГруппаОтборОрганизация.Видимость = БанковскиеСчетаОрганизаций И ИспользуетсяНесколькоОрганизаций;
	
	Если ЗначениеЗаполнено(Владелец) Или БанковскиеСчетаОрганизаций Тогда
		
		ДоступноСоздание = ПравоДоступа("Редактирование", Метаданные.Справочники.БанковскиеСчета);
		
		Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
			
			ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.Контрагенты);
			
			// Переключение на подменю создать
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Создать", "Видимость", Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСоздать", "Видимость", ДоступноСоздание);
			
		ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") Тогда
			
			ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.Организации);
			
			Элементы.СчетБанк.Видимость = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
			
			// Переключение на подменю создать
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Создать", "Видимость", Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСоздать", "Видимость", ДоступноСоздание);
			
			НастроитьЭлементыПрямогоОбмена();
			
		ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			
			ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.ФизическиеЛица);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Создать", "Видимость", ЗначениеЗаполнено(Владелец));
			
		КонецЕсли;
		
		ОсновнойБанковскийСчет = ОсновнойБанковскийСчет(Владелец);
	КонецЕсли;
	
	ЭтоИнтерфейсИнтеграцииСБанком = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
	Элементы.ФормаИспользоватьОсновным.Видимость = (БанковскиеСчетаОрганизаций Или ЗначениеЗаполнено(Владелец))
		И ДоступноИспользоватьОсновным И Не ЭтоИнтерфейсИнтеграцииСБанком;
	
	Элементы.Основной.Видимость = ЗначениеЗаполнено(Владелец) И Не ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.СтатусСчета.Видимость = Справочники.НастройкиИнтеграцииСБанками.ИнтеграцияВИнформационнойБазеВключена();
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.БанковскиеСчета);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	УстановитьПараметрыСписка(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УстановкаОсновногоБанковскогоСчетаВыполнена" Тогда
		
		УстановкаОсновногоБанковскогоСчетаВыполнена = Истина;
		
	ИначеЕсли ИмяСобытия = "УстановкаОсновногоБанковскогоСчетаПриЗаписи" Тогда
		
		Если ЗначениеЗаполнено(Владелец) И Владелец = Параметр.КонтрагентОрганизация Тогда
			
			Если УстановкаОсновногоБанковскогоСчетаВыполнена = Истина Тогда
				
				ОсновнойБанковскийСчет = Параметр.ОсновнойБанковскийСчет;
				УстановитьПараметрыСписка(ЭтотОбъект);
				
				УправлениеФормойКлиент();
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзмененаНастройкаОбмена" Тогда
		
		Элементы.Список.Обновить();
		
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" Тогда
		
		Если Параметр.Свойство("ОсновнойБанковскийСчет")
			И ЗначениеЗаполнено(Владелец) И Владелец = Параметр.Владелец Тогда
			ОсновнойБанковскийСчет = Параметр.ОсновнойБанковскийСчет;
		КонецЕсли;
		
		// Проверка состояния настройки прямого обмена для банка счета
		Если Параметр.Свойство("Владелец") И Параметр.Свойство("Банк") Тогда
			БанкСНастройкойПрямогоОбменаНаСервере(Параметр.Владелец, Параметр.Банк);	
		КонецЕсли;
					
		Элементы.Список.ТекущаяСтрока = Параметр.Ссылка;
		УстановитьПараметрыСписка(ЭтотОбъект);
		УправлениеФормойКлиент();
		
		Элементы.Список.Обновить();
		
	ИначеЕсли ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		Если БанковскиеСчетаОрганизаций И ОсновнаяОрганизация <> Параметр Тогда
			ОсновнаяОрганизация = Параметр;
			ОтборОрганизация = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
			УстановитьОтборПоОрганизации(ЭтотОбъект);
			УправлениеФормойКлиент();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") Тогда
			
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ПриИзмененииОтбораПоОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ПриИзмененииОтбораПоОрганизации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Свойство("Ссылка") И ЗначениеЗаполнено(ТекущиеДанные.Ссылка)
			И ТекущиеДанные.Свойство("Основной") И ТекущиеДанные.Основной
			И (ТекущиеДанные.Свойство("ПометкаУдаления") И ТекущиеДанные.ПометкаУдаления
				Или ТекущиеДанные.Свойство("ДатаЗакрытия") И ЗначениеЗаполнено(ТекущиеДанные.ДатаЗакрытия)) Тогда
			ПараметрОповещения = Новый Структура("Ссылка, Владелец, ОсновнойБанковскийСчет",
				ТекущиеДанные.Ссылка,
				ТекущиеДанные.Владелец,
				ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка"));
			Оповестить("ИзмененБанковскийСчет", ПараметрОповещения);
		Иначе
			УправлениеФормойКлиент();
		КонецЕсли;
	КонецЕсли;
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Банки = Новый Массив;
	БИКБанков = Новый Массив;
	БИКБанковСИнформациейОНастройкеПрямогоОбмена = Неопределено;
	ОтображатьСостояниеПрямогоОбмена = Ложь;
	ПроверятьКолонкиПрямогоОбмена = Ложь;
	ПроверятьИнтеграциюСБанком = Ложь;
	
	Если Настройки.ДополнительныеСвойства.Свойство("БИКБанковСИнформациейОНастройкеПрямогоОбмена",
		БИКБанковСИнформациейОНастройкеПрямогоОбмена) Тогда
		Если БИКБанковСИнформациейОНастройкеПрямогоОбмена.Количество() > 0 Тогда
			ОтображатьСостояниеПрямогоОбмена = Истина;
			ПроверятьКолонкиПрямогоОбмена = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Для каждого ЭлементСписка Из Строки Цикл
		ТекущиеДанные = ЭлементСписка.Значение.Данные;
		БИКБанков.Добавить(ТекущиеДанные.БИК);
		Банки.Добавить(ТекущиеДанные.Банк);
		
		Если ПроверятьКолонкиПрямогоОбмена Тогда
			ОтображатьСостояниеПрямогоОбмена = ТекущиеДанные.Свойство("ДиректБанкСтатус") И
				ТекущиеДанные.Свойство("ДиректБанкКартинка");
			ПроверятьИнтеграциюСБанком = ТекущиеДанные.Свойство("СтатусСчета");
			ПроверятьКолонкиПрямогоОбмена = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	РеквизитыБанков = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Банки, "РучноеИзменение");
	
	ИнформацияОКритичныхСобытияхБанков = НадежностьБанков.ИнформацияОКритичныхСобытиях(БИКБанков);
	
	Для каждого ЭлементСписка Из Строки Цикл
		ТекущиеДанные = ЭлементСписка.Значение.Данные;
		
		Если ОтображатьСостояниеПрямогоОбмена Тогда
			
			СтатусСчетБезИнтеграции = Истина;
			
			Если ПроверятьИнтеграциюСБанком Тогда
				СтатусСчетБезИнтеграции = ЗначениеЗаполнено(ТекущиеДанные.СтатусСчета);
			КонецЕсли;
			
			ЭлементСписка.Значение.Оформление["ДиректБанкКартинка"].УстановитьЗначениеПараметра("Видимость", Истина);
			ЭлементСписка.Значение.Оформление["ДиректБанкСтатус"].УстановитьЗначениеПараметра("Видимость", Ложь);
			
			ДеятельностьБанкаПрекращена = Ложь;
			РеквизитыБанка = РеквизитыБанков[ТекущиеДанные.Банк];
			Если РеквизитыБанка <> Неопределено Тогда
				ДеятельностьБанкаПрекращена = БанковскиеСчетаСервер.ДеятельностьБанкаПрекращена(РеквизитыБанка.РучноеИзменение);
			КонецЕсли;
			
			Если СтатусСчетБезИнтеграции И НЕ ЗначениеЗаполнено(ТекущиеДанные.ДатаЗакрытия)
				И НЕ ТекущиеДанные.ПометкаУдаления
				И НЕ ДеятельностьБанкаПрекращена Тогда
				
				ИнформацияОНастройкахПрямогоОбмена = БИКБанковСИнформациейОНастройкеПрямогоОбмена.Получить(ТекущиеДанные.БИК);
				ЕстьНастройка = Ложь;
				
				Если ИнформацияОНастройкахПрямогоОбмена <> Неопределено 
					И (ТекущиеДанные.Валютный И ИнформацияОНастройкахПрямогоОбмена.Валютный
						ИЛИ НЕ ТекущиеДанные.Валютный И ИнформацияОНастройкахПрямогоОбмена.Рублевый) Тогда
					
					ЕстьНастройка = ИнформацияОНастройкахПрямогоОбмена.Настройка;
					
					Если НЕ ЕстьНастройка Тогда
						ТекущиеДанные.ДиректБанкСтатус = НСтр("ru = 'Подключить'");
					КонецЕсли;
					
				КонецЕсли;
				
				ТекущиеДанные.ДиректБанкКартинка = ЕстьНастройка;
				ЭлементСписка.Значение.Оформление["ДиректБанкКартинка"].УстановитьЗначениеПараметра("Видимость",
					ЕстьНастройка);
				ЭлементСписка.Значение.Оформление["ДиректБанкСтатус"].УстановитьЗначениеПараметра("Видимость",
					НЕ ЕстьНастройка);
			КонецЕсли;
		КонецЕсли;
		
		Если ИнформацияОКритичныхСобытияхБанков.Используется Тогда
			ЕстьСобытие = ИнформацияОКритичныхСобытияхБанков.События[ТекущиеДанные.БИК] <> Неопределено;
			Если ЕстьСобытие Тогда
				ЭлементСписка.Значение.Оформление["Наименование"].УстановитьЗначениеПараметра(
					"ЦветТекста",
					ЦветаСтиля.КритичноеСобытиеНадежностьБанковЦветТекста);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", Владелец);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("БанковскийСчетОрганизации", БанковскиеСчетаОрганизаций);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыполнитьОткрытиеФормыВыбораЗавершение", ЭтотОбъект, ПараметрыФормы);
	
	ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаВыбораВидаСчета",
		ПараметрыФормы,
		,
		,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ДиректБанкСтатус = Неопределено;

	Если Элементы["ДиректБанкСтатус"] = Поле И Элементы.Список.ТекущиеДанные <> Неопределено
		И Элементы.Список.ТекущиеДанные.Свойство("ДиректБанкСтатус", ДиректБанкСтатус)
		И ДиректБанкСтатус = НСтр("ru = 'Подключить'") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ДоступнаНастройкаПрямогоОбмена")
			И Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ДоступнаНастройкаПрямогоОбмена Тогда
			
			ТекущиеДанные = Элементы.Список.ТекущиеДанные;
			ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(ТекущиеДанные.Владелец, 
				ТекущиеДанные.Банк, ТекущиеДанные.НомерСчета);	
		Иначе
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://portal.1c.ru/applications/1C-Direct-bank");		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено
		Или Не Элементы.Список.ТекущиеДанные.Свойство("Ссылка")
		Или Не Элементы.Список.ТекущиеДанные.Свойство("Владелец") Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.Ссылка = ОсновнойБанковскийСчет Тогда
		ОсновнойБанковскийСчет = ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка");
	Иначе
		ОсновнойБанковскийСчет = Элементы.Список.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	Если ФизическомуЛицуЗаписываетсяСчетИПНаСервере(Владелец, Элементы.Список.ТекущиеДанные.НомерСчета) Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВопросЗаписатьФизическомуЛицуСчетИПЗавершение", ЭтотОбъект, Новый Структура);
		
		ТекстВопроса = НСтр("ru='Номер основного счета контрагента был определен, как счет индивидуального предпринимателя.
						|
						|Изменить вид контрагента на ""Индивидуальный предприниматель""?'");
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПараметрыСписка(ЭтотОбъект);
	
	УстановкаОсновногоБанковскогоСчетаВыполнена = Ложь;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("КонтрагентОрганизация",  Элементы.Список.ТекущиеДанные.Владелец);
	СтруктураПараметров.Вставить("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
	
	Оповестить("УстановкаОсновногоБанковскогоСчета", СтруктураПараметров);
	
	// Если форма владельца закрыта, то запишем основной банковский счет самостоятельно.
	Если Не УстановкаОсновногоБанковскогоСчетаВыполнена Тогда
		УстановитьОсновнойБанковскийСчет(СтруктураПараметров);
	КонецЕсли;
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЦифровойСчет(Команда)
	
	Если Не (ЗначениеЗаполнено(Владелец) Или БанковскиеСчетаОрганизаций) Тогда
		Возврат;
	КонецЕсли;

	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", Владелец);

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЦифровойСчет", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("БанковскийСчетОрганизации", БанковскиеСчетаОрганизаций);

	ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаЭлементаЦифровойСчет", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБанковскийСчет(Команда)
	
	Если Не (ЗначениеЗаполнено(Владелец) Или БанковскиеСчетаОрганизаций) Тогда
		Возврат;
	КонецЕсли;

	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", Владелец);

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("БанковскийСчетОрганизации", БанковскиеСчетаОрганизаций);

	ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаЭлемента", ПараметрыФормы);  
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьОсновнойБанковскийСчет(СтруктураПараметров)
	
	Справочники.БанковскиеСчета.УстановитьОсновнойБанковскийСчет(
		СтруктураПараметров.КонтрагентОрганизация,
		СтруктураПараметров.ОсновнойБанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	Если БанковскиеСчетаОрганизаций И Не ОтборОрганизацияИспользование Тогда
		Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ОсновнойБанковскийСчет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ДоступноИспользоватьОсновным И ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Свойство("Ссылка") Тогда
			Элементы.ФормаИспользоватьОсновным.Пометка = ТекущиеДанные.Ссылка = ОсновнойБанковскийСчет;
		КонецЕсли;
		
		Если ТекущиеДанные.Свойство("ПометкаУдаления") И ТекущиеДанные.ПометкаУдаления Тогда
			Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		ИначеЕсли ТекущиеДанные.Свойство("ДатаЗакрытия") И ЗначениеЗаполнено(ТекущиеДанные.ДатаЗакрытия) Тогда
			Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		ИначеЕсли ТекущиеДанные.Свойство("ЦифровойСчет") И ТекущиеДанные.ЦифровойСчет Тогда
			Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
		Иначе
			Элементы.ФормаИспользоватьОсновным.Доступность = Истина;
		КонецЕсли;
	Иначе
		Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтатусыСчетов()
	
	СтатусыСчетов = Новый Структура;
	СтатусыСчетов.Вставить("СчетВРежимеИнтеграции", 0);
	СтатусыСчетов.Вставить("СчетБезИнтеграции", 1);
	
	Возврат СтатусыСчетов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыСписка(Форма)
	
	Список = Форма.Список;
	
	СтатусыСчетов = СтатусыСчетов();
	Список.Параметры.УстановитьЗначениеПараметра("СтатусСчетВРежимеИнтеграции", СтатусыСчетов.СчетВРежимеИнтеграции);
	Список.Параметры.УстановитьЗначениеПараметра("СтатусСчетБезИнтеграции", СтатусыСчетов.СчетБезИнтеграции);
	
	Если ЗначениеЗаполнено(Форма.ОсновнойБанковскийСчет) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", Форма.ОсновнойБанковскийСчет);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", Неопределено);
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("БанковскиеСчетаОрганизаций", Форма.БанковскиеСчетаОрганизаций);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Список.УсловноеОформление.Элементы.Очистить();
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаЗакрытия");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФизическомуЛицуЗаписываетсяСчетИПНаСервере(Знач Контрагент, Знач НомерСчета)
	
	Возврат Справочники.Контрагенты.ФизическомуЛицуЗаписываетсяСчетИП(Контрагент, НомерСчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ИзменитьВидКонтрагентаНаИП(Владелец)
	
	Попытка
		
		ВладелецОбъект = Владелец.ПолучитьОбъект();
		ВладелецОбъект.ИндивидуальныйПредприниматель = Истина;
		ВладелецОбъект.Записать();
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
		
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВопросЗаписатьФизическомуЛицуСчетИПЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если НЕ ИзменитьВидКонтрагентаНаИП(Владелец) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Для контрагента %1 не удалось изменить вид контрагента на ""Индивидуальный предприниматель"".'"),
							Владелец);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПараметрыСписка(ЭтотОбъект);
	
	УстановкаОсновногоБанковскогоСчетаВыполнена = Ложь;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("КонтрагентОрганизация",  Элементы.Список.ТекущиеДанные.Владелец);
	СтруктураПараметров.Вставить("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
	
	Оповестить("УстановкаОсновногоБанковскогоСчета", СтруктураПараметров);
	
	// Если форма владельца закрыта, то запишем основной банковский счет самостоятельно.
	Если Не УстановкаОсновногоБанковскогоСчетаВыполнена Тогда
		УстановитьОсновнойБанковскийСчет(СтруктураПараметров);
	КонецЕсли;
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", Владелец);
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияБанковскиеСчетаОрганизаций());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстБаннераОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВыполнитьОткрытиеФормыВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Возврат;
	КонецЕсли;

	Если РезультатЗакрытия.ЦифровойСчет Тогда 
		
		ДополнительныеПараметры.Вставить("ЦифровойСчет", Истина);
		ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаЭлементаЦифровойСчет", ДополнительныеПараметры);
		
	Иначе 
		
		ОткрытьФорму("Справочник.БанковскиеСчета.ФормаОбъекта", ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаСервере
Процедура БанкСНастройкойПрямогоОбменаНаСервере(ВладелецСчета, Банк)
	СвойстваНастроек = Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	
	Если НЕ ЗначениеЗаполнено(Банк) ИЛИ НЕ ЗначениеЗаполнено(ВладелецСчета) 
		ИЛИ ТипЗнч(Владелец) <> Тип("СправочникСсылка.Организации") ИЛИ ВладелецСчета <> Владелец
		ИЛИ НЕ СвойстваНастроек.Свойство("БИКБанковСИнформациейОНастройкеПрямогоОбмена") Тогда
		Возврат;
	КонецЕсли;
	
	БИКБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Код");
	БИКБанковСИнформациейОНастройкеПрямогоОбмена = СвойстваНастроек.БИКБанковСИнформациейОНастройкеПрямогоОбмена;
	
	Если БИКБанковСИнформациейОНастройкеПрямогоОбмена.Получить(БИКБанка) = Неопределено Тогда	
					
		ИнформацияОНастройкеПрямогоОбмена =
			Справочники.БанковскиеСчета.ИнформацияОНастройкеПрямогоОбменаСБанком(ВладелецСчета, Банк);
		
		Если ИнформацияОНастройкеПрямогоОбмена.Рублевый ИЛИ ИнформацияОНастройкеПрямогоОбмена.Валютный Тогда
			
			БИКБанковСИнформациейОНастройкеПрямогоОбмена.Вставить(БИКБанка, ИнформацияОНастройкеПрямогоОбмена);
			Элементы.ГруппаДиректБанк.Видимость = Истина;
			
		КонецЕсли;		
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоОрганизации(Форма)
	
	Если Форма.ОтборОрганизацияИспользование Тогда
		Форма.Владелец = Форма.ОтборОрганизация;
	Иначе
		Форма.Владелец = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	КонецЕсли;
	
	Форма.ОсновнойБанковскийСчет = ОсновнойБанковскийСчет(Форма.Владелец);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.Список,
		"Владелец", Форма.ОтборОрганизация, ВидСравненияКомпоновкиДанных.Равно, , Форма.ОтборОрганизацияИспользование);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновнойБанковскийСчет(Владелец)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ОсновнойБанковскийСчет");
	
КонецФункции

&НаСервере
Процедура НастроитьЭлементыПрямогоОбмена()
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		БИКБанковСИнформациейОНастройкеПрямогоОбмена =
			Справочники.БанковскиеСчета.БИКБанковСИнформациейОНастройкеПрямогоОбмена(Владелец);
	Иначе
		БИКБанковСИнформациейОНастройкеПрямогоОбмена = Новый Соответствие;
	КонецЕсли;
	
	Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("БИКБанковСИнформациейОНастройкеПрямогоОбмена",
		БИКБанковСИнформациейОНастройкеПрямогоОбмена);
	Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ДоступнаНастройкаПрямогоОбмена",
		ПравоДоступа("Редактирование", Метаданные.Справочники.НастройкиОбменСБанками));
	
	Элементы.ГруппаДиректБанк.Видимость = БИКБанковСИнформациейОНастройкеПрямогоОбмена.Количество() > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОтбораПоОрганизации()
	
	УстановитьОтборПоОрганизации(ЭтотОбъект);
	НастроитьЭлементыПрямогоОбмена();
	Элементы.Список.Обновить();
	УправлениеФормойКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект, Истина);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМККонтекстСписокФормы();
	АдаптироватьМКСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.КомандаСоздать = "Создать";
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	МобильныйКлиентБПФормаСписка.ПеренестиЭлементВМККомандыФормы(ЭтотОбъект, Элементы.ГруппаСоздать);

	МобильныйКлиентБПФормаСписка.ПеренестиКомандыВМККомандыФормы(ЭтотОбъект, Элементы.ГруппаВажныеКоманды.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаСписка.ПеренестиКомандыВМККомандыФормы(ЭтотОбъект, Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаСписка.ПеренестиКомандыВМККомандыФормы(ЭтотОбъект, Элементы.ГруппаСтандартныеКоманды.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.ФормаИспользоватьОсновным,
		Элементы.Список.КонтекстноеМеню);

КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	Элементы.Список.ПоведениеПриСжатииПоГоризонтали = ПоведениеТаблицыПриСжатииПоГоризонтали.ПереноситьЭлементыПоВажности;
	Элементы.Список.Шапка = Ложь;
	
	Элементы.Наименование.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	Элементы.СтатусСчета.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	
	Элементы.ДатаОткрытия.Формат = "ДФ='''Открыт: '' dd.MM.yyyy'";
	Элементы.ДатаЗакрытия.Формат = "ДФ='''Закрыт: '' dd.MM.yyyy'";
	
	МобильныйКлиентБПФормаСписка.СкрытьЭлементФормыСписка(ЭтотОбъект, Элементы.ГруппаДиректБанк);
	
КонецПроцедуры

#КонецОбласти
