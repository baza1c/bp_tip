
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектыПечати.ЗагрузитьЗначения(Параметры.ОбъектыПечати);
	Если ОбъектыПечати.Количество() = 1 Тогда
		ПоОднойОрганизации = Истина;
		Организация = ОбъектыПечати[0].Значение;
	КонецЕсли;
	
	ИнициализироватьФорму();
	
	МобильныйКлиентАдаптацияФормы();
	
	СформироватьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиРеквизитов

&НаКлиенте
Процедура НастройкиРеквизитовПриИзменении(Элемент)
	
	СформироватьПоНастройкам();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПечатныхФормПечататьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиРеквизитов.ТекущиеДанные;
	
	Если ТекущиеДанные.Печатать = СостояниеФлагаНеопределено() Тогда
		ТекущиеДанные.Печатать = СостояниеФлагаВыключено();
	КонецЕсли;
	
	ИзменитьСвязанныеФлажки(Элементы.НастройкиРеквизитов.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяФормыДокумента" , ИмяФормы);
	ПараметрыФормы.Вставить("ОбъектыОтправки"   , ОбъектыПечати.ВыгрузитьЗначения());
	
	ОткрытьФорму("ОбщаяФорма.ОтправкаДокументовПоЭлектроннойПочте", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьСнятьФлажки(НастройкиРеквизитов, СостояниеФлагаВключено());
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьСнятьФлажки(НастройкиРеквизитов, СостояниеФлагаВыключено());
КонецПроцедуры

&НаКлиенте
Процедура ПоУмолчанию(Команда)
	
	ЗаполнитьНастройки(Истина);
	СформироватьПоНастройкам();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	
	Элементы.ФормаНастройки.Пометка = Не Элементы.ФормаНастройки.Пометка;
	Элементы.НастройкиРеквизитов.Видимость = Элементы.ФормаНастройки.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьРедактирование(Команда)
	
	Элементы.РеквизитыОрганизации.Редактирование = Не Элементы.РеквизитыОрганизации.Редактирование;
	Элементы.ФормаПереключитьРедактирование.Пометка = Не Элементы.ФормаПереключитьРедактирование.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект, "РеквизитыОрганизации", ТипФайлаТабличногоДокумента.PDF);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьФорму()
	
	Элементы.НастройкиРеквизитов.Видимость = Ложь;
	
	Если ПоОднойОрганизации Тогда
		Заголовок = СтрШаблон(НСтр("ru='Реквизиты %1'"), Организация);
		ЗаполнитьНастройки();
		ЗаполнитьСведенияОбОрганизации();
	Иначе
		Заголовок = НСтр("ru='Реквизиты организаций'");
		Элементы.ФормаНастройки.Доступность = Ложь;
		КомандаНастройки = Команды.Найти("Настройки");
		Если КомандаНастройки <> Неопределено Тогда
			Подсказка = НСтр("ru='Настройки доступны только при печати реквизитов одной организации'");
			КомандаНастройки.Подсказка = Подсказка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизации()
	
	СведенияОбОрганизации = ПечатьРеквизитовОрганизации.СведенияДляПечати(Организация);
	АдресСведенийОбОрганизации = ПоместитьВоВременноеХранилище(СведенияОбОрганизации, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройки(ПоУмолчанию = Ложь)
	
	Настройки = ПечатьРеквизитовОрганизации.НастройкиПечати(Организация, ПоУмолчанию);
	ЗначениеВРеквизитФормы(Настройки, "НастройкиРеквизитов");
	ПроставитьФлажкиГруппНастройкиПечати();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьФлажки(СтрокаНастроек, СостояниеФлага)
	
	Для Каждого НастройкаПечатнойФормы Из СтрокаНастроек.ПолучитьЭлементы() Цикл
		НастройкаПечатнойФормы.Печатать = СостояниеФлага;
		УстановитьСнятьФлажки(НастройкаПечатнойФормы, СостояниеФлага);
	КонецЦикла;
	
	СформироватьПоНастройкам();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	РеквизитыОрганизации = ПечатьРеквизитовОрганизации.Печать(ОбъектыПечати.ВыгрузитьЗначения(), ОбъектыПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПоНастройкам()
	
	РеквизитыОрганизации = СформироватьПоНастройкамНаСервере(НастройкиРеквизитов,
		АдресСведенийОбОрганизации,
		Организация,
		ОбъектыПечати);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьПоНастройкамНаСервере(Знач НастройкиРеквизитов, АдресСведенийОбОрганизации, Организация, ОбъектыПечати)
	
	Настройки = НастройкиДляПечатиИСохранения(НастройкиРеквизитов);
	СведенияОбОрганизации = ПолучитьИзВременногоХранилища(АдресСведенийОбОрганизации);
	РеквизитыОрганизации = ПечатьРеквизитовОрганизации.ПечатьПоНастройкамИСведениям(Организация,
		Настройки,
		СведенияОбОрганизации,
		ОбъектыПечати);
	
	ПечатьРеквизитовОрганизации.ЗаписатьНастройкиПечати(Настройки, Организация);
	
	Возврат РеквизитыОрганизации;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьСвязанныеФлажки(ТекущиеДанные)
	
	РодительскийЭлемент = ТекущиеДанные.ПолучитьРодителя();
	Если РодительскийЭлемент <> Неопределено Тогда
		
		Если ФлажкиВГруппеОдинаковы(РодительскийЭлемент) Тогда
			РодительскийЭлемент.Печатать = ТекущиеДанные.Печатать;
		Иначе
			РодительскийЭлемент.Печатать = СостояниеФлагаНеопределено();
		КонецЕсли;
		
	КонецЕсли;
	
	ДочерниеЭлементы = ТекущиеДанные.ПолучитьЭлементы();
	Если ДочерниеЭлементы <> Неопределено Тогда
		Для Каждого Элемент Из ДочерниеЭлементы Цикл
			Элемент.Печатать = ТекущиеДанные.Печатать;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ФлажкиВГруппеОдинаковы(Группа)
	
	СоседниеЭлементы = Группа.ПолучитьЭлементы();
	Если СоседниеЭлементы.Количество() Тогда
		ФлагПервогоЭлемента = СоседниеЭлементы[0].Печатать;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Элемент Из СоседниеЭлементы Цикл
		Если ФлагПервогоЭлемента <> Элемент.Печатать Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеФлагаВыключено()
	Возврат 0;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеФлагаВключено()
	Возврат 1;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеФлагаНеопределено()
	Возврат 2;
КонецФункции

// Заполняет флажки групп настроек согласно флажкам подчиненных элементов:
//    0 - если все сняты
//    1 - если все проставлены
//    2 - если часть сняты, часть проставлены
//
&НаСервере
Процедура ПроставитьФлажкиГруппНастройкиПечати()
	
	Для Каждого ГруппаРезультата Из НастройкиРеквизитов.ПолучитьЭлементы() Цикл
		
		Если ГруппаРезультата.ПолучитьЭлементы().Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьПоставленныйФлажок = Ложь;
		ЕстьСнятыйФлажок = Ложь;
		Для Каждого СтрокаРезультата Из ГруппаРезультата.ПолучитьЭлементы() Цикл
			Если СтрокаРезультата.Печатать Тогда
				ЕстьПоставленныйФлажок = Истина;
			Иначе
				ЕстьСнятыйФлажок = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьПоставленныйФлажок И ЕстьСнятыйФлажок Тогда
			ГруппаРезультата.Печатать = СостояниеФлагаНеопределено();
		ИначеЕсли ЕстьПоставленныйФлажок Тогда
			ГруппаРезультата.Печатать = СостояниеФлагаВключено();
		Иначе
			ГруппаРезультата.Печатать = СостояниеФлагаВыключено();
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиДляПечатиИСохранения(НастройкиРеквизитов)
	
	ДеревоНастроек = ПечатьРеквизитовОрганизации.НовоеДеревоНастроекПечати();
	
	// Не копируем дерево, т.к. поле Печатать у коллекций разных типов
	Для Каждого ГруппаНастроек Из НастройкиРеквизитов.ПолучитьЭлементы() Цикл
		ГруппаДереваНастроек = ДеревоНастроек.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(ГруппаДереваНастроек, ГруппаНастроек);
		Для Каждого СтрокаНастроек Из ГруппаНастроек.ПолучитьЭлементы() Цикл
			СтрокаДереваНастроек = ГруппаДереваНастроек.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДереваНастроек, СтрокаНастроек);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоНастроек;
		
КонецФункции

#КонецОбласти

#Область МобильныйКлиент
&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаОтправить",
		"Заголовок",
		НСтр("ru = 'Отправить по почте'"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаОтправить",
		"КнопкаПоУмолчанию",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаСохранить",
		"Заголовок",
		НСтр("ru = 'Отправить в ...'"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаСохранить",
		"Картинка",
		БиблиотекаКартинок.ПередачаДанныхНаУстройствеМК);
		
	КомандыМК = "ФормаОтправить,ФормаСохранить";
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанельФормаОтчета(ЭтотОбъект, КомандыМК);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВМККомандыФормы(Элементы, Элементы.ФормаНастройки);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	ТаблицаНастройкиРеквизитов = Элементы.Найти("НастройкиРеквизитов");
	Если ТаблицаНастройкиРеквизитов <> Неопределено Тогда
		ТаблицаНастройкиРеквизитов.ПоведениеПриСжатииПоГоризонтали =
			ПоведениеТаблицыПриСжатииПоГоризонтали.ПереноситьЭлементыПоВажности;
		ТаблицаНастройкиРеквизитов.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
		ТаблицаНастройкиРеквизитов.РежимВыбора = Истина;
	КонецЕсли;

	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.РеквизитыОрганизации);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.НастройкиРеквизитов);
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

