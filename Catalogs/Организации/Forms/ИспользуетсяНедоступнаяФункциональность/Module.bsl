
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьТекстРекомендаций();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеЧтоСделатьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = ТарификацияБПКлиентСервер.ИмяДействияОплатаСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		ТарификацияБПКлиент.ОткрытьФормуОплатыСервиса();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = ТарификацияБПКлиентСервер.ИмяДействияНастройкиОрганизации() Тогда
		СтандартнаяОбработка = Ложь;
		ТарификацияБПКлиент.ПерейтиВНастройкиОрганизации();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекстРекомендаций()
	
	ПредставлениеВидовОрганизаций = 
		Справочники.Организации.ПредставленияИспользуемыхВидовОрганизацийБезТарифа();
	
	ТекстРекомендаций = Новый Массив;
	ТекстРекомендаций.Добавить(СтрШаблон(
		НСтр("ru = 'На текущем тарифе учет %1 недоступен.'"), ПредставлениеВидовОрганизаций));
	ТекстРекомендаций.Добавить(" ");
	ТекстРекомендаций.Добавить(НСтр("ru = 'Возможность проведения документов заблокирована.'"));
	
	Элементы.ОписаниеЗаголовок.Заголовок = Новый ФорматированнаяСтрока(ТекстРекомендаций);
	
	ПредставленияДоступныхИзменений = 
		Справочники.Организации.ПредставленияДоступныхИзмененийИспользуемыхВидовОрганизацийБезТарифа(Ложь, Истина);
	
	Если Не ПустаяСтрока(ПредставленияДоступныхИзменений) Тогда
		ТекстРекомендаций = Новый Массив;
		ТекстРекомендаций.Добавить(НСтр("ru = 'Чтобы восстановить возможность проведения документов,'"));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(ПредставленияДоступныхИзменений);
		Элементы.ОписаниеФункциональность.Заголовок = Новый ФорматированнаяСтрока(ТекстРекомендаций);
	Иначе
		Элементы.ОписаниеФункциональность.Видимость = Ложь;
	КонецЕсли;
	
	ТекстРекомендаций = СтрШаблон(
		НСтр("ru = 'Если вам нужен учет %1, <a href=""%2"">перейдите на другой тариф</a>'"),
		ПредставлениеВидовОрганизаций,
		ТарификацияБПКлиентСервер.ИмяДействияОплатаСервиса());
	
	Элементы.ОписаниеТариф.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ТекстРекомендаций);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаКартинкаОписание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
КонецПроцедуры

#КонецОбласти