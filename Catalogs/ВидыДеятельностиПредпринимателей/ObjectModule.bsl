#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу() Тогда
		
		ПерваяНоменклатурнаяГруппа = РегистрыСведений.ВидыДеятельностиПредпринимателей.ПолучитьПервуюНоменклатурнуюГруппу(
			Ссылка, ХарактерДеятельности);
		
		Если Не ЗначениеЗаполнено(ПерваяНоменклатурнаяГруппа) Тогда
			МенеджерЗаписиРегистра = РегистрыСведений.ВидыДеятельностиПредпринимателей.СоздатьМенеджерЗаписи();
			МенеджерЗаписиРегистра.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
			МенеджерЗаписиРегистра.ВидДеятельности      = Ссылка;
			МенеджерЗаписиРегистра.ХарактерДеятельности = ХарактерДеятельности;
			МенеджерЗаписиРегистра.Записать(Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	// Очистка регистра сведений от записей не соответствующих заданному характеру деятельности
	РегистрыСведений.ВидыДеятельностиПредпринимателей.ОчиститьСписокНоменклатуныхГрупп(Ссылка, ХарактерДеятельности);
	
	Если ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
		
		ВидДеятельности = РегистрыСведений.ВидыДеятельностиПредпринимателей.ПолучитьВидДеятельности(
			ХарактерДеятельности, НоменклатурнаяГруппа);
		
		Если ВидДеятельности <> Ссылка Тогда
			// В качестве основной номенклатурной группы указана номенклатурная групппа
			// не связанная с видом деятельности
			НоменклатурнаяГруппа = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу()
		И Не ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
		
		НоменклатурнаяГруппа = БухгалтерскийУчетВызовСервераПовтИсп.ОсновнаяНоменклатурнаяГруппа();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
