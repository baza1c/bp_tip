
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Форма    = ПараметрыВыполненияКоманды.Источник;
	Документ = ПараметрКоманды;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ЭтоВозвратПоСБП")
		И Форма.ЭтоВозвратПоСБП Тогда
		Если Форма.ДоступностьВозвратПоСБП Тогда
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ДокументСсылка", Документ);
			ДополнительныеПараметры.Вставить("Форма",          Форма);
			
			Форма.ТекстПредупрежденияСБП = "";
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьЧекаПослеВозвратаСБПЗавершение",
				ЭтотОбъект, ДополнительныеПараметры);
			
			ИнтеграцияССБПБПКлиент.ВозвратОплатыСчет(
				Документ,
				Форма,
				ОписаниеОповещения);
		ИначеЕсли ЗначениеЗаполнено(Форма.ТекстПредупрежденияСБП) Тогда
			ШаблонСообщения = НСтр("ru = 'Невозможно пробить чек. %1'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ШаблонСообщения, Форма.ТекстПредупрежденияСБП));
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьЧекаЗавершение", ЭтотОбъект, Новый Структура("Форма", Форма));
	
	ПечатьФискальныхДокументовКлиент.НапечататьЧек(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник, ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧекаПослеВозвратаСБПЗавершение(РезультатОперации, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатОперации.СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОперации.СообщениеОбОшибке);
	КонецЕсли;
	
	Форма  = ДополнительныеПараметры.Форма;
	Объект = Форма.Объект;
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("Ссылка",          Объект.Ссылка);
	ДанныеДокумента.Вставить("Организация",     Объект.Организация);
	ДанныеДокумента.Вставить("БанковскийСчет",  Объект.БанковскийСчет);
	ДанныеДокумента.Вставить("ЭтоВозвратПоСБП", Форма.ЭтоВозвратПоСБП);
	
	Результат = ИнтеграцияССБПБПВызовСервера.ДанныеВозвратОплаты(ДанныеДокумента);
	СостояниеВозврата = ОплатаПлатежнойКартойКлиентСервер.СостояниеВозвратаСБП(Результат);
	ЗаполнитьЗначенияСвойств(Форма, СостояниеВозврата);
	
	ОплатаПлатежнойКартойКлиентСервер.УправлениеФормой(Форма);
	
	Если Не РезультатОперации.Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьЧекаЗавершение", ЭтотОбъект, Новый Структура("Форма", Форма));
	ПечатьФискальныхДокументовКлиент.НапечататьЧек(ДополнительныеПараметры.ДокументСсылка, Форма, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧекаЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	Если НЕ РезультатВыполнения.Успешно Тогда
		Если ЗначениеЗаполнено(РезультатВыполнения.СообщениеОбОшибке) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатВыполнения.СообщениеОбОшибке);
		КонецЕсли;
		
		Возврат;
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДополнительныеПараметры, РезультатВыполнения);
	
	Если НЕ РезультатВыполнения.ЭтоОблачнаяККТ Тогда
		ПослеПечатиЧека(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ИдентификаторФормы = ДополнительныеПараметры.Форма.УникальныйИдентификатор;
	ДокументОснование = ДополнительныеПараметры.ПараметрыОперации.ДокументОснование;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ПечатьФискальныхДокументовВызовСервера.ДлительнаяОперацияПроверкаСтатуса(ДокументОснование, ИдентификаторФормы), 
		Новый ОписаниеОповещения("ПроверкаСтатусаЧекаЗавершение", ЭтотОбъект, ДополнительныеПараметры), 
		ДлительныеОперацииКлиент.ПараметрыОжидания(ДополнительныеПараметры.Форма));
КонецПроцедуры 

&НаКлиенте
Процедура ПроверкаСтатусаЧекаЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	Если НЕ ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполнения,"Статус") = "Выполнено" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось пробить чек на облачной кассе.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ПараметрыОперации", ДополнительныеПараметры.ПараметрыОперации);
	ПараметрыПроверки.Вставить("ОповещениеПриЗавершении");
	ПараметрыПроверки.Вставить("ПечатающееУстройствоДополнительныхДокументов"); 
	ПараметрыПроверки.Вставить("ПодключенноеПечатающееУстройствоДополнительныхДокументов");
	ПараметрыПроверки.Вставить("ИдентификаторУстройства", ДополнительныеПараметры.ИдентификаторУстройства);
	
	ОборудованиеЧекопечатающиеОблачныеККТКлиент.ПробитиеЧека(ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата), ПараметрыПроверки);
	
	ПослеПечатиЧека(ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ПослеПечатиЧека(ДополнительныеПараметры) Экспорт
	Форма =  ДополнительныеПараметры.Форма;
	Объект = Форма.Объект;
	
	Объект.НомерЧекаККМ = ДополнительныеПараметры.НомерЧекаККМ;
	
	// Параметры операции при оплате картой
	СсылочныйНомер = Неопределено;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "СсылочныйНомер")
		И ДополнительныеПараметры.ПараметрыОплаты.Свойство("СсылочныйНомер", СсылочныйНомер) И СсылочныйНомер = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект, ДополнительныеПараметры.ПараметрыОплаты, , "СсылочныйНомер");
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, ДополнительныеПараметры.ПараметрыОплаты);
	КонецЕсли;
	
	ГруппаПредупреждениеОПечатиЧека = Форма.Элементы.Найти("ПредупреждениеОПечатиЧека");
	Если ГруппаПредупреждениеОПечатиЧека <> Неопределено Тогда
		ГруппаПредупреждениеОПечатиЧека.Видимость = Ложь;
	КонецЕсли; 
	
	Форма.Записать();
	
	Оповестить("ВыполненаЗаписьДокумента", Новый Структура("ДокументСсылка", Объект.Ссылка));
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка);

КонецПроцедуры 

