
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаТовары"
		ИЛИ ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаОбщая" Тогда
		ТекущийЭлементСписка = ПараметрыВыполненияКоманды.Источник.Объект;
	ИначеЕсли ПараметрыВыполненияКоманды.Источник.Элементы.Найти("Список") <> Неопределено Тогда
		ТекущийЭлементСписка = ПараметрыВыполненияКоманды.Источник.Элементы.Список.ТекущиеДанные;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ТекущийЭлементСписка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = УчетНДСВызовСервера.РеквизитыДокументаОснованияСтатформы(ТекущийЭлементСписка.Ссылка);
	
	Если Реквизиты = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Невозможно создать отчет ""Статистическая форма учета перемещения товаров"". Нет доступа к реквизитам документа.'"));
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.ПометкаУдаления Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Отчет ""Статистическая форма учета перемещения товаров"" нельзя создать на основании документа, помеченного на удаление.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ СтранаРеализацииЕАЭС(Реквизиты.Ссылка) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Невозможно создать отчет ""Статистическая форма учета перемещения товаров"": страна реализации не является членом ЕАЭС.'"));
		Возврат;
	КонецЕсли;
	
	СсылкаНаСуществующуюСтатформу = УчетНДСВызовСервера.ОпределитьСсылкуНаСуществующуюСтатформу(Реквизиты);
	НаправлениеПеремещения = "ЭК";
	УчетНДСКлиент.ОткрытьСтатформу(НаправлениеПеремещения, Реквизиты, СсылкаНаСуществующуюСтатформу);
	
КонецПроцедуры

&НаСервере
Функция СтранаРеализацииЕАЭС(ДокументОтгрузки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеализацияТоваровУслуг.СтранаРеализации, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.СтранаРеализации
	|		ИНАЧЕ ЕСТЬNULL(РеализацияТоваровУслуг.Контрагент.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
	|	КОНЕЦ КАК СтранаРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОтгрузки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если ЗначениеЗаполнено(Выборка.СтранаРеализации) Тогда
		Если Выборка.СтранаРеализации <> Справочники.СтраныМира.Россия Тогда
			УчастникЕАЭС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.СтранаРеализации, "УчастникЕАЭС");
			Если ЗначениеЗаполнено(УчастникЕАЭС) Тогда
				Возврат УчастникЕАЭС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции
