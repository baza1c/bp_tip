
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Отказ = Ложь;
	ПроверитьВозможностьФормированияПлатежныхПоручений(ПараметрКоманды, Отказ);
	
	Если НЕ Отказ Тогда
		ОткрытьФорму("Обработка.ВыплатаЗарплатыПлатежнымиПоручениями.Форма", Новый Структура("ПлатежнаяВедомость", ПараметрКоманды), ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьФормированияПлатежныхПоручений(ПараметрКоманды, Отказ)
	
	ВидМестаВыплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрКоманды, "ВидМестаВыплаты");
	Если ВидМестаВыплаты <> Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет Тогда
		Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
			ТекстСообщения = НСтр("ru = 'Для формирования платежных поручений укажите место выплаты ""Перечислением на счет в банке""!'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Платежные поручения на каждого работника не формируются для перечисления по зарплатному проекту!'");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры