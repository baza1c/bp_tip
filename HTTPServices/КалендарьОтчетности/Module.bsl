#Область ОбработчикиСобытий

Функция ПроверкаПодключенияПроверить(Запрос)
	
	Возврат Ответ(ЗапросыREST.КодСтандартногоСостояния());
	
КонецФункции

Функция ОтчетныеКампанииЗапросыЗапросить(Запрос)
	
	ТекстЗапроса = "";
	Попытка
		
		ТекстЗапроса = Запрос.ПолучитьТелоКакСтроку();
		Возврат ЗапроситьДанныеПриложения(ТекстЗапроса);
		
	Исключение
		
		ЗаписатьИсключениеHTTPСервиса(
			ИнформацияОбОшибке(),
			Метаданные.HTTPСервисы.КалендарьОтчетности.ШаблоныURL.ОтчетныеКампанииЗапросы.Методы.Запросить,
			ТекстЗапроса);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецФункции

Функция ОтчетныеКампанииОтветыПолучить(Запрос)
	
	Попытка
		
		Идентификатор = Запрос.ПараметрыURL["RequestId"];
		Ошибка = ИдентификаторЗапроса(Идентификатор);
		Если ЗначениеЗаполнено(Ошибка) Тогда
			Возврат Ошибка;
		КонецЕсли;
		
		Возврат ПолучитьДанныеПриложения(Идентификатор);
		
	Исключение
		
		ЗаписатьИсключениеHTTPСервиса(
			ИнформацияОбОшибке(),
			Метаданные.HTTPСервисы.ExternalAPI.ШаблоныURL.ОтчетныеКампанииЗапросы.Методы.Получить);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецФункции

Функция ОтчетныеКампанииОтветыПодтвердитьПолучение(Запрос)
	
	Попытка
		
		Идентификатор = Запрос.ПараметрыURL["RequestId"];
		Ошибка = ИдентификаторЗапроса(Идентификатор);
		Если ЗначениеЗаполнено(Ошибка) Тогда
			Возврат Ошибка;
		КонецЕсли;
		
		Возврат ПодтвердитьПолучениеДанныхПриложения(Идентификатор);
		
	Исключение
		
		ЗаписатьИсключениеHTTPСервиса(
			ИнформацияОбОшибке(),
			Метаданные.HTTPСервисы.ExternalAPI.ШаблоныURL.ОтчетныеКампанииЗапросы.Методы.ПодтвердитьПолучение);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СообщенияОблачныхПриложений

Функция ЗапроситьДанныеПриложения(ТекстЗапроса)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТребуемыеПоля = Новый Массив;
	ТребуемыеПоля.Добавить("ТипЗапроса");
	
	ДанныеЗапроса = ДанныеЗапроса(ТекстЗапроса, ТребуемыеПоля);
	Если ТипЗнч(ДанныеЗапроса) = Тип("Строка") Тогда
		Возврат Ответ(ЗапросыREST.КодСостоянияПлохойЗапрос(), ДанныеЗапроса);
	КонецЕсли;
	
	// В общем случае запрос должен быть обработан асинхронно. Но есть ситуации, когда запрос не требует длительного времени
	// для обработки и может быть обработан сразу же (например, получение списка организаций приложения).
	Если ДанныеЗапроса.Свойство("ВыполнитьСинхронно") И ДанныеЗапроса.ВыполнитьСинхронно Тогда
		ДанныеОтвета = СообщенияОтчетностиОблачныхПриложений.ОтветНаЗапросОблачногоПриложения(ТекстЗапроса);
		Возврат Ответ(ЗапросыREST.КодСтандартногоСостояния(), ДанныеОтвета);
	КонецЕсли;
	
	НовыйЗапрос = РегистрыСведений.ЗапросыОблачныхПриложений.Создать(ДанныеЗапроса);
		
	Если НовыйЗапрос = Неопределено Тогда
		Возврат Ответ(ЗапросыREST.КодСостоянияВнутренняяОшибкаСервера(), Ошибка(Нстр("ru = 'Новый запрос не может быть создан.'")));
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("Идентификатор", НовыйЗапрос);
	Возврат Ответ(ЗапросыREST.КодСтандартногоСостояния(), ДанныеОтвета);
	
КонецФункции

Функция ПолучитьДанныеПриложения(ИдентификаторЗапроса)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Данные = РегистрыСведений.ЗапросыОблачныхПриложений.ПрочитатьОтвет(
		Новый УникальныйИдентификатор(ИдентификаторЗапроса));
		
	Если Данные = Неопределено Тогда
		Возврат Ответ(ЗапросыREST.КодСостоянияНетДанных());
	ИначеЕсли Данные = "" Тогда
		Возврат Ответ(ЗапросыREST.КодСостоянияОтсутствуетСодержимое());
	КонецЕсли;
	
	Возврат Ответ(ЗапросыREST.КодСтандартногоСостояния(), Данные);
	
КонецФункции

Функция ПодтвердитьПолучениеДанныхПриложения(ИдентификаторЗапроса)
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.ЗапросыОблачныхПриложений.ОтметитьПрочитано(
		Новый УникальныйИдентификатор(ИдентификаторЗапроса));
	
	Возврат Ответ(ЗапросыREST.КодСтандартногоСостояния());
	
КонецФункции

#КонецОбласти

Функция Ответ(КодОтвета, Данные = "")
	
	Ответ = Новый HTTPСервисОтвет(КодОтвета);
	
	Ответ.Заголовки.Вставить("Accept-Charset", "utf-8");
	Ответ.Заголовки.Вставить("Content-Type", "application/json;charset=utf-8");
	Ответ.Заголовки["Cache-Control"] = "no-cache";
	
	Ответ.УстановитьТелоИзСтроки(ДанныеJSON(Данные));
	
	Возврат Ответ;
	
КонецФункции

Функция Ошибка(ТекстОшибки)
	
	Возврат Новый Структура("Ошибка", ТекстОшибки);
	
КонецФункции

Функция ДанныеJSON(Данные)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, " ");
	ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

Функция ДанныеЗапроса(ТекстЗапроса, ТребуемыеПоля = Неопределено, СвойстваТипаДата = Неопределено)
	
	Если ТипЗнч(ТекстЗапроса) <> Тип("Строка") Или Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат НСтр("ru = 'Отсутствует текст запроса'");
	КонецЕсли;
	
	Попытка
		Запрос = ОбщегоНазначенияБП.СтруктураИзСтрокиJSON(ТекстЗапроса, СвойстваТипаДата);
	Исключение
		Возврат НСтр("ru = 'Объект JSON описан неверно'");
	КонецПопытки;

	Если ТребуемыеПоля <> Неопределено Тогда
		ОтсутствующиеПоля = "";
		Для Каждого Поле Из ТребуемыеПоля Цикл
			Если Не Запрос.Свойство(Поле) Тогда
				ОтсутствующиеПоля = ?(ЗначениеЗаполнено(ОтсутствующиеПоля),
					СтрШаблон("%1, %2", ОтсутствующиеПоля, Поле),
					Поле);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ОтсутствующиеПоля) Тогда
			Возврат СтрШаблон(НСтр("ru = 'Отсутствуют обязательные поля: %1'"), ОтсутствующиеПоля);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

Функция ИдентификаторЗапроса(ИдентификаторЗапроса)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗапроса) Тогда
		Возврат Ответ(ЗапросыREST.КодСостоянияПлохойЗапрос(), Ошибка(НСтр("ru = 'Не указан идентификатор запроса'")));
	КонецЕсли;
	
КонецФункции

Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат "ReportingCycles";
	
КонецФункции

Процедура ЗаписатьИсключениеHTTPСервиса(ИнформацияОбОшибке, Метод, ТекстЗапроса = "")
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,
		Метод,
		ТекстЗапроса,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	
КонецПроцедуры

#КонецОбласти