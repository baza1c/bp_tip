#Область ОбработчикиСобытий

Функция ВерсияИнтерфейсаGET(Запрос)
	
	Попытка
		Возврат ОтветВерсияИнтерфейсаGET(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция СпискиИзмененийСинхронизируемыхОбъектовPOST(Запрос)
	
	Попытка
		Возврат ОтветСпискиИзмененийСинхронизируемыхОбъектовPOST(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция СписокИзмененийСинхронизируемыхОбъектовGET(Запрос)
	
	Попытка
		Возврат ОтветСписокИзмененийСинхронизируемыхОбъектовGET(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция СписокИзмененийСинхронизируемыхОбъектовDELETE(Запрос)
	
	Попытка
		Возврат ОтветСписокИзмененийСинхронизируемыхОбъектовDELETE(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция НастройкиПриложенияGET(Запрос)
	
	Попытка
		Возврат ОтветНастройкиПриложенияGET(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция НастройкиСовместногоИспользованияGET(Запрос)
	
	Попытка
		Возврат ОтветНастройкиСовместногоИспользованияGET(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция НастройкиСовместногоИспользованияPUT(Запрос)
	
	Попытка
		Возврат ОтветНастройкиСовместногоИспользованияPUT(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция УчетнаяПолитикаGET(Запрос)
	
	Попытка
		Возврат ОтветУчетнаяПолитикаGET(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция ДанныеПубличныхИдентификаторовPOST(Запрос)
	
	Попытка
		Возврат ОтветДанныеПубличныхИдентификаторовPOST(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

Функция СозданиеОрганизацииPUT(Запрос)
	
	Попытка
		Возврат ОтветСозданиеОрганизацииPUT(Запрос);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(500);
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтветВерсияИнтерфейсаGET(Запрос)
	
	ОписаниеВерсии = Новый Структура;
	ОписаниеВерсии.Вставить("Версия", 1);
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	
	Ответ.УстановитьТелоИзСтроки(ДанныеВСтрокуJSON(ОписаниеВерсии));
	
	Возврат Ответ;
	
КонецФункции

Функция ОтветСпискиИзмененийСинхронизируемыхОбъектовPOST(Запрос)
	
	Попытка
		ДанныеЗапроса = ОбщегоНазначения.JSONВЗначение(Запрос.ПолучитьТелоКакСтроку());
		
		ИдентификаторНастройки = ДанныеЗапроса.Получить("ИдентификаторНастройки");
		
		Если Не ЗначениеЗаполнено(ИдентификаторНастройки) Тогда
			ВызватьИсключение НСтр("ru = 'Некорректный запрос: не задан ИдентификаторНастройки'");
		КонецЕсли;
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	НастройкаСсылка = Справочники.НастройкиСовместногоИспользованияПриложений.НайтиПоКоду(ИдентификаторНастройки);
	Если НастройкаСсылка.Пустая() Тогда
		Возврат Новый HTTPСервисОтвет(404);
	КонецЕсли;
	
	СпискиИзмененийОбъектов = РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.СпискиИзмененийСинхронизируемыхОбъектов(НастройкаСсылка);
	
	СпискиИзменений = Новый Массив;
	Для Каждого СтрокаСписка Из СпискиИзмененийОбъектов Цикл
		ОписаниеСписка = Новый Структура;
		ОписаниеСписка.Вставить("Идентификатор", XMLСтрока(СтрокаСписка.МеткаВремени));
		ОписаниеСписка.Вставить("КоличествоЗаписей", СтрокаСписка.КоличествоЗаписей);
		СпискиИзменений.Добавить(ОписаниеСписка);
	КонецЦикла;
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	
	Ответ.УстановитьТелоИзСтроки(ДанныеВСтрокуJSON(СпискиИзменений));
	
	Возврат Ответ;
	
КонецФункции

Функция ОтветСписокИзмененийСинхронизируемыхОбъектовGET(Запрос)
	
	Попытка
		МеткаВремени = XMLЗначение(Тип("Число"), Запрос.ПараметрыURL.Получить("snapshot-id"));
		
		Если МеткаВремени <= 0 Тогда
			ВызватьИсключение НСтр("ru = 'Некорректный запрос.'");
		КонецЕсли;
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	ТаблицаДанные = Новый ТаблицаЗначений;
	
	// Поля с данными объектов.
	ТаблицаДанные.Колонки.Добавить("ПубличныйИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("ПометкаУдаления", Новый ОписаниеТипов("Булево"));
	ТаблицаДанные.Колонки.Добавить("Проведен", Новый ОписаниеТипов("Булево"));
	ТаблицаДанные.Колонки.Добавить("Номер", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("ВидОперации", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("СуммаДокумента", Новый ОписаниеТипов("Число"));
	ТаблицаДанные.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("РазделУчета", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Организация", Новый ОписаниеТипов("Строка"));
	// Служебные поля общей таблицы.
	ТаблицаДанные.Колонки.Добавить("ИдентификаторМетаданных", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("ТипДанных", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("НавигационнаяСсылка", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("ВерсияФорматаДанных", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("ДанныеОбъекта", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("ПроблемыПриОбмене", Новый ОписаниеТипов("Строка"));
	
	ВерсияФорматаДанных = СовместноеИспользованиеПереопределяемый.ВерсияФорматаДанных();
	УзелОбмена = СовместноеИспользованиеПовтИсп.ТекущийУзелОбмена();
	
	ТипыСсылок = РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.ЗарегистрированныеТипыСсылок(МеткаВремени);
	
	ИдентификаторыОрганизаций = ПубличныеИдентификаторыОрганизаций(УзелОбмена);
	
	Для Каждого ТипСсылки Из ТипыСсылок Цикл
		МенеджерВТ = Новый МенеджерВременныхТаблиц;
		
		РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.ПодготовитьТаблицуИдентификаторов(МенеджерВТ, МеткаВремени, ТипСсылки, УзелОбмена);
		
		ИсточникДанных = Неопределено;
		ПоляДанныхОбъекта = "";
		
		СовместноеИспользованиеПереопределяемый.ПриПолученииДанныхСинхронизируемыхОбъектов(ТипСсылки, МенеджерВТ, ИсточникДанных, ПоляДанныхОбъекта);
		
		Если ИсточникДанных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатыОбмена = ТаблицаРезультатовОбменаДанными(МенеджерВТ, УзелОбмена);
		
		ИдентификаторМетаданных = XMLСтрока(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипСсылки));
		ТипДанныхСтрокой = Метаданные.НайтиПоТипу(ТипСсылки).ПолноеИмя();
		
		Если ТипЗнч(ИсточникДанных) = Тип("РезультатЗапроса") Тогда
			КоллекцияДанных = ИсточникДанных.Выгрузить();
		ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("ТаблицаЗначений") Тогда
			КоллекцияДанных = ИсточникДанных;
		Иначе
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаДанных Из КоллекцияДанных Цикл
			НоваяСтрока = ТаблицаДанные.Добавить();
			НоваяСтрока.ИдентификаторМетаданных = ИдентификаторМетаданных;
			НоваяСтрока.ТипДанных = ТипДанныхСтрокой;
			НоваяСтрока.НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(СтрокаДанных.Ссылка);
			НоваяСтрока.Ссылка = XMLСтрока(СтрокаДанных.Ссылка);
			
			СтрокиИдентификаторыОрганизаций = ИдентификаторыОрганизаций.НайтиСтроки(Новый Структура("Организация", СтрокаДанных.Организация));
			Если СтрокиИдентификаторыОрганизаций.Количество() > 0 Тогда
				НоваяСтрока.Организация = СтрокиИдентификаторыОрганизаций[0].Идентификатор;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(
				НоваяСтрока,
				СтрокаДанных,
				"ПубличныйИдентификатор,Идентификатор,ПометкаУдаления,Проведен,Номер,Дата,ВидОперации,СуммаДокумента,Представление,РазделУчета");
			
			НоваяСтрока.ВерсияФорматаДанных = ВерсияФорматаДанных;
			Если Не ПустаяСтрока(ПоляДанныхОбъекта) Тогда
				ДанныеОбъекта = Новый Структура(ПоляДанныхОбъекта);
				ЗаполнитьЗначенияСвойств(ДанныеОбъекта, СтрокаДанных);
				
				НоваяСтрока.ДанныеОбъекта = ДанныеВСтрокуJSON(ДанныеОбъекта);
			КонецЕсли;
			
			ПроблемыПриОбмене = Новый Массив;
			
			СтрокиРезультаты = РезультатыОбмена.НайтиСтроки(Новый Структура("ПроблемныйОбъект", СтрокаДанных.Ссылка));
			Для Каждого СтрокаРезультаты Из СтрокиРезультаты Цикл
				ОписаниеРезультата = Новый Структура("ТипПроблемы,Причина,Пропущена");
				ЗаполнитьЗначенияСвойств(ОписаниеРезультата, СтрокаРезультаты);
				
				ПроблемыПриОбмене.Добавить(ОписаниеРезультата);
			КонецЦикла;
			
			Если ПроблемыПриОбмене.Количество() > 0 Тогда
				НоваяСтрока.ПроблемыПриОбмене = ДанныеВСтрокуJSON(ПроблемыПриОбмене);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	Ответ.УстановитьТелоИзСтроки(ТаблицаЗначенийВJSON(ТаблицаДанные, "Дата:Дата"));
	
	Возврат Ответ;
	
КонецФункции

Функция ОтветСписокИзмененийСинхронизируемыхОбъектовDELETE(Запрос)
	
	Попытка
		МеткаВремени = XMLЗначение(Тип("Число"), Запрос.ПараметрыURL.Получить("snapshot-id"));
		
		Если МеткаВремени <= 0 Тогда
			ВызватьИсключение НСтр("ru = 'Некорректный запрос.'");
		КонецЕсли;
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.УдалитьРегистрацию(МеткаВремени);
	
	Возврат Новый HTTPСервисОтвет(204);
	
КонецФункции

Функция ОтветНастройкиПриложенияGET(Запрос)
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("ИспользуетсяНесколькоОрганизаций", Справочники.Организации.ИспользуетсяНесколькоОрганизаций());
	ДанныеОтвета.Вставить("КоличествоОрганизаций", Справочники.Организации.КоличествоОрганизаций());
	ДанныеОтвета.Вставить("НачалоРаботы", ПолучитьФункциональнуюОпцию("НачалоРаботы"));
	ДанныеОтвета.Вставить("ВидБизнесаОсновной", XMLСтрока(Константы.ВидБизнесаОсновной.Получить()));
	ДанныеОтвета.Вставить("ПолныйИнтерфейс", ОбщегоНазначенияБП.ЭтоПолныйИнтерфейс());
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	Ответ.УстановитьТелоИзСтроки(ДанныеВСтрокуJSON(ДанныеОтвета));
	
	Возврат Ответ;
	
КонецФункции

Функция ОтветНастройкиСовместногоИспользованияGET(Запрос)
	
	Попытка
		ИдентификаторНастройки = Строка(Новый УникальныйИдентификатор(Запрос.ПараметрыURL.Получить("app-id")));
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	НастройкаСсылка = Справочники.НастройкиСовместногоИспользованияПриложений.НайтиПоКоду(ИдентификаторНастройки);
		
	Если НастройкаСсылка.Пустая() Тогда
		Возврат Новый HTTPСервисОтвет(404);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиСовместногоИспользованияПриложений.УзелОбмена КАК УзелОбмена,
	|	НастройкиСовместногоИспользованияПриложений.ДатаНачалаОтражения КАК ДатаНачалаОтражения,
	|	НастройкиСовместногоИспользованияПриложений.ОтражениеВключено КАК ОтражениеВключено,
	|	НастройкиСовместногоИспользованияПриложений.РазделыУчета.(
	|		Идентификатор КАК Идентификатор
	|	) КАК РазделыУчета,
	|	НастройкиСовместногоИспользованияПриложений.Организации.(
	|		Организация КАК Организация
	|	) КАК Организации
	|ИЗ
	|	Справочник.НастройкиСовместногоИспользованияПриложений КАК НастройкиСовместногоИспользованияПриложений
	|ГДЕ
	|	НастройкиСовместногоИспользованияПриложений.Ссылка = &НастройкаСсылка");
	Запрос.УстановитьПараметр("НастройкаСсылка", НастройкаСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("Идентификатор", ИдентификаторНастройки);
	ДанныеОтвета.Вставить("ДатаНачалаОтражения", Выборка.ДатаНачалаОтражения);
	ДанныеОтвета.Вставить("ОтражениеВключено", Выборка.ОтражениеВключено);
	
	ЗапросПИ = Новый Запрос(
	"ВЫБРАТЬ
	|	ПубличныеИдентификаторыСинхронизируемыхОбъектов.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов КАК ПубличныеИдентификаторыСинхронизируемыхОбъектов
	|ГДЕ
	|	ПубличныеИдентификаторыСинхронизируемыхОбъектов.Ссылка В(&СписокОрганизаций)
	|	И ПубличныеИдентификаторыСинхронизируемыхОбъектов.УзелИнформационнойБазы = &УзелОбмена");
	ЗапросПИ.УстановитьПараметр("СписокОрганизаций", Выборка.Организации.Выгрузить().ВыгрузитьКолонку("Организация"));
	ЗапросПИ.УстановитьПараметр("УзелОбмена", Выборка.УзелОбмена);
	
	ТаблицаПИ = ЗапросПИ.Выполнить().Выгрузить();
	
	ДанныеОтвета.Вставить("Организации", ТаблицаПИ.ВыгрузитьКолонку("Идентификатор"));
	
	ДанныеОтвета.Вставить("РазделыУчета", Выборка.РазделыУчета.Выгрузить().ВыгрузитьКолонку("Идентификатор"));
	
	РеквизитыУзла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Выборка.УзелОбмена,
		"Код,ДатаНачалаВыгрузкиДокументов,ПравилаОтправкиСправочников,ПравилаОтправкиДокументов");
	
	ДанныеОтвета.Вставить("УзелОбмена", Новый Структура);
	ДанныеОтвета.УзелОбмена.Вставить("Идентификатор", РеквизитыУзла.Код);
	ДанныеОтвета.УзелОбмена.Вставить("ДатаНачалаВыгрузкиДокументов", РеквизитыУзла.ДатаНачалаВыгрузкиДокументов);
	ДанныеОтвета.УзелОбмена.Вставить("ПравилаОтправкиСправочников", РеквизитыУзла.ПравилаОтправкиСправочников);
	ДанныеОтвета.УзелОбмена.Вставить("ПравилаОтправкиДокументов", РеквизитыУзла.ПравилаОтправкиДокументов);
	
	ДанныеОтвета.УзелОбмена.Вставить("НастройкаСинхронизацииЗавершена", ОбменДаннымиСервер.НастройкаСинхронизацииЗавершена(Выборка.УзелОбмена));
	ДанныеОтвета.УзелОбмена.Вставить("ПолученоСообщениеДляСопоставления", ПолученоСообщениеДляСопоставления(Выборка.УзелОбмена));
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	Ответ.УстановитьТелоИзСтроки(ДанныеВСтрокуJSON(ДанныеОтвета));
	
	Возврат Ответ;
	
КонецФункции

Функция ОтветНастройкиСовместногоИспользованияPUT(Запрос)
	
	Попытка
		ИдентификаторНастройки = Строка(Новый УникальныйИдентификатор(Запрос.ПараметрыURL.Получить("app-id")));
		ДанныеЗапроса = ОбщегоНазначения.JSONВЗначение(Запрос.ПолучитьТелоКакСтроку(), "ДатаНачалаОтражения,ДатаНачалаВыгрузкиДокументов");
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	НачатьТранзакцию();
	Попытка
		Если Не Константы.СовместноеИспользованиеДоступно.Получить() Тогда
			Константы.СовместноеИспользованиеДоступно.Установить(Истина);
		КонецЕсли;
		
		НастройкаСсылка = Справочники.НастройкиСовместногоИспользованияПриложений.НайтиПоКоду(ИдентификаторНастройки);
		
		Если НастройкаСсылка.Пустая() Тогда
			НастройкаОбъект = Справочники.НастройкиСовместногоИспользованияПриложений.СоздатьЭлемент();
			НастройкаОбъект.Код = ИдентификаторНастройки;
		Иначе
			НастройкаОбъект = НастройкаСсылка.ПолучитьОбъект();
		КонецЕсли;
		
		ЕстьИзменения = Ложь;
		
		ДанныеУзлаОбмена = ДанныеЗапроса.Получить("УзелОбмена");
		Если ДанныеУзлаОбмена <> Неопределено Тогда
			УзелОбмена = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.НайтиПоКоду(ДанныеУзлаОбмена.Получить("Идентификатор"));
			Если НастройкаОбъект.УзелОбмена <> УзелОбмена Тогда
				НастройкаОбъект.УзелОбмена = УзелОбмена;
				ЕстьИзменения = Истина;
			КонецЕсли;
			УзелОбъект = УзелОбмена.ПолучитьОбъект();
			
			Если ДанныеУзлаОбмена.Получить("ДатаНачалаВыгрузкиДокументов") <> Неопределено Тогда
				УзелОбъект.ДатаНачалаВыгрузкиДокументов = ДанныеУзлаОбмена.Получить("ДатаНачалаВыгрузкиДокументов");
			КонецЕсли;
			
			Если ДанныеУзлаОбмена.Получить("ПравилаОтправкиСправочников") <> Неопределено Тогда
				УзелОбъект.ПравилаОтправкиСправочников = ДанныеУзлаОбмена.Получить("ПравилаОтправкиСправочников");
			КонецЕсли;
			
			Если ДанныеУзлаОбмена.Получить("ПравилаОтправкиДокументов") <> Неопределено Тогда
				УзелОбъект.ПравилаОтправкиДокументов = ДанныеУзлаОбмена.Получить("ПравилаОтправкиДокументов");
			КонецЕсли;
			
			Если ДанныеУзлаОбмена.Получить("ПравилаОтправкиЦен") <> Неопределено Тогда
				УзелОбъект.ПравилаОтправкиЦен = ДанныеУзлаОбмена.Получить("ПравилаОтправкиЦен");
			КонецЕсли;
			
			УзелОбъект.Записать();
			
			Если ДанныеУзлаОбмена.Получить("НастройкаСинхронизацииЗавершена") = Истина Тогда
				ОбменДаннымиСервер.ЗавершитьНастройкуСинхронизацииДанных(УзелОбмена);
			КонецЕсли;
		КонецЕсли;
		
		ДатаНачалаОтражения = ДанныеЗапроса.Получить("ДатаНачалаОтражения");
		Если ДатаНачалаОтражения <> Неопределено Тогда
			Если НастройкаОбъект.ДатаНачалаОтражения <> ДатаНачалаОтражения Тогда
				НастройкаОбъект.ДатаНачалаОтражения = ДатаНачалаОтражения;
				ЕстьИзменения = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ОтражениеВключено = ДанныеЗапроса.Получить("ОтражениеВключено");
		Если ОтражениеВключено <> Неопределено Тогда
			Если НастройкаОбъект.ОтражениеВключено <> ОтражениеВключено Тогда
				НастройкаОбъект.ОтражениеВключено = ОтражениеВключено;
				ЕстьИзменения = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеЗапроса.Получить("РазделыУчета") <> Неопределено Тогда
			НастройкаОбъект.РазделыУчета.Очистить();
			Для Каждого ИдентификаторРаздела Из ДанныеЗапроса.Получить("РазделыУчета") Цикл
				СтрокаРазделы = НастройкаОбъект.РазделыУчета.Добавить();
				СтрокаРазделы.Идентификатор = ИдентификаторРаздела;
			КонецЦикла;
			
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Если ДанныеЗапроса.Получить("Организации") <> Неопределено Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ПубличныеИдентификаторы.Ссылка КАК Организация
			|ИЗ
			|	РегистрСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов КАК ПубличныеИдентификаторы
			|ГДЕ
			|	ПубличныеИдентификаторы.УзелИнформационнойБазы = &УзелИнформационнойБазы
			|	И ПубличныеИдентификаторы.Идентификатор В(&Идентификаторы)");
			Запрос.УстановитьПараметр("УзелИнформационнойБазы", НастройкаОбъект.УзелОбмена);
			Запрос.УстановитьПараметр("Идентификаторы", ДанныеЗапроса.Получить("Организации"));
			
			ТаблицаОрганизации = Запрос.Выполнить().Выгрузить();
			НастройкаОбъект.Организации.Загрузить(ТаблицаОрганизации);
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Если НастройкаОбъект.ЭтоНовый()
			Или ЕстьИзменения Тогда
			НастройкаОбъект.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат Новый HTTPСервисОтвет(200);
	
КонецФункции

Функция ОтветУчетнаяПолитикаGET(Запрос)
	
	Попытка                    
		Идентификатор = Строка(Новый УникальныйИдентификатор(Запрос.ПараметрыЗапроса.Получить("company-id")));
		Период = XMLЗначение(Тип("Дата"), Запрос.ПараметрыЗапроса.Получить("date"));
	Исключение
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	ОрганизацияСсылка = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(Идентификатор));
	
	Если Не ОбщегоНазначения.СсылкаСуществует(ОрганизацияСсылка) Тогда
		Возврат Новый HTTPСервисОтвет(404);
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("УчетнаяПолитика", СвойстваУчетнойПолитики(ОрганизацияСсылка, Период));
	ДанныеОтвета.Вставить("СистемаНалогообложения", СвойстваСистемыНалогообложения(ОрганизацияСсылка, Период));
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	Ответ.УстановитьТелоИзСтроки(ДанныеВСтрокуJSON(ДанныеОтвета));
	
	Возврат Ответ;	
	
КонецФункции

Функция ОтветДанныеПубличныхИдентификаторовPOST(Запрос)
	
	Попытка
		ДанныеИдентификаторов = JSONВТаблицуЗначений(Запрос.ПолучитьТелоКакСтроку());
	Исключение
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	Для Каждого СтрокаДанные Из ДанныеИдентификаторов Цикл
		Запись = РегистрыСведений.ДанныеПубличныхИдентификаторовСовместногоИспользования.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаДанные);
		Запись.Записать();
	КонецЦикла;
	
	Возврат Новый HTTPСервисОтвет(200);
	
КонецФункции

Функция ОтветСозданиеОрганизацииPUT(Запрос)
	
	Попытка
		ДанныеЗапроса = ОбщегоНазначения.JSONВЗначение(Запрос.ПолучитьТелоКакСтроку(), "period", Ложь);
	Исключение
		ЗаписатьОшибкуВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;
	
	НачатьТранзакцию();
	Попытка
		
		РезультатПроверки = СовместноеИспользованиеВебСервис.ПроверитьКорректностьДанныхДляОрганизации(ДанныеЗапроса);
		Если РезультатПроверки.Ошибка Тогда
			ЗаписатьОшибкуВЖурналРегистрации(РезультатПроверки.СообщениеОбОшибке);
			Возврат Новый HTTPСервисОтвет(400);
		КонецЕсли;
		
		РезультатСоздания = СовместноеИспользованиеВебСервис.СоздатьОрганизацию(ДанныеЗапроса.entity);
		Если РезультатСоздания.Ошибка Тогда
			ЗаписатьОшибкуВЖурналРегистрации(РезультатСоздания.СообщениеОбОшибке);
			Возврат Новый HTTPСервисОтвет(400);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	
	ОтветJSON = Новый Структура;
	ОтветJSON.Вставить("uuid", XMLСтрока(РезультатСоздания.Организация));
	ОтветJSON.Вставить("state", ?(РезультатСоздания.СозданаНовая = Истина, "new", "existing"));
	
	Ответ.УстановитьТелоИзСтроки(ДанныеВСтрокуJSON(ОтветJSON));
	
	Возврат Ответ;
	
КонецФункции

Функция ТаблицаРезультатовОбменаДанными(МенеджерВТ, УзелОбмена)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РезультатыОбменаДанными.ПроблемныйОбъект КАК ПроблемныйОбъект,
	|	РезультатыОбменаДанными.ТипПроблемы КАК ТипПроблемы,
	|	РезультатыОбменаДанными.Причина КАК Причина,
	|	РезультатыОбменаДанными.Пропущена КАК Пропущена
	|ИЗ
	|	ТаблицаИдентификаторы КАК ТаблицаИдентификаторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыОбменаДанными КАК РезультатыОбменаДанными
	|		ПО (РезультатыОбменаДанными.ПроблемныйОбъект = ТаблицаИдентификаторы.Ссылка)
	|			И (РезультатыОбменаДанными.УзелИнформационнойБазы = &УзелОбмена)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроблемныйОбъект,
	|	ТипПроблемы");
	Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	РезультатыОбмена = Запрос.Выполнить().Выгрузить();
	РезультатыОбмена.Индексы.Добавить("ПроблемныйОбъект");
	
	Возврат РезультатыОбмена;
	
КонецФункции

Функция ПубличныеИдентификаторыОрганизаций(УзелОбмена)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПубличныеИдентификаторы.Идентификатор КАК Идентификатор,
	|	ПубличныеИдентификаторы.Ссылка КАК Организация
	|ИЗ
	|	РегистрСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов КАК ПубличныеИдентификаторы
	|ГДЕ
	|	ПубличныеИдентификаторы.УзелИнформационнойБазы = &УзелОбмена
	|	И ТИПЗНАЧЕНИЯ(ПубличныеИдентификаторы.Ссылка) В (&ТипыОрганизация)");
	Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
	Запрос.УстановитьПараметр("ТипыОрганизация", СовместноеИспользованиеПереопределяемый.ОписаниеТиповОрганизация().Типы());
	
	ТаблицаИдентификаторы = Запрос.Выполнить().Выгрузить();
	
	ТаблицаИдентификаторы.Индексы.Добавить("Организация");
	
	Возврат ТаблицаИдентификаторы;
	
КонецФункции

Функция ДанныеВСтрокуJSON(Данные)
	
	Возврат СовместноеИспользование.ДанныеВСтрокуJSON(Данные, Истина);
	
КонецФункции

Функция ТаблицаЗначенийВJSON(ТаблицаДанные, ТипизацияКолонок = "")
	
	ТипыКолонок = Новый Структура;
	Если Не ПустаяСтрока(ТипизацияКолонок) Тогда
		Для Каждого ОписаниеКолонки Из СтрРазделить(ТипизацияКолонок, ",", Ложь) Цикл
			КолонкаТип = СтрРазделить(ОписаниеКолонки, ":");
			ТипыКолонок.Вставить(КолонкаТип[0], КолонкаТип[1]);
		КонецЦикла;
	КонецЕсли;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Колонки", Новый Массив);
	СтруктураДанные.Вставить("Строки", Новый Массив);
	
	ИменаКолонок = Новый Массив;
	Для Каждого Колонка Из ТаблицаДанные.Колонки Цикл
		ИменаКолонок.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Для Каждого ИмяКолонки Из ИменаКолонок Цикл
		ТипКолонки = Неопределено;
		Если ТипыКолонок.Свойство(ИмяКолонки, ТипКолонки) Тогда
			СтруктураДанные.Колонки.Добавить(ИмяКолонки + ":" + ТипКолонки);
		Иначе
			СтруктураДанные.Колонки.Добавить(ИмяКолонки);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаДанные Из ТаблицаДанные Цикл
		ДанныеСтроки = Новый Массив;
		Для Каждого ИмяКолонки Из ИменаКолонок Цикл
			ДанныеСтроки.Добавить(СтрокаДанные[ИмяКолонки]);
		КонецЦикла;
		СтруктураДанные.Строки.Добавить(ДанныеСтроки);
	КонецЦикла;
	
	Возврат ДанныеВСтрокуJSON(СтруктураДанные);
	
КонецФункции

Функция JSONВТаблицуЗначений(СтрокаJSON)
	
	СтруктураДанные = ОбщегоНазначения.JSONВЗначение(СтрокаJSON, , Ложь);
	
	ИменаКолонок = Новый Массив;
	ТипыКолонок = Новый Структура;
	
	Для Каждого ОписаниеКолонки Из СтруктураДанные.Колонки Цикл
		КолонкаТип = СтрРазделить(ОписаниеКолонки, ":", Ложь);
		ИменаКолонок.Добавить(КолонкаТип[0]);
		Если КолонкаТип.Количество() > 1 Тогда
			ТипыКолонок.Вставить(КолонкаТип[0], КолонкаТип[1]);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаДанные = Новый ТаблицаЗначений;
	Для Каждого ИмяКолонки Из ИменаКолонок Цикл
		ТипКолонки = Неопределено;
		Если ТипыКолонок.Свойство(ИмяКолонки, ТипКолонки) Тогда
			ТаблицаДанные.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов(ТипКолонки));
		Иначе
			ТаблицаДанные.Колонки.Добавить(ИмяКолонки);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаДанные Из СтруктураДанные.Строки Цикл
		НоваяСтрока = ТаблицаДанные.Добавить();
		Для Инд = 0 По ИменаКолонок.ВГраница() Цикл
			ИмяКолонки = ИменаКолонок[Инд];
			ТипКолонки = Неопределено;
			Если ТипыКолонок.Свойство(ИмяКолонки, ТипКолонки) Тогда
				НоваяСтрока[ИмяКолонки] = XMLЗначение(Тип(ТипКолонки), СтрокаДанные[Инд]);
			Иначе
				НоваяСтрока[ИмяКолонки] = СтрокаДанные[Инд];
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаДанные;
	
КонецФункции

Процедура ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки)
	
	ЗаписьЖурналаРегистрации(
		СобытиеЖурналаРегистрацииСервисСовместногоИспользования(),
		УровеньЖурналаРегистрации.Ошибка,
		Метаданные.HTTPСервисы.СовместноеИспользование,
		,
		ТекстОшибки);
	
КонецПроцедуры

Функция СобытиеЖурналаРегистрацииСервисСовместногоИспользования()
	
	Возврат НСтр("ru = 'Совместное использование.Сервис совместного использования'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция ПолученоСообщениеДляСопоставления(УзелОбмена)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОбщиеНастройкиУзловИнформационныхБаз.СообщениеДляСопоставленияДанных КАК СообщениеДляСопоставленияДанных
	|ИЗ
	|	РегистрСведений.ОбщиеНастройкиУзловИнформационныхБаз КАК ОбщиеНастройкиУзловИнформационныхБаз
	|ГДЕ
	|	ОбщиеНастройкиУзловИнформационныхБаз.УзелИнформационнойБазы = &УзелОбмена");
	Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат ЗначениеЗаполнено(Выборка.СообщениеДляСопоставленияДанных);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция СвойстваСистемыНалогообложения(Организация, Период)
	
	Результат = Новый Структура;
	Результат.Вставить("Существует", УчетнаяПолитика.СуществуетСистемаНалогообложения(Организация, Период));
	
	Возврат Результат;
	
КонецФункции

Функция СвойстваУчетнойПолитики(Организация, Период)
	
	Результат = Новый Структура;
	Результат.Вставить("Существует", УчетнаяПолитика.СуществуетУчетнаяПолитика(Организация, Период));
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти