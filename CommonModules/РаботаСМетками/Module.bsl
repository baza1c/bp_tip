#Область ПрограммныйИнтерфейс

// Настраивает форму списка или объекта (справочника, документа) с метками.
// Вызывается из события создания на сервере формы
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с метками
//  ЭтоФормаСписка - Булево - создается форма списка или форма объекта
//
Процедура ПриСозданииНаСервере(Форма, ЭтоФормаСписка = Ложь) Экспорт
	
	Если ЭтоФормаСписка Тогда
		УстановитьПараметрыФормыСписка(Форма);
	Иначе
		УстановитьПараметрыФормыОбъекта(Форма);
	КонецЕсли;
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	Если Не ПараметрыФормы.ИспользоватьМетки Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьВсеМетки(Форма);
	
	Если ПараметрыФормы.ЭтоФормаСписка Тогда
		РаботаСМеткамиКлиентСервер.УстановитьОтборПоМеткам(Форма);
		
		ДополнительныеПараметрыСписка = Новый Структура;
		ДополнительныеПараметрыСписка.Вставить("ОтображатьСтатусыДокументов");
		ЗаполнитьЗначенияСвойств(ДополнительныеПараметрыСписка, Форма);
		Если ДополнительныеПараметрыСписка.ОтображатьСтатусыДокументов = Неопределено Тогда
			ДополнительныеПараметрыСписка.ОтображатьСтатусыДокументов =
				ПолучитьФункциональнуюОпцию("ОтображатьДополнительныеКолонкиВСписках");
		КонецЕсли;
		ПараметрыФормы.ОтображатьМеткиВСписке = ДополнительныеПараметрыСписка.ОтображатьСтатусыДокументов;
	
		Форма.Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить(
			"ОтображатьМеткиВСписке",
			ПараметрыФормы.ИспользоватьМетки И ПараметрыФормы.ОтображатьМеткиВСписке);
		Форма.Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить(
			"ИмяВладельцаМеток",
			ПараметрыФормы.ИмяВладельца);
		
		Форма.Элементы.МеткиСтрокой.Видимость = ПараметрыФормы.ОтображатьМеткиВСписке;
	Иначе
		ЗаполнитьМеткиНаФорме(Форма);
	КонецЕсли;
	
	Форма.Элементы.ГруппаМетки.Видимость = Истина;
	
КонецПроцедуры

// Перезаполняет метки в форме списка или объекта (справочника, документа) с метками.
// Вызывается из события оповещения формы
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с метками
//
Процедура ОбработкаОповещенияНаСервере(Форма) Экспорт
	
	ЗаполнитьВсеМетки(Форма);
	ЗаполнитьСвойстваМеток(Форма);
	
КонецПроцедуры

// Выводит колонку с метками в форме списка.
// Вызывается из события получения данных на сервере
//
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных - содержит копию полных настроек динамического списка
//  Строки    - СтрокиДинамическогоСписка - коллекция содержит данные и оформление всех строк
//
Процедура ПриПолученииДанныхНаСервере(Настройки, Строки) Экспорт
	
	Если Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОтображатьМеткиВСписке = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Настройки.ДополнительныеСвойства, "ОтображатьМеткиВСписке", Ложь);
	Если Не ОтображатьМеткиВСписке Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВладельца = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Настройки.ДополнительныеСвойства, "ИмяВладельцаМеток", "");
	Если Не ЗначениеЗаполнено(ИмяВладельца) Тогда
		Возврат;
	КонецЕсли;
	
	Владельцы = Строки.ПолучитьКлючи();
	
	ВыборкаПоВладельцам = ВыборкаМетокВладельцев(Владельцы, ИмяВладельца);
	Пока ВыборкаПоВладельцам.Следующий() Цикл
		Строка = Строки.Получить(ВыборкаПоВладельцам.Владелец);
		Если Не Строка.Данные.Свойство("МеткиСтрокой") Тогда
			Продолжить;
		КонецЕсли;
		
		МеткиСтрокой = Новый Массив;
		Выборка = ВыборкаПоВладельцам.Выбрать();
		Пока Выборка.Следующий() Цикл
			МеткиСтрокой.Добавить(Выборка.СвойствоНаименование);
		КонецЦикла;
		
		Строка.Данные["МеткиСтрокой"] = СтрСоединить(МеткиСтрокой, ", ");
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает отбор по меткам в форме списка.
// Вызывается из события загрузки сохраненных настроек на сервере
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с метками
//  Настройки - Соответствие - сохраненные настройки
//
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Форма, Настройки) Экспорт
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	Если Не ПараметрыФормы.ИспользоватьМетки Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСМеткамиКлиентСервер.УстановитьОтборПоМеткам(Форма);
	
КонецПроцедуры

// Настраивает условное оформление формы списка или объекта (справочника, документа) с метками
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с метками
//
Процедура УстановитьУсловноеОформление(Форма) Экспорт
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	Если Не ПараметрыФормы.ИспользоватьМетки Тогда
		Возврат;
	КонецЕсли;
	
	УсловноеОформление = Форма.УсловноеОформление;
	
	ИмяОформляемогоПоля = СтрШаблон("%1.МножественноеЗначение", ПараметрыФормы.ИмяЭлементаМетки);
	ИмяПоляОтбора = СтрШаблон("%1.НомерКартинки", ПараметрыФормы.ИмяЭлементаМетки);
	
	КоличествоЦветов = Перечисления.ЦветаСвойств.Количество() - 1;
	
	Для ИндексЦвета = 0 По КоличествоЦветов Цикл
		
		ЦветСвойства = Перечисления.ЦветаСвойств[ИндексЦвета];
		
		ЭлементСтиля = ЭлементСтиляПоЦвету(ЦветСвойства);
		Если ЭлементСтиля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, ИмяОформляемогоПоля);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			ИмяПоляОтбора, ВидСравненияКомпоновкиДанных.Равно, ИндексЦвета + 1);

		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЭлементСтиля);
		
	КонецЦикла;
	
КонецПроцедуры

// Переносит значения из поля Метки в данные объекта (справочника, документа).
// Перезаполняет свойства меток для поля Метки формы списка или объекта (справочника, документа).
// Вызывается из обработчика изменения для поля Метки
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с метками
//
Процедура МеткиПриИзмененииНаСервере(Форма) Экспорт
	
	ПеренестиМеткиНаФормеВОбъект(Форма);
	ЗаполнитьСвойстваМеток(Форма);
	
КонецПроцедуры

Функция МеткиВладельцев(Владельцы, ИмяВладельца) Экспорт
	
	ТаблицаМеток = Новый ТаблицаЗначений;
	ТаблицаМеток.Колонки.Добавить("Владелец");
	ТаблицаМеток.Колонки.Добавить("Метка");
	ТаблицаМеток.Колонки.Добавить("НаименованиеМетки");
	ТаблицаМеток.Колонки.Добавить("ЦветМетки");
	
	ВыборкаПоВладельцам = ВыборкаМетокВладельцев(Владельцы, ИмяВладельца);
	Пока ВыборкаПоВладельцам.Следующий() Цикл
		Выборка = ВыборкаПоВладельцам.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаМеток.Добавить();
			НоваяСтрока.Владелец = Выборка.Владелец;
			НоваяСтрока.Метка = Выборка.Свойство;
			НоваяСтрока.НаименованиеМетки = Выборка.СвойствоНаименование;
			НоваяСтрока.ЦветМетки = Выборка.ЦветСвойства;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаМеток;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПередЗаписьюОбщейМетки(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ВидСвойства = Перечисления.ВидыСвойств.Метки
		И Источник.ЭтоНовый() Тогда
		Источник.ДополнительныеСвойства.Вставить("ЗаписьНовойОбщейМетки");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписиОбщейМетки(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ЗаписьНовойОбщейМетки") Тогда
		НаборыСвойств = НаборыСвойствОбщейМетки();
		Если НаборыСвойств.Найти(Источник.НаборСвойств) <> Неопределено Тогда
			Для Каждого ДополнительныйНабор Из НаборыСвойств Цикл
				Если ДополнительныйНабор = Источник.НаборСвойств Тогда
					Продолжить;
				КонецЕсли;
				ЗаписатьОбщуюМеткуВНаборСвойств(Источник, ДополнительныйНабор);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьПараметрыФормыОбъекта(Форма)
	
	ИспользоватьМетки = ИспользуютсяМетки();
	
	ПараметрыФормы = НовыеПараметрыФормы();
	ПараметрыФормы.ИспользоватьМетки = ИспользоватьМетки;
	Если ИспользоватьМетки Тогда
		ПараметрыФормы.ЭтоФормаСписка = Ложь;
		ПараметрыФормы.ИмяВладельца = ИмяВладельцаПоИмениФормы(Форма.ИмяФормы);
		ПараметрыФормы.ИмяЭлементаМетки = "МеткиОбъекта";
	КонецЕсли;
	
	Форма.ПараметрыФормыСМетками = ПараметрыФормы;
	
КонецПроцедуры

Процедура УстановитьПараметрыФормыСписка(Форма)
	
	ИспользоватьМетки = ИспользуютсяМетки();
	
	ПараметрыФормы = НовыеПараметрыФормы();
	ПараметрыФормы.ИспользоватьМетки = ИспользоватьМетки;
	Если ИспользоватьМетки Тогда
		ПараметрыФормы.ЭтоФормаСписка = Истина;
		ПараметрыФормы.ИмяВладельца = ИмяВладельцаПоИмениФормы(Форма.ИмяФормы);
		ПараметрыФормы.ИмяЭлементаМетки = "ОтборМетки";
	КонецЕсли;
	
	Форма.ПараметрыФормыСМетками = ПараметрыФормы;
	
КонецПроцедуры

Функция НовыеПараметрыФормы()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЭтоФормаСписка", Ложь);
	ПараметрыФормы.Вставить("ИмяВладельца", "");
	ПараметрыФормы.Вставить("ИмяЭлементаМетки", "");
	ПараметрыФормы.Вставить("ИспользоватьМетки", Ложь);
	ПараметрыФормы.Вставить("ОтображатьМеткиВСписке", Ложь);
	
	Возврат ПараметрыФормы;
	
КонецФункции

Функция ИмяВладельцаПоИмениФормы(ИмяФормы)
	
	ПозицияРазделителя = СтрНайти(ИмяФормы, ".", , , 2);
	
	Если ПозицияРазделителя > 0 Тогда
		Возврат Лев(ИмяФормы, ПозицияРазделителя - 1);
	Иначе
		ВызватьИсключение НСтр("ru = 'Не удалось определить имя владельца меток. 
			|Метки допустимо использовать только в формах объектов и в списках'");
	КонецЕсли;
	
КонецФункции

Функция ИспользуютсяМетки()
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения")
		И ПравоДоступа("Чтение", Метаданные.Справочники.НаборыДополнительныхРеквизитовИСведений);

КонецФункции

Функция ВыборкаМетокВладельцев(ВладельцыМеток, ИмяВладельца) Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДополнительныеРеквизиты.Ссылка КАК Владелец,
	|	ДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ДополнительныеРеквизиты.Свойство.Наименование КАК СвойствоНаименование,
	|	ДополнительныеРеквизиты.Свойство.ЦветСвойства КАК ЦветСвойства
	|ИЗ
	|	&ИмяТаблицы КАК ДополнительныеРеквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	ДополнительныеРеквизиты.Ссылка В(&Владельцы)
	|	И ДополнительныеРеквизитыИСведения.ВидСвойства = ЗНАЧЕНИЕ(Перечисление.ВидыСвойств.Метки)
	|ИТОГИ ПО
	|	Владелец";
	
	ИмяТаблицы = СтрШаблон("%1.ДополнительныеРеквизиты", ИмяВладельца);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы);
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Владельцы", ВладельцыМеток);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

Функция СписокМетокПоВидуВладельца(ИмяВладельца) Экспорт
	
	НаборСсылка = РаботаСМеткамиВызовСервера.НаборСвойствВладельца(ИмяВладельца);
	
	Если НаборСсылка = Неопределено Тогда
		Возврат НаборСсылка;
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаСвойств.Свойство КАК Свойство,
		|	ДополнительныеРеквизитыИСведения.Наименование КАК Наименование,
		|	ДополнительныеРеквизитыИСведения.ПометкаУдаления КАК ПометкаУдаления,
		|	ДополнительныеРеквизитыИСведения.ЦветСвойства.Порядок + 1 КАК НомерКартинки,
		|	ТаблицаСвойств.Ссылка КАК Набор
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ТаблицаСвойств
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|		ПО ТаблицаСвойств.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		|ГДЕ
		|	ТаблицаСвойств.Ссылка = &Ссылка
		|	И ДополнительныеРеквизитыИСведения.ВидСвойства = ЗНАЧЕНИЕ(Перечисление.ВидыСвойств.Метки)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", НаборСсылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Свернуть("Свойство,Наименование,ПометкаУдаления,НомерКартинки,Набор");
	Результат.Сортировать("Наименование Возр");
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьВсеМетки(Форма)
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	ВсеМетки = Форма.ВсеМетки;
	ВсеМетки.Очистить();
	
	ЗначенияМеток = СписокМетокПоВидуВладельца(ПараметрыФормы.ИмяВладельца);
		
	Для Каждого Метка Из ЗначенияМеток Цикл
		НоваяСтрока = ВсеМетки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Метка);
	КонецЦикла;
	
	УстановитьПараметрыВыбораМеток(Форма, ПараметрыФормы);
	
КонецПроцедуры

Процедура ЗаполнитьСвойстваМеток(Форма)
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	МеткиНаФорме = Форма[ПараметрыФормы.ИмяЭлементаМетки];
	ВсеМетки = Форма.ВсеМетки;
	
	Для Каждого Метка Из МеткиНаФорме Цикл
		СтрокаМетки = ВсеМетки.НайтиСтроки(Новый Структура("Свойство", Метка.Свойство));
		Если СтрокаМетки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Метка, СтрокаМетки[0]);
		Иначе
			// Метка исключена из набора
			Метка.Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Метка.Свойство, "Наименование");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьПараметрыВыбораМеток(Форма, ПараметрыФормы)
	
	Элементы = Форма.Элементы;
	ВсеМетки = Форма.ВсеМетки;
	
	НовыйПараметр = Новый ПараметрВыбора("Отбор.Ссылка", ВсеМетки.Выгрузить().ВыгрузитьКолонку("Свойство"));
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(НовыйПараметр);
	
	Элементы[ПараметрыФормы.ИмяЭлементаМетки].ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

Функция НаборыСвойствОбщейМетки()
	
	НаборыСвойств = Новый Массив;
	
	ИмяВладельца = Метаданные.Справочники.Контрагенты.ПолноеИмя();
	НаборыСвойств.Добавить(РаботаСМеткамиВызовСервера.НаборСвойствВладельца(ИмяВладельца));
	
	ИмяВладельца = Метаданные.Справочники.КонтактныеЛица.ПолноеИмя();
	НаборыСвойств.Добавить(РаботаСМеткамиВызовСервера.НаборСвойствВладельца(ИмяВладельца));
	
	Возврат НаборыСвойств;
	
КонецФункции

Процедура ЗаписатьОбщуюМеткуВНаборСвойств(ТекущийОбъект, НаборСвойств)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.НаборыДополнительныхРеквизитовИСведений");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", НаборСвойств);
	Блокировка.Заблокировать();
	ЗаблокироватьДанныеДляРедактирования(НаборСвойств);
	
	УстановитьПривилегированныйРежим(Истина);
	ОбъектНаборСвойств = НаборСвойств.ПолучитьОбъект();
	
	НайденнаяСтрока = ОбъектНаборСвойств.ДополнительныеРеквизиты.Найти(ТекущийОбъект.Ссылка, "Свойство");
	Если НайденнаяСтрока = Неопределено Тогда
		НоваяСтрока = ОбъектНаборСвойств.ДополнительныеРеквизиты.Добавить();
		НоваяСтрока.Свойство = ТекущийОбъект.Ссылка;
		ОбъектНаборСвойств.Записать();
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ИзмененныйНабор", НаборСвойств);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ЭлементСтиляПоЦвету(ЦветСвойства) Экспорт
	
	Если ЦветСвойства = Перечисления.ЦветаСвойств.Лаймовый Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиЛаймовый;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Красный Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиКрасный;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Оранжевый Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиОранжевый;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Желтый Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиЖелтый;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Зеленый Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиЗеленый;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Синий Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиСиний;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Голубой Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиГолубой;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Фиолетовый Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиФиолетовый;
	ИначеЕсли ЦветСвойства = Перечисления.ЦветаСвойств.Розовый Тогда
		ЭлементСтиля = ЦветаСтиля.ЦветМеткиРозовый;
	Иначе
		ЭлементСтиля = Неопределено;
	КонецЕсли;
	
	Возврат ЭлементСтиля;
	
КонецФункции

Процедура ЗаполнитьМеткиНаФорме(Форма)
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	Объект = Форма.Объект;
	МеткиНаФорме = Форма[ПараметрыФормы.ИмяЭлементаМетки];
	
	МеткиОбъекта = УправлениеСвойствами.СвойстваПоВидуДополнительныхРеквизитов(
		Объект.ДополнительныеРеквизиты.Выгрузить(),
		Перечисления.ВидыСвойств.Метки);
	
	МеткиНаФорме.Очистить();
	
	Для Каждого Метка Из МеткиОбъекта Цикл
		НоваяМетка = МеткиНаФорме.Добавить();
		НоваяМетка.Свойство = Метка;
	КонецЦикла;
	
	ЗаполнитьСвойстваМеток(Форма);
	
КонецПроцедуры

Процедура ПеренестиМеткиНаФормеВОбъект(Форма)
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	Если ПараметрыФормы.ЭтоФормаСписка Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	МеткиНаФорме = Форма[ПараметрыФормы.ИмяЭлементаМетки];
	
	МеткиОбъекта = УправлениеСвойствами.СвойстваПоВидуДополнительныхРеквизитов(
		Объект.ДополнительныеРеквизиты.Выгрузить(),
		Перечисления.ВидыСвойств.Метки);
	
	Для каждого Метка Из МеткиОбъекта Цикл
		Свойства = Объект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", Метка));
		Для каждого Свойство Из Свойства Цикл
			Объект.ДополнительныеРеквизиты.Удалить(Свойство);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого Метка Из МеткиНаФорме Цикл
		НоваяМетка = Объект.ДополнительныеРеквизиты.Добавить();
		НоваяМетка.Свойство = Метка.Свойство;
		НоваяМетка.Значение = Истина;
	КонецЦикла;
	
КонецПроцедуры

Функция ИспользуетсяНаименование(Наименование, НаборСвойств, ИсключаяСвойство) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СвойстваНабора.Свойство КАК Свойство,
		|	РеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
		|	ВЫБОР
		|		КОГДА РеквизитыИСведения.ВидСвойства = ЗНАЧЕНИЕ(Перечисление.ВидыСвойств.Метки)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоМетка
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК СвойстваНабора
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК РеквизитыИСведения
		|		ПО (РеквизитыИСведения.Ссылка = СвойстваНабора.Свойство)
		|ГДЕ
		|	РеквизитыИСведения.Заголовок = &Наименование
		|	И СвойстваНабора.Ссылка = &НаборСвойств
		|	И СвойстваНабора.Свойство <> &ИсключаяСвойство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СвойстваНабора.Свойство,
		|	РеквизитыИСведения.ЭтоДополнительноеСведение,
		|	ВЫБОР
		|		КОГДА РеквизитыИСведения.ВидСвойства = ЗНАЧЕНИЕ(Перечисление.ВидыСвойств.Метки)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК СвойстваНабора
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК РеквизитыИСведения
		|		ПО (РеквизитыИСведения.Ссылка = СвойстваНабора.Свойство)
		|ГДЕ
		|	РеквизитыИСведения.Заголовок = &Наименование
		|	И СвойстваНабора.Ссылка = &НаборСвойств
		|	И СвойстваНабора.Свойство <> &ИсключаяСвойство";
	
	Запрос.УстановитьПараметр("ИсключаяСвойство", ИсключаяСвойство);
	Запрос.УстановитьПараметр("НаборСвойств", НаборСвойств);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат "";
	КонецЕсли;
	
	Если Выборка.ЭтоМетка Тогда
		ТекстВопроса = НСтр("ru = 'Существует метка с таким наименованием.'");
	ИначеЕсли Выборка.ЭтоДополнительноеСведение Тогда
		ТекстВопроса = НСтр("ru = 'Существует дополнительное сведение с таким наименованием.'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Существует дополнительный реквизит с таким наименованием.'");
	КонецЕсли;
	
	ТекстВопроса = ТекстВопроса + Символы.ПС + Символы.ПС
		+ НСтр("ru = 'Рекомендуется использовать другое наименование, иначе программа может работать некорректно'");
	
	Возврат ТекстВопроса;
	
КонецФункции

#КонецОбласти