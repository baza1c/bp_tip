#Область ПрограммныйИнтерфейс

// Открывает форму для формирования и/или просмотра сформированного чека.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Владелец, который открывает форму чека.
//  ДокументОснование - ДокументСсылка - Ссылка на владельца чека.
//  Организация - СправочникСсылка.Организации - ссылка на организацию.
//  ПолучательЧека - СправочникСсылка.Контрагенты - ссылка на контрагента-получателя чека.
//
Процедура ОткрытьФормуЧека(Форма, ДокументОснование, Организация, ПолучательЧека) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОснованиеЧека", ДокументОснование);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ПолучательЧека", ПолучательЧека);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	ОткрытьФорму("РегистрСведений.ЧекиНПД.Форма.Форма", ПараметрыФормы, Форма);
	
КонецПроцедуры

// Вызывает ожидание аннулирования чека в фоне, назначает оповещение при завершении.
//
// Параметры:
//  ДлительнаяОперация 	 - Структура - параметры длительной операции.
//  Форма					 - ФормаКлиентскогоПриложения - форма документа.
//
Процедура ОжидатьАннулированиеЧекаВФоне(ДлительнаяОперация, Форма) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьАннулированиеЧекаВФонеЗавершение", Форма);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

// Заполняет реквизит "Комментарий", в документе-основании чека НПД, причиной, по которой был аннулирован чек
//
// Параметры:
//  СведенияОЧеке - Структура - подробнее см. РегистрыСведений.ЧекиНПД.СведенияОЧеке
//  Форма - ФормаКлиентскогоПриложения - форма документа-основания для чека НПД, имеющая реквизит с именем "Объект", в состав
//                             которого входит реквизит "Комментарий"
//
Процедура ДобавитьПричинуАннулированияЧекаВКомментарий(СведенияОЧеке, Форма) Экспорт
	
	Если ЗначениеЗаполнено(СведенияОЧеке.ДатаАннулированияЧека)
		И СведенияОЧеке.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЧековНПД.Аннулирован")
		И ЗначениеЗаполнено(СведенияОЧеке.ПредставлениеПричиныОтменыЧека)
		И Не Форма.ТолькоПросмотр
		И Не ЗначениеЗаполнено(Форма.Объект.Комментарий) Тогда
		Форма.Объект.Комментарий = СведенияОЧеке.ПредставлениеПричиныОтменыЧека;
		Если Не Форма.Модифицированность Тогда
			Форма.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обновление статусов офлайн чеков для всех организаций, подключенных к сервису Мой налог.
//
Процедура ОбновитьСтатусыОфлайнЧековНПД() Экспорт
	
	ПодключенныеОрганизации = ИнтеграцияСПлатформойСамозанятыеВызовСервера.ПодключенныеОрганизации();
	
	Для Каждого Организация Из ПодключенныеОрганизации Цикл
		НачатьОбновлениеСтатусовОфлайнЧековНПД(Организация);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает имя события, связанного с обновлением статусов офлайн чеков.
//
// Возвращаемое значение:
//   Строка
//
Функция ИмяСобытияИзменениеСтатусаОфлайнЧека() Экспорт
	
	Возврат "ИзменениеСтатусаОфлайнЧекаНПД";
	
КонецФункции

// Запускает в фоне обновление сведений о сформированном чеке.
//
// Параметры:
//  СведенияОЧеке - Структура - см. РегистрыСведений.ЧекиНПД.НовыйСведенияОЧеке().
//  Форма - ФормаКлиентскогоПриложения - форма документа.
//  ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, вызываемой при завершении обновления.
//
Процедура ОбновитьСведенияОСформированномЧекеНПД(СведенияОЧеке, Форма, ОповещениеОЗавершении) Экспорт
	
	ДлительнаяОперация = ЧекиНПДВызовСервера.ЗапуститьОбновлениеСведенийОЧекеНПД(
		СведенияОЧеке, Форма.УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		ВыполнитьОбработкуОповещенияОЗавершенииОбновленияЧека(ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьСведенияОСформированномЧекеНПДЗавершение", ЭтотОбъект, ОповещениеОЗавершении);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

// Выполняет обновление сведений о чеках, аннулированных в сервисе Мой налог.
//
Процедура ОбновитьСведенияОСформированныхЧекахНПД() Экспорт
	
	ПодключенныеОрганизации = ИнтеграцияСПлатформойСамозанятыеВызовСервера.ПодключенныеОрганизации();
	Если Не ЗначениеЗаполнено(ПодключенныеОрганизации) Тогда
		Возврат;
	КонецЕсли;
	
	КонецПериода = ЧекиНПДВызовСервера.ДатаСведенийНПД();
	
	Для Каждого Организация Из ПодключенныеОрганизации Цикл
		НачалоПериода = ЧекиНПДВызовСервера.ДатаОбновленияСформированныхЧековНПД(Организация);
		НачатьОбновлениеСведенийОСформированныхЧекахНПД(Организация, НачалоПериода, КонецПериода);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает имя события, связанного с обновлением сведений о сформированных чеках.
//
// Возвращаемое значение:
//   Строка
//
Функция ИмяСобытияОбновленияСведенийОСформированныхЧеках() Экспорт
	
	Возврат "ОбновлениеСтатусовСформированныхЧековНПД";
	
КонецФункции

// Определяет право изменения записей в регистре сведений "Чеки НПД".
//
// Возвращаемое значение:
//  Булево
Функция ЕстьПравоОбновленияЧековНПД() Экспорт
	
	Возврат ЧекиНПДКлиентПовтИсп.ЕстьПравоОбновленияЧековНПД();
	
КонецФункции

// Запускает фоновую проверку непредоставленных чеков НПД
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма элемента справочника контрагенты
//
Процедура ПроверитьНепредоставленныеЧекиНПДВФоне(Форма) Экспорт
	
	Контрагент = Форма.Объект.Ссылка;
	
	ДлительнаяОперация = ЧекиНПДВызовСервера.ПроверитьНепредоставленныеЧекиНПДВФонеНаСервере(
		Контрагент, Форма.УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОбработчика = Новый Структура("Контрагент", Контрагент);
		Обработчик = Новый ОписаниеОповещения("ПроверкаЧековНПДЗавершение", ЭтотОбъект, ПараметрыОбработчика);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеОфлайнЧеков

Процедура НачатьОбновлениеСтатусовОфлайнЧековНПД(Организация)
	
	ДлительнаяОперация = ЧекиНПДВызовСервера.НачатьОбновлениеСтатусовОфлайнЧековНПД(Организация);
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновлениеСтатусовОфлайнЧековЗавершение", ЭтотОбъект, Организация);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ОбновлениеСтатусовОфлайнЧековЗавершение(ДлительнаяОперация, Организация) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		НомераОбновленныхЧеков = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		УдалитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если ЗначениеЗаполнено(НомераОбновленныхЧеков) Тогда
			Оповестить(ИмяСобытияИзменениеСтатусаОфлайнЧека(), Организация);
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Отправка чеков в ФНС'"),,
				ТекстОповещенияОбОтправкеОфлайнЧеков(НомераОбновленныхЧеков),
				БиблиотекаКартинок.ПлатформаСамозанятыеЛоготипФНС);
		КонецЕсли;
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ИнтеграцияСПлатформойСамозанятыеВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			ДлительнаяОперация.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстОповещенияОбОтправкеОфлайнЧеков(ОбновленныеОфлайнЧеки)
	
	Текст = "";
	
	Если ОбновленныеОфлайнЧеки.Количество() = 0 Тогда
		Текст = НСтр("ru = 'Нет чеков, отправленных в ФНС'");
	ИначеЕсли ОбновленныеОфлайнЧеки.Количество() = 1 Тогда
		Текст = СтрШаблон(НСтр("ru = 'Чек %1 отправлен в ФНС'"), ОбновленныеОфлайнЧеки[0]);
	ИначеЕсли ОбновленныеОфлайнЧеки.Количество() = 2 Тогда
		Текст = СтрШаблон(НСтр("ru = 'Чеки %1 и %2 отправлены в ФНС'"), ОбновленныеОфлайнЧеки[0], ОбновленныеОфлайнЧеки[1]);
	Иначе
		ПредставлениеЧеков = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 чек;;%1 чека;%1 чеков;%1 чека'"), ОбновленныеОфлайнЧеки.Количество() - 1);
		
		Текст = СтрШаблон(
			НСтр("ru = 'Чек %1 и еще %2 отправлены в ФНС'"),
			ОбновленныеОфлайнЧеки[0],
			ПредставлениеЧеков);
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеЧека

Процедура ОбновитьСведенияОСформированномЧекеНПДЗавершение(ДлительнаяОперация, ОповещениеОЗавершении) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		ВыполнитьОбработкуОповещенияОЗавершенииОбновленияЧека(ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ТекстОшибки");
		НовыеСведенияОЧеке = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "НовыеСведенияОЧеке");
		ВыполнитьОбработкуОповещенияОЗавершенииОбновленияЧека(ОповещениеОЗавершении, ТекстОшибки, НовыеСведенияОЧеке);
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ИнтеграцияСПлатформойСамозанятыеВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			ДлительнаяОперация.КраткоеПредставлениеОшибки);
		ВыполнитьОбработкуОповещенияОЗавершенииОбновленияЧека(
			ОповещениеОЗавершении, ДлительнаяОперация.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обработчик оповещения после завершения обновления чека.
//
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - описание выполняемой процедуры.
//  ТекстОшибки - Строка - текст ошибки при обновлении чека.
//  НовыеСведенияОЧеке - Структура - см. РегистрыСведений.ЧекиНПД.НовыйСведенияОЧеке().
//
Процедура ВыполнитьОбработкуОповещенияОЗавершенииОбновленияЧека(ОповещениеОЗавершении, ТекстОшибки = "", НовыеСведенияОЧеке = Неопределено)
	
	РезультатЗавершения = Новый Структура;
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		РезультатЗавершения.Вставить("ОшибкаОбновленияЧекаНПД", ТекстОшибки);
	ИначеЕсли НовыеСведенияОЧеке <> Неопределено Тогда
		РезультатЗавершения.Вставить("НовыеСведенияОЧеке", НовыеСведенияОЧеке);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, РезультатЗавершения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеСформированныхЧеков

Процедура НачатьОбновлениеСведенийОСформированныхЧекахНПД(Организация, НачалоПериода, КонецПериода)
	
	ДлительнаяОперация = ЧекиНПДВызовСервера.ЗапуститьОбновлениеСведенийОСформированныхЧекахНПД(
		Организация, НачалоПериода, КонецПериода);
	Если ДлительнаяОперация = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновлениеСведенийОСформированныхЧекахНПДЗавершение", ЭтотОбъект, Организация);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ОбновлениеСведенийОСформированныхЧекахНПДЗавершение(ДлительнаяОперация, Организация) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		НомераОбновленныхЧеков = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		УдалитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если ЗначениеЗаполнено(НомераОбновленныхЧеков) Тогда
			Оповестить(ИмяСобытияОбновленияСведенийОСформированныхЧеках(), Организация);
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Обновление чеков по данным ФНС'"),
				ЧекиНПДВызовСервера.ПолучитьНавигационнуюСсылкуПоНомеруЧека(
					НомераОбновленныхЧеков[0], Организация),
				ТекстОповещенияОбОбновленииСформированныхЧеков(НомераОбновленныхЧеков),
				БиблиотекаКартинок.ПлатформаСамозанятыеЛоготипФНС);
		КонецЕсли;
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ИнтеграцияСПлатформойСамозанятыеВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			ДлительнаяОперация.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстОповещенияОбОбновленииСформированныхЧеков(НомераОбновленныхЧеков)
	
	Текст = "";
	
	Если НомераОбновленныхЧеков.Количество() = 1 Тогда
		Текст = СтрШаблон(НСтр("ru = 'Чек %1 аннулирован'"), НомераОбновленныхЧеков[0]);
	ИначеЕсли НомераОбновленныхЧеков.Количество() > 1 Тогда
		ПредставлениеЕщеЧеков = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 чек;;%1 чека;%1 чеков;%1 чека'"), НомераОбновленныхЧеков.ВГраница());
		
		Текст = СтрШаблон(
			НСтр("ru = 'Чек %1 и еще %2 аннулированы'"),
			НомераОбновленныхЧеков[0],
			ПредставлениеЕщеЧеков);
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти

Процедура ПроверкаЧековНПДЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Контрагент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры, "Контрагент", Неопределено);
		Оповестить(
			ИмяСобытияПолученаИнформацияНаличияЧековНПД(), РезультатОперации.ЕстьДокументыБезЧеков, Контрагент);
	КонецЕсли;
	
КонецПроцедуры

Функция ИмяСобытияПолученаИнформацияНаличияЧековНПД() Экспорт
	
	Возврат "ПолученаИнформацияНаличияЧековНПД";
	
КонецФункции

#КонецОбласти
