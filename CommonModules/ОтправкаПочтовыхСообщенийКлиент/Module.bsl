
#Область ПрограммныйИнтерфейс

// Отправляет по электронной почте печатные формы документов.
//
// Параметры:
//  КомандаОтправки  - Массив - строковый идентификатор команды отправки.
//  АдресКомандОтправки - Строка - адрес описания команд отправки во временном хранилище.
//  ОбъектыОтправки - Массив - ссылки на отправляемые документы.
//  ЕстьОшибки - Булево - признак возникновения ошибки при формировании электронного письма.
//  ДополнительныеПараметры - Структура - дополнительные параметры для сохранения печатных форм:
//   * УпаковатьВАрхив - Булево - упаковать печатные формы в архив.
//   * ФорматыСохранения - Массив - содержит формат сохранения файлов см. ТипФайлаТабличногоДокумента.
//
Процедура ОтправитьПоЭлектроннойПочте(КомандыОтправки, АдресКомандОтправки, ОбъектыОтправки, ЕстьОшибки,
			ДополнительныеПараметры = Неопределено) Экспорт
	
	ОписаниеКоманд = Новый Массив;
	ПечатныеФормы = Новый Массив;
	
	Для Каждого Команда Из КомандыОтправки Цикл
		
		ОписаниеКоманды = ОтправкаПочтовыхСообщенийВызовСервера.ОписаниеКомандыОтправки(Команда, АдресКомандОтправки);
		
		ОписаниеКоманды.Вставить("ОбъектыОтправки", ОбъектыОтправки);
		
		Если ОписаниеКоманды.МенеджерПечати = "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда
			
			Для Каждого КлючИЗначение Из ОписаниеКоманды.ДополнительныеПараметры Цикл
				ОписаниеКоманды.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЦикла;
			
			ОписаниеКоманды.Вставить("ЭтоОтчет", Ложь);
			ОписаниеКоманды.Вставить("Вид", ПредопределенноеЗначение("Перечисление.ВидыДополнительныхОтчетовИОбработок.ПечатнаяФорма"));
			
			ПараметрыИсточника = Новый Структура;
			ПараметрыИсточника.Вставить("ИдентификаторКоманды", ОписаниеКоманды.Идентификатор);
			ПараметрыИсточника.Вставить("ОбъектыНазначения"   , ОбъектыОтправки);
			ОписаниеКоманды.Вставить("ПараметрыИсточника", ПараметрыИсточника);
			
		КонецЕсли;
		
		ОписаниеКоманды.ДополнительныеПараметры.Вставить("ФормироватьСтандартноеОписаниеПисьма", Ложь);
		ОписаниеКоманд.Добавить(ОписаниеКоманды);
		
	КонецЦикла;
	
	Если ОписаниеКоманд.Количество() > 0 Тогда
		ПараметрыПисьма = ОтправкаПочтовыхСообщенийВызовСервера.ПараметрыЭлектронногоПисьма(ОписаниеКоманд,
			ЕстьОшибки, ДополнительныеПараметры);
		ПараметрыПисьма.Вставить("ОбъектыНазначения" , ОбъектыОтправки);
		Если ЗначениеЗаполнено(ПараметрыПисьма.Вложения) И НЕ ЕстьОшибки Тогда
			
			Если МобильныйКлиентБПКлиент.ДоступенМобильныйПочтовыйКлиент()
				И Не СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().ИспользоватьПочтовыйКлиент
				И Не РаботаСПочтовымиСообщениямиВызовСервера.ЕстьДоступныеУчетныеЗаписиДляОтправки() Тогда
				
				ОтправитьНовоеПисьмоМК(ПараметрыПисьма);
				Возврат;
				
			КонецЕсли;
			
			КлючеваяОперация = "БыстраяОтправкаДокументаПоЭлектроннойПочте";
			ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
			РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Отправляет по электронной почте результат выполнения отчета.
//
// Параметры:
//  Отчет - ФормаКлиентскогоПриложения - форма отправляемого отчета.
//  ДополнительныеПараметры - Структура - дополнительные параметры для формирования письма:
//   * Вложения - Соответствие - описание дополнительных вложений для передачи по почте:
//     ** Ключ - Строка - адрес двоичных данных во временном хранилище.
//     ** Значение - Строка - имя файла.
//
Процедура ОтправитьОтчет(Отчет, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ТабличныйДокумент", Отчет.Результат);
	ПараметрыОтчета.Вставить("Заголовок"        , Отчет.Заголовок);
	
	КлючеваяОперация = "ОтправкаОтчетаПоЭлектроннойПочте";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	ПараметрыПисьма = ОтправкаПочтовыхСообщенийВызовСервера.ПараметрыЭлектронногоПисьмаДляОтчетов(ПараметрыОтчета,
		ДополнительныеПараметры);
	
	Если МобильныйКлиентБПКлиент.ДоступенМобильныйПочтовыйКлиент()
		И Не СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().ИспользоватьПочтовыйКлиент
		И Не РаботаСПочтовымиСообщениямиВызовСервера.ЕстьДоступныеУчетныеЗаписиДляОтправки() Тогда
		
		ОтправитьНовоеПисьмоМК(ПараметрыПисьма);
		Возврат;
		
	КонецЕсли;
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

Функция СписокПолучателейИзСтроки(Знач Получатели)
	
	Результат = Новый СписокЗначений;
	
	АдресаЭлектроннойПочтыИзСтроки = ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки(Получатели);
	Для Каждого ОписаниеАдреса Из АдресаЭлектроннойПочтыИзСтроки Цикл
		Если ЗначениеЗаполнено(ОписаниеАдреса.Адрес) Тогда
			Результат.Добавить(ОписаниеАдреса.Адрес, ОписаниеАдреса.Псевдоним);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ОтправитьНовоеПисьмоМК(ПараметрыОтправкиПисьма, ОповещениеОЗакрытииФормы = Неопределено) Экспорт
	
		ПараметрыОтправки = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
		ПараметрыОтправки.Вставить("ПоказыватьДиалогВыбораФорматаСохраненияВложений", Ложь);
		ПараметрыОтправки.Вставить("ОповещениеОЗакрытииФормы", Неопределено);
		
		Если ПараметрыОтправкиПисьма <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтправки, ПараметрыОтправкиПисьма, Истина);
		КонецЕсли;
		
		СведенияДляОтправки = РаботаСПочтовымиСообщениямиВызовСервера.СведенияДляОтправки(ПараметрыОтправки);
		ПараметрыОтправки.ПоказыватьДиалогВыбораФорматаСохраненияВложений = СведенияДляОтправки.ПоказыватьДиалогВыбораФорматаСохраненияВложений;
		ПараметрыОтправки.ОповещениеОЗакрытииФормы = ОповещениеОЗакрытииФормы;
		
		Если ТипЗнч(ПараметрыОтправки.Получатель) = Тип("Строка") Тогда
			ПараметрыОтправки.Получатель = СписокПолучателейИзСтроки(ПараметрыОтправки.Получатель);
		КонецЕсли;
		
		ПараметрыПисьмаПоУмолчанию = ОтправкаПочтовыхСообщенийВызовСервера.ПараметрыПисьмаПоУмолчанию();
		
		ПараметрыПисьма = Новый Структура;
		ПараметрыПисьма.Вставить("Тема");
		ПараметрыПисьма.Вставить("Вложения");
		ПараметрыПисьма.Вставить("ШаблоныПисьма");
		ПараметрыПисьма.Вставить("АдресПолучателяСкрытойКопии");
		ПараметрыПисьма.Вставить("УдалятьФайлыПослеОтправки");
		ПараметрыПисьма.Вставить("ВыбиратьПолучателей");
		ПараметрыПисьма.Вставить("АдресИсходногоВложения");
		ПараметрыПисьма.Вставить("ДанныеУчастника");
		ЗаполнитьЗначенияСвойств(ПараметрыПисьма, ПараметрыОтправки);
		
		Если ПараметрыОтправки.Свойство("Отправитель") Тогда
			Если ЗначениеЗаполнено(ПараметрыОтправки.Отправитель) Тогда
				Отправитель = ПараметрыОтправки.Отправитель;
			Иначе
				Отправитель = ПараметрыПисьмаПоУмолчанию.Отправитель;
			КонецЕсли;
			
			ПараметрыПисьма.Вставить("Отправитель", Отправитель);
		КонецЕсли;
		
		Если ПараметрыОтправки.Свойство("Получатель") Тогда
			ПараметрыПисьма.Вставить("Получатель", ПараметрыОтправки.Получатель);
		КонецЕсли;
		
		Если ПараметрыОтправки.Свойство("Текст") Тогда
			Если ЗначениеЗаполнено(ПараметрыОтправки.Текст) Тогда
				Текст = ПараметрыОтправки.Текст;
			Иначе
				Текст = ПараметрыПисьмаПоУмолчанию.Текст;
			КонецЕсли;
			
			ПараметрыПисьма.Вставить("Текст", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыПисьма.АдресПолучателяСкрытойКопии) Тогда
			ПараметрыПисьма.АдресПолучателяСкрытойКопии = ПараметрыПисьмаПоУмолчанию.АдресПолучателяСкрытойКопии;
		КонецЕсли;
	
		ОткрытьФорму("ОбщаяФорма.ОтправкаСообщенияБП", ПараметрыПисьма, , , , , ОповещениеОЗакрытииФормы);
	
КонецПроцедуры

// Дополняет параметры письма для принудительной отправки с использованием "легкой" почты -
// без регистрации письма во взаимодействиях. Используется, например,
// при отправке сообщений в службу технической поддержки или при сборе обратной связи.
//
// Параметры:
//  ПараметрыПисьма - Структура - см. РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма()
//
Процедура ДополнитьПараметрыПисьмаДляОтправкиЛегкойПочтой(ПараметрыПисьма) Экспорт
	
	ПараметрыПисьма.Вставить("ИспользоватьВзаимодействияБП", Ложь);
	
КонецПроцедуры

#КонецОбласти
