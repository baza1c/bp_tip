
#Область СлужебныеПроцедурыИФункции

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	Списки.Вставить(Метаданные.Документы.ДепонированиеЗарплаты, Истина);
	Списки.Вставить(Метаданные.Справочники.ДепонированиеЗарплатыПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Документы.СписаниеДепонированнойЗарплаты, Истина);
	Списки.Вставить(Метаданные.Справочники.СписаниеДепонированнойЗарплатыПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.РегистрыНакопления.ВзаиморасчетыСДепонентами, Истина);
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных.
Процедура ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание) Экспорт
	
	Описание = Описание + "
	|Документ.ДепонированиеЗарплаты.Чтение.ГруппыФизическихЛиц
	|Документ.ДепонированиеЗарплаты.Чтение.Организации
	|Документ.ДепонированиеЗарплаты.Изменение.ГруппыФизическихЛиц
	|Документ.ДепонированиеЗарплаты.Изменение.Организации
	|Справочник.ДепонированиеЗарплатыПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.ДепонированиеЗарплатыПрисоединенныеФайлы.Чтение.Организации
	|Справочник.ДепонированиеЗарплатыПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.ДепонированиеЗарплатыПрисоединенныеФайлы.Изменение.Организации
	|Документ.СписаниеДепонированнойЗарплаты.Чтение.ГруппыФизическихЛиц
	|Документ.СписаниеДепонированнойЗарплаты.Чтение.Организации
	|Документ.СписаниеДепонированнойЗарплаты.Изменение.ГруппыФизическихЛиц
	|Документ.СписаниеДепонированнойЗарплаты.Изменение.Организации
	|Справочник.СписаниеДепонированнойЗарплатыПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.СписаниеДепонированнойЗарплатыПрисоединенныеФайлы.Чтение.Организации
	|Справочник.СписаниеДепонированнойЗарплатыПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.СписаниеДепонированнойЗарплатыПрисоединенныеФайлы.Изменение.Организации
	|РегистрНакопления.ВзаиморасчетыСДепонентами.Чтение.ГруппыФизическихЛиц
	|РегистрНакопления.ВзаиморасчетыСДепонентами.Чтение.Организации";
	
КонецПроцедуры

#КонецОбласти

#Область ДокументДепонированиеЗарплаты

/// Обработчики событий модуля объекта документов Депонирование зарплаты.

Процедура ДепонированиеЗарплатыОбработкаПроведения(ДокументОбъект, Отказ) Экспорт
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ДокументОбъект);
	
	Ведомости	= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОбъект.Ведомость);
	ФизЛица		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДокументОбъект.Депоненты.ВыгрузитьКолонку("ФизическоеЛицо"));
	
	ЗарплатаКадры.ЗарегистрироватьОплатуВедомостей(
		ДокументОбъект, 
		ДокументОбъект.Организация, 
		Ведомости, 
		ФизЛица, 
		ДокументОбъект.Дата, 
		Отказ);
		
	Если НЕ Отказ Тогда
			
		ДанныеВедомости = ВзаиморасчетыССотрудниками.ДанныеВедомостейДляОплатыДокументом(ДокументОбъект.Ссылка, Ведомости, Физлица, Неопределено);
	
		ДепонируемаяЗарплата = УчетДепонированнойЗарплаты.НоваяТаблицаДепонированнойЗарплаты();
		Для Каждого СтрокаДанныхВедомости Из ДанныеВедомости Цикл
			СтрокаДепонируемойЗарплаты = ДепонируемаяЗарплата.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДепонируемойЗарплаты, СтрокаДанныхВедомости);
			СтрокаДепонируемойЗарплаты.Сумма = СтрокаДанныхВедомости.СуммаКВыплате;
		КонецЦикла;	
		ДепонируемаяЗарплата.ЗаполнитьЗначения(ДокументОбъект.Организация, "Организация");
		ДепонируемаяЗарплата.ЗаполнитьЗначения(ДокументОбъект.Дата, "Дата");
		ДепонируемаяЗарплата.ЗаполнитьЗначения(ДокументОбъект.Ведомость, "Ведомость");
			
		УчетДепонированнойЗарплаты.ЗарегистрироватьДепонированнуюЗарплату(ДокументОбъект.Движения, Отказ, ДепонируемаяЗарплата, Истина);
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область Печать

// Возвращает описание команды печати реестра депонированных сумм.
// 
// Возвращаемое значение:
//   Структура - структура с полями:
//		* ПредставлениеКоманды - строка.
//
Функция ОписаниеПечатиРеестраДепонированныхСумм() Экспорт
	
	ОписаниеПечатиРеестраДепонированныхСумм = Новый Структура;
	ОписаниеПечатиРеестраДепонированныхСумм.Вставить("ПредставлениеКоманды",  НСтр("ru = 'Печать реестра депонированных сумм'"));
	ОписаниеПечатиРеестраДепонированныхСумм.Вставить("ИмяМакета", "Документ.ДепонированиеЗарплаты.ПФ_MXL_РеестрДепонированныхСумм");
	ОписаниеПечатиРеестраДепонированныхСумм.Вставить("СинонимМакета", НСтр("ru = 'Реестр депонированных сумм'"));
	
	Возврат ОписаниеПечатиРеестраДепонированныхСумм
	
КонецФункции

Процедура ПриКомпоновкеОтчетаКнигаУчетаДепонентов(Объект, ДокументРезультат, СтандартнаяОбработка) Экспорт
	
	КлючВарианта = ЗарплатаКадрыОтчеты.КлючВарианта(Объект.КомпоновщикНастроек);
	ЭтоРасшифровка = Объект.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ЭтоРасшифровка");
	Если КлючВарианта <> "КнигаУчетаДепонентов" Или ЭтоРасшифровка Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		СтандартнаяОбработка = Ложь;
		ДокументРезультат.НачатьАвтогруппировкуСтрок();
		ДокументРезультат.Очистить();
		
		НастройкиОтчета = Объект.КомпоновщикНастроек.ПолучитьНастройки();
		НастройкиОтчета.Выбор.Элементы.Очистить();
		
		СоответствиеПользовательскихПолей = ЗарплатаКадрыОтчеты.СоответствиеПользовательскихПолей(НастройкиОтчета);
		
		ЗначениеПараметраПериод = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
		
		Если ЗначениеПараметраПериод <> Неопределено И ЗначениеПараметраПериод.Значение <> '00010101' Тогда
			НачалоПериода = ЗначениеПараметраПериод.Значение.ДатаНачала;
			КонецПериода  = ЗначениеПараметраПериод.Значение.ДатаОкончания;
		Иначе
			НачалоПериода = НачалоМесяца(ТекущаяДатаСеанса());
			КонецПериода  = КонецМесяца(ТекущаяДатаСеанса());
		КонецЕсли;
		
		Если НачалоПериода = '00010101' Тогда
			НачалоПериода = НачалоМесяца(ТекущаяДатаСеанса());
		КонецЕсли;
		
		Если КонецПериода = '00010101' Тогда
			КонецПериода  = КонецМесяца(ТекущаяДатаСеанса());
		КонецЕсли;
		
		Данные = Новый ДеревоЗначений;
		ДанныеРасшифровки = Неопределено;
		МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(Объект.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
		
		// Создадим и инициализируем процессор компоновки.
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВывода.УстановитьОбъект(Данные);
		
		// Обозначим начало вывода
		ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
		
		ВывестиМакетыСГруппировкамиКнигаДепонентов(ДокументРезультат,
			Данные, НачалоПериода, КонецПериода, СоответствиеПользовательскихПолей, НастройкиОтчета, ДанныеРасшифровки);
		Объект.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ДанныеРасшифровки", ПоместитьВоВременноеХранилище(ДанныеРасшифровки));
		
		ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
		
	Исключение
		ВызватьИсключение НСтр("ru = 'В настройку отчета внесены критичные изменения. Книга учета депонентов не будет сформирована.'") + " " + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

Процедура ВывестиМакетыСГруппировкамиКнигаДепонентов(ДокументРезультат, Данные, НачалоПериода, КонецПериода, СоответствиеПользовательскихПолей, НастройкиОтчета, ДанныеРасшифровки)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.КнигаУчетаДепонентов.ПФ_MXL_КнигаУчетаДепонентов");
	СтруктураЭлементов = ИнициализироватьСтруктуруПараметровРасшифровки(НастройкиОтчета, ДанныеРасшифровки, СоответствиеПользовательскихПолей);
	
	Для Каждого ДанныеШапкиОтчета Из Данные.Строки Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Шапка = Макет.ПолучитьОбласть("Шапка");
		Шапка.Параметры.Заполнить(ДанныеШапкиОтчета);
		Шапка.Параметры.Начало		= НачалоПериода;
		Шапка.Параметры.Окончание	= КонецПериода;
		
		РодительГруппировки = ДобавитьРасшифровкуГруппировки(СтруктураЭлементов, ДанныеШапкиОтчета, Шапка);
		
		ДокументРезультат.Вывести(Шапка);
		ИтогоОстатокНач = 0;
		
		Для Каждого ДанныеСтрокиОтчета Из ДанныеШапкиОтчета.Строки Цикл
			
			Строка = Макет.ПолучитьОбласть("Строка");
			Строка.Параметры.Заполнить(ДанныеСтрокиОтчета);
			
			ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыПользовательскихПолей(Строка, ДанныеСтрокиОтчета, СоответствиеПользовательскихПолей);
			РодительГруппировки2 = ДобавитьРасшифровкуГруппировки2(СтруктураЭлементов, ДанныеСтрокиОтчета, Строка, РодительГруппировки);
			
			НомераПлатежныхДокументов = "";
			Регистраторы = Новый Массив;
			Для Каждого СтрокаСНомером Из ДанныеСтрокиОтчета.Строки Цикл
				НомераПлатежныхДокументов = ?(ПустаяСтрока(НомераПлатежныхДокументов), "", НомераПлатежныхДокументов + "; ") + СтрокаСНомером.НомерПлатежногоДокумента;
				ДобавитьРасшифровкуГруппировки3(СтруктураЭлементов, ДанныеСтрокиОтчета, Строка, РодительГруппировки2);
			КонецЦикла;
			Строка.Параметры.НомерПлатежногоДокумента = НомераПлатежныхДокументов;
			ДокументРезультат.Вывести(Строка);
			
			Если ЗначениеЗаполнено(ДанныеСтрокиОтчета.ОстатокНач) Тогда
				ИтогоОстатокНач = ИтогоОстатокНач + ДанныеСтрокиОтчета.ОстатокНач;
			ИначеЕсли ЗначениеЗаполнено(ДанныеСтрокиОтчета.ВедомостьСуммаПоступления) Тогда
				ИтогоОстатокНач = ИтогоОстатокНач + ДанныеСтрокиОтчета.ВедомостьСуммаПоступления;
			КонецЕсли;
		КонецЦикла; 
		
		Подвал = Макет.ПолучитьОбласть("Подвал");
		Подвал.Параметры.Заполнить(ДанныеШапкиОтчета);
		Подвал.Параметры.ОстатокНаКонец = ИтогоОстатокНач - ДанныеШапкиОтчета.Выплачено;

		ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыПользовательскихПолей(Подвал, ДанныеШапкиОтчета, СоответствиеПользовательскихПолей);
		ДобавитьЭлементыРасшифровкиПользовательскихПолей(СтруктураЭлементов, ДанныеШапкиОтчета, РодительГруппировки, Подвал, Истина);
		ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, ДанныеШапкиОтчета, СтруктураЭлементов.ТипПоля, "ОстатокКон",
			СтруктураЭлементов.ЭлементГруппировки, ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать, Подвал);
		ЗавершитьГруппировкуРасшифровки(СтруктураЭлементов);
		
		// Главный бухгалтер
		ПараметрГлавныйБухгалтер = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ГлавныйБухгалтер"));
		Если ПараметрГлавныйБухгалтер.Использование Тогда
			ГлавныйБухгалтер = ПараметрГлавныйБухгалтер.Значение;
		Иначе
			
			ЗапрашиваемыеЗначения = Новый Структура("Организация,ГлавныйБухгалтер");
			ЗапрашиваемыеЗначения.Организация = ДанныеШапкиОтчета.Организация;
			ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗапрашиваемыеЗначения);
			
			ГлавныйБухгалтер = ЗапрашиваемыеЗначения.ГлавныйБухгалтер;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ГлавныйБухгалтер) Тогда
			ТаблицаФИО = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ГлавныйБухгалтер, "ИОФамилия", ДанныеШапкиОтчета.Год);
			Если ТаблицаФИО.Количество() > 0 Тогда
				Подвал.Параметры.ГлавныйБухгалтерРасшифровкаПодписи = ТаблицаФИО[0].ИОФамилия;
			КонецЕсли;
		КонецЕсли; 
			
		// Исполнитель
		ПараметрИсполнитель = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Исполнитель"));
		Если ПараметрИсполнитель.Использование Тогда
			Исполнитель = ПараметрИсполнитель.Значение;
		Иначе
			
			ЗапрашиваемыеЗначения = Новый Структура("Организация,Исполнитель");
			ЗапрашиваемыеЗначения.Организация = ДанныеШапкиОтчета.Организация;
			ПодписиДокументов.ЗаполнитьСведенияОПодписяхДокументовПоОрганизации(ЗапрашиваемыеЗначения);
			
			Исполнитель = ЗапрашиваемыеЗначения.Исполнитель;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Исполнитель) Тогда
			ТаблицаФИО = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, Исполнитель, "ИОФамилия", ДанныеШапкиОтчета.Год);
			Если ТаблицаФИО.Количество() > 0 Тогда
				Подвал.Параметры.ИсполнительРасшифровкаПодписи = ТаблицаФИО[0].ИОФамилия;
			КонецЕсли;
		КонецЕсли; 
			
		// Должность исполнителя
		ПараметрДолжностьИсполнителя = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДолжностьИсполнителя"));
		Если ПараметрДолжностьИсполнителя.Использование Тогда
			ДолжностьИсполнителя = ПараметрДолжностьИсполнителя.Значение;
		Иначе
			
			ЗапрашиваемыеЗначения = Новый Структура("Организация,ДолжностьИсполнителя");
			ЗапрашиваемыеЗначения.Организация = ДанныеШапкиОтчета.Организация;
			ПодписиДокументов.ЗаполнитьСведенияОПодписяхДокументовПоОрганизации(ЗапрашиваемыеЗначения);
			
			ДолжностьИсполнителя = ЗапрашиваемыеЗначения.ДолжностьИсполнителя;
			
		КонецЕсли;
		
		Подвал.Параметры.ДолжностьИсполнителя = ДолжностьИсполнителя;
			
		ДокументРезультат.Вывести(Подвал);
		
	КонецЦикла;
	
КонецПроцедуры

#Область КнигаУчетаДепонентовРасшифровка

Функция ИнициализироватьСтруктуруПараметровРасшифровки(НастройкиОтчета, ДанныеРасшифровки, СоответствиеПользовательскихПолей) Экспорт
	СтруктураЭлементов = Новый Структура("ЭлементГруппировки,ЭлементГруппировки2,ЭлементГруппировки3");
	СтруктураЭлементов.Вставить("ЭлементыРасшифровки", ДанныеРасшифровки.Элементы);
	СтруктураЭлементов.Вставить("ТипГруппировка", Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка"));
	СтруктураЭлементов.Вставить("ТипПоля", Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля"));
	СтруктураЭлементов.Вставить("СоответствиеПолей", СоответствиеПользовательскихПолей);
	Возврат СтруктураЭлементов;
КонецФункции

Функция ДобавитьРасшифровкуГруппировки(СтруктураЭлементов, Данные, ОбластьРасшифровки) Экспорт
	Если СтруктураЭлементов.ЭлементГруппировки = Неопределено Тогда
		СтруктураЭлементов.ЭлементГруппировки = СтруктураЭлементов.ЭлементыРасшифровки.Добавить(СтруктураЭлементов.ТипГруппировка, , "Группировка");
	КонецЕсли;
	РодительГруппировки = ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные,
		СтруктураЭлементов.ТипПоля, "Год,Организация", СтруктураЭлементов.ЭлементГруппировки, ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет);
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Организация",
		РодительГруппировки, ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение, ОбластьРасшифровки);
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Год",
		РодительГруппировки, ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Депонировано",
		РодительГруппировки, ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать);
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Выплачено",
		РодительГруппировки, ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать);

	Возврат РодительГруппировки;
КонецФункции

Функция ДобавитьРасшифровкуГруппировки2(СтруктураЭлементов, Данные, ОбластьРасшифровки, РодительГруппировки) Экспорт
	Если СтруктураЭлементов.ЭлементГруппировки2 = Неопределено Тогда
		СтруктураЭлементов.ЭлементГруппировки2 = СтруктураЭлементов.ЭлементыРасшифровки.Добавить(СтруктураЭлементов.ТипГруппировка,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РодительГруппировки), "Группировка2");
	КонецЕсли;
	ИменаПолей = "Ведомость,Сотрудник,ФизическоеЛицо,ОстатокНач";
	РодительГруппировки2 = ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, ИменаПолей,
		СтруктураЭлементов.ЭлементГруппировки2, ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет);
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Сотрудник",
		РодительГруппировки2, ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение, ОбластьРасшифровки);
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "ФизическоеЛицо",
		РодительГруппировки2, ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение, ОбластьРасшифровки);
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Ведомость.ДатаПоступления",
		РодительГруппировки2, ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение, ОбластьРасшифровки);
	ЭлементРасшифровки = ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Ведомость",
		РодительГруппировки2, ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
	ОбластьРасшифровки.Параметры.РасшифровкаВедомостьНомер = ЭлементРасшифровки.Идентификатор;
	ДобавитьЭлементРасшифровки(СтруктураЭлементов.ЭлементыРасшифровки, Данные, СтруктураЭлементов.ТипПоля, "Ведомость.СуммаПоступления",
		РодительГруппировки2, ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать, ОбластьРасшифровки);
	ДобавитьЭлементыРасшифровкиПользовательскихПолей(СтруктураЭлементов, Данные, РодительГруппировки2, ОбластьРасшифровки);
	Возврат РодительГруппировки2;
КонецФункции

Функция ДобавитьРасшифровкуГруппировки3(СтруктураЭлементов, Данные, ОбластьРасшифровки, РодительГруппировки) Экспорт
	Если СтруктураЭлементов.ЭлементГруппировки3 = Неопределено Тогда
		СтруктураЭлементов.ЭлементГруппировки3 = СтруктураЭлементов.ЭлементыРасшифровки.Добавить(СтруктураЭлементов.ТипГруппировка,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РодительГруппировки), "Группировка3");
	КонецЕсли;
	ПоляРасшифровки = Новый ЗначенияПолейРасшифровкиКомпоновкиДанных();
	НовоеПоле = ПоляРасшифровки.Добавить();
	НовоеПоле.Значение = Данные.МесяцДвижения;
	НовоеПоле.Поле = "МесяцДвижения";
	НовоеПоле = ПоляРасшифровки.Добавить();
	НовоеПоле.Значение = Данные.НомерПлатежногоДокумента;
	НовоеПоле.Поле = "НомерПлатежногоДокумента";
	НовоеПоле = ПоляРасшифровки.Добавить();
	НовоеПоле.Значение = Данные.Регистратор;
	НовоеПоле.Поле = "Регистратор";
	РодительГруппировки3 = СтруктураЭлементов.ЭлементыРасшифровки.Добавить(СтруктураЭлементов.ТипПоля,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтруктураЭлементов.ЭлементГруппировки3), ПоляРасшифровки);
	РодительГруппировки3.ОсновноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет;
	Возврат РодительГруппировки3;
КонецФункции

Функция ДобавитьЭлементРасшифровки(ЭлементыРасшифровки, Данные, ТипПоля, ИменаПолей, Родитель, ДействиеРасшифровки, ОбластьРасшифровки = Неопределено) Экспорт
	ИменаПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаПолей);
	ПоляРасшифровки = Новый ЗначенияПолейРасшифровкиКомпоновкиДанных();
	Для Каждого Поле Из ИменаПолей Цикл
		НовоеПоле = ПоляРасшифровки.Добавить();
		НовоеПоле.Значение = Данные[СтрЗаменить(Поле, ".", "")];
		НовоеПоле.Поле = Поле;
	КонецЦикла;
	ЭлементРасшифровки = ЭлементыРасшифровки.Добавить(ТипПоля, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Родитель), ПоляРасшифровки);
	ЭлементРасшифровки.ОсновноеДействие = ДействиеРасшифровки;
	Если ОбластьРасшифровки <> Неопределено Тогда
		ОбластьРасшифровки.Параметры["Расшифровка" + СтрЗаменить(Поле, ".", "")] = ЭлементРасшифровки.Идентификатор;
	КонецЕсли;
	Возврат ЭлементРасшифровки;
КонецФункции

Процедура ДобавитьЭлементыРасшифровкиПользовательскихПолей(СтруктураЭлементов, Данные, Родитель, ОбластьРасшифровки = Неопределено, Итоги = Ложь) Экспорт
	Для Каждого ПользовательскоеПоле Из СтруктураЭлементов.СоответствиеПолей Цикл
		ПоляРасшифровки = Новый ЗначенияПолейРасшифровкиКомпоновкиДанных();
		НовоеПоле = ПоляРасшифровки.Добавить();
		НовоеПоле.Значение = Данные[ПользовательскоеПоле.Значение];
		НовоеПоле.Поле = ПользовательскоеПоле.Значение;
		ЭлементРасшифровки = СтруктураЭлементов.ЭлементыРасшифровки.Добавить(
			СтруктураЭлементов.ТипПоля, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Родитель), ПоляРасшифровки);
		ЭлементРасшифровки.ОсновноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать;
		Если ОбластьРасшифровки <> Неопределено Тогда
			Если Не Итоги И СтрНайти(ПользовательскоеПоле.Ключ, "Итог") <> 0 Тогда
				Продолжить;
			КонецЕсли;
			ОбластьРасшифровки.Параметры["Расшифровка" + ПользовательскоеПоле.Ключ] = ЭлементРасшифровки.Идентификатор;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗавершитьГруппировкуРасшифровки(СтруктураЭлементов) Экспорт
	СтруктураЭлементов.ЭлементыРасшифровки.Добавить(СтруктураЭлементов.ТипГруппировка,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтруктураЭлементов.ЭлементГруппировки2), "Группировка3");
	СтруктураЭлементов.ЭлементГруппировки2 = Неопределено;
	СтруктураЭлементов.ЭлементГруппировки3 = Неопределено;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.КнигаУчетаДепонентов);
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.КнигаУчетаДепонентов, "Форма0504048");
	Вариант.Включен = Ложь;

КонецПроцедуры

#КонецОбласти
