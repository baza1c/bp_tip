
#Область ПрограммныйИнтерфейс

#Область Локализация

Процедура ПриОпределенииМенеджераОбъектаПоПолномуИмени(МенеджерОбъекта, ПолноеИмя) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ПриОпределенииМенеджераОбъектаПоСсылке(МенеджерОбъекта, Ссылка) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОрганизацииИКонтрагенты

// Требуется определить массив ссылок служебных организаций, исключаемых из списков выбора в библиотеке.
// 
// Параметры:
//  СписокОрганизаций - Массив из ОпределяемыйТип.Организация - переопределяемый параметр, массив организаций, исключаемых из списков.
Процедура ПриОпределенииСпискаОрганизацийНедоступныхДляВыбора(СписокОрганизаций) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

// Проверяет использование подразделений в информационной базе.
// 
// Параметры:
//  Использовать - Булево - Подразделения используются (исходящий).
//
Процедура ИспользованиеПодразделений(Использовать) Экспорт
	
	
КонецПроцедуры

// В процедуре требуется определить признак использования характеристик 
//   * для номенклатуры, если передана номенклатура,
//   * в программе, если не передана.
//
// Параметры:
//  Использование - Булево - Признак использования характеристик (для номенклатуры или общий)
//  Номенклатура - ОпределяемыйТип.Номенклатура, Неопределено - Ссылка на элемент номенклатуры.
//
Процедура ИспользованиеХарактеристик(Использование, Номенклатура) Экспорт
	
	Использование = Ложь;
	
КонецПроцедуры

// В процедуре требуется определить признак использования серий
//   * для номенклатуры, если передана номенклатура,
//   * в программе, если не передана.
//
// Параметры:
//  Использование - Булево - Признак использования серий (для номенклатуры или общий)
//  Номенклатура - ОпределяемыйТип.Номенклатура, Неопределено - Ссылка на элемент номенклатуры.
//
Процедура ИспользованиеСерий(Использование, Номенклатура) Экспорт
	
	Использование = Ложь;
	
КонецПроцедуры

// Определяет использование в инофрмационной базе обособленных подразделений выделенных на отдельный баланс
// 
// Параметры:
//  Использовать - Булево - Используются подразделения, выделенных на обособленный баланс
//
Процедура ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс(Использовать) Экспорт

	Использовать = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделения");
	
КонецПроцедуры

// В процедуре требуется определить наличие товаров по виду продукции для которых используется механизм частичного выбытия
//
// Параметры:
//  Использование - Булево - Признак использования частичного выбытия (для вида продукции)
//  ВидПродукцииИС - Перечислениессылка.ВидыПродукцииИС - Проверяемый вид продукции.
//
Процедура ПриОпределенииИспользованияЧастичногоВыбытия(Использование, ВидПродукцииИС) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидПродукцииИС", ВидПродукцииИС);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|		ПО ОписаниеНоменклатурыИС.Номенклатура = СправочникНоменклатура.Ссылка
		|		И ОписаниеНоменклатурыИС.ВариантЧастичногоВыбытия <> ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.ПустаяСсылка)";
	
	Использование = Не Запрос.Выполнить().Пустой();

	
КонецПроцедуры

// В процедуре нужно реализовать заполнение структуры данными ИНН/КПП из информационной базы.
//
// Параметры:
//  Сведения    - Структура - структура с реквизитами организации/контрагента, которые требуется заполнить,
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС, ОпределяемыйТип.КонтрагентГосИС - ссылка на 
//                                                                                              собственную организацию или контрагента
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - ссылка на торговый объект для определения КПП.
Процедура ЗаполнитьИННКПППоОрганизацииКонтрагенту(Сведения, Организация, ТорговыйОбъект = Неопределено) Экспорт
	
	Подразделение = Неопределено;
	
	Если ТорговыйОбъект <> Неопределено Тогда
		РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТорговыйОбъект, "ПодразделениеОрганизации");
		Подразделение   = РеквизитыСклада.ПодразделениеОрганизации;
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		ИННКПП = Новый Структура("ИНН,КПП");
		ИННКПП.ИНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация.Владелец, "ИНН");
		ИННКПП.КПП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "КПП");
	Иначе
		ИННКПП = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ИНН, КПП");
		Если ЗначениеЗаполнено(Подразделение) Тогда
			ИННКПП.Вставить("КПП", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "КПП"));
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Сведения, ИННКПП);
	
КонецПроцедуры

// Процедура предназначена для получения данных ИНН и КПП из прикладных объектов организаций и контрагентов.
// 
// Параметры:
//  СоответствиеДанных    - Соответствие из КлючИЗначение:
//  	* Ключ - ОпределяемыйТип.ОрганизацияКонтрагентГосИС, ОпределяемыйТип.КонтрагентГосИС - запрошенная организация/контрагент
//  	* Значение - Структура из КлючИЗначение:
//  		** ИНН - Строка
//  		** КПП - Строка
//  МассивОрганизацийКонтрагентов - Массив из ОпределяемыйТип.ОрганизацияКонтрагентГосИС, 
//												*ОпределяемыйТип.КонтрагентГосИС - массив ссылок на 
//                                              *собственные организации или контрагенты.
Процедура ЗаполнитьИННКПППоНесколькимОрганизациямКонтрагентам(СоответствиеДанных, МассивОрганизацийКонтрагентов) Экспорт
	
	Организации 	= Новый Массив;
	Контрагенты 	= Новый Массив;
	Подразделения 	= Новый Массив;
	
	Для Каждого ОрганизацияКонтрагент Из МассивОрганизацийКонтрагентов Цикл
		Если ТипЗнч(ОрганизацияКонтрагент) = Тип("СправочникСсылка.Организации") Тогда
			Организации.Добавить(ОрганизацияКонтрагент);
		ИначеЕсли ТипЗнч(ОрганизацияКонтрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
			Контрагенты.Добавить(ОрганизацияКонтрагент);
		ИначеЕсли ТипЗнч(ОрганизацияКонтрагент) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			Подразделения.Добавить(ОрганизацияКонтрагент);
		Иначе
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка КАК Ссылка,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка В(&Контрагенты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка,
	|	Организации.ИНН,
	|	Организации.КПП
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&Организации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка,
	|	ПодразделенияОрганизаций.ГоловнаяОрганизация.ИНН,
	|	ПодразделенияОрганизаций.КПП
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Ссылка В(&Подразделения)";
	
	Запрос.УстановитьПараметр("Организации"		, Организации);
	Запрос.УстановитьПараметр("Контрагенты"		, Контрагенты); 
	Запрос.УстановитьПараметр("Подразделения"	, Подразделения);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОписаниеИННКПП = Новый Структура("ИНН, КПП", "", "");
		ЗаполнитьЗначенияСвойств(ОписаниеИННКПП, Выборка);
		СоответствиеДанных.Вставить(Выборка.Ссылка, ОписаниеИННКПП);
	КонецЦикла;
	
КонецПроцедуры

// В процедуре нужно реализовать заполнение структуры данными ИНН/КПП/GLN из информационной базы.
//
// Параметры:
//  Сведения      - Структура - структура с реквизитами организации, которые требуется заполнить,
//  Организация   - ОпределяемыйТип.Организация - ссылка на собственную организацию,
//  Подразделение - ОпределяемыйТип.Подразделение - Подразделение организации.
Процедура ПриОпределенииИННКППGLNОрганизации(Сведения, Организация, Подразделение) Экспорт
	
	ЗначенияЗаполнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ИНН, КПП");
	
	Если ЗначениеЗаполнено(Подразделение) И Подразделение.ОбособленноеПодразделение Тогда
		ЗначенияЗаполнения.Вставить("КПП", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "КПП"));
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Сведения, ЗначенияЗаполнения);
		
КонецПроцедуры

// Работа с регистром "ОписаниеНоменклатурыИС" при использовании значений описания номенклатуры отличных от напрямую
//   записанных в регистре:
//   * Модифицирует соединение с регистром если в качестве источника описания может использоваться не номенклатура,
//   * Получает значение служебного реквизита "Произвольная единица учета" (для номенклатуры единица учета отличается от
//     маркированной потребительской упаковки),
//   * Получает значение служебного реквизита "Требует взвешивания" (маркированные потребительские упаковки имеют
//     индивидуальный вес).
//
// Параметры:
//  ТекстЗапроса           - Строка - текст запроса с регистром "ОписаниеНоменклатурыИС" (исходящий),
//  ПутьКРегистру          - Строка - имя таблицы регистра "ОписаниеНоменклатурыИС" в запросе,
//  ПутьКПолюНоменклатура  - Строка - путь к номенклатуре в запросе
//  ПутьКИсточникуОписания - Строка - путь к источнику описания
Процедура ОбновитьТекстЗапросаСРегистромОписаниеНоменклатурыИС(ТекстЗапроса, ПутьКРегистру, ПутьКПолюНоменклатура, ПутьКИсточникуОписания) Экспорт
	
	//++ НЕ ГОСИС
	ИмяПоляЗамены = "&ТоварыОписаниеНоменклатурыИС";
	ТекстЗамены = СтрШаблон("%2.Номенклатура = %1", ПутьКПолюНоменклатура, ПутьКРегистру);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяПоляЗамены, ТекстЗамены);
	// Получение признака Произвольная единица учета
	ИмяПоляЗамены = "&ПроизвольнаяЕдиницаУчета";
	ТекстЗамены = СтрШаблон("ЕСТЬNULL(%1.КоличествоВПотребительскойУпаковке, 1) <> 1
	|	ИЛИ ЕСТЬNULL(%1.ЕмкостьПотребительскойУпаковки, 1) > 1
	|	ИЛИ (
	|	1 В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			1
	|		ИЗ
	|			РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НастройкиЧастичногоВыбытияПродукцииИС
	|		ГДЕ
	|			%2 = НастройкиЧастичногоВыбытияПродукцииИС.НоменклатураЧастичногоВыбытия)
	|	)
	|	ИЛИ (%1.Номенклатура ЕСТЬ NULL
	|		И %2.ВидМаркировки В (
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво))
	|		И %2.ЕдиницаИзмерения.Код <> ""796"")
	|", ПутьКРегистру, ПутьКПолюНоменклатура);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяПоляЗамены, ТекстЗамены);
	// Получение признака Требует взвешивания
	ИмяПоляЗамены = "&ТребуетВзвешивания";
	ТекстЗамены = СтрШаблон("(%1.Номенклатура ЕСТЬ NULL ИЛИ %1.ВариантИспользованияЕдиницыХранения = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.МернаяПродукцияТребуетУказанияЗначения))
	|	И %2.ВидМаркировки В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво))
	|	И %2.ЕдиницаИзмерения.Код <> ""796""
	|", ПутьКРегистру, ПутьКПолюНоменклатура);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяПоляЗамены, ТекстЗамены);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Работа с регистром "ОписаниеНоменклатурыИС" при использовании значений описания номенклатуры отличных от напрямую
//   записанных в регистре, а также при использовании режима "ВариантыИспользованияЕдиницыХраненияИС.ЗаданУпаковками":
//   * Дополняет входящий текст запроса.
//
// Параметры:
//  ТекстЗапроса - Строка - Текст запроса получения описания номенклатуры ИС.
Процедура ДополнитьТекстЗапросаОписанияНоменклатуры(ТекстЗапроса) Экспорт
	
	//++ НЕ ГОСИС

	// Дополняем:
	// 1. Получение реквизита "Вариант частичного выбытия" из явного указания субпотребительской упаковки
	// 2. Получение реквизита "ЕмкостьПотребительскойУпаковки" в зависимости от указания упаковок
	// 3. Изменение значения по-умолчанию для продукции с мерным типом измеряемой величины единицы хранения
	// 4. Не для алкоголя и немаркируемой продукции
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ОписаниеНоменклатурыИС.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.ЗаданУпаковками)
		|				И ОписаниеНоменклатурыИС.ПотребительскаяУпаковка <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
		|				И ОписаниеНоменклатурыИС.УпаковкаЧастичногоВыбытия <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
		|			ТОГДА ОписаниеНоменклатурыИС.ЕмкостьПотребительскойУпаковки
		|		КОГДА ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.ЗаданУпаковками)
		|			ТОГДА 0
		|		ИНАЧЕ ОписаниеНоменклатурыИС.ЕмкостьПотребительскойУпаковки
		|	КОНЕЦ                               КАК ЕмкостьПотребительскойУпаковки,
		|	ОписаниеНоменклатурыИС.КоличествоВПотребительскойУпаковке  КАК КоличествоВПотребительскойУпаковке,
		|	ВЫБОР
		|		КОГДА ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.ЗаданУпаковками)
		|			ТОГДА ОписаниеНоменклатурыИС.ВариантЧастичногоВыбытия
		|		КОГДА ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.ЧастьПотребительскойУпаковки)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.ТекущаяНоменклатура)
		|		КОГДА ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.ГрупповаяЛогистическаяУпаковка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.ПустаяСсылка)
		|		КОГДА ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.ПотребительскаяУпаковка)
		|			ТОГДА ОписаниеНоменклатурыИС.ВариантЧастичногоВыбытия
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.ПустаяСсылка)
		|	КОНЕЦ                                                      КАК ВариантЧастичногоВыбытия,
		|	ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения КАК ВариантИспользованияЕдиницыХранения,
		|	ОписаниеНоменклатурыИС.ПотребительскаяУпаковка             КАК ПотребительскаяУпаковка,
		|	ОписаниеНоменклатурыИС.УпаковкаЧастичногоВыбытия           КАК УпаковкаЧастичногоВыбытия
		|ИЗ
		|	РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|ГДЕ
		|	ОписаниеНоменклатурыИС.Номенклатура В (&Номенклатура)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|
		|	СправочникНоменклатура.Ссылка,
		|	Неопределено,
		|	ВЫБОР
		|		КОГДА СправочникНоменклатура.ВидМаркировки В (
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво))
		|			И СправочникНоменклатура.ЕдиницаИзмерения.Код <> ""796"" 
		|			ТОГДА 0
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СправочникНоменклатура.ВидМаркировки <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Духи)
		|			И СправочникНоменклатура.ВидМаркировки <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак)
		|			И СправочникНоменклатура.ВидМаркировки <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)
		|		ТОГДА ЗНАЧЕНИЕ (Перечисление.ВариантыУчетаЧастичногоВыбытияИС.ПустаяСсылка)
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СправочникНоменклатура.ВидМаркировки В (
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво))
		|			И СправочникНоменклатура.ЕдиницаИзмерения.Код <> ""796""
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияЕдиницыХраненияИС.МернаяПродукцияТребуетУказанияЗначения)
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	Неопределено,
		|	Неопределено
		|ИЗ Справочник.Номенклатура КАК СправочникНоменклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|	ПО ОписаниеНоменклатурыИС.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	СправочникНоменклатура.Ссылка В (&Номенклатура)
		|	И ОписаниеНоменклатурыИС.Номенклатура ЕСТЬ NULL
		|";
		
	//-- НЕ ГОСИС

	Возврат;
	
КонецПроцедуры

// Работа с регистром "ОписаниеНоменклатурыИС" при использовании источника описания номенклатуры отличного от ссылки 
//   на номенклатуру:
//   * Дополняет входящий массив классами номенклатуры,
//   * Возвращает соответствие добавленных классов и исходной номенклатуры.
//
// Параметры:
//  Номенклатура - Массив Из ОпределяемыйТип.ИсточникОписанияноменклатурыИС - Источник описания номенклатуры.
//  СоответстиеИсточникаНоменклатуре - Соответствие из КлючИЗначение - описание добавленных классов номенклатуры:
//   * Ключ - ОпределяемыйТип.ИсточникОписанияНоменклатурыИС - Источник описания номенклатуры,
//   * Значение - Массив Из ОпределяемыйТип.Номенклатура - номенклатура из входящего массива
Процедура ДополнитьИсточникиОписанияНоменклатуры(Номенклатура, СоответстиеИсточникаНоменклатуре) Экспорт
	
	Возврат;
	
КонецПроцедуры

// В процедуре нужно реализовать поиск организации предприятия по ИНН и/или КПП.
//
// Параметры:
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - найденная организация (исходящий),
//              - Неопределено - организация не найдена (исходящий),
//  ИНН - Строка - ИНН искомой организации,
//  КПП - Строка - КПП искомой организации.
//
Процедура ЗаполнитьОрганизациюПоИННКПП(Организация, ИНН, КПП) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Организация = КонтекстЭДОСервер.ОпределитьОрганизациюПоИННиКПП(ИНН, КПП);

	
КонецПроцедуры

// Проверка, является ли партнер индивидуальным предпринимателем.
// 
// Параметры:
//  ОрганизацияКонтрагент - ОпределяемыйТип.Организация, ОпределяемыйТип.КонтрагентГосИС - Партнер или организация
//  ЭтоИндивидуальныйПредприниматель - Булево - Истина, если это индивидуальный предприниматель
//
Процедура ПартнерОрганизацияЭтоИндивидуальныйПредприниматель(ОрганизацияКонтрагент, ЭтоИндивидуальныйПредприниматель) Экспорт
	
	ЭтоИндивидуальныйПредприниматель = Ложь;
	
	Если ТипЗнч(ОрганизацияКонтрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		ЭтоИндивидуальныйПредприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОрганизацияКонтрагент, "ИндивидуальныйПредприниматель");
	ИначеЕсли ТипЗнч(ОрганизацияКонтрагент) = Тип("СправочникСсылка.Организации") Тогда
		ЭтоИндивидуальныйПредприниматель = ОрганизацияКонтрагент.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
											И ОрганизацияКонтрагент.СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.ИндивидуальныйПредприниматель;
	КонецЕсли;
				
КонецПроцедуры

// В процедуре необходимо реализовать проверку возможности выбора серии для номенклатуры.
// 
// Параметры:
//  ДанныеДляРасчетаСерии  - Структура - Данные для расчета серий.
//  ПараметрыУказанияСерий - Структура - Параметры указания серий.
//  ТребуетсяВыбор         - Булево - исходящий, признак необходимости выбора серии.
//  СерияДолжнаБытьУказана - Булево - исходящий, признак обязательного заполнения серии.
//  КэшированныеЗначения   - Произвольный - кэшированные значения
//
Процедура ПриОпределенииНеобходимостиВыбораСерии(ДанныеДляРасчетаСерии, ПараметрыУказанияСерий, ТребуетсяВыбор, СерияДолжнаБытьУказана, КэшированныеЗначения = Неопределено) Экспорт
	
	Возврат;
	
КонецПроцедуры

// В процедуре требуется определить наличие маркируемых товаров для которых нет стандартной фабричной упаковки
//   (настраиваемых не в регистре ОписаниеноменклатурыИС).
//
// Параметры:
//  Использование - Булево - Признак использования произвольных единиц учета (исходящий).
//
Процедура ИспользованиеПродукцииТребующейВзвешивания(Использование) Экспорт
	
	Использование = Истина;
	
КонецПроцедуры

// Для БРМК. Определяет коды маркировки на оборудовании розлива по переданной таблице товаров.
// Необходимо заполнить выходной параметр КегиНаОборудованииРозлива
// 
// Параметры:
//  ВскрытыеПотребительскиеУпаковки - ТаблицаЗначений - Коды маркировки подключенные к оборудованию розлива:
//  * Номенклатура - ОпределяемыйТип.Номенклатура - номенклатура кода маркировки кега
//  * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика кода маркировки кега
//  * Серия - ОпределяемыйТип.СерияНоменклатуры - серия кода маркировки кега
//  * ИндексИсходнойСтроки - Число - Служебный, в переопределении можно не заполнять
//  * УчитыватьСерии - Булево - учитвать серии исходной таблицы товаров
//  * ВариантЧастичногоВыбытия - ПеречислениеСсылка.ВариантыУчетаЧастичногоВыбытияИС - настройка частичного выбытия номенклатуры
//  * КодМаркировки - ОпределяемыйТип.ШтрихкодУпаковкиИС - код маркировки кега
//  * Комментарий - ОпределяемыйТип.ОборудованиеРозливаИСМП - описание оборудования, к которому подключен кег см. РегистрыСведений.ВскрытыеПотребительскиеУпаковкиИС
//  * КодМаркировкиСтрокой - Строка - представление кода маркировки см. РегистрыСведений.ВскрытыеПотребительскиеУпаковкиИС
//  * ПолныйКодМаркировки - Строка - полный код маркировки кега Base64 см. РегистрыСведений.ВскрытыеПотребительскиеУпаковкиИС
//  ТаблицаТовары - см. РегистрыСведений.ВскрытыеПотребительскиеУпаковкиИС.НоваяТаблицаПоискаВскрытыхПотребительскихУпаковок
//  ПараметрыСканирования - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
//  ТолькоПолноеСоответствие - Булево - Искать без учета подменной номенклатуры
Процедура ПриОпределенииВскрытыхПотребительскихУпаковок(ВскрытыеПотребительскиеУпаковки, ТаблицаТовары, ПараметрыСканирования, ТолькоПолноеСоответствие) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Для БРМК. Определяет параметры полей описания номенклатуры по коду маркировки.
// 
// Параметры:
//  ПараметрыОписания - Структура:
//  *  ИмяПоляНоменклатураШтрихкода   - Строка - поле Номенклатура
//  *  ИмяПоляХарактеристикаШтрихкода - Строка - поле Характеристика
//  *  ИмяПоляСерияШтрихкода          - Строка - поле Серия
//  *  ПутьКИсточникуДанныхШтрихкода  - Строка - таблица источника
//  *  ИмяПоляКодМаркировки           - Строка - поле таблицы источника
Процедура ПриОпределенииПараметровИсточникаДанныхНоменклатурыПоКодуМаркировки(ПараметрыОписания) Экспорт
	
	Если Не ОбщегоНазначенияИС.ЭтоРасширеннаяВерсияГосИС() Тогда
		ПараметрыОписания.ИмяПоляНоменклатураШтрихкода  = "Владелец";
		ПараметрыОписания.ПутьКИсточникуДанныхШтрихкода = "РегистрСведений.Штрихкоды";
		ПараметрыОписания.ИмяПоляКодМаркировки          = "Штрихкод";
	КонецЕсли;
	
КонецПроцедуры

// Заполняет представление строки номенклатуры.
//
// Параметры:
//  Представление  - Строка                                     - представление для заполнения,
//  Номенклатура   - ОпределяемыйТип.Номенклатура               - ссылка на номенклатуру,
//  Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - ссылка на характеристику номенклатуры,
//  Упаковка       - ОпределяемыйТип.Упаковка                   - ссылка на упаковку.
//  Серия          - ОпределяемыйТип.СерияНоменклатуры          - ссылка на серию номенклатуры.
Процедура ЗаполнитьПредставлениеНоменклатуры(Представление, Номенклатура, Характеристика, Упаковка, Серия) Экспорт
	
	Если ТипЗнч(Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат;
	КонецЕсли; 
	
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "Наименование, НаименованиеПолное, ЕдиницаИзмерения", Истина);
	Представление = ?(ЗначениеЗаполнено(РеквизитыНоменклатуры.НаименованиеПолное), РеквизитыНоменклатуры.НаименованиеПолное, РеквизитыНоменклатуры.Наименование);
	
КонецПроцедуры

// Проверка, является ли переданный документ внутренним перемещением.
// Необходимо для проверки доступности передачи по ОСУ - есть виды продукции, по которым
// разрешена передача по ОСУ между своими подразделениями.
// 
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - Контекст.
//  ЭтоВнутреннееПеремещение - Булево - Истина, если это документ внутреннего перемещения
// 
Процедура ЭтоДокументВнутреннегоПеремещения(Контекст, ЭтоВнутреннееПеремещение) Экспорт
	
	Если Контекст = Неопределено Тогда
		Возврат;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	Если ИнтеграцияИСМППереопределяемый.ЭтоАвизоИсходящее(ИсточникДанных) 
		Или ИсточникДанных.Метаданные() = Метаданные.Документы.ПеремещениеТоваров Тогда
		ЭтоВнутреннееПеремещение = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Область ПодключаемоеОборудование

// Настраивает подключаемое оборудование в форме,
// устанавливает флаг ИспользоватьПодключаемоеОборудование в форме.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо настроить подключаемое оборудование,
//  ПрефиксыЭлементовФормы - Строка - строка имен табличных частей, разделенных запятыми, в которых нужно настроить видимость команд.
Процедура НастроитьПодключаемоеОборудование(Форма, ПрефиксыЭлементовФормы = "Товары") Экспорт
	
	Элементы = Форма.Элементы;
	
	ПодключаемоеОборудованиеМаркировка = Новый Массив;
	ПодключаемоеОборудованиеМаркировка.Добавить("ТерминалСбораДанных");
	ПодключаемоеОборудованиеМаркировка.Добавить("СканерШтрихкода");
	ПодключаемоеОборудованиеМаркировка.Добавить("СчитывательRFID");
	ПодключаемоеОборудованиеМаркировка.Добавить("ПринтерЭтикеток");
	ПодключаемоеОборудованиеМаркировка.Добавить("ЭлектронныеВесы");
	
	Форма.ИспользоватьПодключаемоеОборудование = МенеджерОборудованияБП.ИспользуетсяОборудование(ПодключаемоеОборудованиеМаркировка);
	
	// Использование ТСД
	ИспользуетсяТСД = Форма.ИспользоватьПодключаемоеОборудование 
		И МенеджерОборудованияБП.ИспользуетсяОборудование("ТерминалСбораДанных");
		
	ЗагружатьДанныеИзТСД = ИспользуетсяТСД;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "КоличествоСтрокПодобраннойПродукции") Тогда
		ВыгружатьДанныеВТСД = (Форма.КоличествоСтрокПодобраннойПродукции > 0) И ИспользуетсяТСД;
	Иначе
		ВыгружатьДанныеВТСД = Ложь;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ПрефиксыЭлементовФормы) Тогда
		МассивПрефиксыЭлементовФормы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПрефиксыЭлементовФормы, ",");
		
		Для Каждого Префикс Из МассивПрефиксыЭлементовФормы Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Префикс + "ЗагрузитьДанныеИзТСД", "Видимость", ЗагружатьДанныеИзТСД);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Префикс + "ВыгрузитьДанныеВТСД",  "Видимость", ВыгружатьДанныеВТСД);
		КонецЦикла;
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗагрузитьДанныеИзТСД", "Видимость", ЗагружатьДанныеИзТСД);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыгрузитьДанныеВТСД",  "Видимость", ВыгружатьДанныеВТСД);
	КонецЕсли;
	
	// Использование весов
	ИспользуютсяВесы = Форма.ИспользоватьПодключаемоеОборудование 
		И МенеджерОборудованияБП.ИспользуетсяОборудование("ЭлектронныеВесы");
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПолучитьВес", "Видимость", ИспользуютсяВесы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
