#Область ПрограммныйИнтерфейс

// Возвращает список счетов, по которым требуется инвентаризация.
//
// Параметры:
//    РазделУчета - ПеречислениеСсылка.РазделыУчетаДляИнвентаризации
//                - Неопределено - для получения списка счетов по всем разделам, кроме забалансовых и прочих счетов.
//    Дата - Дата
//    Организация - СправочникСсылка.Организации
//    ИсключитьСчетаСубконтоСклад - Булево
// 
// Возвращаемое значение:
//  Структура:
//    СчетаУчета - Массив
//    ИсключаемыеСчета - Массив
//
Функция СчетаДляИнвентаризации(РазделУчета = Неопределено, Дата, Организация, ИсключитьСчетаСубконтоСклад = Ложь) Экспорт
	
	СчетаДляИнвентаризации = Новый Структура;
	СчетаУчета = Новый Массив;
	ИсключаемыеСчета = Новый Массив;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета КАК СчетУчета
		|ИЗ
		|	РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(&Дата, Организация = &Организация) КАК СчетаБухгалтерскогоУчетаОССрезПоследних";
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Дата", Дата);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СчетаУчета.Добавить(Выборка.СчетУчета);
		КонецЦикла;
		Если Не ЗначениеЗаполнено(СчетаУчета) Тогда
			СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОСвОрганизации);
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ВедетсяУчетГрупповыхОС") Тогда
			СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ГрупповыеОбъектыОС);
		КонецЕсли;
	КонецЕсли;
	Если РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НематериальныеАктивы Тогда
		СчетаУчета = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	КонецЕсли;
	Если РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
		
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловиеОтбора);
		ДополнитьИсключаемыеСчета(ИсключаемыеСчета, СчетаУчетаСубсчета, ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Запасы
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Материалы);                        // 10
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья); // 20.02 
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);                    // 21
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Товары);                           // 41
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);                 // 43
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);                // 45
		
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ); // 41.12
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);                            // 42
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ГТД);                                        // ГТД
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаВЭксплуатации);       // 10.11
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости);              // 45.04
		// Исключим также забалансовые, в помощнике инвентаризации они в отдельном разделе.
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		УсловиеОтбора.Забалансовый = Истина;
		СчетаУчетаЗабалансовые = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(Новый Массив, УсловиеОтбора);
		ДополнитьИсключаемыеСчета(ИсключаемыеСчета, СчетаУчетаЗабалансовые, ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		// Исключить счета с субконто Склад нужно в помощнике при расшифровке суммы по учету по пустому складу.
		Если ИсключитьСчетаСубконтоСклад Тогда
			УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
			УсловиеОтбора.ИспользоватьВПроводках = Истина;
			СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловиеОтбора);
			ДополнитьИсключаемыеСчета(ИсключаемыеСчета, СчетаУчетаСубсчета, ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
		КонецЕсли;
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство
		Или РазделУчета = Неопределено Тогда
		СчетаНЗП = Справочники.НастройкиУчетаЗатрат.КалькуляционныеСчета(Дата, Организация);
		СчетВыполнениеРемонтовОС = ПланыСчетов.Хозрасчетный.ВыполнениеРемонтовОС; // 23.05 инвентаризируем в прочих счетах
		Для Каждого Счет Из СчетаНЗП Цикл
			Если Счет <> СчетВыполнениеРемонтовОС Тогда
				СчетаУчета.Добавить(Счет);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Касса
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Касса);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);    // 51
		ИспользоватьВалютныйУчет = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
		Если ИспользоватьВалютныйУчет Тогда
			СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета); // 52
		КонецЕсли;
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ЦифровойРубль);     // 53
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.СпециальныеСчета);  // 55
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПереводыВПути_);    // 57
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ФинансовыеВложения);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами
		Или РазделУчета = Неопределено Тогда
		Документы.ИнвентаризацияРасчетовСКонтрагентами.СчетаРасчетовСКонтрагентами(СчетаУчета);
		ИсключаемыеСчета = Документы.ИнвентаризацияРасчетовСКонтрагентами.СчетаРасчетовСКонтрагентамиИсключения();
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОтложенныеНалоговыеАктивыОбязательства
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеАктивы);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеОбязательства);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОценочныеОбязательства
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РезервыПредстоящихРасходов);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасходыБудущихПериодов
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);                      // 97
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПлатежиПоДобровольномуСтрахованиюРаботников); // 76.07.2
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПлатежиПоПрочимВидамСтрахования);             // 76.07.9
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ДоходыБудущихПериодов
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ДоходыБудущихПериодов);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ЖивотныеНаВыращиванииИОткорме);    // 11
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ЗаготовлениеИПриобретениеМЦ);      // 15
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОтклонениеВСтоимостиМЦ);           // 16
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВыполнениеРемонтовОС);             // 23.05
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.БракВПроизводстве);                // 28
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства);        // 29
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости);    // 45.04
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВнутрихозяйственныеРасчеты);       // 79
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ЦелевоеФинансирование);            // 86
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей); // 94
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.Забалансовый = Истина;
		СчетаУчетаЗабалансовые = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(Новый Массив, УсловиеОтбора);
		СчетИзносОС = ПланыСчетов.Хозрасчетный.ИзносОсновныхСредств;
		СчетМЦ = ПланыСчетов.Хозрасчетный.МатериальныеЦенностиВЭксплуатации;
		Для Каждого Счет Из СчетаУчетаЗабалансовые Цикл
			КодСчета = Лев(Счет.Код, 3);
			// Исключаем все буквенные счета кроме МЦ
			Если (СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КодСчета) Или Счет = СчетМЦ)
				И Счет <> СчетИзносОС И Счет.Родитель = ПланыСчетов.Хозрасчетный.ПустаяСсылка() Тогда
				СчетаУчета.Добавить(Счет);
			КонецЕсли;
		КонецЦикла;
		// Счета с субконто Номенклатура инвентаризируются в документе ИнвентаризацияТоваровНаСкладе
		ДополнитьИсключаемыеСчета(ИсключаемыеСчета, СчетаУчета, ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		ИсключаемыеСчета.Добавить(СчетМЦ);
	КонецЕсли;
	
	СчетаДляИнвентаризации.Вставить("СчетаУчета", СчетаУчета);
	СчетаДляИнвентаризации.Вставить("ИсключаемыеСчета", ИсключаемыеСчета);
	Возврат СчетаДляИнвентаризации;
	
КонецФункции

#Область ИнвентаризационныеКомиссии

// Функция заполняет табличную часть "ИнвентаризационнаяКомиссия" из предыдущего документа с отбором по организации
// Параметры:
//  Объект - ДанныеФормыСтруктура - заполняемого документа. См. также ВидыДокументовИнвентаризационнаяКомиссия
//  Отбор - Структура - содержит реквизиты документа для дополнительного отбора, вид сравнения при отборе всегда равенство
//    * Ключ     - имя реквизита документа для отбора; синоним реквизита должен быть склоняемым
//    * Значение - значение реквизита отбора
//        - Неопределено - дополнительный отбор не применяется (отбор только по организации)
//
// Возвращаемое значение:
//  Строка - содержит информацию о примененном отборе, если по отбору, переданному в параметре данные не найдены.
//
Функция ЗаполнитьИнвентаризационнуюКомиссию(Объект, Отбор = Неопределено) Экспорт
	
	// очищаем вне зависимости от результата, для того, чтобы пользователь увидел реальный результат работы функции
	Объект.ИнвентаризационнаяКомиссия.Очистить();
	
	ДанныеПредыдущегоДокумента = ДанныеИнвентаризационнойКомиссииПредыдущегоДокумента(
		Объект.Ссылка,
		Объект.Дата,
		Объект.Организация,
		Отбор);
	
	Если ДанныеПредыдущегоДокумента.Комиссия.Количество() = 0 Тогда
		Возврат ДанныеПредыдущегоДокумента.ТекстПредупреждения;
	КонецЕсли;
	
	Для Каждого Элемент Из ДанныеПредыдущегоДокумента.Комиссия Цикл
		НоваяСтрокаКомиссии = Объект.ИнвентаризационнаяКомиссия.Добавить();
		НоваяСтрокаКомиссии.ФизЛицо = Элемент;
	КонецЦикла;
	
	Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
	
	Возврат ДанныеПредыдущегоДокумента.ТекстПредупреждения;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьИсключаемыеСчета(ИсключаемыеСчета, СчетаУчета, ВидСубконто)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК Ссылка
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|		ПО Хозрасчетный.Ссылка = ХозрасчетныйВидыСубконто.Ссылка
		|			И (Хозрасчетный.Ссылка В (&СчетаУчета))
		|			И (ХозрасчетныйВидыСубконто.ВидСубконто = &ВидСубконто)";
	
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
	Запрос.УстановитьПараметр("ВидСубконто", ВидСубконто);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ИсключаемыеСчета.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

#Область ИнвентаризационныеКомиссии

Функция ДанныеИнвентаризационнойКомиссииПредыдущегоДокумента(СсылкаНаДокумент, ДатаДокумента, Организация, Отбор)

	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИнвентаризационнаяКомиссия.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПредыдущаяИнвентаризация
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.ИнвентаризационнаяКомиссия КАК ИнвентаризационнаяКомиссия
	|ГДЕ
	|	ИнвентаризационнаяКомиссия.Ссылка.Проведен
	|	И ИнвентаризационнаяКомиссия.Ссылка.Организация = &Организация
	|	И ИнвентаризационнаяКомиссия.Ссылка.Дата <= &Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИнвентаризационнаяКомиссия.Ссылка.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставКомиссии.Ссылка КАК Ссылка,
	|	СоставКомиссии.ФизЛицо КАК ФизЛицо,
	|	СоставКомиссии.Председатель КАК Председатель
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.ИнвентаризационнаяКомиссия КАК СоставКомиссии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущаяИнвентаризация КАК ПредыдущаяИнвентаризация
	|		ПО СоставКомиссии.Ссылка = ПредыдущаяИнвентаризация.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Председатель УБЫВ,
	|	СоставКомиссии.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
		
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ШаблонТекстаЗапроса);
	
	МетаданныеДокумента = СсылкаНаДокумент.Метаданные();
	
	ПсевдонимТаблицыСсылкиДокумента = "ИнвентаризационнаяКомиссия";
	ПсевдонимТаблицыСоставаКомиссии = "СоставКомиссии";
	
	ИмяТаблицыСТабличнойЧастью = ИмяТаблицыИнвентаризационнаяКомиссия(МетаданныеДокумента);
	ТаблицаСсылкиДокумента = СхемыЗапросов.НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСсылкиДокумента);
	ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСсылкиДокумента, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСсылкиДокумента);
	ТаблицаСоставаКомиссии = СхемыЗапросов.НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСоставаКомиссии);
	ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСоставаКомиссии, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСоставаКомиссии);
	
	Запрос = Новый Запрос;
	
	Если Не СсылкаНаДокумент.Пустая() Тогда
		УстановитьОтбор(Запрос,
		ТаблицаСсылкиДокумента,
		ПсевдонимТаблицыСсылкиДокумента,
		"<>",
		"Ссылка",
		СсылкаНаДокумент);
	КонецЕсли;
	
	ТекстЗапросаДоОтбора = СхемаЗапроса.ПолучитьТекстЗапроса(); // на случай, если поиск с отбором не даст результата
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		ПсевдонимТаблицыДляОтбора = СтрШаблон("%1.Ссылка", ПсевдонимТаблицыСсылкиДокумента);
		Для Каждого Элемент Из Отбор Цикл
			УстановитьОтбор(
				Запрос,
				ТаблицаСсылкиДокумента,
				ПсевдонимТаблицыДляОтбора,
				"=",
				Элемент.Ключ,
				Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(ДатаДокумента));
	РезультатЗапроса = Запрос.Выполнить();
	
	// Часто на небольших предприятиях, в простых случаях инвентаризационная комиссия одна.
	// На более сложных может быть несколько комиссий: территориально удаленных, занимающихся разными классами активов.
	//
	// Поэтому, последовательно ищем комиссию от сложных случаев к простым
	// - с заданным отбором в документах того же вида
	// - без отбора в документах того же вида
	// - без отбора в документах других видов
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатЗапроса);
	КонецЕсли;
	
	СхемаЗапроса = СхемыЗапросов.Создать(ТекстЗапросаДоОтбора);
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		
		// поля отбора добавим в результирующий запрос, чтобы была возможность описать результат поиска
		ЗапросРезультата = СхемыЗапросов.НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСоставаКомиссии);
		Для Каждого Элемент Из Отбор Цикл
			Выражение = СтрШаблон("%1.Ссылка.%2", ПсевдонимТаблицыСоставаКомиссии, Элемент.Ключ);
			СхемыЗапросов.УстановитьВыражение(ЗапросРезультата, Элемент.Ключ, Выражение);
		КонецЦикла;
		
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатЗапроса, Отбор);
		КонецЕсли;
		
	КонецЕсли;
	
	ВидыДокументов = ВидыДокументовИнвентаризационнаяКомиссия();
	Для Каждого ВидДокумента Из ВидыДокументов Цикл
		
		Если ВидДокумента = МетаданныеДокумента Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПравоДоступа("Просмотр", ВидДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяТаблицыСТабличнойЧастью = ИмяТаблицыИнвентаризационнаяКомиссия(ВидДокумента);
		ТаблицаСсылкиДокумента = СхемыЗапросов.НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСсылкиДокумента);
		ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСсылкиДокумента, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСсылкиДокумента);
		ТаблицаСоставаКомиссии = СхемыЗапросов.НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСоставаКомиссии);
		ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСоставаКомиссии, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСоставаКомиссии);
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатЗапроса, ВидДокумента);
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация);
	
КонецФункции

Функция ИмяТаблицыИнвентаризационнаяКомиссия(МетаданныеДокумента)
	
	Возврат СтрШаблон("%1.ИнвентаризационнаяКомиссия", МетаданныеДокумента.ПолноеИмя());
	
КонецФункции

Функция РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатПоиска = Неопределено, УсловияНеточногоПоиска = Неопределено)
	
	Данные = Новый Структура;
	Данные.Вставить("Комиссия",            Новый Массив);
	Данные.Вставить("ТекстПредупреждения", "");
	
	СведенияДокумента = Неопределено;
	
	Если РезультатПоиска <> Неопределено Тогда
		ВыборкаДокумент = РезультатПоиска.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаДокумент.Следующий() Тогда
			СведенияДокумента = ВыборкаДокумент;
			ВыборкаКомиссия = ВыборкаДокумент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКомиссия.Следующий() Цикл
				Данные.Комиссия.Добавить(ВыборкаКомиссия.ФизЛицо);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Данные.Комиссия) Тогда
		
		Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
			Данные.ТекстПредупреждения = НСтр("ru = 'Состав комиссии не был заполнен, так как по этой организации нет предыдущих документов инвентаризации'");
		Иначе
			Данные.ТекстПредупреждения = НСтр("ru = 'Состав комиссии не был заполнен, так как нет предыдущих документов инвентаризации'");
		КонецЕсли;
		
		Возврат Данные;
		
	КонецЕсли;
	
	Если УсловияНеточногоПоиска = Неопределено Тогда
		// найдено точно
		Возврат Данные;
	КонецЕсли;
	
	// опишем, в чем именно отступили от заданных условий поиска
	ТипНеточногоПоиска = ТипЗнч(УсловияНеточногоПоиска);
	Если ТипНеточногоПоиска = Тип("Структура") И СведенияДокумента <> Неопределено Тогда
		
		ПоляПоиска = Новый Структура;
		ПоляПоиска.Вставить("Искали", Новый Структура); // все поля отбора
		ПоляПоиска.Искали.Вставить("Заполненные", Новый Массив); // разделяем на заполненные и пустые в связи с принятым в русском языке построением фразы
		ПоляПоиска.Искали.Вставить("Пустые",      Новый Массив);
		ПоляПоиска.Вставить("Нашли", ОбщегоНазначения.СкопироватьРекурсивно(ПоляПоиска.Искали)); // только отличающиеся от тех, по которым искали
		
		Для Каждого ПолеПоиска Из УсловияНеточногоПоиска Цикл
			ДобавитьПредставлениеПоляПоиска(ПоляПоиска.Искали, МетаданныеДокумента, ПолеПоиска.Ключ, ПолеПоиска.Значение);
			НайденноеЗначение = СведенияДокумента[ПолеПоиска.Ключ];
			Если НайденноеЗначение <> ПолеПоиска.Значение Тогда
				ДобавитьПредставлениеПоляПоиска(ПоляПоиска.Нашли, МетаданныеДокумента, ПолеПоиска.Ключ, НайденноеЗначение);
			КонецЕсли;
		КонецЦикла;
		
		Шаблон = НСтр("ru = 'Состав комиссии заполнен на основании данных предыдущего документа %1, так как нет документов с заполненной комиссией %2'");
		Данные.ТекстПредупреждения = СтрШаблон(Шаблон, ПредставлениеПолейПоиска(ПоляПоиска.Нашли), ПредставлениеПолейПоиска(ПоляПоиска.Искали));
		
	ИначеЕсли ТипНеточногоПоиска = Тип("ОбъектМетаданных") Тогда
		
		Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
			Шаблон = НСтр("ru = 'Состав комиссии заполнен на основании данных документа ""%1"", так как по этой организации нет документов ""%2"" с заполненной комиссией'");
		Иначе
			Шаблон = НСтр("ru = 'Состав комиссии заполнен на основании данных документа ""%1"", так как нет документов ""%2"" с заполненной комиссией'");
		КонецЕсли;
		
		Данные.ТекстПредупреждения = СтрШаблон(Шаблон, УсловияНеточногоПоиска, МетаданныеДокумента);
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ВидыДокументовИнвентаризационнаяКомиссия()
	
	ВидыДокументов = Новый Массив;
	ВидыДокументов.Добавить(Метаданные.Документы.ИнвентаризацияТоваровНаСкладе);
	ВидыДокументов.Добавить(Метаданные.Документы.ИнвентаризацияОС);
	ВидыДокументов.Добавить(Метаданные.Документы.ИнвентаризацияКассы);
	ВидыДокументов.Добавить(Метаданные.Документы.ИнвентаризацияРасчетовСКонтрагентами);
	Возврат ВидыДокументов;
	
КонецФункции

Процедура ДобавитьПредставлениеПоляПоиска(НаборыПредставлений, МетаданныеДокумента, ИмяРеквизита, Значение)
	
	СинонимРеквизита = ОбщегоНазначенияБПКлиентСервер.ДекапитализироватьСтроку(МетаданныеДокумента.Реквизиты[ИмяРеквизита].Синоним);
	
	ПредставлениеПоляПоиска = Строка(Значение);
	Если ЗначениеЗаполнено(Значение) Тогда
		
		Набор = НаборыПредставлений.Заполненные;
		
		Падеж = 3; // Дательный, "по чему?"
		ПредставлениеРеквизита = СклонениеПредставленийОбъектов.ПросклонятьПредставление(СинонимРеквизита, Падеж);
		Если Не ПустаяСтрока(ПредставлениеРеквизита) Тогда
			ПредставлениеПоляПоиска = СтрШаблон(НСтр("ru = '%1 ""%2""'"), ПредставлениеРеквизита, Значение);
		КонецЕсли;
		
	Иначе
		
		Набор = НаборыПредставлений.Пустые;
		
		Падеж = 2; // родительный, "без указания чего?"
		ПредставлениеРеквизита = СклонениеПредставленийОбъектов.ПросклонятьПредставление(СинонимРеквизита, Падеж);
		Если ПустаяСтрока(ПредставлениеРеквизита) Тогда
			ПредставлениеПоляПоиска = ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения();
		Иначе
			ПредставлениеПоляПоиска = ПредставлениеРеквизита;
		КонецЕсли;
		
	КонецЕсли;
	
	Набор.Добавить(ПредставлениеПоляПоиска);
	
КонецПроцедуры

Функция ПредставлениеПолейПоиска(НаборыПредставлений)
	
	// Пример: по подразделению "Офис Дмитровское шоссе, 9" и без указания склада
	
	ВсеПоля = ОбщегоНазначения.СкопироватьРекурсивно(НаборыПредставлений.Заполненные);
	Если ЗначениеЗаполнено(ВсеПоля) Тогда
		ВсеПоля[0] = СтрШаблон(НСтр("ru = 'по %1'"), ВсеПоля[0]);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НаборыПредставлений.Пустые) Тогда
		ИндексПервогоПустогоПоля = ВсеПоля.Количество();
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеПоля, НаборыПредставлений.Пустые);
		ВсеПоля[ИндексПервогоПустогоПоля] = СтрШаблон(НСтр("ru = 'без указания %1'"), ВсеПоля[ИндексПервогоПустогоПоля]);
	КонецЕсли;
	
	Возврат ОбщегоНазначенияБПКлиентСервер.ПредставлениеСписка(ВсеПоля);
	
КонецФункции

Функция ЗаменитьТаблицуВСхемеЗапроса(ТочкаЗапроса, ИмяТаблицы, Псевдоним)
	
	Таблица = ТочкаЗапроса.Запрос.ДоступныеТаблицы.Найти(ИмяТаблицы);
	ИсточникиДанных = ТочкаЗапроса.Оператор.Источники;
	ИндексТаблицы = ИсточникиДанных.Индекс(ТочкаЗапроса.Источник);
	ИсточникиДанных.Заменить(ИндексТаблицы, Таблица);
	ТочкаЗапроса.Таблица.Псевдоним = Псевдоним;

	Возврат ТочкаЗапроса;
	
КонецФункции

Процедура УстановитьОтбор(Запрос, ТочкаЗапроса, ТаблицаЗапроса, ВидСравнения, ПараметрОтбора, ЗначениеПараметра);

	ШаблонОтбора = СтрШаблон("%1.%2 %3 &%2", ТаблицаЗапроса, ПараметрОтбора, ВидСравнения);
	ТочкаЗапроса.Оператор.Отбор.Добавить(ШаблонОтбора);
	Запрос.УстановитьПараметр(ПараметрОтбора, ЗначениеПараметра);
	
КонецПроцедуры

#КонецОбласти

#Область ПечатныеФормы

Функция ПечатьИНВ22(МассивОбъектов, ОбъектыПечати) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru = 'Председатель комиссии'"));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru = 'Члены комиссии'"));
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху              = 10;
	ТабличныйДокумент.ПолеСлева               = 20;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 10;
	ТабличныйДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияОС_ИНВ22";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ИНВ22");
	
	// Получаем области макета для вывода в табличный документ
	Шапка   = ОбластьМакета(Макет, "Шапка");
	Подпись = ОбластьМакета(Макет, "Подпись");
	Подвал  = ОбластьМакета(Макет, "Подвал");
	
	ДопустимыеДокументы = Новый Массив;
	ДопустимыеДокументы.Добавить(Метаданные.Документы.ИнвентаризацияОбъектовУчета);
	ДопустимыеДокументы.Добавить(Метаданные.Документы.ИнвентаризацияКассы);
	ДопустимыеДокументы.Добавить(Метаданные.Документы.ИнвентаризацияНЗП);
	ДопустимыеДокументы.Добавить(Метаданные.Документы.ИнвентаризацияНМА);
		
	СхемаЗапроса = Новый СхемаЗапроса;
	
	ПакетЗапроса = СхемаЗапроса.ПакетЗапросов[0];
	ЗапросРеквизиты = СхемыЗапросов.НайтиЗапросРезультата(СхемаЗапроса);
	
	РеквизитыШапки = "Ссылка, Дата, Номер, Организация, ПодразделениеОрганизации, РазделУчета,
		|ДатаНачалаИнвентаризации, ДатаОкончанияИнвентаризации,
		|ДокументОснованиеДата, ДокументОснованиеНомер, ПричинаПроведенияИнвентаризации";
	РеквизитыШапки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(РеквизитыШапки, ",", Истина, Истина);
	
	ИндексИсточникаДляЗамены = 0;
	Для НомерДокумента = 1 По ДопустимыеДокументы.Количество() Цикл
		ИмяТаблицы = ДопустимыеДокументы[НомерДокумента - 1].ПолноеИмя();
		Если НомерДокумента = 1 Тогда
			Оператор = ПакетЗапроса.Операторы[0]; // Оператор с индексом 0 есть всегда
			Источник = Оператор.Источники.Добавить(ИмяТаблицы, "Реквизиты");
			ИндексИсточникаДляЗамены = Оператор.Источники.Индекс(Источник);
			Для Каждого РеквизитШапки Из РеквизитыШапки Цикл
				СхемыЗапросов.УстановитьВыражение(ЗапросРеквизиты, РеквизитШапки, РеквизитШапки);
			КонецЦикла;

			Оператор.Отбор.Добавить("Реквизиты.Ссылка В (&МассивОбъектов)");
		Иначе
			Оператор = ПакетЗапроса.Операторы.Добавить(ПакетЗапроса.Операторы[0]);
			ИмяДобавляемогоДокумента = ПакетЗапроса.ДоступныеТаблицы.Найти(ИмяТаблицы);
			Оператор.Источники.Заменить(ИндексИсточникаДляЗамены, ИмяДобавляемогоДокумента);
		КонецЕсли;

	КонецЦикла;
	
	СхемыЗапросов.ПоместитьРезультаты(СхемаЗапроса, "Реквизиты");
	
	ПакетЗапросаКомиссия = СхемаЗапроса.ПакетЗапросов.Добавить();
	ЗапросКомиссия = СхемыЗапросов.НайтиЗапросРезультата(СхемаЗапроса);
	РеквизитыИнвентаризационнаяКомиссия = "Ссылка, НомерСтроки, Председатель, ФизЛицо";
	РеквизитыИнвентаризационнаяКомиссия =
		СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(РеквизитыИнвентаризационнаяКомиссия, ",", Истина, Истина);
	
	Для НомерДокумента = 1 По ДопустимыеДокументы.Количество() Цикл
		ИмяДокумента = ДопустимыеДокументы[НомерДокумента - 1].ПолноеИмя();
		ИмяТаблицы = СтрШаблон("%1.ИнвентаризационнаяКомиссия", ИмяДокумента);
		
		Если НомерДокумента = 1 Тогда
			Оператор = ПакетЗапросаКомиссия.Операторы[0]; // Оператор с индексом 0 есть всегда
			
			Источник = Оператор.Источники.Добавить(ИмяТаблицы, "ИнвентаризационнаяКомиссия");
			ИндексИсточникаДляЗамены = Оператор.Источники.Индекс(Источник);
			Для Каждого РеквизитИнвентаризационнаяКомиссия Из РеквизитыИнвентаризационнаяКомиссия Цикл
				СхемыЗапросов.УстановитьВыражение(ЗапросКомиссия, РеквизитИнвентаризационнаяКомиссия, РеквизитИнвентаризационнаяКомиссия);
			КонецЦикла;
			Оператор.Отбор.Добавить("ИнвентаризационнаяКомиссия.Ссылка В(&МассивОбъектов)");
		Иначе
			Оператор = ПакетЗапросаКомиссия.Операторы.Добавить(ПакетЗапросаКомиссия.Операторы[0]);
			ИмяДобавляемогоДокумента = ПакетЗапросаКомиссия.ДоступныеТаблицы.Найти(ИмяТаблицы);
			Оператор.Источники.Заменить(ИндексИсточникаДляЗамены, ИмяДобавляемогоДокумента);
		КонецЕсли;

	КонецЦикла;
	
	СхемыЗапросов.ПоместитьРезультаты(СхемаЗапроса, "ИнвентаризационнаяКомиссия");
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	ЕСТЬNULL(Реквизиты.ПодразделениеОрганизации, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеОрганизации,
	|	ЕСТЬNULL(Реквизиты.РазделУчета, ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ПустаяСсылка)) КАК РазделУчета,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснованиеДата = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Реквизиты.Дата
	|		ИНАЧЕ Реквизиты.ДокументОснованиеДата
	|	КОНЕЦ КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Ссылка КАК Ссылка,
	|	ИнвентаризационнаяКомиссия.Председатель КАК Председатель,
	|	ИнвентаризационнаяКомиссия.НомерСтроки КАК НомерСтроки,
	|	ИнвентаризационнаяКомиссия.ФизЛицо КАК ФизЛицо
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИнвентаризационнаяКомиссия КАК ИнвентаризационнаяКомиссия
	|		ПО Реквизиты.Ссылка = ИнвентаризационнаяКомиссия.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	Председатель УБЫВ,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Док = РезультатЗапроса[0].Выбрать();
	ИнвентаризационнаяКомиссия = РезультатЗапроса[1].Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Док.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выведем шапку документа
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
		
		Если Шапка <> Неопределено Тогда
			Шапка.Параметры.Заполнить(Док);
		КонецЕсли;
	
		ПараметрыПечатнойФормы = Новый Структура;
		ПараметрыПечатнойФормы.Вставить("Подразделение",        Док.ПодразделениеОрганизации);
		
		ПараметрыПечатнойФормы.Вставить("Организация",          ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
		ПараметрыПечатнойФормы.Вставить("ОрганизацияКодПоОКПО", СведенияОбОрганизации.КодПоОКПО);
		
		ПараметрыПечатнойФормы.Вставить("НомерДокумента",       Док.ДокументОснованиеНомер);
		ПараметрыПечатнойФормы.Вставить("ДатаДокумента",        Док.ДокументОснованиеДата);
		
		Если ТипЗнч(Док.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияКассы") Тогда
			ПараметрыПечатнойФормы.Вставить("НаименованиеИмуществаОбязательств",
				НСтр("ru = 'денежных средств, разных ценностей и документов, находящихся в кассе'"));
		ИначеЕсли ТипЗнч(Док.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияНМА") Тогда
			ПараметрыПечатнойФормы.Вставить("НаименованиеИмуществаОбязательств",
				НСтр("ru = 'нематериальных активов организации'"));
		ИначеЕсли ТипЗнч(Док.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияНЗП") Тогда
			ПараметрыПечатнойФормы.Вставить("НаименованиеИмуществаОбязательств",
				НСтр("ru = 'незавершенного производства'"));
		Иначе
			ПараметрыПечатнойФормы.Вставить("НаименованиеИмуществаОбязательств",
				НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(Док.РазделУчета, 2)));
		КонецЕсли;
		
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Шапка, ПараметрыПечатнойФормы);
		
		ИнвентаризационнаяКомиссия.СледующийПоЗначениюПоля("Ссылка");
		
		ВыводитьЗаголовок = Истина;
		КоличествоЧленовКомиссии = 0;

		ПараметрыЧленыКомиссии = Новый Структура();

		Пока ИнвентаризационнаяКомиссия.Следующий() Цикл
			
			Если Не ЗначениеЗаполнено(ИнвентаризационнаяКомиссия.НомерСтроки) Тогда
				Прервать;
			КонецЕсли;
			
			Если КоличествоЧленовКомиссии > 0 Тогда
				
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
					
					// Выведем разрыв страницы
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
					
				КонецЕсли;
				
				ПараметрыЧленыКомиссии.Вставить("ЗаголовокРазделаПодписей", ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.ЧленыКомиссии, ""));
				ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
				
			Иначе
				ПараметрыЧленыКомиссии.Вставить("ЗаголовокРазделаПодписей", ЗаголовокРазделаПодписей.ПредседательКомиссии);
			КонецЕсли;
			
			КоличествоЧленовКомиссии = КоличествоЧленовКомиссии + 1;
			
			ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, Док.Дата);
			
			ПараметрыЧленыКомиссии.Вставить("Должность",                ДанныеЧленаКомиссии.Должность);
			ПараметрыЧленыКомиссии.Вставить("НаименованиеЧленаКомиссии", ДанныеЧленаКомиссии.Представление);
			
			ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыЧленыКомиссии);
			
		КонецЦикла;
		
		// Выведем пустые места для подписей (чтобы в итоге получилось не менее 3-х
		// подписей, как в форме, утвержденной Госкомстатом)
		Для Итератор = КоличествоЧленовКомиссии + 1 По 4 Цикл
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			ПараметрыЧленыКомиссии.Вставить("ЗаголовокРазделаПодписей", ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.ЧленыКомиссии, ""));
			ПараметрыЧленыКомиссии.Вставить("Должность", "");
			ПараметрыЧленыКомиссии.Вставить("НаименованиеЧленаКомиссии", "");
			
			ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыЧленыКомиссии);
	
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			
		КонецЦикла;
		
		// Выведем подвал приказа
		
		ПараметрыПодвала = Новый Структура;
		
		Если ТипЗнч(Док.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияКассы") Тогда
			ПараметрыПодвала.Вставить("НаименованиеИмуществаОбязательств",
				НСтр("ru = 'денежные средства, разные ценности и документы, находящиеся в кассе'"));
		ИначеЕсли ТипЗнч(Док.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияНМА") Тогда
			ПараметрыПодвала.Вставить("НаименованиеИмуществаОбязательств", НСтр("ru = 'нематериальные активы'"));
		ИначеЕсли ТипЗнч(Док.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияНЗП") Тогда
			ПараметрыПодвала.Вставить("НаименованиеИмуществаОбязательств", НСтр("ru = 'незавершенное производство'"));
		Иначе
			ПараметрыПодвала.Вставить("НаименованиеИмуществаОбязательств", НРег(Док.РазделУчета));
		КонецЕсли;
		
		ПараметрыПодвала.Вставить("ДатаНачалаИнвентаризации", Формат(Док.ДатаНачалаИнвентаризации, "ДЛФ=DD"));
		ПараметрыПодвала.Вставить("ДатаОкончанияИнвентаризации", Формат(Док.ДатаОкончанияИнвентаризации, "ДЛФ=DD"));
		ПараметрыПодвала.Вставить("ПричинаПроведенияИнвентаризации", Док.ПричинаПроведенияИнвентаризации);
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Док.Организация, Док.Дата, Док.ПодразделениеОрганизации);
		
		ПараметрыПодвала.Вставить("ДолжностьРуководителя", Руководители.РуководительДолжностьПредставление);
		ПараметрыПодвала.Вставить("РасшифровкаПодписиРуководителя", Руководители.РуководительПредставление);
		
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подвал, ПараметрыПодвала, Истина);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

// Проверяет есть ли в макете область с заданным именем и возвращает ее.
//
// Параметры:
//  Макет      - ТабличныйДокумент - Макет, область которого необходимо получить.
//  ИмяОбласти - Строка - Получаемая область.
//
// Возвращаемое значение:
//  ТабличныйДокумент, Неопределено - Область макета, если она существует.
//
Функция ОбластьМакета(Макет, ИмяОбласти) Экспорт

	Если Макет.Области.Найти(ИмяОбласти) = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Макет.ПолучитьОбласть(ИмяОбласти);
	
КонецФункции

Процедура ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Область, СтруктураПараметров = Неопределено, ПроверитьВывод = Ложь) Экспорт

	Если Область = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверитьВывод И Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Область) Тогда
		// Выведем разрыв страницы
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	Если СтруктураПараметров <> Неопределено Тогда
		Область.Параметры.Заполнить(СтруктураПараметров);
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(Область);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
