#Область ПлатежныеРеквизиты

Функция УстановитьНадписьРеквизитыПлательщика(Форма) Экспорт
	
	Объект = Форма.Объект;
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПоказатьКПП   = Форма.АвтоЗначенияРеквизитов.ВсегдаУказыватьКПППлательщика ИЛИ НЕ ПустаяСтрока(Объект.КПППлательщика);
		ШаблонНадписи = НСтр("ru = 'ИНН %1%2%3, %4'");
		
		ИндивидуальныйПредприниматель =
			Форма.ОрганизацияЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
		КППНетребуется = НЕ ПоказатьКПП И ПустаяСтрока(Объект.КПППлательщика);
		ТекстКПП       = ?(ИндивидуальныйПредприниматель, "", НСтр("ru = ', КПП <не требуется>'"));
		ТекстНадписи   = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНадписи,
			?(ПустаяСтрока(Объект.ИННПлательщика), НСтр("ru = '<не указан>'"), СокрЛП(Объект.ИННПлательщика)),
			?(КППНетребуется, "", НСтр("ru = ', КПП '")),
			?(КППНетребуется, ТекстКПП, СокрЛП(Объект.КПППлательщика)),
			СокрЛП(Объект.ТекстПлательщика));
	Иначе
		ТекстНадписи = "";
	КонецЕсли;
	
	Возврат ТекстНадписи;
	
КонецФункции

Функция УстановитьНадписьРеквизитыПолучателя(Форма) Экспорт
	
	Объект = Форма.Объект;
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПоказатьКПП    = Форма.АвтоЗначенияРеквизитов.ВсегдаУказыватьКПППолучателя ИЛИ НЕ ПустаяСтрока(Объект.КПППолучателя);
		ШаблонНадписи  = НСтр("ru = 'ИНН %1%2%3, %4'");
		
		ФизЛицо = Форма.КонтрагентЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
		КППНетребуется = НЕ ПоказатьКПП И ПустаяСтрока(Объект.КПППолучателя);
		ТекстКПП       = ?(ФизЛицо, "", НСтр("ru = ', КПП <не требуется>'"));
		ТекстНадписи   = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНадписи,
			?(ПустаяСтрока(Объект.ИННПолучателя), НСтр("ru = '<не указан>'"), СокрЛП(Объект.ИННПолучателя)),
			?(КППНетребуется, "", НСтр("ru = ', КПП '")),
			?(КППНетребуется, ТекстКПП, СокрЛП(Объект.КПППолучателя)),
			СокрЛП(Объект.ТекстПолучателя));
	ИначеЕсли УчетДенежныхСредствКлиентСервер.СписаниеСредствНаДругойСчет(Объект.ВидОперации) Тогда
		ТекстНадписи = УстановитьНадписьРеквизитыПлательщика(Форма);
	Иначе
		ТекстНадписи = "";
	КонецЕсли;
	
	Возврат ТекстНадписи;
	
КонецФункции

Функция НадписьРеквизитыПлатежейВБюджет(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Если НЕ Объект.ПеречислениеВБюджет Тогда
		Возврат НСтр("ru = '<КБК> и <ОКТМО> не требуются'");
	КонецЕсли;
	
	Возврат ПлатежиВБюджетКлиентСерверПереопределяемый.НадписьРеквизитыПлатежейВБюджет(Объект);
	
КонецФункции

Процедура СброситьПризнакПлатежаВБюджет(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Если НЕ ПустаяСтрока(Объект.СтатусСоставителя) Тогда
		Объект.СтатусСоставителя = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОчередностьПлатежаПо345ФЗДляПеречисленияВБюджет(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	РеквизитыДокумента           = ПлатежиВБюджетКлиентСерверПереопределяемый.РеквизитыДокумента_ПлатежноеПоручение();
	ИмяРеквизитаОснованиеПлатежа = РеквизитыДокумента["ОснованиеПлатежа"];
	КодОснованияПлатежа          = Объект[ИмяРеквизитаОснованиеПлатежа];
	
	Если ПлатежиВБюджетКлиентСервер.ЭтоПлатежПоИсполнительномуЛисту(Объект.СтатусСоставителя, КодОснованияПлатежа) Тогда
		
		Если Объект.ОчередностьПлатежа > УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоИсполнительномуЛисту() Тогда
			Объект.ОчередностьПлатежа = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоИсполнительномуЛисту();
		КонецЕсли;
		
	Иначе // Не по исполнительному листу
		
		Если Объект.ОчередностьПлатежа < УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаНалогиВзносы() Тогда
			Объект.ОчередностьПлатежа = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаНалогиВзносы();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыОбъектаДляФормированияНазначенияПлатежа(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	РеквизитыОбъекта = Новый Структура("КодБК, Налог, ВидНалоговогоОбязательства, Организация, Дата, ПоказательПериода");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, Объект);
	
	РеквизитыОбъекта.Вставить("ШаблонНазначенияПлатежаВБюджет", СокрЛП(Форма.ШаблонНазначенияПлатежаВБюджет));
	
	Возврат РеквизитыОбъекта;
	
КонецФункции

Функция КодВидаДоходаПоУмолчанию(Форма, ИзменениеДаты = Ложь) Экспорт
	
	Объект = Форма.Объект;
	
	КодВидаДохода = "";
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.КодВидаДохода) Тогда
		// Если в новом документе код заполнен, значит перезаполнять его кодом "по умолчанию" не надо,
		// кроме случаев изменения даты.
		Если ИзменениеДаты Тогда
			СписокОпераций = Новый СписокЗначений;
			НастроитьСписокВыбораКодВидаДохода(СписокОпераций, Объект.ВидОперации, Форма.ПеречислениеНаЛичныйСчет, Объект.Дата);
			ПредставлениеКода = ПолучитьПредставлениеПоКоду(СписокОпераций, Объект.КодВидаДохода);
			Если ЗначениеЗаполнено(ПредставлениеКода) Тогда
				// Код дохода в документе содержит допустимое значение для данного периода.
				Возврат Объект.КодВидаДохода;
			КонецЕсли;
		ИначеЕсли Не ВозможноВзысканиеСПеречисленияФизлицу(Объект.Дата, Объект.ВидОперации, Форма.КонтрагентЮрФизЛицо) Тогда
			Возврат КодВидаДохода;
		Иначе
			Возврат Объект.КодВидаДохода;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ВозможноВзысканиеСПеречисленияФизлицу(Объект.Дата, Объект.ВидОперации, Форма.КонтрагентЮрФизЛицо) Тогда
		Возврат КодВидаДохода;
	КонецЕсли;
	
	ЭтоПеречислениеПодотчет =
		Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу");
	
	Если УчетДенежныхСредствКлиентСервер.ПеречислениеЗарплаты(Объект.ВидОперации)
		И Форма.ПеречислениеНаЛичныйСчет Тогда
		КодВидаДохода = "1";
	ИначеЕсли ЭтоПеречислениеПодотчет Тогда
		Если УчетДенежныхСредствКлиентСервер.ВступилиВСилуИзменения353ФЗ(Объект.Дата) Тогда
			КодВидаДохода = "4";
		Иначе
			КодВидаДохода = "2";
		КонецЕсли;
	КонецЕсли;
	
	Возврат КодВидаДохода;
	
КонецФункции

Процедура ОбновитьПодсказкуОчередностьПлатежа(Форма, ОчередностьПлатежа) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если ОчередностьПлатежа > 0 Тогда
		Если ОчередностьПлатежа > Элементы.ОчередностьПлатежа.СписокВыбора.Количество() Тогда
			// Если ОчередностьПлатежа = 6 будут выбирать после вступления в действие 345-Ф, не будем у такого значения отображать подсказку
			Форма.РасшифровкаОчередностиПлатежа = "";
		Иначе
			Форма.РасшифровкаОчередностиПлатежа = Сред(Элементы.ОчередностьПлатежа.СписокВыбора[
				ОчередностьПлатежа - 1].Представление, 5);
		КонецЕсли;
	Иначе
		Форма.РасшифровкаОчередностиПлатежа = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура НастроитьСписокВыбораКодВидаДохода(СписокВыбора, ВидОперации, ПеречислениеНаЛичныйСчет, Дата = Неопределено) Экспорт
	
	СписокВыбора.Очистить();
	
	ПеречислениеСотруднику = ЭтоПеречислениеСотруднику(ВидОперации);
	
	ПрочиеВыплатыКонтрагентам = ЭтоПрочиеВыплатыКонтрагентам(ВидОперации);
	
	ПеречислениеПодотчетномуЛицу =
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу");
	
	ВыплатаВБанкПоВедомости =
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда");
	
	ВыплатаСамозанятым = ЭтоВыплатыСамозанятым(ВидОперации);
	
	Если ПрочиеВыплатыКонтрагентам Или ПеречислениеПодотчетномуЛицу Или ВыплатаСамозанятым Тогда
		СписокВыбора.Добавить(НСтр("ru = 'разрешено, без ограничения по сумме'"));
	ИначеЕсли ВыплатаВБанкПоВедомости
		Или ПеречислениеСотруднику И Не ПеречислениеНаЛичныйСчет Тогда
		СписокВыбора.Добавить(НСтр("ru = '(указывается в реестре)'"));
	КонецЕсли;
	
	СписокВыбора.Добавить(НСтр("ru = '1 - разрешено с ограничением по сумме'"));
	Если УчетДенежныхСредствКлиентСервер.ВступилиВСилуИзменения353ФЗ(Дата) Тогда
		СписокВыбора.Добавить(НСтр("ru = '2 - запрещено без оговорок для периодических выплат'"));
		СписокВыбора.Добавить(НСтр("ru = '3 - запрещено кроме алиментов для периодических выплат'"));
		СписокВыбора.Добавить(НСтр("ru = '4 - запрещено без оговорок для единовременных выплат'"));
		СписокВыбора.Добавить(НСтр("ru = '5 - запрещено кроме алиментов для единовременных выплат'"));
	Иначе
		СписокВыбора.Добавить(НСтр("ru = '2 - запрещено без оговорок'"));
		СписокВыбора.Добавить(НСтр("ru = '3 - запрещено кроме алиментов'"));
	КонецЕсли;
	
КонецПроцедуры

Функция ВозможноВзысканиеСПеречисленияФизлицу(Дата, ВидОперации, КонтрагентЮрФизЛицо) Экспорт
	
	Результат = Ложь;
	Если Не УчетДенежныхСредствКлиентСервер.ДействуетУказаниеБанкаРоссии5286У(Дата) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПрочиеВыплатыКонтрагентам = ЭтоПрочиеВыплатыКонтрагентам(ВидОперации);
	
	ПеречислениеСотруднику = ЭтоПеречислениеСотруднику(ВидОперации);
	
	ВыплатаВБанкПоВедомости =
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда");
	
	ПеречислениеПодотчетномуЛицу =
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу");
	
	КонтрагентФизическоеЛицо =
		КонтрагентЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
	ВыплатаСамозанятым = ЭтоВыплатыСамозанятым(ВидОперации);
	
	Результат = ПеречислениеСотруднику
		Или ВыплатаВБанкПоВедомости
		Или ПрочиеВыплатыКонтрагентам И КонтрагентФизическоеЛицо
		Или ПеречислениеПодотчетномуЛицу
		Или ВыплатаСамозанятым;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоПрочиеВыплатыКонтрагентам(ВидОперации) Экспорт
	
	ПрочиеВыплатыКонтрагентам = ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДивидендов")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПрочиеРасчетыСКонтрагентами")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПоИсполнительномуЛистуРаботника")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание");
	
	Возврат ПрочиеВыплатыКонтрагентам;
	
КонецФункции

Функция ЭтоПеречислениеСотруднику(ВидОперации) Экспорт
	
	ПеречислениеСотруднику =
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДепонентов");
	
	Возврат ПеречислениеСотруднику;
	
КонецФункции

Функция ЭтоВыплатыСамозанятым(ВидОперации) Экспорт
	
	ВыплатаСамозанятым =
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым");
	
	Возврат ВыплатаСамозанятым;
	
КонецФункции

Функция ПолучитьПредставлениеПоКоду(Список, Код) Экспорт
	
	Результат = "";
	Для Каждого ЭлементСписка Из Список Цикл
		Если СтрНачинаетсяС(ЭлементСписка.Значение, Код) Тогда
			Результат = ЭлементСписка.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
