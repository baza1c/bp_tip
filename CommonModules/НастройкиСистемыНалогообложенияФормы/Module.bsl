
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Серверные методы формы записи регистра сведений НастройкиСистемыНалогообложения
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийФормы

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Запись = Форма.НастройкиСистемыНалогообложения;
	
	Форма.ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Запись.Организация);
	
	ПодготовитьФормуНаСервере(Форма);
	
КонецПроцедуры

Процедура ПодготовитьФормуНаСервере(Форма) Экспорт
	
	Запись = Форма.НастройкиСистемыНалогообложения;
	
	ЗаполнитьОписаниеСистемНалогообложения(Форма);
	
	Если Не ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи) Тогда
		ИндексТекущейСНО = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
				Запись.СистемаНалогообложения);
		
		СвойстваТекущейСНО = Форма.ОписаниеСистемНалогообложения.НайтиСтроки(Новый Структура("Индекс", ИндексТекущейСНО));
		ДоступныеСНО = Форма.ОписаниеСистемНалогообложения.НайтиСтроки(Новый Структура("ДоступнаПоТарифу", Истина));
		Если СвойстваТекущейСНО.Количество() > 0
			И Не СвойстваТекущейСНО[0].ДоступнаПоТарифу
			И ДоступныеСНО.Количество() > 0 Тогда
			Запись.СистемаНалогообложения =
				НастройкиСистемыНалогообложенияФормыКлиентСервер.СистемаНалогообложенияПоИндексу(ДоступныеСНО[0].Индекс);
		КонецЕсли;
	КонецЕсли;
	
	Форма.СистемаНалогообложенияПредставление = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(Запись.СистемаНалогообложения,
		Запись.ПрименяетсяУСНПатент,
		Запись.ПлательщикЕНВД,
		Запись.ОбъектНалогообложенияУСН);
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.ЗаполнитьСписокВыбораСистемНалогообложения(Форма);
	
	Форма.ПоложенияПереходногоПериодаУСН  = ЗначениеЗаполнено(Запись.ДатаПереходаНаУСН);
	
	Форма.ДатаНачалаПримененияЕНП = НастройкиУчетаВызовСервера.ДатаНачалаПримененияЕНП();
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.УправлениеФормой(Форма);
	
	ТарификацияБП.РазместитьИнформациюОбОграничении(Форма);
	
	Форма.ПрименяетсяТолькоЕНВДПослеОтменыНаМоментОткрытия = НастройкиСистемыНалогообложенияФормыКлиентСервер.ПрименяетсяТолькоЕНВДПослеОтмены(Форма);
	Форма.ПрименяетсяНПДНаМоментОткрытия = Форма.СистемаНалогообложенияПредставление = 0;
	
	ТекстОграниченияТарифа = Новый Массив;
	ТекстОграниченияТарифа.Добавить(НСтр("ru = 'На текущем тарифе есть ограничения.'"));
	ТекстОграниченияТарифа.Добавить(
		НСтр("ru = '<a href = ""%1"">Перейдите на другой тариф</a>, чтобы использовать больше налоговых режимов.'"));
	
	Форма.Элементы.ПодсказкаОграниченияТарифа.Заголовок =
		СтроковыеФункции.ФорматированнаяСтрока(СтрСоединить(ТекстОграниченияТарифа, Символы.ПС),
			ТарификацияБПКлиентСервер.ИмяДействияОплатаСервиса());
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполненияНаСервере(Форма, Отказ, ПроверяемыеРеквизиты, ИмяПроверки) Экспорт
	
	Если ИмяПроверки = "СистемаНалогообложения" Тогда
		
		Запись = Форма.НастройкиСистемыНалогообложения;
		
		Если Форма.ПоложенияПереходногоПериодаУСН И Не ЗначениеЗаполнено(Запись.ДатаПереходаНаУСН) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Дата перехода на УСН'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ДатаПереходаНаУСН", "НастройкиСистемыНалогообложения", Отказ);
		КонецЕсли;
		
		Если Запись.ПрименяетсяОсобыйПорядокНалогообложения
			И Запись.ПлательщикЕНВД
			И Запись.Период >= УчетЕНВДКЛиентСервер.ДатаОтменыЕНВД() Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Основная система налогообложения'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СистемаНалогообложенияПредставление",, Отказ);
		КонецЕсли;
		
	ИначеЕсли ИмяПроверки = "НалогНаПрибыль" Тогда

		Запись = Форма.РеквизитФормыВЗначение("НастройкиУчетаНалогаНаПрибыль");
		ИнвестиционныйНалоговыйВычет.ПроверитьКорректностьПериода(Запись, Форма.ДатаИзменения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИспользуетсяДатаИзменения(Организация, СистемаНалогообложения = Неопределено, Период = Неопределено) Экспорт
	
	Если СистемаНалогообложения = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход Тогда
		Возврат Истина;
	ИначеЕсли СистемаНалогообложения = Перечисления.СистемыНалогообложения.АУСН Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяНалогНаПрофессиональныйДоход");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Период) Тогда
		НачалоПериода = Период;
		КонецПериода = Период;
	Иначе
		ГраницыПериода = УчетнаяПолитика.ГраницыПериодаЗаписей(Организация);
		НачалоПериода = ?(ГраницыПериода.Начало <> Неопределено, ГраницыПериода.Начало, '00010101');
		КонецПериода = ?(ГраницыПериода.Конец <> Неопределено, ГраницыПериода.Конец, '39991231');
	КонецЕсли;
		
	ПрименяетсяНалогНаПрофессиональныйДоход = УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоходЗаПериод(
		Организация, НачалоМесяца(НачалоПериода), КонецМесяца(КонецПериода));
	Возврат ПрименяетсяНалогНаПрофессиональныйДоход;
	
КонецФункции

Процедура ЗаполнитьОписаниеСистемНалогообложения(Форма)
	
	Период = НастройкиСистемыНалогообложенияФормыКлиентСервер.ПолучитьПериодФормы(Форма);

	Описание = Форма.ОписаниеСистемНалогообложения;
	
	Описание.Очистить();
	
	Если Не Форма.ЭтоЮрЛицо Тогда
		
		Если ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда
			НовоеОписание = Описание.Добавить();
			НовоеОписание.Идентификатор = "НалогНаПрофессиональныйДоход";
			НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
				Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход);
			НовоеОписание.Наименование = НСтр("ru = 'Налог на профессиональный доход (""самозанятые"")'");
			НовоеОписание.ДоступнаПоТарифу = ТарификацияБП.РазрешенНалогНаПрофессиональныйДоход();
			
		КонецЕсли;
		
	КонецЕсли;
	
	НовоеОписание = Описание.Добавить();
	НовоеОписание.Идентификатор = "УСНДоходы";
	НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
		Перечисления.СистемыНалогообложения.Упрощенная,
		,
		,
		Перечисления.ОбъектыНалогообложенияПоУСН.Доходы);
	НовоеОписание.Наименование = НСтр("ru = 'УСН (доходы)'");
	НовоеОписание.ДоступнаПоТарифу = ТарификацияБП.РазрешенаУСН();
	
	НовоеОписание = Описание.Добавить();
	НовоеОписание.Идентификатор = "УСНДоходыМинусРасходы";
	НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
		Перечисления.СистемыНалогообложения.Упрощенная,
		,
		,
		Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы);
	НовоеОписание.Наименование = НСтр("ru = 'УСН (доходы минус расходы)'");
	НовоеОписание.ДоступнаПоТарифу = ТарификацияБП.РазрешенаУСН();
	
	Если ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда
		НовоеОписание = Описание.Добавить();
		НовоеОписание.Идентификатор = "АУСНДоходы";
		НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
			Перечисления.СистемыНалогообложения.АУСН, , , Перечисления.ОбъектыНалогообложенияПоУСН.Доходы);
		НовоеОписание.Наименование = НСтр("ru = 'АУСН (доходы)'");
		НовоеОписание.ДоступнаПоТарифу = ТарификацияБП.РазрешенаАУСН();
		
		НовоеОписание = Описание.Добавить();
		НовоеОписание.Идентификатор = "АУСНДоходыМинусРасходы";
		НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
			Перечисления.СистемыНалогообложения.АУСН,
			,
			,
			Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы);
		НовоеОписание.Наименование = НСтр("ru = 'АУСН (доходы минус расходы)'");
		НовоеОписание.ДоступнаПоТарифу = ТарификацияБП.РазрешенаАУСН();
		
		Если Не Форма.ЭтоЮрЛицо Тогда
			НовоеОписание = Описание.Добавить();
			НовоеОписание.Идентификатор = "Патент";
			НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
				Перечисления.СистемыНалогообложения.ОсобыйПорядок, Истина, Ложь);
			НовоеОписание.Наименование = НСтр("ru = 'Только патент'");
			НовоеОписание.ДоступнаПоТарифу = ТарификацияБП.РазрешенаПатентнаяСистемаНалогообложения();
			
			Если Период < УчетЕНВДКлиентСервер.ДатаОтменыЕНВД() Тогда
				НовоеОписание = Описание.Добавить();
				НовоеОписание.Идентификатор = "ЕНВД";
				НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
					Перечисления.СистемыНалогообложения.ОсобыйПорядок, Ложь, Истина);
				НовоеОписание.Наименование = НСтр("ru = 'Только ЕНВД (до конца 2020 года)'");
				НовоеОписание.ДоступнаПоТарифу = ТарификацияБП.РазрешенЕНВД();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	НовоеОписание = Описание.Добавить();
	НовоеОписание.Идентификатор = "Общая";
	НовоеОписание.Индекс = НастройкиСистемыНалогообложенияФормыКлиентСервер.ИндексСистемыНалогообложения(
		Перечисления.СистемыНалогообложения.Общая);
	НовоеОписание.Наименование = НСтр("ru = 'Общая'");
	НовоеОписание.ДоступнаПоТарифу = (Форма.ЭтоЮрЛицо И ТарификацияБП.РазрешенаОбщаяСистемаНалогообложения())
		Или (Не Форма.ЭтоЮрЛицо И ТарификацияБП.РазрешенНДФЛИП());
	
КонецПроцедуры

Функция ИндексСтавкиНДСПоЗначению(СтавкаНалогообложенияПриУСН) Экспорт
	
	Если Не ЗначениеЗаполнено(СтавкаНалогообложенияПриУСН) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.ПониженнаяУСН Тогда
		Возврат 0;
	ИначеЕсли СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.ОбщаяУСН Тогда
		Возврат 1;
	ИначеЕсли СтавкаНалогообложенияПриУСН = Перечисления.ВидыСтавокНДС.Общая Тогда
		Возврат 2;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти