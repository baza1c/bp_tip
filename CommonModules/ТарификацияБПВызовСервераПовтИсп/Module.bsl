#Область ПрограммныйИнтерфейс

// Возвращает Истина, если в режиме сервиса разрешена регулярная деятельность.
//
Функция РазрешенУчетРегулярнойДеятельности() Экспорт
	
	Возврат ТарификацияБП.РазрешенУчетРегулярнойДеятельности();
	
КонецФункции

// Возвращает Истина, если тариф предусматривает работу с 1С-Отчетностью.
//
Функция РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		Возврат ИнтернетПоддержкаПользователей.УслугаПодключена(
			ТарификацияБП.ИдентификаторУслуги1СОтчетность());
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает Истина, если тариф предусматривает работу только в интерфейсе интеграции с банком.
//
Функция РазрешенТолькоИнтерфейсИнтеграцииСБанком() Экспорт
	
	Возврат ТарификацияБП.РазрешенТолькоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

// Возвращает Истина, если тариф предусматривает ведение учета по обособленным подразделениям.
//
Функция РазрешенУчетОбособленныхПодразделений() Экспорт
	
	Если Не ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		
		Возврат ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(
			ТарификацияБП.ИдентификаторУслугиОбособленныеПодразделения());
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает Истина, если тариф предусматривает возможность обратного начисления НДС (начисления НДС покупателем).
//
Функция РазрешеноОбратноеНачислениеНДС() Экспорт
	
	Если Не ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		
		Возврат ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(
			ТарификацияБП.ИдентификаторУслугиОбратноеНачислениеНДС());
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает Истина, если тариф предусматривает возможность использования расширенного функционала.
//
Функция РазрешеноИспользованиеРасширенногоФункционала() Экспорт
	
	Если Не ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		
		Возврат ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(
			ТарификацияБП.ИдентификаторУслугиРасширенныйФункционал());
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает Истина, если тариф предусматривает возможность ведения учета по нескольким организациям
//
Функция РазрешеноВестиУчетПоОрганизациям() Экспорт
	
	Если ВариантыПриложений.ЭтоБазоваяВерсия() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		
		Возврат ТарификацияБП.УслугаФункциональностиСервисаДоступа(
			ТарификацияБП.ИдентификаторУслугиВестиУчетПоОрганизациям());
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает Истина, если тариф предусматривает возможность использования контрактов государственного заказа.
//
Функция РазрешенГособоронзаказ() Экспорт
	
	Если Не ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		
		Возврат ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(
			ТарификацияБП.ИдентификаторУслугиГособоронзаказ());
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает Истина, если тариф предусматривает возможность использования уведомлений о контролируемых сделках.
//
Функция РазрешеноИспользованиеУведомленийОКонтролируемыхСделках() Экспорт
	
	Если Не ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		
		Возврат ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(
			ТарификацияБП.ИдентификаторУслугиУведомленияОКонтролируемыхСделках());
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает признак использования вида организации, отсутствующего в тарифе абонента.
// 
// Возвращаемое значение:
//   Булево.
//
Функция ИспользуетсяНедоступныйВидОрганизации() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользуетсяНедоступныйВидОрганизации =
		Справочники.Организации.ЕстьИспользуемыеВидыОрганизацийБезТарифа();
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИспользуетсяНедоступныйВидОрганизации;
	
КонецФункции

// Проверяет использование систем налогообложения, отсутствующих в тарифе абонента.
// 
// Возвращаемое значение:
//   Булево - Истина, если в информационной базе используются системы налогообложения,
// которые не входят в тариф абонента.
//
Функция ИспользуетсяНедоступнаяСистемаНалогообложения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользуетсяНедоступнаяСНО =
		РегистрыСведений.НастройкиСистемыНалогообложения.ЕстьИспользуемыеСистемыНалогообложенияБезТарифа();
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИспользуетсяНедоступнаяСНО;
	
КонецФункции

// Возвращает Истина, если в информационной базе включена функциональность,
// которая не доступна по тарифу.
Функция ИспользуетсяНедоступнаяФункциональность() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользуетсяНедоступнаяФункциональность =
		Обработки.ФункциональностьПрограммы.ЕстьИспользуемаяФункциональностьБезТарифа();
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИспользуетсяНедоступнаяФункциональность;
	
КонецФункции

// Возвращает Истина, если тариф предусматривает возможность использования внешней программы учета зарплаты и кадров.
//
Функция РазрешеноИспользованиеВнешнейПрограммыУчетаЗарплаты() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		Возврат РазрешенУчетРегулярнойДеятельности()
			И НЕ ОбщегоНазначенияБП.ЭтоВитринаБизнесСтарта();
			
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает признак того, что тариф предусматривает работу со всем функционалом бухгалтерского и налогового учета
//
// Возвращаемое значение:
//   Булево - Истина, если разрешено
//
Функция РазрешенБухгалтерскийУчет() Экспорт
	
	Возврат ТарификацияБП.РазрешенБухгалтерскийУчет();
	
КонецФункции

// Возвращает Истина, если тариф предусматривает работу с кадровыми документами.
//
Функция РазрешенТолькоИнтерфейсЭлектронныхТрудовыхКнижек() Экспорт
	
	Возврат ТарификацияБП.РазрешенТолькоИнтерфейсЭлектронныхТрудовыхКнижек();
	
КонецФункции

// Возвращает Истина, если тариф предусматривает начало работы с программой с подбора режима налогообложения.
//
// Возвращаемое значение:
//   Булево
//
Функция НачинатьРаботуСоСравненияРежимовНалогообложения() Экспорт
	
	Возврат ТарификацияБП.НачинатьРаботуСоСравненияРежимовНалогообложения();
	
КонецФункции

// Возвращает Истина, если тариф предусматривает подготовку документов для регистрации ИП или организации.
//
// Возвращаемое значение:
//   Булево
//
Функция ИспользоватьСервисРегистрации() Экспорт
	
	Возврат ТарификацияБП.ИспользоватьСервисРегистрации();
	
КонецФункции

// Возвращает Истина, если тариф предусматривает оказание услуг БухОбслуживания.
//
// Возвращаемое значение:
//   Булево
//
Функция ОказываютсяКонсультацииБухОбслуживание() Экспорт
	
	Возврат ТарификацияБП.ОказываютсяКонсультацииБухОбслуживание();
	
КонецФункции

// Возвращает Истина, если тариф предусматривает взаимодействие с лидами и покупателями.
//
// Возвращаемое значение:
//   Булево
//
Функция РазрешеноВзаимодействиеСЛидами() Экспорт
	
	Возврат ТарификацияБП.РазрешеноВзаимодействиеСЛидами();
	
КонецФункции

// Возвращает Истина, если тариф не предусматривает взаимодействие с лидами и покупателями,
// но у пользователя включена опция.
//
// Возвращаемое значение:
//   Булево
//
Функция ИспользуетсяНеразрешенноеВзаимодействиеСЛидами() Экспорт
	
	Возврат Не РазрешеноВзаимодействиеСЛидами()
		И ПолучитьФункциональнуюОпцию(ФункциональностьПрограммыВызовСервера.НаименованиеОпцииЛиды());
	
КонецФункции

#КонецОбласти
