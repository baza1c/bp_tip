#Область ПрограммныйИнтерфейс

// Формирует из параметров навигационной ссылки параметры для открытия помощника
// подготовки регламентированного отчета, уплаты налога.
//
// Параметры:
//   ПараметрыНавигационнойСсылки - Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиПодготовкаОтчета
//
// Возвращаемое значение:
//   Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыПомощника
//
Функция ПодготовитьПараметрыПомощника(ПараметрыНавигационнойСсылки) Экспорт
	
	ПараметрыПомощника = КалендарьОтчетностиКлиентСервер.НовыйПараметрыОтчета();
	ПустоеЗначениеПараметра = КалендарьОтчетностиКлиентСервер.ПустоеЗначениеПараметраНавигационнойСсылки();
	
	Для Каждого Параметр Из ПараметрыНавигационнойСсылки Цикл
		ПараметрыНавигационнойСсылки[Параметр.Ключ] =
			РаскодироватьСтроку(Параметр.Значение, СпособКодированияСтроки.КодировкаURL);
	КонецЦикла;
	
	Попытка
		ПараметрыПомощника.Организация = Справочники.Организации.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ПараметрыНавигационнойСсылки.organization));
			
		РегистрацияФНС = ПараметрыНавигационнойСсылки.registration;
		ПараметрРегистрацияЗаполнен = ЗначениеЗаполнено(РегистрацияФНС) И РегистрацияФНС <> ПустоеЗначениеПараметра;
		ПараметрыПомощника.РегистрацияВНалоговомОргане = ?(ПараметрРегистрацияЗаполнен,
			Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(
				ПараметрыПомощника.Организация,
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыПомощника.Организация, "КПП"),
				ПараметрыНавигационнойСсылки.registration),
			Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
				
		ПараметрыПомощника.Срок = Дата(ПараметрыНавигационнойСсылки.limit);
		ПараметрыПомощника.ПериодСобытия = КонецДня(Дата(ПараметрыНавигационнойСсылки.period));
		ПараметрыПомощника.КонецПериода = ПараметрыПомощника.ПериодСобытия;
		ПараметрыПомощника.Наименование = ПараметрыНавигационнойСсылки.description;
		ПараметрыПомощника.ИдентификаторЗадачи = ПараметрыНавигационнойСсылки.task_id;
		
		// Календарь отчетности работает с 2-мя видами "правил":
		// ПравилаПредставленияОтчетовУплатыНалогов и ПравилаФинОтчетности.
		// Из общего числа обрабатываемых правил доля у "ПравилаФинОтчетности" незначительна.
		// В приоритете ищем значение "ПравилаПредставленияОтчетовУплатыНалогов'
		ИдентификаторПравила = ПараметрыНавигационнойСсылки.rule_id;
		ПараметрПравилоЗаполнен = ЗначениеЗаполнено(ИдентификаторПравила) И ИдентификаторПравила <> ПустоеЗначениеПараметра;
		ПараметрыПомощника.Правило = ?(ПараметрПравилоЗаполнен,
			Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору(
				ПараметрыНавигационнойСсылки.task_id,
				ИдентификаторПравила),
			Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
				
		Если Не ЗначениеЗаполнено(ПараметрыПомощника.Правило) И ПараметрПравилоЗаполнен Тогда
			ПараметрыПомощника.Правило = Справочники.ПравилаФинОтчетности.НайтиПоКоду(ИдентификаторПравила);
		КонецЕсли;
		
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'КалендарьОтчетностиНавигационнаяСсылка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, ,
			ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(ПараметрыНавигационнойСсылки),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ПараметрыПомощника;
	
КонецФункции

// Формирует из параметров навигационной ссылки параметры открытия требования ФНС.
//
// Параметры:
//   ПараметрыНавигационнойСсылки - Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиТребованиеФНС
//
// Возвращаемое значение:
//   Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыТребованияФНС
//
Функция ПодготовитьПараметрыТребованияФНС(ПараметрыНавигационнойСсылки) Экспорт
	
	ПараметрыПомощника = КалендарьОтчетностиКлиентСервер.НовыйПараметрыТребованияФНС();
	ПустоеЗначениеПараметра = КалендарьОтчетностиКлиентСервер.ПустоеЗначениеПараметраНавигационнойСсылки();
	
	Для Каждого Параметр Из ПараметрыНавигационнойСсылки Цикл
		ПараметрыНавигационнойСсылки[Параметр.Ключ] =
			РаскодироватьСтроку(Параметр.Значение, СпособКодированияСтроки.КодировкаURL);
	КонецЦикла;
	
	Попытка
		ПараметрыПомощника.Организация = Справочники.Организации.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ПараметрыНавигационнойСсылки.organization));
			
		ЭтоТранспортноеСообщение = ПараметрыНавигационнойСсылки.is_transport_links = "true";
		// Если транспортное сообщение, то ссылка неважна. Произойдет расшифровка всех нерасшифрованных сообщений.
		ПараметрыПомощника.Правило = ?(ЭтоТранспортноеСообщение,
			Документы.ТранспортноеСообщение.ПустаяСсылка(),
			Справочники.ДокументыРеализацииПолномочийНалоговыхОрганов.ПолучитьСсылку(
				Новый УникальныйИдентификатор(ПараметрыНавигационнойСсылки.request)));
		
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'КалендарьОтчетностиНавигационнаяСсылка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, ,
			ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(ПараметрыНавигационнойСсылки),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ПараметрыПомощника;
	
КонецФункции

// Формирует из параметров навигационной ссылки параметры открытия документа СЭДО.
//
// Параметры:
//   ПараметрыНавигационнойСсылки - Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиДокументСЭДО
//
// Возвращаемое значение:
//   Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыДокументСЭДО
//
Функция ПодготовитьПараметрыДокументаСЭДО(ПараметрыНавигационнойСсылки) Экспорт
	
	ПараметрыПомощника = КалендарьОтчетностиКлиентСервер.НовыйПараметрыДокументСЭДО();
	
	Для Каждого Параметр Из ПараметрыНавигационнойСсылки Цикл
		ПараметрыНавигационнойСсылки[Параметр.Ключ] =
			РаскодироватьСтроку(Параметр.Значение, СпособКодированияСтроки.КодировкаURL);
	КонецЦикла;
	
	Попытка
		ПараметрыПомощника.Организация = Справочники.Организации.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ПараметрыНавигационнойСсылки.organization));
			
		ПараметрыПомощника.Правило = Документы[ПараметрыНавигационнойСсылки.name].ПолучитьСсылку(
			Новый УникальныйИдентификатор(ПараметрыНавигационнойСсылки.ref));
			
		Если ПараметрыНавигационнойСсылки.quantity <> "1" Тогда
			ПараметрыПомощника.ФормаСписка = СтрШаблон("Документ.%1.ФормаСписка", ПараметрыНавигационнойСсылки.name);
		КонецЕсли;
		
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'КалендарьОтчетностиНавигационнаяСсылка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, ,
			ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(ПараметрыНавигационнойСсылки),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ПараметрыПомощника;
	
КонецФункции

// Возвращает идентификатор этого облачного приложения в механизме обмена сообщениями
//
// Возвращаемое значение:
//   Строка - идентификатор приложения
//
Функция ИдентификаторЭтогоПриложения() Экспорт
	
	Возврат СообщенияОблачныхПриложений.ИдентификаторЭтогоПриложения();
	
КонецФункции

#КонецОбласти
