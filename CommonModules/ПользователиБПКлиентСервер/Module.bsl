#Область ПрограммныйИнтерфейс

// Обновляет подсказку элемента Ответственный документа продаж
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, на которой размещен элемент
//
Процедура УстановитьПодсказкуОтветственного(Форма) Экспорт
	
	Если Форма.Элементы.Найти("Ответственный") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ФункциональностьПрограммыВызовСервера.РазделЛидыДоступен() Тогда
		Форма.Элементы.Ответственный.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.Ответственный.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
	Подстроки = Новый Массив;
	
	Если Форма.ЗаполнятьОтветственногоПоПродажамИзКонтрагента Тогда
		Подстроки.Добавить(НСтр("ru='Заполняется автоматически из контрагента.'"));
	Иначе
		Подстроки.Добавить(НСтр("ru='Заполняется текущим пользователем.'"));
	КонецЕсли;
	
	Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
		ИмяРаздела = НСтр("ru = 'Настройки – Еще – Другие настройки – Настройки пользователей и прав'");
	Иначе
		ИмяРаздела = НСтр("ru = 'Администрирование – Настройки пользователей и прав'");
	КонецЕсли;
	
	Подстроки.Добавить(Символы.ПС + НСтр("ru = 'Изменить заполнение ответственного можно в меню'"));
	
	ЕстьПравоИзмененияНастроекПользователей = ПользователиБПВызовСервера.ПравоИзмененияНастройкиПользователейИПрав();
	Если ЕстьПравоИзмененияНастроекПользователей Тогда
		Подстроки.Добавить(Новый ФорматированнаяСтрока(Символы.ПС + ИмяРаздела,,,,"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.НастройкиПользователейИПрав"));
	Иначе
		Подстроки.Добавить(Символы.ПС + ИмяРаздела);
	КонецЕсли;
	
	Форма.Элементы.ОтветственныйРасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(Подстроки);
	
КонецПроцедуры

#КонецОбласти