////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции для работы с сервисом 1С:Финуслуги:
// - загрузка данных из сервиса.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбращениеКСервису

// Возвращает объект XDTO из корневого элемента XML по схеме пакетаXDTO ФинУслуги.
//
// Параметры:
//	СервисОбменаСБанками - ПеречислениеСсылка.СервисыОбменаСБанками - Сервис.
//	ДанныеXML     - Строка, ХранилищеЗначения - Данные XML из характеристик сервиса заявок на открытие счета.
//
// Возвращаемое значение:
//	ОбъектXDTO, Неопределено - Объект XDTO, прочитанный из XML, или Неопределено в случае ошибок или отсутствия данных.
//
Функция ОбъектXDTOХарактеристики(СервисОбменаСБанками, ДанныеXML) Экспорт
	
	ПредложенияУслуг = ФабрикаXDTO.Тип("http://www.v8.1c.ru/banks/FinancialServices", "ПредложенияУслуг");
	
	Возврат УниверсальныйОбменСБанкамиОбщегоНазначения.ОбъектXDTOХарактеристики(СервисОбменаСБанками, ДанныеXML, ПредложенияУслуг);

КонецФункции

#КонецОбласти

#Область ОбновлениеДанныхСервиса

// Процедура - обработчик начала получения данных о сервисе ФинУслуги.
// Предназначена для вызова из модуля форм, в которых требуются актуальные данные о сервисе.
//
// Параметры:
//  СервисОбменаСБанками - Перечисление.СервисыОбменаСБанками - Сервис обмена.
//  Форма - ФормаКлиентскогоПриложения - должна содержать реквизиты СведенияОСервисе И СведенияОДлительнойОперации
//
Процедура НачатьОбновлениеДанныхСервиса(СервисОбменаСБанками, Форма) Экспорт 

	СведенияОСервисе = Форма.СведенияОСервисе;
	СведенияОДлительнойОперации = Форма.СведенияОДлительнойОперации;
	
	Если Форма.ТолькоПросмотр 
		ИЛИ СведенияОСервисе.ДанныеАктуальны
		ИЛИ СведенияОСервисе.ТребуетсяПодключениеИнтернетПоддержки Тогда
		// Обновление не требуется или невозможно.
		Возврат;
	КонецЕсли;
	
	Если СведенияОДлительнойОперации.ДлительнаяОперация <> Неопределено Тогда
		// Не завершено предыдущее фоновое задание, новое не запускаем.
		Возврат;
	КонецЕсли;
		
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Форма.УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение           = 0; // Не ждем завершения.
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(НСтр("ru = 'Обновление данных сервиса %1'"), СервисОбменаСБанками);
	ПараметрыВыполнения.КлючФоновогоЗадания         = "ОбновитьДанныеСервиса";
	
	СведенияОДлительнойОперации.Имя = ПараметрыВыполнения.КлючФоновогоЗадания;
	СведенияОДлительнойОперации.ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "Обработки.ФинансыОрганизации.ОбновитьДанныеСервиса", СервисОбменаСБанками);

КонецПроцедуры

// Извлекает из архива с файлами файлы креативов по указанным банкам.
//
// Параметры:
//  Банки   - Массив - Массив элементов вида СправочникСсылка.БанкиУниверсальногоОбмена. 
//  ИдентификаторФормы   - Уникальный идентификатор.
//
Функция ДанныеКреативов(Банки, ИдентификаторФормы) Экспорт
	
	КреативыБанков = НовыйКреативыБанков();

	Сервис = Перечисления.СервисыОбменаСБанками.ФинУслуги;
	Результат = Новый Соответствие;
	
	ТаблицаХарактеристик = УниверсальныйОбменСБанками.ХарактеристикиУслугБанков(Сервис, 
		Перечисления.ХарактеристикиСервисаФинУслуги.ФайлыКреативов, Банки);

	Для Каждого Банк Из Банки Цикл
		
		Характеристики = ТаблицаХарактеристик.Найти(Банк, "Банк");
		Если Характеристики = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстBase64 = Характеристики.Значение.Получить();
		Если Не ЗначениеЗаполнено(ТекстBase64) Тогда
			Продолжить;
		КонецЕсли;
	
		ДанныеАрхива = Base64Значение(ТекстBase64);
		Архив = Новый ЧтениеZipФайла(ДанныеАрхива.ОткрытьПотокДляЧтения());
			
		ПапкаШаблона = ФайловаяСистема.СоздатьВременныйКаталог();
		Архив.ИзвлечьВсе(ПапкаШаблона);
		Архив.Закрыть();
		ДанныеАрхива = Неопределено;
		НайденныеФайлы = НайтиФайлы(ПапкаШаблона, ПолучитьМаскуВсеФайлы(), Истина);
		
		Для Каждого ДанныеКреатива Из НайденныеФайлы Цикл
			
			СтрокаТаблицы = КреативыБанков.Добавить();
			СтрокаТаблицы.Банк = Банк;
			СтрокаТаблицы.ИмяКреатива = ДанныеКреатива.Имя;  
			ДвоичныеДанныеКреатива = Новый ДвоичныеДанные(ДанныеКреатива.ПолноеИмя);
			СтрокаТаблицы.АдресКреатива = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКреатива, ИдентификаторФормы);
			
		КонецЦикла;

		ФайловаяСистема.УдалитьВременныйКаталог(ПапкаШаблона);
		
	КонецЦикла;
	
	Возврат КреативыБанков;
	
КонецФункции

Процедура СкрытьБаннерПоИдентификатору(Идентификатор, ТекущаяПозицияБаннеры) Экспорт
	
	ЗакрытыеПользователемБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		Обработки.ФинансыОрганизации.ИмяНастройкиСервиса(),
		Обработки.ФинансыОрганизации.ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		Новый Соответствие);
		
	ЗакрытыеПользователемБаннеры.Вставить(Идентификатор, ТекущаяДатаСеанса());
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		Обработки.ФинансыОрганизации.ИмяНастройкиСервиса(),
		Обработки.ФинансыОрганизации.ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		ЗакрытыеПользователемБаннеры);
		
	ТекущаяПозицияБаннеры = Макс (1, ТекущаяПозицияБаннеры -1);
		
КонецПроцедуры

#КонецОбласти

#Область ОтправкаСтатистикиВСервис

// Процедура - обработчик начала получения данных о сервисе ФинУслуги.
// Предназначена для вызова из модуля форм, в которых требуются актуальные данные о сервисе.
//
// Параметры:
//  СервисОбменаСБанками - Перечисление.СервисыОбменаСБанками - Сервис обмена.
//  Форма - ФормаКлиентскогоПриложения - должна содержать реквизиты СведенияОСервисе И СведенияОДлительнойОперации
//
Процедура НачатьОтправкуСтатистикиВСервис(СервисОбменаСБанками, Форма) Экспорт 

	СодержимоеСтатистики = Форма.СодержимоеСтатистики;
	СведенияОДлительнойОперации = Форма.СведенияОДлительнойОперации;
	ТокенАвторизации = Форма.ТокенАвторизации;
	АдресХранилища = Форма.АдресХранилищаОтправкаСтатистики;

	Если СведенияОДлительнойОперации.ДлительнаяОперация <> Неопределено Тогда
		// Не завершено предыдущее фоновое задание, новое не запускаем.
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.ОжидатьЗавершение           = 0; // Не ждем завершения.
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(НСтр("ru = 'Отправка статистики в сервис %1'"), СервисОбменаСБанками);
	ПараметрыВыполнения.КлючФоновогоЗадания = ПараметрыВыполнения.КлючФоновогоЗадания;
	
	ПараметрыПроцедуры = Новый Структура();
	ПараметрыПроцедуры.Вставить("СервисОбменаСБанками", СервисОбменаСБанками);
	ПараметрыПроцедуры.Вставить("СодержимоеСтатистики", СодержимоеСтатистики);
	ПараметрыПроцедуры.Вставить("ТокенАвторизации", ТокенАвторизации);
	
	СведенияОДлительнойОперации.Имя = "ОтправитьСтатистикуВСервис";
	СведенияОДлительнойОперации.ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
		"ФинУслуги.ОтправитьСтатистикуВСервис", ПараметрыПроцедуры, АдресХранилища);
		
КонецПроцедуры

Процедура ОтправитьСтатистикуВСервис(Параметры, АдресХранилища) Экспорт
	
	СтрокаСтатистики = УниверсальныйОбменСБанками.СтруктуруВСтрокуJSON(Параметры.СодержимоеСтатистики);
	СтрокаСтатистики = СтрЗаменить(СтрокаСтатистики, Символы.ВК + Символы.ПС, ""); 
	СтрокаСтатистики = СтрЗаменить(СтрокаСтатистики, Символы.ПС, "");  
	СтрокаСтатистики = СтрЗаменить(СтрокаСтатистики, ",", "|");
	Результат = УниверсальныйОбменСБанками.СохранитьЗаписьАктивностиСервиса(Параметры.СервисОбменаСБанками,
		Параметры.ТокенАвторизации, "stat_finserv", СтрокаСтатистики);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Вызывается после загрузки данных услуги банка, полученных с сервера.
// Параметры:
//		УслугаБанка - СправочникСсылка.УслугиБанков - ссылка на элемент справочника, данные которого были обновлены.
Процедура ПослеЗагрузкиДанныхУслугиБанка(УслугаБанка) Экспорт
	
	Если Не ЗначениеЗаполнено(УслугаБанка) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Характеристика Из УслугаБанка.Характеристики Цикл
		ОбработатьХарактеристику(Характеристика, УслугаБанка);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает XDTO объект из пакета ФинУслуги с информацией о финансовом предложении банка.
//
// Параметры:
//	СервисОбменаСБанками - ПеречислениеСсылка.СервисыОбменаСБанками - Сервис.
//	УсловияОбслуживанияXDTO - ОбъектXDTO - Корневой объект из пакета XDTO ФинУслуги, в котором производится поиск.
//	ИдентфикаторПродукта - Строка - Идентификатор искомого финансового продукта.
//	Дата - Дата - Дата, по состоянию на которую возвращает информация.
//
// Возвращаемое значение:
//	ОбъектXDTO или Неопределено
//
Функция НайтиФинансовыйПродукт(СервисОбменаСБанками, УсловияОбслуживанияXDTO, Знач ИдентфикаторПродукта, Знач Дата) Экспорт

	Если УсловияОбслуживанияXDTO = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	ТекущаяВерсияСервиса = УниверсальныйОбменСБанками.ВерсияСервиса(СервисОбменаСБанками);
	Дата                 = НачалоДня(Дата);
	ИдентфикаторПродукта = ВРег(ИдентфикаторПродукта);

	ФинансовыйПродуктПоУмолчанию = Неопределено;
	
	Для каждого ФинансовыйПродукт Из УсловияОбслуживанияXDTO.ФинансовыйПродукт Цикл

		Если ФинансовыйПродукт.ДатаНачала > Дата
			ИЛИ ФинансовыйПродукт.ДатаОкончания < Дата Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверим версии сервиса, для которых применяются условия.
		Если ЗначениеЗаполнено(ФинансовыйПродукт.МинВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, ФинансовыйПродукт.МинВерсияСервиса) < 0 Тогда
			// Продукт может быть корректно отображен только в следующих версиях программы, 
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФинансовыйПродукт.МаксВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, ФинансовыйПродукт.МаксВерсияСервиса) > 0 Тогда
			// Продукт предназначен для отображения на предыдущих версиях программы,
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;
	
		Если ФинансовыйПродукт.Свойства().Получить("Идентификатор") = Неопределено
			ИЛИ НЕ ЗначениеЗаполнено(ФинансовыйПродукт.Идентификатор) Тогда
			// В старых настройках могут быть продукты, у которых еще не было идентификатора.
			// Если не найдем никакого продукта с искомым идентификатором, то вернем без идентификатора.
			ФинансовыйПродуктПоУмолчанию = ФинансовыйПродукт;
			Продолжить;
		КонецЕсли;
		
		Если ВРег(ФинансовыйПродукт.Идентификатор) = ИдентфикаторПродукта Тогда
			Возврат ФинансовыйПродукт;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ФинансовыйПродуктПоУмолчанию;

КонецФункции

#Область ОбработчикиОбновления

// Процедура устанавливает значение реквизита "Версия" справочника "Услуги банков" равным 0.
// Может быть использована для принудительного обновления данных в этом справочнике при
// следующей синхронизации с сервером обмена.
Процедура СброситьВерсиюУслугБанкаФинУслуги() Экспорт
	
	УниверсальныйОбменСБанками.СброситьВерсиюУслугБанка(Перечисления.СервисыОбменаСБанками.ФинУслуги);
	
КонецПроцедуры

#КонецОбласти

#Область СведенияДляПредложенийБанков

Функция СведенияОбОрганизациях(ДатаАктуальности, Организация) Экспорт
	
	ТаблицаПоступленияНаСчет = УниверсальныйОбменСБанкамиФормы.ПоступлениеНаСчет(ДатаАктуальности, , Организация);
	ВыручкаОрганизации = УниверсальныйОбменСБанкамиФормы.ВыручкаОрганизации(ДатаАктуальности, Организация, 12);
	ДатаФактическогоНачала = УниверсальныйОбменСБанкамиФормы.ДатаФактическогоНачалаВеденияБизнеса(Организация, ДатаАктуальности);

	Результат = Новый Массив;
	
	МассивОрганизаций = Обработки.ФинансыОрганизации.МассивОрганизаций(Организация);
	РеквизитыОрганизаций = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОрганизаций, 
			"ЮридическоеФизическоеЛицо, ИндивидуальныйПредприниматель, ИностраннаяОрганизация, ДатаРегистрации, КодОКВЭД2, КодОКОПФ, ИНН");
	
	КонтактнаяИнформацияОбъектов = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияОбъектовНаДату(
			МассивОрганизаций, Неопределено,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации),
			ДатаАктуальности);

	Для Каждого ЭлементМассива Из МассивОрганизаций Цикл
		
		РеквизитыОрганизации = РеквизитыОрганизаций.Получить(ЭлементМассива);
		Если РеквизитыОрганизации = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		// Заполним сведения о клиенте для подбора банков по текущим условиям.
		Сведения = Обработки.ФинансыОрганизации.НовыеСведенияОбОрганизации();
		Сведения.Организация               = ЭлементМассива;    
		Сведения.ИНН               = РеквизитыОрганизации.ИНН;
		Сведения.ЮридическоеФизическоеЛицо = РеквизитыОрганизации.ЮридическоеФизическоеЛицо;

		Если РеквизитыОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			ФизическоеЛицо = РеквизитыОрганизации.ИндивидуальныйПредприниматель;
			Гражданство = ГражданствоФизическогоЛица(ФизическоеЛицо);
			Сведения.Нерезидент = Гражданство <> Справочники.СтраныМира.Россия;
			Сведения.ДатаРождения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "ДатаРождения");
			Сведения.ВозрастИП = ОбщегоНазначенияБПКлиентСервер.КоличествоЦелыхЛет(Сведения.ДатаРождения, ДатаАктуальности);
		Иначе
			Сведения.Нерезидент  = РеквизитыОрганизации.ИностраннаяОрганизация;
		КонецЕсли;

		Сведения.ДатаРегистрации   = РеквизитыОрганизации.ДатаРегистрации;
		Сведения.СрокОтДатыРегистрации = ОбщегоНазначенияБПКлиентСервер.КоличествоЦелыхМесяцев(
			Сведения.ДатаРегистрации, ДатаАктуальности);
		Сведения.КодОКВЭД2         = СокрЛП(РеквизитыОрганизации.КодОКВЭД2);
		Сведения.КодОКОПФ          = СокрЛП(РеквизитыОрганизации.КодОКОПФ);
		
		КонтактнаяИнформацияОбъекта = КонтактнаяИнформацияОбъектов.Найти(ЭлементМассива, "Объект");
		Если ЗначениеЗаполнено(КонтактнаяИнформацияОбъекта) Тогда
			Сведения.ЮрАдресОрганизации = КонтактнаяИнформацияОбъекта.Значение;
		КонецЕсли;
		
		СтрокаТаблицы = ТаблицаПоступленияНаСчет.Найти(ЭлементМассива);
		Если СтрокаТаблицы <> Неопределено Тогда
			Сведения.СреднемесячноеПоступлениеНаСчет         = СтрокаТаблицы.Среднее;
			Сведения.МинимальноеЕжемесячноеПоступлениеНаСчет = СтрокаТаблицы.Минимум; 
		КонецЕсли;
		
		СтрокаТаблицы = ВыручкаОрганизации.Найти(ЭлементМассива);
		Если СтрокаТаблицы <> Неопределено Тогда
			Сведения.СуммаВыручки = СтрокаТаблицы.Выручка;
		КонецЕсли;
		
		Сведения.ДатаФактическогоНачалаВеденияБизнеса = ДатаФактическогоНачала;
		Сведения.СрокФактическогоВеденияБизнеса = 
			ОбщегоНазначенияБПКлиентСервер.КоличествоЦелыхМесяцев(ДатаФактическогоНачала, ДатаАктуальности);

		КодРегиона = "";
		Если ЗначениеЗаполнено(Сведения.ЮрАдресОрганизации) Тогда
			СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(Сведения.ЮрАдресОрганизации);
			КодРегиона = СведенияОбАдресе.КодРегиона;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(КодРегиона) Тогда
			// Если из адреса определить код региона не получилось, то ищем через регистрацию в налоговом органе.
			ОсновнаяРегистрацияВНалоговомОргане = Справочники.Организации.РегистрацияВНалоговомОрганеНаДату(
				ЭлементМассива, ДатаАктуальности);
			Если ЗначениеЗаполнено(ОсновнаяРегистрацияВНалоговомОргане) Тогда
				РеквизитыРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОсновнаяРегистрацияВНалоговомОргане, "КодРегиона");
				Если ЗначениеЗаполнено(РеквизитыРегистрации.КодРегиона) Тогда
					// В настройках сервиса код региона задан в виде строки с лидирующем нулем.
					КодРегиона = Формат(РеквизитыРегистрации.КодРегиона, "ЧЦ=2;ЧВН=");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Сведения.КодРегиона = СокрЛП(КодРегиона);
	
		Результат.Добавить(Сведения);
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Процедура ОтправитьСтатистику(Форма, УникальныйИдентификатор) Экспорт
	
	МассивСтатистики = МассивСтатистики(Форма.ТаблицаСтатистики);
	Если МассивСтатистики.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СтруктураДанныхСтатистики = Новый Структура;
	СтруктураДанныхСтатистики.Вставить("ПоказыИПереходы", МассивСтатистики);
	Форма.СодержимоеСтатистики = СтруктураДанныхСтатистики;

	СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ФинУслуги; 
	// В хранилище храним результат отправки статистики. 
	// Если результат отправки неуспешный, статистика накапливается,
	// в противном случае - очищается
	Форма.АдресХранилищаОтправкаСтатистики = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);

	НачатьОтправкуСтатистикиВСервис(СервисОбменаСБанками, Форма);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МассивСтатистики(ТаблицаСтатистики)
	
	Статистика = ТаблицаСтатистики.Выгрузить();
	Если ТаблицаСтатистики.Итог("Показы") = 0 Тогда
		Статистика.Свернуть("Банк, ИДУслуги, ИДРекламы, ИмяФормы", "Переходы");
	ИначеЕсли ТаблицаСтатистики.Итог("Переходы") = 0 Тогда
		Статистика.Свернуть("Банк, ИДУслуги, ИДРекламы, ИмяФормы", "Показы"); 
	Иначе
		Статистика.Свернуть("Банк, ИДУслуги, ИДРекламы, ИмяФормы", "Показы, Переходы"); 
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Статистика);
	
КонецФункции

Процедура ОбработатьХарактеристику(Характеристика, УслугаБанка)
	
	Если Характеристика.Характеристика <> Перечисления.ХарактеристикиСервисаФинУслуги.ГруппыКонтрагентов
		И Характеристика.Характеристика <> Перечисления.ХарактеристикиСервисаФинУслуги.ГруппыКомпанийБанков Тогда
		Возврат;
	КонецЕсли;
				
	ЗначениеХарактеристики = Характеристика.Значение.Получить();
	Если Не ЗначениеЗаполнено(ЗначениеХарактеристики) Тогда
		Возврат;
	КонецЕсли;
	
	СервисХарактеристики = Перечисления.СервисыОбменаСБанками.ФинУслуги;
	
	ОбъектXDTOХарактеристики = ОбъектXDTOХарактеристики(СервисХарактеристики, ЗначениеХарактеристики);
	
	Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УслугаБанка, "Банк");
	
	Если Характеристика.Характеристика = Перечисления.ХарактеристикиСервисаФинУслуги.ГруппыКонтрагентов Тогда
		РегистрыСведений.ГруппыКонтрагентовЗаявкиВБанки.ЗаписатьГруппыКонтрагентов(СервисХарактеристики, 
			ОбъектXDTOХарактеристики, Банк); 
	Иначе
		РегистрыСведений.ГруппыКомпанийБанков.ЗаписатьГруппыКомпанийБанков(СервисХарактеристики, 
			ОбъектXDTOХарактеристики, Банк);
	КонецЕсли;
	
КонецПроцедуры

Функция ГражданствоФизическогоЛица(ФизическоеЛицо) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ГражданствоФизическихЛицСрезПоследних.Страна КАК Страна
		|ИЗ
		|	РегистрСведений.ГражданствоФизическихЛиц.СрезПоследних(, ФизическоеЛицо = &ФизическоеЛицо) КАК ГражданствоФизическихЛицСрезПоследних";
	
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Страна;
	КонецЕсли;
	
	Возврат Справочники.СтраныМира.ПустаяСсылка();
	
КонецФункции

Функция НовыйКреативыБанков()
	
	ТаблицаКреативов = Новый ТаблицаЗначений;
	ТаблицаКреативов.Колонки.Добавить("Банк", Новый ОписаниеТипов("СправочникСсылка.БанкиУниверсальногоОбмена"));
	ТаблицаКреативов.Колонки.Добавить("ИмяКреатива", ОбщегоНазначения.ОписаниеТипаСтрока(0)); 
	ТаблицаКреативов.Колонки.Добавить("АдресКреатива", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	Возврат ТаблицаКреативов;
	
КонецФункции

#КонецОбласти

#КонецОбласти