////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиДепозитыКлиентСервер: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс 

// Возвращает минимальную версию формата обмена с банками, поддерживающую работу с депозитами.
// 
// Возвращаемое значение:
//  Строка - Минимальная версия формата обмена с банками 1С:ДиректБанк
//
Функция МинимальнаяВерсияФорматаОбменСБанкамиДепозиты() Экспорт
	Возврат "2.3.4";
КонецФункции

// Возвращает имя команды депозитного калькулятора.
//
// Параметры:
//  КлючКоманды - Строка - Ключа команды. Возможные ключи:
//   - "ПоказатьРасчетДепозитов" - Ключ команды отображения панели депозитного калькулятора;
//   - "ПовторитьЗапросПредложений" - Ключ команды повторного запроса предложений банков.
//   - "ОтправитьЗаявку" - Ключ команды открытия помощника создания заявки на депозит.
//
// Возвращаемое значение:
//  Строка - Имя команды.
//
Функция ИмяКоманды(КлючКоманды) Экспорт
	
	Результат = "";
	
	Если КлючКоманды = "ПоказатьРасчетДепозитов" Тогда
		Результат = "ПоказатьРасчетДепозитов";
	ИначеЕсли КлючКоманды = "ПовторитьЗапросПредложений" Тогда
		Результат = "ПовторитьЗапросПредложений";
	ИначеЕсли КлючКоманды = "ОтправитьЗаявку" Тогда
		Результат = "ОтправитьЗаявку";
	КонецЕсли;
	
	Если Не ПустаяСтрока(Результат) Тогда
		Результат = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДобавитьПрефиксОбменСБанкамиДепозиты(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеСуммы(Сумма) Экспорт
	
	Если Сумма <= 0 Тогда
		Возврат Строка(Сумма);
	КонецЕсли;
	
	СуммаТысяча = 1000;
	СуммаМиллион = 1000000;
	СуммаМиллиард = 1000000000;
	Разрядность = 3;
	
	Если Сумма >= СуммаМиллиард Тогда
		ПредставлениеСуммы = Окр(Сумма / СуммаМиллиард, Разрядность);
		Возврат СтрШаблон(НСтр("ru = '%1 млрд'"), ПредставлениеСуммы);
	ИначеЕсли Сумма >= СуммаМиллион Тогда
		ПредставлениеСуммы = Окр(Сумма / СуммаМиллион, Разрядность);
		Возврат СтрШаблон(НСтр("ru = '%1 млн'"), ПредставлениеСуммы);
	ИначеЕсли Сумма >= СуммаТысяча Тогда
		ПредставлениеСуммы = Окр(Сумма / СуммаТысяча, Разрядность);
		Возврат СтрШаблон(НСтр("ru = '%1 тыс.'"), ПредставлениеСуммы);
	Иначе
		Возврат СтрШаблон(НСтр("ru = '%1'"), Сумма);
	КонецЕсли;
	
КонецФункции

Функция СформироватьСтрокуИнформацииПоУсловию(ПараметрыПродукта, ПользовательУказалПараметры) Экспорт
	
	ПодстрокаСумма = "";
	ПодстрокаСрок = "";
	
	ПределСуммы = 9999999999999.99;
	ПределСрока = 9999;
	
	СуммаМаксимум = ПараметрыПродукта.СуммаМаксимум;
	СуммаМинимум = ПараметрыПродукта.СуммаМинимум;
	СрокМинимум = ПараметрыПродукта.СрокМинимум;
	СрокМаксимум = ПараметрыПродукта.СрокМаксимум;
	Валюта = ПараметрыПродукта.Валюта;
	
	Если СуммаМинимум > 0 И (СуммаМаксимум = 0 Или СуммаМаксимум = ПределСуммы) Тогда
		СуммаСтрока = ПредставлениеСуммы(СуммаМинимум);
		ПодстрокаСумма = СтрШаблон(НСтр("ru = 'от %1 %2'"), СуммаСтрока, Валюта);
	ИначеЕсли СуммаМинимум = 0 Тогда
		СуммаСтрока = ПредставлениеСуммы(СуммаМаксимум);
		ПодстрокаСумма = СтрШаблон(НСтр("ru = 'до %1 %2'"), СуммаСтрока, Валюта);
	Иначе
		
		МаксСуммаСтрока = ПредставлениеСуммы(СуммаМаксимум);
		МинСуммаСтрока = ПредставлениеСуммы(СуммаМинимум);
		
		Если МинСуммаСтрока = МаксСуммаСтрока Тогда
			ПодстрокаСумма = СтрШаблон(НСтр("ru = '%1 %2'"), МинСуммаСтрока, Валюта);
		Иначе
			ПодстрокаСумма = СтрШаблон(НСтр("ru = 'от %1 - до %2 %3'"), МинСуммаСтрока, МаксСуммаСтрока, 
				Валюта);
		КонецЕсли;
		
	КонецЕсли;
	
	Если СрокМинимум = СрокМаксимум Тогда
		ПодстрокаСрок = СтрШаблон(НСтр("ru = '%1 дн.'"), СрокМинимум);
	ИначеЕсли СрокМинимум > 0 И (СрокМаксимум = 0 Или СрокМаксимум = ПределСрока) Тогда
		ПодстрокаСрок = СтрШаблон(НСтр("ru = 'от %1 дн.'"), СрокМинимум);
	ИначеЕсли СрокМинимум = 0 И СрокМаксимум = ПределСрока Тогда
		ПодстрокаСрок = НСтр("ru = 'до востребования.'");
	ИначеЕсли СрокМинимум = 0 Тогда
		ПодстрокаСрок = СтрШаблон(НСтр("ru = 'до %1 дн.'"), СрокМаксимум);
	Иначе
		ПодстрокаСрок = СтрШаблон(НСтр("ru = 'от %1 - %2 дн.'"), СрокМинимум, СрокМаксимум);
	КонецЕсли;
	
	Результат = СтрШаблон("%1, %2", ПодстрокаСумма, ПодстрокаСрок);
	Возврат Результат;
	
КонецФункции

#Область ТекстыСообщений

// Возвращает текст сообщения, отображаемого пользователю при обновлении предложений банков.
//
// Возвращаемое значение:
//  Строка - Текст сообщения
//
Функция ТекстСообщенияОбновлениеПредложений() Экспорт
	Возврат НСтр("ru = 'Пожалуйста, подождите, выполняется запрос предложений банков.'");
КонецФункции

// Возвращает текст сообщения, отображаемого пользователю если для организации нет предложений банков по депозитам.
//
// Возвращаемое значение:
//  Строка - Текст сообщения
//
Функция ТекстСообщенияНетПредложенийБанковДляОрганизации() Экспорт
	Возврат НСтр("ru = 'Для организации нет доступных предложений банков'");
КонецФункции

// Возвращает текст сообщения, отображаемого пользователю если не выбранная организация при подборе предложений банков.
//
// Возвращаемое значение:
//  Строка - Текст сообщения
//
Функция ТекстСообщенияНеЗаполненаОрганизация() Экспорт
	Возврат НСтр("ru = 'Укажите организацию для подбора предложений банков'");
КонецФункции

#КонецОбласти

#КонецОбласти