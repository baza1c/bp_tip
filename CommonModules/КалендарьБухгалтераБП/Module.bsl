#Область СлужебныйПрограммныйИнтерфейс

Функция НоваяПросроченнаяЗадачаИнформационнойПанели() Экспорт
	
	ПросроченнаяЗадача = Новый Структура;
	ПросроченнаяЗадача.Вставить("Наименование", "");
	ПросроченнаяЗадача.Вставить("Срок", Дата(1, 1, 1));
	
	Возврат ПросроченнаяЗадача;
	
КонецФункции

Функция НоваяТаблицаЗадачИнформационнойПанели() Экспорт
	
	ТипыДействия = Новый Массив();
	ТипыДействия.Добавить(Тип("Строка"));
	ТипыДействия.Добавить(Тип("ПеречислениеСсылка.ВидыДействийКалендаряБухгалтера"));
	
	НоваяТаблица = Новый ТаблицаЗначений;
	НоваяТаблица.Колонки.Добавить("Наименование",             ОбщегоНазначения.ОписаниеТипаСтрока(100));
	НоваяТаблица.Колонки.Добавить("Действие",                 Новый ОписаниеТипов(ТипыДействия, , Новый КвалификаторыСтроки(50)));
	НоваяТаблица.Колонки.Добавить("ОтносительныйСрок",        Новый ОписаниеТипов("ПеречислениеСсылка.ОтносительныеСроки"));
	НоваяТаблица.Колонки.Добавить("ОсталосьДней",             ОбщегоНазначения.ОписаниеТипаЧисло(2, 0));
	НоваяТаблица.Колонки.Добавить("ОтносительныйСрокПорядок", ОбщегоНазначения.ОписаниеТипаЧисло(2, 0));
	НоваяТаблица.Колонки.Добавить("ДействиеПорядок",          ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
	НоваяТаблица.Колонки.Добавить("ОтправкаОтчетаОграничена", Новый ОписаниеТипов("Булево"));
	
	Возврат НоваяТаблица;
	
КонецФункции

Функция ОсталосьДней(ТекущаяДата, Срок) Экспорт
	
	Возврат (НачалоДня(Срок) - НачалоДня(ТекущаяДата))/86400;
	
КонецФункции

Процедура ОбновитьПроверкиКонтролирующимиОрганами() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	Справочники.ПроверкиКонтролирующимиОрганами.ОбновитьПроверки();

КонецПроцедуры

Процедура ОбновитьПроверкиКонтролирующимиОрганамиПоТаблицеОрганизаций(ТаблицаОрганизаций) Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	Справочники.ПроверкиКонтролирующимиОрганами.ОбновитьПроверкиПоТаблицеОрганизаций(ТаблицаОрганизаций);
	
	УстановитьПривилегированныйРежим(Истина);
	ОтборЗаданий = Новый Структура("Метаданные", 
		Метаданные.РегламентныеЗадания.ОбновлениеПроверокКонтролирующимиОрганамиПоТаблицеОрганизаций);
	Задания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(ОтборЗаданий);
	Для каждого Задание Из Задания Цикл
		Задание.Удалить();
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
