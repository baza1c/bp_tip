
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Серверные методы формы записи регистра сведений НастройкиУчетаУСН
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ПодготовитьФормуНаСервере(Форма);
	
КонецПроцедуры

Процедура ПодготовитьФормуНаСервере(Форма) Экспорт
	
	Запись = Форма.НастройкиУчетаУСН;
	
	Если Не ЗначениеЗаполнено(Запись.Организация) Тогда
		Запись.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ПериодЧтенияНастройки = НастройкиУчетаУСНФормыКлиентСервер.ПериодЧтенияНастройки(Форма);
	
	ЗаполнитьЗначенияСвойств(Форма,
		НастройкиУчетаУСНФормыВызовСервера.НастройкиОрганизацииИСистемыНалогообложения(
			Запись.Организация, ПериодЧтенияНастройки));
	
	Форма.ТекущаяСтавкаНалога = Запись.СтавкаНалога;
	ЗаполнитьРеквизитыОснованияЛьготнойСтавки(Форма, ПериодЧтенияНастройки);
	
	Форма.ИспользуютсяПродажиЧерезКомиссионера =
		ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьПродажиЧерезМаркетплейс");
	Форма.ПорядокПризнанияДоходаОтКомиссионера = ?(Запись.ПризнаватьДоходОтКомиссионераНаДатуОтчета, 0, 1);
	
	НастройкиУчетаУСНФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	Запись = Форма.НастройкиУчетаУСН;
	
	СтавкиПоУмолчанию = УчетУСНКлиентСервер.НалоговыеСтавкиПоУмолчанию();
	ПределСтавки = ?(Форма.ПрименяетсяУСНДоходы, СтавкиПоУмолчанию.СтавкаУСНДоходы, СтавкиПоУмолчанию.СтавкаУСНДоходыМинусРасходы);
	Если Запись.СтавкаНалога > ПределСтавки Тогда
		ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Выбранная ставка не может применяться.
			|Основная ставка налога для выбранного объекта налогообложения равна %1%2.'"), ПределСтавки, НСтр("ru = '%' "));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения,,"СтавкаНалога", "НастройкиУчетаУСН");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыОснованияЛьготнойСтавки(Форма, ПериодЧтенияНастройки) Экспорт
	
	Если Не УчетУСНКлиентСервер.ТребуетсяОснованиеЛьготнойСтавкиНалога(ПериодЧтенияНастройки) Тогда
		Возврат;
	КонецЕсли;
	
	Запись = Форма.НастройкиУчетаУСН;
	
	Если Не УчетУСНКлиентСервер.ЭтоЛьготнаяСтавка(Запись.СтавкаНалога, Форма.ПрименяетсяУСНДоходы) Тогда
		Возврат;
	КонецЕсли;
	
	Сведения = УчетУСНКлиентСервер.НормативныеДанныеОснованияЛьготы(Запись.ОснованиеЛьготнойСтавки);
	
	Форма.ОснованиеЛьготыУСННомерСтатьи = Сведения.НомерСтатьи;
	Форма.ОснованиеЛьготыУСНПункт       = Сведения.Пункт;
	Форма.ОснованиеЛьготыУСНПодпункт    = Сведения.Подпункт;
	
КонецПроцедуры

#КонецОбласти