#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗагрузитьЧекНПД(Форма) Экспорт
	
	Объект = Форма.Объект;
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Запишите документ, чтобы загрузить чек'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытиеФормыЧекаСамозанятогоЗавершение", Форма);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СсылкаНаЧек", Объект.СсылкаНаЧек);
	ПараметрыФормы.Вставить("НомерЧека", Объект.НомерЧека);
	ПараметрыФормы.Вставить("ЧекСамозанятого", Объект.ЧекСамозанятого);
	ПараметрыФормы.Вставить("ДокументВладелец", Объект.Ссылка);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	ОткрытьФорму("ОбщаяФорма.ФормаЧекаСамозанятого",
		ПараметрыФормы,
		Форма,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Функция НовыеДанныеЧекаЗагружены(ОтветФормыЗагрузкиЧека, Форма) Экспорт
	
	Если ОтветФормыЗагрузкиЧека = КодВозвратаДиалога.Отмена Или ОтветФормыЗагрузкиЧека = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	Если Объект.СсылкаНаЧек = ОтветФормыЗагрузкиЧека.СсылкаНаЧек И Объект.НомерЧека = ОтветФормыЗагрузкиЧека.НомерЧека
		И Объект.ЧекСамозанятого = ОтветФормыЗагрузкиЧека.ЧекСамозанятого Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, ОтветФормыЗагрузкиЧека);
	Если ЗначениеЗаполнено(Объект.СсылкаНаЧек) Или ЗначениеЗаполнено(Объект.НомерЧека)
		Или ЗначениеЗаполнено(Объект.ЧекСамозанятого) Тогда
		Форма.ПолученЧекНПД = Истина;
	КонецЕсли;
	Если ОтветФормыЗагрузкиЧека.ЧекБылОчищен Тогда
		Форма.ПолученЧекНПД = Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ОчиститьРеквизитыЧекаНПД(Форма) Экспорт
	
	Объект = Форма.Объект;
	Объект.НомерЧека = "";
	Объект.СсылкаНаЧек = "";
	Объект.ЧекСамозанятого = ПредопределенноеЗначение(
		"Справочник.ПоступлениеТоваровУслугПрисоединенныеФайлы.ПустаяСсылка");
	
КонецПроцедуры

#КонецОбласти