// Механизм расчета статусов оформления документов ИС МП.
//

#Область СлужебныйПрограммныйИнтерфейс

// Позволяет переопределить имена реквизитов документа-основания для документа ИСМП.
//
// Параметры:
//  МетаданныеДокументаОснования - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.Основание<Имя документа ИСМП>
//  МетаданныеДокументаИСМП - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.ДокументыИСМППоддерживающиеСтатусыОформления
//  Реквизиты  - Структура - имена реквизитов:
//  * Ключ  - служебное имя реквизита в ИСМП
//  * Значение - имя реквизита документа-основания, которое при необходимости надо переопределить
//  (см. РасчетСтатусовОформленияИСМП.СтруктураРеквизитовДляРасчетаСтатусаОформленияДокументов).
Процедура ПриОпределенииИменРеквизитовДляРасчетаСтатусаОформления(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаИСМП, Реквизиты) Экспорт
	
	// Зададим имена реквизитов по умолчанию.
	Реквизиты.Контрагент = "Организация";
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ПроизводственнаяОперацияВЕТИС Тогда
		Реквизиты.Контрагент = "ХозяйствующийСубъект";
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ВходящаяТранспортнаяОперацияВЕТИС Тогда
		Реквизиты.Контрагент = "ГрузополучательХозяйствующийСубъект";
	КонецЕсли;
		
	// Определим имя реквизита "Ответственный".
	Если МетаданныеДокументаОснования.Реквизиты.Найти("Кассир") <> Неопределено Тогда
		Реквизиты.Ответственный = "Кассир";
	Иначе
		Реквизиты.Ответственный = "Ответственный";
	КонецЕсли;
	
КонецПроцедуры

// Офомлять документ Отгрузка товаров ИС МП можно для:
//- шин, обуви, одежды с операцией Отгрузка ЕАЭС
//- молочной продукции с операцией Отгрузка ЕАЭС при объемно-сортовом учете
//- шуб.
Функция ВидыМаркируемойПродукцииОтгрузкаИСМП()
	ВидыМаркируемойПродукцииОтгрузкаИСМП = Новый Массив;
	
	ВидыМаркируемойПродукцииОтгрузкаИСМП.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь"));
	ВидыМаркируемойПродукцииОтгрузкаИСМП.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность"));
	ВидыМаркируемойПродукцииОтгрузкаИСМП.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность2025"));
	ВидыМаркируемойПродукцииОтгрузкаИСМП.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины"));
	ВидыМаркируемойПродукцииОтгрузкаИСМП.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС"));
	ВидыМаркируемойПродукцииОтгрузкаИСМП.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС"));
	ВидыМаркируемойПродукцииОтгрузкаИСМП.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха"));
	
	Возврат ВидыМаркируемойПродукцииОтгрузкаИСМП;
КонецФункции


// Позволяет переопределить текст запроса выборки данных из документов-основания для расчета статуса оформления.
//   Требования к тексту запроса:
//     Если данные из документа выбирать не требуется, переопределение также не заполнять.
//     Результат запроса обязательно должен содержать следующие поля:
//      * Ссылка - ОпределяемыйТип.Основание<Имя документа ИСМП> - ссылка на документ-основание
//      * ЭтоДвижениеПриход - Булево - вид движения ТМЦ (Истина - приход, Ложь - расход)
//      * Номенклатура - ОпределяемыйТип.Номенклатура - номенклатура
//      * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатуры
//      * Серия - ОпределяемыйТип.СерияНоменклатуры - серия номенклатуры
//      * Количество - Число - количество номенклатуры в ее основной единице измерения
//     В результат запроса нужно включать только подконтрольную номенклатуру ИСМП (табак, обувь)
//     Для отбора документов-основания в запросе нужно использовать отбор "В (&МассивДокументов)"
//     Выбранные данные необходимо поместить во временную таблицу (См. РасчетСтатусовОформленияИС.ИмяВременнойТаблицыДляВыборкиДанныхДокумента).
//
Процедура ПриОпределенииТекстаЗапросаДляРасчетаСтатусаОформления(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаИСМП, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ОтчетПроизводстваЗаСмену
		И МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ОтчетПроизводстваЗаСмену.Продукция КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция";
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПоступлениеИзПереработки
		И МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ПоступлениеИзПереработки.Продукция КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.СписаниеТоваров
		И МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.СписаниеТоваров.Товары КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ТребованиеНакладная
		И МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ТребованиеНакладная.Материалы КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.РеализацияТоваровУслуг
		И МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП) Тогда
		
		// Если на основании реализации есть оформленный документ ЭДО, 
		// то "Отгрузка товаров ИСМП" на его основании создавать не надо
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
		|		ПО ТаблицаТовары.Ссылка.Контрагент = СправочникКонтрагенты.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция
		|	И НЕ(СправочникКонтрагенты.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|				И СправочникКонтрагенты.СтранаРегистрации.УчастникЕАЭС = ИСТИНА
		|				И &УсловиеМаркируемаяПродукцияОтгрузкаИСМП)
		|	И НЕ 1 В
		|				(ВЫБРАТЬ
		|					1
		|				ИЗ
		|					РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|				ГДЕ
		|					ОбъектыУчетаДокументовЭДО.ОбъектУчета = ТаблицаТовары.Ссылка
		|					И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.СодержитМаркируемыеТовары)
		|	И НЕ 1 В (ВЫБРАТЬ
		|                   ЕСТЬNULL(СтатусыКонтрагентовИСМП.СтатусУчастника, 0) КАК Статус
		|				ИЗ
		|					РегистрСведений.СтатусыКонтрагентовИСМП КАК СтатусыКонтрагентовИСМП
		|				ГДЕ 
		|					СтатусыКонтрагентовИСМП.Контрагент = ТаблицаТовары.Ссылка.Контрагент
		|					И СтатусыКонтрагентовИСМП.СтатусУчастника = 1)";

		
		ИнтеграцияИСМПБП.УстановитьУсловиеПоМаркируемойПродукции(
			ТекстЗапроса, ВидыМаркируемойПродукцииОтгрузкаИСМП(), "&УсловиеМаркируемаяПродукцияОтгрузкаИСМП");
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеТоваров
		И МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ОприходованиеТоваров.Товары КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровОтПокупателя
		И МетаданныеДокументаИСМП = Метаданные.Документы.ПеремаркировкаТоваровИСМП) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция
		|	И ТаблицаТовары.Ссылка.Сделка ССЫЛКА Документ.ОтчетОРозничныхПродажах";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ОтчетОРозничныхПродажах
		И МетаданныеДокументаИСМП = Метаданные.Документы.ПеремаркировкаТоваровИСМП) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.Возвраты КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.РеализацияТоваровУслуг
		И МетаданныеДокументаИСМП = Метаданные.Документы.ОтгрузкаТоваровИСМП) Тогда
		
		// Если на основании реализации есть оформленный документ ЭДО, 
		// то "Отгрузка товаров ИСМП" на его основании создавать не надо
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
		|		ПО ТаблицаТовары.Ссылка.Контрагент = СправочникКонтрагенты.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И СправочникКонтрагенты.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|	И СправочникКонтрагенты.СтранаРегистрации.УчастникЕАЭС = ИСТИНА
		|	И &УсловиеМаркируемаяПродукцияОтгрузкаИСМП
		|	И НЕ 1 В
		|				(ВЫБРАТЬ
		|					1
		|				ИЗ
		|					РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|				ГДЕ
		|					ОбъектыУчетаДокументовЭДО.ОбъектУчета = ТаблицаТовары.Ссылка
		|					И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.СодержитМаркируемыеТовары)";
		
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровПоставщику
		И МетаданныеДокументаИСМП = Метаданные.Документы.ОтгрузкаТоваровИСМП) Тогда
		
		// Если на основании реализации есть оформленный документ ЭДО, 
		// то "Отгрузка товаров ИСМП" на его основании создавать не надо
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукцияОтгрузкаИСМП
		|	И НЕ(1 В (ВЫБРАТЬ 1 Из РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО 
		|			ГДЕ ОбъектыУчетаДокументовЭДО.ОбъектУчета = ТаблицаТовары.Ссылка 
		|			И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.СодержитМаркируемыеТовары))";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ПоступлениеТоваровУслуг
		И МетаданныеДокументаИСМП = Метаданные.Документы.ПриемкаТоваровИСМП) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция
		|	И (1 В (ВЫБРАТЬ 1 ИЗ Документ.ПриемкаТоваровИСМП КАК ПриемкаТоваровИСМП 
		|		ГДЕ ПриемкаТоваровИСМП.ДокументОснование = ТаблицаТовары.Ссылка))";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровОтПокупателя
		И МетаданныеДокументаИСМП = Метаданные.Документы.ПриемкаТоваровИСМП) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукция
		|	И (1 В (ВЫБРАТЬ 1 ИЗ Документ.ПриемкаТоваровИСМП КАК ПриемкаТоваровИСМП 
		|		ГДЕ ПриемкаТоваровИСМП.ДокументОснование = ТаблицаТовары.Ссылка))";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.ПередачаТоваров
		И МетаданныеДокументаИСМП = Метаданные.Документы.ОтгрузкаТоваровИСМП) Тогда
		
		// Если на основании реализации есть оформленный документ ЭДО, 
		// то "Отгрузка товаров ИСМП" на его основании создавать не надо
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ПередачаТоваров.Товары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивДокументов)
		|	И &УсловиеМаркируемаяПродукцияОтгрузкаИСМП
		|	И НЕ(1 В (ВЫБРАТЬ 1 Из РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО 
		|			ГДЕ ОбъектыУчетаДокументовЭДО.ОбъектУчета = ТаблицаТовары.Ссылка 
		|			И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.СодержитМаркируемыеТовары))";
		
	ИначеЕсли (МетаданныеДокументаОснования = Метаданные.Документы.МаркировкаТоваровИСМП
		И МетаданныеДокументаИСМП = Метаданные.Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ) Тогда
		//Опеределяется в библиотеке
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.РеализацияТоваровУслуг 
		И МетаданныеДокументаИСМП = Метаданные.Документы.РазрешениеНаОтгрузкуИСМП Тогда
	    ТекстЗапроса = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	РеализацияТоваровУслуг.Дата КАК ДатаДокумента,
		|	ЕСТЬNULL(СостоянияЭД.ЭлектронныйДокумент, НЕОПРЕДЕЛЕНО) КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Характеристика,
		|	НЕОПРЕДЕЛЕНО КАК Серия,
		|	1 КАК Количество,
		|   СправочникНоменклатура.ВидМаркировки КАК ВидПродукцииИС 
		|ПОМЕСТИТЬ втТоварыДокументаОснования
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслуг.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО РеализацияТоваровУслугТовары.Номенклатура = СправочникНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК СостоянияЭД
		|		ПО СостоянияЭД.ОбъектУчета = РеализацияТоваровУслугТовары.Ссылка
		|		И СостоянияЭД.Актуальный
		|		И СостоянияЭД.ЭлектронныйДокумент ССЫЛКА Документ.ЭлектронныйДокументИсходящийЭДО
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
		|	И РеализацияТоваровУслуг.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТоварыДокумента.Ссылка КАК Ссылка,
		|	втТоварыДокумента.ЭтоДвижениеПриход КАК ЭтоДвижениеПриход,
		|	втТоварыДокумента.Номенклатура КАК Номенклатура,
		|	втТоварыДокумента.Характеристика КАК Характеристика,
		|	втТоварыДокумента.Серия КАК Серия,
		|	втТоварыДокумента.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	втТоварыДокументаОснования КАК втТоварыДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаМаркируемойПродукцииИСМП КАК НастройкиУчетаМаркируемойПродукцииИСМП
		|		ПО втТоварыДокумента.ВидПродукцииИС = НастройкиУчетаМаркируемойПродукцииИСМП.ВидПродукции
		|			И (НастройкиУчетаМаркируемойПродукцииИСМП.ДатаПрослеживаемостьПеремещенияМеждуМОД > ДАТАВРЕМЯ(1, 1, 1))
		|			И (НАЧАЛОПЕРИОДА(втТоварыДокумента.ДатаДокумента, ДЕНЬ) >= НастройкиУчетаМаркируемойПродукцииИСМП.ДатаПрослеживаемостьПеремещенияМеждуМОД)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО втТоварыДокумента.Ссылка = ОбъектыУчетаДокументовЭДО.ОбъектУчета
		|			И (ОбъектыУчетаДокументовЭДО.Актуальный)
		|			И (ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент ССЫЛКА Документ.ЭлектронныйДокументИсходящийЭДО)
		|ГДЕ
		|	(втТоварыДокумента.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)
		|			ИЛИ втТоварыДокумента.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках)
		|			ИЛИ втТоварыДокумента.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов))";
		
		СоответствиеТребованиямГИСМТ.ДополнитьТекстРасчетаСтатусовДляРазрешенияНаОтгрузкуИСМП(ТекстЗапроса);
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеТоваров 
		И МетаданныеДокументаИСМП = Метаданные.Документы.ПеремещениеМеждуМОДИСМП Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ИСТИНА               КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	""""         КАК Характеристика,
		|	""""         КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	&ИмяТаблицыТовары КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ИмяТаблицыДокумента КАК ТаблицаДокумента
		|	ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|	ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаМаркируемойПродукцииИСМП КАК НастройкиУчета
		|	ПО НастройкиУчета.ДатаПрослеживаемостьПеремещенияМеждуМОД > ДАТАВРЕМЯ(1, 1, 1)
		|	И НАЧАЛОПЕРИОДА(ТаблицаДокумента.Дата, ДЕНЬ) >= НастройкиУчета.ДатаПрослеживаемостьПеремещенияМеждуМОД
		|	И СправочникНоменклатура.ВидМаркировки = НастройкиУчета.ВидПродукции
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В (&МассивДокументов)
		|	И (СправочникНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)
		|   ИЛИ СправочникНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках)
		|	ИЛИ СправочникНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов))
		|	И ТаблицаДокумента.Проведен";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицыТовары",    СтрШаблон("Документ.%1.Товары", МетаданныеДокументаОснования.Имя));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицыДокумента", СтрШаблон("Документ.%1", МетаданныеДокументаОснования.Имя));

	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЛОЖЬ		 КАК ЭтоДвижениеПриход,
		|	НЕОПРЕДЕЛЕНО КАК Ссылка,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Характеристика,
		|	НЕОПРЕДЕЛЕНО КАК Серия,
		|	0 			 КАК Количество
		|ПОМЕСТИТЬ %1";
		
	КонецЕсли;
	
	ВидыМаркируемойПродукцииОтгрузкаИСМП = ВидыМаркируемойПродукцииОтгрузкаИСМП();
	ИнтеграцияИСМПБП.УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидыМаркируемойПродукцииОтгрузкаИСМП, "&УсловиеМаркируемаяПродукцияОтгрузкаИСМП");
	
	ВидыМаркируемойПродукции = ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииИСМП(Истина);
	ИнтеграцияИСМПБП.УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидыМаркируемойПродукции);
	
КонецПроцедуры

#КонецОбласти
