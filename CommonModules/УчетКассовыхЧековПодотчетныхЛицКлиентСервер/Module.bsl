#Область ПрограммныйИнтерфейс

// Конструктор для коллекции, описывающий реализацию конкретной формы документа.
// 
// Возвращаемое значение:
//  Структура - см. тело функции
//
Функция НовыйОписаниеФормы() Экспорт
	
	ОписаниеФормы = Новый Структура;
	
	// Форма должна содержать следующие реквизиты (стандартные имена указаны в значениях структуры):
	
	// Булево, наличие в документе загруженных чеков
	ОписаниеФормы.Вставить("РеквизитЕстьЗагруженныеЧеки", "ОтображатьНаименованиеИзЧека"); 
	// Строка, имя действия, которое может быть выполнено с чеками к загрузке.
	// См. УчетКассовыхЧековПодотчетныхЛиц.СледующееДействие
	ОписаниеФормы.Вставить("РеквизитСледующееДействие",   "КассовыеЧекиПодотчетныхЛицДействие");
	// Строка, адрес в хранилище, в котором сохраняются данные о чеках к загрузке и иная информация,
	// нужная для подбора чеков
	ОписаниеФормы.Вставить("РеквизитДанныеФормы",         "КассовыеЧекиПодотчетныхЛицАдрес");
	// Строка, текст баннера о наличии чеков к загрузке
	ОписаниеФормы.Вставить("РеквизитТекстБаннера",        "БаннерЧекиПодотчетныхЛицТекст");
	// Строка, имя элемента формы с иконкой баннера об ограничениях тарифа
	ОписаниеФормы.Вставить("РеквизитКартинкаБаннера",      "КартинкаБаннерЧекиПодотчетныхЛицВнимание");
	
	// Форма должна содержать группу с баннером, имя которой указано ниже
	ОписаниеФормы.Вставить("Баннер",                      "ГруппаБаннерЧекиПодотчетныхЛиц");
	
	// Форма должна содержать подключаемый обработчик ожидания, имя которого указано ниже
	ОписаниеФормы.Вставить(
		"ОбработчикОжидания",
		"Подключаемый_ОбновитьЧекиКЗагрузке");
	
	// Форма должна содержать команды загрузки чеков.
	// Команд может быть несколько, если несколько списков.
	// Здесь следует указать имена кнопок формы
	ОписаниеФормы.Вставить("КнопкиЗагрузитьЧеки",         Новый Массив);
	
	Возврат ОписаниеФормы;
	
КонецФункции

Функция ТекстКоличествоЧеков(Количество, ВыделитьКоличествоЖирным = Ложь, ДобавитьТекстОтсканировано = Ложь) Экспорт
	
	ШаблонТекстаЧеки = НСтр("ru=';%1 чек;;%1 чека;%1 чеков;%1 чека'");
	ШаблонТекстаОтсканированоЧеков =
		НСтр("ru=';Отсканирован %1 чек;;Отсканировано %1 чека;Отсканировано %1 чеков;Отсканировано %1 чека'");
	
	Если ВыделитьКоличествоЖирным Тогда
		ШаблонТекстаЧеки = НСтр("ru=';<b>%1</b> чек;;<b>%1</b> чека;<b>%1</b> чеков;<b>%1</b> чека'");
		ШаблонТекстаОтсканированоЧеков =
			НСтр("ru=';Отсканирован <b>%1</b> чек;;Отсканировано <b>%1</b> чека;Отсканировано <b>%1</b> чеков;Отсканировано <b>%1</b> чека'");
	КонецЕсли;
	
	Возврат ?(ДобавитьТекстОтсканировано,
		СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонТекстаОтсканированоЧеков, Количество,, "ЧДЦ=0"),
		СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонТекстаЧеки, Количество,, "ЧДЦ=0"));
	
КонецФункции

Функция ТекстКоличествоОтсканированныхЧеков(Количество, ВыделитьКоличествоЖирным = Ложь) Экспорт
	
	ШаблонТекста = ?(ВыделитьКоличествоЖирным,
		НСтр("ru=';<b>%1</b> отсканированный чек;;<b>%1</b> отсканированных чека;<b>%1</b> отсканированных чеков;<b>%1</b> отсканированных чека'"),
		НСтр("ru=';%1 отсканированный чек;;%1 отсканированных чека;%1 отсканированных чеков;%1 отсканированных чека'"));
	
	Возврат СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонТекста, Количество,, "ЧДЦ=0");
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Актуализирует на форме документа баннер про чеки, доступные к загрузке.
// Баннер должен быть предварительно инициализирован в УчетКассовыхЧековПодотчетныхЛиц.НастроитьФорму
// Вызывается в контексте клиента или сервера.
//
// Параметры:
//  Форма         - УправляемаяФорма - настраиваемая форма документа, в которую можно загружать чеки подотчетных лиц,
//                                     должна соответствовать описанию
//  ОписаниеФормы - Структура - см. НовыйОписаниеФормы
//  ТекстБаннера  - Строка - устанавливаемый текст, см. УчетКассовыхЧековПодотчетныхЛиц.ТекстБаннера
//  ВыводитьКартинкуБаннера - Булево - требуется ли вывести картинку в баннере.
//
Процедура ОбновитьБаннерЧекиКЗагрузке(Форма, ОписаниеФормы, ТекстБаннера, ВыводитьКартинкуБаннера) Экспорт
	
	// РеквизитТекстБаннера заменен с ПолеВвода на Декорация в связи с особенностью работы метода
	// ОбновитьОтображениеДанных (не работает при вызове с параметром из подключаемого обработчика ожидания формы).
	Форма.Элементы[ОписаниеФормы.РеквизитТекстБаннера].Заголовок = ТекстБаннера;
	ПолеБаннера = Форма.Элементы[ОписаниеФормы.Баннер];
	ПолеБаннера.Видимость = Не ПустаяСтрока(ТекстБаннера);
	Форма.Элементы[ОписаниеФормы.РеквизитКартинкаБаннера].Видимость = ВыводитьКартинкуБаннера;
	
КонецПроцедуры

// Получает интервал, через который будет выполнятся проверка на наличие изменений в чеках подотчетных лиц.
// 
// Возвращаемое значение:
//  Число- время в секундах
//
Функция ИнтервалПроверкиЧековПодотчетныхЛиц() Экспорт
	
	Возврат 20;
	
КонецФункции

#КонецОбласти
