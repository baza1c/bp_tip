#Область НастройкиАвторизации

Процедура СохранитьНастройкиАвторизации(НастройкаИнтеграции, Токен) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкаИнтеграции, Токен, ИмяТокена());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПрочитатьНастройкиАвторизации(НастройкаИнтеграции)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(НастройкаИнтеграции, ИмяТокена());
КонецФункции

Функция ИмяТокена()
	Возврат "Токен_Версия_1_4";
КонецФункции

Функция ИнтеграцияНастроена(НастройкаИнтеграции) Экспорт
	Возврат ЗначениеЗаполнено(ПрочитатьНастройкиАвторизации(НастройкаИнтеграции));
КонецФункции 

#КонецОбласти

#Область РаботаСОтчетами

Функция ПрочитатьОтчет(ПараметрыОтчета, КодОтвета, СообщенияОбОшибках)
	
	ТаблицаТоваров = ЭлектронноеВзаимодействиеБП.ПодготовитьТаблицуТоваровМаркетплейс();
	
	ПараметрыЗапроса = НовыйПараметрыЗапросаОтчетОпродажахWildberries();
	
	НастройкиПодключения = НовыйНастройкиПодключения();
	
	НастройкиПодключения.Токен = ПрочитатьНастройкиАвторизации(ПараметрыОтчета.НастройкаИнтеграции);
	
	ПараметрыЗапроса.НастройкиПодключения = НастройкиПодключения;
	ПараметрыЗапроса.ВерсияМетодаAPI = ВерсияМетодаОтчетОПродажахAPIWildberries(ПараметрыОтчета.НачалоПериода);
	ПараметрыЗапроса.НачалоПериода        = ПараметрыОтчета.НачалоПериода;
	ПараметрыЗапроса.ОкончаниеПериода     = ПараметрыОтчета.ОкончаниеПериода;
	
	ПараметрыЗапроса.ПределВыборкиОдногоЗапроса = 10000;
	
	ПоследняяЗагруженнаяСтрока = 0;
	
	ВыполнятьСЗадержкой = Ложь;
	
	//Логирование
	Если РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЛогированиеВключено() Тогда
		Логирование = РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЛогирования(ПараметрыОтчета.НастройкаИнтеграции);
	КонецЕсли;
	//Конец Логирование

	МассивЗагрузок = Новый Массив;
	Пока Истина Цикл
		
		Если ВыполнятьСЗадержкой Тогда
			Приостановить(61);
		КонецЕсли;
		
		ПараметрыЗапроса.НачалоВыборки = ПоследняяЗагруженнаяСтрока;
		РезультатЗапроса = ЗапросОтчетОпродажахWildberries(ПараметрыЗапроса, СообщенияОбОшибках, Логирование);
		
		Если ЗначениеЗаполнено(СообщенияОбОшибках) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		КодОтвета = РезультатЗапроса.КодСостояния;
		Если КодОтвета <> 200 Тогда
			
			Если КодОтвета = 502 Тогда
				СообщенияОбОшибках = НСтр("ru = 'Сервер API Wildberries недоступен. Попробуйте позже'");
			ИначеЕсли КодОтвета = 400 или КодОтвета = 401 Тогда
				СообщенияОбОшибках = НСтр("ru = 'Ошибка авторизации в API Wildberries.
					|Возможно, ваш токен API устарел: срок действия токена - 180 дней от даты создания.
					|Получите токен API с типом ""Статистика"" в личном кабинете Профиль-Настройки-Доступ к API'");
			ИначеЕсли КодОтвета = 403 Тогда
				СообщенияОбОшибках = НСтр("ru = 'Ошибка авторизации в API Wildberries. Проверьте в настройках ключ API'");
			ИначеЕсли КодОтвета = 429 Тогда
				
				Если ВыполнятьСЗадержкой Тогда
					СообщенияОбОшибках = НСтр("ru = 'Сервер API Wildberries ответил: слишком много запросов. Попробуйте позже'");
				Иначе
					ВыполнятьСЗадержкой = Истина;
					Продолжить;
				КонецЕсли;
				
			Иначе
				ПереченьОшибок = РезультатЗапроса.ПолучитьТелоКакСтроку();
				Если СтрНайти(ПереченьОшибок, "Record limit exceeded") > 0
					И ПараметрыЗапроса.ПределВыборкиОдногоЗапроса = 10000 Тогда
					ПараметрыЗапроса.ПределВыборкиОдногоЗапроса = 1000;
					ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Не удалось загрузить отчет о продажах. Получен ответ API Wildberries %1, код ошибки: %2'"),
						ПараметрыЗапроса.ВерсияМетодаAPI,
						ПереченьОшибок);
					ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ТекстОшибки, Перечисления.ВидыМаркетплейсов.Wildberries);
					Продолжить;
				КонецЕсли;
				СообщенияОбОшибках = СтрШаблон(
					НСтр("ru = 'Не удалось загрузить отчет о продажах. Получен ответ API Wildberries %1, код ошибки: %2'"),
						ПараметрыЗапроса.ВерсияМетодаAPI,
						ПереченьОшибок);
			КонецЕсли;
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщенияОбОшибках, Перечисления.ВидыМаркетплейсов.Wildberries);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СообщенияОбОшибках) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Попытка
			РезультатЧтения = ОбменСМаркетплейс.ПолучитьДанныеОтвета(РезультатЗапроса);
		Исключение
			ТекстОтвета = РезультатЗапроса.ПолучитьТелоКакСтроку();
			Если ПустаяСтрока(ТекстОтвета) Тогда
				СообщенияОбОшибках = СтрШаблон(
				НСтр("ru = 'Не удалось загрузить отчет о продажах.
				|Получен ответ API Wildberries %1: <пустой ответ> - ответ не содержит данных. 
				|Обратитесь в техподдержку Wildberries с жалобой на ошибку в работе метода %2
				|при выполнении запроса за период с %3 по %4'"),
					ПараметрыЗапроса.ВерсияМетодаAPI,
					АдресЗапросаСтатистикиWildberries(ПараметрыЗапроса.ВерсияМетодаAPI),
					Формат(ПараметрыОтчета.НачалоПериода,"ДЛФ=D"),
					Формат(ПараметрыОтчета.ОкончаниеПериода,"ДЛФ=D"));
			Иначе
				СообщенияОбОшибках = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			КонецЕсли;
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщенияОбОшибках, Перечисления.ВидыМаркетплейсов.Wildberries);
			Возврат Неопределено;
		КонецПопытки;
		
		//API вернул ответ null
		Если РезультатЧтения = Неопределено Тогда 
			
			СообщенияОбОшибках = СтрШаблон(
				НСтр("ru = 'Не удалось загрузить отчет о продажах.
				|Получен ответ API Wildberries %1: Отсутствует данные отчета за период с %2 по %3. Попробуйте загрузить отчет позже'"),
					ПараметрыЗапроса.ВерсияМетодаAPI,
					Формат(ПараметрыОтчета.НачалоПериода,"ДЛФ=D"),
					Формат(ПараметрыОтчета.ОкончаниеПериода,"ДЛФ=D"));
					ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщенияОбОшибках, Перечисления.ВидыМаркетплейсов.Wildberries);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		ПрочитаноСтрок = РезультатЧтения.Количество(); 
		
		//API вернул ответ []
		Если ПрочитаноСтрок = 0 Тогда 
			
			СообщенияОбОшибках = СтрШаблон(
				НСтр("ru = 'Не удалось загрузить отчет о продажах.
				|Получен ответ API Wildberries %1: Отсутствует данные отчета за период с %2 по %3.'"),
					ПараметрыЗапроса.ВерсияМетодаAPI,
					Формат(ПараметрыОтчета.НачалоПериода,"ДЛФ=D"),
					Формат(ПараметрыОтчета.ОкончаниеПериода,"ДЛФ=D"));
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщенияОбОшибках, Перечисления.ВидыМаркетплейсов.Wildberries);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		ПоследняяЗагруженнаяСтрока = Формат(РезультатЧтения[ПрочитаноСтрок-1].rrd_id, "ЧГ=0");
		
		ДополнитьТаблицуТоваровWildberries(РезультатЧтения, ТаблицаТоваров);
		
		РезультатЧтения = Неопределено;
		
		Если ПрочитаноСтрок < ПараметрыЗапроса.ПределВыборкиОдногоЗапроса Тогда
			//все выбрали, второй запрос не нужен
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаТоваров;

КонецФункции

Функция ПродажаЧерезВыкупТовара(КодСтраны, НазваниеСтраны = "") Экспорт
	
	Код = Врег(КодСтраны);
	Название = Врег(НазваниеСтраны);
	Возврат Код = "KZ" ИЛИ Название = "КАЗАХСТАН" 
			ИЛИ Код = "BY" ИЛИ Название = "БЕЛАРУСЬ"
			ИЛИ Код = "KG" ИЛИ Название = "КИРГИЗИЯ"
			ИЛИ Код = "AM" ИЛИ Название = "АРМЕНИЯ";
	
КонецФункции

Функция СписаниеТовараМаркетплейсом(ВидДвижения, ТипДокумента) Экспорт
	
	ЭтоСписание = (Нрег(ВидДвижения) = "оплата потерянного товара" 
					ИЛИ Нрег(ВидДвижения) = "авансовая оплата за товар без движения" 
					ИЛИ Нрег(ВидДвижения) = "частичная компенсация брака"
					ИЛИ Нрег(ВидДвижения) = "компенсация потерянного товара"
					ИЛИ Нрег(ВидДвижения) = "компенсация подмененного товара"
					ИЛИ Нрег(ВидДвижения) = "компенсация ущерба"
					ИЛИ Нрег(ВидДвижения) = "добровольная компенсация при возврате"
					ИЛИ Нрег(ВидДвижения) = "компенсация брака");
					
	ЭтоСторно   = (Нрег(ТипДокумента) = "возврат");
	
	Возврат ЭтоСписание И НЕ ЭтоСторно;
	
КонецФункции

Функция ПродажаТовараМаркетплейсом(ВидДвижения, ТипДокумента) Экспорт
	
	Возврат Нрег(ВидДвижения) = "продажа"
			ИЛИ Нрег(ВидДвижения) = "корректная продажа"
			ИЛИ Нрег(ВидДвижения) = "сторно возвратов"
			ИЛИ (Нрег(ВидДвижения) = "коррекция продаж"
				И Нрег(ТипДокумента) = "продажа");
	
КонецФункции

Функция ВозвратТовараМаркетплейсом(ВидДвижения, ТипДокумента) Экспорт
	
	Возврат Нрег(ВидДвижения) = "возврат"
			ИЛИ Нрег(ВидДвижения) = "сторно продаж"
			ИЛИ (Нрег(ВидДвижения) = "коррекция продаж"
				И Нрег(ТипДокумента) = "возврат");
	
КонецФункции

// Проверяет необходимость исключения выкупов из документа продаж для организации
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - организация для проверки
//  Дата		 - Дата - дата для проверки
// 
// Возвращаемое значение:
//   Булево - результат проверки
//
Функция ИсключитьВыкупы(Организация, Дата) Экспорт
	
	Возврат (Дата <= '20230901')
		Или УчетнаяПолитика.ПлательщикНДС(Организация, Дата)
		Или УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, Дата);
	
КонецФункции

Функция ПреобразоватьДанныеПрочитанногоОтчета(ПараметрыОтчета, ТаблицаТоваров)
	
	КолонкиТаблицыТоваров = ТаблицаТоваров.Колонки;
	Если КолонкиТаблицыТоваров.Найти("НатуральныйИД") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("НатуральныйИД", ОбщегоНазначения.ОписаниеТипаСтрока(300));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("КоличествоВозврат") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("КоличествоВозврат", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СуммаВозврат") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СуммаВозврат", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СтавкаНДС") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СуммаНДС") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СуммаНДС", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	КонецЕсли;
	
	КолонкаНаимТов = КолонкиТаблицыТоваров.Найти("НаимТов");
	КолонкаНаименование = КолонкиТаблицыТоваров.Найти("Наименование");
	Если КолонкаНаимТов <> Неопределено
		И КолонкаНаименование = Неопределено Тогда
		КолонкаНаимТов.Имя = "Наименование";
	КонецЕсли;
	
	ТаблицаПродажи  = ТаблицаТоваров.СкопироватьКолонки();
	ТаблицаВыкуп    = ТаблицаТоваров.СкопироватьКолонки();
	ТаблицаВыкупУСН = ТаблицаТоваров.СкопироватьКолонки();
	ТаблицаСписание = ТаблицаТоваров.СкопироватьКолонки();
	ТаблицаОприходование    = ТаблицаТоваров.СкопироватьКолонки();
	
	НастройкаИнтеграции     = ПараметрыОтчета.НастройкаИнтеграции;
	КонтрагентДоговор       = Справочники.НастройкиИнтеграцииМаркетплейс.КонтрагентДоговорНаДату(НастройкаИнтеграции, ПараметрыОтчета.ОкончаниеПериода);
	
	Организация             = НастройкаИнтеграции.Владелец;
	Контрагент              = КонтрагентДоговор.Контрагент;
	ДоговорКонтрагента      = КонтрагентДоговор.ДоговорКонтрагента;
	Дата                    = ПараметрыОтчета.ОкончаниеПериода;
	
	ИсключатьВыкупы = ИсключитьВыкупы(Организация, Дата);
	
	СуммаПродаж = 0;
	СуммаВозвратов = 0;
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		// если строки с нулевыми показателями (например ВидДвижения = "логистика") попадут в таблицу продаж,
		// то может быть неверно определен номер отчета (ТаблицаПродажи[0].ИдентификаторОтчета)
		Если СтрокаТаблицы.Количество = 0 И СтрокаТаблицы.КоличествоВозврат = 0
			И СтрокаТаблицы.Сумма = 0 И СтрокаТаблицы.СуммаВозврат = 0
			И СтрокаТаблицы.Доплата = 0 И СтрокаТаблицы.ДоплатаВозврат = 0
			И СтрокаТаблицы.ДоплатаПартнеров = 0 И СтрокаТаблицы.ДоплатаПартнеровВозврат = 0 Тогда
			Продолжить;
		КонецЕсли;
		Если СписаниеТовараМаркетплейсом(СтрокаТаблицы.ВидДвижения, СтрокаТаблицы.ТипДокумента) Тогда
			НоваяСтрока = ТаблицаСписание.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		ИначеЕсли ОприходованиеТовараМаркетплейсом(СтрокаТаблицы.ВидДвижения, СтрокаТаблицы.ТипДокумента) Тогда
			НоваяСтрока = ТаблицаОприходование.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		ИначеЕсли ПродажаЧерезВыкупТовара(СтрокаТаблицы.КодСтраны, СтрокаТаблицы.КодСтраны)
			И (СтрокаТаблицы.ВидДвижения = "продажа" Или СтрокаТаблицы.ВидДвижения = "корректная продажа") Тогда
			Если ИсключатьВыкупы Тогда
				НоваяСтрока = ТаблицаВыкуп.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Иначе
				Если СтрокаТаблицы.ТипОтчета = 2 Тогда
					НоваяСтрока = ТаблицаВыкупУСН.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				Иначе
					НоваяСтрока = ТаблицаПродажи.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					СуммаПродаж = СуммаПродаж + СтрокаТаблицы.Сумма;
				КонецЕсли;
			КонецЕсли;
		Иначе
			НоваяСтрока = ТаблицаПродажи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Если СтрокаТаблицы.ВидДвижения = "возврат"
				Или СтрокаТаблицы.ВидДвижения = "сторно продаж" Тогда
				СуммаВозвратов = СуммаВозвратов + СтрокаТаблицы.Сумма;
			Иначе
				СуммаПродаж = СуммаПродаж + СтрокаТаблицы.Сумма;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НастройкиСопоставления = Новый Структура;
	НастройкиСопоставления.Вставить("ОграничениеТипаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НастройкиСопоставления.Вставить("ОтключитьПоискПоСловарю",     Истина);
	НастройкиСопоставления.Вставить("ТочностьПоискаПоУмолчанию",   100);
	
	ДополнительныеПараметрыПоиска = Новый Структура;
	ДополнительныеПараметрыПоиска.Вставить("НатуральныеКлючи", "Артикул");
	ДополнительныеПараметрыПоиска.Вставить("Маркетплейс",      Перечисления.ВидыМаркетплейсов.Wildberries);
	НастройкиСопоставления.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметрыПоиска);
	
	НовыеКолонкиСопоставления = Новый Массив;
	ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
	ДополнительныйРеквизит.Имя = "КодМагазина";
	ДополнительныйРеквизит.Представление = "Код маркетплейса";
	НовыеКолонкиСопоставления.Добавить(ДополнительныйРеквизит);
	НастройкиСопоставления.Вставить("ДополнительныеРеквизитыСопоставления", НовыеКолонкиСопоставления);
	
	ВидимостьКолонокСопоставления = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ВидимостьКолонокСопоставления.Вставить("ЕдиницаИзмерения", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Упаковка", Ложь);
	ВидимостьКолонокСопоставления.Вставить("СтавкаНДС", Ложь);
	НастройкиСопоставления.Вставить("ВидимостьКолонокСопоставления", ВидимостьКолонокСопоставления);
	
	ДанныеФайлов = Новый Массив();
	
	Если ТаблицаПродажи.Количество() > 0 Тогда
		
		Описание = СтрШаблон(НСтр("ru='Отчет о продажах Wildberries от %1
			|Продажи: %2 Возвраты: %3'"), 
			Формат(Дата, "ДФ=dd.MM.yyyy"), 
			Формат(СуммаПродаж, "ЧДЦ=2; ЧН="), 
			Формат(СуммаВозвратов, "ЧДЦ=2; ЧН="));
			
		НомерВходящегоДокумента = ТаблицаПродажи[0].ИдентификаторОтчета;
			
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Плательщик",     Новый Структура("Ссылка", Контрагент));
		ДанныеДокумента.Вставить("Получатель",     Новый Структура("Ссылка", Организация));
		ДанныеДокумента.Вставить("Договор",        ДоговорКонтрагента);
		ДанныеДокумента.Вставить("ДатаНач",        ПараметрыОтчета.НачалоПериода);
		ДанныеДокумента.Вставить("Дата",           ПараметрыОтчета.ОкончаниеПериода);
		ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаПродажи);
		ДанныеДокумента.Вставить("ВидДокумента"  , ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBДетальный());
		ДанныеДокумента.Вставить("Номер"         , НомерВходящегоДокумента);
		ОбменСМаркетплейс.ПервичныеДанныеМаркетплейсаИзТаблицы(ДанныеДокумента, ТаблицаПродажи);
		
		Файл = Новый Структура;
		Файл.Вставить("ПолноеИмяФайла",          НомерВходящегоДокумента);
		Файл.Вставить("ДанныеДокумента",         ДанныеДокумента);
		Файл.Вставить("ВидДокумента",            ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBДетальный());
		Файл.Вставить("Маркетплейс",             Перечисления.ВидыМаркетплейсов.Wildberries);
		Файл.Вставить("Организация",             Организация);
		Файл.Вставить("Контрагент",              Контрагент);
		Файл.Вставить("ДоговорКонтрагента",      ДоговорКонтрагента);
		Файл.Вставить("Дата",                    Дата);
		Файл.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
		Файл.Вставить("СуммаВыкупа",             0);
		Файл.Вставить("СуммаПродаж",             СуммаПродаж);
		Файл.Вставить("СуммаВозвратов",          СуммаВозвратов);
		Файл.Вставить("СуммаСписания",           0);
		Файл.Вставить("Загружать",               СуммаПродаж > 0 ИЛИ СуммаВозвратов > 0);
		Файл.Вставить("Описание",                Описание);
		Файл.Вставить("ТаблицаНоменклатура",     ТаблицаПродажи);
		Файл.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставления);
		Файл.Вставить("СкорректированНаПродажиЮрлицам", Ложь);
		
		ДанныеФайлов.Добавить(Файл);
		
	КонецЕсли; 
	
	Если ТаблицаСписание.Количество() > 0 Тогда 
		
		Описание = СтрШаблон(НСтр("ru='Отчет о списании Wildberries от %1
			|Сумма компенсации: %2'"),
			Формат(Дата, "ДФ=dd.MM.yyyy"),
			Формат(ТаблицаСписание.Итог("Сумма"), "ЧДЦ=2; ЧН="));
			
		НомерВходящегоДокумента = ТаблицаСписание[0].ИдентификаторОтчета;
			
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Плательщик",     Новый Структура("Ссылка", Контрагент));
		ДанныеДокумента.Вставить("Получатель",     Новый Структура("Ссылка", Организация));
		ДанныеДокумента.Вставить("Договор",        ДоговорКонтрагента);
		ДанныеДокумента.Вставить("ДатаНач",        ПараметрыОтчета.НачалоПериода);
		ДанныеДокумента.Вставить("Дата",           ПараметрыОтчета.ОкончаниеПериода);
		ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаСписание);
		ДанныеДокумента.Вставить("ВидДокумента"  , ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBСписание());
		ДанныеДокумента.Вставить("Номер"         , НомерВходящегоДокумента);
		ОбменСМаркетплейс.ПервичныеДанныеМаркетплейсаИзТаблицы(ДанныеДокумента, ТаблицаСписание);
		
		Файл = Новый Структура;
		Файл.Вставить("ПолноеИмяФайла",          НомерВходящегоДокумента);
		Файл.Вставить("ДанныеДокумента",         ДанныеДокумента);
		Файл.Вставить("ВидДокумента",            ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBСписание());
		Файл.Вставить("Маркетплейс",             Перечисления.ВидыМаркетплейсов.Wildberries);
		Файл.Вставить("Организация",             Организация);
		Файл.Вставить("Контрагент",              Контрагент);
		Файл.Вставить("ДоговорКонтрагента",      ДоговорКонтрагента);
		Файл.Вставить("Дата",                    Дата);
		Файл.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
		Файл.Вставить("СуммаСписания",           ТаблицаСписание.Итог("Сумма"));
		Файл.Вставить("Загружать",               ТаблицаСписание.Итог("Сумма") > 0);
		Файл.Вставить("Описание",                Описание);
		Файл.Вставить("ТаблицаНоменклатура",     ТаблицаСписание);
		Файл.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставления);
		
		ДанныеФайлов.Добавить(Файл);
	
	КонецЕсли;
	
	Если ТаблицаОприходование.Количество() > 0 Тогда 
		
		Описание = СтрШаблон(НСтр("ru='Отчет о списании Wildberries от %1
			|Сумма компенсации: %2'"),
			Формат(Дата, "ДФ=dd.MM.yyyy"),
			Формат(ТаблицаОприходование.Итог("Сумма"), "ЧДЦ=2; ЧН="));
			
		НомерВходящегоДокумента = ТаблицаОприходование[0].ИдентификаторОтчета;
			
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Плательщик",     Новый Структура("Ссылка", Контрагент));
		ДанныеДокумента.Вставить("Получатель",     Новый Структура("Ссылка", Организация));
		ДанныеДокумента.Вставить("Договор",        ДоговорКонтрагента);
		ДанныеДокумента.Вставить("ДатаНач",        ПараметрыОтчета.НачалоПериода);
		ДанныеДокумента.Вставить("Дата",           ПараметрыОтчета.ОкончаниеПериода);
		ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаОприходование);
		ДанныеДокумента.Вставить("ВидДокумента"  , ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBОприходование());
		ДанныеДокумента.Вставить("Номер"         , НомерВходящегоДокумента);
		ОбменСМаркетплейс.ПервичныеДанныеМаркетплейсаИзТаблицы(ДанныеДокумента, ТаблицаОприходование);
		
		Файл = Новый Структура;
		Файл.Вставить("ПолноеИмяФайла",          НомерВходящегоДокумента);
		Файл.Вставить("ДанныеДокумента",         ДанныеДокумента);
		Файл.Вставить("ВидДокумента",            ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBОприходование());
		Файл.Вставить("Маркетплейс",             Перечисления.ВидыМаркетплейсов.Wildberries);
		Файл.Вставить("Организация",             Организация);
		Файл.Вставить("Контрагент",              Контрагент);
		Файл.Вставить("ДоговорКонтрагента",      ДоговорКонтрагента);
		Файл.Вставить("Дата",                    Дата);
		Файл.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
		Файл.Вставить("СуммаВозвратов",          ТаблицаОприходование.Итог("Сумма"));
		Файл.Вставить("Загружать",               ТаблицаОприходование.Итог("Сумма") > 0);
		Файл.Вставить("Описание",                Описание);
		Файл.Вставить("ТаблицаНоменклатура",     ТаблицаОприходование);
		Файл.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставления);
		
		ДанныеФайлов.Добавить(Файл);
	
	КонецЕсли;
	
	Если ТаблицаВыкуп.Количество() > 0 Тогда 
		
		Описание = СтрШаблон(НСтр("ru='Уведомление о выкупе Wildberries от %1
			|Сумма выкупа: %2'"),
			Формат(Дата, "ДФ=dd.MM.yyyy"),
			Формат(ТаблицаВыкуп.Итог("Сумма"), "ЧДЦ=2; ЧН="));
			
		НомерВходящегоДокумента = ТаблицаВыкуп[0].ИдентификаторОтчета;
			
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Плательщик",     Новый Структура("Ссылка", Контрагент));
		ДанныеДокумента.Вставить("Получатель",     Новый Структура("Ссылка", Организация));
		ДанныеДокумента.Вставить("Договор",        ДоговорКонтрагента);
		ДанныеДокумента.Вставить("ДатаНач",        ПараметрыОтчета.НачалоПериода);
		ДанныеДокумента.Вставить("Дата",           ПараметрыОтчета.ОкончаниеПериода);
		ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаВыкуп);
		ДанныеДокумента.Вставить("Ссылка"        , Неопределено);
		ДанныеДокумента.Вставить("Договор"       , Неопределено);
		ДанныеДокумента.Вставить("Номер"         , НомерВходящегоДокумента);
		ОбменСМаркетплейс.ПервичныеДанныеМаркетплейсаИзТаблицы(ДанныеДокумента, ТаблицаВыкуп);
		
		Файл = Новый Структура;
		Файл.Вставить("ПолноеИмяФайла",          НомерВходящегоДокумента);
		Файл.Вставить("ДанныеДокумента",         ДанныеДокумента);
		Файл.Вставить("ВидДокумента",            ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа());
		Файл.Вставить("Маркетплейс",             Перечисления.ВидыМаркетплейсов.Wildberries);
		Файл.Вставить("Организация",             Организация);
		Файл.Вставить("Контрагент",              Контрагент);
		Файл.Вставить("ДоговорКонтрагента",      Неопределено);
		Файл.Вставить("Дата",                    Дата);
		Файл.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
		Файл.Вставить("СуммаВыкупа",             ТаблицаВыкуп.Итог("Сумма"));
		Файл.Вставить("Загружать",               ТаблицаВыкуп.Итог("Сумма") > 0);
		Файл.Вставить("Описание",                Описание);
		Файл.Вставить("ТаблицаНоменклатура",     ТаблицаВыкуп);
		Файл.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставления);
		Файл.Вставить("ЗаполнятьДоговор",        Ложь);
		
		ДанныеФайлов.Добавить(Файл);

	КонецЕсли;

	Если ТаблицаВыкупУСН.Количество() > 0 Тогда 
		
		Описание = СтрШаблон(НСтр("ru='Уведомление о выкупе Wildberries от %1
			|Сумма выкупа: %2'"),
			Формат(Дата, "ДФ=dd.MM.yyyy"),
			Формат(ТаблицаВыкупУСН.Итог("Сумма"), "ЧДЦ=2; ЧН="));
		
		Комментарий = СтрШаблон(НСтр("ru='Уведомление о выкупе Wildberries от %1'"), Формат(Дата, "ДФ=dd.MM.yyyy"));
		
		НомерВходящегоДокумента = ТаблицаВыкупУСН[0].ИдентификаторОтчета;
		
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Плательщик",     Новый Структура("Ссылка", Контрагент));
		ДанныеДокумента.Вставить("Получатель",     Новый Структура("Ссылка", Организация));
		ДанныеДокумента.Вставить("Договор",        ДоговорКонтрагента);
		ДанныеДокумента.Вставить("ДатаНач",        ПараметрыОтчета.НачалоПериода);
		ДанныеДокумента.Вставить("Дата",           ПараметрыОтчета.ОкончаниеПериода);
		ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаВыкупУСН);
		ДанныеДокумента.Вставить("ВидДокумента"  , ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа());
		ДанныеДокумента.Вставить("Номер"         , НомерВходящегоДокумента);
		ОбменСМаркетплейс.ПервичныеДанныеМаркетплейсаИзТаблицы(ДанныеДокумента, ТаблицаВыкупУСН);
		
		Файл = Новый Структура;
		Файл.Вставить("ПолноеИмяФайла",          НомерВходящегоДокумента);
		Файл.Вставить("ДанныеДокумента",         ДанныеДокумента);
		Файл.Вставить("ВидДокумента",            ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBДетальный());
		Файл.Вставить("Маркетплейс",             Перечисления.ВидыМаркетплейсов.Wildberries);
		Файл.Вставить("Организация",             Организация);
		Файл.Вставить("Контрагент",              Контрагент);
		Файл.Вставить("ДоговорКонтрагента",      ДоговорКонтрагента);
		Файл.Вставить("Дата",                    Дата);
		Файл.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
		Файл.Вставить("СуммаВыкупа",             ТаблицаВыкупУСН.Итог("Сумма"));
		Файл.Вставить("Загружать",               ТаблицаВыкупУСН.Итог("Сумма") > 0);
		Файл.Вставить("Описание",                Описание);
		Файл.Вставить("ТаблицаНоменклатура",     ТаблицаВыкупУСН);
		Файл.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставления);
		Файл.Вставить("Комментарий",             Комментарий);
		Файл.Вставить("ЗаполнятьДоговор",        Ложь);
		
		ДанныеФайлов.Добавить(Файл);
	КонецЕсли;
	
	Возврат ДанныеФайлов;

КонецФункции

Функция РазбитьРеестрПродажЮридическимЛицамПоДокументам(ТаблицаТоваров) Экспорт
	
	Результат = Новый Массив;
	
	Реквизиты = ТаблицаТоваров.Скопировать(, "НомерДокумента, ДатаДокумента");
	Реквизиты.Свернуть("НомерДокумента, ДатаДокумента");
	
	Для Каждого Документ Из Реквизиты Цикл
		
		ОтборПоСтрокам = Новый Структура("НомерДокумента, ДатаДокумента", Документ.НомерДокумента, Документ.ДатаДокумента);
		ТаблицаТоваровПоДокументу = ТаблицаТоваров.Скопировать(ОтборПоСтрокам);
		Если ТаблицаТоваровПоДокументу.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Шапка = Новый Структура("ТипДокумента, НомерДокумента, ДатаДокумента, НомерСФ, ДатаСФ, НомерКоррСФ, ДатаКоррСФ,
			|ИННПокупателя, КПППокупателя, НаименованиеПокупателя, КраткоеНаименованиеПокупателя, Контрагент, ДоговорКонтрагента, Документ");
		ЗаполнитьЗначенияСвойств(Шапка, ТаблицаТоваровПоДокументу[0]);
		Шапка.КраткоеНаименованиеПокупателя = Справочники.Контрагенты.ПолучитьКраткоеНаименованиеКонтрагента(
			Шапка.НаименованиеПокупателя);
		
		Результат.Добавить(Новый Структура("Шапка, Товары", Шапка, ТаблицаТоваровПоДокументу));
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьКонтрагентаИДоговорПоРееструПродажЮридическимЛицам(ДанныеДокумента, Организация, ГруппаДляНовыхКонтрагентов, ОписаниеОшибки) Экспорт
	
	Шапка = ДанныеДокумента.Шапка;
	
	РеквизитыПокупателя = Новый Структура("Наименование, НаименованиеПолное, ИНН, КПП, ЮридическоеФизическоеЛицо",
		Шапка.КраткоеНаименованиеПокупателя,
		Шапка.НаименованиеПокупателя,
		Шапка.ИННПокупателя,
		Шапка.КПППокупателя,
		?(СтрДлина(Шапка.ИННПокупателя) = 12 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Шапка.ИННПокупателя),
			Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо,
			Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо));
	Контрагент = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(РеквизитыПокупателя);
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		РеквизитыПокупателя.Вставить("Родитель", ГруппаДляНовыхКонтрагентов);
		Попытка
			Контрагент = Справочники.Контрагенты.СоздатьКонтрагента(РеквизитыПокупателя);
		Исключение
			ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать контрагента: %1 (ИНН: %2),
				|по причине %2'"),
				Шапка.НаименованиеПокупателя,
				Шапка.ИННПокупателя,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
	КонецЕсли;
	Шапка.Контрагент = Контрагент;
	
	// Для возвратов договор берем из документа основания
	Если Не ЭтоДокументНаВозврат(Шапка.ТипДокумента) Тогда
		
		НаименованиеДоговора = СтрШаблон(НСтр("ru = '%1 от %2'"),
			СокрЛП(Шапка.НомерДокумента),
			Формат(Шапка.ДатаДокумента, "ДФ=dd.MM.yyyy"));
		
		РеквизитыДоговора = Новый Структура;
		РеквизитыДоговора.Вставить("Владелец", Контрагент);
		РеквизитыДоговора.Вставить("Организация", Организация);
		РеквизитыДоговора.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		РеквизитыДоговора.Вставить("Наименование", НаименованиеДоговора);
		РеквизитыДоговора.Вставить("Номер", СокрЛП(Шапка.НомерДокумента));
		РеквизитыДоговора.Вставить("Дата", Шапка.ДатаДокумента);
		
		ДополнительныеПараметры = Новый Структура("Наименование, Номер, Дата",
			Новый Структура("ЗначениеОтбора", НаименованиеДоговора),
			Новый Структура("ЗначениеОтбора", СокрЛП(Шапка.НомерДокумента)),
			Новый Структура("ЗначениеОтбора", Шапка.ДатаДокумента));
		
		ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ЕстьДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
			ДоговорКонтрагента,
			РеквизитыДоговора.Владелец,
			РеквизитыДоговора.Организация,
			РеквизитыДоговора.ВидДоговора,
			ДополнительныеПараметры);
		Если НЕ ЕстьДоговорКонтрагента Тогда
			ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", РеквизитыДоговора);
			ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
			Если ДоговорКонтрагента = Неопределено Тогда
				ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать договор с покупателем контрагента: %1 (ИНН: %2).'"),
					Шапка.НаименованиеПокупателя,
					Шапка.ИННПокупателя);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Шапка.ДоговорКонтрагента = ДоговорКонтрагента;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПреобразоватьДанныеФайлов(ДанныеФайлов) Экспорт
	
	ПродажиНаWB = Новый Массив;
	Для Каждого ДанныеФайла Из ДанныеФайлов Цикл
		Если ДанныеФайла.Свойство("ВидДокумента")
			И ДанныеФайла.Свойство("СкорректированНаПродажиЮрлицам")
			И ДанныеФайла.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетWBДетальный()
			И Не ДанныеФайла.СкорректированНаПродажиЮрлицам Тогда
				ПродажиНаWB.Добавить(ДанныеФайла);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ДанныеФайла Из ДанныеФайлов Цикл
		Если ДанныеФайла.Свойство("ВидДокумента") Тогда
			Если ДанныеФайла.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB()
				И ПродажиНаWB.Количество() > 0 Тогда
				Для Каждого Продажа Из ПродажиНаWB Цикл
					Если Продажа.Организация = ДанныеФайла.Организация
						И Продажа.Дата = ДанныеФайла.Дата Тогда
						УбратьПродажиЮрлицамИзОтчетаWBДетальный(Продажа);
						ПродажиНаWB.Удалить(ПродажиНаWB.Найти(Продажа));
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ОприходованиеТовараМаркетплейсом(ВидДвижения, ТипДокумента) Экспорт
	
	ЭтоСписание = (Нрег(ВидДвижения) = "оплата потерянного товара" 
					ИЛИ Нрег(ВидДвижения) = "авансовая оплата за товар без движения" 
					ИЛИ Нрег(ВидДвижения) = "частичная компенсация брака"
					ИЛИ Нрег(ВидДвижения) = "компенсация потерянного товара"
					ИЛИ Нрег(ВидДвижения) = "компенсация подмененного товара"
					ИЛИ Нрег(ВидДвижения) = "компенсация ущерба"
					ИЛИ Нрег(ВидДвижения) = "добровольная компенсация при возврате"
					ИЛИ Нрег(ВидДвижения) = "компенсация брака");
					
	ЭтоСторно   = (Нрег(ТипДокумента) = "возврат");
	
	Возврат ЭтоСписание И ЭтоСторно;
	
КонецФункции

#КонецОбласти

#Область РаботаСТоварами

Функция ПолучитьОстаткиТоваров(НастройкиИнтеграции, Отбор, Отказ) Экспорт
	ТаблицаОстатковТоваров = ОбменСМаркетплейс.НовыйТаблицаОстатковТоваров();
	Если НЕ ЗначениеЗаполнено(Отбор) Тогда
		Возврат ТаблицаОстатковТоваров;
	КонецЕсли;
	
	НастройкиПодключения = НовыйНастройкиПодключения();
	НастройкиПодключения.Токен = ПрочитатьНастройкиАвторизации(НастройкиИнтеграции);
	НастройкиПодключения.АдресСервера = АдресСервераМаркетплейсWilberries();
	//Логирование
	Если РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЛогированиеВключено() ТОгда
		НастройкиПодключения.Вставить("Логирование", РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЛогирования(НастройкиИнтеграции));
	КонецЕсли;
	//Конец Логирование
	ИдентификаторСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкиИнтеграции, "ИдентификаторСклада");
	Если НЕ ЗначениеЗаполнено(ИдентификаторСклада) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("skus", Новый Массив);
	
	АдресРесурса = "/api/v3/stocks/"+Формат(ИдентификаторСклада, "ЧГ=");
	
	ВыполнятьСЗадержкой = Ложь;
	
	ПорцииТоваров = ОбменСМаркетплейс.ПодготовитьПорцииДляОбмена(Отбор, 1000);
	Для каждого Порция Из ПорцииТоваров Цикл
		ПараметрыЗапроса.skus.Очистить();
		
		Для каждого СтрокаТовары Из Порция Цикл
			ПараметрыЗапроса.skus.Добавить(СтрокаТовары.Штрихкод);
		КонецЦикла;
		
		РезультатЗапроса = ВыполнитьPOSTЗапрос(АдресРесурса, ПараметрыЗапроса, НастройкиПодключения);
		Если НЕ РезультатЗапроса.Результат 
			И РезультатЗапроса.ВыполнятьСЗадержкой 
			И НЕ ВыполнятьСЗадержкой тогда
			
			ВыполнятьСЗадержкой = Истина;
		ИначеЕсли НЕ РезультатЗапроса.Результат Тогда
			Отказ = Истина;
			Возврат Неопределено;
		КонецЕсли;
		
		Если ВыполнятьСЗадержкой Тогда
			Приостановить(61);
		КонецЕсли;
		
		ЗаполнитьТаблицуТовары(ТаблицаОстатковТоваров, ИдентификаторСклада, РезультатЗапроса.Данные);
	КонецЦикла;
	
	Возврат ТаблицаОстатковТоваров;
КонецФункции

Процедура ПередатьОстаткиТоваров(НастройкаИнтеграции, ТаблицаТоваров, СообщениеОбОшибке, Отказ, КодОтветаСервера = 0) Экспорт
	ИдентификаторСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ИдентификаторСклада");
	Если НЕ ЗначениеЗаполнено(ИдентификаторСклада) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НастройкиПодключения = НовыйНастройкиПодключения();
	НастройкиПодключения.Токен = ПрочитатьНастройкиАвторизации(НастройкаИнтеграции);
	НастройкиПодключения.АдресСервера = АдресСервераМаркетплейсWilberries();
	//Логирование
	Если РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЛогированиеВключено() Тогда
		НастройкиПодключения.Вставить("Логирование", РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЛогирования(НастройкаИнтеграции));
	КонецЕсли;
	//Конец Логирование
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("stocks", Новый Массив);
	
	АдресРесурса = "/api/v3/stocks/"+Формат(ИдентификаторСклада, "ЧГ=");
	
	ПорцииДляОбмена = ОбменСМаркетплейс.ПодготовитьПорцииДляОбмена(ТаблицаТоваров, 1000);
	
	ВыполнятьСЗадержкой = Ложь;
	Для каждого Порция Из ПорцииДляОбмена Цикл
		ПараметрыЗапроса.stocks.Очистить();
		Для каждого СтрокаТаблицыТовары Из Порция Цикл
			ОстатокТовара = Новый Структура;
			ОстатокТовара.Вставить("sku", СтрокаТаблицыТовары.Штрихкод);
			ОстатокТовара.Вставить("amount", СтрокаТаблицыТовары.Остаток);
			ПараметрыЗапроса.stocks.Добавить(ОстатокТовара);
		КонецЦикла;
		
		РезультатЗапроса = ВыполнитьPUTЗапрос(АдресРесурса, ПараметрыЗапроса, НастройкиПодключения);
		Если НЕ РезультатЗапроса.Результат 
			И РезультатЗапроса.ВыполнятьСЗадержкой 
			И НЕ ВыполнятьСЗадержкой тогда
			
			ВыполнятьСЗадержкой = Истина;
		ИначеЕсли НЕ РезультатЗапроса.Результат Тогда
			СообщениеОбОшибке = РезультатЗапроса.СообщениеОбОшибке;
			Отказ = Истина;
			Прервать;
		Иначе
			ОбменСМаркетплейс.ОбновитьДанныеОбОстатках(НастройкаИнтеграции, Порция);
		КонецЕсли;
		
		Если ВыполнятьСЗадержкой Тогда
			Приостановить(61);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьТоварыИзСервиса(НастройкаИнтеграции, СообщениеОбОшибке)
	// Размер получаемой порции, определяется wb
	РазмерПорции = 100;
	
	НастройкиПодключения = НовыйНастройкиПодключения();
	НастройкиПодключения.АдресСервера = АдресСервераКонтентWildberries();
	НастройкиПодключения.Токен = ПрочитатьНастройкиАвторизации(НастройкаИнтеграции);
	
	//Логирование
	Если РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЛогированиеВключено() Тогда
		НастройкиПодключения.Вставить("Логирование", РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЛогирования(НастройкаИнтеграции));
	КонецЕсли;
	//Конец Логирование
	
	Параметры = Новый Структура;
	Параметры.Вставить("cursor", Новый Структура("limit", РазмерПорции));
	Параметры.Вставить("filter", Новый Структура("withPhoto", -1));
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("settings", Параметры);
	
	КарточкиТоваров = Новый Массив;
	Пока Истина Цикл
		РезультатЗапроса = ВыполнитьPOSTЗапрос("/content/v2/get/cards/list?locale=ru", ПараметрыЗапроса, НастройкиПодключения);
		Если НЕ РезультатЗапроса.Результат Тогда
			СообщениеОбОшибке = РезультатЗапроса.СообщениеОбОшибке;
			Возврат Неопределено;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КарточкиТоваров, РезультатЗапроса.Данные.cards);
		
		Пагинатор = РезультатЗапроса.Данные.cursor;
		Если Пагинатор.total < РазмерПорции Тогда
			Прервать;
		КонецЕсли;
		ПараметрыЗапроса.settings.Вставить("cursor", Пагинатор);
	КонецЦикла;
	
	Возврат КарточкиТоваров;
КонецФункции

Функция СписокТоваровМаркетплейс(НастройкиИнтеграции, СообщениеОбОшибке, Отказ) Экспорт
	СписокТоваровСервиса = ПолучитьТоварыИзСервиса(НастройкиИнтеграции, СообщениеОбОшибке); 
	Если ЗначениеЗаполнено(СообщениеОбОшибке) тогда
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
	
	Контрагент = Справочники.НастройкиИнтеграцииМаркетплейс.КонтрагентДоговорНаДату(НастройкиИнтеграции, ТекущаяДатаСеанса()).Контрагент;
	Владелец = СопоставлениеНоменклатурыКонтрагентовСлужебный.ВладелецНоменклатурыКонтрагента(Контрагент);
	
	СписокТоваров = Новый Массив;
	Для Каждого ИнформацияОТоваре Из СписокТоваровСервиса Цикл
		КодМагазина = Формат(ИнформацияОТоваре.nmID, "ЧГ=");
		
		Для каждого Размер Из ИнформацияОТоваре.sizes Цикл
			Для каждого Штрихкод Из Размер.skus Цикл // У wildberries штрихкод является ключевым полем
				НоменклатураМаркетплейса = ОбменСМаркетплейс.НовыйНоменклатураМаркетплейса(Перечисления.ВидыМаркетплейсов.Wildberries, Владелец);
				НоменклатураМаркетплейса.КодМагазина = КодМагазина;
				НоменклатураМаркетплейса.Наименование = СтрШаблон("%1/%2/%3",
					ИнформацияОТоваре.subjectname,
					ИнформацияОТоваре.brand,
					КодМагазина);
				НоменклатураМаркетплейса.Артикул = ИнформацияОТоваре.VendorCode;
				НоменклатураМаркетплейса.Штрихкоды.Добавить(Штрихкод);
				
				ОбменСМаркетплейс.ДобавитьНоменклатуруВСписок(СписокТоваров, НоменклатураМаркетплейса);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СписокТоваров;
КонецФункции

Процедура ЗаполнитьТаблицуТовары(ТаблицаОстатковТоваров, ИдентификаторСклада, РезультатЧтения)
	ДанныеМаркетплейса = Новый ТаблицаЗначений;
	ДанныеМаркетплейса.Колонки.Добавить("Sku",    ОбщегоНазначения.ОписаниеТипаСтрока(36));
	ДанныеМаркетплейса.Колонки.Добавить("amount", ОбщегоНазначения.ОписаниеТипаЧисло(6,0));
	
	Для каждого ЭлементРезультата Из РезультатЧтения["stocks"] Цикл
		ЗаполнитьЗначенияСвойств(ДанныеМаркетплейса.Добавить(), ЭлементРезультата);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеМаркетплейса.sku КАК Штрихкод,
	|	ДанныеМаркетплейса.amount КАК Остаток
	|ПОМЕСТИТЬ ВТ_ДанныеМаркетплейса
	|ИЗ
	|	&ДанныеМаркетплейса КАК ДанныеМаркетплейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	&ИдентификаторСклада КАК ИдентификаторСклада,
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод,
	|	ВТ_ДанныеМаркетплейса.Остаток КАК Остаток
	|ИЗ
	|	ВТ_ДанныеМаркетплейса КАК ВТ_ДанныеМаркетплейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ПО ВТ_ДанныеМаркетплейса.Штрихкод = НоменклатураМаркетплейсов.Штрихкод
	|ГДЕ
	|	НЕ НоменклатураМаркетплейсов.Штрихкод ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ДанныеМаркетплейса",  ДанныеМаркетплейса);
	Запрос.УстановитьПараметр("ИдентификаторСклада", ИдентификаторСклада);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОстатковТоваров.Добавить(), Выборка);
	КонецЦикла;
КонецПроцедуры

Функция ПодготовитьФайлОстатков(НастройкаИнтеграции, ТаблицаОстатки) Экспорт
	СтруктураФайла = Новый Структура;
	СтруктураФайла.Вставить("ИмяФайла", "stocks.xlsx");
	СтруктураФайла.Вставить("Страницы", Новый Соответствие);
	
	Макет = РегистрыСведений.ОстаткиТоваровМаркетплейсов.ПолучитьМакет("ФормаОстатковWB");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Вывести(Макет.ПолучитьОбласть("ОстаткиНаСкладе_Шапка"));
	
	ОбластьОстатки = Макет.ПолучитьОбласть("ОстаткиНаСкладе_Строка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОстатки", ТаблицаОстатки);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОстатки.Остаток КАК Остаток,
	|	ТаблицаОстатки.ИдентификаторТовара КАК ИдентификаторТовара
	|ПОМЕСТИТЬ ВТ_ОстаткиТоваров
	|ИЗ
	|	&ТаблицаОстатки КАК ТаблицаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод,
	|	ВТ_ОстаткиТоваров.Остаток КАК Остаток
	|ИЗ
	|	ВТ_ОстаткиТоваров КАК ВТ_ОстаткиТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ПО ВТ_ОстаткиТоваров.ИдентификаторТовара = НоменклатураМаркетплейсов.ИдентификаторТовара";
	
	ВыборкаРезультатов = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаРезультатов.Следующий() Цикл
		ОбластьОстатки.Параметры.Заполнить(ВыборкаРезультатов);
		
		ТабличныйДокумент.Вывести(ОбластьОстатки);
	КонецЦикла;
	
	СтруктураФайла.Страницы.Вставить("Остатки", ПоместитьВоВременноеХранилище(ТабличныйДокумент));
	
	Возврат СтруктураФайла;
КонецФункции


#КонецОбласти

#Область РаботаСоСкладами

Функция СписокСкладов(НастройкаИнтеграции, Отказ) Экспорт
	НастройкиПодключения = НовыйНастройкиПодключения();
	НастройкиПодключения.Токен = ПрочитатьНастройкиАвторизации(НастройкаИнтеграции);
	НастройкиПодключения.АдресСервера = АдресСервераМаркетплейсWilberries();
    //Логирование
	Если РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЛогированиеВключено() Тогда
		НастройкиПодключения.Вставить("Логирование", РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЛогирования(НастройкаИнтеграции));
	КонецЕсли;
	//КонецЛогирование
	РезультатЗапроса = ВыполнитьGETЗапрос("/api/v3/warehouses", НастройкиПодключения);
	Если НЕ РезультатЗапроса.Результат тогда
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
	
	СписокСкладов = Новый СписокЗначений;
	Для каждого СтрокаСклад Из РезультатЗапроса.Данные Цикл
		СписокСкладов.Добавить(Формат(СтрокаСклад.id, "ЧГ="), СтрокаСклад.name);
	КонецЦикла;
	
	Возврат СписокСкладов;
	
КонецФункции

#КонецОбласти

#Область ОтчетОПродажах

Функция ПолучитьДанныеДокумента(ПараметрыОтчета, КодОтветаСервера, СообщениеОбОшибке) Экспорт
	РезультатЧтения = ПрочитатьОтчет(ПараметрыОтчета, КодОтветаСервера, СообщениеОбОшибке);
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатЧтения) Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось разобрать ответ от маркетплейс. Попробуйте загрузить отчет позже'");
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.Wildberries);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПреобразоватьДанныеПрочитанногоОтчета(ПараметрыОтчета, РезультатЧтения);
КонецФункции

Процедура ДополнитьТаблицуТоваровWildberries(Источник, ТаблицаТоваров)
	
	Для Каждого СтрокаОтчета Из Источник Цикл
		
		Сумма        = СтрокаОтчета.retail_amount; 
		ВидДвижения  = НРег(СтрокаОтчета.supplier_oper_name);
		ТипДокумента = НРег(СтрокаОтчета.doc_type_name);
		КодСтраны    = СтрокаОтчета.site_country;
		
		Если ПродажаЧерезВыкупТовара(КодСтраны, КодСтраны)
			Или ПродажаТовараМаркетплейсом(ВидДвижения, ТипДокумента)
			Или ВозвратТовараМаркетплейсом(ВидДвижения, ТипДокумента) Тогда
		ИначеЕсли ВидДвижения = "компенсация подмененного товара" Тогда
			Сумма = СтрокаОтчета.ppvz_for_pay;
		ИначеЕсли СписаниеТовараМаркетплейсом(ВидДвижения, ТипДокумента)
			ИЛИ ОприходованиеТовараМаркетплейсом(ВидДвижения, ТипДокумента) Тогда
			Сумма = СтрокаОтчета.ppvz_for_pay;
		Иначе
			Продолжить;
		КонецЕсли;
		
		ИдентификаторТовара = Формат(СтрокаОтчета.nm_id, "ЧГ=0");
		
		//По таким позициям, даже при наличии ШК, сопоставление невозможно
		Если Не ЗначениеЗаполнено(СтрокаОтчета.subject_name)
			И Не ЗначениеЗаполнено(СтрокаОтчета.brand_name)
			И Не ЗначениеЗаполнено(ИдентификаторТовара) Тогда
			Продолжить;
		КонецЕсли;
		
		Артикул = СтрокаОтчета.sa_name;
		ЛеваяЧасть = Лев(Артикул, Стрдлина(Артикул)/2);
		ПраваяЧасть = Прав(Артикул, Стрдлина(Артикул)/2);
		Артикул = ?(ЛеваяЧасть = ПраваяЧасть, ЛеваяЧасть, Артикул);
		
		НаименованиеТовара = СтрШаблон("%1/%2/%3",
			СтрокаОтчета.subject_name,
			СтрокаОтчета.brand_name,
			ИдентификаторТовара);
		
		Если ЗначениеЗаполнено(СтрокаОтчета.barcode) Тогда
			ИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(ИдентификаторТовара + "#" + СтрокаОтчета.barcode);
		Иначе
			ИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(ИдентификаторТовара);
		КонецЕсли;
		
		НоваяСтрокаТовара = ТаблицаТоваров.Добавить();
		НоваяСтрокаТовара.ИД                  = ИД;
		НоваяСтрокаТовара.НаимТов             = НаименованиеТовара;
		НоваяСтрокаТовара.Артикул             = Артикул;
		НоваяСтрокаТовара.ВидДвижения         = ВидДвижения;
		НоваяСтрокаТовара.ТипДокумента        = СтрокаОтчета.doc_type_name;
		НоваяСтрокаТовара.ИдентификаторОтчета = Формат(СтрокаОтчета.realizationreport_id, "ЧГ=0");
		НоваяСтрокаТовара.ТипОтчета           = СтрокаОтчета.report_type;
		НоваяСтрокаТовара.Количество          = СтрокаОтчета.quantity;
		НоваяСтрокаТовара.Сумма               = Сумма;
		НоваяСтрокаТовара.КодСтраны           = КодСтраны;
		НоваяСтрокаТовара.НазваниеСтраны      = КодСтраны;
		НоваяСтрокаТовара.ШтрихКод            = СтрокаОтчета.barcode;
		НоваяСтрокаТовара.КодМагазина         = ИдентификаторТовара;
		НоваяСтрокаТовара.ДанныеМаркетплейса  = СтрокаОтчета;
		
		Если ВидДвижения = "Возврат" Тогда
			//WB: Литера Z в конце строки означает часовой пояс UTC. При ее отсутствии время считается в часовом поясе МСК (UTC+3)
			//Приведем все часовые пояса к началу дня
			НоваяСтрокаТовара.ДатаПродажи = НачалоДня(ПрочитатьДатуJSON(СтрокаОтчета.sale_dt, ФорматДатыJSON.ISO));
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция АдресЗапросаСтатистикиWildberries(ВерсияМетодаAPI)
	
	Возврат СтрШаблон("/api/%1/supplier/reportDetailByPeriod", ВерсияМетодаAPI);
	
КонецФункции

Функция НомерОтчетаИзИмениФайла(ПолноеИмяФайла) Экспорт
	
	СтрокаЦифровыхСимволов = "0123456789";
	
	Номер = "";
	//Исключим символы до номера
	Для Индекс = 1 По СтрДлина(ПолноеИмяФайла) Цикл
		Символ = Сред(ПолноеИмяФайла, Индекс, 1);
		Если СтрНайти(СтрокаЦифровыхСимволов, Символ) > 0 Тогда
			Номер = Сред(ПолноеИмяФайла, Индекс);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Результат = "";
	//Отбросим символы после номера
	Для Индекс = 1 По СтрДлина(Номер) Цикл
		Символ = Сред(Номер, Индекс, 1);
		Если СтрНайти(СтрокаЦифровыхСимволов, Символ) = 0 Тогда
			Прервать;
		КонецЕсли;
		Результат = Результат + Символ;
	КонецЦикла;
	
	Если Результат = "0" Тогда
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ЗапросОтчетОпродажахWildberries(ПараметрыЗапроса, СообщениеОбОшибке, Логирование = Неопределено)
	
	НастройкиПодключения = ПараметрыЗапроса.НастройкиПодключения;
	
	АдресЗапроса = АдресЗапросаСтатистикиWildberries(ПараметрыЗапроса.ВерсияМетодаAPI);
	
	СписокПараметровМетода = Новый Структура;
	СписокПараметровМетода.Вставить("dateFrom", Формат(ПараметрыЗапроса.НачалоПериода,"ДФ=гггг-ММ-дд"));
	СписокПараметровМетода.Вставить("dateTo", Формат(ПараметрыЗапроса.ОкончаниеПериода,"ДФ=гггг-ММ-дд"));
	СписокПараметровМетода.Вставить("limit", Формат(ПараметрыЗапроса.ПределВыборкиОдногоЗапроса, "ЧГ=0"));
	СписокПараметровМетода.Вставить("rrdid", Формат(ПараметрыЗапроса.НачалоВыборки, "ЧН=0; ЧГ=0"));
	
	ПараметрыСтрокой = Новый Массив;
	Для Каждого Параметр Из СписокПараметровМетода Цикл
		ПараметрыСтрокой.Добавить(Параметр.Ключ+"="+Параметр.Значение);
	КонецЦикла;
	
	МетодЗапроса = АдресЗапроса+ "?" + СтрСоединить(ПараметрыСтрокой,"&");
	
	АдресСервера = АдресСервераСтатистикиWildberries();
	
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	HTTPСоединениеВайлдберризАпиСтатистики = Новый HTTPСоединение(
		АдресСервера,
		, , ,
		ИнтернетПрокси,
		,
		ЗащищенноеСоединение);
	
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json;charset=UTF-8");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", НастройкиПодключения.Токен);
	
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	
	Попытка
		HTTPОтвет = HTTPСоединениеВайлдберризАпиСтатистики.Получить(HTTPЗапрос); // GET
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.Wildberries);
		ВызватьИсключение;
	КонецПопытки;
	
	//Логирование
	Если Не Логирование = Неопределено Тогда 
		Если HTTPСоединениеВайлдберризАпиСтатистики  = Неопределено Тогда
			Логирование.АдресСервера 	= АдресСервера;
		Иначе
			Логирование.АдресСервера 	= HTTPСоединениеВайлдберризАпиСтатистики.Сервер +":"+HTTPСоединениеВайлдберризАпиСтатистики.Порт;
			Логирование.Соединение 		= Истина;
			Логирование.Метод 			= "GET";
			Логирование.Запрос 			= HTTPЗапрос;
			Логирование.Ответ 			= HTTPОтвет;
		КонецЕсли;
		РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЗаписатьВЖурнал(Логирование);
	КонецЕсли;
	//Конец Логирование

	Возврат HTTPОтвет;
	
КонецФункции

Функция НовыйПараметрыЗапросаОтчетОпродажахWildberries() Экспорт
	
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("НастройкиПодключения");
	ПараметрыЗапроса.Вставить("ВерсияМетодаAPI","");
	ПараметрыЗапроса.Вставить("НачалоПериода", Дата(1,1,1));
	ПараметрыЗапроса.Вставить("ОкончаниеПериода", Дата(1,1,1));
	ПараметрыЗапроса.Вставить("ПределВыборкиОдногоЗапроса", 0);
	ПараметрыЗапроса.Вставить("НачалоВыборки", 0);
	
	Возврат ПараметрыЗапроса;

КонецФункции

// Приостанавливает работу программы на указанное количество секунд.
//
Процедура Приостановить(Секунд)
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		
		ПараметрыЗапускаПрограммы = ФайловаяСистема.ПараметрыЗапускаПрограммы();
		ПараметрыЗапускаПрограммы.ДождатьсяЗавершения = Истина;
		ФайловаяСистема.ЗапуститьПрограмму("timeout " + Секунд, ПараметрыЗапускаПрограммы);
		
	Иначе
		
		МодульОбщегоНазначенияБТС = ОбщегоНазначения.ОбщийМодуль("ОбщегоНазначенияБТС");
		МодульОбщегоНазначенияБТС.Пауза(Секунд);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РеестрПродажЮридическимЛицам

Процедура УбратьПродажиЮрлицамИзОтчетаWBДетальный(ДанныеФайла)
	
	ТаблицаТоваров = ДанныеФайла.ДанныеДокумента.ТаблицаТоваров;
	ТаблицаТоваров = ТаблицаТоваров.Скопировать(Новый Структура("ПродажаЮрлицу", "Нет"));
	ДанныеФайла.ДанныеДокумента.ТаблицаТоваров = ТаблицаТоваров;
	ДанныеФайла.ДанныеДокумента.ПервичныеДанныеМаркетплейса = ОбменСМаркетплейс.ОтобратьПервичныеДанныеМаркетплейса(
		ДанныеФайла.ДанныеДокумента.ПервичныеДанныеМаркетплейса, "признакпродажиюридическомулицу", "Нет");
	
	СуммаВыкупа = 0;
	СуммаПродаж = 0;
	СуммаВозвратов = 0;
	СуммаСписания = 0;
	
	Для Каждого СтрокаТовара Из ДанныеФайла.ДанныеДокумента.ТаблицаТоваров Цикл
		Если ПродажаТовараМаркетплейсом(СтрокаТовара.ВидДвижения, СтрокаТовара.ТипДокумента) Тогда
			Если ПродажаЧерезВыкупТовара(СтрокаТовара.КодСтраны, СтрокаТовара.НазваниеСтраны) Тогда
				СуммаВыкупа = СуммаВыкупа + СтрокаТовара.Сумма;
			Иначе
				СуммаПродаж = СуммаПродаж + СтрокаТовара.Сумма;
			КонецЕсли;
		ИначеЕсли ВозвратТовараМаркетплейсом(СтрокаТовара.ВидДвижения,СтрокаТовара.ТипДокумента) Тогда
			СуммаВозвратов = СуммаВозвратов + СтрокаТовара.Сумма;
		ИначеЕсли СписаниеТовараМаркетплейсом(СтрокаТовара.ВидДвижения, СтрокаТовара.ТипДокумента) Тогда
			Если СтрокаТовара.Сумма = 0 Тогда
				СтрокаТовара.Сумма = СтрокаТовара.КПеречислению;
			КонецЕсли;
			СуммаСписания = СуммаСписания + СтрокаТовара.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	Если СуммаВыкупа > 0 Тогда
		ОписаниеВыкупа = СтрШаблон(НСтр("ru=' Выкупы: %1'"), Формат(СуммаВыкупа, "ЧДЦ=2; ЧН="));
	Иначе 
		ОписаниеВыкупа = "";
	КонецЕсли;
	
	Описание = СтрШаблон(НСтр("ru='Отчет о продажах Wildberries от %1
		|Продажи: %2 Возвраты: %3 %4
		|Скорректирован на сумму продаж юрлицам'"),
		Формат(ДанныеФайла.ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy"),
		Формат(СуммаПродаж, "ЧДЦ=2; ЧН="),
		Формат(СуммаВозвратов, "ЧДЦ=2; ЧН="),
		ОписаниеВыкупа);
	
	ДанныеФайла.Вставить("СуммаВыкупа",    СуммаВыкупа);
	ДанныеФайла.Вставить("СуммаПродаж",    СуммаПродаж);
	ДанныеФайла.Вставить("СуммаВозвратов", СуммаВозвратов);
	ДанныеФайла.Вставить("СуммаСписания",  СуммаСписания);
	ДанныеФайла.Вставить("Загружать",      СуммаПродаж > 0 ИЛИ СуммаВозвратов > 0);
	ДанныеФайла.Вставить("Описание",       Описание);
	ДанныеФайла.Вставить("СкорректированНаПродажиЮрлицам", Истина);
	
КонецПроцедуры

Функция ЭтоДокументНаВозврат(ТипДокумента)
	
	Возврат Врег(Лев(СокрЛП(ТипДокумента), 3)) = "УКД";
	
КонецФункции

#КонецОбласти

#Область АвтозагрузкаОтчетовМаркетплейсов

Функция ПериодыАвтозагрузки(Интеграция, ПериодыАвтозагрузки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Интеграция.Организация);
	Запрос.УстановитьПараметр("Контрагент", Интеграция.Контрагент);
	Запрос.УстановитьПараметр("ПериодыАвтозагрузки", ПериодыАвтозагрузки);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, НЕДЕЛЯ)), ДАТАВРЕМЯ(1, 1, 1)) КАК МаксимальныйПериод
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером)
	|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, НЕДЕЛЯ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах)
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, НЕДЕЛЯ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера)
	|	И РеализацияТоваровУслуг.Комиссионер = &Контрагент
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, НЕДЕЛЯ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, НЕДЕЛЯ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|	И ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ТоварыНаСкладКомиссионера)
	|	И ВозвратТоваровОтПокупателя.Комиссионер = &Контрагент
	|	И ВозвратТоваровОтПокупателя.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, НЕДЕЛЯ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОСписании)
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОбОприходовании)
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, НЕДЕЛЯ) В (&ПериодыАвтозагрузки)";
	
	Результат = Новый Массив;
	
	МаксимальныйПериод = Дата(1, 1, 1);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МаксимальныйПериод = Макс(МаксимальныйПериод, Выборка.МаксимальныйПериод);
	КонецЦикла;
	
	Для Каждого ПериодАвтозагрузки Из ПериодыАвтозагрузки Цикл
		Если ПериодАвтозагрузки > МаксимальныйПериод Тогда
			Результат.Добавить(Новый Структура("НачалоПериода, ОкончаниеПериода",
				НачалоНедели(ПериодАвтозагрузки), КонецНедели(ПериодАвтозагрузки)));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция АвтозагрузкаДокументов(Интеграция, ДанныеМаркеплейса) Экспорт
	
	Ошибки = Новый Массив;
	СписокЗагрузки = Новый Массив;
	Для Каждого ДанныеФайла Из ДанныеМаркеплейса Цикл
		ПодготовитьДокументыКСозданию(СписокЗагрузки, ДанныеФайла, Интеграция, Ошибки);
		Если ЗначениеЗаполнено(Ошибки) Тогда
			Для Каждого ОписаниеОшибки Из Ошибки Цикл
				ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ОписаниеОшибки, Интеграция.Маркетплейс);
			КонецЦикла;
			Возврат Новый Массив;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоздатьДокументы(СписокЗагрузки);
	
КонецФункции

#КонецОбласти

#Область СозданиеДокументов

Функция СоздатьДокументы(ДанныеДокументов) Экспорт
	
	СозданныеДокументы = Новый Массив;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	ЭлектронноеВзаимодействиеБП.СоздатьДокументыИзСтруктуры(ДанныеДокументов, АдресХранилища);
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	Для Каждого СтрокаРезультата Из Результат Цикл
		Если СтрокаРезультата.Свойство("СозданДокумент") Тогда
			СозданныеДокументы.Добавить(СтрокаРезультата.СозданДокумент);
		ИначеЕсли СтрокаРезультата.Свойство("ТекстОшибки") Тогда
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СтрокаРезультата.ТекстОшибки, Перечисления.ВидыМаркетплейсов.Wildberries);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СозданныеДокументы;
	
КонецФункции

Процедура ПодготовитьДокументыКСозданию(СписокЗагрузки, ДанныеМаркеплейса, ПараметрыИнтеграции, Ошибки) Экспорт
	
	Если ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа() Тогда
		СворачиваемыеКолонки = "Наименование,Артикул,ИД,НатуральныйИД,СтавкаНДС,ШтрихКод,КодМагазина";
		СуммируемыеКолонки = "Количество,Сумма,СуммаНДС";
	ИначеЕсли ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB() Тогда
		СворачиваемыеКолонки = "Артикул,ДатаДокумента,ДатаКоррСФ,ДатаСФ,ИД,ИННПокупателя,КПППокупателя,КодМагазина,
			|Наименование,НаименованиеПокупателя,НатуральныйИД,НомерДокумента,НомерКоррСФ,НомерСФ,СтавкаНДС,
			|ТипДокумента,Цена,Штрихкод";
		СуммируемыеКолонки = "Количество,Сумма,СуммаБезНДС,СуммаНДС";
	Иначе
		СворачиваемыеКолонки = "Наименование,Артикул,ИД,НатуральныйИД,ВидДвижения,ТипДокумента,КодСтраны,НазваниеСтраны,
			|ШтрихКод,КодМагазина,ДатаПродажи";
		СуммируемыеКолонки = "Количество,Сумма,КоличествоВозврат,СуммаВозврат";
	КонецЕсли;
	
	Если ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB() Тогда
		Для Каждого ПодчиненныйДокумент Из ДанныеМаркеплейса.ДанныеДокумента.ПодчиненныеДокументы Цикл
			Шапка = ПодчиненныйДокумент.Шапка;
			
			ОписаниеОшибки = "";
			СоздатьКонтрагентаИДоговорПоРееструПродажЮридическимЛицам(
				ПодчиненныйДокумент,
				ПараметрыИнтеграции.Организация,
				ПараметрыИнтеграции.ГруппаДляНовыхКонтрагентов,
				ОписаниеОшибки);
			Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				Ошибки.Добавить(ОписаниеОшибки);
			КонецЕсли;
			
			Если ПараметрыИнтеграции.СпособСверткиОтчетов = "Сворачивать" Тогда
				ПодчиненныйДокумент.Товары.Свернуть(СворачиваемыеКолонки, СуммируемыеКолонки);
			КонецЕсли;
			
			ОписаниеРодителя = ДанныеМаркеплейса.Описание;
			Если ЗначениеЗаполнено(ОписаниеРодителя) Тогда
				МассивПодстрок = СтрРазделить(ОписаниеРодителя, Символы.ПС, Ложь);
				Если МассивПодстрок.Количество() > 0 Тогда
					ОписаниеРодителя = МассивПодстрок[0];
				КонецЕсли;
			КонецЕсли; 
			
			ЭтоДокументНаВозврат = ЭтоДокументНаВозврат(ПодчиненныйДокумент.Шапка.ТипДокумента);
			
			ДанныеДляЗагрузки = Новый Структура;
			Если ЭтоДокументНаВозврат Тогда
				ШаблонОписание = НСтр("ru = 'Возврат на склад комиссионера %1 от %2'");
			Иначе
				ШаблонОписание = НСтр("ru = 'Продажа со склада комиссионера %1 от %2'");
			КонецЕсли;
			ДанныеДляЗагрузки.Вставить("Описание", СтрШаблон(ШаблонОписание, Шапка.НомерДокумента, Формат(Шапка.ДатаДокумента, "ДФ=dd.MM.yyyy")));
			ДанныеДляЗагрузки.Вставить("ОписаниеРодителя", ОписаниеРодителя);
			ДанныеДляЗагрузки.Вставить("Маркетплейс", Перечисления.ВидыМаркетплейсов.Wildberries);
			ДанныеДляЗагрузки.Вставить("ВидДокумента", ДанныеМаркеплейса.ВидДокумента);
			ДанныеДляЗагрузки.Вставить("Документ", ПодчиненныйДокумент);
			ДанныеДляЗагрузки.Вставить("Организация", ПараметрыИнтеграции.Организация);
			ДанныеДляЗагрузки.Вставить("КонтрагентМаркетплейс", ДанныеМаркеплейса.Контрагент);
			ДанныеДляЗагрузки.Вставить("ЭтоВозврат", ЭтоДокументНаВозврат);
			ДанныеДляЗагрузки.Вставить("ПервичныеДанныеМаркетплейса", ОбменСМаркетплейс.ОтобратьПервичныеДанныеМаркетплейса(
				ДанныеМаркеплейса.ДанныеДокумента.ПервичныеДанныеМаркетплейса, "номердокумента", Шапка.НомерДокумента));
			ДанныеДляЗагрузки.Вставить("СпособЗагрузки", ПараметрыИнтеграции.СпособЗагрузки);
			СписокЗагрузки.Добавить(ДанныеДляЗагрузки);
			
		КонецЦикла;
		
	Иначе
		Если ПараметрыИнтеграции.СпособСверткиОтчетов = "Сворачивать" Тогда
			Если НЕ (ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа()
				ИЛИ ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonВыкуп()) Тогда 
				ОбработатьДатуПродажи(ДанныеМаркеплейса.ДанныеДокумента.ТаблицаТоваров);
			КонецЕсли;
			ДанныеМаркеплейса.ДанныеДокумента.ТаблицаТоваров.Свернуть(СворачиваемыеКолонки, СуммируемыеКолонки);
		КонецЕсли;
		
		ДанныеМаркеплейса.Вставить("СпособЗагрузки", ПараметрыИнтеграции.СпособЗагрузки);
		
		ДобавитьПолучателяВСтруктуру(ДанныеМаркеплейса.ДанныеДокумента, ПараметрыИнтеграции.Организация);
		
		СписокЗагрузки.Добавить(ДанныеМаркеплейса);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьПолучателяВСтруктуру(ДанныеДокумента, Организация)
	
	Если ДанныеДокумента.Свойство("Получатель") 
		И ТипЗнч(ДанныеДокумента.Получатель) = Тип("Структура") Тогда
		ДанныеДокумента.Получатель.Вставить("Ссылка", Организация);
	Иначе
		Получатель = Новый Структура;
		Получатель.Вставить("Ссылка", Организация);
		ДанныеДокумента.Вставить("Получатель", Получатель);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработатьДатуПродажи(ТаблицаТоваров)
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		Если СтрокаТовара.ВидДвижения <> "Возврат"
			И СтрокаТовара.ВидДвижения <> "Сторно продаж" Тогда
			СтрокаТовара.ДатаПродажи = Дата(1,1,1);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОтложенноеСопоставлениеНоменклатуры

Функция НастройкиСопоставленияНоменклатуры(ВидДокумента) Экспорт
	
	ЕстьСтавкаНДС = (ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаWBУведомлениеВыкупа())
		Или (ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамWB());
	
	ВидимостьКолонокСопоставления = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ВидимостьКолонокСопоставления.Вставить("ЕдиницаИзмерения", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Упаковка", Ложь);
	ВидимостьКолонокСопоставления.Вставить("СтавкаНДС", ЕстьСтавкаНДС);
	
	ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
	ДополнительныйРеквизит.Имя = "КодМагазина";
	ДополнительныйРеквизит.Представление = "Код маркетплейса";
	НовыеКолонкиСопоставления = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДополнительныйРеквизит);
	
	ДополнительныеПараметрыПоиска = Новый Структура;
	ДополнительныеПараметрыПоиска.Вставить("НатуральныеКлючи", "Артикул");
	ДополнительныеПараметрыПоиска.Вставить("Маркетплейс", Перечисления.ВидыМаркетплейсов.Wildberries);
	
	НастройкиСопоставления = Новый Структура;
	НастройкиСопоставления.Вставить("ВидимостьКолонокСопоставления", ВидимостьКолонокСопоставления);
	НастройкиСопоставления.Вставить("ДополнительныеРеквизитыСопоставления", НовыеКолонкиСопоставления);
	НастройкиСопоставления.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметрыПоиска);
	НастройкиСопоставления.Вставить("ОграничениеТипаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НастройкиСопоставления.Вставить("ОтключитьПоискПоСловарю", Истина);
	НастройкиСопоставления.Вставить("ТочностьПоискаПоУмолчанию", 100);
	
	Возврат НастройкиСопоставления;
	
КонецФункции

Функция ПодготовитьОтложенноеСопоставлениеНоменклатуры(Контрагент, НезаполненнаяНоменклатура) Экспорт
	
	НесопоставленнаяНоменклатура = Новый Массив;
	
	Для Каждого СтрокаКСопоставлению Из НезаполненнаяНоменклатура Цикл
		СтрокаТовара = СтрокаКСопоставлению.СтрокаТовара;
		
		НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(Контрагент);
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, СтрокаТовара);
		НоменклатураКонтрагента.Идентификатор             = СтрокаТовара.ИД;
		НоменклатураКонтрагента.ИдентификаторНоменклатуры = СтрокаТовара.ИД;
		НоменклатураКонтрагента.ШтрихкодКомбинации        = СтрокаТовара.Штрихкод;
		НоменклатураКонтрагента.ШтрихкодыНоменклатуры     = СтрокаТовара.Штрихкод;
		Если ЗначениеЗаполнено(СтрокаТовара.НатуральныйИД) Тогда
			НоменклатураКонтрагента.ИсторияИдентификаторов.Добавить(СтрокаТовара.НатуральныйИД);
		КонецЕсли;
		НоменклатураКонтрагента.Вставить("ИмяТабличнойЧасти", СтрокаКСопоставлению.ИмяТабличнойЧасти);
		НоменклатураКонтрагента.Вставить("ИндексТабличнойЧасти", СтрокаКСопоставлению.ИндексТабличнойЧасти);
		НесопоставленнаяНоменклатура.Добавить(НоменклатураКонтрагента);
		
	КонецЦикла;
	
	Возврат НесопоставленнаяНоменклатура;
	
КонецФункции

#КонецОбласти

#Область РаботаСЗапросами

Функция НовыйНастройкиПодключения()
	НастройкиПодключения = Новый Структура("АдресСервера, Токен");
	Возврат НастройкиПодключения;
КонецФункции

Функция ВерсияМетодаОтчетОПродажахAPIWildberries(ПериодДействия)
	Если ПериодДействия >= Дата(2024,1,29) Тогда
		Возврат "v5";
	Иначе
		Возврат "v1";
	КонецЕсли;
КонецФункции

Функция АдресСервераСтатистикиWildberries()
	Возврат "statistics-api.wildberries.ru";
КонецФункции

Функция АдресСервераМаркетплейсWilberries()
	Возврат "marketplace-api.wildberries.ru";
КонецФункции


Функция ВыполнитьPOSTЗапрос(АдресРесурса, ПараметрыЗапроса, НастройкиПодключения, КодУспех = 200, ПолучитьДанные = Истина)
	Возврат ВыполнитьHTTPЗапрос(АдресРесурса, ПараметрыЗапроса, НастройкиПодключения,  "POST", КодУспех, ПолучитьДанные);
КонецФункции

Функция ВыполнитьPUTЗапрос(АдресРесурса, ПараметрыЗапроса, НастройкиПодключения, КодУспех = 204, ПолучитьДанные = Ложь)
	Возврат ВыполнитьHTTPЗапрос(АдресРесурса, ПараметрыЗапроса, НастройкиПодключения,  "PUT", КодУспех, ПолучитьДанные);
КонецФункции

Функция ВыполнитьGETЗапрос(АдресРесурса, НастройкиПодключения, КодУспех = 200, ПолучитьДанные = Истина)
	Возврат ВыполнитьHTTPЗапрос(АдресРесурса, Неопределено, НастройкиПодключения,  "GET", КодУспех, ПолучитьДанные);
КонецФункции

Функция ВыполнитьHTTPЗапрос(АдресРесурса, ПараметрыЗапроса, НастройкиПодключения, МетодЗапроса, КодУспех, ПолучитьДанные)
	
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	HTTPСоединение = Новый HTTPСоединение(
		НастройкиПодключения.АдресСервера,
		, , ,
		ИнтернетПрокси,
		,
		ЗащищенноеСоединение);
	
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json;charset=UTF-8");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", НастройкиПодключения.Токен);
	
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, ЗаголовкиHTTPЗапроса);
	Если ЗначениеЗаполнено(ПараметрыЗапроса) Тогда
		ТелоHTTPЗапроса = ОбменСМаркетплейс.ВJSON(ПараметрыЗапроса);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоHTTPЗапроса, "UTF-8");
	КонецЕсли;
	Попытка	
		HTTPОтвет = HTTPСоединение.ВызватьHTTPМетод(МетодЗапроса, HTTPЗапрос);
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.Wildberries);
		ВызватьИсключение;
	КонецПопытки;
	//Логирование
	Если НастройкиПодключения.Свойство("Логирование") Тогда
		Логирование = НастройкиПодключения.Логирование;
		Если HTTPСоединение  = Неопределено Тогда
			Логирование.АдресСервера 	= НастройкиПодключения.АдресСервера;
		Иначе
			Логирование.АдресСервера 	= HTTPСоединение.Сервер +":"+HTTPСоединение.Порт;
			Логирование.Соединение 		= Истина;
			Логирование.Метод 			= МетодЗапроса;
			Логирование.Запрос 			= HTTPЗапрос;
			Логирование.Ответ 			= HTTPОтвет;
		КонецЕсли;
		РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЗаписатьВЖурнал(Логирование);
	КонецЕсли;
	//Конец Логирование
	КодСостояния = HTTPОтвет.КодСостояния;
	
	РезультатЗапроса = Новый Структура;
	РезультатЗапроса.Вставить("Результат",           КодСостояния = КодУспех);
	РезультатЗапроса.Вставить("ВыполнятьСЗадержкой", КодСостояния = 429);
	РезультатЗапроса.Вставить("СообщениеОбОшибке");
	РезультатЗапроса.Вставить("Данные");
	
	СообщениеОбОшибке = "";
	Если НЕ РезультатЗапроса.Результат Тогда
		СообщениеОбОшибке = СообщениеОбОшибкеПоКоду(HTTPОтвет);
	ИначеЕсли ПолучитьДанные Тогда
		Попытка
			ДанныеОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет);
			РезультатЗапроса.Вставить("Данные", ДанныеОтвета);
		Исключение
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка разбора данных, полученных от маркетплейса. Причина: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.Wildberries);
	КонецЕсли;
	
	Если КодСостояния = 500 ИЛИ КодСостояния = 401 ИЛИ КодСостояния = 403 Тогда
		РезультатЗапроса.СообщениеОбОшибке = СообщениеОбОшибке;
	КонецЕсли;
	
	Возврат РезультатЗапроса;
КонецФункции 

Функция СообщениеОбОшибкеПоКоду(HTTPОтвет)
	КодСостояния = HTTPОтвет.КодСостояния;
	Если КодСостояния = 500 Тогда
		СообщенияОбОшибках = НСтр("ru = 'Внутренняя ошибка на сервере Wildberries. Попробуйте позже'");
	ИначеЕсли КодСостояния = 401 Тогда
		СообщенияОбОшибках = НСтр("ru = 'Ошибка авторизации в API Wildberries. Проверьте тип и срок действия токена.
			|Получить новый токен API можно в личном кабинете: Профиль-Настройки-Доступ к API'");
	ИначеЕсли КодСостояния = 403 Тогда
		СообщенияОбОшибках = НСтр("ru = 'Ошибка авторизации в API Wildberries. Проверьте в настройках ключ API'");
	ИначеЕсли КодСостояния = 429 Тогда
		СообщенияОбОшибках = НСтр("ru = 'Сервер API Wildberries ответил: слишком много запросов. Попробуйте позже'");
	Иначе
		ПереченьОшибок = HTTPОтвет.ПолучитьТелоКакСтроку();
		СообщенияОбОшибках = СтрШаблон(НСтр("ru = 'Ошибка получения данных из маркетплейса. Ответ сервера: %1'"), ПереченьОшибок);
	КонецЕсли;

	Возврат СообщенияОбОшибках;
КонецФункции

Функция АдресСервераКонтентWildberries()
	Возврат "content-api.wildberries.ru";
КонецФункции 

#КонецОбласти