#Область ПрограммныйИнтерфейс

// Обработчик команды формы, требующей контекстного вызова сервера.
//
// Параметры:
//   Форма - УправляемаяФорма - форма, из которой выполняется команда.
//   ПараметрыВызова - Структура - параметры вызова.
//   Источник - ТаблицаФормы, ДанныеФормыСтруктура - объект или список формы с полем "Ссылка".
//   Результат - Структура - результат выполнения команды.
//
Процедура ВыполнитьПереопределяемуюКоманду(Знач Форма, Знач ПараметрыВызова, Знач Источник, Результат) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область СобытияФорм

// Серверная переопределяемая процедура, вызываемая при заполнении реквизитов формы созданных ИСМП (при открытии)
//
// Параметры:
//   Форма - УправляемаяФорма - форма, из которой происходит вызов процедуры.
//
Процедура ЗаполнениеРеквизитовФормы(Форма) Экспорт
	
	ПараметрыИнтеграции = Неопределено;
	Для Каждого ВидПродукции Из ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииИСМП(Истина) Цикл
		ПараметрыИнтеграции = Форма.ПараметрыИнтеграцииГосИС.Получить(ВидПродукции);
		Если ПараметрыИнтеграции <> Неопределено Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыИнтеграции = Неопределено Тогда
		ПараметрыИнтеграции = Форма.ПараметрыИнтеграцииГосИС.Получить("ФормаСверкиИСМП");
		Если ПараметрыИнтеграции = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыИнтеграции) = Тип("Массив") Тогда
		ПараметрыИнтеграции = ПараметрыИнтеграции[0];
	КонецЕсли;
	
	Если ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора Тогда
	
		Объект = ПараметрыИнтеграции.ИмяРеквизитаФормыОбъект;
		Товары = ПараметрыИнтеграции.ИмяТабличнойЧастиТовары;
		
		МассивНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(Форма[Объект][Товары], "Номенклатура", Истина);
		Если ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "КорректировкаРеализации") Тогда
			Индекс = 0;
			Пока Индекс < МассивНоменклатуры.Количество() Цикл
				Если ТипЗнч(МассивНоменклатуры[Индекс]) <> Тип("СправочникСсылка.Номенклатура") Тогда
					МассивНоменклатуры.Удалить(Индекс);
				Иначе
					Индекс = Индекс+1;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли;
		
		ПараметрыСпискаНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивНоменклатуры, "ВидМаркировки");
		Для каждого СтрокаТаблицы Из Форма[Объект][Товары] Цикл
			ВидПродукцииИС = ПараметрыСпискаНоменклатуры.Получить(СтрокаТаблицы.Номенклатура);
			Если ВидПродукцииИС <> Неопределено Тогда
				ДанныеЗаполнения = Новый Структура; 
				ДанныеЗаполнения.Вставить("Номенклатура",         СтрокаТаблицы.Номенклатура);
				ДанныеЗаполнения.Вставить("ВидПродукцииИС",       ВидПродукцииИС);
				ДанныеЗаполнения.Вставить("МаркируемаяПродукция", ВидПродукцииИС <> Перечисления.ВидыПродукцииИС.ПустаяСсылка());
				ДанныеЗаполнения.Вставить("АвтоматическийОСУИС", Ложь);
				
				ПроверкаИПодборПродукцииИСМП.ЗаполнитьПризнакАвтоматическийОСУИСВСтроке(ДанныеЗаполнения);
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеЗаполнения, , "Номенклатура");
			КонецЕсли;
		КонецЦикла; 
	
	КонецЕсли; 
	
	Если ПараметрыИнтеграции.Свойство("ИспользоватьКолонкуРасхожденияПоКодамМаркировки")
		И ПараметрыИнтеграции.ИспользоватьКолонкуРасхожденияПоКодамМаркировки Тогда
		
		Объект = ПараметрыИнтеграции.ИмяРеквизитаФормыОбъект;
		Товары = ПараметрыИнтеграции.ИмяТабличнойЧастиТовары;
		
		ИмяКолонкиРасхождения                = ПараметрыИнтеграции.ИмяКолонкиРасхождения;
		Расхождения                          = ПараметрыИнтеграции.ИмяТабличнойЧастиШтрихкодыУпаковокРасхождения;
		ДоступноСогласованиеРасхождений      = ПараметрыИнтеграции.ДоступноСогласованиеРасхождений;
		
		СверкаКодовМаркировкиИСМП.ЗаполнитьКолонкуРасхожденияПоКодамМаркировки(
			Форма[Объект][Товары],
			Форма[Объект][Расхождения],
			ИмяКолонкиРасхождения,
			ДоступноСогласованиеРасхождений);
		Если ДоступноСогласованиеРасхождений Тогда
			ТребуетсяОбработкаКодовМаркировки = Ложь;
			СверкаКодовМаркировкиИСМППереопределяемый.ПриОпределенииНеобходимостиОбработкиКодовМаркировкиВДокументе(
				Форма[Объект],
				ПараметрыИнтеграции,
				ТребуетсяОбработкаКодовМаркировки);
			Форма[ПараметрыИнтеграции.ИмяРеквизитаФормыТребуетсяОбработкаКодовМаркировки] =ТребуетсяОбработкаКодовМаркировки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Переопределение параметров интеграции ИСМП (расположения команды проверки и подбора)
//
// Параметры:
//   Форма                - ФормаКлиентскогоПриложения - прикладная форма для встраивания форматированной строки
//   СтандартнаяОбработка - Булево - стандартная работа с элементами проверки подбора
//   ПараметрыИнтеграции  - Соответствие - автоматически заданные параметры интеграции
//
Процедура ПриОпределенииПараметровИнтеграции(Форма, СтандартнаяОбработка, ПараметрыИнтеграции) Экспорт
	
	// см. ПриОпределенииДоступностиИнтеграцииФормыСверкиКодовМаркировки
	Если ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "АктОРасхождениях")
		Или ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "АктОРасхожденияхПолученный") Тогда
			СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

//Переопределение параметров интеграции ИСМП (расположения форматированной строки перехода к связанному объекту)
//
//Параметры:
//   Форма            - УправляемаяФорма - прикладная форма для встраивания форматированной строки
//   ПараметрыНадписи - Структура        - (см. СобытияФормИСМП.ПараметрыИнтеграцииГиперссылкиИСМП)
//
Процедура ПриОпределенииПараметровИнтеграцииГиперссылкиИСМП(Форма, ПараметрыНадписи) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект")
		И ТипЗнч(Форма.Объект) = Тип("ДанныеФормыСтруктура")
		И ИнтеграцияИСМП.ИспользуетсяИнтеграцияВФормеДокументаОснования(Форма, Форма.Объект)
		И (Не ОбщегоНазначенияБП.ЭтоПростойИнтерфейс()) Тогда
		
		ПараметрыНадписи.Вставить("ИмяЭлементаФормы",  "ТекстДокументаИСМП");
		ПараметрыНадписи.Вставить("ИмяРеквизитаФормы", "ТекстДокументаИСМП");
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ГруппаСостояние") Тогда
		ПараметрыНадписи.РазмещениеВ = "ГруппаСостояние";
	КонецЕсли;
	
КонецПроцедуры

// Переопределение доступности интеграции формы сверки для прикладной формы (расположения форматированной строки перехода к связанному объекту)
//
// Параметры:
//   Форма            - ФормаКлиентскогоПриложения - прикладная форма для встраивания форматированной строки
//   СтандартнаяОбработка - Булево - установить ложь, если требуется отменить выполнение стандартной обработки.
//
Процедура ПриОпределенииДоступностиИнтеграцииФормыСверкиКодовМаркировки(Форма, СтандартнаяОбработка) Экспорт
	
	Если ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "АктОРасхождениях")
		ИЛИ ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "АктОРасхожденияхПолученный") Тогда
		СтандартнаяОбработка = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
//  Форма         - ФормаКлиентскогоПриложения - форма, из обработчика события которой происходит вызов процедуры.
//  ТекущийОбъект - Объект - объект, который будет прочитан.
//
Процедура ПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ПослеЗаписиНаСервере(Форма) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФорм

// Серверная переопределяемая процедура, вызываемая из обработчика события элемента.
//
// Параметры:
//   Форма                   - УправляемаяФорма - форма, из которой происходит вызов процедуры.
//   Элемент                 - Строка           - имя элемента-источника события "При изменении"
//   ДополнительныеПараметры - Структура        - значения дополнительных параметров влияющих на обработку.
//
Процедура ПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры) Экспорт
	
	Если Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаОбщая"
		Или Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаТовары"
		ИЛИ ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "КорректировкаРеализации") 
		ИЛИ ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "АктОРасхождениях") 
		ИЛИ Форма.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокументаТовары"
		ИЛИ ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "ВозвратТоваровОтПокупателя") 
		ИЛИ ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "АктОРасхожденияхПолученный")
		ИЛИ ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "ПередачаТоваров")
		Тогда
		
		Если Элемент = "Номенклатура" 
			И ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
			И ДополнительныеПараметры.Свойство("Номенклатура")
			И ЗначениеЗаполнено(ДополнительныеПараметры.Номенклатура) Тогда
			
			ВидПродукцииИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.Номенклатура, "ВидМаркировки");
			ЕстьМаркируемаяПродукция = НЕ (ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПустаяСсылка());
			Если ЕстьМаркируемаяПродукция Тогда
				ДополнительныеПараметры.Вставить("МаркируемаяПродукция", Истина);
				ДополнительныеПараметры.Вставить("ВидПродукцииИС", ВидПродукцииИС);
				ДополнительныеПараметры.Вставить("АвтоматическийОСУИС", Ложь);
				ПроверкаИПодборПродукцииИСМП.ЗаполнитьПризнакАвтоматическийОСУИСВСтроке(ДополнительныеПараметры);
			КонецЕсли;
		Иначе
			ПроверкаИПодборПродукцииИСМП.ПрименитьКешШтрихкодовУпаковок(Форма, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыВыбора

// Устанавливает параметры выбора шаблона этикетки.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой нужно установить параметры выбора,
//  ИмяПоляВвода - Строка - имя поля ввода шаблона этикетки.
Процедура УстановитьПараметрыВыбораШаблонаЭтикетки(Форма,  ИмяПоляВвода) Экспорт
	
	НовыеПараметры = Новый Массив;
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.МаркированнаяПродукция", Истина));
	
	Форма.Элементы[ИмяПоляВвода].ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметры);
	
КонецПроцедуры

// Устанавливает параметры выбора контрагента.
//
//Параметры:
//   Форма                   - ФормаКлиентскогоПриложения - форма, в которой нужно установить параметры выбора.
//   ТолькоЮрЛицаНерезиденты - Неопределено, Булево - Признак нерезидента.
//   ИмяПоляВвода            - Строка               - имя поля ввода номенклатуры.
//
Процедура УстановитьПараметрыВыбораКонтрагента(Форма, ТолькоЮрЛицаНерезиденты = Неопределено, ИмяПоляВвода = "Контрагент") Экспорт
	
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет действия при изменении номенклатуры в строке табличной части.
//
// Параметры:
//  Форма                  - УправляемаяФорма - форма, в которой произошло событие,
//  ТекущаяСтрока          - ДанныеФормыЭлементКоллекции - редактируемая строка таблицы,
//  КэшированныеЗначения   - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - (См. ПроверкаИПодборПродукцииМОТП.ПараметрыУказанияСерий).
Процедура ПриИзмененииНоменклатуры(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий = Неопределено) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Количество")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КоличествоУпаковок") Тогда

		ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоУпаковок;
	
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Номенклатура")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КодТНВЭД") Тогда

		Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			ТекущаяСтрока.КодТНВЭД = ИнтеграцияИСМПБПВызовСервера.КодТНВЭД(ТекущаяСтрока.Номенклатура);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Выполняет действия при изменении подобранного количества в строке таблицы формы.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - ФиксированнаяСтруктура - параметры указаний серий формы
Процедура ПриИзмененииКоличества(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий = Неопределено) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Количество")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КоличествоУпаковок") Тогда

		ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоУпаковок;
	
	КонецЕсли;
	
КонецПроцедуры

// Выполняет действия при изменении подобранного количества (поле Количество) в строке таблицы формы.
//
// Параметры:
//  Форма - ФормаУправляемогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - ФиксированнаяСтруктура - параметры указаний серий формы
Процедура ПриИзмененииКоличестваЕдиниц(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий = Неопределено) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

// Заполняет табличную часть Товары подобранными товарами.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой производится подбор,
//  ВыбранноеЗначение - Произвольный - данные, содержащие подобранную пользователем номенклатуру,
//  ПараметрыЗаполнения - Структура - дополнительные параметры заполнения
//  ПараметрыЗаполнения - Структура - параметры заполнения,
//  КэшированныеЗначения - Неопределено, Структура - сохраненные значения параметров, используемых при обработке,
//  ДобавленныеСтроки - Неопределено, Массив из ДанныеФормыЭлементКоллекции - массив добавленных строк таблицы товаров
Процедура ОбработкаРезультатаПодбораНоменклатуры(
	Форма, ВыбранноеЗначение, ПараметрыЗаполнения,
	КэшированныеЗначения = Неопределено, ДобавленныеСтроки = Неопределено) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
	
	ТекущаяСтрока = Неопределено;
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Форма.Объект.Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, "Номенклатура");
		
		Если ТекущаяСтрока.Свойство("КоличествоУпаковок") Тогда
			ТекущаяСтрока.КоличествоУпаковок = СтрокаТовара.Количество;
		КонецЕсли;
	КонецЦикла;
		
	Если ТекущаяСтрока <> Неопределено Тогда
		Форма.Элементы.Товары.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
