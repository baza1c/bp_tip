////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиДепозитыСлужебныйКлиентСервер: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Префикс обмен с банками депозиты.
// 
// Возвращаемое значение:
//  Строка - Префикс обмен с банками депозиты.
//
Функция ПрефиксОбменСБанкамиДепозиты() Экспорт
	
	Возврат "ОбменСБанкамиДепозиты";
	
КонецФункции

// Добавить префикс директ банк депозиты.
// 
// Параметры:
//  ИсходнаяСтрока - Строка - Исходная строка.
// 
// Возвращаемое значение:
//  Строка - Добавить префикс директ банк депозиты.
//
Функция ДобавитьПрефиксОбменСБанкамиДепозиты(ИсходнаяСтрока) Экспорт
	
	Возврат СтрШаблон("%1_%2", ПрефиксОбменСБанкамиДепозиты(), ИсходнаяСтрока);
	
КонецФункции

// Возвращает имя процедуры обработчика команды или события элемента параметра депозитного калькулятора.
//
// Параметры:
//  КлючОбработчика - Строка - Ключ обработчика. Возможные ключи:
//   - "ВыполнитьКоманду" - Ключ процедуры обработчика команд депозитного калькулятора;
//   - "ПриИзменении" - Ключ процедуры обработчиков события "ПриИзменении";
//   - "ОбработкаВыбора" - Ключ процедуры обработчиков события "ОбработкаВыбора";
//   - "АвтоПодбор" - Ключ процедуры обработчиков события "АвтоПодбор".
//
// Возвращаемое значение:
//  Строка - Имя процедуры.
//
Функция ИмяОбработчика(КлючОбработчика) Экспорт
	
	Результат = "";
	
	Если КлючОбработчика = "ВыполнитьКоманду" Тогда
		Результат = "ВыполнитьКоманду";
	ИначеЕсли КлючОбработчика = "ПриИзменении" Тогда
		Результат = "ПриИзменении";
	ИначеЕсли КлючОбработчика = "ОбработкаВыбора" Тогда
		Результат = "ОбработкаВыбора";
	ИначеЕсли КлючОбработчика = "АвтоПодбор" Тогда
		Результат = "АвтоПодбор";
	КонецЕсли;
	
	Если Не ПустаяСтрока(Результат) Тогда
		ПрефиксДепозиты = ПрефиксОбменСБанкамиДепозиты();
		Результат = СтрШаблон("Подключаемый_%1%2", ПрефиксДепозиты, Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает имя параметра депозитного калькулятора.
// 
// Параметры:
//  КлючПараметра - Строка - Ключ параметра. Возможные ключи:
//   - "Сумма" - сумма депозита;
//   - "Срок" - срок депозита;
//   - "ДатаЗакрытия" - дата закрытия депозита;
//   - "Ставка" - ставка депозита;
//   - "Доход" - доход на конец срока;
//   - "Итог" - итог на конец срока;
//   - "Предложение" - депозитный продукт банка;
//   - "ДополнительныеПараметры" - дополнительные параметры депозитного калькулятора;
//   - "ПараметрыДепозита" - параметры текущего продукта.
//
// Возвращаемое значение:
//  Строка - Имя параметра.
//
Функция ИмяПараметра(КлючПараметра) Экспорт
	
	Результат = "";
	
	Если КлючПараметра = "Сумма" Тогда
		Результат = "Сумма";
	ИначеЕсли КлючПараметра = "Срок" Тогда
		Результат = "Срок";
	ИначеЕсли КлючПараметра = "ДатаЗакрытия" Тогда
		Результат = "ДатаЗакрытия";
	ИначеЕсли КлючПараметра = "Ставка" Тогда
		Результат = "Ставка";
	ИначеЕсли КлючПараметра = "Доход" Тогда
		Результат = "Доход";
	ИначеЕсли КлючПараметра = "Итог" Тогда
		Результат = "Итог";
	ИначеЕсли КлючПараметра = "Предложение" Тогда
		Результат = "Предложение";
	ИначеЕсли КлючПараметра = "ДополнительныеПараметры" Тогда
		Результат = "ДополнительныеПараметры";
	ИначеЕсли КлючПараметра = "ПараметрыДепозита" Тогда
		Результат = "ПараметрыДепозита";
	КонецЕсли;
	
	Если Не ПустаяСтрока(Результат) Тогда
		Результат = ДобавитьПрефиксОбменСБанкамиДепозиты(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание типа параметра депозитного калькулятора.
// 
// Параметры:
//  КлючПараметра - Строка - Ключ параметра. Возможные ключи:
//   - "Сумма" - сумма депозита;
//   - "Срок" - срок депозита;
//   - "Предложение" - депозитный продукт банка;
//   - "ДополнительныеПараметры" - дополнительные параметры депозитного калькулятора.
//
// Возвращаемое значение:
//  Строка - Имя параметра.
//
Функция ОписаниеТипаРеквизитаПараметраДепозита(КлючПараметра) Экспорт
	
	Результат = Неопределено;
	
	Если КлючПараметра = "Сумма" Тогда
		Результат = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный));
	ИначеЕсли КлючПараметра = "Срок" Тогда
		Результат = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 0, ДопустимыйЗнак.Неотрицательный));
	ИначеЕсли КлючПараметра = "ДатаЗакрытия" Тогда
		Результат = Новый ОписаниеТипов("Дата", Новый КвалификаторыДаты(ЧастиДаты.Дата));
	ИначеЕсли КлючПараметра = "Предложение" Тогда
		Результат = Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная));
	ИначеЕсли КлючПараметра = "ДополнительныеПараметры" Тогда
		Результат = Новый ОписаниеТипов();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает имя элемента формы депозитного калькулятора созданного при инициализации в форме размещения.
// (имена кнопок команд соответствуют названию команд см. ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды).
// Параметры:
//  КлючЭлемента - Строка - Ключ элемента. Возможные ключи:
//   - "Сумма" - элемент формы параметра сумма депозита;
//   - "Срок" - элемент формы параметра срок депозита;
//   - "ДатаЗакрытия" - элемент формы параметра дата закрытия депозита;
//   - "Ставка" - элемент формы параметра ставка депозита;
//   - "Доход" - элемент формы параметра доход на конец срока;
//   - "РасчетПриОформлении" - элемент формы сообщения "Расчет при оформлении";
//   - "Итог" - элемент формы параметра итог на конец срока;
//   - "ГруппаПараметровДоходности" - группа формы параметров доходности (доход, итого);
//   - "Предложение" - элемент формы параметра предложение;
//   - "ОписаниеДепозита" - элемент формы описание депозитного продукта;
//   - "ДоходностьОписаниеДепозита" - элемент формы описание доходности депозитного продукта;
//   - "СрокиИУсловия" - элемент формы сроки и условия;
//   - "СтраницаИнформации" - страница информации;
//   - "СтраницаРасчетДепозита" - страница расчет депозита;
//   - "ОжиданиеКартинка" - картинка страницы информации;
//   - "ОшибкаКартинка" - картинка страницы информации;
//   - "ТекстСообщения" - текст сообщения страницы информации.
//
// Возвращаемое значение:
//  Строка - Имя параметра.
//
Функция ИмяЭлементаФормы(КлючЭлемента) Экспорт
	
	Результат = "";
	ЭтоГруппа = Ложь;
	ЭтоДекорация = Ложь;
	Если КлючЭлемента = "Сумма" Тогда
		Результат = "Сумма";
	ИначеЕсли КлючЭлемента = "Срок" Тогда
		Результат = "Срок";
	ИначеЕсли КлючЭлемента = "ДатаЗакрытия" Тогда
		Результат = "ДатаЗакрытия";
	ИначеЕсли КлючЭлемента = "Ставка" Тогда
		Результат = "Ставка";
	ИначеЕсли КлючЭлемента = "Доход" Тогда
		Результат = "Доход";
	ИначеЕсли КлючЭлемента = "РасчетПриОформлении" Тогда
		Результат = "РасчетПриОформлении";
		ЭтоДекорация = Истина;
	ИначеЕсли КлючЭлемента = "Итог" Тогда
		Результат = "Итог";
	ИначеЕсли КлючЭлемента = "ГруппаПараметровДоходности" Тогда
		Результат = "ПараметровДоходности";
		ЭтоГруппа = Истина;
	ИначеЕсли КлючЭлемента = "Предложение" Тогда
		Результат = "Предложение";
	ИначеЕсли КлючЭлемента = "ОписаниеДепозита" Тогда
		Результат = "ОписаниеДепозита";
	ИначеЕсли КлючЭлемента = "ДоходностьОписаниеДепозита" Тогда
		Результат = "ДоходностьОписаниеДепозита";
	ИначеЕсли КлючЭлемента = "СрокиИУсловия" Тогда
		Результат = "СрокиИУсловия";
	ИначеЕсли КлючЭлемента = "СтраницаИнформации" Тогда
		Результат = "СтраницаИнформации";
		ЭтоГруппа = Истина;
	ИначеЕсли КлючЭлемента = "СтраницаРасчетДепозита" Тогда
		Результат = "СтраницаРасчетДепозита";
		ЭтоГруппа = Истина;
	ИначеЕсли КлючЭлемента = "ОжиданиеКартинка" Тогда
		Результат = "ОжиданиеКартинка";
		ЭтоДекорация = Истина;
	ИначеЕсли КлючЭлемента = "ОшибкаКартинка" Тогда
		Результат = "ОшибкаКартинка";
		ЭтоДекорация = Истина;
	ИначеЕсли КлючЭлемента = "ТекстСообщения" Тогда
		Результат = "ТекстСообщения";
		ЭтоДекорация = Истина;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Результат) Тогда
		
		Результат = ДобавитьПрефиксОбменСБанкамиДепозиты(Результат);
		Если ЭтоГруппа Тогда
			Результат = СтрШаблон("Группа%1", Результат);
		ИначеЕсли ЭтоДекорация Тогда
			Результат = СтрШаблон("Декорация%1", Результат);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает дополнительные параметры депозитов.
// Если реквизит формы не создан возвращает неопределено
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, предназначенная для вывода депозитного калькулятора.
// 
// Возвращаемое значение:
//  РеквизитФормы, Неопределено
//
Функция ДополнительныеПараметрыДепозитов(Форма) Экспорт
	
	Результат = Неопределено;
	ИмяРеквизитаДополнительныеПараметры = ИмяПараметра("ДополнительныеПараметры");
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, 
			ИмяРеквизитаДополнительныеПараметры) Тогда
		Результат = Форма[ИмяРеквизитаДополнительныеПараметры];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает результат проверки завершения инициализации страницы расчеты депозитного калькулятора в форме владельца.
// 
// Параметры:
//  ПараметрыДепозитногоКалькулятора см. ОбменСбанкамиДепозиты.ПараметрыДепозитногоКалькулятора.
// 
// Возвращаемое значение:
//  Булево - Если Истина, отложенная инициализаци выполненна
//
Функция ОтложеннаяИнициализацияВыполнена(ПараметрыДепозитногоКалькулятора) Экспорт
	
	Результат = Ложь;
	
	Если Не ПараметрыДепозитногоКалькулятора.ОтложеннаяИнициализация 
		ИЛИ (ПараметрыДепозитногоКалькулятора.ОтложеннаяИнициализация 
			И ПараметрыДепозитногоКалькулятора.ВыполненаИнициализация) Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция проверяет вхождение Срока и Суммы в диапазоны условий депозитного продукта
// 
// Параметры:
//  УсловиеДепозита - Структура - условие депозитного продукта
//  Сумма - Число - сумма депозита
//  Срок - Число - срок депозита
// 
// Возвращаемое значение:
//  Булево
//
Функция ВыполняютсяУсловияПродукта(УсловиеДепозита, Сумма, Срок) Экспорт
	
	Результат = Ложь;
	УсловиеСуммы = Не ЗначениеЗаполнено(Сумма);
	УсловиеСрок = Не ЗначениеЗаполнено(Срок);
	
	Если Сумма >= УсловиеДепозита.СуммаМинимум 
		И (Сумма <= УсловиеДепозита.СуммаМаксимум Или УсловиеДепозита.СуммаМаксимум = 0) Тогда
		УсловиеСуммы = Истина;
	КонецЕсли;
	
	Если Срок >= УсловиеДепозита.СрокМинимум 
		И (Срок <= УсловиеДепозита.СрокМаксимум Или УсловиеДепозита.СрокМаксимум = 0)Тогда
		УсловиеСрок = Истина;
	КонецЕсли;
	
	Если УсловиеСуммы И УсловиеСрок Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция рассчитывает доход по депозиту по формуле простых процентов
//
// Параметры:
//  ДатаРасчета - Дата - текущая дата;
//  Сумма - Число - сумма депозита;
//  Ставка - Число - процентная ставка;
//  Срок - Число - срок депозита в днях.
// 
// Возвращаемое значение:
//  Число
//
Функция РассчитатьДоход(ДатаРасчета, Сумма, Ставка, Срок) Экспорт
	
	Доход = (Сумма * Ставка * (Срок / ДеньГода(КонецГода(ДатаРасчета)))) / 100;
	Возврат Доход;
	
КонецФункции

// Функция возвращает новую структуру параметров для расчета депозита в банке.
// 
// Возвращаемое значение:
//  Структура:
//   * ИдентификаторПродукта - Строка - идентификатор депозитного продукта в системе банка.
//   * СчетОткрытия - ОпределяемыйТип.СчетОрганизацииОбменСБанками, Неопределено - счет открытия депозита.
//   * СуммаДепозита - Число - сумма депозита.
//   * Срок - Число - срок на который открывается депозит в днях.
//
Функция НовыеПараметрыРасчетаДепозитаВБанке() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИдентификаторПродукта", "");
	Результат.Вставить("СчетОткрытия", Неопределено);
	Результат.Вставить("СуммаДепозита", 0);
	Результат.Вставить("Срок", 0);
	
	Возврат Результат;
	
КонецФункции

// Функция формирует представление состояния заявки на депозит для отображения в интерфейсе.
//
// Параметры:
//  СостояниеЗаявки - Строка - см. СостояниеЗаявкиНаДепозит.
// 
// Возвращаемое значение:
//  Строка - представление состояния заявки на депозит
//
Функция ПредставлениеСостоянияЗаявкиНаДепозит(СостояниеЗаявки) Экспорт
	
	Результат = "";
	
	Если СостояниеЗаявки = "Исполнено" Тогда
		
		Результат = НСтр("ru = 'Исполнено'");
		
	ИначеЕсли СостояниеЗаявки = "Отклонено" Тогда
		
		Результат = НСтр("ru = 'Отклонено'");
		
	ИначеЕсли СостояниеЗаявки = "Подготовлено" Тогда
		
		Результат = НСтр("ru = 'Подготовлено'");
		
	ИначеЕсли СостояниеЗаявки = "НаПодписи" Тогда
		
		Результат = НСтр("ru = 'На подписи'");
		
	ИначеЕсли СостояниеЗаявки = "ВОбработке" Тогда
		
		Результат = НСтр("ru = 'В обработке'");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
