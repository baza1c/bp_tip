///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "СПАРКРиски".
// ОбщийМодуль.СПАРКФакторыРиска.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает факторы рисков СПАРК контрагента по ИНН.
//
// Параметры:
//  Контрагент - ОпределяемыйТип.КонтрагентБИП, Строка - Контрагент или ИНН контрагента;
//  Параметры  - Структура - См. СПАРКФакторыРискаКлиентСервер.ПараметрыПолученияФакторов().
// 
// Возвращаемое значение:
//  см. СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска
//
Функция ФакторыРискаКонтрагента(
		Контрагент,
		Параметры) Экспорт
	
	Результат = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
	Результат.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
	
	Если ТипЗнч(Контрагент) = Тип("Строка") Тогда
		КонтрагентИНН = СокрЛП(Контрагент);
		Результат.ИНН = КонтрагентИНН;
		Результат.Контрагент = Неопределено;
	Иначе
		Результат.Контрагент = Контрагент;
		
		// Получение вспомогательных данных.
		МассивКонтрагентов = Новый Массив;
		МассивКонтрагентов.Добавить(Контрагент);
		
		ТаблицаЗначенийРеквизитов = СПАРКРиски.ЗначенияРеквизитовКонтрагентов(МассивКонтрагентов);
		Если ТаблицаЗначенийРеквизитов.Количество() >= 1 Тогда
			КонтрагентИНН = СокрЛП(ТаблицаЗначенийРеквизитов[0].ИНН);
			Результат.ИНН = КонтрагентИНН;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.ВидКонтрагента = СПАРКРискиКлиентСервер.ВидКонтрагента(КонтрагентИНН);
	
	ТекстОшибки = "";
	Результат.ПодлежитПроверке = СПАРКРиски.ИННСоответствуетТребованиям(
		КонтрагентИНН,
		Результат.ВидКонтрагента,
		ТекстОшибки);
	Если Не Результат.ПодлежитПроверке Тогда
		Результат.ВидОшибки = Перечисления.ВидыОшибокСПАРКРиски.НекорректныйИНН;
		Результат.ТекстОшибки = ТекстОшибки;
		Возврат Результат;
	КонецЕсли;
	
	Результат.ДанныеАутентификацииЗаполнены =
		ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	Если Не Результат.ДанныеАутентификацииЗаполнены Тогда
		Результат.ВидОшибки = Перечисления.ВидыОшибокСПАРКРиски.ИнтернетПоддержкаНеПодключена;
		Возврат Результат;
	КонецЕсли;

	Если Не СПАРКРиски.ИспользованиеРазрешено() Тогда
		Результат.ИспользованиеРазрешено = Ложь;
		Результат.ВидОшибки = Перечисления.ВидыОшибокСПАРКРиски.ИспользованиеЗапрещено;
		Возврат Результат;
	КонецЕсли;
	
	УслугаПодключена = ИнтернетПоддержкаПользователей.УслугаПодключена(
		СПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска(),
		Неопределено);
	
	Если УслугаПодключена <> Истина Тогда
		Результат.ВидОшибки = Перечисления.ВидыОшибокСПАРКРиски.ТребуетсяОплатаИлиПревышенЛимит;
		Возврат Результат;
	КонецЕсли;
	
	ОбластьДанныхЛокальная = -1;
	ОписаниеКонтрагента = НовыйОписаниеКонтрагентаФакторыРиска();
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ОбластьДанныхЛокальная = ИнтернетПоддержкаПользователей.ЗначениеРазделителяСеанса();
		ОписаниеКонтрагента.ОбластьДанных = ОбластьДанныхЛокальная;
	КонецЕсли;
	
	ОписаниеКонтрагента.ВидКонтрагента = Результат.ВидКонтрагента;
	ОписаниеКонтрагента.ИНН            = КонтрагентИНН;
	
	РезультатФакторыРиска = ФакторыРискаКонтрагентаИзСервиса(
		ОбластьДанныхЛокальная,
		ОписаниеКонтрагента,
		Параметры);
	
	Если Не РезультатФакторыРиска.ВидОшибки.Пустая() Тогда
		Результат.ВидОшибки   = РезультатФакторыРиска.ВидОшибки;
		Результат.ТекстОшибки = РезультатФакторыРиска.ТекстОшибки;
	ИначеЕсли РезультатФакторыРиска.ОжиданиеОтвета Тогда
		Результат.ОжиданиеОтвета     = РезультатФакторыРиска.ОжиданиеОтвета;
		Результат.ДлительнаяОперация = РезультатФакторыРиска.ДлительнаяОперация;
	Иначе
		Результат = РезультатФакторыРиска;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РегламентноеЗадание

// Регламентное задание ОбновлениеФакторовРисковСПАРК.
//
Процедура ОбновлениеФакторовРисковСПАРК() Экспорт

	// Регламентные задания блокируются на время служебных операций с информационной базой.
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеФакторовРисковСПАРК);
	
	Если Не СПАРКРиски.ИспользованиеСПАРКРискиВключено() Тогда
		СПАРКРиски.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Проверка надежности контрагентов в сервисе 1СПАРК Риски отключена.
				|Задание не выполнено.'"));
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Если база работает в монопольном режиме (например, запущено фоновое задание,
	// которое включило монопольный режим и выгружает данные из области данных для 
	// последующей загрузки в модели сервиса),то будут ошибки при всех попытках записи
	//  данных в базу. Поэтому в монопольном режиме не следует выполнять никаких
	// обновлений в подсистеме СПАРКРиски.
	Если МонопольныйРежим() Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОперации = ИдентификаторОперацииЗагрузкаФакторовРиска();
	ПараметрыОбновленияФакторов = СПАРКРискиПовтИсп.ПараметрыОбновленияФакторовРисков();
	Если Не ПараметрыОбновленияФакторов.РазрешитьРаботуЗадания Тогда
		
		РасписанияРегламентныхЗаданий.ОперацияВыполнена(
			ИдентификаторОперации,
			РасписанияРегламентныхЗаданий.ОписаниеПараметровВыполнения(1435));
			
		Возврат;
	КонецЕсли;
	
	Если Не РасписанияРегламентныхЗаданий.ТребуетсяВыполнение(ИдентификаторОперации) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ОбновлениеФакторовРисковСПАРКРазделениеВключено();
	Иначе
		ОбновлениеФакторовРисковСПАРКРазделениеВыключено();
	КонецЕсли;
	
	РасписанияРегламентныхЗаданий.ОперацияВыполнена(
		ИдентификаторОперации,
		РасписанияРегламентныхЗаданий.ОписаниеПараметровВыполнения(1435));
	
КонецПроцедуры

Процедура ОбновлениеФакторовРисковСПАРКРазделениеВключено()
	
	КоличествоКонтрагентовКОбновлению = 1000;
	
	ПараметрыПолученияФакторов = ПараметрыПолученияФакторовИзСервиса();
	ПараметрыПолученияФакторов.ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	ПараметрыПолученияФакторов.ЭтоВызовРегламентногоЗадания = Истина;
	
	ОбластиБезУслуги = Новый Массив;
	
	// Для ИБ в модели сервиса необходимо загрузить данные по каждой области данных.
	// По каждой области данных факторов загружаются со своими данными аутентификации.
	// Области данных со статусом <> Перечисление.СтатусыОбластейДанных.Используется не обрабатываются.
	// Определим список контрагентов, по которым есть кэш - их и надо обновить.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФакторыРискаСПАРК.ОбластьДанныхВспомогательныеДанные КАК ОбластьДанных,
		|	ФакторыРискаСПАРК.ИНН                                КАК ИНН
		|ИЗ
		|	РегистрСведений.ФакторыРискаСПАРК КАК ФакторыРискаСПАРК
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ОбластиДанных КАК ОбластиДанных
		|	ПО
		|		(ОбластиДанных.ОбластьДанныхВспомогательныеДанные = ФакторыРискаСПАРК.ОбластьДанныхВспомогательныеДанные)
		|		И (ОбластиДанных.Статус = &СтатусИспользуется)
		|		И НЕ (ОбластиДанных.ОбластьДанныхВспомогательныеДанные В (&ОбластиБезУслуги))
		|
		|ГДЕ
		|	РегФакторы.ДатаАктуальности <= &ДатаАктуальности
		|УПОРЯДОЧИТЬ ПО
		|	ФакторыРискаСПАРК.ОбластьДанныхВспомогательныеДанные";
	
	Запрос.УстановитьПараметр("ОбластиБезУслуги", ОбластиБезУслуги);
	Запрос.УстановитьПараметр("ДатаАктуальности", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр(
		"СтатусИспользуется",
		ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.СтатусыОбластейДанных.Используется"));
		
	КоличествоПопыток = 0;
	ОтсутствуютОшибки = Истина;
	ОжиданиеОтвета = Истина;
	
	Пока ОтсутствуютОшибки
		И ОжиданиеОтвета Цикл
		
		ОжиданиеОтвета = Ложь;
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Прервать;
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("ОбластьДанных") Цикл
			
			// Если услуга не подключена, то продолжить со следующей области данных.
			// В этой области данных данные останутся просроченными.
			// В кэш ошибка записываться не будет.
			УслугаПодключена = ИнтернетПоддержкаПользователей.УслугаПодключена(
				СПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска(),
				Выборка.ОбластьДанных);
			Если Не УслугаПодключена Тогда
				ОбластиБезУслуги.Добавить(Выборка.ОбластьДанных);
				Продолжить;
			КонецЕсли;
			ОписанияКонтрагентов = Новый Массив;
			Пока Выборка.СледующийПоЗначениюПоля("ИНН") Цикл
				ОписаниеКонтрагента = НовыйОписаниеКонтрагентаФакторыРиска();
				ОписаниеКонтрагента.ОбластьДанных  = Выборка.ОбластьДанных;
				ОписаниеКонтрагента.ИНН            = СокрЛП(Выборка.ИНН);
				ОписаниеКонтрагента.ВидКонтрагента = СПАРКРискиКлиентСервер.ВидКонтрагента(ОписаниеКонтрагента.ИНН);
				ОписанияКонтрагентов.Добавить(ОписаниеКонтрагента);
				Если ОписанияКонтрагентов.Количество() >= КоличествоКонтрагентовКОбновлению Тогда
					РезультатВызоваВебСервиса = ЗагрузитьФакторыРисков(
						Выборка.ОбластьДанных,
						ОписанияКонтрагентов,
						ПараметрыПолученияФакторов);
					ОписанияКонтрагентов = Новый Массив;
					// Если в процессе загрузки факторов возникали ошибки (ошибки соединения, ошибки подключения и т.п.),
					//  то пропустить загрузку данных по этой области.
					Если Не РезультатВызоваВебСервиса.ВидОшибки.Пустая() Тогда
						ОтсутствуютОшибки = Ложь;
						Прервать;
					КонецЕсли;
					ОжиданиеОтвета = ОжиданиеОтвета Или РезультатВызоваВебСервиса.ОжиданиеОтвета;
				КонецЕсли;
			КонецЦикла;
			Если ОтсутствуютОшибки
				И ОписанияКонтрагентов.Количество() > 0 Тогда
				
				РезультатВызоваВебСервиса = ЗагрузитьФакторыРисков(
					Выборка.ОбластьДанных,
					ОписанияКонтрагентов,
					ПараметрыПолученияФакторов);
				Если Не РезультатВызоваВебСервиса.ВидОшибки.Пустая() Тогда
					ОтсутствуютОшибки = Ложь;
					Прервать;
				КонецЕсли;
				ОжиданиеОтвета = ОжиданиеОтвета Или РезультатВызоваВебСервиса.ОжиданиеОтвета;
				
			КонецЕсли;
			
		КонецЦикла;
		
		КоличествоПопыток = КоличествоПопыток + 1;
		
		Если КоличествоПопыток = ОграничениеКоличестваЗапросов() Тогда
			
			ОтсутствуютОшибки = Ложь;
			СПАРКРиски.ЗаписатьОшибкуВЖурналРегистрации(
				НСтр("ru = 'Не удалось загрузить обновления факторов риска СПАРК за отведенное количество попыток.'"));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновлениеФакторовРисковСПАРКРазделениеВыключено()
	
	КоличествоКонтрагентовКОбновлению = 1000;
	
	ПараметрыПолученияФакторов = ПараметрыПолученияФакторовИзСервиса();
	ПараметрыПолученияФакторов.ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	ПараметрыПолученияФакторов.ЭтоВызовРегламентногоЗадания = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	-1 КАК ОбластьДанных,
		|	ФакторыРискаСПАРК.ИНН КАК ИНН
		|ИЗ
		|	РегистрСведений.ФакторыРискаСПАРК КАК ФакторыРискаСПАРК
		|ГДЕ
		|	ФакторыРискаСПАРК.ДатаАктуальности <= &ДатаАктуальности";
	
	Запрос.УстановитьПараметр("ДатаАктуальности", ТекущаяДатаСеанса());
	
	КоличествоПопыток = 0;
	ОтсутствуютОшибки = Истина;
	ОжиданиеОтвета = Истина;
	Пока ОтсутствуютОшибки
		И ОжиданиеОтвета Цикл
		
		ОжиданиеОтвета = Ложь;
	
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Прервать;
		КонецЕсли;
		
		ОписанияКонтрагентов = Новый Массив;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ОписаниеКонтрагента = НовыйОписаниеКонтрагентаФакторыРиска();
			ОписаниеКонтрагента.ОбластьДанных  = Выборка.ОбластьДанных;
			ОписаниеКонтрагента.ИНН            = СокрЛП(Выборка.ИНН);
			ОписаниеКонтрагента.ВидКонтрагента = СПАРКРискиКлиентСервер.ВидКонтрагента(ОписаниеКонтрагента.ИНН);
			ОписанияКонтрагентов.Добавить(ОписаниеКонтрагента);
			Если ОписанияКонтрагентов.Количество() >= КоличествоКонтрагентовКОбновлению Тогда
				РезультатВызоваВебСервиса = ЗагрузитьФакторыРисков(
					Выборка.ОбластьДанных,
					ОписанияКонтрагентов,
					ПараметрыПолученияФакторов);
				ОписанияКонтрагентов = Новый Массив;
				Если Не РезультатВызоваВебСервиса.ВидОшибки.Пустая() Тогда
					ОтсутствуютОшибки = Ложь;
					Прервать;
				КонецЕсли;
				ОжиданиеОтвета = ОжиданиеОтвета Или РезультатВызоваВебСервиса.ОжиданиеОтвета;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ОтсутствуютОшибки
			И ОписанияКонтрагентов.Количество() > 0 Тогда
			
			РезультатВызоваВебСервиса = ЗагрузитьФакторыРисков(
				Выборка.ОбластьДанных,
				ОписанияКонтрагентов,
				ПараметрыПолученияФакторов);
			
			Если Не РезультатВызоваВебСервиса.ВидОшибки.Пустая() Тогда
				ОтсутствуютОшибки = Ложь;
				Прервать;
			КонецЕсли;
			
			ОжиданиеОтвета = ОжиданиеОтвета Или РезультатВызоваВебСервиса.ОжиданиеОтвета;
			
		КонецЕсли;
		
		КоличествоПопыток = КоличествоПопыток + 1;
		Если КоличествоПопыток = ОграничениеКоличестваЗапросов() Тогда
			
			ОтсутствуютОшибки = Ложь;
			СПАРКРиски.ЗаписатьОшибкуВЖурналРегистрации(
				НСтр("ru = 'Не удалось загрузить обновления факторов риска СПАРК за отведенное количество попыток.'"));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОграничениеКоличестваЗапросов()
	
	Возврат 10;
	
КонецФункции

// Возвращает структуру контрагента для получения по нему факторов риска СПАРК.
//
// Возвращаемое значение:
//   Структура - Структура возвращаемых данных с ключами:
//     * ВидКонтрагента - ПеречислениеСсылка.ВидыКонтрагентовСПАРКРиски - вид проверки данных контрагента;
//     * ОбластьДанных  - Число - номер области данных или -1 при работе не в модели сервиса.
//     * ИНН            - Строка.
//
Функция НовыйОписаниеКонтрагентаФакторыРиска() Экспорт
	
	Результат = Новый Структура();
	
	Результат.Вставить("ВидКонтрагента"  , Неопределено);
	Результат.Вставить("ОбластьДанных", -1);
	Результат.Вставить("ИНН"          , "");
	
	Возврат Результат;

КонецФункции

// Выполняет настройку обновления факторов риска СПАРК.
//
Процедура НастройкаОбновленияФакторовРискаСПАРК(Параметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СПАРКРиски.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Добавление регламентного задания ОбновлениеФакторовРисковСПАРК. Начало обновления.'"));
	
	ДобавитьРегламентноеЗаданиеОбновлениеФакторовРисковСПАРК();
	
	СПАРКРиски.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Добавление регламентного задания ОбновлениеФакторовРисковСПАРК. Успешно завершено.'"));
	
КонецПроцедуры

#КонецОбласти

// Выводит информацию о факторах риска в элемент управления.
// В случае, когда отсутствует реквизит "ФакторыРискаСПАРК", информация не выводится.
// В случае, если информации нет в кэше, то инициируется фоновое задание.
//
// Параметры:
//  ФакторыРиска         - Структура - См. СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
//  Форма                - ФормаКлиентскогоПриложения - форма, в которой необходимо вывести информацию о факторах риска.
//  КонтрагентОбъект     - ОпределяемыйТип.КонтрагентБИПОбъект, Неопределено - заполняется в том случае, если форма
//                         это форма элемента справочника, а не форма документа.
//  Контрагент           - ОпределяемыйТип.КонтрагентБИП, Строка - Контрагент или ИНН контрагента;
//  ПараметрыОтображения - Структура - прочие параметры. См. СПАРКРискиКлиентСервер.ПараметрыОтображенияДанныхСПАРК.
//
Процедура ОтобразитьФакторыРиска(
		ФакторыРиска,
		Форма,
		КонтрагентОбъект,
		Контрагент,
		ПараметрыОтображения = Неопределено) Экспорт
	
	ИспользованиеРазрешено = СПАРКРиски.ИспользованиеРазрешено();
	
	Если ИспользованиеРазрешено Тогда
		Если ТипЗнч(ФакторыРиска) <> Тип("Структура")
			Или ФакторыРиска.ОжиданиеОтвета Тогда
			
			ПараметрыВыполнения = СПАРКФакторыРискаКлиентСервер.ПараметрыПолученияФакторов();
			ПараметрыВыполнения.ИдентификаторФормы = Форма.УникальныйИдентификатор;
			ФакторыРиска = ФакторыРискаКонтрагента(
				Контрагент,
				ПараметрыВыполнения);
		КонецЕсли;
	КонецЕсли;
	
	Форма.ФакторыРискаСПАРК = ФакторыРиска;
	СПАРКФакторыРискаКлиентСервер.ОтобразитьФакторыРиска(
		ФакторыРиска,
		КонтрагентОбъект,
		Контрагент,
		Форма,
		ИспользованиеРазрешено,
		ПараметрыОтображения);
	
КонецПроцедуры

#Область ФакторыРискаКонтрагента

Функция ФакторыРискаКонтрагентаИзСервиса(
	ОбластьДанных,
	ОписаниеКонтрагента,
	Параметры)
	
	Результат = ФакторыРискаИзКэша(ОписаниеКонтрагента.ИНН);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеКонтрагентов = Новый Массив;
	ОписаниеКонтрагентов.Добавить(ОписаниеКонтрагента);
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		Параметры.ИдентификаторФормы);
		
	ПараметрыЗагрузки = ПараметрыПолученияФакторовИзСервиса();
	ПараметрыЗагрузки.ИдентификаторЗапроса = Строка(Параметры.ИдентификаторФормы) + "_" + ОписаниеКонтрагента.ИНН;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"СПАРКФакторыРиска.ЗагрузитьФакторыРисков",
		ОбластьДанных,
		ОписаниеКонтрагентов,
		ПараметрыЗагрузки);
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		Результат = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
		Результат.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
		Результат.ОжиданиеОтвета = Истина;
		Результат.ДлительнаяОперация = ДлительнаяОперация;
	ИначеЕсли ДлительнаяОперация.Статус = "Выполнено" Тогда
		РезультатЗагрузки = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если РезультатЗагрузки.ВидОшибки.Пустая() Тогда
			Результат = РезультатЗагрузки.ЗначенияФакторов.Получить(ОписаниеКонтрагента.ИНН);
		Иначе
			Результат = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
			Результат.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
			ЗаполнитьЗначенияСвойств(Результат, РезультатЗагрузки);
		КонецЕсли;
	ИначеЕсли ДлительнаяОперация.Статус = "Отменено" Тогда
		Результат = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
		Результат.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
		Результат.ВидОшибки   = Перечисления.ВидыОшибокСПАРКРиски.НеизвестнаяОшибка;
		Результат.ТекстОшибки = НСтр("ru='Выполнение фонового задания отменено администратором'");
	Иначе
		Результат = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
		Результат.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
		Результат.ВидОшибки   = Перечисления.ВидыОшибокСПАРКРиски.НеизвестнаяОшибка;
		Результат.ТекстОшибки = ДлительнаяОперация.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ФакторыРискаИзКэша(ИНН)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФакторыРискаСПАРК.ФакторыРиска КАК ФакторыРиска
		|ИЗ
		|	РегистрСведений.ФакторыРискаСПАРК КАК ФакторыРискаСПАРК
		|ГДЕ
		|	ФакторыРискаСПАРК.ИНН = &ИНН
		|	И ФакторыРискаСПАРК.ДатаАктуальности > &ДатаАктуальности";
	
	Запрос.УстановитьПараметр("ДатаАктуальности", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат = Выборка.ФакторыРиска.Получить();
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДатаАктуальностиФакторовРиска()
	Возврат ТекущаяДатаСеанса() + 24*60*60;
КонецФункции

Функция ЗагрузитьФакторыРисков(
		ОбластьДанных,
		ОписаниеКонтрагентов,
		ПараметрыВыполнения) Экспорт
	
	Результат = СПАРКРискиСервис.НовыйРезультатВызоваСервиса();
	Результат.Вставить("ЗначенияФакторов", Новый Соответствие);
	Результат.Вставить("ИдентификаторыКлассификаторов", ИдентификаторыКлассификаторов());
	
	// Проверка, подключена ли интернет-поддержка.
	ДанныеАутентификации = Неопределено;
	СПАРКРискиСервис.ПередВызовомОперацииСервиса(
		Результат,
		?(ОбластьДанных = -1, Неопределено, ОбластьДанных),
		ДанныеАутентификации);
	
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПолучитьДанныеФакторовРисковИзСервиса(
		Результат,
		ДанныеАутентификации,
		ОписаниеКонтрагентов,
		ПараметрыВыполнения);
	
	УстановитьПривилегированныйРежим(Истина);
	ПолучитьПредставленияПоИдентификаторуРиска(Результат);
	ЗаполнитьОписаниеРисков(Результат);
	
	СохранитьДанныеФакторовРискаВКэше(Результат);
	
	Результат.Вставить(
		"ОжиданиеОтвета",
		ТребуетсяОжиданиеОтвета(Результат.ЗначенияФакторов));
	
	Возврат Результат;
	
КонецФункции

Процедура ПолучитьДанныеФакторовРисковИзСервиса(
		Результат,
		ДанныеАутентификации,
		ОписаниеКонтрагентов,
		ПараметрыЗапроса)
	
	// В сервисе существует ограничение по количеству ИНН в вызове.
	// Для потребителя сервис вызывается прозрачно - без разбиения списка.
	ПараметрыЗапроса.Вставить(
		"ДополнительныеПараметры",
		СПАРКРискиСервис.ДополнительныеПараметрыВызоваОперацииСервиса());
	
	НомерПачки        = 1;
	ПараметрСписокИНН = Новый Массив;
	ОбработанныеИНН   = Новый Соответствие;
	РазмерЗапроса     = 0;
	
	// 1. Запрос данных юридических лиц
	Для Каждого ТекущийКонтрагент Из ОписаниеКонтрагентов Цикл
		
		// Не заполненное значение может быть в случае если не завершено
		// отложенное обновление информационной базы.
		Если ЗначениеЗаполнено(ТекущийКонтрагент.ВидКонтрагента)
			И ТекущийКонтрагент.ВидКонтрагента <> Перечисления.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбработанныеИНН[ТекущийКонтрагент.ИНН] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОбработанныеИНН.Вставить(ТекущийКонтрагент.ИНН, Истина);
		
		ПараметрСписокИНН.Добавить(ТекущийКонтрагент.ИНН);
		РазмерЗапроса = РазмерЗапроса + 1;
		Если РазмерЗапроса >= РазмерЗапросаФакторыРиска() Тогда
			ВызватьОперациюФакторыРискаЮридическогоЛица(
				ПараметрСписокИНН,
				Результат,
				ДанныеАутентификации,
				ПараметрыЗапроса);
			НомерПачки = НомерПачки + 1;
			ПараметрСписокИНН = Новый Массив;
			РазмерЗапроса = 0;
			Если Не Результат.ВидОшибки.Пустая() Тогда
				// При ошибке вызова - возвратить результат.
				// При наличии успешных вызовов возвращается частичный результат.
				Прервать;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Если РазмерЗапроса > 0 Тогда
		ВызватьОперациюФакторыРискаЮридическогоЛица(
			ПараметрСписокИНН,
			Результат,
			ДанныеАутентификации,
			ПараметрыЗапроса);
	КонецЕсли;
	
	СПАРКРискиСервис.ПослеВызоваОперацииСервиса(Результат);
	
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	НомерПачки        = 1;
	ПараметрСписокИНН = Новый Массив;
	РазмерЗапроса     = 0;
	
	// 2. Запрос данных индивидуальных предпринимателей
	Для Каждого ТекущийКонтрагент Из ОписаниеКонтрагентов Цикл
		
		Если ТекущийКонтрагент.ВидКонтрагента <> Перечисления.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбработанныеИНН[ТекущийКонтрагент.ИНН] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОбработанныеИНН.Вставить(ТекущийКонтрагент.ИНН, Истина);
		
		ПараметрСписокИНН.Добавить(ТекущийКонтрагент.ИНН);
		РазмерЗапроса = РазмерЗапроса + 1;
		Если РазмерЗапроса >= РазмерЗапросаФакторыРиска() Тогда
			ВызватьОперациюФакторыРискаИндивидуальногоПредпринимателя(
				ПараметрСписокИНН,
				Результат,
				ДанныеАутентификации,
				ПараметрыЗапроса);
			НомерПачки = НомерПачки + 1;
			ПараметрСписокИНН = Новый Массив;
			РазмерЗапроса = 0;
			Если Не Результат.ВидОшибки.Пустая() Тогда
				// При ошибке вызова - возвратить результат.
				// При наличии успешных вызовов возвращается частичный результат.
				Прервать;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Если РазмерЗапроса > 0 Тогда
		ВызватьОперациюФакторыРискаИндивидуальногоПредпринимателя(
			ПараметрСписокИНН,
			Результат,
			ДанныеАутентификации,
			ПараметрыЗапроса);
	КонецЕсли;
	
	СПАРКРискиСервис.ПослеВызоваОперацииСервиса(Результат);

КонецПроцедуры

Функция ПараметрыПолученияФакторовИзСервиса()
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоВызовРегламентногоЗадания", Ложь);
	Результат.Вставить("НомерСеанса", Формат(НомерСеансаИнформационнойБазы(), "ЧГ="));
	Результат.Вставить("ИдентификаторЗапроса", Неопределено);
	Результат.Вставить("ИдентификаторИнформационнойБазы", СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы());
	
	Возврат Результат;
	
КонецФункции

Процедура СохранитьДанныеФакторовРискаВКэше(Результат)
	
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЗначенияФакторов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	НачатьТранзакцию();
	Попытка
		
		Для Каждого ЗначениеФакторовРиска Из Результат.ЗначенияФакторов Цикл
			
			ДанныеФактораРиска = ЗначениеФакторовРиска.Значение;
			Если ДанныеФактораРиска.ОжиданиеОтвета Тогда
				Продолжить;
			КонецЕсли;
			Запись = РегистрыСведений.ФакторыРискаСПАРК.СоздатьМенеджерЗаписи();
			Запись.ИНН = ДанныеФактораРиска.ИНН;
			Запись.ФакторыРиска = Новый ХранилищеЗначения(ДанныеФактораРиска, Новый СжатиеДанных(9));
			Запись.ДатаАктуальности = ДанныеФактораРиска.ДатаАктуальности;
			Запись.Записать(Истина);
		
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция ТребуетсяОжиданиеОтвета(ЗначенияФакторов)
	
	Результат = Ложь;
	
	Для Каждого ЗначениеФакторовРиска Из ЗначенияФакторов Цикл
		ДанныеФактораРиска = ЗначениеФакторовРиска.Значение;
		Если ДанныеФактораРиска.ОжиданиеОтвета Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции /organization/risk-factors/get-risk-factors

Процедура ВызватьОперациюФакторыРискаЮридическогоЛица(
		СписокИНН,
		Результат,
		ДанныеАутентификации,
		Параметры)
	
	URLОперации = СПАРКРискиСервис.URLОперацииСервиса("rest/organization/risk-factors/get-risk-factors");
	СПАРКРискиСервис.ЗаполнитьТикетАутентификации(ДанныеАутентификации, URLОперации, Результат);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ТелоЗапроса = НовыйТелоЗапросаOrganizationRiskFactorsGetRiskFactors(
		СписокИНН,
		ДанныеАутентификации,
		Параметры.ДополнительныеПараметры);

	ТелоОтвета = "";
	ПараметрыЗапроса = СПАРКРискиСервис.НовыйПараметрыЗапросаКСервису();
	ПараметрыЗапроса.ДанныеДляОбработки = ТелоЗапроса;
	Если ИспользоватьGZip() Тогда
		ПараметрыЗапроса.Заголовки.Вставить("Accept-Encoding", "gzip");
	КонецЕсли;
	ЗаполнитьЗаголовки(ПараметрыЗапроса.Заголовки, Параметры);
	СПАРКРискиСервис.ВызватьОперациюСервиса(
		URLОперации,
		ПараметрыЗапроса,
		Результат,
		ТелоОтвета);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ВидКонтрагента = Перечисления.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо;
	
	Попытка
		ОбработатьТелоОтветаOrganizationRiskFactorsGetRiskFactors(ТелоОтвета, Результат);
		Для Каждого ИНН Из СписокИНН Цикл
			Если Результат.ЗначенияФакторов.Получить(ИНН) = Неопределено Тогда
				РезультатФакторыРиска = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
				РезультатФакторыРиска.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
				РезультатФакторыРиска.ИНН = ИНН;
				РезультатФакторыРиска.ВидКонтрагента = ВидКонтрагента;
				РезультатФакторыРиска.ПодлежитПроверке = СПАРКРиски.ИННСоответствуетТребованиям(ИНН, ВидКонтрагента);
				РезультатФакторыРиска.ДатаАктуальности = ДатаАктуальностиФакторовРиска();
				Результат.ЗначенияФакторов.Вставить(ИНН, РезультатФакторыРиска);
			КонецЕсли;
			
		КонецЦикла;
	Исключение
		СПАРКРискиСервис.ПриОшибкеОбработкиОтветаСервиса(ИнформацияОбОшибке(), URLОперации, ТелоОтвета, Результат);
	КонецПопытки;
	
КонецПроцедуры

Функция НовыйТелоЗапросаOrganizationRiskFactorsGetRiskFactors(
	СписокИНН,
	ДанныеАутентификации,
	ДополнительныеПараметрыЗапроса)
	
	Возврат СПАРКРискиСервис.НовыйТелоЗапросаСписокИНН(
		СписокИНН,
		0,
		СписокИНН.ВГраница(),
		ДанныеАутентификации,
		ДополнительныеПараметрыЗапроса);
	
КонецФункции

Процедура ОбработатьТелоОтветаOrganizationRiskFactorsGetRiskFactors(ТелоОтвета, Результат)
	// {
	//   "items": [
	//     {
	//       "inn": "string",
	//       "status": "string",
	//       "dataTimestamp": "string",
	//       "data": {}
	//     }
	//   ]
	// }
	ТипСтруктура = Тип("Структура");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоОтвета);
	ДанныеОтвета = ПрочитатьJSON(ЧтениеОтвета, Ложь);
	ЧтениеОтвета.Закрыть();

	Если ТипЗнч(ДанныеОтвета) <> ТипСтруктура Тогда
		Возврат;
	КонецЕсли;
	
	ВидКонтрагента = Перечисления.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо;
	// АПК:1415-выкл, АПК:1416-выкл Данные из внешних источников
	Для Каждого ДанныеКонтрагента Из ДанныеОтвета.items Цикл
		РезультатФакторыРиска = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
		РезультатФакторыРиска.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
		РезультатФакторыРиска.ВидКонтрагента = ВидКонтрагента;
		Если ДанныеКонтрагента.status = "UPDATE_IN_PROGRESS" Тогда
			РезультатФакторыРиска.ИНН = ДанныеКонтрагента.inn;
			РезультатФакторыРиска.ОжиданиеОтвета = Истина;
		Иначе
			РезультатФакторыРиска.ИНН = ДанныеКонтрагента.inn;
			РезультатФакторыРиска.ДатаАктуальности = ДатаАктуальностиФакторовРиска();
			ОбработатьСводныеДанныеОбАрбитражныхДелах(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				Результат.ИдентификаторыКлассификаторов);
			ОбработатьДанныеПо115ФЗ(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				"ФакторыРискаЮридическогоЛицаПо115ФЗ",
				Результат.ИдентификаторыКлассификаторов);
			ОбработатьДанныеГосударственныеКонтракты(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data);
			ОбработатьДанныеПоФакторамРиска(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				"ФакторыРискаЮридическогоЛица",
				Результат.ИдентификаторыКлассификаторов);
			ОбработатьДанныеПоНегативнымСпискам(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				Результат.ИдентификаторыКлассификаторов);
			
		КонецЕсли;
		РезультатФакторыРиска.ПодлежитПроверке = СПАРКРиски.ИННСоответствуетТребованиям(
			РезультатФакторыРиска.ИНН,
			ВидКонтрагента);
		Результат.ЗначенияФакторов.Вставить(РезультатФакторыРиска.ИНН, РезультатФакторыРиска);
	КонецЦикла;
	
	Возврат;
	
	// АПК:1415-вкл, АПК:1416-вкл

КонецПроцедуры

#Область АрбитражныеДела

Процедура ОбработатьСводныеДанныеОбАрбитражныхДелах(Результат, ДанныеОтвета, ИдентификаторыКлассификаторов)
	
	СводныеДанныеОбАрбитражныхДелах = Неопределено;
	Если Не ДанныеОтвета.Свойство("arbitrationSummary", СводныеДанныеОбАрбитражныхДелах)
		Или ТипЗнч(СводныеДанныеОбАрбитражныхДелах) <> Тип("Структура") Тогда
		
		Результат.СводныеДанныеОбАрбитражныхДелах = Неопределено;
		Возврат;
		
	КонецЕсли;
	
	ТипЧисло = Тип("Число");
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат.СводныеДанныеОбАрбитражныхДелах,
		СводныеДанныеОбАрбитражныхДелах,
		"ОбщееКоличествоДел",
		"total",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат.СводныеДанныеОбАрбитражныхДелах,
		СводныеДанныеОбАрбитражныхДелах,
		"КоличествоОбжалуемыхДел",
		"appealed",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат.СводныеДанныеОбАрбитражныхДелах,
		СводныеДанныеОбАрбитражныхДелах,
		"КоличествоЗавершенныхДел",
		"completed",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат.СводныеДанныеОбАрбитражныхДелах,
		СводныеДанныеОбАрбитражныхДелах,
		"КоличествоРассматриваемыхДел",
		"considered",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат.СводныеДанныеОбАрбитражныхДелах,
		СводныеДанныеОбАрбитражныхДелах,
		"КоличествоРешенийИПостановлений",
		"decisionsAndRulings",
		ТипЧисло,
		0);
	
	ОбработатьСводныеДанныеОбАрбитражныхДелахСводноПоТипам(
		Результат.СводныеДанныеОбАрбитражныхДелах.СводныеДанныеПоТипам,
		СводныеДанныеОбАрбитражныхДелах,
		ИдентификаторыКлассификаторов);
	ОбработатьСводныеДанныеОбАрбитражныхДелахСводноПоГодам(
		Результат.СводныеДанныеОбАрбитражныхДелах.СводныеДанныеПоГодам,
		СводныеДанныеОбАрбитражныхДелах);
	ОбработатьСводныеДанныеОбАрбитражныхДелахВКачестве(
		Результат.СводныеДанныеОбАрбитражныхДелах.ДанныеОДелахВКачествеИстца,
		СводныеДанныеОбАрбитражныхДелах,
		"plaintiff",
		ИдентификаторыКлассификаторов);
	ОбработатьСводныеДанныеОбАрбитражныхДелахВКачестве(
		Результат.СводныеДанныеОбАрбитражныхДелах.ДанныеОДелахВКачествеОтветчика,
		СводныеДанныеОбАрбитражныхДелах,
		"defendant",
		ИдентификаторыКлассификаторов);
	
КонецПроцедуры

Процедура ОбработатьСводныеДанныеОбАрбитражныхДелахСводноПоТипам(
		Результат,
		ДанныеОтвета,
		ИдентификаторыКлассификаторов)
	
	СводныеДанныеОбАрбитражныхДелахСводноПоТипам = Неопределено;
	Если Не ДанныеОтвета.Свойство("arbitrationByType", СводныеДанныеОбАрбитражныхДелахСводноПоТипам)
		Или ТипЗнч(СводныеДанныеОбАрбитражныхДелахСводноПоТипам) <> Тип("Массив") Тогда
		
		Возврат;
	КонецЕсли;
	
	ТипЧисло = Тип("Число");
	ВидИдентификатора = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ТипыАрбитража;
	Для Каждого СводныеДанныеОбАрбитражныхДелахСводно Из СводныеДанныеОбАрбитражныхДелахСводноПоТипам Цикл
		СводныеДанные = НовыйСводныеДанныеОбАрбитражныхДелахСводноПоТипам();
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			СводныеДанные,
			СводныеДанныеОбАрбитражныхДелахСводно,
			"ИдентификаторТипа",
			"id",
			ТипЧисло,
			0);
		ИдентификаторыКлассификаторов.Получить(ВидИдентификатора).Вставить(СводныеДанные.ИдентификаторТипа);
		ОбработатьСводныеДанныеОбАрбитражныхДелахСводноПоТипамВКачестве(
			СводныеДанные.ДанныеОДелахВКачествеИстца,
			СводныеДанныеОбАрбитражныхДелахСводно,
			"Plaintiff");
		ОбработатьСводныеДанныеОбАрбитражныхДелахСводноПоТипамВКачестве(
			СводныеДанные.ДанныеОДелахВКачествеОтветчика,
			СводныеДанныеОбАрбитражныхДелахСводно,
			"Defendant");
		Результат.Добавить(СводныеДанные);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСводныеДанныеОбАрбитражныхДелахСводноПоТипамВКачестве(СводныеДанныеОДелах, Родитель, Тег)
	
	СводныеДанныеПоТегу = Неопределено;
	Родитель.Свойство(Тег, СводныеДанныеПоТегу);
	Если СводныеДанныеПоТегу = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипЧисло = Тип("Число");
	СводныеДанныеОДелах = НовыйСводныеДанныеОДелах();
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"ОбщееКоличествоДел",
		"Total",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"СуммаПоВсемДелам",
		"TotalSum",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"КоличествоРассматриваемыхДел",
		"Considered",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"СуммаПоРассматриваемымДелам",
		"ConsideredSum",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"КоличествоОбжалуемыхДел",
		"Appealed",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"СуммаПоОбжалуемымДелам",
		"AppealedSum",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"КоличествоЗавершенныхДел",
		"Completed",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		СводныеДанныеОДелах,
		СводныеДанныеПоТегу,
		"СуммаПоЗавершеннымДелам",
		"CompletedSum",
		ТипЧисло,
		0);
	
КонецПроцедуры

Процедура ОбработатьСводныеДанныеОбАрбитражныхДелахСводноПоГодам(Результат, ДанныеОтвета)
	
	СводныеДанныеОбАрбитражныхДелахСводноПоГодам = Неопределено;
	Если Не ДанныеОтвета.Свойство("arbitrationByYear", СводныеДанныеОбАрбитражныхДелахСводноПоГодам)
		Или ТипЗнч(СводныеДанныеОбАрбитражныхДелахСводноПоГодам) <> Тип("Массив") Тогда
		
		Возврат;
	КонецЕсли;
	
	ТипЧисло = Тип("Число");
	Для Каждого СводныеДанныеОбАрбитражныхДелахСводно Из СводныеДанныеОбАрбитражныхДелахСводноПоГодам Цикл
		СводныеДанные = НовыйСводныеДанныеОбАрбитражныхДелахСводноПоГодам();
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			СводныеДанные,
			СводныеДанныеОбАрбитражныхДелахСводно,
			"Год",
			"year",
			ТипЧисло,
			0);
			
		Если СводныеДанныеОбАрбитражныхДелахСводно.Свойство("plaintiff") Тогда
			
			СводныеДанные.ДанныеОДелахВКачествеИстца = Новый Структура(
				"СуммаПоДелам, КоличествоДел");
			СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
				СводныеДанные.ДанныеОДелахВКачествеИстца,
				СводныеДанныеОбАрбитражныхДелахСводно.plaintiff,
				"СуммаПоДелам",
				"sum",
				ТипЧисло,
				0);
			СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
				СводныеДанные.ДанныеОДелахВКачествеИстца,
				СводныеДанныеОбАрбитражныхДелахСводно.plaintiff,
				"КоличествоДел",
				"casesNumber",
				ТипЧисло,
				0);
		КонецЕсли;
		
		Если СводныеДанныеОбАрбитражныхДелахСводно.Свойство("defendant") Тогда
			
			СводныеДанные.ДанныеОДелахВКачествеОтветчика = Новый Структура(
				"СуммаПоДелам, КоличествоДел");
			СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
				СводныеДанные.ДанныеОДелахВКачествеОтветчика,
				СводныеДанныеОбАрбитражныхДелахСводно.defendant,
				"СуммаПоДелам",
				"sum",
				ТипЧисло,
				0);
			СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
				СводныеДанные.ДанныеОДелахВКачествеОтветчика,
				СводныеДанныеОбАрбитражныхДелахСводно.defendant,
				"КоличествоДел",
				"casesNumber",
				ТипЧисло,
				0);
		КонецЕсли;
		
		Если СводныеДанныеОбАрбитражныхДелахСводно.Свойство("thirdOrOtherPerson") Тогда
			
			СводныеДанные.ДанныеОДелахВКачествеТретьихЛиц = Новый Структура(
				"КоличествоДел");
			СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
				СводныеДанные.ДанныеОДелахВКачествеТретьихЛиц,
				СводныеДанныеОбАрбитражныхДелахСводно.thirdOrOtherPerson,
				"КоличествоДел",
				"casesNumber",
				ТипЧисло,
				0);
		КонецЕсли;
		
		Результат.Добавить(СводныеДанные);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСводныеДанныеОбАрбитражныхДелахВКачестве(
		Результат,
		Родитель,
		Тег,
		ИдентификаторыКлассификаторов)
	
	СводныеДанныеОбАрбитражныхДелахСводно = Неопределено;
	Если Не Родитель.Свойство(Тег, СводныеДанныеОбАрбитражныхДелахСводно)
		Или ТипЗнч(СводныеДанныеОбАрбитражныхДелахСводно) <> Тип("Структура") Тогда
		Результат = Неопределено;
		Возврат;
	КонецЕсли;
	
	ТипЧисло = Тип("Число");
	Результат = СПАРКФакторыРискаКлиентСервер.НовыйДанныеОДелах();
	Результат.Вставить("ДанныеОЗавершенныхДелах", Новый Массив);
	Результат.Вставить("ДанныеОРешенияхИПостановлениях", Новый Массив);

	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"ОбщееКоличествоДел",
		"total",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"СуммаПоВсемДелам",
		"totalSum",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"КоличествоРассматриваемыхДел",
		"considered",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"СуммаПоРассматриваемымДелам",
		"consideredSum",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"КоличествоОбжалуемыхДел",
		"appealed",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"СуммаПоОбжалуемымДелам",
		"appealedSum",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"КоличествоРешенийИПостановлений",
		"decisionsAndRulings",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"СуммаПоРешениямИПостановлениям",
		"decisionsAndRulingsSum",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"КоличествоЗавершенныхДел",
		"completed",
		ТипЧисло,
		0);
	СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
		Результат,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"СуммаПоЗавершеннымДелам",
		"completedSum",
		ТипЧисло,
		0);
		
	ОбработатьСводныеДанныеОбАрбитражныхДелахВКачествеПоТипу(
		Результат.ДанныеОЗавершенныхДелах,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"completedByResult",
		ИдентификаторыКлассификаторов);
	ОбработатьСводныеДанныеОбАрбитражныхДелахВКачествеПоТипу(
		Результат.ДанныеОРешенияхИПостановлениях,
		СводныеДанныеОбАрбитражныхДелахСводно,
		"DecisionsAndRulingsByResult",
		ИдентификаторыКлассификаторов);
	
КонецПроцедуры

Процедура ОбработатьСводныеДанныеОбАрбитражныхДелахВКачествеПоТипу(
		Результат,
		Родитель,
		Тег,
		ИдентификаторыКлассификаторов)
	
	СводныеДанныеОбАрбитражныхДелахСводноПоТипам = Неопределено;
	Если Не Родитель.Свойство(Тег, СводныеДанныеОбАрбитражныхДелахСводноПоТипам)
		Или ТипЗнч(СводныеДанныеОбАрбитражныхДелахСводноПоТипам) <> Тип("Массив") Тогда
	
		Возврат;
	КонецЕсли;
	
	ТипЧисло = Тип("Число");
	ВидИдентификатора = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ТипыАрбитражныхРешений;
	Для Каждого СводныеДанныеОбАрбитражныхДелахСводно Из СводныеДанныеОбАрбитражныхДелахСводноПоТипам Цикл
		СводныеДанные = НовыйСводныеДанныеОбАрбитражныхДелахСводноПоТипу();
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			СводныеДанные,
			СводныеДанныеОбАрбитражныхДелахСводно,
			"ИдентификаторТипа",
			"id",
			ТипЧисло,
			0);
		ИдентификаторыКлассификаторов.Получить(ВидИдентификатора).Вставить(СводныеДанные.ИдентификаторТипа);
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			СводныеДанные,
			СводныеДанныеОбАрбитражныхДелахСводно,
			"СуммаПоДелам",
			"sum",
			ТипЧисло,
			0);
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			СводныеДанные,
			СводныеДанныеОбАрбитражныхДелахСводно,
			"КоличествоДел",
			"casesNumber",
			ТипЧисло,
			0);
		Результат.Добавить(СводныеДанные);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РискиПо115ФЗ

Процедура ОбработатьДанныеПо115ФЗ(Результат, ДанныеОтвета, Ключ, ИдентификаторыКлассификаторов)
	
	СводныеДанныеПоРискамПо115ФЗ = Неопределено;
	Если Не ДанныеОтвета.Свойство("federalLaw115RiskFactors", СводныеДанныеПоРискамПо115ФЗ)
		Или ТипЗнч(СводныеДанныеПоРискамПо115ФЗ) <> Тип("Массив") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Результат[Ключ] = Новый Массив;
	
	ТипЧисло = Тип("Число");
	ВидИдентификатораРиска = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК[Ключ];
	ИдентификаторыКлассификаторовРиска = ИдентификаторыКлассификаторов.Получить(ВидИдентификатораРиска);
	Для Каждого ДанныеПоРискамПо115ФЗ Из СводныеДанныеПоРискамПо115ФЗ Цикл
		
		ДанныеПоРиску = Новый Структура(
			"Идентификатор, ДополнительнаяИнформация, Название",
			0,
			Новый Массив,
			"");
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоРиску,
			ДанныеПоРискамПо115ФЗ,
			"Идентификатор",
			"id",
			ТипЧисло,
			0);
		ИдентификаторыКлассификаторовРиска.Вставить(ДанныеПоРиску.Идентификатор);
		ДобавитьДополнительнуюИнформациюПоРиску(
			ДанныеПоРискамПо115ФЗ,
			ДанныеПоРиску,
			ИдентификаторыКлассификаторов);
		
		Результат[Ключ].Добавить(ДанныеПоРиску);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ГосударственныеКонтракты

Процедура ОбработатьДанныеГосударственныеКонтракты(Результат, ДанныеОтвета)
	
	ДанныеГосударственныеКонтракты = Неопределено;
	Если Не ДанныеОтвета.Свойство("stateContractsSummary", ДанныеГосударственныеКонтракты)
		Или ТипЗнч(ДанныеГосударственныеКонтракты) <> Тип("Структура") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗагрузитьДанныеОГосударственныхКонтрактах(
		Результат.ДанныеГосударственныеКонтракты.ДанныеОГосКонтрактахПо94ФЗИ44ФЗ,
		ДанныеГосударственныеКонтракты,
		"FederalLaw94",
		Результат.ДанныеГосударственныеКонтракты);
	ЗагрузитьДанныеОГосударственныхКонтрактах(
		Результат.ДанныеГосударственныеКонтракты.ДанныеОГосКонтрактахПо223ФЗ,
		ДанныеГосударственныеКонтракты,
		"federalLaw223",
		Результат.ДанныеГосударственныеКонтракты);
		
КонецПроцедуры

Процедура ЗагрузитьДанныеОГосударственныхКонтрактах(
		ДанныеОГосКонтрактах,
		Родитель,
		Тег,
		ДанныеГосударственныеКонтракты);
	
	ДанныеОГосКонтрактахПоФЗ = Неопределено;
	Если Не Родитель.Свойство(Тег, ДанныеОГосКонтрактахПоФЗ)
		Или ТипЗнч(ДанныеОГосКонтрактахПоФЗ) <> Тип("Массив") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДанныеОГосКонтрактах = Новый Массив;
	ТипЧисло  = Тип("Число");
	Для Каждого ДанныеЗаГод Из ДанныеОГосКонтрактахПоФЗ Цикл
		
		ДанныеПоГосКонтрактамЗаГод = Новый Структура;
		ДанныеПоГосКонтрактамЗаГод.Вставить("Год");
		ДанныеПоГосКонтрактамЗаГод.Вставить(
			"Тендеры",
			НовыйДанныеПоТендерамЗаГод());
		ДанныеПоГосКонтрактамЗаГод.Вставить(
			"Контракты",
			НовыйДанныеПоГосКонтрактамЗаГод());
			
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоГосКонтрактамЗаГод,
			ДанныеЗаГод,
			"Год",
			"year",
			ТипЧисло,
			0);
			
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоГосКонтрактамЗаГод.Тендеры,
			ДанныеЗаГод.tenders,
			"КоличествоТендеровСУчастием",
			"admittedNumber",
			ТипЧисло,
			0);
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоГосКонтрактамЗаГод.Тендеры,
			ДанныеЗаГод.tenders,
			"КоличествоВыигранныхТендеров",
			"winnerNumber",
			ТипЧисло,
			0);
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоГосКонтрактамЗаГод.Тендеры,
			ДанныеЗаГод.tenders,
			"КоличествоНедопусковКТендеру",
			"notAdmittedNumber",
			ТипЧисло,
			0);
		
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоГосКонтрактамЗаГод.Контракты,
			ДанныеЗаГод.contracts,
			"СуммаКонтрактов",
			"sum",
			ТипЧисло,
			0);
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоГосКонтрактамЗаГод.Контракты,
			ДанныеЗаГод.contracts,
			"КоличествоКонтрактов",
			"signedNumber",
			ТипЧисло,
			0);
		
		ДанныеГосударственныеКонтракты.ОбщееКоличествоКонтрактов = ДанныеГосударственныеКонтракты.ОбщееКоличествоКонтрактов
			+ ДанныеПоГосКонтрактамЗаГод.Контракты.КоличествоКонтрактов;
		ДанныеГосударственныеКонтракты.ОбщаяСумма = ДанныеГосударственныеКонтракты.ОбщаяСумма
			+ ДанныеПоГосКонтрактамЗаГод.Контракты.СуммаКонтрактов;
		ДанныеГосударственныеКонтракты.ОбщееКоличествоТендеров = ДанныеГосударственныеКонтракты.ОбщееКоличествоТендеров
			+ ДанныеПоГосКонтрактамЗаГод.Тендеры.КоличествоНедопусковКТендеру;
		ДанныеГосударственныеКонтракты.ОбщееКоличествоТендеров = ДанныеГосударственныеКонтракты.ОбщееКоличествоТендеров
			+ ДанныеПоГосКонтрактамЗаГод.Тендеры.КоличествоТендеровСУчастием;
		ДанныеОГосКонтрактах.Добавить(ДанныеПоГосКонтрактамЗаГод);
		
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйДанныеПоТендерамЗаГод()
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоТендеровСУчастием", 0);
	Результат.Вставить("КоличествоВыигранныхТендеров", 0);
	Результат.Вставить("КоличествоНедопусковКТендеру", 0);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйДанныеПоГосКонтрактамЗаГод()
	
	Результат = Новый Структура;
	Результат.Вставить("СуммаКонтрактов", 0);
	Результат.Вставить("КоличествоКонтрактов", 0);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФакторыРиска

Процедура ОбработатьДанныеПоФакторамРиска(Результат, ДанныеОтвета, Ключ, ИдентификаторыКлассификаторов)
	
	СводныеДанныеПоРискам = Неопределено;
	Если Не ДанныеОтвета.Свойство("riskFactors", СводныеДанныеПоРискам)
		Или ТипЗнч(СводныеДанныеПоРискам) <> Тип("Массив") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Результат[Ключ] = Новый Массив;
	
	ТипЧисло  = Тип("Число");
	ВидИдентификатораРиска = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК[Ключ];
	ИдентификаторыКлассификаторовРиска = ИдентификаторыКлассификаторов.Получить(ВидИдентификатораРиска);
	Для Каждого СводныеДанные Из СводныеДанныеПоРискам Цикл
		
		ДанныеПоРиску = Новый Структура(
			"Идентификатор, ДополнительнаяИнформация, Название",
			0,
			Новый Массив,
			"");
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоРиску,
			СводныеДанные,
			"Идентификатор",
			"id",
			ТипЧисло,
			0);
		ИдентификаторыКлассификаторовРиска.Вставить(ДанныеПоРиску.Идентификатор);
		ДобавитьДополнительнуюИнформациюПоРиску(
			СводныеДанные,
			ДанныеПоРиску,
			ИдентификаторыКлассификаторов);
		
		Результат[Ключ].Добавить(ДанныеПоРиску);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область НегативныеСписки

Процедура ОбработатьДанныеПоНегативнымСпискам(Результат, ДанныеОтвета, ИдентификаторыКлассификаторов)
	
	ДанныеПоНегативнымСпискам = Неопределено;
	Если Не ДанныеОтвета.Свойство("NegativeLists", ДанныеПоНегативнымСпискам)
		Или ТипЗнч(ДанныеПоНегативнымСпискам) <> Тип("Массив") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Результат.НегативныеСписки = Новый Массив;
	
	ТипЧисло  = Тип("Число");
	ВидИдентификатораРиска = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.НегативныеСписки;
	ИдентификаторыКлассификаторовРиска = ИдентификаторыКлассификаторов.Получить(ВидИдентификатораРиска);
	Для Каждого СводныеДанные Из ДанныеПоНегативнымСпискам Цикл
		
		ДанныеПоСписку = Новый Структура(
			"Идентификатор, Название, ДополнительнаяИнформация",
			0,
			"",
			Новый Массив);
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеПоСписку,
			СводныеДанные,
			"Идентификатор",
			"id",
			ТипЧисло,
			0);
		
		ИдентификаторыКлассификаторовРиска.Вставить(ДанныеПоСписку.Идентификатор);
		ДобавитьДополнительнуюИнформациюПоСписку(
			СводныеДанные,
			ДанныеПоСписку,
			ИдентификаторыКлассификаторов);
		Результат.НегативныеСписки.Добавить(ДанныеПоСписку);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ДобавитьДополнительнуюИнформациюПоСписку(СводныеДанные, ДанныеПоСписку, ИдентификаторыКлассификаторов)
	
	ДополнительнаяИнформация = Неопределено;
	Если Не СводныеДанные.Свойство("addInfo", ДополнительнаяИнформация) Тогда
		Возврат;
	КонецЕсли;
	
	ТипЧисло  = Тип("Число");
	ТипСтрока = Тип("Строка");
	ИдентификаторыКлассификаторовДопПолей = ИдентификаторыКлассификаторов.Получить(
		Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ДополнительныеПоля);
	Для Каждого ДанныеДополнительнойИнформации Из ДополнительнаяИнформация.addFields Цикл
		ДанныеИнформации = Новый Структура(
			"Идентификатор, Значение, Название");
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеИнформации,
			ДанныеДополнительнойИнформации,
			"Идентификатор",
			"id",
			ТипЧисло,
			0);
		ИдентификаторыКлассификаторовДопПолей.Вставить(ДанныеИнформации.Идентификатор);
		СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
			ДанныеИнформации,
			ДанныеДополнительнойИнформации,
			"Значение",
			"value",
			ТипСтрока,
			"");
		ДанныеПоСписку.ДополнительнаяИнформация.Добавить(ДанныеИнформации);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьДополнительнуюИнформациюПоРиску(СводныеДанные, ДанныеПоРиску, ИдентификаторыКлассификаторов)
	
	ДополнительнаяИнформация = Неопределено;
	Если Не СводныеДанные.Свойство("addInfo", ДополнительнаяИнформация) Тогда
		Возврат;
	КонецЕсли;
	
	ТипЧисло  = Тип("Число");
	ТипСтрока = Тип("Строка");
	ИдентификаторыКлассификаторовДопПолей = ИдентификаторыКлассификаторов.Получить(
		Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ДополнительныеПоля);
	Для Каждого ДанныеДополнительнойИнформации Из ДополнительнаяИнформация Цикл
		ДанныеИнформации = Новый Структура(
			"Название, ДополнительныеПоля",
			Неопределено,
			Новый Массив);
		
		Для Каждого ДополнительныеПоля Из ДанныеДополнительнойИнформации.addFields Цикл
			ДанныеПоля = Новый Структура
				("Идентификатор, Значение, Название");
			СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
				ДанныеПоля,
				ДополнительныеПоля,
				"Идентификатор",
				"id",
				ТипЧисло,
				0);
			ИдентификаторыКлассификаторовДопПолей.Вставить(ДанныеПоля.Идентификатор);
			СПАРКРискиСервис.ПрочитатьЗначениеИзСтруктуры(
				ДанныеПоля,
				ДополнительныеПоля,
				"Значение",
				"value",
				ТипСтрока,
				"");
			ДанныеИнформации.ДополнительныеПоля.Добавить(ДанныеПоля);
		КонецЦикла;
		ДанныеПоРиску.ДополнительнаяИнформация.Добавить(ДанныеИнформации);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПолучитьПредставленияПоИдентификаторуРиска(Результат)
	
	Идентификаторы = Новый ТаблицаЗначений;
	Идентификаторы.Колонки.Добавить(
		"ВидКлассификатора",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКлассификаторовФакторовРискаСПАРК"));
	Идентификаторы.Колонки.Добавить(
		"ИдентификаторРиска",
		ОбщегоНазначения.ОписаниеТипаЧисло(10,0));
	
	Для Каждого ИдентификаторыКлассификатора Из Результат.ИдентификаторыКлассификаторов Цикл
		Для Каждого ИдентификаторКлассификатора Из ИдентификаторыКлассификатора.Значение Цикл
			
			НовСтрока = Идентификаторы.Добавить();
			НовСтрока.ВидКлассификатора = ИдентификаторыКлассификатора.Ключ;
			НовСтрока.ИдентификаторРиска = ИдентификаторКлассификатора.Ключ;
			
		КонецЦикла;
	КонецЦикла;
	
	КоличествоИдентификаторов = Идентификаторы.Количество();
	Если КоличествоИдентификаторов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Классификатор = КлассификаторРисковСПАРК(Идентификаторы);
	Если КоличествоИдентификаторов <> Классификатор.Количество() Тогда
		ОбновитьКлассификаторыФакторовРиска();
		Классификатор = КлассификаторРисковСПАРК(Идентификаторы);
	КонецЕсли;
	
	Для Каждого ИдентификаторКлассификатора Из Классификатор Цикл
		Результат.ИдентификаторыКлассификаторов.Получить(ИдентификаторКлассификатора.ВидКлассификатора).Вставить(
			ИдентификаторКлассификатора.ИдентификаторРиска,
			ИдентификаторКлассификатора.Название);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРисков(Результат)
	Для Каждого ЗначениеФакторовРиска Из Результат.ЗначенияФакторов Цикл
		
		РезультатФакторыРиска = ЗначениеФакторовРиска.Значение;
		Если РезультатФакторыРиска.ОжиданиеОтвета Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьТипыАрбитража(
			РезультатФакторыРиска,
			Результат.ИдентификаторыКлассификаторов);
		ЗаполнитьТипыАрбитражныхРешений(
			РезультатФакторыРиска,
			Результат.ИдентификаторыКлассификаторов);
		ЗаполнитьНегативныеСписки(
			РезультатФакторыРиска,
			Результат.ИдентификаторыКлассификаторов);
		ЗаполнитьПредставленияФакторыРиска(
			РезультатФакторыРиска,
			"ФакторыРискаФизическогоЛица",
			Результат.ИдентификаторыКлассификаторов);
		ЗаполнитьПредставленияФакторыРиска(
			РезультатФакторыРиска,
			"ФакторыРискаФизическогоЛицаПо115ФЗ",
			Результат.ИдентификаторыКлассификаторов);
		ЗаполнитьПредставленияФакторыРиска(
			РезультатФакторыРиска,
			"ФакторыРискаЮридическогоЛица",
			Результат.ИдентификаторыКлассификаторов);
		ЗаполнитьПредставленияФакторыРиска(
			РезультатФакторыРиска,
			"ФакторыРискаЮридическогоЛицаПо115ФЗ",
			Результат.ИдентификаторыКлассификаторов);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТипыАрбитража(Результат, ИдентификаторыКлассификаторов)
	
	Если Результат.СводныеДанныеОбАрбитражныхДелах <> Неопределено
		И Результат.СводныеДанныеОбАрбитражныхДелах.СводныеДанныеПоТипам <> Неопределено Тогда
		
		ВидКлассификатора = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ТипыАрбитража;
		Для Каждого СводныеДанныеПоТипу Из Результат.СводныеДанныеОбАрбитражныхДелах.СводныеДанныеПоТипам Цикл
			
			СводныеДанныеПоТипу.Наименование = ИдентификаторыКлассификаторов
				.Получить(ВидКлассификатора)
					.Получить(СводныеДанныеПоТипу.ИдентификаторТипа);
			
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаполнитьТипыАрбитражныхРешений(Результат, ИдентификаторыКлассификаторов)
	
	Если Результат.СводныеДанныеОбАрбитражныхДелах = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ИдентификаторыКлассификатора = ИдентификаторыКлассификаторов.Получить(
		Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ТипыАрбитражныхРешений);
	ЗаполнитьТипыАрбитражныхРешенийВДанныхОДелах(
		Результат.СводныеДанныеОбАрбитражныхДелах.ДанныеОДелахВКачествеИстца,
		ИдентификаторыКлассификатора);
	ЗаполнитьТипыАрбитражныхРешенийВДанныхОДелах(
		Результат.СводныеДанныеОбАрбитражныхДелах.ДанныеОДелахВКачествеОтветчика,
		ИдентификаторыКлассификатора);
	
КонецПроцедуры

Процедура ЗаполнитьТипыАрбитражныхРешенийВДанныхОДелах(
		ДанныеОДелах,
		ИдентификаторыКлассификатора)
	
	Если ДанныеОДелах = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ЗаполнитьТипыАрбитражныхРешенийВДанныхОДелахУточнение(
		ДанныеОДелах.ДанныеОЗавершенныхДелах,
		ИдентификаторыКлассификатора);
	ЗаполнитьТипыАрбитражныхРешенийВДанныхОДелахУточнение(
		ДанныеОДелах.ДанныеОРешенияхИПостановлениях,
		ИдентификаторыКлассификатора);
	
КонецПроцедуры

Процедура ЗаполнитьТипыАрбитражныхРешенийВДанныхОДелахУточнение(
		ДанныеОДелах,
		ИдентификаторыКлассификатора)
	
	Для Каждого ДанныеОДеле Из ДанныеОДелах Цикл
		ДанныеОДеле.Наименование = ИдентификаторыКлассификатора.Получить(ДанныеОДеле.ИдентификаторТипа);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНегативныеСписки(Результат, ИдентификаторыКлассификаторов)
	
	Если Результат.НегативныеСписки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ИдентификаторыКлассификатора = ИдентификаторыКлассификаторов.Получить(
		Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.НегативныеСписки);
	Для Каждого НегативныйСписок Из Результат.НегативныеСписки Цикл
		НегативныйСписок.Название = ИдентификаторыКлассификатора.Получить(
			НегативныйСписок.Идентификатор);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПредставленияФакторыРиска(Результат, Ключ, ИдентификаторыКлассификаторов)
	
	ФакторыРиска = Результат[Ключ];
	
	Если ФакторыРиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ИдентификаторыКлассификатораРиска = ИдентификаторыКлассификаторов.Получить(
		Перечисления.ВидыКлассификаторовФакторовРискаСПАРК[Ключ]);
	ИдентификаторыКлассификатораДопПолей = ИдентификаторыКлассификаторов.Получить(
		Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ДополнительныеПоля);
	Для Каждого ФакторРиска Из ФакторыРиска Цикл
		ФакторРиска.Название = ИдентификаторыКлассификатораРиска.Получить(
			ФакторРиска.Идентификатор);
		ЗаполнитьДополнительныеПоляФактораРиска(
			ФакторРиска.ДополнительнаяИнформация,
			ИдентификаторыКлассификатораДопПолей);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДополнительныеПоляФактораРиска(
		ДополнительнаяИнформация,
		ИдентификаторыКлассификатора)
	
	Для Каждого ЗначениеДополнительнойИнформации Из ДополнительнаяИнформация Цикл
		Для Каждого ЗначениеДополнительногоПоля Из ЗначениеДополнительнойИнформации.ДополнительныеПоля Цикл
			ЗначениеДополнительногоПоля.Название = ИдентификаторыКлассификатора.Получить(
				ЗначениеДополнительногоПоля.Идентификатор);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция КлассификаторРисковСПАРК(Идентификаторы = Неопределено)
	
	Запрос = Новый Запрос;
	Если Идентификаторы = Неопределено Тогда
		Запрос.Текст =
			"ВЫБРАТЬ
			|	СПАРККлассификаторРисков.ВидКлассификатора КАК ВидКлассификатора,
			|	СПАРККлассификаторРисков.ИдентификаторРиска КАК ИдентификаторРиска,
			|	СПАРККлассификаторРисков.Название КАК Название
			|ИЗ
			|	РегистрСведений.СПАРККлассификаторРисков КАК СПАРККлассификаторРисков";
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Идентификаторы.ВидКлассификатора КАК ВидКлассификатора,
			|	Идентификаторы.ИдентификаторРиска КАК ИдентификаторРиска
			|ПОМЕСТИТЬ Идентификаторы
			|ИЗ
			|	&Идентификаторы КАК Идентификаторы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СПАРККлассификаторРисков.ВидКлассификатора КАК ВидКлассификатора,
			|	СПАРККлассификаторРисков.ИдентификаторРиска КАК ИдентификаторРиска,
			|	СПАРККлассификаторРисков.Название КАК Название
			|ИЗ
			|	РегистрСведений.СПАРККлассификаторРисков КАК СПАРККлассификаторРисков
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Идентификаторы КАК Идентификаторы
			|		ПО СПАРККлассификаторРисков.ВидКлассификатора = Идентификаторы.ВидКлассификатора
			|			И СПАРККлассификаторРисков.ИдентификаторРиска = Идентификаторы.ИдентификаторРиска";
		Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов операции /entrepreneur/risk-factors/get-risk-factors

Процедура ВызватьОперациюФакторыРискаИндивидуальногоПредпринимателя(
		СписокИНН,
		Результат,
		ДанныеАутентификации,
		Параметры)

	URLОперации = СПАРКРискиСервис.URLОперацииСервиса("rest/entrepreneur/risk-factors/get-risk-factors");
	СПАРКРискиСервис.ЗаполнитьТикетАутентификации(ДанныеАутентификации, URLОперации, Результат);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ТелоЗапроса = НовыйТелоЗапросаEnterpreneurRiskFactorsGetRiskFactors(
		СписокИНН,
		ДанныеАутентификации,
		Параметры.ДополнительныеПараметры);

	ТелоОтвета = "";
	ПараметрыЗапроса = СПАРКРискиСервис.НовыйПараметрыЗапросаКСервису();
	ПараметрыЗапроса.ДанныеДляОбработки = ТелоЗапроса;
	Если ИспользоватьGZip() Тогда
		ПараметрыЗапроса.Заголовки.Вставить("Accept-Encoding", "gzip");
	КонецЕсли;
	ЗаполнитьЗаголовки(ПараметрыЗапроса.Заголовки, Параметры);
	СПАРКРискиСервис.ВызватьОперациюСервиса(
		URLОперации,
		ПараметрыЗапроса,
		Результат,
		ТелоОтвета);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ВидКонтрагента = Перечисления.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель;
	
	Попытка
		ОбработатьТелоОтветаEnterpreneurRiskFactorsGetRiskFactors(ТелоОтвета, Результат);
		Для Каждого ИНН Из СписокИНН Цикл
			Если Результат.ЗначенияФакторов.Получить(ИНН) = Неопределено Тогда
				РезультатФакторыРиска = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
				РезультатФакторыРиска.ИНН = ИНН;
				РезультатФакторыРиска.ВидКонтрагента = ВидКонтрагента;
				РезультатФакторыРиска.ПодлежитПроверке = СПАРКРиски.ИННСоответствуетТребованиям(ИНН, ВидКонтрагента);
				РезультатФакторыРиска.ДатаАктуальности = ДатаАктуальностиФакторовРиска();
				Результат.ЗначенияФакторов.Вставить(ИНН, РезультатФакторыРиска);
			КонецЕсли;
			
		КонецЦикла;
	Исключение
		СПАРКРискиСервис.ПриОшибкеОбработкиОтветаСервиса(ИнформацияОбОшибке(), URLОперации, ТелоОтвета, Результат);
	КонецПопытки;

КонецПроцедуры

Функция НовыйТелоЗапросаEnterpreneurRiskFactorsGetRiskFactors(
	СписокИНН,
	ДанныеАутентификации,
	ДополнительныеПараметрыЗапроса)
	
	Возврат СПАРКРискиСервис.НовыйТелоЗапросаСписокИНН(
		СписокИНН,
		0,
		СписокИНН.ВГраница(),
		ДанныеАутентификации,
		ДополнительныеПараметрыЗапроса);
	
КонецФункции

Процедура ОбработатьТелоОтветаEnterpreneurRiskFactorsGetRiskFactors(ТелоОтвета, Результат)
	// {
	//   "items": [
	//     {
	//       "inn": "string",
	//       "status": "string",
	//       "dataTimestamp": "string",
	//       "data": {}
	//     }
	//   ]
	// }
	ТипСтруктура = Тип("Структура");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоОтвета);
	ДанныеОтвета = ПрочитатьJSON(ЧтениеОтвета, Ложь);
	ЧтениеОтвета.Закрыть();

	Если ТипЗнч(ДанныеОтвета) <> ТипСтруктура Тогда
		Возврат;
	КонецЕсли;
	
	ВидКонтрагента = Перечисления.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель;
	
	// АПК:1415-выкл, АПК:1416-выкл Данные из внешних источников
	Для Каждого ДанныеКонтрагента Из ДанныеОтвета.items Цикл
		
		РезультатФакторыРиска = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
		РезультатФакторыРиска.РаботаВМоделиСервиса = ОбщегоНазначения.РазделениеВключено();
		
		РезультатФакторыРиска.ИНН = ДанныеКонтрагента.inn;
		РезультатФакторыРиска.ВидКонтрагента = ВидКонтрагента;
		Если ДанныеКонтрагента.status = "UPDATE_IN_PROGRESS" Тогда
			РезультатФакторыРиска.ОжиданиеОтвета = Истина;
		Иначе
			РезультатФакторыРиска.ДатаАктуальности = ДатаАктуальностиФакторовРиска();
			ОбработатьСводныеДанныеОбАрбитражныхДелах(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				Результат.ИдентификаторыКлассификаторов);
			ОбработатьДанныеПо115ФЗ(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				"ФакторыРискаФизическогоЛицаПо115ФЗ",
				Результат.ИдентификаторыКлассификаторов);
			ОбработатьДанныеГосударственныеКонтракты(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data);
			ОбработатьДанныеПоФакторамРиска(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				"ФакторыРискаФизическогоЛица",
				Результат.ИдентификаторыКлассификаторов);
			ОбработатьДанныеПоНегативнымСпискам(
				РезультатФакторыРиска,
				ДанныеКонтрагента.data,
				Результат.ИдентификаторыКлассификаторов);
			
		КонецЕсли;
		РезультатФакторыРиска.ПодлежитПроверке = СПАРКРиски.ИННСоответствуетТребованиям(
			РезультатФакторыРиска.ИНН,
			ВидКонтрагента);
		Результат.ЗначенияФакторов.Вставить(РезультатФакторыРиска.ИНН, РезультатФакторыРиска);
	КонецЦикла;
	
	Возврат;
	
	// АПК:1415-вкл, АПК:1416-вкл

КонецПроцедуры

Процедура ЗаполнитьЗаголовки(Заголовки, Параметры)
	
	Если Параметры.ЭтоВызовРегламентногоЗадания Тогда
		Сценарий = "SCHEDULED_JOB";
	Иначе
		Сценарий = "RAPID"
	КонецЕсли;
	
	Заголовки.Вставить("X-Use-Case", Сценарий);
	Заголовки.Вставить("X-Client-ID", Параметры.ИдентификаторИнформационнойБазы);
	Заголовки.Вставить("X-Session-ID", Параметры.НомерСеанса);
	Заголовки.Вставить("X-Corellation-ID", Параметры.ИдентификаторЗапроса);
	
КонецПроцедуры

Функция ИспользоватьGZip()
	
	Результат = Ложь;
	
	СистемнаяИнформация = ОбщегоНазначения.СистемнаяИнформацияКлиента();
	Если СистемнаяИнформация = Неопределено Тогда
		СистемнаяИнформация = Новый СистемнаяИнформация;
	КонецЕсли;
	ВерсияПриложения = СистемнаяИнформация.ВерсияПриложения;
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияПриложения, "8.3.26.1498") >= 0 Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Определяет идентификатор операции загрузки факторов риска СПАРК.
//
// Возвращаемое значение:
//  Строка - идентификатор операции.
//
Функция ИдентификаторОперацииЗагрузкаФакторовРиска()
	
	Возврат "ЗагрузкаФакторовРискаСПАРК";
	
КонецФункции

Функция НовыйСводныеДанныеОДелах()
	
	Результат = Новый Структура;
	Результат.Вставить("ОбщееКоличествоДел",           0);
	Результат.Вставить("СуммаПоВсемДелам",             0);
	Результат.Вставить("КоличествоРассматриваемыхДел", 0);
	Результат.Вставить("СуммаПоРассматриваемымДелам",  0);
	Результат.Вставить("КоличествоОбжалуемыхДел",      0);
	Результат.Вставить("СуммаПоОбжалуемымДелам",       0);
	Результат.Вставить("КоличествоЗавершенныхДел",     0);
	Результат.Вставить("СуммаПоЗавершеннымДелам",      0);
	
	Возврат Результат;
КонецФункции

Функция НовыйСводныеДанныеОбАрбитражныхДелахСводноПоТипам()
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторТипа");
	Результат.Вставить("Наименование", "");
	Результат.Вставить("ДанныеОДелахВКачествеИстца");
	Результат.Вставить("ДанныеОДелахВКачествеОтветчика");
	
	Возврат Результат;
	
КонецФункции

Функция НовыйСводныеДанныеОбАрбитражныхДелахСводноПоТипу()
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторТипа");
	Результат.Вставить("Наименование", "");
	Результат.Вставить("СуммаПоДелам");
	Результат.Вставить("КоличествоДел");
	
	Возврат Результат;
	
КонецФункции

Функция НовыйСводныеДанныеОбАрбитражныхДелахСводноПоГодам()
	
	Результат = Новый Структура;
	Результат.Вставить("Год");
	Результат.Вставить("ДанныеОДелахВКачествеИстца");
	Результат.Вставить("ДанныеОДелахВКачествеОтветчика");
	Результат.Вставить("ДанныеОДелахВКачествеТретьихЛиц");
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторыКлассификаторов()
	
	Перечисление = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК;
	Результат = Новый Соответствие;
	Результат.Вставить(Перечисление.ДополнительныеПоля,                  Новый Соответствие);
	Результат.Вставить(Перечисление.НегативныеСписки,                    Новый Соответствие);
	Результат.Вставить(Перечисление.ТипыАрбитража,                       Новый Соответствие);
	Результат.Вставить(Перечисление.ТипыАрбитражныхРешений,              Новый Соответствие);
	Результат.Вставить(Перечисление.ФакторыРискаФизическогоЛица,         Новый Соответствие);
	Результат.Вставить(Перечисление.ФакторыРискаФизическогоЛицаПо115ФЗ,  Новый Соответствие);
	Результат.Вставить(Перечисление.ФакторыРискаЮридическогоЛица,        Новый Соответствие);
	Результат.Вставить(Перечисление.ФакторыРискаЮридическогоЛицаПо115ФЗ, Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

#Область РегламентныеЗадания

// Определяет созданные регламентные задания ОбновлениеФакторовРисковСПАРК.
//
// Возвращаемое значение:
//  Массив из РегламентноеЗадание - массив регламентных заданий см. описание метода РегламентноеЗадание
//  в синтакс-помощнике.
//
Функция ЗаданияОбновлениеФакторовРисковСПАРК()
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбновлениеФакторовРисковСПАРК);
	Возврат РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
КонецФункции

Процедура ДобавитьРегламентноеЗаданиеОбновлениеФакторовРисковСПАРК()
	
	ЗаданияОбновления = ЗаданияОбновлениеФакторовРисковСПАРК();
	
	Если ЗаданияОбновления.Количество() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	Использование = Константы.ИспользоватьСервисСПАРКРиски.Получить();
	
	// Чтобы не создавать пиковых нагрузок на сервис,
	// время обновления будет выбрано случайным образом
	// между 01:00 и 08:00.
	Генератор = Новый ГенераторСлучайныхЧисел;
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ВремяНачала       = Дата("00010101010000") + Генератор.СлучайноеЧисло(0, 25200);
	Расписание.ПериодПовтораДней = 1;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Использование", Использование);
	ПараметрыЗадания.Вставить("Метаданные",    Метаданные.РегламентныеЗадания.ОбновлениеФакторовРисковСПАРК);
	ПараметрыЗадания.Вставить("Расписание",    Расписание);
	ПараметрыЗадания.Вставить("Наименование",  НСтр("ru = 'Обновление факторов рисков СПАРК'"));
	
	Попытка
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	Исключение
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СПАРКРиски.ЗаписатьОшибкуВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось создать регламентное задание обновления факторов риска СПАРК:
					|%1'"),
				ИнформацияОбОшибке));
		ВызватьИсключение ИнформацияОбОшибке;
	КонецПопытки;
	
	СПАРКРиски.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Создано регламентное задание обновления факторов риска СПАРК.'"));
		
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеКлассификатора

Процедура ОбновитьКлассификаторыФакторовРиска(ТелоОтвета = Неопределено)
	
	Результат = СПАРКРискиСервис.НовыйРезультатВызоваСервиса();
	Результат.Вставить("Классификатор", Новый Структура);
	
	СПАРКРискиСервис.ПередВызовомОперацииСервиса(Результат, , , Ложь);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ВызватьОперациюКлассификаторФакторовРиска(Результат, ТелоОтвета);
	СПАРКРискиСервис.ПослеВызоваОперацииСервиса(Результат);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СПАРККлассификаторРисков.СоздатьНаборЗаписей();
	Для Каждого ИдентификаторыВидаКлассификатора Из Результат.Классификатор Цикл
		ВидКлассификатора = ВидКлассификатораПоТегу(ИдентификаторыВидаКлассификатора.Ключ);
		Для Каждого ОписаниеИдентификатора Из ИдентификаторыВидаКлассификатора.Значение Цикл
			
			НовЗапись = НаборЗаписей.Добавить();
			НовЗапись.ВидКлассификатора  = ВидКлассификатора;
			НовЗапись.ИдентификаторРиска = ОписаниеИдентификатора.Идентификатор;
			НовЗапись.Название           = ОписаниеИдентификатора.Название;
			
		КонецЦикла;
	КонецЦикла;
	НаборЗаписей.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вызов операции /risk-data/classifier

Процедура ВызватьОперациюКлассификаторФакторовРиска(Результат, ТелоОтвета)
	
	Если ТелоОтвета = Неопределено Тогда
		URLОперации = СПАРКРискиСервис.URLОперацииСервиса("rest/risk-data/classifier");
		ТелоОтвета = "";
		ПараметрыЗапроса = СПАРКРискиСервис.НовыйПараметрыЗапросаКСервису();
		Если ИспользоватьGZip() Тогда
			ПараметрыЗапроса.Заголовки.Вставить("Accept-Encoding", "gzip");
		КонецЕсли;
		ПараметрыЗапроса.Метод = "GET";
		СПАРКРискиСервис.ВызватьОперациюСервиса(
			URLОперации,
			ПараметрыЗапроса,
			Результат,
			ТелоОтвета);
		Если Не Результат.ВидОшибки.Пустая() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ОбработатьТелоОтветаRiskDataClassifier(ТелоОтвета, Результат);
	Исключение
		СПАРКРискиСервис.ПриОшибкеОбработкиОтветаСервиса(ИнформацияОбОшибке(), URLОперации, ТелоОтвета, Результат);
	КонецПопытки;
	
КонецПроцедуры

Процедура ОбработатьТелоОтветаRiskDataClassifier(ТелоОтвета, Результат)
	
	// {
	//  "addInfoFields": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ],
	//  "negativeLists": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ],
	//  "companyRiskFactors": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ],
	//  "entrepreneurRiskFactors": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ],
	//  "company115FLRiskFactors": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ],
	//  "entrepreneur115FLRiskFactors": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ],
	//  "arbitrationTypes": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ],
	//  "arbitrationAdjudgementTypes": [
	//    {
	//      "id": string,
	//      "name": string
	//     }
	//  ]
	// }
	
	ДанныеСервиса = ОбщегоНазначения.JSONВЗначение(
		ТелоОтвета,
		"",
		Ложь);
	
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"addInfoFields",
		Результат,
		"ДополнительныеПоля");
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"negativeLists",
		Результат,
		"НегативныеСписки");
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"arbitrationTypes",
		Результат,
		"ТипыАрбитража");
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"arbitrationAdjudgementTypes",
		Результат,
		"ТипыАрбитражныхРешений");
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"entrepreneurRiskFactors",
		Результат,
		"ФакторыРискаФизическогоЛица");
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"entrepreneur115FLRiskFactors",
		Результат,
		"ФакторыРискаФизическогоЛицаПо115ФЗ");
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"companyRiskFactors",
		Результат,
		"ФакторыРискаЮридическогоЛица");
	ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		"company115FLRiskFactors",
		Результат,
		"ФакторыРискаЮридическогоЛицаПо115ФЗ");
	
КонецПроцедуры

Процедура ОбработатьТелоОтветаВидКлассификатора(
		ДанныеСервиса,
		Тег,
		Результат,
		Ключ);
	
	ДанныеКлассификатораПоВиду = Неопределено;
	Если Не ДанныеСервиса.Свойство(Тег, ДанныеКлассификатораПоВиду) Тогда
		Возврат;
	КонецЕсли;
	
	Результат.Классификатор.Вставить(Ключ, Новый Массив);
	Для Каждого ВидДанных Из ДанныеКлассификатораПоВиду Цикл
		
		Данные = Новый Структура(
			"Идентификатор, Название",
			ВидДанных.id,
			ВидДанных.name);
		Результат.Классификатор[Ключ].Добавить(Данные);
		
	КонецЦикла;

КонецПроцедуры

Функция ВидКлассификатораПоТегу(Тег)
	
	Попытка
		Результат = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК[Тег];
	Исключение
		СПАРКРиски.ЗаписатьОшибкуВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Неизвестный тег вида классификатора: %1'"),
				Тег));
		Результат = Перечисления.ВидыКлассификаторовФакторовРискаСПАРК.ПустаяСсылка();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПараметрыОбновленияФакторовРисков

// Возвращает параметры обновления факторов рисков.
// 
// Возвращаемое значение:
//  Структура - параметры обновления:
//  * РазрешитьРаботуЗадания - Булево - признак использования задания;
//  * РазмерЗапросаВЗадании - Число - разрешенное количество ИНН на получение данных. Минимум 1.
//
Функция ПараметрыОбновленияФакторовРисков() Экспорт
	
	Результат = СПАРКРискиСервис.НовыйРезультатВызоваСервиса();
	Результат.Вставить("РазрешитьРаботуЗадания", Ложь);
	Результат.Вставить("РазмерЗапросаВЗадании", 1);
	
	СПАРКРискиСервис.ПередВызовомОперацииСервиса(Результат, , , Ложь);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВызватьОперациюПолученияНастроек(Результат);
	СПАРКРискиСервис.ПослеВызоваОперацииСервиса(Результат);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ВызватьОперациюПолученияНастроек(Результат)
	
	URLОперации = СПАРКРискиСервис.URLОперацииСервиса("settings");
	ТелоОтвета = "";
	ПараметрыЗапроса = СПАРКРискиСервис.НовыйПараметрыЗапросаКСервису();
	ПараметрыЗапроса.Метод = "GET";
	СПАРКРискиСервис.ВызватьОперациюСервиса(
		URLОперации,
		ПараметрыЗапроса,
		Результат,
		ТелоОтвета);
	Если Не Результат.ВидОшибки.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ОбработатьТелоОтветаSettings(ТелоОтвета, Результат);
	Исключение
		СПАРКРискиСервис.ПриОшибкеОбработкиОтветаСервиса(ИнформацияОбОшибке(), URLОперации, ТелоОтвета, Результат);
	КонецПопытки;
	
КонецПроцедуры

Процедура ОбработатьТелоОтветаSettings(ТелоОтвета, Результат)
	
	// {
	//   "riskFactorScheduledDataDownloadEnabled": false,
	//   "riskFactorScheduledDataDownloadChunkSize": 20
	// }
	
	ДанныеСервиса = ОбщегоНазначения.JSONВЗначение(
		ТелоОтвета,
		"",
		Ложь);
	Результат.РазрешитьРаботуЗадания = ДанныеСервиса.riskFactorScheduledDataDownloadEnabled;
	Результат.РазмерЗапросаВЗадании = Макс(ДанныеСервиса.riskFactorScheduledDataDownloadChunkSize, 1);
	
КонецПроцедуры

Функция РазмерЗапросаФакторыРиска()
	
	Возврат СПАРКРискиПовтИсп.ПараметрыОбновленияФакторовРисков().РазмерЗапросаВЗадании;
	
КонецФункции

#КонецОбласти

#КонецОбласти

