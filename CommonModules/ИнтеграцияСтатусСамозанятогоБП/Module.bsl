#Область ПрограммныйИнтерфейс

// Возвращает статус самозанятого, сохраненный в базе
//
// Параметры:
//  ИсточникДанных - СправочникСсылка.Контрагенты, СправочникСсылка.ДоговорыКонтрагентов - источник данных
//                   о самозанятом контрагенте
//  ДатаПроверки - Дата - дата в часовом поясе "Europe/Moscow",
//                 на которую будет получена информация о статусе самозанятого
//
// Возвращаемое значение:
//  Структура, Неопределено - см. РегистрыСведений.СтатусыСамозанятых.ПлательщикНПД
//
Функция СохраненныйСтатус(ИсточникДанных, ДатаПроверки) Экспорт
	
	Если Не ИнтеграцияСтатусСамозанятого.СервисИспользуется()
		Или Не ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам")
		Или Не ПолучитьФункциональнуюОпцию("ИспользуютсяДоговорыССамозанятыми") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникДанных) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(ИсточникДанных) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		РеквизитыСамозанятого = РеквизитыСамозанятогоПоДоговору(ИсточникДанных);
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("СправочникСсылка.Контрагенты") Тогда
		РеквизитыСамозанятого = РеквизитыСамозанятогоПоКонтрагенту(ИсточникДанных);
	Иначе
		ВызватьИсключение НСтр(
			"ru = 'Вызван метод общего модуля ИнтеграцияСтатусСамозанятогоБП с некорректным параметром ИсточникДанных'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыСамозанятого.ИНН) Тогда
		СохраненныйСтатус = РегистрыСведений.СтатусыСамозанятых.ПлательщикНПД(
			РеквизитыСамозанятого.Контрагент, ДатаПроверки);
	Иначе
		СохраненныйСтатус = Неопределено;
	КонецЕсли;
	
	Возврат СохраненныйСтатус;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура регламентного задания ПолучениеСтатусовСамозанятых
//
Процедура ВыполнитьРегламентноеЗаданиеПоПолучениюСтатусовСамозанятых() Экспорт
	
	Если Не ИнтеграцияСтатусСамозанятого.СервисИспользуется() Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ПолучениеСтатусовСамозанятых);
	
	ПроверяемыеДанные = ДанныеТребующиеПроверкиСтатусаСамозанятых();
	Если Не ЗначениеЗаполнено(ПроверяемыеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		НастройкиЛимитов = ИнтеграцияСтатусСамозанятого.ПолучитьНастройкиЛимитов();
	Исключение
		Возврат;
	КонецПопытки;
	
	МаксимальноеЧислоСтрокВЗадании = НастройкиЛимитов.МаксимальноеЧислоСтрокВЗадании;
	Если Не ЗначениеЗаполнено(МаксимальноеЧислоСтрокВЗадании) Тогда
		Возврат;
	КонецЕсли;
	МаксимальныйИндекс = МаксимальноеЧислоСтрокВЗадании - 1;
	ТаблицаЗаданий = ИнтеграцияСтатусСамозанятого.НоваяТаблицаЗадания();
	ДокументыКонтрагентовУтратившихСтатус = Новый Массив();
	
	Для Индекс = 0 По ПроверяемыеДанные.Количество() - 1 Цикл
		СтрокаДанные = ПроверяемыеДанные[Индекс];
		РезультатПроверкиИНН = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(
			СтрокаДанные.ИНН, Ложь);
		Если Не РезультатПроверкиИНН.СоответствуетТребованиям Тогда
			Продолжить;
		КонецЕсли;
		СтрокаЗадания = ТаблицаЗаданий.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗадания, СтрокаДанные);
		Если Индекс = МаксимальныйИндекс Тогда
			ВыполнитьПроверкуСтатусов(ТаблицаЗаданий, ПроверяемыеДанные, ДокументыКонтрагентовУтратившихСтатус);
			ТаблицаЗаданий = ИнтеграцияСтатусСамозанятого.НоваяТаблицаЗадания();
			МаксимальныйИндекс = МаксимальныйИндекс + МаксимальноеЧислоСтрокВЗадании;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТаблицаЗаданий) Тогда
		ВыполнитьПроверкуСтатусов(ТаблицаЗаданий, ПроверяемыеДанные, ДокументыКонтрагентовУтратившихСтатус);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументыКонтрагентовУтратившихСтатус) Тогда
		ОбщегоНазначенияБПВызовСервера.УстановитьСтатусыЧековНПД(
			ДокументыКонтрагентовУтратившихСтатус, Перечисления.СтатусыЧековНПД.УтраченСтатусПлательщикаНПД);
	КонецЕсли;
	
КонецПроцедуры

// Изменяет параметр "Использование" регламентного задания по получению статусов самозанятых
//
// Параметры:
//  Применять - Булево - значение, в которое будет установлен параметр "Использование"
//
Процедура ИзменитьИспользованиеЗадания(Применять) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПолучениеСтатусовСамозанятых);
	НайденныеЗадания = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоиска);
	Для Каждого Задание Из НайденныеЗадания Цикл
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Применять);
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления. Добавляет регламентное задание "ПолучениеСтатусовСамозанятых", и включает его, если в базе
// применяются договоры с самозанятыми
//
// Параметры:
//  ПараметрыЗадания - Структура - параметры из контекста обработчика обновления,
//                     см. ОбновлениеИнформационнойБазыСлужебный.НовыйКонтекстОбработчика
//                     описание свойства "Параметры"
//
Процедура ДобавитьЗаданиеПолученияСтатусовСамозанятых(ПараметрыЗадания) Экспорт
	
	МетаданныеЗадания = Метаданные.РегламентныеЗадания.ПолучениеСтатусовСамозанятых;
	ФильтрПоиска = Новый Структура("Метаданные", МетаданныеЗадания);
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(ФильтрПоиска);
	Если ЗначениеЗаполнено(Задания) Тогда
		// Задание уже добавлено
		Возврат;
	КонецЕсли;
	
	ПараметрыДобавленияЗадания = Новый Структура;
	ПараметрыДобавленияЗадания.Вставить("Метаданные", МетаданныеЗадания);
	ПараметрыДобавленияЗадания.Вставить(
		"Использование", ПолучитьФункциональнуюОпцию("ИспользуютсяДоговорыССамозанятыми"));
	ПараметрыДобавленияЗадания.Вставить("Ключ", МетаданныеЗадания.ИмяМетода);
	ПараметрыДобавленияЗадания.Вставить("Наименование", МетаданныеЗадания.Наименование);
	ПараметрыДобавленияЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияПолученияСтатусовСамозанятых());
	
	РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыДобавленияЗадания);
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ПолучениеСтатусовСамозанятых.ИмяМетода);
	
КонецПроцедуры

// См. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий
//
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ПолучениеСтатусовСамозанятых;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользуютсяДоговорыССамозанятыми;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	
КонецПроцедуры

// Возвращает период, за который проверяется наличие чеков НПД, период включает в себя последние полные
// 12 месяцев от текущей даты
//
// Возвращаемое значение:
//  Структура:
//    * ДатаНачала - Дата
//    * ДатаОкончания - Дата
//
Функция ПериодПроверкиНаличияЧековНПД() Экспорт
	
	ВремяПоПоясуИнформационнойБазы = МестноеВремя(ТекущаяУниверсальнаяДата(), ПолучитьЧасовойПоясИнформационнойБазы());
	КонецПериода = НачалоМесяца(ВремяПоПоясуИнформационнойБазы) - 1;
	НачалоПериода = НачалоМесяца(ДобавитьМесяц(КонецПериода, -11));
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаНачала", НачалоПериода);
	Результат.Вставить("ДатаОкончания", КонецПериода);
	
	Возврат Результат;
	
КонецФункции

// Возвращает дату, на которую будет получен статус самозанятого
// Параметры:
//  ОсновнаяДата - Дата
//  ДатаВходящегоДокумента - Дата - если в документе заполнена дата входящего документа, то
//                           проверка будет выполнена на эту дату
//
// Возвращаемое значение:
//  Дата - не пустая дата, не превышающая текущей даты сеанса, в часовом поясе "Europe/Moscow"
//
Функция ДатаПолученияСтатуса(ОсновнаяДата, ДатаВходящегоДокумента = Неопределено) Экспорт
	
	ДатаПроверки = ?(ЗначениеЗаполнено(ДатаВходящегоДокумента), ДатаВходящегоДокумента, ОсновнаяДата);
	Если ЗначениеЗаполнено(ДатаПроверки) Тогда
		ЧасовойПояс = ПолучитьЧасовойПоясИнформационнойБазы();
	Иначе
		ДатаПроверки = ТекущаяУниверсальнаяДата();
		ЧасовойПояс = "UTC";
	КонецЕсли;
	Результат = ИнтеграцияСтатусСамозанятогоВызовСервера.ДатаНеПозжеТекущейПоЧасовомуПоясуСервиса(
		ДатаПроверки, ЧасовойПояс);
	Возврат НачалоДня(Результат);
	
КонецФункции

// Возвращает реквизиты самозанятого, определяемые по переданному договору
//
// Параметры:
//  Договор - СправочникСсылка.ДоговорыКонтрагентов
//
// Возвращаемое значение:
//  Структура - см. НовыйРеквизитыСамозанятого
//
Функция РеквизитыСамозанятогоПоДоговору(Договор) Экспорт
	
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Договор, "НалогообложениеКонтрагента, Владелец.ИНН, Владелец");
	РеквизитыСамозанятого = НовыйРеквизитыСамозанятого();
	Если РеквизитыДоговора.НалогообложениеКонтрагента =
		Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход Тогда
		РеквизитыСамозанятого.ИНН = РеквизитыДоговора.ВладелецИНН;
		РеквизитыСамозанятого.Контрагент = РеквизитыДоговора.Владелец;
	КонецЕсли;

	Возврат РеквизитыСамозанятого;
	
КонецФункции

// Возвращает реквизиты самозанятого, определяемые по переданному контрагенту
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
//
// Возвращаемое значение:
//  Структура - см. НовыйРеквизитыСамозанятого
//
Функция РеквизитыСамозанятогоПоКонтрагенту(Контрагент) Экспорт
	
	РеквизитыСамозанятого = НовыйРеквизитыСамозанятого();
	ИННКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ИНН");
	Если Не ИдентификационныеНомераНалогоплательщиков.ЭтоИННФизическогоЛица(ИННКонтрагента) Тогда
		ИННКонтрагента = Неопределено;
	КонецЕсли;
	РеквизитыСамозанятого.ИНН = ИННКонтрагента;
	РеквизитыСамозанятого.Контрагент = Контрагент;
	
	Возврат РеквизитыСамозанятого;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеТребующиеПроверкиСтатусаСамозанятых()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДокументов.НаДату КАК НаДату,
		|	ДанныеДокументов.ИНН КАК ИНН,
		|	ДанныеДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокументов.Контрагент КАК Контрагент
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА ПоступлениеТоваровУслуг.ДатаВходящегоДокумента > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ПоступлениеТоваровУслуг.ДатаВходящегоДокумента
		|			ИНАЧЕ ПоступлениеТоваровУслуг.Дата
		|		КОНЕЦ КАК НаДату,
		|		ПоступлениеТоваровУслуг.Контрагент.ИНН КАК ИНН,
		|		ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
		|		ПоступлениеТоваровУслуг.Контрагент КАК Контрагент
		|	ИЗ
		|		РегистрСведений.СтатусыДокументов КАК СтатусыДокументов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|			ПО СтатусыДокументов.Документ = ПоступлениеТоваровУслуг.Ссылка
		|	ГДЕ
		|		СтатусыДокументов.СтатусЧекаНПД = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековНПД.НеПолучен)
		|		И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ПоступлениеТоваровУслуг.Проведен
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА ПоступлениеДопРасходов.ДатаВходящегоДокумента > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ПоступлениеДопРасходов.ДатаВходящегоДокумента
		|			ИНАЧЕ ПоступлениеДопРасходов.Дата
		|		КОНЕЦ,
		|		ПоступлениеДопРасходов.Контрагент.ИНН,
		|		ПоступлениеДопРасходов.Ссылка,
		|		ПоступлениеДопРасходов.Контрагент
		|	ИЗ
		|		РегистрСведений.СтатусыДокументов КАК СтатусыДокументов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
		|			ПО СтатусыДокументов.Документ = ПоступлениеДопРасходов.Ссылка
		|	ГДЕ
		|		СтатусыДокументов.СтатусЧекаНПД = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековНПД.НеПолучен)
		|		И ПоступлениеДопРасходов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ПоступлениеДопРасходов.Проведен) КАК ДанныеДокументов";
	
	ПериодПроверки = ПериодПроверкиНаличияЧековНПД();
	
	Запрос.УстановитьПараметр("НачалоПериода", ПериодПроверки.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ПериодПроверки.ДатаОкончания);
	
	ДанныеТребующиеПроверки = НовыйДанныеТребующиеПроверки();
	ЧасовойПоясИнформационнойБазы = ПолучитьЧасовойПоясИнформационнойБазы();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаДату = НачалоДня(ИнтеграцияСтатусСамозанятого.ДатаПоСервисуСтатусаСамозанятого(
			Выборка.НаДату, ЧасовойПоясИнформационнойБазы));
		Фильтр = Новый Структура("НаДату, ИНН", НаДату, Выборка.ИНН);
		НайденныеСтроки = ДанныеТребующиеПроверки.НайтиСтроки(Фильтр);
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			ТекущаяСтрока = НайденныеСтроки[0];
		Иначе
			ТекущаяСтрока = ДанныеТребующиеПроверки.Добавить();
			ТекущаяСтрока.Контрагент = Выборка.Контрагент;
			ТекущаяСтрока.НаДату = НаДату;
			ТекущаяСтрока.ИНН = Выборка.ИНН;
			ТекущаяСтрока.ПроверяемыеДокументы = Новый Массив;
		КонецЕсли;
		ТекущаяСтрока.ПроверяемыеДокументы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	РеквизитыПроверкиСтатусов = ДанныеТребующиеПроверки.Скопировать(, "ИНН, НаДату, Контрагент");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеквизитыПроверкиСтатусов.ИНН КАК ИНН,
		|	РеквизитыПроверкиСтатусов.НаДату КАК НаДату,
		|	РеквизитыПроверкиСтатусов.Контрагент КАК Контрагент
		|ПОМЕСТИТЬ ВТ_РеквизитыПроверкиСтатусов
		|ИЗ
		|	&РеквизитыПроверкиСтатусов КАК РеквизитыПроверкиСтатусов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	НаДату
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РеквизитыПроверкиСтатусов.ИНН КАК ИНН,
		|	СтатусыСамозанятых.Период КАК НаДату
		|ИЗ
		|	ВТ_РеквизитыПроверкиСтатусов КАК ВТ_РеквизитыПроверкиСтатусов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСамозанятых КАК СтатусыСамозанятых
		|		ПО ВТ_РеквизитыПроверкиСтатусов.Контрагент = СтатусыСамозанятых.Контрагент
		|			И ВТ_РеквизитыПроверкиСтатусов.НаДату = СтатусыСамозанятых.Период
		|ГДЕ
		|	НЕ СтатусыСамозанятых.Период ЕСТЬ NULL
		|	И (СтатусыСамозанятых.ЭтоОкончательноеЗначение
		|			ИЛИ СтатусыСамозанятых.ДатаПроверки >= ДОБАВИТЬКДАТЕ(СтатусыСамозанятых.Период, СЕКУНДА, &ВремяВступленияВСилуСтатуса))";
	Запрос.УстановитьПараметр(
		"ВремяВступленияВСилуСтатуса", РегистрыСведений.СтатусыСамозанятых.ВремяВступленияВСилуСтатусаСамозанятого());
	Запрос.УстановитьПараметр("РеквизитыПроверкиСтатусов", РеквизитыПроверкиСтатусов);
	Выборка = Запрос.Выполнить().Выбрать();
	РеквизитыПроверкиСтатусов.Очистить();
	СтрокиКУдалению = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Фильтр = Новый Структура("НаДату, ИНН", Выборка.НаДату, Выборка.ИНН);
		НайденныеСтроки = ДанныеТребующиеПроверки.НайтиСтроки(Фильтр);
		Для Каждого Строка Из НайденныеСтроки Цикл
			СтрокиКУдалению.Добавить(Строка);
		КонецЦикла;
	КонецЦикла;
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		ДанныеТребующиеПроверки.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	ДанныеТребующиеПроверки.Сортировать("НаДату Убыв");
	
	Возврат ДанныеТребующиеПроверки;
	
КонецФункции

Процедура ВыполнитьПроверкуСтатусов(ТаблицаЗаданий, ПроверяемыеДанные, ДокументыКонтрагентовУтратившихСтатус)
	
	Попытка
		РезультатПроверкиСтатусов = ИнтеграцияСтатусСамозанятого.ПолучитьСтатусыСамозанятых(ТаблицаЗаданий);
	Исключение
		Возврат;
	КонецПопытки;
	Для Каждого СтрокаПроверяемыхДанных Из РезультатПроверкиСтатусов Цикл
		Если Не СтрокаПроверяемыхДанных.ПлательщикНПД Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("ИНН", СтрокаПроверяемыхДанных.ИНН);
			Отбор.Вставить("НаДату", СтрокаПроверяемыхДанных.НаДату);
			НайденныеСтроки = ПроверяемыеДанные.НайтиСтроки(Отбор);
			Для Каждого СтрокаДанные Из НайденныеСтроки Цикл
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
					ДокументыКонтрагентовУтратившихСтатус, СтрокаДанные.ПроверяемыеДокументы, Истина);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйДанныеТребующиеПроверки()
	
	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("ИНН", ОбщегоНазначения.ОписаниеТипаСтрока(12));
	Результат.Колонки.Добавить("НаДату", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Результат.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	Результат.Колонки.Добавить("ПроверяемыеДокументы", Новый ОписаниеТипов("Массив"));
	Результат.Индексы.Добавить("НаДату, ИНН");
	
	Возврат Результат;
	
КонецФункции

Функция РасписаниеРегламентногоЗаданияПолученияСтатусовСамозанятых()
	
	ПараметрыРасписания = ОбщегоНазначенияБП.ПараметрыРасписанияЕжедневногоРегламентногоЗадания();
	
	// Для серверных баз установим запуск регламентного задания на нерабочее время, чтобы избежать повышенной нагрузки
	// на сервис и не расходовать лимит обращений к сервису, которые могут понадобиться пользователю во время работы.
	// Для файловых баз необходимо запускать задание в рабочее время,
	// так как если база не запущена, то задание не выполнится.
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПараметрыРасписания.ВремяНачалаПериода = Дата(1, 1, 1, 14, 0, 0);
		ПараметрыРасписания.ВремяКонцаПериода = Дата(1, 1, 1, 17, 30, 0);
	Иначе
		ПараметрыРасписания.ВремяНачалаПериода = Дата(1, 1, 1, 19, 0, 0);
	КонецЕсли;
	
	Возврат ОбщегоНазначенияБП.РасписаниеЗаданияЕжедневноВСлучайноеВремя(ПараметрыРасписания);
	
КонецФункции

Функция НовыйРеквизитыСамозанятого()
	
	Реквизиты = Новый Структура;

	Реквизиты.Вставить("ИНН", "");
	Реквизиты.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти