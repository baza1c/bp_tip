///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////
//
////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.НастройкиПлатежныхСистем".
// ОбщийМодуль.НастройкиПлатежныхСистемСобытия.
// 
// Обработчики событий, возникающих при работе с платежными системами:
//  - создание формы;
//  - подготовка параметров формы общего списка настроек подключения;
//  - формирование дерева по отдельной подсистеме;
//  - добавление и обработка нажатия команд на форме.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
// 
// Параметры:
//  Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
// 
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия              = "2.8.3.5";
		Обработчик.Идентификатор       = Новый УникальныйИдентификатор("70a8c266-1b5d-4d03-8ced-0ee0cfa2a277");
		Обработчик.Процедура           = "НастройкиПлатежныхСистемСлужебный.ПроверитьЗаданиеЗагрузкаСтатусовОпераций";
		Обработчик.РежимВыполнения     = "Отложенно";
		Обработчик.ОбщиеДанные         = Ложь;
		Обработчик.НачальноеЗаполнение = Ложь;
		Обработчик.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1. Проверка наличия регламентного задания ""Загрузка статусов операций платежных систем"".'"),
			НастройкиПлатежныхСистемСлужебный.ИмяСобытияЖурналаРегистрации());
			
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из процедуры ДобавитьКомандыПечати модуля менеджера объектов
// Если команда печати не зависит от выделенной ссылки в общем списке настроек подключения,
// то, чтобы она была доступна, необходимо указать все доступные типы
// в параметре ТипыОбъектовПечати.
// Если этого не сделать, то команда печати будет скрываться при выделении строки,
// в которой тип ссылки не соответствует доступным типам.
// Пример вызова см. Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ДобавитьКомандыПечати.
//
// Параметры:
//  ТипыОбъектовПечати - Массив Из Тип - список типов, для которых будет доступна команда печати.
Процедура ПриОпределенииТиповОбъектовПечати(ТипыОбъектовПечати) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		ТипыОбъектовПечати.Добавить(
			МодульСистемаБыстрыхПлатежейСлужебный.ТипНастройкаПодключения());
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		ТипыОбъектовПечати.Добавить(
			МодульИнтернетЭквайрингСлужебный.ТипНастройкаПодключения());
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПлатежныеСистемы.БазоваяФункциональностьПлатежныхСистем") Тогда
		МодульПлатежныеСистемыСлужебный = ОбщегоНазначения.ОбщийМодуль("ПлатежныеСистемыСлужебный");
		ТипыОбъектовПечати.Добавить(
			МодульПлатежныеСистемыСлужебный.ТипНастройкаПодключения());
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при создании общей формы списка настроек подключения к платежным системам.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - открываемая форма.
//     Реквизиты формы:
//       * КешПараметров - Структура - содержит настройки, которые используются при работе формы. Устанавливаются в
//           процедуре ПодготовитьПараметрыФормы;
//       * Список - ДеревоЗначений - дерево значений, в которое выводится список доступных настроек;
//       * ТолькоГруппы - Булево - Истина, если при для выбора доступны только группы;
//       * ТолькоЭлементы - Булево - Истина, если при для выбора доступны только элементы.
//     Параметры формы:
//       * Подсистема - Строка - имя подсистемы, которую необходимо отобразить в списке. Если не заполнено или "Все",
//           то выводятся все подсистемы. Доступные значения определяются методом ПриОпределенииИменПодсистем
//       * ПараметрыПодсистемы - Неопределено, Произвольный - параметры подсистемы, переданные при открытии формы.
//
Процедура ПриСозданииФормы(Форма) Экспорт
	
	// При добавлении новой платежной системы, которая использует свой собственный справочник для хранения
	// настроек, необходимо дополнить тип данных у реквизитов и параметров форм.
	
	Параметры = Форма.Параметры;
	Элементы = Форма.Элементы;

	ИмяПодсистемыСБП = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП();
	ИмяПодсистемыЭквайринг = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг();
	ОписаниеПлатежныхСистем = НастройкиПлатежныхСистемПовтИсп.ОписаниеПлатежныхСервисов();

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда

		ТипыДанных = Неопределено;
		Если Параметры.Подсистема = "Все" Тогда
			ТипыДанных = Новый Массив;
			Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
				МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
				ТипыДанных.Добавить(
					МодульСистемаБыстрыхПлатежейСлужебный.ТипНастройкаПодключения());
			КонецЕсли;
			Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
				МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
				ТипыДанных.Добавить(
					МодульИнтернетЭквайрингСлужебный.ТипНастройкаПодключения());
			КонецЕсли;
			Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПлатежныеСистемы.БазоваяФункциональностьПлатежныхСистем") Тогда
				МодульПлатежныеСистемыСлужебный = ОбщегоНазначения.ОбщийМодуль("ПлатежныеСистемыСлужебный");
				ТипыДанных.Добавить(
					МодульПлатежныеСистемыСлужебный.ТипНастройкаПодключения());
			КонецЕсли;
		ИначеЕсли Параметры.Подсистема = ИмяПодсистемыСБП Тогда
			ТипыДанных = "СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей";
		ИначеЕсли Параметры.Подсистема = ИмяПодсистемыЭквайринг Тогда
			ТипыДанных = "СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу";
		ИначеЕсли ОписаниеПлатежныхСистем.Получить(Параметры.Подсистема) <> Неопределено Тогда
			ТипыДанных = "СправочникСсылка.НастройкиПодключенияКПлатежнымСистемам";
		КонецЕсли;
		
		Если ТипыДанных <> Неопределено Тогда
		
			МодульПодключаемыеКоманды = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКоманды");
			ПараметрыРазмещения = МодульПодключаемыеКоманды.ПараметрыРазмещения();
			ПараметрыРазмещения.КоманднаяПанель = Элементы.Список.КоманднаяПанель;
			ПараметрыРазмещения.Источники = Новый ОписаниеТипов(ТипыДанных);
			ПараметрыРазмещения.ВладелецКоманд = Элементы.Список;
			
			МодульПодключаемыеКоманды.ПриСозданииНаСервере(Форма, ПараметрыРазмещения);
			
		КонецЕсли;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Если выводится только одна подсистема.
	Если Параметры.Подсистема = ИмяПодсистемыСБП Тогда
		Форма.Заголовок = НастройкиПлатежныхСистемСлужебный.ПредставлениеСписка(
			"Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей");
			
	ИначеЕсли Параметры.Подсистема = ИмяПодсистемыЭквайринг Тогда
		Форма.Заголовок = НастройкиПлатежныхСистемСлужебный.ПредставлениеСписка(
			"Справочник.НастройкиПодключенияКИнтернетЭквайрингу");
	
	Иначе
		ОписаниеПодсистемы = ОписаниеПлатежныхСистем.Получить(Параметры.Подсистема);
		Если ОписаниеПодсистемы <> Неопределено Тогда
			Форма.Заголовок = ОписаниеПодсистемы.ЗаголовокСписка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при выводе в список информации по отдельной подсистеме. Здесь необходимо вызвать метод соответствующей
//  подсистемы, который выполнит формирование дерева со списком ссылок.
// 
// Параметры:
//  ИмяПодсистемы - Строка - Имя подсистемы, для которой выполняется вывод списка.
//  Дерево - ДеревоЗначений - дерево, которое необходимо заполнить.
//    см. НастройкиПлатежныхСистемСлужебный.НовыйДеревоНастроек.
//  ПараметрыДерева - Структура - параметры вывода дерева.
//    см. НастройкиПлатежныхСистемСлужебный.НовыйПараметрыДерева.
//
Процедура ПриФормированииДереваПоПодсистеме(ИмяПодсистемы, Дерево, ПараметрыДерева) Экспорт

	// Вызывается только для подсистем, видимых в списке. Видимость определяется в методе ПодготовитьПараметрыФормы.
	//  Если подсистема не существует, то она не будет видна.
	//  Можно не проверять наличие подсистемы.
	
	Если ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ПриФормированииДереваПоПодсистеме(Дерево, ПараметрыДерева);

	ИначеЕсли ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг() Тогда
		
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		МодульИнтернетЭквайрингСлужебный.ПриФормированииДереваПоПодсистеме(Дерево, ПараметрыДерева);

	Иначе
		
		ОписаниеПлатежныхСистем = НастройкиПлатежныхСистемПовтИсп.ОписаниеПлатежныхСервисов();
		
		Если ОписаниеПлатежныхСистем.Получить(ИмяПодсистемы) <> Неопределено Тогда
		
			// Существование подсистемы ПлатежныеСистемы проверяется в методе НастройкиПлатежныхСистемПовтИсп.ОписаниеПлатежныхСервисов().
			// Если подсистема не используется то сюда не зайдет.
			МодульПлатежныеСистемыСлужебный = ОбщегоНазначения.ОбщийМодуль("ПлатежныеСистемыСлужебный");
			МодульПлатежныеСистемыСлужебный.ПриФормированииДереваПоПодсистеме(
				Дерево,
				ПараметрыДерева,
				ИмяПодсистемы);

		Иначе
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет номер картинки из набора ОбщаяКартинка.НастройкиПлатежныхСистем, которая должна использоваться при
//   выводе корневого элемента дерева.
//
// Параметры:
//  ИмяПодсистемы - Строка - имя подсистемы, для которой необходимо определить номер картинки.
//    Доступные значения определяются методами ИмяПодсистемыСБП(), ИмяПодсистемыЭквайринг(), ИмяПодсистемыОзонПэй().
//  НомерКартинки - Число - номер картинки в коллекции.
//
Процедура ПриОпределенииНомераКартинкиПодсистемы(ИмяПодсистемы, НомерКартинки) Экспорт
	
	Если ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		НомерКартинки = 4;
	ИначеЕсли ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг() Тогда
		НомерКартинки = 5;
	ИначеЕсли ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй() Тогда
		НомерКартинки = 6;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет реквизит формы КешПараметров параметрами, которые необходимы для дальнейшей работы.
//  В реквизит добавляются следующие значения:
//    * ВсеИменаПодсистем - Массив Из Строка - см. ПриОпределенииИменПодсистем
//    * ВидимостьПодсистемВСписке - Соответствие Из КлючИЗначение - признак видимости подсистемы в общем списке.
//        Подсистема добавляется в параметр даже в том случае, если она не включена в конфигурацию.
//        ** Ключ - Строка - имя подсистемы. Список доступных значений ограничивается методом
//                  ПриОпределенииИменПодсистем.
//        ** Значение - Булево - признак видимости подсистемы.
//    * ДоступностьНастроек - Соответствие Из КлючИЗначение - признак доступности для пользователя возможности
//        изменения настроек каждой подсистемы. Подсистема добавляется в параметр даже в том случае, если она не
//        включена в конфигурацию.
//        ** Ключ - Строка - имя подсистемы. Список доступных значений ограничивается методом
//                  ПриОпределенииИменПодсистем.
//        ** Значение - Булево - признак доступности создания и изменения настроек по подсистеме.
//    * ВидимостьКассовыхСсылок - Булево - видимость кнопки открытия кассовых ссылок для подсистемы СБП.
//    * ПодсистемыКнопкиСоздать - Соответствие Из КлючИЗначение - связь имени команды создания новой настройки с
//        именем подсистемы.
//        ** Ключ - Строка - имя команды;
//        ** Значение - Строка - имя подсистемы. Список доступных значений ограничивается методом
//                      ПриОпределенииИменПодсистем.
//    * ДоступностьКоманд - Соответствие из КлючИЗначение - доступность команд:
//        ** Ключ - Строка - имя подсистемы. Список доступных значений ограничивается методом
//                  ПриОпределенииИменПодсистем.
//        ** Значение - Массив Из Строка - имена команд, доступные для подсистемы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - открываемая форма.
//
Процедура ПриПодготовкеПараметровФормы(Форма) Экспорт
	
	Параметры = Форма.Параметры;
	
	ВидимостьПодсистемВСписке = Новый Соответствие;
	ДоступностьНастроек       = Новый Соответствие;
	ВидимостьКассовыхСсылок   = Ложь;
	
	ВидимостьПоУмолчанию = Параметры.Подсистема = "Все";
	
	ПараметрыПроверки = Новый Массив;
	ПараметрыПроверки.Добавить(
		Новый Структура(
			"ИмяПодсистемы, ИмяПодсистемыПолное",
			НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП(),
			"ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП"));
	ПараметрыПроверки.Добавить(
		Новый Структура(
			"ИмяПодсистемы, ИмяПодсистемыПолное",
			НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг(),
			"ИнтернетПоддержкаПользователей.ИнтернетЭквайринг"));
	
	ПараметрыПодсистем = НастройкиПлатежныхСистемПовтИсп.ОписаниеПлатежныхСервисов();
	Для Каждого КлючЗначение Из ПараметрыПодсистем Цикл
		ПараметрыПроверки.Добавить(
		Новый Структура(
			"ИмяПодсистемы, ИмяПодсистемыПолное",
			КлючЗначение.Ключ,
			КлючЗначение.Значение.Подсистема));
	КонецЦикла;
		
	Для Каждого ОписаниеПодсистемы Из ПараметрыПроверки Цикл
		
		ТекущаяПодсистема  = ОписаниеПодсистемы.ИмяПодсистемы;
		ВидимостьТекущей   = Ложь;
		ДоступностьТекущей = Ложь;
		
		Если ОбщегоНазначения.ПодсистемаСуществует(ОписаниеПодсистемы.ИмяПодсистемыПолное) Тогда
			ВидимостьТекущей = ВидимостьПоУмолчанию;
			Если Параметры.Подсистема = ТекущаяПодсистема Тогда
				ВидимостьТекущей = Истина;
			КонецЕсли;
		
			Если ТекущаяПодсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
				// СБП
				МодульСистемаБыстрыхПлатежейСобытия = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСобытия");
				МодульСистемаБыстрыхПлатежейСобытия.ПриПодготовкеПараметровФормыСпискаНастроек(
					ВидимостьТекущей,
					ДоступностьТекущей,
					ВидимостьКассовыхСсылок);
				
			ИначеЕсли ТекущаяПодсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг() Тогда
				// Интернет-эквайринг
				МодульИнтернетЭквайрингСобытия = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСобытия");
				МодульИнтернетЭквайрингСобытия.ПриПодготовкеПараметровФормыСпискаНастроек(
					ВидимостьТекущей,
					ДоступностьТекущей);
					
			ИначеЕсли ТекущаяПодсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй() Тогда
				// Озон пэй
				МодульОзонПэйСобытия = ОбщегоНазначения.ОбщийМодуль("ОзонПэйСобытия");
				МодульОзонПэйСобытия.ПриПодготовкеПараметровФормыСпискаНастроек(
					ВидимостьТекущей,
					ДоступностьТекущей);
			
			КонецЕсли;
		
		КонецЕсли;
		
		ВидимостьПодсистемВСписке.Вставить(ТекущаяПодсистема, ВидимостьТекущей);
		ДоступностьНастроек.Вставить(ТекущаяПодсистема, ДоступностьТекущей);
		
	КонецЦикла;
	
	// Добавляем информацию о подсистемах, которые не были интегрированы в конфигурацию
	ИменаПлатежныхСистем = Новый Массив;
	ИменаПлатежныхСистем.Добавить(НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй());
	
	Для Каждого ИмяПодсистемы Из ИменаПлатежныхСистем Цикл
		Если ВидимостьПодсистемВСписке.Получить(ИмяПодсистемы) = Неопределено Тогда
			ВидимостьПодсистемВСписке.Вставить(ИмяПодсистемы, Ложь);
		КонецЕсли;
		Если ДоступностьНастроек.Получить(ИмяПодсистемы) = Неопределено Тогда
			ДоступностьНастроек.Вставить(ИмяПодсистемы, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	Форма.КешПараметров.Вставить("ВсеИменаПодсистем",         Новый Массив);
	Форма.КешПараметров.Вставить("ДоступностьКоманд",         Новый Соответствие);
	Форма.КешПараметров.Вставить("ВидимостьПодсистемВСписке", ВидимостьПодсистемВСписке);
	Форма.КешПараметров.Вставить("ДоступностьНастроек",       ДоступностьНастроек);
	Форма.КешПараметров.Вставить("ВидимостьКассовыхСсылок",   ВидимостьКассовыхСсылок);
	Форма.КешПараметров.Вставить("ПодсистемыКнопкиСоздать",   Новый Соответствие);
	Форма.КешПараметров.Вставить("ОбщиеКоманды",              Новый Массив);
	
КонецПроцедуры

// Устанавливает видимость и доступность элементов формы в зависимости от настроек ее реквизитов.
// 
// Параметры:
//   КешПараметров - Структура - см. описание метода ПриПодготовкеПараметровФормы.
//   ВидимостьОбщихКоманд - Булево - видимость команд создания и изменения настроек.
//
Процедура ПриОпределенииВидимостиОбщихКоманд(КешПараметров, ВидимостьОбщихКоманд) Экспорт
	
	ИмяПодсистемыСБП = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП();
	ИмяПодсистемыЭквайринг = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг();
	ИмяПодсистемыОзон = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй();
	
	ВидимостьСБП = КешПараметров.ВидимостьПодсистемВСписке.Получить(ИмяПодсистемыСБП)
		И КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемыСБП);
		
	ВидимостьЭквайринг = КешПараметров.ВидимостьПодсистемВСписке.Получить(ИмяПодсистемыЭквайринг)
		И КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемыЭквайринг);
		
	ВидимостьОзон = КешПараметров.ВидимостьПодсистемВСписке.Получить(ИмяПодсистемыОзон)
		И КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемыОзон);
	
	ВидимостьОбщихКоманд = ВидимостьСБП
		Или ВидимостьЭквайринг
		Или ВидимостьОзон;
	
КонецПроцедуры

// Выполняет добавление команд на форму.
//  Добавляемые команды:
//    - Команды создания настроек по всем доступным подсистемам.
//    - Команда ОткрытьШаблоныНазначения.
//    - Команда ОткрытьКассовыеСсылки.
// 
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - открываемая форма.
//
Процедура ПриДобавленииКоманд(Форма) Экспорт
	
	// Можно не проверять существование подсистемы. ДоступностьНастроек = Ложь, если подсистема не существует.
	
	ИмяПодсистемыСБП = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП();
	ИмяПодсистемыЭквайринг = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг();
	
	// Добавление кнопок создания новых настроек.
	Если Форма.КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемыСБП) Тогда
		НастройкиПлатежныхСистемСлужебный.ДобавитьКнопкуСоздания(
			Форма,
			ИмяПодсистемыСБП,
			НСтр("ru = 'СБП'"),
			БиблиотекаКартинок.ЛоготипСБПБезНадписи);
	КонецЕсли;
	
	Если Форма.КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемыЭквайринг) Тогда
		НастройкиПлатежныхСистемСлужебный.ДобавитьКнопкуСоздания(
			Форма,
			ИмяПодсистемыЭквайринг,
			НСтр("ru = 'Интернет-эквайринг'"),
			БиблиотекаКартинок["ЛоготипИнтернетЭквайринг"]);
			// Картинка ЛоготипИнтернетЭквайринг относится к подсистеме ИнтернетЭквайринг, которая может
			// не существовать, поэтому через []. Если попали внутрь условия, то подсистема существует.
	КонецЕсли;
	
	ПараметрыПодсистем = НастройкиПлатежныхСистемПовтИсп.ОписаниеПлатежныхСервисов();
	ДоступныПлатежныеСистемы = Ложь;
	ДоступныШаблоныНазначений = Ложь;
	
	Для Каждого КлючЗначение Из ПараметрыПодсистем Цикл
		Если Форма.КешПараметров.ДоступностьНастроек.Получить(КлючЗначение.Ключ) Тогда
			НастройкиПлатежныхСистемСлужебный.ДобавитьКнопкуСоздания(
				Форма,
				КлючЗначение.Ключ,
				КлючЗначение.Значение.Представление,
				БиблиотекаКартинок[КлючЗначение.Значение.Картинка]);
			ДоступныПлатежныеСистемы = Истина;
		КонецЕсли;
		Если КлючЗначение.Значение.ИспользуетНазначениеПлатежа Тогда
			ДоступныШаблоныНазначений = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Добавление общих для нескольких подсистем команд в подменю "Настройки"
	Если Форма.КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемыСБП)
		Или Форма.КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемыЭквайринг)
		Или (ДоступныПлатежныеСистемы И ДоступныШаблоныНазначений) Тогда
	
		НастройкиПлатежныхСистемСлужебный.ДобавитьКнопкуДополнительно(
			Форма,
			"ОткрытьШаблоныНазначения",
			НСтр("ru = 'Шаблоны назначения'"));
		
	КонецЕсли;
	
	// Добавление прочих команд в подменю "Настройки"
	Если Форма.КешПараметров.ВидимостьКассовыхСсылок Тогда
		НастройкиПлатежныхСистемСлужебный.ДобавитьКнопкуДополнительно(
			Форма,
			"ОткрытьКассовыеСсылки",
			НСтр("ru = 'Кассовые QR-коды'"));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список имена всех подсистем, которые могут выводиться в общий список настроек, независимо от их наличия в конфигурации.
// 
// Параметры:
//  ИменаПодсистем - Массив Из Строка - имена подсистем. Содержит значения, возвращаемые следующими методами:
//    НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП,
//    НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг,
//    НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй.
//
Процедура ПриОпределенииИменПодсистем(ИменаПодсистем) Экспорт
	
	ИменаПодсистем.Добавить(НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП());
	ИменаПодсистем.Добавить(НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг());
	ИменаПодсистем.Добавить(НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй());
	
КонецПроцедуры

// Формирует описание доступности команд на форме для каждой подсистемы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - открываемая форма.
//
Процедура ПриОпределенииДоступностиКоманд(Форма) Экспорт
	
	ВсеКоманды = Новый Соответствие;
	КешПараметров = Форма.КешПараметров;
	
	КомандыОтчеты = Новый Массив;
	
	Для Каждого Элемент Из Форма.Элементы Цикл
		ОписаниеЭлемента = Новый Структура("Имя, ИмяКоманды", "", "");
		ЗаполнитьЗначенияСвойств(ОписаниеЭлемента, Элемент);
		Если ЗначениеЗаполнено(ОписаниеЭлемента.ИмяКоманды) Тогда
			ВсеКоманды.Вставить(ОписаниеЭлемента.Имя, ОписаниеЭлемента.ИмяКоманды);
			
			Если СтрНачинаетсяС(ОписаниеЭлемента.ИмяКоманды, "ПодменюОтчеты") Тогда
				КомандыОтчеты.Добавить(ОписаниеЭлемента.ИмяКоманды);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	КешПараметров.ОбщиеКоманды = КомандыОтчеты;
	
	Для Каждого ИмяПодсистемы Из Форма.КешПараметров.ВсеИменаПодсистем Цикл
		
		ДоступныеКомандыПодсистемы = Новый Массив;
		
		Если КешПараметров.ВидимостьПодсистемВСписке.Получить(ИмяПодсистемы) Тогда

			Если ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
				
				МодульСистемаБыстрыхПлатежейСобытия = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСобытия");
				МодульСистемаБыстрыхПлатежейСобытия.ПриОпределенииДоступностиКомандСпискаНастроек(
					КешПараметров,
					ВсеКоманды,
					ДоступныеКомандыПодсистемы);
			
			ИначеЕсли ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг() Тогда
		
				МодульИнтернетЭквайрингСобытия = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСобытия");
				МодульИнтернетЭквайрингСобытия.ПриОпределенииДоступностиКомандСпискаНастроек(
					КешПараметров,
					ВсеКоманды,
					ДоступныеКомандыПодсистемы);
			
			ИначеЕсли ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй() Тогда
		
				// Нет доступных команд. Назначение платежа отсутствует, поэтому нет шаблонов.
			
			КонецЕсли;
			
			ПриОпределенииКомандДоступныхВсемНастройкам(
				ВсеКоманды,
				ДоступныеКомандыПодсистемы);
			
		КонецЕсли;
		
		КешПараметров.ДоступностьКоманд.Вставить(ИмяПодсистемы, ДоступныеКомандыПодсистемы);
		
	КонецЦикла;
	
КонецПроцедуры

// Определяет возможность изменения константы ДлительностьОперацииПлатежныхСистем.
//
// Параметры:
//  ИзменениеДоступно - Булево - Истина, если пользователю доступно изменение константы.
//
Процедура ПриОпределенииДоступностиИзмененияДлительностиОперации(ИзменениеДоступно) Экспорт
	
	// Изменение константы доступно, если есть право менять настройки подключения для любой из зависимых подсистем.
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		
		МодульСистемаБыстрыхПлатежей = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежей");
		Если МодульСистемаБыстрыхПлатежей.НастройкаПодключенияДоступна() Тогда
			ИзменениеДоступно = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
			
		МодульИнтернетЭквайринг = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайринг");
		Если МодульИнтернетЭквайринг.НастройкаПодключенияДоступна() Тогда
			ИзменениеДоступно = Истина;
			Возврат;
		КонецЕсли;

	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПлатежныеСистемы.БазоваяФункциональностьПлатежныхСистем") Тогда
			
		МодульПлатежныеСистемы = ОбщегоНазначения.ОбщийМодуль("ПлатежныеСистемы");
		Если МодульПлатежныеСистемы.НастройкаПодключенияДоступна() Тогда
			ИзменениеДоступно = Истина;
			Возврат;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// См. РаботаВБезопасномРежимеПереопределяемый.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт

	ПредставлениеПодсистем = ПредставлениеВнедренныхПлатежныхСистем();
	
	ПредставлениеПодсистемы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сервис интеграции с платежными системами (%1)'"),
		ПредставлениеПодсистем);
	
	НовыеРазрешения = Новый Массив;
	МодульРаботаВБезопасномРежиме = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");
	
	Разрешение = МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		"HTTPS",
		НастройкиПлатежныхСистемСлужебный.ХостСервисаОбменаДанными(),
		443,
		ПредставлениеПодсистемы);
	НовыеРазрешения.Добавить(Разрешение);
	
	ЗапросыРазрешений.Добавить(
		МодульРаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(НовыеРазрешения));
	
КонецПроцедуры

// Заполняет описание используемых в подсистеме хостов сервисов Интернет-поддержки.
//   Разные платежные системы могут использовать один и тот же адрес сервиса.
//   В связи с этим, при выводе в список хостов будет отображаться только последняя из выведенных.
//   Поэтому, если в конфигурации есть подсистема НастройкиПлатежныхСистем, то она заменяет все другие подсистемы,
//   использующие этот же адрес сервиса.
//   Если зависимые подсистемы будут использовать другой адрес сервиса, то их нужно будет отключать независимо.
//
// Параметры:
//  ХостыСервисовИнтернетПоддержки - Соответствие Из КлючИЗначение - хост и название используемого сервиса:
//    * Ключ - Строка - хост.
//    * Значение - Строка - название сервиса
//
Процедура ПриЗаполненииХостовСервисовИнтернетПоддержки(ХостыСервисовИнтернетПоддержки) Экспорт

	ПредставлениеПодсистем = ПредставлениеВнедренныхПлатежныхСистем();

	Если ЗначениеЗаполнено(ПредставлениеПодсистем) Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Платежные системы (%1)'"),
			ПредставлениеПодсистем);
		
		ХостыСервисовИнтернетПоддержки.Вставить(
			НастройкиПлатежныхСистемСлужебный.ХостСервисаОбменаДанными(),
			Представление);
			
	КонецЕсли;
		
КонецПроцедуры

// Выполняет проверку необходимости привязки настройки подключения к настройке Онлайн-заказов.
// 
// Параметры:
//  Подсистема - Строка - идентификатор подсистемы.
//  ТребуетсяПривязка - Булево - необходимо установить в Истина, если для выставления ссылок на оплату обязательна
//    привязка к Онлайн-заказам.
// 
Процедура ПриОпределенииНеобходимостиПривязкиКОнлайнЗаказам(Подсистема, ТребуетсяПривязка) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		Возврат;
	КонецЕсли;
	
	Если Подсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг()
		Или Подсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыОзонПэй()
		Или Подсистема = ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ПлатежныеСервисы.ОзонПэй") Тогда
		ТребуетсяПривязка = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет, какие подключаемые команды должны быть доступны при активности любой строки
// на форме общих настроек подключения.
// Чтобы команда была доступна для любой ссылки, необходимо правильно заполнить параметр
// ТипыОбъектовПечати при добавлении команды. См. ПриОпределенииТиповОбъектовПечати.
// 
// Параметры:
//  КешПараметров - Структура - см. описание метода НастройкиПлатежныхСистемСобытия.ПриПодготовкеПараметровФормы
//  ВсеКоманды - Соответствие Из КлючИЗначение - описание кнопок формы:
//    * Ключ - Строка - имя элемента формы, с которым связана команда.
//    * Значение - Строка - имя команды.
//  ДоступныеКоманды - Массив Из Строка - имена команд, доступных для подсистемы СБП.
//
Процедура ПриОпределенииКомандДоступныхВсемНастройкам(
		ВсеКоманды,
		ДоступныеКоманды)
		
	// Не проверяем наличие подсистем, поскольку проверка по имени команды и, если она
	// выведена на форму, значит подсистема существует.
	Для Каждого КлючЗначение Из ВсеКоманды Цикл
		Если СтрЗаканчиваетсяНа(КлючЗначение.Ключ, "_НаклейкаСБПДверь")
			Или СтрЗаканчиваетсяНа(КлючЗначение.Ключ, "_НаклейкаСБПКасса") Тогда
				ДоступныеКоманды.Добавить(КлючЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

// Формирует строку с представлениями внедренных подсистем, относящихся к платежным системам.
//
// Возвращаемое значение:
//  Строка - представление подсистем, перечисленные через запятую.
//
Функция ПредставлениеВнедренныхПлатежныхСистем()
	
	// Проверка существования подсистем выполняется в методе ДобавитьСинонимОбъектаМетаданныхПоПодсистеме
	
	ЗависимыеПодсистемы = Новый Массив;
	
	ДобавитьСинонимОбъектаМетаданныхПоПодсистеме(
		"ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП",
		"Подсистема.ИнтернетПоддержкаПользователей.Подсистема.СистемаБыстрыхПлатежей",
		ЗависимыеПодсистемы);
	
	ДобавитьСинонимОбъектаМетаданныхПоПодсистеме(
		"ИнтернетПоддержкаПользователей.ИнтернетЭквайринг",
		"Подсистема.ИнтернетПоддержкаПользователей.Подсистема.ИнтернетЭквайринг",
		ЗависимыеПодсистемы);
		
	ДобавитьСинонимОбъектаМетаданныхПоПодсистеме(
		"ИнтернетПоддержкаПользователей.ПлатежныеСистемы.ОзонПэй",
		"Подсистема.ИнтернетПоддержкаПользователей.Подсистема.ПлатежныеСистемы.Подсистема.ОзонПэй",
		ЗависимыеПодсистемы);
	
	Возврат СтрСоединить(ЗависимыеПодсистемы, ", ");
	
КонецФункции

// Проверяет существование указанной подсистемы и добавляет синоним указанного объекта метаданных в параметр Получатель.
// 
// Параметры:
//  ИмяПодсистемы - Строка - имя подсистемы, существование которой нужно проверить.
//  ИмяОбъекта - Строка - полное имя объекта метаданных, синоним которого необходимо добавить.
//  Получатель - Массив Из Строка - переменная для добавления синонима.
// 
Процедура ДобавитьСинонимОбъектаМетаданныхПоПодсистеме(ИмяПодсистемы, ИмяОбъекта, Получатель)
	
	Если ОбщегоНазначения.ПодсистемаСуществует(ИмяПодсистемы) Тогда
		Мета = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ИмяОбъекта);
		Если Мета <> Неопределено Тогда
			Получатель.Добавить(Мета.Синоним);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти