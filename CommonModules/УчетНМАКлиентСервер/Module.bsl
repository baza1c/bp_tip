#Область СлужебныйПрограммныйИнтерфейс

// Возвращает представление элементов амортизации для отображения в интерфейсе.
// 
// Параметры:
//  СведенияНМА - см. НовыйСведенияНМА
//  ДанныеДокументаНМА - см. НовыйДанныеДокументаНМА
//  Постфикс - Строка - в некоторых случаях к названию реквизита добавляется окончание,
//                      например, СрокИспользованияДоПерехода или СрокИспользованияПересмотрено.
// 
// Возвращаемое значение:
//  Строка - Представление элементов амортизации
//
Функция ПредставлениеЭлементовАмортизации(СведенияНМА, ДанныеДокументаНМА, Постфикс = "") Экспорт
	
	ПредставлениеЭлементовАмортизации = "";
	
	Если ДанныеДокументаНМА.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.МалоценныйНМА")
		Или ДанныеДокументаНМА.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.ПрочийОбъектПрава") Тогда
		
			Если СведенияНМА.НеопределенныйСрокПолезногоИспользования Тогда
				ПредставлениеЭлементовАмортизации = НСтр("ru = 'Неопределенный срок использования'");
			Иначе
				ПредставлениеЭлементовАмортизации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Остаток срока использования: %1 мес.'"),
				Формат(СведенияНМА.СрокПолезногоИспользования, "ЧН=0;"));
			КонецЕсли;
			
			Возврат ПредставлениеЭлементовАмортизации;
			
	ИначеЕсли ДанныеДокументаНМА.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА")
		И Не ДанныеДокументаНМА.НаличиеПоДаннымУчета Тогда
			Возврат ПредставлениеЭлементовАмортизации;
	КонецЕсли;
	
	Если СведенияНМА.НеопределенныйСрокПолезногоИспользования Или Не ЗначениеЗаполнено(СведенияНМА.СпособНачисленияАмортизации) Тогда
		Возврат НСтр("ru = 'Неопределенный срок полезного использования'");
	КонецЕсли;
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(СведенияНМА.СпособНачисленияАмортизации);
	Если СведенияНМА.СпособНачисленияАмортизации = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.Линейный") Тогда
		ЧастиСтроки.Добавить(Формат(СведенияНМА.СрокПолезногоИспользования, "ЧН=0;") + НСтр("ru = ' мес.'"));
	ИначеЕсли СведенияНМА.СпособНачисленияАмортизации = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка") Тогда
		ЧастиСтроки.Добавить(Формат(СведенияНМА.СрокПолезногоИспользования, "ЧН=0;") + НСтр("ru = ' мес.'"));
		ЧастиСтроки.Добавить(НСтр("ru = 'коэф.'") + " " + Формат(СведенияНМА.Коэффициент, "ЧН=0;"));
	ИначеЕсли СведенияНМА.СпособНачисленияАмортизации = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции") Тогда
		ЧастиСтроки.Добавить(Формат(СведенияНМА.ОбъемПродукцииРабот, "ЧН=0;") + НСтр("ru = ' ед.'"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СведенияНМА.ЛиквидационнаяСтоимость) Тогда
		ЧастиСтроки.Добавить(Формат(СведенияНМА.ЛиквидационнаяСтоимость, "ЧФ='Ч руб.';"));
	КонецЕсли;
	
	Возврат СтрСоединить(ЧастиСтроки, ", ");

КонецФункции

Функция НовыйСведенияНМА() Экспорт

	СведенияНМА = Новый Структура;
	СведенияНМА.Вставить("НеопределенныйСрокПолезногоИспользования", Ложь);
	СведенияНМА.Вставить("СпособНачисленияАмортизации", ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.ПустаяСсылка"));
	СведенияНМА.Вставить("СрокПолезногоИспользования", 0);
	СведенияНМА.Вставить("Коэффициент", 0);
	СведенияНМА.Вставить("ОбъемПродукцииРабот", 0);
	СведенияНМА.Вставить("ЛиквидационнаяСтоимость", 0);
	
	Возврат СведенияНМА;
	
КонецФункции

Функция НовыйДанныеДокументаНМА() Экспорт
	
	ДанныеДокументаНМА = Новый Структура;
	ДанныеДокументаНМА.Вставить("ВидОбъекта", ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.ПустаяСсылка"));
	ДанныеДокументаНМА.Вставить("НаличиеПоДаннымУчета", Ложь);
	
	Возврат ДанныеДокументаНМА;
	
КонецФункции

Процедура ЗаполнитьСведенияНМАБезПостфиксов(СтрокаНМА, Постфикс, СведенияНМА, ДанныеДокументаНМА) Экспорт
	
	СведенияСПостфиксом = Новый Структура;	
	Для Каждого ЭлементСведений Из СведенияНМА Цикл
		СведенияСПостфиксом.Вставить(ЭлементСведений.Ключ + Постфикс, ЭлементСведений.Значение);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(СведенияСПостфиксом, СтрокаНМА);
	
	СведенияБезПостфиксов = Новый Структура; // Ключи без постфиксов
	Для Каждого КлючИЗначение Из СведенияСПостфиксом Цикл
		СведенияБезПостфиксов.Вставить(СтрЗаменить(КлючИЗначение.Ключ, Постфикс, ""), КлючИЗначение.Значение);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(СведенияНМА, СведенияБезПостфиксов);
	ЗаполнитьЗначенияСвойств(ДанныеДокументаНМА, СтрокаНМА);
	
КонецПроцедуры

// Конструктор реквизитов документов о регистрации НМА
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * Наименование - Строка
// * Номер - Строка
// * Дата - Дата
//
Функция НовыйРеквизитыДокументаРегистрации() Экспорт
	РеквизитыДокументаРегистрации = Новый Структура();
	РеквизитыДокументаРегистрации.Вставить("Наименование", "");
	РеквизитыДокументаРегистрации.Вставить("Номер", "");
	РеквизитыДокументаРегистрации.Вставить("Дата", Дата(1, 1, 1));
	Возврат Новый ФиксированнаяСтруктура(РеквизитыДокументаРегистрации);
КонецФункции

#КонецОбласти
