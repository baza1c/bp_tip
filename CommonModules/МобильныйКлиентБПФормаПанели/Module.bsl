#Область СлужебныйПрограммныйИнтерфейс

Процедура НастроитьФорму(Форма, ГруппаРодитель = Неопределено) Экспорт
	
	Форма.СворачиваниеЭлементовПоВажности =
			СворачиваниеЭлементовФормыПоВажности.Использовать;
	
	Форма.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы = Форма.Элементы;
	
	Если ГруппаРодитель= Неопределено Тогда
		ГруппаРодитель = Форма;
	КонецЕсли;
	
	ГруппаМКФорма = Элементы.Добавить("ГруппаМКФорма", Тип("ГруппаФормы"), ГруппаРодитель);
	ГруппаМКФорма.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФорма.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФорма.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФорма.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКФормаМобильная = Элементы.Добавить("ГруппаМКФормаДоступныеФункции", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМКФормаМобильная.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаМобильная.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаМобильная.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФормаМобильная.ОтображатьЗаголовок = Ложь;
	
	Элемент = Элементы.Добавить("НадписьМКОтступ", Тип("ДекорацияФормы"), ГруппаМКФорма);
	Элемент.Вид = ВидДекорацииФормы.Надпись;
	
	ГруппаМКФормаМобильная = Элементы.Добавить("ГруппаМКФормаНедоступныеФункции", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМКФормаМобильная.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаМобильная.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаМобильная.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	ГруппаМКФормаМобильная.Скрыть();
	ГруппаМКФормаМобильная.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ГруппаМКФормаМобильная.ОтображатьЗаголовок = Истина;
	ГруппаМКФормаМобильная.Заголовок = НСтр("ru = 'Больше функций доступно на компьютере'");
	
	ГруппаМКФормаМобильная = Элементы.Добавить("ГруппаМКФормаНедоступныеФункцииКонтейнер", Тип("ГруппаФормы"), ГруппаМКФормаМобильная);
	ГруппаМКФормаМобильная.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаМобильная.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаМобильная.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФормаМобильная.ОтображатьЗаголовок = Ложь;
	ГруппаМКФормаМобильная.Доступность = Ложь;
	
КонецПроцедуры

Процедура УстановитьДоступностьЭлементовПанелиПоГруппам(Форма, ГруппаФормы, ДоступноВМобильном = "") Экспорт
	
	Элементы = Форма.Элементы;
	ГруппаДоступные = Элементы.Найти("ГруппаМКФормаДоступныеФункции");
	ГруппаНедоступные = Элементы.Найти("ГруппаМКФормаНедоступныеФункцииКонтейнер");
	
	ДоступныеЭлементы = СтрРазделить(ДоступноВМобильном, ",", Ложь);
	ДоступныеЭлементы.Добавить("ГруппаМКФорма");
	
	Если ДоступныеЭлементы.Количество() = 0 Тогда
		Элементы.Переместить(ГруппаФормы, ГруппаНедоступные);
		Возврат;
	Иначе
		Элементы.Переместить(ГруппаФормы, ГруппаДоступные);
	КонецЕсли;
	
	ЭлементыГруппы = ГруппаФормы.ПодчиненныеЭлементы;
	
	ГруппаМКФорма = Элементы.Найти("ГруппаМКФорма_" + ГруппаФормы.Имя);
	Если ГруппаМКФорма = Неопределено Тогда
		ГруппаМКФорма = Элементы.Добавить("ГруппаМКФорма_" + ГруппаФормы.Имя, Тип("ГруппаФормы"), ГруппаНедоступные);
		ГруппаМКФорма.Вид = ГруппаФормы.Вид;
		ГруппаМКФорма.Группировка = ГруппаФормы.Группировка;
		ГруппаМКФорма.Отображение = ГруппаФормы.Отображение;
		ГруппаМКФорма.ОтображатьЗаголовок = ГруппаФормы.ОтображатьЗаголовок;
		ГруппаМКФорма.Заголовок = ГруппаФормы.Заголовок;
		ГруппаМКФорма.Видимость = ГруппаФормы.Видимость;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы,
		ЭлементыГруппы,
		ГруппаМКФорма,
		ДоступныеЭлементы);
	
КонецПроцедуры

Процедура АктуализироватьСвойстваНедоступныхГрупп(Форма, ИменаИсходныхГрупп = "") Экспорт
	
	Элементы = Форма.Элементы;
	
	Для Каждого ИмяИсходногоЭлемента Из СтрРазделить(ИменаИсходныхГрупп,",", Ложь) Цикл
		ОригинальныйЭлемент = Элементы.Найти(ИмяИсходногоЭлемента);
		Если ОригинальныйЭлемент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СвязанныйЭлемент = Элементы.Найти("ГруппаМКФорма_" + ИмяИсходногоЭлемента);
		Если СвязанныйЭлемент <> Неопределено Тогда
			СвязанныйЭлемент.Видимость = ОригинальныйЭлемент.Видимость;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьДоступностьЭлементовПанели(Форма, ДоступноВМобильном = "") Экспорт
	
	Элементы = Форма.Элементы;
	ГруппаНедоступные = Элементы.Найти("ГруппаМКФормаНедоступныеФункцииКонтейнер");
	
	ДоступныеЭлементы = СтрРазделить(ДоступноВМобильном, ",", Ложь);
	ДоступныеЭлементы.Добавить("ГруппаМКФорма");
	
	ЭлементыГруппы = Форма.ПодчиненныеЭлементы;
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы,
		ЭлементыГруппы,
		ГруппаНедоступные,
		ДоступныеЭлементы);
	
КонецПроцедуры

#КонецОбласти