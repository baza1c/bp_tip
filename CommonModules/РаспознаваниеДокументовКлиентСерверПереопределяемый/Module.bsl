#Область СлужебныеПроцедурыИФункции

Процедура ТипДокументаИВидОперации(ИмяКоманды, Результат) Экспорт
	
	// БП
	
	ИмяКоманды = СтрЗаменить(ИмяКоманды, "Форма", "");
	Если ИмяКоманды = "СоздатьРеализацияПродажаКомиссия" Тогда
		Результат.ТипДокумента = "РеализацияТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПродажаКомиссия");
	ИначеЕсли ИмяКоманды = "СоздатьРеализацияТовары" Тогда
		Результат.ТипДокумента = "РеализацияТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары");
	ИначеЕсли ИмяКоманды = "СоздатьРеализацияУслуги" Тогда
		Результат.ТипДокумента = "РеализацияТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Услуги");
		
	ИначеЕсли ИмяКоманды = "СоздатьПоступлениеПокупкаКомиссия" Тогда
		Результат.ТипДокумента = "ПоступлениеТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия");
	ИначеЕсли ИмяКоманды = "СоздатьПоступлениеТовары" Тогда
		Результат.ТипДокумента = "ПоступлениеТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Товары");
	ИначеЕсли ИмяКоманды = "СоздатьПоступлениеУслуги" Тогда
		Результат.ТипДокумента = "ПоступлениеТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги");
	ИначеЕсли ИмяКоманды = "СоздатьАвансовыйОтчетТовары" Тогда
		Результат.ТипДокумента = "АвансовыйОтчет";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийАвансовыйОтчет.ПокупкаОплатаПрочее");
	ИначеЕсли ИмяКоманды = "СоздатьАвансовыйОтчетПрочее" Тогда
		Результат.ТипДокумента = "АвансовыйОтчет";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийАвансовыйОтчет.ПокупкаОплатаПрочее");
	ИначеЕсли ИмяКоманды = "СоздатьАвансовыйОтчетПоКомандировке" Тогда
		Результат.ТипДокумента = "АвансовыйОтчет";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийАвансовыйОтчет.Командировка");
	ИначеЕсли ИмяКоманды = "СоздатьПутевойЛист" Тогда
		Результат.ТипДокумента = "ПутевойЛист";
	ИначеЕсли ИмяКоманды = "СоздатьРасходыПредпринимателя" Тогда
		Результат.ТипДокумента = "РасходыПредпринимателя";
	ИначеЕсли ИмяКоманды = "СоздатьКорректировкаПоступления" Тогда
		Результат.ТипДокумента = "КорректировкаПоступления";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение");
	ИначеЕсли ИмяКоманды = "СоздатьВозвратТоваровПоставщику" Тогда
		Результат.ТипДокумента = "ВозвратТоваровПоставщику";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийВозвратТоваровПоставщику.ПокупкаКомиссия");
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ТипДокументаИВидОперацииДляДокументаПоУмолчанию(Направление, ТипДокумента, Результат) Экспорт
	
	// БП
	
	Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий") Тогда
		Результат.ТипДокумента = "ПоступлениеТоваровУслуг";
		Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг") Тогда
			Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги");
		Иначе
			Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Товары");
		КонецЕсли;
	Иначе
		Результат.ТипДокумента = "РеализацияТоваровУслуг";
		Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг") Тогда
			Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Услуги");
		Иначе
			Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары");
		КонецЕсли;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ПодходящиеТипыРаспознанногоДокумента(ТипДокумента, Результат) Экспорт
	
	// БП
	
	Если ТипДокумента = Тип("ДокументСсылка.СчетНаОплатуПоставщика")
		Или ТипДокумента = Тип("ДокументСсылка.СчетНаОплатуПокупателю")
		Или ТипДокумента = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату"));
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.ТОРГ12"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД"));
		// Т.к. поступление или реализацию можно создать по распознанной СФ
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура"));
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СчетФактураПолученный")
		Или ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УКД"));
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.АвансовыйОтчет")
		Или ТипДокумента = Тип("ДокументСсылка.ПутевойЛист")
		Или ТипДокумента = Тип("ДокументСсылка.РасходыПредпринимателя") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.КассовыйЧек"));
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаПоступления")
		Или ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УКД"));
		
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ПриЗаполненииПараметровВыбораДоговора(ДокументОбъект, ПараметрыВыбораДоговора) Экспорт
	
	// БП
	
	ВидыДоговоров = Новый Массив;
	Если ДокументОбъект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"));
	Иначе
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"));
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"));
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"));
	КонецЕсли;
	
	ПараметрыВыбораДоговора.Вставить("ВидыДоговоров", ВидыДоговоров);
	
	// Конец БП
	
КонецПроцедуры

Процедура ОбязательныеРеквизитыПодчиненногоДокумента(ЭтотТипДокумента1С, ТипИВид, Реквизиты) Экспорт
	
	// БП
	
	Если ЭтотТипДокумента1С = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		Или ЭтотТипДокумента1С = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Реквизиты.Вставить("НомерДокумента");
		Реквизиты.Вставить("ДатаДокумента");
		Если ТипИВид.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Товары")
			Или ТипИВид.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия")
			Или ТипИВид.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары")
			Или ТипИВид.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПродажаКомиссия") Тогда
			
			Реквизиты.Вставить("Склад");
		КонецЕсли;
		
	ИначеЕсли ЭтотТипДокумента1С = Тип("ДокументСсылка.СчетФактураПолученный")
			Или ЭтотТипДокумента1С = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		Реквизиты.Вставить("НомерДокумента");
		Реквизиты.Вставить("ДатаДокумента");
		
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ЗаполнитьАналитикуУчета(СтрокаТабличнойЧасти, ПараметрыОбъекта) Экспорт
	
	// БП
	
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	
	Если Не СтрокаТабличнойЧасти.Свойство("АналитикаУчета") 
		ИЛИ НЕ ПользовательУправляетСчетамиУчета Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОбъекта.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
		// При вызове с клиента: чтобы сократить обмен с сервером, передадим только нужные данные
		// При вызове с сервера: функциональная опция может скрыть эти поля
		ДанныеЗаполнения = Новый Структура("СчетУчета, СчетДоходов, Субконто, СчетРасходов, СчетУчетаНДСПоРеализации, СуммаНДС, Услуга");
	Иначе
		ДанныеЗаполнения = Новый Структура("Сумма, СчетУчета, СчетУчетаНДС, СуммаНДС, СпособУчетаНДС,
			|СчетЗатрат, Субконто1, Субконто2, Субконто3,
			|СчетЗатратНУ, СубконтоНУ1, СубконтоНУ2, СубконтоНУ3,
			|ПодразделениеЗатрат, ОтражениеВУСН,
			|ВсегоПоДокументу, СуммаПоДокументу, СуммаНДСПоДокументу,
			|ВсегоОтклонение, СуммаОтклонение, СуммаНДСОтклонение,
			|НадписьПоДокументу, НадписьПоФакту, НадписьОтклонение,
			|Всего, АналитикаУчета, АналитикаУчетаЗаполнена, Услуга");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, СтрокаТабличнойЧасти);
	
	ОписаниеАналитикиУчета = РаспознаваниеДокументовБПВызовСервера.ОписаниеАналитикиУчета(
		ДанныеЗаполнения,
		ПараметрыОбъекта,
		Не ПараметрыОбъекта.ДокументБезНДС,
		ПараметрыОбъекта.ПлательщикНДФЛ,
		ПараметрыОбъекта.ПрименяетсяМалоценка);
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ОписаниеАналитикиУчета);
	
	// Конец БП
	
КонецПроцедуры

#КонецОбласти