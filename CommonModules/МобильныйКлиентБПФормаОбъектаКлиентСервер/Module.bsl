#Область ПрограммныйИнтерфейс

// Обновляется надпись доступных отборов пользователю
//
// Параметры:
//		Форма - Форма с динамическим списком
//
Процедура ОбновитьНадписьФиксированногоОтбора(Форма) Экспорт
	
	ПредставлениеОтбора = "";
	
	Список = Форма[Форма.МК_НаименованиеРеквизитаФормыДинамическийСписок];
	
	ИменаИсключаемыхОтборов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрЗаменить(Форма.МК_ИсключаемыеИзПредставленияИменаОтборов, " ",""), ",");
	//В мобильном не будем показывать "постоянные" отборы, для экономии места
	ИменаИсключаемыхОтборов.Добавить("ПометкаУдаления");
	КоллекцияОтборов = ФиксированныйОтборДинамическогоСписка(Форма, Список);
	Для каждого СоответствиеЭлементаОтбора Из КоллекцияОтборов Цикл
		ЭлементОтбора = СоответствиеЭлементаОтбора.Значение;
		Если ИменаИсключаемыхОтборов.Найти(Строка(ЭлементОтбора.ЛевоеЗначение)) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ДополнитьНадписьНастроекОтбораДинамическогоСписка(
				ПредставлениеЭлементаОтбораДинамическогоСписка(Форма, ЭлементОтбора),
				ПредставлениеОтбора, ЭлементОтбора);
	КонецЦикла;
	
	// Если нет отборов, то и элементы не нужно выводить на форму
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "НадписьМКПредставлениеОтбора", "Видимость", Не ПустаяСтрока(ПредставлениеОтбора));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "НадписьМКПредставлениеОтбора", "Заголовок", ПредставлениеОтбора);
	
КонецПроцедуры

Процедура УправлениеФормойСпискаКолонкаПрочее(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	ГруппаКолонокПрочее = Элементы.Найти("ГруппаКолонокМК_Прочее");
	КнопкаМКПоказатьСкрытьПрочиеКолонки = Элементы.Найти("КнопкаМКПоказатьСкрытьПрочиеКолонки");
	Если ГруппаКолонокПрочее <> Неопределено
		И КнопкаМКПоказатьСкрытьПрочиеКолонки  <> Неопределено Тогда
		
		ГруппаКолонокПрочее.Видимость = Не ГруппаКолонокПрочее.Видимость;
		Пометка = Не КнопкаМКПоказатьСкрытьПрочиеКолонки.Пометка;
		КнопкаМКПоказатьСкрытьПрочиеКолонки.Пометка = Пометка;
		КнопкаМКПоказатьСкрытьПрочиеКолонки.Заголовок = ?(КнопкаМКПоказатьСкрытьПрочиеКолонки.Пометка,
			НСтр("ru = 'Оставить только важное'"),
			НСтр("ru = 'Показать все поля'"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеФормойОтбораМК(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Элементы.ГруппаМКБыстрыеОтборы.Видимость = Не Элементы.ГруппаМКБыстрыеОтборы.Видимость;
	Элементы.НадписьМКПредставлениеОтбора.Видимость = Не Элементы.ГруппаМКБыстрыеОтборы.Видимость;
	Если Элементы.НадписьМКПредставлениеОтбора.Видимость Тогда
		МобильныйКлиентБПФормаОбъектаКлиентСервер.ОбновитьНадписьФиксированногоОтбора(Форма);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает не доступные пользователю отборы
//
// Параметры:
//		Форма - Форма с динамическим списком
//		Список - Реквизит формы "Динамический список".
//
// Возвращаемое значение:
//		КоллекцияОтборов - Соответствие
//				Ключ - Поле компоновки данных
//				Значение - Элемент отбора компоновки данных или структура, соответствующая свойствам элементу отбора компоновки данных.
//
Функция ФиксированныйОтборДинамическогоСписка(Форма, Список) Экспорт
	
	КоллекцияОтборов = Новый Соответствие;
	
	ИменаИсключаемыхОтборов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрЗаменить(Форма.МК_ИсключаемыеИзПредставленияИменаОтборов, " ",""), ",");
	
	// Сначала пользовательские настройки, т.к. добавление идет в начало коллекции
	Для каждого ЭлементНастроек Из Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастроек) = Тип("ОтборКомпоновкиДанных") Тогда
			Для каждого ЭлементОтбора Из ЭлементНастроек.Элементы Цикл
				Если ЭлементОтбора.Использование
					Или Не ПустаяСтрока(ЭлементОтбора.ИдентификаторПользовательскойНастройки)
					Или ТипЗнч(ЭлементОтбора) <> Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
					И ИменаИсключаемыхОтборов.Найти(Строка(ЭлементОтбора.ЛевоеЗначение)) <> Неопределено Тогда
					Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
						СтруктураЗначенияОтбора = Новый Структура;
						СтруктураЗначенияОтбора.Вставить("ЛевоеЗначение", ЭлементОтбора.Представление);
						СтруктураЗначенияОтбора.Вставить("ВидСравнения", ВидСравненияКомпоновкиДанных.Равно);
						СтруктураЗначенияОтбора.Вставить("Использование", ЭлементОтбора.Использование);
						СтруктураЗначенияОтбора.Вставить("ПравоеЗначение", Истина);
						СтруктураЗначенияОтбора.Вставить("РежимОтображения", ЭлементОтбора.РежимОтображения);
						СтруктураЗначенияОтбора.Вставить("Представление", "");
						СтруктураЗначенияОтбора.Вставить("ЭтоГруппа", Истина);
						КоллекцияОтборов.Вставить(ЭлементОтбора.Представление, СтруктураЗначенияОтбора);
					Иначе
						КоллекцияОтборов.Вставить(ЭлементОтбора.ЛевоеЗначение, ЭлементОтбора);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ЭлементОтбора Из Список.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ЭлементОтбора.Использование
			Или Не ПустаяСтрока(ЭлементОтбора.ИдентификаторПользовательскойНастройки)
			Или ТипЗнч(ЭлементОтбора) <> Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
				И ИменаИсключаемыхОтборов.Найти(Строка(ЭлементОтбора.ЛевоеЗначение)) <> Неопределено Тогда
			Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				СтруктураЗначенияОтбора = Новый Структура;
				СтруктураЗначенияОтбора.Вставить("ЛевоеЗначение", ЭлементОтбора.Представление);
				СтруктураЗначенияОтбора.Вставить("ВидСравнения", ВидСравненияКомпоновкиДанных.Равно);
				СтруктураЗначенияОтбора.Вставить("Использование", ЭлементОтбора.Использование);
				СтруктураЗначенияОтбора.Вставить("ПравоеЗначение", Истина);
				СтруктураЗначенияОтбора.Вставить("РежимОтображения", ЭлементОтбора.РежимОтображения);
				СтруктураЗначенияОтбора.Вставить("Представление", "");
				СтруктураЗначенияОтбора.Вставить("ЭтоГруппа", Истина);
				КоллекцияОтборов.Вставить(ЭлементОтбора.Представление, СтруктураЗначенияОтбора);
			Иначе
				КоллекцияОтборов.Вставить(ЭлементОтбора.ЛевоеЗначение, ЭлементОтбора);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоллекцияОтборов;
	
КонецФункции

// Возвращает пользовательское представление идентификатора
//
// Параметры:
//		ИмяБезПробелов - Идентификатор, по которому требуется получить представление.
//
// Возвращаемое значение:
//		ПредставлениеЭлементаОтбора - Строка - Пользовательское представление идентификатора.
//
Функция ПредставлениеИмениБезПробелов(ИмяБезПробелов) Экспорт
	
	ПредставлениеЭлементаОтбора = "";
	БылВРег = Ложь;
	Для НомерСимвола = 1 По СтрДлина(ИмяБезПробелов) Цикл
		Символ = Сред(ИмяБезПробелов, НомерСимвола, 1);
		СледующийСимволВРег = Ложь;
		Если НомерСимвола + 1 <= СтрДлина(ИмяБезПробелов) Тогда
			СледующийСимвол = Сред(ИмяБезПробелов, НомерСимвола + 1, 1);
			СледующийСимволВРег = СледующийСимвол = ВРег(СледующийСимвол);
		КонецЕсли;
		ЭтоВРег = Символ = ВРег(Символ);
		Если Не БылВРег И ЭтоВРег И Не НомерСимвола = 1 Тогда
			Если СледующийСимволВРег Тогда
				Символ = " " + ВРег(Символ);
			Иначе
				Символ = " " + НРег(Символ);
			КонецЕсли;
		КонецЕсли;
		БылВРег = ЭтоВРег;
		ПредставлениеЭлементаОтбора = ПредставлениеЭлементаОтбора + Символ;
	КонецЦикла;
	
	Возврат ПредставлениеЭлементаОтбора;
	
КонецФункции

Процедура ДополнитьНадписьНастроекОтбораДинамическогоСписка(НаименованиеЭлементаОтбора, СписокНастройкиОтбораНадпись, ЭлементОтбора)
	
	Если Не ЭлементОтбора.Использование Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЭлементаОтбора = НаименованиеЭлементаОтбора;
	
	ПравоеЗначениеОтбора = ЭлементОтбора.ПравоеЗначение;
	Если ТипЗнч(ПравоеЗначениеОтбора) = Тип("Дата") Тогда
		ПравоеЗначениеОтбора = Формат(ПравоеЗначениеОтбора, "ДЛФ=DD");
	КонецЕсли;
	
	Если ТипЗнч(ЭлементОтбора) = Тип("Структура") И ЭлементОтбора.Свойство("ЭтоГруппа") И ЭлементОтбора.ЭтоГруппа = Истина Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1'"), ТекстЭлементаОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
		Если ТипЗнч(ПравоеЗначениеОтбора) = Тип("ФиксированныйМассив") Или ТипЗнч(ПравоеЗначениеОтбора) = Тип("Массив") Тогда
			Если ПравоеЗначениеОтбора.Количество() > 0 Тогда
				ТекстЭлементаОтбора = СтрШаблон(
					НСтр("ru = '%1 в списке:%2'"),
					ТекстЭлементаОтбора,
					?(ПравоеЗначениеОтбора.Количество() > 1, НСтр("ru = 'отобрано по нескольким значениям'"), ПравоеЗначениеОтбора[0]));
			КонецЕсли;
		Иначе
			ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1: %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
		КонецЕсли;
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 > %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 >= %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 в иерархии %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
		Если ПравоеЗначениеОтбора.Количество() > 0 Тогда
			ТекстЭлементаОтбора = СтрШаблон(
				НСтр("ru = '%1 в списке:%2'"),
				ТекстЭлементаОтбора,
				?(ПравоеЗначениеОтбора.Количество() > 1, НСтр("ru = 'отобрано по нескольким значениям'"), ПравоеЗначениеОтбора[0]));
		КонецЕсли;
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
		Если ПравоеЗначениеОтбора.Количество() > 0 Тогда
			ТекстЭлементаОтбора = СтрШаблон(
				НСтр("ru = '%1 в списке по иерархии:%2'"),
				ТекстЭлементаОтбора,
				?(ПравоеЗначениеОтбора.Количество() > 1, НСтр("ru = 'отобрано по нескольким значениям'"), ПравоеЗначениеОтбора[0]));
		КонецЕсли;
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 заполнено'"), ТекстЭлементаОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 < %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 <= %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НачинаетсяС Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 начинается с %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 не в иерархии %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке Тогда
		Если ПравоеЗначениеОтбора.Количество() > 0 Тогда
			ТекстЭлементаОтбора = СтрШаблон(
				НСтр("ru = '%1 не в списке:%2'"),
				ТекстЭлементаОтбора,
				?(ПравоеЗначениеОтбора.Количество() > 1, НСтр("ru = 'отобрано по нескольким значениям'"), ПравоеЗначениеОтбора[0]));
		КонецЕсли;
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		Если ПравоеЗначениеОтбора.Количество() > 0 Тогда
			ТекстЭлементаОтбора = СтрШаблон(
				НСтр("ru = '%1 не в списке по иерархии:%2'"),
				ТекстЭлементаОтбора,
				?(ПравоеЗначениеОтбора.Количество() > 1, НСтр("ru = 'отобрано по нескольким значениям'"), ПравоеЗначениеОтбора[0]));
		КонецЕсли;
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 не заполнено'"), ТекстЭлементаОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеНачинаетсяС Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 не начинается с %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеПодобно Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 не содержит %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 <> %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеСодержит Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 не содержит %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Подобно Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 содержит %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит Тогда
		ТекстЭлементаОтбора = СтрШаблон(НСтр("ru = '%1 содержит %2'"), ТекстЭлементаОтбора, ПравоеЗначениеОтбора);
	Иначе
		Возврат;
	КонецЕсли;
	
	ТекстЭлементаОтбора = ?(СтрДлина(ТекстЭлементаОтбора)>50, Лев(ТекстЭлементаОтбора, 50) + "...", ТекстЭлементаОтбора);
	ТекстРазделителя = ?(ПустаяСтрока(СписокНастройкиОтбораНадпись), "", "; ");
	СписокНастройкиОтбораНадпись = СтрШаблон(НСтр("ru = '%1%2%3'"), СписокНастройкиОтбораНадпись, ТекстРазделителя, ТекстЭлементаОтбора);
	
КонецПроцедуры

Функция ПредставлениеЭлементаОтбораДинамическогоСписка(Форма, ЭлементОтбора)
	
	Если ТипЗнч(ЭлементОтбора) = Тип("Строка") Тогда
		НаименованиеОтбора = ЭлементОтбора;
	Иначе
		ПредставлениеЭлементаОтбора = ЭлементОтбора.Представление;
		Если Не ПустаяСтрока(ПредставлениеЭлементаОтбора) Тогда
			Возврат ПредставлениеЭлементаОтбора;
		КонецЕсли;
		
		ЛевоеЗначениеОтбора = ЭлементОтбора.ЛевоеЗначение;
		
		НаименованиеОтбора = Строка(ЛевоеЗначениеОтбора);
	КонецЕсли;
		
	Если НаименованиеОтбора = "Тип" Тогда
		Возврат НСтр("ru = 'Тип документа'");
	КонецЕсли;
	
	ЭлементСписка = Форма.МК_СинонимыЭлементовОтбора.НайтиПоЗначению(НаименованиеОтбора);
	Если ЭлементСписка <> Неопределено И Не ПустаяСтрока(ЭлементСписка.Представление) Тогда
		НаименованиеОтбора = ЭлементСписка.Представление;
	Иначе
		НаименованиеОтбора = ПредставлениеИмениБезПробелов(НаименованиеОтбора);
	КонецЕсли;
	
	Возврат НаименованиеОтбора;
	
КонецФункции

Процедура АдаптироватьЭлементыМКФормы(Форма) Экспорт
	
	#Если Не МобильныйКлиент Тогда
		#Если Сервер Тогда
			Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
				Возврат;
			КонецЕсли;
		#Иначе
			Возврат;
		#КонецЕсли
	#КонецЕсли

	Элементы = Форма.Элементы;
	
	//Адаптация для формы не инициализирована, адаптацию выполнять не требуется
	Если Элементы.Найти("ГруппаМКФорма") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаМКФормаВажное = Элементы.Найти("ГруппаМКФормаВажное");
	
	ГруппаМКТабличныеЧасти = Элементы.Найти("ГруппаМКФормаТабличныеЧасти");
	ГруппаМКПрочее = Элементы.Найти("ГруппаМКФормаПрочее");
	
	ИменаЭлементовИсключений = СтрРазделить(Форма.МКИменаЭлементовИсключеныИзАдаптации,",", Ложь);
	ИменаЭлементовИсключений.Добавить("ГруппаМКДатаНомер");
	
	АдаптироватьЭлементыФормыРекурсивно(Форма, ГруппаМКФормаВажное.ПодчиненныеЭлементы, ИменаЭлементовИсключений);
	АдаптироватьЭлементыФормыРекурсивно(Форма, ГруппаМКТабличныеЧасти.ПодчиненныеЭлементы, ИменаЭлементовИсключений);
	АдаптироватьЭлементыФормыРекурсивно(Форма, ГруппаМКПрочее.ПодчиненныеЭлементы, ИменаЭлементовИсключений);
	
КонецПроцедуры

Процедура АдаптироватьЭлементФормы(Форма, ЭлементФормы) Экспорт
	
	ЭлементФормы.ЦветТекстаЗаголовка = Форма.МКАдаптацияЦветТекстаЗаголовка;
	ЭлементФормы.ШрифтЗаголовка = Форма.МКАдаптацияШрифтЗаголовка;
	ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	
КонецПроцедуры

Процедура АдаптироватьЭлементыФормыРекурсивно(Форма, КоллекцияЭлементов, ИменаЭлементовИсключений) Экспорт
	
	Для каждого ПодчиненныйЭлемент Из КоллекцияЭлементов Цикл
		Если ИменаЭлементовИсключений.Найти(ПодчиненныйЭлемент.Имя) <> Неопределено
			Или Типзнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа
			Или ПодчиненныйЭлемент.Вид = ВидГруппыФормы.Страница
			Или ПодчиненныйЭлемент.Вид = ВидГруппыФормы.Страницы Тогда
			АдаптироватьЭлементыФормыРекурсивно(Форма, ПодчиненныйЭлемент.ПодчиненныеЭлементы,
				ИменаЭлементовИсключений);
		Иначе
			Если (ПодчиненныйЭлемент.Вид = ВидПоляФормы.ПолеВвода
					Или ПодчиненныйЭлемент.Вид = ВидПоляФормы.ПолеНадписи)
				И ПодчиненныйЭлемент.ПоложениеЗаголовка <> ПоложениеЗаголовкаЭлементаФормы.Нет Тогда
				АдаптироватьЭлементФормы(Форма, ПодчиненныйЭлемент);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

