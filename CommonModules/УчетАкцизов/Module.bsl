// Содержимое модуля отличается в версиях ПРОФ и КОРП.
// В версии ПРОФ отсутствует:
// - тело экспортных процедур (остаются только сигнатуры методов)
// - неэкспортные процедуры и все функции.
// Поэтому не допускается реализация в модуле экспортных функций: вместо них следует использовать процедуры с возвращаемым параметром.

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьКлассификаторыПоАкцизам(Организация, ВидТовара, Импорт) Экспорт
	Справочники.ВидыНоменклатуры.НайтиСоздатьПодакцизныйВидНоменклатуры(Организация, ВидТовара, Импорт);
	НайтиСоздатьКодыПодакцизныхТоваров(ВидТовара);
	РегистрыСведений.СтавкиАкциза.ЗаполнитьСтавкиАкцизаПоВидуПодакцизногоТовара(ВидТовара);
КонецПроцедуры

Процедура СписокПроизводимыхПодакцизныхТоваров(Организация, СписокПроизводимыхТоваров) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокУплатыАкцизов.ВидТовара КАК ВидТовара
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|	И ПорядокУплатыАкцизов.Производство = ИСТИНА";
	Запрос.УстановитьПараметр("Организация", Организация);
	Результат = Запрос.Выполнить().Выгрузить();
	
	СписокПроизводимыхТоваров.ЗагрузитьЗначения(Результат.ВыгрузитьКолонку("ВидТовара"));
	
КонецПроцедуры

Процедура СписокИмпортируемыхПодакцизныхТоваров(Организация, СписокИмпортируемыхТоваров) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокУплатыАкцизов.ВидТовара КАК ВидТовара
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|	И ПорядокУплатыАкцизов.Импорт = ИСТИНА";
	Запрос.УстановитьПараметр("Организация", Организация);
	Результат = Запрос.Выполнить().Выгрузить();
	
	СписокИмпортируемыхТоваров.ЗагрузитьЗначения(Результат.ВыгрузитьКолонку("ВидТовара"));
	
КонецПроцедуры

Процедура ПолучитьСтавкуАкциза(КодВидаПодакцизногоТовара, Дата, ДанныеПоСтавкеАкциза) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтавкиАкцизаСрезПоследних.Сумма КАК Сумма,
	|	СтавкиАкцизаСрезПоследних.ОкруглятьДоЦелого КАК ОкруглятьДоЦелого
	|ИЗ
	|	РегистрСведений.СтавкиАкциза.СрезПоследних(&Дата, КодВидаПодакцизногоТовара = &КодВидаПодакцизногоТовара) КАК СтавкиАкцизаСрезПоследних";
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("КодВидаПодакцизногоТовара", КодВидаПодакцизногоТовара);
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		ДанныеПоСтавкеАкциза.Ставка            = Результат.Сумма;
		ДанныеПоСтавкеАкциза.ОкруглятьДоЦелого = Результат.ОкруглятьДоЦелого;
	КонецЕсли;
		
КонецПроцедуры

Процедура СформироватьДвиженияНачисленияАкцизов(ТаблицаАкцизов, Реквизиты, Движения) Экспорт
	Если НЕ ЗначениеЗаполнено(ТаблицаАкцизов) Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Реквизиты) Тогда
		Возврат;
	КонецЕсли;
	СтрокаРеквизитов = Реквизиты[0];
	ВедетсяУчетПоПодразделениям = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();

	Для Каждого Строка Из ТаблицаАкцизов Цикл
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период = СтрокаРеквизитов.Период;
		Проводка.Организация =  СтрокаРеквизитов.Организация;
		Проводка.Содержание  = "Начисление акциза";
		Проводка.СчетДт = Строка.СчетАкциза;
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Строка.СчетАкциза);
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаРеквизитов.ПодразделениеОрганизации;
		КонецЕсли;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеГруппы", Строка.Субконто);
		Проводка.СчетКт = ПланыСчетов.Хозрасчетный.Акцизы;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		Если ВедетсяУчетПоПодразделениям Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РегистрацияВНалоговомОргане", Строка.СубконтоАкцизы);
		КонецЕсли;
		Проводка.Сумма = Строка.СуммаАкциза;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьДвиженияСторноАкцизов(ТаблицаАкцизов, Реквизиты, Движения) Экспорт
	Если НЕ ЗначениеЗаполнено(ТаблицаАкцизов) Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Реквизиты) Тогда
		Возврат;
	КонецЕсли;
	СтрокаРеквизитов = Реквизиты[0];
	Корректировка = ТипЗнч(СтрокаРеквизитов.Регистратор) = Тип("ДокументСсылка.КорректировкаРеализации");
	Если Корректировка Тогда
		КорректировкаПоСогласованиюСторон = СтрокаРеквизитов.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
	Иначе
		КорректировкаПоСогласованиюСторон = Истина;
	КонецЕсли;
	Если КорректировкаПоСогласованиюСторон Тогда
		КорСчет = ПланыСчетов.Хозрасчетный.АкцизыПоОплаченнымМатериальнымЦенностям;
	Иначе
		КорСчет = ПланыСчетов.Хозрасчетный.Акцизы;
	КонецЕсли;
	ВедетсяУчетПоПодразделениям = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();
	Для Каждого Строка Из ТаблицаАкцизов Цикл
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период = СтрокаРеквизитов.Период;
		Проводка.Организация =  СтрокаРеквизитов.Организация;
		Проводка.Содержание  = "Сторно акциза";
		Проводка.СчетДт = Строка.СчетАкциза;
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Строка.СчетАкциза);
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаРеквизитов.ПодразделениеОрганизации;
		КонецЕсли;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеГруппы", Строка.Субконто);
		Проводка.СчетКт = КорСчет;
		Если Не КорректировкаПоСогласованиюСторон Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			Если ВедетсяУчетПоПодразделениям Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РегистрацияВНалоговомОргане", Строка.СубконтоАкцизы);
			КонецЕсли;
		КонецЕсли;
		Проводка.Сумма = Строка.СуммаАкциза;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьДвиженияНачисленияАкцизовПриИмпорте(ТаблицаАкцизов, Реквизиты, Движения) Экспорт
	Если НЕ ЗначениеЗаполнено(ТаблицаАкцизов) Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Реквизиты) Тогда
		Возврат;
	КонецЕсли;
	СтрокаРеквизитов = Реквизиты[0];
	ЗаявлениеОВвозе = ТипЗнч(СтрокаРеквизитов.Регистратор) = Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров");
	ПартионныйУчет                  = УчетнаяПолитика.СпособОценкиМПЗ(СтрокаРеквизитов.Организация, СтрокаРеквизитов.Период) = Перечисления.СпособыОценки.ФИФО;
	ЕстьПодразделениеВТаблице 		= ТаблицаАкцизов.Колонки.Найти("Подразделение") <> Неопределено;
	ЕстьПодразделениеВШапке 		= Реквизиты.Колонки.Найти("ПодразделениеОрганизации") <> Неопределено;
	ТаблицаАкцизовПоУплатеТаможне = ТаблицаАкцизов.Скопировать();
	Если ЗаявлениеОВВозе Тогда
		ТаблицаАкцизовПоУплатеТаможне.Свернуть("СчетАкциза, СубконтоАкцизы, ДатаПринятияКУчету", "СуммаАкциза");
	Иначе
		ТаблицаАкцизовПоУплатеТаможне.Свернуть("Подразделение, СчетАкциза", "СуммаАкциза"); 
	КонецЕсли;
	ВедетсяУчетПоПодразделениям = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();
	Для Каждого СтрокаТаблицы Из ТаблицаАкцизовПоУплатеТаможне Цикл
		
		Подразделение 	= Неопределено;
		Если ЕстьПодразделениеВТаблице И НЕ ЗаявлениеОВвозе Тогда
			Подразделение = СтрокаТаблицы.Подразделение;
		ИначеЕсли ЕстьПодразделениеВШапке И НЕ ЗаявлениеОВвозе Тогда
			Подразделение = СтрокаРеквизитов.Подразделение;
		КонецЕсли;
		
		// Проводка Дт 19.06 Кт 60.х
		// Или 19.06-68.03 - если по заявлению о ввозе
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = СтрокаРеквизитов.Период;
		Проводка.Организация = СтрокаРеквизитов.Организация;
		Если ЗаявлениеОВвозе Тогда
			Проводка.Содержание  = "Начислен акциз при ввозе из таможенного союза";
		Иначе
			Проводка.Содержание  = "Уплата акциза на таможне";
		КонецЕсли;
		
		Проводка.СчетДт      = СтрокаТаблицы.СчетАкциза;
		Проводка.СчетКт      = СтрокаРеквизитов.СчетУчетаРасчетовСКонтрагентом;
		Если Не ЗаявлениеОВвозе Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			1, СтрокаРеквизитов.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			2, СтрокаРеквизитов.ДоговорКонтрагента);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			3, СтрокаРеквизитов.Регистратор);
			
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = Подразделение;
			КонецЕсли;
		Иначе
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			Если ВедетсяУчетПоПодразделениям Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РегистрацияВНалоговомОргане", СтрокаТаблицы.СубконтоАкцизы);
			КонецЕсли;
		КонецЕсли;
		Проводка.Сумма = СтрокаТаблицы.СуммаАкциза;
		
		Если ЗаявлениеОВвозе Тогда
			МесяцУплаты               = ДобавитьМесяц(СтрокаТаблицы.ДатаПринятияКУчету, 1);
			СрокУплаты                = Дата(Год(МесяцУплаты), Месяц(МесяцУплаты), 20);
			ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
			ГрафикПереноса            = КалендарныеГрафики.БлижайшиеРабочиеДаты(ПроизводственныйКалендарь,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СрокУплаты));
			Если Не ЕдиныйНалоговыйСчет.ЭтоПростойУчет(ГрафикПереноса[СрокУплаты], СтрокаРеквизитов.Организация) Тогда
				Продолжить;
			КонецЕсли;
			
			Проводка = Движения.Хозрасчетный.Добавить();
			Проводка.Период      = ГрафикПереноса[СрокУплаты];
			Проводка.Организация = СтрокаРеквизитов.Организация;
			Проводка.Содержание  = "Единый налоговый счет: Начисление акциза за товары из Таможенного союза";
			Проводка.СчетДт      = СтрокаРеквизитов.СчетУчетаРасчетовСКонтрагентом;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			Если ВедетсяУчетПоПодразделениям Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РегистрацияВНалоговомОргане", СтрокаТаблицы.СубконтоАкцизы);
			КонецЕсли;
			Проводка.СчетКт      = ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет;
			Проводка.Сумма       = СтрокаТаблицы.СуммаАкциза;
		КонецЕсли;

	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаАкцизов Цикл
		Подразделение 	= Неопределено;
		Если ЕстьПодразделениеВТаблице Тогда
			Подразделение = СтрокаТаблицы.Подразделение;
		ИначеЕсли ЕстьПодразделениеВШапке Тогда
			Подразделение = СтрокаРеквизитов.Подразделение;
		КонецЕсли;
		
		// Проводка Дт Счет учета (41.х или 10.х) Кт 19.06
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период = СтрокаРеквизитов.Период;
		Проводка.Организация = СтрокаРеквизитов.Организация;
		Проводка.Содержание  = "Включение суммы акциза в стоимость";
		Проводка.СчетДт      = СтрокаТаблицы.СчетУчета;
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
				Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
				Ном, СтрокаТаблицы.Номенклатура);
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаТаблицы.СчетУчета) Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
					Ном, СтрокаТаблицы.Склад);
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если ПартионныйУчет Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
					Ном, СтрокаТаблицы.Партия);
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла;
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = Подразделение;
		КонецЕсли;
		Проводка.СчетКт = СтрокаТаблицы.СчетАкциза;
		Проводка.Сумма  = СтрокаТаблицы.СуммаАкциза;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
КонецПроцедуры

Процедура ЗаполнитьКодВидаПодакцизногоТовараПоУмолчанию(ВидТовара, КодВидаПодакцизногоТовара) Экспорт
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыВидовПодакцизныхТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КодыВидовПодакцизныхТоваров КАК КодыВидовПодакцизныхТоваров
	|ГДЕ
	|	КодыВидовПодакцизныхТоваров.ВидТовара = &ВидТовара
	|	И КодыВидовПодакцизныхТоваров.ПометкаУдаления = ЛОЖЬ
	|	И ВЫБОР
	|			КОГДА КодыВидовПодакцизныхТоваров.ДействуетС = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ КодыВидовПодакцизныхТоваров.ДействуетС >= &Дата
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА КодыВидовПодакцизныхТоваров.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ КОНЕЦПЕРИОДА(КодыВидовПодакцизныхТоваров.ДействуетПо, ДЕНЬ) <= &Дата
	|		КОНЕЦ";

	Запрос.УстановитьПараметр("ВидТовара", ВидТовара);
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 1 Тогда
		КодВидаПодакцизногоТовара = Результат[0].Ссылка;
	Иначе
		КодВидаПодакцизногоТовара = Неопределено;
	КонецЕсли;
КонецПроцедуры

Процедура ПроверкаДействияАкцизаПоСпискуНоменклатуры(СписокНоменклатуры, Дата, ЗначенияРеквизитов) Экспорт
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокНоменклатуры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|			КОГДА КодыВидовПодакцизныхТоваров.ДействуетС = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ КодыВидовПодакцизныхТоваров.ДействуетС <= &Дата
	|		КОНЕЦ
	|		И ВЫБОР
	|			КОГДА КодыВидовПодакцизныхТоваров.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ КОНЕЦПЕРИОДА(КодыВидовПодакцизныхТоваров.ДействуетПо, ДЕНЬ) >= &Дата
	|		КОНЕЦ КАК АкцизСчитаетсяНаДату,
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|					КОГДА КодыВидовПодакцизныхТоваров.ДействуетС = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ КодыВидовПодакцизныхТоваров.ДействуетС <= &Дата
	|				КОНЕЦ
	|				И ВЫБОР
	|					КОГДА КодыВидовПодакцизныхТоваров.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ КОНЕЦПЕРИОДА(КодыВидовПодакцизныхТоваров.ДействуетПо, ДЕНЬ) >= &Дата
	|				КОНЕЦ
	|			ТОГДА Номенклатура.КодВидаПодакцизногоТовара
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КодВидаПодакцизногоТовара
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КодыВидовПодакцизныхТоваров КАК КодыВидовПодакцизныхТоваров
	|		ПО Номенклатура.КодВидаПодакцизногоТовара = КодыВидовПодакцизныхТоваров.Ссылка
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокНоменклатуры)";
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.УстановитьПараметр("Дата",                Дата);
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Результат = Новый Структура("КодВидаПодакцизногоТовара, АкцизСчитаетсяНаДату");
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		ЗначенияРеквизитов[Выборка.Номенклатура] = Результат;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеПоАкцизам(ТаблицаТовары, Организация, Дата, ПараметрыДляЗаполнения) Экспорт
	Если ТаблицаТовары = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПорядокУплатыАкцизов.ВидТовара КАК ВидТовара
		|ПОМЕСТИТЬ СписокУчитываемыхПодакцизныхТоваров
		|ИЗ
		|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
		|ГДЕ
		|	ПорядокУплатыАкцизов.Организация = &Организация
		|	И ПорядокУплатыАкцизов.Производство = &Производство
		|	И ПорядокУплатыАкцизов.Импорт = &Импорт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.КодВидаПодакцизногоТовара КАК КодВидаПодакцизногоТовара,
		|	Номенклатура.КоэффициентПересчета КАК КоэффициентПересчета,
		|	КодыВидовПодакцизныхТоваров.ВидТовара КАК ВидТовара,
		|	Номенклатура.ЕдиницаИзмеренияДляРасчетаАкциза КАК ЕдиницаИзмеренияДляРасчетаАкциза,
		|	ВЫБОР
		|		КОГДА &ЗаявлениеОВвозеТоваров
		|			ТОГДА Номенклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция)
		|					ИЛИ Номенклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)
		|					ИЛИ Номенклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво)
		|					ИЛИ Номенклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.СоковаяПродукция)
		|					ИЛИ Номенклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
		|					ИЛИ Номенклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак)
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК МаркируемаяПродукция
		|ПОМЕСТИТЬ СписокНоменклатуры
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КодыВидовПодакцизныхТоваров КАК КодыВидовПодакцизныхТоваров
		|		ПО Номенклатура.КодВидаПодакцизногоТовара = КодыВидовПодакцизныхТоваров.Ссылка
		|ГДЕ
		|	Номенклатура.Ссылка В(&СписокНоменклатуры)
		|	И ВЫБОР
		|			КОГДА КодыВидовПодакцизныхТоваров.ДействуетС = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ КодыВидовПодакцизныхТоваров.ДействуетС <= &Дата
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА КодыВидовПодакцизныхТоваров.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ КОНЕЦПЕРИОДА(КодыВидовПодакцизныхТоваров.ДействуетПо, ДЕНЬ) >= &Дата
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокУчитываемыхПодакцизныхТоваров.ВидТовара КАК ВидТовара,
		|	СписокНоменклатуры.Ссылка КАК Ссылка,
		|	СписокНоменклатуры.КодВидаПодакцизногоТовара КАК КодВидаПодакцизногоТовара,
		|	СписокНоменклатуры.КоэффициентПересчета КАК КоэффициентПересчета,
		|	СписокНоменклатуры.ЕдиницаИзмеренияДляРасчетаАкциза КАК ЕдиницаИзмеренияДляРасчетаАкциза
		|ПОМЕСТИТЬ СписокНоменклатурыУчитываемойВТекОрганизации
		|ИЗ
		|	СписокУчитываемыхПодакцизныхТоваров КАК СписокУчитываемыхПодакцизныхТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ СписокНоменклатуры КАК СписокНоменклатуры
		|		ПО СписокУчитываемыхПодакцизныхТоваров.ВидТовара = СписокНоменклатуры.ВидТовара
		|ГДЕ
		|	СписокНоменклатуры.МаркируемаяПродукция = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокНоменклатурыУчитываемойВТекОрганизации.Ссылка КАК Номенклатура,
		|	СписокНоменклатурыУчитываемойВТекОрганизации.КодВидаПодакцизногоТовара КАК КодВидаПодакцизногоТовара,
		|	СписокНоменклатурыУчитываемойВТекОрганизации.КоэффициентПересчета КАК КоэффициентПересчета,
		|	СписокНоменклатурыУчитываемойВТекОрганизации.ВидТовара КАК ВидТовара,
		|	ЕСТЬNULL(СтавкиАкцизаСрезПоследних.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(СтавкиАкцизаСрезПоследних.Сумма, 0) КАК Сумма,
		|	ЕСТЬNULL(СтавкиАкцизаСрезПоследних.ОкруглятьДоЦелого, ЛОЖЬ) КАК ОкруглятьДоЦелого,
		|	СписокНоменклатурыУчитываемойВТекОрганизации.ЕдиницаИзмеренияДляРасчетаАкциза.Код КАК ЕдиницаИзмеренияАкциза
		|ИЗ
		|	СписокНоменклатурыУчитываемойВТекОрганизации КАК СписокНоменклатурыУчитываемойВТекОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиАкциза.СрезПоследних(&ДатаПолученияСтавки, ) КАК СтавкиАкцизаСрезПоследних
		|		ПО СписокНоменклатурыУчитываемойВТекОрганизации.КодВидаПодакцизногоТовара = СтавкиАкцизаСрезПоследних.КодВидаПодакцизногоТовара";
	
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("Дата",                   Дата);
	Запрос.УстановитьПараметр("ДатаПолученияСтавки",    ?(ПараметрыДляЗаполнения.ДатаПолученияСтавки = '00010101', Дата, ПараметрыДляЗаполнения.ДатаПолученияСтавки));
	Запрос.УстановитьПараметр("СписокНоменклатуры",     ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТовары, "Номенклатура", Истина));
	Запрос.УстановитьПараметр("Импорт",                 ПараметрыДляЗаполнения.Импорт);
	Запрос.УстановитьПараметр("Производство",           Не ПараметрыДляЗаполнения.Импорт);
	Запрос.УстановитьПараметр("ЗаявлениеОВвозеТоваров", ПараметрыДляЗаполнения.ЗаявлениеОВвозе);

	Результат = Запрос.Выполнить().Выгрузить();
	Для Каждого Строка Из ТаблицаТовары Цикл
		СтрокаПоАкцизу = Результат.Найти(Строка.Номенклатура, "Номенклатура");
		Если СтрокаПоАкцизу <> Неопределено Тогда 
			Строка.КоэффициентПересчета      = СтрокаПоАкцизу.КоэффициентПересчета;
			Строка.ЕдиницаИзмеренияАкциза    = СтрокаПоАкцизу.ЕдиницаИзмеренияАкциза;
			Если ПараметрыДляЗаполнения.ЗаявлениеОВвозе Тогда
				Строка.ТвердаяСтавкаАкциза = СтрокаПоАкцизу.Сумма;
			Иначе
				Строка.СтавкаАкциза = СтрокаПоАкцизу.Сумма;
			КонецЕсли;
			Если СтрокаПоАкцизу.Сумма = 0 Тогда
				Строка.ПодакцизныйТовар = Истина;
			Иначе
				Строка.ОкруглятьДоЦелого         = СтрокаПоАкцизу.ОкруглятьДоЦелого;
				Строка.ПодакцизныйТовар = Истина;
				Если ПараметрыДляЗаполнения.ЗаявлениеОВвозе Тогда
					УчетАкцизовКлиентСервер.РассчитатьСуммуАкцизаВТабЧастиПоЗаявлениюОВвозе(Строка);
				ИначеЕсли ПараметрыДляЗаполнения.Корректировка Тогда
					Если Строка.КоличествоДоИзменения = Строка.Количество Тогда
						Строка.СуммаАкциза = Строка.СуммаАкцизаДоИзменения;
					Иначе
						УчетАкцизовКлиентСервер.РассчитатьСуммуАкцизаВТабЧасти(Строка, ПараметрыДляЗаполнения.ЭкспортПриРеализации,
						ПараметрыДляЗаполнения.Импорт, ПараметрыДляЗаполнения.ЭтоПередачаТоваров);
					КонецЕсли;
				Иначе
					УчетАкцизовКлиентСервер.РассчитатьСуммуАкцизаВТабЧасти(Строка, ПараметрыДляЗаполнения.ЭкспортПриРеализации,
					ПараметрыДляЗаполнения.Импорт, ПараметрыДляЗаполнения.ЭтоПередачаТоваров);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Строка.КоэффициентПересчета      = 0;
			Строка.ЕдиницаИзмеренияАкциза = "";
			Если ПараметрыДляЗаполнения.ЗаявлениеОВвозе Тогда
				Строка.ТвердаяСтавкаАкциза = 0;
				
			Иначе
				Строка.СтавкаАкциза          = 0;
			КонецЕсли;
			Строка.ОкруглятьДоЦелого         = 0;
			Строка.ПодакцизныйТовар          = Ложь;
			Строка.СуммаАкциза               = 0;
			Если ПараметрыДляЗаполнения.Корректировка Тогда
				Строка.СуммаАкцизаДоИзменения     = 0;
				Строка.СуммаАкцизаДоКорректировки = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПараметрыДляЗаполненияДанныхПоАкцизам(Параметры) Экспорт
	
	Параметры.Вставить("Импорт",               Ложь);
	Параметры.Вставить("ЭкспортПриРеализации", Ложь);
	Параметры.Вставить("Корректировка",        Ложь);
	Параметры.Вставить("ЗаявлениеОВвозе",      Ложь);
	Параметры.Вставить("ЭтоПередачаТоваров",   Ложь);
	Параметры.Вставить("ДатаПолученияСтавки",  '00010101');
	
КонецПроцедуры

Процедура ПоказыватьНовостьПриОткрытии(Импорт, ПоказыватьНовость) Экспорт
	ЕстьУчетПодакцизныхТоваров = Ложь;
	Если Импорт Тогда
		ЕстьУчетПодакцизныхТоваров = ПолучитьФункциональнуюОпцию("ИмпортПодакцизныхТоваров");
	Иначе
		ЕстьУчетПодакцизныхТоваров = ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров");
	КонецЕсли;
	
	Если НЕ ЕстьУчетПодакцизныхТоваров Тогда
		ПоказыватьНовость = ЕстьУчетПодакцизныхТоваров;
	Иначе
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.КодВидаПодакцизногоТовара <> ЗНАЧЕНИЕ(Справочник.КодыВидовПодакцизныхТоваров.ПустаяСсылка)";
		Результат = Запрос.Выполнить();
		ПоказыватьНовость = Результат.Пустой();
	КонецЕсли;
КонецПроцедуры

Процедура УстановкаСпособаПолученияТоваровПриОбновленииИБ() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокУплатыАкцизов.Организация КАК Организация,
	|	ПорядокУплатыАкцизов.ВидТовара КАК ВидТовара,
	|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСН КАК ПрименяетсяУСН
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, ) КАК НастройкиСистемыНалогообложенияСрезПоследних
	|		ПО ПорядокУплатыАкцизов.Организация = НастройкиСистемыНалогообложенияСрезПоследних.Организация
	|ГДЕ
	|	ПорядокУплатыАкцизов.Импорт = ЛОЖЬ
	|	И ПорядокУплатыАкцизов.Производство = ЛОЖЬ";
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВедетсяУчетИмпортаТоваров = ПолучитьФункциональнуюОпцию("ВедетсяУчетИмпортаТоваров");
	ВедетсяПроизводство       = ПолучитьФункциональнуюОпцию("ИспользоватьВыпускПродукции");
	
	Если ВедетсяУчетИмпортаТоваров Тогда
		Константы.ИмпортПодакцизныхТоваров.Установить(Истина);
	КонецЕсли;
	Если ВедетсяПроизводство Тогда
		Константы.ПроизводствоПодакцизныхТоваров.Установить(Истина);
	КонецЕсли;
	
	Если ВедетсяУчетИмпортаТоваров Или ВедетсяПроизводство Тогда
		Для Каждого Строка Из Результат Цикл
			НаборЗаписейПорядокУплатыАкцизов = РегистрыСведений.ПорядокУплатыАкцизов.СоздатьНаборЗаписей();
			НаборЗаписейПорядокУплатыАкцизов.Отбор.Организация.Установить(Строка.Организация);
			НаборЗаписейПорядокУплатыАкцизов.Отбор.ВидТовара.Установить(Строка.ВидТовара);
			НаборЗаписейПорядокУплатыАкцизов.Прочитать();
			Если НаборЗаписейПорядокУплатыАкцизов.Количество() <> 0 Тогда
				Запись = НаборЗаписейПорядокУплатыАкцизов[0];
				Если ВедетсяУчетИмпортаТоваров И НЕ ВедетсяПроизводство Тогда
					Запись.Импорт = Истина;
				ИначеЕсли ВедетсяПроизводство И НЕ ВедетсяУчетИмпортаТоваров Тогда
					Запись.Производство = Истина;
				Иначе
					Если Строка.ПрименяетсяУСН Тогда
						Запись.Импорт = Истина;
					Иначе
						Запись.Производство = Истина;
					КонецЕсли;
				КонецЕсли;
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейПорядокУплатыАкцизов, Истина);
				Справочники.ВидыНоменклатуры.НайтиСоздатьПодакцизныйВидНоменклатуры(Строка.Организация, Строка.ВидТовара, Запись.Импорт);
			КонецЕсли;
		КонецЦикла;
		Результат.Свернуть("ВидТовара");
		Для Каждого Строка Из Результат Цикл
			НайтиСоздатьКодыПодакцизныхТоваров(Строка.ВидТовара);
			РегистрыСведений.СтавкиАкциза.ЗаполнитьСтавкиАкцизаПоВидуПодакцизногоТовара(Строка.ВидТовара);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ДозаполнитьСчетАкциза(ДокументОбъект) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров") Тогда
		СчетАкцизов = ПланыСчетов.Хозрасчетный.Продажи_Акцизы;
		Для Каждого Строка Из ДокументОбъект.Товары Цикл
			Если Строка.ПодакцизныйТовар И Строка.СуммаАкциза <> 0 И Не ЗначениеЗаполнено(Строка.СчетАкциза) Тогда
				Строка.СчетАкциза = СчетАкцизов;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПерезаполнитьДанныеПоАкцизамПриИзмененииОперации(Объект) Экспорт
	Если Не ЗначениеЗаполнено(Объект.ДокументРеализации) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента = Объект.ДокументРеализации.Метаданные();
	ИмяВидаДокумента = МетаданныеДокумента.Имя;
	
	Если ИмяВидаДокумента <> "КорректировкаРеализации" И ИмяВидаДокумента <> "РеализацияТоваровУслуг" Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументРеализации", Объект.ДокументРеализации);
	ТекстЗапроса = "ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугТовары.СуммаАкциза КАК СуммаАкциза,
	|	РеализацияТоваровУслугТовары.СчетАкциза КАК СчетАкциза,
	|	РеализацияТоваровУслугТовары.СубконтоАкцизы КАК СубконтоАкцизы,
	|	РеализацияТоваровУслугТовары.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	&ПолеСуммаАкцизаДоИзменения
	|ИЗ
	|	Документ." + ИмяВидаДокумента + ".Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &ДокументРеализации";
	
	Если ИмяВидаДокумента = "КорректировкаРеализации" Тогда
		ПолеСуммаАкцизаДоИзменения = "РеализацияТоваровУслугТовары.СуммаАкцизаДоИзменения КАК СуммаАкцизаДоИзменения";
	Иначе
		ПолеСуммаАкцизаДоИзменения = "0 КАК СуммаАкцизаДоИзменения";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСуммаАкцизаДоИзменения", ПолеСуммаАкцизаДоИзменения);
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из Объект.Товары Цикл
		НайденнаяСтрока = РезультатЗапроса.Найти(Строка.ИдентификаторСтроки, "ИдентификаторСтроки");
		Если НайденнаяСтрока <> Неопределено Тогда
			Строка.ПодакцизныйТовар       = НайденнаяСтрока.ПодакцизныйТовар;
			Строка.СуммаАкцизаДоИзменения = НайденнаяСтрока.СуммаАкциза;
			Строка.СчетАкциза             = НайденнаяСтрока.СчетАкциза;
			Строка.СубконтоАкцизы         = НайденнаяСтрока.СубконтоАкцизы;
			Если ИмяВидаДокумента = "КорректировкаРеализации" Тогда
				Строка.СуммаАкцизаДоКорректировки = НайденнаяСтрока.СуммаАкцизаДоИзменения;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура ОбновитьСтавкиПоВидуТоваров(ВидТовара)
	
	ЕстьУчетАкцизов = ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров")
	Или ПолучитьФункциональнуюОпцию("ИмпортПодакцизныхТоваров");
	Если Не ЕстьУчетАкцизов Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПорядокУплатыАкцизов.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.ВидТовара = &ВидТовара";
	Запрос.УстановитьПараметр("ВидТовара", ВидТовара);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	НайтиСоздатьКодыПодакцизныхТоваров(ВидТовара,Истина);
	РегистрыСведений.СтавкиАкциза.ЗаполнитьСтавкиАкцизаПоВидуПодакцизногоТовара(ВидТовара, Истина);
	
КонецПроцедуры

Процедура ОбновитьСтавкиПоСпиртосодержащейПродукцииС01_01_2025() Экспорт 
	
	ВидТовара = Перечисления.ВидыПодакцизныхТоваров.СпиртосодержащаяПродукция;
	
	ОбновитьСтавкиПоВидуТоваров(ВидТовара)
	
КонецПроцедуры

Функция ПолучитьСтатистикуПоОрганизациям() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	ПорядокУплатыАкцизов.ВидТовара КАК ВидТовара,
	|	ВЫБОР
	|		КОГДА ПорядокУплатыАкцизов.Импорт = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоИмпорт,
	|	ВЫБОР
	|		КОГДА ПорядокУплатыАкцизов.Производство = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПроизводство,
	|	ВЫБОР
	|		КОГДА (ПорядокУплатыАкцизов.Импорт ЕСТЬ NULL
	|				ИЛИ ПорядокУплатыАкцизов.Импорт = ЛОЖЬ)
	|				И (ПорядокУплатыАкцизов.Производство ЕСТЬ NULL
	|					ИЛИ ПорядокУплатыАкцизов.Производство = ЛОЖЬ)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоБезАкцизов
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|		ПО Организации.Ссылка = ПорядокУплатыАкцизов.Организация
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ
	|ИТОГИ
	|	СУММА(КоличествоИмпорт),
	|	СУММА(КоличествоПроизводство),
	|	СУММА(КоличествоБезАкцизов)
	|ПО
	|	ВидТовара";
	
	Результат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСтатистикуПоКодамПодакцизныхТоваров() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КодыВидовПодакцизныхТоваров.Код КАК Код,
	|	СУММА(1) КАК Количество
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КодыВидовПодакцизныхТоваров КАК КодыВидовПодакцизныхТоваров
	|		ПО Номенклатура.КодВидаПодакцизногоТовара = КодыВидовПодакцизныхТоваров.Ссылка
	|ГДЕ
	|	Номенклатура.ПометкаУдаления = ЛОЖЬ
	|	И Номенклатура.КодВидаПодакцизногоТовара <> ЗНАЧЕНИЕ(Справочник.КодыВидовПодакцизныхТоваров.ПустаяССылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	КодыВидовПодакцизныхТоваров.Код";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьСтавкиПоАкцизам() Экспорт
	
	ЕстьУчетАкцизов = ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров")
	Или ПолучитьФункциональнуюОпцию("ИмпортПодакцизныхТоваров");
	Если Не ЕстьУчетАкцизов Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПорядокУплатыАкцизов.ВидТовара КАК ВидТовара
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НайтиСоздатьКодыПодакцизныхТоваров(Выборка.ВидТовара,Истина, Истина);
		РегистрыСведений.СтавкиАкциза.ЗаполнитьСтавкиАкцизаПоВидуПодакцизногоТовара(Выборка.ВидТовара, Истина);
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НайтиСоздатьКодыПодакцизныхТоваров(ВидТовара, Обновление = Ложь, Перезаписать = Ложь)
	
	Макет = Справочники.КодыВидовПодакцизныхТоваров.ПолучитьМакет("КлассификаторКодов");
	ОбластьПоВидуТовара = Макет.Области.Найти(СтрЗаменить(ВидТовара, " ", ""));
	Если ОбластьПоВидуТовара <> Неопределено Тогда
		ОбластьПоВидуТовара = Макет.ПолучитьОбласть(СтрЗаменить(ВидТовара, " ", ""));
		Если Не Обновление Тогда
			Запрос = Новый Запрос();
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	КодыВидовПодакцизныхТоваров.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.КодыВидовПодакцизныхТоваров КАК КодыВидовПодакцизныхТоваров
			|ГДЕ
			|	КодыВидовПодакцизныхТоваров.ВидТовара = &ВидТовара
			|	И КодыВидовПодакцизныхТоваров.ПометкаУдаления = ЛОЖЬ";
			Запрос.УстановитьПараметр("ВидТовара", ВидТовара);
			Результат = Запрос.Выполнить().Выбрать();
			Если Результат.Следующий() Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		КолвоСтрок = ОбластьПоВидуТовара.ВысотаТаблицы;
		Для Стр = 1 По КолВоСтрок Цикл
			КодВида = ОбластьПоВидуТовара.Область(Стр, 1);
			ВидПодакцизногоТовара = Справочники.КодыВидовПодакцизныхТоваров.НайтиПоКоду(КодВида.Текст);
			Если ВидПодакцизногоТовара = Неопределено Или ВидПодакцизногоТовара = Справочники.КодыВидовПодакцизныхТоваров.ПустаяСсылка() Тогда
				НовыйВид = Справочники.КодыВидовПодакцизныхТоваров.СоздатьЭлемент();
				НовыйВид.Код                = КодВида.Текст;
				НовыйВид.Наименование       = ОбластьПоВидуТовара.Область(Стр, 3).Текст;
				НовыйВид.НаименованиеПолное = ОбластьПоВидуТовара.Область(Стр, 2).Текст;
				НовыйВид.ВидТовара          = ВидТовара;
				Если ОбластьПоВидуТовара.Область(Стр, 4).Текст <> "" Тогда
					НовыйВид.ДействуетС         = СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбластьПоВидуТовара.Область(Стр, 4).Текст, ЧастиДаты.Дата);
				КонецЕсли;
				Если ОбластьПоВидуТовара.Область(Стр, 5).Текст <> "" Тогда
					НовыйВид.ДействуетПо        = СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбластьПоВидуТовара.Область(Стр, 5).Текст, ЧастиДаты.Дата);
				КонецЕсли;
				НовыйВид.Записать();
			ИначеЕсли Перезаписать Тогда
				ПодакцизныйТовар = ВидПодакцизногоТовара.ПолучитьОбъект();
				ПодакцизныйТовар.Код                = КодВида.Текст;
				ПодакцизныйТовар.Наименование       = ОбластьПоВидуТовара.Область(Стр, 3).Текст;
				ПодакцизныйТовар.НаименованиеПолное = ОбластьПоВидуТовара.Область(Стр, 2).Текст;
				ПодакцизныйТовар.ВидТовара          = ВидТовара;
				Если ОбластьПоВидуТовара.Область(Стр, 4).Текст <> "" Тогда
					ПодакцизныйТовар.ДействуетС         = СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбластьПоВидуТовара.Область(Стр, 4).Текст, ЧастиДаты.Дата);
				КонецЕсли;
				Если ОбластьПоВидуТовара.Область(Стр, 5).Текст <> "" Тогда
					ПодакцизныйТовар.ДействуетПо        = СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбластьПоВидуТовара.Область(Стр, 5).Текст, ЧастиДаты.Дата);
				КонецЕсли;

				ПодакцизныйТовар.Записать();

			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти