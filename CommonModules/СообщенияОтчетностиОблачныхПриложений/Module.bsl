
#Область ПрограммныйИнтерфейс

// Возвращает список приложений в облаке, доступных пользователю сервиса.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - см. ПрограммныйИнтерфейсСервиса.Приложения()
//
Функция ДоступныеПриложения() Экспорт
	
	Результат = Неопределено;
	Абоненты = ПрограммныйИнтерфейсСервиса.Абоненты();
	КодЭтогоПриложения = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
	
	Для Каждого Абонент Из Абоненты Цикл
		
		СвойстваОтвета = Неопределено;
		Приложения = ПрограммныйИнтерфейсСервиса.ПриложенияАбонента(Абонент.Код, Ложь, СвойстваОтвета);
		Если ЗначениеЗаполнено(СвойстваОтвета.Сообщение) Тогда
			ЗаписьЖурналаРегистрации(
				ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				СвойстваОтвета.Сообщение);
		КонецЕсли;
		Если Приложения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Результат = Неопределено Тогда
			Результат = Приложения.СкопироватьКолонки();
		КонецЕсли;
		Для Каждого Приложение Из Приложения Цикл
			
			Если Приложение.СостояниеПриложения <> Перечисления.СостоянияПриложений.Используется
				Или Приложение.Код = КодЭтогоПриложения Тогда
				
				Продолжить;
			КонецЕсли;
			
			// Получение доступных приложений выполняется синхронно, поэтому для большей отзывчивости интерфейса
			// возможность работы приложений с календарем отчетности на этом шаге не проверяется.
			// Проверка будет выполнена позже в методе ПодключитьНовыеПриложения(), который уже выполняется асинхронно.
			
			НоваяСтрока = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Приложение);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Возвращает список организаций, данные по которым доступны пользователю.
// 
// Возвращаемое значение:
//   Массив из элементов типа СправочникСсылка.Организации
//
Функция ДоступныеОрганизацииПользователя() Экспорт
	
	ДоступныеОрганизации = ДоступныеОрганизации();
		
	Результат = Новый Массив;
	
	Для Каждого Организация Из ДоступныеОрганизации Цикл
		ДанныеОрганизациии = Новый Структура("Наименование, Идентификатор",
			Организация.Наименование, XMLСтрока(Организация.УникальныйИдентификатор()));
		Результат.Добавить(ДанныеОрганизациии);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Запрашивает у другого облачного приложения данные отчетной кампании.
// 
// Параметры:
//   Приложение - СправочникСсылка.ОблачныеПриложения - экземпляр приложения, которому направляется запрос
//   Параметры - Структура
//      *ДатаНачала - Дата - период, начиная с которого необходимо запросить данные
//      *ДатаКонца - Дата - конечный период, по который требуется запросить данные
//      *СобытияКалендаря - Строка - отбор по виду отчетов (все отчеты" или "только уведомления ЕНС").
//                                   Устарело. Следует использовать ПоОтчетам, ПоТребованиямФНС, ПоУведомлениямЕНС.
//      *ПоОтчетам - Булево - отбор по виду отчетов
//      *ПоТребованиямФНС - Булево - отбор по виду отчетов
//      *ПоУведомлениямЕНС - Булево - отбор по виду отчетов
//
// Возвращаемое значение:
//   см. возращаемые значения метода СообщенияОблачныхПриложений.ЗапроситьДанныеПриложения
//
Функция ЗапроситьДанныеОтчетнойКампании(Приложение, Параметры) Экспорт
	
	ПараметрыЗапроса = НовыйПараметрыЗапросаДанных();
	ПараметрыЗапроса.Отбор.Период.Начало = Параметры.ДатаНачала;
	ПараметрыЗапроса.Отбор.Период.Конец = Параметры.ДатаКонца;
	ПараметрыЗапроса.Отбор.ПоОтчетам = Параметры.ПоОтчетам;
	ПараметрыЗапроса.Отбор.ПоТребованиямФНС = Параметры.ПоТребованиямФНС;
	ПараметрыЗапроса.Отбор.ПоУведомлениямЕНС = Параметры.ПоУведомлениямЕНС;
	ПараметрыЗапроса.Отбор.ПоНалоговымПлатежам = Параметры.ПоНалоговымПлатежам;
	ПараметрыЗапроса.Отбор.ПоДокументамСЭДО = Параметры.ПоДокументамСЭДО;
	ПараметрыЗапроса.Отбор.ПоЗарплате = Параметры.ПоЗарплате;
	
	УстановитьПривилегированныйРежим(Истина);
	Организации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
	УстановитьПривилегированныйРежим(Ложь);
	
	Отбор = Новый Структура("ПриложениеСсылка, ВыводитьВКалендарь", Приложение, Ложь);
	СтрокиОрганизацииНеЗапрашивать = Организации.НайтиСтроки(Отбор);
	Для Каждого Строка Из СтрокиОрганизацииНеЗапрашивать Цикл
		ПараметрыЗапроса.Отбор.ИсключитьОрганизации.Добавить(Строка.ОрганизацияИдентификатор);
	КонецЦикла;
	
	Запрос = СообщенияОблачныхПриложений.ЗапроситьДанныеПриложения(
		Приложение,
		ИмяПрикладногоМеханизма(),
		ПараметрыЗапроса);
		
	Возврат Запрос;
КонецФункции

// Отправляет другому облачному приложению запрос синхронного получения данных.
// Следует выполнять синхронно только те запросы, на обработку которых приложению
// понадобится заведомо мало времени. "Тяжелые" запросы данных следует выполнять асинхронно.
// 
// Параметры:
//   ТипЗапроса - Строка - вид запрашиваемых данных.
//   Приложение - СправочникСсылка.ОблачныеПриложения - экземпляр приложения, которому направляется запрос
//   Параметры - Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыЗапросаИзмененияОтчета()
//               Неопределено - если не требуется каких-либо дополнительных параметров
//
// Возвращаемое значение:
//   см. возращаемые значения метода СообщенияОблачныхПриложений.ЗапроситьДанныеПриложения
//
Функция ЗапроситьСинхронно(ТипЗапроса, Приложение, Параметры = Неопределено) Экспорт
	
	ПараметрыЗапроса = Неопределено;
	Если ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаСписокОрганизаций() Тогда
		ПараметрыЗапроса = НовыйПараметрыЗапросаСпискаОрганизаций();
		
	ИначеЕсли ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьСтатусОтчета()
		Или ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьКомментарийОтчета() Тогда
		
		ПараметрыЗапроса = НовыйПараметрыЗапросаЗаписиСтатусаКомментарияОтчета(ТипЗапроса);
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса.ПараметрыОтчета, Параметры);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = СообщенияОблачныхПриложений.ЗапроситьДанныеПриложения(
		Приложение,
		ИмяПрикладногоМеханизма(),
		ПараметрыЗапроса);
		
	Возврат Запрос;
	
КонецФункции

// Обрабатывает запрос от облачного приложения.
// 
// Параметры:
//   Запрос - Строка - запрос в виде строки JSON
//
// Возвращаемое значение:
//   Структура
//      *ТипЗапроса - Строка - тип обработанного запроса
//      *Ответ - Структура,
//               для запроса с типом запроса данных отчетности (см. ТипЗапросаДанныеОтчетности()):
//               таблица значений, преобразованная в массив структур
//               см. КалендарьОтчетности.НовыйДанныеОтчетности,
//               для запроса с типом запроса списка организаций (см. ТипЗапросаСписокОрганизаций()):
//                  массив структур c ключами:
//                  *Наименование - Строка - наименование организации
//                  *Идентификатор - Строка - уникальный идентификатор организации
//   В случае ошибки : Cтруктура
//      *Ошибка - Строка - описание возникшей ошибки
//
Функция ОтветНаЗапросОблачногоПриложения(Запрос) Экспорт
	
	ДанныеЗапроса = ОбщегоНазначенияБП.СтруктураИзСтрокиJSON(Запрос);
	// Обязательно должен быть передан тип запроса.
	Если Не ДанныеЗапроса.Свойство("ТипЗапроса") Тогда
		Возврат Новый Структура("Ошибка", Нстр("ru = 'Не передан тип запроса'"));
	КонецЕсли;
	
	ТипЗапроса = ДанныеЗапроса.ТипЗапроса;
	Если ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаДанныеОтчетности() Тогда
		// Обработка запроса на получение данных отчетности.
		Возврат ОтветДанныеОтчетности(ДанныеЗапроса.Отбор);
		
	ИначеЕсли ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаСписокОрганизаций() Тогда
		// Обработка запроса на получение списка организаций
		Возврат ОтветСписокОрганизаций();
		
	ИначеЕсли ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьСтатусОтчета()
		Или ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьКомментарийОтчета() Тогда
		// Обработка запроса на обновление статуса отчета или комментария.
		Возврат ОтветЗаписатьСтатусКомментарийОтчета(ТипЗапроса, ДанныеЗапроса.ПараметрыОтчета);
		
	ИначеЕсли ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаПодключитьОблачноеПриложение() Тогда
		// Подключение нового приложения
		Возврат ОтветПодключитьОблачноеПриложение();
		
	КонецЕсли;
	
	Возврат Новый Структура("Ошибка", Нстр("ru = 'Неизвестный тип запроса'"));
КонецФункции

// Получает ответы от подключенных приложений, которым был ранее отправлен запрос
// на получение данных.
//
// Параметры:
//  Запросы - ТаблицаЗначений - отправленные ранее запросы другим облачным приложениям,
//            см. НовыйЗапросыОблачнымПриложениям().
//
// Возвращаемое значение:
//   Структура - 
//      *Ответы - ТаблицаЗначений
//         **Приложение - СправочникСсылка.ОблачныеПриложения - приложение, от которого получен ответ
//         **Данные - Строка- ответы приложений в виде строки JSON,
//      *ОжидающиеЗапросы - ТаблицаЗначений - запросы, по которым не удалось получить ответ,
//                          см. НовыйЗапросыОблачнымПриложениям()
//      *ОшибкиПриложений - Массив - список приложений, получение данных от которых более невозможно,
//                        опрашивать повторно их не следует.
//
Функция ПолучитьОтветыНаЗапросы(Запросы) Экспорт
	
	Ответы = Новый ТаблицаЗначений;
	Ответы.Колонки.Добавить("Приложение", Новый ОписаниеТипов("СправочникСсылка.ОблачныеПриложения"));
	Ответы.Колонки.Добавить("Данные", Новый ОписаниеТипов("Строка"));
	
	ОжидающиеЗапросы = НовыйЗапросыОблачнымПриложениям();
	ОшибкиПриложений = Новый Массив;
	
	Для Каждого Запрос Из Запросы Цикл
		
		ДанныеПриложения = СообщенияОблачныхПриложений.ПолучитьДанныеПриложения(
			Запрос.Приложение,
			ИмяПрикладногоМеханизма(),
			Запрос.ИдентификаторЗапроса);
			
		Если ДанныеПриложения = Неопределено Тогда
			// Не удалось получить данные, повторять запрос не следует.
			ОшибкиПриложений.Добавить(Запрос.Приложение);
			Продолжить;
			
		ИначеЕсли ДанныеПриложения = "" Тогда
			НоваяСтрока = ОжидающиеЗапросы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Запрос);
			Продолжить;
		КонецЕсли;
		
		Ответ = Ответы.Добавить();
		Ответ.Приложение = Запрос.Приложение;
		Ответ.Данные = ДанныеПриложения;
		
	КонецЦикла;
	
	Возврат Новый Структура("Ответы,ОжидающиеЗапросы,ОшибкиПриложений", Ответы, ОжидающиеЗапросы, ОшибкиПриложений);
КонецФункции

// Подтверждает получение данных от другого облачного приложения по отправленному ранее запросу.
//
// Параметры:
//   Приложение - СправочникСсылка.ОблачныеПриложения - экземпляр приложения, которому был отправлен запрос.
//   ИдентификаторЗапроса - Строка - идентификатор запроса, полученный методом
//                                   СообщенияОблачныхПриложений.ЗапроситьДанныеПриложения()
//
Процедура ОтметитьДанныеПолучены(Приложение, ИдентификаторЗапроса) Экспорт
	
	СообщенияОблачныхПриложений.ОтметитьДанныеПолучены(
		Приложение,
		ИмяПрикладногоМеханизма(),
		ИдентификаторЗапроса);
		
КонецПроцедуры

// Возвращает параметры запуска фонового задания для подключения новых приложений.
//
// Возвращаемое значение:
//   - Структура:
//     * Пользователь - СправочникСсылка.Пользователи - пользователь, подключающий новые приложения.
//     * ПриложенияПользователя - ТаблицаЗначений - см. ПрограммныйИнтерфейсСервиса.Приложения()
//
Функция НовыйПараметрыПодключенияПриложений() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Пользователь", Справочники.Пользователи.ПустаяСсылка());
	Параметры.Вставить("ПриложенияПользователя");
	
	Возврат Параметры;
КонецФункции

// Подключает новые приложения пользователя к календарю отчетности.
// Метод выполняется асинхронно (в фоновом задании).
// 
// Параметры:
//   Параметры - Структура - см. НовыйПараметрыПодключенияПриложений().
//
// Возвращаемое значение:
//   ОшибкиПодключения - Массив - наименования приложений, которые не удалось подключить.
//
Функция ПодключитьНовыеПриложения(Параметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПодключенныеПриложения = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Приложения();
	ПодключенныеОрганизации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПодключенныеПриложения.Колонки.Добавить("ЕстьВМенеджереСервиса", Новый ОписаниеТипов("Булево"));
	
	ОшибкиПодключения = Новый Массив;
	КоличествоПриложений = Параметры.ПриложенияПользователя.Количество();
	
	Счетчик = 0;
	Для Каждого Приложение Из Параметры.ПриложенияПользователя Цикл
		
		Счетчик = Счетчик + 1;
		ДоступноПодключение = СообщенияОблачныхПриложений.ПриложениеГотовоПриниматьЗапросы(
			Приложение.АдресПриложения);
			
		ПроцентВыполнения = Окр(Счетчик / КоличествоПриложений * 100);
		ТекстИдетПодключение = НСтр("ru='Идет подключение приложений...'");
		ТекстОбработано = СтрШаблон(НСтр("ru='Обработано %1 из %2'"), Счетчик, КоличествоПриложений);
		
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения,
			СтрШаблон("%1%2%3", ТекстИдетПодключение, Символы.ПС, ТекстОбработано));
		
		ЭлементыАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(Приложение.АдресПриложения);
		ПриложениеПодключено = Ложь;
		Для Каждого ПодключенноеПриложение Из ПодключенныеПриложения Цикл
			Если СтрНайти(Нрег(ПодключенноеПриложение.Адрес), Нрег(ЭлементыАдреса.ПутьНаСервере)) > 0 Тогда
				ПриложениеПодключено = Истина;
				ПодключенноеПриложение.ЕстьВМенеджереСервиса = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ДоступноПодключение Тогда
			Если ПриложениеПодключено Тогда
				УстановитьПривилегированныйРежим(Истина);
				РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.ОтключитьПриложение(
					ПодключенноеПриложение.ПриложениеСсылка);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если ПриложениеПодключено Тогда
			// Это приложение уже подключено.
			ПриложениеСсылка = ПодключенноеПриложение.ПриложениеСсылка;
			// Переименуем подключенное приложение, если изменилось его имя.
			Если Приложение.Наименование <> ПодключенноеПриложение.Наименование Тогда
				УстановитьПривилегированныйРежим(Истина);
				Справочники.ОблачныеПриложения.Переименовать(ПриложениеСсылка, Приложение.Наименование);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			// Обновим список организаций приложения.
			ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаСписокОрганизаций();
			Запрос = ЗапроситьСинхронно(ТипЗапроса, ПриложениеСсылка);
			Если Запрос = "" Тогда
				// Доступ к приложению отсутствует. Отключим приложение от Календаря отчетности.
				УстановитьПривилегированныйРежим(Истина);
				РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.ОтключитьПриложение(
					ПриложениеСсылка);
				УстановитьПривилегированныйРежим(Ложь);
				Продолжить;
			КонецЕсли;
			Если ТипЗнч(Запрос) = Тип("Строка") Или Запрос = Неопределено Тогда
				// В процессе обновления на Fresh некоторые ноды могут быть ещё не обновлены и не суметь
				// обработать запрос синхронно. Ничего не делаем с такими запросами.
				Продолжить;
			КонецЕсли;
			КалендарьОтчетности.ОбработатьОтветНаЗапросСпискаОрганизаций(
				Запрос.Ответ,
				ПриложениеСсылка,
				ПодключенныеОрганизации);
			Продолжить;
		КонецЕсли;
		
		// Подключаем новое приложение.
		ДанныеПодключения = СообщенияОблачныхПриложений.ПодключитьОблачноеПриложение(
			Приложение.АдресПриложения,
			Приложение.Наименование);
			
		Если ДанныеПодключения = Неопределено Тогда
			ОшибкиПодключения.Добавить(Приложение.Наименование);
			Продолжить;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		Для Каждого Организация Из ДанныеПодключения.ДоступныеОрганизации Цикл
			РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.УстановитьСостояние(
				Организация,
				Истина)
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
	// Убираем записи, которых нет в менеджере сервиса
	НеактуальныеПриложения = ПодключенныеПриложения.Скопировать(Новый Структура("ЕстьВМенеджереСервиса", Ложь));
	Если НеактуальныеПриложения.Количество() > 0 Тогда
		УстановитьПривилегированныйРежим(Истина);
		Для Каждого ПриложениеКУдалению Из НеактуальныеПриложения Цикл
			РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.ОтключитьПриложение(
				ПриложениеКУдалению.ПриложениеСсылка);
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ОшибкиПодключения;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НовыйЗапросыОблачнымПриложениям() Экспорт

	Запросы = Новый ТаблицаЗначений;
	Запросы.Колонки.Добавить("Приложение", Новый ОписаниеТипов("СправочникСсылка.ОблачныеПриложения"));
	Запросы.Колонки.Добавить("ИдентификаторЗапроса", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(36)));
	Запросы.Колонки.Добавить("ТипЗапроса", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(250)));
	
	Возврат Запросы;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтветДанныеОтчетности(Отбор)
	
	ОрганизацииНеЗапрашивать = Новый Массив;
	Для Каждого НеЗапрашивать Из Отбор.ИсключитьОрганизации Цикл
		ИдентификаторОрганизации = Новый УникальныйИдентификатор(НеЗапрашивать);
		ОрганизацииНеЗапрашивать.Добавить(Справочники.Организации.ПолучитьСсылку(ИдентификаторОрганизации));
	КонецЦикла;
	
	Организации = ДоступныеОрганизации();
	
	Если Организации.Количество() Тогда
		Параметры = Обработки.КалендарьОтчетности.НовыйПараметрыОтчета();
		Параметры.Организации = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Организации, ОрганизацииНеЗапрашивать);
		Параметры.ПоОтчетам = Отбор.ПоОтчетам;
		Параметры.ПоТребованиямФНС = Отбор.ПоТребованиямФНС;
		Параметры.ПоУведомлениямЕНС = Отбор.ПоУведомлениямЕНС;
		Параметры.ПоНалоговымПлатежам = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Отбор, "ПоНалоговымПлатежам", Ложь);
		Параметры.ПоДокументамСЭДО = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Отбор, "ПоДокументамСЭДО", Ложь);
		Параметры.ДатаНачала = ОбщегоНазначенияБП.ДесериализоватьДатуISO(Отбор.Период.Начало);
		Параметры.ДатаКонца = ОбщегоНазначенияБП.ДесериализоватьДатуISO(Отбор.Период.Конец);
		Параметры.НаДату = ОбщегоНазначенияБП.ДесериализоватьДатуISO(НачалоДня(ОбщегоНазначенияБП.ТекущаяДатаНаСервере()));
		
		ДанныеПриложения = Обработки.КалендарьОтчетности.ДанныеПриложения(Параметры);
		
		// Ссылочные типы не могут быть переданы в ответе в формате JSON.
		ДанныеПриложения.Колонки.Удалить("Организация");
		ДанныеПриложения.Колонки.Удалить("Правило");
		
		Ответ = ОбщегоНазначения.ТаблицаЗначенийВМассив(ДанныеПриложения);
		
	Иначе
		Ответ = Новый Массив;
	КонецЕсли;
	
	ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаДанныеОтчетности();
	Возврат Новый Структура("ТипЗапроса, Ответ", ТипЗапроса, Ответ);
	
КонецФункции

Функция ОтветСписокОрганизаций()
	
	ДанныеОрганизаций = ДоступныеОрганизацииПользователя();
	
	ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаСписокОрганизаций();
	Ответ = Новый Структура("ТипЗапроса, Ответ", ТипЗапроса, ДанныеОрганизаций);
		
	Возврат Ответ;
КонецФункции

Функция ОтветЗаписатьСтатусКомментарийОтчета(ТипЗапроса, ПараметрыЗапроса)
	
	Ответ = Новый Структура("Записан, Причина, СтатусОтчета", Ложь, "", "");
	Результат = Новый Структура("ТипЗапроса, Ответ", ТипЗапроса, Ответ);
	
	Если ТипЗнч(ПараметрыЗапроса) <> Тип("Структура") Тогда
		Результат.Ответ.Причина = Нстр("ru='Параметры запроса должны быть Структура'");
		Возврат Результат;
	КонецЕсли;
	
	ПараметрыОтчета = КалендарьОтчетности.ПодготовитьПараметрыОтчета(ПараметрыЗапроса);
	Если ПараметрыОтчета = Неопределено Или Не ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		Результат.Ответ.Причина = Нстр("ru='Неверно переданы параметры отчета'");
		Возврат Результат;
	КонецЕсли;
	
	ПараметрыДействия = КалендарьОтчетностиКлиентСервер.НовыйПараметрыДействияСОтчетом();
	ЗаполнитьЗначенияСвойств(ПараметрыДействия, ПараметрыЗапроса);
	
	Если ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьКомментарийОтчета() Тогда
		// Запись комментария к отчету.
		Результат.Ответ.Записан = ВыполнениеЗадачБухгалтера.ЗаписатьКомментарийЗадачи(ПараметрыОтчета, ПараметрыЗапроса.Комментарий);
		Для Каждого ПодчиненноеПравило Из ПараметрыОтчета.ПодчиненныеПравила Цикл
			Параметры = КалендарьОтчетностиКлиентСервер.НовыйПараметрыОтчета();
			ЗаполнитьЗначенияСвойств(Параметры, ПараметрыОтчета);
			Параметры.Правило = ПодчиненноеПравило;
			ВыполнениеЗадачБухгалтера.ЗаписатьКомментарийЗадачи(Параметры, ПараметрыЗапроса.Комментарий);
		КонецЦикла;
	Иначе
		// Изменение статуса отчета.
		Если ПараметрыДействия.СтатусУстановленВручную Тогда
			Результат.Ответ.Записан = ВыполнениеЗадачБухгалтера.ЗаписатьРучнойСтатусЗадачи(
				ПараметрыОтчета,
				ПараметрыДействия.Статус);
		Иначе
			Статус = ВыполнениеЗадачБухгалтера.ВернутьАвтоматическийСтатусЗадачи(ПараметрыОтчета);
			Если Статус <> Неопределено Тогда
				Результат.Ответ.Записан = Истина;
				Результат.Ответ.СтатусОтчета = Статус;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не Результат.Ответ.Записан Тогда
		Результат.Ответ.Причина = Нстр("ru='Внутренняя ошибка'");
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ОтветПодключитьОблачноеПриложение()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеПодключения = НовыйДанныеПодключенияПриложения();
	ДанныеПодключения.ИдентификаторПриложения = СообщенияОблачныхПриложений.ИдентификаторЭтогоПриложения();
	ДанныеПодключения.ДоступныеОрганизации = СообщенияОтчетностиОблачныхПриложений.ДоступныеОрганизацииПользователя();
		
	Ответ = Новый Структура("ТипЗапроса, Ответ",
		СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаПодключитьОблачноеПриложение(),
		ДанныеПодключения);
		
	Возврат Ответ;
	
КонецФункции

Функция ДоступныеОрганизации()
	
	Возврат Справочники.Организации.ДоступныеОрганизацииПоRLS();
	
КонецФункции

Функция ИмяПрикладногоМеханизма()
	
	Возврат "reporting_cycles";
	
КонецФункции

Функция НовыйПараметрыЗапросаДанных()
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИсключитьОрганизации", Новый Массив);
	Отбор.Вставить("ПоОтчетам", Ложь);
	Отбор.Вставить("ПоТребованиямФНС", Ложь);
	Отбор.Вставить("ПоУведомлениямЕНС", Ложь);
	Отбор.Вставить("ПоНалоговымПлатежам", Ложь);
	Отбор.Вставить("ПоДокументамСЭДО", Ложь);
	Отбор.Вставить("ПоЗарплате", Ложь);
	ОтборПериод = Новый Структура("Начало,Конец", '0001-01-01', '0001-01-01');
	Отбор.Вставить("Период", ОтборПериод);
	
	Параметры = Новый Структура;
	Параметры.Вставить("ТипЗапроса", СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаДанныеОтчетности());
	Параметры.Вставить("ИдентификаторПриложения", СообщенияОблачныхПриложений.ИдентификаторЭтогоПриложения());
	Параметры.Вставить("Отбор", Отбор);
	
	Возврат Параметры;
	
КонецФункции

Функция НовыйПараметрыЗапросаСпискаОрганизаций()
	
	Возврат Новый Структура("ТипЗапроса, ВыполнитьСинхронно, ИдентификаторПриложения",
		СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаСписокОрганизаций(),
		Истина,
		СообщенияОблачныхПриложений.ИдентификаторЭтогоПриложения());
	
КонецФункции

Функция НовыйПараметрыЗапросаЗаписиСтатусаКомментарияОтчета(ТипЗапроса)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ТипЗапроса", ТипЗапроса);
	Параметры.Вставить("ИдентификаторПриложения", СообщенияОблачныхПриложений.ИдентификаторЭтогоПриложения());
	Параметры.Вставить("ПараметрыОтчета", КалендарьОтчетностиКлиентСервер.НовыйПараметрыЗапросаИзмененияОтчета());
	Параметры.Вставить("ВыполнитьСинхронно", Истина);
	
	Возврат Параметры;
КонецФункции

Функция НовыйДанныеПодключенияПриложения()
	
	ДанныеПодключения = Новый Структура;
	ДанныеПодключения.Вставить("ИдентификаторПриложения", "");
	ДанныеПодключения.Вставить("ДоступныеОрганизации", Новый Массив);
	
	Возврат ДанныеПодключения;
	
КонецФункции

Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Сообщения отчетности облачных приложений'");
	
КонецФункции

#КонецОбласти