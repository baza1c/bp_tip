
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиент-серверные методы формы записи регистра сведений НастройкиСистемыНалогообложения
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает признак того, что переход на систему налогообложения возможен только с начала года
//
// Параметры:
//  СистемаНалогообложения - ПеречислениеСсылка.СистемыНалогообложения
//
// Возвращаемое значение:
//  Булево
//
Функция ПереходНаСистемуНалогообложенияСНачалаГода(СистемаНалогообложения) Экспорт
	
	СтандартнаяОбработка = Истина;
	Результат = Неопределено;
	НастройкиСистемыНалогообложенияФормыКлиентСерверПереопределяемый.ПереходНаСистемуНалогообложенияСНачалаГода(
		СистемаНалогообложения, Результат, СтандартнаяОбработка);
	Если СтандартнаяОбработка Тогда
		Результат = Ложь;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УправлениеФормой(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Период = ПолучитьПериодФормы(Форма);
	
	СистемаНалогообложения = СистемаНалогообложенияПоИндексу(Форма.СистемаНалогообложенияПредставление);
	
	Элементы.ГруппаЕНВД.Видимость = ДопускаетсяСовмещениеСЕНВД(СистемаНалогообложения)
		И Период < УчетЕНВДКлиентСервер.ДатаОтменыЕНВД();
	Элементы.ГруппаПатент.Видимость = Не Форма.ЭтоЮрЛицо И ДопускаетсяСовмещениеСПатентом(СистемаНалогообложения);
	Элементы.ГруппаТорговыйСбор.Видимость = ОблагаетсяТорговымСбором(СистемаНалогообложения);
	Элементы.ГруппаСпецрежим.Видимость = Элементы.ГруппаЕНВД.Видимость
		Или Элементы.ГруппаПатент.Видимость
		Или Элементы.ГруппаТорговыйСбор.Видимость;
		
	Если Форма.Параметры.Свойство("КонтекстныйВызов") Тогда
		Элементы.ГруппаНДСПриУСН.Видимость = СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная");
	Иначе
		Элементы.ГруппаНДСПриУСН.Видимость = СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная")
			И УчетНДСКлиентСервер.Применяется176ФЗ(Период);
	КонецЕсли;
	
	НастроитьПодсказкуНДСПриУСН(Форма);
	
	Элементы.ГруппаПереходНаУСН.Видимость = (СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"));
	Элементы.ДатаПереходаНаУСН.Доступность = Форма.ПоложенияПереходногоПериодаУСН;
	Элементы.ДатаПереходаНаУСН.АвтоОтметкаНезаполненного = Форма.ПоложенияПереходногоПериодаУСН;
	
	Элементы.СистемаНалогообложенияТолькоЕНВДПослеОтмены.Видимость = Форма.ПрименяетсяТолькоЕНВДПослеОтменыНаМоментОткрытия
		И Период >= УчетЕНВДКЛиентСервер.ДатаОтменыЕНВД();
	Элементы.ГруппаПредупреждениеЕНВД.Видимость = ПрименяетсяТолькоЕНВДПослеОтмены(Форма);
	
	Элементы.СистемаНалогообложенияНПДНеприменимо.Видимость = Форма.ЭтоЮрЛицо И Форма.ПрименяетсяНПДНаМоментОткрытия;
	Элементы.ГруппаОшибкаОПрименимостиСистемыНалогообложенияНПД.Видимость = Форма.ЭтоЮрЛицо И Форма.СистемаНалогообложенияПредставление = 0;
	
	Элементы.СистемаНалогообложенияПредставление.Видимость =
		Элементы.СистемаНалогообложенияПредставление.СписокВыбора.Количество() > 0;
	Элементы.СистемаНалогообложенияНедоступноПоТарифу.Видимость =
		Элементы.СистемаНалогообложенияНедоступноПоТарифу.СписокВыбора.Количество() > 0;
	Элементы.ГруппаОграниченияТарифа.Видимость = Элементы.СистемаНалогообложенияНедоступноПоТарифу.СписокВыбора.Количество() > 0;
	
	Элементы.ПредупреждениеЕНВДУказатьДействующуюСистему.Видимость = Форма.ПравоРедактированияНастроек;
	Элементы.ПредупреждениеЕНВДОбратитесьКСотруднику.Видимость = НЕ Форма.ПравоРедактированияНастроек;
	
	Элементы.НастройкиСистемыНалогообложенияПрименяетсяУСНПатент.ТолькоПросмотр =
		Элементы.СистемаНалогообложенияНедоступноПоТарифу.СписокВыбора.НайтиПоЗначению("Патент") <> Неопределено;
		
	Если СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.АУСН")
		Или СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход") Тогда
		Элементы.ГруппаЕНП.Видимость = Ложь;
	Иначе
		Если Форма.Параметры.Свойство("КонтекстныйВызов") Тогда
			Элементы.ГруппаЕНП.Видимость =
				Год(Форма.ТекущаяДата) >= Год(Форма.ДатаНачалаПримененияЕНП)
				И Форма.ТекущаяДата < НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж();
		Иначе
			Элементы.ГруппаЕНП.Видимость = Период >= Форма.ДатаНачалаПримененияЕНП
				И Период < НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж();
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

Процедура ЗаполнитьСписокВыбораСистемНалогообложения(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Период = ПолучитьПериодФормы(Форма);
	
	ОписаниеСистемНалогообложения = Форма.ОписаниеСистемНалогообложения;
	
	СписокВыбораДоступныеСНО = Элементы.СистемаНалогообложенияПредставление.СписокВыбора;
	СписокВыбораНедоступныеПоТарифу = Элементы.СистемаНалогообложенияНедоступноПоТарифу.СписокВыбора;
	
	СписокВыбораДоступныеСНО.Очистить();
	СписокВыбораНедоступныеПоТарифу.Очистить();
	
	Для Индекс = 0 По ОписаниеСистемНалогообложения.Количество() - 1 Цикл
		
		Описание = ОписаниеСистемНалогообложения[Индекс];
		
		Если Описание.Идентификатор = "ЕНВД" И Период >= УчетЕНВДКлиентСервер.ДатаОтменыЕНВД() Тогда
			Продолжить;
		КонецЕсли;
		
		Если Описание.ДоступнаПоТарифу Тогда
			СписокВыбораДоступныеСНО.Добавить(Описание.Индекс, Описание.Наименование);
		Иначе
			СписокВыбораНедоступныеПоТарифу.Добавить(Описание.Индекс, Описание.Наименование);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьСистемуНалогообложения(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	СписокВыбора = Элементы.СистемаНалогообложенияПредставление.СписокВыбора;
	
	Если СписокВыбора.НайтиПоЗначению(Форма.СистемаНалогообложенияПредставление) = Неопределено Тогда
		Форма.СистемаНалогообложенияПредставление = СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

Функция ИндексСистемыНалогообложения(СистемаНалогообложения, ПрименяетсяУСНПатент = Ложь, ПлательщикЕНВД = Ложь, ОбъектНалогообложенияУСН = Неопределено) Экспорт
	
	Индекс = 9; // Неопределено
	
	Если СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход") Тогда
		Индекс = 0;
	ИначеЕсли СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная") Тогда
		Если ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы") Тогда
			Индекс = 1;
		Иначе
			Индекс = 2;
		КонецЕсли;
	ИначеЕсли СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок") Тогда
		Если ПрименяетсяУСНПатент Тогда
			Индекс = 3;
		ИначеЕсли ПлательщикЕНВД Тогда
			Индекс = 4;
		КонецЕсли;
	ИначеЕсли СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая") Тогда
		Индекс = 5;
	ИначеЕсли СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.АУСН") Тогда
		Если ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы") Тогда
			Индекс = 6;
		Иначе
			Индекс = 7;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Индекс;
	
КонецФункции

Функция СистемаНалогообложенияПоИндексу(Индекс) Экспорт
	
	Если Индекс = 0 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.НалогНаПрофессиональныйДоход");
	ИначеЕсли Индекс = 1 Или Индекс = 2 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная");
	ИначеЕсли Индекс = 3 Или Индекс = 4 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок");
	ИначеЕсли Индекс = 5 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая");
	ИначеЕсли Индекс = 6 Или Индекс = 7 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.АУСН");
	Иначе
		СистемаНалогообложения = Неопределено;
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

Процедура СброситьПризнакПлательщикЕНВД(Форма) Экспорт
	
	Период = ПолучитьПериодФормы(Форма);
	
	Если Период >= УчетЕНВДКлиентСервер.ДатаОтменыЕНВД() Тогда
		Форма.НастройкиСистемыНалогообложения.ПлательщикЕНВД = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура СброситьПризнакПлательщикНДСПрименяющийУСН(Форма) Экспорт
	
	Период = ПолучитьПериодФормы(Форма);
	
	Если Период < УчетУСНКлиентСервер.ДатаНачалаПримененияНДС() Тогда
		Форма.НастройкиСистемыНалогообложения.ПлательщикНДСПрименяющийУСН = Ложь;
		Форма.ПрименениеНДСНаУСН = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ПрименяетсяТолькоЕНВДПослеОтмены(Форма) Экспорт
	
	Запись = Форма.НастройкиСистемыНалогообложения;
	Период = ПолучитьПериодФормы(Форма);
	
	Возврат Запись.ПрименяетсяОсобыйПорядокНалогообложения
		И Запись.ПлательщикЕНВД
		И Период >= УчетЕНВДКЛиентСервер.ДатаОтменыЕНВД();
	
КонецФункции

Функция ДопускаетсяСовмещениеСЕНВД(СистемаНалогообложения)
	
	Возврат (СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая")
		Или СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"));
	
КонецФункции

Функция ДопускаетсяСовмещениеСПатентом(СистемаНалогообложения)
	
	Возврат (СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая")
		Или СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"));
	
КонецФункции

Функция ОблагаетсяТорговымСбором(СистемаНалогообложения)
	
	Возврат (СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая")
		Или СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная")
		Или СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.АУСН"));
	
КонецФункции

Функция ПолучитьПериодФормы(Форма) Экспорт
	
	Если Форма.ИмяФормы = "ОбщаяФорма.НалогиИОтчеты" Тогда
		Возврат Форма.ДатаИзменения;
	Иначе
		Возврат Форма.НастройкиСистемыНалогообложения.Период;
	КонецЕсли;
	
КонецФункции

Процедура НастроитьПодсказкуНДСПриУСН(Форма)

	Элементы = Форма.Элементы;
	
	Период = ПолучитьПериодФормы(Форма);
	
	ТекстПодсказки = СтрШаблон(
		НСтр("ru='При доходе более %1 млн необходимо начислять НДС'"),
		Окр(УчетУСНКлиентСервер.ГраницаПримененияНДС(Период) / 1000000, 2));
	
	Если Форма.ИмяФормы = "ОбщаяФорма.НалогиИОтчеты" Тогда
		Элементы.ДекорацияПрименениеНДСПриУСН.Заголовок = ТекстПодсказки;
	КонецЕсли;
	Элементы.НастройкиСистемыНалогообложенияПлательщикНДСПрименяющийУСН.Подсказка = ТекстПодсказки;
		
КонецПроцедуры

#КонецОбласти