
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для справочника Организации
//
// Серверные вызовы методов форм справочника Организации
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыСистемыНалогообложенияПоОрганизации(Знач ОрганизацияСУчетнойПолитикой) Экспорт
	
	ДатаПолученияИнформации = ТекущаяДатаСеанса();
	
	ПараметрыУчетнойПолитики = НастройкиУчета.ОписаниеСтруктурыУчетнойПолитики();
	
	ПараметрыУчетнойПолитики.Вставить("СистемаНалогообложения",
		УчетнаяПолитика.СистемаНалогообложения(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПрименяетсяАУСН",
		УчетнаяПолитика.ПрименяетсяАУСН(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПрименяетсяУСН",
		УчетнаяПолитика.ПрименяетсяУСН(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПрименяетсяУСНПатент",
		УчетнаяПолитика.ПрименяетсяУСНПатент(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПлательщикЕНВД",
		УчетнаяПолитика.ПлательщикЕНВД(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПлательщикНДС",
		УчетнаяПолитика.ПлательщикНДС(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПлательщикНалогаНаПрибыль",
		УчетнаяПолитика.ПлательщикНалогаНаПрибыль(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПлательщикНДФЛ",
		УчетнаяПолитика.ПлательщикНДФЛ(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПрименяетсяОсобыйПорядокНалогообложения",
		УчетнаяПолитика.ПрименяетсяОсобыйПорядокНалогообложения(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПлательщикТорговогоСбора",
		УчетнаяПолитика.ПлательщикТорговогоСбора(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПрименяетсяУСНДоходы",
		УчетнаяПолитика.ПрименяетсяУСНДоходы(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПрименяетсяУСНДоходыМинусРасходы",
		УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ОбъектНалогообложенияУСН",
		УчетнаяПолитика.ОбъектНалогообложенияУСН(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("СпособОценкиМПЗ",
		?(ПараметрыУчетнойПолитики.ПрименяетсяУСНДоходыМинусРасходы ИЛИ ПараметрыУчетнойПолитики.ПлательщикНДФЛ, Перечисления.СпособыОценки.ФИФО, Перечисления.СпособыОценки.ПоСредней));
	ПараметрыУчетнойПолитики.Вставить("ПрименяетсяНалогНаПрофессиональныйДоход",
		УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
	ПараметрыУчетнойПолитики.Вставить("ПлательщикНДСПрименяющийУСН",
		УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(ОрганизацияСУчетнойПолитикой, ДатаПолученияИнформации));
		
	Возврат ПараметрыУчетнойПолитики;
	
КонецФункции

Функция АдресДемобазы() Экспорт
	
	Возврат Константы.АдресДемобазы.Получить();
	
КонецФункции

Функция КонтактнаяИнформацияПоПредставлению(Знач Текст, Знач ОжидаемыйВид) Экспорт
	
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Текст, ОжидаемыйВид);
	
КонецФункции

Функция НаименованиеОКВЭД2(Код) Экспорт
	
	ДанныеОКВЭД2 = КлассификаторОКВЭД.ЗначениеПоКоду(Код, Ложь);
	НаименованиеОКВЭД2 = Неопределено;
	Если ЗначениеЗаполнено(ДанныеОКВЭД2) Тогда
		НаименованиеОКВЭД2 = ДанныеОКВЭД2[0].Наименование;
	КонецЕсли;
	
	Возврат НаименованиеОКВЭД2;
	
КонецФункции

Функция СтруктураУчетнойПолитикиНПД(ВидОрганизации) Экспорт

Возврат РегистрыСведений.НастройкиСистемыНалогообложения.СтруктураУчетнойПолитики(
			ВидОрганизации, "НалогНаПрофессиональныйДоход", Ложь);
КонецФункции

// Формирует двичные данные файлов в формате CML
//
// Параметры:
//   Организации - Массив из СправочникСсылка.Организации
//
// Возвращаемое значение:
//   Массив из ОписаниеПередаваемогоФайла
//
Функция КарточкиОрганизацийВXML(Организации) Экспорт
	
	Файлы = Справочники.Организации.СформироватьКарточкиОрганизацииВXML(Организации);
	
	Результат = Новый Массив;
	Для Каждого Файл Из Файлы Цикл
		ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(Файл.Представление, Файл.АдресВоВременномХранилище);
		Результат.Добавить(ОписаниеФайла);
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

#КонецОбласти
