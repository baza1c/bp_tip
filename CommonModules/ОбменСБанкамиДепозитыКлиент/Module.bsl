////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиДепозитыКлиент: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура обработчик команд депозитного калькулятора
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  Команда - ДекорацияФормы, КнопкаФормы, Неопределено - источник действия;
//  ИмяКоманды - Строка, Неопределено - имя команды, может быть Неопределено если задан параметр Команда;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//
Процедура ВыполнитьКоманду(Форма, ПараметрыПредложений, Команда = Неопределено, Знач ИмяКоманды = Неопределено) Экспорт 
	
	Если Не Команда = Неопределено И Не ЗначениеЗаполнено(ИмяКоманды) Тогда
		ИмяКоманды = Команда.Имя;
	КонецЕсли;
	
	ДополнительныеПараметрыДепозитов = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	Если ДополнительныеПараметрыДепозитов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяКоманды = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПоказатьРасчетДепозитов") Тогда
		
		ИмяГруппыРазмещения = ДополнительныеПараметрыДепозитов.ИмяГруппыРазмещения;
		ЭлементКалькулятор = Форма.Элементы[ИмяГруппыРазмещения];
		ЭтоОткрытиеДепозитногоКалькулятора = Не ЭлементКалькулятор.Видимость;
		
		ЭлементКалькулятор.Видимость = Не ЭлементКалькулятор.Видимость;
		
		Если ДополнительныеПараметрыДепозитов.ИспользоватьКомандуДепозиты Тогда
			Форма.Элементы[ИмяКоманды].Пометка = ЭлементКалькулятор.Видимость;
		КонецЕсли;
		
		Если Не ЭтоОткрытиеДепозитногоКалькулятора Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметрыДепозитов.ВыполняетсяАктуализация Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДополнительныеПараметрыДепозитов.ДатаОбновленияПредложений) Тогда
			ПроверитьАктуальностьИОбновитьДанныеДепозитныхПродуктов(Форма);
		Иначе
			ПоказатьРасчетДепозитов(Форма, ПараметрыПредложений);
		КонецЕсли;
		
	ИначеЕсли ИмяКоманды = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ОтправитьЗаявку") Тогда
		
		ДополнительныеПараметрыДепозитов = 
			ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
		ПараметрыОткрытияФормы = Новый Структура();
		ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Сумма");
		ПараметрыОткрытияФормы.Вставить("Сумма", Форма[ИмяПараметра]);
		
		ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Срок");
		ПараметрыОткрытияФормы.Вставить("Срок", Форма[ИмяПараметра]);
		
		ПараметрыОткрытияФормы.Вставить("КлючПродукта", ДополнительныеПараметрыДепозитов.ВыбранноеПредложение);
		ПараметрыОткрытияФормы.Вставить("Организация", ПараметрыПредложений.Организация);

		ОткрытьФорму("Документ.ЗаявкаНаДепозитОбменСБанками.Форма.ФормаДокумента", ПараметрыОткрытияФормы,,,,,);
		
	ИначеЕсли ИмяКоманды = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СрокиИУсловия") Тогда
		
		ДополнительныеПараметрыДепозитов = 
			ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
		ВыбранноеПредложение = ДополнительныеПараметрыДепозитов.ВыбранноеПредложение;
		
		ДанныеТекущегоПродукта = Неопределено;
		Для каждого ТекПродукт Из ПараметрыПредложений.МассивПредложений Цикл
			ДанныеПродукта = ТекПродукт.ДополнительныеДанные;
			Если ДанныеПродукта.КлючПродукта = ВыбранноеПредложение Тогда
				ДанныеТекущегоПродукта = ТекПродукт.ДополнительныеДанные;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ДанныеТекущегоПродукта = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыОткрытия = НовыеПараметрыОткрытияФормыСрокиИУсловия();
		
		ПараметрыПродукта = НовыеПараметрыПродукта();
		ПараметрыПродукта.КлючПродукта = ДанныеТекущегоПродукта.КлючПродукта;
		ПараметрыПродукта.Наименование = ДанныеТекущегоПродукта.Наименование;
		ПараметрыПродукта.НаименованиеБанка = ДанныеТекущегоПродукта.НаименованиеБанка;
		ПараметрыПродукта.ПодробноеОписание = ДанныеТекущегоПродукта.ОписаниеПродукта;
		ПараметрыПродукта.ОдинБанк = ПараметрыПредложений.ОдинБанк;
		ПараметрыПродукта.РасчетПриОформлении = ДанныеТекущегоПродукта.РасчетПриОформлении;
		
		ПараметрыОткрытия.ПараметрыПродукта = ПараметрыПродукта;
		
		ПараметрыПользователя = НовыеПараметрыПользователя();
		ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Сумма"); 
		ПараметрыПользователя.Сумма = Форма[ИмяПараметра];
		ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Срок");
		ПараметрыПользователя.Срок = Форма[ИмяПараметра];
		
		ПараметрыПользователя.ДатаРасчета = ДополнительныеПараметрыДепозитов.ДатаОбновленияПредложений;
		
		ПараметрыОткрытия.ПараметрыПользователя = ПараметрыПользователя;
		
		ПараметрыОткрытия.ПараметрыПользователя = ПараметрыПользователя;
		ПараметрыОткрытия.АдресХранилищаПредложенийБанков = ДополнительныеПараметрыДепозитов.АдресХранилищаПредложенийБанков;
		
		ОткрытьФорму("Документ.ЗаявкаНаДепозитОбменСБанками.Форма.СрокиИУсловия",
			ПараметрыОткрытия, Форма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	ИначеЕсли ИмяКоманды = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПовторитьЗапросПредложений") Тогда
		
		Если ДополнительныеПараметрыДепозитов.ВыполняетсяАктуализация Тогда
			Возврат;
		КонецЕсли;
		
		ПроверитьАктуальностьИОбновитьДанныеДепозитныхПродуктов(Форма, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработчик события "ОбработкаВыбора" списка выбора предложений депозитных продуктов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка;
//  Элемент - ПолеФормы - элемент источник события;
//  ВыбранноеЗначение - Строка - имя предложения банка;
//  ДополнительныеДанные - Структура - дополнительные данные списка выбора предложений 
//    см. ОбменСБанкамиДепозитыСлужебный.СформироватьМассивДляСпискаВыбораПредложений; 
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаВыбора(Форма, ПараметрыПредложений, Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	
	Если ДополнительныеПараметры.ВыбранноеПредложение = ДополнительныеДанные.КлючПродукта Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ВыбранноеПредложение = ДополнительныеДанные.КлючПродукта;
	
	ИмяЭлементаСрок = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Срок");
	ИмяЭлементаСумма = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Сумма");
	
	Если ДополнительныеДанные.Сумма = Форма[ИмяЭлементаСрок] 
		И ДополнительныеДанные.Срок = Форма[ИмяЭлементаСумма]
		Или Не ПараметрыПредложений.ПользовательУказалПараметры Тогда
		
		УстановитьПредложениеБанка(Форма, ВыбранноеЗначение, ДополнительныеДанные,
			ПараметрыПредложений);
		
	Иначе
			
		Форма.ОбменСБанкамиДепозитыОбновитьУсловияПродукта();
		
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает изменение элементов формы депозитов
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  Элемент - ЭлементФормы - Измененный элемент формы
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//
Процедура ДепозитыПриИзменении(Форма, Элемент, ПараметрыПредложений) Экспорт
	
	ЭлементыФормы = Форма.Элементы;
	ДополнительныеПараметры =
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ИмяЭлементаОрганизация = ДополнительныеПараметры.ИмяЭлементаРеквизитаОрганизация;
	ИмяЭлементаСрок = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Срок");
	ИмяЭлементаДатаЗакрытия = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ДатаЗакрытия");
	ИмяЭлементаСумма = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Сумма");
	ИмяГруппыРазмещения = ДополнительныеПараметры.ИмяГруппыРазмещения;
	ЭлементКалькулятор = ЭлементыФормы[ИмяГруппыРазмещения];
	ДлинаСуток = 24 * 60 * 60;
	
	// Для расчета сроков предложений используем ДатуОбновленияПредложений, т.к. ДатаОбновленияПредложений - это
	// НачалоДня(ТекущаяДатаСеанса()), полученная при проверке актуальности предложений,
	// и ее использование предпочтительней, чем использование ТекущаяДата(), и позволяет избежать возможных ошибок
	// при получении текущей даты во время расчета условий депозитных продуктов.
	//
	Если ИмяЭлементаСрок = Элемент.Имя Тогда
		Форма[ИмяЭлементаДатаЗакрытия] = ДополнительныеПараметры.ДатаОбновленияПредложений + Форма[ИмяЭлементаСрок] * ДлинаСуток;
	ИначеЕсли ИмяЭлементаДатаЗакрытия = Элемент.Имя Тогда
		Если Форма[ИмяЭлементаДатаЗакрытия] <= ДополнительныеПараметры.ДатаОбновленияПредложений Тогда
			Форма[ИмяЭлементаСрок] = 1;
			Форма[ИмяЭлементаДатаЗакрытия] = ДополнительныеПараметры.ДатаОбновленияПредложений + ДлинаСуток;
		Иначе
			Форма[ИмяЭлементаСрок] = Окр((Форма[ИмяЭлементаДатаЗакрытия] - ДополнительныеПараметры.ДатаОбновленияПредложений) / (ДлинаСуток), 0);
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяЭлементаСумма = Элемент.Имя
		Или ИмяЭлементаСрок = Элемент.Имя
		Или ИмяЭлементаДатаЗакрытия = Элемент.Имя Тогда
		ПараметрыПредложений.ПользовательУказалПараметры = Истина;
		ПроверитьОбновитьРасчетПоУсловиямТекущегоПродукта(Форма, ПараметрыПредложений);
	ИначеЕсли ИмяЭлементаОрганизация = Элемент.Имя И ЭлементКалькулятор.Видимость Тогда
		ОбновитьПредложенияПриИзмененииОрганизации(Форма, ПараметрыПредложений);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет данные выбора предложений банков
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//  Элемент - ПолеФормы - элемент источник события.
//  Текст - Строка - строка текста, введенная в поле ввода.
//  ДанныеВыбора - СписокЗначений - Список для заполнения предложениями
//  ПараметрыПолученияДанных - Структура, Неопределено - содержит параметры поиска, которые будут переданы в метод ПолучитьДанныеВыбора.
//  Ожидание - Число - интервал в секундах после ввода текста, через который произошло событие.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ЗаполнитьДанныеВыбора(Форма, ПараметрыПредложений, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПараметрыПредложений.ПользовательУказалПараметры Тогда
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(ПараметрыПредложений.МассивПредложений);
		
	Иначе
		
		ДополнительныеПараметры =
			ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
		ТекущаяОрганизация = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, 
			ДополнительныеПараметры.ПутьКРеквизитуОрганизация);
		ОбновитьДанныеПредложенияБанков(Форма, ПараметрыПредложений, ТекущаяОрганизация);
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(ПараметрыПредложений.МассивПредложений);
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет предложения банков при изменении организации
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//
Процедура ОбновитьПредложенияПриИзмененииОрганизации(Форма, ПараметрыПредложений) Экспорт
	
	ДополнительныеПараметрыДепозитов = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	Если Не ЗначениеЗаполнено(ДополнительныеПараметрыДепозитов)
		Или ДополнительныеПараметрыДепозитов.ВыполняетсяАктуализация Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы = Форма.Элементы;
	ЭлементРазмещения = ЭлементыФормы[ДополнительныеПараметрыДепозитов.ИмяГруппыРазмещения];
	
	Если Не ЭлементРазмещения.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы = Форма.Элементы;
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СтраницаИнформации");
	ЭлементСтраницаИнформации = ЭлементыФормы[ИмяЭлемента];
	ЭлементСтраницаИнформации.Видимость = Ложь;
	
	Если Не ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОтложеннаяИнициализацияВыполнена(
			ДополнительныеПараметрыДепозитов) Тогда
		Форма.ОбменСБанкамиДепозитыВыполнитьИнициализацию();
	Иначе
		
		ТекущаяОрганизация = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, 
			ДополнительныеПараметрыДепозитов.ПутьКРеквизитуОрганизация);
		ВыбранноеПредложение = ДополнительныеПараметрыДепозитов.ВыбранноеПредложение;
		
		УсловияВыбранногоПредложения = Неопределено;
		
		Если ПараметрыПредложений = Неопределено Тогда
			ПараметрыПредложений = НовыйПараметрыПредложенийБанка();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыбранноеПредложение)
			Или Не ТекущаяОрганизация = ПараметрыПредложений.Организация
			Или Не ПараметрыПредложений.Свойство("УсловияТекущегоПродукта", УсловияВыбранногоПредложения)
			Или Не ВыбранноеПредложение = УсловияВыбранногоПредложения.КлючПродукта Тогда
			
			ОбновитьДанныеПредложенияБанков(форма, ПараметрыПредложений, ТекущаяОрганизация);
			
		Иначе
			
			ДанныеВыбранногоПредложения = Неопределено;
			ДанныеВыбранногоУсловия = Неопределено;
			
			Для каждого ТекПредложение Из ПараметрыПредложений.МассивПредложений Цикл
				ДанныеВыбранногоУсловия = ТекПредложение.ДополнительныеДанные;
				Если ДанныеВыбранногоУсловия.КлючПродукта = ВыбранноеПредложение Тогда
					ДанныеВыбранногоПредложения = ТекПредложение.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Не ДанныеВыбранногоПредложения = Неопределено 
				И Не ДанныеВыбранногоУсловия = Неопределено Тогда
				
				УстановитьПредложениеБанка(Форма, ДанныеВыбранногоПредложения, 
					ДанныеВыбранногоУсловия, ПараметрыПредложений);
					
			КонецЕсли;
				
			НастроитьВидимостьСтраницыРасчетДепозитов(Форма, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет данные депозитного калькулятора в соответствии с выбранными условиями
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ВыбранноеЗначение - Структура - Выбранные пользователем условия депозита
//  ИсточникВыбора - Форма - Форма-источник выбора условий
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//
Процедура ОбработатьВыборУсловия(Форма, ВыбранноеЗначение, ИсточникВыбора, ПараметрыПредложений) Экспорт
	
	ДополнительныеПараметрыДепозитов = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	Если Не ЗначениеЗаполнено(ДополнительныеПараметрыДепозитов) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) 
		Или ИсточникВыбора = Неопределено
		Или Не ИсточникВыбора.ИмяФормы = "Документ.ЗаявкаНаДепозитОбменСБанками.Форма.СрокиИУсловия"
		Или ПараметрыПредложений = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
		
	МассивПредложений = ПараметрыПредложений.МассивПредложений;
	ТекущийПродукт = Неопределено;
	ДополнительныеДанные = Неопределено;
	Для каждого ТекущийПродукт Из МассивПредложений Цикл
		ДополнительныеДанные = ТекущийПродукт.ДополнительныеДанные;
		Если ДополнительныеДанные.КлючПродукта = ВыбранноеЗначение.КлючПродукта Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеТекущегоПродукта = ПараметрыПредложений.УсловияТекущегоПродукта;
	УсловияТекущегоПродукта = ДанныеТекущегоПродукта.Условия;
	
	ТекущееУсловие = Неопределено;
	Для каждого ТекущееУсловие Из ДанныеТекущегоПродукта.Условия Цикл
			
		Если ТекущееУсловие.ИдентификаторУсловия = ВыбранноеЗначение.ИдентификаторУсловия Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДополнительныеДанные = Неопределено
		Или ТекущееУсловие = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПредложений.ПользовательУказалПараметры = Истина;
	
	ЗаполнитьЗначенияСвойств(ДополнительныеДанные, ТекущееУсловие, 
		"СрокМаксимум, СрокМинимум, СуммаМаксимум, СуммаМинимум, Ставка, ИдентификаторУсловия");
	ДополнительныеДанные.Срок = ВыбранноеЗначение.Срок;
	ДополнительныеДанные.Сумма = ВыбранноеЗначение.Сумма;
	ДополнительныеДанные.Доход = ВыбранноеЗначение.Доход; 
	ДополнительныеДанные.НеВыполненыУсловия = Ложь;
	
	
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Сумма");
	Форма[ИмяПараметра] = ВыбранноеЗначение.Сумма;
	
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Срок");
	Форма[ИмяПараметра] = ВыбранноеЗначение.Срок;
	
	Если ПараметрыПредложений.ОдинБанк Тогда
		НаименованиеПродукта = СтрШаблон("%1%% %2", ДополнительныеДанные.Ставка, ДополнительныеДанные.Наименование);
	Иначе
		НаименованиеПродукта = СтрШаблон("%1: %2%% %3", ДополнительныеДанные.НаименованиеБанка, 
			ДополнительныеДанные.Ставка, ДополнительныеДанные.Наименование);
	КонецЕсли;
	
	ТекущийПродукт.Значение = НаименованиеПродукта;
	ВыбранноеПредложение = ТекущийПродукт.Значение;
	
	УстановитьПредложениеБанка(Форма, ВыбранноеПредложение, ДополнительныеДанные, ПараметрыПредложений);
	
КонецПроцедуры

// Настраивает видимость элементов формы и проверяет актуальность данных
// о депозитных продуктах при открытии формы
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор.
//
Процедура ПриОткрытии(форма) Экспорт
	
	ДополнительныеПараметрыДепозитов = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
		
	Если ДополнительныеПараметрыДепозитов = Неопределено 
		Или Не ОрганизацияЗаполнена(Форма) 
		Или Не ДополнительныеПараметрыДепозитов.ПоказыватьПанельРасчетаДепозитов Тогда
		Возврат;
	КонецЕсли; 
	
	Если ДополнительныеПараметрыДепозитов.ИспользоватьКомандуДепозиты Тогда
		ИмяКоманды = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПоказатьРасчетДепозитов");
		Форма.Элементы[ИмяКоманды].Пометка = Истина;
	КонецЕсли;
	
	ИмяГруппыРазмещения = ДополнительныеПараметрыДепозитов.ИмяГруппыРазмещения;
	ЭлементКалькулятор = Форма.Элементы[ИмяГруппыРазмещения];
	ЭлементКалькулятор.Видимость = Истина;
	Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбменСБанкамиДепозитыПроверитьАктуальностьПредложений", 0.1, Истина);
	
КонецПроцедуры

// Проверяет видимость панели расчета депозитов и обновляет данные в хранилище общих настроек
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//
Процедура ПередЗакрытием(Форма) Экспорт
	
	ДополнительныеПараметрыДепозитов = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
		
	Если ДополнительныеПараметрыДепозитов = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИмяГруппыРазмещения = ДополнительныеПараметрыДепозитов.ИмяГруппыРазмещения;
	ЭлементКалькулятор = Форма.Элементы[ИмяГруппыРазмещения];
	
	ВидимостьПанелиРасчета = ЭлементКалькулятор.Видимость;
	ОбменСБанкамиДепозитыВызовСервера.ОбработатьСостояниеПанелиРасчетаДепозитов(ВидимостьПанелиРасчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПослеОбновленияДанныхДепозитныхПродуктов(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	ТекстСообщения = "";
	
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда // задание было отменено
		ТекстСообщения =  НСтр("ru = 'Не удалось отправить запрос в банк.'");
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	ДополнительныеПараметрыДепозитов =
			ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ДополнительныеПараметрыДепозитов.ВыполняетсяАктуализация = Ложь;
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		ДополнительныеПараметрыДепозитов.ДатаОбновленияПредложений = Дата(1, 1, 1);
		НастроитьОтображениеСтраницыИнформации(ДополнительныеПараметры.Форма, ТекстСообщения, Истина, Ложь, Истина);
	Иначе
		ПоказатьРасчетДепозитов(ДополнительныеПараметры.Форма, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// Настраивает отображение и заполняет доп параметры депозитов на форме
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//  РезультатИнициализации - Структура: см. ОбменСБанкамиДепозиты.ПроверитьПродуктыБанковИЗавершитьИнициализацию.
//
Процедура УстановитьПредложенияБанковПослеИнициализации(Форма, ПараметрыПредложений, РезультатИнициализации) Экспорт
	
	ДополнительныеПараметры = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ТекущаяОрганизация = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, 
			ДополнительныеПараметры.ПутьКРеквизитуОрганизация);
	
	Если Не РезультатИнициализации.ЕстьПредложенияБанков Тогда
		ЕстьОшибка = Ложь;
		Если ЗначениеЗаполнено(РезультатИнициализации.ОшибкаПолученияПредложений) Тогда
			ТекстСообщения = РезультатИнициализации.ОшибкаПолученияПредложений;
			ЕстьОшибка = Истина;
		Иначе
			ТекстСообщения = 
				ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияНетПредложенийБанковДляОрганизации();
		КонецЕсли;
		
		НастроитьОтображениеСтраницыИнформации(Форма, ТекстСообщения, Истина, Ложь, ЕстьОшибка);
		
		Возврат;
		
	КонецЕсли;
	
	ЭлементыФормы = Форма.Элементы;
	ИмяЭлементСтраницаИнформации = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СтраницаИнформации");
	ЭлементСтраницаИнформации = ЭлементыФормы[ИмяЭлементСтраницаИнформации];
	ЭлементСтраницаИнформации.Видимость = Ложь;
	
	ИмяЭлементСтраницаРасчетДепозита =
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СтраницаРасчетДепозита");
	ЭлементСтраницаРасчетДепозита = ЭлементыФормы[ИмяЭлементСтраницаРасчетДепозита];
	ЭлементСтраницаРасчетДепозита.Видимость = Истина;
	
	ПараметрыРасчета = НовыеПараметрыРасчетаПредложений();
	ПараметрыРасчета.ДатаРасчета = ДополнительныеПараметры.ДатаОбновленияПредложений;
	АдресХранилищаПредложенийБанков = ДополнительныеПараметры.АдресХранилищаПредложенийБанков;
	
	ДанныеПредложенийБанков = ОбменСБанкамиДепозитыВызовСервера.ДанныеПредложенийБанков(АдресХранилищаПредложенийБанков, ПараметрыРасчета);
	
	ПараметрыПредложений = НовыйПараметрыПредложенийБанка();
	ПараметрыПредложений.Организация = ТекущаяОрганизация;
	
	УстановитьПараметрыИПредложенияПослеПолучения(Форма, ПараметрыПредложений, ДанныеПредложенийБанков);
	
КонецПроцедуры

// Выполняет поиск подходящих условий по сроку и сумме в списке условий продукта 
// и устанавливает данные найденного условия на форме. 
// Если условие не найденно, установить пустые значения.
// Возвращает результат выполнения поиска.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//
// Возвращаемое значение:
//  Булево - Истина, если найдено подходящие условие.
//
Функция ПроверитьОбновитьРасчетПоУсловиямТекущегоПродукта(Форма, ПараметрыПредложений) Экспорт
	
	ДополнительныеПараметры = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ДатаРасчета = ДополнительныеПараметры.ДатаОбновленияПредложений;
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Сумма");
	СуммаДепозита = Форма[ИмяПараметра];
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Срок");
	СрокДепозита = Форма[ИмяПараметра];
	СписокДоходности = Новый СписокЗначений();
	ЕстьПодходящиеУсловие = Ложь;
	УсловияТекущегоПродукта = ПараметрыПредложений.УсловияТекущегоПродукта;
	МассивПредложений = ПараметрыПредложений.МассивПредложений;
	КлючПродуктаУсловий = УсловияТекущегоПродукта.КлючПродукта;
	СписокУсловий = УсловияТекущегоПродукта.Условия;

	Если СписокУсловий.Количество() > 0 
		И Не ДополнительныеПараметры.ВыбранноеПредложение = КлючПродуктаУсловий Тогда
		
		СписокУсловий = ОбменСбанкамиДепозитыВызовСервера.ПолучитьУсловияПродукта(
			ДополнительныеПараметры.АдресХранилищаПредложенийБанков, ДополнительныеПараметры.ВыбранноеПредложение);
		УсловияТекущегоПродукта.КлючПродукта = ДополнительныеПараметры.ВыбранноеПредложение;
		УсловияТекущегоПродукта.Условия = СписокУсловий;
		
	КонецЕсли;
	
	МаксСтавка = 0;
	
	Для ТекИндес = 0 По СписокУсловий.ВГраница() Цикл
		
		ТекУсловие = СписокУсловий[ТекИндес];
		
		Если ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ВыполняютсяУсловияПродукта(
				ТекУсловие, СуммаДепозита, СрокДепозита) Тогда
			
			ЕстьПодходящиеУсловие = Истина;
			Доход = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.РассчитатьДоход(ДатаРасчета, 
				СуммаДепозита, ТекУсловие.Ставка, СрокДепозита);
			СписокДоходности.Добавить(Доход, ТекИндес);
			
		КонецЕсли;
		
		МаксСтавка = Макс(МаксСтавка, ТекУсловие.Ставка);
		
	КонецЦикла;
	
	Если СписокДоходности.Количество() > 1 Тогда
		СписокДоходности.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	КонецЕсли;
	
	КлючПродукта = ДополнительныеПараметры.ВыбранноеПредложение;
	ДополнительныеДанные = Неопределено;
	
	Для каждого ТекПродукт Из МассивПредложений Цикл
		
		ДополнительныеДанные = ТекПродукт.ДополнительныеДанные;
		
		Если Дополнительныеданные.КлючПродукта = КлючПродукта Тогда
			
			Если ЕстьПодходящиеУсловие Тогда
				
				УсловиеПродукта = СписокУсловий[Число(СписокДоходности[0].Представление)];
				ДополнительныеДанные.ИдентификаторУсловия = УсловиеПродукта.ИдентификаторУсловия;
				ДополнительныеДанные.Сумма = СуммаДепозита;
				ДополнительныеДанные.Срок = СрокДепозита;
				ДополнительныеДанные.Ставка = УсловиеПродукта.Ставка;
				ДополнительныеДанные.Доход = СписокДоходности[0].Значение;
				ДополнительныеДанные.НеВыполненыУсловия = Ложь;
				ДополнительныеДанные.СрокМинимум = УсловиеПродукта.СрокМинимум;
				ДополнительныеДанные.СрокМаксимум = УсловиеПродукта.СрокМаксимум;
				ДополнительныеДанные.СуммаМинимум = УсловиеПродукта.СуммаМинимум;
				ДополнительныеДанные.СуммаМаксимум = УсловиеПродукта.СуммаМаксимум;
			Иначе
				ДополнительныеДанные.НеВыполненыУсловия = Истина;
			КонецЕсли;
			
			Если ПараметрыПредложений.ОдинБанк Тогда
				НаименованиеПродукта = СтрШаблон("%1%% %2", ДополнительныеДанные.Ставка, ДополнительныеДанные.Наименование);
			Иначе
				НаименованиеПродукта = СтрШаблон("%1: %2%% %3", ДополнительныеДанные.НаименованиеБанка, 
					ДополнительныеДанные.Ставка, ДополнительныеДанные.Наименование);
			КонецЕсли;
			
			ТекПродукт.Значение = НаименованиеПродукта;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПредложениеБанка(Форма, ТекПродукт.Значение, ДополнительныеДанные, ПараметрыПредложений);
	
	Возврат ЕстьПодходящиеУсловие;
	
КонецФункции

// Подготавливает запуск задания проверки актуальности предложений, и обновляет их данные на форме.
// Устанавливает страницу информации на панели расчета депозита в состояние ожидания завершения операции.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор.
//  ОбновитьВсеБанки - Булево - если Истина, данные для запроса предложений будут получены для всех доступных банков,
//    вне зависимости от актуальности уже полученных предложений.
//
Процедура ПроверитьАктуальностьИОбновитьДанныеДепозитныхПродуктов(Форма, ОбновитьВсеБанки = Ложь) Экспорт
	
	ДополнительныеПараметрыДепозитов = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ТекстСообщения = ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияОбновлениеПредложений();
	НастроитьОтображениеСтраницыИнформации(Форма, ТекстСообщения, Ложь, Истина);
	ДополнительныеПараметрыОповещения = Новый Структура;
	ДополнительныеПараметрыОповещения.Вставить("Форма", Форма);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеОбновленияДанныхДепозитныхПродуктов", 
		ЭтотОбъект, ДополнительныеПараметрыОповещения);
	ИдентификаторФормы = Форма.УникальныйИдентификатор;
	РезультатЗапускаЗадания = ОбменСБанкамиДепозитыВызовСервера.ЗапускЗаданияОбновленияДанныхДепозитныхПродуктов(ОбновитьВсеБанки);
	
	Если ЗначениеЗаполнено(РезультатЗапускаЗадания.ДлительнаяОперация) Тогда
		ДополнительныеПараметрыДепозитов.ДатаОбновленияПредложений = РезультатЗапускаЗадания.ДатаОбновленияПредложений;
		ДополнительныеПараметрыДепозитов.ВыполняетсяАктуализация = Истина;
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатЗапускаЗадания.ДлительнаяОперация,
			ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли
	
КонецПроцедуры

// Функция - конструктор параметров открытия формы Документы.ЗаявкаНаДепозит.Формы.СрокиИУсловия.
//
// Возвращаемое значение:
//  Структура:
//   * ПараметрыПродукта - Структура: см. НовыеПараметрыПродукта.
//   * ПараметрыПользователя - Структура: см. НовыеПараметрыПользователя.
//   * АдресХранилищаПредложенийБанков - Строка - адрес временного хранилища с данными предложений банков.
//   * РежимВыбора - Булево - значение по умолчанию Истина.
//   * МножественныйВыбор - Булево - значение по умолчанию Ложь.
//   * ЗакрыватьПриВыборе - Булево - значение по умолчанию Истина.
//
Функция НовыеПараметрыОткрытияФормыСрокиИУсловия() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыПродукта", Новый Структура);
	Результат.Вставить("ПараметрыПользователя", Новый Структура);
	Результат.Вставить("АдресХранилищаПредложенийБанков", "");
	Результат.Вставить("РежимВыбора", Истина);
	Результат.Вставить("МножественныйВыбор", Ложь);
	Результат.Вставить("ЗакрыватьПриВыборе", Истина);
	
	Возврат Результат;
	
КонецФункции

// Функция - конструктор свойств параметра "ПараметрыПродукта" для 
//  параметров открытия формы Документы.ЗаявкаНаДепозит.Формы.СрокиИУсловия.
//
// Возвращаемое значение:
//  Структура:
//   * КлючПродукта - Строка - уникальный идентификатор депозитного продукта.
//   * Наименование - Строка - наименование депозитного продукта.
//   * НаименованиеБанка - Строка - наименование банка.
//   * ПодробноеОписание - Строка - описание депозитного продукта.
//   * РасчетПриОформлении - Булево - если Истина, расчет доходности выполняется в банке.
//   * ОдинБанк - Булево - если Истина, все депозитные продукты от одного банка.
//
Функция НовыеПараметрыПродукта() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КлючПродукта", "");
	Результат.Вставить("Наименование", "");
	Результат.Вставить("НаименованиеБанка", "");
	Результат.Вставить("ПодробноеОписание", "");
	Результат.Вставить("РасчетПриОформлении", Ложь);
	Результат.Вставить("ОдинБанк", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Функция - конструктор свойств параметра "ПараметрыПользователя" для 
// параметров открытия формы Документы.ЗаявкаНаДепозит.Формы.СрокиИУсловия.
// см. НовыеПараметрыОткрытияФормыСрокиИУсловия.
//
// Возвращаемое значение:
//  Структура:
//   * Срок - Число - срок депозита в днях.
//   * Сумма - Число - сумма депозита.
//   * ДатаРасчета - Дата - дата расчета.
//
Функция НовыеПараметрыПользователя() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Срок", 0);
	Результат.Вставить("Сумма", 0);
	Результат.Вставить("ДатаРасчета", Дата(1, 1, 1));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПоказатьРасчетДепозитов(Форма, ПараметрыПредложений)
	
	ДополнительныеПараметры =
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	
	Если Не ОрганизацияЗаполнена(Форма) Тогда
		ТекстСообщения = ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияНеЗаполненаОрганизация();
		НастроитьОтображениеСтраницыИнформации(Форма, ТекстСообщения, Ложь);
	ИначеЕсли Не ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОтложеннаяИнициализацияВыполнена(
			ДополнительныеПараметры) Тогда
		Форма.ОбменСБанкамиДепозитыВыполнитьИнициализацию();
	Иначе
		ОбновитьПредложенияПриИзмененииОрганизации(Форма, ПараметрыПредложений);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет отображение данных предложений банков по депозитам
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка;
//  ДанныеПредложенийБанков - Структура: см. СформироватьПредложенияБанков.СформироватьПредложенияБанков.
//
Процедура УстановитьПараметрыИПредложенияПослеПолучения(Форма, ПараметрыПредложений,
		ДанныеПредложенийБанков)
	
	ДополнительныеПараметрыДепозиты =
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
		
	ВыбранноеПредложение = ДанныеПредложенийБанков.МассивПредложений[0];
	ДанныеВыбранногоУсловия = ВыбранноеПредложение.ДополнительныеДанные;
	ДанныеВыбранногоПредложения = ВыбранноеПредложение.Значение;
	КлючПродукта = ДанныеВыбранногоУсловия.КлючПродукта;
	ИмяПараметраСумма = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Сумма");
	ИмяПараметраСрок = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Срок");
	ИмяПараметраДатаЗакрытия = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("ДатаЗакрытия");
	ДлинаСуток = 24 * 60 * 60;
	
	// Для расчета сроков предложений используем ДатуОбновленияПредложений, т.к. ДатаОбновленияПредложений - это
	// НачалоДня(ТекущаяДатаСеанса()), полученная при проверке актуальности предложений,
	// и ее использование предпочтительней, чем использование ТекущаяДата(), и позволяет избежать возможных ошибок
	// при получении текущей даты во время расчета условий депозитных продуктов.
	//
	Если Форма[ИмяПараметраСумма] = 0 Тогда
		Форма[ИмяПараметраСумма] = ДанныеВыбранногоУсловия.Сумма;
	КонецЕсли;
	
	Если Форма[ИмяПараметраСрок] = 0 Тогда
		Форма[ИмяПараметраСрок] = ДанныеВыбранногоУсловия.Срок;
		Форма[ИмяПараметраДатаЗакрытия] = ДополнительныеПараметрыДепозиты.ДатаОбновленияПредложений + ДанныеВыбранногоУсловия.Срок * ДлинаСуток;
	КонецЕсли;
	
	ЭлементыФормы = Форма.Элементы;
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Предложение");
	ЭлементПредложение = ЭлементыФормы[ИмяЭлемента];
	ТекстЗаголовка = НСтр("ru = 'Предложения'");
	
	Если ДанныеПредложенийБанков.ОдинБанк Тогда
		ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Предложения банка %1'"), ДанныеВыбранногоУсловия.НаименованиеБанка);
	КонецЕсли;

	ПользовательУказалПараметры = ПараметрыПредложений.ПользовательУказалПараметры;
	ЭлементПредложение.Заголовок = ТекстЗаголовка;

	ПараметрыПредложений.МассивПредложений = ДанныеПредложенийБанков.МассивПредложений;
	ПараметрыПредложений.УсловияТекущегоПродукта = ДанныеПредложенийБанков.УсловияТекущегоПродукта;
	ПараметрыПредложений.ОдинБанк = ДанныеПредложенийБанков.ОдинБанк;
	
	Если Не ПользовательУказалПараметры Тогда
		ДополнительныеПараметрыДепозиты.ВыбранноеПредложение = КлючПродукта;
		ИмяПараметраДополнительныеПараметры = 
			ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("ДополнительныеПараметры");
		Форма[ИмяПараметраДополнительныеПараметры] = ДополнительныеПараметрыДепозиты;
		УстановитьПредложениеБанка(Форма, ДанныеВыбранногоПредложения, 
			ДанныеВыбранногоУсловия, ПараметрыПредложений);
	КонецЕсли;
	
	НастроитьВидимостьСтраницыРасчетДепозитов(Форма, Истина);
	
КонецПроцедуры

// Устанавливает данные выбранного предложения банка на форме.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ВыбранноеПредложение - Строка - выбранное пользвоателем предложение в списке выбора предложений;
//  ВыбранноеУсловие - Структура: см. ОбменСБанкамиДепозитыСлужебный.НовыеПараметрыУсловияДепозитногоПродукта;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка.
//
Процедура УстановитьПредложениеБанка(Форма, ВыбранноеПредложение, ВыбранноеУсловие, ПараметрыПредложений)
	
	ЭлементыФормы = Форма.Элементы;
	КлючПараметраСумма = "Сумма";
	ИмяПараметраСумма = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметраСумма);
	ИмяПараметраСрок = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Срок");
	ИмяПараметраДатаЗакрытия = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("ДатаЗакрытия");
	ДополнительныеПараметры = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ДлинаСуток = 24 * 60 * 60;
	ПользовательВыбралПараметры = ПараметрыПредложений.ПользовательУказалПараметры;
	МассивПредложений = ПараметрыПредложений.МассивПредложений;
	ОдноПредложение = Ложь;
	
	// Для расчета сроков предложений используем ДатуОбновленияПредложений, т.к. ДатаОбновленияПредложений - это
	// НачалоДня(ТекущаяДатаСеанса()), полученная при проверке актуальности предложений,
	// и ее использование предпочтительней, чем использование ТекущаяДата(), и позволяет избежать возможных ошибок
	// при получении текущей даты во время расчета условий депозитных продуктов.
	//
	Если МассивПредложений.Количество() = 1 Тогда
		ОдноПредложение = Истина;
	КонецЕсли;
	
	Если Не ПользовательВыбралПараметры Тогда
	
		Форма[ИмяПараметраСрок] = ВыбранноеУсловие.Срок;
		Форма[ИмяПараметраСумма] = ВыбранноеУсловие.Сумма;
		Форма[ИмяПараметраДатаЗакрытия] = ДополнительныеПараметры.ДатаОбновленияПредложений + ВыбранноеУсловие.Срок * ДлинаСуток;
		
	КонецЕсли;
	
	ЗначениеПараметраСумма = Форма[ИмяПараметраСумма];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметраСумма);
	ЭлементПараметраСумма = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Ставка");
	ЭлементПараметраСтавка = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ОтправитьЗаявку");
	ЭлементФормыКнопкаЗаявки = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Доход");
	ЭлементПараметраДоход = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("РасчетПриОформлении");
	ЭлементРасчетПриОформлении = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("Итог");
	ЭлементПараметраИтог = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ГруппаПараметровДоходности");
	ЭлементГруппаПараметровДоходности = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ДоходностьОписаниеДепозита");
	ЭлементДоходность = ЭлементыФормы[ИмяЭлемента];
	
	ИмяЭлемента = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ОписаниеДепозита");
	ЭлементОписание = ЭлементыФормы[ИмяЭлемента];
	
	ФорматЧислаСтрока = "ЧЦ=15; ЧДЦ=2;";
	
	ЭлементГруппаПараметровДоходности.Видимость = Истина;
	ЭлементРасчетПриОформлении.Видимость = Ложь;
	
	Если ВыбранноеУсловие.РасчетПриОформлении Тогда
		
		ЭлементГруппаПараметровДоходности.Видимость = Ложь;
		ЭлементРасчетПриОформлении.Видимость = Истина;
		
	КонецЕсли;
	
	Если Не ВыбранноеУсловие.НеВыполненыУсловия Тогда
		
		ЭлементПараметраИтог.Заголовок = Формат(ВыбранноеУсловие.Доход + ЗначениеПараметраСумма, ФорматЧислаСтрока);
		ЭлементДоходность.Заголовок = ОбменСБанкамиДепозитыКлиентСервер.СформироватьСтрокуИнформацииПоУсловию(ВыбранноеУсловие, 
			ПользовательВыбралПараметры);
		ЭлементОписание.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ВыбранноеУсловие.ОписаниеПродукта);
		ЭлементПараметраДоход.Заголовок = Формат(ВыбранноеУсловие.Доход, ФорматЧислаСтрока);
		ШаблонЗаголовка = НСтр("ru = '%1 %% год.'");
		ЭлементПараметраСтавка.Заголовок = СтрШаблон(ШаблонЗаголовка, ВыбранноеУсловие.Ставка);
		ЭлементФормыКнопкаЗаявки.Доступность = Истина;
		
	Иначе
		
		ЭлементПараметраСтавка.Заголовок = "";
		ЭлементПараметраДоход.Заголовок = Формат(0, ФорматЧислаСтрока);
		ЭлементПараметраИтог.Заголовок = Формат(0, ФорматЧислаСтрока);
		ЭлементДоходность.Заголовок = НСтр("ru = 'Не выполнены условия продукта'");
		Если ОдноПредложение Тогда
			ЭлементОписание.Заголовок = НСтр("ru = 'Укажите другие параметры депозита или подберите интересующие вас условия'");
		Иначе
			ЭлементОписание.Заголовок = НСтр("ru = 'Выберите другой продукт из списка предложений или подберите интересующие вас условия'");
		КонецЕсли;
		ЭлементФормыКнопкаЗаявки.Доступность = Ложь;
		
	КонецЕсли;
	
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Предложение");
	Форма[ИмяПараметра] = ВыбранноеПредложение;
	
КонецПроцедуры

// Выводит сообщение, переданное в параметрах, и настраивает отображение страницы информации.
//
Процедура НастроитьОтображениеСтраницыИнформации(Форма, ТекстСообщения, ПовторитьЗапрос, ПоказатьОжидание = Ложь, ПоказатьОшибку = Ложь)
	
	Элементы = Форма.Элементы;
	ДополнительныеПараметры = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ИмяЭлементаСтраницаИнформации = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(
		"СтраницаИнформации");
	ИмяЭлементаСтраницаРасчетДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(
		"СтраницаРасчетДепозита");
	ЭлементСтраницаИнформации = Элементы[ИмяЭлементаСтраницаИнформации];
	
	ЭлементСтраницаИнформации.Видимость = Истина;
	
	Если ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОтложеннаяИнициализацияВыполнена(ДополнительныеПараметры) Тогда
		ЭлементСтраницаРасчетДепозита = Элементы[ИмяЭлементаСтраницаРасчетДепозита];
		ЭлементСтраницаРасчетДепозита.Видимость = Ложь;
	КонецЕсли;
	
	ИмяЭлементаОжидание = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ОжиданиеКартинка");
	ЭлементОжиданиеКартинка = Элементы[ИмяЭлементаОжидание];
	ЭлементОжиданиеКартинка.Видимость = ПоказатьОжидание;
	
	ИмяЭлементаОшибка = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ОшибкаКартинка");
	ЭлементОшибкаКартинка = Элементы[ИмяЭлементаОшибка];
	ЭлементОшибкаКартинка.Видимость = ПоказатьОшибку;
	
	ИмяЭлементаТекстСообщения = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ТекстСообщения");
	ЭлементОжиданиеТекст = Элементы[ИмяЭлементаТекстСообщения];
	ЭлементОжиданиеТекст.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ТекстСообщения);
	
	ИмяЭлементаКомандаПовторитьЗапрос = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПовторитьЗапросПредложений");
	ЭлементКомандаПовторитьЗапрос = Элементы[ИмяЭлементаКомандаПовторитьЗапрос];
	
	ЭлементКомандаПовторитьЗапрос.Видимость = ПовторитьЗапрос;
	
КонецПроцедуры

Процедура НастроитьВидимостьСтраницыРасчетДепозитов(Форма, ВидимостьСтраницы)
	
	Элементы = Форма.Элементы;
	ИмяЭлементаСтраницаИнформации = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(
		"СтраницаИнформации");
	ИмяЭлементаСтраницаРасчетДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(
		"СтраницаРасчетДепозита");
	
	ЭлементСтраницаИнформации = Элементы[ИмяЭлементаСтраницаИнформации];
	ЭлементСтраницаИнформации.Видимость = Не ВидимостьСтраницы;

	ЭлементСтраницаРасчетДепозита = Элементы[ИмяЭлементаСтраницаРасчетДепозита];
	ЭлементСтраницаРасчетДепозита.Видимость = ВидимостьСтраницы;
	
КонецПроцедуры

Функция ОрганизацияЗаполнена(Форма)
	
	ДополнительныеПараметры = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ТекущаяОрганизация = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, 
		ДополнительныеПараметры.ПутьКРеквизитуОрганизация);
	
	Возврат ЗначениеЗаполнено(ТекущаяОрганизация);
	
КонецФункции

// Возвращает новую структуру параметров для расчета предложений банков.
//
// Возвращаемое значение:
//  Структура:
//   * ДатаРасчета - текущая дата расчета доходности предложения.
//   * Сумма - Число - сумма депозита.
//   * Срок - Число - срок депозита в днях.
//
Функция НовыеПараметрыРасчетаПредложений()
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаРасчета", Дата(1, 1, 1));
	Результат.Вставить("Сумма", 0);
	Результат.Вставить("Срок", 0);
	
	Возврат Результат;
	
КонецФункции

// Создает и возвращает структуру параметров предложений банков по депозитам
//
// Возвращаемое значение:
//  Структура - Содержит следующие поля:
//   * Организация - Неопределено, ОпределяемыйТип.Организация - ссылка на организацию;
//   * МассивПредложений - Массив из Структура - доступные предложения банков
//        см. ОбменСБанкамиДепозитыСлужебный.СформироватьДанныеСпискаВыбораПредложений;
//   * УсловияТекущегоПродукта - Соответствие - условия выбранного депозитного продукта 
//        см. ОбменСБанкамиДепозитыСлужебный.ДанныеДепозитныхПродуктов;
//   * ПользовательУказалПараметры - Булево - признак указания параметров пользователем;
//   * ОдинБанк - Булево - признак указывающий что все доступные предложения от одного банка;
//
Функция НовыйПараметрыПредложенийБанка()
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Неопределено);
	Результат.Вставить("МассивПредложений", Новый Массив);
	Результат.Вставить("УсловияТекущегоПродукта", Новый Соответствие);
	Результат.Вставить("ПользовательУказалПараметры", Ложь);
	Результат.Вставить("ОдинБанк", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Выполняет обновление данных депозитных продуктов после изменения организации
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
//  ПараметрыПредложений - Структура: см. НовыйПараметрыПредложенийБанка;
//  Организация - ОпределяемыйТип.Организация - ссылка на организацию.
//
Процедура ОбновитьДанныеПредложенияБанков(Форма, ПараметрыПредложений, Организация = Неопределено)
	
	ДополнительныеПараметры = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ИмяПараметраСумма = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Сумма");
	ИмяПараметраСрок = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("Срок");
	
	СуммаДепозита = 0;
	СрокДепозита = 0 ;
	
	Если ПараметрыПредложений.ПользовательУказалПараметры Тогда
		СуммаДепозита = Форма[ИмяПараметраСумма];
		СрокДепозита = Форма[ИмяПараметраСрок];
	КонецЕсли;
	
	ПараметрыРасчета = НовыеПараметрыРасчетаПредложений();
	ПараметрыРасчета.ДатаРасчета = ДополнительныеПараметры.ДатаОбновленияПредложений;
	ПараметрыРасчета.Сумма = СуммаДепозита;
	ПараметрыРасчета.Срок = СрокДепозита;
	
	ПредложенияБанков = Неопределено;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		ИдентификаторФормы = Форма.УникальныйИдентификатор;
		ДанныеПредложенийБанков = 
			ОбменСБанкамиДепозитыВызовСервера.ПроверитьПродуктыИСформироватьДанныеПредложенийБанков(Организация, 
				ИдентификаторФормы, ПараметрыРасчета, ДополнительныеПараметры.АдресХранилищаПредложенийБанков);
		
		Если Не ЗначениеЗаполнено(ДанныеПредложенийБанков.ПредложенияБанков) Тогда
			ЕстьОшибка = Ложь;
			Если ЗначениеЗаполнено(ДанныеПредложенийБанков.ОшибкаПолученияПредложений) Тогда
				ТекстСообщения = ДанныеПредложенийБанков.ОшибкаПолученияПредложений;
				ЕстьОшибка = Истина;
			Иначе
				ТекстСообщения = 
					ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияНетПредложенийБанковДляОрганизации();
			КонецЕсли;
			
			НастроитьОтображениеСтраницыИнформации(Форма, ТекстСообщения, ЕстьОшибка, Ложь, ЕстьОшибка);
			Возврат;
			
		КонецЕсли;
		
		ДополнительныеПараметры.АдресХранилищаПредложенийБанков = 
			ДанныеПредложенийБанков.АдресХранилища;
		ПредложенияБанков = ДанныеПредложенийБанков.ПредложенияБанков;
		ПараметрыПредложений.Организация = Организация;
		
	Иначе
		
		ТекстСообщения = 
				ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияНеЗаполненаОрганизация();
		НастроитьОтображениеСтраницыИнформации(Форма, ТекстСообщения, Ложь);
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПараметрыИПредложенияПослеПолучения(Форма, ПараметрыПредложений, ПредложенияБанков);
	
КонецПроцедуры

#КонецОбласти