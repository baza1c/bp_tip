////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиСлужебныйКлиентСервер: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ТекстСлужебногоМаркераВЖР() Экспорт
	
	Возврат НСтр("ru = '#ДеталиОшибкиВыведены#'");
	
КонецФункции

// Возвращает дату, с которой Сбербанк отключил возможность подписания документов по sms.
// 
// Возвращаемое значение:
// Дата
//
Функция ДатаОтключенияПодписанияSMSСбербанк() Экспорт

	Возврат '2023-06-01';
	
КонецФункции

// Сравнивает версию формата с минимальной версией.
//
// Параметры:
//   ВерсияФормата - Строка - версия формата для сравнения
//   МинимальнаяВерсия - Строка - минимально допустимая версия
//
// Возвращаемое значение:
//   Булево - Истина, если версия формата больше или равна минимальной версии
//
Функция СравнитьВерсиюФормата(ВерсияФормата, МинимальнаяВерсия) Экспорт
	
	СоставВерсии = СтрРазделить(ВерсияФормата, ".");
	Если СоставВерсии.Количество() <> 3 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = ОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ВерсияФормата, МинимальнаяВерсия);
	
	Возврат Результат >= 0;
	
КонецФункции

// Проверяет, что переданное значение является одной из программ Сбербанка.
//
// Параметры:
//  ПрограммаБанка - ПеречислениеСсылка.ПрограммыБанка
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоПрограммаСбербанка(ПрограммаБанка) Экспорт
	
	Результат = ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн")
		Или ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.FintechIntegration")
		Или ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.SberAPI");
	
	Возврат Результат;
	
КонецФункции

#Область ОшибкиСетевойДоступности

// Определяет тип ошибки интернет соединения по представлению ошибки
// 
// Параметры:
//  ПредставлениеОшибки - Строка, Число - краткое описание ошибки, сформированное платформой,
//											или код состояния http ответа.
// 
// Возвращаемое значение: 
// Строка - тип ошибки интернет соединения.
//
Функция ТипОшибкиИнтернетСоединения(ПредставлениеОшибки)Экспорт

	ТипОшибки = "";
	
	Если ТипЗнч(ПредставлениеОшибки) = Тип("Строка") Тогда
		Если СтрНайти(ВРег(ПредставлениеОшибки), ВРег("Couldn't resolve host name")) > 0
			Или СтрНайти(ВРег(ПредставлениеОшибки), ВРег("ERR_SECURE_CONNECT_FAIL")) > 0 Тогда
			ТипОшибки = "НаличиеИнтернетСоединения";
		ИначеЕсли СтрНайти(ВРег(ПредставлениеОшибки), ВРег("Не могу установить соединение")) > 0
			Или СтрНайти(ВРег(ПредставлениеОшибки), ВРег("ERR_ACCESS_DENIED")) > 0
			Или СтрНайти(ВРег(ПредставлениеОшибки), ВРег("Unable to connect")) > 0 Тогда
			ТипОшибки = "ДоступностьСервисов";
		ИначеЕсли СтрНачинаетсяС(ПредставлениеОшибки, "500 ") > 0
			Или СтрНайти(ПредставлениеОшибки, "Код: 500") > 0 Тогда
			ТипОшибки = "АктивностьСервисов";
		ИначеЕсли СтрНайти(ВРег(ПредставлениеОшибки), ВРег("CERTIFICATE_ACCESS_EXCEPTION")) > 0
			Или СтрНайти(ВРег(ПредставлениеОшибки), ВРег("No required SSL certificate was sent")) > 0
			Или СтрНайти(ВРег(ПредставлениеОшибки), ВРег("The SSL certificate error")) > 0 Тогда
			ТипОшибки = "НаличиеСертификата";
		КонецЕсли;
	ИначеЕсли ТипЗнч(ПредставлениеОшибки) = Тип("Число") Тогда
		Если ПредставлениеОшибки >= 500 И ПредставлениеОшибки <= 599 Тогда
			ТипОшибки = "АктивностьСервисов";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТипОшибки;
	
КонецФункции

// Возвращает детали ошибки интернет соединения в виде структуры
// 
// Параметры:
//  ТипОшибки - Строка - тип ошибки, см. ТипОшибкиИнтернетСоединения.
// 
// Возвращаемое значение:
//	Структура - состоящая из полей:
// 		* ТипОшибки - Строка - тип ошибки, см. ТипОшибкиИнтернетСоединения,
//		* КраткоеПредставлениеТипаОшибки - Строка - краткое представление ошибки пользователю.
//		* РекомендацияПользователю - Строка - Рекомендация пользователю о необходимых действиях.
//		* РекомендацияАдминистратору - Строка - Рекомендация администратору или техническому специалисту 
//												о необходимых действиях. Заполняется только если ошибка находится 
//												в зоне ответственности Администратора.
Функция ДеталиОшибкиИнтернетСоединения(ТипОшибки)Экспорт
	
	Если ТипОшибки = "НаличиеИнтернетСоединения" Тогда
		Возврат ОшибкаИнтернетСоединения();
	ИначеЕсли ТипОшибки = "ДоступностьСервисов" Тогда
		Возврат ОшибкаДоступностьСервисов();
	ИначеЕсли ТипОшибки = "АктивностьСервисов" Тогда
		Возврат ОшибкаАктивностьСервисов();
	ИначеЕсли ТипОшибки = "НаличиеСертификата" Тогда
		Возврат ОшибкаСертификата();
	Иначе
		Возврат НеизвестнаяОшибкаСетевойДоступности();
	КонецЕсли;
		
КонецФункции

// Возвращает список кодов ошибок Сбербанка.
//
// Параметры:
//
// Возвращаемое значение:
//   Соответствие - информация об соответствии кодов и описания ошибок
//
Функция СписокКодовСбербанк() Экспорт
	
	СписокКодовСбербанк = Новый Соответствие;
	Описание = НСтр("ru = 'Неверный логин/пароль или учетная запись заблокирована.'");
	СписокКодовСбербанк.Вставить("AQ==", Новый Структура("Код, Описание", "01", Описание));
	Описание = НСтр("ru = 'Необходимо сменить пароль.'");
	СписокКодовСбербанк.Вставить("Ag==", Новый Структура("Код, Описание", "02", Описание));
	Описание = НСтр("ru = 'Срок действия сертификата истек.'");
	СписокКодовСбербанк.Вставить("Aw==", Новый Структура("Код, Описание", "03", Описание));
	Описание = НСтр("ru = 'Офис организации пользователя заблокирован.'");
	СписокКодовСбербанк.Вставить("BA==", Новый Структура("Код, Описание", "04", Описание));
	Описание = НСтр("ru = 'В аутентификации отказано ФРОД-мониторингом.'");
	СписокКодовСбербанк.Вставить("BQ==", Новый Структура("Код, Описание", "05", Описание));
	Описание = НСтр("ru = 'IP изменился.'");
	СписокКодовСбербанк.Вставить("Bg==", Новый Структура("Код, Описание", "06", Описание));
	Описание = НСтр("ru = 'Финансовый договор заблокирован.'");
	СписокКодовСбербанк.Вставить("Bw==", Новый Структура("Код, Описание", "07", Описание));
	Описание = НСтр("ru = 'Ошибка доступа к серверу.'");
	СписокКодовСбербанк.Вставить("CA==", Новый Структура("Код, Описание", "08", Описание));
	Описание = НСтр("ru = 'Не специфицированная ошибка.'");
	СписокКодовСбербанк.Вставить("CQ==", Новый Структура("Код, Описание", "09", Описание));
	Описание = НСтр("ru = 'Слишком частая ошибка входа в систему.'");
	СписокКодовСбербанк.Вставить("Cg==", Новый Структура("Код, Описание", "0A", Описание));
	Описание = НСтр("ru = 'Учетная запись отключена.'");
	СписокКодовСбербанк.Вставить("Cw==", Новый Структура("Код, Описание", "0B", Описание));
	Описание = НСтр("ru = 'Точка входа недоступна.'");
	СписокКодовСбербанк.Вставить("DA==", Новый Структура("Код, Описание", "0C", Описание));
	Описание = НСтр("ru = 'Ожидается заключение договора.'");
	СписокКодовСбербанк.Вставить("DQ==", Новый Структура("Код, Описание", "0D", Описание));
	Описание = НСтр("ru = 'Договор закрыт.'");
	СписокКодовСбербанк.Вставить("Dg==", Новый Структура("Код, Описание", "0E", Описание));
	Описание = НСтр("ru = 'Доступ закрыт настройками клиента.'");
	СписокКодовСбербанк.Вставить("Dw==", Новый Структура("Код, Описание", "0F", Описание));
	Описание = НСтр("ru = 'У пользователя в настройках отсутствует точка входа УПШ.'");
	СписокКодовСбербанк.Вставить("EA==", Новый Структура("Код, Описание", "10", Описание));
	Описание = НСтр("ru = 'У пользователя в настройках отсутствует точка входа УПШ_Холдинг.'");
	СписокКодовСбербанк.Вставить("EQ==", Новый Структура("Код, Описание", "11", Описание));
	Описание = НСтр("ru = 'Сертификат не найден в базе данных либо не привязан ни к одному пользователю.'");
	СписокКодовСбербанк.Вставить("Eg==", Новый Структура("Код, Описание", "12", Описание));
	Описание = НСтр("ru = 'Установленная сессия требует подтверждения кодом СМС.'");
	СписокКодовСбербанк.Вставить("Ew==", Новый Структура("Код, Описание", "13", Описание));
	Описание = НСтр("ru = 'Неверный код СМС.'");
	СписокКодовСбербанк.Вставить("FA==", Новый Структура("Код, Описание", "14", Описание));
	Описание = НСтр("ru = 'Срок действия кода СМС истек.'");
	СписокКодовСбербанк.Вставить("FQ==", Новый Структура("Код, Описание", "15", Описание));
	Описание = НСтр("ru = 'Неверные настройки СМС аутентификации.'");
	СписокКодовСбербанк.Вставить("Fg==", Новый Структура("Код, Описание", "16", Описание));
	Описание = НСтр("ru = 'Сессия не найдена.'");
	СписокКодовСбербанк.Вставить("Fw==", Новый Структура("Код, Описание", "17", Описание));
	Описание = НСтр("ru = 'Пользователь с указанными учетными данными должен использовать токен.'");
	СписокКодовСбербанк.Вставить("GA==", Новый Структура("Код, Описание", "18", Описание));
	Описание = НСтр("ru = 'Ошибка валидации параметров ФРОД-мониторинга при аутентификации.'");
	СписокКодовСбербанк.Вставить("GQ==", Новый Структура("Код, Описание", "19", Описание));
	Описание = НСтр("ru = 'Для организации недоступна точка входа ""Банк-Клиент"".'");
	СписокКодовСбербанк.Вставить("Gg==", Новый Структура("Код, Описание", "1А", Описание));
	
	Возврат СписокКодовСбербанк;
	
КонецФункции


#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОшибкаИнтернетСоединения()
	
	Результат = НоваяОшибкаСетевойДоступности();
	Результат.ТипОшибки                      = "НаличиеИнтернетСоединения";
	Результат.КраткоеПредставлениеТипаОшибки =
		НСтр("ru = 'Нет подключения к сети Интернет'");
	Результат.РекомендацияПользователю       =
		НСтр("ru = 'Проверьте подключение к сети Интернет, настройки прокси сервера либо обратитесь к администратору или техническому специалисту'");
	Результат.РекомендацияАдминистратору     =
		НСтр("ru = 'Проверьте качество Интернет соединения или настройки прокси сервера'");
	
	Возврат Результат;
	
КонецФункции

Функция ОшибкаДоступностьСервисов()
	
	Результат = НоваяОшибкаСетевойДоступности();
	Результат.ТипОшибки                      = "ДоступностьСервисов";
	Результат.КраткоеПредставлениеТипаОшибки = 
		НСтр("ru = 'Доступ к сервисам банков заблокирован'");
	Результат.РекомендацияПользователю       = 
		НСтр("ru = 'Проверьте сетевую доступность указанных адресов либо обратитесь к администратору или техническому специалисту'");
	Результат.РекомендацияАдминистратору     = 
		НСтр("ru = 'Необходимо проверить сетевую доступность следующих адресов:'");
	
	Возврат Результат;
	
КонецФункции

Функция ОшибкаАктивностьСервисов()
	
	Результат = НоваяОшибкаСетевойДоступности();
	Результат.ТипОшибки                      = "АктивностьСервисов";
	Результат.КраткоеПредставлениеТипаОшибки = 
		НСтр("ru = 'Сервисы банков временно не активны'");
	Результат.РекомендацияПользователю       = 
		НСтр("ru = 'Повторите отправку документов позднее или обратитесь в службу поддержки банка'");
	Результат.РекомендацияАдминистратору     = "";
	
	Возврат Результат;
	
КонецФункции

Функция ОшибкаСертификата()
	
	Результат = НоваяОшибкаСетевойДоступности();
	Результат.ТипОшибки                      = "НаличиеСертификата";
	Результат.КраткоеПредставлениеТипаОшибки =
		НСтр("ru = 'Недействительный TLS-сертификат для подключения к сервису банка'");
	Результат.РекомендацияПользователю       =
		НСтр("ru = 'Скачайте новый файл настроек в личном кабинете банка и переполучите настройку ДиректБанк'");
	
	Возврат Результат;
	
КонецФункции

Функция НеизвестнаяОшибкаСетевойДоступности()

	Результат = НоваяОшибкаСетевойДоступности();
	Результат.ТипОшибки                      = "НеизвестнаяОшибка";
	Результат.КраткоеПредставлениеТипаОшибки =
		НСтр("ru = 'Неизвестная ошибка'");
	Результат.РекомендацияПользователю       = "";
	Результат.РекомендацияАдминистратору     = "";
	
	Возврат Результат;
	
КонецФункции 

Функция НоваяОшибкаСетевойДоступности()
	
	Результат = Новый Структура;
	Результат.Вставить("ТипОшибки","");
	Результат.Вставить("КраткоеПредставлениеТипаОшибки","");
	Результат.Вставить("РекомендацияПользователю","");
	Результат.Вставить("РекомендацияАдминистратору","");
	
	Возврат Результат;
	
КонецФункции

// Возвращает шаблон структуры возврата Http запроса
//
Функция СтруктураВозвратаHttpЗапроса() Экспорт
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Статус", "");
	СтруктураВозврата.Вставить("Тело", Неопределено);
	СтруктураВозврата.Вставить("СообщениеОбОшибке", "");
	СтруктураВозврата.Вставить("КодСостояния", 0);
	СтруктураВозврата.Вставить("ТребуетсяПовторнаяАутентификация", Ложь);
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ТекстПредупрежденияМобильныйКлиентНедоступнаРабота(ПрограммаБанка, Банк = Неопределено) Экспорт
	
	Шаблон = НСтр("ru = 'Работа 1С:ДиректБанк для %1 возможна только в приложении для компьютера.'");
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") 
		ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.SberAPI") Тогда
		
		ТекстПредупреждения = СтрШаблон(Шаблон, НСтр("ru = 'Сбербанк'"));
		
	ИначеЕсли ЗначениеЗаполнено(Банк) Тогда
		
		ТекстПредупреждения = СтрШаблон(Шаблон, Строка(Банк));
		
	Иначе
		
		ТекстПредупреждения = СтрШаблон(Шаблон, НСтр("ru = 'вашего банка'"));
		
	КонецЕсли;
	
	Возврат ТекстПредупреждения;
	
КонецФункции

#Область Письма

// Возвращает форматированное наименование для переписки с банком
// 
// Параметры:
//		Наименование - Строка - наименование файла
//		Расширение - Строка - расширение файла
//
// Возвращаемое значение:
//		Строка
//
Функция ПредставлениеПрисоединенногоФайла(Наименование, Расширение) Экспорт 
	
	ПредставлениеРасширение = СтрЗаменить(Расширение,".","");   
	ПредставлениеНаименование = СтрШаблон("%1.%2",Наименование, ПредставлениеРасширение);   
	Возврат ПредставлениеНаименование;      
	
КонецФункции    

// Пересчитывает размер присоединенного файла из Байт в КБ
// Параметры:
//		Размер - Число - размер файла в Байтах
//
// Возвращаемое значение:
//		Число
//
Функция ПеревестиРазмерПрисоединенногоФайлаИзБайт_В_КБ(Размер) Экспорт 
	
	РазмерКБ = Размер/1024; 
	Возврат Окр(РазмерКБ);      
	
КонецФункции   

// Имя оповещения о новых письмах.
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяОповещенияОНовыхПисьмах() Экспорт
	Возврат "ЭлектронноеВзаимодействие.ОбменСбанками.ЕстьНовыеПисьма";
КонецФункции  

// Имя оповещения о непрочитанных письмах.
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяОповещенияОНепрочитанныхПисьмах() Экспорт
	Возврат "ЭлектронноеВзаимодействие.ОбменСбанками.ЕстьНепрочитанныеПисьма";
КонецФункции

// Имя оповещения о неотправленных письмах.
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяОповещенияОНеотправленныхПисьмах() Экспорт
	Возврат "ЭлектронноеВзаимодействие.ОбменСбанками.ЕстьНеотправленныеПисьма";
КонецФункции

#КонецОбласти

#Область ОжиданиеЗавершенияОбработкиДокументовВБанке

// Имя оповещения об отклоненных банком платежных документов.
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяОповещенияОбОтклоненныхДокументах() Экспорт
	Возврат "ЭлектронноеВзаимодействие.ОбменСБанками.ЕстьОтклоненныеДокументы";
КонецФункции

// Возвращает время в секундах, в пределах которого сессия обмена с банком поддерживается открытой.
// Сессия может поддерживается открытой в течение 70 минут.
// 
// Возвращаемое значение:
//  Число
//
Функция ДлительностьОткрытойСессииОбменаСБанком() Экспорт
	Возврат 4200;
КонецФункции

// Возвращает время жизни сессии обмена с банком по умолчанию.
//
// Возвращаемое значение:
//  Число
//
Функция ВремяЖизниСессииПоУмолчанию() Экспорт
	Возврат 300;
КонецФункции

// Дополняет свойство НастройкиОбменаСДаннымиСессии в приемнике значениями из
// свойства НастройкиОбменаСДаннымиСессии источника.
//
// Параметры:
//   Приемник - Структура - коллекция, в свойство НастройкиОбменаСДаннымиСессии
//                          которой будут добавляться новые значения.
//   Источник - Структура - коллекция, из свойства НастройкиОбменаСДаннымиСессии
//                          которой будут считываться пары Ключ и Значение для заполнения.
//
Процедура ДополнитьПараметрыДаннымиСессииОбменаСБанком(Приемник, Источник) Экспорт
	
	НастройкиОбменаСДаннымиСессии = Неопределено;
	ИмяСвойства = "НастройкиОбменаСДаннымиСессии";
	ТипСтруктура = Тип("Структура");
	
	Если Не ТипЗнч(Приемник) = ТипСтруктура
		Или Не ТипЗнч(Источник) = ТипСтруктура
		Или Не Источник.Свойство(ИмяСвойства, НастройкиОбменаСДаннымиСессии) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Приемник.Свойство(ИмяСвойства) Тогда
		Приемник.Вставить(ИмяСвойства, Новый Соответствие);
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Приемник.НастройкиОбменаСДаннымиСессии,
			НастройкиОбменаСДаннымиСессии, Истина);
	
КонецПроцедуры

// Формирует структуру данных сессии обмена с банками.
// 
// Возвращаемое значение:
//  Структура:
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками, Неопределено - ссылка на настройку обмена с банком.
//   * ИдентификаторСессии - Строка - SID открытой сессии с банком.
//   * УниверсальнаяДатаОбновленияСессии - Число - дата последней активности сессии в миллисекундах.
//   * ИмяЗадания - Строка - имя запущенного фонового задания проверки состояния.
//   * ВремяЖизниСессии - Число - время жизни сессии в секундах.
//   * КоличествоПолученныхПакетов - Число - количество полученных пакетов при очередном выполнении задания.
//   * ТолькоОбменДепозитами - Булево - если Истина, поддержка сессии доступна только для обмена депозитами.
//
Функция НовыеДанныеСессииОбменаСБанком() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкаОбмена", Неопределено);
	Результат.Вставить("ИдентификаторСессии", "");
	Результат.Вставить("УниверсальнаяДатаОбновленияСессии", 0);
	Результат.Вставить("ИмяЗадания", "");
	Результат.Вставить("ВремяЖизниСессии", 0);
	Результат.Вставить("КоличествоПолученныхПакетов", 0);
	Результат.Вставить("ТолькоОбменДепозитами", Ложь);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

