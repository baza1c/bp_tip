#Область ПрограммныйИнтерфейс

// Возвращает данные, необходимые для оформления статуса самозанятого в форме документа
//
// Параметры:
//  Форма- ФормаКлиентскогоПриложения - форма, основной реквизит которой содержит дату и дату входящего документа
//  ПлательщикНПД - Булево - признак того, что контрагент является самозанятым
//  ДатаРегистрации - Дата - дата в часовом поясе "Europe/Moscow",
//                    на которую контрагент встал на учет в качестве самозанятого, либо снялся с учета
//  ТекстОшибки - Строка - заполняется, если при получении статуса возникла ошибка
//
// Возвращаемое значение:
//  Структура - см. НовыйСтатусСамозанятого
//
Функция РезультатПроверкиСтатусаСамозанятогоДляДокумента(Форма, ПлательщикНПД, ДатаРегистрации = Неопределено, ТекстОшибки = "") Экспорт
	
	Объект = Форма.Объект;
	Результат = НовыйСтатусСамозанятого();
	Результат.ТекстОшибки = ТекстОшибки;
	Результат.ПлательщикНПД = ПлательщикНПД;
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Если ЗначениеЗаполнено(Объект.ДатаВходящегоДокумента) Тогда
			Результат.Описание = ОписаниеСтатусаСамозанятогоНаДатуВходящегоДокумента(
				ПлательщикНПД, ДатаРегистрации, Объект.ДатаВходящегоДокумента);
		Иначе
			Результат.Описание =
				ОписаниеСтатусаСамозанятогоНаДатуДокумента(ПлательщикНПД, ДатаРегистрации, Объект.Дата);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные, необходимые для оформления статуса самозанятого в форме договора
//
// Параметры:
//  ПлательщикНПД - Булево - признак того, что контрагент является самозанятым
//  ДатаПроверкиСтатуса - Дата - дата в часовом поясе "Europe/Moscow", на которую выполняется проверка статуса
//  ДатаРегистрации - Дата - дата в часовом поясе "Europe/Moscow",
//                    на которую контрагент встал на учет в качестве самозанятого, либо снялся с учета
//  ТекстОшибки - Строка - заполняется, если при получении статуса возникла ошибка
//
// Возвращаемое значение:
//  Структура - см. НовыйСтатусСамозанятого
//
Функция РезультатПроверкиСтатусаСамозанятогоДляДоговора(ПлательщикНПД, ДатаПроверкиСтатуса, ДатаРегистрации = Неопределено, ТекстОшибки = "") Экспорт
	
	Результат = НовыйСтатусСамозанятого();
	Результат.ТекстОшибки = ТекстОшибки;
	Результат.ПлательщикНПД = ПлательщикНПД;
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Результат.Описание = ОписаниеСтатусаСамозанятогоДляДоговора(ПлательщикНПД, ДатаРегистрации, ДатаПроверкиСтатуса);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные для оформления на форме статуса самозанятого в соответствии с результатом проверки статуса
//
// Параметры:
//  ПлательщикНПД - Булево - признак того, что контрагент является плательщиком НПД
//  ОписаниеСтатуса - Строка - описание статуса плательщика НПД,
//                    см. ИнтеграцияСтатусСамозанятогоКлиентСервер.ОписаниеСтатуса
//  ТекстОшибки - Строка - заполняется в случае ошибки соединения с сервисом проверки статуса самозанятого
//
// Возвращаемое значение:
//  Структура - см. НовыйДанныеДляОтображенияСтатусаСамозанятогоВФорме
//
Функция ДанныеДляОформленияСтатусаСамозанятогоНаФорме(ПлательщикНПД, ОписаниеСтатуса = "", ТекстОшибки = "") Экспорт
	
	Результат = НовыйДанныеДляОтображенияСтатусаСамозанятогоВФорме();
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ЦветФона = "ИнтеграцияСтатусСамозанятогоЦветФонаОшибкиОбращенияКСервису";
		ЗаголовокПодсказки =
				НСтр("ru = 'Не удалось выполнить проверку статуса самозанятого в сервисе по причине:
				|%1
				|
				|<a href = ""%2"">Повторить проверку</a>'");
		ПараметрЗаголовка = ТекстОшибки;
	ИначеЕсли ЗначениеЗаполнено(ОписаниеСтатуса) Тогда
		Если ПлательщикНПД Тогда
			ЦветФона = "ЦветФонаКорректногоЗначенияБП";
		Иначе
			ЦветФона = "ЦветФонаНекорректногоЗначенияБП";
		КонецЕсли;
		ЗаголовокПодсказки =
			НСтр("ru = '%1
				|
				|<a href = ""%2"">Повторить проверку</a>'");
		ПараметрЗаголовка = ОписаниеСтатуса;
	КонецЕсли;
	
	#Если Клиент Тогда
	Результат.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля(ЦветФона);
	Результат.ЗаголовокПодсказки = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		ЗаголовокПодсказки, ПараметрЗаголовка, ИмяСобытияВызовСервиса());
	#Иначе
	Результат.ЦветФона = ЦветаСтиля[ЦветФона];
	Результат.ЗаголовокПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
		ЗаголовокПодсказки, ПараметрЗаголовка, ИмяСобытияВызовСервиса());
	#КонецЕсли
	
	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

// Возвращает описание статуса самозанятого для отображения в форме договора
//
// Параметры:
//  ПлательщикНПД - Булево - признак наличия у контрагента статуса самозанятого
//  ДатаРегистрации - Дата - дата присвоения или утраты статуса самозанятого в часовом поясе "Europe/Moscow"
//  ДатаДоговора - Дата - дата заключения договора в часовом поясе "Europe/Moscow"
//
// Возвращаемое значение:
//  Строка
//
Функция ОписаниеСтатусаСамозанятогоДляДоговора(ПлательщикНПД, ДатаРегистрации, ДатаДоговора) Экспорт
	
	Если ПлательщикНПД Тогда
		Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Результат = СтрШаблон(
				НСтр("ru = 'На дату заключения договора %1 контрагент являлся самозанятым (по московскому времени)'"),
				Формат(ДатаДоговора, "ДЛФ=D"));
		Иначе
			Результат = СтрШаблон(
				НСтр("ru = 'На дату заключения договора %1 контрагент являлся самозанятым. Зарегистрирован %2 (по московскому времени).'"),
				Формат(ДатаДоговора, "ДЛФ=D"),
				Формат(ДатаРегистрации, "ДЛФ=D"));
		КонецЕсли;
	Иначе
		Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Результат = СтрШаблон(
				НСтр("ru = 'На дату заключения договора %1 контрагент не являлся самозанятым (по московскому времени)'"),
				Формат(ДатаДоговора, "ДЛФ=D"));
		Иначе
			Результат = СтрШаблон(
				НСтр("ru = 'На дату заключения договора %1 контрагент не являлся самозанятым. Снят с учета %2 (по московскому времени).'"),
				Формат(ДатаДоговора, "ДЛФ=D"),
				Формат(ДатаРегистрации, "ДЛФ=D"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание статуса самозанятого для отображения в форме документа
//
// Параметры:
//  ПлательщикНПД - Булево - признак наличия у контрагента статуса самозанятого
//  ДатаРегистрации - Дата - дата присвоения или утраты статуса самозанятого в часовом поясе "Europe/Moscow"
//  ДатаПроверки - Дата - дата входящего документа в часовом поясе "Europe/Moscow"
//
// Возвращаемое значение:
//  Строка
//
Функция ОписаниеСтатусаСамозанятогоНаДатуВходящегоДокумента(ПлательщикНПД, ДатаРегистрации, ДатаПроверки) Экспорт
	
	Если ПлательщикНПД Тогда
		Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Результат = СтрШаблон(
				НСтр("ru = 'На дату входящего документа %1 контрагент являлся самозанятым (по московскому времени)'"),
				Формат(ДатаПроверки, "ДЛФ=D"));
		Иначе
			Результат = СтрШаблон(
				НСтр("ru = 'На дату входящего документа %1 контрагент являлся самозанятым. Зарегистрирован %2 (по московскому времени).'"),
				Формат(ДатаПроверки, "ДЛФ=D"),
				Формат(ДатаРегистрации, "ДЛФ=D"));
		КонецЕсли;
	Иначе
		Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Результат = СтрШаблон(
				НСтр("ru = 'На дату входящего документа %1 контрагент не являлся самозанятым (по московскому времени).
				|Выберите договор с другой системой налогообложения.'"),
				Формат(ДатаПроверки, "ДЛФ=D"));
		Иначе
			Результат = СтрШаблон(
				НСтр("ru = 'На дату входящего документа %1 контрагент не являлся самозанятым. Снят с учета %2 (по московскому времени).
				|Выберите договор с другой системой налогообложения.'"),
				Формат(ДатаПроверки, "ДЛФ=D"),
				Формат(ДатаРегистрации, "ДЛФ=D"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание статуса самозанятого для отображения в форме документа
//
// Параметры:
//  ПлательщикНПД - Булево - признак наличия у контрагента статуса самозанятого
//  ДатаРегистрации - Дата - дата присвоения или утраты статуса самозанятого в часовом поясе "Europe/Moscow"
//  ДатаПроверки - Дата - дата документа в часовом поясе "Europe/Moscow"
//
// Возвращаемое значение:
//  Строка
//
Функция ОписаниеСтатусаСамозанятогоНаДатуДокумента(ПлательщикНПД, ДатаРегистрации, ДатаПроверки) Экспорт
	
	Если ПлательщикНПД Тогда
		Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Результат = СтрШаблон(
				НСтр("ru = 'На дату документа %1 контрагент являлся самозанятым (по московскому времени)'"),
				Формат(ДатаПроверки, "ДЛФ=D"));
		Иначе
			Результат = СтрШаблон(
				НСтр("ru = 'На дату документа %1 контрагент являлся самозанятым. Зарегистрирован %2 (по московскому времени).'"),
				Формат(ДатаПроверки, "ДЛФ=D"),
				Формат(ДатаРегистрации, "ДЛФ=D"));
		КонецЕсли;
	Иначе
		Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Результат = СтрШаблон(
					НСтр("ru = 'На дату документа %1 контрагент не являлся самозанятым (по московскому времени).
					|Выберите договор с другой системой налогообложения.'"),
					Формат(ДатаПроверки, "ДЛФ=D"));
		Иначе
			Результат = СтрШаблон(
				НСтр("ru = 'На дату документа %1 контрагент не являлся самозанятым. Снят с учета %2 (по московскому времени).
				|Выберите договор с другой системой налогообложения.'"),
				Формат(ДатаПроверки, "ДЛФ=D"),
				Формат(ДатаРегистрации, "ДЛФ=D"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ИмяСобытияВызовСервиса() Экспорт
	
	Возврат "ВызватьСервисПроверкиСтатусаСамозанятого";
	
КонецФункции

// Конструктор статуса самозанятого, используемого для оформления на форме связанных с ним элементов
//
// Возвращаемое значение:
//  Структура
//
Функция НовыйСтатусСамозанятого() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Описание", "");
	Результат.Вставить("КраткоеОписание", "");
	Результат.Вставить("ПлательщикНПД", Ложь);
	Результат.Вставить("ТекстОшибки", "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйДанныеДляОтображенияСтатусаСамозанятогоВФорме()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ЦветФона", Новый Цвет);
	Результат.Вставить("ЗаголовокПодсказки", Новый ФорматированнаяСтрока(""));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти