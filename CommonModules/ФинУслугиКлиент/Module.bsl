#Область СлужебныйПрограммныйИнтерфейс

Процедура ОжидатьЗавершениеОбновленияДанныхСервиса(Форма) Экспорт
	
	СведенияОДлительнойОперации = Форма.СведенияОДлительнойОперации;
	Если СведенияОДлительнойОперации.ДлительнаяОперация <> Неопределено
		И СведенияОДлительнойОперации.Имя = "ОбновитьДанныеСервиса" Тогда
		
		// В настоящее время еще выполняется фоновое задание по обновлению данных из сервиса,
		// дождемся его завершения.
		ТекстПоясненияОжидания = НСтр("ru = 'Поиск предложений банков'");
		
	Иначе
		
		// В модели сервиса фоновое задание не запущено.
		ОбновитьФормуПослеОбновленияДанныхСервиса(Форма, "");

		Возврат;
		
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);

	ОповещенияОЗавершении = Новый ОписаниеОповещения("ОбновлениеДанныхСервисаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		СведенияОДлительнойОперации.ДлительнаяОперация,
		ОповещенияОЗавершении,
		ПараметрыОжидания);

КонецПроцедуры

Процедура ОбновлениеДанныхСервисаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;	
	КонецЕсли;
	Форма = ДополнительныеПараметры.Форма;
	ТекстОшибки = "";
	Форма.СведенияОДлительнойОперации.ДлительнаяОперация = Неопределено; // Сбросим признак выполнения.
	
	ОписаниеОшибкиОбновленияДанныхСервиса = "";
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус", "") = "Ошибка" Тогда
		// Сохраним описание ошибки.
		ОписаниеОшибкиОбновленияДанныхСервиса = Результат.КраткоеПредставлениеОшибки;
		ТекстОшибки = 
			УниверсальныйОбменСБанкамиВызовСервера.ПодготовитьТекстОшибкиОбновленияДанныхСервиса(ОписаниеОшибкиОбновленияДанныхСервиса);
	Иначе
			
		РезультатОбновленияДанных = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОбновленияДанных.ИмяСобытия <> "ДанныеАктуальны" Тогда
			// Ошибка результата фонового задания. Сохраним и отобразим ее описание ошибки.
			ЕстьОшибки = Истина;
			
			ОписаниеОшибкиОбновленияДанныхСервиса = РезультатОбновленияДанных.ОписаниеСобытия;
			Если РезультатОбновленияДанных.ИмяСобытия = "ОшибкаАвторизацииПоЛогину" Тогда
				
				ШаблонСообщенияОбОшибке = НСтр("ru = 'Ошибка при авторизации по логину ИТС: %1
                                                |Проверьте правильность указания пользователя и пароля.'"); 
				ОшибкаАвторизацииПоЛогину = Истина;
				
			ИначеЕсли РезультатОбновленияДанных.ИмяСобытия = "ОшибкаПриПолученииТикета" Тогда
				
				ШаблонСообщенияОбОшибке = НСтр("ru = 'Ошибка прав доступа к ресурсу (тикета): %1
                                                |Обратитесь к админинстраторам ресурса.'");
				
			ИначеЕсли РезультатОбновленияДанных.ИмяСобытия = "ОшибкаПриПолученииДанных" Тогда
				
				ШаблонСообщенияОбОшибке = НСтр("ru = 'Ошибка при получении данных: %1
                                                |Попробуйте повторно запустить обновление данных сервиса.'");
				                              
			КонецЕсли;
			Если РезультатОбновленияДанных.ИмяСобытия = "Ошибка" Тогда
				ТекстОшибки = УниверсальныйОбменСБанкамиВызовСервера.ПодготовитьТекстОшибкиОбновленияДанныхСервиса(
					ОписаниеОшибкиОбновленияДанныхСервиса);
			Иначе
				ТекстОшибки = СтрШаблон(ШаблонСообщенияОбОшибке, ОписаниеОшибкиОбновленияДанныхСервиса);
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;

	ОбновитьФормуПослеОбновленияДанныхСервиса(Форма, ТекстОшибки);

КонецПроцедуры

Процедура ОбновитьОтображениеФинУслуги(Форма) Экспорт
	
	ОбновитьДоступныеПредложения(Форма);
	ОбновитьБаннеры(Форма);
	ОбновитьГруппуСервисыБанка(Форма);
	
КонецПроцедуры

Процедура ПослеВыбораИзМенюБаннер(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "Перейти" Тогда
		
		ИмяРеквизита = СтрШаблон("Баннер%1Идентификатор", ДополнительныеПараметры.НомерБаннера);
		ПерейтиПоНавигационнойСсылкеБаннера(Форма, ИмяРеквизита, 
			ДополнительныеПараметры.Организация, ДополнительныеПараметры.Банк);
			
	ИначеЕсли ВыбранныйЭлемент.Значение = "Скрыть" Тогда
		
		ИмяРеквизита = СтрШаблон("Баннер%1Идентификатор", ДополнительныеПараметры.НомерБаннера);
		Идентификатор = Форма[ИмяРеквизита];
		ПрямоеНаправление = Форма.ТекущаяПозицияБаннеры = 1;
		ФинУслугиВызовСервера.СкрытьБаннерПоИдентификатору(Идентификатор, Форма.ТекущаяПозицияБаннеры); 
		Форма.ТаблицаПредложенийБанков.Очистить(); 
		ОбновитьРотациюБаннеров(Форма, ПрямоеНаправление);

	КонецЕсли;
	
КонецПроцедуры

Процедура ПерейтиПоНавигационнойСсылкеБаннера(Форма, ИмяРеквизита, Организация, Банк) Экспорт
	
	ИдентификаторУслуги = Форма[ИмяРеквизита];
	Отбор = Новый Структура("ИдентификаторУслуги", ИдентификаторУслуги);
	МассивПредложений = Форма.ТаблицаПредложенийБанков.НайтиСтроки(Отбор);
	
	Если МассивПредложений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НавигационнаяСсылка = МассивПредложений[0].НавигационнаяСсылка; 
	
	Если НавигационнаяСсылка = "ДиректБанк" Тогда
		
		Если ФинУслугиВызовСервера.РолиДоступны() Тогда
			ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(Организация, Банк);
			НавигационнаяСсылка = "";
		Иначе
			НавигационнаяСсылка = Сайт1С(ФинУслугиКлиент.СервисДиректБанк());
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = "ФинОтчетность" Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Организация);  
		БанкУниверсальногоОбмена = ФинУслугиВызовСервера.БанкУниверсальногоОбменаПоБанку(Банк);
		ПараметрыФормы.Вставить("Банк", БанкУниверсальногоОбмена);
		
		ОткрытьФорму("РегистрСведений.ЖурналСтатусовФинОтчетностиВБанки.ФормаСписка", ПараметрыФормы);
				
		НавигационнаяСсылка = "";

	ИначеЕсли НавигационнаяСсылка = "СПАРКРиски" Тогда
		
		ОткрытьФорму("Документ.СправкиСПАРКРиски.Форма.ФормаСписка");
		НавигационнаяСсылка = "";

	ИначеЕсли НавигационнаяСсылка = "Контрагент" Тогда
		
		ОткрытьФорму("Отчет.ДосьеКонтрагента.Форма");
        НавигационнаяСсылка = "";

	ИначеЕсли НавигационнаяСсылка = "СБП" Тогда
		
		Если ФинУслугиВызовСервера.РолиДоступны() Тогда
			ПерейтиПодключитьСБП();
			НавигационнаяСсылка = "";
		Иначе 
			НавигационнаяСсылка = Сайт1С(ФинУслугиКлиент.СервисСБП());
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = "ОткрытиеСчета" Тогда
		
		Если ФинУслугиВызовСервера.РолиДоступны() Тогда

			ПараметрыОткрываемойФормы = Новый Структура;
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Организация", Организация);
			ПараметрыОткрываемойФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.ЗаявкаНаОткрытиеСчета.Форма.ФормаДокумента", ПараметрыОткрываемойФормы);
			НавигационнаяСсылка = "";

		Иначе
			
			НавигационнаяСсылка = Сайт1С(ФинУслугиКлиент.СервисОткрытиеСчета());
			
		КонецЕсли;;
		
	ИначеЕсли НавигационнаяСсылка = "Кредит" Тогда
		
		ПерейтиПоСсылкеКредитЛизинг(Ложь, Организация);
		НавигационнаяСсылка = "";
	
	ИначеЕсли НавигационнаяСсылка = "Лизинг" Тогда
		
		ПерейтиПоСсылкеКредитЛизинг(Истина, Форма.Организация);
		НавигационнаяСсылка = "";
		
	ИначеЕсли ЗначениеЗаполнено(МассивПредложений[0].НавигационнаяСсылкаСПараметрами) Тогда
		
		НавигационнаяСсылка = МассивПредложений[0].НавигационнаяСсылкаСПараметрами;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылка);
				
	КонецЕсли;
	
	ИмяФормы  = СтрЗаменить(Форма.ИмяФормы, ".Форма.ФормаЭлемента", "");
	ДополнитьТаблицуСтатистики(Форма.ТаблицаСтатистики, МассивПредложений[0], 0, 1, ИмяФормы);
		
КонецПроцедуры

Процедура ПерейтиПоСсылкеКредитЛизинг(Это1СЛизинг, Организация) Экспорт
			
	Если Это1СЛизинг Тогда
		СервисОбменаСБанками = ПредопределенноеЗначение("Перечисление.СервисыОбменаСБанками.ЗаявкиНаЛизинг");
		КлючеваяОперация = "СозданиеФормыЗаявкаНаЛизинг";
		Сайт = ФинУслугиКлиент.Сайт1С(ФинУслугиКлиент.СервисЛизинг());
	Иначе
		СервисОбменаСБанками = ПредопределенноеЗначение("Перечисление.СервисыОбменаСБанками.ЗаявкиНаКредит");
		КлючеваяОперация = "СозданиеФормыЗаявкаНаКредит"; 
		Сайт = ФинУслугиКлиент.Сайт1С(ФинУслугиКлиент.СервисКредит());
	КонецЕсли;
	
	Если Не ФинУслугиВызовСервера.РолиДоступны() Тогда
		ПерейтиПоНавигационнойСсылке(Сайт);
		Возврат;
	КонецЕсли;

	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
	ЗначенияЗаполнения = Новый Структура();
	ЗначенияЗаполнения.Вставить("СервисОбменаСБанками", СервисОбменаСБанками);
	ЗначенияЗаполнения.Вставить("Организация", Организация);

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ЗаявкаНаКредит.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

Процедура ПерейтиПодключитьСБП()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияСБП", ЭтотОбъект);
	ПараметрыПодключения = СистемаБыстрыхПлатежейКлиент.НовыйПараметрыПодключения(Неопределено, Неопределено);
	
	СистемаБыстрыхПлатежейКлиент.ПодключитьСистемуБыстрыхПлатежей(
		ПараметрыПодключения,
		ОписаниеОповещения);

КонецПроцедуры

Процедура ПослеПодключенияСБП(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ИзмененаНастройкаОбмена");
	ПоказатьОповещениеПользователя(НСтр("ru = 'Создано подключение к СБП'"));
	
	
КонецПроцедуры

Процедура ОбновитьРотациюБаннеров(Форма, НаправлениеВперед) Экспорт
	
	// После скрытия баннеров получим предложения
	Если Форма.ТаблицаПредложенийБанков.Количество() = 0 Тогда
		ПолучитьПредложенияБанков(Форма);
	КонецЕсли;
	
	ОбновитьБаннеры(Форма, НаправлениеВперед);

КонецПроцедуры

Процедура ПерейтиПоСсылке(Услуга, ТаблицаПредложенийБанков, ТаблицаСтатистики, ИмяФормы) Экспорт
	
	Отбор = Новый Структура(Услуга, Истина);
	СтрокиПредложений = ТаблицаПредложенийБанков.НайтиСтроки(Отбор);
	
	Сервис = СоответствиеСервисыУслуги().Получить(Услуга);

	Если СтрокиПредложений.Количество() > 0 Тогда
		
		Если ЗначениеЗаполнено(СтрокиПредложений[0].НавигационнаяСсылкаСПараметрами) Тогда
			Сайт = СтрокиПредложений[0].НавигационнаяСсылкаСПараметрами;
		Иначе	
			Сайт = СтрокиПредложений[0].НавигационнаяСсылка;
		КонецЕсли;
		ДополнитьТаблицуСтатистики(ТаблицаСтатистики, СтрокиПредложений[0], 0, 1, ИмяФормы);

	Иначе
		
		Сайт = ?(Сервис = СервисСБП(), Сайт1С(Сервис), СайтИТС(Сервис));
		
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке(Сайт);	

КонецПроцедуры

Функция Сайт1С(Сервис) Экспорт
	
	Возврат СтрШаблон("https://v8.1c.ru/tekhnologii/obmen-dannymi-i-integratsiya/realizovannye-resheniya/%1/", Сервис);
	
КонецФункции

Функция СайтИТС(Сервис) Экспорт
	
	Возврат СтрШаблон("https://its.1c.ru/db/%1/", Сервис);
	
КонецФункции

Функция СервисКредит() Экспорт
	
	Возврат "1s-kredit";
	
КонецФункции

Функция СервисЛизинг() Экспорт
	
	Возврат "1s-lizing";
	
КонецФункции

Функция СервисСБП() Экспорт
	
	Возврат "sistema-bystrykh-platezhey";
	
КонецФункции

Функция СервисОткрытиеСчета() Экспорт
	
	Возврат "servis-1s-otkrytie-scheta";
	
КонецФункции

Функция СервисДиректБанк() Экспорт
	
	Возврат "directbank-pryamoy-obmen-s-bankom";
	
КонецФункции

Функция СервисБизнесКарты() Экспорт
	
	Возврат "hoosn/content/754/1cbuh8-3";
	
КонецФункции

Функция СервисЭквайринг() Экспорт
	
	Возврат "contracts#content:28878:hdoc";
	
КонецФункции

Функция СервисЗарплатныйПроект() Экспорт
	
	Возврат "staff1c#content:34878:buh30:_top:зарплатный%20проект";
	
КонецФункции

Процедура ОжидатьЗавершениеОтправкиСтатистикиВСервис(Форма) Экспорт
	
	Если Форма.СведенияОДлительнойОперации.ДлительнаяОперация <> Неопределено
		И Форма.СведенияОДлительнойОперации.Имя = "ОтправитьСтатистикуВСервис" Тогда
		
		// В настоящее время еще выполняется фоновое задание по отправке статистики в сервис,
		// дождемся его завершения.
		ТекстПоясненияОжидания = НСтр("ru = 'Отправка статистики'");
		
	Иначе
		
		// Фоновое задание не запущено.
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СведенияОДлительнойОперации", Форма.СведенияОДлительнойОперации); 
	ДополнительныеПараметры.Вставить("АдресХранилищаОтправкаСтатистики", Форма.АдресХранилищаОтправкаСтатистики);
    ДополнительныеПараметры.Вставить("ТаблицаСтатистики", Форма.ТаблицаСтатистики);
    ДополнительныеПараметры.Вставить("ТокенАвторизации", Форма.ТокенАвторизации);

	ОповещенияОЗавершении = Новый ОписаниеОповещения("ОтправкаСтатистикиВСервисЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		Форма.СведенияОДлительнойОперации.ДлительнаяОперация,
		ОповещенияОЗавершении,
		ПараметрыОжидания);

КонецПроцедуры

Процедура ПодменюБаннер(Форма, Команда, Организация, Банк, РазрешениеЭкрана = "") Экспорт
	
	СписокВыбора = Новый СписокЗначений; 
	СписокВыбора.Добавить("Перейти",  НСтр("ru='Перейти'"));
	СписокВыбора.Добавить("Скрыть",  НСтр("ru='Скрыть'"));

	ИмяЭлемента = Форма.ТекущийЭлемент.Имя; 
	ИмяЭлементаБезПостфикса = СтрЗаменить(ИмяЭлемента, РазрешениеЭкрана, "");
	НомерБаннера = СтрЗаменить(ИмяЭлементаБезПостфикса, Команда.Имя, "");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("НомерБаннера", СтроковыеФункцииКлиентСервер.СтрокаВЧисло(НомерБаннера));
	ДополнительныеПараметры.Вставить("Форма", Форма);
    ДополнительныеПараметры.Вставить("Организация", Организация);
    ДополнительныеПараметры.Вставить("Банк", Банк);

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМенюБаннер", ЭтотОбъект, ДополнительныеПараметры); 
	Форма.ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Форма.Элементы[Форма.ТекущийЭлемент.Имя]);
	
КонецПроцедуры

Процедура ОтправкаСтатистикиВСервисЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;	
	КонецЕсли;
	СведенияОДлительнойОперации = ДополнительныеПараметры.СведенияОДлительнойОперации;
	СведенияОДлительнойОперации.ДлительнаяОперация = Неопределено; // Сбросим признак выполнения.
	
	ОписаниеОшибки = "";
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус", "") <> "Ошибка" Тогда
		
		Если Не ЭтоАдресВременногоХранилища(ДополнительныеПараметры.АдресХранилищаОтправкаСтатистики) Тогда
			Возврат;
		КонецЕсли;
		РезультатВыполнения = ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресХранилищаОтправкаСтатистики); 
		Если ТипЗнч(РезультатВыполнения) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		Если РезультатВыполнения.Выполнено Тогда
		
			ДополнительныеПараметры.ТокенАвторизации = РезультатВыполнения.Токен;
			ДополнительныеПараметры.ТаблицаСтатистики.Очистить();
			

		КонецЕсли;
		
		УдалитьИзВременногоХранилища(ДополнительныеПараметры.АдресХранилищаОтправкаСтатистики);
		ДополнительныеПараметры.АдресХранилищаОтправкаСтатистики = "";

	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьДоступныеПредложения(Форма) Экспорт
	
	Форма.ТекущаяПозицияБаннеры = 1;
	Если Форма.ТаблицаПредложенийБанков.Количество() = 0 Тогда
		ПолучитьПредложенияБанков(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьТаблицуСтатистики(ТаблицаСтатистики, ПредложениеБанка, Показы, Переходы, ИмяФормы) Экспорт 
	
	СтрокаСтатистики = ТаблицаСтатистики.Добавить(); 
	СтрокаСтатистики.Банк = ПредложениеБанка.НаименованиеБанка;
	СтрокаСтатистики.ИДРекламы = ПредложениеБанка.ИдентификаторРекламы;
	СтрокаСтатистики.ИДУслуги = ПредложениеБанка.ИдентификаторУслуги;
	СтрокаСтатистики.Показы = Показы;
	СтрокаСтатистики.Переходы = Переходы;
	СтрокаСтатистики.ИмяФормы = ИмяФормы;
	
КонецПроцедуры

Процедура УстановитьПараметрыБаннера(Форма, ПредложениеБанка, НомерБаннера, РазрешениеЭкрана = "") Экспорт
	
	Если ПредложениеБанка = Неопределено Тогда
		ДанныеКреатива = "";
		ИдентификаторУслуги = ""; 
		ВидимостьПодменю = Ложь; 
		ЦветБаннера = Новый Цвет();
	Иначе	
		ДанныеКреатива = ДанныеКреатива(Форма, ПредложениеБанка.ИмяФайлаКреатива, ПредложениеБанка.Банк);
		ИдентификаторУслуги = ПредложениеБанка.ИдентификаторУслуги;
		ВидимостьПодменю = Истина;
		ЦветБаннера = ЦветБаннера();
	КонецЕсли;
	ИмяРеквизита = СтрШаблон("Баннер%1", НомерБаннера);
	Форма[ИмяРеквизита] = ДанныеКреатива;
	ИмяРеквизита = СтрШаблон("Баннер%1Идентификатор", НомерБаннера);
	Форма[ИмяРеквизита] = ИдентификаторУслуги; 
	Наименование = СтрШаблон("ГруппаГоризонтальнаяБаннер%1%2", НомерБаннера, РазрешениеЭкрана);
	Форма.Элементы[Наименование].ЦветФона = ЦветБаннера;
	Форма.Элементы[Наименование].Видимость = Истина;
	Наименование = СтрШаблон("Баннер%1%2", НомерБаннера, РазрешениеЭкрана);
	Форма.Элементы[Наименование].Доступность = Истина;
	
	// Только для формы Финансы организации 
	Если РазрешениеЭкрана <> "" Тогда
		Наименование = СтрШаблон("ПодменюБаннер%1", НомерБаннера);
		УстановитьВидимостьЭлемента(Форма.Элементы, Наименование, ВидимостьПодменю, РазрешениеЭкрана);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьБаннеры(Форма, РазрешениеЭкрана = "") Экспорт
		
	Для НомерБаннера = 1 По МаксимальноеКоличествоБаннеров(Форма) Цикл
		
		УстановитьПараметрыБаннера(Форма, Неопределено, НомерБаннера, РазрешениеЭкрана) 
		
	КонецЦикла;
	
КонецПроцедуры

Функция НомерСтрокиПоПозиции(Позиция, ТекущаяПозиция) Экспорт
	
	СтрокПредыдущихРазделов = ТекущаяПозиция - 1; 
	НомерСтроки = Позиция - СтрокПредыдущихРазделов;
	
	Возврат НомерСтроки;
	
КонецФункции

#КонецОбласти 

#Область ПредложенияФинУслуг

Процедура ОбновитьФормуПослеОбновленияДанныхСервиса(Форма, ТекстОшибки)
	
	Если СтрНайти(Форма.ИмяФормы, "ФинансыОрганизации") <> 0 Тогда
		Форма.ОбновитьФормуПослеОбновленияДанныхСервиса(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.Объект.Банк) Тогда
		УстановитьВидимостьФинУслуги(Форма, Ложь);
		Возврат; 
	КонецЕсли;
	
	Форма.ТаблицаПредложенийБанков.Очистить();
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбновитьОтображениеФинУслуги(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПредложенияБанков(Форма)
	
	СведенияОрганизацияБанкДата = ОрганизацияБанкДатаПоВидуФормы(Форма);
	УникальныйИдентификатор = Форма.УникальныйИдентификатор;
	ОчиститьКреативыБанков(Форма.КреативыБанков);
	
	СведенияОПредложениях = ФинУслугиВызовСервера.ДанныеПредложенийИКреативов(
		СведенияОрганизацияБанкДата.Организация, СведенияОрганизацияБанкДата.Банк, 
		УникальныйИдентификатор, Форма.ИмяФормы, СведенияОрганизацияБанкДата.ДатаПолучения); 
			
	ЗаполнитьТаблицуФормыПоМассиву(СведенияОПредложениях.Предложения, Форма.ТаблицаПредложенийБанков);
	ЗаполнитьТаблицуФормыПоМассиву(СведенияОПредложениях.Креативы, Форма.КреативыБанков);

КонецПроцедуры

Процедура ОчиститьКреативыБанков(КреативыБанков)
	
	МассивАдресов = Новый Массив;
	
	Для Каждого Креатив Из КреативыБанков Цикл 
		МассивАдресов.Добавить(Креатив.АдресКреатива);
	КонецЦикла;
	
	ФинУслугиВызовСервера.ОчиститьАдресВоВременномХранилище(МассивАдресов);
	КреативыБанков.Очистить();
	
КонецПроцедуры

Функция ОрганизацияБанкДатаПоВидуФормы(Форма) 
	
	Результат = Новый Структура;
	Результат.Вставить("Организация");
	Результат.Вставить("Банк");
    Результат.Вставить("ДатаПолучения");

	Если СтрНайти(Форма.ИмяФормы, "ФинансыОрганизации") <> 0 Тогда
		Результат.Организация = Форма.Организация;
		Результат.Банк = Форма.Банк;
		Результат.ДатаПолучения = Форма.ДатаАктуальности;
	Иначе 
		Результат.Организация = Форма.Объект.Владелец;
		Результат.Банк = Форма.Объект.Банк;
		Результат.ДатаПолучения = ТекущаяДата();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТаблицуФормыПоМассиву(Сведения, ТаблицаФормы) 
		
	Для Каждого СтрокаСведений Из Сведения Цикл
		СтрокаТаблицы = ТаблицаФормы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаСведений);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьГруппуСервисыБанка(Форма)
	
	Элементы = Форма.Элементы;
	ТаблицаПредложенийБанков = Форма.ТаблицаПредложенийБанков; 
	Для Каждого СтрокаУслуг Из ТаблицаПредложенийБанков Цикл
		
		Если ЗначениеЗаполнено(СтрокаУслуг.КраткоеОписание) Тогда
			ЗаполнитьКраткоеОписание(Элементы, СтрокаУслуг);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаУслуг.ПодробноеОписание) Тогда
			ЗаполнитьПодробноеОписание(Элементы, СтрокаУслуг, Форма.ЦветГиперссылки);
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКраткоеОписание(Элементы, СтрокаУслуг)
	
	Если СтрокаУслуг.УслугаСБП Тогда
		Элементы.КраткоеОписаниеСБП.Заголовок = СтрокаУслуг.КраткоеОписание;
	ИначеЕсли СтрокаУслуг.УслугаЭквайринг Тогда
		Элементы.КраткоеОписаниеЭквайринг.Заголовок = СтрокаУслуг.КраткоеОписание;
	ИначеЕсли СтрокаУслуг.УслугаБизнесКарты Тогда
		Элементы.КраткоеОписаниеБизнесКарты.Заголовок = СтрокаУслуг.КраткоеОписание;
	ИначеЕсли СтрокаУслуг.УслугаЗарплатныйПроект Тогда
		Элементы.КраткоеОписаниеЗарплатныйПроект.Заголовок = СтрокаУслуг.КраткоеОписание;
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаполнитьПодробноеОписание(Элементы, СтрокаУслуг, ЦветГиперссылки)
	
	Если СтрокаУслуг.УслугаСБП Тогда	
		Элементы.ОписаниеСБП.Заголовок = ОписаниеУслуги(
			СтрокаУслуг.ПодробноеОписание, ЦветГиперссылки, "УслугаСБП");	
	ИначеЕсли СтрокаУслуг.УслугаЭквайринг Тогда
		Элементы.ОписаниеЭквайринг.Заголовок = ОписаниеУслуги(
			СтрокаУслуг.ПодробноеОписание, ЦветГиперссылки, "УслугаЭквайринг");
	ИначеЕсли СтрокаУслуг.УслугаБизнесКарты Тогда
		Элементы.ОписаниеБизнесКарты.Заголовок = ОписаниеУслуги(
			СтрокаУслуг.ПодробноеОписание, ЦветГиперссылки, "УслугаБизнесКарты");
	ИначеЕсли СтрокаУслуг.УслугаЗарплатныйПроект Тогда
		Элементы.ОписаниеЗарплатныйПроект.Заголовок = ОписаниеУслуги(
			СтрокаУслуг.ПодробноеОписание, ЦветГиперссылки, "УслугаЗарплатныйПроект");
	КонецЕсли;
		
КонецПроцедуры

Функция ОписаниеУслуги(Описание, ЦветГиперссылки, НаименованиеУслуги)
	
	СоставСтрокиОписание = Новый Массив;	
	ТекстГиперссылки = НСтр("ru = 'Подробнее'");

	ТекстОписание = Новый ФорматированнаяСтрока(Описание + " ");
	СоставСтрокиОписание.Добавить(ТекстОписание);
	Подробнее = Новый ФорматированнаяСтрока(
		ТекстГиперссылки,, ЦветГиперссылки,, НаименованиеУслуги);
	СоставСтрокиОписание.Добавить(Подробнее);
    ОписаниеСГиперссылкой = Новый ФорматированнаяСтрока(СоставСтрокиОписание);
	
	Возврат ОписаниеСГиперссылкой;
	
КонецФункции

Процедура ОбновитьБаннеры(Форма, НаправлениеВперед = Неопределено)
	
	Если СтрНайти(Форма.ИмяФормы, "ФинансыОрганизации") <> 0 Тогда
		Форма.ОбновитьБаннеры();
		Возврат;
	КонецЕсли;

	ОчиститьБаннеры(Форма);
	ТекущаяПозицияБаннеры = Форма.ТекущаяПозицияБаннеры;
	КоличествоПредложений = Форма.ТаблицаПредложенийБанков.Количество();
	ЕстьПредложенияБанков = КоличествоПредложений <> 0;
	УстановитьВидимостьФинУслуги(Форма, ЕстьПредложенияБанков);
	
	Если Не ЕстьПредложенияБанков Тогда
		Возврат;
	КонецЕсли;
	
	СмещениеПозиции = Форма.КоличествоБаннеров - 1;
	ПоследняяПозиция = ТекущаяПозицияБаннеры + СмещениеПозиции;
	ПоследняяПозицияВИтерации = Мин(ПоследняяПозиция, КоличествоПредложений);
	
	Для Итератор = ТекущаяПозицияБаннеры По ПоследняяПозицияВИтерации Цикл
		
		НомерСтроки = НомерСтрокиПоПозиции(Итератор, ТекущаяПозицияБаннеры);		
		ПредложениеБанка = Форма.ТаблицаПредложенийБанков.Получить(Итератор - 1);
		УстановитьПараметрыБаннера(Форма, ПредложениеБанка, НомерСтроки);
		Если НаправлениеВперед = Неопределено 
			Или (НаправлениеВперед = Истина И Итератор = ПоследняяПозицияВИтерации)
			Или (НаправлениеВперед = Ложь И Итератор = ТекущаяПозицияБаннеры) Тогда
			
            ИмяФормы  = СтрЗаменить(Форма.ИмяФормы, ".Форма.ФормаЭлемента", "");
			ДополнитьТаблицуСтатистики(Форма.ТаблицаСтатистики, ПредложениеБанка, 1, 0, ИмяФормы); 
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьВидимостьПереключателяБаннеров(Форма, ТекущаяПозицияБаннеры, ПоследняяПозиция, КоличествоПредложений);

КонецПроцедуры

Процедура УстановитьВидимостьФинУслуги(Форма, ЕстьПредложенияБанков)
	
	Форма.Элементы.ГруппаВиджеты.Видимость = ЕстьПредложенияБанков;
    Форма.Элементы.ГруппаСервисыБанка.Видимость = ЕстьПредложенияБанков;
   
КонецПроцедуры

Процедура УстановитьВидимостьПереключателяБаннеров(
		Форма, ТекущаяПозиция, ПоследняяПозиция, КоличествоСтрокТаблицы, РазрешениеЭкрана = "") Экспорт
		
	Элементы = Форма.Элементы;
	ЕстьСледующиеСтраницы = ПоследняяПозиция < КоличествоСтрокТаблицы;
	УстановитьВидимостьЭлемента(Элементы, "ПереключитьСтраницуБаннеров", ЕстьСледующиеСтраницы, РазрешениеЭкрана);
	УстановитьВидимостьЭлемента(Элементы, "ПереключитьНедоступный", Не ЕстьСледующиеСтраницы, РазрешениеЭкрана);

	ЕстьПредыдущиеСтраницы = ТекущаяПозиция > 1;
	УстановитьВидимостьЭлемента(Элементы, "ПереключитьОбратноСтраницуБаннеров", ЕстьПредыдущиеСтраницы, РазрешениеЭкрана);
	УстановитьВидимостьЭлемента(Элементы, "ПереключитьОбратноНедоступный", Не ЕстьПредыдущиеСтраницы, РазрешениеЭкрана);
	
	ОтображатьГруппу = ЕстьСледующиеСтраницы Или ЕстьПредыдущиеСтраницы; 
	УстановитьВидимостьЭлемента(Элементы, "ГруппаПереключитьСтраницуБаннеров", ОтображатьГруппу, РазрешениеЭкрана);

КонецПроцедуры

Процедура УстановитьВидимостьЭлемента(Элементы, Раздел, Видимость, РазрешениеЭкрана)
	
	ИмяЭлемента = СтрШаблон("%1%2", Раздел, РазрешениеЭкрана);
	Элементы[ИмяЭлемента].Видимость = Видимость;
		
КонецПроцедуры

Функция ЦветБаннера() 
	
	Возврат Новый Цвет(248, 248, 248);
	
КонецФункции

Функция ДанныеКреатива(Форма, ИмяФайлаКреатива, Банк)
	
	Отбор = Новый Структура("Банк, ИмяКреатива", Банк, ИмяФайлаКреатива); 
	МассивСтрок = Форма.КреативыБанков.НайтиСтроки(Отбор);
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат МассивСтрок[0].АдресКреатива;
	
КонецФункции

Функция МаксимальноеКоличествоБаннеров(Форма)
	
	Если СтрНайти(Форма.ИмяФормы, "ФинансыОрганизации") <> 0 Тогда
    	Возврат 4;
    Иначе
		Возврат 1;
	КонецЕсли;
	
КонецФункции

Функция СоответствиеСервисыУслуги()
	
	Результат = Новый Соответствие;
	Результат.Вставить("УслугаСБП", СервисСБП());
	Результат.Вставить("УслугаЭквайринг", СервисЭквайринг());
	Результат.Вставить("УслугаБизнесКарты", СервисБизнесКарты());
    Результат.Вставить("УслугаЗарплатныйПроект", СервисЗарплатныйПроект());

	Возврат Результат;

КонецФункции

#КонецОбласти
