////////////////////////////////////////////////////////////////////////////////
// УчетЗарплаты: методы, используемые:
//               - при настройке зарплаты и кадров;
//               - при отражении зарплаты и налогов/взносов с ФОТ в учете;
//               - при получении сведений о документах подсистем учета зарплаты и кадров;
//               - ...
////////////////////////////////////////////////////////////////////////////////

#Область ИнтерфейсВзаимодейтвияБЗКБ

////////////////////////////////////////////////////////////////////////////////
// ИНТЕРФЕЙС ВЗАИМОДЕЙСТВИЯ С БЗКБ
//
// Предназначена для генерации всех движений, связанных с отражением в регл. учете
//
// Параметры:
//	Движения
//	Отказ
//	Организация
//	ПериодРегистрации
//	ДанныеДляОтражения - таблицы движений
//
Процедура СформироватьДвиженияПоОтражениюЗарплатыВРегламентированномУчете(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляОтражения) Экспорт
		
	// Начисление зарплаты и налогов с ФОТ
	ДанныеПоЗарплате = Новый Структура;
	Если ДанныеДляОтражения.Свойство("НачисленнаяЗарплатаИВзносы") Тогда
		Если ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы.Колонки.Найти("ВзносыПоЕдиномуТарифу") = Неопределено Тогда
			ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы.Колонки.Добавить("ВзносыПоЕдиномуТарифу", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		КонецЕсли;
		ДанныеПоЗарплате.Вставить("НачисленнаяЗарплатаИВзносы", ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы);
		Если ДанныеДляОтражения.Свойство("УдержаннаяЗарплата") Тогда
			ДанныеПоЗарплате.Вставить("УдержаннаяЗарплата", ДанныеДляОтражения.УдержаннаяЗарплата);
		КонецЕсли;
	КонецЕсли;
	Если ДанныеДляОтражения.Свойство("НачисленныйНДФЛ") Тогда
		ДанныеПоЗарплате.Вставить("НачисленныйНДФЛ", ДанныеДляОтражения.НачисленныйНДФЛ);
	КонецЕсли;
	Если ДанныеПоЗарплате.Количество() <> 0 Тогда
		СформироватьДвиженияНачислениеЗарплаты(Движения, Отказ, Организация, ПериодРегистрации, ДанныеПоЗарплате);
	КонецЕсли;
			
	Если ДанныеДляОтражения.Свойство("Депоненты") Тогда
		ДанныеПоДепонентам = ДанныеДляОтражения.Депоненты;
		Если ДанныеПоДепонентам.Количество() <> 0 Тогда
			СформироватьДвиженияДепонированиеЗарплаты(Движения, Отказ, Организация, ПериодРегистрации, ДанныеПоДепонентам);
		КонецЕсли;
	КонецЕсли;
		
	Если ДанныеДляОтражения.Свойство("УдержаннаяЗарплата") Тогда
		ДанныеПоУдержаниям = ДанныеДляОтражения.УдержаннаяЗарплата;
		Если ДанныеДляОтражения.Свойство("НачисленнаяЗарплатаИВзносы") Тогда
			НачисленнаяЗарплатаИВзносы = ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы;
		Иначе
			НачисленнаяЗарплатаИВзносы = Неопределено;
		КонецЕсли;
		Если ДанныеПоУдержаниям.Колонки.Найти("ДокументОснование") = Неопределено Тогда
			ДанныеПоУдержаниям.Колонки.Добавить("ДокументОснование", Новый ОписаниеТипов("ДокументСсылка.ИсполнительныйЛист"));
		КонецЕсли;
		Если ДанныеПоУдержаниям.Количество() <> 0 Тогда
			СформироватьДвиженияУдержания(Движения, Отказ, Организация, ПериодРегистрации, ДанныеПоУдержаниям, НачисленнаяЗарплатаИВзносы);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеДляОтражения.Свойство("ОценочныеОбязательства") Тогда
		ДанныеПоРезервам = ДанныеДляОтражения.ОценочныеОбязательства;
		Если ДанныеПоРезервам.Количество() <> 0 Тогда
			СформироватьДвиженияНачислениеРезервов(Движения, Отказ, Организация, ПериодРегистрации, ДанныеПоРезервам);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Начисление

Процедура СформироватьДвиженияНачислениеЗарплаты(Движения, Отказ, Организация, Знач ПериодРегистрации, ДанныеДляОтражения)
	
	Дополнять         = Ложь;
	Реквизиты         = Неопределено;
	Регистратор       = Движения.Хозрасчетный.Отбор.Регистратор.Значение;
	ПериодРегистрации = ПолучитьПериодРегистрации(Регистратор, ПериодРегистрации);
	
	Если ДанныеДляОтражения.Свойство("НачисленнаяЗарплатаИВзносы") Тогда
		Параметры = ПодготовитьПараметрыПоОтражениюЗарплатыВУчете(ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы, Организация, ПериодРегистрации, Регистратор);
		Реквизиты = Параметры.Реквизиты[0];
		ТаблицаЗаработнойПлаты = Параметры.ТаблицаЗаработнойПлаты;
		Если ТаблицаЗаработнойПлаты.Колонки.Найти("РегистрацияВНалоговомОргане") = Неопределено Тогда
			Массив = Новый Массив;
			Массив.Добавить(Тип("СправочникСсылка.РегистрацииВНалоговомОргане"));
			ТаблицаЗаработнойПлаты.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов(Массив, , ));
		КонецЕсли;
		РазвернутьТаблицуЗаработнойПлаты(ТаблицаЗаработнойПлаты);
		Дополнять = Истина;
	КонецЕсли;
	
	Если ДанныеДляОтражения.Свойство("НачисленныйНДФЛ") Тогда
		ПараметрыНДФЛ = ПодготовитьПараметрыПоОтражениюНДФЛВУчете(ДанныеДляОтражения.НачисленныйНДФЛ, Организация, ПериодРегистрации, Регистратор);
		ТаблицаНДФЛ = ПараметрыНДФЛ.ТаблицаНДФЛ;
		Если Реквизиты = Неопределено Тогда
			Реквизиты = ПараметрыНДФЛ.Реквизиты[0];
		КонецЕсли;
		
		ПроверитьЗаполненностьРегистрацииВНалоговомОргане(ТаблицаНДФЛ, Реквизиты, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ТаблицаНДФЛ = ДополнитьТаблицуНДФЛАналитикойПоРегистрации(ТаблицаНДФЛ, Организация);
		Если Дополнять Тогда
			ТаблицаЗаработнойПлаты = ДополнитьТаблицуЗаработнойПлатыДаннымиПоНДФЛ(ТаблицаЗаработнойПлаты, ТаблицаНДФЛ, Организация, ПериодРегистрации, Регистратор);
		Иначе
			ТаблицаЗаработнойПлаты = ТаблицаНДФЛ;
			ТаблицаЗаработнойПлаты.Колонки.Добавить("ВидОперацииРасходовЗаСчетФСС",Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
			Дополнять = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ОчиститьТаблицуЗаработнойПлаты(ТаблицаЗаработнойПлаты);
	
	СинхронизироватьПодразделенияИРегистрацииВНалоговомОргане(ТаблицаЗаработнойПлаты, Реквизиты);
	
	ТаблицаЗаработнойПлаты = ДополнитьТаблицуЗарплатыАналитикойБухУчета(ТаблицаЗаработнойПлаты);
	
	РаспределитьЗарплатуПоВидамДеятельности(ТаблицаЗаработнойПлаты, Организация, ПериодРегистрации);
	
	ПроверитьЗаполненностьСпособаОтраженияВУчете(ТаблицаЗаработнойПлаты, Реквизиты);
	
	СформироватьПроводкиПоОтражениюЗарплатыВУчете(ТаблицаЗаработнойПлаты, Реквизиты, Движения, Отказ);
	
	// Проведение по подсистеме УСН
	УдержаннаяЗарплата = Неопределено;
	ДанныеДляОтражения.Свойство("УдержаннаяЗарплата", УдержаннаяЗарплата);
	
	СформироватьДвиженияПоРегистрамУСННачислениеЗарплаты(Параметры, ТаблицаЗаработнойПлаты, УдержаннаяЗарплата, Реквизиты, Движения, Отказ);
	
	// Учет доходов и расходов ИП
	СформироватьДвиженияПоРегистрамИПНачислениеЗарплаты(Параметры, ТаблицаЗаработнойПлаты, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Процедура СформироватьДвиженияДепонированиеЗарплаты(Движения, Отказ, Организация, Знач ПериодРегистрации, Депоненты)
	
	Регистратор            = Движения.Хозрасчетный.Отбор.Регистратор.Значение;
	ПериодРегистрации      = ПолучитьПериодРегистрации(Регистратор, ПериодРегистрации);
	Параметры              = ПодготовитьПараметрыПоОтражениюДепонентовУчете(Депоненты, Организация, ПериодРегистрации, Регистратор);
	Реквизиты              = Параметры.Реквизиты[0];
	ТаблицаЗаработнойПлаты = Параметры.ТаблицаДепонирование;
	Дополнять = Истина;
	
	ТаблицаЗаработнойПлаты = ДополнитьТаблицуЗарплатыАналитикойБухУчета(ТаблицаЗаработнойПлаты);
	
	РаспределитьЗарплатуПоВидамДеятельности(ТаблицаЗаработнойПлаты, Организация, ПериодРегистрации);
	
	ПроверитьЗаполненностьСпособаОтраженияВУчете(ТаблицаЗаработнойПлаты, Реквизиты);
	
	СформироватьПроводкиПоОтражениюЗарплатыВУчете(ТаблицаЗаработнойПлаты, Реквизиты, Движения, Отказ);
	
	// Проведение по подсистеме УСН
	СформироватьДвиженияПоРегистрамУСННачислениеЗарплаты(Параметры, ТаблицаЗаработнойПлаты, , Реквизиты, Движения, Отказ);
	
	// Учет доходов и расходов ИП
	СформироватьДвиженияПоРегистрамИПНачислениеЗарплаты(Параметры, ТаблицаЗаработнойПлаты, Реквизиты, Движения, Отказ);
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ДепонированиеЗарплаты") Тогда;
		СформироватьДвиженияПоУдержанномуНДФЛПриВыплатеДохода(Движения, Параметры, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура РазвернутьТаблицуЗаработнойПлаты(ТаблицаЗаработнойПлаты)
	
	ВидыОпераций = Новый Соответствие;
	ВидыОпераций.Вставить("ПФРНакопительная",
		Перечисления.ВидыОперацийПоЗарплате.ПФРНакопительнаяЧасть);
	ВидыОпераций.Вставить("ПФРСтраховая",
		Перечисления.ВидыОперацийПоЗарплате.ПФРСтраховаяЧасть);
	ВидыОпераций.Вставить("ПФРПоСуммарномуТарифу",
		Перечисления.ВидыОперацийПоЗарплате.ПФРПоСуммарномуТарифу);
	ВидыОпераций.Вставить("ПФРДоПредельнойВеличины",
		Перечисления.ВидыОперацийПоЗарплате.ПФРДоПредельнойВеличины);
	ВидыОпераций.Вставить("ПФРСПревышения",
		Перечисления.ВидыОперацийПоЗарплате.ПФРСПревышения);
	ВидыОпераций.Вставить("ПФРНаДоплатуЛетчикам",
		Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифЛЭ);
	ВидыОпераций.Вставить("ПФРНаДоплатуШахтерам",
		Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифШахтеры);
	ВидыОпераций.Вставить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботах",
		Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах);
	ВидыОпераций.Вставить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах",
		Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах);
	ВидыОпераций.Вставить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки",
		Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки);
	ВидыОпераций.Вставить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка",
		Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка);
	ВидыОпераций.Вставить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки",
		Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки);
	ВидыОпераций.Вставить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка",
		Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка);
	ВидыОпераций.Вставить("ФСС",
		Перечисления.ВидыОперацийПоЗарплате.ФСС);
	ВидыОпераций.Вставить("ФССНесчастныеСлучаи",
		Перечисления.ВидыОперацийПоЗарплате.ФССНС);
	ВидыОпераций.Вставить("ФФОМС",
		Перечисления.ВидыОперацийПоЗарплате.ФФОМС);
	ВидыОпераций.Вставить("ТФОМС",
		Перечисления.ВидыОперацийПоЗарплате.ТФОМС);
	ВидыОпераций.Вставить("ВзносыПоЕдиномуТарифу",
		Перечисления.ВидыОперацийПоЗарплате.ВзносыПоЕдиномуТарифу);
	ВидыОпераций.Вставить("ОПСпоМеждународнымДоговорам",
		Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорам);
	ВидыОпераций.Вставить("ОПСпоМеждународнымДоговорамСПревышения",
		Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорамСПревышения);
	ВидыОпераций.Вставить("ОМСпоМеждународнымДоговорам",
		Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорам);
	ВидыОпераций.Вставить("ОМСпоМеждународнымДоговорамСПревышения",
		Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорамСПревышения);
	ВидыОпераций.Вставить("ОССпоМеждународнымДоговорам",
		Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорам);
	ВидыОпераций.Вставить("ОССпоМеждународнымДоговорамСПревышения",
		Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорамСПревышения);
		
	МассивОперацийРасходовЗаСчетФСС = Новый Массив;
	МассивОперацийРасходовЗаСчетФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("ПФРНакопительная");
	МассивКолонок.Добавить("ПФРСтраховая");
	МассивКолонок.Добавить("ПФРПоСуммарномуТарифу");
	МассивКолонок.Добавить("ПФРДоПредельнойВеличины");
	МассивКолонок.Добавить("ПФРСПревышения");
	МассивКолонок.Добавить("ПФРНаДоплатуЛетчикам");
	МассивКолонок.Добавить("ПФРНаДоплатуШахтерам");
	МассивКолонок.Добавить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботах");
	МассивКолонок.Добавить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах");
	МассивКолонок.Добавить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки");
	МассивКолонок.Добавить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка");
	МассивКолонок.Добавить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки");
	МассивКолонок.Добавить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка");
	МассивКолонок.Добавить("ФСС");
	МассивКолонок.Добавить("ФССНесчастныеСлучаи");
	МассивКолонок.Добавить("ФФОМС");
	МассивКолонок.Добавить("ТФОМС");
	МассивКолонок.Добавить("ВзносыПоЕдиномуТарифу");
	МассивКолонок.Добавить("ОПСпоМеждународнымДоговорам");
	МассивКолонок.Добавить("ОПСпоМеждународнымДоговорамСПревышения");
	МассивКолонок.Добавить("ОМСпоМеждународнымДоговорам");
	МассивКолонок.Добавить("ОМСпоМеждународнымДоговорамСПревышения");
	МассивКолонок.Добавить("ОССпоМеждународнымДоговорам");
	МассивКолонок.Добавить("ОССпоМеждународнымДоговорамСПревышения");
	
	КоличествоИндексовМассива = МассивКолонок.Количество() - 1;
	
	ТаблицаЗаработнойПлаты.Колонки.Добавить("ВидОперацииРасходовЗаСчетФСС",Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	ТаблицаЗаработнойПлаты.Колонки.Добавить("ВедущийВидОперации",Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	
	ТаблицаНалоговСФОТ = ТаблицаЗаработнойПлаты.СкопироватьКолонки();

	Для Каждого СтрокаТаблицы ИЗ ТаблицаЗаработнойПлаты Цикл
		
		СтрокаТаблицы.ВедущийВидОперации = СтрокаТаблицы.ВидОперации;
		
		Для ИндексМассива = 0 По КоличествоИндексовМассива Цикл
			
			Если СтрокаТаблицы[МассивКолонок[ИндексМассива]] <> 0 Тогда
				НоваяСтрока = ТаблицаНалоговСФОТ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.Сумма = СтрокаТаблицы[МассивКолонок[ИндексМассива]];
				Если МассивОперацийРасходовЗаСчетФСС.Найти(НоваяСтрока.ВидОперации) <> Неопределено Тогда
					НоваяСтрока.ВидОперацииРасходовЗаСчетФСС = НоваяСтрока.ВидОперации;
				КонецЕсли;
				НоваяСтрока.ВидОперации = ВидыОпераций.Получить(МассивКолонок[ИндексМассива]);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;

	Для Каждого СтрокаТаблицы ИЗ ТаблицаНалоговСФОТ Цикл
		
		НоваяСтрока = ТаблицаЗаработнойПлаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
	КонецЦикла;
	
	ТаблицаЗаработнойПлаты.Свернуть("ФизическоеЛицо, ВидОперации, Подразделение,
									|СпособОтраженияЗарплатыВБухучете, СтатьяРасходов, ОблагаетсяЕНВД,
									|ВидНачисленияОплатыТрудаДляНУ,
									|РегистрацияВНалоговомОргане,
									|ВидОперацииРасходовЗаСчетФСС,
									|ВедущийВидОперации,
									|Резерв, РезервБУ, РезервНУ", "Сумма");
	
КонецПроцедуры

Процедура ОчиститьТаблицуЗаработнойПлаты(ТаблицаЗаработнойПлаты)
	
	Отбор = Новый Структура("Сумма", 0);
	МассивСтрок = ТаблицаЗаработнойПлаты.НайтиСтроки(Отбор);
	Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
		ТаблицаЗаработнойПлаты.Удалить(СтрокаМассива);
	КонецЦикла;
		
КонецПроцедуры

Функция ПодготовитьПараметрыПоОтражениюЗарплатыВУчете(ДанныеДляПроводок, Организация, ПериодРегистрации, Регистратор)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	Реквизиты = Новый ТаблицаЗначений;
	
	Реквизиты.Колонки.Добавить("Организация");
	Реквизиты.Колонки.Добавить("Период");
	Реквизиты.Колонки.Добавить("Регистратор");
	
	НоваяСтрока             = Реквизиты.Добавить();
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Период      = ПериодРегистрации;
	НоваяСтрока.Регистратор = Регистратор;
	
	Параметры.Вставить("Реквизиты", Реквизиты);
		
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "ФизическоеЛицо,"
	+ "ВидОперации,"
	+ "Подразделение,"
	+ "СпособОтраженияЗарплатыВБухучете,"
	+ "СтатьяРасходов,"
	+ "ОблагаетсяЕНВД,"	
	+ "ВидНачисленияОплатыТрудаДляНУ,"
	+ "ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,"
	+ "ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,"
	+ "ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки,"
	+ "ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка,"
	+ "ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки,"
	+ "ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка,"
	+ "ПФРНаДоплатуЛетчикам,"
	+ "ПФРНаДоплатуШахтерам,"
	+ "ПФРНакопительная,"
	+ "ПФРСтраховая,"
	+ "ПФРПоСуммарномуТарифу,"
	+ "ПФРДоПредельнойВеличины,"
	+ "ПФРСПревышения,"
	+ "ТФОМС,"
	+ "ФСС,"
	+ "ФССНесчастныеСлучаи,"
	+ "ФФОМС,"
	+ "ВзносыПоЕдиномуТарифу,"
	+ "ОПСпоМеждународнымДоговорам,"
	+ "ОПСпоМеждународнымДоговорамСПревышения,"
	+ "ОМСпоМеждународнымДоговорам,"
	+ "ОМСпоМеждународнымДоговорамСПревышения,"
	+ "ОССпоМеждународнымДоговорам,"
	+ "ОССпоМеждународнымДоговорамСПревышения,"
	+ "Сумма,"
	+ "Резерв,"
	+ "РезервБУ,"
	+ "РезервНУ"
	;	
	Параметры.Вставить("ТаблицаЗаработнойПлаты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ДанныеДляПроводок, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ПодготовитьПараметрыПоОтражениюНДФЛВУчете(ДанныеДляПроводок, Организация, ПериодРегистрации, Регистратор)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	Реквизиты = Новый ТаблицаЗначений;
	
	Реквизиты.Колонки.Добавить("Организация");
	Реквизиты.Колонки.Добавить("Период");
	Реквизиты.Колонки.Добавить("Регистратор");
	
	НоваяСтрока             = Реквизиты.Добавить();
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Период      = КонецМесяца(ПериодРегистрации);
	НоваяСтрока.Регистратор = Регистратор;
	
	Параметры.Вставить("Реквизиты", Реквизиты);
		
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "ФизическоеЛицо,"
	+ "ВидОперации,"
	+ "СтатьяРасходов,"
	+ "КПП,"
	+ "КодНалоговогоОргана,"
	+ "КодПоОКАТО,"
	+ "КодПоОКТМО,"
	+ "РегистрацияВНалоговомОргане,"
	+ "Сумма"
	;	
	Параметры.Вставить("ТаблицаНДФЛ", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ДанныеДляПроводок, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ПодготовитьПараметрыПоОтражениюДепонентовУчете(ДанныеДляПроводок, Организация, ПериодРегистрации, Регистратор)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	Реквизиты = Новый ТаблицаЗначений;
	
	Реквизиты.Колонки.Добавить("Организация");
	Реквизиты.Колонки.Добавить("Период");
	Реквизиты.Колонки.Добавить("Регистратор");
	
	НоваяСтрока             = Реквизиты.Добавить();
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Период      = ПериодРегистрации;
	НоваяСтрока.Регистратор = Регистратор;
	
	Параметры.Вставить("Реквизиты", Реквизиты);
		
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "ФизическоеЛицо,"
	+ "ВидОперации,"
	+ "СтатьяРасходов,"
	+ "Сумма"
	;	
	Параметры.Вставить("ТаблицаДепонирование", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ДанныеДляПроводок, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ДополнитьТаблицуЗаработнойПлатыДаннымиПоНДФЛ(НачисленнаяЗарплатаИВзносы, НачисленныйНДФЛ, Организация, ПериодРегистрации, Регистратор)
	
	ДополнятьТаблицуНДФЛ = Ложь;
	
	Если НачисленнаяЗарплатаИВзносы.Колонки.Найти("ВидОперацииРасходовЗаСчетФСС") = Неопределено Тогда
		НачисленнаяЗарплатаИВзносы.Колонки.Добавить("ВидОперацииРасходовЗаСчетФСС",Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	КонецЕсли;
	
	Если НачисленнаяЗарплатаИВзносы.Колонки.Найти("СтатьяРасходов") = Неопределено Тогда
		НачисленнаяЗарплатаИВзносы.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	КонецЕсли;
	
	Если УчетнаяПолитика.ПрименяетсяУСН(Организация, ПериодРегистрации) Тогда
		
		Если НачисленнаяЗарплатаИВзносы.Колонки.Найти("ОтражениеВУСН") = Неопределено Тогда
			НачисленнаяЗарплатаИВзносы.Колонки.Добавить("ОтражениеВУСН", Новый ОписаниеТипов("ПеречислениеСсылка.ОтражениеВУСН"));
		КонецЕсли;
		
		Если НачисленнаяЗарплатаИВзносы.Колонки.Найти("РасчетныйДокумент") = Неопределено Тогда
			НачисленнаяЗарплатаИВзносы.Колонки.Добавить("РасчетныйДокумент", Метаданные.РегистрыНакопления.РасходыПриУСН.Измерения.РасчетныйДокумент.Тип);
		КонецЕсли;
		
		ДополнятьТаблицуНДФЛ = Истина;
		
		МассивОперацийВзносов = Новый Массив;
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРНакопительнаяЧасть);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРСтраховаяЧасть);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРПоСуммарномуТарифу);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДоПредельнойВеличины);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРСПревышения);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифЛЭ);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифШахтеры);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФСС);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФССНС);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФФОМС);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ТФОМС);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ВзносыПоЕдиномуТарифу);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорам);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорамСПревышения);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорам);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорамСПревышения);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорам);
		МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорамСПревышения);
		
		МассивОсновныхНачислений = Новый Массив;
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.НатуральныйДоход);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска);
		МассивОсновныхНачислений.Добавить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства);
		
		МассивОперацийРасходовПоСтрахованию = Новый Массив;
		МассивОперацийРасходовПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
		МассивОперацийРасходовПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
		МассивОперацийРасходовПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет);
		
		НДФЛСПревышения = Новый Массив;
		НДФЛСПревышения.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышения);
		НДФЛСПревышения.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке18);
		НДФЛСПревышения.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке20);
		НДФЛСПревышения.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке22);
		
		Если Месяц(Регистратор.Дата) >= Месяц(ПериодРегистрации) Тогда
			КонецПериодаОсмотра = КонецМесяца(ПериодРегистрации);
		Иначе
			КонецПериодаОсмотра = Регистратор.Дата;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Регистратор",                            Регистратор);
		Запрос.УстановитьПараметр("НачисленнаяЗарплатаИВзносы",             НачисленнаяЗарплатаИВзносы);
		Запрос.УстановитьПараметр("НачисленныйНДФЛ",                        НачисленныйНДФЛ);

		Запрос.УстановитьПараметр("МассивОсновныхНачислений",               МассивОсновныхНачислений);
		Запрос.УстановитьПараметр("НДФЛОсновныхНачислений",                 Перечисления.ВидыОперацийПоЗарплате.НДФЛ);
		
		Запрос.УстановитьПараметр("УсловныйВидОперации",                    Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход);
		
		Запрос.УстановитьПараметр("МассивОперацийРасходовПоСтрахованию",    МассивОперацийРасходовПоСтрахованию);
		
		Запрос.УстановитьПараметр("ДивидендыСотрудников",                   Перечисления.ВидыОперацийПоЗарплате.ДивидендыСотрудников);
		Запрос.УстановитьПараметр("НФДЛДивидендыСотрудникам",               Перечисления.ВидыОперацийПоЗарплате.НФДЛДивидендыСотрудникам);
		
		Запрос.УстановитьПараметр("Дивиденды",                              Перечисления.ВидыОперацийПоЗарплате.Дивиденды);
		Запрос.УстановитьПараметр("НФДЛДивиденды",                          Перечисления.ВидыОперацийПоЗарплате.НФДЛДивиденды);
		
		Запрос.УстановитьПараметр("ВыплатыБывшимСотрудникам",               Перечисления.ВидыОперацийПоЗарплате.ВыплатыБывшимСотрудникам);
		Запрос.УстановитьПараметр("НДФЛРасчетыСБывшимиСотрудниками",        Перечисления.ВидыОперацийПоЗарплате.НДФЛРасчетыСБывшимиСотрудниками);
		
		Запрос.УстановитьПараметр("ДоходыКонтрагентов",                     Перечисления.ВидыОперацийПоЗарплате.ДоходыКонтрагентов);
		Запрос.УстановитьПараметр("НДФЛДоходыКонтрагентов",                 Перечисления.ВидыОперацийПоЗарплате.НДФЛДоходыКонтрагентов);
		
		Запрос.УстановитьПараметр("ПрочиеРасчетыСПерсоналом",               Перечисления.ВидыОперацийПоЗарплате.ПрочиеРасчетыСПерсоналом);
		Запрос.УстановитьПараметр("НДФЛПрочиеРасчетыСПерсоналом",           Перечисления.ВидыОперацийПоЗарплате.НДФЛПрочиеРасчетыСПерсоналом);
		
		Запрос.УстановитьПараметр("НДФЛСПревышения",                        НДФЛСПревышения);
		
		Запрос.УстановитьПараметр("МассивОперацийВзносов",                  МассивОперацийВзносов);
		
		Запрос.УстановитьПараметр("ПериодРегистрации",                      ПериодРегистрации);
		Запрос.УстановитьПараметр("КонецПериодаОсмотра",                    КонецПериодаОсмотра);
		Запрос.УстановитьПараметр("Организация",                            Организация);
		Запрос.УстановитьПараметр("ВидРасходаНалоги",                       Перечисления.ВидыРасходовУСН.Налоги);
		Запрос.УстановитьПараметр("ВидРасходаЗарплата",                     Перечисления.ВидыРасходовУСН.Зарплата);
		Запрос.УстановитьПараметр("СчетУчетаНДФЛ",                          ПланыСчетов.Хозрасчетный.НДФЛ);
		
		Запрос.УстановитьПараметр("УчитыватьСтатьюРасходов",
			ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме")
			ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам")
			ИЛИ Регистратор.Дата >= ДатаНачалаУчетаПрогрессивногоНДФЛ());
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленнаяЗарплатаИВзносы.Сумма КАК Сумма,
		|	НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
		|	НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	НачисленнаяЗарплатаИВзносы.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
		|	НачисленнаяЗарплатаИВзносы.Подразделение КАК Подразделение,
		|	НачисленнаяЗарплатаИВзносы.Резерв КАК Резерв,
		|	НачисленнаяЗарплатаИВзносы.РезервБУ КАК РезервБУ,
		|	НачисленнаяЗарплатаИВзносы.РезервНУ КАК РезервНУ
		|ПОМЕСТИТЬ ВР_НачисленнаяЗарплатаИВзносы
		|ИЗ
		|	&НачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплатаИВзносы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	БольничныйЛист.Ссылка КАК Ссылка,
		|	БольничныйЛист.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТ_БольничныеИОтпуска
		|ИЗ
		|	Документ.БольничныйЛист КАК БольничныйЛист
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(&Регистратор) <> ТИП(Документ.БольничныйЛист)
		|	И ТИПЗНАЧЕНИЯ(&Регистратор) <> ТИП(Документ.Отпуск)
		|	И БольничныйЛист.ФизическоеЛицо В
		|			(ВЫБРАТЬ
		|				ВР_НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо
		|			ИЗ
		|				ВР_НачисленнаяЗарплатаИВзносы КАК ВР_НачисленнаяЗарплатаИВзносы)
		|	И БольничныйЛист.Организация = &Организация
		|	И НАЧАЛОПЕРИОДА(БольничныйЛист.ПериодРегистрации, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
		|
		|СГРУППИРОВАТЬ ПО
		|	БольничныйЛист.Ссылка,
		|	БольничныйЛист.ФизическоеЛицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Отпуск.Ссылка,
		|	Отпуск.ФизическоеЛицо
		|ИЗ
		|	Документ.Отпуск КАК Отпуск
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(&Регистратор) <> ТИП(Документ.БольничныйЛист)
		|	И ТИПЗНАЧЕНИЯ(&Регистратор) <> ТИП(Документ.Отпуск)
		|	И Отпуск.ФизическоеЛицо В
		|			(ВЫБРАТЬ
		|				ВР_НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо
		|			ИЗ
		|				ВР_НачисленнаяЗарплатаИВзносы КАК ВР_НачисленнаяЗарплатаИВзносы)
		|	И Отпуск.Организация = &Организация
		|	И НАЧАЛОПЕРИОДА(Отпуск.ПериодРегистрации, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
		|
		|СГРУППИРОВАТЬ ПО
		|	Отпуск.Ссылка,
		|	Отпуск.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходыПриУСНОбороты.Регистратор КАК Регистратор,
		|	РасходыПриУСНОбороты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	СУММА(РасходыПриУСНОбороты.СуммаПриход - РасходыПриУСНОбороты.СуммаРасход) КАК СуммаПриход,
		|	РасходыПриУСНОбороты.ВидРасхода КАК ВидРасхода,
		|	РасходыПриУСНОбороты.СчетУчета КАК СчетУчета,
		|	РасходыПриУСНОбороты.ОтражениеВУСН КАК ОтражениеВУСН
		|ПОМЕСТИТЬ ВТ_РасходыПриУСНОбороты
		|ИЗ
		|	РегистрНакопления.РасходыПриУСН.Обороты(НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), Регистратор, Организация = &Организация) КАК РасходыПриУСНОбороты
		|ГДЕ
		|	РасходыПриУСНОбороты.Регистратор В
		|			(ВЫБРАТЬ
		|				ВТ_БольничныеИОтпуска.Ссылка КАК Ссылка
		|			ИЗ
		|				ВТ_БольничныеИОтпуска)
		|	И РасходыПриУСНОбороты.СуммаПриход - РасходыПриУСНОбороты.СуммаРасход > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходыПриУСНОбороты.Регистратор,
		|	РасходыПриУСНОбороты.РасчетныйДокумент,
		|	РасходыПриУСНОбороты.ВидРасхода,
		|	РасходыПриУСНОбороты.СчетУчета,
		|	РасходыПриУСНОбороты.ОтражениеВУСН
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасходыПриУСНОбороты.РасчетныйДокумент,
		|	РасходыПриУСНОбороты.РасчетныйДокумент,
		|	СУММА(-РасходыПриУСНОбороты.СуммаРасход),
		|	РасходыПриУСНОбороты.ВидРасхода,
		|	РасходыПриУСНОбороты.СчетУчета,
		|	РасходыПриУСНОбороты.ОтражениеВУСН
		|ИЗ
		|	РегистрНакопления.РасходыПриУСН.Обороты(НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), &КонецПериодаОсмотра, Регистратор, Организация = &Организация) КАК РасходыПриУСНОбороты
		|ГДЕ
		|	РасходыПриУСНОбороты.РасчетныйДокумент В
		|			(ВЫБРАТЬ
		|				ВТ_БольничныеИОтпуска.Ссылка КАК Ссылка
		|			ИЗ
		|				ВТ_БольничныеИОтпуска)
		|	И РасходыПриУСНОбороты.РасчетныйДокумент <> РасходыПриУСНОбороты.Регистратор
		|	И РасходыПриУСНОбороты.СуммаРасход > 0
		|	И РасходыПриУСНОбороты.ВидРасхода = &ВидРасходаЗарплата
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходыПриУСНОбороты.РасчетныйДокумент,
		|	РасходыПриУСНОбороты.ВидРасхода,
		|	РасходыПриУСНОбороты.СчетУчета,
		|	РасходыПриУСНОбороты.ОтражениеВУСН,
		|	РасходыПриУСНОбороты.РасчетныйДокумент
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РасходыПриУСНОбороты.Регистратор КАК Регистратор,
		|	ВТ_РасходыПриУСНОбороты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	СУММА(ВТ_РасходыПриУСНОбороты.СуммаПриход) КАК СуммаПриход,
		|	ВТ_РасходыПриУСНОбороты.ВидРасхода КАК ВидРасхода,
		|	ВТ_РасходыПриУСНОбороты.СчетУчета КАК СчетУчета,
		|	ВТ_РасходыПриУСНОбороты.ОтражениеВУСН КАК ОтражениеВУСН
		|ПОМЕСТИТЬ ВТ_РасходыПриУСНОборотыСводная
		|ИЗ
		|	ВТ_РасходыПриУСНОбороты КАК ВТ_РасходыПриУСНОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_РасходыПриУСНОбороты.Регистратор,
		|	ВТ_РасходыПриУСНОбороты.РасчетныйДокумент,
		|	ВТ_РасходыПриУСНОбороты.ВидРасхода,
		|	ВТ_РасходыПриУСНОбороты.СчетУчета,
		|	ВТ_РасходыПриУСНОбороты.ОтражениеВУСН
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходыПриУСНОбороты.Регистратор КАК Ссылка
		|ПОМЕСТИТЬ ВТ_ДокументыИсключения
		|ИЗ
		|	ВТ_БольничныеИОтпуска КАК ВТ_БольничныеИОтпуска
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасходыПриУСНОборотыСводная КАК РасходыПриУСНОбороты
		|		ПО ВТ_БольничныеИОтпуска.Ссылка = РасходыПриУСНОбороты.Регистратор
		|ГДЕ
		|	РасходыПриУСНОбороты.ВидРасхода = &ВидРасходаНалоги
		|	И РасходыПриУСНОбороты.СчетУчета В (&СчетУчетаНДФЛ)
		|	И РасходыПриУСНОбороты.Регистратор <> &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_БольничныеИОтпуска.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(РасходыПриУСНОбороты.СуммаПриход) КАК Сумма,
		|	РасходыПриУСНОбороты.ОтражениеВУСН КАК ОтражениеВУСН,
		|	РасходыПриУСНОбороты.РасчетныйДокумент КАК РасчетныйДокумент
		|ПОМЕСТИТЬ ВТ_РасходыМежрасчетныхВыплат
		|ИЗ
		|	ВТ_БольничныеИОтпуска КАК ВТ_БольничныеИОтпуска
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасходыПриУСНОборотыСводная КАК РасходыПриУСНОбороты
		|		ПО ВТ_БольничныеИОтпуска.Ссылка = РасходыПриУСНОбороты.Регистратор
		|ГДЕ
		|	НЕ РасходыПриУСНОбороты.Регистратор В
		|				(ВЫБРАТЬ
		|					ВТ_ДокументыИсключения.Ссылка КАК Ссылка
		|				ИЗ
		|					ВТ_ДокументыИсключения)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходыПриУСНОбороты.РасчетныйДокумент,
		|	ВТ_БольничныеИОтпуска.ФизическоеЛицо,
		|	РасходыПриУСНОбороты.ОтражениеВУСН
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленнаяЗарплатаИВзносы.Сумма КАК Сумма,
		|	НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
		|	НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	НачисленнаяЗарплатаИВзносы.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
		|	НачисленнаяЗарплатаИВзносы.Подразделение КАК Подразделение,
		|	NULL КАК ОтражениеВУСН,
		|	NULL КАК РасчетныйДокумент,
		|	НачисленнаяЗарплатаИВзносы.Резерв КАК Резерв,
		|	НачисленнаяЗарплатаИВзносы.РезервБУ КАК РезервБУ,
		|	НачисленнаяЗарплатаИВзносы.РезервНУ КАК РезервНУ
		|ПОМЕСТИТЬ ВТ_НачисленнаяЗарплатаИВзносы
		|ИЗ
		|	ВР_НачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплатаИВзносы
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_РасходыМежрасчетныхВыплат.ФизическоеЛицо,
		|	ВТ_РасходыМежрасчетныхВыплат.Сумма,
		|	&УсловныйВидОперации,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ВТ_РасходыМежрасчетныхВыплат.ОтражениеВУСН,
		|	ВТ_РасходыМежрасчетныхВыплат.РасчетныйДокумент,
		|	ЗНАЧЕНИЕ(справочник.Резервы.ПустаяСсылка),
		|	ЛОЖЬ,
		|	ЛОЖЬ
		|ИЗ
		|	ВТ_РасходыМежрасчетныхВыплат КАК ВТ_РасходыМежрасчетныхВыплат
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленныйНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленныйНДФЛ.Сумма КАК Сумма,
		|	НачисленныйНДФЛ.ВидОперации КАК ВидОперации,
		|	НачисленныйНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ВЫБОР
		|		КОГДА &УчитыватьСтатьюРасходов
		|			ТОГДА НачисленныйНДФЛ.СтатьяРасходов
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|	КОНЕЦ КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТ_НачисленныйНДФЛ
		|ИЗ
		|	&НачисленныйНДФЛ КАК НачисленныйНДФЛ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НачисленныйНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТ_НачисленныйНДФЛ.ВидОперации КАК ВидОперации,
		|	ВТ_НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	ВЫБОР
		|		КОГДА &УчитыватьСтатьюРасходов
		|			ТОГДА ВТ_НачисленныйНДФЛ.СтатьяРасходов
		|		ИНАЧЕ ВТ_НачисленнаяЗарплатаИВзносы.СтатьяРасходов
		|	КОНЕЦ КАК СтатьяРасходов,
		|	ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ВТ_НачисленныйНДФЛ.Сумма КАК СуммаНалога,
		|	ВТ_НачисленнаяЗарплатаИВзносы.Сумма КАК СуммаДохода,
		|	ВТ_НачисленныйНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ВТ_НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
		|	ВЫБОР
		|		КОГДА ВТ_НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
		|				И ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОперацийРасходовПоСтрахованию)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются)
		|		ИНАЧЕ ВТ_НачисленнаяЗарплатаИВзносы.ОтражениеВУСН
		|	КОНЕЦ КАК ОтражениеВУСН,
		|	ВТ_НачисленнаяЗарплатаИВзносы.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ВТ_НачисленнаяЗарплатаИВзносы.Резерв КАК Резерв,
		|	ВТ_НачисленнаяЗарплатаИВзносы.РезервБУ КАК РезервБУ,
		|	ВТ_НачисленнаяЗарплатаИВзносы.РезервНУ КАК РезервНУ
		|ПОМЕСТИТЬ ВТ_ПромежуточныйНДФЛ
		|ИЗ
		|	ВТ_НачисленнаяЗарплатаИВзносы КАК ВТ_НачисленнаяЗарплатаИВзносы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НачисленныйНДФЛ КАК ВТ_НачисленныйНДФЛ
		|		ПО ВТ_НачисленнаяЗарплатаИВзносы.ФизическоеЛицо = ВТ_НачисленныйНДФЛ.ФизическоеЛицо
		|			И (ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОсновныхНачислений)
		|					И ВТ_НачисленныйНДФЛ.ВидОперации = &НДФЛОсновныхНачислений
		|				ИЛИ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации = &ДивидендыСотрудников
		|					И ВТ_НачисленныйНДФЛ.ВидОперации = &НФДЛДивидендыСотрудникам
		|				ИЛИ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации = &Дивиденды
		|					И ВТ_НачисленныйНДФЛ.ВидОперации = &НФДЛДивиденды
		|				ИЛИ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации = &ВыплатыБывшимСотрудникам
		|					И ВТ_НачисленныйНДФЛ.ВидОперации = &НДФЛРасчетыСБывшимиСотрудниками
		|				ИЛИ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации = &ДоходыКонтрагентов
		|					И ВТ_НачисленныйНДФЛ.ВидОперации = &НДФЛДоходыКонтрагентов
		|				ИЛИ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации = &ПрочиеРасчетыСПерсоналом
		|					И ВТ_НачисленныйНДФЛ.ВидОперации = &НДФЛПрочиеРасчетыСПерсоналом
		|				ИЛИ ВТ_НачисленнаяЗарплатаИВзносы.СтатьяРасходов = ВТ_НачисленныйНДФЛ.СтатьяРасходов
		|					И ВТ_НачисленныйНДФЛ.ВидОперации В (&НДФЛСПревышения))
		|			И (ВЫБОР
		|				КОГДА &УчитыватьСтатьюРасходов
		|					ТОГДА ВТ_НачисленнаяЗарплатаИВзносы.СтатьяРасходов = ВТ_НачисленныйНДФЛ.СтатьяРасходов
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|ГДЕ
		|	НЕ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОперацийВзносов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_НачисленныйНДФЛ.ФизическоеЛицо,
		|	ВТ_НачисленныйНДФЛ.ВидОперации,
		|	ВТ_НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете,
		|	ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД,
		|	ВТ_НачисленныйНДФЛ.Сумма,
		|	ВТ_НачисленнаяЗарплатаИВзносы.Сумма,
		|	ВТ_НачисленныйНДФЛ.РегистрацияВНалоговомОргане,
		|	ВТ_НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС,
		|	ВТ_НачисленнаяЗарплатаИВзносы.РасчетныйДокумент,
		|	ВЫБОР
		|		КОГДА ВТ_НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
		|				И ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОперацийРасходовПоСтрахованию)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются)
		|		ИНАЧЕ ВТ_НачисленнаяЗарплатаИВзносы.ОтражениеВУСН
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &УчитыватьСтатьюРасходов
		|			ТОГДА ВТ_НачисленныйНДФЛ.СтатьяРасходов
		|		ИНАЧЕ ВТ_НачисленнаяЗарплатаИВзносы.СтатьяРасходов
		|	КОНЕЦ,
		|	ВТ_НачисленнаяЗарплатаИВзносы.Резерв,
		|	ВТ_НачисленнаяЗарплатаИВзносы.РезервБУ,
		|	ВТ_НачисленнаяЗарплатаИВзносы.РезервНУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПромежуточныйНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТ_ПромежуточныйНДФЛ.ВидОперации КАК ВидОперации,
		|	СУММА(ВТ_ПромежуточныйНДФЛ.СуммаНалога) КАК СуммаНалога,
		|	СУММА(ВТ_ПромежуточныйНДФЛ.СуммаДохода) КАК СуммаДохода,
		|	ВТ_ПромежуточныйНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
		|ПОМЕСТИТЬ ВТ_ПромежуточныйНДФЛИтог
		|ИЗ
		|	ВТ_ПромежуточныйНДФЛ КАК ВТ_ПромежуточныйНДФЛ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ПромежуточныйНДФЛ.ФизическоеЛицо,
		|	ВТ_ПромежуточныйНДФЛ.ВидОперации,
		|	ВТ_ПромежуточныйНДФЛ.РегистрацияВНалоговомОргане
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПромежуточныйНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТ_ПромежуточныйНДФЛ.ВидОперации КАК ВидОперации,
		|	ВТ_ПромежуточныйНДФЛ.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	ВТ_ПромежуточныйНДФЛ.СтатьяРасходов КАК СтатьяРасходов,
		|	ВТ_ПромежуточныйНДФЛ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ВТ_ПромежуточныйНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(ВЫБОР
		|				КОГДА ЕСТЬNULL(СУММА(ВТ_ПромежуточныйНДФЛИтог.СуммаДохода), 0) = 0
		|					ТОГДА 0
		|				КОГДА ВТ_ПромежуточныйНДФЛ.СуммаДохода = СУММА(ВТ_ПромежуточныйНДФЛИтог.СуммаДохода)
		|					ТОГДА ВТ_ПромежуточныйНДФЛ.СуммаНалога
		|				ИНАЧЕ СУММА(ВТ_ПромежуточныйНДФЛИтог.СуммаНалога) * (ВТ_ПромежуточныйНДФЛ.СуммаДохода / СУММА(ВТ_ПромежуточныйНДФЛИтог.СуммаДохода))
		|			КОНЕЦ, 0) КАК ЧИСЛО(15, 0)) КАК Сумма,
		|	ВТ_ПромежуточныйНДФЛ.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
		|	ВТ_ПромежуточныйНДФЛ.ОтражениеВУСН КАК ОтражениеВУСН,
		|	ВТ_ПромежуточныйНДФЛ.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ВТ_ПромежуточныйНДФЛ.Резерв КАК Резерв,
		|	ВТ_ПромежуточныйНДФЛ.РезервБУ КАК РезервБУ,
		|	ВТ_ПромежуточныйНДФЛ.РезервНУ КАК РезервНУ
		|ИЗ
		|	ВТ_ПромежуточныйНДФЛ КАК ВТ_ПромежуточныйНДФЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПромежуточныйНДФЛИтог КАК ВТ_ПромежуточныйНДФЛИтог
		|		ПО ВТ_ПромежуточныйНДФЛ.ФизическоеЛицо = ВТ_ПромежуточныйНДФЛИтог.ФизическоеЛицо
		|			И ВТ_ПромежуточныйНДФЛ.ВидОперации = ВТ_ПромежуточныйНДФЛИтог.ВидОперации
		|			И ВТ_ПромежуточныйНДФЛ.РегистрацияВНалоговомОргане = ВТ_ПромежуточныйНДФЛИтог.РегистрацияВНалоговомОргане
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ПромежуточныйНДФЛ.ФизическоеЛицо,
		|	ВТ_ПромежуточныйНДФЛ.ВидОперации,
		|	ВТ_ПромежуточныйНДФЛ.СпособОтраженияЗарплатыВБухУчете,
		|	ВТ_ПромежуточныйНДФЛ.СтатьяРасходов,
		|	ВТ_ПромежуточныйНДФЛ.ОблагаетсяЕНВД,
		|	ВТ_ПромежуточныйНДФЛ.СуммаНалога,
		|	ВТ_ПромежуточныйНДФЛ.СуммаДохода,
		|	ВТ_ПромежуточныйНДФЛ.РегистрацияВНалоговомОргане,
		|	ВТ_ПромежуточныйНДФЛ.ВидОперацииРасходовЗаСчетФСС,
		|	ВТ_ПромежуточныйНДФЛ.ОтражениеВУСН,
		|	ВТ_ПромежуточныйНДФЛ.РасчетныйДокумент,
		|	ВТ_ПромежуточныйНДФЛ.Резерв,
		|	ВТ_ПромежуточныйНДФЛ.РезервБУ,
		|	ВТ_ПромежуточныйНДФЛ.РезервНУ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизическоеЛицо,
		|	ВидОперации,
		|	РегистрацияВНалоговомОргане";
	
		Результат = Запрос.Выполнить().Выгрузить();
		
		Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
			ЗаполнитьСтатьюРасходовПоВидуОперации(Результат);
		КонецЕсли;
		
		// корректировка погрешности вычисления
		СтруктураОтбора = Новый Структура();
		СтруктураОтбораНДФЛ = Новый Структура();
		
		Для Каждого СтрокаТаблицы ИЗ НачисленныйНДФЛ Цикл
			
			НачисленныйНДФЛСуммаНалога = СтрокаТаблицы.Сумма;
			НачисленныйНДФЛСуммаНалогаИсходный = СтрокаТаблицы.Сумма;
			
			СтруктураОтбора.Вставить("ФизическоеЛицо",              СтрокаТаблицы.ФизическоеЛицо);
			СтруктураОтбора.Вставить("ВидОперации",                 СтрокаТаблицы.ВидОперации);
			СтруктураОтбора.Вставить("РегистрацияВНалоговомОргане", СтрокаТаблицы.РегистрацияВНалоговомОргане);
			СтруктураОтбора.Вставить("СтатьяРасходов",              СтрокаТаблицы.СтатьяРасходов);
			
			МассивОтбора = Результат.НайтиСтроки(СтруктураОтбора);
			
			Если МассивОтбора.Количество() <> 0 Тогда
				Если МассивОтбора.Количество() = 1 Тогда
					Если МассивОтбора[0].Сумма <> СтрокаТаблицы.Сумма Тогда
						МассивОтбора[0].Сумма = СтрокаТаблицы.Сумма;
					КонецЕсли;
					
					СтруктураОтбораНДФЛ.Вставить("ФизическоеЛицо",             МассивОтбора[0].ФизическоеЛицо);
					СтруктураОтбораНДФЛ.Вставить("ВидОперации",                МассивОтбора[0].ВидОперации);
					СтруктураОтбораНДФЛ.Вставить("РегистрацияВНалоговомОргане",МассивОтбора[0].РегистрацияВНалоговомОргане);
					СтруктураОтбораНДФЛ.Вставить("СтатьяРасходов",             МассивОтбора[0].СтатьяРасходов);
					
					МассивОтбораНачисленныйНДФЛ = НачисленныйНДФЛ.НайтиСтроки(СтруктураОтбораНДФЛ);
					
					Если МассивОтбораНачисленныйНДФЛ.Количество() <> 0 Тогда
						СуммаНалогаНачисленного = 0;
						МассивСуммНалогаНачисленного = Новый Массив;
						Для Каждого СтрокаМассиваНалогаНачисленного ИЗ МассивОтбораНачисленныйНДФЛ Цикл
							СуммаНалогаНачисленного = СуммаНалогаНачисленного + СтрокаМассиваНалогаНачисленного.Сумма;
							МассивСуммНалогаНачисленного.Добавить(СтрокаМассиваНалогаНачисленного.Сумма);
						КонецЦикла;
						
						Если НачисленныйНДФЛСуммаНалогаИсходный - СуммаНалогаНачисленного <> 0 Тогда
							МассивНовыхСумм = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(НачисленныйНДФЛСуммаНалогаИсходный, МассивСуммНалогаНачисленного, 0);
							Если МассивНовыхСумм <> Неопределено Тогда
								НомерСтроки = 0;
								Для Каждого СтрокаМассива ИЗ МассивОтбораНачисленныйНДФЛ Цикл
									СтрокаМассива.Сумма = СтрокаМассива.Сумма - МассивНовыхСумм[НомерСтроки];
									НомерСтроки = НомерСтроки + 1;
								КонецЦикла;
							КонецЕсли;
						Иначе
							Для Каждого СтрокаМассива ИЗ МассивОтбораНачисленныйНДФЛ Цикл
								СтрокаМассива.Сумма = 0;
							КонецЦикла;
						КонецЕсли; 
						
					КонецЕсли;
					
				Иначе
					СуммаНалога = 0;
					МассивСумм = Новый Массив;
					Для Каждого СтрокаМассива ИЗ МассивОтбора Цикл
						СуммаНалога = СуммаНалога + СтрокаМассива.Сумма;
						МассивСумм.Добавить(СтрокаМассива.Сумма);
						
						СтруктураОтбораНДФЛ.Вставить("ФизическоеЛицо",              СтрокаМассива.ФизическоеЛицо);
						СтруктураОтбораНДФЛ.Вставить("ВидОперации",                 СтрокаМассива.ВидОперации);
						СтруктураОтбораНДФЛ.Вставить("РегистрацияВНалоговомОргане", СтрокаМассива.РегистрацияВНалоговомОргане);
						СтруктураОтбораНДФЛ.Вставить("СтатьяРасходов",              СтрокаМассива.СтатьяРасходов);
					
						МассивОтбораНачисленныйНДФЛ = НачисленныйНДФЛ.НайтиСтроки(СтруктураОтбораНДФЛ);
						
						Если МассивОтбораНачисленныйНДФЛ.Количество() <> 0 Тогда
							СуммаНалогаНачисленного = 0;
							МассивСуммНалогаНачисленного = Новый Массив;
							Для Каждого СтрокаМассиваНалогаНачисленного ИЗ МассивОтбораНачисленныйНДФЛ Цикл
								СуммаНалогаНачисленного = СуммаНалогаНачисленного + СтрокаМассиваНалогаНачисленного.Сумма;
								МассивСуммНалогаНачисленного.Добавить(СтрокаМассиваНалогаНачисленного.Сумма);
							КонецЦикла;
							
							Если НачисленныйНДФЛСуммаНалогаИсходный - СуммаНалогаНачисленного <> 0 Тогда
								МассивНовыхСумм = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(НачисленныйНДФЛСуммаНалогаИсходный, МассивСуммНалогаНачисленного, 0);
								Если МассивНовыхСумм <> Неопределено Тогда
									НомерСтроки = 0;
									Для Каждого СтрокаМассива ИЗ МассивОтбораНачисленныйНДФЛ Цикл
										СтрокаМассива.Сумма = СтрокаМассива.Сумма - МассивНовыхСумм[НомерСтроки];
										НомерСтроки = НомерСтроки + 1;
									КонецЦикла;
								КонецЕсли; 
							Иначе
								Для Каждого СтрокаМассива ИЗ МассивОтбораНачисленныйНДФЛ Цикл
									СтрокаМассива.Сумма = 0;
								КонецЦикла;
							КонецЕсли; 
							
						КонецЕсли;
						
					КонецЦикла;
						
					Если НачисленныйНДФЛСуммаНалога - СуммаНалога <> 0 Тогда
						МассивНовыхСумм = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(НачисленныйНДФЛСуммаНалога, МассивСумм, 0);
						Если МассивНовыхСумм <> Неопределено Тогда
							НомерСтроки = 0;
							Для Каждого СтрокаМассива ИЗ МассивОтбора Цикл
								СтрокаМассива.Сумма = МассивНовыхСумм[НомерСтроки];
								НомерСтроки = НомерСтроки + 1;
							КонецЦикла;
						КонецЕсли; 
					КонецЕсли;
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		Результат = НачисленныйНДФЛ;
	КонецЕсли;
	
	Для Каждого СтрокаРезультата ИЗ Результат Цикл
		НоваяСтрока = НачисленнаяЗарплатаИВзносы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРезультата);
	КонецЦикла;	
	
	Если ДополнятьТаблицуНДФЛ Тогда
		Для Каждого СтрокаРезультата ИЗ НачисленныйНДФЛ Цикл
			Если СтрокаРезультата.Сумма <> 0 Тогда
				НоваяСтрока = НачисленнаяЗарплатаИВзносы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРезультата);
				НоваяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Возврат НачисленнаяЗарплатаИВзносы;
	
КонецФункции

Процедура ЗаполнитьСтатьюРасходовПоВидуОперации(ДанныеДляОтражения)

	СоответствиеОпераций = ОтражениеЗарплатыВБухучетеБазовый.СоответствиеВидОперацииСтатьяРасходов();
	
	Для каждого СтрокаТЗ Из ДанныеДляОтражения Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.СтатьяРасходов) Тогда
			СтрокаТЗ.СтатьяРасходов = СоответствиеОпераций[СтрокаТЗ.ВидОперации];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ДополнитьТаблицуНДФЛАналитикойПоРегистрации(ТаблицаНДФЛ, Организация)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТаблицаНДФЛ", ТаблицаНДФЛ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНДФЛ.ФизическоеЛицо,
	|	ТаблицаНДФЛ.Сумма,
	|	ТаблицаНДФЛ.ВидОперации,
	|	ТаблицаНДФЛ.СтатьяРасходов,
	|	ВЫРАЗИТЬ(ТаблицаНДФЛ.КПП КАК СТРОКА(9)) КАК КПП,
	|	ВЫРАЗИТЬ(ТаблицаНДФЛ.КодНалоговогоОргана КАК СТРОКА(4)) КАК КодНалоговогоОргана,
	|	ВЫРАЗИТЬ(ТаблицаНДФЛ.КодПоОКАТО КАК СТРОКА(11)) КАК КодПоОКАТО,
	|	ВЫРАЗИТЬ(ТаблицаНДФЛ.КодПоОКТМО КАК СТРОКА(11)) КАК КодПоОКТМО,
	|	ТаблицаНДФЛ.РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТ_ТаблицаНДФЛ
	|ИЗ
	|	&ТаблицаНДФЛ КАК ТаблицаНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(РегистрацииВНалоговомОргане.Ссылка) КАК Ссылка,
	|	РегистрацииВНалоговомОргане.КПП,
	|	РегистрацииВНалоговомОргане.Код КАК КодНалоговогоОргана,
	|	РегистрацииВНалоговомОргане.КодПоОКАТО,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО
	|ПОМЕСТИТЬ ВТ_РегистрацииВНалоговомОргане
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	(РегистрацииВНалоговомОргане.Владелец = &Организация
	|			ИЛИ РегистрацииВНалоговомОргане.Владелец.ГоловнаяОрганизация = &Организация)
	|	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацииВНалоговомОргане.КПП,
	|	РегистрацииВНалоговомОргане.Код,
	|	РегистрацииВНалоговомОргане.КодПоОКАТО,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНДФЛ.ФизическоеЛицо,
	|	СУММА(ВТ_ТаблицаНДФЛ.Сумма) КАК Сумма,
	|	ВТ_ТаблицаНДФЛ.ВидОперации,
	|	ВТ_ТаблицаНДФЛ.СтатьяРасходов,
	|	ВТ_ТаблицаНДФЛ.КПП,
	|	ВТ_ТаблицаНДФЛ.КодПоОКАТО,
	|	ВТ_ТаблицаНДФЛ.КодПоОКТМО,
	|	ВТ_РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТ_ДополненныеДанные
	|ИЗ
	|	ВТ_ТаблицаНДФЛ КАК ВТ_ТаблицаНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацииВНалоговомОргане КАК ВТ_РегистрацииВНалоговомОргане
	|		ПО ВТ_ТаблицаНДФЛ.КПП = ВТ_РегистрацииВНалоговомОргане.КПП
	|			И ВТ_ТаблицаНДФЛ.КодПоОКАТО = ВТ_РегистрацииВНалоговомОргане.КодПоОКАТО
	|			И ВТ_ТаблицаНДФЛ.КодПоОКТМО = ВТ_РегистрацииВНалоговомОргане.КодПоОКТМО
	|			И ВТ_ТаблицаНДФЛ.КодНалоговогоОргана = ВТ_РегистрацииВНалоговомОргане.КодНалоговогоОргана
	|ГДЕ
	|	ВТ_ТаблицаНДФЛ.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаНДФЛ.ФизическоеЛицо,
	|	ВТ_ТаблицаНДФЛ.КодПоОКАТО,
	|	ВТ_ТаблицаНДФЛ.КодПоОКТМО,
	|	ВТ_ТаблицаНДФЛ.ВидОперации,
	|	ВТ_ТаблицаНДФЛ.СтатьяРасходов,
	|	ВТ_ТаблицаНДФЛ.КПП,
	|	ВТ_ТаблицаНДФЛ.КодНалоговогоОргана,
	|	ВТ_РегистрацииВНалоговомОргане.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНДФЛ.ФизическоеЛицо,
	|	СУММА(ВТ_ТаблицаНДФЛ.Сумма),
	|	ВТ_ТаблицаНДФЛ.ВидОперации,
	|	ВТ_ТаблицаНДФЛ.СтатьяРасходов,
	|	ВТ_ТаблицаНДФЛ.КПП,
	|	ВТ_ТаблицаНДФЛ.КодПоОКАТО,
	|	ВТ_ТаблицаНДФЛ.КодПоОКТМО,
	|	ВТ_ТаблицаНДФЛ.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТ_ТаблицаНДФЛ КАК ВТ_ТаблицаНДФЛ
	|ГДЕ
	|	ВТ_ТаблицаНДФЛ.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаНДФЛ.ФизическоеЛицо,
	|	ВТ_ТаблицаНДФЛ.КодПоОКАТО,
	|	ВТ_ТаблицаНДФЛ.КодПоОКТМО,
	|	ВТ_ТаблицаНДФЛ.ВидОперации,
	|	ВТ_ТаблицаНДФЛ.СтатьяРасходов,
	|	ВТ_ТаблицаНДФЛ.КПП,
	|	ВТ_ТаблицаНДФЛ.КодНалоговогоОргана,
	|	ВТ_ТаблицаНДФЛ.РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДополненныеДанные.ФизическоеЛицо,
	|	СУММА(ВТ_ДополненныеДанные.Сумма) КАК Сумма,
	|	ВТ_ДополненныеДанные.ВидОперации,
	|	ВТ_ДополненныеДанные.СтатьяРасходов,
	|	ВТ_ДополненныеДанные.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТ_ДополненныеДанные КАК ВТ_ДополненныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДополненныеДанные.ВидОперации,
	|	ВТ_ДополненныеДанные.СтатьяРасходов,
	|	ВТ_ДополненныеДанные.ФизическоеЛицо,
	|	ВТ_ДополненныеДанные.РегистрацияВНалоговомОргане";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ДополнитьТаблицуЗарплатыАналитикойБухУчета(ТаблицаЗаработнойПлаты)
	
	ДополнитьТаблицуЗаработнойПлатыНедостающимиКолонками(ТаблицаЗаработнойПлаты);
	
	МассивОпераций = Новый Массив;
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРНакопительнаяЧасть);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРСтраховаяЧасть);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРПоСуммарномуТарифу);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДоПредельнойВеличины);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРСПревышения);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифЛЭ);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифШахтеры);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФСС);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФССНС);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФФОМС);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ТФОМС);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ВзносыПоЕдиномуТарифу);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорам);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорамСПревышения);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорам);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорамСПревышения);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорам);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорамСПревышения);

	
	МассивОперацийРасходовПоСтрахованию = Новый Массив;
	МассивОперацийРасходовПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	МассивОперацийРасходовПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	МассивОперацийРасходовПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет);
	
	МассивОперацийНезарплатногоНДФЛ = Новый Массив;
	МассивОперацийНезарплатногоНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛДоначисленныйПоРезультатамПроверки);
	МассивОперацийНезарплатногоНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛДоходыКонтрагентов);
	МассивОперацийНезарплатногоНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛПередачаЗадолженностиВНалоговыйОрган);
	МассивОперацийНезарплатногоНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛРасчетыСБывшимиСотрудниками);
	МассивОперацийНезарплатногоНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НФДЛДивиденды);
	МассивОперацийНезарплатногоНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НФДЛДивидендыСотрудникам);
	МассивОперацийНезарплатногоНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛПрочиеРасчетыСПерсоналом);
	
	МассивОперацийДивиденды = Новый Массив;
	МассивОперацийДивиденды.Добавить(Перечисления.ВидыОперацийПоЗарплате.Дивиденды);
	МассивОперацийДивиденды.Добавить(Перечисления.ВидыОперацийПоЗарплате.ДивидендыСотрудников);
	
	МассивОперацийРезервыБУ = Новый Массив;
	МассивОперацийРезервыБУ.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы);
	МассивОперацийРезервыБУ.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства);
	МассивОперацийРезервыБУ.Добавить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства);
	
	МассивОперацийРезервыНУ = Новый Массив;
	МассивОперацийРезервыНУ.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы);
	МассивОперацийРезервыНУ.Добавить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы);
	
	МассивСчетовУчетаНУ = Новый Массив;
	МассивСчетовУчетаНУ.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаОплатуТрудаБудущихПериодов);
	
	СчетаУчетаНераспределеннаяПрибыль = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль);
	СчетУчетаПрочиеРасходы            = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	СчетУчетаРезервы                  = ПланыСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям;
	СчетУчетаВзносыРезервов           = ПланыСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам;
	
	РасходыПоУплатеСтраховыхВзносов =
		Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("РасходыПоУплатеСтраховыхВзносов");
	ОплатаТрудаНеУчитываемаяДляНалогообложения =
		Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент( "ОплатаТрудаНеУчитываемаяДляНалогообложения");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачисленнаяЗарплатаИВзносы",                 ТаблицаЗаработнойПлаты);
	Запрос.УстановитьПараметр("МассивСчетовУчетаНУ",                        МассивСчетовУчетаНУ);
	Запрос.УстановитьПараметр("МассивОпераций",                             МассивОпераций);
	Запрос.УстановитьПараметр("СчетаУчетаНераспределеннаяПрибыль",          СчетаУчетаНераспределеннаяПрибыль);
	Запрос.УстановитьПараметр("СчетУчетаПрочиеРасходы",                     СчетУчетаПрочиеРасходы);
	Запрос.УстановитьПараметр("СчетУчетаРезервы",                           СчетУчетаРезервы);
	Запрос.УстановитьПараметр("СчетУчетаВзносыРезервов",                    СчетУчетаВзносыРезервов);
	Запрос.УстановитьПараметр("РасходыПоУплатеСтраховыхВзносов",            РасходыПоУплатеСтраховыхВзносов);
	Запрос.УстановитьПараметр("ОплатаТрудаНеУчитываемаяДляНалогообложения", ОплатаТрудаНеУчитываемаяДляНалогообложения);
	Запрос.УстановитьПараметр("МассивОперацийРасходовПоСтрахованию",        МассивОперацийРасходовПоСтрахованию);
	Запрос.УстановитьПараметр("МассивОперацийНезарплатногоНДФЛ",            МассивОперацийНезарплатногоНДФЛ);
	Запрос.УстановитьПараметр("МассивОперацийДивиденды",                    МассивОперацийДивиденды);
	Запрос.УстановитьПараметр("МассивОперацийРезервыБУ",                    МассивОперацийРезервыБУ);
	Запрос.УстановитьПараметр("МассивОперацийРезервыНУ",                    МассивОперацийРезервыНУ);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.Сумма КАК Сумма,
	|	НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
	|	НачисленнаяЗарплатаИВзносы.СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленнаяЗарплатаИВзносы.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	НачисленнаяЗарплатаИВзносы.Подразделение КАК Подразделение,
	|	НачисленнаяЗарплатаИВзносы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
	|	НачисленнаяЗарплатаИВзносы.ВедущийВидОперации КАК ВедущийВидОперации,
	|	НачисленнаяЗарплатаИВзносы.ОтражениеВУСН КАК ОтражениеВУСН,
	|	НачисленнаяЗарплатаИВзносы.РасчетныйДокумент КАК РасчетныйДокумент,
	|	НачисленнаяЗарплатаИВзносы.Резерв КАК Резерв,
	|	НачисленнаяЗарплатаИВзносы.РезервБУ КАК РезервБУ,
	|	НачисленнаяЗарплатаИВзносы.РезервНУ КАК РезервНУ
	|ПОМЕСТИТЬ ВТ_ТаблицаЗаработнойПлаты
	|ИЗ
	|	&НачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплатаИВзносы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаЗаработнойПлаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_ТаблицаЗаработнойПлаты.Сумма КАК Сумма,
	|	ВТ_ТаблицаЗаработнойПлаты.ВидОперации КАК ВидОперации,
	|	ВТ_ТаблицаЗаработнойПлаты.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТ_ТаблицаЗаработнойПлаты.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	ВТ_ТаблицаЗаработнойПлаты.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СпособыОтраженияЗарплатыВБухУчете.Ссылка КАК СпособОтраженияЗарплатыВБухУчете,
	|	ВТ_ТаблицаЗаработнойПлаты.Подразделение КАК Подразделение,
	|	ВТ_ТаблицаЗаработнойПлаты.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_ТаблицаЗаработнойПлаты.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
	|	ВТ_ТаблицаЗаработнойПлаты.ВедущийВидОперации КАК ВедущийВидОперации,
	|	ВТ_ТаблицаЗаработнойПлаты.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ВТ_ТаблицаЗаработнойПлаты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	СпособыОтраженияЗарплатыВБухУчете.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ВТ_ТаблицаЗаработнойПлаты.Резерв КАК Резерв,
	|	ВТ_ТаблицаЗаработнойПлаты.РезервБУ
	|		ИЛИ ВТ_ТаблицаЗаработнойПлаты.ВидОперации В (&МассивОперацийРезервыБУ)
	|		ИЛИ ВТ_ТаблицаЗаработнойПлаты.ВедущийВидОперации В (&МассивОперацийРезервыБУ) КАК РезервБУ,
	|	ВТ_ТаблицаЗаработнойПлаты.РезервНУ
	|		ИЛИ ВТ_ТаблицаЗаработнойПлаты.ВидОперации В (&МассивОперацийРезервыНУ)
	|		ИЛИ ВТ_ТаблицаЗаработнойПлаты.ВедущийВидОперации В (&МассивОперацийРезервыНУ) КАК РезервНУ
	|ПОМЕСТИТЬ ВТ_НачисленнаяЗарплатаИВзносы
	|ИЗ
	|	ВТ_ТаблицаЗаработнойПлаты КАК ВТ_ТаблицаЗаработнойПлаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтраженияЗарплатыВБухУчете
	|		ПО ВТ_ТаблицаЗаработнойПлаты.СпособОтраженияЗарплатыВБухУчете = СпособыОтраженияЗарплатыВБухУчете.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_НачисленнаяЗарплатаИВзносы.Сумма КАК Сумма,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
	|	ВТ_НачисленнаяЗарплатаИВзносы.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ВТ_НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	ВТ_НачисленнаяЗарплатаИВзносы.Подразделение КАК Подразделение,
	|	ВТ_НачисленнаяЗарплатаИВзносы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокОтраженияЗарплатыВБухУчете.Счет КАК Счет,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1 КАК Субконто1,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2 КАК Субконто2,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3 КАК Субконто3,
	|	ЕСТЬNULL(ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН, ВТ_НачисленнаяЗарплатаИВзносы.ОтражениеВУСН) КАК ОтражениеВУСН,
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|				И НЕ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОперацийДивиденды)
	|			ТОГДА &СчетУчетаПрочиеРасходы
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОпераций)
	|					ТОГДА ВЫБОР
	|							КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД
	|								ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|							КОГДА НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&МассивСчетовУчетаНУ)
	|								ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|							ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.СчетНУ
	|						КОНЕЦ
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СчетНУ,
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|				И НЕ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОперацийДивиденды)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОпераций)
	|						ТОГДА &РасходыПоУплатеСтраховыхВзносов
	|					ИНАЧЕ &ОплатаТрудаНеУчитываемаяДляНалогообложения
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОпераций)
	|					ТОГДА ВЫБОР
	|							КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД
	|								ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|							КОГДА НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&МассивСчетовУчетаНУ)
	|								ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|							ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.СубконтоНУ1
	|						КОНЕЦ
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СубконтоНУ1,
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|				И НЕ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОперацийДивиденды)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОпераций)
	|					ТОГДА ВЫБОР
	|							КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД
	|								ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|							КОГДА НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&МассивСчетовУчетаНУ)
	|								ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|							ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.СубконтоНУ2
	|						КОНЕЦ
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СубконтоНУ2,
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|				И НЕ ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОперацийДивиденды)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации В (&МассивОпераций)
	|					ТОГДА ВЫБОР
	|							КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД
	|								ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|							КОГДА НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&МассивСчетовУчетаНУ)
	|								ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|							ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.СубконтоНУ3
	|						КОНЕЦ
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СубконтоНУ3,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВедущийВидОперации КАК ВедущийВидОперации,
	|	ВТ_НачисленнаяЗарплатаИВзносы.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ВТ_НачисленнаяЗарплатаИВзносы.Резерв КАК Резерв,
	|	ВТ_НачисленнаяЗарплатаИВзносы.РезервБУ КАК РезервБУ,
	|	ВТ_НачисленнаяЗарплатаИВзносы.РезервНУ КАК РезервНУ
	|ПОМЕСТИТЬ ВТ_ТаблицаСПроводками
	|ИЗ
	|	ВТ_НачисленнаяЗарплатаИВзносы КАК ВТ_НачисленнаяЗарплатаИВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|		ПО ВТ_НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
	|			И (ВЫБОР
	|				КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ВТ_НачисленнаяЗарплатаИВзносы.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ
	|			КОНЕЦ = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСПроводками.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_ТаблицаСПроводками.Сумма КАК Сумма,
	|	ВТ_ТаблицаСПроводками.ВидОперации КАК ВидОперации,
	|	ВТ_ТаблицаСПроводками.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТ_ТаблицаСПроводками.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	ВТ_ТаблицаСПроводками.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ВТ_ТаблицаСПроводками.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	ВТ_ТаблицаСПроводками.Подразделение КАК Подразделение,
	|	ВТ_ТаблицаСПроводками.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаСПроводками.ОблагаетсяЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_ТаблицаСПроводками.Счет, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|						ТОГДА ПорядокОтраженияЗарплатыВБухУчете.Счет
	|					ИНАЧЕ ВТ_ТаблицаСПроводками.Счет
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ТаблицаСПроводками.Счет
	|	КОНЕЦ КАК Счет,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаСПроводками.ОблагаетсяЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_ТаблицаСПроводками.Счет, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|						ТОГДА ПорядокОтраженияЗарплатыВБухУчете.Субконто1
	|					ИНАЧЕ ВТ_ТаблицаСПроводками.Субконто1
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ТаблицаСПроводками.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаСПроводками.ОблагаетсяЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_ТаблицаСПроводками.Счет, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|						ТОГДА ПорядокОтраженияЗарплатыВБухУчете.Субконто2
	|					ИНАЧЕ ВТ_ТаблицаСПроводками.Субконто2
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ТаблицаСПроводками.Субконто2
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаСПроводками.ОблагаетсяЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_ТаблицаСПроводками.Счет, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|						ТОГДА ПорядокОтраженияЗарплатыВБухУчете.Субконто3
	|					ИНАЧЕ ВТ_ТаблицаСПроводками.Субконто3
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ТаблицаСПроводками.Субконто3
	|	КОНЕЦ КАК Субконто3,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаСПроводками.ОблагаетсяЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_ТаблицаСПроводками.Счет, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются)
	|					ИНАЧЕ ВТ_ТаблицаСПроводками.ОтражениеВУСН
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ТаблицаСПроводками.ОтражениеВУСН
	|	КОНЕЦ КАК ОтражениеВУСН,
	|	ВТ_ТаблицаСПроводками.СчетНУ КАК СчетНУ,
	|	ВТ_ТаблицаСПроводками.СубконтоНУ1 КАК СубконтоНУ1,
	|	ВТ_ТаблицаСПроводками.СубконтоНУ2 КАК СубконтоНУ2,
	|	ВТ_ТаблицаСПроводками.СубконтоНУ3 КАК СубконтоНУ3,
	|	ВТ_ТаблицаСПроводками.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
	|	ВТ_ТаблицаСПроводками.ВедущийВидОперации КАК ВедущийВидОперации,
	|	ВТ_ТаблицаСПроводками.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВТ_ТаблицаСПроводками.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ВТ_ТаблицаСПроводками.Резерв КАК Резерв,
	|	ВТ_ТаблицаСПроводками.РезервБУ КАК РезервБУ,
	|	ВТ_ТаблицаСПроводками.РезервНУ КАК РезервНУ
	|ПОМЕСТИТЬ ВТ_ИтоговаяТаблица
	|ИЗ
	|	ВТ_ТаблицаСПроводками КАК ВТ_ТаблицаСПроводками
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|		ПО ВТ_ТаблицаСПроводками.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
	|			И (ВТ_ТаблицаСПроводками.ОблагаетсяЕНВД)
	|			И (ЕСТЬNULL(ВТ_ТаблицаСПроводками.Счет, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИтоговаяТаблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_ИтоговаяТаблица.Сумма КАК Сумма,
	|	ВТ_ИтоговаяТаблица.ВидОперации КАК ВидОперации,
	|	ВТ_ИтоговаяТаблица.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТ_ИтоговаяТаблица.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	ВТ_ИтоговаяТаблица.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ВТ_ИтоговаяТаблица.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	ВТ_ИтоговаяТаблица.Подразделение КАК Подразделение,
	|	ВТ_ИтоговаяТаблица.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|						ТОГДА &СчетУчетаВзносыРезервов
	|					ИНАЧЕ &СчетУчетаРезервы
	|				КОНЕЦ
	|		КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|						ТОГДА &СчетУчетаПрочиеРасходы
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Счет
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.Счет
	|	КОНЕЦ КАК Счет,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ВТ_ИтоговаяТаблица.Резерв
	|		КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|						ТОГДА &РасходыПоУплатеСтраховыхВзносов
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто1
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|						ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто2
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто2
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|						ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто3
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто3
	|	КОНЕЦ КАК Субконто3,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.ОтражениеВУСН = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|				И (ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОперацийРасходовПоСтрахованию)
	|					ИЛИ ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОперацийНезарплатногоНДФЛ)
	|					ИЛИ ВТ_ИтоговаяТаблица.ВидОперацииРасходовЗаСчетФСС В (&МассивОперацийРасходовПоСтрахованию))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются)
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.ОтражениеВУСН
	|	КОНЕЦ КАК ОтражениеВУСН,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|				И ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ОблагаетсяЕНВД
	|						ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|					КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|						ТОГДА &СчетУчетаВзносыРезервов
	|					ИНАЧЕ &СчетУчетаРезервы
	|				КОНЕЦ
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ОблагаетсяЕНВД
	|						ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|					КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|						ТОГДА &СчетУчетаПрочиеРасходы
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Счет
	|				КОНЕЦ
	|		КОГДА ВТ_ИтоговаяТаблица.СчетНУ В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА &СчетУчетаПрочиеРасходы
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.СчетНУ
	|	КОНЕЦ КАК СчетНУ,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|				И ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|			ТОГДА ВТ_ИтоговаяТаблица.Резерв
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|						ТОГДА ВЫБОР
	|								КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|									ТОГДА &РасходыПоУплатеСтраховыхВзносов
	|								ИНАЧЕ &ОплатаТрудаНеУчитываемаяДляНалогообложения
	|							КОНЕЦ
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто1
	|				КОНЕЦ
	|		КОГДА ВТ_ИтоговаяТаблица.СчетНУ В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.ВидОперации В (&МассивОпераций)
	|						ТОГДА &РасходыПоУплатеСтраховыхВзносов
	|					ИНАЧЕ &ОплатаТрудаНеУчитываемаяДляНалогообложения
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.СубконтоНУ1
	|	КОНЕЦ КАК СубконтоНУ1,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|				И ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|						ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто2
	|				КОНЕЦ
	|		КОГДА ВТ_ИтоговаяТаблица.СчетНУ В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.СубконтоНУ2
	|	КОНЕЦ КАК СубконтоНУ2,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|				И ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.РезервНУ
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		КОГДА ВТ_ИтоговаяТаблица.РезервБУ
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ИтоговаяТаблица.Счет В (&СчетаУчетаНераспределеннаяПрибыль)
	|						ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|					ИНАЧЕ ВТ_ИтоговаяТаблица.Субконто3
	|				КОНЕЦ
	|		КОГДА ВТ_ИтоговаяТаблица.СчетНУ В (&СчетаУчетаНераспределеннаяПрибыль)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|		ИНАЧЕ ВТ_ИтоговаяТаблица.СубконтоНУ3
	|	КОНЕЦ КАК СубконтоНУ3,
	|	ВТ_ИтоговаяТаблица.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
	|	ВТ_ИтоговаяТаблица.ВедущийВидОперации КАК ВедущийВидОперации,
	|	ВТ_ИтоговаяТаблица.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА ВТ_ИтоговаяТаблица.ОблагаетсяЕНВД
	|			ТОГДА ВТ_ИтоговаяТаблица.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|	ВТ_ИтоговаяТаблица.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат
	|ИЗ
	|	ВТ_ИтоговаяТаблица КАК ВТ_ИтоговаяТаблица";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура РаспределитьЗарплатуПоВидамДеятельности(ТаблицаЗаработнойПлаты, Организация, ПериодРегистрации)
	
	Если ТаблицаЗаработнойПлаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПериодРегистрации < УчетЕНВДКлиентСервер.ДатаОтменыЕНВД() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, ПериодРегистрации) Тогда
		Возврат;
	КонецЕсли;
	
	ДоляПатента = УчетПСН.ДоляДоходовПатентнойСистемыНалогообложения(
		Организация, ПериодРегистрации, Перечисления.Периодичность.Месяц);
	
	Если ДоляПатента = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПустойСчет     = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	ПустоеСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка();
	ОтражениеВУСН  = Перечисления.ОтражениеВУСН.НеПринимаются;
	
	ПустойСпособОтражения = Новый Структура("Счет, Субконто1, Субконто2, Субконто3, ОтражениеВУСН",
		ПустойСчет, ПустоеСубконто, ПустоеСубконто, ПустоеСубконто, ОтражениеВУСН);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаЗаработнойПлаты", ТаблицаЗаработнойПлаты);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаЗаработнойПлаты.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	ТаблицаЗаработнойПлаты.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат
	|ПОМЕСТИТЬ ВТ_ТаблицаЗаработнойПлаты
	|ИЗ
	|	&ТаблицаЗаработнойПлаты КАК ТаблицаЗаработнойПлаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаЗаработнойПлаты.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете
	|ПОМЕСТИТЬ ВТ_РаспределяемыйСпособОтраженияВУчете
	|ИЗ
	|	ВТ_ТаблицаЗаработнойПлаты КАК ВТ_ТаблицаЗаработнойПлаты
	|ГДЕ
	|	ВТ_ТаблицаЗаработнойПлаты.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	ПорядокОтраженияЗарплатыВБухУчете.Счет КАК Счет,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1 КАК Субконто1,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2 КАК Субконто2,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3 КАК Субконто3,
	|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются) КАК ОтражениеВУСН
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|ГДЕ
	|	ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
	|	И ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете В
	|			(ВЫБРАТЬ
	|				ВТ_РаспределяемыйСпособОтраженияВУчете.СпособОтраженияЗарплатыВБухУчете
	|			ИЗ
	|				ВТ_РаспределяемыйСпособОтраженияВУчете)";
	
	ТаблицаСпособовОтраженияВУчете = Запрос.Выполнить().Выгрузить();
	
	МассивРаспределяемыхСтрок = ТаблицаЗаработнойПлаты.НайтиСтроки(Новый Структура("ВидДеятельностиДляНалоговогоУчетаЗатрат",
		Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты));
	
	Для Каждого ЗаписьМассива Из МассивРаспределяемыхСтрок Цикл
		
		СпособОтражения = ТаблицаСпособовОтраженияВУчете.Найти(ЗаписьМассива.СпособОтраженияЗарплатыВБухУчете,
			"СпособОтраженияЗарплатыВБухУчете");
		
		Если СпособОтражения = Неопределено Тогда
			СпособОтражения = ПустойСпособОтражения;
		КонецЕсли;
		
		Если ДоляПатента = 1 Тогда
			
			ЗаполнитьЗначенияСвойств(ЗаписьМассива, СпособОтражения);
			
		Иначе
			
			СуммаПоПатенту = Окр(ЗаписьМассива.Сумма * ДоляПатента, 2);
			ЗаписьМассива.Сумма = ЗаписьМассива.Сумма - СуммаПоПатенту;
			
			НоваяСтрока = ТаблицаЗаработнойПлаты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗаписьМассива);
			НоваяСтрока.Сумма = СуммаПоПатенту;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СпособОтражения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьПроводкиПоОтражениюЗарплатыВУчете(ТаблицаЗаработнойПлаты, Реквизиты, Движения, Отказ)
	
	Если ТаблицаЗаработнойПлаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ВестиУчетПоРаботникам   = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеПоРаботникам");
		
	ТаблицаНачисленийУдержаний = ПодготовитьТаблицуНачисленийУдержаний(ТаблицаЗаработнойПлаты,,,Реквизиты);
	ТаблицаНалоговВзносовСФОТ  = ПодготовитьТаблицуНалоговВзносовСФОТ(ТаблицаЗаработнойПлаты,,,Реквизиты);
	ТаблицаДепонентов          = ПодготовитьТаблицуДепонентов(Реквизиты, ТаблицаЗаработнойПлаты);
	
	МассивТаблицПоЗаработнойПлате = Новый Массив;
	МассивТаблицПоЗаработнойПлате.Добавить(ТаблицаНачисленийУдержаний);
	МассивТаблицПоЗаработнойПлате.Добавить(ТаблицаНалоговВзносовСФОТ);
	МассивТаблицПоЗаработнойПлате.Добавить(ТаблицаДепонентов);
	
	СодержанияПроводок = Новый Соответствие;	
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход,
									НСтр("ru = 'Начислена заработная плата'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НатуральныйДоход,
									НСтр("ru = 'Начислен доход в натуральной форме'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход,
									НСтр("ru = 'Начислена заработная плата (сдельно)'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДивидендыСотрудников,
									НСтр("ru = 'Начислены дивиденды'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.Дивиденды,
									НСтр("ru = 'Начислены дивиденды'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДоходыКонтрагентов,
									НСтр("ru = 'Начислен доход прочему физическому лицу'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВыплатыБывшимСотрудникам,
									НСтр("ru = 'Начислен доход бывшему работнику'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа,
									НСтр("ru = 'Начислен доход по договору авторского заказа'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги,
									НСтр("ru = 'Начислен доход по договору гражданско-правового характера'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛ,
									НСтр("ru = 'Удержан НДФЛ'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышения,
									НСтр("ru = 'Удержан НДФЛ с доходов свыше предельной величины'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке18,
									НСтр("ru = 'Удержан НДФЛ с доходов свыше предельной величины'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке20,
									НСтр("ru = 'Удержан НДФЛ с доходов свыше предельной величины'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке22,
									НСтр("ru = 'Удержан НДФЛ с доходов свыше предельной величины'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛДоначисленныйПоРезультатамПроверки,
									НСтр("ru = 'Доначислен НДФЛ'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НФДЛДивидендыСотрудникам,
									НСтр("ru = 'Удержан НДФЛ с дивидендов'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НФДЛДивиденды,
									НСтр("ru = 'Удержан НДФЛ с дивидендов'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛПрочиеРасчетыСПерсоналом,
									НСтр("ru = 'Удержан НДФЛ по прочим расчетам с персоналом'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛПередачаЗадолженностиВНалоговыйОрган,
									НСтр("ru = 'Задолженность по НДФЛ передана в налоговый орган'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛДоходыКонтрагентов,
									НСтр("ru = 'Удержан НДФЛ по расчетам с прочими физическими лицами'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛРасчетыСБывшимиСотрудниками,
									НСтр("ru = 'Удержан НДФЛ по расчетам с бывшим работником'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВзносыПоЕдиномуТарифу,
									НСтр("ru = 'Страховые взносы (единый тариф)'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорам,
									НСтр("ru = 'Обязательное пенсионное страхование по тарифу до предельной величины для иностранцев'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорамСПревышения,
									НСтр("ru = 'Обязательное пенсионное страхование по тарифу свыше предельной величины для иностранцев'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорам,
									НСтр("ru = 'Обязательное медицинское страхование по тарифу до предельной величины для иностранцев'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорамСПревышения,
									НСтр("ru = 'Обязательное медицинское страхование по тарифу свыше предельной величины для иностранцев'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорам,
									НСтр("ru = 'Обязательное социальное страхование по тарифу до предельной величины для иностранцев'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорамСПревышения,
									НСтр("ru = 'Обязательное социальное страхование по тарифу свыше предельной величины для иностранцев'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРНакопительнаяЧасть,
									НСтр("ru = 'Накопительная часть трудовой пенсии'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРСтраховаяЧасть,
									НСтр("ru = 'Страховая часть трудовой пенсии'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРПоСуммарномуТарифу,
									НСтр("ru = 'Обязательное пенсионное страхование'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРДоПредельнойВеличины,
									НСтр("ru = 'Обязательное пенсионное страхование'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРСПревышения,
									НСтр("ru = 'Обязательное пенсионное страхование по тарифу свыше предельной величины базы для начисления страховых взносов'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифЛЭ,
									НСтр("ru = 'Взносы на доплату к пенсии членам летных экипажей'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифШахтеры,
									НСтр("ru = 'Взносы на доплату к пенсии работникам угольной промышленности'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
									НСтр("ru = 'Дополнительные взносы на работников, занятым на работах с вредными условиями труда'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
									НСтр("ru = 'Дополнительные взносы на работников, занятым на работах с тяжелыми условиями труда'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки,
									НСтр("ru = 'Дополнительные взносы на работников, занятым на работах с вредными условиями труда при отсутствии спецоценки'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка,
									НСтр("ru = 'Дополнительные взносы на работников, занятым на работах с вредными условиями труда'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки,
									НСтр("ru = 'Дополнительные взносы на работников, занятым на работах с тяжелыми условиями труда при отсутствии спецоценки'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка,
									НСтр("ru = 'Дополнительные взносы на работников, занятым на работах с тяжелыми условиями труда'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ФСС,
									НСтр("ru = 'Расчеты по социальному страхованию'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ФССНС,
									НСтр("ru = 'Расчеты по обязательному социальному страхованию от НС и ПЗ'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ФФОМС,
									НСтр("ru = 'Федеральный фонд ОМС'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ТФОМС,
									НСтр("ru = 'Территориальный фонд ОМС'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС,
									НСтр("ru = 'Расчеты по обязательному социальному страхованию от НС и ПЗ'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС,
									НСтр("ru = 'Расчеты по социальному страхованию'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.Депонирование,
									НСтр("ru = 'Депонирование невыплаченной заработной платы'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.СписаниеДепонента,
									НСтр("ru = 'Списание депонированной заработной платы'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПрочиеРасчетыСПерсоналом,
									НСтр("ru = 'Прочие расчеты с персоналом'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.МатериальнаяПомощь,
									НСтр("ru = 'Материальная помощь'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель,
									НСтр("ru = 'Оплата больничного за счет работодателя'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты,
									НСтр("ru = 'Компенсация за задержку зарплаты'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск,
									НСтр("ru = 'Начислен ежегодный отпуск'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства,
									НСтр("ru = 'Начислен ежегодный отпуск'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы,
									НСтр("ru = 'Начислен ежегодный отпуск'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы,
									НСтр("ru = 'Начислен ежегодный отпуск'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска,
									НСтр("ru = 'Компенсация ежегодного отпуска'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства,
									НСтр("ru = 'Компенсация ежегодного отпуска'"));
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение,
									НСтр("ru = 'Социальное пособие на погребение'"));
	
	СчетРасчетовСПерсоналомПоОплатеТруда = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
	
	Для Каждого ТаблицаМассива ИЗ МассивТаблицПоЗаработнойПлате Цикл
		
		Если ТаблицаМассива.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьКолонкаСчетНУ = Ложь;
		Если ТаблицаМассива.Колонки.Найти("СчетНУ") <> Неопределено Тогда
			ЕстьКолонкаСчетНУ = Истина;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ТаблицаМассива Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Счет)
				ИЛИ НЕ ЗначениеЗаполнено(СтрокаТаблицы.КорСчет) Тогда
				// не заполнены счет и аналитика
				Продолжить;
			КонецЕсли;
			
			Если СтрокаТаблицы.Сумма = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			УказанСчетНУ = Ложь;
			Если ЕстьКолонкаСчетНУ Тогда
				УказанСчетНУ = ЗначениеЗаполнено(СтрокаТаблицы.СчетНУ);
			КонецЕсли;
			
			Проводка = Движения.Хозрасчетный.Добавить();
			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;
			Проводка.Сумма       = СтрокаТаблицы.Сумма;
			Проводка.Содержание  = СодержанияПроводок.Получить(СтрокаТаблицы.ВидОперации);
			
			Проводка.СчетДт = СтрокаТаблицы.Счет;
			
			Если СтрокаТаблицы.Счет = СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				Если ВестиУчетПоРаботникам Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РаботникиОрганизаций", СтрокаТаблицы.Субконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидНачисленийОплатыТрудаПоСтатье255НК", СтрокаТаблицы.Субконто2);
				Иначе
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидНачисленийОплатыТрудаПоСтатье255НК", СтрокаТаблицы.Субконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.Субконто2);
				КонецЕсли;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.Субконто3);
			Иначе
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.Субконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.Субконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.Субконто3);
			КонецЕсли;
			
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если ОтражатьВНалоговомУчете И СвойстваСчетаДт.НалоговыйУчет Тогда
				Если СтрокаТаблицы.Счет = СчетРасчетовСПерсоналомПоОплатеТруда Тогда
					Проводка.СуммаНУДт = СтрокаТаблицы.Сумма;
				Иначе
					Проводка.СуммаНУДт = СтрокаТаблицы.СуммаНУ;
					Если УказанСчетНУ Тогда
						Проводка.СуммаВРДт = СтрокаТаблицы.Сумма - Проводка.СуммаНУДт;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.КорСчет;
			
			Если СтрокаТаблицы.КорСчет = СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				Если ВестиУчетПоРаботникам Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РаботникиОрганизаций", СтрокаТаблицы.КорСубконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидНачисленийОплатыТрудаПоСтатье255НК", СтрокаТаблицы.КорСубконто2);
				Иначе
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидНачисленийОплатыТрудаПоСтатье255НК", СтрокаТаблицы.КорСубконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.КорСубконто2);
				КонецЕсли;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.КорСубконто3);
			Иначе
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.КорСубконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.КорСубконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.КорСубконто3);
			КонецЕсли;
			
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если ОтражатьВНалоговомУчете И СвойстваСчетаКт.НалоговыйУчет Тогда
				Если СтрокаТаблицы.КорСчет = СчетРасчетовСПерсоналомПоОплатеТруда Тогда
					Проводка.СуммаНУКт = СтрокаТаблицы.Сумма;
				Иначе
					Проводка.СуммаНУКт = СтрокаТаблицы.СуммаНУ;
					Если УказанСчетНУ Тогда
						Проводка.СуммаВРКт = СтрокаТаблицы.Сумма - Проводка.СуммаНУКт;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если УказанСчетНУ 
				И ОтражатьВНалоговомУчете Тогда
				
				// указан счет НУ - отличный от БУ
				// это в частности страховые взносы 
				// по переходящим отпускам
				
				СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетНУ);
				Если НЕ СвойстваСчетаДт.НалоговыйУчет Тогда
					Продолжить;
				КонецЕсли;
				
				Проводка = Движения.Хозрасчетный.Добавить();
				Проводка.Период      = Реквизиты.Период;
				Проводка.Организация = Реквизиты.Организация;
				Проводка.СуммаНУДт   = СтрокаТаблицы.Сумма;
				Проводка.СуммаВРДт   = - СтрокаТаблицы.Сумма;
				Проводка.Содержание  = СодержанияПроводок.Получить(СтрокаТаблицы.ВидОперации);
				
				Проводка.СчетДт = СтрокаТаблицы.СчетНУ;
				
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.СубконтоНУ1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.СубконтоНУ2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.СубконтоНУ3);
				
				СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
				Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
					Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
				КонецЕсли;
				
				Проводка.СчетКт = СтрокаТаблицы.КорСчет;
				
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.КорСубконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.КорСубконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.КорСубконто3);
				
				СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
				Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
					Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Отражение в ЕНС
	Если ЕдиныйНалоговыйСчет.ЭтоПростойУчет(Реквизиты.Период, Реквизиты.Организация) Тогда
		Если ЗначениеЗаполнено(ТаблицаНалоговВзносовСФОТ) Тогда
			
			МесяцУплатыВзносов = ДобавитьМесяц(Реквизиты.Период, 1);
			СрокУплаты = Дата(Год(МесяцУплатыВзносов), Месяц(МесяцУплатыВзносов), 28);
			
			ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
			ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
			ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
			ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
				ПроизводственныйКалендарь,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СрокУплаты),
				ПараметрыПолучения);
			СрокУплаты = ГрафикПереноса[СрокУплаты];
			СчетУчетаЕНС = ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет;
			
			СчетаУчетаСтраховыхВзносов = ЕдиныйНалоговыйСчетПовтИсп.ОбслуживаемыеСчетаУчетаСтраховыхВзносов(Реквизиты.Период);
			ТаблицаНалоговВзносовСФОТ.Свернуть("КорСчет, КорСубконто1, КорСубконто2, КорСубконто3", "Сумма");
			НалогиКУплате = ЕдиныйНалоговыйСчет.НовыйНачислениеНалоговНаЕНС();
			
			Для Каждого СчетУчетаСтраховыхВзносов Из СчетаУчетаСтраховыхВзносов Цикл
				МассивСтрок = ТаблицаНалоговВзносовСФОТ.НайтиСтроки(Новый Структура("КорСчет", СчетУчетаСтраховыхВзносов));
				Для Каждого СтрокаМассива Из МассивСтрок Цикл
					
					НоваяСтрока = НалогиКУплате.Добавить();
					
					НоваяСтрока.СчетУчета = СтрокаМассива.КорСчет;
					НоваяСтрока.Субконто1 = СтрокаМассива.КорСубконто1;
					НоваяСтрока.Субконто2 = СтрокаМассива.КорСубконто2;
					НоваяСтрока.Субконто3 = СтрокаМассива.КорСубконто3;
					НоваяСтрока.СрокУплаты = СрокУплаты;
					
					НоваяСтрока.СуммаНалога = СтрокаМассива.Сумма;
					НоваяСтрока.Начислено = СтрокаМассива.Сумма;
					НоваяСтрока.Сумма = СтрокаМассива.Сумма;
					
					НоваяСтрока.НачалоПериода = НачалоМесяца(Реквизиты.Период);
					НоваяСтрока.КонецПериода = КонецМесяца(Реквизиты.Период);
					
					НоваяСтрока.Содержание = НСтр("ru = 'Учет страховых взносов в составе совокупной налоговой обязанности'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
					
				КонецЦикла;
			КонецЦикла;
			
			ЕдиныйНалоговыйСчет.СформироватьДвиженияНачислениеНалоговНаЕНС(НалогиКУплате, Реквизиты, Движения);
			
		КонецЕсли;
	КонецЕсли;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция СоздатьКлючСортировки(ИсходныеДанные)
	
	ПриоритетВидаОперации = 1000;
	
	// Доходы
	// Диапазон ключей 1-100
	// 1-50   :: Доходы работников
	// 51-70  :: Договора
	// 71-80  :: Дивиденды
	// 81-100 :: Прочие

	Если ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход Тогда
		ПриоритетВидаОперации = 1;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход Тогда
		ПриоритетВидаОперации = 2;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НатуральныйДоход Тогда
		ПриоритетВидаОперации = 3;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.МатериальнаяПомощь Тогда
		ПриоритетВидаОперации = 4;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет Тогда
		ПриоритетВидаОперации = 5;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель Тогда
		ПриоритетВидаОперации = 6;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС Тогда
		ПриоритетВидаОперации = 7;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС Тогда
		ПриоритетВидаОперации = 8;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск Тогда
		ПриоритетВидаОперации = 9;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы Тогда
		ПриоритетВидаОперации = 10;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства Тогда
		ПриоритетВидаОперации = 11;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы Тогда
		ПриоритетВидаОперации = 12;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска Тогда
		ПриоритетВидаОперации = 31;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства Тогда
		ПриоритетВидаОперации = 32;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги Тогда
		ПриоритетВидаОперации = 51;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДоходыКонтрагентов Тогда
		ПриоритетВидаОперации = 52;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа Тогда
		ПриоритетВидаОперации = 53;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДивидендыСотрудников Тогда
		ПриоритетВидаОперации = 71;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Дивиденды Тогда
		ПриоритетВидаОперации = 72;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПрочиеРасчетыСПерсоналом Тогда
		ПриоритетВидаОперации = 82;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента Тогда
		ПриоритетВидаОперации = 83;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВыплатыБывшимСотрудникам Тогда
		ПриоритетВидаОперации = 84;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение Тогда
		ПриоритетВидаОперации = 85;
	КонецЕсли;
	
	// Удержания
	// Диапазон ключей 201-300
	// 201-220 :: НДФЛ
	// 221-240 :: Исполительные документы
	// 241-260 :: Удержания текущей деятельности
	// 261-270 :: Удержания текущей деятельности (нарушения)
	// 271–280 :: Удержания добровольных взносов
	// 291-300 :: Прочие
	Если ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛ Тогда
		ПриоритетВидаОперации = 201;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛДоходыКонтрагентов Тогда
		ПриоритетВидаОперации = 202;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НФДЛДивидендыСотрудникам Тогда
		ПриоритетВидаОперации = 203;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛДоначисленныйПоРезультатамПроверки Тогда
		ПриоритетВидаОперации = 204;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НФДЛДивиденды Тогда
		ПриоритетВидаОперации = 205;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛПрочиеРасчетыСПерсоналом Тогда
		ПриоритетВидаОперации = 206;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛРасчетыСБывшимиСотрудниками Тогда
		ПриоритетВидаОперации = 207;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышения Тогда
		ПриоритетВидаОперации = 208;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке18 Тогда
		ПриоритетВидаОперации = 209;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке20 Тогда
		ПриоритетВидаОперации = 210;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке22 Тогда
		ПриоритетВидаОперации = 211;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛПередачаЗадолженностиВНалоговыйОрган Тогда
		ПриоритетВидаОперации = 219;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты Тогда
		ПриоритетВидаОперации = 221;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Профвзносы Тогда
		ПриоритетВидаОперации = 241;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПроцентыПоЗайму Тогда
		ПриоритетВидаОперации = 242;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПогашениеЗаймов Тогда
		ПриоритетВидаОперации = 243;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВозмещениеУщерба Тогда
		ПриоритетВидаОперации = 261;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм Тогда
		ПриоритетВидаОперации = 262;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.УдержаниеЗаОтпуск Тогда
		ПриоритетВидаОперации = 263;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.УдержаниеПоПрочимОперациямСРаботниками Тогда
		ПриоритетВидаОперации = 264;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДСВ Тогда
		ПриоритетВидаОперации = 271;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ Тогда
		ПриоритетВидаОперации = 272;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоПроцентовПоЗайму Тогда
		ПриоритетВидаОперации = 291;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛистыКонтрагенты Тогда
		ПриоритетВидаОперации = 292;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгентаКонтрагенты Тогда
		ПриоритетВидаОперации = 293;
	КонецЕсли;
	
	// Взносы
	// Диапазон ключей 301-400
	Если ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ФСС Тогда
		ПриоритетВидаОперации = 301;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРПоСуммарномуТарифу Тогда
		ПриоритетВидаОперации = 302;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРСтраховаяЧасть Тогда
		ПриоритетВидаОперации = 303;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРНакопительнаяЧасть Тогда
		ПриоритетВидаОперации = 304;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ФФОМС Тогда
		ПриоритетВидаОперации = 305;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ТФОМС Тогда
		ПриоритетВидаОперации = 306;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ФССНС Тогда
		ПриоритетВидаОперации = 307;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифШахтеры Тогда
		ПриоритетВидаОперации = 308;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифЛЭ Тогда
		ПриоритетВидаОперации = 309;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах Тогда
		ПриоритетВидаОперации = 310;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах Тогда
		ПриоритетВидаОперации = 311;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРДоПредельнойВеличины Тогда
		ПриоритетВидаОперации = 312;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРСПревышения Тогда
		ПриоритетВидаОперации = 313;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки Тогда
		ПриоритетВидаОперации = 314;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка Тогда
		ПриоритетВидаОперации = 315;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки Тогда
		ПриоритетВидаОперации = 316;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка Тогда
		ПриоритетВидаОперации = 317;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВзносыПоЕдиномуТарифу Тогда
		ПриоритетВидаОперации = 318;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорам Тогда
		ПриоритетВидаОперации = 319;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорамСПревышения Тогда
		ПриоритетВидаОперации = 320;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорам Тогда
		ПриоритетВидаОперации = 321;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорамСПревышения Тогда
		ПриоритетВидаОперации = 322;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорам Тогда
		ПриоритетВидаОперации = 323;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорамСПревышения Тогда
		ПриоритетВидаОперации = 324;
	КонецЕсли;
	
	// Расчеты
	// Диапазон ключей 401-...
	// 401-410  :: Депоненты
	// 411-420  :: Компенсации
	// 421-430  :: Расчеты по налогам
	// 431-...  :: Расчеты по доходам
	Если ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Депонирование Тогда
		ПриоритетВидаОперации = 401;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.СписаниеДепонента Тогда
		ПриоритетВидаОперации = 402;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты Тогда
		ПриоритетВидаОперации = 411;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛПередачаЗадолженностиВНалоговыйОрган Тогда
		ПриоритетВидаОперации = 421;
	ИначеЕсли ИсходныеДанные.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок Тогда
		ПриоритетВидаОперации = 431;
	КонецЕсли;
	
	Возврат ПриоритетВидаОперации;
		
КонецФункции

Функция ПодготовитьТаблицуНачисленийУдержаний(ТаблицаЗаработнойПлаты, УСН = Ложь, ИП = Ложь, Реквизиты)
	
	НовыйУчетПоДоговорамПодряда = Год(Реквизиты.Период) >= 2016;
	НовыйУчетПоБольничным       = Реквизиты.Период >= ДатаНачалаАдминистрированияСтраховыхВзносовФНС();
	НовыйУчетДивидендов         = Год(Реквизиты.Период) >= 2018;
	
	ТаблицаНачисленийИУдержаний = ТаблицаЗаработнойПлаты.СкопироватьКолонки();
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСчет");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСубконто1");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСубконто2");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСубконто3");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КлючСортировки");
	
	ЕстьУчетНУ = Ложь;
	Если ТаблицаЗаработнойПлаты.Найти(Перечисления.ВидыОперацийПоЗарплате.МатериальнаяПомощь, "ВидОперации") <> Неопределено
		ИЛИ ТаблицаЗаработнойПлаты.Найти(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы, "ВидОперации") <> Неопределено
		ИЛИ ТаблицаЗаработнойПлаты.Найти(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства , "ВидОперации") <> Неопределено
		ИЛИ ТаблицаЗаработнойПлаты.Найти(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства , "ВидОперации") <> Неопределено Тогда
		ЕстьУчетНУ = Истина;
	ИначеЕсли ЕстьНастройкиРезервовПоОплатеТруда(Реквизиты.Организация, Реквизиты.Период) Тогда
		ЕстьУчетНУ = Истина;
	Иначе
		МассивСчетов = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль);
		Для Каждого Счет ИЗ МассивСчетов Цикл
			Если ТаблицаЗаработнойПлаты.Найти(Счет, "Счет") <> Неопределено Тогда
				ЕстьУчетНУ = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Счета учета
	СчетРасчетовСПерсоналомПоОплатеТруда        = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;             // 70
	РасчетыПоПредоставленнымЗаймам              = ПланыСчетов.Хозрасчетный.РасчетыПоПредоставленнымЗаймам;              // 73.01
	РасчетыПоПрочимОперациям                    = ПланыСчетов.Хозрасчетный.РасчетыПоПрочимОперациям;                    // 73.03
	РасчетыПоВыплатеДоходов                     = ПланыСчетов.Хозрасчетный.РасчетыПоВыплатеДоходов;                     // 75.02
	ПрочиеРасчетыСРазнымиДебиторамиИКредиторами = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами; // 76.09
	ПрочиеРасчетыСФизическимиЛицами             = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами;             // 76.10
	ПрочиеРасходы                               = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;                               // 91.02
	СчетРезерваОтпусков                         = ПланыСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям;     // 96.01.1
	РасчетыПоБольничнымФСС                      = ПланыСчетов.Хозрасчетный.ФСС;                                         // 69.01
	РасчетыПоБольничнымФССНС                    = ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ;                                   // 69.11
	СчетУчетаНДФЛ                               = ПланыСчетов.Хозрасчетный.НДФЛ;                                        // 68.01
	ПустойСчет                                  = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	
	// Соответствие статей расходов и счетов учета
	СоответствиеСтатейРасходов = ПолучитьСоответствиеСтатейРасходов();
	
	// Аналитика учета
	ВидПлатежаВГосБюджет                        = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	ВидПлатежаВГосБюджетАкт                     = Перечисления.ВидыПлатежейВГосБюджет.НалогАкт;
	ВидПлатежаВГосБюджетРасходыПоСтрахованию    = Перечисления.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию;
	ВидыНачисленийОплатыТрудаДляНУ_пп7ст255     = Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп7ст255;
	ВидыНачисленийОплатыТрудаДляНУ_пп8ст255     = Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп8ст255;
	ПустоеСубконто                              = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка();
	
	СтатьиПрочихРасходов = КлассификаторыДоходовРасходов.НовыйКешГрупповойОперации(Справочники.ПрочиеДоходыИРасходы);
	
	НастройкиУчетаЗарплаты = РегистрыСведений.НастройкиУчетаЗарплаты.Получить(Новый Структура("Организация", Реквизиты.Организация));
	РезервОтпусковЭлемент  = НастройкиУчетаЗарплаты.РезервОтпусков;
	
	ВестиУчетПоРаботникам = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеПоРаботникам")
			ИЛИ ((УСН ИЛИ ИП) И ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии"));

	УчетРезервовОтпусковНУ = ВедетсяУчетРезервовОтпусковПоВидамУчета(Реквизиты.Организация, Реквизиты.Период, "НУ");
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаЗаработнойПлаты Цикл
		
		Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДивидендыСотрудников
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.МатериальнаяПомощь
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска
			Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
				НоваяСтрока.КорСубконто2 = СтрокаТаблицы.ВидНачисленияОплатыТрудаДляНУ;
				
			Иначе
				
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ВидНачисленияОплатыТрудаДляНУ;
				
			КонецЕсли;
			
			Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС
				ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС Тогда
				Если (НЕ ЗначениеЗаполнено(НоваяСтрока.Счет)) // на случай когда счет не пришел
					ИЛИ // на случай когда счет задан и надо сохранить аналитику
					(ЗначениеЗаполнено(НоваяСтрока.Счет) И (НоваяСтрока.Счет <> РасчетыПоБольничнымФССНС) И (НоваяСтрока.Счет <> РасчетыПоБольничнымФСС))
					Тогда
					Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС Тогда
						НоваяСтрока.Счет = РасчетыПоБольничнымФССНС;
					Иначе
						НоваяСтрока.Счет = РасчетыПоБольничнымФСС;
					КонецЕсли;
					Если НовыйУчетПоБольничным Тогда
						НоваяСтрока.Субконто1 = ВидПлатежаВГосБюджет;
					Иначе
						НоваяСтрока.Субконто1 = ВидПлатежаВГосБюджетРасходыПоСтрахованию;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
				НоваяСтрока.КорСубконто2 = ВидыНачисленийОплатыТрудаДляНУ_пп7ст255;
				
			Иначе
				
				НоваяСтрока.КорСубконто1 = ВидыНачисленийОплатыТрудаДляНУ_пп7ст255;
				
			КонецЕсли;
			
			НоваяСтрока.Счет      = СчетРезерваОтпусков;
			НоваяСтрока.Субконто1 = РезервОтпусковЭлемент;
			НоваяСтрока.Субконто2 = ПустоеСубконто;
			НоваяСтрока.Субконто3 = ПустоеСубконто;
			
			Если НЕ СтрокаТаблицы.ОблагаетсяЕНВД Тогда
				Если УчетРезервовОтпусковНУ
					И НЕ ЗначениеЗаполнено(НоваяСтрока.СуммаНУ) Тогда
					НоваяСтрока.СуммаНУ = НоваяСтрока.Сумма;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
				НоваяСтрока.КорСубконто2 = ВидыНачисленийОплатыТрудаДляНУ_пп7ст255;
				
			Иначе
				
				НоваяСтрока.КорСубконто1 = ВидыНачисленийОплатыТрудаДляНУ_пп7ст255;
				
			КонецЕсли;
			
			Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства Тогда
				
				Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					НоваяСтрока.СуммаНУ = 0;
				КонецЕсли;
				
				НоваяСтрока.Счет      = СчетРезерваОтпусков;
				НоваяСтрока.Субконто1 = РезервОтпусковЭлемент;
				НоваяСтрока.Субконто2 = ПустоеСубконто;
				НоваяСтрока.Субконто3 = ПустоеСубконто;
			
			ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы
				И НЕ СтрокаТаблицы.ОблагаетсяЕНВД Тогда
				
				НоваяСтрока.СчетНУ      = СчетРезерваОтпусков;
				НоваяСтрока.СубконтоНУ1 = РезервОтпусковЭлемент;
				НоваяСтрока.СубконтоНУ2 = ПустоеСубконто;
				НоваяСтрока.СубконтоНУ3 = ПустоеСубконто;
				
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
				НоваяСтрока.КорСубконто2 = ВидыНачисленийОплатыТрудаДляНУ_пп8ст255;
				
			Иначе
				
				НоваяСтрока.КорСубконто1 = ВидыНачисленийОплатыТрудаДляНУ_пп8ст255;
				
			КонецЕсли;
			
			НоваяСтрока.Счет      = СчетРезерваОтпусков;
			НоваяСтрока.Субконто1 = РезервОтпусковЭлемент;
			НоваяСтрока.Субконто2 = ПустоеСубконто;
			НоваяСтрока.Субконто3 = ПустоеСубконто;
			
			Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
				НоваяСтрока.СуммаНУ = 0;
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПрочиеРасчетыСПерсоналом Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = РасчетыПоПрочимОперациям;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДоходыКонтрагентов
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВыплатыБывшимСотрудникам Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			Если НовыйУчетПоДоговорамПодряда Тогда
				
				ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
				Если ТекущийСчетУчета = Неопределено Тогда
					ТекущийСчетУчета  = ПрочиеРасчетыСФизическимиЛицами;
				КонецЕсли;
				
				НоваяСтрока.КорСчет      = ТекущийСчетУчета;
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			Иначе
				НоваяСтрока.КорСчет      = ПрочиеРасчетыСРазнымиДебиторамиИКредиторами;
				НоваяСтрока.КорСубконто1 = ПустоеСубконто; //Контрагент
				НоваяСтрока.КорСубконто2 = ПустоеСубконто; //Договор
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = ПрочиеРасчетыСФизическимиЛицами;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Счет) Тогда
				НоваяСтрока.Счет      = РасчетыПоБольничнымФСС;
				НоваяСтрока.Субконто1 = ВидПлатежаВГосБюджет;
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Дивиденды Тогда
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = РасчетыПоВыплатеДоходов;
			КонецЕсли;
			
			Если УСН И ТекущийСчетУчета  = РасчетыПоВыплатеДоходов Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			Если НовыйУчетДивидендов Тогда
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо; //Учредитель
			Иначе
				НоваяСтрока.КорСубконто1 = ПустоеСубконто; //Контрагент
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			КонецЕсли;
			НоваяСтрока.Счет      = ПрочиеРасходы;
			НоваяСтрока.Субконто1 = КлассификаторыДоходовРасходов.ПредопределенныйЭлементГрупповойОперации(
				"КомпенсацияЗаЗадержкуЗарплаты",
				СтатьиПрочихРасходов);
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛ
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НФДЛДивидендыСотрудникам Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.Счет         = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			Иначе
				
				НоваяСтрока.Субконто1    = ПустоеСубконто;
				
			КонецЕсли;
			
			НоваяСтрока.Субконто2    = ПустоеСубконто;
			НоваяСтрока.Субконто3    = ПустоеСубконто;
			
			НоваяСтрока.КорСчет      = СчетУчетаНДФЛ;
			НоваяСтрока.КорСубконто1 = ВидПлатежаВГосБюджет;
			НоваяСтрока.КорСубконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛДоначисленныйПоРезультатамПроверки Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.Счет         = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			Иначе
				
				НоваяСтрока.Субконто1    = ПустоеСубконто;
				
			КонецЕсли;
			
			НоваяСтрока.Субконто2    = ПустоеСубконто;
			НоваяСтрока.Субконто3    = ПустоеСубконто;
			
			НоваяСтрока.КорСчет      = СчетУчетаНДФЛ;
			НоваяСтрока.КорСубконто1 = ВидПлатежаВГосБюджетАкт;
			НоваяСтрока.КорСубконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛПрочиеРасчетыСПерсоналом Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = РасчетыПоПрочимОперациям;
			КонецЕсли;
			
			НоваяСтрока.Счет         = ТекущийСчетУчета;
			НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			НоваяСтрока.Субконто2    = ПустоеСубконто;
			НоваяСтрока.Субконто3    = ПустоеСубконто;
			
			НоваяСтрока.КорСчет      = СчетУчетаНДФЛ;
			НоваяСтрока.КорСубконто1 = ВидПлатежаВГосБюджет;
			НоваяСтрока.КорСубконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛПередачаЗадолженностиВНалоговыйОрган Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			НоваяСтрока.Счет         = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			Иначе
				
				НоваяСтрока.Субконто1    = ПустоеСубконто;
				
			КонецЕсли;
			
			НоваяСтрока.Субконто2    = ПустоеСубконто;
			НоваяСтрока.Субконто3    = ПустоеСубконто;
			
			НоваяСтрока.КорСчет      = СчетУчетаНДФЛ;
			НоваяСтрока.КорСубконто1 = ВидПлатежаВГосБюджетАкт;
			НоваяСтрока.КорСубконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			НоваяСтрока.Сумма        = - НоваяСтрока.Сумма;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛДоходыКонтрагентов 
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛРасчетыСБывшимиСотрудниками Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			Если НовыйУчетПоДоговорамПодряда Тогда
				
				ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
				Если ТекущийСчетУчета = Неопределено Тогда
					ТекущийСчетУчета  = ПрочиеРасчетыСФизическимиЛицами;
				КонецЕсли;
				
				НоваяСтрока.Счет         = ТекущийСчетУчета;
				НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
				НоваяСтрока.Субконто2    = ПустоеСубконто; // Договор
				НоваяСтрока.Субконто3    = ПустоеСубконто;
			Иначе
				НоваяСтрока.Счет         = ПрочиеРасчетыСРазнымиДебиторамиИКредиторами;
				НоваяСтрока.Субконто1    = ПустоеСубконто; // Контрагент
				НоваяСтрока.Субконто2    = ПустоеСубконто; // Договор
				НоваяСтрока.Субконто3    = ПустоеСубконто;
			КонецЕсли;
			
			НоваяСтрока.КорСчет      = СчетУчетаНДФЛ;
			НоваяСтрока.КорСубконто1 = ВидПлатежаВГосБюджет;
			НоваяСтрока.КорСубконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НФДЛДивиденды Тогда
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = РасчетыПоВыплатеДоходов;
			КонецЕсли;
			
			Если УСН И ТекущийСчетУчета  = РасчетыПоВыплатеДоходов Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			НоваяСтрока.Счет         = ТекущийСчетУчета;
			Если НовыйУчетДивидендов Тогда
				НоваяСтрока.Субконто1 = СтрокаТаблицы.ФизическоеЛицо; //Учредитель
			Иначе
				НоваяСтрока.Субконто1 = ПустоеСубконто; //Контрагент
			КонецЕсли;
			НоваяСтрока.Субконто2    = ПустоеСубконто;
			НоваяСтрока.Субконто3    = ПустоеСубконто;
			
			НоваяСтрока.КорСчет      = СчетУчетаНДФЛ;
			НоваяСтрока.КорСубконто1 = ВидПлатежаВГосБюджет;
			НоваяСтрока.КорСубконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышения
			Или СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке18
			Или СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке20
			Или СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке22 Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			
			Если УСН И ТекущийСчетУчета  = РасчетыПоВыплатеДоходов Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока.Счет         = ТекущийСчетУчета;
			
			Если ВестиУчетПоРаботникам ИЛИ ТекущийСчетУчета <> СчетРасчетовСПерсоналомПоОплатеТруда Тогда
				
				НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			Иначе
				
				НоваяСтрока.Субконто1    = ПустоеСубконто;
				
			КонецЕсли;
			
			НоваяСтрока.Субконто2    = ПустоеСубконто;
			НоваяСтрока.Субконто3    = ПустоеСубконто;
			
			НоваяСтрока.КорСчет      = СчетУчетаНДФЛ;
			НоваяСтрока.КорСубконто1 = ВидПлатежаВГосБюджет;
			НоваяСтрока.КорСубконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		КонецЕсли;
			
		
	КонецЦикла;
	
	// Замена статей затрат, видов прочих расходов.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления <> НЕОПРЕДЕЛЕНО
	|			ТОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатрат ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяСсылка)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяЗатратНачисления,
	|	СтатьиЗатратПоЗарплате.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	РегистрСведений.СтатьиЗатратПоЗарплате КАК СтатьиЗатратПоЗарплате
	|ГДЕ
	|	СтатьиЗатратПоЗарплате.ВидСтатьиЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДополнительныхСтатейЗатрат.ПособияЗаСчетРаботодателя)";
	
	СтатьиЗатрат_ПособияЗаСчетРаботодателя = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНачисленийИУдержаний Цикл
		
		Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель Тогда
			
			// Больничный за счет работодателя.
			
			Для НомерСубконто = 1 По 3 Цикл
				
				ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто, , СтатьиЗатрат_ПособияЗаСчетРаботодателя);
				Если ЕстьУчетНУ И НЕ (УСН ИЛИ ИП) Тогда
					ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто, "НУ", СтатьиЗатрат_ПособияЗаСчетРаботодателя);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаСвертки = "КлючСортировки, ВидОперации, Подразделение, Счет, Субконто1, Субконто2, Субконто3, КорСчет, КорСубконто1, КорСубконто2, КорСубконто3";
	Если УСН Тогда
		СтрокаСвертки = СтрокаСвертки + ", ОтражениеВУСН, РасчетныйДокумент";
	КонецЕсли;
	
	Если ИП Тогда
		СтрокаСвертки = СтрокаСвертки + ", ВидНачисленияУдержания";
	КонецЕсли;
	
	Если ЕстьУчетНУ И НЕ (УСН ИЛИ ИП) Тогда
		СтрокаСвертки = СтрокаСвертки + ", СчетНУ, СубконтоНУ1, СубконтоНУ2, СубконтоНУ3";
	КонецЕсли;
	
	ТаблицаНачисленийИУдержаний.Свернуть(СтрокаСвертки, "Сумма, СуммаНУ");
	ТаблицаНачисленийИУдержаний.Сортировать(СтрокаСвертки);
	
	Возврат ТаблицаНачисленийИУдержаний;
		
КонецФункции

Функция ПодготовитьТаблицуНалоговВзносовСФОТ(ТаблицаЗаработнойПлаты, УСН = Ложь, ИП = Ложь, Реквизиты)
	
	СчетаУчетаНалоговВзносовСФОТ = СоответствиеСчетовУчетаНалоговВзносовСФОТВидамОпераций();
	
	ОперацииУчетаОсобыхДопВзносов = ОперацииУчетаОсобыхДопВзносов();
	
	ТаблицаНалоговВзносовСФОТ = ТаблицаЗаработнойПлаты.СкопироватьКолонки();
	ТаблицаНалоговВзносовСФОТ.Колонки.Добавить("КорСчет");
	ТаблицаНалоговВзносовСФОТ.Колонки.Добавить("КорСубконто1");
	ТаблицаНалоговВзносовСФОТ.Колонки.Добавить("КорСубконто2");
	ТаблицаНалоговВзносовСФОТ.Колонки.Добавить("КорСубконто3");
	ТаблицаНалоговВзносовСФОТ.Колонки.Добавить("КлючСортировки");
	
	// Счета учета
	РасчетыПоБольничнымФСС   = ПланыСчетов.Хозрасчетный.ФСС;                                      // 69.01
	РасчетыПоБольничнымФССНС = ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ;                                // 69.11
	ПрочиеРасходы            = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;                            // 91.02
	СчетУчетаРезерва         = ПланыСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям;  // 96.01.1
	СчетУчетаВзносовРезерва  = ПланыСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам; // 96.01.2
	
	МассивСчет84 = Новый Массив();
	ВыборкаСчетов = ПланыСчетов.Хозрасчетный.ВыбратьИерархически(ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль);
	Пока ВыборкаСчетов.Следующий() Цикл
		МассивСчет84.Добавить(ВыборкаСчетов.Ссылка);
	КонецЦикла;
	
	// Аналитика учета
	ПустоеСубконто                           = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка();
	ВидПлатежаВГосБюджет                     = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	ВидПлатежаВГосБюджетВзносыБезСпецоценки  = Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки;
	ВидПлатежаВГосБюджетРасходыПоСтрахованию = Перечисления.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию;
	
	СтатьиПрочихРасходов = КлассификаторыДоходовРасходов.НовыйКешГрупповойОперации(Справочники.ПрочиеДоходыИРасходы);
	
	НастройкиУчетаЗарплаты = РегистрыСведений.НастройкиУчетаЗарплаты.Получить(Новый Структура("Организация", Реквизиты.Организация));
	РезервОтпусковЭлемент  = НастройкиУчетаЗарплаты.РезервОтпусков;
	
	УчетРезервовОтпусковНУ = ВедетсяУчетРезервовОтпусковПоВидамУчета(Реквизиты.Организация, Реквизиты.Период, "НУ");
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаЗаработнойПлаты Цикл
		
		Если СчетаУчетаНалоговВзносовСФОТ.Получить(СтрокаТаблицы.ВидОперации) = Неопределено Тогда
			
			Продолжить;
			
		Иначе
			
			НоваяСтрока = ТаблицаНалоговВзносовСФОТ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			Если НоваяСтрока.ВедущийВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы Тогда
				
				НоваяСтрока.Счет         = СчетУчетаВзносовРезерва;
				НоваяСтрока.Субконто1    = РезервОтпусковЭлемент;
				НоваяСтрока.Субконто2    = ПустоеСубконто;
				НоваяСтрока.Субконто3    = ПустоеСубконто;
				
				Если НЕ СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					Если УчетРезервовОтпусковНУ
						И НЕ ЗначениеЗаполнено(НоваяСтрока.СуммаНУ) Тогда
						НоваяСтрока.СуммаНУ = НоваяСтрока.Сумма;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли НоваяСтрока.ВедущийВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства Тогда
				
				Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					НоваяСтрока.СуммаНУ = 0;
				КонецЕсли;
				
				НоваяСтрока.Счет      = СчетУчетаВзносовРезерва;
				НоваяСтрока.Субконто1 = РезервОтпусковЭлемент;
				НоваяСтрока.Субконто2 = ПустоеСубконто;
				НоваяСтрока.Субконто3 = ПустоеСубконто;
				
			ИначеЕсли НоваяСтрока.ВедущийВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы Тогда
				
				Если НЕ СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					НоваяСтрока.СчетНУ      = СчетУчетаВзносовРезерва;
					НоваяСтрока.СубконтоНУ1 = РезервОтпусковЭлемент;
					НоваяСтрока.СубконтоНУ2 = ПустоеСубконто;
					НоваяСтрока.СубконтоНУ3 = ПустоеСубконто;
				КонецЕсли;
				
			ИначеЕсли НоваяСтрока.ВедущийВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства Тогда
				
				Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					НоваяСтрока.СуммаНУ = 0;
				КонецЕсли;
				
				НоваяСтрока.Счет      = СчетУчетаВзносовРезерва;
				НоваяСтрока.Субконто1 = РезервОтпусковЭлемент;
				НоваяСтрока.Субконто2 = ПустоеСубконто;
				НоваяСтрока.Субконто3 = ПустоеСубконто;
				
			ИначеЕсли НоваяСтрока.ВедущийВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты
				И НЕ ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
				
				НоваяСтрока.Счет         = ПрочиеРасходы;
				НоваяСтрока.Субконто1    = КлассификаторыДоходовРасходов.ПредопределенныйЭлементГрупповойОперации(
					"КомпенсацияЗаЗадержкуЗарплаты",
					СтатьиПрочихРасходов);
				НоваяСтрока.Субконто2    = ПустоеСубконто;
				НоваяСтрока.Субконто3    = ПустоеСубконто;
				
			ИначеЕсли МассивСчет84.Найти(НоваяСтрока.Счет) = Неопределено Тогда
				
				Если НоваяСтрока.Счет = СчетУчетаРезерва Тогда
					НоваяСтрока.Счет         = СчетУчетаВзносовРезерва;
				Иначе
					НоваяСтрока.Счет         = НоваяСтрока.Счет;
				КонецЕсли;
				НоваяСтрока.Субконто1    = НоваяСтрока.Субконто1;
				НоваяСтрока.Субконто2    = НоваяСтрока.Субконто2;
				НоваяСтрока.Субконто3    = НоваяСтрока.Субконто3;
			Иначе
				НоваяСтрока.Счет         = ПрочиеРасходы;
				НоваяСтрока.Субконто1    = КлассификаторыДоходовРасходов.ПредопределенныйЭлементГрупповойОперации(
					"РасходыПоУплатеСтраховыхВзносов",
					СтатьиПрочихРасходов);
				НоваяСтрока.Субконто2    = ПустоеСубконто;
				НоваяСтрока.Субконто3    = ПустоеСубконто;
			КонецЕсли;
			
			КорСубконто = ОперацииУчетаОсобыхДопВзносов.Найти(СтрокаТаблицы.ВидОперации);
			НоваяСтрока.КорСчет      = СчетаУчетаНалоговВзносовСФОТ.Получить(СтрокаТаблицы.ВидОперации);
			НоваяСтрока.КорСубконто1 = ?(КорСубконто = Неопределено, ВидПлатежаВГосБюджет, ВидПлатежаВГосБюджетВзносыБезСпецоценки);
			НоваяСтрока.КорСубконто2 = ПустоеСубконто;
			НоваяСтрока.КорСубконто3 = ПустоеСубконто;
			
			Если Год(Реквизиты.Период) >= 2015 Тогда
				Если СтрокаТаблицы.ВидОперацииРасходовЗаСчетФСС = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС Тогда
					Если (НЕ ЗначениеЗаполнено(НоваяСтрока.Счет)) // на случай когда счет не пришел
						ИЛИ // на случай когда счет задан и надо сохранить аналитику
						(ЗначениеЗаполнено(НоваяСтрока.Счет) И (НоваяСтрока.Счет <> РасчетыПоБольничнымФССНС) И (НоваяСтрока.Счет <> РасчетыПоБольничнымФСС))
						Тогда
						Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СпособОтраженияЗарплатыВБухУчете) Тогда // способ отражения был задан
							НоваяСтрока.Счет = РасчетыПоБольничнымФСС;
							
							Если Реквизиты.Период >= ДатаНачалаАдминистрированияСтраховыхВзносовФНС() Тогда
								НоваяСтрока.Субконто1 = ВидПлатежаВГосБюджет;
							Иначе
								НоваяСтрока.Субконто1 = ВидПлатежаВГосБюджетРасходыПоСтрахованию;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаСвертки = "КлючСортировки, ВидОперации, Подразделение, Счет, Субконто1, Субконто2, Субконто3, КорСчет, КорСубконто1, КорСубконто2, КорСубконто3";
	Если УСН Тогда
		СтрокаСвертки = СтрокаСвертки + ", ОтражениеВУСН";
	ИначеЕсли ИП Тогда
		СтрокаСвертки = СтрокаСвертки + ", ВидНачисленияУдержания";
	Иначе
		СтрокаСвертки = СтрокаСвертки + ", СчетНУ, СубконтоНУ1, СубконтоНУ2, СубконтоНУ3";
	КонецЕсли;
	
	ТаблицаНалоговВзносовСФОТ.Свернуть(СтрокаСвертки, "Сумма, СуммаНУ");
	ТаблицаНалоговВзносовСФОТ.Сортировать(СтрокаСвертки);
	
	// Замена статей затрат, видов прочих расходов
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления <> НЕОПРЕДЕЛЕНО
	|			ТОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатрат ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяСсылка)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяЗатратНачисления,
	|	СтатьиЗатратПоЗарплате.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	РегистрСведений.СтатьиЗатратПоЗарплате КАК СтатьиЗатратПоЗарплате
	|ГДЕ
	|	СтатьиЗатратПоЗарплате.ВидСтатьиЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДополнительныхСтатейЗатрат.ФСС_НС)";
	
	СтатьиЗатрат_ФСС_НСиПЗ = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления <> НЕОПРЕДЕЛЕНО
	|			ТОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатрат ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяСсылка)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяЗатратНачисления,
	|	СтатьиЗатратПоЗарплате.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	РегистрСведений.СтатьиЗатратПоЗарплате КАК СтатьиЗатратПоЗарплате
	|ГДЕ
	|	СтатьиЗатратПоЗарплате.ВидСтатьиЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДополнительныхСтатейЗатрат.СтраховыеВзносы)";
	
	СтатьиЗатрат_СтраховыеВзносы = Запрос.Выполнить().Выгрузить();
	
	ЕстьУчетНУ = Ложь;
	Если ТаблицаНалоговВзносовСФОТ.Колонки.Найти("СчетНУ") <> Неопределено Тогда
		ЕстьУчетНУ = Истина;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНалоговВзносовСФОТ Цикл
		
		Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ФССНС
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС Тогда
			
			// Взносы в ФСС от НС и ПЗ
			
			Для НомерСубконто = 1 По 3 Цикл
				
				ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто, , СтатьиЗатрат_ФСС_НСиПЗ);
				Если ЕстьУчетНУ Тогда
					ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто, "НУ", СтатьиЗатрат_ФСС_НСиПЗ);
				КонецЕсли;

			КонецЦикла;
						
			
		Иначе
			
			// Взносы в ПФР, ФОМС и ФСС
			
			Для НомерСубконто = 1 По 3 Цикл
				
				ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто, , СтатьиЗатрат_СтраховыеВзносы);
				Если ЕстьУчетНУ Тогда
					ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто, "НУ", СтатьиЗатрат_СтраховыеВзносы);
				КонецЕсли;
				
			КонецЦикла;
			
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаНалоговВзносовСФОТ;
	
КонецФункции

Функция ПодготовитьТаблицуДепонентов(Реквизиты, ТаблицаЗаработнойПлаты, УСН = Ложь)
	
	ТаблицаНачисленийИУдержаний = ТаблицаЗаработнойПлаты.СкопироватьКолонки();
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСчет");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСубконто1");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСубконто2");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КорСубконто3");
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КлючСортировки");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокОтраженияЗарплатыВБухУчете.Счет КАК КорСчет,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1 КАК КорСубконто1,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2 КАК КорСубконто2,
	|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3 КАК КорСубконто3,
	|	ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН КАК ОтражениеВУСН,
	|	НастройкиУчетаЗарплаты.СписаниеДепонированныхСумм
	|ИЗ
	|	РегистрСведений.НастройкиУчетаЗарплаты КАК НастройкиУчетаЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|		ПО НастройкиУчетаЗарплаты.СписаниеДепонированныхСумм = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
	|ГДЕ
	|	НастройкиУчетаЗарплаты.Организация = &Организация
	|	И НЕ ПорядокОтраженияЗарплатыВБухУчете.ЕНВД";
	
	ПорядокСписанияДепонентов = Запрос.Выполнить().Выбрать();
	ПорядокСписанияДепонентов.Следующий();
	
	
	СчетРасчетовСПерсоналомПоОплатеТруда = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
	СчетУчетаДепонентов                  = ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам;
	СчетУчетаПрочихДоходов               = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
	СчетПрочихРасчетовСФизическимиЛицами = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами;
	
	ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасходовРасчетыСКонтрагентами = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	
	ПустоеСубконто                       = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка();
	
	ВидПрочегоДохода                     = ПустоеСубконто;
	
	ВестиУчетПоРаботникам                    = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеПоРаботникам");
	УчетЗарплатыИКадровСредствамиБухгалтерии = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаЗаработнойПлаты Цикл
		
		Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Депонирование Тогда
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			НоваяСтрока.КорСчет      = СчетУчетаДепонентов;
			
			Если СтрокаТаблицы.СтатьяРасходов = СтатьяРасходовРасчетыСКонтрагентами Тогда
				
				НоваяСтрока.Счет      = СчетПрочихРасчетовСФизическимиЛицами;
				НоваяСтрока.Субконто1 = СтрокаТаблицы.ФизическоеЛицо;
				
				Если ВестиУчетПоРаботникам ИЛИ (УСН И УчетЗарплатыИКадровСредствамиБухгалтерии) Тогда
					НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
				КонецЕсли;
			Иначе
				
				НоваяСтрока.Счет = СчетРасчетовСПерсоналомПоОплатеТруда;
				
				Если ВестиУчетПоРаботникам ИЛИ (УСН И УчетЗарплатыИКадровСредствамиБухгалтерии) Тогда
					НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
					НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
				КонецЕсли;
				
			КонецЕсли;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.СписаниеДепонента Тогда
			
			Если НЕ ЗначениеЗаполнено(ПорядокСписанияДепонентов.СписаниеДепонированныхСумм) Тогда
				ТекстСообщения = НСтр("ru = '%1. В настройках учета зарплаты не задан способ учета для списания депонированных сумм.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения,
				Реквизиты.Регистратор); 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор);
			КонецЕсли;
			
			НоваяСтрока = ТаблицаНачисленийИУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПорядокСписанияДепонентов);
						
			НоваяСтрока.Счет = СчетУчетаДепонентов;
			
			Если ВестиУчетПоРаботникам Тогда
				
				НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;

			КонецЕсли;
			
			НоваяСтрока.ОтражениеВУСН = СтрокаТаблицы.ОтражениеВУСН;
			
			НоваяСтрока.КлючСортировки = СоздатьКлючСортировки(СтрокаТаблицы);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаСвертки = "КлючСортировки, ВидОперации, Подразделение, Счет, Субконто1, Субконто2, Субконто3, КорСчет, КорСубконто1, КорСубконто2, КорСубконто3";
	Если УСН Тогда
		СтрокаСвертки = СтрокаСвертки + ", ОтражениеВУСН";
	КонецЕсли;
		
	ТаблицаНачисленийИУдержаний.Свернуть(СтрокаСвертки, "Сумма, СуммаНУ");
	ТаблицаНачисленийИУдержаний.Сортировать(СтрокаСвертки);
	
	Возврат ТаблицаНачисленийИУдержаний;
		
КонецФункции

Процедура ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто, ВидУчета = "", ТаблицаЗамены)
	
	ТекущаяКолонка = "Субконто" + ВидУчета + НомерСубконто;
	
	ИскомоеЗначение = СтрокаТаблицы[ТекущаяКолонка];
	
	СтрокаЗамещения = ТаблицаЗамены.Найти(ИскомоеЗначение, "СтатьяЗатратНачисления");
	
	Если СтрокаЗамещения <> Неопределено Тогда
		СтрокаТаблицы[ТекущаяКолонка] = СтрокаЗамещения.СтатьяЗатрат;
	Иначе
		Если ТипЗнч(ИскомоеЗначение) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
			СтрокаЗамещения = ТаблицаЗамены.Найти(Справочники.СтатьиЗатрат.ПустаяСсылка(), "СтатьяЗатратНачисления");
			Если СтрокаЗамещения <> Неопределено Тогда
				СтрокаТаблицы[ТекущаяКолонка] = СтрокаЗамещения.СтатьяЗатрат;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ИскомоеЗначение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
			СтрокаЗамещения = ТаблицаЗамены.Найти(Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка(), "СтатьяЗатратНачисления");
			Если СтрокаЗамещения <> Неопределено Тогда
				СтрокаТаблицы[ТекущаяКолонка] = СтрокаЗамещения.СтатьяЗатрат;
			КонецЕсли;
		КонецЕсли;					
	КонецЕсли;
		
КонецПроцедуры

Процедура ПроверитьЗаполненностьСпособаОтраженияВУчете(ТаблицаЗаработнойПлаты, Реквизиты)
	
	ТаблицаПроверки = ТаблицаЗаработнойПлаты.Скопировать(,"ФизическоеЛицо, ВидОперации, СпособОтраженияЗарплатыВБухУчете, Счет");	
	ТаблицаПроверки.Свернуть("ФизическоеЛицо, ВидОперации, СпособОтраженияЗарплатыВБухУчете, Счет",);
	
	ВидыОперацийНачисления = Перечисления.ВидыОперацийПоЗарплате.Начисления();
	
	НепроверяемыеНачисления = Новый Массив;
	НепроверяемыеНачисления.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС); 
	НепроверяемыеНачисления.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	НепроверяемыеНачисления.Добавить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты);
	НепроверяемыеНачисления.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение);
	
	ДополнениеСообщения = "";
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		ДополнениеСообщения = НСтр("ru = ' Проверьте настройку начислений/удержаний.'");
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаПроверки Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВидОперации) Тогда
			Продолжить;
		ИначеЕсли ВидыОперацийНачисления.Найти(СтрокаТаблицы.ВидОперации) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если НепроверяемыеНачисления.Найти(СтрокаТаблицы.ВидОперации) = Неопределено Тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СпособОтраженияЗарплатыВБухУчете) Тогда
				ТекстСообщения = НСтр("ru = '%1. Работник: %2 - не указан способ учета зарплаты для вида операции ""%3""!'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстСообщения,
					Реквизиты.Регистратор,
					СтрокаТаблицы.ФизическоеЛицо,
					СтрокаТаблицы.ВидОперации); 
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.СпособОтраженияЗарплатыВБухУчете) Тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
				ТекстСообщения = НСтр("ru = '%1. Работник: %2 - не указаны счет и аналитика для способа учета зарплаты ""%3""!%4'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстСообщения,
					Реквизиты.Регистратор,
					СтрокаТаблицы.ФизическоеЛицо,
					СтрокаТаблицы.СпособОтраженияЗарплатыВБухУчете,
					ДополнениеСообщения); 
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполненностьРегистрацииВНалоговомОргане(ТаблицаНДФЛ, Реквизиты, Отказ)
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПроверки = ТаблицаНДФЛ.Скопировать(,"ФизическоеЛицо, РегистрацияВНалоговомОргане");
	
	ДополнениеСообщения = "";
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаПроверки Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.РегистрацияВНалоговомОргане) Тогда
			ТекстСообщения = НСтр("ru = '%1. Работник: %2 - на закладке ""Начисленный НДФЛ"" не указана регистрация в налоговом органе!%4'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения,
				Реквизиты.Регистратор,
				СтрокаТаблицы.ФизическоеЛицо,
				ДополнениеСообщения); 
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор,,,Отказ);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Функция ПолучитьСоответствиеСтатейРасходов()
	
	СоответствиеСтатей = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиРасходовЗарплата.Ссылка КАК Ссылка,
	|	СтатьиРасходовЗарплата.СпособРасчетовСФизическимиЛицами КАК СпособРасчетовСФизическимиЛицами
	|ИЗ
	|	Справочник.СтатьиРасходовЗарплата КАК СтатьиРасходовЗарплата
	|ГДЕ
	|	СтатьиРасходовЗарплата.СпособРасчетовСФизическимиЛицами <> ЗНАЧЕНИЕ(Перечисление.СпособыРасчетовСФизическимиЛицами.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		СчетРасчетовСПерсоналомПоОплатеТруда        = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;             // 70
		РасчетыПоПрочимОперациям                    = ПланыСчетов.Хозрасчетный.РасчетыПоПрочимОперациям;                    // 73.03
		РасчетыПоВыплатеДоходов                     = ПланыСчетов.Хозрасчетный.РасчетыПоВыплатеДоходов;                     // 75.02
		ПрочиеРасчетыСФизическимиЛицами             = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами;             // 76.10
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда Тогда
				СчетУчета = СчетРасчетовСПерсоналомПоОплатеТруда;
			ИначеЕсли Выборка.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.ПрочиеРасчетыСПерсоналом Тогда
				СчетУчета = РасчетыПоПрочимОперациям;
			ИначеЕсли Выборка.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами Тогда
				СчетУчета = ПрочиеРасчетыСФизическимиЛицами;
			ИначеЕсли Выборка.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.ДивидендыСотрудникам Тогда
				СчетУчета = СчетРасчетовСПерсоналомПоОплатеТруда;
			ИначеЕсли Выборка.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.Дивиденды Тогда
				СчетУчета = РасчетыПоВыплатеДоходов;
			КонецЕсли;
			
			СоответствиеСтатей.Вставить(Выборка.Ссылка, СчетУчета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СоответствиеСтатей;
	
КонецФункции

#Область НачислениеУСН

Процедура СформироватьДвиженияПоРегистрамУСННачислениеЗарплаты(Параметры, ТаблицаЗаработнойПлаты, УдержаннаяЗарплата = Неопределено, Реквизиты, Движения, Отказ)
	
	Если ТаблицаЗаработнойПлаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Движения.Найти("РасходыПриУСН") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТиповПланСчетов =  Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный");
	ОписаниеТиповСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;
	ОписаниеТиповЧисло15_2 = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
		
	ТаблицаУСННачислениеЗарплаты = Новый ТаблицаЗначений;
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("Счет",              ОписаниеТиповПланСчетов);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("Субконто1",         ОписаниеТиповСубконто);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("Субконто2",         ОписаниеТиповСубконто);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("Субконто3",         ОписаниеТиповСубконто);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("КорСчет",           ОписаниеТиповПланСчетов);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("КорСубконто1",      ОписаниеТиповСубконто);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("КорСубконто2",      ОписаниеТиповСубконто);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("КорСубконто3",      ОписаниеТиповСубконто);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("ОтражениеВУСН",     Новый ОписаниеТипов("ПеречислениеСсылка.ОтражениеВУСН"));
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("Сумма",             ОписаниеТиповЧисло15_2);
	ТаблицаУСННачислениеЗарплаты.Колонки.Добавить("РасчетныйДокумент", Метаданные.РегистрыНакопления.РасходыПриУСН.Измерения.РасчетныйДокумент.Тип);
	
	ДетальныйУчетИсполнительныхЛистов = ДетальныйУчетИсполнительныхЛистов(Реквизиты.Период);
	ТаблицаУдержаний = ПодготовитьТаблицуУдержаний(УдержаннаяЗарплата, Истина, Ложь, ДетальныйУчетИсполнительныхЛистов);
	
	ТаблицаНачислений = ПодготовитьТаблицуНачисленийУдержаний(ТаблицаЗаработнойПлаты, Истина,, Реквизиты);
	ТаблицаНалоговВзносовСФОТ  = ПодготовитьТаблицуНалоговВзносовСФОТ(ТаблицаЗаработнойПлаты, Истина,, Реквизиты);
	
	ОтражениеВУСН_НеПринимаются = Перечисления.ОтражениеВУСН.НеПринимаются;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНачислений Цикл
		
		НоваяСтрока = ТаблицаУСННачислениеЗарплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.ОтражениеВУСН) Тогда
			НоваяСтрока.ОтражениеВУСН = ОтражениеВУСН_НеПринимаются;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНалоговВзносовСФОТ Цикл
		
		НоваяСтрока = ТаблицаУСННачислениеЗарплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.ОтражениеВУСН) Тогда
			НоваяСтрока.ОтражениеВУСН = ОтражениеВУСН_НеПринимаются;
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаУСННачислениеЗарплаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ТаблицаУСННачислениеЗарплаты", ТаблицаУСННачислениеЗарплаты);
	Параметры.Вставить("ТаблицаУдержаний",             ТаблицаУдержаний);
	Параметры.Вставить("ПериодРегистрации",            Реквизиты.Период);
	
	ДокументОбъект = Новый Структура();
	ДокументОбъект.Вставить("Организация", Реквизиты.Организация);
	ДокументОбъект.Вставить("Дата",        Реквизиты.Период);
	ДокументОбъект.Вставить("Имя",         "НачислениеЗарплаты");
	ДокументОбъект.Вставить("Ссылка",      Реквизиты.Регистратор);
	ДокументОбъект.Вставить("Движения",    Движения);
	
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ДокументОбъект, Параметры, Ложь);
	
КонецПроцедуры

// Формирует движения по отражению зарплаты в УСН
Процедура СформироватьДвиженияОтраженияЗарплатыУСН(ТаблицаОтраженияЗарплатыУСН, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаОтраженияЗарплатыУСН) Тогда
		Возврат;
	КонецЕсли;		
	
	Если Движения.Найти("РасходыПриУСН") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыДвиженийОтраженияЗарплатыУСН(ТаблицаОтраженияЗарплатыУСН, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	Параметры.Вставить("ТаблицаУСННачислениеЗарплаты", Параметры.ТаблицаОтраженияЗарплатыУСН);
	Параметры.Вставить("ПериодРегистрации", Реквизиты.Период);
	
	ДокументОбъект = Новый Структура();
	ДокументОбъект.Вставить("Организация", Реквизиты.Организация);
	ДокументОбъект.Вставить("Дата",        Реквизиты.Период);
	ДокументОбъект.Вставить("Имя",         "НачислениеЗарплаты");
	ДокументОбъект.Вставить("Ссылка",      Реквизиты.Ссылка);	
	ДокументОбъект.Вставить("Движения",    Движения);
	
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ДокументОбъект, Параметры, Ложь);

КонецПроцедуры

Функция ПодготовитьПараметрыДвиженийОтраженияЗарплатыУСН(ТаблицаОтраженияЗарплатыУСН, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = ""
	+ "Период,"				// <Дата> - счет по дебету проводки (счет учета денежных средств)
	+ "Организация,"		// <СправочникСсылка.Организации> - организация документа
	+ "Ссылка,"				// <ДокументСсылка> - документ-регистратор
	+ "ПериодРегистрации"   // <Дата> - период регистрации
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "Счет,"						// <ПланСчетовСсылка.Хозрасчетный>
	+ "Субконто1,"					// <> - значение аналитики 
	+ "Субконто2,"					// <> - значение аналитики 
	+ "Субконто3,"					// <> - значение аналитики
	+ "КорСчет,"					// <ПланСчетовСсылка.Хозрасчетный>
	+ "КорСубконто1,"				// <> - значение аналитики 
	+ "КорСубконто2,"				// <> - значение аналитики 
	+ "КорСубконто3,"				// <> - значение аналитики	
	+ "ОтражениеВУСН,"				// <Перечисление.ОтражениеВУСН>
	+ "РасчетныйДокумент,"			// <ДокументСсылка> - документ-регистратор
	+ "Сумма"						// <Число(15,2)>
	;	
	
	Параметры.Вставить("ТаблицаОтраженияЗарплатыУСН", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаОтраженияЗарплатыУСН, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область НачислениеИП

Процедура ОпределитьВидНачисленийУдержанийИП(ТаблицаЗаработнойПлаты, Реквизиты)
	
	СоответствиеВидовНачисленийУдержаний = Новый Соответствие;
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход,               Перечисления.ИПВидыНачисленийУдержаний.НачисленоПовременно);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск,              Перечисления.ИПВидыНачисленийУдержаний.НачисленоПовременно);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска, Перечисления.ИПВидыНачисленийУдержаний.НачисленоПовременно);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход,        Перечисления.ИПВидыНачисленийУдержаний.НачисленоСдельно);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НатуральныйДоход,             Перечисления.ИПВидыНачисленийУдержаний.НачисленоНатуральныйДоход);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛ,                         Перечисления.ИПВидыНачисленийУдержаний.УдержаноНДФЛ);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышения,              Перечисления.ИПВидыНачисленийУдержаний.УдержаноНДФЛ);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке18,    Перечисления.ИПВидыНачисленийУдержаний.УдержаноНДФЛ);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке20,    Перечисления.ИПВидыНачисленийУдержаний.УдержаноНДФЛ);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке22,    Перечисления.ИПВидыНачисленийУдержаний.УдержаноНДФЛ);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги, Перечисления.ИПВидыНачисленийУдержаний.НачисленоДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДоходыКонтрагентов,  Перечисления.ИПВидыНачисленийУдержаний.НачисленоДругое);
	
	Если Реквизиты.Период >= ДатаНачалаАдминистрированияСтраховыхВзносовФНС() Тогда
		СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель, Перечисления.ИПВидыНачисленийУдержаний.НачисленоДругое);
		СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС,          Перечисления.ИПВидыНачисленийУдержаний.НачисленоДругое);
		СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС,        Перечисления.ИПВидыНачисленийУдержаний.НачисленоДругое);
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("ВидНачисленияУдержания") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("ВидНачисленияУдержания", Новый ОписаниеТипов("ПеречислениеСсылка.ИПВидыНачисленийУдержаний"));
		Для каждого Начисление Из ТаблицаЗаработнойПлаты Цикл
			Начисление.ВидНачисленияУдержания	= СоответствиеВидовНачисленийУдержаний.Получить(Начисление.ВидОперации);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияПоРегистрамИПНачислениеЗарплаты(Параметры, ТаблицаЗаработнойПлаты, Реквизиты, Движения, Отказ)
	
	Если Не ЗначениеЗаполнено(ТаблицаЗаработнойПлаты) Тогда
		Возврат;
	КонецЕсли;		
	
	ОпределитьВидНачисленийУдержанийИП(ТаблицаЗаработнойПлаты, Реквизиты);
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("ПланСчетовСсылка.Хозрасчетный"));
	ОписаниеТиповПланСчетов =  Новый ОписаниеТипов(Массив, , );
	
	ОписаниеТиповСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;
		
	Массив = Новый Массив;
	Массив.Добавить(Тип("ПеречислениеСсылка.ИПВидыНачисленийУдержаний"));
	ОписаниеТиповВидНачисленияУдержания =  Новый ОписаниеТипов(Массив, , );
	
	ОписаниеТиповЧисло15_2 = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
		
	ТаблицаИПНачислениеЗарплаты = Новый ТаблицаЗначений;
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("Счет",          ОписаниеТиповПланСчетов);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("Субконто1",     ОписаниеТиповСубконто);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("Субконто2",     ОписаниеТиповСубконто);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("Субконто3",     ОписаниеТиповСубконто);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("КорСчет",       ОписаниеТиповПланСчетов);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("КорСубконто1",  ОписаниеТиповСубконто);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("КорСубконто2",  ОписаниеТиповСубконто);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("КорСубконто3",  ОписаниеТиповСубконто);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("ВидНачисленияУдержания", ОписаниеТиповВидНачисленияУдержания);
	ТаблицаИПНачислениеЗарплаты.Колонки.Добавить("Сумма",         ОписаниеТиповЧисло15_2);
			
	ТаблицаНачисленийУдержаний = ПодготовитьТаблицуНачисленийУдержаний(ТаблицаЗаработнойПлаты, , Истина, Реквизиты);
	ТаблицаНалоговВзносовСФОТ  = ПодготовитьТаблицуНалоговВзносовСФОТ(ТаблицаЗаработнойПлаты, , Истина, Реквизиты);
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНачисленийУдержаний Цикл
		
		НоваяСтрока = ТаблицаИПНачислениеЗарплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНалоговВзносовСФОТ Цикл
		
		НоваяСтрока = ТаблицаИПНачислениеЗарплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
	КонецЦикла;
	
	Если ТаблицаИПНачислениеЗарплаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Движения.Найти("ИПРасходыНаОплатуТруда") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Реквизиты.Колонки.Найти("Ссылка") = Неопределено Тогда
		Параметры.Реквизиты.Колонки.Добавить("Ссылка",	Документы.ТипВсеСсылки());
		Параметры.Реквизиты.ЗаполнитьЗначения(Реквизиты.Регистратор, "Ссылка");
	КонецЕсли;
	
	Если Параметры.Реквизиты.Колонки.Найти("Удержания") = Неопределено Тогда
		Параметры.Реквизиты.Колонки.Добавить("Удержания");
		Параметры.Реквизиты.ЗаполнитьЗначения(Ложь, "Удержания");
	КонецЕсли;
	
	Параметры.Вставить("ТаблицаНачисленияУдержания", ТаблицаИПНачислениеЗарплаты);
	
	ТаблицаПрочиеРасходы = УчетДоходовИРасходовПредпринимателя.ПустаяТаблицаРегистраНакопления("ИППрочиеРасходы", Ложь);
	Параметры.Вставить("ТаблицаПрочиеРасходы", ТаблицаПрочиеРасходы);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияРасходовНаОплатуТруда(Параметры, Движения, Отказ);
	
	ОтразитьРасчетыПоНачисленнымПособиямИП(Параметры, ТаблицаЗаработнойПлаты, ТаблицаИПНачислениеЗарплаты, ТаблицаПрочиеРасходы, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыДвиженийОтраженияЗарплатыИП(ТаблицаНачисленияУдержания, ТаблицаРеквизитов)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Период,"						// <Дата> - период движений - дата документа
		+ "Ссылка,"						// <ДокументСсылка.*> - документ-регистратор движений
		+ "Организация";				// <СправочникСсылка.Организации> - оранизация
		
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ТаблицаОтраженияЗарплатыИП
	
	СписокОбязательныхКолонок = ""
		+ "Счет,"						//	<ПланСчетовСсылка.Хозрасчетный>
		+ "Субконто1,"					//  <Характеристика.ВидыСубконтоХозрасчетные>
		+ "Субконто2,"					//  <Характеристика.ВидыСубконтоХозрасчетные> 
		+ "Субконто3,"					//  <Характеристика.ВидыСубконтоХозрасчетные>
		+ "КорСчет,"					//	<ПланСчетовСсылка.Хозрасчетный> 
		+ "КорСубконто1,"				//  <Характеристика.ВидыСубконтоХозрасчетные>
		+ "КорСубконто2,"				//  <Характеристика.ВидыСубконтоХозрасчетные> 
		+ "КорСубконто3,"				//  <Характеристика.ВидыСубконтоХозрасчетные>
		+ "ВидНачисленияУдержания,"		//  <ПеречислениеСсылка.ИПВидыНачисленийУдержаний> - вид начисления/удержания
		+ "Сумма";						//	<Сумма, 15, 2> - сумма начисления/удержания в рублях
		
	Параметры.Вставить("ТаблицаОтраженияЗарплатыИП", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаНачисленияУдержания, СписокОбязательныхКолонок));
		
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

Процедура СинхронизироватьПодразделенияИРегистрацииВНалоговомОргане(ТаблицаЗаработнойПлаты, Реквизиты)
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнитьТаблицуЗаработнойПлатыНедостающимиКолонками(ТаблицаЗаработнойПлаты);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",                     Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",                Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделения",              ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗаработнойПлаты, "Подразделение", Истина));
	Запрос.УстановитьПараметр("НачисленнаяЗарплатаИВзносы", ТаблицаЗаработнойПлаты);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.Сумма КАК Сумма,
	|	НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
	|	НачисленнаяЗарплатаИВзносы.СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленнаяЗарплатаИВзносы.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	НачисленнаяЗарплатаИВзносы.Подразделение КАК Подразделение,
	|	НачисленнаяЗарплатаИВзносы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
	|	НачисленнаяЗарплатаИВзносы.ВедущийВидОперации КАК ВедущийВидОперации,
	|	НачисленнаяЗарплатаИВзносы.ОтражениеВУСН КАК ОтражениеВУСН,
	|	НачисленнаяЗарплатаИВзносы.Резерв КАК Резерв,
	|	НачисленнаяЗарплатаИВзносы.РезервБУ КАК РезервБУ,
	|	НачисленнаяЗарплатаИВзносы.РезервНУ КАК РезервНУ,
	|	НачисленнаяЗарплатаИВзносы.РасчетныйДокумент КАК РасчетныйДокумент
	|ПОМЕСТИТЬ ВТ_НачисленнаяЗарплатаИВзносы
	|ИЗ
	|	&НачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплатаИВзносы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница = &Организация
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|		ИНАЧЕ ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТ_ИсторияРегистраций
	|ИЗ
	|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(
	|			&Период,
	|			СтруктурнаяЕдиница = &Организация
	|				ИЛИ СтруктурнаяЕдиница В (&Подразделения)) КАК ИсторияРегистрацийВНалоговомОрганеСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница = &Организация
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|		ИНАЧЕ ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница
	|	КОНЕЦ,
	|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтруктурнаяЕдиница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_НачисленнаяЗарплатаИВзносы.Сумма КАК Сумма,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
	|	ВТ_НачисленнаяЗарплатаИВзносы.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ВТ_НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
	|	ВТ_НачисленнаяЗарплатаИВзносы.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ВТ_НачисленнаяЗарплатаИВзносы.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ВТ_ИсторияРегистраций.РегистрацияВНалоговомОргане, ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|		ИНАЧЕ ВТ_НачисленнаяЗарплатаИВзносы.РегистрацияВНалоговомОргане
	|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВидОперацииРасходовЗаСчетФСС КАК ВидОперацииРасходовЗаСчетФСС,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ВедущийВидОперации КАК ВедущийВидОперации,
	|	ВТ_НачисленнаяЗарплатаИВзносы.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ВТ_НачисленнаяЗарплатаИВзносы.Резерв КАК Резерв,
	|	ВТ_НачисленнаяЗарплатаИВзносы.РезервБУ КАК РезервБУ,
	|	ВТ_НачисленнаяЗарплатаИВзносы.РезервНУ КАК РезервНУ,
	|	ВТ_НачисленнаяЗарплатаИВзносы.РасчетныйДокумент КАК РасчетныйДокумент
	|ИЗ
	|	ВТ_НачисленнаяЗарплатаИВзносы КАК ВТ_НачисленнаяЗарплатаИВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияРегистраций КАК ВТ_ИсторияРегистраций
	|		ПО ВТ_НачисленнаяЗарплатаИВзносы.Подразделение = ВТ_ИсторияРегистраций.СтруктурнаяЕдиница";
	
	ТаблицаЗаработнойПлаты = Запрос.Выполнить().Выгрузить();
	
	МассивОперацийНДФЛ = Новый Массив;
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛ);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышения);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛДоначисленныйПоРезультатамПроверки);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛДоходыКонтрагентов);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛПередачаЗадолженностиВНалоговыйОрган);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛРасчетыСБывшимиСотрудниками);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НФДЛДивиденды);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НФДЛДивидендыСотрудникам);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛПрочиеРасчетыСПерсоналом);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке18);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке20);
	МассивОперацийНДФЛ.Добавить(Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышенияПоСтавке22);
	
	МассивОперацийВзносов = Новый Массив;
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРНакопительнаяЧасть);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРСтраховаяЧасть);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРПоСуммарномуТарифу);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДоПредельнойВеличины);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРСПревышения);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифЛЭ);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифШахтеры);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФСС);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФССНС);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ФФОМС);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ТФОМС);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ВзносыПоЕдиномуТарифу);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорам);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорамСПревышения);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорам);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорамСПревышения);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорам);
	МассивОперацийВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорамСПревышения);

	
	ТаблицаНовыхСтрок = ТаблицаЗаработнойПлаты.СкопироватьКолонки();
	ТаблицаНовыхСтрок.Колонки.Добавить("ИндексСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(6, 0)));
	
	МасивСтарыхСтрок = Новый Массив;
	
	ПромежуточнаяТаблица = ТаблицаЗаработнойПлаты.Скопировать(, "ФизическоеЛицо, РегистрацияВНалоговомОргане, СтатьяРасходов");
	ПромежуточнаяТаблица.Свернуть("ФизическоеЛицо, РегистрацияВНалоговомОргане, СтатьяРасходов");
	Для Каждого СтрокаТаблицы ИЗ ПромежуточнаяТаблица Цикл
		
		Отбор = Новый Структура("ФизическоеЛицо, РегистрацияВНалоговомОргане, СтатьяРасходов");
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		МассивСтрок = ТаблицаЗаработнойПлаты.НайтиСтроки(Отбор);
		
		МасивНачислений = Новый Массив;
		МассивУдержаний = Новый Массив;
		
		Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
			
			Если МассивОперацийВзносов.Найти(СтрокаМассива.ВидОперации) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если МассивОперацийНДФЛ.Найти(СтрокаМассива.ВидОперации) = Неопределено Тогда
				МасивНачислений.Добавить(СтрокаМассива);
			Иначе
				Если НЕ ЗначениеЗаполнено(СтрокаМассива.Подразделение) Тогда
					МассивУдержаний.Добавить(СтрокаМассива);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если МасивНачислений.Количество() = 0
			ИЛИ МассивУдержаний.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		МассивКоэффициентов = Новый Массив;
		Для Каждого СтрокаМассива ИЗ МасивНачислений Цикл
			МассивКоэффициентов.Добавить(СтрокаМассива.Сумма);
		КонецЦикла;
		
		Для Каждого СтрокаМассива ИЗ МассивУдержаний Цикл
			МассивНовыхСумм = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(СтрокаМассива.Сумма, МассивКоэффициентов, 0);
			Если МассивНовыхСумм <> Неопределено Тогда
				МасивСтарыхСтрок.Добавить(СтрокаМассива);
				НомерСтроки = 0;
				Для Каждого СтрокаМассиваМассивНовыхСумм ИЗ МассивНовыхСумм Цикл
					НоваяСтрока = ТаблицаНовыхСтрок.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМассива);
					НоваяСтрока.Сумма = СтрокаМассиваМассивНовыхСумм;
					НоваяСтрока.Подразделение = МасивНачислений[НомерСтроки].Подразделение;
					НоваяСтрока.ИндексСтроки  = ТаблицаЗаработнойПлаты.Индекс(СтрокаМассива);
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТаблицаНовыхСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНовыхСтрок.Свернуть("ИндексСтроки, ФизическоеЛицо, ВидОперации, Подразделение,
									|СпособОтраженияЗарплатыВБухучете, СтатьяРасходов, ОблагаетсяЕНВД,
									|ВидНачисленияОплатыТрудаДляНУ, РегистрацияВНалоговомОргане,
									|ВидОперацииРасходовЗаСчетФСС, ВедущийВидОперации,ОтражениеВУСН,
									|РасчетныйДокумент, Резерв, РезервБУ, РезервНУ", "Сумма");
	ТаблицаНовыхСтрок.Сортировать("ИндексСтроки Убыв");
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНовыхСтрок Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаЗаработнойПлаты.Вставить(СтрокаТаблицы.ИндексСтроки), СтрокаТаблицы);
	КонецЦикла;
	
	Для Каждого СтрокаМассива ИЗ МасивСтарыхСтрок Цикл
		ТаблицаЗаработнойПлаты.Удалить(СтрокаМассива);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьТаблицуЗаработнойПлатыНедостающимиКолонками(ТаблицаЗаработнойПлаты)
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("ВидНачисленияОплатыТрудаДляНУ") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("ВидНачисленияОплатыТрудаДляНУ", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНачисленийОплатыТрудаДляНУ"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("СпособОтраженияЗарплатыВБухУчете") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("СпособОтраженияЗарплатыВБухУчете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("Подразделение") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("ОблагаетсяЕНВД") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("РегистрацияВНалоговомОргане") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("ВидОперацииРасходовЗаСчетФСС") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("ВидОперацииРасходовЗаСчетФСС", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("ВедущийВидОперации") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("ВедущийВидОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
		Для Каждого СтрокаТаблицы ИЗ ТаблицаЗаработнойПлаты Цикл
			СтрокаТаблицы.ВедущийВидОперации = СтрокаТаблицы.ВидОперации;
		КонецЦикла;
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("ОтражениеВУСН") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("ОтражениеВУСН", Новый ОписаниеТипов("ПеречислениеСсылка.ОтражениеВУСН"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("РасчетныйДокумент") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("РасчетныйДокумент", Метаданные.РегистрыНакопления.РасходыПриУСН.Измерения.РасчетныйДокумент.Тип);
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("СтатьяРасходов") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("Резерв") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("Резерв", Новый ОписаниеТипов("СправочникСсылка.Резервы"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("РезервБУ") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("РезервБУ", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("РезервНУ") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("РезервНУ", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Удержание

Процедура СформироватьДвиженияУдержания(Движения, Отказ, Организация, Знач ПериодРегистрации, ДанныеДляОтражения, НачисленнаяЗарплатаИВзносы = Неопределено)
		
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") И НЕ ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	
	Регистратор       = Движения.Хозрасчетный.Отбор.Регистратор.Значение;
	ПериодРегистрации = ПолучитьПериодРегистрации(Регистратор, ПериодРегистрации);
	Параметры         = ПодготовитьПараметрыПоОтражениюУдержанийВУчете(ДанныеДляОтражения, Организация, ПериодРегистрации, Регистратор);
	Реквизиты         = Параметры.Реквизиты[0];
	
	СформироватьПроводкиПоОтражениюУдержанийВУчете(Параметры.ТаблицаУдержаний, Реквизиты, Движения, Отказ);
	
	// Учет доходов и расходов ИП
	Если НачисленнаяЗарплатаИВзносы <> Неопределено Тогда
		ПараметрыДоходов = ПодготовитьПараметрыПоОтражениюЗарплатыВУчете(НачисленнаяЗарплатаИВзносы, Организация, ПериодРегистрации, Регистратор);
		ТаблицаЗаработнойПлаты = ПараметрыДоходов.ТаблицаЗаработнойПлаты;
	Иначе
		ТаблицаЗаработнойПлаты = Неопределено;
	КонецЕсли;
	
	СформироватьДвиженияПоРегистрамИПУдержания(Параметры, Параметры.ТаблицаУдержаний, Реквизиты, ТаблицаЗаработнойПлаты, Движения, Отказ);
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		ЗарегистрироватьДанныеДляПечатиЧеков(ДанныеДляОтражения, Реквизиты, Движения, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьПараметрыПоОтражениюУдержанийВУчете(ДанныеДляПроводок, Организация, ПериодРегистрации, Регистратор)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	Реквизиты = Новый ТаблицаЗначений;
	
	Реквизиты.Колонки.Добавить("Организация");
	Реквизиты.Колонки.Добавить("Период");
	Реквизиты.Колонки.Добавить("Регистратор");
	
	НоваяСтрока             = Реквизиты.Добавить();
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Период      = ПериодРегистрации;
	НоваяСтрока.Регистратор = Регистратор;
	
	Параметры.Вставить("Реквизиты", Реквизиты);
		
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "ФизическоеЛицо,"
	+ "ВидОперации,"
	+ "Подразделение,"
	+ "Контрагент,"
	+ "СтатьяРасходов,"
	+ "ДокументОснование,"
	+ "Сумма"
	;
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ","
		+ "ЯвляетсяОснованиемОформленияКассовогоЧека,"
		+ "ОписаниеУдержанияДляЧека"
		;
	КонецЕсли;
	Параметры.Вставить("ТаблицаУдержаний", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ДанныеДляПроводок, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура СформироватьПроводкиПоОтражениюУдержанийВУчете(ТаблицаУдержаний, Реквизиты, Движения, Отказ)
	
	Если ТаблицаУдержаний.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДетальныйУчетИсполнительныхЛистов = ДетальныйУчетИсполнительныхЛистов(Реквизиты.Период);
	ТаблицаУдержанийРаботников = ПодготовитьТаблицуУдержаний(ТаблицаУдержаний, Ложь, Ложь, ДетальныйУчетИсполнительныхЛистов);
	
	СодержанияПроводок = Новый Соответствие;
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.НачисленоПроцентовПоЗайму,
								"Начислены проценты по займу");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПроцентыПоЗайму,
									"Удержаны проценты по займам");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПогашениеЗаймов,
									"Удержано погашение займа");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты,
									"Удержано по исполнительному листу");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.Профвзносы,
									"Удержаны профвзносы");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДСВ,
									"Удержаны добровольные взносы на накопительная часть трудовой пенсии");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПрочиеУдержания,
									"Прочие удержания из заработной платы");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ,
									"Удержаны добровольные взносы в негосударственный пенсионный фонд");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента,
									"Удержано вознаграждение платежного агента");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВозмещениеУщерба,
									"Удержано возмещение ущерба");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.УдержаниеПоПрочимОперациямСРаботниками,
									"Удержание по прочим операциям с работниками");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм,
									"Удержание неизрасходованных подотчетных сумм");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок,
									"Возврат излишне выплаченных сумм");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.УдержаниеЗаОтпуск,
									"Удержание за неотработанные дни предоставленного авансом отпуска");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгентаКонтрагенты,
									"Удержано вознаграждение платежного агента из доходов контрагента");
	СодержанияПроводок.Вставить(Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛистыКонтрагенты,
									"Удержано по исполнительному листу из доходов контрагента");
	
	ВестиУчетПоРаботникам = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеПоРаботникам");
	СчетРасчетовСПерсоналомПоОплатеТруда = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
		
	Для Каждого СтрокаТаблицы Из ТаблицаУдержанийРаботников Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Счет)
			ИЛИ НЕ ЗначениеЗаполнено(СтрокаТаблицы.КорСчет) Тогда
			// не заполнены счет и аналитика
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Сумма       = СтрокаТаблицы.Сумма;
		Проводка.Содержание  = СодержанияПроводок.Получить(СтрокаТаблицы.ВидОперации);
		
		Проводка.СчетКт = СтрокаТаблицы.Счет;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.Субконто3);
		
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Проводка.СчетДт = СтрокаТаблицы.КорСчет;
		Если СтрокаТаблицы.КорСчет = СчетРасчетовСПерсоналомПоОплатеТруда Тогда
			Если ВестиУчетПоРаботникам Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РаботникиОрганизаций", СтрокаТаблицы.КорСубконто1);
			КонецЕсли;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.КорСубконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.КорСубконто3);
		Иначе
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.КорСубконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.КорСубконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.КорСубконто3);
		КонецЕсли;
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьТаблицуУдержаний(ТаблицаУдержанийРаботника, УСН = Ложь, ИП = Ложь, ДетальныйУчетИсполнительныхЛистов = Ложь)
	
	Если ТаблицаУдержанийРаботника = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТаблицаУдержанийРаботника.Колонки.Найти("ДокументОснование") = Неопределено Тогда
		ТаблицаУдержанийРаботника.Колонки.Добавить("ДокументОснование",  Новый ОписаниеТипов("ДокументСсылка.ИсполнительныйЛист"));
	КонецЕсли;
	
	ТаблицаУдержаний = ТаблицаУдержанийРаботника.СкопироватьКолонки();
	ТаблицаУдержаний.Колонки.Добавить("Счет");
	ТаблицаУдержаний.Колонки.Добавить("Субконто1");
	ТаблицаУдержаний.Колонки.Добавить("Субконто2");
	ТаблицаУдержаний.Колонки.Добавить("Субконто3");
	ТаблицаУдержаний.Колонки.Добавить("КорСчет");
	ТаблицаУдержаний.Колонки.Добавить("КорСубконто1");
	ТаблицаУдержаний.Колонки.Добавить("КорСубконто2");
	ТаблицаУдержаний.Колонки.Добавить("КорСубконто3");
	
	СчетРасчетовСПерсоналомПоОплатеТруда = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
	
	РасчетыСПодотчетнымиЛицами                  = ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами;                  // 71.01
	РасчетыПоПредоставленнымЗаймам              = ПланыСчетов.Хозрасчетный.РасчетыПоПредоставленнымЗаймам;              // 73.01
	РасчетыПоВозмещениюМатериальногоУщерба      = ПланыСчетов.Хозрасчетный.РасчетыПоВозмещениюМатериальногоУщерба;      // 73.02
	РасчетыПоПрочимОперациям                    = ПланыСчетов.Хозрасчетный.РасчетыПоПрочимОперациям;                    // 73.03
	ПрочиеРасчетыСФизическимиЛицами             = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами;             // 76.10
	РасчетыПоИсполнительнымДокументамРаботников = ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументамРаботников; // 76.41
	РасчетыПоПрочимУдержаниямРаботников         = ПланыСчетов.Хозрасчетный.РасчетыПоПрочимУдержаниямРаботников;         // 76.49
	ПФР_ДОБР_сотр                               = ПланыСчетов.Хозрасчетный.ПФР_ДОБР_сотр;                               // 69.05.2
	ПрочиеДоходы                                = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;                                // 91.01
	
	ПустоеСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка();
	
	ВидПлатежаВГосБюджет                 = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	
	СтатьиПрочихДоходов = КлассификаторыДоходовРасходов.НовыйКешГрупповойОперации(Справочники.ПрочиеДоходыИРасходы);
	
	ЗаполнитьСтатьюРасходовПоВидуОперации(ТаблицаУдержанийРаботника);
	
	// Соответствие статей расходов и счетов учета
	СоответствиеСтатейРасходов = ПолучитьСоответствиеСтатейРасходов();

	Для Каждого СтрокаТаблицы ИЗ ТаблицаУдержанийРаботника Цикл
		
		Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПогашениеЗаймов
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПроцентыПоЗайму Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет      = РасчетыПоПредоставленнымЗаймам;
			НоваяСтрока.Субконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛистыКонтрагенты Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = РасчетыПоИсполнительнымДокументамРаботников;
			НоваяСтрока.Субконто1    = СтрокаТаблицы.Контрагент;
			Если ДетальныйУчетИсполнительныхЛистов
				И ЗначениеЗаполнено(СтрокаТаблицы.ДокументОснование) Тогда
				НоваяСтрока.Субконто2 = СтрокаТаблицы.ФизическоеЛицо;
				НоваяСтрока.Субконто3 = СтрокаТаблицы.ДокументОснование;
			КонецЕсли;
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛистыКонтрагенты Тогда
					ТекущийСчетУчета = ПрочиеРасчетыСФизическимиЛицами;
				Иначе
					ТекущийСчетУчета = СчетРасчетовСПерсоналомПоОплатеТруда;
				КонецЕсли;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДСВ Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = ПФР_ДОБР_сотр;
			НоваяСтрока.Субконто1    = ВидПлатежаВГосБюджет;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Профвзносы Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = РасчетыПоПрочимУдержаниямРаботников;
			НоваяСтрока.Субконто1    = СтрокаТаблицы.Контрагент;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ПрочиеУдержания Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Если ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
				НоваяСтрока.Счет      = РасчетыПоПрочимУдержаниямРаботников;
				НоваяСтрока.Субконто1 = СтрокаТаблицы.Контрагент;
			Иначе
				НоваяСтрока.Счет      = РасчетыПоПрочимОперациям;
				НоваяСтрока.Субконто1 = СтрокаТаблицы.ФизическоеЛицо;
			КонецЕсли;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента
			ИЛИ СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгентаКонтрагенты Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = РасчетыПоПрочимУдержаниямРаботников;
			НоваяСтрока.Субконто1    = СтрокаТаблицы.Контрагент;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				Если СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгентаКонтрагенты Тогда
					ТекущийСчетУчета = ПрочиеРасчетыСФизическимиЛицами;
				Иначе
					ТекущийСчетУчета = СчетРасчетовСПерсоналомПоОплатеТруда;
				КонецЕсли;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВозмещениеУщерба Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = РасчетыПоВозмещениюМатериальногоУщерба;
			НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.УдержаниеПоПрочимОперациямСРаботниками Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = РасчетыПоПрочимОперациям;
			НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = РасчетыСПодотчетнымиЛицами;
			НоваяСтрока.Субконто1    = СтрокаТаблицы.ФизическоеЛицо;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = ПрочиеДоходы;
			НоваяСтрока.Субконто1    = КлассификаторыДоходовРасходов.ПредопределенныйЭлементГрупповойОперации(
				"ИзлишнеВыплаченнаяОплатаТруда",
				СтатьиПрочихДоходов);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.УдержаниеЗаОтпуск Тогда
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет         = ПрочиеДоходы;
			НоваяСтрока.Субконто1    = КлассификаторыДоходовРасходов.ПредопределенныйЭлементГрупповойОперации(
				"ИзлишнеВыплаченнаяОплатаТруда",
				СтатьиПрочихДоходов);
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено Тогда
				ТекущийСчетУчета  = СчетРасчетовСПерсоналомПоОплатеТруда;
			КонецЕсли;
			НоваяСтрока.КорСчет      = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1 = СтрокаТаблицы.ФизическоеЛицо;
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоПроцентовПоЗайму Тогда
			
			Если УСН ИЛИ ИП Тогда
				// это не удержание, а получение дохода работодателем
				// для указанных ситуаций не обслуживается
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Счет      = ПрочиеДоходы;
			НоваяСтрока.Субконто1 = КлассификаторыДоходовРасходов.ПредопределенныйЭлементГрупповойОперации(
				"ПроцентыПоЗаймамСотрудникам",
				СтатьиПрочихДоходов);
			
			НоваяСтрока.Субконто2 = ПустоеСубконто;
			НоваяСтрока.Субконто3 = ПустоеСубконто;
			
			ТекущийСчетУчета = СоответствиеСтатейРасходов.Получить(СтрокаТаблицы.СтатьяРасходов);
			Если ТекущийСчетУчета = Неопределено
				ИЛИ ТекущийСчетУчета = РасчетыПоПрочимОперациям Тогда
				ТекущийСчетУчета  = РасчетыПоПредоставленнымЗаймам;
			КонецЕсли;
			
			НоваяСтрока.КорСчет         = ТекущийСчетУчета;
			НоваяСтрока.КорСубконто1    = СтрокаТаблицы.ФизическоеЛицо;
			НоваяСтрока.КорСубконто2    = ПустоеСубконто;
			НоваяСтрока.КорСубконто3    = ПустоеСубконто;
			
		КонецЕсли;
		
	КонецЦикла;
		
	СтрокаСвертки = "ВидОперации, Подразделение, Счет, Субконто1, Субконто2, Субконто3, КорСчет, КорСубконто1, КорСубконто2, КорСубконто3";
	
	Если ИП Тогда
		СтрокаСвертки = СтрокаСвертки + ", ВидНачисленияУдержания";
	КонецЕсли;
		
	ТаблицаУдержаний.Свернуть(СтрокаСвертки, "Сумма");
	
	Возврат ТаблицаУдержаний;
	
КонецФункции

Процедура ЗарегистрироватьДанныеДляПечатиЧеков(ТаблицаУдержаний, Реквизиты, Движения, Отказ)
	
	Если ТаблицаУдержаний.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ТаблицаУдержанийРаботников = ТаблицаУдержаний.Скопировать(Новый Структура("ЯвляетсяОснованиемОформленияКассовогоЧека", Истина));
	
	Если ТаблицаУдержанийРаботников.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаУдержанийРаботников.Свернуть("ФизическоеЛицо, Подразделение, Контрагент, ОписаниеУдержанияДляЧека", "Сумма");
		
	Для Каждого СтрокаТаблицы Из ТаблицаУдержанийРаботников Цикл
		
		Если СтрокаТаблицы.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаписьУдержания = Движения.ДанныеПоУдержаниямДляПечатиЧеков.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьУдержания, СтрокаТаблицы);
		ЗаписьУдержания.Период              = Реквизиты.Период;
		ЗаписьУдержания.Организация         = Реквизиты.Организация;
		ЗаписьУдержания.ИдентификаторЗаписи = ИдентификаторЧека(Реквизиты.Ссылка, СтрокаТаблицы);
		
	КонецЦикла;
	
	Движения.ДанныеПоУдержаниямДляПечатиЧеков.Записывать = Истина;
	
КонецПроцедуры

// Возвращает сведения об удержанном НДФЛ при выплате заработной платы
//
// Параметры:
//  Движения - КоллекцияДвижений - коллекция движений документа выплаты заработной платы.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//    * ФизЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо, с которого удержали НДФЛ.
//    * РасчетныйДокумент - ДокументСсылка - документ начисления заработной платы.
//    * Сумма - Число - сумма удержанного НДФЛ.
//
Функция УдержанныйПриВыплатеНДФЛ(Движения) Экспорт
	
	УдержанныйНДФЛ = НоваяТаблицаУдержанногоПриВыплатеНДФЛ();
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме")
		И ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеПоРаботникам") Тогда
	
		Если Движения.Найти("ОплатаВедомостейНаВыплатуЗарплаты") = Неопределено Тогда
			Возврат УдержанныйНДФЛ;
		КонецЕсли;
	
		ОплаченныеВедомости = Движения.ОплатаВедомостейНаВыплатуЗарплаты.ВыгрузитьКолонку("Ведомость");
		Ведомости = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОплаченныеВедомости);
	
		УдержанныйНДФЛПоВедомостям = УдержанныйНДФЛПоДаннымВедомостейНаВыплатуЗарплаты(Ведомости);
	
		Если Не ЗначениеЗаполнено(УдержанныйНДФЛПоВедомостям) Тогда
			Возврат УдержанныйНДФЛ;
		КонецЕсли;
	
		Для Каждого СтрокаУдержанныйНДФЛПоВедомостям Из УдержанныйНДФЛПоВедомостям Цикл
			ЗаполнитьЗначенияСвойств(УдержанныйНДФЛ.Добавить(), СтрокаУдержанныйНДФЛПоВедомостям);
		КонецЦикла;
	
	Иначе
		
		Если Движения.Найти("НДФЛКПеречислению") = Неопределено Тогда
			Возврат УдержанныйНДФЛ;
		КонецЕсли;
		
		Отбор = Новый Структура("ВариантУдержания", Перечисления.ВариантыУдержанияНДФЛ.Удержано);
		Колонки = "ВариантУдержания, ФизическоеЛицо, ДокументОснование," + УчетНДФЛКлиентСервер.РесурсыИсчисленногоНалогаСтрокой("Сумма", Ложь, Ложь);
		ТаблицаДвижений = Движения.НДФЛКПеречислению.Выгрузить( , Колонки).Скопировать(Отбор);
		
		Для Каждого СтрокаТаблицыДвижений Из ТаблицаДвижений Цикл
			СуммаНалога = УчетНДФЛКлиентСервер.ИтоговаяСуммаНДФЛпоСтроке(СтрокаТаблицыДвижений, "Сумма");
			Если СуммаНалога < 0 Тогда
				НоваяСтрока = УдержанныйНДФЛ.Добавить();
				НоваяСтрока.ФизЛицо = СтрокаТаблицыДвижений.ФизическоеЛицо;
				НоваяСтрока.РасчетныйДокумент = СтрокаТаблицыДвижений.ДокументОснование;
				НоваяСтрока.Сумма = -СуммаНалога;
			КонецЕсли;
		КонецЦикла;
		
		УдержанныйНДФЛ.Свернуть("ФизЛицо, РасчетныйДокумент", "Сумма");
		
	КонецЕсли;
	
	Возврат УдержанныйНДФЛ;
	
КонецФункции

Функция НоваяТаблицаУдержанногоПриВыплатеНДФЛ()
	
	ТипРасчетногоДокумента = Метаданные.РегистрыНакопления.ПрочиеРасчеты.Измерения.РасчетныйДокумент.Тип;
	
	ТаблицаУдержанныйНДФЛ = Новый ТаблицаЗначений;
	ТаблицаУдержанныйНДФЛ.Колонки.Добавить("ФизЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаУдержанныйНДФЛ.Колонки.Добавить("РасчетныйДокумент", ТипРасчетногоДокумента);
	ТаблицаУдержанныйНДФЛ.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(13));
	
	Возврат ТаблицаУдержанныйНДФЛ;
	
КонецФункции

#Область УдержанияИП

Процедура ОпределитьВидУдержанийИП(ТаблицаЗаработнойПлаты)
	
	СоответствиеВидовНачисленийУдержаний = Новый Соответствие;
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПрочиеУдержания,                           Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДСВ,                                       Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ,                    Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента,            Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты,         Перечисления.ИПВидыНачисленийУдержаний.УдержаноИсполнительныйЛист);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПогашениеЗаймов,                           Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПроцентыПоЗайму,                           Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВозмещениеУщерба,                          Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.УдержаниеПоПрочимОперациямСРаботниками,    Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
	СоответствиеВидовНачисленийУдержаний.Вставить(Перечисления.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм, Перечисления.ИПВидыНачисленийУдержаний.УдержаноДругое);
		
	Если ТаблицаЗаработнойПлаты.Колонки.Найти("ВидНачисленияУдержания") = Неопределено Тогда
		ТаблицаЗаработнойПлаты.Колонки.Добавить("ВидНачисленияУдержания", Новый ОписаниеТипов("ПеречислениеСсылка.ИПВидыНачисленийУдержаний"));
		Для каждого Начисление Из ТаблицаЗаработнойПлаты Цикл
			Начисление.ВидНачисленияУдержания = СоответствиеВидовНачисленийУдержаний.Получить(Начисление.ВидОперации);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияПоРегистрамИПУдержания(Параметры, ТаблицаУдержаний, Реквизиты, ТаблицаЗаработнойПлаты, Движения, Отказ)
	
	Если Не ЗначениеЗаполнено(ТаблицаУдержаний) Тогда
		Возврат;
	КонецЕсли;		
	
	Если Движения.Найти("ИПРасходыНаОплатуТруда") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОпределитьВидУдержанийИП(ТаблицаУдержаний);
	
	ДетальныйУчетИсполнительныхЛистов = ДетальныйУчетИсполнительныхЛистов(Реквизиты.Период);
	ТаблицаУдержаний = ПодготовитьТаблицуУдержаний(ТаблицаУдержаний, Ложь, Истина, ДетальныйУчетИсполнительныхЛистов);
	
	Если ТаблицаЗаработнойПлаты <> Неопределено Тогда
		ДополнитьТаблицуУдержанийИсточникамиДоходов(ТаблицаУдержаний, ТаблицаЗаработнойПлаты, Реквизиты);
	КонецЕсли;
		
	Если Параметры.Реквизиты.Колонки.Найти("Ссылка") = Неопределено Тогда
		Параметры.Реквизиты.Колонки.Добавить("Ссылка",	Документы.ТипВсеСсылки());
		Параметры.Реквизиты.ЗаполнитьЗначения(Реквизиты.Регистратор, "Ссылка");
	КонецЕсли;
	
	Если Параметры.Реквизиты.Колонки.Найти("Удержания") = Неопределено Тогда
		Параметры.Реквизиты.Колонки.Добавить("Удержания");
		Параметры.Реквизиты.ЗаполнитьЗначения(Истина, "Удержания");
	КонецЕсли;
	
	Параметры.Вставить("ТаблицаНачисленияУдержания", ТаблицаУдержаний);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияРасходовНаОплатуТруда_Удержания(Параметры, Движения, Отказ);
	
КонецПроцедуры

Процедура ДополнитьТаблицуУдержанийИсточникамиДоходов(ТаблицаУдержаний, ТаблицаЗаработнойПлаты, Реквизиты)
	
	ОчиститьТаблицуЗаработнойПлаты(ТаблицаЗаработнойПлаты);
	ТаблицаЗаработнойПлаты = ДополнитьТаблицуЗарплатыАналитикойБухУчета(ТаблицаЗаработнойПлаты);
	ОпределитьВидНачисленийУдержанийИП(ТаблицаЗаработнойПлаты, Реквизиты);
	ТаблицаЗаработнойПлаты = ПодготовитьТаблицуНачисленийУдержаний(ТаблицаЗаработнойПлаты, , Истина, Реквизиты);
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаЗаработнойПлаты Цикл
		
		СтруктураСчетАналитика = Новый Структура("Счет, Субконто1, Субконто2, Субконто3",
			СтрокаТаблицы.КорСчет, СтрокаТаблицы.КорСубконто1, СтрокаТаблицы.КорСубконто2, СтрокаТаблицы.КорСубконто3);
		
		СтруктураКорСчетАналитика = Новый Структура("КорСчет, КорСубконто1, КорСубконто2, КорСубконто3",
			СтрокаТаблицы.Счет, СтрокаТаблицы.Субконто1, СтрокаТаблицы.Субконто2, СтрокаТаблицы.Субконто3);
			
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураСчетАналитика);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураКорСчетАналитика);
		
		НоваяСтрока = ТаблицаУдержаний.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

// Возвращает сведения об удержанном НДФЛ при выплате депонированной зарплаты
//
// Параметры:
//  Движения - КоллекцияДвижений - коллекция движений документа выплаты депонированной заработной платы.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//    * ФизЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо, с которого удержали НДФЛ.
//    * РасчетныйДокумент - ДокументСсылка - документ начисления заработной платы.
//    * Сумма - Число - сумма удержанного НДФЛ.
//
Функция УдержанныйНДФЛПоДаннымДепонированияЗарплаты(Движения) Экспорт

	УдержанныйНДФЛ = НоваяТаблицаУдержанногоПриВыплатеНДФЛ();
	
	Если Движения.Найти("ВзаиморасчетыСДепонентами") = Неопределено Тогда
		Возврат УдержанныйНДФЛ;
	КонецЕсли;
	
	ВыплатыДепонентов = Движения.ВзаиморасчетыСДепонентами.Выгрузить(, "Ведомость, ФизическоеЛицо, Сумма");
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВыплатыДепонентов", ВыплатыДепонентов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыплатыДепонентов.Ведомость КАК Ведомость,
	|	ВыплатыДепонентов.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВыплатыДепонентов.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТ_ВыплатыДепонентов
	|ИЗ
	|	&ВыплатыДепонентов КАК ВыплатыДепонентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВзаиморасчетыСДепонентамиОбороты.Регистратор КАК ДокументДепонирования,
	|	ВзаиморасчетыСДепонентамиОбороты.Ведомость КАК Ведомость,
	|	ВзаиморасчетыСДепонентамиОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВзаиморасчетыСДепонентамиОбороты.СуммаПриход КАК Сумма
	|ПОМЕСТИТЬ ВТ_НачислениеДепонентов
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСДепонентами.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			(Ведомость, ФизическоеЛицо) В
	|				(ВЫБРАТЬ
	|					ВТ_ВыплатыДепонентов.Ведомость,
	|					ВТ_ВыплатыДепонентов.ФизическоеЛицо
	|				ИЗ
	|					ВТ_ВыплатыДепонентов КАК ВТ_ВыплатыДепонентов)) КАК ВзаиморасчетыСДепонентамиОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_НачислениеДепонентов.ДокументДепонирования, НЕОПРЕДЕЛЕНО) КАК ДокументДепонирования,
	|	ВТ_ВыплатыДепонентов.Ведомость КАК Ведомость,
	|	ВТ_ВыплатыДепонентов.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(ВТ_НачислениеДепонентов.Сумма, 0) КАК СуммаНачисления,
	|	ВТ_ВыплатыДепонентов.Сумма КАК СуммаВыплаты
	|ИЗ
	|	ВТ_ВыплатыДепонентов КАК ВТ_ВыплатыДепонентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НачислениеДепонентов КАК ВТ_НачислениеДепонентов
	|		ПО ВТ_ВыплатыДепонентов.Ведомость = ВТ_НачислениеДепонентов.Ведомость
	|			И ВТ_ВыплатыДепонентов.ФизическоеЛицо = ВТ_НачислениеДепонентов.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛКПеречислению.Регистратор КАК ДокументДепонирования,
	|	НДФЛКПеречислению.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НДФЛКПеречислению.ДокументОснование КАК ДокументОснование,
	|	НДФЛКПеречислению.Сумма + НДФЛКПеречислению.СуммаСПревышения + НДФЛКПеречислению.СуммаСПревышенияПоСтавке18 + НДФЛКПеречислению.СуммаСПревышенияПоСтавке20 + НДФЛКПеречислению.СуммаСПревышенияПоСтавке22 КАК Сумма
	|ИЗ
	|	РегистрНакопления.НДФЛКПеречислению КАК НДФЛКПеречислению
	|ГДЕ
	|	НДФЛКПеречислению.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТ_НачислениеДепонентов.ДокументДепонирования
	|			ИЗ
	|				ВТ_НачислениеДепонентов КАК ВТ_НачислениеДепонентов)
	|	И НДФЛКПеречислению.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.Удержано)";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеНДФЛКПеречислению = РезультатЗапроса[3].Выгрузить();
	
	ДанныеДепонирования = РезультатЗапроса[2].Выбрать();
	
	Пока ДанныеДепонирования.Следующий() Цикл
		
		КоэффициентВыплаты =
			?(ДанныеДепонирования.СуммаНачисления = 0, 0, ДанныеДепонирования.СуммаВыплаты / ДанныеДепонирования.СуммаНачисления);
		КоэффициентВыплаты = Мин(КоэффициентВыплаты, 1);
		
		Отбор = Новый Структура("ДокументДепонирования, ФизическоеЛицо",
			ДанныеДепонирования.ДокументДепонирования, ДанныеДепонирования.ФизическоеЛицо);
		
		НайденныеСтрокиНДФЛ = ДанныеНДФЛКПеречислению.НайтиСтроки(Отбор);
		Для Каждого СтрокаНДФЛ Из НайденныеСтрокиНДФЛ Цикл
			Если СтрокаНДФЛ.Сумма < 0 Тогда
				НоваяСтрока = УдержанныйНДФЛ.Добавить();
				НоваяСтрока.ФизЛицо = СтрокаНДФЛ.ФизическоеЛицо;
				НоваяСтрока.РасчетныйДокумент = СтрокаНДФЛ.ДокументОснование;
				НоваяСтрока.Сумма = -СтрокаНДФЛ.Сумма * КоэффициентВыплаты;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	УдержанныйНДФЛ.Свернуть("ФизЛицо, РасчетныйДокумент", "Сумма");
	
	Возврат УдержанныйНДФЛ;
	
КонецФункции

#КонецОбласти

#Область Резервы

Процедура СформироватьДвиженияНачислениеРезервов(Движения, Отказ, Организация, Знач ПериодРегистрации, ДанныеПоРезервам)
	
	Регистратор       = Движения.Хозрасчетный.Отбор.Регистратор.Значение;
	ПериодРегистрации = ПолучитьПериодРегистрации(Регистратор, ПериодРегистрации);
	
	Если ДанныеПоРезервам.Колонки.Найти("ОблагаетсяЕНВД") = Неопределено Тогда
		ДанныеПоРезервам.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Параметры         = ПодготовитьПараметрыПоОтражениюРезервовВУчете(ДанныеПоРезервам, Организация, ПериодРегистрации, Регистратор);
	Реквизиты         = Параметры.Реквизиты[0];
	ТаблицаРезервов   = Параметры.ТаблицаРезервов;
	
	РазвернутьТаблицуРезервов(ТаблицаРезервов, Реквизиты);
	
	СкорректироватьТаблицуРезервов(ТаблицаРезервов, Реквизиты);
	
	ТаблицаРезервов = ДополнитьТаблицуРезервовАналитикойБухУчета(ТаблицаРезервов, Реквизиты);
	
	ПроверитьЗаполненностьСпособаОтраженияВУчетеРезервов(ТаблицаРезервов, Реквизиты);
	
	СформироватьПроводкиПоОтражениюРезервовВУчете(ТаблицаРезервов, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыПоОтражениюРезервовВУчете(ДанныеДляПроводок, Организация, ПериодРегистрации, Регистратор)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	Реквизиты = Новый ТаблицаЗначений;
	
	Реквизиты.Колонки.Добавить("Организация");
	Реквизиты.Колонки.Добавить("Период");
	Реквизиты.Колонки.Добавить("Регистратор");
	Реквизиты.Колонки.Добавить("ВидОперации");
	Реквизиты.Колонки.Добавить("Резерв");
	Реквизиты.Колонки.Добавить("АрхивныйАлгоритм");
	
	НоваяСтрока             = Реквизиты.Добавить();
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Период      = КонецМесяца(ПериодРегистрации);
	НоваяСтрока.Регистратор = Регистратор;
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.РезервыПоОплатеТруда") Тогда
		СвойстваРегистратора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Регистратор, "ВидОперации, Резерв");
		НоваяСтрока.ВидОперации      = СвойстваРегистратора.ВидОперации;
		НоваяСтрока.Резерв           = СвойстваРегистратора.Резерв;
		НоваяСтрока.АрхивныйАлгоритм = Ложь;
	ИначеЕсли ТипЗнч(Регистратор) = Тип("ДокументСсылка.НачислениеОценочныхОбязательствПоОтпускам") Тогда
		СвойстваРегистратора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Регистратор, "КорректировкаОбязательств, ПериодРегистрации");
		Если СвойстваРегистратора.КорректировкаОбязательств Тогда
			ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Корректировка;
		Иначе
			Если КонецМесяца(СвойстваРегистратора.ПериодРегистрации) = КонецГода(СвойстваРегистратора.ПериодРегистрации) Тогда
				ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация;
			Иначе
				ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление;
			КонецЕсли;
		КонецЕсли;
		НоваяСтрока.ВидОперации      = ВидОперации;
		НоваяСтрока.Резерв           = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.Резервы.РезервЕжегодныхОтпусков");
		НоваяСтрока.АрхивныйАлгоритм = Истина;
	Иначе
		НоваяСтрока.ВидОперации      = Перечисления.ВидыОперацийРезервовПоОплатеТруда.ПустаяСсылка();
		НоваяСтрока.Резерв           = Справочники.Резервы.ПустаяСсылка();
		НоваяСтрока.АрхивныйАлгоритм = Ложь;
	КонецЕсли;
	
	Параметры.Вставить("Реквизиты", Реквизиты);
		
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "Подразделение,"
	+ "СпособОтраженияЗарплатыВБухучете,"
	+ "ОблагаетсяЕНВД,"
	+ "СуммаРезерва,"
	+ "СуммаРезерваСтраховыхВзносов,"
	+ "СуммаРезерваФССНесчастныеСлучаи,"
	+ "СуммаРезерваНУ,"
	+ "СуммаРезерваСтраховыхВзносовНУ,"
	+ "СуммаРезерваФССНесчастныеСлучаиНУ"
	;	
	Параметры.Вставить("ТаблицаРезервов", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ДанныеДляПроводок, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура РазвернутьТаблицуРезервов(ТаблицаРезервов, Реквизиты)
	
	///////////////////////////////////////
	// Описание типов для дополнения таблиц
	ТипОперации = Новый КвалификаторыЧисла(1,0);
	ТипЧисла    = Новый КвалификаторыЧисла(15,2);
	Массив      = Новый Массив;
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧисла    = Новый ОписаниеТипов(Массив, , ,ТипЧисла);
	ОписаниеТиповОперации = Новый ОписаниеТипов(Массив, , ,ТипОперации);
	
	// Описание типов для дополнения таблиц
	///////////////////////////////////////
	Если ТаблицаРезервов.Колонки.Найти("ВидОперацииДокумента") = Неопределено Тогда
		ТаблицаРезервов.Колонки.Добавить("ВидОперацииДокумента", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийРезервовПоОплатеТруда"));
	КонецЕсли;
	
	Если ТаблицаРезервов.Колонки.Найти("Резерв") = Неопределено Тогда
		ТаблицаРезервов.Колонки.Добавить("Резерв", Новый ОписаниеТипов("СправочникСсылка.Резервы"));
	КонецЕсли;
	
	Если ТаблицаРезервов.Колонки.Найти("ВидОперации") = Неопределено Тогда
		ТаблицаРезервов.Колонки.Добавить("ВидОперации", ОписаниеТиповЧисла);
	КонецЕсли;
	
	Если ТаблицаРезервов.Колонки.Найти("СуммаНУ") = Неопределено Тогда
		ТаблицаРезервов.Колонки.Добавить("СуммаНУ", ОписаниеТиповЧисла);
	КонецЕсли;
	
	Если ТаблицаРезервов.Колонки.Найти("Сумма") = Неопределено Тогда
		ТаблицаРезервов.Колонки.Добавить("Сумма", ОписаниеТиповЧисла);
	КонецЕсли;

	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("СуммаРезерва");                        // Вид операции - 1
	МассивКолонок.Добавить("СуммаРезерваСтраховыхВзносов");        // Вид операции - 2
	МассивКолонок.Добавить("СуммаРезерваФССНесчастныеСлучаи");     // Вид операции - 3
	
	КоличествоИндексовМассива   = МассивКолонок.Количество() - 1;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаРезервов Цикл
		
		Если СтрокаТаблицы.ВидОперации = 0 Тогда
			
			Для ИндексМассива = 0 По КоличествоИндексовМассива Цикл
				Если СтрокаТаблицы[МассивКолонок[ИндексМассива]] <> 0 Тогда
					НоваяСтрока = ТаблицаРезервов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.Сумма                = СтрокаТаблицы[МассивКолонок[ИндексМассива]];
					НоваяСтрока.ВидОперации          = ИндексМассива + 1;
					НоваяСтрока.ВидОперацииДокумента = Реквизиты.ВидОперации;
					НоваяСтрока.Резерв               = Реквизиты.Резерв;
				КонецЕсли;
				Если СтрокаТаблицы[МассивКолонок[ИндексМассива]+"НУ"] <> 0 Тогда
					НоваяСтрока = ТаблицаРезервов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.СуммаНУ              = СтрокаТаблицы[МассивКолонок[ИндексМассива]+"НУ"];
					НоваяСтрока.ВидОперации          = ИндексМассива + 1;
					НоваяСтрока.ВидОперацииДокумента = Реквизиты.ВидОперации;
					НоваяСтрока.Резерв               = Реквизиты.Резерв;
				КонецЕсли;
			КонецЦикла;
		Иначе
			СтрокаТаблицы.ВидОперацииДокумента = Реквизиты.ВидОперации;
			СтрокаТаблицы.Резерв               = Реквизиты.Резерв;
		КонецЕсли;
		
	КонецЦикла;

	ТаблицаРезервов.Свернуть("Резерв,ВидОперации,ВидОперацииДокумента,Подразделение,СпособОтраженияЗарплатыВБухучете,ОблагаетсяЕНВД",
							"Сумма, СуммаНУ");
	
КонецПроцедуры

Процедура СкорректироватьТаблицуРезервов(ТаблицаРезервов, Реквизиты)
	
	Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.РезервыПоОплатеТруда") Тогда
		НастройкиРезервов = НастройкиРезерва(Реквизиты.Организация, Реквизиты.Резерв, Реквизиты.Период);
	Иначе
		НастройкиРезервов = НастройкиРезервовОтпусков(Реквизиты.Организация, Реквизиты.Период);
	КонецЕсли;
	
	ФормироватьРезервНУ = НастройкиРезервов.ФормироватьРезервНУ;
	
	МассивСтрокКУдалению = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТаблицаРезервов Цикл
		Если НЕ ФормироватьРезервНУ Тогда
			СтрокаТаблицы.СуммаНУ = 0;
		КонецЕсли;
		Если СтрокаТаблицы.Сумма = 0
			И СтрокаТаблицы.СуммаНУ = 0 Тогда
			МассивСтрокКУдалению.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаМассива Из МассивСтрокКУдалению Цикл
		ТаблицаРезервов.Удалить(СтрокаМассива);
	КонецЦикла;
	
КонецПроцедуры

Функция ДополнитьТаблицуРезервовАналитикойБухУчета(ТаблицаРезервов, Реквизиты)
	
	МассивСчетовУчетаНУ = Новый Массив;
	МассивСчетовУчетаНУ.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаОплатуТрудаБудущихПериодов);
	
	СчетУчетаПрочиеДоходы                = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
	РасходыПоУплатеСтраховыхВзносов      = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("РасходыПоУплатеСтраховыхВзносов");
	СтатьяДоходовРезервыПоОтпускам       = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("РезервыПоОплатеТруда");
	Если ТаблицаРезервов.Найти(Истина, "ОблагаетсяЕНВД") <> Неопределено Тогда
		СтатьяДоходовРезервыПоОтпускамЕНВД = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("РезервыПоОплатеТрудаЕНВД");
	Иначе
		СтатьяДоходовРезервыПоОтпускамЕНВД = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка();
	КонецЕсли;
	СчетаУчетаВнутрихозяйственныеРасчеты =
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ВнутрихозяйственныеРасчеты);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаРезервов",                      ТаблицаРезервов);
	Запрос.УстановитьПараметр("АрхивныйАлгоритм",                     Реквизиты.АрхивныйАлгоритм);
	Запрос.УстановитьПараметр("СчетУчетаПрочиеДоходы",                СчетУчетаПрочиеДоходы);
	Запрос.УстановитьПараметр("СтатьяДоходовРезервыПоОтпускам",       СтатьяДоходовРезервыПоОтпускам);
	Запрос.УстановитьПараметр("СтатьяДоходовРезервыПоОтпускамЕНВД",   СтатьяДоходовРезервыПоОтпускамЕНВД);
	Запрос.УстановитьПараметр("СчетаУчетаВнутрихозяйственныеРасчеты", СчетаУчетаВнутрихозяйственныеРасчеты);
	Если Не ИспользуетсяНовыйПорядокУчетаРезервов(Реквизиты.Период) Тогда
		// Сохраняем "старое" поведение
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаРезервов.Сумма КАК Сумма,
		|	ТаблицаРезервов.СуммаНУ КАК СуммаНУ,
		|	ТаблицаРезервов.Резерв КАК Резерв,
		|	ТаблицаРезервов.ВидОперации КАК ВидОперации,
		|	ТаблицаРезервов.ВидОперацииДокумента КАК ВидОперацииДокумента,
		|	ТаблицаРезервов.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ТаблицаРезервов.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	ТаблицаРезервов.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВТ_Резервы
		|ИЗ
		|	&ТаблицаРезервов КАК ТаблицаРезервов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА &АрхивныйАлгоритм
		|					ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
		|					ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация)
		|				ТОГДА ВЫБОР
		|						КОГДА ВТ_Резервы.Сумма >= 0
		|							ТОГДА ВТ_Резервы.Сумма
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма,
		|	СУММА(ВЫБОР
		|			КОГДА &АрхивныйАлгоритм
		|					ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
		|					ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация)
		|				ТОГДА ВЫБОР
		|						КОГДА ВТ_Резервы.СуммаНУ >= 0
		|							ТОГДА ВТ_Резервы.СуммаНУ
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНУ,
		|	ВТ_Резервы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение КАК Подразделение,
		|	ВТ_Резервы.Резерв КАК Резерв,
		|	ВТ_Резервы.ВидОперации КАК ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента КАК ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет КАК Счет,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1 КАК Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2 КАК Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3 КАК Субконто3,
		|	ЛОЖЬ КАК ДоступнаЗаписьСторно
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА &АрхивныйАлгоритм
		|					ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
		|				ТОГДА ВЫБОР
		|						КОГДА ВТ_Резервы.Сумма < 0
		|							ТОГДА ВТ_Резервы.Сумма
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА &АрхивныйАлгоритм
		|					ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
		|				ТОГДА ВЫБОР
		|						КОГДА ВТ_Резервы.СуммаНУ < 0
		|							ТОГДА ВТ_Резервы.СуммаНУ
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	&СчетУчетаПрочиеДоходы,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ОблагаетсяЕНВД
		|			ТОГДА &СтатьяДоходовРезервыПоОтпускамЕНВД
		|		ИНАЧЕ &СтатьяДоходовРезервыПоОтпускам
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЛОЖЬ
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА НЕ &АрхивныйАлгоритм
		|					И ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация)
		|				ТОГДА ВЫБОР
		|						КОГДА ВТ_Резервы.Сумма < 0
		|							ТОГДА ВТ_Резервы.Сумма
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА НЕ &АрхивныйАлгоритм
		|					И ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация)
		|				ТОГДА ВЫБОР
		|						КОГДА ВТ_Резервы.СуммаНУ < 0
		|							ТОГДА ВТ_Резервы.СуммаНУ
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	&СчетУчетаПрочиеДоходы,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ОблагаетсяЕНВД
		|			ТОГДА &СтатьяДоходовРезервыПоОтпускамЕНВД
		|		ИНАЧЕ &СтатьяДоходовРезервыПоОтпускам
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЛОЖЬ
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ОблагаетсяЕНВД
		|			ТОГДА &СтатьяДоходовРезервыПоОтпускамЕНВД
		|		ИНАЧЕ &СтатьяДоходовРезервыПоОтпускам
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА НЕ &АрхивныйАлгоритм
		|					И (ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|						ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка))
		|				ТОГДА ВТ_Резервы.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА НЕ &АрхивныйАлгоритм
		|					И (ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|						ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка))
		|				ТОГДА ВТ_Резервы.СуммаНУ
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ИСТИНА
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|				ТОГДА -ВТ_Резервы.Сумма
		|			ИНАЧЕ ВТ_Резервы.Сумма
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|				ТОГДА -ВТ_Резервы.СуммаНУ
		|			ИНАЧЕ ВТ_Резервы.СуммаНУ
		|		КОНЕЦ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ &АрхивныйАлгоритм
		|	И ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ";
	Иначе
		// В БУ списание излишка отражается как "сторно" по способу учета.
		// В НУ списание излишка отражается как прочий доход.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаРезервов.Сумма КАК Сумма,
		|	ТаблицаРезервов.СуммаНУ КАК СуммаНУ,
		|	ТаблицаРезервов.Резерв КАК Резерв,
		|	ТаблицаРезервов.ВидОперации КАК ВидОперации,
		|	ТаблицаРезервов.ВидОперацииДокумента КАК ВидОперацииДокумента,
		|	ТаблицаРезервов.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ТаблицаРезервов.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	ТаблицаРезервов.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВТ_Резервы
		|ИЗ
		|	&ТаблицаРезервов КАК ТаблицаРезервов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТ_Резервы.Сумма) КАК Сумма,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Резервы.СуммаНУ > 0
		|				ТОГДА ВТ_Резервы.СуммаНУ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНУ,
		|	ВТ_Резервы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение КАК Подразделение,
		|	ВТ_Резервы.Резерв КАК Резерв,
		|	ВТ_Резервы.ВидОперации КАК ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента КАК ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплатыВБухУчете,
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет КАК Счет,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1 КАК Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2 КАК Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3 КАК Субконто3,
		|	ИСТИНА КАК ДоступнаЗаписьСторно
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	(&АрхивныйАлгоритм
		|			ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
		|			ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация))
		|	И (ВТ_Резервы.Сумма <> 0
		|			ИЛИ ВТ_Резервы.СуммаНУ > 0)
		|	И НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(0),
		|	СУММА(ВТ_Резервы.СуммаНУ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	&СчетУчетаПрочиеДоходы,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ОблагаетсяЕНВД
		|			ТОГДА &СтатьяДоходовРезервыПоОтпускамЕНВД
		|		ИНАЧЕ &СтатьяДоходовРезервыПоОтпускам
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЛОЖЬ
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	(&АрхивныйАлгоритм
		|			ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
		|			ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация))
		|	И ВТ_Резервы.СуммаНУ < 0
		|	И НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ОблагаетсяЕНВД
		|			ТОГДА &СтатьяДоходовРезервыПоОтпускамЕНВД
		|		ИНАЧЕ &СтатьяДоходовРезервыПоОтпускам
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|				ТОГДА -ВТ_Резервы.Сумма
		|			ИНАЧЕ ВТ_Резервы.Сумма
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|				ТОГДА 0
		|			ИНАЧЕ ВТ_Резервы.СуммаНУ
		|		КОНЕЦ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|			ТОГДА ПорядокОтраженияЗарплатыВБухУчете.Счет <> &СчетУчетаПрочиеДоходы
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ &АрхивныйАлгоритм
		|	И (ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|			ИЛИ ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка))
		|	И (ВТ_Резервы.Сумма <> 0
		|			ИЛИ ВТ_Резервы.СуммаНУ <> 0)
		|	И НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|			ТОГДА ПорядокОтраженияЗарплатыВБухУчете.Счет <> &СчетУчетаПрочиеДоходы
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(0),
		|	СУММА(-ВТ_Резервы.СуммаНУ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	&СчетУчетаПрочиеДоходы,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ОблагаетсяЕНВД
		|			ТОГДА &СтатьяДоходовРезервыПоОтпускамЕНВД
		|		ИНАЧЕ &СтатьяДоходовРезервыПоОтпускам
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	ЛОЖЬ
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ &АрхивныйАлгоритм
		|	И ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|	И ВТ_Резервы.СуммаНУ <> 0
		|	И НЕ ПорядокОтраженияЗарплатыВБухУчете.Счет В (&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ОблагаетсяЕНВД
		|			ТОГДА &СтатьяДоходовРезервыПоОтпускамЕНВД
		|		ИНАЧЕ &СтатьяДоходовРезервыПоОтпускам
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|				ТОГДА -ВТ_Резервы.Сумма
		|			ИНАЧЕ ВТ_Резервы.Сумма
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
		|				ТОГДА -ВТ_Резервы.СуммаНУ
		|			ИНАЧЕ ВТ_Резервы.СуммаНУ
		|		КОНЕЦ),
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Подразделение,
		|	ВТ_Резервы.Резерв,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		|		ПО ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
		|			И ВТ_Резервы.ОблагаетсяЕНВД = ПорядокОтраженияЗарплатыВБухУчете.ЕНВД
		|ГДЕ
		|	НЕ &АрхивныйАлгоритм
		|	И ПорядокОтраженияЗарплатыВБухУчете.Счет В(&СчетаУчетаВнутрихозяйственныеРасчеты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Резервы.СпособОтраженияЗарплатыВБухУчете,
		|	ВТ_Резервы.ОблагаетсяЕНВД,
		|	ВТ_Резервы.Резерв,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто3,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
		|	ПорядокОтраженияЗарплатыВБухУчете.Счет,
		|	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
		|	ВТ_Резервы.ВидОперации,
		|	ВТ_Резервы.ВидОперацииДокумента,
		|	ВТ_Резервы.Подразделение,
		|	ВЫБОР
		|		КОГДА ВТ_Резервы.ВидОперацииДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ";
	КонецЕсли;
	
	ТаблицаРезервовСчетаИАналитика =  Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления <> НЕОПРЕДЕЛЕНО
	|			ТОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатрат ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяСсылка)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяЗатратНачисления,
	|	СтатьиЗатратПоЗарплате.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	РегистрСведений.СтатьиЗатратПоЗарплате КАК СтатьиЗатратПоЗарплате
	|ГДЕ
	|	СтатьиЗатратПоЗарплате.ВидСтатьиЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДополнительныхСтатейЗатрат.ФСС_НС)";
	
	СтатьиЗатрат_ФСС_НСиПЗ = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления <> НЕОПРЕДЕЛЕНО
	|			ТОГДА СтатьиЗатратПоЗарплате.СтатьяЗатратНачисления
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СтатьиЗатратПоЗарплате.СтатьяЗатрат ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяСсылка)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяЗатратНачисления,
	|	СтатьиЗатратПоЗарплате.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	РегистрСведений.СтатьиЗатратПоЗарплате КАК СтатьиЗатратПоЗарплате
	|ГДЕ
	|	СтатьиЗатратПоЗарплате.ВидСтатьиЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДополнительныхСтатейЗатрат.СтраховыеВзносы)";
	
	СтатьиЗатрат_СтраховыеВзносы = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаРезервовСчетаИАналитика Цикл
		
		Если СтрокаТаблицы.ВидОперации = 3 Тогда
			
			// Взносы в ФСС от НС и ПЗ
			
			Для НомерСубконто = 1 По 3 Цикл
				
				ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто,     , СтатьиЗатрат_ФСС_НСиПЗ);

			КонецЦикла;
						
			
		ИначеЕсли СтрокаТаблицы.ВидОперации = 2 Тогда
			
			// Взносы в ПФР, ФОМС и ФСС
			
			Для НомерСубконто = 1 По 3 Цикл
				
				ЗаменитьЗначениеСтатьиЗатрат(СтрокаТаблицы, НомерСубконто,     , СтатьиЗатрат_СтраховыеВзносы);

			КонецЦикла;
			
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаРезервовСчетаИАналитика;
	
КонецФункции

Процедура СформироватьПроводкиПоОтражениюРезервовВУчете(ТаблицаРезервов, Реквизиты, Движения, Отказ)
	
	Если ТаблицаРезервов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СодержанияПроводок = Новый Соответствие;
	Если Реквизиты.АрхивныйАлгоритм Тогда
		Если КонецГода(Реквизиты.Период) = КонецМесяца(Реквизиты.Период) Тогда
			СодержанияПроводок.Вставить(1, НСтр("ru = 'Инвентаризация обязательств (резервов) по предстоящим отпускам'"));
			СодержанияПроводок.Вставить(2, НСтр("ru = 'Инвентаризация страховых взносов с обязательств (резервов) по предстоящим отпускам'"));
			СодержанияПроводок.Вставить(3, НСтр("ru = 'Инвентаризация взносов в ФСС от НС и ПЗ с обязательств (резервов) по предстоящим отпускам'"));
		Иначе
			СодержанияПроводок.Вставить(1, НСтр("ru = 'Начисление обязательств (резервов) по предстоящим отпускам'"));
			СодержанияПроводок.Вставить(2, НСтр("ru = 'Начисление страховых взносов с обязательств (резервов) по предстоящим отпускам'"));
			СодержанияПроводок.Вставить(3, НСтр("ru = 'Начисление взносов в ФСС от НС и ПЗ с обязательств (резервов) по предстоящим отпускам'"));
		КонецЕсли;
		
		НастройкиУчетаЗарплаты = РегистрыСведений.НастройкиУчетаЗарплаты.Получить(Новый Структура("Организация", Реквизиты.Организация));
		РезервОтпусковЭлемент = НастройкиУчетаЗарплаты.РезервОтпусков;
		ТаблицаРезервов.ЗаполнитьЗначения(РезервОтпусковЭлемент, "Резерв");
	Иначе
		Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация Тогда
			СодержанияПроводок.Вставить(1, НСтр("ru = 'Инвентаризация обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(2, НСтр("ru = 'Инвентаризация страховых взносов с обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(3, НСтр("ru = 'Инвентаризация взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда'"));
		ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление Тогда
			СодержанияПроводок.Вставить(1, НСтр("ru = 'Начисление обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(2, НСтр("ru = 'Начисление страховых взносов с обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(3, НСтр("ru = 'Начисление взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда'"));
		ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Списание Тогда
			СодержанияПроводок.Вставить(1, НСтр("ru = 'Списание обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(2, НСтр("ru = 'Списание страховых взносов с обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(3, НСтр("ru = 'Списание взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда'"));
		Иначе // Корректировка
			СодержанияПроводок.Вставить(1, НСтр("ru = 'Корректировка обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(2, НСтр("ru = 'Корректировка страховых взносов с обязательств (резервов) по оплате труда'"));
			СодержанияПроводок.Вставить(3, НСтр("ru = 'Корректировка взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда'"));
		КонецЕсли;
	КонецЕсли;
	
	СчетРезерваОтпусков        = ПланыСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям;
	СчетВзносовРезерваОтпусков = ПланыСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам;
	
	ТаблицаРезервов.Сортировать("ВидОперации, Счет, Подразделение, Субконто1, Субконто2, Субконто3");
	
	ИспользуетсяНовыйПорядокУчетаРезервов = ИспользуетсяНовыйПорядокУчетаРезервов(Реквизиты.Период);
	
	Для Каждого СтрокаТаблицы Из ТаблицаРезервов Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
			// не заполнены счет и аналитика
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.Сумма = 0 
			И СтрокаТаблицы.СуммаНУ = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Содержание  = СодержанияПроводок.Получить(СтрокаТаблицы.ВидОперации);
		
		Если СтрокаТаблицы.Сумма > 0 
			ИЛИ СтрокаТаблицы.СуммаНУ > 0
			ИЛИ СтрокаТаблицы.ДоступнаЗаписьСторно Тогда
			
			Проводка.Сумма       = СтрокаТаблицы.Сумма;
			
			Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Списание
				И Не ИспользуетсяНовыйПорядокУчетаРезервов Тогда
				
				Проводка.СчетКт = СтрокаТаблицы.Счет;
				
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.Субконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.Субконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.Субконто3);
				
				Проводка.СчетДт = ?(СтрокаТаблицы.ВидОперации = 1, СчетРезерваОтпусков, СчетВзносовРезерваОтпусков);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.Резерв);
			Иначе
				Проводка.СчетДт = СтрокаТаблицы.Счет;
				
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.Субконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.Субконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.Субконто3);
				
				Проводка.СчетКт = ?(СтрокаТаблицы.ВидОперации = 1, СчетРезерваОтпусков, СчетВзносовРезерваОтпусков);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.Резерв);
			КонецЕсли;
			
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если СвойстваСчетаДт.НалоговыйУчет Тогда
				Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					Проводка.СуммаНУДт = СтрокаТаблицы.Сумма;
				Иначе
					Проводка.СуммаНУДт = СтрокаТаблицы.СуммаНУ;
					Проводка.СуммаВРДт = СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНУ;
				КонецЕсли;
			КонецЕсли;
			
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если СвойстваСчетаКт.НалоговыйУчет Тогда
				Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					Проводка.СуммаНУКт = СтрокаТаблицы.Сумма;
				Иначе
					Проводка.СуммаНУКт = СтрокаТаблицы.СуммаНУ;
					Проводка.СуммаВРКт = СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНУ;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			Проводка.Сумма       = -СтрокаТаблицы.Сумма;
			
			Проводка.СчетКт = СтрокаТаблицы.Счет;
			
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.Субконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.Субконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.Субконто3);
			
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если СвойстваСчетаКт.НалоговыйУчет Тогда
				Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					Проводка.СуммаНУКт = -СтрокаТаблицы.Сумма;
				Иначе
					Проводка.СуммаНУКт = -СтрокаТаблицы.СуммаНУ;
					Проводка.СуммаВРКт = -СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНУ;
				КонецЕсли;
			КонецЕсли;
			
			Проводка.СчетДт = ?(СтрокаТаблицы.ВидОперации = 1, СчетРезерваОтпусков, СчетВзносовРезерваОтпусков);
			
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.Резерв);
			
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если СвойстваСчетаДт.НалоговыйУчет Тогда
				Если СтрокаТаблицы.ОблагаетсяЕНВД Тогда
					Проводка.СуммаНУДт = -СтрокаТаблицы.Сумма;
				Иначе
					Проводка.СуммаНУДт = -СтрокаТаблицы.СуммаНУ;
					Проводка.СуммаВРДт = -СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНУ;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
	Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Процедура ПроверитьЗаполненностьСпособаОтраженияВУчетеРезервов(ТаблицаРезервов, Реквизиты)
	
	ТаблицаПроверки = ТаблицаРезервов.Скопировать(,"СпособОтраженияЗарплатыВБухУчете, Счет");	
	ТаблицаПроверки.Свернуть("СпособОтраженияЗарплатыВБухУчете, Счет",);
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаПроверки Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.СпособОтраженияЗарплатыВБухУчете) Тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
				ТекстСообщения = НСтр("ru = '%1. Не указаны счет и аналитика для способа отражения ""%2""!'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстСообщения,
					Реквизиты.Регистратор,
					СтрокаТаблицы.СпособОтраженияЗарплатыВБухУчете); 
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Функция ИспользуетсяНовыйПорядокУчетаРезервов(Период)
	
	// Суть нового порядка учета:
	// 1. Списание излишка отражается в БУ как "сторно" начислений по способу учета указанному в документе
	// 2. Списание излишка отражается в НУ всегда как прочий доход
	// 3. Для упрощения и минимизации изменений при учете зарплаты в БП п. 1 используется только для операции "Списание"
	
	ИспользуетсяНовыйПорядокУчетаРезервов =
		Период >= ДатаНачалаПримененияНовойМетодикиУчетаРезервов();
	
	Возврат ИспользуетсяНовыйПорядокУчетаРезервов;
	
КонецФункции

#КонецОбласти

#Область Выплата

#Область ВыплатаЗарплаты

// Предназначена для генерации движений, связанных с отражением в регл. учете перечисления/выплаты зарплаты
//
// Параметры:
//	ТаблицаПеречислениеЗарплаты - таблица значений с колонками
//      Подразделение
//		ФизическоеЛицо
//      СуммаКВыплате
//      КомпенсацияЗаЗадержкуЗарплаты
//      СтатьяДвиженияДенежныхСредств
//      БанковскийСчет
//      СчетУчета
//      СчетУчетаКомпенсации
//      СубконтоУчетаКомпенсации1
//      СубконтоУчетаКомпенсации2
//      СубконтоУчетаКомпенсации3
//      ПодразделениеУчетаКомпенсации
//      Ведомость
//	ТаблицаРеквизиты  - таблица значений содержит реквизиты документа
//	Движения
//	Отказ
//
Процедура СформироватьДвиженияПеречислениеВыплатаЗарплаты(ДокументОбъект, ТаблицаПеречислениеЗарплаты, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаПеречислениеЗарплаты) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВыплатыЗарплаты(ТаблицаПеречислениеЗарплаты, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	СформироватьПроводкиПоВыплатеЗарплаты(Параметры.ВыплатаЗаработнойПлаты, Реквизиты, Движения, Отказ);
	
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	ВедетсяУчетРасчетовПоЗарплатеСводно   = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно");
	
	// Обращение к БЗКБ
	Если НЕ (УчетЗарплатыИКадровВоВнешнейПрограмме И ВедетсяУчетРасчетовПоЗарплатеСводно) Тогда
		МассивВедомостей    = ТаблицаПеречислениеЗарплаты.ВыгрузитьКолонку("Ведомость");
		МассивФизическихЛиц = ТаблицаПеречислениеЗарплаты.ВыгрузитьКолонку("ФизическоеЛицо");
		ЗарплатаКадры.ЗарегистрироватьОплатуВедомостей(ДокументОбъект, Реквизиты.Организация, МассивВедомостей, МассивФизическихЛиц,, Отказ);
	КонецЕсли;
	
	Если Не УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		СформироватьДвиженияПоУдержанномуНДФЛПриВыплатеДохода(Движения, Параметры, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьПараметрыВыплатыЗарплаты(ТаблицаПеречислениеЗарплаты, ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата> - период движений - дата документа
	+ "Регистратор,"               // <ДокументСсылка>
	+ "Содержание,"                // <Строка>
	+ "Организация";               // <СправочникСсылка.Организации>
	
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ВыплатаЗаработнойПлаты
	
	СписокОбязательныхКолонок = ""
	+ "Подразделение,"                 // <Ссылка на справочник подразделений>  - подразделение, задолженность по которому погашается
	+ "ФизическоеЛицо,"                // <СправочникСсылка.ФизическиеЛица> - физическое лико, которому выплачивается зарплата
	+ "СуммаКВыплате,"                 // <Число,15,2> - сумма выплаты в валюте документа
	+ "КомпенсацияЗаЗадержкуЗарплаты," // <Число,15,2> - сумма компенсации в валюте документа
	+ "СтатьяДвиженияДенежныхСредств," // <СправочникСсылка.СтатьиДвиженияДенежныхСредств>
	+ "БанковскийСчет,"                // <СправочникСсылка.БанковскиеСчета>
	+ "СчетУчета,"                     // <ПланСчетовСсылка.Хозрасчетный> - счет учета денежных средств
	+ "СчетУчетаКомпенсации,"          // <ПланСчетовСсылка.Хозрасчетный> - счет учета компенсации
	+ "СубконтоУчетаКомпенсации1,"     //<Характеристика.ВидыСубконтоХозрасчетные> - субконто 1 по счету учета комппенсации
	+ "СубконтоУчетаКомпенсации2,"     //<Характеристика.ВидыСубконтоХозрасчетные> - субконто 2 по счету учета комппенсации
	+ "СубконтоУчетаКомпенсации3,"     //<Характеристика.ВидыСубконтоХозрасчетные> - субконто 3 по счету учета комппенсации
	+ "ПодразделениеУчетаКомпенсации," // <Ссылка на справочник подразделений>  - подразделение, задолженность по депонентам которого погашается	
	+ "Ведомость";                     // <ДокументСсылка.ВедомостьВКассу...ДокументСсылка.ВедомостьВБанк> - ведомости, по которым производится выплата
	
	Параметры.Вставить("ВыплатаЗаработнойПлаты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПеречислениеЗарплаты, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

Процедура СформироватьПроводкиПоВыплатеЗарплаты(ТаблицаВыплатыЗаработнойПлаты, Реквизиты, Движения, Отказ)
	
	ТаблицаВыплаты = ТаблицаВыплатыЗаработнойПлаты.Скопировать();
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	СчетРасчетовСПерсоналомПоОплатеТруда = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
	СчетПрочихРасчетовСФизическимиЛицами = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами;
	
	ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасходовРасчетыСКонтрагентами = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	
	ВестиУчетПоРаботникам = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеПоРаботникам");
	
	Содержание = СокрЛП(Реквизиты.Содержание);
	ЕстьТекстЗаработнаяПлата = СтрНайти(Содержание, "Заработн") > 0 И СтрНайти(Содержание, "плат") > 0;
	
	Если НЕ ВестиУчетПоРаботникам Тогда
		Если НЕ ЕстьТекстЗаработнаяПлата Тогда
			Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
				Ведомости = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(
				ТаблицаВыплаты.ВыгрузитьКолонку("Ведомость"));
				Если Ведомости.ВГраница() > 0 Тогда
					НачалоСодержания = НСтр("ru = 'Выплата заработной платы по ведомостям:'");
				ИначеЕсли Ведомости.ВГраница() = 0 Тогда
					НачалоСодержания = НСтр("ru = 'Выплата заработной платы по ведомости'");
				Иначе
					Содержание = НСтр("ru = 'Выплата заработной платы без указания ведомости'");
				КонецЕсли;
				Для инд = 0 По Ведомости.ВГраница() Цикл
					Если ЗначениеЗаполнено(Ведомости[инд]) Тогда
						ДанныеВедомости = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ведомости[инд], "Дата, Номер");
						НомерВедомости  = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеВедомости.Номер, Истина, Истина);
						ДатаВедомости   = Формат(ДанныеВедомости.Дата,"ДЛФ=D");
					Иначе
						НомерВедомости  = НСтр("ru = 'б/н'");
						ДатаВедомости   = НСтр("ru = 'б/д'");
					КонецЕсли;
					Содержание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1 № %2 от %3%4'"),
					?(инд = 0, НачалоСодержания, Содержание),
					НомерВедомости,
					ДатаВедомости,
					?(инд = Ведомости.ВГраница(), "", ",")); 
				КонецЦикла;
			Иначе
				Содержание = НСтр("ru = 'Выплата заработной платы без указания ведомости'");
			КонецЕсли;
			ЕстьТекстЗаработнаяПлата = Истина;
		КонецЕсли;
		
		ТаблицаВыплаты.Свернуть("Подразделение, СтатьяДвиженияДенежныхСредств, СчетУчета, БанковскийСчет, СчетУчетаКомпенсации,
			|СубконтоУчетаКомпенсации1, СубконтоУчетаКомпенсации2, СубконтоУчетаКомпенсации3, ПодразделениеУчетаКомпенсации", "СуммаКВыплате, КомпенсацияЗаЗадержкуЗарплаты");
	Иначе
		ШаблонСодержания = НСтр("ru = 'Выплата заработной платы по ведомости № %1 от %2'");
		ТаблицаВыплаты.Свернуть("Ведомость, ФизическоеЛицо, Подразделение, СтатьяДвиженияДенежныхСредств, СчетУчета, БанковскийСчет, СчетУчетаКомпенсации,
			|СубконтоУчетаКомпенсации1, СубконтоУчетаКомпенсации2, СубконтоУчетаКомпенсации3, ПодразделениеУчетаКомпенсации", "СуммаКВыплате, КомпенсацияЗаЗадержкуЗарплаты");
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаВыплаты Цикл
		
		Если СтрокаТаблицы.СуммаКВыплате + СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты = 0 Тогда
			
			Продолжить;
		КонецЕсли;
		
		// Выплата заработной платы
		
		Если СтрокаТаблицы.СуммаКВыплате <> 0 Тогда
			
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период       = Реквизиты.Период;
			Проводка.Организация  = Реквизиты.Организация;
			
			Если ЕстьТекстЗаработнаяПлата Тогда
				Проводка.Содержание = Содержание;
			Иначе
				Если ВестиУчетПоРаботникам Тогда
					Если ЗначениеЗаполнено(СтрокаТаблицы.Ведомость) Тогда
						ДанныеВедомости = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.Ведомость, "Дата, Номер");
						НомерВедомости  = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеВедомости.Номер, Истина, Истина);
						ДатаВедомости   = Формат(ДанныеВедомости.Дата,"ДЛФ=D");
						Проводка.Содержание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонСодержания,
							НомерВедомости,
							ДатаВедомости); 
					Иначе
						Проводка.Содержание = НСтр("ru = 'Выплата заработной платы без указания ведомости'");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Проводка.Сумма = СтрокаТаблицы.СуммаКВыплате;
			
			Проводка.СчетДт = СчетРасчетовСПерсоналомПоОплатеТруда;
			
			Если ВестиУчетПоРаботникам Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РаботникиОрганизаций", СтрокаТаблицы.ФизическоеЛицо);
			КонецЕсли;
			
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.СчетУчета;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиДвиженияДенежныхСредств", СтрокаТаблицы.СтатьяДвиженияДенежныхСредств);
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет) Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "БанковскиеСчета", СтрокаТаблицы.БанковскийСчет);
			КонецЕсли;
			
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если ОтражатьВНалоговомУчете Тогда
				
				Если СвойстваСчетаДт.НалоговыйУчет Тогда
					Проводка.СуммаНУДт = СтрокаТаблицы.СуммаКВыплате;
				КонецЕсли;
				Если СвойстваСчетаКт.НалоговыйУчет Тогда
					Проводка.СуммаНУКт = СтрокаТаблицы.СуммаКВыплате;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Выплата компенсации за задержку заработной платы
		
		Если СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты <> 0 Тогда
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетУчетаКомпенсации) Тогда
				ТекстСообщения = НСтр("ru = 'Не указан счет учета расчетов по компенсации за задержку заработной платы!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор,,, Отказ);
				Прервать;
			КонецЕсли;
			
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период       = Реквизиты.Период;
			Проводка.Организация  = Реквизиты.Организация;
			Проводка.Содержание   = СтрШаблон(НСтр("ru = 'Выплата компенсации за задержку заработной платы. %1'"), Содержание);
			
			Проводка.Сумма = СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты;
			
			Проводка.СчетДт = СтрокаТаблицы.СчетУчетаКомпенсации;
			
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.СубконтоУчетаКомпенсации1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.СубконтоУчетаКомпенсации2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.СубконтоУчетаКомпенсации3);
			
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.ПодразделениеУчетаКомпенсации;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.СчетУчета;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиДвиженияДенежныхСредств", СтрокаТаблицы.СтатьяДвиженияДенежныхСредств);
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет) Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "БанковскиеСчета", СтрокаТаблицы.БанковскийСчет);
			КонецЕсли;
			
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если ОтражатьВНалоговомУчете Тогда
				
				Если СвойстваСчетаДт.НалоговыйУчет Тогда
					Проводка.СуммаНУДт = СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты;
				КонецЕсли;
				Если СвойстваСчетаКт.НалоговыйУчет Тогда
					Проводка.СуммаНУКт = СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

// Предназначена для генерации движений, связанных с отражением в регл. учете оплат по договорам ГПХ
//
// Параметры:
//	ТаблицаПеречислениеЗарплаты - таблица значений с колонками
//      Подразделение
//		ФизическоеЛицо
//      СуммаКВыплате
//      СтатьяДвиженияДенежныхСредств
//      БанковскийСчет
//      СчетУчета
//	ТаблицаРеквизиты  - таблица значений содержит реквизиты документа
//	Движения
//	Отказ
//
Процедура СформироватьДвиженияОплатаПоДоговорамПодряда(ДокументОбъект, ТаблицаОплатаПоДоговорамПодряда, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаОплатаПоДоговорамПодряда) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыОплатаПоДоговорамПодряда(ТаблицаОплатаПоДоговорамПодряда, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	ТаблицаОплатаПоДоговорамПодряда = Параметры.ОплатаПоДоговорамПодряда;
	
	СформироватьПроводкиПоОплатеПоДоговорамПодряда(ТаблицаОплатаПоДоговорамПодряда, Реквизиты, Движения, Отказ);
	
	УчетЗарплатыИКадровСредствамиБухгалтерии = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	УчетЗарплатыИКадровВоВнешнейПрограмме    = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	ИспользоватьНачисленияПоДоговорам        = ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам");
	
	// Обращение к БЗКБ
	Если ((УчетЗарплатыИКадровСредствамиБухгалтерии И ИспользоватьНачисленияПоДоговорам)
		Или УчетЗарплатыИКадровВоВнешнейПрограмме)
		И ЗначениеЗаполнено(ТаблицаОплатаПоДоговорамПодряда[0].Ведомость) Тогда
		МассивВедомостей    = ТаблицаОплатаПоДоговорамПодряда.ВыгрузитьКолонку("Ведомость");
		МассивФизическихЛиц = ТаблицаОплатаПоДоговорамПодряда.ВыгрузитьКолонку("ФизическоеЛицо");
		ЗарплатаКадры.ЗарегистрироватьОплатуВедомостей(ДокументОбъект, Реквизиты.Организация, МассивВедомостей, МассивФизическихЛиц,, Отказ);
		СформироватьДвиженияПоУдержанномуНДФЛПриВыплатеДохода(Движения, Параметры, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьПараметрыОплатаПоДоговорамПодряда(ТаблицаОплатаПоДоговорамПодряда, ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата> - период движений - дата документа
	+ "Регистратор,"               // <ДокументСсылка>
	+ "Содержание,"                // <Строка>
	+ "Организация";               // <СправочникСсылка.Организации>
	
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ВыплатаЗаработнойПлаты
	
	СписокОбязательныхКолонок = ""
	+ "Подразделение,"                 // <Ссылка на справочник подразделений>  - подразделение, задолженность по которому погашается
	+ "ФизическоеЛицо,"                // <СправочникСсылка.ФизическиеЛица> - физическое лицо, которому выплачивается зарплата
	+ "СуммаКВыплате,"                 // <Число,15,2> - сумма выплаты в валюте документа
	+ "СтатьяДвиженияДенежныхСредств," // <СправочникСсылка.СтатьиДвиженияДенежныхСредств>
	+ "БанковскийСчет,"                // <СправочникСсылка.БанковскиеСчета>
	+ "СчетУчета,"                     // <ПланСчетовСсылка.Хозрасчетный> - счет учета денежных средств
	+ "Ведомость";                     // <ДокументСсылка.ВедомостьВКассу...ДокументСсылка.ВедомостьВБанк> - ведомости, по которым производится выплата
	
	Параметры.Вставить("ОплатаПоДоговорамПодряда", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаОплатаПоДоговорамПодряда, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

Процедура СформироватьПроводкиПоОплатеПоДоговорамПодряда(ТаблицаОплатаПоДоговорамПодряда, Реквизиты, Движения, Отказ)
	
	ТаблицаВыплаты = ТаблицаОплатаПоДоговорамПодряда.Скопировать();
	ТаблицаВыплаты.Свернуть("ФизическоеЛицо, Подразделение, СтатьяДвиженияДенежныхСредств, СчетУчета, БанковскийСчет", "СуммаКВыплате");
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	СчетПрочихРасчетовСФизическимиЛицами = ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами;
	
	Содержание = СокрЛП(Реквизиты.Содержание);
	
	Для Каждого СтрокаТаблицы Из ТаблицаВыплаты Цикл
		
		Если СтрокаТаблицы.СуммаКВыплате = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Оплата по договорам ГПХ
		
		Проводка = Движения.Хозрасчетный.Добавить();
		
		Проводка.Период       = Реквизиты.Период;
		Проводка.Организация  = Реквизиты.Организация;
		Проводка.Содержание = Содержание;
		
		Проводка.Сумма = СтрокаТаблицы.СуммаКВыплате;
		
		Проводка.СчетДт = СчетПрочихРасчетовСФизическимиЛицами;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РаботникиОрганизаций", СтрокаТаблицы.ФизическоеЛицо);
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Проводка.СчетКт = СтрокаТаблицы.СчетУчета;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиДвиженияДенежныхСредств", СтрокаТаблицы.СтатьяДвиженияДенежныхСредств);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет) Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "БанковскиеСчета", СтрокаТаблицы.БанковскийСчет);
		КонецЕсли;
		
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Если ОтражатьВНалоговомУчете Тогда
			
			Если СвойстваСчетаДт.НалоговыйУчет Тогда
				Проводка.СуммаНУДт = СтрокаТаблицы.СуммаКВыплате;
			КонецЕсли;
			Если СвойстваСчетаКт.НалоговыйУчет Тогда
				Проводка.СуммаНУКт = СтрокаТаблицы.СуммаКВыплате;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ВыплатаЗарплатыПоДокументам(СписокВедомостей, Организация) Экспорт
	
	ВыплатаЗарплатыПоДокументам = Новый ТаблицаЗначений;
	ВыплатаЗарплатыПоДокументам.Колонки.Добавить("ФизЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ВыплатаЗарплатыПоДокументам.Колонки.Добавить("РасчетныйДокумент", Метаданные.ОпределяемыеТипы.ДокументОснованиеЗарплатыКВыплате.Тип);
	ВыплатаЗарплатыПоДокументам.Колонки.Добавить("ДатаРасчетногоДокумента", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ВыплатаЗарплатыПоДокументам.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	ВедетсяУчетРасчетовПоЗарплатеСводно = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно");
	
	Если Не ЗначениеЗаполнено(СписокВедомостей) Или УчетЗарплатыИКадровВоВнешнейПрограмме Или ВедетсяУчетРасчетовПоЗарплатеСводно Тогда
		Возврат ВыплатаЗарплатыПоДокументам;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокВедомостей", СписокВедомостей);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Подзапрос.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Подзапрос.ДокументОснование КАК ДокументОснование,
	|	СУММА(Подзапрос.СуммаКВыплате) КАК СуммаКВыплате
	|ПОМЕСТИТЬ ВТ_ЗарплатаКВыплате
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗарплатаКВыплате.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ЗарплатаКВыплате.ДокументОснование КАК ДокументОснование,
	|		ЗарплатаКВыплате.СуммаКВыплате КАК СуммаКВыплате
	|	ИЗ
	|		РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
	|	ГДЕ
	|		ЗарплатаКВыплате.Организация = &Организация
	|		И ЗарплатаКВыплате.Регистратор В(&СписокВедомостей)
	|		И ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗарплатаКВыплатеАвансом.ФизическоеЛицо,
	|		ЗарплатаКВыплатеАвансом.ДокументОснование,
	|		ЗарплатаКВыплатеАвансом.СуммаКВыплате
	|	ИЗ
	|		РегистрНакопления.ЗарплатаКВыплатеАвансом КАК ЗарплатаКВыплатеАвансом
	|	ГДЕ
	|		ЗарплатаКВыплатеАвансом.Организация = &Организация
	|		И ЗарплатаКВыплатеАвансом.Регистратор В(&СписокВедомостей)
	|		И ЗарплатаКВыплатеАвансом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)) КАК Подзапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	Подзапрос.ФизическоеЛицо,
	|	Подзапрос.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗарплатаКВыплате.ФизическоеЛицо КАК ФизЛицо,
	|	ВТ_ЗарплатаКВыплате.ДокументОснование КАК РасчетныйДокумент,
	|	ЕСТЬNULL(РеквизитыРасчетныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаРасчетногоДокумента,
	|	ВТ_ЗарплатаКВыплате.СуммаКВыплате КАК Сумма
	|ИЗ
	|	ВТ_ЗарплатаКВыплате КАК ВТ_ЗарплатаКВыплате
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыРасчетныхДокументов
	|		ПО (РеквизитыРасчетныхДокументов.Организация = &Организация)
	|			И ВТ_ЗарплатаКВыплате.ДокументОснование = РеквизитыРасчетныхДокументов.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРасчетногоДокумента,
	|	РасчетныйДокумент,
	|	ФизЛицо";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.РасчетныйДокумент) Тогда
			ЗаполнитьЗначенияСвойств(ВыплатаЗарплатыПоДокументам.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВыплатаЗарплатыПоДокументам;
	
КонецФункции

#КонецОбласти

#Область ВыплатаДепонентов

// Предназначена для генерации движений, связанных с отражением в регл. учете выплаты депонентов
//
// Параметры:
//	ТаблицаПеречислениеЗарплаты - таблица значений с колонками
//      Подразделение
//		ФизическоеЛицо
//      СуммаКВыплате
//      СтатьяДвиженияДенежныхСредств
//      СчетУчета
//      Ведомость
//	ТаблицаРеквизиты  - таблица значений содержит реквизиты документа
//	Движения
//	Отказ
//
Процедура СформироватьДвиженияВыплатаДепонентов(ТаблицаВыплатаДепонентов, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаВыплатаДепонентов) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВыплатыДепонентов(ТаблицаВыплатаДепонентов, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	СуммаДокумента = Реквизиты.СуммаДокумента;
    	
	ВыплатаДепонентов = Параметры.ВыплатаДепонентов;
	СуммаПоПустойВедомости = Макс(0, СуммаДокумента - ВыплатаДепонентов.Итог("СуммаКВыплате"));
	
	Если СуммаПоПустойВедомости <> 0 Тогда
		НоваяСтрока = ВыплатаДепонентов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыплатаДепонентов[0]);
		НоваяСтрока.СуммаКВыплате = СуммаПоПустойВедомости;
		НоваяСтрока.Ведомость = Неопределено;
	КонецЕсли;
	
	СтрокиКУдалению = Новый Массив();
	Для Каждого СтрокаТаблицы ИЗ ВыплатаДепонентов Цикл
		Если СтрокаТаблицы.СуммаКВыплате = 0 Тогда
			СтрокиКУдалению.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиКУдалению.Количество() > 0 Тогда
		Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ВыплатаДепонентов.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;
		
	СформироватьПроводкиПоВыплатеДепонентов(ВыплатаДепонентов, Реквизиты, Движения, Отказ);
	
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	ВедетсяУчетРасчетовПоЗарплатеСводно   = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно");
	// Обращение к БЗКБ
	Если НЕ (УчетЗарплатыИКадровВоВнешнейПрограмме И ВедетсяУчетРасчетовПоЗарплатеСводно) Тогда
		Параметры.ВыплатаДепонентов.Колонки.СуммаКВыплате.Имя = "Сумма";
		ДанныеПервичногоДокумента = РегистрыСведений.ДанныеПервичныхДокументов.ДанныеПервичногоДокумента(Реквизиты.Регистратор);
		НомерПлатежногоДокумента = ?(ПустаяСтрока(ДанныеПервичногоДокумента.Номер), ДанныеПервичногоДокумента.НомерРегистратора, ДанныеПервичногоДокумента.Номер);
		УчетДепонированнойЗарплаты.СписатьДепонированнуюЗарплату(Движения, Отказ, Реквизиты.Организация, Реквизиты.Период, Параметры.ВыплатаДепонентов, , НомерПлатежногоДокумента);
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьПараметрыВыплатыДепонентов(ТаблицаВыплатаДепонентов, ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата> - период движений - дата документа
	+ "Регистратор,"               // <ДокументСсылка>
	+ "СуммаДокумента,"            // <Число,15,2> - сумма выплаты в валюте документа
	+ "Организация";               // <СправочникСсылка.Организации>
	
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ВыплатаДепонентов
	
	СписокОбязательныхКолонок = ""
	+ "Подразделение,"                 // <Ссылка на справочник подразделений>  - подразделение, задолженность по которому погашается
	+ "ФизическоеЛицо,"                // <СправочникСсылка.ФизическиеЛица> - физическое лико, которому выплачивается зарплата
	+ "СуммаКВыплате,"                 // <Число,15,2> - сумма выплаты в валюте документа	
	+ "БанковскийСчет,"                // <СправочникСсылка.БанковскиеСчета>
	+ "СтатьяДвиженияДенежныхСредств," // <СправочникСсылка.СтатьиДвиженияДенежныхСредств>
	+ "СчетУчета,"                     // <ПланСчетовСсылка.Хозрасчетный> - счет учета денежных средств
	+ "Ведомость";                     // <ДокументСсылка.ВедомостьВКассу> - ведомость, по которым производится выплата
	
	Параметры.Вставить("ВыплатаДепонентов", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВыплатаДепонентов, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

Процедура СформироватьПроводкиПоВыплатеДепонентов(ТаблицаВыплатаДепонентов, Реквизиты, Движения, Отказ)
	
	ТаблицаВыплаты = ТаблицаВыплатаДепонентов.Скопировать();

	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	СчетРасчетовПоДепонированнымСуммам = ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам;
	
	ВестиУчетПоРаботникам = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеПоРаботникам");
	
	СтрокаСвертки = "Подразделение, СтатьяДвиженияДенежныхСредств, СчетУчета, БанковскийСчет";
	Если ВестиУчетПоРаботникам Тогда
		СтрокаСвертки = "ФизическоеЛицо," + СтрокаСвертки;
	КонецЕсли;
	ТаблицаВыплаты.Свернуть(СтрокаСвертки, "СуммаКВыплате");
		
	Для Каждого СтрокаТаблицы Из ТаблицаВыплаты Цикл
		
		Если СтрокаТаблицы.СуммаКВыплате = 0 Тогда
			
			Продолжить;
		КонецЕсли;
				
		Если СтрокаТаблицы.СуммаКВыплате <> 0 Тогда
			
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период       = Реквизиты.Период;
			Проводка.Организация  = Реквизиты.Организация;
			Проводка.Содержание   = НСтр("ru = 'Выплата депонентов'");
			
			Проводка.Сумма = СтрокаТаблицы.СуммаКВыплате;
			
			Проводка.СчетДт = СчетРасчетовПоДепонированнымСуммам;
			
			Если ВестиУчетПоРаботникам Тогда			
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РаботникиОрганизаций", СтрокаТаблицы.ФизическоеЛицо);
			КонецЕсли;
			
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.СчетУчета;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиДвиженияДенежныхСредств", СтрокаТаблицы.СтатьяДвиженияДенежныхСредств);
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет) Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "БанковскиеСчета", СтрокаТаблицы.БанковскийСчет);
			КонецЕсли;
			
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если ОтражатьВНалоговомУчете Тогда
				
				Если СвойстваСчетаДт.НалоговыйУчет Тогда
					Проводка.СуммаНУДт = СтрокаТаблицы.СуммаКВыплате;
				КонецЕсли;
				Если СвойстваСчетаКт.НалоговыйУчет Тогда
					Проводка.СуммаНУКт = СтрокаТаблицы.СуммаКВыплате;
				КонецЕсли;
				
			КонецЕсли;
						
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СинхронизацияДокументовВыплаты

Процедура ОбработатьИзменениеСостоянияПлатежногоДокумента(Ведомость, ДокументСсылка, ТипОбработки, НомерСтроки, Отказ) Экспорт
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	
	ТекстЖурналаВСлучаеОшибки   = НСтр("ru = 'Обработка контекстной записи документов выплаты по ведомости'",
		ОбщегоНазначения.КодОсновногоЯзыка());
	ТекстСообщенияВСлучаеОшибки = "";
	
	Если ТипОбработки = "Проведение" Тогда
		ТекстСообщенияВСлучаеОшибки = НСтр("ru = 'Не удалось провести документ %1'");
		Попытка
			Если ДокументОбъект.ПроверитьЗаполнение() Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				Отказ = Истина;
			КонецЕсли;
		Исключение
				Отказ = Истина;
		КонецПопытки;
	ИначеЕсли ТипОбработки = "ОтменаПроведения" Тогда
		ТекстСообщенияВСлучаеОшибки = НСтр("ru = 'Не удалось отменить проведение документа %1'");
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Исключение
			Отказ = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если Отказ Тогда
		ТекстСообщенияПользователю = СтрШаблон(ТекстСообщенияВСлучаеОшибки, ДокументСсылка);
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщенияПользователю,
			Ведомость,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОплатыТаблица", НомерСтроки, "Представление"),
			,
			Отказ);
		
		ТекстСообщения = СтрШаблон(ТекстСообщенияВСлучаеОшибки,
			Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ТекстЖурналаВСлучаеОшибки,
			УровеньЖурналаРегистрации.Ошибка,
			ДокументОбъект.Метаданные(),
			ДокументСсылка,
			ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьТаблицуОплат(ДокументОбъект, ОплатыТаблица) Экспорт
	
	ОплатыТаблица.Очистить();
	// Получим связанные документы
	
	МассивТипов = Новый Массив;
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплатыВКассу")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплаты") Тогда
		МассивТипов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ДепонированиеЗарплаты"));
	КонецЕсли;
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплатыВБанк")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплаты") Тогда
		МассивТипов.Добавить(Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ПодтверждениеЗачисленияЗарплаты"));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ведомость",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Организация",       ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("ОбслуживаемыеТипы", МассивТипов);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СвязанныеДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_СвязанныеДокументы
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&Ведомость) КАК СвязанныеДокументы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СвязанныеДокументы.Ссылка) В (&ОбслуживаемыеТипы)
	|
	|СГРУППИРОВАТЬ ПО
	|	СвязанныеДокументы.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Ссылка КАК Ссылка,
	|	РасходныйКассовыйОрдер.Номер КАК Номер,
	|	РасходныйКассовыйОрдер.Дата КАК Дата
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	НЕ РасходныйКассовыйОрдер.ПометкаУдаления
	|	И РасходныйКассовыйОрдер.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_СвязанныеДокументы.Ссылка
	|			ИЗ
	|				ВТ_СвязанныеДокументы)
	|	И РасходныйКассовыйОрдер.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписаниеСРасчетногоСчета.Ссылка,
	|	СписаниеСРасчетногоСчета.Номер,
	|	СписаниеСРасчетногоСчета.Дата
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|ГДЕ
	|	НЕ СписаниеСРасчетногоСчета.ПометкаУдаления
	|	И СписаниеСРасчетногоСчета.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_СвязанныеДокументы.Ссылка
	|			ИЗ
	|				ВТ_СвязанныеДокументы)
	|	И СписаниеСРасчетногоСчета.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДепонированиеЗарплаты.Ссылка,
	|	ДепонированиеЗарплаты.Номер,
	|	ДепонированиеЗарплаты.Дата
	|ИЗ
	|	Документ.ДепонированиеЗарплаты КАК ДепонированиеЗарплаты
	|ГДЕ
	|	НЕ ДепонированиеЗарплаты.ПометкаУдаления
	|	И ДепонированиеЗарплаты.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_СвязанныеДокументы.Ссылка
	|			ИЗ
	|				ВТ_СвязанныеДокументы)
	|	И ДепонированиеЗарплаты.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка,
	|	ПодтверждениеЗачисленияЗарплаты.Номер,
	|	ПодтверждениеЗачисленияЗарплаты.Дата
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|ГДЕ
	|	НЕ ПодтверждениеЗачисленияЗарплаты.ПометкаУдаления
	|	И ПодтверждениеЗачисленияЗарплаты.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_СвязанныеДокументы.Ссылка
	|			ИЗ
	|				ВТ_СвязанныеДокументы)
	|	И ПодтверждениеЗачисленияЗарплаты.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатаВедомостей.Регистратор КАК Ссылка,
	|	ОплатаВедомостей.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОплатаВедомостей.Регистратор.Дата КАК ДатаДокумента
	|ИЗ
	|	РегистрСведений.ОплатаВедомостейНаВыплатуЗарплаты КАК ОплатаВедомостей
	|ГДЕ
	|	ОплатаВедомостей.Ведомость = &Ведомость
	|	И ОплатаВедомостей.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ФизическоеЛицо,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.Дата
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты.Сотрудники КАК ПодтверждениеЗачисленияЗарплатыСотрудники
	|ГДЕ
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.Проведен
	|	И ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.ПервичныйДокумент = &Ведомость
	|	И ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка,
	|	ФизическоеЛицо";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВсеРегистраторы         = Результат[Результат.Количество() - 2].Выгрузить();
	ПроведенныеРегистраторы = Результат[Результат.Количество() - 1].Выгрузить();
	
	ВсеРегистраторы.Свернуть("Ссылка, Номер, Дата");
	
	МассивДокументовБанкКасса     = Новый Массив;
	МассивДокументовДепонирование = Новый Массив;
	МассивДокументовПодтверждение = Новый Массив;
	Для Каждого Регистратор Из ВсеРегистраторы Цикл
		Если ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
			Или ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
			МассивДокументовБанкКасса.Добавить(Регистратор.Ссылка);
		ИначеЕсли ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.ДепонированиеЗарплаты") Тогда
			МассивДокументовДепонирование.Добавить(Регистратор.Ссылка);
		ИначеЕсли ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.ПодтверждениеЗачисленияЗарплаты") Тогда
			МассивДокументовПодтверждение.Добавить(Регистратор.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ДопРеквизитыРегистратораБанкКасса     = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивДокументовБанкКасса, "ВидОперации, Контрагент");
	ДопРеквизитыРегистратораДепонирование = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивДокументовДепонирование, "Депоненты");
	ДопРеквизитыРегистратораПодтверждение = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивДокументовПодтверждение, "Сотрудники");
	
	ШаблонПредставления = НСтр("ru = '%1 №%2 от %3 - %4'");
	Для Каждого Регистратор Из ВсеРегистраторы Цикл
		ДетализацияРегистратора = ПроведенныеРегистраторы.Скопировать(Новый Структура("Ссылка", Регистратор.Ссылка), "ФизическоеЛицо");
		ФизЛица = ОбщегоНазначения.ВыгрузитьКолонку(ДетализацияРегистратора, "ФизическоеЛицо", Истина);
		Если Не ЗначениеЗаполнено(ФизЛица) Тогда
			ФизЛица = Новый Массив;
			Если ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
				ДопРеквизитыРегистратора = ДопРеквизитыРегистратораБанкКасса[Регистратор.Ссылка];
				Если ДопРеквизитыРегистратора.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику
					Или ДопРеквизитыРегистратора.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаСотрудникуПоДоговоруПодряда Тогда
					ФизЛица.Добавить(ДопРеквизитыРегистратора.Контрагент);
				Иначе
					ФизЛица = ОбщегоНазначения.ВыгрузитьКолонку(ДокументОбъект.Зарплата, "ФизическоеЛицо", Истина);
				КонецЕсли;
			ИначеЕсли ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
				ДопРеквизитыРегистратора = ДопРеквизитыРегистратораБанкКасса[Регистратор.Ссылка];
				Если ДопРеквизитыРегистратора.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
					Или ДопРеквизитыРегистратора.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда
					ФизЛица.Добавить(ДопРеквизитыРегистратора.Контрагент);
				Иначе
					ФизЛица = ОбщегоНазначения.ВыгрузитьКолонку(ДокументОбъект.Зарплата, "ФизическоеЛицо", Истина);
				КонецЕсли;
			ИначеЕсли ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.ДепонированиеЗарплаты") Тогда
				ДопРеквизитыРегистратора = ДопРеквизитыРегистратораДепонирование[Регистратор.Ссылка];
				ФизЛица = ОбщегоНазначения.ВыгрузитьКолонку(ДопРеквизитыРегистратора.Депоненты.Выгрузить(), "ФизическоеЛицо", Истина);
			ИначеЕсли ТипЗнч(Регистратор.Ссылка) = Тип("ДокументСсылка.ПодтверждениеЗачисленияЗарплаты") Тогда
				ДопРеквизитыРегистратора = ДопРеквизитыРегистратораПодтверждение[Регистратор.Ссылка];
				ФизЛица = ОбщегоНазначения.ВыгрузитьКолонку(ДопРеквизитыРегистратора.Сотрудники.Выгрузить(), "ФизическоеЛицо", Истина);
			КонецЕсли;
		КонецЕсли;
		
		ФизЛица = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ФизЛица);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ФизЛица, Справочники.ФизическиеЛица.ПустаяСсылка());
		
		НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Регистратор.Номер, Истина, Ложь);
		ДатаДокумента  = Формат(Регистратор.Дата, "ДЛФ=Д");
		
		Представление = 
			СтрШаблон(
				ШаблонПредставления,
				Регистратор.Ссылка.Метаданные().Представление(),
				НомерДокумента,
				ДатаДокумента,
				ЗарплатаКадры.КраткийСоставФизЛиц(ФизЛица, Регистратор.Дата));
		
		НоваяСтрока = ОплатыТаблица.Добавить();
		НоваяСтрока.Документ      = Регистратор.Ссылка;
		НоваяСтрока.Представление = Представление;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОформитьТаблицуОплат(ОплатыТаблица) Экспорт
	
	// Состояние документа
	// 0 - не проведен
	// 1 - проведен
	// 8 - проведен (ручная корректировка)
	// 9 - не проведен (ручная корректировка)
	
	МассивДокументовРучнаяКорректировка = Новый Массив;
	Для Каждого Строка Из ОплатыТаблица Цикл
		Если Строка.Документ.Метаданные().Реквизиты.Найти("РучнаяКорректировка") <> Неопределено Тогда
			МассивДокументовРучнаяКорректировка.Добавить(Строка.Документ);
		КонецЕсли;
	КонецЦикла;
	РеквизитыДокументовРучнаяКорректировка = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивДокументовРучнаяКорректировка, "РучнаяКорректировка");
	
	МассивВсехДокументов = ОбщегоНазначения.ВыгрузитьКолонку(ОплатыТаблица, "Документ", Истина);
	РеквизитыВсехРегистраторов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивВсехДокументов, "Проведен");
	
	Для Каждого Строка Из ОплатыТаблица Цикл
		
		РеквизитыРегистратора = РеквизитыВсехРегистраторов[Строка.Документ];
		Проведен = РеквизитыРегистратора.Проведен;
		
		РеквизитыРегистратора = РеквизитыДокументовРучнаяКорректировка[Строка.Документ];
		РучнаяКорректировка   = РеквизитыРегистратора <> Неопределено И РеквизитыРегистратора.РучнаяКорректировка;
		
		Если РучнаяКорректировка Тогда
			Если Не Проведен Тогда
				СостояниеДокумента = 9;
			Иначе
				СостояниеДокумента = 8;
			КонецЕсли;
		Иначе
			Если Проведен Тогда
				СостояниеДокумента = 1;
			Иначе
				СостояниеДокумента = 0;
			КонецЕсли;
		КонецЕсли;
		Строка.СостояниеДокумента = СостояниеДокумента;
		Строка.Проведен           = Проведен;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаДокументовВыплаты

Процедура ДобавитьБлокировкуИзмененияОбъекта(КоллекцияБлокировок, СсылкаНаОбъект, Форма) Экспорт
	
	ДобавитьБлокировкуИзмененияОбъектаКадровыйДокумент(КоллекцияБлокировок, СсылкаНаОбъект, Форма);
	ДобавитьБлокировкуИзмененияОбъектаВыплатыЗарплаты(КоллекцияБлокировок, СсылкаНаОбъект, Форма);
	
КонецПроцедуры

Процедура ДобавитьБлокировкуИзмененияОбъектаКадровыйДокумент(КоллекцияБлокировок, СсылкаНаОбъект, Форма) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.ПриемНаРаботу")
		И ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.КадровыйПеревод")
		И ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.Увольнение") Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОДокументе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "ЗагруженИзВнешнегоИсточника, Проведен");
	
	Если СведенияОДокументе.ЗагруженИзВнешнегоИсточника Тогда
		Если Не СведенияОДокументе.Проведен Тогда
			Возврат;
		КонецЕсли;
		
		Описание = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Кадровый документ создан автоматически при синхронизации данных.'"),
			НСтр("ru = '
				|Корректировать документ следует только в программе-источнике.'"));
	Иначе
		Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")
			Или ПолучитьФункциональнуюОпцию("ИнтерфейсЭлектронныхТрудовыхКнижек") Тогда
			Возврат;
		КонецЕсли;
		
		Описание = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Кадровый документ был введен в режиме учета кадров в этой программе.'"),
			НСтр("ru = '
				|В текущем режиме корректировка документа недоступна.'"));
	КонецЕсли;
	БлокировкаИзмененияОбъекта = БлокировкаИзмененияОбъектов.ОписаниеБлокировкиИзмененияОбъекта("КадровыйДокумент", Описание, Ложь);
	КоллекцияБлокировок.Добавить(БлокировкаИзмененияОбъекта);
	
КонецПроцедуры

Процедура ДобавитьБлокировкуИзмененияОбъектаВыплатыЗарплаты(КоллекцияБлокировок, СсылкаНаОбъект, Форма) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк")
		И ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу")
		И ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеОбъекта = СсылкаНаОбъект.Метаданные();
	Если Не ПравоДоступа("Изменение", МетаданныеОбъекта) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОбъектаМетаданных = СсылкаНаОбъект.Метаданные().Имя;
	
	Если (ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк")
			Или ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу"))
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВводНачальныхДанных") Тогда
		
		Описание = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Ведомость введена при регистрации начальных данных.
				|Корректировку следует выполнять в документе ввода остатков.'"));
		БлокировкаИзмененияОбъекта = БлокировкаИзмененияОбъектов.ОписаниеБлокировкиИзмененияОбъекта("ДокументОплаты", Описание, Ложь,
			НСтр("ru = 'Ведомость введеная при регистрации начальных данных не редактируется.'"));
		КоллекцияБлокировок.Добавить(БлокировкаИзмененияОбъекта);
	КонецЕсли;
	
	ЕстьОплатаПоВедомости = ВзаиморасчетыССотрудниками.ЕстьОплатаПоВедомости(СсылкаНаОбъект)
		Или Не ОбменСБанкамиПоЗарплатнымПроектам.ДоступностьПлатежногоДокумента(Форма.Объект.Ссылка);
	ДокументыОплаты = Форма.ОплатыСписок;
	
	Если ЕстьОплатаПоВедомости Тогда
		
		Если ДокументыОплаты.Количество() > 0 Тогда
			
			ПредставлениеРегистраторов = Новый ФорматированнаяСтрока(
				Строка(ДокументыОплаты[0].Представление), , , , ПолучитьНавигационнуюСсылку(ДокументыОплаты[0].Значение));
			
			Если ДокументыОплаты.Количество() > 1 Тогда
				
				Представление = СтрШаблон(НСтр("ru = '%1 и еще %2 док.'"), Строка(ДокументыОплаты[0].Представление), ДокументыОплаты.Количество() - 1);
				
				ПредставлениеРегистраторов = Новый ФорматированнаяСтрока(
					Представление, , , , "РасшифровкаСпискаДокументов");
				
			КонецЕсли;
			
			РасшифровкаРегистраторов = Новый ФорматированнаяСтрока(НСтр("ru = ' - см. вкладку '"),
				Новый ФорматированнаяСтрока(НСтр("ru = 'Платежные документы'"), , , , "ПоказатьПлатежныеДокументы"));
			
			Описание = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Ведомость оплачена ('"),
				ПредставлениеРегистраторов,
				РасшифровкаРегистраторов,
				НСтр("ru = ').
					|Для редактирования ведомости требуется отменить проведение платежных документов.'"));
			
			БлокировкаИзмененияОбъекта = БлокировкаИзмененияОбъектов.ОписаниеБлокировкиИзмененияОбъекта("ДокументОплаты", Описание, Ложь);
			Если Не ПравоДоступа("Изменение", Метаданные.Документы[ИмяОбъектаМетаданных]) Тогда
				БлокировкаИзмененияОбъекта.УстановитьБлокировку = Ложь;
			КонецЕсли;
			КоллекцияБлокировок.Добавить(БлокировкаИзмененияОбъекта);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОформитьПроверкуЗаполненияВедомостей(Ведомость, НомерСтроки, ТекстОшибки, ПроверкаВТихомРежиме, УстанавливатьОтказ, ИмяКолонки = "ФизическоеЛицо", Отказ)
	
	Если Не ПроверкаВТихомРежиме Тогда
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			Ведомость,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Состав", НомерСтроки, ИмяКолонки));
	КонецЕсли;
	Если УстанавливатьОтказ Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполненияВедомостей(Ведомость, Отказ, ПроверяемыеРеквизиты, ПроверкаВТихомРежиме = Ложь, УстанавливатьОтказ = Ложь) Экспорт
	
	Если Ведомость.ВводНачальныхДанных Тогда
		Возврат;
	КонецЕсли;
	
	Если Не УчетЗарплаты.ИспользоватьРасчетПервойПоловиныМесяца(Ведомость.Организация, Ведомость.ПериодРегистрации) Тогда
		Возврат;
	КонецЕсли;
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	// Мягкий контроль качества заполнения
	Для Каждого СтрокаСостава Из Ведомость.Состав Цикл
		НДФЛСтроки = Ведомость.НДФЛ.Выгрузить(Новый Структура("ИдентификаторСтроки", СтрокаСостава.ИдентификаторСтроки));
		ИтогНДФЛ = 0;
		Для Каждого НДФЛСтрока Из НДФЛСтроки Цикл
			ИтогНДФЛ = ИтогНДФЛ +  УчетНДФЛКлиентСервер.ИтоговаяСуммаНДФЛпоСтроке(НДФЛСтрока, "Сумма");
		КонецЦикла;
		Если ИтогНДФЛ = 0 Тогда
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'У сотрудника %1 возможно ошибочно не указана сумма НДФЛ'"), 
				СтрокаСостава.ФизическоеЛицо);
			ОформитьПроверкуЗаполненияВедомостей(Ведомость,
				СтрокаСостава.НомерСтроки,
				ТекстОшибки,
				ПроверкаВТихомРежиме,
				УстанавливатьОтказ,
				"НДФЛСумма",
				Отказ);
			Если ПроверкаВТихомРежиме И УстанавливатьОтказ И Отказ Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		ИтогНДФЛ = 0;
		Для Каждого НДФЛСтрока Из НДФЛСтроки Цикл
			Если Не ЗначениеЗаполнено(НДФЛСтрока.ДокументОснование) Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'У сотрудника %1 возможно ошибочно не указан документ-основание в расшифровке сумм НДФЛ'"), 
					СтрокаСостава.ФизическоеЛицо);
				ОформитьПроверкуЗаполненияВедомостей(Ведомость,
					СтрокаСостава.НомерСтроки,
					ТекстОшибки,
					ПроверкаВТихомРежиме,
					УстанавливатьОтказ,
					"НДФЛСумма",
					Отказ);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(НДФЛСтрока.КатегорияДохода) Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'У сотрудника %1 возможно ошибочно не указан вид дохода в расшифровке сумм НДФЛ'"), 
					СтрокаСостава.ФизическоеЛицо);
				ОформитьПроверкуЗаполненияВедомостей(Ведомость,
					СтрокаСостава.НомерСтроки,
					ТекстОшибки,
					ПроверкаВТихомРежиме,
					УстанавливатьОтказ,
					"НДФЛСумма",
					Отказ);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(НДФЛСтрока.СтавкаНалогообложенияРезидента) Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'У сотрудника %1 возможно ошибочно не указана ставка в расшифровке сумм НДФЛ'"), 
					СтрокаСостава.ФизическоеЛицо);
				ОформитьПроверкуЗаполненияВедомостей(Ведомость,
					СтрокаСостава.НомерСтроки,
					ТекстОшибки,
					ПроверкаВТихомРежиме,
					УстанавливатьОтказ,
					"НДФЛСумма",
					Отказ);
			КонецЕсли;
			Если ПроверкаВТихомРежиме И УстанавливатьОтказ И Отказ Тогда
				Возврат;
			КонецЕсли;
			Прервать;
		КонецЦикла;
		ЗарплатаСтроки = Ведомость.Зарплата.Выгрузить(Новый Структура("ИдентификаторСтроки", СтрокаСостава.ИдентификаторСтроки), "ДокументОснование");
		Для Каждого ЗарплатаСтрока Из ЗарплатаСтроки Цикл
			Если Не ЗначениеЗаполнено(ЗарплатаСтрока.ДокументОснование) Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'У сотрудника %1 возможно ошибочно не указан документ-основание в расшифровке сумм к выплате'"), 
					СтрокаСостава.ФизическоеЛицо);
				ОформитьПроверкуЗаполненияВедомостей(Ведомость,
					СтрокаСостава.НомерСтроки,
					ТекстОшибки,
					ПроверкаВТихомРежиме,
					УстанавливатьОтказ,
					,
					Отказ);
				Если ПроверкаВТихомРежиме И УстанавливатьОтказ И Отказ Тогда
					Возврат;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ВедомостьНаВыплатуЗарплатыПередЗаписью(Ведомость, Отказ, РежимЗаписи) Экспорт
	
	ВедомостьСсылка = Ведомость.Ссылка;
	
	Если ТипЗнч(Ведомость) = Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплаты") Тогда
		Если ЗначениеЗаполнено(ВедомостьСсылка) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВедомостьСсылка, "ПометкаУдаления") Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗарплатаВыплачиваетсяБезВедомости() Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОплатаПоВедомости = ВзаиморасчетыССотрудниками.ЕстьОплатаПоВедомости(ВедомостьСсылка)
		Или Не ОбменСБанкамиПоЗарплатнымПроектам.ДоступностьПлатежногоДокумента(ВедомостьСсылка);
		
	Если ЕстьОплатаПоВедомости Тогда
		ТестСообщения = СтрШаблон(НСтр("ru = 'Документ ""%1"" оплачен, сначала требуется отменить проведение платежных документов'"), ВедомостьСсылка);
		ВызватьИсключение ТестСообщения;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Дивиденды

Функция НалогСДивидендовАкционера(ДатаПолученияДохода, Организация, Акционер, Начислено, Регистратор = Неопределено) Экспорт
	
	СуммыНалога = Новый Структура("СуммаНалога, СуммаНалогаСПревышения", 0, 0);
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")
		ИЛИ ДатаПолученияДохода < УчетЗарплаты.ДатаНачалаУчетаПрогрессивногоНДФЛ() 	Тогда
		
		РезультатРасчетаНалога = УчетНДФЛ.НалогСДивидендовАкционера(ДатаПолученияДохода, Организация, Акционер, Начислено, 0, , Регистратор);
	
		СуммыНалога.Вставить("СуммаНалога", РезультатРасчетаНалога.НДФЛ);
		СуммыНалога.Вставить("СуммаНалогаСПревышения", РезультатРасчетаНалога.НДФЛСПревышения);
	КонецЕсли;
	
	
	Возврат СуммыНалога;
	
КонецФункции

Процедура СформироватьДвиженияНачислениеДивидендов(ТаблицаРеквизиты, ТаблицаОтраженияВУчете, Движения, Отказ) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТаблицаОтраженияВУчете) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры        = ПодготовитьПараметрыДвиженийОтраженияДивидендов(ТаблицаОтраженияВУчете, ТаблицаРеквизиты);
	Реквизиты        = Параметры.Реквизиты[0];
	ТаблицаДивиденды = Параметры.ТаблицаОтраженияВУчете;
	ОтразитьДивидендыВУчетеНДФЛ(Реквизиты, ТаблицаДивиденды, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыДвиженийОтраженияДивидендов(ТаблицаДивиденды, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = ""
	+ "Регистратор,"			// <ДокументСсылка.НачислениеДивидендов> - Регистратор
	+ "Период,"					// <Дата>
	+ "Организация,"			// <СправочникСсылка.Организации> - организация документа
	+ "ПланируемаяДатаВыплаты"	// <Дата>
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "Период,"					// <Дата>
	+ "Акционер,"				// <Ссылка на справочник "Физические лица">
	+ "Начислено,"				// <Число(15,2)>
	+ "НДФЛ,"					// <Число(15,2)>
	+ "НДФЛСПревышения"			// <Число(15,2)>
	;
	
	Параметры.Вставить("ТаблицаОтраженияВУчете", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаДивиденды, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура ОтразитьДивидендыВУчетеНДФЛ(Реквизиты, ТаблицаДивиденды, Движения, Отказ)
	
	ТаблицаДивиденды.Колонки.Добавить("ВычетПоНДФЛ", Новый ОписаниеТипов("Число"));
	ТаблицаДивиденды.Колонки.Добавить("НалогНаПрибыльКЗачету", Новый ОписаниеТипов("Число"));
	
	УчетНДФЛ.СформироватьДоходыИНДФЛСДивидендов(Реквизиты.Регистратор,
		Движения,
		Отказ,
		Реквизиты.Организация,
		Реквизиты.ПланируемаяДатаВыплаты,
		ТаблицаДивиденды,
		Ложь,
		,
		,
		,
		Истина,
		Реквизиты.Период);
	
КонецПроцедуры

#Область ВыплатаДивидендов

// Предназначена для генерации движений, связанных с отражением в регл. учете выплаты депонентов
//
// Параметры:
//	ТаблицаПеречислениеЗарплаты - таблица значений с колонками
//      Подразделение
//		ФизическоеЛицо
//      СуммаКВыплате
//      СтатьяДвиженияДенежныхСредств
//      СчетУчета
//      Ведомость
//	ТаблицаРеквизиты  - таблица значений содержит реквизиты документа
//	Движения
//	Отказ
//
Процедура СформироватьДвиженияВыплатаДивидендов(ТаблицаВыплатаДивидендов, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	
	Если УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТаблицаВыплатаДивидендов) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВыплатыДивидендов(ТаблицаВыплатаДивидендов, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	РегистрацияУдержанногоНДФЛ(ТаблицаВыплатаДивидендов, Отказ, Движения, Реквизиты);
	
	СформироватьДвиженияПоУдержанномуНДФЛПриВыплатеДохода(Движения, Параметры, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыВыплатыДивидендов(ТаблицаВыплатаДивидендов, ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата> - период движений - дата документа
	+ "Регистратор,"               // <ДокументСсылка>
	+ "Организация";               // <СправочникСсылка.Организации>
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ВыплатаДепонентов
	
	СписокОбязательныхКолонок = ""
	+ "Контрагент,"                    // <СправочникСсылка.ФизическиеЛица> - физическое лико, которому выплачиваются дивиденды
	+ "СуммаКВыплате,"                 // <Число,15,2> - сумма выплаты в валюте документа	
	+ "НачислениеДивидендов";          // <ДокументСсылка.НачислениеДивидендов>
	
	Параметры.Вставить("ВыплатаДивидендов", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВыплатаДивидендов, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

Процедура РегистрацияУдержанногоНДФЛ(ТаблицаВыплатаДивидендов, Отказ, Движения, Реквизиты)
	
	Если НЕ Отказ Тогда
		
		// определение выплат в разрезе физлиц, документов и источников
		
		ДокументНачисления = ТаблицаВыплатаДивидендов[0].НачислениеДивидендов;
		СуммаКВыплате      = ТаблицаВыплатаДивидендов[0].СуммаКВыплате;
		СуммаВыплаты       = 0;
		СуммаВыплаченная   = 0;
		
		Если ЗначениеЗаполнено(ДокументНачисления) Тогда
			
			РеквизитыНачисления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументНачисления, "Дата, Организация, СуммаДохода, СуммаНалога, СуммаНалогаСПревышения");
			
			СуммаДохода            = РеквизитыНачисления.СуммаДохода;
			СуммаНалога            = РеквизитыНачисления.СуммаНалога;
			СуммаНалогаСПревышения = РеквизитыНачисления.СуммаНалогаСПревышения;
			СуммаВыплаты           = СуммаДохода;
			
			КоэффициентОплаты = СуммаКВыплате/(СуммаДохода - СуммаНалога - СуммаНалогаСПревышения);
			
			Если КоэффициентОплаты <> 1 Тогда
				
				СуммаВыплаты = Окр(СуммаДохода * КоэффициентОплаты, 0);
				
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("ДатаДокументаОснования", НачалоДня(РеквизитыНачисления.Дата));
				Запрос.УстановитьПараметр("Регистратор",            Реквизиты.Регистратор);
				Запрос.УстановитьПараметр("ДокументОснование",      ДокументНачисления);
				Запрос.УстановитьПараметр("Организация",            РеквизитыНачисления.Организация);
				
				Запрос.Текст =
				"ВЫБРАТЬ
				|	ЕСТЬNULL(СУММА(СведенияОДоходахНДФЛ.СуммаДохода), 0) КАК СуммаДохода
				|ИЗ
				|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
				|ГДЕ
				|	СведенияОДоходахНДФЛ.Организация = &Организация
				|	И СведенияОДоходахНДФЛ.Период >= &ДатаДокументаОснования
				|	И СведенияОДоходахНДФЛ.Регистратор <> &Регистратор
				|	И СведенияОДоходахНДФЛ.Регистратор <> &ДокументОснование
				|	И СведенияОДоходахНДФЛ.ДокументОснование = &ДокументОснование
				|	И СведенияОДоходахНДФЛ.Активность
				|	И СведенияОДоходахНДФЛ.СуммаДохода > 0
				|	И НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.ДатаПолученияДохода, ДЕНЬ) = НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.Период, ДЕНЬ)";
				
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					СуммаВыплаченная = Выборка.СуммаДохода;
				КонецЕсли;
				
			КонецЕсли;
		
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка",           ДокументНачисления);
		Запрос.УстановитьПараметр("СуммаВыплаты",     СуммаВыплаты);
		Запрос.УстановитьПараметр("СуммаВыплаченная", СуммаВыплаченная);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Ведомости.Учредитель КАК ФизическоеЛицо,
		|	&СуммаВыплаты КАК СуммаВыплаты,
		|	Ведомости.Ссылка КАК ДокументОснование,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	Ведомости.СуммаДохода КАК СуммаНачисленная,
		|	&СуммаВыплаченная КАК СуммаВыплаченная
		|ПОМЕСТИТЬ ВТСписокСотрудников
		|ИЗ
		|	Документ.НачислениеДивидендов КАК Ведомости
		|ГДЕ
		|	Ведомости.Ссылка = &Ссылка";
		
		Запрос.Выполнить();
		
		// учет фактической даты получения доходов НДФЛ
		УчетНДФЛ.ЗарегистрироватьНовуюДатуПолученияДохода(
			Реквизиты.Регистратор,
			Движения,
			Запрос.МенеджерВременныхТаблиц,
			Реквизиты.Период,
			Реквизиты.Период,
			Отказ,
			Истина);
			
		// регистрация удержания НДФЛ 
		УчетНДФЛ.РассчитатьИЗарегистрироватьУдержанныеНалоги(
			Реквизиты.Регистратор,
			Движения,
			Отказ,
			Реквизиты.Организация,
			Реквизиты.Период,
			Запрос.МенеджерВременныхТаблиц,
			Истина,
			?(Реквизиты.Период >= ДатаИзмененияСроковУплатыНалога(),
				Реквизиты.Период,
				Неопределено),
			Ложь,
			,
			?(Реквизиты.Период >= ДатаИзмененияСроковУплатыНалога(),
				Реквизиты.Период,
				Неопределено));
			
	КонецЕсли;

КонецПроцедуры

Функция ПодготовитьТаблицуВыплатаДивидендов(ТаблицаВыплатаДивидендов) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаВыплатаДивидендов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаВыплатФизическомуЛицу = ТаблицаВыплатаДивидендов.СкопироватьКолонки();
	Для Каждого СтрокаТаблицы ИЗ ТаблицаВыплатаДивидендов Цикл
		Если СтрокаТаблицы.ФизическоеЛицо Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаВыплатФизическомуЛицу.Добавить(), СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаВыплатФизическомуЛицу;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РасчетыПоНалогамВзносам

#Область ПеречислениеНалоговВзносов

Функция ПодготовитьПараметрыПеречисленияНалоговИВзносовСФОТ(ТаблицаРеквизиты, ТаблицыУплатаНалогов, ДополнятьРеквизитамиПервичногоДокумента)
	
	Перем ТаблицаПеречислениеНалогов, ТаблицаПеречислениеНДФЛ;
	
	ТаблицыУплатаНалогов.Свойство("ПеречислениеНалогов", ТаблицаПеречислениеНалогов);
	ТаблицыУплатаНалогов.Свойство("ПеречислениеНДФЛ",    ТаблицаПеречислениеНДФЛ);
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата> - период движений - дата документа
	+ "Регистратор,"               // <ДокументСсылка>
	+ "ВидОперации,"               // <ПеречислениеСсылка.ВидыОперацийСписаниеДенежныхСредств>
	+ "ДатаВходящегоДокумента,"    // <Дата> - дата платежного поручения
	+ "НомерВходящегоДокумента,"   // Строка
	+ "Организация";               // <СправочникСсылка.Организации>
	
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ТаблицаПеречислениеНалогов
	
	СписокОбязательныхКолонок = ""
	+ "НалоговыйПериод,"             // <Дата> - период за который отражена уплата налога
	+ "Период,"                      // <Дата> - период движений - дата документа
	+ "Налог,"                       // <СправочникСсылка.ВидыНалоговИПлатежейВБюджет> - вид уплачиваемого налога
	+ "СчетДт,"                      // <ПланСчетовСсылка.Хозрасчетный> - счет учета налогов и взносов с ФОТ
	+ "СубконтоДт1,"                 // <ПланВидовХарактеристик.ВидыСубконтоХозрасчетные>
	+ "СубконтоДт2,"                 // <ПланВидовХарактеристик.ВидыСубконтоХозрасчетные>
	+ "СубконтоДт3,"                 // <ПланВидовХарактеристик.ВидыСубконтоХозрасчетные>
	+ "ПодразделениеДт,"             // <СправочникСсылка.ПодразделенияОрганизации>
	+ "РегистрацияВНалоговомОргане," // <СправочникСсылка.РегистрацииВНалоговомОргане>
	+ "СуммаРуб";                    // <Число,15,2> - сумма налога, взноса
	Если Не ДополнятьРеквизитамиПервичногоДокумента Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ","
		+ "ПлатежноеПоручениеНомер," // <Строка> - Номер платежного документа
		+ "ПлатежноеПоручениеДата";  // <Дата> - дата платежного документа
	КонецЕсли;
	
	Параметры.Вставить("ТаблицаПеречислениеНалогов", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПеречислениеНалогов, СписокОбязательныхКолонок));
	
	Если ТаблицаПеречислениеНДФЛ <> Неопределено Тогда
		// Подготовка таблицы Параметры.ТаблицаПеречислениеНДФЛ
		
		СписокОбязательныхКолонок = ""
		+ "ФизическоеЛицо,"                       // <СправочникСсылка.ФизическиеЛица>
		+ "Сумма,"                                // <Число,15,2> - сумма налога, взноса
		+ "Ставка,"                               // <ПеречислениеСсылка.НДФЛСтавки>
		+ "ИсчисленоПоДивидендам,"                // Булево
		+ "ВключатьВДекларациюПоНалогуНаПрибыль," // Булево
		+ "МесяцНалоговогоПериода";               // Дата
		
		Параметры.Вставить("ТаблицаПеречислениеНДФЛ", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПеречислениеНДФЛ, СписокОбязательныхКолонок));
	Иначе
		Параметры.Вставить("ТаблицаПеречислениеНДФЛ", Неопределено);
	КонецЕсли;
	
	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияПоНалогамИВзносамСФОТ(ТаблицаРеквизиты, ТаблицыУплатаНалогов, Движения, Отказ) Экспорт

	Если (Не ТаблицыУплатаНалогов.Свойство("ПеречислениеНалогов")
	 Или Не ЗначениеЗаполнено(ТаблицыУплатаНалогов.ПеречислениеНалогов))
	 И (Не ТаблицыУплатаНалогов.Свойство("ТаблицаУплатыПоЕдиномуНалоговомуСчету")
		Или Не ЗначениеЗаполнено(ТаблицыУплатаНалогов.ТаблицаУплатыПоЕдиномуНалоговомуСчету)) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Реквизиты.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога
	   И Реквизиты.ВидОперации <> Перечисления.ВидыОперацийРКО.УплатаНалога
	   И Реквизиты.ВидОперации <> Перечисления.ВидыРегламентныхОпераций.ЗачетАвансаПоЕдиномуНалоговомуСчету Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицыУплатаНалогов.Свойство("ТаблицаУплатыПоЕдиномуНалоговомуСчету")
		И ЗначениеЗаполнено(ТаблицыУплатаНалогов.ТаблицаУплатыПоЕдиномуНалоговомуСчету) Тогда
		
		СчетаУчета = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицыУплатаНалогов.ТаблицаУплатыПоЕдиномуНалоговомуСчету, "СчетУчета", Истина);
		
		Если СчетаУчета.Найти(ПланыСчетов.Хозрасчетный.НДФЛ) <> Неопределено Тогда
			ОчиститьДвиженияПередОтражениемУплатыНДФЛ(Движения);
		КонецЕсли;
		
		Для Каждого СчетУчета Из СчетаУчета Цикл
			ПеречислениеНалогов = ЕдиныйНалоговыйСчет.ТаблицаРасшифровкиПлатежа();
			ПеречислениеНалогов.Колонки.Добавить("ПлатежноеПоручениеНомер");
			ПеречислениеНалогов.Колонки.Добавить("ПлатежноеПоручениеДата");
			Для Каждого СтрокаТаблицы Из ТаблицыУплатаНалогов.ТаблицаУплатыПоЕдиномуНалоговомуСчету Цикл
				Если СтрокаТаблицы.СчетУчета <> СчетУчета Тогда
					Продолжить;
				КонецЕсли;
				Для Каждого СтрокаТаблицыНалогов Из СтрокаТаблицы.ТаблицаУплатыПоНалогам Цикл
					НоваяСтрока = ПеречислениеНалогов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыНалогов);
					НоваяСтрока.ПлатежноеПоручениеНомер = СтрокаТаблицы.НомерВходящегоДокумента;
					НоваяСтрока.ПлатежноеПоручениеДата  = СтрокаТаблицы.ДатаВходящегоДокумента;
				КонецЦикла;
			КонецЦикла;
			ТаблицыУплатаНалогов.Вставить("ПеречислениеНалогов", ПеречислениеНалогов);
			ПеречисленныйНалог = ТаблицыУплатаНалогов.ПеречислениеНалогов.Итог("СуммаРуб");
			Если ПеречисленныйНалог = 0 Тогда
				Продолжить;
			КонецЕсли;
			СформироватьДвиженияПоНалогамИВзносамСФОТПоТаблицам(ТаблицаРеквизиты, ТаблицыУплатаНалогов, Движения, Отказ, Ложь);
		КонецЦикла;
	Иначе
		ПеречисленныйНалог = ТаблицыУплатаНалогов.ПеречислениеНалогов.Итог("СуммаРуб");
		Если ПеречисленныйНалог = 0 Тогда
			Возврат;
		КонецЕсли;
		СформироватьДвиженияПоНалогамИВзносамСФОТПоТаблицам(ТаблицаРеквизиты, ТаблицыУплатаНалогов, Движения, Отказ, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияПоНалогамИВзносамСФОТПоТаблицам(ТаблицаРеквизиты, ТаблицыУплатаНалогов, Движения, Отказ, ДополнятьРеквизитамиПервичногоДокумента)
	
	Параметры = ПодготовитьПараметрыПеречисленияНалоговИВзносовСФОТ(ТаблицаРеквизиты, ТаблицыУплатаНалогов, ДополнятьРеквизитамиПервичногоДокумента);
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаПеречислениеНалогов = Параметры.ТаблицаПеречислениеНалогов.Скопировать();
	
	СчетаУчетаНалоговВзносовСФОТ = Новый Соответствие;
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_нак,                     Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРНакопительная);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_страх,                   Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРСтраховая);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_доп,                     Перечисления.ВидыОбязательногоСтрахованияСотрудников.ДоплатаКПенсииЛетчикам);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_доп_шахтеры,             Перечисления.ВидыОбязательногоСтрахованияСотрудников.ДоплатаКПенсииШахтерам);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_доп_ВредныеУсловияТруда, Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_доп_ТяжелыеУсловияТруда, Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ФСС,                         Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФСС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ,                   Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФССНесчастныеСлучаи);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ФФОМС,                       Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ТФОМС,                       Перечисления.ВидыОбязательногоСтрахованияСотрудников.ТФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_ОПС,                     Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРПоСуммарномуТарифу);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.СтраховыеВзносыЕдиныйТариф,  Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРПоСуммарномуТарифу);
	
	ТаблицаПеречислениеНалогов.Колонки.Добавить("ВидОбязательногоСтрахованияСотрудников");
	ТаблицаПеречислениеНалогов.Колонки.Добавить("ЭтоСтраховыеВзносы");
	ТаблицаПеречислениеНалогов.Колонки.Добавить("ВидДвижения");
	
	Если ДополнятьРеквизитамиПервичногоДокумента Тогда
		ТаблицаПеречислениеНалогов.Колонки.Добавить("ПлатежноеПоручениеНомер");
		ТаблицаПеречислениеНалогов.Колонки.Добавить("ПлатежноеПоручениеДата");
		ТаблицаПеречислениеНалогов.ЗаполнитьЗначения(Реквизиты.НомерВходящегоДокумента, "ПлатежноеПоручениеНомер");
		ТаблицаПеречислениеНалогов.ЗаполнитьЗначения(Реквизиты.ДатаВходящегоДокумента,  "ПлатежноеПоручениеДата");
	КонецЕсли;
	
	ТаблицаПеречислениеНалогов.Колонки.СуммаРуб.Имя = "Сумма";
	ТаблицаПеречислениеНалогов.Колонки.ПодразделениеДт.Имя = "Подразделение";
			
	Для Каждого СтрокаТаблицы ИЗ ТаблицаПеречислениеНалогов Цикл
		
		ВидОбязательногоСтрахованияСотрудников = СчетаУчетаНалоговВзносовСФОТ.Получить(ТаблицаПеречислениеНалогов[0].СчетДт);
		
		СтрокаТаблицы.ВидОбязательногоСтрахованияСотрудников = ВидОбязательногоСтрахованияСотрудников;
		СтрокаТаблицы.ЭтоСтраховыеВзносы = Истина;
		СтрокаТаблицы.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЦикла;
	
	МассивНазначенийПлатежа = Новый Массив;
	МассивНазначенийПлатежа.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог);
	МассивНазначенийПлатежа.Добавить(Перечисления.ВидыПлатежейВГосБюджет.НалогСам);
	МассивНазначенийПлатежа.Добавить(Перечисления.ВидыПлатежейВГосБюджет.НалогАкт);
			
	Если ТаблицаПеречислениеНалогов[0].СчетДт = ПланыСчетов.Хозрасчетный.НДФЛ Тогда
		
		Если МассивНазначенийПлатежа.Найти(ТаблицаПеречислениеНалогов[0].СубконтоДт1) = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		МесяцНалоговогоПериода = ТаблицаПеречислениеНалогов[0].НалоговыйПериод;
		Если НЕ ЗначениеЗаполнено(МесяцНалоговогоПериода) Тогда
			Возврат;
		КонецЕсли;
		
		Если БухгалтерскийУчетПереопределяемый.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций()
			И ЗначениеЗаполнено(ТаблицаПеречислениеНалогов[0].РегистрацияВНалоговомОргане) Тогда
			РегистрацияВНалоговомОргане = ТаблицаПеречислениеНалогов[0].РегистрацияВНалоговомОргане;
		ИначеЕсли ЗначениеЗаполнено(ТаблицаПеречислениеНалогов[0].СубконтоДт2) Тогда
			РегистрацияВНалоговомОргане = ТаблицаПеречислениеНалогов[0].СубконтоДт2;
		Иначе
			РегистрацияВНалоговомОргане = Неопределено;
		КонецЕсли;
		
		ТаблицаПеречислениеНалогов.Колонки.НалоговыйПериод.Имя = "МесяцНалоговогоПериода";
		// НДФЛ
		
		ФизическиеЛица = Новый Массив;
		Если Параметры.ТаблицаПеречислениеНДФЛ <> Неопределено Тогда
			ФизическиеЛица = Параметры.ТаблицаПеречислениеНДФЛ.ВыгрузитьКолонку("ФизическоеЛицо");
		КонецЕсли;
		
		МассивВидовНалога = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПеречислениеНалогов, "Налог", Истина);
		РеквизитыВидовНалога = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивВидовНалога, "КодБК");
		
		Если ФизическиеЛица.Количество() = 0 Тогда
			МассивРегистраций = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПеречислениеНалогов, "РегистрацияВНалоговомОргане", Истина);
			Для Каждого ЗначениеМассива Из МассивРегистраций Цикл
				Для Каждого ЗначениеВидаНалога Из МассивВидовНалога Цикл
					
					ЭтоНДФЛСПревышения = Справочники.ВидыНалоговИПлатежейВБюджет.ЭтоНДФЛНалоговыйАгент15(РеквизитыВидовНалога[ЗначениеВидаНалога])
						Или Справочники.ВидыНалоговИПлатежейВБюджет.ЭтоНДФЛНалоговыйАгентДивиденды15(РеквизитыВидовНалога[ЗначениеВидаНалога]);
					
					Ставка = ?(ЭтоНДФЛСПревышения, СтавкаНДФЛСПревышения(), Неопределено);
					
					ТаблицаПеречислениеНалоговПоРегистрации = ТаблицаПеречислениеНалогов.Скопировать(Новый Структура("РегистрацияВНалоговомОргане, Налог", ЗначениеМассива, ЗначениеВидаНалога));
					УчетНДФЛ.СформироватьПеречисленныйНалог(
						Реквизиты.Регистратор, Движения, Отказ, Реквизиты.Организация, Реквизиты.Период,
						ТаблицаПеречислениеНалоговПоРегистрации, ,
						?(ЗначениеЗаполнено(ЗначениеМассива), ЗначениеМассива, РегистрацияВНалоговомОргане), Год(Реквизиты.Период) > 2014,
						Ставка, Реквизиты.ВидОперации <> Перечисления.ВидыРегламентныхОпераций.ЗачетАвансаПоЕдиномуНалоговомуСчету);
				КонецЦикла;
			КонецЦикла;
		Иначе
			
			Если РегистрацияВНалоговомОргане = Неопределено Тогда
				РегистрацияВНалоговомОргане = ЗарплатаКадры.РегистрацияВНалоговомОргане(Реквизиты.Организация, Реквизиты.Период);
			КонецЕсли;
			
			ВТПеречислениеНДФЛ = СформироватьВременнуюТаблицуПеречислениеНДФЛ(Параметры.ТаблицаПеречислениеНДФЛ, МесяцНалоговогоПериода, РегистрацияВНалоговомОргане,
				Реквизиты.ДатаВходящегоДокумента, Реквизиты.НомерВходящегоДокумента);
			
			УчетНДФЛ.СформироватьПеречисленныйНалогПоВременнойТаблице(Движения, Отказ, Реквизиты.Организация, Реквизиты.Период, ВТПеречислениеНДФЛ);
			
			Для Каждого ЗначениеВидаНалога Из МассивВидовНалога Цикл
				
				ЭтоНДФЛСПревышения = Справочники.ВидыНалоговИПлатежейВБюджет.ЭтоНДФЛНалоговыйАгент15(РеквизитыВидовНалога[ЗначениеВидаНалога])
					Или Справочники.ВидыНалоговИПлатежейВБюджет.ЭтоНДФЛНалоговыйАгентДивиденды15(РеквизитыВидовНалога[ЗначениеВидаНалога]);
				
				Ставка = ?(ЭтоНДФЛСПревышения, СтавкаНДФЛСПревышения(), Неопределено);
				
				ТаблицаПеречислениеНалоговВременная = ТаблицаПеречислениеНалогов.Скопировать(Новый Структура("Налог", ЗначениеВидаНалога));
				
				СтрокаДанных = Новый Структура;
				Для Каждого Колонка ИЗ ТаблицаПеречислениеНалоговВременная.Колонки Цикл
					СтрокаДанных.Вставить(Колонка.Имя);
				КонецЦикла;
				ЗаполнитьЗначенияСвойств(СтрокаДанных, ТаблицаПеречислениеНалоговВременная[0]);
				СтрокаДанных.Сумма = ТаблицаПеречислениеНалоговВременная.Итог("Сумма");
				
				РеквизитыПлатежногоПоручения = УчетНДФЛ.РеквизитыПлатежногоПорученияПоДатеНомеру(Реквизиты.ДатаВходящегоДокумента, Реквизиты.НомерВходящегоДокумента);
				
				СтрокаПриход = СтрокаУплатыНалоговогоАгента(Движения, Реквизиты.Организация, Реквизиты.Период, ВидДвиженияНакопления.Приход, СтрокаДанных, Ставка);
				СтрокаПриход.РеквизитыПлатежногоПоручения = РеквизитыПлатежногоПоручения;
				СтрокаПриход.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
				СтрокаПриход.ДатаПлатежа = Реквизиты.Период;
				
				СтрокаРасход = СтрокаУплатыНалоговогоАгента(Движения, Реквизиты.Организация, Реквизиты.Период, ВидДвиженияНакопления.Расход, СтрокаДанных, Ставка);
				СтрокаРасход.РеквизитыПлатежногоПоручения = РеквизитыПлатежногоПоручения;
				СтрокаРасход.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
				СтрокаРасход.ДатаПлатежа = Реквизиты.Период;
			КонецЦикла;
			
			Движения.УплатаНДФЛНалоговымиАгентамиКРаспределению.Записывать = Истина;
			
		КонецЕсли;
		
	ИначеЕсли СчетаУчетаНалоговВзносовСФОТ.Получить(ТаблицаПеречислениеНалогов[0].СчетДт) <> Неопределено Тогда 
		
		МассивНазначенийПлатежа.Добавить(Перечисления.ВидыПлатежейВГосБюджет.НеПринято);
		Если МассивНазначенийПлатежа.Найти(ТаблицаПеречислениеНалогов[0].СубконтоДт1) = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		//Взносы
		УчетСтраховыхВзносов.СформироватьУплатуВзносовВФонд(Движения, Отказ, Реквизиты.Организация, ТаблицаПеречислениеНалогов);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПеречисленныйНалогРегистратораПоФизическимЛицам(Регистратор) Экспорт
	
	Если Год(Регистратор.Дата) > 2014 Тогда
		Возврат УчетНДФЛ.ПеречисленныйНалогРегистратораПоФизическимЛицам(Регистратор);
	Иначе
		ПустаяТаблица = Новый ТаблицаЗначений;
		ПустаяТаблица.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		ПустаяТаблица.Колонки.Добавить("Сумма",          ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		Возврат ПустаяТаблица;
	КонецЕсли;
	
КонецФункции

Функция СтрокаУплатыНалоговогоАгента(Движения, Организация, ДатаОперации, ВидДвижения, ДанныеДокумента, Ставка)
	
	НоваяСтрока = Движения.УплатаНДФЛНалоговымиАгентамиКРаспределению.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДокумента);
	НоваяСтрока.ВидДвижения = ВидДвижения;
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Ставка      = Ставка;
	НоваяСтрока.Период      = ДатаОперации;
	Если НоваяСтрока.МесяцНалоговогоПериода >= '20150101' Тогда
		НоваяСтрока.МесяцНалоговогоПериода = '00010101'
	КонецЕсли;
	
    Возврат НоваяСтрока
	
КонецФункции

Процедура ОчиститьДвиженияПередОтражениемУплатыНДФЛ(Движения)
	
	ОчищаемыеРегистры = Новый Массив;
	ОчищаемыеРегистры.Добавить("НДФЛПеречисленный");
	ОчищаемыеРегистры.Добавить("РасчетыНалоговыхАгентовСБюджетомПоНДФЛ");
	ОчищаемыеРегистры.Добавить("УплатаНДФЛНалоговымиАгентамиКРаспределению");
	
	Для Каждого ИмяРегистра Из ОчищаемыеРегистры Цикл
		Если Движения.Найти(ИмяРегистра) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Движения[ИмяРегистра].Очистить();
		Движения[ИмяРегистра].Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ВозвратВозмещениеНалоговВзносов

Функция ПодготовитьПараметрыВозвратаНалоговИВзносовСФОТ(ТаблицаВозвратНалогов, ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата> - период движений - дата документа
	+ "Регистратор,"               // <ДокументСсылка>
	+ "Организация";               // <СправочникСсылка.Организации>
	
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ТаблицаПеречислениеНалогов
	
	СписокОбязательныхКолонок = ""
	+ "СчетКт,"                 // <ПланСчетовСсылка.Хозрасчетный> - счет учета налогов и взносов с ФОТ
	+ "СубконтоКт1,"            // <ПланВидовХарактеристик.ВидыСубконтоХозрасчетные>
	+ "СубконтоКт2,"            // <ПланВидовХарактеристик.ВидыСубконтоХозрасчетные>     
	+ "СубконтоКт3,"            // <ПланВидовХарактеристик.ВидыСубконтоХозрасчетные>
	+ "СуммаРуб";               // <Число,15,2> - сумма налога, взноса
	
	Параметры.Вставить("ТаблицаВозвратНалогов", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВозвратНалогов, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияПоВозвратамНалоговИВзносовСФОТ(ТаблицаВозвратНалогов, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаВозвратНалогов) Тогда
		Возврат;
	КонецЕсли;

	Если ТаблицаВозвратНалогов.Итог("СуммаРуб") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВозвратаНалоговИВзносовСФОТ(ТаблицаВозвратНалогов, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	СчетаУчетаРасчетовСФСС = Новый Соответствие;
	СчетаУчетаРасчетовСФСС.Вставить(ПланыСчетов.Хозрасчетный.ФСС,             Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФСС);
	СчетаУчетаРасчетовСФСС.Вставить(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ,       Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФССНесчастныеСлучаи);
	
	ПромежуточнаяТаблица = Параметры.ТаблицаВозвратНалогов.Скопировать();
	
	ТаблицаВозвратНалогов = Новый ТаблицаЗначений;
	ТаблицаВозвратНалогов.Колонки.Добавить("Период");
	ТаблицаВозвратНалогов.Колонки.Добавить("ВидОбязательногоСтрахованияСотрудников");
	ТаблицаВозвратНалогов.Колонки.Добавить("ЭтоСтраховыеВзносы");
	ТаблицаВозвратНалогов.Колонки.Добавить("Сумма");
	
	// возмещение расходов по соц.страхованию
	Для Каждого СтрокаТаблицы ИЗ ПромежуточнаяТаблица Цикл
		ВидОбязательногоСтрахованияСотрудников = СчетаУчетаРасчетовСФСС.Получить(СтрокаТаблицы.СчетКт);
		Если ВидОбязательногоСтрахованияСотрудников <> Неопределено Тогда
			Если Реквизиты.Период < ДатаНачалаАдминистрированияСтраховыхВзносовФНС()
				И (СтрокаТаблицы.СубконтоКт1 = Перечисления.ВидыПлатежейВГосБюджет.ПолученоИзФонда
				ИЛИ СтрокаТаблицы.СубконтоКт1 = Перечисления.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию) Тогда
				НоваяСтрока = ТаблицаВозвратНалогов.Добавить();
				НоваяСтрока.Период = Реквизиты.Период;
				НоваяСтрока.ВидОбязательногоСтрахованияСотрудников = ВидОбязательногоСтрахованияСотрудников;
				НоваяСтрока.ЭтоСтраховыеВзносы = Истина;
				НоваяСтрока.Сумма = СтрокаТаблицы.СуммаРуб;
			ИначеЕсли Реквизиты.Период >= ДатаНачалаАдминистрированияСтраховыхВзносовФНС()
				И СтрокаТаблицы.СубконтоКт1 = Перечисления.ВидыПлатежейВГосБюджет.Налог Тогда
				НоваяСтрока = ТаблицаВозвратНалогов.Добавить();
				НоваяСтрока.Период = Реквизиты.Период;
				НоваяСтрока.ВидОбязательногоСтрахованияСотрудников = ВидОбязательногоСтрахованияСотрудников;
				НоваяСтрока.ЭтоСтраховыеВзносы = Истина;
				НоваяСтрока.Сумма = СтрокаТаблицы.СуммаРуб;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
	Если ТаблицаВозвратНалогов.Количество() <> 0 Тогда
		УчетСтраховыхВзносов.СформироватьВозмещениеРасходовПоСоциальномуСтрахованию(Движения, Отказ, Реквизиты.Организация, ТаблицаВозвратНалогов);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область РегистрацияРасчетовПоНалогамВзносамДокументами_ВНО_Операция_пр

Функция ПодготовитьПараметрыПроведенияОстатки_РасчетыПоНалогамИСборам(
			ТаблицаРасчетыПоНалогамИСборам,
			ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаРасчетыПоНалогамИСборам

	СписокОбязательныхКолонок = ""
	+ "Период,"               // <Дата>
	+ "СчетУчета,"            // <План счетов бухгалтерского учета>
	+ "ВидПлатежаВБюджет,"    // <ПеречислениеСсылка.Виды платежей в бюджет (фонды)>
	+ "Сумма,"                // <Число (15,2)>
	+ "СуммаКт,"              // <Число (15,2)>
	+ "РегистрацияВНалоговомОргане";     // <СправочникСсылка.РегистрацииВНалоговомОргане>

	Параметры.Вставить("ТаблицаРасчетыПоНалогамИСборам",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРасчетыПоНалогамИСборам, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                         // <Дата> - период движений - дата документа
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "ПодразделениеОрганизации,"       // <СправочникСсылка.ПодразделенияОрганизации>
	+ "РазделУчета,"                    // <ПеречислениеСсылка.РазделыУчетаДляВводаОстатков>
	+ "ОтражатьВБухгалтерскомУчете,"    // <Булево>
	+ "ОтражатьВНалоговомУчете,"        // <Булево>
	+ "ОтражатьПоСпециальнымРегистрам"; // <Булево>

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура СформироватьОстатки_РасчетыПоНалогамИСборам(ПараметрыПроведения, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если ПараметрыПроведения.Реквизиты[0].РазделУчета <> Перечисления.РазделыУчетаДляВводаОстатков.РасчетыПоНалогамИСборам Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПроведения.ТаблицаРасчетыПоНалогамИСборам) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не Реквизиты.ОтражатьПоСпециальнымРегистрам Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыПроведенияОстатки_РасчетыПоНалогамИСборам(
		ПараметрыПроведения.ТаблицаРасчетыПоНалогамИСборам,
		ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	ПромежуточнаяТаблица = Параметры.ТаблицаРасчетыПоНалогамИСборам;
	
	// регистрация расчетов с фондами
			
	ТаблицаРасчетовСФондами = Новый ТаблицаЗначений;
	ТаблицаРасчетовСФондами.Колонки.Добавить("Период");
	ТаблицаРасчетовСФондами.Колонки.Добавить("ВидОбязательногоСтрахованияСотрудников");
	ТаблицаРасчетовСФондами.Колонки.Добавить("ЭтоСтраховыеВзносы");
	ТаблицаРасчетовСФондами.Колонки.Добавить("ГодЗадолженностиПоЕСН");
	ТаблицаРасчетовСФондами.Колонки.Добавить("ВидДвижения");
	ТаблицаРасчетовСФондами.Колонки.Добавить("РасчетыПоПособиям");
	ТаблицаРасчетовСФондами.Колонки.Добавить("Сумма");
	
	СчетаУчетаНалоговВзносовСФОТ = Новый Соответствие;
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_нак,                    Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРНакопительная);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_страх,                  Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРСтраховая);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_доп,                    Перечисления.ВидыОбязательногоСтрахованияСотрудников.ДоплатаКПенсииЛетчикам);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_доп_шахтеры,            Перечисления.ВидыОбязательногоСтрахованияСотрудников.ДоплатаКПенсииШахтерам);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ФСС,                        Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФСС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ,                  Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФССНесчастныеСлучаи);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ФФОМС,                      Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ТФОМС,                      Перечисления.ВидыОбязательногоСтрахованияСотрудников.ТФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.ПФР_ОПС,                    Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРПоСуммарномуТарифу);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(ПланыСчетов.Хозрасчетный.СтраховыеВзносыЕдиныйТариф, Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРПоСуммарномуТарифу);
	
	СчетаУчетаРасчетовСФСС = Новый Соответствие;
	СчетаУчетаРасчетовСФСС.Вставить(ПланыСчетов.Хозрасчетный.ФСС,       Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФСС);
	СчетаУчетаРасчетовСФСС.Вставить(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ, Перечисления.ВидыОбязательногоСтрахованияСотрудников.ФССНесчастныеСлучаи);
	
	ВидыРасчетовПоСредствамФСС = Новый Соответствие;
	ВидыРасчетовПоСредствамФСС.Вставить(Перечисления.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию, Перечисления.ВидыРасчетовПоСредствамФСС.РасходыПоСтрахованию);
	ВидыРасчетовПоСредствамФСС.Вставить(Перечисления.ВидыПлатежейВГосБюджет.ПолученоИзФонда,      Перечисления.ВидыРасчетовПоСредствамФСС.ПолученоИзФонда);
	ВидыРасчетовПоСредствамФСС.Вставить(Перечисления.ВидыПлатежейВГосБюджет.НеПринято,            Перечисления.ВидыРасчетовПоСредствамФСС.НеПринято);
	
	Для Каждого СтрокаТаблицы ИЗ ПромежуточнаяТаблица Цикл			
		ВидОбязательногоСтрахованияСотрудников = СчетаУчетаНалоговВзносовСФОТ.Получить(СтрокаТаблицы.СчетУчета);
		Если ВидОбязательногоСтрахованияСотрудников <> Неопределено Тогда
			Если (СтрокаТаблицы.ВидПлатежаВБюджет = Перечисления.ВидыПлатежейВГосБюджет.Налог)
				ИЛИ (СтрокаТаблицы.ВидПлатежаВБюджет = Перечисления.ВидыПлатежейВГосБюджет.НалогСам)
				ИЛИ (СтрокаТаблицы.ВидПлатежаВБюджет = Перечисления.ВидыПлатежейВГосБюджет.ПолученоИзФонда)
				ИЛИ (СтрокаТаблицы.ВидПлатежаВБюджет = Перечисления.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию) Тогда
				
				НоваяСтрока = ТаблицаРасчетовСФондами.Добавить();
				НоваяСтрока.Период = СтрокаТаблицы.Период;
				НоваяСтрока.ВидОбязательногоСтрахованияСотрудников = ВидОбязательногоСтрахованияСотрудников;
				НоваяСтрока.ЭтоСтраховыеВзносы = ?(СтрокаТаблицы.Период >= Дата('20100101'),Истина, Ложь);				
				Если НЕ НоваяСтрока.ЭтоСтраховыеВзносы Тогда
					НоваяСтрока.ГодЗадолженностиПоЕСН = Год(СтрокаТаблицы.Период);
				КонецЕсли;					
				Если СтрокаТаблицы.СуммаКт <> 0 Тогда
					НоваяСтрока.Сумма = СтрокаТаблицы.СуммаКт;
					НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Приход;
				Иначе
					НоваяСтрока.Сумма = СтрокаТаблицы.Сумма;			
					НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
				КонецЕсли;
				
				Если СчетаУчетаНалоговВзносовСФОТ.Получить(СтрокаТаблицы.СчетУчета) <> Неопределено Тогда
					РасчетыПоПособиям = ВидыРасчетовПоСредствамФСС.Получить(СтрокаТаблицы.ВидПлатежаВБюджет);
					Если РасчетыПоПособиям <> Неопределено Тогда
						НоваяСтрока.РасчетыПоПособиям = РасчетыПоПособиям;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаРасчетовСФондами.Количество() <> 0 Тогда
		УчетСтраховыхВзносов.СформироватьРасчетыСФондом(Движения, Отказ, Реквизиты.Организация, ТаблицаРасчетовСФондами);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РасчетыВзносовАУСН

Процедура СформироватьДвиженияНачислениеВзносовОтНесчастныхСлучаев(ПараметрыПроведения,	Движения) Экспорт
	
	Параметры = ПодготовитьПараметрыНачислениеВзносовОтНесчастныхСлучаев(ПараметрыПроведения.ТаблицаВзносыОтНесчастныхСлучаев);
	
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаВзносов = ТаблицаВзносовОтНесчастныхСлучаев(Реквизиты);
	
	// Бухгалтерский учет
	Для Каждого СтрокаТаблицы Из ТаблицаВзносов Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Счет)
			Или Не ЗначениеЗаполнено(СтрокаТаблицы.КорСчет) Тогда
			// не заполнены счет и аналитика
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Сумма       = СтрокаТаблицы.Сумма;
		Проводка.Содержание  = НСтр("ru = 'Расчеты по обязательному социальному страхованию от НС и ПЗ'");
		
		Проводка.СчетКт = СтрокаТаблицы.Счет;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.Субконто3);
		
		Проводка.СчетДт = СтрокаТаблицы.КорСчет;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.КорСубконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.КорСубконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.КорСубконто3);
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
	// Отражение в УСН
	Параметры.Вставить("ТаблицаУСННачислениеЗарплаты", ТаблицаВзносов);
	Параметры.Вставить("ПериодРегистрации",            Реквизиты.Период);
	
	ДокументОбъект = Новый Структура();
	ДокументОбъект.Вставить("Организация", Реквизиты.Организация);
	ДокументОбъект.Вставить("Дата",        Реквизиты.Период);
	ДокументОбъект.Вставить("Имя",         "НачислениеЗарплаты");
	ДокументОбъект.Вставить("Ссылка",      Реквизиты.Регистратор);
	ДокументОбъект.Вставить("Движения",    Движения);
	
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ДокументОбъект, Параметры, Ложь);
			
КонецПроцедуры

Функция ТаблицаВзносовОтНесчастныхСлучаев(Реквизиты)

	Размер = УчетАУСНВызовСервера.СтраховыеВзносыОССНС(Реквизиты.Период);
	
	МесяцРасчета = Месяц(Реквизиты.Период);
	
	МесячныйРазмерВзноса = 0;
	ВзносыЗаПредыдущиеМесяцы = 0;
	
	Для Месяц = 1 По МесяцРасчета Цикл
		РазмерВзносаСНачалаГода = Окр(Размер * Месяц / 12, 2);
		МесячныйРазмерВзноса = РазмерВзносаСНачалаГода - ВзносыЗаПредыдущиеМесяцы;
		ВзносыЗаПредыдущиеМесяцы = ВзносыЗаПредыдущиеМесяцы + МесячныйРазмерВзноса;
	КонецЦикла;

	// Способ отражения фиксированный
	
	ОписаниеТиповПланСчетов =  Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный");
	ОписаниеТиповСубконто   = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;
	ОписаниеТиповЧисло15_2  = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
		
	ТаблицаВзносов = Новый ТаблицаЗначений;
	ТаблицаВзносов.Колонки.Добавить("Счет",              ОписаниеТиповПланСчетов);
	ТаблицаВзносов.Колонки.Добавить("Субконто1",         ОписаниеТиповСубконто);
	ТаблицаВзносов.Колонки.Добавить("Субконто2",         ОписаниеТиповСубконто);
	ТаблицаВзносов.Колонки.Добавить("Субконто3",         ОписаниеТиповСубконто);
	ТаблицаВзносов.Колонки.Добавить("КорСчет",           ОписаниеТиповПланСчетов);
	ТаблицаВзносов.Колонки.Добавить("КорСубконто1",      ОписаниеТиповСубконто);
	ТаблицаВзносов.Колонки.Добавить("КорСубконто2",      ОписаниеТиповСубконто);
	ТаблицаВзносов.Колонки.Добавить("КорСубконто3",      ОписаниеТиповСубконто);
	ТаблицаВзносов.Колонки.Добавить("ОтражениеВУСН",     Новый ОписаниеТипов("ПеречислениеСсылка.ОтражениеВУСН"));
	ТаблицаВзносов.Колонки.Добавить("Сумма",             ОписаниеТиповЧисло15_2);
	ТаблицаВзносов.Колонки.Добавить("РасчетныйДокумент", Метаданные.РегистрыНакопления.РасходыПриУСН.Измерения.РасчетныйДокумент.Тип);
	
	НоваяСтрока = ТаблицаВзносов.Добавить();
	НоваяСтрока.Счет              = ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ;
	НоваяСтрока.Субконто1         = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	НоваяСтрока.КорСчет           = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	НоваяСтрока.КорСубконто1      = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("РасходыПоУплатеСтраховыхВзносов");
	НоваяСтрока.ОтражениеВУСН     = Перечисления.ОтражениеВУСН.Принимаются;
	НоваяСтрока.Сумма             = МесячныйРазмерВзноса;
		
	Возврат ТаблицаВзносов;

КонецФункции

Функция СтраховыеВзносы_ФСС_НСиПЗ(Организация, Период) Экспорт
	
	СуммаВзноса = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(КонецМесяца(Период)));
	Запрос.УстановитьПараметр("КонецПериода",  КонецДня(КонецМесяца(Период)));
	Запрос.УстановитьПараметр("СчетУчета",     ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Счет = &СчетУчета, , Организация = &Организация, , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РегламентнаяОперация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СуммаВзноса = Выборка.Сумма;
	КонецЕсли;
	
	Возврат СуммаВзноса;
	
КонецФункции

Функция ПодготовитьПараметрыНачислениеВзносовОтНесчастныхСлучаев(ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = ""
	+ "Период,"			// <Дата> - счет по дебету проводки (счет учета денежных средств)
	+ "Организация,"	// <СправочникСсылка.Организации> - организация документа
	+ "Регистратор"			// <ДокументСсылка.ОтражениеЗарплатыВУчете>
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область ОбщиеМетодыПересчетаНалоговИВзносовВДокументах

Процедура ПересчитатьНДФЛ(Объект, ОбъектФормы, ОкончательныйРасчет = Ложь) Экспорт
	
	ИмяДокумента = ОбъектФормы.Метаданные().Имя;
	
	Объект.НДФЛ.Очистить();
	Объект.ПримененныеВычетыНаДетейИИмущественные.Очистить();
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Установка транзакции для расчета
	РасчетВыполняетсяОтдельнойТранзакцией = Не ТранзакцияАктивна();
	Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	ДокументОбъект = ОбъектФормы.Скопировать();
	ДокументОбъект.ОбменДанными.Загрузка = Истина;
	ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	ДокументОбъект.СформироватьДоходыНДФЛ();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетныйДокумент.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Документ.#ИмяДокумента# КАК РасчетныйДокумент
		|ГДЕ
		|	РасчетныйДокумент.Ссылка = &Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяДокумента#", ИмяДокумента);
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.Выполнить();
	
	РезультатРасчетаНДФЛ = УчетНДФЛ.РезультатРасчетаНДФЛ(
		Запрос.МенеджерВременныхТаблиц, Объект.Ссылка, Объект.Организация, Объект.ПериодРегистрации, ОкончательныйРасчет, Объект.ПланируемаяДатаВыплаты, ДокументОбъект.Движения);
		
	Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	// Отмена расчетной транзакции
	
	Для Каждого ДанныеНДФЛ Из РезультатРасчетаНДФЛ.НДФЛ Цикл
		ЗаполнитьЗначенияСвойств(Объект.НДФЛ.Добавить(), ДанныеНДФЛ);
	КонецЦикла;
	
	Для Каждого ДанныеВычетов Из РезультатРасчетаНДФЛ.ПримененныеВычетыНаДетейИИмущественные Цикл
		ЗаполнитьЗначенияСвойств(Объект.ПримененныеВычетыНаДетейИИмущественные.Добавить(), ДанныеВычетов);
	КонецЦикла;
	
	РасчетЗарплатыБазовый.ЗаполнитьКорректировкиВыплаты(Объект, "ПериодРегистрации");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ВспомогательныеДанныеДляОтраженияЗарплатыВУчете

// Процедура дополняет таблицу выплат, полученную из подсистемы учета зарплаты
// колконками, в которых будет храниться необходимая для формирования проводок 
// информация
//
// Параметры:
//	ВыплатаЗарплаты - таблица значений
//
Процедура ПодготовитьТаблицуВыплатыЗарплаты(ВыплатаЗарплаты) Экспорт
	
	ВыплатаЗарплаты.Колонки.Добавить("Подразделение", Метаданные.ОпределяемыеТипы.Подразделение.Тип);
	ВыплатаЗарплаты.Колонки.Добавить("СтатьяДвиженияДенежныхСредств", Новый ОписаниеТипов("СправочникСсылка.СтатьиДвиженияДенежныхСредств"));
	ВыплатаЗарплаты.Колонки.Добавить("БанковскийСчет", Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчета"));
	ВыплатаЗарплаты.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ВыплатаЗарплаты.Колонки.Добавить("СчетУчетаКомпенсации", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ВыплатаЗарплаты.Колонки.Добавить("СубконтоУчетаКомпенсации1", Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип);
	ВыплатаЗарплаты.Колонки.Добавить("СубконтоУчетаКомпенсации2", Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип);
	ВыплатаЗарплаты.Колонки.Добавить("СубконтоУчетаКомпенсации3", Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип);
	ВыплатаЗарплаты.Колонки.Добавить("ПодразделениеУчетаКомпенсации", Метаданные.ОпределяемыеТипы.Подразделение.Тип);
	
КонецПроцедуры

// Функция формирует таблицу выплаты зарплаты для передачи в подсистему УСН
//
Функция ПолучитьТаблицуВыплатыЗарплатыДляУСН(ВыплатаЗарплаты) Экспорт
	
	ВедетсяУчетРасчетовПоЗарплатеСводно   = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно");
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	

	ТаблицаВыплатаЗарплаты = Новый ТаблицаЗначений;
	ТаблицаВыплатаЗарплаты.Колонки.Добавить("Работник");	
	ТаблицаВыплатаЗарплаты.Колонки.Добавить("Сделка");
	ТаблицаВыплатаЗарплаты.Колонки.Добавить("КурсВзаиморасчетов");
	ТаблицаВыплатаЗарплаты.Колонки.Добавить("КратностьВзаиморасчетов");
	ТаблицаВыплатаЗарплаты.Колонки.Добавить("Сумма");
	
	Если ВедетсяУчетРасчетовПоЗарплатеСводно И УчетЗарплатыИКадровВоВнешнейПрограмме ИЛИ ВыплатаЗарплаты = Неопределено Тогда
		Возврат ТаблицаВыплатаЗарплаты;
	КонецЕсли;
		
	Для каждого СтрокаТаблицы Из ВыплатаЗарплаты Цикл
		
		НоваяСтрока = ТаблицаВыплатаЗарплаты.Добавить();
		НоваяСтрока.Работник                = СтрокаТаблицы.ФизическоеЛицо;
		НоваяСтрока.Сделка                  = Неопределено;
		НоваяСтрока.КурсВзаиморасчетов      = 1;
		НоваяСтрока.КратностьВзаиморасчетов = 1;
		НоваяСтрока.Сумма                   = СтрокаТаблицы.СуммаКВыплате;
		
	КонецЦикла;
	
	ТаблицаВыплатаЗарплаты.Свернуть("Работник, Сделка, КурсВзаиморасчетов, КратностьВзаиморасчетов", "Сумма");
	
	Возврат ТаблицаВыплатаЗарплаты;
	
КонецФункции

// Функция формирует соответствие видов операций и счетов учета взносов
//
Функция СоответствиеСчетовУчетаНалоговВзносовСФОТВидамОпераций() Экспорт
	
	СчетаУчетаНалоговВзносовСФОТ = Новый Соответствие;
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРНакопительнаяЧасть,
		ПланыСчетов.Хозрасчетный.ПФР_нак);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРСтраховаяЧасть,
		ПланыСчетов.Хозрасчетный.ПФР_страх);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРПоСуммарномуТарифу,
		ПланыСчетов.Хозрасчетный.ПФР_ОПС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРДоПредельнойВеличины,
		ПланыСчетов.Хозрасчетный.ПФР_ОПС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРСПревышения,
		ПланыСчетов.Хозрасчетный.ПФР_ОПС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифЛЭ,
		ПланыСчетов.Хозрасчетный.ПФР_доп);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРДополнительныйТарифШахтеры,
		ПланыСчетов.Хозрасчетный.ПФР_доп_шахтеры);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		ПланыСчетов.Хозрасчетный.ПФР_доп_ВредныеУсловияТруда);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		ПланыСчетов.Хозрасчетный.ПФР_доп_ТяжелыеУсловияТруда);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки,
		ПланыСчетов.Хозрасчетный.ПФР_доп_ВредныеУсловияТруда);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахСпецОценка,
		ПланыСчетов.Хозрасчетный.ПФР_доп_ВредныеУсловияТруда);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки,
		ПланыСчетов.Хозрасчетный.ПФР_доп_ТяжелыеУсловияТруда);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахСпецОценка,
		ПланыСчетов.Хозрасчетный.ПФР_доп_ТяжелыеУсловияТруда);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ФСС,
		ПланыСчетов.Хозрасчетный.ФСС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ФССНС,
		ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ФФОМС,
		ПланыСчетов.Хозрасчетный.ФФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ТФОМС,
		ПланыСчетов.Хозрасчетный.ТФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ВзносыПоЕдиномуТарифу,
		ПланыСчетов.Хозрасчетный.СтраховыеВзносыЕдиныйТариф);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорам,
		ПланыСчетов.Хозрасчетный.ПФР_ОПС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОПСпоМеждународнымДоговорамСПревышения,
		ПланыСчетов.Хозрасчетный.ПФР_ОПС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорам,
		ПланыСчетов.Хозрасчетный.ФФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОМСпоМеждународнымДоговорамСПревышения,
		ПланыСчетов.Хозрасчетный.ФФОМС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорам,
		ПланыСчетов.Хозрасчетный.ФСС);
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ОССпоМеждународнымДоговорамСПревышения,
		ПланыСчетов.Хозрасчетный.ФСС);
		
	Возврат СчетаУчетаНалоговВзносовСФОТ;
	
КонецФункции

Функция ОперацииУчетаОсобыхДопВзносов() Экспорт
	
	ОперацииУчетаОсобыхДопВзносов = Новый Массив;
	ОперацииУчетаОсобыхДопВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахБезСпецОценки);
	ОперацииУчетаОсобыхДопВзносов.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахБезСпецОценки);
	
	Возврат ОперацииУчетаОсобыхДопВзносов;
	
КонецФункции

Функция СоответствиеСчетовУчетаДобровольныхВзносовВидамОпераций() Экспорт
	
	СчетаУчетаНалоговВзносовСФОТ = Новый Соответствие;
	СчетаУчетаНалоговВзносовСФОТ.Вставить(Перечисления.ВидыОперацийПоЗарплате.ДСВ,
		ПланыСчетов.Хозрасчетный.ПФР_ДОБР_сотр);
	
	Возврат СчетаУчетаНалоговВзносовСФОТ;
	
КонецФункции

#КонецОбласти

#Область ДанныеДокументовПодсистемыЗарплата

Функция ПолучитьДанныеВедомостейДляОплатыТаблично(Регистратор = Неопределено, Ведомости, ФизическиеЛица = Неопределено, Оплаченные = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаВедомостей = ВзаиморасчетыССотрудниками.ДанныеВедомостейДляОплатыДокументом(Регистратор, Ведомости, ФизическиеЛица, Оплаченные);
	
	Если Ведомости.Количество() > 0 Тогда
		УчитыватьКомпенсациюЗаЗадержкуЗарплаты = Ложь;
		
		Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
			Если ЗначениеЗаполнено(Регистратор) Тогда
				Организация = Регистратор.Организация;
			Иначе
				Организация = Ведомости[0].Организация;
			КонецЕсли;
			УчитыватьКомпенсациюЗаЗадержкуЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьФорматОбменаЗУП25", Новый Структура("Организация", Организация));
		КонецЕсли;
		
		Если НЕ УчитыватьКомпенсациюЗаЗадержкуЗарплаты Тогда
			Для Каждого СтрокаТаблицы ИЗ ТаблицаВедомостей Цикл
				СтрокаТаблицы.СуммаКВыплате = СтрокаТаблицы.СуммаКВыплате + СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты;
				СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты = 0;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаВедомостей.Свернуть("Ведомость, ФизическоеЛицо", "СуммаКВыплате, КомпенсацияЗаЗадержкуЗарплаты, ВзысканнаяСумма");
	
	Возврат ТаблицаВедомостей;
	
КонецФункции

Функция ВзысканоПоВедомости(ПлатежнаяВедомость, Получатель) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Получатель) 
		ИЛИ НЕ ЗначениеЗаполнено(ПлатежнаяВедомость) Тогда
		Возврат 0;
	КонецЕсли;
	
	Ведомости = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПлатежнаяВедомость);
	
	ТаблицаНеоплаченныхВедомостей = УчетЗарплаты.ПолучитьДанныеВедомостейДляОплатыТаблично( , Ведомости, Получатель, Ложь);
	ТаблицаОплаченныхВедомостей   = УчетЗарплаты.ПолучитьДанныеВедомостейДляОплатыТаблично( , Ведомости, Получатель, Истина);
	
	Возврат ТаблицаНеоплаченныхВедомостей.Итог("ВзысканнаяСумма") + ТаблицаОплаченныхВедомостей.Итог("ВзысканнаяСумма");
	
КонецФункции

Функция НевыплаченныеДепоненты(Регистратор, Организация, ДатаВыплаты, ФизическиеЛица = Неопределено, ДепонированоДо = Неопределено) Экспорт

	Возврат УчетДепонированнойЗарплаты.ОстатокДепонированнойЗарплаты(Регистратор, Организация, ДатаВыплаты, ФизическиеЛица, ДепонированоДо);		                
	
КонецФункции

Функция ВыплачиватьКомпенсациюЗаЗадержкуЗарплаты(Организация, ДокументВыплаты, Ведомости, ФизическоеЛицо = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		// В 1С:Бухгалтерии компенсация не может быть рассчитана
		Возврат Ложь;
	КонецЕсли;
	
	ИспользуетсяЗУП25 = ПолучитьФункциональнуюОпцию("ИспользоватьФорматОбменаЗУП25", Новый Структура("Организация", Организация));
	Если Не ИспользуетсяЗУП25 Тогда
		// Расчет компенсации выполняется в ЗУП 2.5
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеВедомостей = ПолучитьДанныеВедомостейДляОплатыТаблично(ДокументВыплаты, Ведомости, ФизическоеЛицо, Неопределено);
		
	Возврат ДанныеВедомостей.Итог("КомпенсацияЗаЗадержкуЗарплаты") <> 0;
	
КонецФункции

// Проверяет оплаченность расчетного документа.
//
// Параметры:
//  РасчетныйДокумент - ДокументСсылка (Отпуск, БольничныйЛист, НачислениеЗарплаты).
//
Функция МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РасчетныйДокумент", РасчетныйДокумент);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток КАК СуммаКВыплатеОстаток
	|ИЗ
	|	РегистрНакопления.ЗарплатаКВыплате.Остатки(, ДокументОснование = &РасчетныйДокумент) КАК ЗарплатаКВыплатеОстатки
	|ГДЕ
	|	ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗарплатаКВыплатеАвансомОстатки.СуммаКВыплатеОстаток
	|ИЗ
	|	РегистрНакопления.ЗарплатаКВыплатеАвансом.Остатки(, ДокументОснование = &РасчетныйДокумент) КАК ЗарплатаКВыплатеАвансомОстатки
	|ГДЕ
	|	ЗарплатаКВыплатеАвансомОстатки.СуммаКВыплатеОстаток > 0";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой()Тогда
		ТекстСообщения = НСтр("ru = 'По документу нет сумм к выплате'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции

// Проверяет наличие проведенных документов начисления зарплаты
//
// Параметры:
//  ГоловнаяОрганизация - СправочникСсылка.Организации - проверяются документы переданной организации и всех ее филиалов
//  НачалоПериода       - Дата - начало проверяемого интервала
//  КонецПериода        - Дата - конец проверяемого интервала
// 
// Возвращаемое значение:
//  Булево - Истина, если наличие этих документов предполагается настройками и они есть (проведены)
//
Функция ЕстьНачислениеЗарплаты(ГоловнаяОрганизация, НачалоПериода, КонецПериода) Экспорт
	
	Если Не УчетЗарплаты.ИспользуетсяПодсистемаУчетаЗарплатыИКадров()
		Или Не Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Получить() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВсяОрганизация", БухгалтерскийУчетПереопределяемый.ВсяОрганизация(ГоловнаяОрганизация));
	Запрос.УстановитьПараметр("НачалоПериода",  НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",   КонецПериода);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НачислениеЗарплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
	|ГДЕ
	|	НачислениеЗарплаты.Проведен
	|	И НачислениеЗарплаты.МесяцНачисления МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НачислениеЗарплаты.Организация В(&ВсяОрганизация)";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Проверяет наличие сведений о начисенных доходах для расчета НДФЛ за выбранный период.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - ссылка на организацию.
//  НачалоПериода - Дата - начало проверяемого периода.
//  КонецПериода - Дата - конец проверяемого периода.
//
Функция ЕстьСведенияОДоходахНДФЛ(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если Не УчетЗарплаты.ИспользуетсяПодсистемаУчетаЗарплатыИКадров()
		Или Не Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Получить() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СведенияОДоходахНДФЛОбороты.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК СведенияОДоходахНДФЛОбороты";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает таблицу удержанного НДФЛ по физическим лицам из ведомостей на выплату зарплаты.
//
// Параметры:
//  Ведомости - Массив из ДокументСсылка.ВедомостьНаВыплатуЗарплаты - Список ведомостей на выплату зарплаты.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//    * ФизЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо, с которого удержали НДФЛ.
//    * Сумма - Число - сумма удержанного НДФЛ.
//
Функция УдержанныйНДФЛПоДаннымВедомостейНаВыплатуЗарплаты(Ведомости) Экспорт
	
	Если Не ЗначениеЗаполнено(Ведомости) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ведомости", Ведомости);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыНДФЛ.ФизическоеЛицо КАК ФизЛицо,
	|	СУММА(ВедомостьНаВыплатуЗарплатыНДФЛ.Сумма + ВедомостьНаВыплатуЗарплатыНДФЛ.СуммаСПревышения + ВедомостьНаВыплатуЗарплатыНДФЛ.СуммаСПревышенияПоСтавке18 + ВедомостьНаВыплатуЗарплатыНДФЛ.СуммаСПревышенияПоСтавке20 + ВедомостьНаВыплатуЗарплатыНДФЛ.СуммаСПревышенияПоСтавке22) КАК Сумма
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплаты.НДФЛ КАК ВедомостьНаВыплатуЗарплатыНДФЛ
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыНДФЛ.Ссылка В(&Ведомости)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВедомостьНаВыплатуЗарплатыНДФЛ.ФизическоеЛицо";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ОплаченнаяСуммаПоВедомости(Организация, Ведомость) Экспорт
	
	ОплаченнаяСумма = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ведомость", Ведомость);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОплатаВедомостейНаВыплатуЗарплаты.Ведомость КАК Ведомость,
	|	ОплатаВедомостейНаВыплатуЗарплаты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ПлатежныеДокументы
	|ИЗ
	|	РегистрСведений.ОплатаВедомостейНаВыплатуЗарплаты КАК ОплатаВедомостейНаВыплатуЗарплаты
	|ГДЕ
	|	ОплатаВедомостейНаВыплатуЗарплаты.Организация = &Организация
	|	И ОплатаВедомостейНаВыплатуЗарплаты.Ведомость = &Ведомость
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплатаВедомостейНаВыплатуЗарплаты.Ведомость,
	|	ОплатаВедомостейНаВыплатуЗарплаты.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент,
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|ГДЕ
	|	ПодтверждениеЗачисленияЗарплаты.Проведен
	|	И ПодтверждениеЗачисленияЗарплаты.Организация = &Организация
	|	И ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент = &Ведомость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(РасходныйКассовыйОрдер.СуммаДокумента, 0) КАК СуммаВыплаты,
	|	0 КАК СуммаПодтверждения
	|ПОМЕСТИТЬ ВТ_СуммыВыплат
	|ИЗ
	|	ВТ_ПлатежныеДокументы КАК ВТ_ПлатежныеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ПО ВТ_ПлатежныеДокументы.Регистратор = РасходныйКассовыйОрдер.Ссылка
	|ГДЕ
	|	РасходныйКассовыйОрдер.Организация = &Организация
	|	И РасходныйКассовыйОрдер.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СписаниеСРасчетногоСчета.СуммаДокумента, 0),
	|	0
	|ИЗ
	|	ВТ_ПлатежныеДокументы КАК ВТ_ПлатежныеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|		ПО ВТ_ПлатежныеДокументы.Регистратор = СписаниеСРасчетногоСчета.Ссылка
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Организация = &Организация
	|	И СписаниеСРасчетногоСчета.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ВзаиморасчетыСДепонентами.Сумма, 0)),
	|	0
	|ИЗ
	|	ВТ_ПлатежныеДокументы КАК ВТ_ПлатежныеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыСДепонентами КАК ВзаиморасчетыСДепонентами
	|		ПО ВТ_ПлатежныеДокументы.Регистратор = ВзаиморасчетыСДепонентами.Регистратор
	|ГДЕ
	|	ВзаиморасчетыСДепонентами.Организация = &Организация
	|	И ВзаиморасчетыСДепонентами.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	СУММА(ЕСТЬNULL(ПодтверждениеЗачисленияЗарплатыСотрудники.Сумма, 0))
	|ИЗ
	|	ВТ_ПлатежныеДокументы КАК ВТ_ПлатежныеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодтверждениеЗачисленияЗарплаты.Сотрудники КАК ПодтверждениеЗачисленияЗарплатыСотрудники
	|		ПО ВТ_ПлатежныеДокументы.Регистратор = ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка
	|ГДЕ
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.РезультатЗачисленияЗарплаты = ЗНАЧЕНИЕ(Перечисление.РезультатыЗачисленияЗарплаты.Зачислено)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_СуммыВыплат.СуммаВыплаты) КАК СуммаВыплаты,
	|	СУММА(ВТ_СуммыВыплат.СуммаПодтверждения) КАК СуммаПодтверждения
	|ИЗ
	|	ВТ_СуммыВыплат КАК ВТ_СуммыВыплат";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОплаченнаяСумма = Макс(Выборка.СуммаВыплаты, Выборка.СуммаПодтверждения);
	КонецЕсли;
	
	Возврат ОплаченнаяСумма;
	
КонецФункции

Процедура УстановитьРасширеннуюПодсказкуРасчетаСреднего(Форма) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");	
	
	КалендарныхДней = 0;
	СуммаЗаработка = 0;
	МассивРасшифровкиМесяцев = Новый Массив;
	МаксимальнаяДлинаРасшифровкиМесяца = 0;	
	МаксимальнаяДлинаРасшифровкиДней = 0;	
	
	Для НомерМесяца = 1 По Форма.МесяцыРасчета.Количество() Цикл
		
		СуммаЗаработка = СуммаЗаработка + Форма.ТаблицаЗаработка[НомерМесяца + Форма.ДобавляемыеМесяцы-1].ОсновныеНачисления;
		
		КалендарныхДнейМесяца = Форма.ТаблицаЗаработка[НомерМесяца + Форма.ДобавляемыеМесяцы -1].КалендарныхДней;
			
		Если КалендарныхДнейМесяца <> 0 Тогда
			
			Месяц = Форма.МесяцыРасчета[НомерМесяца - 1];
			УчтеноДней =
				МодульРасчетЗарплатыДляНебольшихОрганизаций.КоличествоУчтенныхДнейОтпуска(Форма.ДатаНачалаСобытия, Месяц, КалендарныхДнейМесяца);
			КалендарныхДней = КалендарныхДней + УчтеноДней;
			
			РасшифровкаМесяца = Новый Структура("Месяц,Дни", Формат(Месяц, "ДФ='ММММ гггг'"), Формат(УчтеноДней, "ЧДЦ=2"));
			МассивРасшифровкиМесяцев.Добавить(РасшифровкаМесяца);
			
			ДлинаМесяца = СтрДлина(РасшифровкаМесяца.Месяц);
			Если МаксимальнаяДлинаРасшифровкиМесяца < ДлинаМесяца Тогда
				МаксимальнаяДлинаРасшифровкиМесяца = ДлинаМесяца;
			КонецЕсли; 
						
			ДлинаДней = СтрДлина(РасшифровкаМесяца.Дни);
			Если МаксимальнаяДлинаРасшифровкиДней < ДлинаДней Тогда
				МаксимальнаяДлинаРасшифровкиДней = ДлинаДней;
			КонецЕсли;
			
			Форма.ТаблицаЗаработка[НомерМесяца + Форма.ДобавляемыеМесяцы -1].УчтеноДней = УчтеноДней;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Если СуммаЗаработка = 0 ИЛИ КалендарныхДней = 0 Тогда
		ТекстРасшифровкиСреднедневной = НСтр("ru='Нет данных для расчета среднего заработка'");
	Иначе
		
		МасивСтрок = Новый Массив;
		МасивСтрок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Средний заработок: %1 / %2 = '"),
			Формат(Форма.СуммаУчтенногоЗаработка, "ЧДЦ=2"),
			Формат(КалендарныхДней, "ЧДЦ=2")));
		МасивСтрок.Добавить(Новый ФорматированнаяСтрока(
							Формат(?(КалендарныхДней = 0,0, Форма.СреднийЗаработок), "ЧДЦ=2; ЧН=0,00"),
							Новый Шрифт(,, Истина)));
			
		ТекстРасшифровкиСреднедневной = Новый ФорматированнаяСтрока(МасивСтрок);
		
	КонецЕсли;
	
	Форма.ОсновныеНачисленияВсего = СуммаЗаработка;
	Форма.УчтеноДнейВсего = КалендарныхДней;
	
	Форма.Элементы.СреднийЗаработокДекорация.Заголовок = ТекстРасшифровкиСреднедневной;
	
	// Заголовок группы
	Если СуммаЗаработка = 0 ИЛИ КалендарныхДней = 0 Тогда
		ЗаголовокСвернутогоОтображения = НСтр("ru='<нет данных для расчета>'");
	Иначе
		ЗаголовокСвернутогоОтображения = СтрШаблон(НСтр("ru='Средний заработок: %1'"),
			Формат(?(КалендарныхДней = 0,0, Форма.СреднийЗаработок), "ЧДЦ=2; ЧН=0,00"));
	КонецЕсли;
		
	Форма.Элементы.ГруппаСреднийЗаработок.ЗаголовокСвернутогоОтображения = ЗаголовокСвернутогоОтображения;
	
КонецПроцедуры

#КонецОбласти

#Область УчетЗарплатыВоВнешнейПрограмме

// Формирует движения по отражению зарплаты в бух. учете
Процедура СформироватьДвиженияОтраженияЗарплаты(ТаблицаОтраженияВУчете, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаОтраженияВУчете) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыДвиженийОтраженияЗарплаты(ТаблицаОтраженияВУчете, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаПБУ18          = УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период);
	
	ТаблицаОтраженияВУчете = Параметры.ТаблицаОтраженияВУчете;

	Для Каждого СтрокаТаблицы из ТаблицаОтраженияВУчете Цикл
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Организация	= Реквизиты.Организация;
		Проводка.Период			= Реквизиты.Период;
		

		Проводка.СчетДт	= СтрокаТаблицы.СчетДт;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.СубконтоДт1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.СубконтоДт2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.СубконтоДт3);
		Если СтрокаТаблицы.УчетПоПодразделениямДт Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.ПодразделениеДт;
		КонецЕсли;
		
		Проводка.СчетКт	= СтрокаТаблицы.СчетКт;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.СубконтоКт1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.СубконтоКт2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.СубконтоКт3);
		
		Если СтрокаТаблицы.УчетПоПодразделениямКт Тогда
			Проводка.ПодразделениеКт = СтрокаТаблицы.ПодразделениеКт;
		КонецЕсли;
		
		Проводка.Сумма = СтрокаТаблицы.Сумма;
		
		Если ОтражатьВНалоговомУчете Тогда
			НалоговыйУчет.ЗаполнитьНалоговыеСуммыПроводки(СтрокаТаблицы.СуммаНУДт, СтрокаТаблицы.СуммаНУКт, СтрокаТаблицы.СуммаПРДт, СтрокаТаблицы.СуммаПРКт, СтрокаТаблицы.СуммаВРДт, СтрокаТаблицы.СуммаВРКт , Проводка, ПоддержкаПБУ18);
		КонецЕсли;
		
		Проводка.Содержание = СтрокаТаблицы.Содержание;
	КонецЦикла;
	
	// Отражение в ЕНС
	Если Реквизиты.Период >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета() Тогда
		Если ЗначениеЗаполнено(ТаблицаОтраженияВУчете) Тогда
			
			МесяцУплатыВзносов = ДобавитьМесяц(Реквизиты.Период, 1);
			СрокУплаты = Дата(Год(МесяцУплатыВзносов), Месяц(МесяцУплатыВзносов), 28);
			
			ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
			ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
			ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
			ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
				ПроизводственныйКалендарь,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СрокУплаты),
				ПараметрыПолучения);
			СрокУплаты = ГрафикПереноса[СрокУплаты];
			СчетУчетаЕНС = ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет;
			
			СчетаУчетаСтраховыхВзносов = ЕдиныйНалоговыйСчетПовтИсп.ОбслуживаемыеСчетаУчетаСтраховыхВзносов(Реквизиты.Период);
			ТаблицаНалоговВзносовСФОТ = ТаблицаОтраженияВУчете.Скопировать();
			ТаблицаНалоговВзносовСФОТ.Свернуть("СчетКт, СубконтоКт1, СубконтоКт2, СубконтоКт3", "Сумма");
			НалогиКУплате = ЕдиныйНалоговыйСчет.НовыйНачислениеНалоговНаЕНС();
			
			Для Каждого СчетУчетаСтраховыхВзносов Из СчетаУчетаСтраховыхВзносов Цикл
				МассивСтрок = ТаблицаНалоговВзносовСФОТ.НайтиСтроки(Новый Структура("СчетКт", СчетУчетаСтраховыхВзносов));
				Для Каждого СтрокаМассива Из МассивСтрок Цикл
					
					НоваяСтрока = НалогиКУплате.Добавить();
					
					НоваяСтрока.СчетУчета = СтрокаМассива.СчетКт;
					НоваяСтрока.Субконто1 = СтрокаМассива.СубконтоКт1;
					НоваяСтрока.Субконто2 = СтрокаМассива.СубконтоКт2;
					НоваяСтрока.Субконто3 = СтрокаМассива.СубконтоКт3;
					НоваяСтрока.СрокУплаты = СрокУплаты;
					
					НоваяСтрока.СуммаНалога = СтрокаМассива.Сумма;
					НоваяСтрока.Начислено = СтрокаМассива.Сумма;
					НоваяСтрока.Сумма = СтрокаМассива.Сумма;
					
					НоваяСтрока.НачалоПериода = НачалоМесяца(Реквизиты.Период);
					НоваяСтрока.КонецПериода = КонецМесяца(Реквизиты.Период);
					
					НоваяСтрока.Содержание  = НСтр("ru = 'Учет страховых взносов в составе совокупной налоговой обязанности'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
					
				КонецЦикла;
			КонецЦикла;
			
			ЕдиныйНалоговыйСчет.СформироватьДвиженияНачислениеНалоговНаЕНС(НалогиКУплате, Реквизиты, Движения);
			
		КонецЕсли;
	КонецЕсли;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		
		ТаблицаОтраженияВУчете.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
		ТаблицаОтраженияВУчете.Колонки.Добавить("Контрагент",   Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		
		Для Каждого СтрокаТаблицы из ТаблицаОтраженияВУчете Цикл
			Если ЗначениеЗаполнено("ПодразделениеДт") Тогда
				СтрокаТаблицы.Подразделение = СтрокаТаблицы.ПодразделениеДт;
			Иначе
				СтрокаТаблицы.Подразделение = СтрокаТаблицы.ПодразделениеКт;
			КонецЕсли;
			СтрокаТаблицы.Контрагент = СтрокаТаблицы.СубконтоКт1;
		КонецЦикла;
		ЗарегистрироватьДанныеДляПечатиЧеков(ТаблицаОтраженияВУчете, Реквизиты, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьПараметрыДвиженийОтраженияЗарплаты(ТаблицаОтраженияВУчете, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = ""
	+ "Период,"			// <Дата> - счет по дебету проводки (счет учета денежных средств)
	+ "Организация,"	// <СправочникСсылка.Организации> - организация документа
	+ "Ссылка"			// <ДокументСсылка.ОтражениеЗарплатыВУчете>
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы по заработной плате:
	СписокОбязательныхКолонок = ""
	+ "СчетДт,"										// <ПланСчетовСсылка.Хозрасчетный>
	+ "ПодразделениеДт,"							// <Ссылка на справочник подразделений>
	+ "УчетПоПодразделениямДт,"						// Булево - признак учета по подразделениям счета Дт
	+ "СубконтоДт1,"								// <> - значение аналитики 
	+ "СубконтоДт2,"								// <> - значение аналитики 
	+ "СубконтоДт3,"								// <> - значение аналитики
	+ "СчетКт,"										// <ПланСчетовСсылка.Хозрасчетный>
	+ "УчетПоПодразделениямКт,"						// Булево - признак учета по подразделениям счета Кт
	+ "ПодразделениеКт,"							// <Ссылка на справочник подразделений>
	+ "СубконтоКт1,"								// <> - значение аналитики 
	+ "СубконтоКт2,"								// <> - значение аналитики 
	+ "СубконтоКт3,"								// <> - значение аналитики	
	+ "Содержание,"									// Строка - содержание проводки
	+ "Сумма,"										// <Число(15,2)>
	+ "СуммаНУДт,"									// <Число(15,2)>
	+ "СуммаПРДт,"									// <Число(15,2)>
	+ "СуммаВРДт,"									// <Число(15,2)>
	+ "СуммаНУКт,"									// <Число(15,2)>
	+ "СуммаПРКт,"									// <Число(15,2)>
	+ "СуммаВРКт,"									// <Число(15,2)>
	+ "ЯвляетсяОснованиемОформленияКассовогоЧека,"	// Булево
	+ "ОписаниеУдержанияДляЧека,"					// <Строка(100)>
	+ "ФизическоеЛицо"								// <СправочникСсылка.ФизическиеЛица>
	;	
	
	Параметры.Вставить("ТаблицаОтраженияВУчете", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаОтраженияВУчете, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// Формирует движения по отражению зарплаты для ИП
Процедура СформироватьДвиженияОтраженияЗарплатыИП(ТаблицаОтраженияЗарплатыИП, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаОтраженияЗарплатыИП) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыДвиженийОтраженияЗарплатыИП(ТаблицаОтраженияЗарплатыИП, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ВедетсяУчетРасчетовПоЗарплатеСводно   = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно");
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	
	ТаблицаОтраженияЗарплатыИП = Параметры.ТаблицаОтраженияЗарплатыИП;
	
	Если  ВедетсяУчетРасчетовПоЗарплатеСводно И УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		СтрокиКУдалению = Новый Массив();
		Для Каждого СтрокаТаблицы ИЗ ТаблицаОтраженияЗарплатыИП	Цикл		
			Если СтрокаТаблицы.КорСчет = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда Тогда
				СтрокиКУдалению.Добавить(СтрокаТаблицы);				
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокиКУдалению.Количество() > 0 Тогда
			Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
				ТаблицаОтраженияЗарплатыИП.Удалить(СтрокаКУдалению);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Реквизиты.Колонки.Найти("Удержания") = Неопределено Тогда
		Параметры.Реквизиты.Колонки.Добавить("Удержания");
		Параметры.Реквизиты.ЗаполнитьЗначения(Ложь, "Удержания");
	КонецЕсли;
	
	Параметры.Вставить("ТаблицаНачисленияУдержания", ТаблицаОтраженияЗарплатыИП);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияРасходовНаОплатуТруда(Параметры, Движения, Отказ);

КонецПроцедуры

Процедура ЗаполнитьНедостающиеРеквизитыЗагруженныхОбъектов(Элемент) Экспорт
	
	Если ТипЗнч(Элемент) = Тип("СправочникСсылка.СтатьиЗатрат")
		ИЛИ ТипЗнч(Элемент) = Тип("СправочникОбъект.СтатьиЗатрат") Тогда
		Если НЕ ЗначениеЗаполнено(Элемент.ВидДеятельностиДляНалоговогоУчетаЗатрат) Тогда
			Если ТипЗнч(Элемент) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
				Объект = Элемент.ПолучитьОбъект();
			Иначе
				Объект = Элемент;
			КонецЕсли;
			
			Если Объект.ЭтоГруппа Тогда
				Возврат;
			КонецЕсли;
			
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			Объект.ОбменДанными.Загрузка = Истина;
			Объект.Записать();
			Возврат;			
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Элемент) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы")
		ИЛИ ТипЗнч(Элемент) = Тип("СправочникОбъект.ПрочиеДоходыИРасходы") Тогда
		Если НЕ ЗначениеЗаполнено(Элемент.ВидДеятельностиДляНалоговогоУчетаЗатрат) Тогда
			Если ТипЗнч(Элемент) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
				Объект = Элемент.ПолучитьОбъект();
			Иначе
				Объект = Элемент;
			КонецЕсли;
			
			Если Объект.ЭтоГруппа Тогда
				Возврат;
			КонецЕсли;
						
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			Объект.ОбменДанными.Загрузка = Истина;
			Объект.Записать();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиУчетЗарплаты

// Обработчик подписки на событие ПроверитьНеобходимостьИспользованияПодсистемыЗарплатаИКадры.
// Вызывается при записи элемента справочника "Организации".
//
Процедура ПроверитьНеобходимостьИспользованияПодсистемыЗарплатаИКадры(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если СервисЭлектронныхТрудовыхКнижек.ИспользуетсяСервисЭлектронныхТрудовыхКнижек() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТарификацияБП.РазрешенУчетЗарплаты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользуетсяПодсистемаУчетаЗарплатыИКадров() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);

	Если Источник.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо 
		ИЛИ ИПИспользуетТрудНаемныхРаботников(Источник, Ложь)
		ИЛИ Источник.ДополнительныеСвойства.Свойство("ИспользуетсяТрудНаемныхРаботников") Тогда
			МенеджерЗначения = Константы.ВедетсяУчетЗарплатыКадров.СоздатьМенеджерЗначения();
			МенеджерЗначения.Значение = Истина;
			МенеджерЗначения.ДополнительныеСвойства.Вставить("НастраиватьКадровыйУчетИПоддержкуСовместительства",
				Источник.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
			МенеджерЗначения.Записать();
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Проверяется соответствующая константа, в отдельных случаях
// когда проверить функциональную опцию нельзя - не обновлен командный интерфейс
//
Функция ВключенаПодсистемаУчетаЗарплатыИКадров() Экспорт
	
	Возврат Константы.ВедетсяУчетЗарплатыКадров.Получить();
	
КонецФункции

// Проверяется соответствующая функциональная опция.
//
Функция ИспользуетсяПодсистемаУчетаЗарплатыИКадров() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров");
	
КонецФункции

// Отключается подсистема учета зарплаты и кадров при первом запуске,
// а также при выключении соответствующей функциональности
//
Процедура ОтключитьПодсистемуУчетаЗарплатыИКадров() Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	// Отключение подсистемы	
	Константы.ОтображатьПодсистемуЗарплатаИКадры.Установить(Ложь);
	Константы.ОтображатьПодсистемуСотрудники.Установить(Ложь);
		
	МенеджерЗначенияКонстанты = Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.СоздатьМенеджерЗначения();
	МенеджерЗначенияКонстанты.Значение = Ложь;
	МенеджерЗначенияКонстанты.ОбменДанными.Загрузка = Истина;
	МенеджерЗначенияКонстанты.Записать();
	
	МенеджерЗначенияКонстанты = Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.СоздатьМенеджерЗначения();
	МенеджерЗначенияКонстанты.Значение = Ложь;
	МенеджерЗначенияКонстанты.ОбменДанными.Загрузка = Истина;
	МенеджерЗначенияКонстанты.Записать();
	
	МенеджерЗначенияКонстанты = Константы.ИспользоватьКадровыйУчетПриВнешнейЗарплате.СоздатьМенеджерЗначения();
	МенеджерЗначенияКонстанты.Значение = Ложь;
	МенеджерЗначенияКонстанты.ОбменДанными.Загрузка = Истина;
	МенеджерЗначенияКонстанты.Записать();
	
	Константы.ИспользоватьНачислениеЗарплаты.Установить(Ложь);
	Константы.ИспользоватьКадровыйУчет.Установить(Ложь);
	Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.Установить(Ложь);
	Константы.РасчетЗарплатыДляНебольшихОрганизаций.Установить(Ложь);
	Константы.ПоддержкаСовместительства.Установить(Ложь);
	
	Константы.ИспользоватьНачисленияПоДоговорам.Установить(Ложь);
	Константы.ИспользоватьРаботуНаНеполнуюСтавку.Установить(Ложь);
	
	МенеджерЗначенияКонстанты = Константы.ИспользоватьВоинскийУчет.СоздатьМенеджерЗначения();
	МенеджерЗначенияКонстанты.Значение = Ложь;
	МенеджерЗначенияКонстанты.ОбменДанными.Загрузка = Истина;
	МенеджерЗначенияКонстанты.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	Иначе
		Выборка = Результат.Выбрать();
	КонецЕсли;
	
	ОтражениеВРегламентированномУчетеНастройкиОрганизаций = РегистрыСведений.ОтражениеВРегламентированномУчетеНастройкиОрганизаций.СоздатьМенеджерЗаписи();
	
	Пока Выборка.Следующий() Цикл
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Организация = Выборка.Организация;
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Прочитать();
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Организация = Выборка.Организация;
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.ФормироватьПроводкиВКонцеПериода = Ложь;
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Записать();
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.НастройкиКадровогоУчета.СоздатьНаборЗаписей();
	НаборЗаписей.Очистить();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать();
	
	НаборЗаписей = РегистрыСведений.НастройкиРасчетаЗарплаты.СоздатьНаборЗаписей();
	НаборЗаписей.Очистить();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ВключитьПодсистемуУчетаЗарплатыИКадров(ТребуетсяСоздатьПоставляемыеВидыНалогов = Истина, НастраиватьКадровыйУчетИПоддержкуСовместительства = Истина) Экспорт

	НастроитьПодсистемуУчетаЗарплатыИКадров(
			ТребуетсяСоздатьПоставляемыеВидыНалогов,
			НастраиватьКадровыйУчетИПоддержкуСовместительства);
			
КонецПроцедуры

// Обработчик обновления
// Определеляет необходимость использования подсистемы учета зарплаты и кадров
// Если подсистема не требуется - она отключается
//
Процедура НастроитьПодсистемуЗарплатаИКадры() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И ВЫБОР
	|			КОГДА Организации.РегистрационныйНомерПФР = """"
	|					И Организации.КодОрганаПФР = """"
	|					И Организации.РегистрационныйНомерТФОМС = """"
	|					И Организации.РегистрационныйНомерФСС = """"
	|					И Организации.КодПодчиненностиФСС = """"
	|					И (ВЫРАЗИТЬ(Организации.НаименованиеТерриториальногоОрганаФСС КАК СТРОКА(1))) = """"
	|					И (ВЫРАЗИТЬ(Организации.НаименованиеТерриториальногоОрганаПФР КАК СТРОКА(1))) = """"
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Хозрасчетный.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Хозрасчетный.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОтключитьПодсистемуУчетаЗарплатыИКадров();
	Иначе
		Константы.ОтображатьПодсистемуЗарплатаИКадры.Установить(Константы.ИнтерфейсТакси.Получить());
		Константы.ОтображатьПодсистемуСотрудники.Установить(Константы.ИнтерфейсТаксиПростой.Получить());
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьНастройкиУчетаЗарплаты(ВестиУчетЗарплатыИКадровВоВнешнейПрограмме) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.ИспользоватьНачислениеЗарплаты.Установить(НЕ ВестиУчетЗарплатыИКадровВоВнешнейПрограмме);
	Если ВестиУчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		Константы.ИспользоватьКадровыйУчет.Установить(Ложь);
		Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.Установить(Ложь);
		Константы.ИспользоватьНачисленияПоДоговорам.Установить(Ложь);
		Константы.ИспользоватьРаботуНаНеполнуюСтавку.Установить(Ложь);
		
		МенеджерЗначенияКонстанты = Константы.ИспользоватьВоинскийУчет.СоздатьМенеджерЗначения();
		МенеджерЗначенияКонстанты.Значение = Ложь;
		МенеджерЗначенияКонстанты.ОбменДанными.Загрузка = Истина;
		МенеджерЗначенияКонстанты.Записать();
	Иначе
		Константы.РасчетЗарплатыДляНебольшихОрганизаций.Установить(
			Константы.ИспользоватьНачислениеЗарплаты.Получить()
			И РасчетЗарплатыДляНебольшихОрганизацийВозможен());
		Константы.ИспользоватьОбменЗарплата3Бухгалтерия3ПоВсемОрганизациям.Установить(Ложь);
		НастройкиОбмена = РегистрыСведений.ИспользованиеОбменаЗарплата3Бухгалтерия3ПоОрганизациям.СоздатьНаборЗаписей();
		НастройкиОбмена.Очистить();
		НастройкиОбмена.Записать();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	Иначе
		Выборка = Результат.Выбрать();
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		// Отражение в регламентированном учете.
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций = РегистрыСведений.ОтражениеВРегламентированномУчетеНастройкиОрганизаций.СоздатьНаборЗаписей();
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Отбор.Организация.Установить(Выборка.Организация);
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Прочитать();
		
		Если ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Количество() = 0 Тогда
			НоваяЗапись = ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Добавить();
			НоваяЗапись.Организация = Выборка.Организация;
		КонецЕсли;
		
		Для Каждого Запись Из ОтражениеВРегламентированномУчетеНастройкиОрганизаций Цикл
			Запись.ФормироватьПроводкиВКонцеПериода = ВестиУчетЗарплатыИКадровВоВнешнейПрограмме;
		КонецЦикла;
		
		ОтражениеВРегламентированномУчетеНастройкиОрганизаций.Записать();
		
		// Зарплата и кадры.
		НастройкиЗарплатаКадры = РегистрыСведений.НастройкиЗарплатаКадры.СоздатьНаборЗаписей();
		НастройкиЗарплатаКадры.Отбор.Организация.Установить(Выборка.Организация);
		НастройкиЗарплатаКадры.Прочитать();
	
		Если НастройкиЗарплатаКадры.Количество() = 0 Тогда
			НоваяЗапись = НастройкиЗарплатаКадры.Добавить();
			НоваяЗапись.Организация = Выборка.Организация;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ПорядокВыплатыЗарплатыПоУмолчанию());
		КонецЕсли;
		НастройкиЗарплатаКадры.Записать();
	
		Если НЕ ВестиУчетЗарплатыИКадровВоВнешнейПрограмме Тогда
			
			// Настройки учета зарплаты.
			НастройкиУчетаЗарплаты = РегистрыСведений.НастройкиУчетаЗарплаты.СоздатьНаборЗаписей();
			НастройкиУчетаЗарплаты.Отбор.Организация.Установить(Выборка.Организация);
			НастройкиУчетаЗарплаты.Прочитать();
			
			Если НастройкиУчетаЗарплаты.Количество() = 0 Тогда
				НоваяЗапись = НастройкиУчетаЗарплаты.Добавить();
				НоваяЗапись.Организация = Выборка.Организация;
				РегистрыСведений.НастройкиУчетаЗарплаты.ЗначенияНастройкиПоУмолчанию(НоваяЗапись);
			КонецЕсли;
			
			Для Каждого Запись Из НастройкиУчетаЗарплаты Цикл
				Запись.ИспользоватьФорматОбменаЗУП25 = Ложь;
			КонецЦикла;
				
			НастройкиУчетаЗарплаты.Записать();
			
			Если ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Выборка.Организация) = Выборка.Организация Тогда
				
				// Учетная политика по НДФЛ.
				УчетнаяПолитикаПоНДФЛ = РегистрыСведений.УчетнаяПолитикаПоНДФЛ.СоздатьНаборЗаписей();
				УчетнаяПолитикаПоНДФЛ.Отбор.ГоловнаяОрганизация.Установить(Выборка.Организация);
				УчетнаяПолитикаПоНДФЛ.Прочитать();
				
				Если УчетнаяПолитикаПоНДФЛ.Количество() = 0 Тогда
					НоваяЗапись = УчетнаяПолитикаПоНДФЛ.Добавить();
					НоваяЗапись.ГоловнаяОрганизация = Выборка.Организация;
				КонецЕсли;
				
				Для Каждого Запись Из УчетнаяПолитикаПоНДФЛ Цикл
					Запись.ОсобенностиИсчисленияНДФЛ = Перечисления.ОсобенностиИсчисленияНДФЛ.СтандартныеВычетыНарастающимИтогом;
				КонецЦикла;
				
				УчетнаяПолитикаПоНДФЛ.Записать();
				
			КонецЕсли;
			
		Иначе
			
			// Учет страховых взносов.
			ОтключитьНастройкиУчетаСтраховыхВзносов(Выборка.Организация);
			
			// Зарплата и кадры.
			НастройкиЗарплатаКадры = РегистрыСведений.НастройкиЗарплатаКадры.СоздатьНаборЗаписей();
			НастройкиЗарплатаКадры.Отбор.Организация.Установить(Выборка.Организация);
			НастройкиЗарплатаКадры.Прочитать();
		
			Если НастройкиЗарплатаКадры.Количество() = 0 Тогда
				НоваяЗапись = НастройкиЗарплатаКадры.Добавить();
				НоваяЗапись.Организация = Выборка.Организация;
			КонецЕсли;
			
			Для Каждого Запись Из НастройкиЗарплатаКадры Цикл
				Запись.ПрименятьСевернуюНадбавку                   = Ложь;
				Запись.ПрименятьРайонныйКоэффициент                = Ложь;
				Запись.ПрименятьСевернуюНадбавкуВПодразделениях    = Ложь;
				Запись.ПрименятьРайонныйКоэффициентВПодразделениях = Ложь;
			КонецЦикла;
			
			НастройкиЗарплатаКадры.Записать();
			
		КонецЕсли;
			
	КонецЦикла;
	
	УправлениеСвойствамиБЗК.УстановитьДоступностьВсехНаборовДополнительныхРеквизитов();
	
	ВключитьАвтообменПоСЭДОФСС();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОтключитьНастройкиУчетаСтраховыхВзносов(Организация) Экспорт
	
	НастройкиУчетаСтраховыхВзносов = РегистрыСведений.НастройкиУчетаСтраховыхВзносов.СоздатьНаборЗаписей();
	НастройкиУчетаСтраховыхВзносов.Отбор.Организация.Установить(Организация);
	НастройкиУчетаСтраховыхВзносов.Прочитать();
	
	Если НастройкиУчетаСтраховыхВзносов.Количество() = 0 Тогда
		НоваяЗапись = НастройкиУчетаСтраховыхВзносов.Добавить();
		НоваяЗапись.Организация = Организация;
	КонецЕсли;
	
	Для Каждого Запись Из НастройкиУчетаСтраховыхВзносов Цикл
		Запись.ИспользуетсяТрудФармацевтов                        = Ложь;
		Запись.ИспользуетсяТрудЧленовЛетныхЭкипажей               = Ложь;
		Запись.ИспользуетсяТрудЧленовЭкипажейМорскихСудов         = Ложь;
		Запись.ИспользуетсяТрудШахтеров                           = Ложь;
		Запись.ИспользуютсяРаботыСДосрочнойПенсией                = Ложь;
		Запись.ПрименяютсяРезультатыСпециальнойОценкиУсловийТруда = Ложь;
	КонецЦикла;
	
	НастройкиУчетаСтраховыхВзносов.Записать();
	
КонецПроцедуры

Процедура СоздатьПараметрыУчетаЗарплаты(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.НастройкиУчетаЗарплаты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация                   = Организация;
		РегистрыСведений.НастройкиУчетаЗарплаты.ЗначенияНастройкиПоУмолчанию(Запись);
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиЗарплатаКадры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация                   = Организация;
		ЗаполнитьЗначенияСвойств(Запись, ПорядокВыплатыЗарплатыПоУмолчанию());
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыОрганизаций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Прочитать();
	
	
	СпособОтраженияЗарплатыВБухучете = СпособОтраженияПоУмолчанию();
	Если НаборЗаписей.Количество() = 0 Тогда
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация = Организация;
		Запись.Период = ДатаОтсчетаПериодическихСведений();
		Запись.СпособОтраженияЗарплатыВБухучете = СпособОтраженияЗарплатыВБухучете;
		Запись.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД;
		НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация) = Организация Тогда
		НаборЗаписей = РегистрыСведений.УчетнаяПолитикаПоНДФЛ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(Организация);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			
			Запись = НаборЗаписей.Добавить();
			Запись.ГоловнаяОрганизация = Организация;
			Запись.ОсобенностиИсчисленияНДФЛ = Перечисления.ОсобенностиИсчисленияНДФЛ.СтандартныеВычетыНарастающимИтогом;
			
			НаборЗаписей.Записать();
			
		КонецЕсли;
	КонецЕсли;
	
	Если Справочники.Календари.НайтиПоРеквизиту("ВладелецГрафика", Организация) = Справочники.Календари.ПустаяСсылка() Тогда
		ОсновнойПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
		Если ОсновнойПроизводственныйКалендарь <> Неопределено Тогда
			СоздатьГрафикРаботыОрганизации(Организация, ОсновнойПроизводственныйКалендарь);
		КонецЕсли;
	КонецЕсли;
	
	НастроитьСпособыОтраженияДополнительныхНачислений();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура СоздатьГрафикРаботыОрганизации(Организация, ПроизводственныйКалендарь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		НовыйГрафикРаботы = МодульРасчетЗарплатыДляНебольшихОрганизаций.НовыйГрафикРаботыОрганизации(Организация, ПроизводственныйКалендарь);
		НовыйГрафикРаботы.Записать();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция НаименованиеСпособаОтраженияЗарплатыПоУмолчанию()
	
	Возврат НСтр("ru = 'Отражение начислений по умолчанию'");
	
КонецФункции

Функция СпособОтраженияПоУмолчанию()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", НаименованиеСпособаОтраженияЗарплатыПоУмолчанию());
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпособыОтраженияЗарплатыВБухУчете.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтраженияЗарплатыВБухУчете
	|ГДЕ
	|	СпособыОтраженияЗарплатыВБухУчете.Наименование = &Наименование
	|	И НЕ СпособыОтраженияЗарплатыВБухУчете.ЭтоГруппа";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() <> 0 Тогда
		Выборка.Следующий();
		СпособОтраженияЗарплатыВБухучете = Выборка.Ссылка;
	Иначе
		НовыйСпособОтраженияЗарплатыВБухучете = Справочники.СпособыОтраженияЗарплатыВБухУчете.СоздатьЭлемент();
		НовыйСпособОтраженияЗарплатыВБухучете.Наименование = НаименованиеСпособаОтраженияЗарплатыПоУмолчанию();
		НовыйСпособОтраженияЗарплатыВБухучете.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
		НовыйСпособОтраженияЗарплатыВБухучете.Записать();
		СпособОтраженияЗарплатыВБухучете = НовыйСпособОтраженияЗарплатыВБухучете.Ссылка;
	КонецЕсли;
	
	НастроитьПорядокОтраженияЗарплатыПоУмолчанию(СпособОтраженияЗарплатыВБухучете);
	
	Возврат СпособОтраженияЗарплатыВБухучете;
	
КонецФункции

Процедура НастроитьПорядокОтраженияЗарплатыПоУмолчанию(СпособОтраженияЗарплаты = Неопределено) Экспорт
	
	Если СпособОтраженияЗарплаты = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Наименование", НаименованиеСпособаОтраженияЗарплатыПоУмолчанию());
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СпособыОтраженияЗарплатыВБухУчете.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтраженияЗарплатыВБухУчете
		|ГДЕ
		|	СпособыОтраженияЗарплатыВБухУчете.Наименование = &Наименование
		|	И НЕ СпособыОтраженияЗарплатыВБухУчете.ЭтоГруппа";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			СпособОтраженияЗарплаты = Выборка.Ссылка
		Иначе
			СпособОтраженияЗарплаты = СпособОтраженияПоУмолчанию();
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	СпособОтраженияЗарплатыОбъект = СпособОтраженияЗарплаты.ПолучитьОбъект();
	
	// В момент вызова функциональные опции еще могут быть
	// не обновлены, поэтому читаем значения констант.
	ПрименяетсяОсобыйПорядокНалогообложения   = Константы.ИспользуетсяЕНВД.Получить()
		Или Константы.ИспользуетсяУСНПатент.Получить();
	ПрименяетсяОсновнаяСистемаНалогообложения = Константы.ИспользуетсяОСНО.Получить()
		Или Константы.ИспользуетсяУСН.Получить() Или Константы.ИспользуетсяАУСН.Получить();
	
	Если ПрименяетсяОсобыйПорядокНалогообложения И НЕ ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		СпособОтраженияЗарплатыОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения;
	ИначеЕсли ПрименяетсяОсобыйПорядокНалогообложения И ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		СпособОтраженияЗарплатыОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты;
	Иначе
		СпособОтраженияЗарплатыОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
	КонецЕсли;
	
	СпособОтраженияЗарплатыОбъект.Записать();
	
	Если ПрименяетсяОсновнаяСистемаНалогообложения Или ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	Если ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		ЕНВД = Ложь;
		НаборЗаписей = РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СпособОтраженияЗарплатыВБухУчете.Установить(СпособОтраженияЗарплаты);
		НаборЗаписей.Отбор.ЕНВД.Установить(ЕНВД);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьПоУмолчанию(СпособОтраженияЗарплаты, ЕНВД);
		КонецЕсли;
	КонецЕсли;
	
	Если ПрименяетсяОсобыйПорядокНалогообложения Тогда
		ЕНВД = Истина;
		НаборЗаписей = РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СпособОтраженияЗарплатыВБухУчете.Установить(СпособОтраженияЗарплаты);
		НаборЗаписей.Отбор.ЕНВД.Установить(ЕНВД);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьПоУмолчанию(СпособОтраженияЗарплаты, ЕНВД);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура УточнитьОтношениеКЕНВД(Организация, Период, ПлательщикЕНВД, ПрименяетсяОсобыйПорядокНалогообложения) Экспорт
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыОрганизаций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(Период);
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		
		ДействующиеНастройки = РегистрыСведений.БухучетЗарплатыОрганизаций.СрезПервых(Период, Новый Структура("Организация", Организация));
		Если ДействующиеНастройки.Количество() = 0 Тогда
			ДействующиеНастройки = РегистрыСведений.БухучетЗарплатыОрганизаций.СрезПоследних(Период, Новый Структура("Организация", Организация));
		КонецЕсли;
		Запись = НаборЗаписей.Добавить();
		Если ДействующиеНастройки.Количество() <> 0 Тогда
			ЗаполнитьЗначенияСвойств(Запись, ДействующиеНастройки[0]);
		Иначе
			Запись.СпособОтраженияЗарплатыВБухучете = СпособОтраженияПоУмолчанию();
		КонецЕсли;
		Запись.Организация = Организация;
		Запись.Период = Период;
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли;
	
	Если ПлательщикЕНВД И ПрименяетсяОсобыйПорядокНалогообложения Тогда
		Запись.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД;
	ИначеЕсли НЕ ПлательщикЕНВД Тогда
		Запись.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД;
	Иначе
		Запись.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом;
	КонецЕсли;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ВедетсяКадровыйУчет() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
		
КонецФункции

Процедура УстановитьНастройкиКадровогоУчета(КадровыйУчет = Неопределено, УчетЗарплатыИКадровСредствамиБухгалтерии = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если УчетЗарплатыИКадровСредствамиБухгалтерии = Неопределено Тогда
		УчетЗарплатыИКадровСредствамиБухгалтерии = Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Получить();
	КонецЕсли;
	
	Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		Константы.ИспользоватьКадровыйУчет.Установить(КадровыйУчет);
		Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.Установить(КадровыйУчет);
	Иначе
		Константы.ИспользоватьКадровыйУчет.Установить(Ложь);
		Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.Установить(Ложь);
	КонецЕсли;
		
КонецПроцедуры

Функция ИспользуетсяОбменСЗУП30(Организация) Экспорт
	
	//*********************************
	// Обслуживание обмена с ЗУП 3.0
	//*********************************
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЗарплата3Бухгалтерия3ПоВсемОрганизациямЗарплатаКадрыБазовая")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьОбменЗарплата3Бухгалтерия3ПоОрганизацииЗарплатаКадрыБазовая", Новый Структура("Организация", Организация)) Тогда
		Если  ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно") Тогда
			Возврат Ложь;
		Иначе
			Возврат Истина;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

Процедура ПроверитьВозможностьИспользованияОбменаЗарплата3Бухгалтерия3ПередЗаписью(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ОтключитьМеханизмРегистрацииОбъектов")
	   И Не Источник.ДополнительныеСвойства.Свойство("РегистрироватьНаУзлахПлановОбменаПриОбновленииИБ") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоОбменЗУПБП = ТипЗнч(Источник) = Тип("ПланОбменаОбъект.ОбменЗарплата3Бухгалтерия3")
		Или (ТипЗнч(Источник) = Тип("ПланОбменаОбъект.СинхронизацияДанныхЧерезУниверсальныйФормат")
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ВариантНастройки") = "ОбменЗУПБП");
	
	Если НЕ ЭтоОбменЗУПБП Тогда
			Возврат;
	КонецЕсли;
	
	Если Не Источник.ЭтотУзел Тогда
		Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
			ТекстСообщения = НСтр("ru = 'В настройках параметров учета установлен учет зарплаты во внешней программе.'");
			Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Установить(Истина);
			Константы.ИспользоватьНачислениеЗарплаты.Установить(НЕ Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить());
			Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Установить(НЕ Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить());
			УстановитьНастройкиУчетаЗарплаты(Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить());
			УстановитьНастройкиКадровогоУчета(Ложь);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция РегистрационныйНомерПФР(Организация, ДатаСведений) Экспорт
	
	Запрос = Новый Запрос;
	
	ИзмеренияДаты = Новый ТаблицаЗначений;
	ИзмеренияДаты.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ИзмеренияДаты.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	НоваяСтрока = ИзмеренияДаты.Добавить();
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Период = ?(ДатаСведений = Неопределено, ДатаОтсчетаПериодическихСведений(), ДатаСведений);
	
	Запрос.УстановитьПараметр("ВТИзмеренияДатыПараметр", ИзмеренияДаты);	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИзмеренияДаты.Период,
	|	ИзмеренияДаты.Организация,
	|	ИзмеренияДаты.Период КАК Период1
	|ПОМЕСТИТЬ ВТИзмеренияДаты
	|ИЗ
	|	&ВТИзмеренияДатыПараметр КАК ИзмеренияДаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИзмеренияДаты.Период КАК ЗаданныйПериод,
	|	ИзмеренияДаты.Организация КАК Организация,
	|	МАКСИМУМ(РегистрСведений.Период) КАК Период
	|ПОМЕСТИТЬ ВТМаксимальныеПериодыИсторияРегистрацийВОрганеПФР
	|{ВЫБРАТЬ
	|	ЗаданныйПериод,
	|	Организация}
	|ИЗ
	|	ВТИзмеренияДаты КАК ИзмеренияДаты
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВОрганеПФР КАК РегистрСведений
	|		ПО (РегистрСведений.Период <= ИзмеренияДаты.Период
	|				ИЛИ ИзмеренияДаты.Период = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|			И (РегистрСведений.Организация = ИзмеренияДаты.Организация)}
	|{ГДЕ
	|	РегистрСведений.Организация.*}
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмеренияДаты.Период,
	|	ИзмеренияДаты.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МаксимальныеПериоды.ЗаданныйПериод КАК Период,
	|	РегистрСведений.Период КАК ПериодЗаписи,
	|	МаксимальныеПериоды.Организация,
	|	РегистрСведений.РегистрационныйНомерПФР КАК РегистрационныйНомерПФР
	|ПОМЕСТИТЬ ВТИсторияРегистрацийВОрганеПФРСрезПоследних
	|{ВЫБРАТЬ
	|	Период,
	|	ПериодЗаписи,
	|	Организация,
	|	РегистрационныйНомерПФР}
	|ИЗ
	|	ВТМаксимальныеПериодыИсторияРегистрацийВОрганеПФР КАК МаксимальныеПериоды
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВОрганеПФР КАК РегистрСведений
	|		ПО (РегистрСведений.Период = МаксимальныеПериоды.Период)
	|			И (РегистрСведений.Организация = МаксимальныеПериоды.Организация)}
	|ГДЕ
	|	НЕ РегистрСведений.Период ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияРегистрацийВОрганеПФР.РегистрационныйНомерПФР КАК РегистрационныйНомерПФР
	|ИЗ
	|	ВТИсторияРегистрацийВОрганеПФРСрезПоследних КАК ИсторияРегистрацийВОрганеПФР";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат "";
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.РегистрационныйНомерПФР;
	
	
КонецФункции

Функция РегистрационныйНомерСФР(Организация, ДатаСведений) Экспорт
	
	Запрос = Новый Запрос;
	
	ИзмеренияДаты = Новый ТаблицаЗначений;
	ИзмеренияДаты.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ИзмеренияДаты.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	НоваяСтрока = ИзмеренияДаты.Добавить();
	НоваяСтрока.Организация = Организация;
	НоваяСтрока.Период = ?(ДатаСведений = Неопределено, ДатаОтсчетаПериодическихСведений(), ДатаСведений);
	
	Запрос.УстановитьПараметр("ВТИзмеренияДатыПараметр", ИзмеренияДаты);	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИзмеренияДаты.Период КАК Период,
	|	ИзмеренияДаты.Организация КАК Организация,
	|	ИзмеренияДаты.Период КАК Период1
	|ПОМЕСТИТЬ ВТИзмеренияДаты
	|ИЗ
	|	&ВТИзмеренияДатыПараметр КАК ИзмеренияДаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИзмеренияДаты.Период КАК ЗаданныйПериод,
	|	ИзмеренияДаты.Организация КАК Организация,
	|	МАКСИМУМ(РегистрСведений.Период) КАК Период
	|ПОМЕСТИТЬ ВТМаксимальныеПериодыИсторияРегистрацийВОрганеПФР
	|{ВЫБРАТЬ
	|	ЗаданныйПериод,
	|	Организация}
	|ИЗ
	|	ВТИзмеренияДаты КАК ИзмеренияДаты
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВОрганеПФР КАК РегистрСведений
	|		ПО (РегистрСведений.Период <= ИзмеренияДаты.Период
	|				ИЛИ ИзмеренияДаты.Период = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|			И (РегистрСведений.Организация = ИзмеренияДаты.Организация)}
	|{ГДЕ
	|	РегистрСведений.Организация.*}
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмеренияДаты.Период,
	|	ИзмеренияДаты.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МаксимальныеПериоды.ЗаданныйПериод КАК Период,
	|	РегистрСведений.Период КАК ПериодЗаписи,
	|	МаксимальныеПериоды.Организация КАК Организация,
	|	РегистрСведений.РегистрационныйНомерСФР КАК РегистрационныйНомерСФР
	|ПОМЕСТИТЬ ВТИсторияРегистрацийВОрганеПФРСрезПоследних
	|{ВЫБРАТЬ
	|	Период,
	|	ПериодЗаписи,
	|	Организация}
	|ИЗ
	|	ВТМаксимальныеПериодыИсторияРегистрацийВОрганеПФР КАК МаксимальныеПериоды
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВОрганеПФР КАК РегистрСведений
	|		ПО (РегистрСведений.Период = МаксимальныеПериоды.Период)
	|			И (РегистрСведений.Организация = МаксимальныеПериоды.Организация)}
	|ГДЕ
	|	НЕ РегистрСведений.Период ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияРегистрацийВОрганеПФР.РегистрационныйНомерСФР КАК РегистрационныйНомерСФР
	|ИЗ
	|	ВТИсторияРегистрацийВОрганеПФРСрезПоследних КАК ИсторияРегистрацийВОрганеПФР";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат "";
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.РегистрационныйНомерСФР;
	
КонецФункции

Функция ЕстьНастройкиРезервовПоОплатеТруда(Организация, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРезервыПоОплатеТруда") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Организации = БухгалтерскийУчетПереопределяемый.ВсяОрганизация(Организация);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиРасчетаРезервовПоОплатеТруда.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Организация В(&Организации)
	|	И ГОД(НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода) <= ГОД(&Период)
	|	И НЕ НастройкиРасчетаРезервовПоОплатеТруда.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Не Результат.Пустой();
	
КонецФункции

Функция ЕстьНастройкиРезерваОтпусков(Организация, Период) Экспорт
	
	Возврат РезервыПоОплатеТруда.ЕстьНастройкиРезерваОтпусков(Организация, Период);
	
КонецФункции

Функция ЕстьНастройкиРезерваОтпусковАрхивные(Организация, Период) Экспорт
	
	Возврат РезервОтпусков.ЕстьНастройкиРезерваОтпусков(Организация, Период);
	
КонецФункции

Функция ВедетсяУчетРезервовОтпусков(Организация, ДатаСведений) Экспорт
	
	ВедетсяУчетРезервовОтпусков = Ложь;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРезервыПоОплатеТруда") Тогда
		Возврат ВедетсяУчетРезервовОтпусков;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИспользуетсяПодсистемаУчетаЗарплатыИКадров() Тогда
		Если ЕстьНастройкиРезерваОтпусков(Организация, ДатаСведений) Тогда
			НастройкиРезервовОтпусков = РезервыПоОплатеТрудаДляНебольшихОрганизаций.НастройкаРезерваПоВиду(Организация,
				Перечисления.ВидыРезервовПоОплатеТруда.Отпуск,
				ДатаСведений);
			ВедетсяУчетРезервовОтпусков = ЗначениеЗаполнено(НастройкиРезервовОтпусков.Ссылка);
		Иначе
			НастройкиРезервовОтпусков = НастройкиРезервовОтпусков(Организация, ДатаСведений);
			ВедетсяУчетРезервовОтпусков = НастройкиРезервовОтпусков.ФормироватьРезервБУ 
				ИЛИ НастройкиРезервовОтпусков.ФормироватьРезервНУ;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВедетсяУчетРезервовОтпусков;
	
КонецФункции

Функция ВедетсяУчетРезервовОтпусковНУ(Организация, ДатаСведений) Экспорт
	
	ВедетсяУчетРезервовОтпусков = Ложь;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРезервыПоОплатеТруда") Тогда
		Возврат ВедетсяУчетРезервовОтпусков;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИспользуетсяПодсистемаУчетаЗарплатыИКадров() Тогда
		Если ЕстьНастройкиРезерваОтпусков(Организация, ДатаСведений) Тогда
			НастройкиРезервовОтпусков = РезервыПоОплатеТрудаДляНебольшихОрганизаций.НастройкаРезерваПоВиду(Организация,
				Перечисления.ВидыРезервовПоОплатеТруда.Отпуск,
				ДатаСведений);
			ВедетсяУчетРезервовОтпусков = ЗначениеЗаполнено(НастройкиРезервовОтпусков.Ссылка)
				И НастройкиРезервовОтпусков.ФормироватьРезервНУ;
		Иначе
			НастройкиРезервовОтпусков = НастройкиРезервовОтпусков(Организация, ДатаСведений);
			ВедетсяУчетРезервовОтпусков = НастройкиРезервовОтпусков.ФормироватьРезервБУ 
				ИЛИ НастройкиРезервовОтпусков.ФормироватьРезервНУ;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВедетсяУчетРезервовОтпусков;
	
КонецФункции

Функция ВедетсяУчетРезервовПремий(Организация, ДатаСведений) Экспорт
	
	ВедетсяУчетРезервовПремий = Ложь;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРезервыПоОплатеТруда") Тогда
		Возврат ВедетсяУчетРезервовПремий;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИспользуетсяПодсистемаУчетаЗарплатыИКадров() Тогда
		НастройкиРезервовПремий = РезервыПоОплатеТрудаДляНебольшихОрганизаций.НастройкаРезерваПоВиду(Организация,
			Перечисления.ВидыРезервовПоОплатеТруда.ГодоваяПремия,
			ДатаСведений);
		ВедетсяУчетРезервовПремий = ЗначениеЗаполнено(НастройкиРезервовПремий.Ссылка);
	КонецЕсли;
	
	Возврат ВедетсяУчетРезервовПремий;
	
КонецФункции

Функция ВедетсяУчетРезервовПремийНУ(Организация, ДатаСведений) Экспорт
	
	ВедетсяУчетРезервовПремий = Ложь;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРезервыПоОплатеТруда") Тогда
		Возврат ВедетсяУчетРезервовПремий;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИспользуетсяПодсистемаУчетаЗарплатыИКадров() Тогда
		НастройкиРезервовПремий = РезервыПоОплатеТрудаДляНебольшихОрганизаций.НастройкаРезерваПоВиду(Организация,
			Перечисления.ВидыРезервовПоОплатеТруда.ГодоваяПремия,
			ДатаСведений);
		ВедетсяУчетРезервовПремий = ЗначениеЗаполнено(НастройкиРезервовПремий.Ссылка)
			И НастройкиРезервовПремий.ФормироватьРезервНУ;
	КонецЕсли;
	
	Возврат ВедетсяУчетРезервовПремий;
	
КонецФункции

Функция ВедетсяУчетРезервовОтпусковПоВидамУчета(Организация, ДатаСведений, ВидУчета = "БУ") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВедетсяУчетРезервовОтпусков(Организация, ДатаСведений) Тогда
		Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
			НастройкиРезервовОтпусков = НастройкиРезервовОтпусков(Организация, ДатаСведений);
			Возврат НастройкиРезервовОтпусков["ФормироватьРезерв" + ВидУчета];
		Иначе
			Возврат Истина;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ПараметрыУчетаРезервовВОрганизации(Организация, Период) Экспорт
	
	СтруктураНастроекРезервов = РезервыПоОплатеТруда.СтруктураНастроекРезервов();
	
	ФормироватьРезервБУ = ЕстьНастройкиРезервовПоОплатеТруда(Организация, Период);
	ФормироватьРезервНУ = Ложь;
	
	Если ФормироватьРезервБУ Тогда
		
		НастройкиДействующихРезервов = Справочники.НастройкиРасчетаРезервовПоОплатеТруда.НастройкиДействующихРезервов(Организация, Период);
		
		СоответствиеНастроек = НастройкиДействующихРезервов.СоответствиеНастроек;
		Для Каждого КлючИЗначениеНастройкиРезерва Из СоответствиеНастроек Цикл
			НастройкаРезерва = КлючИЗначениеНастройкиРезерва.Значение;
			Если НастройкаРезерва.ФормироватьРезервНУ Тогда
				ФормироватьРезервНУ = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураНастроекРезервов.ФормироватьРезервБУ = ФормироватьРезервБУ;
	СтруктураНастроекРезервов.ФормироватьРезервНУ = ФормироватьРезервНУ;
	
	Возврат СтруктураНастроекРезервов;
	
КонецФункции

Функция РасчетЗарплатыДляНебольшихОрганизаций() Экспорт
	
	РасширеннаяЗарплата = Ложь;
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		Если ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
			РасширеннаяЗарплата = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РасширеннаяЗарплата;
	
КонецФункции

Функция ИспользуетсяРасчетПервойПоловиныМесяца(Организация) Экспорт
	
	// Только для проверки настройки расчета авансов до 2023 г.
	// После 2023 г. учет расчетов первой половины месяца - обязателен.
	
	УстановитьПривилегированныйРежим(Истина);
	
	Организации = БухгалтерскийУчетПереопределяемый.ВсяОрганизация(Организация);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации",                      Организации);
	Запрос.УстановитьПараметр("ДатаНачалаУдержанияНДФЛПоАвансам", ДатаНачалаУдержанияНДФЛПоАвансам());
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НачислениеЗарплаты.Организация КАК Организация
		|ИЗ
		|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
		|ГДЕ
		|	НачислениеЗарплаты.Организация В(&Организации)
		|	И НачислениеЗарплаты.ПерваяПоловинаМесяца
		|	И НачислениеЗарплаты.МесяцНачисления < &ДатаНачалаУдержанияНДФЛПоАвансам
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиСистемыНалогообложения.Организация
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
		|ГДЕ
		|	НастройкиСистемыНалогообложения.Организация В(&Организации)
		|	И НастройкиСистемыНалогообложения.ПрименяетсяАУСН";
	
	Результат = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат  Не Результат.Пустой();
	
КонецФункции

Функция ДатаОтсчетаПериодическихСведений() Экспорт
	
	Возврат '18991231000000';
	
КонецФункции

Функция НастройкиРезервовОтпусков(Организация, ДатаСведений) Экспорт
	
	Возврат РезервОтпусков.НастройкиРезервовОтпусков(Организация, ДатаСведений);
	
КонецФункции

Функция НастройкиРезерва(Организация, Резерв, ДатаСведений) Экспорт
	
	Возврат РезервыПоОплатеТруда.НастройкиРезерва(Организация, Резерв, ДатаСведений);
	
КонецФункции

Процедура ВключитьКадровыйУчетИПоддержкуСовместительства() Экспорт
	Если НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой") Тогда
		Константы.ИспользоватьКадровыйУчет.Установить(Истина);
		Константы.ПоддержкаСовместительства.Установить(Истина);
		Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.Установить(Истина);
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсЭлектронныхТрудовыхКнижек") Тогда
		Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.Установить(Константы.ИспользоватьКадровыйУчет.Получить());
	КонецЕсли;
КонецПроцедуры

Функция ЗарплатаВыплачиваетсяБезВедомости() Экспорт
	
	ВедомостьМожноНеУказывать = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме")
		И ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно");
	
	Возврат ВедомостьМожноНеУказывать;
	
КонецФункции

Процедура УправлениеФункциональностьюУчетаЗарплатыИКадров(Включить, ДополнительныеСвойства = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина); 
	Если Включить Тогда
		НастраиватьКадровыйУчетИПоддержкуСовместительства = Ложь;
		Если ДополнительныеСвойства.Свойство("НастраиватьКадровыйУчетИПоддержкуСовместительства") Тогда
			НастраиватьКадровыйУчетИПоддержкуСовместительства = Истина;
		КонецЕсли;		
		ВключитьПодсистемуУчетаЗарплатыИКадров(Истина, НастраиватьКадровыйУчетИПоддержкуСовместительства);
		УправлениеСвойствамиБЗК.УстановитьДоступностьВсехНаборовДополнительныхРеквизитов();
		
		КлассификаторыДоходовРасходов.ОбеспечитьФункциональность(Справочники.ПрочиеДоходыИРасходы, "ВедетсяУчетЗарплатыКадров");
		КлассификаторыДоходовРасходов.ОбеспечитьФункциональность(Справочники.СтатьиЗатрат,         "ВедетсяУчетЗарплатыКадров");
		
	Иначе
		ОтключитьПодсистемуУчетаЗарплатыИКадров();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура НастроитьПодсистемуУчетаЗарплатыИКадров(ТребуетсяСоздатьПоставляемыеВидыНалогов = Истина, НастраиватьКадровыйУчетИПоддержкуСовместительства = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СервисЭлектронныхТрудовыхКнижек.ИспользуетсяСервисЭлектронныхТрудовыхКнижек() Тогда
		СервисЭлектронныхТрудовыхКнижек.ВключитьПодсистемуЭлектронныхТрудовыхКнижек();
	Иначе
		
		Константы.ОтображатьПодсистемуЗарплатаИКадры.Установить(Константы.ИнтерфейсТакси.Получить());
		Константы.ОтображатьПодсистемуСотрудники.Установить(Константы.ИнтерфейсТаксиПростой.Получить());
		
		Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Установить(Истина);
		Если НастраиватьКадровыйУчетИПоддержкуСовместительства Тогда
			Если НЕ ПолучитьФункциональнуюОпцию("НачалоРаботы") Тогда
				ВключитьКадровыйУчетИПоддержкуСовместительства();
			КонецЕсли;
		КонецЕсли;
		УстановитьНастройкиУчетаЗарплаты(Ложь);
		
		Константы.РасчетЗарплатыДляНебольшихОрганизаций.Установить(
			РасчетЗарплатыДляНебольшихОрганизацийВозможен());
		
		Если ТребуетсяСоздатьПоставляемыеВидыНалогов Тогда
			Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьПоставляемыеЭлементы();
		КонецЕсли;
		
		ВключитьРасчетЗаПервуюПоловинуМесяца();
		
	КонецЕсли;
	
	УправлениеСвойствамиБЗК.УстановитьДоступностьВсехНаборовДополнительныхРеквизитов();
	
	УстановитьПривилегированныйРежим(Ложь)
	
КонецПроцедуры

// Процедура заполнит список задач задачами по зарплате, в случае если пользователь 
// не вводил номер ФСС, а сразу ввел сотрудника - опосредованно определяем необходимость учет зарплаты.
//
Процедура ОбновитьИЗаполнитьРегулярныеЗадачи(УникальныйИдентификатор, Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	// Проеверять "косвенное" включение надо только для ИП
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") = 
		Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда 
		Возврат;
	КонецЕсли;
	
	// Если сотрудников больше одного - заполнение уже производилось.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	Сотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация";
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	// Если сотрудник один - исключим повторное перезаполнение списка задач
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("УсловиеЕстьСотрудники", УсловияПримененияТребованийЗаконодательства.УсловиеЕстьСотрудники());
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Условие КАК Условие,
	|	ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов.Условия КАК ПравилаПредставленияОтчетовУплатыНалоговУсловия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО ПравилаПредставленияОтчетовУплатыНалоговУсловия.Ссылка = ЗадачиБухгалтера.Правило
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ПравилаПредставленияОтчетовУплатыНалоговУсловия.Условие = &УсловиеЕстьСотрудники";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(УникальныйИдентификатор, Организация, Ложь, Ложь, Истина);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Обработчик смены настройки при изменения приложения.
// Вызывается при записи константы СервисЭлектронныхТрудовыхКнижек.
//
Процедура ВключитьИспользованияПодсистемыЗарплатаИКадрыПриСменеТарифа() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПриемНаРаботу.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КадровыйПеревод.Ссылка
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Увольнение.Ссылка
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Сотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.РегистрационныйНомерФСС КАК РегистрационныйНомерФСС
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)";
	
	Результат = Запрос.ВыполнитьПакет();
	ЗапросДокументы = Результат[0].Выгрузить();
	Если ЗапросДокументы.Количество() = 0 Тогда // Документов нет
		ЗапросСотрудники = Результат[1].Выгрузить();
		Если ЗапросСотрудники.Количество() = 0 Тогда // Сотрудников нет
			ЗапросЮридическиеЛица = Результат[2].Выгрузить();
			Если ЗапросЮридическиеЛица.Количество() = 0 Тогда // Юридических лиц нет
				ЗапросФизическиеЛица = Результат[3].Выгрузить();
				Если ЗапросФизическиеЛица.Количество() = 0 Тогда // ИП нет
					Возврат;
				Иначе
					РегистрационныйНомерФСС = "";
					Для Каждого ОрганизацияФизическоеЛицо ИЗ ЗапросФизическиеЛица Цикл
						РегистрационныйНомерФСС = ОрганизацияФизическоеЛицо.РегистрационныйНомерФСС;
						Если ЗначениеЗаполнено(РегистрационныйНомерФСС) Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если НЕ ЗначениеЗаполнено(РегистрационныйНомерФСС) Тогда // Нет ИП с номером ФСС
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	МенеджерЗначения = Константы.ВедетсяУчетЗарплатыКадров.СоздатьМенеджерЗначения();
	МенеджерЗначения.Значение = Истина;
	МенеджерЗначения.ДополнительныеСвойства.Вставить("НастраиватьКадровыйУчетИПоддержкуСовместительства", Истина);
	МенеджерЗначения.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ВедетсяУчетДивидендов() Экспорт
	
	ВедетсяУчетДивидендов = Константы.ВестиУчетЮридическогоЛица.Получить();
	
	Возврат ВедетсяУчетДивидендов;
	
КонецФункции

Функция ВедетсяУчетРКиСН() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос= Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиЗарплатаКадры.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиЗарплатаКадры КАК НастройкиЗарплатаКадры
	|ГДЕ
	|	(НастройкиЗарплатаКадры.ПрименятьСевернуюНадбавку
	|			ИЛИ НастройкиЗарплатаКадры.ПрименятьРайонныйКоэффициент)";
	
	ВедетсяУчетРКиСН = Не Запрос.Выполнить().Пустой();
	
	Возврат ВедетсяУчетРКиСН;
	
КонецФункции

Процедура ВключитьИспользоватьКадровыйУчетПриВнешнейЗарплате() Экспорт
	
	Если Не Константы.ИспользоватьКадровыйУчетПриВнешнейЗарплате.Получить() Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ИспользоватьКадровыйУчетПриВнешнейЗарплате.Установить(Истина);
		ОбновитьПовторноИспользуемыеЗначения();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РолиПодсистемы

// Перечень ролей, предоставляющих доступ на просмотр данных подсистемы.
// 
// Возвращаемое значение:
//  Массив - содержит строки, имена ролей
//
Функция РолиПросмотраДанных() Экспорт
	
	Роли = Новый Массив;
	
	Роли.Добавить("БазовыеПраваЗарплатаКадры");
	Роли.Добавить("БазовыеПраваЗарплатаКадрыРасширенная");
	Роли.Добавить("БазовыеПраваОбразованийФизическихЛиц");
	Роли.Добавить("БазовыеПраваСемейныхПоложенийФизическихЛиц");
	
	Роли.Добавить("ЧтениеАнкетПерсУчета");
	Роли.Добавить("ЧтениеБухгалтерскогоУчетаЗарплаты");
	Роли.Добавить("ЧтениеВыплаченнойЗарплаты");
	Роли.Добавить("ЧтениеДанныхДляНачисленияЗарплаты");
	Роли.Добавить("ЧтениеДанныхСотрудников");
	Роли.Добавить("ЧтениеДанныхФизическихЛицЗарплатаКадры");
	Роли.Добавить("ЧтениеДепонированнойЗарплаты");
	Роли.Добавить("ЧтениеДокументовЗарплатаКадры");
	Роли.Добавить("ЧтениеДокументЭДОБЗК");
	Роли.Добавить("ЧтениеДокументовПоПрямымВыплатамПособийСоциальногоСтрахования");
	Роли.Добавить("ЧтениеДокументовУчетаНДФЛ");
	Роли.Добавить("ЧтениеЗарплатныхПроектов");
	Роли.Добавить("ЧтениеИсходящейСправкиОЗаработке");
	Роли.Добавить("ЧтениеЛицевыхСчетовПоЗарплатнымПроектам");
	Роли.Добавить("ЧтениеНалоговИВзносов");
	Роли.Добавить("ЧтениеНачисленнойЗарплаты");
	Роли.Добавить("ЧтениеПерсУчета");
	Роли.Добавить("ЧтениеДолжностей");
	Роли.Добавить("ЧтениеМедицинскихОрганизаций");
	Роли.Добавить("ЧтениеСЗВ_КОРР");
	Роли.Добавить("ЧтениеЖурналаДокументовОбменаСБанкамиПоЗарплатнымПроектам");
	Роли.Добавить("ЧтениеОбменаСБанкамиПоЗачислениюЗарплаты");
	Роли.Добавить("ЧтениеОбменаСБанкамиПоЛицевымСчетамЗарплатныхПроектов");
	Роли.Добавить("ЧтениеБанковскихКарт");
	Роли.Добавить("ЧтениеВедомостейНаВыплатуЗарплатыВнешних");
	Роли.Добавить("ЧтениеРеестровЭЛН");
	Роли.Добавить("ЧтениеЭЛН");
	Роли.Добавить("ПросмотрБанковскихКарт");
	Роли.Добавить("ЧтениеОценочныхОбязательствЗарплатаКадры");
	Роли.Добавить("ЧтениеОтраженияЗарплатыВБухучете");
	Роли.Добавить("ЧтениеНастроекОценочныхОбязательствЗарплатаКадры");
	Роли.Добавить("ЧтениеНастроекОбменаЗарплата3Бухгалтерия3");
	Роли.Добавить("ЧтениеДоговоров");
	Роли.Добавить("ЧтениеДоговоровЗарплатаКадры");
	Роли.Добавить("ЧтениеДанныхОТрудовойДеятельности");
	Роли.Добавить("ЧтениеИзвещенийФСС");
	Роли.Добавить("ЧтениеПВРНачисления");
	Роли.Добавить("ЧтениеОтправкиСотрудниковБронированияКомандировок");
	Роли.Добавить("ЧтениеСогласийНаПрисоединениеКЭДОБЗК");
	
	// Кабинет сотрудника
	Роли.Добавить("ЧтениеЗаявокКабинетСотрудника");
	Роли.Добавить("ЧтениеЖурналаЗаявокКабинетСотрудника");
	Роли.Добавить("ЧтениеДанныхДляОформленияНаРаботу");
	
	// Из БЗКР
	Роли.Добавить("ЧтениеВоинскогоУчета");
	Роли.Добавить("ЧтениеДанныхОбОбразованииФизическихЛиц");
	Роли.Добавить("ЧтениеПрофессийФизическихЛиц");
	Роли.Добавить("ЧтениеСводныхДанныхФизическихЛиц");
	Роли.Добавить("ЧтениеСемейныхПоложенийФизическихЛиц");
	
	// Статистика
	Роли.Добавить("ЧтениеРегистрацийВОрганеСтатистики");
	
	Возврат Роли;
	
КонецФункции

// Перечень ролей, предоставляющих доступ на редактирование данных подсистемы.
// 
// Возвращаемое значение:
//  Массив - содержит строки, имена ролей
//
Функция РолиРедактированияДанных() Экспорт

	Роли = Новый Массив;
	
	Роли.Добавить("БазовыеПраваЗарплатаКадры");
	Роли.Добавить("БазовыеПраваЗарплатаКадрыРасширенная");
	Роли.Добавить("БазовыеПраваОбразованийФизическихЛиц");
	Роли.Добавить("БазовыеПраваСемейныхПоложенийФизическихЛиц");
	
	Роли.Добавить("НастройкаКлассовУсловийТрудаПоДолжностям");// Право на Должности включено в роль ДобавлениеИзменениеДанныхБухгалтерии
	Роли.Добавить("НастройкаПерсУчета");// Данные, которые используются при сдаче отчетности перс. учета
	Роли.Добавить("ДобавлениеИзменениеАнкетПерсУчета");
	Роли.Добавить("ДобавлениеИзменениеБухгалтерскогоУчетаЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеВыплаченнойЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеДанныхДляНачисленияЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеДанныхСотрудников");
	Роли.Добавить("ДобавлениеИзменениеДанныхФизическихЛицЗарплатаКадры");
	Роли.Добавить("ДобавлениеИзменениеДепонированнойЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеДокументовЗарплатаКадры");
	Роли.Добавить("ДобавлениеИзменениеДокументЭДОБЗК");
	Роли.Добавить("ДобавлениеИзменениеДокументовПоПрямымВыплатамПособийСоциальногоСтрахования");
	Роли.Добавить("ДобавлениеИзменениеДокументовУчетаНДФЛ");
	Роли.Добавить("ДобавлениеИзменениеЗарплатныхПроектов");
	Роли.Добавить("ДобавлениеИзменениеИсходящейСправкиОЗаработке");
	Роли.Добавить("ДобавлениеИзменениеЛицевыхСчетовПоЗарплатнымПроектам");
	Роли.Добавить("ДобавлениеИзменениеНалоговИВзносов");
	Роли.Добавить("ДобавлениеИзменениеНачисленнойЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеПерсУчета");
	Роли.Добавить("ДобавлениеИзменениеДолжностей");
	Роли.Добавить("ДобавлениеИзменениеЗначенийСтроковыхРеквизитов");
	Роли.Добавить("ДобавлениеИзменениеСЗВ_КОРР");
	Роли.Добавить("ДобавлениеИзменениеМедицинскихОрганизаций");
	Роли.Добавить("ВыполнениеОбменаСБанкамиПоЗарплатнымПроектам");
	Роли.Добавить("ДобавлениеИзменениеОбменаСБанкамиПоЗачислениюЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеОбменаСБанкамиПоЛицевымСчетамЗарплатныхПроектов");
	Роли.Добавить("ИспользованиеОбработкиОбменСБанкамиПоЗарплатнымПроектам");
	Роли.Добавить("ЧтениеЖурналаДокументовОбменаСБанкамиПоЗарплатнымПроектам");
	Роли.Добавить("ДобавлениеИзменениеБанковскихКарт");
	Роли.Добавить("ДобавлениеИзменениеВедомостейНаВыплатуЗарплатыВнешних");
	Роли.Добавить("ДобавлениеИзменениеРеестровЭЛН");
	Роли.Добавить("ДобавлениеИзменениеЭЛН");
	Роли.Добавить("ПросмотрБанковскихКарт");
	Роли.Добавить("ДобавлениеИзменениеОценочныхОбязательствЗарплатаКадры");
	Роли.Добавить("ДобавлениеИзменениеОтраженияЗарплатыВБухучете");
	Роли.Добавить("ДобавлениеИзменениеНастроекОценочныхОбязательствЗарплатаКадры");
	Роли.Добавить("ЧтениеНастроекОбменаЗарплата3Бухгалтерия3");
	Роли.Добавить("ДобавлениеИзменениеДоговоров");
	Роли.Добавить("ДобавлениеИзменениеДоговоровЗарплатаКадры");
	Роли.Добавить("ДобавлениеИзменениеДанныхОТрудовойДеятельности");
	Роли.Добавить("ПубликацияРасчетныхЛистков");
	Роли.Добавить("ДобавлениеИзменениеИзвещенийФСС");
	Роли.Добавить("ИспользованиеОбработкиОбщиеФормыСЭДОФСС");
	Роли.Добавить("ЧтениеПВРНачисления");
	Роли.Добавить("ДобавлениеИзменениеОтправкиСотрудниковБронированияКомандировок");
	Роли.Добавить("ДобавлениеИзменениеСогласийНаПрисоединениеКЭДОБЗК");
	
	// Кабинет сотрудника
	Роли.Добавить("ДобавлениеИзменениеЗаявокСотрудника");
	Роли.Добавить("ДобавлениеИзменениеЗаданий");
	Роли.Добавить("ДобавлениеИзменениеЗаявокКабинетСотрудника");
	Роли.Добавить("ЧтениеЖурналаЗаявокКабинетСотрудника");
	Роли.Добавить("ДобавлениеИзменениеДанныхДляОформленияНаРаботу");
	
	// Управление персоналом
	Роли.Добавить("НастройкаИнтеграцияУправлениеПерсоналом");
	
	// Из БЗКР
	Роли.Добавить("ЧтениеВоинскогоУчета");
	Роли.Добавить("ДобавлениеИзменениеВоинскогоУчета");
	Роли.Добавить("ДобавлениеИзменениеДанныхОбОбразованииФизическихЛиц");
	Роли.Добавить("ДобавлениеИзменениеПрофессийФизическихЛиц");
	Роли.Добавить("ДобавлениеИзменениеСводныхДанныхФизическихЛиц");
	Роли.Добавить("ДобавлениеИзменениеСемейныхПоложенийФизическихЛиц");
	
	// Статистика
	Роли.Добавить("ЧтениеРегистрацийВОрганеСтатистики");
	
	Возврат Роли;
	
КонецФункции

// Перечень ролей, предоставляющих доступ к настройке подсистемы.
// 
// Возвращаемое значение:
//  Массив - содержит строки, имена ролей
//
Функция РолиНастройки() Экспорт
	
	Роли = Новый Массив;
	
	Роли.Добавить("НастройкаБухгалтерскогоУчетаЗарплаты");
	Роли.Добавить("НастройкаКадровыхДанных");
	Роли.Добавить("НастройкаНалогиИВзносы");
	Роли.Добавить("НастройкаНачисленияЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеОснованийПолномочийОтветственныхЛиц");
	Роли.Добавить("НастройкаДанныхДляНачисленияЗарплаты");
	Роли.Добавить("ДобавлениеИзменениеПВРНачисления");
	Роли.Добавить("ИспользованиеОбработкиНастройкаОтправкиСотрудниковБронированияКомандировок");
	Роли.Добавить("ЗащитаПерсональныхДанных");
	
	// Кабинет сотрудника
	Роли.Добавить("НастройкаПравилОбработкиЗаявокСотрудников");
	Роли.Добавить("НастройкаСервисаКабинетСотрудника");
	Роли.Добавить("НастройкаСервисаКабинетСотрудникаБазовая");
	Роли.Добавить("НастройкаПечатныхФормЗарплатаКадры");
	Роли.Добавить("НастройкаШаблоновДокументов");
	
	// КЭДО
	Роли.Добавить("НастройкаПриложения1СПерсонал");
	
	// Из БЗКР
	Роли.Добавить("НастройкаВоинскогоУчета");
	Роли.Добавить("НастройкаКадровыхДанныхРасширенная");
	Роли.Добавить("НастройкаКадровыхДанныхОбразованийФизическихЛиц");
	
	// Статистика
	Роли.Добавить("ДобавлениеИзменениеРегистрацийВОрганеСтатистики");
	
	Возврат Роли;
	
КонецФункции

#КонецОбласти

#Область КадровыеДанные

Функция ПолучитьФизическоеЛицо(Объект) Экспорт
	
	ТипОбъекта = ТипЗнч(Объект);
	Если ТипОбъекта = Тип("СправочникСсылка.ФизическиеЛица") или ТипОбъекта = Тип("СправочникОбъект.ФизическиеЛица") Тогда
		ФизическоеЛицо = Объект;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Сотрудники") или ТипОбъекта = Тип("СправочникОбъект.Сотрудники") Тогда
		ФизическоеЛицо = Объект.ФизическоеЛицо;
	Иначе
		ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ФизическоеЛицо;
	
КонецФункции

// Возвращает представление физического лица.
// Может выполнятся под ограниченными правами.
//
// Параметры:
//  ФизическоеЛицо - Справочник.ФизическиеЛица - Физическое лицо, по которому следует получить представление
//  ДатаСреза - Дата - Дата на которую получаются данные по физическому лицу
//
// Возвращаемое значение:
//   Структура - Структура с представлением физического лица, с ключами:
// * Фамилия - Строка
// * Имя - Строка
// * Отчество - Строка
// * ФамилияИО - Строка
// * ФИОПолные - Строка
//
Функция ПредставлениеФизическогоЛица(ФизическоеЛицо, ДатаСреза) Экспорт
	
	СписокКадровыхДанных = "Фамилия,Имя,Отчество,ФамилияИО,ФИОПолные";
	
	КадровыеДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Истина,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо),
		СписокКадровыхДанных,
		ДатаСреза);
	
	Результат = Новый Структура(СписокКадровыхДанных);
	
	КадровыеДанные = КадровыеДанныеФизЛиц.Найти(ФизическоеЛицо, "ФизическоеЛицо");
	Если КадровыеДанные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Результат, КадровыеДанные);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает должность физического лица.
// 
// Параметры:
//  ФизическоеЛицо - Справочник.ФизическиеЛица - Физическое лицо, по которому следует получить представление
//  Организация - Справочник.Организации - Организация, в которой работает сотрудник, связанный с физическим лицом
//  ДатаСреза - Дата - Дата на которую получаются данные по физическому лицу
//
// Возвращаемое значение:
//   Должность - Справочник.Должности
//
Функция ДолжностьФизическогоЛица(ФизическоеЛицо, Организация, ДатаСреза) Экспорт
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организация);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "Должность,Организация");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Истина);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоДня(ДатаСреза));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецДня(ДатаСреза));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("СписокФизическихЛиц",           ФизическоеЛицо);
	
	УстановитьПривилегированныйРежим(Истина);
	// Правами доступа нельзя предоставить доступ только к части кадровых данных (должности).
	// Поэтому обращение к кадровым данным выполняется привилегированно -
	// считаем, что данные о должности физического лица в организации не являются секретными.
	// Из всех прочитанных кадровых данных функция вернет только должность физического лица
	КадровыеДанные = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если КадровыеДанные.Количество() = 0 Тогда
		Должность = Справочники.Должности.ПустаяСсылка();
	Иначе
		НайденныеСтроки = КадровыеДанные.НайтиСтроки(Новый Структура("Организация", Организация));
		Если НайденныеСтроки.Количество() <> 0 Тогда
			Должность = НайденныеСтроки[0].Должность;
		Иначе
			Должность = КадровыеДанные[0].Должность;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Должность;
	
КонецФункции

Функция ДанныеФизическихЛиц(Организация = Неопределено, ФизическиеЛица, ДатаСреза, ФИОКратко = Истина, РезультатТаблично = Ложь) Экспорт
	
	СписокФизическихЛиц = Новый Массив;
	Если ТипЗнч(ФизическиеЛица) = Тип("Массив") Тогда
		Для Каждого ФизическоеЛицо ИЗ ФизическиеЛица Цикл
			Если СписокФизическихЛиц.Найти(ФизическоеЛицо) = Неопределено Тогда
				СписокФизическихЛиц.Добавить(ФизическоеЛицо);
			КонецЕсли;
		КонецЦикла;
	Иначе
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическиеЛица);
	КонецЕсли;
	
	Если НЕ РезультатТаблично И СписокФизическихЛиц.Количество() > 1 Тогда
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокФизическихЛиц[0]);
	КонецЕсли;
	
	СписокПерсональныхДанных = "Фамилия,Имя,Отчество,Пол,ДатаРождения,МестоРождения,Страна,ФамилияИО,СтраховойНомерПФР,ИНН,ФИОПолные,ДокументВид,ДокументСерия,ДокументНомер,ДокументДатаВыдачи,ДокументКемВыдан,ДокументКодПодразделения,ДокументПредставление";
	СписокКадровыхДанных = "ТабельныйНомер,Подразделение,Должность,ДатаПриема,ОсновноеРабочееМестоВОрганизации";
	Если Организация <> Неопределено Тогда
		СписокДанныхСотрудника = СписокПерсональныхДанных + "," + СписокКадровыхДанных ;
	Иначе
		СписокДанныхСотрудника = СписокПерсональныхДанных;
	КонецЕсли;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организация);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                СписокДанныхСотрудника);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоДня(ДатаСреза));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецДня(ДатаСреза));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("СписокФизическихЛиц",           СписокФизическихЛиц);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("РаботникиПоДоговорамГПХ",       Истина);
	
	КадровыеДанныеФизЛиц = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	НепринятыеФизическиеЛица = Новый Массив;
	Для Каждого ФизическоеЛицо ИЗ СписокФизическихЛиц Цикл
		Если НЕ ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			Продолжить;
		КонецЕсли;
		Если КадровыеДанныеФизЛиц.Найти(ФизическоеЛицо, "ФизическоеЛицо") = Неопределено Тогда
			НепринятыеФизическиеЛица.Добавить(ФизическоеЛицо);
		КонецЕсли;
	КонецЦикла;
	
	Если НепринятыеФизическиеЛица.Количество() <> 0 Тогда
		ПерсональныеДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, НепринятыеФизическиеЛица, СписокПерсональныхДанных, ДатаСреза);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ПерсональныеДанныеФизЛиц, КадровыеДанныеФизЛиц);
	КонецЕсли;
	
	Если РезультатТаблично Тогда
		
		ТабРезультат = Новый ТаблицаЗначений();
		// Сведения физ.лица
		ТабРезультат.Колонки.Добавить("ФизическоеЛицо",                   Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		ТабРезультат.Колонки.Добавить("Фамилия",                          Метаданные.РегистрыСведений.ФИОФизическихЛиц.Ресурсы.Фамилия.Тип);
		ТабРезультат.Колонки.Добавить("Имя",                              Метаданные.РегистрыСведений.ФИОФизическихЛиц.Ресурсы.Имя.Тип);
		ТабРезультат.Колонки.Добавить("Отчество",                         Метаданные.РегистрыСведений.ФИОФизическихЛиц.Ресурсы.Отчество.Тип);
		ТабРезультат.Колонки.Добавить("Пол",                              Новый ОписаниеТипов("ПеречислениеСсылка.ПолФизическогоЛица"));
		ТабРезультат.Колонки.Добавить("ДатаРождения",                     Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		ТабРезультат.Колонки.Добавить("МестоРождения",                    Метаданные.Справочники.ФизическиеЛица.Реквизиты.МестоРождения.Тип);
		ТабРезультат.Колонки.Добавить("Страна",                           Новый ОписаниеТипов("СправочникСсылка.СтраныМира"));
		ТабРезультат.Колонки.Добавить("Представление",                    Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(250)));
		ТабРезультат.Колонки.Добавить("ИНН",                              Метаданные.Справочники.ФизическиеЛица.Реквизиты.ИНН.Тип);
		ТабРезультат.Колонки.Добавить("СтраховойНомерПФР",                Метаданные.Справочники.ФизическиеЛица.Реквизиты.СтраховойНомерПФР.Тип);
		ТабРезультат.Колонки.Добавить("ВидДокумента",                     Новый ОписаниеТипов("СправочникСсылка.ВидыДокументовФизическихЛиц"));
		ТабРезультат.Колонки.Добавить("Серия",                            Метаданные.РегистрыСведений.ДокументыФизическихЛиц.Ресурсы.Серия.Тип);
		ТабРезультат.Колонки.Добавить("Номер",                            Метаданные.РегистрыСведений.ДокументыФизическихЛиц.Ресурсы.Номер.Тип);
		ТабРезультат.Колонки.Добавить("ДатаВыдачи",                       Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		ТабРезультат.Колонки.Добавить("КемВыдан",                         Метаданные.РегистрыСведений.ДокументыФизическихЛиц.Ресурсы.КемВыдан.Тип);
		ТабРезультат.Колонки.Добавить("КодПодразделения",                 Метаданные.РегистрыСведений.ДокументыФизическихЛиц.Ресурсы.КодПодразделения.Тип);
		ТабРезультат.Колонки.Добавить("ПредставлениеДокумента",           Метаданные.РегистрыСведений.ДокументыФизическихЛиц.Ресурсы.Представление.Тип);
		// Сведения сотрудника
		ТабРезультат.Колонки.Добавить("ТабельныйНомер",                   Метаданные.Справочники.Сотрудники.СтандартныеРеквизиты.Код.Тип);
		ТабРезультат.Колонки.Добавить("Должность",                        Новый ОписаниеТипов("СправочникСсылка.Должности"));
		ТабРезультат.Колонки.Добавить("ПодразделениеОрганизации",         Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
		ТабРезультат.Колонки.Добавить("Сотрудник",                        Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ТабРезультат.Колонки.Добавить("ДатаПриема",                       Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		ТабРезультат.Колонки.Добавить("ОсновноеРабочееМестоВОрганизации", Новый ОписаниеТипов("Булево"));
		
	Иначе
		
		Результат = Новый Структура("Фамилия, Имя, Отчество, Пол, ДатаРождения, МестоРождения, Страна, Представление, ИНН, СтраховойНомерПФР, ТабельныйНомер, Должность, ПодразделениеОрганизации, ВидДокумента, Серия, Номер, ДатаВыдачи, КемВыдан, КодПодразделения, ПредставлениеДокумента, Сотрудник, ДатаПриема, ОсновноеРабочееМестоВОрганизации");
		
	КонецЕсли;
	
	ТолькоПерсональныеДанные = КадровыеДанныеФизЛиц.Колонки.Найти("ТабельныйНомер") = Неопределено;
	
	Для Каждого ФизическоеЛицо ИЗ СписокФизическихЛиц Цикл
		
		Если ТолькоПерсональныеДанные Тогда
			КадровыеДанныеФизЛица = КадровыеДанныеФизЛиц.НайтиСтроки(Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
		Иначе
			КадровыеДанныеФизЛица = КадровыеДанныеФизЛиц.НайтиСтроки(Новый Структура("ФизическоеЛицо, ОсновноеРабочееМестоВОрганизации", ФизическоеЛицо, Истина));
			Если КадровыеДанныеФизЛица.Количество() = 0 Тогда
				КадровыеДанныеФизЛица = КадровыеДанныеФизЛиц.НайтиСтроки(Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
			КонецЕсли;
		КонецЕсли;
		Если КадровыеДанныеФизЛица.Количество() = 0 Тогда
			ДанныеФизЛиц = КадровыеДанныеФизЛиц.Добавить();
			ДанныеФизЛиц.ФизическоеЛицо = ФизическоеЛицо;
		Иначе
			ДанныеФизЛиц = КадровыеДанныеФизЛица[0];
		КонецЕсли;
		
		Если РезультатТаблично Тогда
			Результат = ТабРезультат.Добавить();
			Результат.ФизическоеЛицо= ДанныеФизЛиц.ФизическоеЛицо;
		КонецЕсли;
		
		Результат.Фамилия       = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Фамилия, "");
		Результат.Имя           = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Имя, "");
		Результат.Отчество      = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Отчество, "");
		Результат.Пол           = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Пол, Перечисления.ПолФизическогоЛица.ПустаяСсылка());
		Результат.ДатаРождения  = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДатаРождения, Дата(1, 1, 1));
		Результат.МестоРождения = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.МестоРождения, "");
		Результат.Страна        = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Страна, Справочники.СтраныМира.Россия);
		Если ФИОКратко Тогда
			Результат.Представление = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ФамилияИО, "");
		Иначе
			Результат.Представление = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ФИОПолные, "");
		КонецЕсли;
		Результат.ВидДокумента             = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДокументВид, "");
		Результат.Серия                    = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДокументСерия, "");
		Результат.Номер                    = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДокументНомер, "");
		Результат.ДатаВыдачи               = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДокументДатаВыдачи, "");
		Результат.КемВыдан                 = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДокументКемВыдан, "");
		Результат.КодПодразделения         = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДокументКодПодразделения, "");
		Результат.ПредставлениеДокумента   = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДокументПредставление, "");
		Результат.ИНН                      = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ИНН, "");
		Результат.СтраховойНомерПФР        = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.СтраховойНомерПФР, "");
		Если Не ТолькоПерсональныеДанные Тогда
			Результат.ТабельныйНомер                   = ПрефиксацияОбъектовКлиентСервер.УдалитьПрефиксыИзНомераОбъекта(ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ТабельныйНомер, ""), Истина, Истина);
			Результат.Должность                        = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Должность, "");
			Результат.ПодразделениеОрганизации         = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Подразделение, "");
			Результат.Сотрудник                        = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.Сотрудник, "");
			Результат.ДатаПриема                       = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ДатаПриема, "");
			Результат.ОсновноеРабочееМестоВОрганизации = ОбщегоНазначенияБП.ЕстьNull(ДанныеФизЛиц.ОсновноеРабочееМестоВОрганизации, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Если РезультатТаблично Тогда
		Возврат ТабРезультат;
	Иначе
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

Процедура ПолучитьТаблицуСотрудников(ТаблицаФизическихЛиц) Экспорт
	
	ТаблицаФизическихЛиц.Колонки.Добавить("Сотрудник",                            Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаФизическихЛиц.Колонки.Добавить("СотрудникКод",                         Новый ОписаниеТипов("Строка"));
	ТаблицаФизическихЛиц.Колонки.Добавить("ПодразделениеСотрудника",              Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаФизическихЛиц.Колонки.Добавить("ПодразделениеСотрудникаКод",           Новый ОписаниеТипов("Строка"));
	ТаблицаФизическихЛиц.Колонки.Добавить("ПредставлениеПодразделенияСотрудника", Новый ОписаниеТипов("Строка"));
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаФизическихЛиц Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ФизическоеЛицо) Тогда
			Продолжить;
		КонецЕсли;
		ДанныеСотрудника = ДанныеФизическихЛиц(СтрокаТаблицы.Организация, СтрокаТаблицы.ФизическоеЛицо, СтрокаТаблицы.Дата);
		Подразделение = ДанныеСотрудника.ПодразделениеОрганизации;
		СтрокаТаблицы.ПодразделениеСотрудника = ДанныеСотрудника.ПодразделениеОрганизации;
		СтрокаТаблицы.СотрудникКод            = ДанныеСотрудника.ТабельныйНомер;
		Если ЗначениеЗаполнено(Подразделение) Тогда
			СтрокаТаблицы.ПодразделениеСотрудникаКод = Подразделение.Код;
			Если ЗначениеЗаполнено(Подразделение.НаименованиеПолное) Тогда
				СтрокаТаблицы.ПредставлениеПодразделенияСотрудника = Подразделение.НаименованиеПолное;
			Иначе
				СтрокаТаблицы.ПредставлениеПодразделенияСотрудника = Подразделение.Наименование;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла
		
КонецПроцедуры

Функция ФизическиеЛицаРаботавшиеВОрганизации(ТолькоРазрешенные, Организация, НачалоПериода, ОкончаниеПериода, Подразделение = Неопределено) Экспорт
	
	Возврат КадровыйУчет.ФизическиеЛицаРаботавшиеВОрганизации(ТолькоРазрешенные, Организация, НачалоПериода, ОкончаниеПериода, Подразделение);
	
КонецФункции

// Возвращает признак необходимости начислять зарплату
// для данной организации в указанный период
//
// Параметры:
//		Организация - СправочникСсылка.Организации
//		Период      - Дата
//
// Возвращаемое значение:
//		Булево
//
Функция НеобходимоНачислятьЗарплату(Организация, Период) Экспорт
	
	ЭтоФизЛицо	= Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	УчетЗарплатыИКадровСредствамиБухгалтерии = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	Если НЕ УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
		Возврат Ложь;
	ИначеЕсли ЭтоФизЛицо И НЕ ИПИспользуетТрудНаемныхРаботников(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организация);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 ?(ЭтоФизЛицо, НачалоМесяца(Период), '00010101'));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецМесяца(Период));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("РаботникиПоДоговорамГПХ",       Истина);
	
	ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	Если НЕ ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда
		// При отсутствии регулярной деятельности
		// игнорируем сотрудников в отпуске без содержания
		ТаблицаСотрудников = АктивныеСотрудники(ТаблицаСотрудников, Период);
	КонецЕсли;
	
	Возврат ТаблицаСотрудников.Количество() > 0;
	
КонецФункции

Функция ИПИспользуетТрудНаемныхРаботников(Организация, ПроверятьФункциональность = Истина) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если ПроверятьФункциональность Тогда
		Если Не ТарификацияБП.РазрешенУчетЗарплаты() Тогда
			Возврат Ложь;
		КонецЕсли;
		Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации")
		И НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		ОрганизацияСсылка = Организация;
	ИначеЕсли  ТипЗнч(Организация) = Тип("СправочникОбъект.Организации")
		И Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ОрганизацияСсылка = Организация.Ссылка;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	// Дальнейшая проверка только для индивидуального предпринимателя
	
	// при наличии регистрации в ФСС можно судить о наличии работников
	РегистрационныйНомерФСС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОрганизацияСсылка, "РегистрационныйНомерФСС");
	Если ЗначениеЗаполнено(РегистрационныйНомерФСС) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// не заполняли реквизиты, а сотрудников ввели
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОрганизацияСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Сотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		Возврат Истина;
	КонецЕсли;
	
	// сотрудников вообще не вводили, но возможно есть обмен, а в нем используются физлица
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		
		//Проверям настройку обмена - он либо настроен по организации либо по всем (в этом случае считаем что зарплата есть).
		ИспользуетсяОбмен = Ложь;
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиЗарплата3Бухгалтерия3");
			ИспользуетсяОбмен = Модуль.ОбменИспользуется(ОрганизацияСсылка);
		КонецЕсли;
		Если Не ИспользуетсяОбмен Тогда
			ИспользуетсяОбмен = ИспользуетсяОбменЗУПБП(ОрганизацияСсылка);
		КонецЕсли;
		Если ИспользуетсяОбмен Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИПИспользуетТрудНаемныхРаботниковЗаПериод(Организация, Знач НачалоПериода, Знач КонецПериода) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации")
		И НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		ОрганизацияСсылка = Организация;
	ИначеЕсли  ТипЗнч(Организация) = Тип("СправочникОбъект.Организации")
		И Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ОрганизацияСсылка = Организация.Ссылка;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	// Дальнейшая проверка только для индивидуального предпринимателя
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// В заданном периоде были сотрудники (актуально только для внутреннего учета)
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   ОрганизацияСсылка);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "");
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоПериода);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецПериода);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("РаботникиПоДоговорамГПХ",       Истина);
		
		ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
		
		Если ТаблицаСотрудников.Количество() <> 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Проверка, что в заданном периоде были зарегистрированы начисления: зарплата, ГПХ, взносы, НДФЛ
	// При этом ставим "отсечку" - не ранее даты ввода начальных остатов
	ПроверятьРасчеты = Истина;
	ДатаНачалаУчета = РегистрыСведений.ДатыВводаНачальныхОстатков.ДатаВводаНачальныхОстатков(ОрганизацияСсылка);
	Если ЗначениеЗаполнено(ДатаНачалаУчета) Тогда
		// Если есть дата ввода начальных остатков, то "глубже" этой даты анализировать не будем.
		Если ДатаНачалаУчета >= КонецПериода Тогда
			ПроверятьРасчеты = Ложь;
		ИначеЕсли ДатаНачалаУчета >= НачалоПериода Тогда
			// Дата ввода остатков это предыдущий день начала учета, поэтому сдвигаем на сутки
			НачалоПериода = ДатаНачалаУчета + 86400;
		КонецЕсли;
	КонецЕсли;
	
	Если ПроверятьРасчеты Тогда
		ЗарегистрированыРасчетыОрганизацииРаботодателя = ЗарегистрированыРасчетыОрганизацииРаботодателя(ОрганизацияСсылка, НачалоПериода, КонецПериода);
		Возврат ЗарегистрированыРасчетыОрганизацииРаботодателя;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ЗарегистрированыРасчетыОрганизацииРаботодателя(Организация, НачалоПериода, КонецПериода)
	
	// В заданном периоде были начисления (зарплата, ГПХ, взносы)
	СчетаРасчетов = СчетаРасчетовПоЗарплате();
	
	СчетаВзносов = СчетаВзносов();
	
	// Необходимо исключить обороты операций, не являющихся начислением взносов:
	//   - возврат из фондов;
	//   - корректировки;
	//   - техническое операции:
	//       - ввод остатков;
	//       - перенос на ЕНС...
	//  и т.п.
	КорСчетаПрочихОпераций = СчетаПрочихОпераций();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("НачалоПериода",          НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",           КонецПериода);
	Запрос.УстановитьПараметр("СчетаРасчетов",          СчетаРасчетов);
	Запрос.УстановитьПараметр("СчетаВзносов",           СчетаВзносов);
	Запрос.УстановитьПараметр("КорСчетаПрочихОпераций", КорСчетаПрочихОпераций);
	Запрос.УстановитьПараметр("ВидыПлатежей",           Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХозрасчетныйОбороты.Организация КАК Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			Счет В (&СчетаВзносов),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосбюджет),
	|			Организация = &Организация
	|				И Субконто1 В (&ВидыПлатежей),
	|			НЕ КорСчет В (&КорСчетаПрочихОпераций),
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотКт <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Период, Счет В (&СчетаРасчетов), , Организация = &Организация, , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотКт <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СведенияОбУдержанномНДФЛ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
	|ГДЕ
	|	СведенияОбУдержанномНДФЛ.Организация = &Организация
	|	И СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СведенияОбУдержанномНДФЛ.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтражениеЗарплатыВБухучете.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВБухучете КАК ОтражениеЗарплатыВБухучете
	|ГДЕ
	|	ОтражениеЗарплатыВБухучете.Организация = &Организация
	|	И ОтражениеЗарплатыВБухучете.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ОтражениеЗарплатыВБухучете.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтражениеЗарплатыВУчете.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВУчете КАК ОтражениеЗарплатыВУчете
	|ГДЕ
	|	ОтражениеЗарплатыВУчете.Организация = &Организация
	|	И ОтражениеЗарплатыВУчете.ПериодРегистрации МЕЖДУ &НачалоПериода И &НачалоПериода
	|	И ОтражениеЗарплатыВУчете.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СведенияОбУдержанномНДФЛНДФЛ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
	|ГДЕ
	|	СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Организация = &Организация
	|	И (СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания МЕЖДУ &НачалоПериода И &КонецПериода)
	|	И СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Проведен";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ЗарегистрированыРасчетыОрганизацииРаботодателя = Ложь;
	Для Каждого РезультатЗапроса Из Результат Цикл
		Если Не РезультатЗапроса.Пустой() Тогда
			ЗарегистрированыРасчетыОрганизацииРаботодателя = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗарегистрированыРасчетыОрганизацииРаботодателя;
	
КонецФункции

Функция ПолучитьДокументНачисленияЗарплаты(МесяцНачисления, Организация, СообщениеОбОшибке = "", ПерваяПоловинаМесяца = Ложь) Экспорт
	
	ДокументНачисленияЗарплаты = Неопределено;
	
	РезультатПроверки = РезультатПроверкиУсловийТарификацииПоКоличествуСотрудников(Организация);
	
	Если Не РезультатПроверки.УсловияВыполнены Тогда
		// В случае несоблюдения условий тарифа по допустимому количеству сотрудников отключается автоматическое
		// заполнение документа начисления зарплаты
		ДокументНачисленияЗарплаты = НайтиДокументНачисленияЗарплаты(МесяцНачисления, Организация, ПерваяПоловинаМесяца);
	Иначе
		Попытка
			ДокументНачисленияЗарплаты = РасчетЗарплаты.ДокументНачисленияЗарплаты(МесяцНачисления, Организация, , ПерваяПоловинаМесяца);
		Исключение
			ШаблонСообщения = НСтр("ru = 'Не удалось записать документ'");		
			ОписаниеОшибки = ИнформацияОбОшибке();
			СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ОписаниеОшибки);
			ЗаписьЖурналаРегистрации(ШаблонСообщения, УровеньЖурналаРегистрации.Ошибка,,, СообщениеОбОшибке);
		КонецПопытки;
	КонецЕсли;
	
	Возврат ДокументНачисленияЗарплаты;
	
КонецФункции

Функция ЕстьДокументНачисленияЗарплаты(МесяцНачисления, Организация) Экспорт

	ДокументНачисления = НайтиДокументНачисленияЗарплаты(МесяцНачисления, Организация);

	Возврат ЗначениеЗаполнено(ДокументНачисления);

КонецФункции

Функция ПолучитьДокументНачисленияРезерваОтпусков(МесяцНачисления, Организация) Экспорт
	
	ДокументНачисленияРезерваОтпусков = Неопределено;
	Попытка
		ДополнительныеСвойства = Новый Структура("ПроведенВХронологическойПоследовательности", Истина);
		Если ЕстьНастройкиРезерваОтпусков(Организация, МесяцНачисления) Тогда
			ДокументНачисленияРезерваОтпусков = РезервыПоОплатеТрудаДляНебольшихОрганизаций.ДокументРезервыПоОплатеТруда(МесяцНачисления,
				Организация,
				Перечисления.ВидыРезервовПоОплатеТруда.Отпуск,
				Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление,
				ДополнительныеСвойства);
		Иначе
			ДокументНачисленияРезерваОтпусков = РезервОтпусков.ДокументНачислениеОценочныхОбязательствПоОтпускам(МесяцНачисления, Организация, ДополнительныеСвойства);
		КонецЕсли;
	Исключение
		ШаблонСообщения = НСтр("ru = 'Не удалось записать документ'");
		ОписаниеОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(ШаблонСообщения, УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
	КонецПопытки;
	
	Возврат ДокументНачисленияРезерваОтпусков;
	
КонецФункции

Функция ПолучитьДокументИнвентаризацииРезерваОтпусков(МесяцНачисления, Организация) Экспорт
	
	ДокументНачисленияРезерваОтпусков = Неопределено;
	Попытка
		ДополнительныеСвойства = Новый Структура("ПроведенВХронологическойПоследовательности", Истина);
		Если ЕстьНастройкиРезерваОтпусков(Организация, МесяцНачисления) Тогда
			ДокументНачисленияРезерваОтпусков = РезервыПоОплатеТрудаДляНебольшихОрганизаций.ДокументРезервыПоОплатеТруда(МесяцНачисления,
				Организация,
				Перечисления.ВидыРезервовПоОплатеТруда.Отпуск,
				Перечисления.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация,
				ДополнительныеСвойства);
		КонецЕсли;
	Исключение
		ШаблонСообщения = НСтр("ru = 'Не удалось записать документ'");		
		ОписаниеОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(ШаблонСообщения, УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
	КонецПопытки;
	
	Возврат ДокументНачисленияРезерваОтпусков;
	
КонецФункции

Функция ПолучитьДокументНачисленияРезерваПремий(МесяцНачисления, Организация) Экспорт
	
	ДокументНачисленияРезерваПремий = Неопределено;
	Попытка
		ДокументНачисленияРезерваПремий = РезервыПоОплатеТрудаДляНебольшихОрганизаций.ДокументРезервыПоОплатеТруда(МесяцНачисления,
			Организация,
			Перечисления.ВидыРезервовПоОплатеТруда.ГодоваяПремия,
			Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление);
	Исключение
		ШаблонСообщения = НСтр("ru = 'Не удалось записать документ'");		
		ОписаниеОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(ШаблонСообщения, УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
	КонецПопытки;
	
	Возврат ДокументНачисленияРезерваПремий;
	
КонецФункции

Функция ПолучитьДокументИнвентаризацииРезерваПремий(МесяцНачисления, Организация) Экспорт
	
	ДокументНачисленияРезерваПремий = Неопределено;
	Попытка
		ДокументНачисленияРезерваПремий = РезервыПоОплатеТрудаДляНебольшихОрганизаций.ДокументРезервыПоОплатеТруда(МесяцНачисления,
			Организация,
			Перечисления.ВидыРезервовПоОплатеТруда.ГодоваяПремия,
			Перечисления.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация);
	Исключение
		ШаблонСообщения = НСтр("ru = 'Не удалось записать документ'");		
		ОписаниеОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(ШаблонСообщения, УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
	КонецПопытки;
	
	Возврат ДокументНачисленияРезерваПремий;
	
КонецФункции

Процедура СформироватьДокументыНачисленияРезервовПоОплатеТруда(МесяцНачисления, Организация) Экспорт
	
	ПараметрыОбработки = Обработки.ПомощникНачисленияРезервовПоОплатеТруда.ПараметрыФормированияДокументов();
	ПараметрыОбработки.Вставить("Организация",                 Организация);
	ПараметрыОбработки.Вставить("МесяцНачисления",             МесяцНачисления);
	ПараметрыОбработки.Вставить("ВедетсяУчетРезервовОтпусков", ВедетсяУчетРезервовОтпусков(Организация, МесяцНачисления));
	ПараметрыОбработки.Вставить("ВедетсяУчетРезервовПремий",   ВедетсяУчетРезервовПремий(Организация, МесяцНачисления));
	Обработки.ПомощникНачисленияРезервовПоОплатеТруда.СформироватьДокументы(ПараметрыОбработки);
	
КонецПроцедуры

Функция ЕстьДокументНачисленияРезервовОтпусков(МесяцНачисления, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("МесяцНачисления", 	НачалоМесяца(МесяцНачисления));
	Запрос.УстановитьПараметр("Резерв", 			РезервОтпусков(МесяцНачисления, Организация));
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НачислениеОценочныхОбязательствПоОтпускам.Ссылка КАК Ссылка,
	|	НачислениеОценочныхОбязательствПоОтпускам.Дата КАК Дата
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК НачислениеОценочныхОбязательствПоОтпускам
	|ГДЕ
	|	НачислениеОценочныхОбязательствПоОтпускам.ПериодРегистрации = &МесяцНачисления
	|	И НачислениеОценочныхОбязательствПоОтпускам.Организация = &Организация
	|	И НачислениеОценочныхОбязательствПоОтпускам.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РезервыПоОплатеТруда.Ссылка,
	|	РезервыПоОплатеТруда.Дата
	|ИЗ
	|	Документ.РезервыПоОплатеТруда КАК РезервыПоОплатеТруда
	|ГДЕ
	|	РезервыПоОплатеТруда.МесяцНачисления = &МесяцНачисления
	|	И РезервыПоОплатеТруда.Организация = &Организация
	|	И РезервыПоОплатеТруда.Проведен
	|	И РезервыПоОплатеТруда.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
	|	И РезервыПоОплатеТруда.Резерв = &Резерв
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";

	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

Функция ЕстьДокументНачисленияРезервовПремий(МесяцНачисления, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("МесяцНачисления", 	НачалоМесяца(МесяцНачисления));
	Запрос.УстановитьПараметр("Резерв", 			РезервПремий(МесяцНачисления, Организация));
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РезервыПоОплатеТруда.Ссылка КАК Ссылка,
	|	РезервыПоОплатеТруда.Дата КАК Дата
	|ИЗ
	|	Документ.РезервыПоОплатеТруда КАК РезервыПоОплатеТруда
	|ГДЕ
	|	РезервыПоОплатеТруда.МесяцНачисления = &МесяцНачисления
	|	И РезервыПоОплатеТруда.Организация = &Организация
	|	И РезервыПоОплатеТруда.Проведен
	|	И РезервыПоОплатеТруда.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление)
	|	И РезервыПоОплатеТруда.Резерв = &Резерв
	|
	|УПОРЯДОЧИТЬ ПО
	|	РезервыПоОплатеТруда.Дата,
	|	РезервыПоОплатеТруда.Ссылка";

	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

Функция ЕстьДокументНачисленияРезервовПоОплатеТруда(МесяцНачисления, Организация) Экспорт
	
	Возврат ЕстьДокументНачисленияРезервовОтпусков(МесяцНачисления, Организация)
		Или ЕстьДокументНачисленияРезервовПремий(МесяцНачисления, Организация);
	
КонецФункции

Функция РезервОтпусков(МесяцНачисления, Организация) Экспорт
	
	НастройкаРезерваПоВиду = РезервыПоОплатеТрудаДляНебольшихОрганизаций.НастройкаРезерваПоВиду(Организация,
		Перечисления.ВидыРезервовПоОплатеТруда.Отпуск,
		МесяцНачисления);
	
	Возврат НастройкаРезерваПоВиду.Резерв;
	
КонецФункции

Функция РезервПремий(МесяцНачисления, Организация) Экспорт
	
	НастройкаРезерваПоВиду = РезервыПоОплатеТрудаДляНебольшихОрганизаций.НастройкаРезерваПоВиду(Организация,
		Перечисления.ВидыРезервовПоОплатеТруда.ГодоваяПремия,
		МесяцНачисления);
	
	Возврат НастройкаРезерваПоВиду.Резерв;
	
КонецФункции

Процедура СформироватьКадровыеПриказы() Экспорт
	
	КадровыйУчетБазовый.СформироватьКадровыеПриказы();
	
КонецПроцедуры

Функция СоздатьФизическоеЛицо(Наименование, ИНН, СсылкаНового = Неопределено) Экспорт
	
	ДанныеФизическогоЛица = КадровыйУчет.НовыеДанныеФизическогоЛица();
	ДанныеФизическогоЛица.Вставить("ИНН", ИНН);
	Если СсылкаНового <> Неопределено Тогда
		ДанныеФизическогоЛица.Вставить("Ссылка", СсылкаНового);
	КонецЕсли;
	
	Если ТипЗнч(Наименование) = Тип("Строка") Тогда
		ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(Наименование);
	Иначе
		ФИО = Наименование;
	КонецЕсли;
	
	НовоеФизическоеЛицо = КадровыйУчет.НовоеФизическоеЛицо(ФИО, ДанныеФизическогоЛица);
	
	Возврат НовоеФизическоеЛицо.Ссылка;
	
КонецФункции

Функция СоздатьСчетФизическогоЛица(ФизическоеЛицо, СтруктураРеквизиовСчета) Экспорт
	
	НовыйЭлемент = Справочники.БанковскиеСчета.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтруктураРеквизиовСчета,
	"НомерСчета, Банк, БанкДляРасчетов, ВалютаДенежныхСредств, ВидСчета, ТекстКорреспондента, ТекстНазначения, МесяцПрописью, СуммаБезКопеек, ВсегдаУказыватьКПП");
	НовыйЭлемент.Владелец = ФизическоеЛицо;
	НовыйЭлемент.Наименование = Лев(СокрЛП(СтруктураРеквизиовСчета.НомерСчета) + ", " + СтруктураРеквизиовСчета.Банк, 100);
	
	Если НовыйЭлемент.ПолучитьСсылкуНового().Пустая() Тогда
		НовыйЭлемент.УстановитьСсылкуНового(Справочники.БанковскиеСчета.ПолучитьСсылку());
	КонецЕсли;
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции

Функция Просклонять(Знач ФИО, Знач Падеж, Результат, Знач Пол = Неопределено) Экспорт
	
	Возврат ФизическиеЛицаЗарплатаКадры.Просклонять(ФИО, Падеж, Результат, Пол);
	
КонецФункции

// Возвращает ссылку на документ начисления зарплаты за месяц по организации.
// Если документа не существует, то возвращает пустую ссылку.
//
// Параметры:
//	МесяцНачисления - Дата - Первое число месяца.
//	Организация - СправочникСсылка.Организации - Организация.
//
// Возвращаемое значение:
//	ДокументСсылка.НачислениеЗарплаты - найденный документ или пустая ссылка.
//
Функция НайтиДокументНачисленияЗарплаты(МесяцНачисления, Организация, ПерваяПоловинаМесяца = Ложь) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", 			Организация);
	Запрос.УстановитьПараметр("МесяцНачисления", 		НачалоМесяца(МесяцНачисления));
	Запрос.УстановитьПараметр("ПерваяПоловинаМесяца", 	ПерваяПоловинаМесяца);

	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НачислениеЗарплаты.Ссылка КАК Документ
	|ИЗ
	|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
	|ГДЕ
	|	НачислениеЗарплаты.МесяцНачисления = &МесяцНачисления
	|	И НачислениеЗарплаты.Организация = &Организация
	|	И НачислениеЗарплаты.Проведен
	|	И НачислениеЗарплаты.ПерваяПоловинаМесяца = &ПерваяПоловинаМесяца
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачислениеЗарплаты.Дата,
	|	НачислениеЗарплаты.Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Документ;
	Иначе
		Результат = Документы.НачислениеЗарплаты.ПустаяСсылка();
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ФизическоеЛицо(Фамилия, Имя, Отчество, Пол, ДатаРождения, НомераДокументов, УникальныйИдентификатор = Неопределено, ЭтоНовый = Ложь, СоздаватьНового = Истина) Экспорт
	
	Ссылка = Неопределено;
	
	Если УникальныйИдентификатор <> Неопределено Тогда
		Ссылка = Справочники.ФизическиеЛица.ПолучитьСсылку(УникальныйИдентификатор);
		Если ОбщегоНазначения.СсылкаСуществует(Ссылка) Тогда
			Возврат Ссылка;
		КонецЕсли;
	КонецЕсли;
		
	ПараметрыПоискаПохожих = СотрудникиКлиентСервер.ПараметрыПоискаПохожих();
	ПараметрыПоискаПохожих.Фамилия  = Фамилия;
	ПараметрыПоискаПохожих.Имя      = Имя;
	ПараметрыПоискаПохожих.Отчество = Отчество;
	ФизическиеЛицаПоИмени = СотрудникиФормы.РезультатПоискаФизическихЛиц(ПараметрыПоискаПохожих);
	
	НайденыЛюдиПоИмени = Не ФизическиеЛицаПоИмени.ФизическоеЛицоУникально;
	
	Если НайденыЛюдиПоИмени Тогда
		
		// свойство ДанныеФизическихЛицДоступны определено только если люди найдены
		Если Не ФизическиеЛицаПоИмени.ДанныеФизическихЛицДоступны Тогда
			Возврат Справочники.ФизическиеЛица.ПустаяСсылка();
		КонецЕсли;
		
		Для Каждого ОписаниеФизическогоЛица Из ФизическиеЛицаПоИмени.ДанныеФизическихЛиц Цикл
			
			Если ЗначениеЗаполнено(ОписаниеФизическогоЛица.ДатаРождения) 
				И ЗначениеЗаполнено(ДатаРождения)
				И ОписаниеФизическогоЛица.ДатаРождения <> ДатаРождения Тогда
				Продолжить;
			КонецЕсли;
			
			Возврат ОписаниеФизическогоЛица.ФизическоеЛицо;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не СоздаватьНового Тогда
		Возврат Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;
	
	// Не нашли - создадим
	ФИО = Новый Структура;
	ФИО.Вставить("Фамилия",  Фамилия);
	ФИО.Вставить("Имя",      Имя);
	ФИО.Вставить("Отчество", Отчество);
	
	ДанныеФизическогоЛица = КадровыйУчет.НовыеДанныеФизическогоЛица();
	ДанныеФизическогоЛица.Вставить("Пол", Пол);
	
	Если ЗначениеЗаполнено(ДатаРождения) Тогда
		ДанныеФизическогоЛица.Вставить("ДатаРождения", ДатаРождения);
	КонецЕсли;
	
	Если Ссылка <> Неопределено Тогда
		ДанныеФизическогоЛица.Вставить("Ссылка", Ссылка);
	КонецЕсли;
	
	ЭтоНовый = Истина;
	
	Возврат КадровыйУчет.НовоеФизическоеЛицо(ФИО, ДанныеФизическогоЛица).Ссылка;
	
КонецФункции

Функция ОплаченныеДоговорыПодрядаСотрудниковИП(Организация, ФизическиеЛица, ДокументыОплаты) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("ФизическиеЛица",  ФизическиеЛица);
	Запрос.УстановитьПараметр("ДокументыОплаты", ДокументыОплаты);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИПРасходыНаОплатуТруда.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ИПРасходыНаОплатуТруда.ФизЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТ_ОплаченныеДокументыНачисления
	|ИЗ
	|	РегистрНакопления.ИПРасходыНаОплатуТруда КАК ИПРасходыНаОплатуТруда
	|ГДЕ
	|	ИПРасходыНаОплатуТруда.Регистратор В(&ДокументыОплаты)
	|	И ИПРасходыНаОплатуТруда.ФизЛицо В(&ФизическиеЛица)
	|	И ИПРасходыНаОплатуТруда.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыНачисленияПоДоговорам.ДокументОснование.Дата КАК ДатаДоговора,
	|	НачислениеЗарплатыНачисленияПоДоговорам.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	Документ.НачислениеЗарплаты.НачисленияПоДоговорам КАК НачислениеЗарплатыНачисленияПоДоговорам
	|ГДЕ
	|	НачислениеЗарплатыНачисленияПоДоговорам.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_ОплаченныеДокументыНачисления.РасчетныйДокумент
	|			ИЗ
	|				ВТ_ОплаченныеДокументыНачисления)
	|	И НачислениеЗарплатыНачисленияПоДоговорам.Сотрудник.ФизическоеЛицо В
	|			(ВЫБРАТЬ
	|				ВТ_ОплаченныеДокументыНачисления.ФизическоеЛицо
	|			ИЗ
	|				ВТ_ОплаченныеДокументыНачисления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДоговора";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура СформироватьСписокСтавок(Элемент) Экспорт
	
	ТаблицаСтавок = Новый ТаблицаЗначений;
	ТаблицаСтавок.Колонки.Добавить("КоличествоСтавок", ОбщегоНазначения.ОписаниеТипаЧисло(6,5));
	
	// Заполняем основными вариантами ставок
	НоваяСтрока = ТаблицаСтавок.Добавить();
	НоваяСтрока.КоличествоСтавок = 1;
	
	НоваяСтрока = ТаблицаСтавок.Добавить();
	НоваяСтрока.КоличествоСтавок = 0.75;

	НоваяСтрока = ТаблицаСтавок.Добавить();
	НоваяСтрока.КоличествоСтавок = 0.5;

	НоваяСтрока = ТаблицаСтавок.Добавить();
	НоваяСтрока.КоличествоСтавок = 0.25;
	
	// Получаем список используемых ставок
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТекущиеКадровыеДанныеСотрудников.КоличествоСтавок КАК КоличествоСтавок
	|ИЗ
	|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|ГДЕ
	|	ТекущиеКадровыеДанныеСотрудников.КоличествоСтавок <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КадроваяИсторияСотрудников.КоличествоСтавок
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
	|ГДЕ
	|	КадроваяИсторияСотрудников.КоличествоСтавок <> 0";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТаблицаСтавок.Найти(Выборка.КоличествоСтавок) = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаСтавок.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСтавок.Сортировать("КоличествоСтавок Убыв");
	
	Элемент.СписокВыбора.Очистить();
	Для Каждого СтрокаТаблицы ИЗ ТаблицаСтавок Цикл
		Элемент.СписокВыбора.Добавить(СтрокаТаблицы.КоличествоСтавок);
	КонецЦикла;
	Элемент.СписокВыбора.Добавить("Другая");
	
КонецПроцедуры

Функция АктивныеСотрудники(ТаблицаСотрудников, Период)
	
	// Исключаем из списка сотрудников,
	// которые в отпуске без сохранения
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала",         НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания",      КонецМесяца(Период));
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаСотрудников.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТТаблицаСотрудников
	|ИЗ
	|	&ТаблицаСотрудников КАК ТаблицаСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтпускБезСохраненияОплаты.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТСотрудникиВОтпуске
	|ИЗ
	|	ВТТаблицаСотрудников КАК ВТТаблицаСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	|		ПО ВТТаблицаСотрудников.Сотрудник = ОтпускБезСохраненияОплаты.Сотрудник
	|ГДЕ
	|	ОтпускБезСохраненияОплаты.ДатаНачала <= &ДатаНачала
	|	И ОтпускБезСохраненияОплаты.ДатаОкончания >= &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтпускБезСохраненияОплаты.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаСотрудников.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТТаблицаСотрудников КАК ВТТаблицаСотрудников
	|ГДЕ
	|	НЕ ВТТаблицаСотрудников.Сотрудник В
	|				(ВЫБРАТЬ
	|					ВТСотрудникиВОтпуске.Сотрудник
	|				ИЗ
	|					ВТСотрудникиВОтпуске)";
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ОбъектЗаблокирован(Ссылка, ПометкаУдаления) Экспорт
	
	Возврат ЭДОБЗК.ОбъектЗаблокирован(Ссылка, ПометкаУдаления)
	
КонецФункции

#Область КлассификаторДолжностей

Функция ДанныеВыбораДолжности(Параметры, ОкончаниеВводаТекста = Ложь) Экспорт
	
	ДанныеВыбора = Справочники.Должности.ПолучитьДанныеВыбора(Параметры);
	
	Если ДанныеВыбора.Количество() = 0 Тогда
		
		ДобавитьВДанныеВыбора(ДанныеВыбора,
			Параметры.СтрокаПоиска);
			
		Если ОкончаниеВводаТекста И ДанныеВыбора.Количество() = 0 Тогда
			// Добавляем вторую строку, чтобы в этом режиме не срабатывал автовыбор единственной строки.
			ДанныеВыбора.Добавить(""); 
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

Процедура ДобавитьВДанныеВыбора(ДанныеВыбора, СтрокаПоиска)
	
	Если Не ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	ШрифтКоманды          = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста);
	ШрифтПодСтроки        = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , Истина);
	ШрифтДополненияСтроки = Новый Шрифт(ШрифтыСтиля.КрупныйШрифтТекста, , , Истина);
	ЦветПодстроки = Новый Цвет(0, 175, 0);
	
	Представление = Новый ФорматированнаяСтрока(
				Новый ФорматированнаяСтрока(НСтр("ru='Создать должность'") + " """, ШрифтКоманды),
				Новый ФорматированнаяСтрока(СокрЛП(СтрокаПоиска), ШрифтПодСтроки, ЦветПодстроки),
				Новый ФорматированнаяСтрока("""", ШрифтКоманды));
	ДанныеВыбора.Добавить(Новый Структура("Наименование", СтрокаПоиска), Представление);
	
	МассивСодержимого = Новый Массив;
	МассивСодержимого.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='+'"), ШрифтДополненияСтроки, ЦветПодстроки));
	МассивСодержимого.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=' Выбрать должность из классификатора'"), ШрифтКоманды));
	Представление = Новый ФорматированнаяСтрока(МассивСодержимого);
	
	ЗначениеВыбора = Новый Структура;
	ДанныеВыбора.Добавить(ЗначениеВыбора, Представление);
	
КонецПроцедуры

Функция ЗаполнитьДолжностьПоПараметрам(ПараметрыДолжности) Экспорт
	
	ОписаниеДолжности = Новый Структура;
	ОписаниеДолжности.Вставить("Наименование");
	ОписаниеДолжности.Вставить("ТрудоваяФункция");
	ОписаниеДолжности.Вставить("НаименованиеКраткое");
	ОписаниеДолжности.Вставить("НаименованиеДляЗаписейОТрудовойДеятельности");
	
	Если ОбщегоНазначения.ТекущаяДатаПользователя() < ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026() Тогда
		ИмяРеквизитаКодОКЗ = "КодПоОКЗ";
	Иначе
		ИмяРеквизитаКодОКЗ = "КодПоОКЗ2026";
	КонецЕсли;
	
	ДолжностьСсылка = Справочники.Должности.ПустаяСсылка();
	ТрудоваяФункция = Справочники.ТрудовыеФункции.ПустаяСсылка();
	КодПоОКЗ        = Справочники.КлассификаторЗанятий.ПустаяСсылка();
	
	ОКЗКод = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыДолжности, "ОКЗКод", "");
	Если Не ЗначениеЗаполнено(ОКЗКод) Тогда
		ПараметрыДолжностиИзКлассификатора = НайтиВКлассификатореПоТочномуНаименованиюДолжности(ПараметрыДолжности.Наименование);
		ОКЗКод = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыДолжностиИзКлассификатора, "ОКЗКод", "");
	КонецЕсли;
	КодПоОКЗ = ЗначениеОКЗПоКоду(ОКЗКод);
	Если ЗначениеЗаполнено(КодПоОКЗ) Тогда
		ТрудоваяФункция = Справочники.ТрудовыеФункции.НайтиПоРеквизиту(ИмяРеквизитаКодОКЗ, КодПоОКЗ);
		Если Не ЗначениеЗаполнено(ТрудоваяФункция) Тогда
			ТрудоваяФункцияОбъект = Справочники.ТрудовыеФункции.СоздатьЭлемент();
			ТрудоваяФункцияОбъект[ИмяРеквизитаКодОКЗ] = КодПоОКЗ;
			ТрудоваяФункцияОбъект.Наименование = СтрШаблон(НСтр("ru = '%1: %2'"),
				ОКЗКод, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КодПоОКЗ, "НаименованиеГруппЗанятий"));
			ТрудоваяФункцияОбъект.Записать();
			ТрудоваяФункция = ТрудоваяФункцияОбъект.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ОписаниеДолжности, ПараметрыДолжности);
	ОписаниеДолжности.ТрудоваяФункция                             = ТрудоваяФункция;
	ОписаниеДолжности.НаименованиеКраткое                         = ОписаниеДолжности.Наименование;
	ОписаниеДолжности.НаименованиеДляЗаписейОТрудовойДеятельности = ОписаниеДолжности.Наименование;
	
	Возврат ОписаниеДолжности;
	
КонецФункции

Функция ПодобратьДолжностьПоПараметрам(ПараметрыДолжности) Экспорт
	
	// Сначала ищем. Если не находим - пытаемся создать
	Должность = НайтиДолжностьПоПараметрам(ПараметрыДолжности);
	Если Не ЗначениеЗаполнено(Должность) Тогда
		Должность = СоздатьДолжностьПоПараметрам(ПараметрыДолжности);
	КонецЕсли;
	
	Возврат Должность;
	
КонецФункции

Функция НайтиДолжностьПоПараметрам(ПараметрыДолжности) Экспорт
	
	ДолжностьСсылка = Справочники.Должности.ПустаяСсылка();
	
	ОписаниеДолжности = ЗаполнитьДолжностьПоПараметрам(ПараметрыДолжности);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование",    ОписаниеДолжности.Наименование);
	Запрос.УстановитьПараметр("ТрудоваяФункция", ОписаниеДолжности.ТрудоваяФункция);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Должности.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Должности КАК Должности
	|ГДЕ
	|	Должности.Наименование = &Наименование
	|	И Должности.ТрудоваяФункция = &ТрудоваяФункция";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДолжностьСсылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ДолжностьСсылка;
	
КонецФункции

Функция СоздатьДолжностьПоПараметрам(ПараметрыДолжности) Экспорт
	
	ДолжностьСсылка = Справочники.Должности.ПустаяСсылка();
	
	ОписаниеДолжности = ЗаполнитьДолжностьПоПараметрам(ПараметрыДолжности);

	Если ЗначениеЗаполнено(ОписаниеДолжности.ТрудоваяФункция) Тогда
		ДолжностьОбъект = Справочники.Должности.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(ДолжностьОбъект, ОписаниеДолжности);
		ДолжностьОбъект.Записать();
		ДолжностьСсылка = ДолжностьОбъект.Ссылка;
	КонецЕсли;
	
	Возврат ДолжностьСсылка;
	
КонецФункции

Функция ЗначениеОКЗПоКоду(КодОКЗ) Экспорт
	
	Возврат Справочники.КлассификаторЗанятий.НайтиПоКоду(КодОКЗ);
	
КонецФункции

Функция ПодобратьПоКлассификаторуПоНаименованиюДолжности(Данные, АдресКлассификатора = "", МаксимальноеКоличествоОтбора = 0) Экспорт
	
	ОграничиватьСписок = МаксимальноеКоличествоОтбора > 0;
	
	НайденныеДолжности = Новый Массив;
	НайденныеОКЗ       = Новый Соответствие;
	
	Классификатор = Неопределено;
	Если Не ПустаяСтрока(АдресКлассификатора) Тогда
		Классификатор = ПолучитьИзВременногоХранилища(АдресКлассификатора);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Классификатор) Тогда
		Классификатор = КлассификаторДолжностей();
	КонецЕсли;
	
	Выражение = "";
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Данные, " ", Истина, Истина);
	Для Индекс = 0 По МассивПодстрок.ВГраница() Цикл
		Значение = МассивПодстрок[Индекс];
		Если ОбщегоНазначенияКлиентСервер.ЭтоЧисло(Значение) Тогда
			Продолжить;
		Иначе
			Выражение = Выражение + ?(ПустаяСтрока(Выражение),"", " ") + Значение + "[а-я]*";
		КонецЕсли;
	КонецЦикла;
	
	Для НомерСтроки = 1 По Классификатор.Количество() Цикл
		НазваниеДолжности = Классификатор[НомерСтроки - 1].Name;
		Результат = СтрНайтиПоРегулярномуВыражению(НазваниеДолжности, Выражение,,,, Истина,);
		Если Результат.Длина = 0 Тогда
			Продолжить;
		КонецЕсли;
		НайденныеДолжности.Добавить(НазваниеДолжности);
		НайденныеОКЗ.Вставить(НазваниеДолжности, Классификатор[НомерСтроки - 1].OKZ);
	КонецЦикла;
	
	МассивБлизкихЗначений = Новый Массив;
	МассивДальнихЗначений = Новый Массив;
	Для Каждого НазваниеДолжности Из НайденныеДолжности Цикл
		Если СтрНачинаетсяС(НазваниеДолжности, МассивПодстрок[0]) Тогда
			МассивБлизкихЗначений.Добавить(НазваниеДолжности);
		Иначе
			МассивДальнихЗначений.Добавить(НазваниеДолжности);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеДолжность = Новый Массив;
	ДанныеОКЗ       = Новый Соответствие;
	
	Для Каждого НазваниеДолжности Из МассивБлизкихЗначений Цикл
		ДанныеДолжность.Добавить(НазваниеДолжности);
		ДанныеОКЗ.Вставить(НазваниеДолжности, НайденныеОКЗ[НазваниеДолжности]);
		Если ОграничиватьСписок И ДанныеДолжность.Количество() > МаксимальноеКоличествоОтбора Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ОграничиватьСписок И ДанныеДолжность.Количество() < МаксимальноеКоличествоОтбора Тогда
		Для Каждого НазваниеДолжности Из МассивДальнихЗначений Цикл
			ДанныеДолжность.Добавить(НазваниеДолжности);
			ДанныеОКЗ.Вставить(НазваниеДолжности, НайденныеОКЗ[НазваниеДолжности]);
			Если ОграничиватьСписок И ДанныеДолжность.Количество() > МаксимальноеКоличествоОтбора Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДанныеКлассификатораДолжность = Новый ФиксированныйМассив(ДанныеДолжность);
	ДанныеКлассификатораОКЗ       = Новый ФиксированноеСоответствие(ДанныеОКЗ);
	
	Возврат Новый Структура("ДанныеКлассификатораДолжность, ДанныеКлассификатораОКЗ",
		ДанныеКлассификатораДолжность, ДанныеКлассификатораОКЗ);
	
КонецФункции

Функция НайтиВКлассификатореПоТочномуНаименованиюДолжности(Данные) Экспорт
	
	ПараметрыДолжности = Новый Структура("Наименование, ОКЗКод");
	Классификатор = КлассификаторДолжностей();
	НайденноеЗначение = Классификатор.Найти(Врег(Данные), "NameUppercase");
	Если НайденноеЗначение <> Неопределено Тогда
		ПараметрыДолжности.Вставить("Наименование", НайденноеЗначение.Name);
		ПараметрыДолжности.Вставить("ОКЗКод", НайденноеЗначение.OKZ);
	КонецЕсли;
	
	Возврат ПараметрыДолжности;
	
КонецФункции

Процедура ПолучитьКлассификаторДолжностей(Адрес) Экспорт
	
	Классификатор = КлассификаторДолжностей();
	ПоместитьВоВременноеХранилище(Классификатор, Адрес);
	
КонецПроцедуры

Функция КлассификаторДолжностей(ДополнятьРасшифровкой = Ложь) Экспорт
	
	Классификатор = Новый ТаблицаЗначений;
	Классификатор.Колонки.Добавить("Name", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(250)));
	Классификатор.Колонки.Добавить("OKZ", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4)));
	Классификатор.Колонки.Добавить("Order", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1)));
	Классификатор.Колонки.Добавить("NameUppercase", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(250)));
	
	Классификаторы = Новый Массив;
	Классификаторы.Добавить("КлассификаторПоставляемыеДолжности");
	Если ОбщегоНазначения.ТекущаяДатаПользователя() < ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026() Тогда
		Классификаторы.Добавить("КлассификаторОКПДТРДолжности");
		Классификаторы.Добавить("КлассификаторОКПДТРПрофессии");
	Иначе
		Классификаторы.Добавить("КлассификаторОКПДТР2026Должности");
		Классификаторы.Добавить("КлассификаторОКПДТР2026Профессии");
	КонецЕсли;
	Для Индекс = 0 По Классификаторы.ВГраница() Цикл
		КлассификаторXML = ПолучитьОбщийМакет(Классификаторы[Индекс]).ПолучитьТекст();
		КлассификаторОКПДТР = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
		Для Каждого СтрокаТаблицы Из КлассификаторОКПДТР Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.OKZ) Тогда
				НоваяСтрока = Классификатор.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.NameUppercase = Врег(НоваяСтрока.Name);
				НоваяСтрока.Order = Индекс;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ДополнятьРасшифровкой Тогда
		
		Классификатор.Колонки.Добавить("Description", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(200)));
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Классификатор", Классификатор);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Классификатор.Name КАК Name,
		|	Классификатор.OKZ КАК OKZ,
		|	Классификатор.Order КАК Order,
		|	Классификатор.NameUppercase КАК NameUppercase
		|ПОМЕСТИТЬ ВТ_Классификатор
		|ИЗ
		|	&Классификатор КАК Классификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Классификатор.Name КАК Name,
		|	ВТ_Классификатор.OKZ КАК OKZ,
		|	ВТ_Классификатор.Order КАК Order,
		|	ВТ_Классификатор.NameUppercase КАК NameUppercase,
		|	КлассификаторЗанятий.НаименованиеГруппЗанятий КАК Description
		|ИЗ
		|	ВТ_Классификатор КАК ВТ_Классификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторЗанятий КАК КлассификаторЗанятий
		|		ПО ВТ_Классификатор.OKZ = КлассификаторЗанятий.Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	Name";
		
		Классификатор = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Классификатор.Сортировать("Name");
	
	Возврат Классификатор;
	
КонецФункции

#КонецОбласти

Процедура РассчитатьКоличествоДнейОстаткаОтпуска(Организация, Сотрудник, Период, СведенияПоОтпуску) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
	
	ДатаОкончания = Период;
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ДатаПриема", ДатаОкончания);
	Если КадровыеДанные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПриема = КадровыеДанные[0].ДатаПриема;
	Если Не ЗначениеЗаполнено(ДатаПриема) Или ДатаПриема >= ДатаОкончания Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчетаЗарплаты    = РегистрыСведений.НастройкиУчетаЗарплаты.Получить(Новый Структура("Организация", Организация));
	ПродолжительностьОтпуска  = НастройкиУчетаЗарплаты.ПродолжительностьОтпуска;
	
	ДнейОтпускаВМесяц = ПродолжительностьОтпуска/12;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",             Организация);
	Запрос.УстановитьПараметр("Сотрудник",               Сотрудник);
	Запрос.УстановитьПараметр("НачалоПериодаОсмотра",    ДатаПриема);
	Запрос.УстановитьПараметр("ОкончаниеПериодаОсмотра", ДатаОкончания);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтпускБезСохраненияОплаты.ДатаНачала КАК ДатаНачалаОтсутствия,
	|	ОтпускБезСохраненияОплаты.ДатаОкончания КАК ДатаОкончанияОтсутствия,
	|	РАЗНОСТЬДАТ(ОтпускБезСохраненияОплаты.ДатаНачала, ВЫБОР
	|			КОГДА ОтпускБезСохраненияОплаты.ДатаОкончания > &ОкончаниеПериодаОсмотра
	|				ТОГДА &ОкончаниеПериодаОсмотра
	|			ИНАЧЕ ОтпускБезСохраненияОплаты.ДатаОкончания
	|		КОНЕЦ, ДЕНЬ) + 1 КАК КоличествоДнейОтсутствия,
	|	0 КАК КоличествоДнейПродления,
	|	ОтпускБезСохраненияОплаты.Сотрудник КАК Сотрудник,
	|	ИСТИНА КАК ЯвляетсяОтпускомБезСохраненияЗарплаты
	|ИЗ
	|	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	|ГДЕ
	|	ОтпускБезСохраненияОплаты.Организация = &Организация
	|	И ОтпускБезСохраненияОплаты.Сотрудник = &Сотрудник
	|	И ОтпускБезСохраненияОплаты.Проведен
	|	И ОтпускБезСохраненияОплаты.ДатаНачала >= &НачалоПериодаОсмотра
	|	И ОтпускБезСохраненияОплаты.ДатаНачала < &ОкончаниеПериодаОсмотра
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачалаОтсутствия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Отпуск.ДатаНачалаОсновногоОтпуска КАК ДатаНачалаОсновногоОтпуска,
	|	Отпуск.ДатаОкончанияОсновногоОтпуска КАК ДатаОкончанияОсновногоОтпуска,
	|	Отпуск.Сотрудник КАК Сотрудник
	|ИЗ
	|	Документ.Отпуск КАК Отпуск
	|ГДЕ
	|	Отпуск.Организация = &Организация
	|	И Отпуск.Сотрудник = &Сотрудник
	|	И Отпуск.Проведен
	|	И Отпуск.ДатаНачалаОсновногоОтпуска >= &НачалоПериодаОсмотра
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачалаОсновногоОтпуска";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаОтклоненийПоВидуОтпуска = Результат[0].Выгрузить();
	ТаблицаИспользованныхОтпусков  = Результат[1].Выгрузить();
	
	ИспользованоДнейОтпуска = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаИспользованныхОтпусков Цикл
		ИспользованоДнейОтпуска = ИспользованоДнейОтпуска + МодульРасчетЗарплатыДляНебольшихОрганизаций.КоличествоДнейОтпуска(
		СтрокаТаблицы.ДатаНачалаОсновногоОтпуска, СтрокаТаблицы.ДатаОкончанияОсновногоОтпуска, Организация);
	КонецЦикла;
	
	ПоловиныМесяцевОбычные = Новый Массив;
	ПоловиныМесяцевОбычные.Добавить(15.5);
	ПоловиныМесяцевОбычные.Добавить(14);
	ПоловиныМесяцевОбычные.Добавить(15.5);
	ПоловиныМесяцевОбычные.Добавить(15);
	ПоловиныМесяцевОбычные.Добавить(15.5);
	ПоловиныМесяцевОбычные.Добавить(15);
	ПоловиныМесяцевОбычные.Добавить(15.5);
	ПоловиныМесяцевОбычные.Добавить(15.5);
	ПоловиныМесяцевОбычные.Добавить(15);
	ПоловиныМесяцевОбычные.Добавить(15.5);
	ПоловиныМесяцевОбычные.Добавить(15);
	ПоловиныМесяцевОбычные.Добавить(15.5);
	
	ДлинаСуток = 86400;

	РабочиеГода = Новый ТаблицаЗначений;
	РабочиеГода.Колонки.Добавить("ДатаНачала",		Новый ОписаниеТипов("Дата"));
	РабочиеГода.Колонки.Добавить("ДатаОкончания",	Новый ОписаниеТипов("Дата"));
	РабочиеГода.Колонки.Добавить("РабочиеМесяцы",	Новый ОписаниеТипов("ТаблицаЗначений"));
	
	ДатаНачалаРабочегоГода    = ДатаПриема;
	ДатаОкончанияРабочегоГода = ДобавитьМесяц(ДатаНачалаРабочегоГода, 12) - 1;
	
	МаксимальнаяДатаОкончания = ДатаОкончания;
	Пока ДатаНачалаРабочегоГода <= МаксимальнаяДатаОкончания Цикл
		
		РабочийГод = РабочиеГода.Добавить();
		РабочийГод.ДатаНачала				= ДатаНачалаРабочегоГода;
		Если ЗначениеЗаполнено(ДатаОкончания) И ДатаОкончанияРабочегоГода > ДатаОкончания Тогда
			ДатаОкончанияРабочегоГода = ДатаОкончания;
		КонецЕсли;
		РабочийГод.ДатаОкончания = ДатаОкончанияРабочегоГода;
		
		РабочийГод.РабочиеМесяцы.Колонки.Добавить("ДатаМесяца",		Новый ОписаниеТипов("Дата"));
		РабочийГод.РабочиеМесяцы.Колонки.Добавить("КоличествоДней",	Новый ОписаниеТипов("Число"));
		РабочийГод.РабочиеМесяцы.Колонки.Добавить("Заполнен",		Новый ОписаниеТипов("Булево"));
		
		НачалоПериода = РабочийГод.ДатаНачала;
		
		Если РабочийГод.ДатаОкончания < Дата(2006, 10, 6) Тогда
			КоличествоДнейВключаемых = 7;
		Иначе
			КоличествоДнейВключаемых = 14;
		КонецЕсли;
		
		// Заполним рабочие месяца датами по умолчанию.
		НомерМесяца = 1;
		
		ПоловинаПервогоМесяца = ПоловиныМесяцевОбычные[Месяц(РабочийГод.ДатаНачала)-1] + ?(ЭтоВисокосныйМесяц(РабочийГод.ДатаНачала), 0.5, 0);
		ВПервомМесяцеОтработанаПоловина = (ПоловинаПервогоМесяца + 1 >= День(РабочийГод.ДатаНачала));
		
		ДеньПервогоМесяца = День(РабочийГод.ДатаНачала);
		ПоследнийДеньПервогоМесяца = День(КонецМесяца(РабочийГод.ДатаНачала));
		ОтработаноВПервомМесяце = ПоследнийДеньПервогоМесяца - ДеньПервогоМесяца + 1;
		ОсталосьОтработатьДней = ?(ДеньПервогоМесяца = 17 И ПоследнийДеньПервогоМесяца = 31, 16, 15) - ОтработаноВПервомМесяце;
		
		Пока НачалоПериода < РабочийГод.ДатаОкончания Или НомерМесяца = 12 Цикл
			
			Если НомерМесяца = 12 Тогда
				// П. 28 правил об очередных и дополнительных отпусках.
				ДатаМесяца = НачалоПериода - ДлинаСуток;
			Иначе
				// Если в первом месяце отработана половина, определяем дату месяца по продолжительности текущего месяца.
				// Если в первом месяце отработано меньше половины, используем правило 15 дней.
				Если ВПервомМесяцеОтработанаПоловина Тогда 
					ПоловинаМесяца = Окр(ПоловиныМесяцевОбычные[Месяц(НачалоПериода)-1] + ?(ЭтоВисокосныйМесяц(НачалоПериода), 0.5, 0));
					ПоследнийДеньМесяца = НачалоДня(КонецМесяца(НачалоПериода));
					ДатаМесяца = Мин(НачалоПериода - ДлинаСуток + ПоловинаМесяца * ДлинаСуток, ПоследнийДеньМесяца);
				Иначе 
					ДатаМесяца = ?(День(НачалоПериода) = 1, НачалоПериода, КонецМесяца(НачалоПериода) + 1) + (ОсталосьОтработатьДней - 1) * ДлинаСуток;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДатаОкончания) И ДатаМесяца > ДатаОкончания Тогда
				Прервать;
			КонецЕсли;
			
			Если ДатаМесяца > РабочийГод.ДатаОкончания Тогда // Дата месяца не попала в текущий рабочий год.
				Прервать;
			КонецЕсли;
			
			// Перед добавлением месяца проверим что он не добавлен остатками.
			ОтборМесяцев = Новый Структура("ДатаМесяца", ДатаМесяца);
			Если РабочийГод.РабочиеМесяцы.НайтиСтроки(ОтборМесяцев).Количество() = 0 Тогда
				РабочийГод.РабочиеМесяцы.Добавить().ДатаМесяца = ДатаМесяца;
			КонецЕсли;
			
			НачалоПериода = ДобавитьМесяц(РабочийГод.ДатаНачала, НомерМесяца);
			Если День(НачалоПериода) < ДеньПервогоМесяца Тогда 
				НачалоПериода = КонецМесяца(НачалоПериода) + 1;
			КонецЕсли;
			
			НомерМесяца = НомерМесяца + 1;
		КонецЦикла;
		
		// Продлим рабочие года, изменив дату окончания рабочего года и подвинув каждую дату рабочего месяца.
		ОтклоненияДляУдаления = Новый Массив;
		Для Каждого СтрокаОтклонения Из ТаблицаОтклоненийПоВидуОтпуска Цикл
			Если РабочийГод.ДатаОкончания < СтрокаОтклонения.ДатаНачалаОтсутствия Тогда
				Продолжить;
			КонецЕсли;
			
			Если РабочийГод.ДатаНачала > СтрокаОтклонения.ДатаНачалаОтсутствия Тогда
				Если ЗначениеЗаполнено(СтрокаОтклонения.ДатаОкончанияОтсутствия)
					И СтрокаОтклонения.ДатаОкончанияОтсутствия < РабочийГод.ДатаНачала Тогда 
					Продолжить;
				Иначе 
					СтрокаОтклонения.ДатаНачалаОтсутствия = РабочийГод.ДатаНачала;
				КонецЕсли;
			КонецЕсли;
			
			// Отпуска без сохранения первые 7/14 дней в рабочем году не увеличивают рабочий год.
			КоличествоДнейВключаемыхБыло = 0;
			Если СтрокаОтклонения.ЯвляетсяОтпускомБезСохраненияЗарплаты Тогда
				
				КоличествоДнейВключаемыхБыло = КоличествоДнейВключаемых;
				КоличествоДнейПродления = Макс(СтрокаОтклонения.КоличествоДнейОтсутствия - КоличествоДнейВключаемых, 0);
				КоличествоДнейВключаемых = Макс(КоличествоДнейВключаемых - СтрокаОтклонения.КоличествоДнейОтсутствия, 0);
				
				СтрокаОтклонения.КоличествоДнейПродления = КоличествоДнейПродления;
				
			КонецЕсли;
			
			// Увеличение рабочего года
			Если СтрокаОтклонения.КоличествоДнейПродления = NULL Тогда
				// Даты окончания нет, сотрудник ушел надолго, что с ним - мы не знаем, расчет прекращаем датой начала отсутствия.
				РабочийГод.ДатаОкончания = СтрокаОтклонения.ДатаНачалаОтсутствия - ДлинаСуток;
				МаксимальнаяДатаОкончания = СтрокаОтклонения.ДатаНачалаОтсутствия - ДлинаСуток;
			ИначеЕсли СтрокаОтклонения.КоличествоДнейПродления > 0 Тогда
				// Отсутствие с известной датой окончания - сдвигаем дату года и месяцы.
				РабочийГод.ДатаОкончания		= РабочийГод.ДатаОкончания + СтрокаОтклонения.КоличествоДнейПродления * ДлинаСуток;
				Если ЗначениеЗаполнено(ДатаОкончания) И РабочийГод.ДатаОкончания > ДатаОкончания Тогда
					РабочийГод.ДатаОкончания	= ДатаОкончания;
				КонецЕсли;
				// Сдвиг всех рабочих месяцев после даты начала продления.
				Для Каждого РабочийМесяц Из РабочийГод.РабочиеМесяцы Цикл
					Если РабочийМесяц.ДатаМесяца >= СтрокаОтклонения.ДатаНачалаОтсутствия + КоличествоДнейВключаемыхБыло * ДлинаСуток Тогда
						РабочийМесяц.ДатаМесяца = РабочийМесяц.ДатаМесяца + СтрокаОтклонения.КоличествоДнейПродления * ДлинаСуток;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ОтклоненияДляУдаления.Добавить(СтрокаОтклонения);
		КонецЦикла;
		
		Для Каждого СтрокаОтклонения Из ОтклоненияДляУдаления Цикл
			ТаблицаОтклоненийПоВидуОтпуска.Удалить(СтрокаОтклонения);
		КонецЦикла;
		
		// Удаление рабочих месяцев, вышедших за дату окончания рабочего года.
		РабочиеМесяцыДляУдаления = Новый Массив;
		Для Каждого РабочийМесяц Из РабочийГод.РабочиеМесяцы Цикл
			Если РабочийМесяц.ДатаМесяца > РабочийГод.ДатаОкончания Тогда
				РабочиеМесяцыДляУдаления.Добавить(РабочийМесяц);
			КонецЕсли;
		КонецЦикла;
		Для Каждого РабочийМесяц Из РабочиеМесяцыДляУдаления Цикл
			РабочийГод.РабочиеМесяцы.Удалить(РабочийМесяц);
		КонецЦикла;
		
		РабочийГод.РабочиеМесяцы.Сортировать("ДатаМесяца");
		
		ДатаНачалаРабочегоГода		= КонецДня(РабочийГод.ДатаОкончания) + 1;
		ДатаОкончанияРабочегоГода	= ДобавитьМесяц(ДатаНачалаРабочегоГода, 12) - 1;
	КонецЦикла;
	
	Если РабочиеГода.Количество() <> 0 Тогда
		Для Каждого РабочийГод Из РабочиеГода Цикл
			Для Каждого РабочийМесяц Из РабочийГод.РабочиеМесяцы Цикл
				Если Не РабочийМесяц.Заполнен Тогда
					РабочийМесяц.КоличествоДней = ДнейОтпускаВМесяц;
					РабочийМесяц.Заполнен = Истина;
				КонецЕсли;
				ДатаНачалаОтсчета = КонецДня(РабочийМесяц.ДатаМесяца) + 1;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ПравоСНачалаРаботы = 0;
	Для Каждого РабочийГод Из РабочиеГода Цикл
		Для Каждого РабочийМесяц Из РабочийГод.РабочиеМесяцы Цикл
			ПравоСНачалаРаботы = ПравоСНачалаРаботы + РабочийМесяц.КоличествоДней;
		КонецЦикла;
	КонецЦикла;
	
	КоличествоДней = Макс(0, ПравоСНачалаРаботы - ИспользованоДнейОтпуска);
	
	Если КоличествоДней <> 0 Тогда
		РабочийГодПо      = ДатаОкончания;
		КоличествоМесяцев = ?(КоличествоДней < ДнейОтпускаВМесяц, 1, Окр(КоличествоДней/ДнейОтпускаВМесяц));
		РабочийГодС       = ДобавитьМесяц(РабочийГодПо, - КоличествоМесяцев);
		
		РабочиеГода.Сортировать("ДатаОкончания Убыв");
		
		Для Каждого РабочийГод Из РабочиеГода Цикл
			КоличествоМесяцевГода = РабочийГод.РабочиеМесяцы.Количество();
			Если КоличествоМесяцевГода = 0 Тогда
				Продолжить;
			КонецЕсли;
			Поправка = 0;
			Если КоличествоМесяцевГода > 1 Тогда
				Если НачалоМесяца(РабочийГод.РабочиеМесяцы[КоличествоМесяцевГода - 1].ДатаМесяца)
					= НачалоМесяца(РабочийГод.РабочиеМесяцы[КоличествоМесяцевГода - 2].ДатаМесяца) Тогда
					//учтем особенности заполнения таблицы РабочиеМесяцы
					Поправка = 1;
				КонецЕсли;
			КонецЕсли;
			Если КоличествоМесяцев = КоличествоМесяцевГода Тогда
				// выбираем первое значение таблицы
				РабочийГодС = РабочийГод.РабочиеМесяцы[0].ДатаМесяца;
				Прервать;
			ИначеЕсли КоличествоМесяцев < КоличествоМесяцевГода Тогда
				РабочийГодС = РабочийГод.РабочиеМесяцы[КоличествоМесяцевГода - КоличествоМесяцев - Поправка].ДатаМесяца;
				Прервать;
			Иначе
				КоличествоМесяцев = КоличествоМесяцев - КоличествоМесяцевГода + Поправка;
			КонецЕсли;
		КонецЦикла;
		
		//если совпало с месяцем приема - то указать дату приема
		Если ЗначениеЗаполнено(ДатаПриема) Тогда
			Если НачалоМесяца(ДатаПриема) >= НачалоМесяца(РабочийГодС) Тогда
				РабочийГодС = ДатаПриема;
			Иначе
				ДатаГод            = Год(РабочийГодС);
				ДатаМесяц          = Месяц(РабочийГодС);
				ДатаДень           = День(ДатаПриема);
				ДатаДень           = Мин(ДатаДень, День(КонецМесяца(РабочийГодС)));
				РабочийГодС = Дата(ДатаГод, ДатаМесяц, ДатаДень);
			КонецЕсли;
		КонецЕсли;
		
		СведенияПоОтпуску.Вставить("ДнейКомпенсацииУдержанияОтпуска", КоличествоДней);
		СведенияПоОтпуску.Вставить("РабочийГодС",                     РабочийГодС);
		СведенияПоОтпуску.Вставить("РабочийГодПо",                    РабочийГодПо);
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоВисокосныйМесяц(ДатаГода)
	Возврат Месяц(ДатаГода) = 2 И ОбщегоНазначенияБЗККлиентСервер.ЭтоВисокосныйГод(ДатаГода)
КонецФункции

Функция ИспользоватьКадровуюИсторию() Экспорт
	
	ИспользоватьКадровуюИсторию = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчетПриВнешнейЗарплате");
		
	Возврат ИспользоватьКадровуюИсторию;
		
КонецФункции

#КонецОбласти

#Область ЗаписьЭлектроннойТрудовойКнижки

Процедура СтатусЗаписиЭлектроннойТрудовойКнижки(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Форма.МероприятиеПереданоПФР = Ложь;
	Форма.ЗаписиТрудовойКнижки.Очистить();
	
	Если Объект.Проведен Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
			|	МероприятияТрудовойДеятельности.Отменено КАК Отменено
			|ПОМЕСТИТЬ ВТИдМероприятий
			|ИЗ
			|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
			|ГДЕ
			|	МероприятияТрудовойДеятельности.Регистратор = &Регистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИдМероприятий.ИдМероприятия КАК ИдМероприятия
			|ИЗ
			|	ВТИдМероприятий КАК ИдМероприятий";
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Не РезультатЗапроса.Пустой() Тогда
			СостояниеМероприятияТрудовойДеятельности(Форма, Запрос);
		КонецЕсли;
		
	КонецЕсли;
	
	Форма.НадписьЗаписьЭлектроннойТрудовойКнижки = ПредставлениеСтатусаЗаписиЭлектроннойТрудовойКнижки(Форма);
	
КонецПроцедуры

Процедура СостояниеМероприятияТрудовойДеятельности(Форма, Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МероприятияТрудовойДеятельностиПереданные.Регистратор КАК Регистратор,
		|	МероприятияТрудовойДеятельностиПереданные.Регистратор.Номер КАК Номер,
		|	МероприятияТрудовойДеятельностиПереданные.Регистратор.Дата КАК Дата,
		|	МероприятияТрудовойДеятельностиПереданные.ПринятоВПФР КАК ДокументПринятВПФР,
		|	ВЫБОР
		|		КОГДА МероприятияТрудовойДеятельностиПереданные.Регистратор.ДокументПринятВПФР ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК МероприятиеПереданоПФР,
		|	ЖурналОтчетовСтатусы.Статус КАК Статус
		|ИЗ
		|	ВТИдМероприятий КАК ИдМероприятий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
		|			ПО МероприятияТрудовойДеятельностиПереданные.Регистратор = ЖурналОтчетовСтатусы.Ссылка
		|		ПО ИдМероприятий.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
		|			И ИдМероприятий.Отменено = МероприятияТрудовойДеятельностиПереданные.Отменено
		|
		|СГРУППИРОВАТЬ ПО
		|	МероприятияТрудовойДеятельностиПереданные.ПринятоВПФР,
		|	МероприятияТрудовойДеятельностиПереданные.Регистратор,
		|	ЖурналОтчетовСтатусы.Статус,
		|	МероприятияТрудовойДеятельностиПереданные.Регистратор.Номер,
		|	МероприятияТрудовойДеятельностиПереданные.Регистратор.Дата,
		|	ВЫБОР
		|		КОГДА МероприятияТрудовойДеятельностиПереданные.Регистратор.ДокументПринятВПФР ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Форма.ЗаписиТрудовойКнижки.Добавить();
			НоваяСтрока.ЗаписьЭлектроннойТрудовойКнижки = Выборка.Регистратор;
			Форма.МероприятиеПереданоПФР = Выборка.МероприятиеПереданоПФР;
			Если Выборка.ДокументПринятВПФР Тогда
				НоваяСтрока.Статус = НСтр("ru = 'принято в ПФР'");
			ИначеЕсли ПустаяСтрока(Выборка.Статус) Тогда
				НоваяСтрока.Статус = НСтр("ru = 'в работе'");
			Иначе
				НоваяСтрока.Статус = НРег(Лев(Выборка.Статус, 1)) + Сред(Выборка.Статус,2);
			КонецЕсли;
			НоваяСтрока.Представление = СтрШаблон(НСтр("ru = '%1 №%2 от %3 (%4)'"),
				ИмяФормыОтчетаПоЭТК(Выборка.Дата), Выборка.Номер, Формат(Выборка.Дата, "ДФ=dd.MM.yyyy"),НоваяСтрока.Статус);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПредставлениеСтатусаЗаписиЭлектроннойТрудовойКнижки(Форма)
	
	Если Форма.ЗаписиТрудовойКнижки.Количество() > 0 Тогда
		ДатаОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.ЗаписиТрудовойКнижки[0].ЗаписьЭлектроннойТрудовойКнижки, "Дата");
	Иначе
		ДатаОтчета = ТекущаяДатаСеанса();
	КонецЕсли;
	ИмяОтчета = ИмяФормыОтчетаПоЭТК(ДатаОтчета);
	
	Если Форма.ЗаписиТрудовойКнижки.Количество() = 0 Тогда
		
		ПредставлениеДокументов  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 отсутствует'"),
			ИмяОтчета);
			
		ПредставлениеЗаписи = Новый ФорматированнаяСтрока(ПредставлениеДокументов);
		
	ИначеЕсли Форма.ЗаписиТрудовойКнижки.Количество() = 1 Тогда
		
		ПредставлениеЗаписи = Новый ФорматированнаяСтрока(Форма.ЗаписиТрудовойКнижки[0].Представление, , , , "ссылка");
		
	Иначе
		
		ПредставлениеКоличества = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '(%1 %2)'"),
				Форма.ЗаписиТрудовойКнижки.Количество(),
				СтрокаСЧислом("; документ; ; документа; документов; документа",
					Форма.ЗаписиТрудовойКнижки.Количество(),
					ВидЧисловогоЗначения.Количественное,
					"L=ru_RU"));
					
		ПредставлениеДокументов  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 %2'"),
			ИмяОтчета,
			ПредставлениеКоличества);
			
		ПредставлениеЗаписи = Новый ФорматированнаяСтрока(ПредставлениеДокументов,, , , "ссылка");
		
	КонецЕсли;
	
	Возврат ПредставлениеЗаписи
	
КонецФункции

Функция ИмяФормыОтчетаПоЭТК(ДатаОтчета)
	
	Если ДатаОтчета < ДатаПереходаНаЕФС1() Тогда
		ИмяОтчета = НСтр("ru = 'СЗВ-ТД'");
	Иначе
		ИмяОтчета = НСтр("ru = 'ЕФС-1 подр.1.1'");
	КонецЕсли;
	
	Возврат ИмяОтчета;
	
КонецФункции

Процедура УправлениеДоступностьюКадровыхРеквизитов(Форма, ДоступностьКадровыхРеквизитов, ФормаСотрудника = Ложь) Экспорт
	
	Элементы = Форма.Элементы;
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("Организация");
	МассивРеквизитов.Добавить("НаименованиеДокумента");
	МассивРеквизитов.Добавить("НомерПриказа");
	МассивРеквизитов.Добавить("Дата");
	МассивРеквизитов.Добавить("Сотрудник");
	МассивРеквизитов.Добавить("ДатаНачала");
	МассивРеквизитов.Добавить("ВидЗанятости");
	МассивРеквизитов.Добавить("Подразделение");
	МассивРеквизитов.Добавить("Должность");
	МассивРеквизитов.Добавить("ТрудоваяФункция");
	МассивРеквизитов.Добавить("ОбособленноеПодразделение");
	МассивРеквизитов.Добавить("СтатьяТКРФ");
	МассивРеквизитов.Добавить("ОтразитьВТрудовойКнижке");
	МассивРеквизитов.Добавить("ЭлектронныеТрудовыеКнижки_ОтразитьВТрудовойКнижке");
	
	Если НЕ ФормаСотрудника
		Или (ФормаСотрудника 
			И Не Форма.ИспользоватьКадровыйУчет И Не Форма.ИспользоватьКадровыйУчетПриВнешнейЗарплате) Тогда
		МассивРеквизитов.Добавить("ДатаПриема");
		МассивРеквизитов.Добавить("ДатаУвольнения");
		МассивРеквизитов.Добавить("ТекущееПодразделение");
		МассивРеквизитов.Добавить("ТекущаяДолжность");
		МассивРеквизитов.Добавить("ТекущийВидЗанятости");
	КонецЕсли;
	
	Если НЕ ФормаСотрудника Тогда
		МассивРеквизитов.Добавить("НаименованиеВторогоДокументаОснования");
		МассивРеквизитов.Добавить("СерияВторогоДокументаОснования");
		МассивРеквизитов.Добавить("НомерВторогоДокументаОснования");
		МассивРеквизитов.Добавить("ДатаВторогоДокументаОснования");
		МассивРеквизитов.Добавить("ЕстьВторойДокументОснование");
	КонецЕсли;
	
	Для Каждого ЗначениеМассива Из МассивРеквизитов Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			ЗначениеМассива,
			"ТолькоПросмотр",
			НЕ ДоступностьКадровыхРеквизитов);
		
	КонецЦикла;
	
	ПравоРедактирования = ПравоДоступа("Редактирование", Метаданные.Справочники.Сотрудники);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаИндикатораЭТК",
		"Видимость",
		НЕ ДоступностьКадровыхРеквизитов);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ИзменитьДокументЭТК",
		"Видимость",
		НЕ ДоступностьКадровыхРеквизитов);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаБаннерСТекстомРедактирование",
		"Видимость",
		ПравоРедактирования);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаБаннерСТекстомПросмотр",
		"Видимость",
		НЕ ПравоРедактирования);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ИзменитьДокументЭТК",
		"Доступность",
		ПравоРедактирования И НЕ Форма.ТолькоПросмотр);
		
КонецПроцедуры

Процедура УправлениеФормойКадровогоМероприятия(Форма) Экспорт
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ДоговорРаботыУслуги") Тогда
		ОтразитьВТрудовойКнижке = Объект.ОтразитьТрудовуюДеятельность;
	Иначе
		ОтразитьВТрудовойКнижке = Объект.ОтразитьВТрудовойКнижке;
	КонецЕсли;
	
	ЕстьЗаписиТрудовойКнижки = Форма.ЗаписиТрудовойКнижки.Количество() > 0;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаписьЭлектроннойТрудовойКнижкиСсылка",
		"Видимость",
		ЕстьЗаписиТрудовойКнижки);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаписьЭлектроннойТрудовойКнижкиКнопка",
		"Видимость",
		НЕ ЕстьЗаписиТрудовойКнижки);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗарегистрироватьЗаписьЭлектроннойТрудовойКнижки",
		"Доступность",
		ОтразитьВТрудовойКнижке);
	
КонецПроцедуры

Процедура ЗаполнитьНаименованияКадровыхДокументов() Экспорт
	
	МассивНаименований = Новый Массив;
	МассивНаименований.Добавить(НСтр("ru = 'Приказ'"));
	МассивНаименований.Добавить(НСтр("ru = 'Постановление'"));
	МассивНаименований.Добавить(НСтр("ru = 'Протокол'"));
	МассивНаименований.Добавить(НСтр("ru = 'Распоряжение'"));
	МассивНаименований.Добавить(НСтр("ru = 'Решение'"));
	МассивНаименований.Добавить(НСтр("ru = 'Уведомление'"));
	МассивНаименований.Добавить(НСтр("ru = 'Присвоение'"));
	МассивНаименований.Добавить(НСтр("ru = 'Решение общего собрания'"));
	МассивНаименований.Добавить(НСтр("ru = 'Решение единственного учредителя'"));
	МассивНаименований.Добавить(НСтр("ru = 'Протокол общего собрания участников общества'"));
	МассивНаименований.Добавить(НСтр("ru = 'Протокол заседания Правления'"));
	
	ВидСтроки = "НаименованиеДокументаТК";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСтроки", ВидСтроки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗначенияСтроковыхРеквизитов.ЗначениеСтроки КАК НаименованиеДокумента
	|ИЗ
	|	РегистрСведений.ЗначенияСтроковыхРеквизитов КАК ЗначенияСтроковыхРеквизитов
	|ГДЕ
	|	ЗначенияСтроковыхРеквизитов.ВидСтроки = &ВидСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Для Каждого ЗначениеМассива ИЗ МассивНаименований Цикл
			Если ВРег(ЗначениеМассива) = ВРег(СокрЛП(Выборка.НаименованиеДокумента)) Тогда
				СуществующееНаименование = МассивНаименований.Найти(ЗначениеМассива);
				МассивНаименований.Удалить(СуществующееНаименование);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если МассивНаименований.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ЗначенияСтроковыхРеквизитов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВидСтроки.Установить(ВидСтроки);
	НаборЗаписей.Прочитать();
	
	Для Каждого НаименованиеДокумента ИЗ МассивНаименований Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ВидСтроки      = ВидСтроки;
		НоваяЗапись.ЗначениеСтроки = НаименованиеДокумента;
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
КонецПроцедуры

Процедура ЗаполнитьНомераИНаименованияКадровыхДокументов() Экспорт
	
	НаименованиеДокумента = НСтр("ru = 'Приказ'");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриемНаРаботу.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|ГДЕ
	|	(ПриемНаРаботу.НаименованиеДокумента ПОДОБНО &ПустаяСтрока
	|			ИЛИ ПриемНаРаботу.НомерПриказа ПОДОБНО &ПустаяСтрока)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КадровыйПеревод.Ссылка
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|ГДЕ
	|	(КадровыйПеревод.НаименованиеДокумента ПОДОБНО &ПустаяСтрока
	|			ИЛИ КадровыйПеревод.НомерПриказа ПОДОБНО &ПустаяСтрока)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Увольнение.Ссылка
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|ГДЕ
	|	(Увольнение.НаименованиеДокумента ПОДОБНО &ПустаяСтрока
	|			ИЛИ Увольнение.НомерПриказа ПОДОБНО &ПустаяСтрока)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.НаименованиеДокумента) Тогда
				ДокументОбъект.НаименованиеДокумента = НаименованиеДокумента;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.НомерПриказа) Тогда
				ДокументОбъект.НомерПриказа = ЭлектронныеТрудовыеКнижкиВызовСервера.НомерНаПечать(ДокументОбъект.Номер);
			КонецЕсли;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не удалось обработать кадровый документ
			|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				Выборка.Ссылка,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СтатусЗаписиЭлектроннойТрудовойКнижкиСотрудника(Форма) Экспорт
	
	Если НЕ (ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")
		И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет")) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	Форма.МероприятиеПереданоПФР = Ложь;
	Форма.ЗаписиТрудовойКнижки.Очистить();
	
	Если Не ЗначениеЗаполнено(Форма.ТекущаяОрганизация) Тогда
		Возврат;
	КонецЕсли;
	
	ВидыМероприятий = Новый Массив;
	Если ЗначениеЗаполнено(Форма.ДатаУвольнения) Тогда
		ДатаМероприятия = Форма.ДатаУвольнения;
		ВидыМероприятий.Добавить(Перечисления.ВидыМероприятийТрудовойДеятельности.Увольнение);
	Иначе
		ДатаМероприятия = Форма.ДатаПриема;
		ВидыМероприятий.Добавить(Перечисления.ВидыМероприятийТрудовойДеятельности.Прием);
		ВидыМероприятий.Добавить(Перечисления.ВидыМероприятийТрудовойДеятельности.Перевод);
	КонецЕсли;
	
	ЗаполнятьПодразделения = Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.ТекущаяОрганизация, "НеЗаполнятьПодразделенияВМероприятияхТрудовойДеятельности");
	Если ЗаполнятьПодразделения Тогда
		ПодразделениеОрганизации = Форма.ТекущееПодразделение;
	Иначе
		ПодразделениеОрганизации = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация",           Форма.ТекущаяОрганизация);
	Запрос.УстановитьПараметр("Сотрудник",             Форма.СотрудникСсылка);
	Запрос.УстановитьПараметр("ФизическоеЛицо",        Форма.ФизическоеЛицоСсылка);
	Запрос.УстановитьПараметр("ДатаМероприятия",       ДатаМероприятия);
	Запрос.УстановитьПараметр("Подразделение",         ПодразделениеОрганизации);
	Запрос.УстановитьПараметр("Должность",             Форма.ТекущаяДолжность);
	Запрос.УстановитьПараметр("ЯвляетсяСовместителем", Форма.ТекущийВидЗанятости <> Перечисления.ВидыЗанятости.ОсновноеМестоРаботы);
	Запрос.УстановитьПараметр("ВидыМероприятий",       ВидыМероприятий);
	Запрос.УстановитьПараметр("ТекущаяДата",           КонецДня(ТекущаяДатаСеанса()+86400));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия
	|ПОМЕСТИТЬ ВТ_ОтмененныеМероприятия
	|ИЗ
	|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|ГДЕ
	|	МероприятияТрудовойДеятельности.ФизическоеЛицо = &ФизическоеЛицо
	|	И МероприятияТрудовойДеятельности.Организация = &Организация
	|	И МероприятияТрудовойДеятельности.Сотрудник = &Сотрудник
	|	И МероприятияТрудовойДеятельности.Отменено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МероприятияТрудовойДеятельности.ФизическоеЛицо КАК ФизическоеЛицо,
	|	МероприятияТрудовойДеятельности.Организация КАК Организация,
	|	МероприятияТрудовойДеятельности.Сотрудник КАК Сотрудник,
	|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
	|	МероприятияТрудовойДеятельности.Отменено КАК Отменено,
	|	МероприятияТрудовойДеятельности.ДатаМероприятия КАК ДатаМероприятия,
	|	МероприятияТрудовойДеятельности.Подразделение КАК Подразделение,
	|	МероприятияТрудовойДеятельности.Должность КАК Должность,
	|	МероприятияТрудовойДеятельности.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
	|	МероприятияТрудовойДеятельности.ВидМероприятия КАК ВидМероприятия
	|ПОМЕСТИТЬ ВТ_Мероприятия
	|ИЗ
	|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|ГДЕ
	|	МероприятияТрудовойДеятельности.Организация = &Организация
	|	И МероприятияТрудовойДеятельности.ФизическоеЛицо = &ФизическоеЛицо
	|	И МероприятияТрудовойДеятельности.Сотрудник = &Сотрудник
	|	И НЕ МероприятияТрудовойДеятельности.Отменено
	|	И ВЫБОР
	|			КОГДА МероприятияТрудовойДеятельности.ВидМероприятия <> ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Увольнение)
	|				ТОГДА МероприятияТрудовойДеятельности.ДатаМероприятия <= &ТекущаяДата
	|			ИНАЧЕ МероприятияТрудовойДеятельности.ДатаМероприятия = &ДатаМероприятия
	|		КОНЕЦ
	|	И МероприятияТрудовойДеятельности.ВидМероприятия В(&ВидыМероприятий)
	|	И НЕ МероприятияТрудовойДеятельности.ИдМероприятия В
	|				(ВЫБРАТЬ
	|					ВТ_ОтмененныеМероприятия.ИДМероприятия
	|				ИЗ
	|					ВТ_ОтмененныеМероприятия)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Мероприятия.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_Мероприятия.Организация КАК Организация,
	|	ВТ_Мероприятия.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ВТ_Мероприятия.ДатаМероприятия) КАК ДатаМероприятия
	|ПОМЕСТИТЬ ВТ_ПоследнееМероприятие
	|ИЗ
	|	ВТ_Мероприятия КАК ВТ_Мероприятия
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Мероприятия.Организация,
	|	ВТ_Мероприятия.Сотрудник,
	|	ВТ_Мероприятия.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоследнееМероприятие.Организация КАК Организация,
	|	ВТ_ПоследнееМероприятие.Сотрудник КАК Сотрудник,
	|	ВТ_ПоследнееМероприятие.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_Мероприятия.ИдМероприятия КАК ИдМероприятия,
	|	ВТ_Мероприятия.Отменено КАК Отменено
	|ПОМЕСТИТЬ ВТИдМероприятий
	|ИЗ
	|	ВТ_ПоследнееМероприятие КАК ВТ_ПоследнееМероприятие
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Мероприятия КАК ВТ_Мероприятия
	|		ПО ВТ_ПоследнееМероприятие.ФизическоеЛицо = ВТ_Мероприятия.ФизическоеЛицо
	|			И ВТ_ПоследнееМероприятие.Сотрудник = ВТ_Мероприятия.Сотрудник
	|			И ВТ_ПоследнееМероприятие.Организация = ВТ_Мероприятия.Организация
	|			И ВТ_ПоследнееМероприятие.ДатаМероприятия = ВТ_Мероприятия.ДатаМероприятия
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТ_Мероприятия.ВидМероприятия <> ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Увольнение)
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_Мероприятия.ВидМероприятия = ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Прием)
	|							ТОГДА ВТ_Мероприятия.Подразделение = &Подразделение
	|									И ВТ_Мероприятия.Должность = &Должность
	|									И ВТ_Мероприятия.ЯвляетсяСовместителем = &ЯвляетсяСовместителем
	|									И ВТ_Мероприятия.ДатаМероприятия = &ДатаМероприятия
	|						ИНАЧЕ ВТ_Мероприятия.Подразделение = &Подразделение
	|								И ВТ_Мероприятия.Должность = &Должность
	|								И ВТ_Мероприятия.ЯвляетсяСовместителем = &ЯвляетсяСовместителем
	|					КОНЕЦ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИдМероприятий.ИдМероприятия КАК ИдМероприятия
	|ИЗ
	|	ВТИдМероприятий КАК ИдМероприятий";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаписьЭлектроннойТрудовойКнижкиКнопка",
		"Видимость",
		РезультатЗапроса.Пустой());
		
	Если Не РезультатЗапроса.Пустой() Тогда
		СостояниеМероприятияТрудовойДеятельности(Форма, Запрос);
	ИначеЕсли НЕ ЗначениеЗаполнено(Форма.ДатаУвольнения) Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МероприятияТрудовойДеятельности.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МероприятияТрудовойДеятельности.Организация КАК Организация,
		|	МероприятияТрудовойДеятельности.Сотрудник КАК Сотрудник,
		|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельности.Отменено КАК Отменено
		|ИЗ
		|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|ГДЕ
		|	МероприятияТрудовойДеятельности.Организация = &Организация
		|	И МероприятияТрудовойДеятельности.ФизическоеЛицо = &ФизическоеЛицо
		|	И МероприятияТрудовойДеятельности.Сотрудник = &Сотрудник
		|	И НЕ МероприятияТрудовойДеятельности.Отменено
		|	И (МероприятияТрудовойДеятельности.ВидМероприятия = ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Прием)
		|				И МероприятияТрудовойДеятельности.ДатаМероприятия = &ДатаМероприятия
		|			ИЛИ МероприятияТрудовойДеятельности.ВидМероприятия <> ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Прием)
		|				И МероприятияТрудовойДеятельности.ДатаМероприятия >= &ДатаМероприятия)
		|	И МероприятияТрудовойДеятельности.ВидМероприятия <> ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Увольнение)
		|	И НЕ МероприятияТрудовойДеятельности.ИдМероприятия В
		|				(ВЫБРАТЬ
		|					ВТ_ОтмененныеМероприятия.ИДМероприятия
		|				ИЗ
		|					ВТ_ОтмененныеМероприятия)";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Форма.ТребуетсяЗарегистрироватьКадровыйПеревод = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Форма.НадписьЗаписьЭлектроннойТрудовойКнижки = ПредставлениеСтатусаЗаписиЭлектроннойТрудовойКнижки(Форма);
	
	ТекстПодсказки = "";
	Если Форма.ТребуетсяЗарегистрироватьКадровыйПеревод Тогда
		ТекстПодсказки = НСтр("ru = 'Зарегистрируйте и отправьте сведения о кадровом переводе в СФР
									|не позднее 25 числа следующего месяца после издания кадрового приказа'")
	ИначеЕсли ЗначениеЗаполнено(Форма.ДатаУвольнения) Тогда
		ТекстПодсказки = НСтр("ru = 'Зарегистрируйте и отправьте сведения об увольнении в СФР
									|не позднее следующего дня после издания кадрового приказа'")
	ИначеЕсли ЗначениеЗаполнено(Форма.ДатаПриема) Тогда
		ТекстПодсказки = НСтр("ru = 'Зарегистрируйте и отправьте сведения о приеме в СФР
									|не позднее следующего дня после издания кадрового приказа'")
	КонецЕсли;
	Форма.Элементы.ЗаписьЭлектроннойТрудовойКнижкиКнопкаИПодсказка.РасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

Функция МероприятияПриемУвольнение() Экспорт
	
	МероприятияПриемУвольнение = Новый Массив;
	МероприятияПриемУвольнение.Добавить(Перечисления.ВидыМероприятийТрудовойДеятельности.Прием);
	МероприятияПриемУвольнение.Добавить(Перечисления.ВидыМероприятийТрудовойДеятельности.Увольнение);
	МероприятияПриемУвольнение.Добавить(Перечисления.ВидыМероприятийТрудовойДеятельности.НачалоДоговораГПХ);
	МероприятияПриемУвольнение.Добавить(Перечисления.ВидыМероприятийТрудовойДеятельности.ОкончаниеДоговораГПХ);
	
	Возврат МероприятияПриемУвольнение;
	
КонецФункции

Процедура ВТЗадачиПоКадровымМероприятиям(Запрос, ИмяТаблицы) Экспорт
	
	Запрос.УстановитьПараметр("МероприятияПриемУвольнение", МероприятияПриемУвольнение());
	Запрос.УстановитьПараметр("СтатусСдано",                ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Индекс КАК Индекс,
	|	Задачи.СложноеУсловиеПроверки КАК СложноеУсловиеПроверки,
	|	Задачи.УсловиеПроверки КАК УсловиеПроверки,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.ДатаОкончания КАК ДатаОкончания,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
	|	МероприятияТрудовойДеятельности.ДатаДокументаОснования КАК ДатаМероприятия
	|ПОМЕСТИТЬ МероприятияТрудовойДеятельности
	|ИЗ
	|	ЗадачиСИсключениями КАК Задачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|		ПО Задачи.Организация = МероприятияТрудовойДеятельности.Организация
	|			И (ВЫБОР
	|				КОГДА Задачи.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|					ТОГДА МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|							И НАЧАЛОПЕРИОДА(Задачи.ПериодСобытия, ДЕНЬ) = НАЧАЛОПЕРИОДА(МероприятияТрудовойДеятельности.ДатаДокументаОснования, ДЕНЬ)
	|				ИНАЧЕ НЕ МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|						И НАЧАЛОПЕРИОДА(Задачи.ПериодСобытия, МЕСЯЦ) = НАЧАЛОПЕРИОДА(МероприятияТрудовойДеятельности.ДатаДокументаОснования, МЕСЯЦ)
	|			КОНЕЦ)
	|ГДЕ
	|	(МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|				И Задачи.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ИЛИ НЕ МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|				И Задачи.Периодичность <> ЗНАЧЕНИЕ(Перечисление.Периодичность.День))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МероприятияТрудовойДеятельности.Организация КАК Организация,
	|	МероприятияТрудовойДеятельности.ДатаМероприятия КАК ДатаМероприятия,
	|	МероприятияТрудовойДеятельностиПереданные.Регистратор КАК Регистратор,
	|	МероприятияТрудовойДеятельности.Индекс КАК Индекс,
	|	МероприятияТрудовойДеятельности.СложноеУсловиеПроверки КАК СложноеУсловиеПроверки,
	|	МероприятияТрудовойДеятельности.УсловиеПроверки КАК УсловиеПроверки,
	|	МероприятияТрудовойДеятельности.Периодичность КАК Периодичность,
	|	МероприятияТрудовойДеятельности.ДатаОкончания КАК ДатаОкончания,
	|	МероприятияТрудовойДеятельности.ПериодСобытия КАК ПериодСобытия,
	|	МероприятияТрудовойДеятельностиПереданные.Регистратор.ДокументПринятВПФР КАК РегистраторДокументПринятВПФР
	|ПОМЕСТИТЬ ПереданныеКадровыеМероприятия
	|ИЗ
	|	МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
	|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
	|ГДЕ
	|	НЕ МероприятияТрудовойДеятельностиПереданные.Отменено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереданныеКадровыеМероприятия.Индекс КАК Индекс,
	|	ПереданныеКадровыеМероприятия.СложноеУсловиеПроверки КАК СложноеУсловиеПроверки,
	|	ПереданныеКадровыеМероприятия.УсловиеПроверки КАК УсловиеПроверки,
	|	ЖурналОтчетовСтатусы.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПереданныеКадровыеМероприятия.РегистраторДокументПринятВПФР, ЛОЖЬ)
	|			ТОГДА &СтатусСдано
	|		ИНАЧЕ ЖурналОтчетовСтатусы.Статус
	|	КОНЕЦ КАК Статус,
	|	ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности КАК СостояниеСдачиОтчетности,
	|	ЖурналОтчетовСтатусы.ДатаОкончания КАК ДатаОкончания,
	|	ПереданныеКадровыеМероприятия.ПериодСобытия КАК ПериодСобытия,
	|	ПереданныеКадровыеМероприятия.Периодичность КАК Периодичность,
	|	ЖурналОтчетовСтатусы.ВариантОтчета КАК ВариантОтчета
	|ПОМЕСТИТЬ ВТ_ЖурналОтчетовСтатусы
	|ИЗ
	|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПереданныеКадровыеМероприятия КАК ПереданныеКадровыеМероприятия
	|		ПО ЖурналОтчетовСтатусы.Организация = ПереданныеКадровыеМероприятия.Организация
	|			И ЖурналОтчетовСтатусы.Ссылка = ПереданныеКадровыеМероприятия.Регистратор
	|ГДЕ
	|	НЕ ЖурналОтчетовСтатусы.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МероприятияТрудовойДеятельности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПереданныеКадровыеМероприятия";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТ_ЖурналОтчетовСтатусы", ИмяТаблицы);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаполнитьНомераДоговоровРаботыУслуги() Экспорт
	
	НаименованиеДокумента = НСтр("ru = 'Приказ'");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорРаботыУслуги.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ДоговорРаботыУслуги КАК ДоговорРаботыУслуги
	|ГДЕ
	|	ДоговорРаботыУслуги.НомерПервичногоДокумента ПОДОБНО &ПустаяСтрока";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.НомерПервичногоДокумента) Тогда
				ДокументОбъект.НомерПервичногоДокумента = ЭлектронныеТрудовыеКнижкиВызовСервера.НомерНаПечать(ДокументОбъект.Номер);
			КонецЕсли;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не удалось обработать договор ГПХ
			|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				Выборка.Ссылка,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиЗаписиТрудовыхКнижек(Отбор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                Отбор.Организация);
	Запрос.УстановитьПараметр("ПериодСобытия",              Отбор.ПериодСобытия);
	Запрос.УстановитьПараметр("ПериодичностьДень",          Отбор.ПериодичностьДень);
	Запрос.УстановитьПараметр("МероприятияПриемУвольнение", МероприятияПриемУвольнение());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия
	|ПОМЕСТИТЬ МероприятияТрудовойДеятельности
	|ИЗ
	|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ПериодичностьДень
	|				ТОГДА МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|						И НАЧАЛОПЕРИОДА(МероприятияТрудовойДеятельности.ДатаДокументаОснования, ДЕНЬ) = НАЧАЛОПЕРИОДА(&ПериодСобытия, ДЕНЬ)
	|			ИНАЧЕ НЕ МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|						И НАЧАЛОПЕРИОДА(МероприятияТрудовойДеятельности.ДатаДокументаОснования, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодСобытия, МЕСЯЦ)
	|		КОНЕЦ
	|	И МероприятияТрудовойДеятельности.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МероприятияТрудовойДеятельностиПереданные.Регистратор КАК Регистратор,
	|	МероприятияТрудовойДеятельностиПереданные.Регистратор.Дата КАК РегистраторДата
	|ПОМЕСТИТЬ ПереданныеКадровыеМероприятия
	|ИЗ
	|	МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
	|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
	|ГДЕ
	|	НЕ МероприятияТрудовойДеятельностиПереданные.Отменено
	|	И МероприятияТрудовойДеятельностиПереданные.Организация = &Организация
	|	И ТИПЗНАЧЕНИЯ(МероприятияТрудовойДеятельностиПереданные.Регистратор) = ТИП(Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереданныеКадровыеМероприятия.Регистратор КАК Ссылка,
	|	ПереданныеКадровыеМероприятия.РегистраторДата КАК Дата
	|ИЗ
	|	ПереданныеКадровыеМероприятия КАК ПереданныеКадровыеМероприятия
	|
	|СГРУППИРОВАТЬ ПО
	|	ПереданныеКадровыеМероприятия.Регистратор,
	|	ПереданныеКадровыеМероприятия.РегистраторДата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПереданныеКадровыеМероприятия.РегистраторДата УБЫВ";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ЕстьНедооформленныеЗаписиТрудовыхКнижек(Отбор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                Отбор.Организация);
	Запрос.УстановитьПараметр("ПериодСобытия",              Отбор.ПериодСобытия);
	Запрос.УстановитьПараметр("ПериодичностьДень",          Отбор.ПериодичностьДень);
	Запрос.УстановитьПараметр("МероприятияПриемУвольнение", МероприятияПриемУвольнение());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия
	|ПОМЕСТИТЬ МероприятияТрудовойДеятельности
	|ИЗ
	|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ПериодичностьДень
	|				ТОГДА МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|						И НАЧАЛОПЕРИОДА(МероприятияТрудовойДеятельности.ДатаДокументаОснования, ДЕНЬ) = НАЧАЛОПЕРИОДА(&ПериодСобытия, ДЕНЬ)
	|			ИНАЧЕ НЕ МероприятияТрудовойДеятельности.ВидМероприятия В (&МероприятияПриемУвольнение)
	|					И НАЧАЛОПЕРИОДА(МероприятияТрудовойДеятельности.ДатаДокументаОснования, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодСобытия, МЕСЯЦ)
	|		КОНЕЦ
	|	И МероприятияТрудовойДеятельности.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия
	|ИЗ
	|	МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
	|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
	|ГДЕ
	|	МероприятияТрудовойДеятельностиПереданные.Регистратор ЕСТЬ NULL";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ОписаниеРеквизитовКадровогоДокумента(Объект) Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("Дата");
	Описание.Вставить("Организация");
	Описание.Вставить("НаименованиеВторогоДокументаОснования");
	Описание.Вставить("ДатаВторогоДокументаОснования");
	Описание.Вставить("СерияВторогоДокументаОснования");
	Описание.Вставить("НомерВторогоДокументаОснования");
	Описание.Вставить("Ссылка");
	
	ЗаполнитьЗначенияСвойств(Описание, Объект);
	
	Возврат Описание;
	
КонецФункции

Функция ДатаПредставленияЗаписиТрудовойКнижки(РеквизитыДокумента) Экспорт
	
	ЕстьВторойДокументОснование = ЗначениеЗаполнено(РеквизитыДокумента.НаименованиеВторогоДокументаОснования)
		Или ЗначениеЗаполнено(РеквизитыДокумента.ДатаВторогоДокументаОснования)
		Или ЗначениеЗаполнено(РеквизитыДокумента.СерияВторогоДокументаОснования)
		Или ЗначениеЗаполнено(РеквизитыДокумента.НомерВторогоДокументаОснования);
		
	ДатаПриказа = ?(ЕстьВторойДокументОснование,
		Мин(РеквизитыДокумента.ДатаВторогоДокументаОснования, РеквизитыДокумента.Дата),
			РеквизитыДокумента.Дата);
	
	Если ТипЗнч(РеквизитыДокумента.Ссылка) = Тип("ДокументСсылка.КадровыйПеревод") Тогда
		//15 или 25 число в зависимости от периода
		ДатаСобытия = ДобавитьМесяц(ДатаПриказа, 1);
		ДеньСобытия = ?(Год(ДатаПриказа) < 2023, 15, 25);
		ДатаСобытия = Дата(Год(ДатаСобытия), Месяц(ДатаСобытия), ДеньСобытия);
	Иначе
		// Следующий рабочий день
		ДатаСобытия = ДатаПриказа + 86400;
	КонецЕсли;
	
	// Поправка на рабочие дни
	ДатаСобытия = РасчетЗарплатыДляНебольшихОрганизаций.ДатаСобытияСУчетомНерабочихДней(ДатаСобытия, РеквизитыДокумента.Организация, Ложь);
	
	Возврат ДатаСобытия;
	
КонецФункции

Функция ОписаниеКадровогоМероприятияЭТК(ДокументСсылка) Экспорт
	
	ТекстМероприятия = "";
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриемНаРаботу") Тогда
		ТекстМероприятия = НСтр("ru = 'о приеме'");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КадровыйПеревод") Тогда
		ТекстМероприятия = НСтр("ru = 'о переводе'");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.Увольнение") Тогда
		ТекстМероприятия = НСтр("ru = 'об увольнении'");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ДоговорРаботыУслуги") Тогда
		ТекстМероприятия = НСтр("ru = 'о заключении договора'");
	КонецЕсли;
	
	Возврат ТекстМероприятия;
	
КонецФункции

Функция ПолучательКадровогоМероприятияЭТК(ДатаМероприятия) Экспорт
	
	Получатель = ?(Год(ДатаМероприятия) < 2023, НСтр("ru = 'ПФР'"), НСтр("ru = 'СФР'"));
	
	Возврат Получатель;
	
КонецФункции

Функция ТекстПодсказкиКадровогоМероприятияЭТК(ДатаМероприятия, ДокументСсылка, ШаблонНачалаТекста) Экспорт
	
	ТекстМероприятия = УчетЗарплаты.ОписаниеКадровогоМероприятияЭТК(ДокументСсылка);
	ДатаСобытияПредставление = Формат(ДатаМероприятия, "ДЛФ=D");
	Получатель = УчетЗарплаты.ПолучательКадровогоМероприятияЭТК(ДатаМероприятия);
	
	ТекстПодсказки = "";
	Если НачалоДня(ДатаМероприятия) > НачалоДня(ТекущаяДатаСеанса()) Тогда
		// Срок еще не наступил
		ТекстПериодаСобытия = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = 'не позднее %1 г.'"),
				ДатаСобытияПредставление), Новый Шрифт( , , Истина)));
			
		ТекстПодсказки = Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = '%1 %2 в %3 '"),
				ШаблонНачалаТекста, ТекстМероприятия, Получатель),
			ТекстПериодаСобытия);
			
	ИначеЕсли НачалоДня(ДатаМероприятия) = НачалоДня(ТекущаяДатаСеанса()) Тогда
		// Срок - сегодня
		ТекстПериодаСобытия = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(НСтр("ru = 'сегодня'"), Новый Шрифт( , , Истина)));
			
		ТекстПодсказки = Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = '%1 %2 в %3 '"),
				ШаблонНачалаТекста, ТекстМероприятия, Получатель),
			ТекстПериодаСобытия);
			
	ИначеЕсли ДатаМероприятия < ТекущаяДатаСеанса() Тогда
		// Просрочка
		ТекстПериодаСобытия = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = 'просрочено с %1 г.'"),
				ДатаСобытияПредставление), Новый Шрифт( , , Истина)));
			
		ТекстПодсказки = Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = '%1 %2 в %3 ('"),
				ШаблонНачалаТекста, ТекстМероприятия, Получатель),
			ТекстПериодаСобытия,
			НСтр("ru = ')'"));
	КонецЕсли;
	
	Возврат ТекстПодсказки;
	
КонецФункции

Процедура ОписаниеПодсказкиЭТК(Форма) Экспорт
	
	ОтразитьВТрудовойКнижке = Ложь;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		ТекстПодсказки = "";
	Иначе
		Если ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ДоговорРаботыУслуги") Тогда
			ОтразитьВТрудовойКнижке = Форма.Объект.ОтразитьТрудовуюДеятельность;
		Иначе
			ОтразитьВТрудовойКнижке = Форма.Объект.ОтразитьВТрудовойКнижке;
		КонецЕсли;
		
		ШаблонНачалаТекста = НСтр("ru = 'Зарегистрируйте и отправьте сведения'");
		
		ТекстПодсказки =
			УчетЗарплаты.ТекстПодсказкиКадровогоМероприятияЭТК(
				Форма.ДатаПредставленияЗаписиТрудовойКнижки, Форма.Объект.Ссылка, ШаблонНачалаТекста);
		
		Просрочено = НачалоДня(Форма.ДатаПредставленияЗаписиТрудовойКнижки) < НачалоДня(ТекущаяДатаСеанса()) И Форма.ЗаписиТрудовойКнижки.Количество() = 0;
		
		Если Просрочено И ОтразитьВТрудовойКнижке Тогда
			Форма.Элементы.ЗаписьЭлектроннойТрудовойКнижкиКнопка.ЦветФона = ЦветаСтиля.НекорректныйКонтрагентЦветФонаВСписке;
		Иначе
			Форма.Элементы.ЗаписьЭлектроннойТрудовойКнижкиКнопка.ЦветФона = ЦветаСтиля.ФонУправляющегоПоля;
		КонецЕсли;
		
	КонецЕсли;
	Форма.Элементы.ЗаписьЭлектроннойТрудовойКнижкиКнопкаИПодсказка.РасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	Форма.Элементы.ЗаписьЭлектроннойТрудовойКнижкиКнопкаИПодсказка.ОтображениеПодсказки =
		?(ОтразитьВТрудовойКнижке, ОтображениеПодсказки.ОтображатьСнизу, ОтображениеПодсказки.Нет);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеОтчетности

Процедура ПолучитьСведенияОПоказателяхОтчетаПодтверждениеВидаДеятельностиСтрахователяДляФСС(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020000101", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020000102", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020000103", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьПодтверждениеВидаДеятельностиСтрахователяДляФСС(ПараметрыОтчета, Контейнер) Экспорт
	
	Организация             = ПараметрыОтчета.Организация;
	ДатаНачалаПериодаОтчета = ПараметрыОтчета.ДатаНачалаПериодаОтчета;
	ДатаКонцаПериодаОтчета  = ПараметрыОтчета.ДатаКонцаПериодаОтчета;
	
	Контейнер.Приложение2.Вставить("СреднесписочнаяЧисленность", СреднесписочнаяЧисленность(
		Организация, ДатаНачалаПериодаОтчета, ДатаКонцаПериодаОтчета).СреднесписочнаяЧисленностьРаботников);
	
	// Заполнение П000020000103 аналогично заполнению П00010021104 (Выручка) из ОФР Бухгалтерской отчетности.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаПериодаОтчета);
	Запрос.УстановитьПараметр("КонецПериода",  ДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("Счета90",       БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Счет КАК Счет,
	|	ХозрасчетныйОбороты.СуммаОборотДт КАК СуммаОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&Счета90), , Организация = &Организация, , ) КАК ХозрасчетныйОбороты
	|ИТОГИ
	|	СУММА(СуммаОборотДт),
	|	СУММА(СуммаОборотКт)
	|ПО
	|	Счет ТОЛЬКО ИЕРАРХИЯ";
	
	ОборотыПоСчетам = Запрос.Выполнить().Выгрузить();
	ОборотыПоСчетам.Индексы.Добавить("Счет");
	
	КО_90_01 = ЗаполнениеБухгалтерскойОтчетностиИтоги.КО("90.01", ОборотыПоСчетам);
	ДО_90_03 = ЗаполнениеБухгалтерскойОтчетностиИтоги.ДО("90.03", ОборотыПоСчетам);
	ДО_90_04 = ЗаполнениеБухгалтерскойОтчетностиИтоги.ДО("90.04", ОборотыПоСчетам);
	ДО_90_05 = ЗаполнениеБухгалтерскойОтчетностиИтоги.ДО("90.05", ОборотыПоСчетам);
	
	Если Контейнер.Приложение2_ДанныеДопСтрок.Количество() > 0 Тогда
		Контейнер.Приложение2_ДанныеДопСтрок[0]["П000020000103"] = КО_90_01 - ДО_90_03 - ДО_90_04 - ДО_90_05;
	КонецЕсли;
	
КонецПроцедуры

//***********************************
// Среднесписочная численность

Процедура ПолучитьСведенияОПоказателяхОтчетаРегламентированныйОтчетСтатистикаФормаП4ФормаОтчета2012Кв1(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0001001В1", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0001002В1", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0001002А1", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000202", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000203", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000205", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000206", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000208", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000209", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000211", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П030001801", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П030002001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П030002401", Истина, Ложь);

КонецПроцедуры

Процедура ЗаполнитьРегламентированныйОтчетСтатистикаФормаП4ФормаОтчета2012Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	П0001001В1 = "";
	П0001002В1 = "";
	П0001002А1 = "";
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("Организация",    ПараметрыОтчета.Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.КодОКВЭД КАК КодОКВЭД,
	|	Организации.НаименованиеОКВЭД КАК НаименованиеОКВЭД
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		П0001001В1 = Выборка.КодОКВЭД;
		П0001002В1 = Выборка.КодОКВЭД;
		П0001002А1 = Выборка.НаименованиеОКВЭД;
	КонецЕсли;		
	
	Запрос.УстановитьПараметр("НачалоГода", 		       НачалоГода(ПараметрыОтчета.ДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("ОкончаниеИнтервала", 	   КонецМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("ОкончаниеПериодаИнтервала", КонецМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("КонецКвартала", 		       КонецМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета));	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ТекущиеКадровыеДанныеСотрудников.Сотрудник) КАК КоличествоСотрудников
	|ИЗ
	|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|ГДЕ
	|	(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация
	|			ИЛИ ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация ЕСТЬ NULL )
	|	И (ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	|			И ТекущиеКадровыеДанныеСотрудников.ДатаПриема <= &ОкончаниеПериодаИнтервала)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Возврат;
	Иначе
		ФормироватьСНачалаГода = Выборка.КоличествоСотрудников > 15;
	КонецЕсли;
	
	ВидыВыплатСоциальногоХарактера = Новый Массив;
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп9ст255);
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп12_1ст255);
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп13ст255);
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп16ст255);
	ВидыВыплатСоциальногоХарактера.Добавить("2710");		
	ВидыВыплатСоциальногоХарактера.Добавить("2720");
	ВидыВыплатСоциальногоХарактера.Добавить("2760");
	ВидыВыплатСоциальногоХарактера.Добавить("2762");
	ВидыВыплатСоциальногоХарактера.Добавить("2510");		
	
	Запрос.УстановитьПараметр("ВидыВыплатСоциальногоХарактера", ВидыВыплатСоциальногоХарактера);
	
	Если НЕ ФормироватьСНачалаГода Тогда
		Запрос.УстановитьПараметр("НачалоПериодаИнтервала",    НачалоМесяца(ПараметрыОтчета.ДатаНачалаПериодаОтчета));
		Запрос.УстановитьПараметр("НачалоИнтервала", 		   НачалоМесяца(ПараметрыОтчета.ДатаНачалаПериодаОтчета));
	Иначе
		Запрос.УстановитьПараметр("НачалоПериодаИнтервала",    НачалоГода(ПараметрыОтчета.ДатаНачалаПериодаОтчета));
		Запрос.УстановитьПараметр("НачалоИнтервала", 		   НачалоГода(ПараметрыОтчета.ДатаНачалаПериодаОтчета));
	КонецЕсли;
	
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	0 КАК Цифра
	 |ПОМЕСТИТЬ Цифры
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	1
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	2
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	3
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	4
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	5
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	6
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	7
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	8
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	9
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВЫБОР
	 |		КОГДА &НачалоПериодаИнтервала <> ДАТАВРЕМЯ(1, 1, 1)
	 |			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериодаИнтервала, ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	 |		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1980, 1, 1), ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	 |	КОНЕЦ КАК Дата
	 |ПОМЕСТИТЬ Календарь
	 |ИЗ
	 |	Цифры КАК Цифры
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры10
	 |		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 9)
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры100
	 |		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 99)
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры1000
	 |		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 999)
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры10000
	 |		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 9999)
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры100000
	 |		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 99999)
	 |ГДЕ
	 |	ВЫБОР
	 |			КОГДА &НачалоПериодаИнтервала <> ДАТАВРЕМЯ(1, 1, 1)
	 |				ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериодаИнтервала, ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	 |			ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1980, 1, 1), ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	 |		КОНЕЦ МЕЖДУ &НачалоИнтервала И &ОкончаниеИнтервала
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ Цифры
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	Календарь.Дата КАК Дата,
	 |	НАЧАЛОПЕРИОДА(Календарь.Дата, МЕСЯЦ) КАК Месяц
	 |ПОМЕСТИТЬ ВТСписокДат
	 |ИЗ
	 |	Календарь КАК Календарь
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	 |	СписокДат.Дата КАК Дата,
	 |	СписокДат.Месяц,
	 |	ЛОЖЬ КАК Совместитель
	 |ПОМЕСТИТЬ ВТДатыИСотрудники
	 |ИЗ
	 |	ВТСписокДат КАК СписокДат
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	 |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	 |			ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	 |				И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы))
	 |		ПО СписокДат.Дата >= ТекущиеКадровыеДанныеСотрудников.ДатаПриема
	 |			И (СписокДат.Дата <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
	 |					И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	 |				ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
	 |ГДЕ
	 |	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	 |	СписокДат.Дата,
	 |	СписокДат.Месяц,
	 |	ИСТИНА
	 |ИЗ
	 |	ВТСписокДат КАК СписокДат
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	 |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	 |			ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	 |				И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
	 |		ПО СписокДат.Дата >= ТекущиеКадровыеДанныеСотрудников.ДатаПриема
	 |			И (СписокДат.Дата <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
	 |					И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	 |				ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
	 |ГДЕ
	 |	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	СУММА(ВЫБОР
	 |			КОГДА СписокДат.Сотрудник ЕСТЬ НЕ NULL 
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК КоличествоСотрудников,
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СписокДат.Дата) КАК ДнейВМесяце,
	 |	СписокДат.Месяц,
	 |	СписокДат.Совместитель
	 |ПОМЕСТИТЬ ВТСредняяЧисленностьПоМесяцам
	 |ИЗ
	 |	ВТДатыИСотрудники КАК СписокДат
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	СписокДат.Месяц,
	 |	СписокДат.Совместитель
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	НачислениеЗарплатыНачисления.Сотрудник,
	 |	НачислениеЗарплатыНачисления.ОтработаноЧасов,
	 |	НачислениеЗарплатыНачисления.Ссылка.МесяцНачисления,
	 |	НачислениеЗарплатыНачисления.Результат,
	 |	НачислениеЗарплатыНачисления.Начисление
	 |ПОМЕСТИТЬ ВТНачислениеЗарплаты
	 |ИЗ
	 |	Документ.НачислениеЗарплаты.Начисления КАК НачислениеЗарплатыНачисления
	 |ГДЕ
	 |	НачислениеЗарплатыНачисления.Ссылка.Организация = &Организация
	 |	И НачислениеЗарплатыНачисления.Ссылка.МесяцНачисления МЕЖДУ &НачалоИнтервала И &ОкончаниеИнтервала
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВТДатыИСотрудники.Сотрудник,
	 |	ВТДатыИСотрудники.Месяц,
	 |	ВТДатыИСотрудники.Совместитель
	 |ПОМЕСТИТЬ ВТСписокСотрудниковИДат
	 |ИЗ
	 |	ВТДатыИСотрудники КАК ВТДатыИСотрудники
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ВТДатыИСотрудники.Сотрудник,
	 |	ВТДатыИСотрудники.Месяц,
	 |	ВТДатыИСотрудники.Совместитель
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	Начисления.Ссылка КАК Начисление
	 |ПОМЕСТИТЬ ВТВидыНачисленийСоциальногоХарактера
	 |ИЗ
	 |	ПланВидовРасчета.Начисления КАК Начисления
	 |ГДЕ
	 |	(Начисления.ВидНачисленияДляНУ В (&ВидыВыплатСоциальногоХарактера)
	 |			ИЛИ Начисления.КодДоходаНДФЛ.Код В (&ВидыВыплатСоциальногоХарактера))
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВЫБОР
	 |		КОГДА СредняяЧисленностьПоМесяцам.Совместитель
	 |			ТОГДА 0
	 |		ИНАЧЕ ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.КоличествоСотрудников / СредняяЧисленностьПоМесяцам.ДнейВМесяце КАК ЧИСЛО(15, 0))) / КОЛИЧЕСТВО(СредняяЧисленностьПоМесяцам.Месяц), 0)
	 |	КОНЕЦ КАК СреднесписочнаяЧисленность,
	 |	ВЫБОР
	 |		КОГДА СредняяЧисленностьПоМесяцам.Совместитель
	 |			ТОГДА ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.КоличествоСотрудников / СредняяЧисленностьПоМесяцам.ДнейВМесяце КАК ЧИСЛО(15, 0))) / КОЛИЧЕСТВО(СредняяЧисленностьПоМесяцам.Месяц), 0)
	 |		ИНАЧЕ 0
	 |	КОНЕЦ КАК СреднесписочнаяЧисленностьСовместители
	 |ИЗ
	 |	ВТСредняяЧисленностьПоМесяцам КАК СредняяЧисленностьПоМесяцам
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	СредняяЧисленностьПоМесяцам.Совместитель
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВЫРАЗИТЬ(ЕСТЬNULL(НачислениеЗарплатыНачисления.Результат, 0) / 1000 КАК ЧИСЛО(15, 1)) КАК ВыплатыСоциальногоХарактера,
	 |	НачислениеЗарплатыНачисления.Сотрудник
	 |ИЗ
	 |	Документ.НачислениеЗарплаты.Начисления КАК НачислениеЗарплатыНачисления
	 |ГДЕ
	 |	НачислениеЗарплатыНачисления.Ссылка.Организация = &Организация
	 |	И НачислениеЗарплатыНачисления.Ссылка.МесяцНачисления МЕЖДУ &НачалоГода И &ОкончаниеИнтервала
	 |	И НачислениеЗарплатыНачисления.Начисление В
	 |			(ВЫБРАТЬ
	 |				ВидыНачисленийСоциальногоХарактера.Начисление КАК Начисление
	 |			ИЗ
	 |				ВТВидыНачисленийСоциальногоХарактера КАК ВидыНачисленийСоциальногоХарактера)
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВТСписокСотрудниковИДат.Сотрудник,
	 |	ВТСписокСотрудниковИДат.Месяц,
	 |	СУММА(ЕСТЬNULL(ВТНачислениеЗарплаты.ОтработаноЧасов, 0)) КАК ОтработаноЧасов,
	 |	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(ВТНачислениеЗарплаты.Результат, 0) / 1000 КАК ЧИСЛО(15, 1))) КАК Результат,
	 |	0 КАК ОтработаноЧасовСовместитель,
	 |	0 КАК РезультатСовместитель,
	 |	ВТСписокСотрудниковИДат.Совместитель
	 |ИЗ
	 |	ВТСписокСотрудниковИДат КАК ВТСписокСотрудниковИДат
	 |		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеЗарплаты КАК ВТНачислениеЗарплаты
	 |		ПО ВТСписокСотрудниковИДат.Сотрудник = ВТНачислениеЗарплаты.Сотрудник
	 |			И ВТСписокСотрудниковИДат.Месяц = ВТНачислениеЗарплаты.МесяцНачисления
	 |ГДЕ
	 |	НЕ ВТСписокСотрудниковИДат.Совместитель
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ВТСписокСотрудниковИДат.Сотрудник,
	 |	ВТСписокСотрудниковИДат.Месяц,
	 |	ВТСписокСотрудниковИДат.Совместитель
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	ВТСписокСотрудниковИДат.Сотрудник,
	 |	ВТСписокСотрудниковИДат.Месяц,
	 |	0,
	 |	0,
	 |	СУММА(ЕСТЬNULL(ВТНачислениеЗарплаты.ОтработаноЧасов, 0)),
	 |	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(ВТНачислениеЗарплаты.Результат, 0) / 1000 КАК ЧИСЛО(15, 1))),
	 |	ВТСписокСотрудниковИДат.Совместитель
	 |ИЗ
	 |	ВТСписокСотрудниковИДат КАК ВТСписокСотрудниковИДат
	 |		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеЗарплаты КАК ВТНачислениеЗарплаты
	 |		ПО ВТСписокСотрудниковИДат.Сотрудник = ВТНачислениеЗарплаты.Сотрудник
	 |			И ВТСписокСотрудниковИДат.Месяц = ВТНачислениеЗарплаты.МесяцНачисления
	 |ГДЕ
	 |	ВТСписокСотрудниковИДат.Совместитель
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ВТСписокСотрудниковИДат.Сотрудник,
	 |	ВТСписокСотрудниковИДат.Месяц,
	 |	ВТСписокСотрудниковИДат.Совместитель
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	КОЛИЧЕСТВО(ТекущиеКадровыеДанныеСотрудников.ДатаПриема) КАК Принятые,
	 |	0 КАК Уволенные
	 |ИЗ
	 |	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	 |		ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	 |			И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы))
	 |ГДЕ
	 |	ТекущиеКадровыеДанныеСотрудников.ДатаПриема >= &НачалоГода
	 |	И ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	0,
	 |	КОЛИЧЕСТВО(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения)
	 |ИЗ
	 |	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	 |		ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	 |			И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы))
	 |ГДЕ
	 |	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <= &ОкончаниеПериодаИнтервала
	 |	И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	 |	И ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация";
	 
	 Запрос.ВыполнитьПакет();
	 
	 Результаты = Запрос.ВыполнитьПакет();
	 КоличестовРезультатов = Результаты.Количество();
	 
	 СведенияОЧисленности           = Результаты[КоличестовРезультатов-4].Выбрать();
	 П010000202 = 0;
	 П010000203 = 0;
	 Пока СведенияОЧисленности.Следующий() Цикл
		 П010000202 = П010000202 + СведенияОЧисленности.СреднесписочнаяЧисленность;
		 П010000203 = П010000203 + СведенияОЧисленности.СреднесписочнаяЧисленностьСовместители;
	 КонецЦикла;
	 
	 СведенияОСоциальныхВыплатах    = Результаты[КоличестовРезультатов-3].Выбрать();
	 П010000211 = 0;
	 Пока СведенияОСоциальныхВыплатах.Следующий() Цикл
		 П010000211 = П010000211 + СведенияОСоциальныхВыплатах.ВыплатыСоциальногоХарактера;
	 КонецЦикла;
	 
	 СведенияОЗаработнойПлате       = Результаты[КоличестовРезультатов-2].Выбрать();
	 П010000205 = 0;
	 П010000206 = 0;
	 П010000208 = 0;
	 П010000209 = 0;
	 Пока СведенияОЗаработнойПлате.Следующий() Цикл
		 П010000205 = П010000205 + СведенияОЗаработнойПлате.ОтработаноЧасов;
		 П010000206 = П010000206 + СведенияОЗаработнойПлате.ОтработаноЧасовСовместитель;
		 П010000208 = П010000208 + СведенияОЗаработнойПлате.Результат;
		 П010000209 = П010000209 + СведенияОЗаработнойПлате.РезультатСовместитель;
	 КонецЦикла;
	 
	 СведенияОбИзмененииЧисленности = Результаты[КоличестовРезультатов-1].Выбрать();
	 П030001801 = 0;
	 П030002001 = 0;
	 Пока СведенияОбИзмененииЧисленности.Следующий() Цикл
		 П030001801 = П030001801 + СведенияОбИзмененииЧисленности.Принятые;
		 П030002001 = П030002001 + СведенияОбИзмененииЧисленности.Уволенные;
	 КонецЦикла;
	 
	 ПоляОтчета = Контейнер.ПолеТабличногоДокументаФормаОтчета;
	 
	 ПоляОтчета.П0001001В1 = П0001001В1;
	 ПоляОтчета.П0001002В1 = П0001002В1;
	 ПоляОтчета.П0001002А1 = П0001002А1;
	 
	 ПоляОтчета.П010000202 = П010000202;
	 ПоляОтчета.П010000203 = П010000203;
	 ПоляОтчета.П010000205 = П010000205;
	 ПоляОтчета.П010000206 = П010000206;
	 ПоляОтчета.П010000211 = П010000211;
	 ПоляОтчета.П010000205 = П010000205;
	 ПоляОтчета.П010000206 = П010000206;
	 ПоляОтчета.П010000208 = П010000208;
	 ПоляОтчета.П010000209 = П010000209;
	 ПоляОтчета.П030001801 = П030001801;
	 ПоляОтчета.П030002001 = П030002001;
	 ПоляОтчета.П030002401 = П010000202;
	 
КонецПроцедуры

// Возвращает данные о средней численности и выплатах сотрудникам в виде массива результатов запроса
//
// Параметры:
//  ПараметрыОтчета				 - Структура - обязательно должна содержать свойство "Организации" с массивом организаций. 
//  ДатаНачала					 - Дата - дата начала отчетного периода.
//  ДатаКонца					 - Дата - дата окончания отчетного периода.
//  ИсключаемыеПодразделения	 - Массив - массив обособленных подразделений, данные по которым требуется изключить из расчета.
//									Имеет смысл, если отчет заполняется по организации без обособленных подразделений
//									(ПараметрыОтчета.ИсключитьДанныеОбособленныхПодразделений = Истина)
//  ПодразделенияОбособленного	 - Массив - массив обособленных подразделений, данные по которым включаются в расчет.
//									Имеет смысл, если отчет заполняется по обособленному подразделению
//									(ПараметрыОтчета.ИсключитьДанныеОбособленныхПодразделений = Ложь)
// 
// Возвращаемое значение:
//   - Массив - массив результатов запроса
//
Функция СведенияОЧисленностиИВыплатах(ПараметрыОтчета, Знач ДатаНачала, Знач ДатаКонца, Знач ИсключаемыеПодразделения, Знач ПодразделенияОбособленного) Экспорт

	ВидыВыплатСоциальногоХарактера = Новый Массив;
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп9ст255);
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп12_1ст255);
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп13ст255);
	ВидыВыплатСоциальногоХарактера.Добавить(Перечисления.ВидыНачисленийОплатыТрудаДляНУ.пп16ст255);
	ВидыВыплатСоциальногоХарактера.Добавить("2710");
	ВидыВыплатСоциальногоХарактера.Добавить("2720");
	ВидыВыплатСоциальногоХарактера.Добавить("2760");
	ВидыВыплатСоциальногоХарактера.Добавить("2762");
	ВидыВыплатСоциальногоХарактера.Добавить("2510");
	
	ВидыИсключаемыхВыплат = Новый Массив;
	ВидВыплаты = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыДоходовПоСтраховымВзносам.ПособияЗаСчетФСС");
	Если ВидВыплаты <> Неопределено Тогда
		ВидыИсключаемыхВыплат.Добавить(ВидВыплаты);
	КонецЕсли;
	ВидВыплаты = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыДоходовПоСтраховымВзносам.ПособияЗаСчетФСС_НС");
	Если ВидВыплаты <> Неопределено Тогда
		ВидыИсключаемыхВыплат.Добавить(ВидВыплаты);
	КонецЕсли;
	ВидыИсключаемыхВыплат.Добавить("2300");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыВыплатСоциальногоХарактера",	ВидыВыплатСоциальногоХарактера);
	Запрос.УстановитьПараметр("ВидыИсключаемыхВыплат",			ВидыИсключаемыхВыплат);
	Запрос.УстановитьПараметр("НачалоГода",						НачалоГода(ДатаКонца));
	Запрос.УстановитьПараметр("ОкончаниеИнтервала",				КонецМесяца(ДатаКонца));
	Запрос.УстановитьПараметр("ОкончаниеПериодаИнтервала",		КонецМесяца(ДатаКонца));
	Запрос.УстановитьПараметр("КонецКвартала",					КонецМесяца(ДатаКонца));
	Запрос.УстановитьПараметр("Организации",					ПараметрыОтчета.Организации);
	
	Если НачалоМесяца(ДатаНачала) = НачалоМесяца(ДатаКонца) Тогда
		Запрос.УстановитьПараметр("НачалоПериодаИнтервала",		НачалоМесяца(ДатаНачала));
		Запрос.УстановитьПараметр("НачалоИнтервала",			НачалоМесяца(ДатаНачала));
	Иначе
		Запрос.УстановитьПараметр("НачалоПериодаИнтервала",		НачалоГода(ДатаНачала));
		Запрос.УстановитьПараметр("НачалоИнтервала",			НачалоГода(ДатаНачала));
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("ЗаполняетсяВЦеломПоОрганизации") И ПараметрыОтчета.ЗаполняетсяВЦеломПоОрганизации Тогда
		Запрос.УстановитьПараметр("ПоВсейОрганизации", Истина);
		Запрос.УстановитьПараметр("ИсключитьДанныеОбособленныхПодразделений", Ложь);
		Запрос.УстановитьПараметр("ПоОбособленномуПодразделению", Ложь);
	ИначеЕсли ПараметрыОтчета.Свойство("ИсключитьДанныеОбособленныхПодразделений") 
		И ПараметрыОтчета.ИсключитьДанныеОбособленныхПодразделений Тогда
		Запрос.УстановитьПараметр("ПоВсейОрганизации", Ложь);
		Запрос.УстановитьПараметр("ИсключитьДанныеОбособленныхПодразделений", Истина);
		Запрос.УстановитьПараметр("ПоОбособленномуПодразделению", Ложь);
		Запрос.УстановитьПараметр("ПоВсейОрганизации", Ложь);
	Иначе
		Запрос.УстановитьПараметр("ПоВсейОрганизации", Ложь);
		Запрос.УстановитьПараметр("ИсключитьДанныеОбособленныхПодразделений", Ложь);
		Запрос.УстановитьПараметр("ПоОбособленномуПодразделению", ПодразделенияОбособленного.Количество() > 0);
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("ИсключаемыеПодразделения", ИсключаемыеПодразделения);
	Запрос.УстановитьПараметр("ПодразделенияОбособленного",	ПодразделенияОбособленного);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	0 КАК Цифра
	|ПОМЕСТИТЬ Цифры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &НачалоПериодаИнтервала <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериодаИнтервала, ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1980, 1, 1), ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	|	КОНЕЦ КАК Дата
	|ПОМЕСТИТЬ Календарь
	|ИЗ
	|	Цифры КАК Цифры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры10
	|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 9)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры100
	|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 99)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры1000
	|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 999)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры10000
	|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 9999)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цифры КАК Цифры100000
	|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ДЕНЬ) > 99999)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &НачалоПериодаИнтервала <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериодаИнтервала, ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1980, 1, 1), ДЕНЬ, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ДЕНЬ)
	|		КОНЕЦ МЕЖДУ &НачалоИнтервала И &ОкончаниеИнтервала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Цифры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Календарь.Дата КАК Дата,
	|	НАЧАЛОПЕРИОДА(Календарь.Дата, МЕСЯЦ) КАК Месяц
	|ПОМЕСТИТЬ ВТСписокДат
	|ИЗ
	|	Календарь КАК Календарь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация КАК Организация,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	СписокДат.Дата КАК Дата,
	|	СписокДат.Месяц КАК Месяц,
	|	ЛОЖЬ КАК Совместитель
	|ПОМЕСТИТЬ ВТДатыИСотрудники
	|ИЗ
	|	ВТСписокДат КАК СписокДат
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|			ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	|				И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы))
	|		ПО СписокДат.Дата >= ТекущиеКадровыеДанныеСотрудников.ДатаПриема
	|			И (СписокДат.Дата <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
	|					И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
	|ГДЕ
	|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация В(&Организации)
	|	И (&ПоВсейОрганизации 
	|			ИЛИ &ИсключитьДанныеОбособленныхПодразделений
	|				И НЕ ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение В (&ИсключаемыеПодразделения)
	|			ИЛИ &ПоОбособленномуПодразделению
	|				И ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение В (&ПодразделенияОбособленного))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	СписокДат.Дата,
	|	СписокДат.Месяц,
	|	ИСТИНА
	|ИЗ
	|	ВТСписокДат КАК СписокДат
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|			ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	|				И (ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
	|		ПО СписокДат.Дата >= ТекущиеКадровыеДанныеСотрудников.ДатаПриема
	|			И (СписокДат.Дата <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
	|					И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
	|ГДЕ
	|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация В(&Организации)
	|	И (&ПоВсейОрганизации 
	|			ИЛИ &ИсключитьДанныеОбособленныхПодразделений
	|				И НЕ ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение В (&ИсключаемыеПодразделения)
	|			ИЛИ &ПоОбособленномуПодразделению
	|				И ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение В (&ПодразделенияОбособленного))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА СписокДат.Сотрудник ЕСТЬ НЕ NULL 
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоСотрудников,
	|	СУММА(ВЫБОР
	|			КОГДА СписокДат.Сотрудник.ФизическоеЛицо.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Женский)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоСотрудниковЖенщин,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СписокДат.Дата) КАК ДнейВМесяце,
	|	СписокДат.Месяц КАК Месяц,
	|	СписокДат.Совместитель КАК Совместитель
	|ПОМЕСТИТЬ ВТСредняяЧисленностьПоМесяцам
	|ИЗ
	|	ВТДатыИСотрудники КАК СписокДат
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокДат.Месяц,
	|	СписокДат.Совместитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Ссылка КАК Начисление
	|ПОМЕСТИТЬ ВТВидыИсключаемыхВыплат
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	(Начисления.ВидНачисленияДляНУ В (&ВидыИсключаемыхВыплат)
	|			ИЛИ Начисления.КодДоходаНДФЛ.Код В (&ВидыИсключаемыхВыплат))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Ссылка КАК Начисление
	|ПОМЕСТИТЬ ВТВидыНачисленийСоциальногоХарактера
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	(Начисления.ВидНачисленияДляНУ В (&ВидыВыплатСоциальногоХарактера)
	|			ИЛИ Начисления.КодДоходаНДФЛ.Код В (&ВидыВыплатСоциальногоХарактера))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыНачисления.Ссылка.Организация КАК Организация,
	|	НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
	|	НачислениеЗарплатыНачисления.ОтработаноЧасов КАК ОтработаноЧасов,
	|	НачислениеЗарплатыНачисления.Ссылка.МесяцНачисления КАК МесяцНачисления,
	|	НачислениеЗарплатыНачисления.Результат КАК Результат,
	|	НачислениеЗарплатыНачисления.Начисление КАК Начисление
	|ПОМЕСТИТЬ ВТНачислениеЗарплаты
	|ИЗ
	|	Документ.НачислениеЗарплаты.Начисления КАК НачислениеЗарплатыНачисления
	|ГДЕ
	|	НачислениеЗарплатыНачисления.Ссылка.Организация В(&Организации)
	|	И (&ПоВсейОрганизации 
	|			ИЛИ &ИсключитьДанныеОбособленныхПодразделений
	|				И НЕ НачислениеЗарплатыНачисления.Подразделение В (&ИсключаемыеПодразделения)
	|			ИЛИ &ПоОбособленномуПодразделению
	|				И НачислениеЗарплатыНачисления.Подразделение В (&ПодразделенияОбособленного))
	|	И НачислениеЗарплатыНачисления.Ссылка.МесяцНачисления МЕЖДУ &НачалоИнтервала И &ОкончаниеИнтервала
	|	И НЕ НачислениеЗарплатыНачисления.Начисление В
	|				(ВЫБРАТЬ
	|					ВТВидыИсключаемыхВыплат.Начисление
	|				ИЗ
	|					ВТВидыИсключаемыхВыплат КАК ВТВидыИсключаемыхВыплат)
	|	И НачислениеЗарплатыНачисления.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтпускНачисления.Ссылка.Организация,
	|	ОтпускНачисления.Сотрудник,
	|	0,
	|	ОтпускНачисления.Ссылка.ПериодРегистрации,
	|	ОтпускНачисления.Результат,
	|	ОтпускНачисления.Начисление
	|ИЗ
	|	Документ.Отпуск.Начисления КАК ОтпускНачисления
	|ГДЕ
	|	ОтпускНачисления.Ссылка.Организация В(&Организации)
	|	И (&ПоВсейОрганизации 
	|			ИЛИ &ИсключитьДанныеОбособленныхПодразделений
	|				И НЕ ОтпускНачисления.Подразделение В (&ИсключаемыеПодразделения)
	|			ИЛИ &ПоОбособленномуПодразделению
	|				И ОтпускНачисления.Подразделение В (&ПодразделенияОбособленного))
	|	И ОтпускНачисления.Ссылка.ПериодРегистрации МЕЖДУ &НачалоИнтервала И &ОкончаниеИнтервала
	|	И НЕ ОтпускНачисления.Начисление В
	|				(ВЫБРАТЬ
	|					ВТВидыИсключаемыхВыплат.Начисление
	|				ИЗ
	|					ВТВидыИсключаемыхВыплат КАК ВТВидыИсключаемыхВыплат)
	|	И ОтпускНачисления.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДатыИСотрудники.Организация КАК Организация,
	|	ВТДатыИСотрудники.Сотрудник КАК Сотрудник,
	|	ВТДатыИСотрудники.Месяц КАК Месяц,
	|	ВТДатыИСотрудники.Совместитель КАК Совместитель
	|ПОМЕСТИТЬ ВТСписокСотрудниковИДат
	|ИЗ
	|	ВТДатыИСотрудники КАК ВТДатыИСотрудники
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДатыИСотрудники.Организация,
	|	ВТДатыИСотрудники.Сотрудник,
	|	ВТДатыИСотрудники.Месяц,
	|	ВТДатыИСотрудники.Совместитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СредняяЧисленностьПоМесяцам.Совместитель
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.КоличествоСотрудников / СредняяЧисленностьПоМесяцам.ДнейВМесяце КАК ЧИСЛО(15, 0))) / КОЛИЧЕСТВО(СредняяЧисленностьПоМесяцам.Месяц), 0)
	|	КОНЕЦ КАК СреднесписочнаяЧисленность,
	|	ВЫБОР
	|		КОГДА СредняяЧисленностьПоМесяцам.Совместитель
	|			ТОГДА ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.КоличествоСотрудников / СредняяЧисленностьПоМесяцам.ДнейВМесяце КАК ЧИСЛО(15, 0))) / КОЛИЧЕСТВО(СредняяЧисленностьПоМесяцам.Месяц), 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СреднесписочнаяЧисленностьСовместители,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.КоличествоСотрудниковЖенщин / СредняяЧисленностьПоМесяцам.ДнейВМесяце КАК ЧИСЛО(15, 0))) / КОЛИЧЕСТВО(СредняяЧисленностьПоМесяцам.Месяц), 0) КАК СреднесписочнаяЧисленностьЖенщины
	|ИЗ
	|	ВТСредняяЧисленностьПоМесяцам КАК СредняяЧисленностьПоМесяцам
	|
	|СГРУППИРОВАТЬ ПО
	|	СредняяЧисленностьПоМесяцам.Совместитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ЕСТЬNULL(НачислениеЗарплатыНачисления.Результат, 0) / 1000 КАК ЧИСЛО(15, 1)) КАК ВыплатыСоциальногоХарактера,
	|	НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
	|ИЗ
	|	Документ.НачислениеЗарплаты.Начисления КАК НачислениеЗарплатыНачисления
	|ГДЕ
	|	НачислениеЗарплатыНачисления.Ссылка.Организация В(&Организации)
	|	И (&ПоВсейОрганизации 
	|			ИЛИ &ИсключитьДанныеОбособленныхПодразделений
	|				И НЕ НачислениеЗарплатыНачисления.Подразделение В (&ИсключаемыеПодразделения)
	|			ИЛИ &ПоОбособленномуПодразделению
	|				И НачислениеЗарплатыНачисления.Подразделение В (&ПодразделенияОбособленного))
	|	И НачислениеЗарплатыНачисления.Ссылка.МесяцНачисления МЕЖДУ &НачалоГода И &ОкончаниеИнтервала
	|	И НачислениеЗарплатыНачисления.Начисление В
	|			(ВЫБРАТЬ
	|				ВидыНачисленийСоциальногоХарактера.Начисление КАК Начисление
	|			ИЗ
	|				ВТВидыНачисленийСоциальногоХарактера КАК ВидыНачисленийСоциальногоХарактера)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСписокСотрудниковИДат.Сотрудник КАК Сотрудник,
	|	ВТСписокСотрудниковИДат.Месяц КАК Месяц,
	|	СУММА(ЕСТЬNULL(ВТНачислениеЗарплаты.ОтработаноЧасов, 0)) КАК ОтработаноЧасов,
	|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(ВТНачислениеЗарплаты.Результат, 0) / 1000 КАК ЧИСЛО(15, 1))) КАК Результат,
	|	0 КАК ОтработаноЧасовСовместитель,
	|	0 КАК РезультатСовместитель,
	|	ВТСписокСотрудниковИДат.Совместитель КАК Совместитель
	|ИЗ
	|	ВТСписокСотрудниковИДат КАК ВТСписокСотрудниковИДат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеЗарплаты КАК ВТНачислениеЗарплаты
	|		ПО ВТСписокСотрудниковИДат.Организация = ВТНачислениеЗарплаты.Организация
	|			И ВТСписокСотрудниковИДат.Сотрудник = ВТНачислениеЗарплаты.Сотрудник
	|			И ВТСписокСотрудниковИДат.Месяц = ВТНачислениеЗарплаты.МесяцНачисления
	|ГДЕ
	|	НЕ ВТСписокСотрудниковИДат.Совместитель
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСписокСотрудниковИДат.Сотрудник,
	|	ВТСписокСотрудниковИДат.Месяц,
	|	ВТСписокСотрудниковИДат.Совместитель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСписокСотрудниковИДат.Сотрудник,
	|	ВТСписокСотрудниковИДат.Месяц,
	|	0,
	|	0,
	|	СУММА(ЕСТЬNULL(ВТНачислениеЗарплаты.ОтработаноЧасов, 0)),
	|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(ВТНачислениеЗарплаты.Результат, 0) / 1000 КАК ЧИСЛО(15, 1))),
	|	ВТСписокСотрудниковИДат.Совместитель
	|ИЗ
	|	ВТСписокСотрудниковИДат КАК ВТСписокСотрудниковИДат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеЗарплаты КАК ВТНачислениеЗарплаты
	|		ПО ВТСписокСотрудниковИДат.Организация = ВТНачислениеЗарплаты.Организация
	|			И ВТСписокСотрудниковИДат.Сотрудник = ВТНачислениеЗарплаты.Сотрудник
	|			И ВТСписокСотрудниковИДат.Месяц = ВТНачислениеЗарплаты.МесяцНачисления
	|ГДЕ
	|	ВТСписокСотрудниковИДат.Совместитель
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСписокСотрудниковИДат.Сотрудник,
	|	ВТСписокСотрудниковИДат.Месяц,
	|	ВТСписокСотрудниковИДат.Совместитель";
	
	Возврат Запрос.ВыполнитьПакет();

КонецФункции

//***********************************
// 4-ФСС

Функция ПолучитьСведенияОПоказателях4ФСС(ПоказателиОтчета, ИДРедакцииОтчета) Экспорт
	
	Если ИДРедакцииОтчета = "ФормаОтчета2022Кв2" Тогда
		Возврат ПолучитьСведенияОПоказателях4ФСС_2022Кв2(ПоказателиОтчета, ИДРедакцииОтчета);
	Иначе
		Возврат ПолучитьСведенияОПоказателях4ФСС_2017Кв1(ПоказателиОтчета, ИДРедакцииОтчета);
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСведенияОПоказателях4ФСС_2022Кв2(ПоказателиОтчета, ИДРедакцииОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000920000101", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000920000102", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000920000103", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000920000104", Истина, Истина);
	
	Возврат ПоказателиОтчета;
	
КонецФункции

Функция ПолучитьСведенияОПоказателях4ФСС_2017Кв1(ПоказателиОтчета, ИДРедакцииОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016104", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016105", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016101", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016204", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016205", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016201", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016304", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016305", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020016301", Истина, Истина);
	
	Возврат ПоказателиОтчета;
	
КонецФункции

Процедура ЗаполнитьРегламентированныйОтчет4ФСС(ПараметрыОтчета, Контейнер, ИДРедакцииОтчета) Экспорт
	
	Если ИДРедакцииОтчета = "ФормаОтчета2022Кв2" Тогда
		ЗаполнитьРегламентированныйОтчет4ФСС_2022Кв2(ПараметрыОтчета, Контейнер, ИДРедакцииОтчета);
	Иначе
		ЗаполнитьРегламентированныйОтчет4ФСС_2017Кв1(ПараметрыОтчета, Контейнер, ИДРедакцииОтчета);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРегламентированныйОтчет4ФСС_2022Кв2(ПараметрыОтчета, Контейнер, ИДРедакцииОтчета) Экспорт
	
	Организация = ПараметрыОтчета.Организация;
	ДатаКонцаПериодаОтчета = ПараметрыОтчета.ДатаКонцаПериодаОтчета;
	НачалоТекущегоКвартала = НачалоКвартала(ДатаКонцаПериодаОтчета);
	
	НачалоПериода = НачалоГода(ДатаКонцаПериодаОтчета);
	ОкончаниеПериода = КонецМесяца(ДатаКонцаПериодаОтчета);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("КонецОтчетногоПериода",  КонецДня(ДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("НачалоТекущегоКвартала", НачалоТекущегоКвартала);
	Запрос.УстановитьПараметр("Месяц1",                 Месяц(НачалоТекущегоКвартала));
	Запрос.УстановитьПараметр("Месяц3",                 Месяц(НачалоТекущегоКвартала) + 2);
	Запрос.УстановитьПараметр("ПустаяДата",             Дата(1,1,1));
	Запрос.УстановитьПараметр("Налог",                  Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(
															Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ, Истина));
	
	УплаченоТекст =
	"ВЫБРАТЬ
	|	СписаниеСРасчетногоСчета.НомерВходящегоДокумента КАК ПлатежноеПоручениеНомер,
	|	СписаниеСРасчетногоСчета.ДатаВходящегоДокумента КАК ПлатежноеПоручениеДата,
	|	СписаниеСРасчетногоСчета.СуммаДокумента КАК ФССНесчастныеСлучаиУплачено,
	|	ВЫБОР
	|		КОГДА СписаниеСРасчетногоСчета.ДатаВходящегоДокумента = &ПустаяДата
	|			ТОГДА НАЧАЛОПЕРИОДА(СписаниеСРасчетногоСчета.Дата, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СписаниеСРасчетногоСчета.ДатаВходящегоДокумента, МЕСЯЦ)
	|	КОНЕЦ КАК Период
	|ПОМЕСТИТЬ ВТРасчетыСФондом
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Проведен
	|	И СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога)
	|	И СписаниеСРасчетногоСчета.Налог = &Налог
	|	И СписаниеСРасчетногоСчета.ВидНалоговогоОбязательства = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог)
	|	И СписаниеСРасчетногоСчета.Дата МЕЖДУ &НачалоТекущегоКвартала И &КонецОтчетногоПериода
	|	И СписаниеСРасчетногоСчета.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Номер,
	|	РасходныйКассовыйОрдер.Дата,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдер.Дата, МЕСЯЦ)
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.Проведен
	|	И РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога)
	|	И РасходныйКассовыйОрдер.Налог = &Налог
	|	И РасходныйКассовыйОрдер.ВидНалоговогоОбязательства = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог)
	|	И РасходныйКассовыйОрдер.Дата МЕЖДУ &НачалоТекущегоКвартала И &КонецОтчетногоПериода
	|	И РасходныйКассовыйОрдер.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСФондом.ПлатежноеПоручениеНомер КАК П000920000102,
	|	РасчетыСФондом.ПлатежноеПоручениеДата КАК П000920000103,
	|	РасчетыСФондом.ФССНесчастныеСлучаиУплачено КАК П000920000104
	|ИЗ
	|	ВТРасчетыСФондом КАК РасчетыСФондом
	|ГДЕ
	|	РасчетыСФондом.ФССНесчастныеСлучаиУплачено <> 0
	|	И МЕСЯЦ(РасчетыСФондом.Период) МЕЖДУ &Месяц1 И &Месяц3
	|
	|УПОРЯДОЧИТЬ ПО
	|	П000920000103";
	
	ПоказателиОтчета = Новый Структура;
	ПолучитьСведенияОПоказателях4ФСС(ПоказателиОтчета, ИДРедакцииОтчета);
	
	Контейнер.Таблица92_ДанныеДопСтрок.Очистить();
	
	Запрос.Текст = УплаченоТекст;
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если РезультатЗапроса.Количество() > 0 Тогда
		НомерПП = 1;
		Для каждого СтрокаДанных Из РезультатЗапроса Цикл
			НоваяСтрока = Контейнер.Таблица92_ДанныеДопСтрок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
			НоваяСтрока.П000920000101 = НомерПП;
			НомерПП = НомерПП + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРегламентированныйОтчет4ФСС_2017Кв1(ПараметрыОтчета, Контейнер, ИДРедакцииОтчета) Экспорт
	
	Организация = ПараметрыОтчета.Организация;
	ДатаКонцаПериодаОтчета = ПараметрыОтчета.ДатаКонцаПериодаОтчета;
	НачалоТекущегоКвартала = НачалоКвартала(ДатаКонцаПериодаОтчета);
	
	НачалоПериода = НачалоГода(ДатаКонцаПериодаОтчета);
	ОкончаниеПериода = КонецМесяца(ДатаКонцаПериодаОтчета);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("НачалоОтчетногоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецОтчетногоПериода",  КонецДня(ДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("НачалоТекущегоКвартала", НачалоТекущегоКвартала);
	Запрос.УстановитьПараметр("КонецПредыдущегоПериода", НачалоТекущегоКвартала-1);
	Запрос.УстановитьПараметр("Месяц1",                 Месяц(НачалоТекущегоКвартала));
	Запрос.УстановитьПараметр("Месяц2",                 Месяц(НачалоТекущегоКвартала) + 1);
	Запрос.УстановитьПараметр("Месяц3",                 Месяц(НачалоТекущегоКвартала) + 2);
	Запрос.УстановитьПараметр("ПустаяДата",             Дата(1,1,1));
	Запрос.УстановитьПараметр("Налог",                  Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(
															Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ, Истина));
	
	УплаченоТекст =
	"ВЫБРАТЬ
	|	СписаниеСРасчетногоСчета.НомерВходящегоДокумента КАК ПлатежноеПоручениеНомер,
	|	СписаниеСРасчетногоСчета.ДатаВходящегоДокумента КАК ПлатежноеПоручениеДата,
	|	СписаниеСРасчетногоСчета.СуммаДокумента КАК ФССНесчастныеСлучаиУплачено,
	|	ВЫБОР
	|		КОГДА СписаниеСРасчетногоСчета.ДатаВходящегоДокумента = &ПустаяДата
	|			ТОГДА НАЧАЛОПЕРИОДА(СписаниеСРасчетногоСчета.Дата, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СписаниеСРасчетногоСчета.ДатаВходящегоДокумента, МЕСЯЦ)
	|	КОНЕЦ КАК Период
	|ПОМЕСТИТЬ ВТРасчетыСФондом
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Проведен
	|	И СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога)
	|	И СписаниеСРасчетногоСчета.Налог = &Налог
	|	И СписаниеСРасчетногоСчета.ВидНалоговогоОбязательства = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог)
	|	И СписаниеСРасчетногоСчета.Дата МЕЖДУ &НачалоТекущегоКвартала И &КонецОтчетногоПериода
	|	И СписаниеСРасчетногоСчета.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Номер,
	|	РасходныйКассовыйОрдер.Дата,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдер.Дата, МЕСЯЦ)
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.Проведен
	|	И РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога)
	|	И РасходныйКассовыйОрдер.Налог = &Налог
	|	И РасходныйКассовыйОрдер.ВидНалоговогоОбязательства = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог)
	|	И РасходныйКассовыйОрдер.Дата МЕЖДУ &НачалоТекущегоКвартала И &КонецОтчетногоПериода
	|	И РасходныйКассовыйОрдер.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц1
	|			ТОГДА РасчетыСФондом.ФССНесчастныеСлучаиУплачено
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016101,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц1
	|			ТОГДА РасчетыСФондом.ПлатежноеПоручениеДата
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016104,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц1
	|			ТОГДА РасчетыСФондом.ПлатежноеПоручениеНомер
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016105,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц2
	|			ТОГДА РасчетыСФондом.ФССНесчастныеСлучаиУплачено
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016201,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц2
	|			ТОГДА РасчетыСФондом.ПлатежноеПоручениеДата
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016204,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц2
	|			ТОГДА РасчетыСФондом.ПлатежноеПоручениеНомер
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016205,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц3
	|			ТОГДА РасчетыСФондом.ФССНесчастныеСлучаиУплачено
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016301,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц3
	|			ТОГДА РасчетыСФондом.ПлатежноеПоручениеДата
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016304,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(РасчетыСФондом.Период) = &Месяц3
	|			ТОГДА РасчетыСФондом.ПлатежноеПоручениеНомер
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК П000020016305,
	|	МЕСЯЦ(РасчетыСФондом.Период) КАК МесяцПериода,
	|	РасчетыСФондом.ФССНесчастныеСлучаиУплачено КАК ФССНесчастныеСлучаиУплачено
	|ИЗ
	|	ВТРасчетыСФондом КАК РасчетыСФондом
	|ГДЕ
	|	РасчетыСФондом.ФССНесчастныеСлучаиУплачено <> 0
	|	И МЕСЯЦ(РасчетыСФондом.Период) МЕЖДУ &Месяц1 И &Месяц3
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетыСФондом.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеСРасчетногоСчета.СуммаДокумента КАК П000020016001
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Проведен
	|	И СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога)
	|	И СписаниеСРасчетногоСчета.Налог = &Налог
	|	И СписаниеСРасчетногоСчета.ВидНалоговогоОбязательства = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог)
	|	И СписаниеСРасчетногоСчета.Дата МЕЖДУ &НачалоОтчетногоПериода И &КонецПредыдущегоПериода
	|	И СписаниеСРасчетногоСчета.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.СуммаДокумента
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.Проведен
	|	И РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога)
	|	И РасходныйКассовыйОрдер.Налог = &Налог
	|	И РасходныйКассовыйОрдер.ВидНалоговогоОбязательства = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог)
	|	И РасходныйКассовыйОрдер.Дата МЕЖДУ &НачалоОтчетногоПериода И &КонецПредыдущегоПериода
	|	И РасходныйКассовыйОрдер.Организация = &Организация";
	
	ПоказателиОтчета = Новый Структура;
	ПолучитьСведенияОПоказателях4ФСС(ПоказателиОтчета, ИДРедакцииОтчета);
	
	Контейнер.Таблица2_1_ДанныеДопСтрок.Очистить();
	Контейнер.Таблица2_2_ДанныеДопСтрок.Очистить();
	Контейнер.Таблица2_3_ДанныеДопСтрок.Очистить();
	
	Запрос.Текст = УплаченоТекст;
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = РезультатЗапроса[1].Выгрузить();
	Для каждого СтрокаДанных Из Результат Цикл
		Если СтрокаДанных.ФССНесчастныеСлучаиУплачено <> 0 Тогда
			ИмяКоллекции = "Таблица2_" + (СтрокаДанных.МесяцПериода - Месяц(НачалоТекущегоКвартала) + 1) + "_ДанныеДопСтрок";
			ЗаполнитьЗначенияСвойств(Контейнер[ИмяКоллекции].Добавить(), СтрокаДанных);
		КонецЕсли;
	КонецЦикла;
	
	Результат = РезультатЗапроса[2].Выгрузить();
	Контейнер.Таблица2.П000020016001 = Результат.Итог("П000020016001");
	
КонецПроцедуры

//***********************************
// Заявление о субсидии субъектам МСП в пострадавших от коронавируса регионах в 2021 году

Процедура ПолучитьСведенияОПоказателяхРегламентированноеУведомлениеСубсидияПострадавшимМСП(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "КПП", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ОКТМО", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ЧислРаб", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьРегламентированноеУведомлениеСубсидияПострадавшимМСП(ПараметрыОтчета, Контейнер) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаОтчета = Контейнер.МнгСтр;
	ТаблицаОтчета.Очистить();
	
	// Получим работающих сотрудников в июне 2021 г. в разрезе подразделений.
	
	// Обособленные подразделения на отдельном балансе также учитываем.
	Организации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(ПараметрыОтчета.Организация);
	
	ИндивидуальныйПредприниматель = Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(ПараметрыОтчета.Организация);
	
	ПериодСведений = ПериодДанныхОСотрудникахДляРегламентированногоУведомленияСубсидияПострадавшимМСП();
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организации);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "Организация, Подразделение");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоМесяца(ПериодСведений));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецМесяца(ПериодСведений));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("РаботникиПоДоговорамГПХ",       Ложь);
	
	ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Ложь, ПараметрыПолученияСотрудниковОрганизаций);
	
	ОбщееКоличествоСотрудников = ТаблицаСотрудников.Количество();
	
	Если ОбщееКоличествоСотрудников > 0 Тогда
		
		// Если есть сотрудники, определим количество сотрудников отдельно в организации
		// и обособленных подразделениях.
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("ПериодСведений", КонецМесяца(ПериодСведений));
		Запрос.УстановитьПараметр("ИндивидуальныйПредприниматель", ИндивидуальныйПредприниматель);
		Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаСотрудников.Сотрудник КАК Сотрудник,
		|	ТаблицаСотрудников.Организация КАК Организация,
		|	ТаблицаСотрудников.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ СотрудникиПоПодразделениям
		|ИЗ
		|	&ТаблицаСотрудников КАК ТаблицаСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
		|ПОМЕСТИТЬ РегистрацииВНалоговыхОрганах
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(
		|			&ПериодСведений,
		|			СтруктурнаяЕдиница В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СотрудникиПоПодразделениям.Организация КАК СтруктурнаяЕдиница
		|				ИЗ
		|					СотрудникиПоПодразделениям КАК СотрудникиПоПодразделениям
		|			
		|				ОБЪЕДИНИТЬ ВСЕ
		|			
		|				ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СотрудникиПоПодразделениям.Подразделение
		|				ИЗ
		|					СотрудникиПоПодразделениям КАК СотрудникиПоПодразделениям)) КАК ИсторияРегистрацийВНалоговомОрганеСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиПоПодразделениям.Сотрудник КАК Сотрудник,
		|	СотрудникиПоПодразделениям.Организация КАК Организация,
		|	СотрудникиПоПодразделениям.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА ПодразделенияОрганизаций.ОбособленноеПодразделение
		|			ТОГДА РегистрацииПодразделений.РегистрацияВНалоговомОргане
		|		ИНАЧЕ РегистрацииОрганизаций.РегистрацияВНалоговомОргане
		|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
		|	НЕ Организации.ОбособленноеПодразделение КАК ЭтоГоловнаяОрганизация,
		|	ПодразделенияОрганизаций.ОбособленноеПодразделение КАК ЭтоОбособленноеПодразделение
		|ПОМЕСТИТЬ СотрудникиСРегистрациями
		|ИЗ
		|	СотрудникиПоПодразделениям КАК СотрудникиПоПодразделениям
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|		ПО СотрудникиПоПодразделениям.Подразделение = ПодразделенияОрганизаций.Ссылка
		|			И СотрудникиПоПодразделениям.Организация = ПодразделенияОрганизаций.Владелец
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО СотрудникиПоПодразделениям.Организация = Организации.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацииВНалоговыхОрганах КАК РегистрацииОрганизаций
		|		ПО СотрудникиПоПодразделениям.Организация = РегистрацииОрганизаций.СтруктурнаяЕдиница
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацииВНалоговыхОрганах КАК РегистрацииПодразделений
		|		ПО СотрудникиПоПодразделениям.Подразделение = РегистрацииПодразделений.СтруктурнаяЕдиница
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА &ИндивидуальныйПредприниматель
		|			ТОГДА """"
		|		ИНАЧЕ РегистрацииВНалоговомОргане.КПП
		|	КОНЕЦ КАК КПП,
		|	РегистрацииВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиСРегистрациями.Сотрудник) КАК КоличествоСотрудников
		|ИЗ
		|	СотрудникиСРегистрациями КАК СотрудникиСРегистрациями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|		ПО СотрудникиСРегистрациями.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА &ИндивидуальныйПредприниматель
		|			ТОГДА """"
		|		ИНАЧЕ РегистрацииВНалоговомОргане.КПП
		|	КОНЕЦ,
		|	РегистрацииВНалоговомОргане.КодПоОКТМО,
		|	СотрудникиСРегистрациями.ЭтоГоловнаяОрганизация,
		|	СотрудникиСРегистрациями.ЭтоОбособленноеПодразделение
		|
		|УПОРЯДОЧИТЬ ПО
		|	СотрудникиСРегистрациями.ЭтоГоловнаяОрганизация УБЫВ,
		|	СотрудникиСРегистрациями.ЭтоОбособленноеПодразделение,
		|	КПП,
		|	КодПоОКТМО";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаОтчета.Добавить();
			НоваяСтрока.КПП = Выборка.КПП;
			НоваяСтрока.ОКТМО = СокрЛП(Выборка.КодПоОКТМО);
			НоваяСтрока.ЧислРаб = Выборка.КоличествоСотрудников;
		КонецЦикла;
		
	КонецЕсли;
	
	РегистрацияВНалоговомОргане = Справочники.Организации.РегистрацияВНалоговомОрганеНаДату(
		ПараметрыОтчета.Организация, КонецМесяца(ПериодСведений));
	ДанныеРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РегистрацияВНалоговомОргане, "КодПоОКТМО, КПП");
	
	Если ИндивидуальныйПредприниматель Тогда
		
		// Добавим строки с ОКТМО патентов, полученных не по месту жительства, даже если отсутствуют
		// сведения о работниках в подразделениях с этими ОКТМО (таковы требования к заявлению).
		ОКТМОПатентов = КодыОКТМОПатентовНеПоМестуЖительства(ПараметрыОтчета.Организация,
			НачалоМесяца(ПериодСведений), КонецМесяца(ПериодСведений));
		
		Для Каждого ОКТМОПатента Из ОКТМОПатентов Цикл
			Если ТаблицаОтчета.Найти(ОКТМОПатента, "ОКТМО") = Неопределено Тогда
				НоваяСтрока = ТаблицаОтчета.Добавить();
				НоваяСтрока.ОКТМО = ОКТМОПатента; // КПП и число работников не заполняем.
			КонецЕсли;
		КонецЦикла;
		
		// Если нет сотрудников по основному ОКТМО предпринимателя, вставим на первое место
		// строку с основным ОКТМО и нулевым количеством работников в соответствии с требованиями к заявлению.
		Если ТаблицаОтчета.Найти(СокрЛП(ДанныеРегистрации.КодПоОКТМО), "ОКТМО") = Неопределено Тогда
			НоваяСтрока = ТаблицаОтчета.Вставить(0);
			НоваяСтрока.ОКТМО = СокрЛП(ДанныеРегистрации.КодПоОКТМО); // КПП и число работников не заполняем.
		КонецЕсли;
		
	Иначе
		
		// Если сотрудники юрлица отсутствуют - заполняем единственную строку
		// со сведениями по месту регистрации организации и нулевым количеством работников.
		Если ТаблицаОтчета.Количество() = 0 Тогда
			НоваяСтрока = ТаблицаОтчета.Добавить();
			НоваяСтрока.КПП = ДанныеРегистрации.КПП;
			НоваяСтрока.ОКТМО = СокрЛП(ДанныеРегистрации.КодПоОКТМО); // Число работников не заполняем.
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция КодыОКТМОПатентовНеПоМестуЖительства(Организация, НачалоПериода, КонецПериода)
	
	КодыОКТМОПатентов = Новый Массив;
	
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		Возврат КодыОКТМОПатентов;
	КонецЕсли;
	
	Если Не УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, КонецПериода)
		И Не УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
		Возврат КодыОКТМОПатентов;
	КонецЕсли;
	
	ПатентыОрганизации = УчетПСН.ПатентыОрганизацииЗаПериод(Организация, НачалоПериода, КонецПериода);
	
	Для Каждого ДанныеПатента Из ПатентыОрганизации Цикл
		Если ДанныеПатента.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
			КодыОКТМОПатентов.Добавить(СокрЛП(ДанныеПатента.КодПоОКТМО));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(КодыОКТМОПатентов);
	
КонецФункции

Функция ПериодДанныхОСотрудникахДляРегламентированногоУведомленияСубсидияПострадавшимМСП()
	
	Возврат '20210601'; // Данные о числе сотрудников получаются за июнь 2021 г.
	
КонецФункции

#КонецОбласти

#Область СтатистикаПерсонала

// Функция рассчитывает численность всех работников.
// Если учет зарплаты ведется в этой программе, то возвращает данные учета.
// Если учет зарплаты ведется в этой программе, то возвращает данные документов "Статистика персонала".
// Параметры:
//		Организация				- СправочникСсылка.Организации
//		НачалоПериода			- Дата
//		КонецПериода			- Дата
//		ПоГоловнойОрганизации	- Булево, если требуется получить данные по организации со всеми филиалами
//		Точность				- Число, точность получаемых данных
//
// Возвращаемое значение:
//		Структура - см. СтатистикаПерсонала.ДанныеОЧисленности 
//
Функция СреднесписочнаяЧисленность(Организация, НачалоПериода, КонецПериода, ПоГоловнойОрганизации = Ложь, Точность = 0) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		
		ДанныеОЧисленности = КадровыйУчет.СреднесписочнаяЧисленностьРаботающих(Организация,
			НачалоГода(НачалоПериода),
			КонецКвартала(КонецПериода),
			ПоГоловнойОрганизации,
			Точность);
			
		ДанныеОЧисленности.Вставить("ЕстьДанныеОЧисленностиЗаПериод", Истина);
		
	Иначе
		
		ДанныеОЧисленности = СреднесписочнаяЧисленностьРаботающих(Организация,
			НачалоГода(НачалоПериода),
			КонецКвартала(КонецПериода),
			ПоГоловнойОрганизации,
			Точность);
			
		ЕстьДанныеОЧисленностиЗаПериод = ЕстьДанныеОЧисленностиЗаПериод(Организация,
			НачалоГода(НачалоПериода),
			КонецКвартала(КонецПериода),
			ПоГоловнойОрганизации);
			
		ДанныеОЧисленности.Вставить("ЕстьДанныеОЧисленностиЗаПериод", ЕстьДанныеОЧисленностиЗаПериод);
		
	КонецЕсли;
	
	Возврат ДанныеОЧисленности;
	
КонецФункции

// Функция рассчитывает численность всех работников и возвращает данные в табличной виде в разрезе подразделений.
// Если учет зарплаты ведется в этой программе, то возвращает данные учета.
// Если учет зарплаты ведется в этой программе, то возвращает данные документов "Статистика персонала".
// Параметры:
//		Организация				- СправочникСсылка.Организации
//		НачалоПериода			- Дата
//		КонецПериода			- Дата
//		ПоГоловнойОрганизации	- Булево, если требуется получить данные по организации со всеми филиалами
//		ПоПодразделениям		- Булево, если требуется получить данные по подразделениям организации
//		Точность				- Число, точность получаемых данных
//
// Возвращаемое значение:
//		ТаблицаЗначений - см. СтатистикаПерсонала.СведенияОЧисленности 
//
Функция СреднесписочнаяЧисленностьДетальная(Организация, НачалоПериода, КонецПериода, ПоГоловнойОрганизации = Ложь, ПоПодразделениям = Ложь, Точность = 0) Экспорт
	
	ПараметрыЧисленности = СтатистикаПерсонала.ПараметрыДляСведенийОЧисленности();
	
	ПараметрыЧисленности.Организация           = Организация;
	ПараметрыЧисленности.ДатаНачала            = НачалоПериода;
	ПараметрыЧисленности.ДатаОкончания         = КонецПериода;
	ПараметрыЧисленности.ПоГоловнойОрганизации = ПоГоловнойОрганизации;
	ПараметрыЧисленности.ПоПодразделениям      = ПоПодразделениям;
	
	СведенияОЧисленности = СтатистикаПерсонала.СведенияОЧисленности(Точность);
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		СтатистикаПерсонала.ЗаполнитьСведенияОЧисленности(СведенияОЧисленности, ПараметрыЧисленности);
	Иначе
		ЗаполнитьСведенияОЧисленности(СведенияОЧисленности, ПараметрыЧисленности);
	КонецЕсли;
	
	Возврат СведенияОЧисленности;
	
КонецФункции

// Функция проверяет наличие данных о численности за период.
// Если учет зарплаты ведется в этой программе, то считается что данные всегда есть.
// Если учет зарплаты ведется в этой программе, то данные есть только при наличии 
// документов "Статистика персонала" за запрашиваемый период.
// Параметры:
//		Организация				- СправочникСсылка.Организации
//		НачалоПериода			- Дата
//		КонецПериода			- Дата
//		ПоГоловнойОрганизации	- Булево, если требуется получить данные по организации со всеми филиалами
//
// Возвращаемое значение:
//		Булево - Истина, если в ИБ есть данные численности, Ложь, если данных нет
//
Функция ЕстьДанныеОЧисленностиЗаПериод(Организация, НачалоПериода, КонецПериода, ПоГоловнойОрганизации = Ложь) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		// При учете зарплаты в программе сведения будут получены из данных учета.
		Возврат Истина;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПоГоловнойОрганизации Тогда
		ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ГоловнаяОрганизация");
		Организации = БухгалтерскийУчетПереопределяемый.ВсяОрганизация(ГоловнаяОрганизация);
	Иначе
		Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организации",   Организации);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатистикаПерсонала.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СтатистикаПерсонала КАК СтатистикаПерсонала
	|ГДЕ
	|	СтатистикаПерсонала.Организация В (&Организации)
	|	И СтатистикаПерсонала.Проведен
	|	И СтатистикаПерсонала.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

Функция СреднесписочнаяЧисленностьРаботающих(Организация, НачалоПериода, КонецПериода, ПоГоловнойОрганизации = Ложь, Точность = 0)
	
	ПараметрыЧисленности = СтатистикаПерсонала.ПараметрыДляСведенийОЧисленности();
	
	ПараметрыЧисленности.Организация           = Организация;
	ПараметрыЧисленности.ДатаНачала            = НачалоПериода;
	ПараметрыЧисленности.ДатаОкончания         = КонецПериода;
	ПараметрыЧисленности.ПоГоловнойОрганизации = ПоГоловнойОрганизации;
	
	СведенияОЧисленности = СтатистикаПерсонала.СведенияОЧисленности(Точность);
	ЗаполнитьСведенияОЧисленности(СведенияОЧисленности, ПараметрыЧисленности);
	
	ДанныеЧисленности = СтатистикаПерсонала.ДанныеОЧисленности();
	
	Для Каждого СтрокаЧисленности Из СведенияОЧисленности Цикл
		
		ДанныеЧисленности.СреднесписочнаяЧисленностьРаботников = ДанныеЧисленности.СреднесписочнаяЧисленностьРаботников
			+ СтрокаЧисленности.СреднесписочнаяЧисленностьСотрудников;
		ДанныеЧисленности.СредняяЧисленностьДоговорников = ДанныеЧисленности.СредняяЧисленностьДоговорников
			+ СтрокаЧисленности.СредняяЧисленностьДоговорников;
		ДанныеЧисленности.СреднесписочнаяЧисленностьВнешнихСовместителей = ДанныеЧисленности.СреднесписочнаяЧисленностьВнешнихСовместителей
			+ СтрокаЧисленности.СреднесписочнаяЧисленностьВнешнихСовместителей;
		ДанныеЧисленности.СредняяЧисленность = ДанныеЧисленности.СредняяЧисленность
			+ СтрокаЧисленности.СреднесписочнаяЧисленностьСотрудников 
			+ СтрокаЧисленности.СредняяЧисленностьДоговорников
			+ СтрокаЧисленности.СреднесписочнаяЧисленностьВнешнихСовместителей;
		ДанныеЧисленности.ЧисленностьРаботников = ДанныеЧисленности.ЧисленностьРаботников
			+ СтрокаЧисленности.ЧисленностьСотрудников;
		ДанныеЧисленности.ЧисленностьДоговорников = ДанныеЧисленности.ЧисленностьДоговорников
			+ СтрокаЧисленности.ЧисленностьДоговорников;
		ДанныеЧисленности.ЧисленностьВнешнихСовместителей = ДанныеЧисленности.ЧисленностьВнешнихСовместителей
			+ СтрокаЧисленности.ЧисленностьВнешнихСовместителей;
		ДанныеЧисленности.Численность = ДанныеЧисленности.Численность
			+ СтрокаЧисленности.ЧисленностьСотрудников
			+ СтрокаЧисленности.ЧисленностьДоговорников
			+ СтрокаЧисленности.ЧисленностьВнешнихСовместителей;
		
	КонецЦикла;
		
	Возврат ДанныеЧисленности;
	
КонецФункции

Процедура ЗаполнитьСведенияОЧисленности(СведенияОЧисленности, Знач ПараметрыДляСведенийОЧисленности)
	
	РезультатЧисленности = ДанныеЧисленности(ПараметрыДляСведенийОЧисленности);
	СведенияОЧисленностиРаботников = РезультатЧисленности.Выбрать();
	Пока СведенияОЧисленностиРаботников.Следующий() Цикл
		НоваяСтрока = СведенияОЧисленности.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СведенияОЧисленностиРаботников);
	КонецЦикла;
	
	// Если группировка по подразделениям не требуется, то ТЗ будет свернута по организации, числовые показатели суммируются.
	Если Не ПараметрыДляСведенийОЧисленности.ПоПодразделениям Тогда
		СведенияОЧисленности.Свернуть("Организация",СписокКолонокДляСуммированияПоказателей(СведенияОЧисленности.Колонки));
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеЧисленности(ПараметрыСведений)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Если ПараметрыСведений.ПоГоловнойОрганизации Тогда
		ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСведений.Организация, "ГоловнаяОрганизация");
		Организации = БухгалтерскийУчетПереопределяемый.ВсяОрганизация(ГоловнаяОрганизация);
	Иначе
		Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыСведений.Организация);
	КонецЕсли;
	Запрос.УстановитьПараметр("Организации", Организации);
	
	ОкончаниеИнтервала = КонецМесяца(ПараметрыСведений.ДатаОкончания);
	Запрос.УстановитьПараметр("ОкончаниеИнтервала", ОкончаниеИнтервала);

	Если НачалоМесяца(ПараметрыСведений.ДатаНачала) = НачалоМесяца(ОкончаниеИнтервала) Тогда
		НачалоИнтервала = НачалоМесяца(ПараметрыСведений.ДатаНачала);
		Запрос.УстановитьПараметр("НачалоИнтервала", НачалоИнтервала);
	Иначе
		НачалоИнтервала = НачалоГода(ПараметрыСведений.ДатаНачала);
		Запрос.УстановитьПараметр("НачалоИнтервала", НачалоГода(ПараметрыСведений.ДатаНачала));
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатистикаПерсонала.Ссылка КАК Ссылка,
	|	СтатистикаПерсонала.Организация КАК Организация,
	|	СтатистикаПерсонала.ПериодРегистрации КАК ПериодРегистрации
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.СтатистикаПерсонала КАК СтатистикаПерсонала
	|ГДЕ
	|	СтатистикаПерсонала.Организация В(&Организации)
	|	И СтатистикаПерсонала.Проведен
	|	И СтатистикаПерсонала.ПериодРегистрации МЕЖДУ &НачалоИнтервала И &ОкончаниеИнтервала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_Документы.Ссылка КАК Ссылка,
	|	ВТ_Документы.Организация КАК Организация,
	|	ВТ_Документы.ПериодРегистрации КАК ПериодРегистрации
	|ПОМЕСТИТЬ ВТ_ПоследниеДокументы
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы
	|ГДЕ
	|	КОНЕЦПЕРИОДА(ВТ_Документы.ПериодРегистрации, МЕСЯЦ) = КОНЕЦПЕРИОДА(&ОкончаниеИнтервала, МЕСЯЦ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодРегистрации УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Документы.Организация КАК Организация,
	|	СтатистикаПерсоналаПоказатели.ПодразделениеОрганизации КАК Подразделение,
	|	СУММА(ЕСТЬNULL(СтатистикаПерсоналаПоказатели.СреднесписочнаяЧисленностьСотрудников, 0)) КАК СреднесписочнаяЧисленностьСотрудников,
	|	СУММА(ЕСТЬNULL(СтатистикаПерсоналаПоказатели.СреднесписочнаяЧисленностьВнешнихСовместителей, 0)) КАК СреднесписочнаяЧисленностьВнешнихСовместителей,
	|	СУММА(ЕСТЬNULL(СтатистикаПерсоналаПоказатели.СредняяЧисленностьДоговорников, 0)) КАК СредняяЧисленностьДоговорников,
	|	РАЗНОСТЬДАТ(&НачалоИнтервала, &ОкончаниеИнтервала, МЕСЯЦ) + 1 КАК ДлительностьПериода
	|ПОМЕСТИТЬ ВТ_СредняяЧисленностьПоПериодам
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СтатистикаПерсонала.Показатели КАК СтатистикаПерсоналаПоказатели
	|		ПО ВТ_Документы.Ссылка = СтатистикаПерсоналаПоказатели.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Документы.Организация,
	|	СтатистикаПерсоналаПоказатели.ПодразделениеОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СредняяЧисленностьПоПериодам.Организация КАК Организация,
	|	ВТ_СредняяЧисленностьПоПериодам.Подразделение КАК Подразделение,
	|	СУММА(ВЫРАЗИТЬ(ВТ_СредняяЧисленностьПоПериодам.СреднесписочнаяЧисленностьСотрудников / ВТ_СредняяЧисленностьПоПериодам.ДлительностьПериода КАК ЧИСЛО(15, 2))) КАК СреднесписочнаяЧисленностьСотрудников,
	|	СУММА(ВЫРАЗИТЬ(ВТ_СредняяЧисленностьПоПериодам.СреднесписочнаяЧисленностьВнешнихСовместителей / ВТ_СредняяЧисленностьПоПериодам.ДлительностьПериода КАК ЧИСЛО(15, 2))) КАК СреднесписочнаяЧисленностьВнешнихСовместителей,
	|	СУММА(ВЫРАЗИТЬ(ВТ_СредняяЧисленностьПоПериодам.СредняяЧисленностьДоговорников / ВТ_СредняяЧисленностьПоПериодам.ДлительностьПериода КАК ЧИСЛО(15, 2))) КАК СредняяЧисленностьДоговорников,
	|	СУММА(0) КАК ЧисленностьРаботников,
	|	СУММА(0) КАК ЧисленностьВнешнихСовместителей,
	|	СУММА(0) КАК ЧисленностьДоговорников
	|ИЗ
	|	ВТ_СредняяЧисленностьПоПериодам КАК ВТ_СредняяЧисленностьПоПериодам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СредняяЧисленностьПоПериодам.Организация,
	|	ВТ_СредняяЧисленностьПоПериодам.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПоследниеДокументы.Организация,
	|	СтатистикаПерсоналаПоказатели.ПодразделениеОрганизации,
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(СтатистикаПерсоналаПоказатели.СреднесписочнаяЧисленностьСотрудников, 0) КАК ЧИСЛО(15, 2))),
	|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(СтатистикаПерсоналаПоказатели.СреднесписочнаяЧисленностьВнешнихСовместителей, 0) КАК ЧИСЛО(15, 2))),
	|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(СтатистикаПерсоналаПоказатели.СредняяЧисленностьДоговорников, 0) КАК ЧИСЛО(15, 2)))
	|ИЗ
	|	ВТ_ПоследниеДокументы КАК ВТ_ПоследниеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СтатистикаПерсонала.Показатели КАК СтатистикаПерсоналаПоказатели
	|		ПО ВТ_ПоследниеДокументы.Ссылка = СтатистикаПерсоналаПоказатели.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПоследниеДокументы.Организация,
	|	СтатистикаПерсоналаПоказатели.ПодразделениеОрганизации";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция СписокКолонокДляСуммированияПоказателей(КолонкиТаблицыЗначений)
	
	КолонкиСуммирования = Новый Массив;
	Для ИндексКолонки = 0 По КолонкиТаблицыЗначений.Количество()-1 Цикл
		Если КолонкиТаблицыЗначений[ИндексКолонки].Имя <> "Организация" 
			И КолонкиТаблицыЗначений[ИндексКолонки].Имя <> "Подразделение" Тогда
			КолонкиСуммирования.Добавить(КолонкиТаблицыЗначений[ИндексКолонки].Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(КолонкиСуммирования,",");
	
КонецФункции

// Функция рассчитывает численность всех работников и возвращает данные сводно по организации в табличном виде.
// Если учет зарплаты ведется в этой программе, то возвращает данные учета.
// Если учет зарплаты ведется в этой программе, то возвращает данные документов "Статистика персонала".
// Параметры:
//      Организация             - СправочникСсылка.Организации
//      НачалоПериода           - Дата
//      КонецПериода            - Дата
//      ИсключитьДанныеОбособленныхПодразделений - Булево - Истина, если требуется получить данные по организации
//                                без обособленных подразделений (должен быть заполнен массив ИсключаемыеПодразделения)
//      ИсключаемыеПодразделения - Массив- подразделения, данные по которым необходимо исключить из итога (обычно все обособленные)
//      ПодразделенияОбособленного - Массив- подразделения, данные по которым необходимо включить в итог
//                                (имеет смысл, если ИсключитьДанныеОбособленныхПодразделений = Ложь). Применяется,
//                                 если в отчет по обособленному подразделению нужно включить данные других подразделений
//                                (на стороне получателя они представляют собой одну отчетную единицу)
//
// Возвращаемое значение:
//      ТаблицаЗначений - см. СтатистикаПерсоналаБазовый.СведенияОЧисленности 
//
Функция ПоказателиЧисленностиФормСтатистики(Организация, НачалоПериода, КонецПериода,
	ИсключитьДанныеОбособленныхПодразделений, ИсключаемыеПодразделения, ПодразделенияОбособленного) Экспорт
	
	ПараметрыЧисленности = СтатистикаПерсонала.ПараметрыДляСведенийОЧисленности();
	
	ПараметрыЧисленности.Организация           = Организация;
	ПараметрыЧисленности.ДатаНачала            = НачалоПериода;
	ПараметрыЧисленности.ДатаОкончания         = КонецПериода;
	ПараметрыЧисленности.ИсключитьДанныеОбособленныхПодразделений = ИсключитьДанныеОбособленныхПодразделений;
	ПараметрыЧисленности.ПодразделенияОбособленного = ПодразделенияОбособленного;
	ПараметрыЧисленности.ИсключаемыеПодразделения = ИсключаемыеПодразделения;
	
	СведенияОЧисленности = СтатистикаПерсонала.СведенияОЧисленности(0);
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		СтатистикаПерсонала.ЗаполнитьСведенияОЧисленности(СведенияОЧисленности, ПараметрыЧисленности);
	Иначе
		ЗаполнитьСведенияОЧисленности(СведенияОЧисленности, ПараметрыЧисленности);
	КонецЕсли;
	
	Возврат СведенияОЧисленности;
	
КонецФункции

#КонецОбласти

#Область ЛьготныеКредиты

Функция ЧисленностьСотрудниковДляЛьготногоКредита(Организация, ПериодЗаявки) Экспорт
	
	Если Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить() Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация)
		И Не УчетЗарплаты.ИПИспользуетТрудНаемныхРаботников(Организация) Тогда
		// У ИП без сотрудников всегда только один сотрудник - он сам.
		Возврат 1;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодЗаявки", ПериодЗаявки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ТекущиеКадровыеДанныеСотрудников.Сотрудник) КАК КоличествоСотрудников
	|ИЗ
	|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|ГДЕ
	|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = &Организация
	|	И ТекущиеКадровыеДанныеСотрудников.ДатаПриема <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	И (ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения МЕЖДУ НАЧАЛОПЕРИОДА(&ПериодЗаявки, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ПериодЗаявки, МЕСЯЦ))";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КоличествоСотрудников;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

#КонецОбласти

#Область ВариантыОтчетов

// Определяет разделы, в которых доступна панель отчетов.
//
// Параметры:
//   Разделы (Массив) из (ОбъектМетаданных)
//
// Описание:
//   В Разделы необходимо добавить метаданные подсистем тех разделов,
//   в которых размещены команды вызова панелей отчетов.
//
// Например:
//	Разделы.Добавить(Метаданные.Подсистемы.ИмяПодсистемы);
//
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.ЗарплатаИКадры.Подсистемы.КадровыйУчет);
	Разделы.Добавить(Метаданные.Подсистемы.ЗарплатаИКадры.Подсистемы.Зарплата);
	Разделы.Добавить(Метаданные.Подсистемы.ЗарплатаИКадры.Подсистемы.Пособия);
	Разделы.Добавить(Метаданные.Подсистемы.ЗарплатаИКадры.Подсистемы.ВоинскийУчет);
	
	Разделы.Добавить(Метаданные.Подсистемы.Отчеты.Подсистемы.Сотрудники);
	
КонецПроцедуры

Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ОстаткиИОборотыРезервовПоОплатеТруда);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.АнализРасходовНаОплатуТруда);
	ЗарплатаКадрыОтчеты.НастроитьВариантыОтчетов(Настройки);
		
КонецПроцедуры

Функция ИспользуетсяТрудЧленовЛетныхЭкипажей(Организация = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Если ЗначениеЗаполнено(Организация) Тогда
		Параметры.Вставить("Организация", Организация);
	КонецЕсли;
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяТрудЧленовЛетныхЭкипажей", Параметры);
	
КонецФункции

Функция ИспользуетсяТрудШахтеров(Организация = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Если ЗначениеЗаполнено(Организация) Тогда
		Параметры.Вставить("Организация", Организация);
	КонецЕсли;
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяТрудШахтеров", Параметры);
	
КонецФункции

Функция ИспользуютсяРаботыСДосрочнойПенсией(Организация = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Если ЗначениеЗаполнено(Организация) Тогда
		Параметры.Вставить("Организация", Организация);
	КонецЕсли;
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользуютсяРаботыСДосрочнойПенсией", Параметры);
	
КонецФункции

Функция УдерживаютсяДобровольныеВзносыВПФР() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Удержания.Ссылка
	|ИЗ
	|	ПланВидовРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ДобровольныеВзносыВНПФ)";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ИспользуетсяТрудИностранцевСОсобымиВзносами(Организация = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Если ЗначениеЗаполнено(Организация) Тогда
		Параметры.Вставить("Организация", Организация);
	КонецЕсли;
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяТрудИностранцевСОсобымиВзносами", Параметры);
	
КонецФункции

Функция НачисляютсяДополнительныеВзносыЧленамЛетныхЭкипажейИлиШахтерам(Организация = Неопределено) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		Возврат ИспользуетсяТрудЧленовЛетныхЭкипажей(Организация)
			Или ИспользуетсяТрудШахтеров(Организация);
		Иначе
			
		СчетаУчета = Новый Массив;
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПФР_доп);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПФР_доп_шахтеры);
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Хозрасчетный.Организация КАК Организация
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.СчетДт В (&СчетаУчета)
		|	И &УсловиеОрганизация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Хозрасчетный.Организация
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.СчетКт В (&СчетаУчета)
		|	И &УсловиеОрганизация";
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
		ПодстрокаЗамены = "ИСТИНА";
		Если ЗначениеЗаполнено(Организация) Тогда
			ПодстрокаЗамены = "Хозрасчетный.Организация = &Организация";
			Запрос.УстановитьПараметр("Организация", Организация);
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&УсловиеОрганизация", ПодстрокаЗамены);
		
		Возврат Не Запрос.Выполнить().Пустой();
		
	КонецЕсли;

КонецФункции

Функция ПрименятьСевернуюНадбавку() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ПрименятьСевернуюНадбавку");
	
КонецФункции

Функция ПрименятьРайонныйКоэффициент() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ПрименятьРайонныйКоэффициент");
	
КонецФункции

#КонецОбласти

#Область ЗадачиКалендаряБухгалтера

Процедура СоздатьПравилоВыплатаЗарплатыУплатаНДФЛ(МенеджерВременныхТаблиц, Организация, Отбор = Неопределено) Экспорт
	
	Перем ОтборПоИмениЗадачи, ОтборПоДействию;
	
	ИдентификаторЗадачиЗарплата = "НДФЛ_Агент_Уплата";
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		ОтборПоИмениЗадачи = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Отбор, "ИмяЗадачи");
		ОтборПоДействию    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Отбор, "Действие");
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ОтборПоИмениЗадачи) И ОтборПоИмениЗадачи <> ИдентификаторЗадачиЗарплата)
		ИЛИ (ЗначениеЗаполнено(ОтборПоДействию) И ОтборПоДействию <> Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата) Тогда
		СоздатьПустоеПравилоВыплатаЗарплатыУплатаНДФЛ(МенеджерВременныхТаблиц);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ИдентификаторЗадачи",                     ИдентификаторЗадачиЗарплата);
	Запрос.УстановитьПараметр("ИдентификаторПравилаЗарплата",            "НДФЛ_Уплата");
	Запрос.УстановитьПараметр("ИдентификаторПравилаАванс",               "Аванс");
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Правило,
	|	ВЫБОР
	|		КОГДА НастройкиЗарплатаКадры.ВыплачиватьЗарплатуВПоследнийДеньМесяца
	|			ТОГДА 0
	|		ИНАЧЕ НастройкиЗарплатаКадры.ДатаВыплатыЗарплатыНеПозжеЧем
	|	КОНЕЦ КАК СрокДней,
	|	0 КАК СрокМесяцев
	|ПОМЕСТИТЬ ПравилоУплатыНДФЛНалоговымАгентом
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Ссылка = ПравилаПредставленияОтчетовУплатыНалогов.Владелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиЗарплатаКадры КАК НастройкиЗарплатаКадры
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЗадачиБухгалтера.Код = &ИдентификаторЗадачи
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Код = &ИдентификаторПравилаЗарплата
	|	И НастройкиЗарплатаКадры.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка,
	|	НастройкиЗарплатаКадры.ДатаВыплатыАвансаНеПозжеЧем,
	|	-1
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Ссылка = ПравилаПредставленияОтчетовУплатыНалогов.Владелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиЗарплатаКадры КАК НастройкиЗарплатаКадры
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЗадачиБухгалтера.Код = &ИдентификаторЗадачи
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Код = &ИдентификаторПравилаАванс
	|	И НастройкиЗарплатаКадры.Организация = &Организация";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьПустоеПравилоВыплатаЗарплатыУплатаНДФЛ(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка) КАК Правило,
	|	0 КАК СрокДней,
	|	0 КАК СрокМесяцев
	|ПОМЕСТИТЬ ПравилоУплатыНДФЛНалоговымАгентом
	|ГДЕ
	|	ЛОЖЬ";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ВыплачиваетсяЗарплата(Организация, Период) Экспорт
	
	НачалоПериода    = НачалоМесяца(Период);
	ОкончаниеПериода = КонецМесяца(Период);
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
			Возврат Истина;
		Иначе
			Возврат ИПИспользуетТрудНаемныхРаботниковЗаПериод(Организация, НачалоПериода, ОкончаниеПериода);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организация);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "Организация");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоПериода);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              ОкончаниеПериода);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("РаботникиПоДоговорамГПХ",       Истина);
	
	ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	Если ТаблицаСотрудников.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",        Организация);
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("НачалоПериода",      НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("ОкончаниеПериода",   НачалоДня(ОкончаниеПериода));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаСотрудников.Сотрудник КАК Сотрудник,
	|	ТаблицаСотрудников.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_Сотрудники
	|ИЗ
	|	&ТаблицаСотрудников КАК ТаблицаСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтпускБезСохраненияОплаты.Сотрудник КАК Сотрудник,
	|	ОтпускБезСохраненияОплаты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	|ГДЕ
	|	ОтпускБезСохраненияОплаты.Проведен
	|	И ОтпускБезСохраненияОплаты.ДатаНачала <= &НачалоПериода
	|	И ОтпускБезСохраненияОплаты.ДатаОкончания >= &ОкончаниеПериода
	|	И ОтпускБезСохраненияОплаты.Организация = &Организация
	|	И ОтпускБезСохраненияОплаты.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТ_Сотрудники.Сотрудник
	|			ИЗ
	|				ВТ_Сотрудники)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Сотрудники.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ВТ_Документы.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетНачислений
	|ПОМЕСТИТЬ ВТ_СотрудникиДокументы
	|ИЗ
	|	ВТ_Сотрудники КАК ВТ_Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Документы КАК ВТ_Документы
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_Документы.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СотрудникиДокументы.Сотрудник КАК Сотрудник,
	|	ВТ_СотрудникиДокументы.ТребуетНачислений КАК ТребуетНачислений
	|ИЗ
	|	ВТ_СотрудникиДокументы КАК ВТ_СотрудникиДокументы
	|ГДЕ
	|	ВТ_СотрудникиДокументы.ТребуетНачислений";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Определяет наличие у организации, применяющей АУСН, как минимум одного оформленного платежного поручения
// на выплату зарплаты или перечисление НДФЛ за указанный налоговый период
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, для которой производится проверка.
//  Период      - Дата - дата в месяце, выплаты за который проверяются.
//
// Возвращаемое значение:
//   Булево
//
Функция ОформленыПлатежныеПорученияНаВыплатыСотрудникамАУСН(Организация, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не УчетнаяПолитика.ПрименяетсяАУСН(Организация, Период) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыОперацииЗарплата = Новый Массив;
	ВидыОперацииЗарплата.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику);
	ВидыОперацииЗарплата.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидыОперацииЗарплата", ВидыОперацииЗарплата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПлатежноеПоручение.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Проведен
	|	И ПлатежноеПоручение.Организация = &Организация
	|	И ПлатежноеПоручение.ВидОперации В(&ВидыОперацииЗарплата)
	|	И ПлатежноеПоручение.ПлатежнаяВедомость ССЫЛКА Документ.ВедомостьНаВыплатуЗарплатыВБанк
	|	И ПлатежноеПоручение.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция НачисленыВзносыНаТравматизм(УсловияПримененияПравил, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	Возврат ЕстьДвиженияРасчетовПоВзносамЗаПериода(УсловияПримененияПравил, Период, ВидыВзносов.Травматизм);
	
КонецФункции

Функция НачисленыСтраховыеВзносы(УсловияПримененияПравил, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	Возврат ЕстьДвиженияРасчетовПоВзносамЗаПериода(УсловияПримененияПравил, Период, ВидыВзносов.СтраховыеВзносы);
	
КонецФункции

Функция НачисленыСтраховыеВзносыЧленовЛетныхЭкипажейИлиШахтеров(УсловияПримененияПравил, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	Возврат ЕстьДвиженияРасчетовПоВзносамЗаПериода(УсловияПримененияПравил, Период, ВидыВзносов.ДопВзносыЛетчикиШахтеры);
	
КонецФункции

Функция НачисленыСтраховыеВзносыДобровольные(УсловияПримененияПравил, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	Возврат ЕстьДвиженияРасчетовПоВзносамЗаПериода(УсловияПримененияПравил, Период, ВидыВзносов.ДобровольныеВзносы);
	
КонецФункции

Функция СчетаУчетаСтраховыхВзносов(Период, ВариантВыборкиСтраховыхВзносов, ПараметрыСтраховыхВзносов = Неопределено) Экспорт
	
	СчетаСтраховыхВзносов = Новый Массив;
	
	ВариантыВыборкиСтраховыхВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	
	Если ПараметрыСтраховыхВзносов = Неопределено Тогда
		ПараметрыСтраховыхВзносов = ВыполнениеЗадачБухгалтераПовтИспНаВызов.ПараметрыСтраховыхВзносов(Период, Ложь);
	КонецЕсли;
	
	СчетУчетаТравматизм         = ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ;
	СчетУчетаДобровольныеВзносы = ПланыСчетов.Хозрасчетный.ПФР_ДОБР;
	СчетУчетаДовВзносыЛетчики   = ПланыСчетов.Хозрасчетный.ПФР_доп;
	СчетУчетаДовВзносыШахтеры   = ПланыСчетов.Хозрасчетный.ПФР_доп_шахтеры;
	Если ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.Травматизм Тогда
		СчетаСтраховыхВзносов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СчетУчетаТравматизм);
		ПараметрыСтраховыхВзносов = ПараметрыСтраховыхВзносов.Скопировать(Новый Структура("СчетУчета", СчетУчетаТравматизм));
	ИначеЕсли ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.ДобровольныеВзносы Тогда
		МассивСчетов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СчетУчетаДобровольныеВзносы);
		УсловияОтбораСубсчетов = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловияОтбораСубсчетов.ИспользоватьВПроводках = Истина;
		СчетаСтраховыхВзносов = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(МассивСчетов, УсловияОтбораСубсчетов);
	ИначеЕсли ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.ДопВзносыЛетчикиШахтеры Тогда
		МассивСчетов = Новый Массив;
		МассивСчетов.Добавить(СчетУчетаДовВзносыЛетчики);
		МассивСчетов.Добавить(СчетУчетаДовВзносыШахтеры);
		УсловияОтбораСубсчетов = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловияОтбораСубсчетов.ИспользоватьВПроводках = Истина;
		СчетаСтраховыхВзносов = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(МассивСчетов, УсловияОтбораСубсчетов);
	Иначе
		СчетаСтраховыхВзносов = ПараметрыСтраховыхВзносов.ВыгрузитьКолонку("СчетУчета");
		Если ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.СтраховыеВзносы Тогда
			СтрокаТравматизм = СчетаСтраховыхВзносов.Найти(СчетУчетаТравматизм);
			Если СтрокаТравматизм <> Неопределено Тогда
				СчетаСтраховыхВзносов.Удалить(СтрокаТравматизм);
			КонецЕсли;
			Если Период >= ДатаВыделенияДополнительныхВзносовЛетчиковИШахтеровИзСоставаЕНП() Тогда
				СтрокаДопВзносы = СчетаСтраховыхВзносов.Найти(СчетУчетаДовВзносыЛетчики);
				Если СтрокаДопВзносы <> Неопределено Тогда
					СчетаСтраховыхВзносов.Удалить(СтрокаДопВзносы);
				КонецЕсли;
				СтрокаДопВзносы = СчетаСтраховыхВзносов.Найти(СчетУчетаДовВзносыШахтеры);
				Если СтрокаДопВзносы <> Неопределено Тогда
					СчетаСтраховыхВзносов.Удалить(СтрокаДопВзносы);
				КонецЕсли;
			КонецЕсли;
			МассивСчетов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СчетУчетаДобровольныеВзносы);
			Для Каждого СчетУчета Из МассивСчетов Цикл
				СтрокаДобровольныеВзносы = СчетаСтраховыхВзносов.Найти(СчетУчета);
				Если СтрокаДобровольныеВзносы <> Неопределено Тогда
					СчетаСтраховыхВзносов.Удалить(СтрокаДобровольныеВзносы);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СчетаСтраховыхВзносов;
	
КонецФункции

Функция ЕстьДвиженияРасчетовПоВзносамЗаПериода(УсловияПримененияПравил, Период, ВариантВыборкиСтраховыхВзносов)
	
	ПериодыРасчетовПоСоциальномуСтрахованию = УсловияПримененияПравил.Получить("ПериодыРасчетовПоСоциальномуСтрахованию");
	Если ПериодыРасчетовПоСоциальномуСтрахованию.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УсловияПоиска = Новый Структура("Период", Период);
	РасчетыЗаПериод = ПериодыРасчетовПоСоциальномуСтрахованию.НайтиСтроки(УсловияПоиска);
	Если РасчетыЗаПериод.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СчетаУчета = СчетаУчетаСтраховыхВзносов(Период, ВариантВыборкиСтраховыхВзносов);
	
	Для Каждого СтрокаРасчета Из РасчетыЗаПериод Цикл
		Для Каждого Счет Из СчетаУчета Цикл
			Если Счет = СтрокаРасчета.Счет Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗапуститьАктуализациюСпискаЗадачПриПроведенииДокументаОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если СервисЭлектронныхТрудовыхКнижек.ИспользуетсяСервисЭлектронныхТрудовыхКнижек() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоСписаниеДенежныхСредств = ТипЗнч(Источник) = Тип("ДокументОбъект.РасходныйКассовыйОрдер")
		Или ТипЗнч(Источник) = Тип("ДокументОбъект.СписаниеСРасчетногоСчета");
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		
		Если (ТипЗнч(Источник) = Тип("ДокументОбъект.ОтражениеЗарплатыВБухучете")
			Или ТипЗнч(Источник) = Тип("ДокументОбъект.ОтражениеЗарплатыВУчете"))
			И (Источник.Дата < ЕдиныйНалоговыйСчет.НачалоПростогоУчета()
				Или Источник.ПериодРегистрации < ЕдиныйНалоговыйСчет.НачалоПростогоУчета()) Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.СведенияОбУдержанномНДФЛ")
			И (Источник.Дата < ЕдиныйНалоговыйСчет.НачалоПростогоУчета()
				Или Источник.Месяц < ЕдиныйНалоговыйСчет.НачалоПростогоУчета()) Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПриемНаРаботу")
			Или ТипЗнч(Источник) = Тип("ДокументОбъект.КадровыйПеревод")
			Или ТипЗнч(Источник) = Тип("ДокументОбъект.Увольнение") Тогда
			Возврат;
		КонецЕсли;
		
		Если ЭтоСписаниеДенежныхСредств Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		
		Если ЭтоСписаниеДенежныхСредств
			И Не ВыполнениеЗадачБухгалтераБП.ЭтоРасчетыПоДоходамСотрудников(Источник.Видоперации) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Источник.Движения.Найти("Хозрасчетный") <> Неопределено
		И Источник.Движения.Хозрасчетный.Модифицированность() Тогда
		Источник.Движения.Хозрасчетный.Записать();
		Источник.Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);
		Источник.Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("ДвиженияПоМалоценнымОбъектамСформированы", Истина);
	КонецЕсли;
	
	ЗапуститьЗаполнениеКалендаряБухгалтера(Источник.Организация);
	
КонецПроцедуры

Процедура ЗапуститьАктуализациюСпискаЗадачПриУдаленииПроведенияКадровогоДокумента(Источник, Отказ, РежимПроведения) Экспорт
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если СервисЭлектронныхТрудовыхКнижек.ИспользуетсяСервисЭлектронныхТрудовыхКнижек() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьЗаполнениеКалендаряБухгалтера(Источник.Организация);
	
КонецПроцедуры

Процедура ЗапуститьЗаполнениеКалендаряБухгалтера(Организация) Экспорт
	
	УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор("b0ebe075-1372-4bac-8702-9d7e43aafd4b");
	
	УстановитьПривилегированныйРежим(Истина);
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(УникальныйИдентификаторЗадания);
	
	Если Задание = Неопределено Тогда
		КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(УникальныйИдентификаторЗадания, Организация, Ложь, Ложь, Истина);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция КадровыеМероприятияЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Результат = Новый ТаблицаЗначений;
		Результат.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
		Результат.Колонки.Добавить("Периодичность", Новый ОписаниеТипов("ПеречислениеСсылка.Периодичность"));
		Возврат Результат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецДня(КонецПериода));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(КадровыеМероприятия.Период, ДЕНЬ) КАК Период,
	|	КадровыеМероприятия.Периодичность КАК Периодичность
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПриемНаРаботу.ДатаВторогоДокументаОснования = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ПриемНаРаботу.Дата
	|			КОГДА ПриемНаРаботу.ДатаВторогоДокументаОснования > ПриемНаРаботу.Дата
	|				ТОГДА ПриемНаРаботу.Дата
	|			ИНАЧЕ ПриемНаРаботу.ДатаВторогоДокументаОснования
	|		КОНЕЦ КАК Период,
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.День) КАК Периодичность
	|	ИЗ
	|		Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА ПриемНаРаботу.ДатаВторогоДокументаОснования = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ПриемНаРаботу.Дата
	|				КОГДА ПриемНаРаботу.ДатаВторогоДокументаОснования > ПриемНаРаботу.Дата
	|					ТОГДА ПриемНаРаботу.Дата
	|				ИНАЧЕ ПриемНаРаботу.ДатаВторогоДокументаОснования
	|			КОНЕЦ МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ПриемНаРаботу.Организация = &Организация
	|		И ПриемНаРаботу.ОтразитьВТрудовойКнижке
	|		И ПриемНаРаботу.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Увольнение.ДатаВторогоДокументаОснования = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА Увольнение.Дата
	|			КОГДА Увольнение.ДатаВторогоДокументаОснования > Увольнение.Дата
	|				ТОГДА Увольнение.Дата
	|			ИНАЧЕ Увольнение.ДатаВторогоДокументаОснования
	|		КОНЕЦ,
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|	ИЗ
	|		Документ.Увольнение КАК Увольнение
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА Увольнение.ДатаВторогоДокументаОснования = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА Увольнение.Дата
	|				КОГДА Увольнение.ДатаВторогоДокументаОснования > Увольнение.Дата
	|					ТОГДА Увольнение.Дата
	|				ИНАЧЕ Увольнение.ДатаВторогоДокументаОснования
	|			КОНЕЦ МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И Увольнение.Организация = &Организация
	|		И Увольнение.ОтразитьВТрудовойКнижке
	|		И Увольнение.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТекущиеКадровыеДанныеСотрудников.ДатаПриема,
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|	ИЗ
	|		РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|	ГДЕ
	|		ТекущиеКадровыеДанныеСотрудников.ОформленПоТрудовомуДоговору
	|		И ТекущиеКадровыеДанныеСотрудников.ДатаПриема МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения,
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|	ИЗ
	|		РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|	ГДЕ
	|		ТекущиеКадровыеДанныеСотрудников.ОформленПоТрудовомуДоговору
	|		И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = &Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДоговорРаботыУслуги.Дата,
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|	ИЗ
	|		Документ.ДоговорРаботыУслуги КАК ДоговорРаботыУслуги
	|	ГДЕ
	|		ДоговорРаботыУслуги.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДоговорРаботыУслуги.Организация = &Организация
	|		И ДоговорРаботыУслуги.ОтразитьТрудовуюДеятельность
	|		И ДоговорРаботыУслуги.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДоговорРаботыУслуги.ДатаОкончания,
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|	ИЗ
	|		Документ.ДоговорРаботыУслуги КАК ДоговорРаботыУслуги
	|	ГДЕ
	|		ДоговорРаботыУслуги.ДатаОкончания МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДоговорРаботыУслуги.Организация = &Организация
	|		И ДоговорРаботыУслуги.ОтразитьТрудовуюДеятельность
	|		И ДоговорРаботыУслуги.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ЗаявленияОПредоставленииСведенийОТрудовойДеятельности.Дата, МЕСЯЦ),
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|	ИЗ
	|		Документ.ЗаявленияОПредоставленииСведенийОТрудовойДеятельности КАК ЗаявленияОПредоставленииСведенийОТрудовойДеятельности
	|	ГДЕ
	|		ЗаявленияОПредоставленииСведенийОТрудовойДеятельности.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ЗаявленияОПредоставленииСведенийОТрудовойДеятельности.Организация = &Организация
	|		И ЗаявленияОПредоставленииСведенийОТрудовойДеятельности.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ВЫБОР
	|				КОГДА КадровыйПеревод.ДатаВторогоДокументаОснования = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА КадровыйПеревод.Дата
	|				КОГДА КадровыйПеревод.ДатаВторогоДокументаОснования > КадровыйПеревод.Дата
	|					ТОГДА КадровыйПеревод.Дата
	|				ИНАЧЕ КадровыйПеревод.ДатаВторогоДокументаОснования
	|			КОНЕЦ, МЕСЯЦ),
	|		ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|	ИЗ
	|		Документ.КадровыйПеревод КАК КадровыйПеревод
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА КадровыйПеревод.ДатаВторогоДокументаОснования = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА КадровыйПеревод.Дата
	|				КОГДА КадровыйПеревод.ДатаВторогоДокументаОснования > КадровыйПеревод.Дата
	|					ТОГДА КадровыйПеревод.Дата
	|				ИНАЧЕ КадровыйПеревод.ДатаВторогоДокументаОснования
	|			КОНЕЦ МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И КадровыйПеревод.Организация = &Организация
	|		И КадровыйПеревод.ОтразитьВТрудовойКнижке
	|		И КадровыйПеревод.Проведен) КАК КадровыеМероприятия
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Период,Периодичность");
	Возврат Результат;
	
КонецФункции

Функция ПериодыУдержанияНДФЛ(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Результат = Новый ТаблицаЗначений;
		Результат.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
		Результат.Колонки.Добавить("ВтораяПоловинаМесяца", Новый ОписаниеТипов("Булево"));
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	НачалоПериодаОтбора = ?(ЗначениеЗаполнено(НачалоПериода), НачалоМесяца(ДобавитьМесяц(НачалоПериода, -1)), НачалоПериода);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериодаОтбора);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(КонецПериода));
	Запрос.УстановитьПараметр("СчетДт", ПланыСчетов.Хозрасчетный.НДФЛ);
	Запрос.УстановитьПараметр("СчетКт", ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ОборотыДтКт.Регистратор ССЫЛКА Документ.СписаниеСРасчетногоСчета
	|			ТОГДА ВЫРАЗИТЬ(ОборотыДтКт.Регистратор КАК Документ.СписаниеСРасчетногоСчета).Дата
	|		КОГДА ОборотыДтКт.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(ОборотыДтКт.Регистратор КАК Документ.РасходныйКассовыйОрдер).Дата
	|		КОГДА ОборотыДтКт.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(ОборотыДтКт.Регистратор КАК Документ.ПриходныйКассовыйОрдер).Дата
	|		КОГДА ОборотыДтКт.Регистратор ССЫЛКА Документ.КорректировкаЕНС
	|			ТОГДА ВЫРАЗИТЬ(ОборотыДтКт.Регистратор КАК Документ.КорректировкаЕНС).Дата
	|		ИНАЧЕ ОборотыДтКт.Период
	|	КОНЕЦ КАК Период
	|ПОМЕСТИТЬ ОборотыПоДокументам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Регистратор, СчетДт = &СчетДт, , СчетКт = &СчетКт, , Организация = &Организация) КАК ОборотыДтКт
	|ГДЕ
	|	НЕ ОборотыДтКт.Регистратор ССЫЛКА Документ.СведенияОбУдержанномНДФЛ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания
	|ИЗ
	|	Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
	|ГДЕ
	|	СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Проведен
	|	И СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛ.МесяцНалоговогоПериода
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛУдержанный КАК ОперацияНалоговогоУчетаПоНДФЛ
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛ.МесяцНалоговогоПериода МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ОперацияНалоговогоУчетаПоНДФЛ.Ссылка.Проведен
	|	И ОперацияНалоговогоУчетаПоНДФЛ.Ссылка.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(ОборотыПоДокументам.Период, МЕСЯЦ) КАК Период,
	|	ДЕНЬ(ОборотыПоДокументам.Период) > 22 КАК ВтораяПоловинаМесяца
	|ИЗ
	|	ОборотыПоДокументам КАК ОборотыПоДокументам";
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Период,ВтораяПоловинаМесяца");
	Возврат Результат;
	
КонецФункции

Функция ПериодыВыплатыЗарплаты(Организация, ЭтоЮрЛицо, НачалоПериода, КонецПериода) Экспорт
	
	Результат = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		ТекущийПериод = НачалоМесяца(НачалоПериода);
		
		ПериодыРасчетов = Новый Массив;
		Если Не ЭтоЮрЛицо Тогда
			ПериодыРасчетов = ПериодыРасчетовОрганизацииРаботодателя(Организация, НачалоПериода, КонецПериода)
		КонецЕсли;
		
		Пока ТекущийПериод < КонецПериода Цикл
			Если ЭтоЮрЛицо Или ПериодыРасчетов.Найти(ТекущийПериод) <> Неопределено Тогда
				Результат.Добавить(ТекущийПериод);
				
			КонецЕсли;
			ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 1);
		КонецЦикла;
		
		Возврат Результат;
	КонецЕсли;
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолучения.Организация = Организация;
	ПараметрыПолучения.КадровыеДанные = "Организация,ДатаПриема,ДатаУвольнения";
	ПараметрыПолучения.ОтбиратьПоГоловнойОрганизации = Ложь;
	ПараметрыПолучения.НачалоПериода = НачалоПериода;
	ПараметрыПолучения.ОкончаниеПериода = КонецПериода;
	ПараметрыПолучения.Отборы = Неопределено;
	ПараметрыПолучения.РаботникиПоДоговорамГПХ = Истина;
	
	СотрудникиОрганизации = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолучения);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СотрудникиОрганизации", СотрудникиОрганизации);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", НачалоДня(КонецПериода));
	
	// Метод КадровыйУчет.СотрудникиОрганизации() не возвращает даты начала и окончания работы
	// по договорам ГПХ. Эти данные следует выбрать отдельно.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СотрудникиОрганизации.Сотрудник КАК Сотрудник,
	|	СотрудникиОрганизации.ДатаПриема КАК ДатаПриема,
	|	СотрудникиОрганизации.ДатаУвольнения КАК ДатаУвольнения
	|ПОМЕСТИТЬ ВсеСотрудники
	|ИЗ
	|	&СотрудникиОрганизации КАК СотрудникиОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СотрудникиОрганизации.Сотрудник КАК Сотрудник,
	|	СотрудникиОрганизации.ДатаПриема КАК ДатаПриема,
	|	СотрудникиОрганизации.ДатаУвольнения КАК ДатаУвольнения
	|ПОМЕСТИТЬ Сотрудники
	|ИЗ
	|	ВсеСотрудники КАК СотрудникиОрганизации
	|ГДЕ
	|	СотрудникиОрганизации.ДатаПриема > ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыГПХ.Сотрудник,
	|	ДоговорыГПХ.ДатаНачала,
	|	ДоговорыГПХ.ДатаОкончания
	|ИЗ
	|	РегистрСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера КАК ДоговорыГПХ
	|ГДЕ
	|	ДоговорыГПХ.ДатаНачала <= &КонецПериода
	|	И ДоговорыГПХ.ДатаОкончания >= &НачалоПериода
	|	И ДоговорыГПХ.Организация = &Организация
	|	И ДоговорыГПХ.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВсеСотрудники.Сотрудник
	|			ИЗ
	|				ВсеСотрудники)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтпускБезСохраненияОплаты.Сотрудник КАК Сотрудник,
	|	ОтпускБезСохраненияОплаты.ДатаНачала КАК ДатаНачала,
	|	ОтпускБезСохраненияОплаты.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ОтпускБезСохраненияОплаты
	|ИЗ
	|	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	|ГДЕ
	|	ОтпускБезСохраненияОплаты.Проведен
	|	И ОтпускБезСохраненияОплаты.ДатаНачала <= &КонецПериода
	|	И ОтпускБезСохраненияОплаты.ДатаОкончания >= &НачалоПериода
	|	И ОтпускБезСохраненияОплаты.Организация = &Организация
	|	И ОтпускБезСохраненияОплаты.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВсеСотрудники.Сотрудник
	|			ИЗ
	|				ВсеСотрудники)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ДатаПриема КАК ДатаПриема,
	|	Сотрудники.ДатаУвольнения КАК ДатаУвольнения,
	|	ЕСТЬNULL(ОтпускБезСохраненияОплаты.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачалаОтпуска,
	|	ЕСТЬNULL(ОтпускБезСохраненияОплаты.ДатаОкончания, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОкончанияОтпуска
	|ИЗ
	|	Сотрудники КАК Сотрудники
	|		ПОЛНОЕ СОЕДИНЕНИЕ ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	|		ПО Сотрудники.Сотрудник = ОтпускБезСохраненияОплаты.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудники.Сотрудник,
	|	Сотрудники.ДатаПриема";
	
	ДанныеСотрудников = Запрос.Выполнить().Выгрузить();
	
	НачалоТекущегоПериода = НачалоПериода;
	ПустаяДата = Дата(1,1,1);
	Пока НачалоТекущегоПериода < КонецПериода Цикл
		КонецТекущегоПериода = КонецМесяца(НачалоТекущегоПериода);
		Для Каждого Строка Из ДанныеСотрудников Цикл
			Если Не ЗначениеЗаполнено(Строка.Сотрудник) Тогда
				Продолжить;
			КонецЕсли;
			
			СотрудникРаботает = Ложь;
			ОтсутствуетУвольнение = Строка.ДатаУвольнения = ПустаяДата;
			
			Если ОтсутствуетУвольнение Тогда
				// Сотрудник после трудоустройства не увольнялся.
				Если Строка.ДатаПриема <= КонецТекущегоПериода Тогда
					СотрудникРаботает = Истина;
				КонецЕсли;
			ИначеЕсли Макс(Строка.ДатаПриема, НачалоТекущегоПериода) <= Мин(Строка.ДатаУвольнения, КонецТекущегоПериода) Тогда
				// Сотрудник уволился позже проверяемого периода.
				СотрудникРаботает = Истина;
			КонецЕсли;
			
			Если СотрудникРаботает И Строка.ДатаНачалаОтпуска > ПустаяДата Тогда
				// Отпуск без сохранения заработной платы.
				Если Строка.ДатаНачалаОтпуска <= НачалоТекущегоПериода И Строка.ДатаОкончанияОтпуска >= КонецТекущегоПериода Тогда
					СотрудникРаботает = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если СотрудникРаботает Тогда
				Результат.Добавить(НачалоТекущегоПериода);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		НачалоТекущегоПериода = НачалоМесяца(ДобавитьМесяц(НачалоТекущегоПериода, 1));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПериодыРасчетовПоСоциальномуСтрахованию(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетУчета", ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Хозрасчетный.Период, МЕСЯЦ) КАК Период,
	|	Хозрасчетный.СчетКт КАК Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.СчетКт В ИЕРАРХИИ(&СчетУчета)
	|	И Хозрасчетный.Организация = &Организация
	|	И Хозрасчетный.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Период");
	Возврат Результат;
	
КонецФункции

Функция ПериодыРасчетовОрганизацииРаботодателя(Организация, НачалоПериода, КонецПериода) Экспорт
	
	// В заданном периоде были начисления (зарплата, ГПХ, взносы)
	СчетаРасчетов = СчетаРасчетовПоЗарплате();
	
	СчетаВзносов = СчетаВзносов();
	
	// Необходимо исключить обороты операций, не являющихся начислением взносов:
	//   - возврат из фондов;
	//   - корректировки;
	//   - техническое операции:
	//       - ввод остатков;
	//       - перенос на ЕНС...
	//  и т.п.
	КорСчетаПрочихОпераций = СчетаПрочихОпераций();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("НачалоПериода",          НачалоМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода",           КонецМесяца(КонецПериода));
	Запрос.УстановитьПараметр("СчетаРасчетов",          СчетаРасчетов);
	Запрос.УстановитьПараметр("СчетаВзносов",           СчетаВзносов);
	Запрос.УстановитьПараметр("КорСчетаПрочихОпераций", КорСчетаПрочихОпераций);
	Запрос.УстановитьПараметр("ВидыПлатежей",           Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Хозрасчетный.Период КАК Период
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Хозрасчетный.Период, МЕСЯЦ) КАК Период
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто КАК Хозрасчетный
	|	ГДЕ
	|		Хозрасчетный.Период >= &НачалоПериода
	|		И Хозрасчетный.Период <= &КонецПериода
	|		И Хозрасчетный.СчетКт В(&СчетаВзносов)
	|		И Хозрасчетный.Организация = &Организация
	|		И Хозрасчетный.СубконтоКт1 В(&ВидыПлатежей)
	|		И НЕ Хозрасчетный.СчетДт В (&КорСчетаПрочихОпераций)
	|		И Хозрасчетный.Сумма <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Хозрасчетный.Период, МЕСЯЦ)
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|	ГДЕ
	|		Хозрасчетный.Период >= &НачалоПериода
	|		И Хозрасчетный.Период <= &КонецПериода
	|		И Хозрасчетный.СчетКт В(&СчетаРасчетов)
	|		И Хозрасчетный.Организация = &Организация
	|		И Хозрасчетный.Сумма <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(СведенияОбУдержанномНДФЛ.Месяц, МЕСЯЦ)
	|	ИЗ
	|		Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
	|	ГДЕ
	|		СведенияОбУдержанномНДФЛ.Организация = &Организация
	|		И СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СведенияОбУдержанномНДФЛ.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОтражениеЗарплатыВБухучете.ПериодРегистрации, МЕСЯЦ)
	|	ИЗ
	|		Документ.ОтражениеЗарплатыВБухучете КАК ОтражениеЗарплатыВБухучете
	|	ГДЕ
	|		ОтражениеЗарплатыВБухучете.Организация = &Организация
	|		И ОтражениеЗарплатыВБухучете.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ОтражениеЗарплатыВБухучете.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОтражениеЗарплатыВУчете.ПериодРегистрации, МЕСЯЦ)
	|	ИЗ
	|		Документ.ОтражениеЗарплатыВУчете КАК ОтражениеЗарплатыВУчете
	|	ГДЕ
	|		ОтражениеЗарплатыВУчете.Организация = &Организация
	|		И ОтражениеЗарплатыВУчете.ПериодРегистрации МЕЖДУ &НачалоПериода И &НачалоПериода
	|		И ОтражениеЗарплатыВУчете.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания, МЕСЯЦ)
	|	ИЗ
	|		Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
	|	ГДЕ
	|		СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Организация = &Организация
	|		И СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Проведен) КАК Хозрасчетный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Период");
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеМетоды

Функция НеобходимоСформироватьКадровыеПриказы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Получение необходимых текущих данных.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения <> ТекущиеКадровыеДанныеСотрудников.ДатаПриема";
		
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура УстановкаНастроекПоУмолчанию(ИмяРегистра, Запись, ДанныеЗаполнения) Экспорт
	
	Если ИмяРегистра = "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев" Тогда
		УстановкаНастроекПоУмолчаниюСтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев(Запись, ДанныеЗаполнения);
	ИначеЕсли ИмяРегистра = "ПрименяемыеТарифыСтраховыхВзносов" Тогда
		УстановкаНастроекПоУмолчаниюПрименяемыеТарифыСтраховыхВзносов(Запись, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановкаНастроекПоУмолчаниюСтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев(Запись, ДанныеЗаполнения)
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Период",
		НачалоГода(ТекущаяДатаСеанса()));
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Ставка",
		НулеваяСтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев());
	
КонецПроцедуры

Функция НулеваяСтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев() Экспорт
	
	// В отдельных случаях по умолчанию требуется установить значение "0".
	// Например, при применении АУСН.
	
	Возврат 0;
	
КонецФункции

Процедура УстановкаНастроекПоУмолчаниюПрименяемыеТарифыСтраховыхВзносов(Запись, ДанныеЗаполнения)
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"Период",
		НачалоГода(ТекущаяДатаСеанса()));
	
	НастройкиСистемыНалогообложения = РегистрыСведений.НастройкиСистемыНалогообложения.СрезПоследних(Запись.Период, Новый Структура("Организация", Запись.ГоловнаяОрганизация));
	Если НастройкиСистемыНалогообложения.Количество() <> 0 Тогда
		СистемаНалогообложения    = НастройкиСистемыНалогообложения[0].СистемаНалогообложения;
		ПрименяетсяАУСН           = НастройкиСистемыНалогообложения[0].ПрименяетсяАУСН
			Или НастройкиСистемыНалогообложения[0].ПрименяетсяАУСНДоходы;
		ПрименяетсяУСН            = НастройкиСистемыНалогообложения[0].ПрименяетсяУСН;
		ПлательщикНалогаНаПрибыль = НастройкиСистемыНалогообложения[0].ПлательщикНалогаНаПрибыль;
		ПлательщикЕНВД         = НастройкиСистемыНалогообложения[0].ПлательщикЕНВД;
		ПрименяетсяУСНПатент   = НастройкиСистемыНалогообложения[0].ПрименяетсяУСНПатент;
	Иначе
		СистемаНалогообложения    = УчетнаяПолитика.СистемаНалогообложения(Запись.ГоловнаяОрганизация, Запись.Период);
		ПрименяетсяАУСН           = УчетнаяПолитика.ПрименяетсяАУСН(Запись.ГоловнаяОрганизация, Запись.Период);
		ПрименяетсяУСН            = УчетнаяПолитика.ПрименяетсяУСН(Запись.ГоловнаяОрганизация, Запись.Период);
		ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Запись.ГоловнаяОрганизация, Запись.Период);
		ПлательщикЕНВД            = УчетнаяПолитика.ПлательщикЕНВД(Запись.ГоловнаяОрганизация, Запись.Период);
		ПрименяетсяУСНПатент       = УчетнаяПолитика.ПрименяетсяУСНПатент(Запись.ГоловнаяОрганизация, Запись.Период);
	КонецЕсли;
	
	Если ПрименяетсяАУСН Тогда
		ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.АУСН;
	ИначеЕсли ПрименяетсяУСН Тогда
		ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим;
	ИначеЕсли СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок
		И ПлательщикЕНВД Тогда
		ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.ЕНВД;
	ИначеЕсли СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок
			И ПрименяетсяУСНПатент Тогда
		ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент;
	Иначе
		ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим;
	КонецЕсли;
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ВидТарифа",
		ВидТарифа);
	
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ОрганизацияИнвалидов",
		Ложь);
		
	НастройкиУчета.УстановитьЗначениеПоУмолчанию(
		Запись,
		ДанныеЗаполнения,
		"ДатаРегистрацииИзменений",
		Запись.Период);
	
КонецПроцедуры

Процедура ВключитьИспользоватьРасчетПервойПоловиныМесяца() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Константы.ИспользоватьРасчетПервойПоловиныМесяца.Получить() Тогда
		ВключитьРасчетЗаПервуюПоловинуМесяца();
	КонецЕсли;
	
КонецПроцедуры

Функция ТарифВзносовСоответствуетУчетнойПолитике(ГоловнаяОрганизация, Период) Экспорт
	
	ПроверкаПройдена = Ложь;
	
	ПрименяемыеТарифыСтраховыхВзносов = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СрезПоследних(Период, Новый Структура("ГоловнаяОрганизация", ГоловнаяОрганизация));
	НастройкиСистемыНалогообложения = РегистрыСведений.НастройкиСистемыНалогообложения.СрезПоследних(Период, Новый Структура("Организация", ГоловнаяОрганизация));
	
	Если НастройкиСистемыНалогообложения.Количество() <> 0 И ПрименяемыеТарифыСтраховыхВзносов.Количество() <> 0 Тогда
		
		СистемаНалогообложения = НастройкиСистемыНалогообложения[0].СистемаНалогообложения;
		ПрименяетсяАУСН        = НастройкиСистемыНалогообложения[0].ПрименяетсяАУСН
			Или НастройкиСистемыНалогообложения[0].ПрименяетсяАУСНДоходы;
		ПрименяетсяУСН         = НастройкиСистемыНалогообложения[0].ПрименяетсяУСН;
		ПрименяетсяУСНПатент   = НастройкиСистемыНалогообложения[0].ПрименяетсяУСНПатент;
		ПлательщикЕНВД         = НастройкиСистемыНалогообложения[0].ПлательщикЕНВД;
		ВидТарифа              = ПрименяемыеТарифыСтраховыхВзносов[0].ВидТарифа;
		
		Если ПрименяетсяАУСН Тогда
			
			ПроверкаПройдена = ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.АУСН;
			
		ИначеЕсли СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок
			И ПрименяетсяУСНПатент Тогда
			
			ПроверкаПройдена =
				ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент
				Или ВидТарифа = Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства;
			
		ИначеЕсли СистемаНалогообложения = Перечисления.СистемыНалогообложения.ОсобыйПорядок
			И ПлательщикЕНВД Тогда
			
			ПроверкаПройдена = ЗначениеЗаполнено(ВидТарифа);
			
		Иначе
			
			ВидОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГоловнаяОрганизация, "ЮридическоеФизическоеЛицо");
			ДоступныеТарифыСтраховыхВзносов = УчетСтраховыхВзносов.ДоступныеТарифыСтраховыхВзносов(ВидОрганизации,
				ПрименяетсяУСН);
			
			ПроверкаПройдена = ДоступныеТарифыСтраховыхВзносов.Найти(ВидТарифа) <> Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПроверкаПройдена;
	
КонецФункции

Функция СформироватьВременнуюТаблицуПеречислениеНДФЛ(ТаблицаПеречислениеНДФЛ, МесяцНалоговогоПериода, РегистрацияВНалоговомОргане,
			ПлатежноеПоручениеДата, ПлатежноеПоручениеНомер)
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаПеречислениеНДФЛ",      ТаблицаПеречислениеНДФЛ);
	Запрос.УстановитьПараметр("МесяцНалоговогоПериода",       МесяцНалоговогоПериода);
	Запрос.УстановитьПараметр("РеквизитыПлатежногоПоручения", УчетНДФЛ.РеквизитыПлатежногоПорученияПоДатеНомеру(ПлатежноеПоручениеДата, ПлатежноеПоручениеНомер));
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане",  ?(РегистрацияВНалоговомОргане = Неопределено, Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка(), РегистрацияВНалоговомОргане));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПеречислениеНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТаблицаПеречислениеНДФЛ.Сумма КАК Сумма,
	|	ТаблицаПеречислениеНДФЛ.Ставка КАК Ставка,
	|	ТаблицаПеречислениеНДФЛ.ИсчисленоПоДивидендам КАК ИсчисленоПоДивидендам,
	|	ТаблицаПеречислениеНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ТаблицаПеречислениеНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода
	|ПОМЕСТИТЬ ТаблицаПеречислениеНДФЛ
	|ИЗ
	|	&ТаблицаПеречислениеНДФЛ КАК ТаблицаПеречислениеНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Налоги.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Налоги.Ставка КАК Ставка,
	|	ВЫБОР
	|		КОГДА Налоги.МесяцНалоговогоПериода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &МесяцНалоговогоПериода
	|		ИНАЧЕ Налоги.МесяцНалоговогоПериода
	|	КОНЕЦ КАК МесяцНалоговогоПериода,
	|	Налоги.Сумма КАК Сумма,
	|	Налоги.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	Налоги.ИсчисленоПоДивидендам КАК ИсчисленоПоДивидендам,
	|	&РеквизитыПлатежногоПоручения КАК РеквизитыПлатежногоПоручения,
	|	&РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТНалогПеречисленный
	|ИЗ
	|	ТаблицаПеречислениеНДФЛ КАК Налоги";
	
	Результаты = Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция СписокФизическихЛицДокументаВыплаты(Ведомость) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ведомость", Ведомость);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.КодДохода КАК КодДохода,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.Подразделение КАК Подразделение,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.Сумма КАК Сумма,
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.КатегорияДохода КАК КатегорияДохода
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВБанк.НДФЛ КАК ВедомостьНаВыплатуЗарплатыВБанкНДФЛ
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВБанкНДФЛ.Ссылка = &Ведомость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.ФизическоеЛицо,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.СтавкаНалогообложенияРезидента,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.МесяцНалоговогоПериода,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.КодДохода,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.РегистрацияВНалоговомОргане,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.Подразделение,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.Сумма,
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.КатегорияДохода
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВКассу.НДФЛ КАК ВедомостьНаВыплатуЗарплатыВКассуНДФЛ
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВКассуНДФЛ.Ссылка = &Ведомость";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВедомостьНаВыплатуЗарплатыВБанкФизическиеЛица.ФизическоеЛицо
		|ИЗ
		|	Документ.ВедомостьНаВыплатуЗарплатыВБанк.ФизическиеЛица КАК ВедомостьНаВыплатуЗарплатыВБанкФизическиеЛица
		|ГДЕ
		|	ВедомостьНаВыплатуЗарплатыВБанкФизическиеЛица.Ссылка = &Ведомость
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВедомостьНаВыплатуЗарплатыВКассуФизическиеЛица.ФизическоеЛицо
		|ИЗ
		|	Документ.ВедомостьНаВыплатуЗарплатыВКассу.ФизическиеЛица КАК ВедомостьНаВыплатуЗарплатыВКассуФизическиеЛица
		|ГДЕ
		|	ВедомостьНаВыплатуЗарплатыВКассуФизическиеЛица.Ссылка = &Ведомость";
		Результат = Запрос.Выполнить();
	КонецЕсли;

	Возврат Результат.Выгрузить();
	
КонецФункции

Функция СтавкиНДФЛФизическихЛиц(ФизическиеЛица, НалоговыйПериод) Экспорт
	
	ДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическиеЛица, "СтатусНалогоплательщика", КонецМесяца(НалоговыйПериод));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДанныеФизЛиц",          ДанныеФизЛиц);
	Запрос.УстановитьПараметр("СтавкаНДФЛРезидента",   СтавкаНДФЛОсновная());
	Запрос.УстановитьПараметр("СтавкаНДФЛНерезидента", СтавкаНДФЛНерезидента());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеФизЛиц.СтатусНалогоплательщика КАК СтатусНалогоплательщика
	|ПОМЕСТИТЬ ВТ_ДанныеФизЛиц
	|ИЗ
	|	&ДанныеФизЛиц КАК ДанныеФизЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ДанныеФизЛиц.СтатусНалогоплательщика, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|			ТОГДА &СтавкаНДФЛНерезидента
	|		ИНАЧЕ &СтавкаНДФЛРезидента
	|	КОНЕЦ КАК Ставка
	|ИЗ
	|	ВТ_ДанныеФизЛиц КАК ВТ_ДанныеФизЛиц";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СтавкаНДФЛДивиденды(НалоговыйПериод) Экспорт
	
	ВидДохода = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыДоходовНДФЛ.Код1010");
	СтавкаНалогообложения = УчетНДФЛБазовый.СтавкаНалогообложенияРезидентаПоКодуДоходаНДФЛ(ВидДохода);
	
	Возврат УчетНДФЛ.СтавкаНДФЛ(СтавкаНалогообложения,
					ВидДохода,
					ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент"),
					Год(НалоговыйПериод));
	
КонецФункции

Функция СтавкаНДФЛСПревышения() Экспорт
	
	Возврат Перечисления.НДФЛСтавки.Ставка15;
	
КонецФункции

Функция СтавкаНДФЛОсновная() Экспорт
	
	Возврат Перечисления.НДФЛСтавки.Ставка13;
	
КонецФункции

Функция СтавкаНДФЛНерезидента() Экспорт
	
	Возврат Перечисления.НДФЛСтавки.Ставка30;
	
КонецФункции

Процедура ОтразитьРасчетыПоНачисленнымПособиямИП(Параметры, ТаблицаЗаработнойПлаты, ТаблицаИПНачислениеЗарплаты, ТаблицаПрочиеРасходы, Реквизиты, Движения, Отказ)
	
	Если Реквизиты.Период < ДатаНачалаАдминистрированияСтраховыхВзносовФНС() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Реквизиты.Регистратор) <> Тип("ДокументСсылка.БольничныйЛист") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРасчетовПоНачисленнымПособиям = ПодготовитьТаблицуРасчетовПоНачисленнымПособиямИП(ТаблицаЗаработнойПлаты, Реквизиты);
	
	РеквизитыИПТаблица = Параметры.Реквизиты.Скопировать();
	РеквизитыИПТаблица.Колонки.Добавить("СчетУчета",   Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	РеквизитыИПТаблица.Колонки.Добавить("ВидОперации", Перечисления.ТипВсеСсылки());
	РеквизитыИПТаблица.Колонки.Добавить("ПлатежныйДокумент", Документы.ТипВсеСсылки());
	
	РеквизитыИПТаблица[0].Период            = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.Регистратор, "Дата");
	РеквизитыИПТаблица[0].ПлатежныйДокумент = Реквизиты.Регистратор;
	
	ПеречислениеНалогаИПТаблица = Новый ТаблицаЗначений;
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("ИмяСписка",     ОбщегоНазначения.ОписаниеТипаСтрока(1));
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("СинонимСписка", ОбщегоНазначения.ОписаниеТипаСтрока(1));
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("НомерСтроки",   ОбщегоНазначения.ОписаниеТипаЧисло(1));
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("СчетДт",        Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("СубконтоДт1",   Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПлатежейВГосБюджет"));
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("СубконтоДт2",   Перечисления.ТипВсеСсылки());
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("СубконтоДт3",   Перечисления.ТипВсеСсылки());
	ПеречислениеНалогаИПТаблица.Колонки.Добавить("СуммаРуб",      ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	НоваяСтрока = ПеречислениеНалогаИПТаблица.Добавить();
	НоваяСтрока.СубконтоДт1 = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаРасчетовПоНачисленнымПособиям Цикл
		
		СписокПереплат = УчетДоходовИРасходовПредпринимателя.ПолучитьПустуюТаблицуСписокПереплат();
		
		Если СтрокаТаблицы.СуммаПереплаты > 0 Тогда
			НоваяСтрока = СписокПереплат.Добавить();
			НоваяСтрока.Контрагент = Неопределено;
			НоваяСтрока.СчетУчета  = СтрокаТаблицы.СчетУчета;
			НоваяСтрока.Сумма = СтрокаТаблицы.СуммаПереплаты;
			
			РеквизитыИПТаблица[0].СчетУчета = СтрокаТаблицы.СчетУчета;
			
			ТаблицыУплатыВзносовФОТИП = Новый Соответствие;
			ТаблицыУплатыВзносовФОТИП.Вставить(СтрокаТаблицы.СчетУчета,
				Новый Структура("СписокВзносов, СписокПереплат", УчетДоходовИРасходовПредпринимателя.ПолучитьПустуюТаблицуСписокВзносов(),, СписокПереплат));
			УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияУплатаВзносовФОТ(
				ТаблицыУплатыВзносовФОТИП,
				РеквизитыИПТаблица,
				Движения,
				Отказ);
		КонецЕсли;
		
		Если СтрокаТаблицы.СуммаКЗачету <> 0 Тогда
			
			РеквизитыИПТаблица[0].СчетУчета         = СтрокаТаблицы.СчетУчета;
			ПеречислениеНалогаИПТаблица[0].СчетДт   = СтрокаТаблицы.СчетУчета;
			ПеречислениеНалогаИПТаблица[0].СуммаРуб = СтрокаТаблицы.СуммаКЗачету;
			
			ТаблицыУплатаНалогов = Новый Структура("ПеречислениеНалогаИП",
				ПеречислениеНалогаИПТаблица);
		
			ТаблицыУплатыВзносовФОТИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыУплатыВзносовФОТ(
				ТаблицыУплатаНалогов, РеквизитыИПТаблица);
			
			СтруктураОтбора = Новый Структура("Счет, Субконто1", СтрокаТаблицы.СчетУчета, Перечисления.ВидыПлатежейВГосБюджет.Налог);
			МассивСтрок = ТаблицаИПНачислениеЗарплаты.НайтиСтроки(СтруктураОтбора);
			
			Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
				СтруктураОтбора = Новый Структура("СчетУчета, СтатьяЗатрат", СтрокаТаблицы.СчетУчета, СтрокаМассива.Субконто1);
				МассивСтрокПрочиеРасходы = ТаблицаПрочиеРасходы.НайтиСтроки(СтруктураОтбора);
				Для Каждого СтрокаМассиваПрочиеРасходы ИЗ МассивСтрокПрочиеРасходы Цикл
					Если СтрокаТаблицы.СуммаКЗачету = 0 Тогда
						Прервать;
					КонецЕсли;
					НоваяСтрока = ТаблицыУплатыВзносовФОТИП[ТаблицыУплатыВзносовФОТИП].СписокВзносов.Добавить();
					НоваяСтрока.ДокументОплаты       = Реквизиты.Регистратор;
					НоваяСтрока.РасчетныйДокумент    = Реквизиты.Регистратор;
					НоваяСтрока.СтатьяЗатрат         = СтрокаМассиваПрочиеРасходы.СтатьяЗатрат;
					НоваяСтрока.НоменклатурнаяГруппа = СтрокаМассиваПрочиеРасходы.НоменклатурнаяГруппа;
					НоваяСтрока.ХарактерДеятельности = СтрокаМассиваПрочиеРасходы.ХарактерДеятельности;
					НоваяСтрока.СуммаОплаты          = Мин(СтрокаТаблицы.СуммаКЗачету, СтрокаМассива.Сумма);
					
					СтрокаТаблицы.СуммаКЗачету = СтрокаТаблицы.СуммаКЗачету - НоваяСтрока.СуммаОплаты;
					
				КонецЦикла;
			КонецЦикла;
			
			Если ТаблицыУплатыВзносовФОТИП[СтрокаТаблицы.СчетУчета].СписокВзносов.Количество() <> 0 Тогда
				СписокПереплат.Очистить();
				ТаблицыУплатыВзносовФОТИП[СтрокаТаблицы.СчетУчета].СписокПереплат = СписокПереплат;
				УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияУплатаВзносовФОТ(
					ТаблицыУплатыВзносовФОТИП,
					РеквизитыИПТаблица,
					Движения,
					Отказ);
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьТаблицуРасчетовПоНачисленнымПособиямИП(ТаблицаЗаработнойПлаты, Реквизиты)
	
	СчетаУчетаВзносов = Новый Массив;
	СчетаУчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ФСС);
	СчетаУчетаВзносов.Добавить(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",       Реквизиты.Организация);
	Запрос.УстановитьПараметр("Период",            Новый Граница(Новый МоментВремени(Реквизиты.Регистратор.Дата, Реквизиты.Регистратор), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("СчетаУчетаВзносов", СчетаУчетаВзносов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрочиеРасчетыОстатки.СчетУчета КАК СчетУчета,
	|	СУММА(ПрочиеРасчетыОстатки.СуммаОстаток) КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ПрочиеРасчеты.Остатки(
	|			&Период,
	|			Контрагент = НЕОПРЕДЕЛЕНО
	|				И СчетУчета В (&СчетаУчетаВзносов)
	|				И Организация = &Организация) КАК ПрочиеРасчетыОстатки
	|ГДЕ
	|	ПрочиеРасчетыОстатки.СуммаОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеРасчетыОстатки.СчетУчета";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаВзносов = Новый ТаблицаЗначений;
	ТаблицаВзносов.Колонки.Добавить("СчетУчета",      Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаВзносов.Колонки.Добавить("СуммаКЗачету",   ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаВзносов.Колонки.Добавить("СуммаПереплаты", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	ТаблицаЗаработнойПлаты.Индексы.Добавить("ВидОперации");
	
	ТаблицаРасчетов    = ТаблицаЗаработнойПлаты.Скопировать(Новый Структура("ВидОперации", Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС));
	СуммаРасчетовФСС   = ТаблицаРасчетов.Итог("Сумма");
	ТаблицаРасчетов    = ТаблицаЗаработнойПлаты.Скопировать(Новый Структура("ВидОперации", Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС));
	СуммаРасчетовФССНС = ТаблицаРасчетов.Итог("Сумма");
	
	Если РезультатЗапроса.Пустой() Тогда
		Если СуммаРасчетовФСС <> 0 Тогда
			НоваяСтрока = ТаблицаВзносов.Добавить();
			НоваяСтрока.СчетУчета      = ПланыСчетов.Хозрасчетный.ФСС;
			НоваяСтрока.СуммаПереплаты = СуммаРасчетовФСС;
		КонецЕсли;
		Если СуммаРасчетовФССНС <> 0 Тогда
			НоваяСтрока = ТаблицаВзносов.Добавить();
			НоваяСтрока.СчетУчета      = ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ;
			НоваяСтрока.СуммаПереплаты = СуммаРасчетовФССНС;
		КонецЕсли;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаВзносов.Добавить();
			НоваяСтрока.СчетУчета = Выборка.СчетУчета;
			Если Выборка.СчетУчета = ПланыСчетов.Хозрасчетный.ФСС Тогда
				СуммаРасчетов = СуммаРасчетовФСС;
			Иначе
				СуммаРасчетов = СуммаРасчетовФССНС;
			КонецЕсли;
			
			Если СуммаРасчетов < Выборка.СуммаОстаток Тогда
				НоваяСтрока.СуммаКЗачету = СуммаРасчетов;
			Иначе
				НоваяСтрока.СуммаКЗачету   = Выборка.СуммаОстаток;
				НоваяСтрока.СуммаПереплаты = СуммаРасчетов - Выборка.СуммаОстаток;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаВзносов;
	
КонецФункции

Функция ДатаНачалаАдминистрированияСтраховыхВзносовФНС() Экспорт
	
	Возврат '20170101';
	
КонецФункции

Процедура ПроверитьПоддержкуСовместительства(ВидЗанятости) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ПоддержкаСовместительства") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидЗанятости)
		И ВидЗанятости <> Перечисления.ВидыЗанятости.ОсновноеМестоРаботы Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ПоддержкаСовместительства.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПериодРегистрации(Регистратор, Знач ПериодРегистрации) Экспорт
	
	ПериодРегистрации = КонецМесяца(ПериодРегистрации);
	ДатаРегистратора  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Дата");
	
	Если КонецМесяца(ДатаРегистратора) = ПериодРегистрации Тогда
		Если КонецДня(ДатаРегистратора) = ПериодРегистрации Тогда
			ПериодРегистрации = ДатаРегистратора;
		Иначе
			Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.Отпуск")
				ИЛИ ТипЗнч(Регистратор) = Тип("ДокументСсылка.БольничныйЛист")
				ИЛИ ТипЗнч(Регистратор) = Тип("ДокументСсылка.ДепонированиеЗарплаты")
				ИЛИ ТипЗнч(Регистратор) = Тип("ДокументСсылка.Увольнение")
				ИЛИ ТипЗнч(Регистратор) = Тип("ДокументСсылка.Премия") Тогда
				ПериодРегистрации = ДатаРегистратора;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПериодРегистрации;
	
КонецФункции

Процедура ЗаполнитьСведенияОбОтветственных(ОписаниеПодписей, Организация, ЗначенияПодписей) Экспорт

	ОтветственныеЛицаОрганизации = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, ТекущаяДата());
	
	ОписаниеРуководителя = ОписаниеПодписей["Руководитель"];
	Если ОписаниеРуководителя <> Неопределено Тогда
		ЗначенияПодписей.Вставить(ОписаниеРуководителя["ФизическоеЛицо"], ОтветственныеЛицаОрганизации.Руководитель);
		ЗначенияПодписей.Вставить(ОписаниеРуководителя["Должность"], ОтветственныеЛицаОрганизации.РуководительДолжность);
	КонецЕсли;
	
	ОписаниеГлавногоБухгалтера = ОписаниеПодписей["ГлавныйБухгалтер"];
	Если ОписаниеГлавногоБухгалтера <> Неопределено Тогда
		ЗначенияПодписей.Вставить(ОписаниеГлавногоБухгалтера["ФизическоеЛицо"], ОтветственныеЛицаОрганизации.ГлавныйБухгалтер);
		ЗначенияПодписей.Вставить(ОписаниеГлавногоБухгалтера["Должность"], ОтветственныеЛицаОрганизации.ГлавныйБухгалтерДолжность);
	КонецЕсли;
	
	ОписаниеКассира = ОписаниеПодписей["Кассир"];
	Если ОписаниеКассира <> Неопределено Тогда
		ЗначенияПодписей.Вставить(ОписаниеКассира["ФизическоеЛицо"], ОтветственныеЛицаОрганизации.Кассир);
		ЗначенияПодписей.Вставить(ОписаниеКассира["Должность"], ОтветственныеЛицаОрганизации.КассирДолжность);
	КонецЕсли;
	
	ОписаниеБухгалтера = ОписаниеПодписей["Бухгалтер"];
	Если ОписаниеБухгалтера <> Неопределено Тогда
		ЗначенияПодписей.Вставить(ОписаниеБухгалтера["ФизическоеЛицо"], ОтветственныеЛицаОрганизации.ГлавныйБухгалтер);
		ЗначенияПодписей.Вставить(ОписаниеБухгалтера["Должность"], ОтветственныеЛицаОрганизации.ГлавныйБухгалтерДолжность);
	КонецЕсли;
	
	ОписаниеПредставительСФР = ОписаниеПодписей["ПредставительСФР"];
	Если ОписаниеПредставительСФР <> Неопределено Тогда
		Если ЗначениеЗаполнено(ОтветственныеЛицаОрганизации.ГлавныйБухгалтер) Тогда
			ЗначенияПодписей.Вставить(ОписаниеПредставительСФР["ФизическоеЛицо"], ОтветственныеЛицаОрганизации.ГлавныйБухгалтер);
			ЗначенияПодписей.Вставить(ОписаниеПредставительСФР["Должность"], ОтветственныеЛицаОрганизации.ГлавныйБухгалтерДолжность);
		ИначеЕсли ЗначениеЗаполнено(ОтветственныеЛицаОрганизации.Руководитель) Тогда
			ЗначенияПодписей.Вставить(ОписаниеПредставительСФР["ФизическоеЛицо"], ОтветственныеЛицаОрганизации.Руководитель);
			ЗначенияПодписей.Вставить(ОписаниеПредставительСФР["Должность"], ОтветственныеЛицаОрганизации.РуководительДолжность);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив начислений, соответствующие параметрам.
//
// Параметры:
//   КатегорияНачисления - ПеречислениеСсылка.КатегорииНачисленийИНеоплаченногоВремени - Категория начисления.
//   Отбор - Структура - Отбор по начислениям. См. описание параметра "Отбор" функции НачисленияПоОтбору.
//
// Возвращаемое значение:
//   Массив - Начисления, соответствующие отбору.
//
Функция НачисленияПоКатегории(КатегорияНачисления, Отбор = Неопределено) Экспорт
	
	Возврат ПланыВидовРасчета.Начисления.НачисленияПоКатегории(КатегорияНачисления, Отбор);
	
КонецФункции

Функция МинимальныйРазмерОплатыТрудаРФ(Период) Экспорт
	
	Возврат ЗарплатаКадры.МинимальныйРазмерОплатыТрудаРФ(Период);
	
КонецФункции

Процедура УстановитьРольСотрудникаПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ПериодыДействияДоговоровГражданскоПравовогоХарактера") Тогда
		Если Источник.Отбор.Регистратор.Использование И ЗначениеЗаполнено(Источник.Отбор.Регистратор.Значение) Тогда
			Регистратор = Источник.Отбор.Регистратор.Значение;
			Сотрудник   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Сотрудник");
			
			Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
				Возврат;
			КонецЕсли;
			
			РольСотрудника = Перечисления.РолиСотрудников.Договорник;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Сотрудник",      Сотрудник);
			Запрос.УстановитьПараметр("Регистратор",    Регистратор);
			Запрос.УстановитьПараметр("РольСотрудника", РольСотрудника);
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПериодыДействияДоговоровГражданскоПравовогоХарактера.Сотрудник КАК Сотрудник
			|ИЗ
			|	РегистрСведений.РолиСотрудников КАК РолиСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера КАК ПериодыДействияДоговоровГражданскоПравовогоХарактера
			|		ПО РолиСотрудников.Сотрудник = ПериодыДействияДоговоровГражданскоПравовогоХарактера.Сотрудник
			|			И (ПериодыДействияДоговоровГражданскоПравовогоХарактера.Регистратор <> &Регистратор)
			|ГДЕ
			|	РолиСотрудников.Сотрудник = &Сотрудник
			|	И РолиСотрудников.РольСотрудника = &РольСотрудника
			|	И НЕ ПериодыДействияДоговоровГражданскоПравовогоХарактера.Регистратор ЕСТЬ NULL";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Возврат;
			КонецЕсли;
			
			Сотрудники = Новый ТаблицаЗначений;
			Сотрудники.Колонки.Добавить("Сотрудник");
			НоваяСтрока = Сотрудники.Добавить();
			НоваяСтрока.Сотрудник = Сотрудник;
		Иначе
			
			РольСотрудника = Перечисления.РолиСотрудников.Договорник;
			Сотрудники = Источник.Выгрузить(, "Сотрудник");
			Сотрудники.Свернуть("Сотрудник");
		КонецЕсли;
	Иначе
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") И
			ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ТекущиеКадровыеДанныеСотрудников") Тогда
			// Этот регистр только при упрощенном учете используется
			Возврат;
		КонецЕсли;
		
		Сотрудники = Источник.Выгрузить(, "Сотрудник");
		Сотрудники.Свернуть("Сотрудник");
		РольСотрудника = Перечисления.РолиСотрудников.Работник;
	КонецЕсли;
	
	Для каждого СтрокаСотрудник Из Сотрудники Цикл
		НаборЗаписей = РегистрыСведений.РолиСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаСотрудник.Сотрудник);
		НаборЗаписей.Отбор.РольСотрудника.Установить(РольСотрудника);
		НаборЗаписей.Прочитать();
		
		Если Источник.Выгрузить().Количество() = 0 Или ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
			// Запись удалена
			Если НаборЗаписей.Количество() <> 0 Тогда
				НаборЗаписей.Удалить(0);
				НаборЗаписей.Записать();
			КонецЕсли;
		Иначе
			// Запись обновлена
			Если НаборЗаписей.Количество() = 0 Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Сотрудник = СтрокаСотрудник.Сотрудник;
				НоваяЗапись.РольСотрудника = РольСотрудника;
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	
КонецПроцедуры

Функция ВедомостьПоДоговоруПодряда(ПлатежнаяВедомость) Экспорт
	
	Если ТипЗнч(ПлатежнаяВедомость) <> Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк")
		И ТипЗнч(ПлатежнаяВедомость) <> Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу")
		И ТипЗнч(ПлатежнаяВедомость) <> Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПлатежнаяВедомость) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ПлатежнаяВедомость) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
		СпособыВыплатыДоговорПодряда     = Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами;
		СпособыВыплатыПлатежнойВедомости = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПлатежнаяВедомость, "СпособРасчетов");
	Иначе
		СпособыВыплатыДоговорПодряда     = Справочники.СпособыВыплатыЗарплаты.ДоговорПодряда();
		СпособыВыплатыПлатежнойВедомости = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПлатежнаяВедомость, "СпособВыплаты");
	КонецЕсли;
	
	Возврат СпособыВыплатыПлатежнойВедомости = СпособыВыплатыДоговорПодряда;
	
КонецФункции

// Используется для получения ссылки на регистрацию в налоговом органе подразделения.
//
// Параметры:
//  СтруктурнаяЕдиница	 - СправочникСсылка.ПодразделенияОрганизаций, 
//						   СправочникСсылка.Организации, 
//						   ОпределяемыйТип.ТерриторияВыполненияРабот  - структурная единица для получения регистрации в НО.
//  ДатаАктуальности     - Дата                                       - дата, на которую необходимо получить
//                                                                          регистрацию в НО.
// 
// Возвращаемое значение:
//  СправочникСсылка.РегистрацииВНалоговомОргане - ссылка на существующую регистрацию, либо ПустаяСсылка().
//
Функция РегистрацияВНалоговомОргане(СтруктурнаяЕдиница, Знач ДатаАктуальности = Неопределено) Экспорт
	
	Если ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		
		Возврат ЗарплатаКадры.РегистрацияВНалоговомОрганеПодразделения(СтруктурнаяЕдиница, ДатаАктуальности);
		
	Иначе
		
		Возврат ЗарплатаКадры.РегистрацияВНалоговомОрганеОрганизации(СтруктурнаяЕдиница, ДатаАктуальности);
		
	КонецЕсли;
	
КонецФункции

// Возвращает структуру с настройкой бухучета сотрудника на указанную дату.
//
// Параметры:
//  Сотрудник - Тип СправочникСсылка.Сотрудник
//  ДатаАктуальности - Тип Дата, дата на которую получаем способ отражения.
//
// Возвращаемое значение: Структура, ключи СпособОтраженияЗарплатыВБухучете, ОтношениеКЕНВД, Период
//		СпособОтраженияЗарплатыВБухучете, тип Справочник.СпособыОтраженияЗарплатыВБухУчете
//		ОтношениеКЕНВД, тип Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.
//		Период, дата на которую установлено значение.
//
Функция НастройкаБухучетаЗарплатыСотрудника(Сотрудник, ДатаАктуальности) Экспорт
	
	Возврат ОтражениеЗарплатыВБухучетеБазовый.НастройкаБухучетаЗарплатыСотрудника(Сотрудник, ДатаАктуальности);
	
КонецФункции

Функция ПланируемаяДатаВыплатыЗарплаты(Организация, ТекущийМесяц) Экспорт
	
	ДатыВыплаты = РасчетЗарплатыДляНебольшихОрганизацийПовтИсп.ДатыВыплатыОрганизации(Организация, ТекущийМесяц);
	
	Возврат ДатыВыплаты.Зарплата;
	
КонецФункции

Функция ПланируемаяДатаВыплатыАванса(Организация, ТекущийМесяц) Экспорт
	
	ДатыВыплаты = РасчетЗарплатыДляНебольшихОрганизацийПовтИсп.ДатыВыплатыОрганизации(Организация, ТекущийМесяц);
	
	Возврат ДатыВыплаты.Аванс;
	
КонецФункции

Функция ПорядокВыплатыЗарплатыПоУмолчанию() Экспорт
	
	ПорядокВыплатыЗарплатыПоУмолчанию = Новый Структура("ДатаВыплатыАвансаНеПозжеЧем, ДатаВыплатыЗарплатыНеПозжеЧем, ВыплачиватьЗарплатуВПоследнийДеньМесяца");
	
	ДатаВыплатыАвансаНеПозжеЧем   = Метаданные.РегистрыСведений.НастройкиЗарплатаКадры.Ресурсы.ДатаВыплатыАвансаНеПозжеЧем.ЗначениеЗаполнения;
	Если Не ЗначениеЗаполнено(ДатаВыплатыАвансаНеПозжеЧем) Тогда
		ДатаВыплатыАвансаНеПозжеЧем = 20;
	КонецЕсли;
	ДатаВыплатыЗарплатыНеПозжеЧем = Метаданные.РегистрыСведений.НастройкиЗарплатаКадры.Ресурсы.ДатаВыплатыЗарплатыНеПозжеЧем.ЗначениеЗаполнения;
	Если Не ЗначениеЗаполнено(ДатаВыплатыЗарплатыНеПозжеЧем) Тогда
		ДатаВыплатыЗарплатыНеПозжеЧем = 5;
	КонецЕсли;
	
	ПорядокВыплатыЗарплатыПоУмолчанию.ДатаВыплатыАвансаНеПозжеЧем             = ДатаВыплатыАвансаНеПозжеЧем;
	ПорядокВыплатыЗарплатыПоУмолчанию.ДатаВыплатыЗарплатыНеПозжеЧем           = ДатаВыплатыЗарплатыНеПозжеЧем;
	ПорядокВыплатыЗарплатыПоУмолчанию.ВыплачиватьЗарплатуВПоследнийДеньМесяца = Ложь;
	
	Возврат ПорядокВыплатыЗарплатыПоУмолчанию;
	
КонецФункции

Функция ГрафикРаботыОрганизации(Организация) Экспорт
	
	ГрафикРаботы = Справочники.Календари.НайтиПоРеквизиту("ВладелецГрафика", Организация);
	Если ГрафикРаботы = Справочники.Календари.ПустаяСсылка() Тогда
		УчетЗарплаты.СоздатьГрафикРаботыОрганизации(Организация);
		ГрафикРаботы = Справочники.Календари.НайтиПоРеквизиту("ВладелецГрафика", Организация);
	КонецЕсли;
	
	Возврат ГрафикРаботы;
	
КонецФункции

Процедура ФормаДокументаУстановитьПараметрыВыбораНачислений(УправляемаяФорма, Организация, Период) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ФормаДокументаУстановитьПараметрыВыбораНачислений(УправляемаяФорма, Организация, Период);
	КонецЕсли;
	
	ЭлементФормы = УправляемаяФорма.Элементы.Найти("НачисленияНачисление");
	Если ЭлементФормы <> Неопределено Тогда
		ПараметрыВыбора = Новый Массив(ЭлементФормы["ПараметрыВыбора"]);
	Иначе
		Возврат;
	КонецЕсли;
	
	МассивКатегорий = Новый Массив;
	МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
	МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее);
	
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.КатегорияНачисленияИлиНеоплаченногоВремени", Новый ФиксированныйМассив(МассивКатегорий)));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"НачисленияНачисление",
		"ПараметрыВыбора",
		Новый ФиксированныйМассив(ПараметрыВыбора));
	
КонецПроцедуры

Функция ОплатаПоОкладу(Организация, Дата) Экспорт
	
	ОтборНачислений = Новый Структура;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ДополнитьОтборНачислений(ОтборНачислений, Организация, Дата);
	КонецЕсли;
	
	ПовременныеНачисления = ПланыВидовРасчета.Начисления.НачисленияПоКатегории(
		Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда, ОтборНачислений);
	
	Если ПовременныеНачисления.Количество() = 0 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ПланыВидовРасчета.Начисления.СоздатьОплатуПоОкладу();
		УстановитьПривилегированныйРежим(Ложь);
		
		ПовременныеНачисления = ПланыВидовРасчета.Начисления.НачисленияПоКатегории(
			Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
		
	КонецЕсли;
	
	Возврат ПовременныеНачисления[0];
	
КонецФункции

Функция КодВидаДоходаПоВидуДоходаИсполнительногоПроизводства(ВидДоходаИсполнительногоПроизводства) Экспорт
	
	КодВидаДохода = "";
	// Перечисления.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий
	// Не связан с оплатой труда, поэтому не поддерживается
	Если ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения Тогда
		КодВидаДохода = "1";
	ИначеЕсли ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемые Тогда
		КодВидаДохода = "2";
	ИначеЕсли ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеПериодические Тогда
		КодВидаДохода = "2";
	ИначеЕсли ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемые Тогда
		КодВидаДохода = "3";
	ИначеЕсли ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеПериодические Тогда
		КодВидаДохода = "3";
	ИначеЕсли ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеЕдиновременные Тогда
		КодВидаДохода = "4";
	ИначеЕсли ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеЕдиновременные Тогда
		КодВидаДохода = "5";
	Иначе
		КодВидаДохода = "1";
	КонецЕсли;
	
	Возврат КодВидаДохода;
	
КонецФункции

Функция ВедетсяУчетПоПодразделениям() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ВестиУчетЗатратПоПодразделениям")
		ИЛИ ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям")
		ИЛИ ПолучитьФункциональнуюОпцию("РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоПодразделений");
	
КонецФункции

Процедура ПереопределитьСтатусИСостояниеСдачиОтчетности(Ссылка, СтатусОтправки, СостояниеСдачиОтчетности) Экспорт
	
	Если СостояниеСдачиОтчетности <> Перечисления.СостояниеСдачиОтчетности.ДокументооборотНеНачат Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Проведен, ПометкаУдаления, ДокументПринятВПФР");
		
		Если ЗначенияРеквизитов.ДокументПринятВПФР
				И ЗначенияРеквизитов.Проведен Тогда
			СтатусОтправки = НСтр("ru = 'Сдано'");
		ИначеЕсли ЗначенияРеквизитов.Проведен Тогда
			СтатусОтправки = НСтр("ru = 'Подготовлено'");
		Иначе
			СтатусОтправки = НСтр("ru = 'В работе'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДатаРазделенияНДФЛ() Экспорт 

	Возврат '20230101'

КонецФункции

Функция ДатаНачалаУдержанияНДФЛПоАвансам() Экспорт
	
	Возврат УчетНДФЛ.ДатаЗакона263ФЗ();
	
КонецФункции

Функция ДатаНачалаУчетаПрогрессивногоНДФЛ() Экспорт
	
	Возврат '20210101';
	
КонецФункции

Функция ДатаИзмененияСроковУплатыНалога() Экспорт 

	Возврат '20160101'

КонецФункции

Функция ДатаПереходаНаЕдиныйТарифСтраховыхВзносов() Экспорт 

	Возврат '20230101'

КонецФункции

Функция ДатаНачалаПередачиЧастичныхУведомленийПоНДФЛ() Экспорт
	
	НачалоПередачиЧастичныхУведомлений = Неопределено;
	Если Метаданные.РегистрыСведений.ДатыВступленияВСилуНА.Ресурсы.Найти("НачалоПередачиЧастичныхУведомленийПоНДФЛ") <> Неопределено Тогда
		НачалоПередачиЧастичныхУведомлений = ЗарплатаКадрыПовтИсп.ДатаВступленияВСилуНА("НачалоПередачиЧастичныхУведомленийПоНДФЛ");
	КонецЕсли;
	Если НачалоПередачиЧастичныхУведомлений = Неопределено Тогда
		НачалоПередачиЧастичныхУведомлений = Дата('00010101');
	КонецЕсли;
	
	Возврат НачалоПередачиЧастичныхУведомлений;
	
КонецФункции

Функция ДатаВыделенияДополнительныхВзносовЛетчиковИШахтеровИзСоставаЕНП() Экспорт

	Возврат '20250101'

КонецФункции

Функция ДатаНачалаУчетаПрогрессивногоНДФЛПоМногоступенчатойШкале() Экспорт
	
	Возврат '20250101';
	
КонецФункции

Функция ПрименяетсяЕдиныйТарифСтраховыхВзносов(ДатаПроверки) Экспорт 

	Возврат ДатаПроверки >= ДатаПереходаНаЕдиныйТарифСтраховыхВзносов();

КонецФункции

Функция УдерживаетсяНДФЛПоАвансам(ДатаПроверки) Экспорт
	
	Возврат ДатаПроверки >= ДатаНачалаУдержанияНДФЛПоАвансам();
	
КонецФункции

Функция ИсчисленныеСтраховыеВзносыЗаПериод(Организация, Период, ВариантВыборкиСтраховыхВзносов = 0) Экспорт
	
	ВариантыВыборкиСтраховыхВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	
	СчетУчетаТравматизм = ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ;
	
	ТаблицаВзносов = Новый ТаблицаЗначений;
	ТаблицаВзносов.Колонки.Добавить("СчетУчета",                  Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаВзносов.Колонки.Добавить("ВидНалоговогоОбязательства", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПлатежейВГосБюджет"));
	ТаблицаВзносов.Колонки.Добавить("Сумма",                      Новый ОписаниеТипов("Число"));
	
	Если УчетнаяПолитика.ПрименяетсяАУСН(Организация, Период) Тогда
		Сумма = СтраховыеВзносы_ФСС_НСиПЗ(Организация, Период);
		Если Сумма <> 0 Тогда
			НоваяСтрока = ТаблицаВзносов.Добавить();
			НоваяСтрока.СчетУчета = СчетУчетаТравматизм;
			НоваяСтрока.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			НоваяСтрока.Сумма = Сумма;
		КонецЕсли;
	Иначе
		
		Если ВариантВыборкиСтраховыхВзносов <> ВариантыВыборкиСтраховыхВзносов.ДобровольныеВзносы Тогда
			
			СчетаУчетаНалоговВзносовСФОТ = УчетЗарплаты.СоответствиеСчетовУчетаНалоговВзносовСФОТВидамОпераций();
			ТаблицаОтраженияВУчете = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносов(Организация, Период);
			ТаблицаОтраженияВУчете.Свернуть("ВидОперации", "Сумма");
			ОперацииУчетаОсобыхДопВзносов = УчетЗарплаты.ОперацииУчетаОсобыхДопВзносов();
			
			Для Каждого СтрокаТаблицы Из ТаблицаОтраженияВУчете Цикл
				СчетУчета = СчетаУчетаНалоговВзносовСФОТ[СтрокаТаблицы.ВидОперации];
				Если ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.СтраховыеВзносы И СчетУчета = СчетУчетаТравматизм Тогда
					Продолжить;
				КонецЕсли;
				Если СчетУчета <> Неопределено Тогда
					НоваяСтрока = ТаблицаВзносов.Добавить();
					НоваяСтрока.СчетУчета = СчетУчета;
					Если ОперацииУчетаОсобыхДопВзносов.Найти(СтрокаТаблицы.ВидОперации) = Неопределено Тогда
						НоваяСтрока.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
					Иначе
						НоваяСтрока.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки;
					КонецЕсли;
					НоваяСтрока.Сумма = СтрокаТаблицы.Сумма;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.ДобровольныеВзносы Тогда
			
			СчетаУчетаНалоговВзносовСФОТ = СоответствиеСчетовУчетаДобровольныхВзносовВидамОпераций();
			ТаблицаОтраженияВУчете = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеУдержаний(Организация, Период);
			ТаблицаОтраженияВУчете.Свернуть("ВидОперации", "Сумма");
			
			Для Каждого СтрокаТаблицы Из ТаблицаОтраженияВУчете Цикл
				СчетУчета = СчетаУчетаНалоговВзносовСФОТ[СтрокаТаблицы.ВидОперации];
				Если СчетУчета <> Неопределено Тогда
					НоваяСтрока = ТаблицаВзносов.Добавить();
					НоваяСтрока.СчетУчета = СчетУчета;
					НоваяСтрока.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
					НоваяСтрока.Сумма = СтрокаТаблицы.Сумма;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаВзносов;
	
КонецФункции

Функция ИсчисленныйНДФЛЗаПериод(Организация, Период, ПлательщикЕНП, Аванс, РасчетныйДокумент = Неопределено) Экспорт
	
	Если Аванс Или ЗначениеЗаполнено(РасчетныйДокумент) Тогда
		ТаблицаОтраженияВУчете = ДанныеДляОтражениеВУчетеНДФЛ(Организация, Период, Аванс, РасчетныйДокумент);
	Иначе
		ТаблицаОтраженияВУчете = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНДФЛ(Организация, Период);
	КонецЕсли;
	ТаблицаОтраженияВУчете.Свернуть("ВидОперации, РегистрацияВНалоговомОргане", "Сумма");
	
	ТаблицаНДФЛ = Новый ТаблицаЗначений;
	ТаблицаНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	ТаблицаНДФЛ.Колонки.Добавить("Сумма",                       Новый ОписаниеТипов("Число"));
	ТаблицаНДФЛ.Колонки.Добавить("ВидНалоговогоОбязательства",  Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПлатежейВГосБюджет"));
	ТаблицаНДФЛ.Колонки.Добавить("КБК",                         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("КодНалоговогоОргана",         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("КПП",                         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("КодПоОКТМО",                  Новый ОписаниеТипов("Строка"));
	
	Налог_ДоходыДоПредела    = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.НДФЛ,
		, , Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛАгент13());
	Налог_ДоходыСвышеПредела = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.НДФЛ,
		, , Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛАгент15());
	КБК_ДоходыДоПредела      = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог_ДоходыДоПредела, , Период);
	КБК_ДоходыСвышеПредела   = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог_ДоходыСвышеПредела, , Период);
	
	Для Каждого СтрокаТаблицы Из ТаблицаОтраженияВУчете Цикл
		НоваяСтрока = ТаблицаНДФЛ.Добавить();
		НоваяСтрока.РегистрацияВНалоговомОргане = СтрокаТаблицы.РегистрацияВНалоговомОргане;
		НоваяСтрока.Сумма                       = СтрокаТаблицы.Сумма;
		НоваяСтрока.ВидНалоговогоОбязательства  = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		Если Не ПлательщикЕНП Тогда
			НоваяСтрока.КБК = ?(СтрокаТаблицы.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НДФЛСПревышения,
				КБК_ДоходыСвышеПредела,
				КБК_ДоходыДоПредела);
		КонецЕсли;
	КонецЦикла;

	Возврат ТаблицаНДФЛ;
	
КонецФункции

Функция УдержанныйНДФЛЗаПериод(Организация, Период, ВтораяПоловинаМесяца = Ложь, ТолькоПроверка = Ложь) Экспорт
	
	ПереноситьСрокУплаты = Период >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж();
	
	ТаблицаНДФЛ = НоваяТаблицаУдержанногоНДФЛ();
	
	ПоследнийПериод = Ложь;
	
	Если ПереноситьСрокУплаты Тогда
		
		// Федеральный закон от 31.07.2023 № 389-ФЗ:
		// 12.2. В течение 2023 года налоговые агенты могут подавать уведомления об исчисленных суммах по налогу на доходы физических лиц, 
		// подлежащему перечислению в сроки, установленные пунктом 6 статьи 226, пунктом 9 статьи 226.1, пунктом 7 статьи 226.2 Налогового кодекса Российской Федерации, 
		// до истечения 12-го числа текущего месяца, указав в таком уведомлении сумму налога, 
		// удержанную в период с 23-го числа предыдущего месяца до 9-го числа текущего месяца.
		НачалоПериодаПодачиПромежуточныхУведомлений = НачалоДня(Дата(2023, 10, 1));
		КонецПериодаПодачиПромежуточныхУведомлений = КонецДня(Дата(2023, 12, 31));
		
		Если ВтораяПоловинаМесяца Тогда // 23 число - конец месяца
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период), 23);
			ДатаОкончанияОтбора = КонецМесяца(Период);
			ПоследнийПериод = КонецМесяца(Период) = КонецГода(Период);
		ИначеЕсли НачалоМесяца(Период) = НачалоГода(Период) Тогда // Январь
			ДатаНачалаОтбора    = НачалоГода(Период);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 22);
		ИначеЕсли Период >= НачалоПериодаПодачиПромежуточныхУведомлений 
			И Период <= КонецПериодаПодачиПромежуточныхУведомлений 
			И День(Период) >= 9 И День(Период) < 23 Тогда
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период)-1, 23);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 9);
		ИначеЕсли Период < ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ() Тогда // остальные периоды 2023 г.
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период)-1, 23);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 22);
		Иначе // остальные периоды 2024 г.
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период), 1);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 22);
		КонецЕсли;
	Иначе
		ДатаНачалаОтбора    = НачалоМесяца(Период);
		ДатаОкончанияОтбора = КонецМесяца(Период)
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		
		УдержаноНалогаЗаПериод = УчетНДФЛ.УдержаноНалогаЗаПериод(Организация, ДатаНачалаОтбора, ДатаОкончанияОтбора);
		
		Если ТолькоПроверка Тогда
			// Достаточно знать, что есть удержанный НДФЛ
			Возврат УдержаноНалогаЗаПериод;
		КонецЕсли;
		
		КодТерритории = Справочники.Организации.КодТерриторииМестаРегистрации(Организация);
		ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(КодТерритории, Период);
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") =
				Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			КПП = "0";
		Иначе
			КПП = Справочники.Организации.КППНаДату(Организация, КонецМесяца(Период), Истина);
		КонецЕсли;
		
		РегистрацииВНалоговомОргане = ОбщегоНазначения.ВыгрузитьКолонку(УдержаноНалогаЗаПериод, "РегистрацияВНалоговомОргане", Истина);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(
			РегистрацииВНалоговомОргане, Неопределено);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(
			РегистрацииВНалоговомОргане, Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
		
		РеквизитыРегистраций = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(РегистрацииВНалоговомОргане, "КодПоОКТМО, Код, КПП");
		
		Если ПереноситьСрокУплаты Тогда
			ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
			ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
			ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
			ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
				ПроизводственныйКалендарь,
				ОбщегоНазначения.ВыгрузитьКолонку(УдержаноНалогаЗаПериод, "СрокПеречисленияНалога", Истина),
				ПараметрыПолучения);
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из УдержаноНалогаЗаПериод Цикл
			
			ЗначениеКодТерритории = КодТерритории;
			ЗначениеКПП           = КПП;
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.РегистрацияВНалоговомОргане) Тогда
				РеквизитыРегистрации = РеквизитыРегистраций[СтрокаТаблицы.РегистрацияВНалоговомОргане];
				ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(РеквизитыРегистрации.КодПоОКТМО, Период);
				Если Не ПустаяСтрока(РеквизитыРегистрации.КодПоОКТМО) Тогда
					ЗначениеКодТерритории = РеквизитыРегистрации.КодПоОКТМО;
				КонецЕсли;
				Если Не ПустаяСтрока(РеквизитыРегистрации.КПП) Тогда
					ЗначениеКПП = РеквизитыРегистрации.КПП;
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаТаблицы.Сумма <> 0 Тогда
				НоваяСтрока = ТаблицаНДФЛ.Добавить();
				НоваяСтрока.Организация                 = Организация;
				НоваяСтрока.РегистрацияВНалоговомОргане = СтрокаТаблицы.РегистрацияВНалоговомОргане;
				НоваяСтрока.КПП                         = ЗначениеКПП;
				НоваяСтрока.ОКАТО                       = ЗначениеКодТерритории;
				НоваяСтрока.Сумма                       = СтрокаТаблицы.Сумма;
				НоваяСтрока.СрокУплаты                  = ?(ПереноситьСрокУплаты, ГрафикПереноса[СтрокаТаблицы.СрокПеречисленияНалога], СтрокаТаблицы.СрокПеречисленияНалога);
				НоваяСтрока.КБК                         = СтрокаТаблицы.КБК;
				НоваяСтрока.Налог                       = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(СтрокаТаблицы.КБК);
				НоваяСтрока.ИсходныйСрокУплаты          = ?(ПоследнийПериод, НачалоДня(КонецГода(Период)), СтрокаТаблицы.СрокПеречисленияНалога);
			КонецЕсли;
			
		КонецЦикла;

	Иначе
		
		Если Период >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж() Тогда
			
			Если ВтораяПоловинаМесяца И Месяц(Период) = 12 Тогда // 23-31 декабря
				НачалоПериодаУплаты    = Дата(Год(Период), Месяц(Период), 23);
				ОкончаниеПериодаУплаты = КонецГода(Период);
				ПоследнийПериод = КонецМесяца(Период) = КонецГода(Период);
			Иначе
				НачалоПериодаУплаты    = НачалоМесяца(Период);
				ОкончаниеПериодаУплаты = КонецМесяца(Период);
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Организация",            Организация);
			Запрос.УстановитьПараметр("НачалоПериода",          НачалоМесяца(ДатаНачалаОтбора));
			Запрос.УстановитьПараметр("КонецПериода",           КонецМесяца(ДатаОкончанияОтбора));
			Запрос.УстановитьПараметр("НачалоПериодаУплаты",    НачалоПериодаУплаты);
			Запрос.УстановитьПараметр("ОкончаниеПериодаУплаты", ОкончаниеПериодаУплаты);
			Запрос.УстановитьПараметр("ПостояннаяПередачаПромежуточныхУведомлений",
				Период >= ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ());
			
			Запрос.УстановитьПараметр("НачалоПериодаПодачиПромежуточныхУведомлений", НачалоПериодаПодачиПромежуточныхУведомлений);
			Запрос.УстановитьПараметр("КонецПериодаПодачиПромежуточныхУведомлений",  КонецПериодаПодачиПромежуточныхУведомлений);
			Запрос.УстановитьПараметр("ДатаНачалаОтбора",                            ДатаНачалаОтбора);
			Запрос.УстановитьПараметр("ДатаОкончанияОтбора",                         ДатаОкончанияОтбора);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК КАК КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана КАК КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП КАК КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО КАК КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты КАК СрокУплаты,
			|	СУММА(СведенияОбУдержанномНДФЛНДФЛ.Сумма) КАК Сумма
			|ИЗ
			|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
			|		ПО СведенияОбУдержанномНДФЛ.Ссылка = СведенияОбУдержанномНДФЛНДФЛ.Ссылка
			|ГДЕ
			|	СведенияОбУдержанномНДФЛ.Организация = &Организация
			|	И СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериода И &КонецПериода
			|	И СведенияОбУдержанномНДФЛ.Проведен
			|	И ВЫБОР
			|			КОГДА НЕ &ПостояннаяПередачаПромежуточныхУведомлений
			|				ТОГДА СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты МЕЖДУ &НачалоПериодаУплаты И &ОкончаниеПериодаУплаты
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ
			|	И ВЫБОР
			|			КОГДА СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериодаПодачиПромежуточныхУведомлений И &КонецПериодаПодачиПромежуточныхУведомлений
			|					ИЛИ &ПостояннаяПередачаПромежуточныхУведомлений
			|				ТОГДА СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания МЕЖДУ &ДатаНачалаОтбора И &ДатаОкончанияОтбора
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты";
		Иначе
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Организация",   Организация);
			Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаОтбора);
			Запрос.УстановитьПараметр("КонецПериода",  ДатаОкончанияОтбора);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК КАК КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана КАК КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП КАК КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО КАК КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты КАК СрокУплаты,
			|	СУММА(СведенияОбУдержанномНДФЛНДФЛ.Сумма) КАК Сумма
			|ИЗ
			|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
			|		ПО СведенияОбУдержанномНДФЛ.Ссылка = СведенияОбУдержанномНДФЛНДФЛ.Ссылка
			|ГДЕ
			|	СведенияОбУдержанномНДФЛ.Организация = &Организация
			|	И СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериода И &КонецПериода
			|	И СведенияОбУдержанномНДФЛ.Проведен
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты";
			
		КонецЕсли;
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		Если ТолькоПроверка Тогда
			// Достаточно знать, что есть удержанный НДФЛ
			Возврат Результат;
		КонецЕсли;
		
		Если ПереноситьСрокУплаты Тогда
			ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
			ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
			ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
			ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
				ПроизводственныйКалендарь,
				ОбщегоНазначения.ВыгрузитьКолонку(Результат, "СрокУплаты", Истина),
				ПараметрыПолучения);
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из Результат Цикл
			
			Если СтрокаТаблицы.Сумма <> 0 Тогда
				НоваяСтрока = ТаблицаНДФЛ.Добавить();
				НоваяСтрока.Организация = Организация;
				НоваяСтрока.Налог                       = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(СтрокаТаблицы.КодБК);
				НоваяСтрока.ВидНалога                   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Налог, "ВидНалога");
				НоваяСтрока.КБК                         = СтрокаТаблицы.КодБК;
				НоваяСтрока.КодНалоговогоОргана         = СтрокаТаблицы.КодНалоговогоОргана;
				НоваяСтрока.КПП                         = СтрокаТаблицы.КПП;
				НоваяСтрока.ОКАТО                       = СтрокаТаблицы.КодПоОКТМО;
				НоваяСтрока.Сумма                       = СтрокаТаблицы.Сумма;
				НоваяСтрока.СрокУплаты                  = ?(ПереноситьСрокУплаты, ГрафикПереноса[СтрокаТаблицы.СрокУплаты], СтрокаТаблицы.СрокУплаты);
				НоваяСтрока.ИсходныйСрокУплаты          = ?(ПоследнийПериод, НачалоДня(КонецГода(Период)), СтрокаТаблицы.СрокУплаты);
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, ТаблицаНДФЛ);
		
	КонецЕсли;
	
	КолонкиСвертки = Новый Массив;
	Для Каждого КолонкаНДФЛ Из ТаблицаНДФЛ.Колонки Цикл
		Если КолонкаНДФЛ.Имя <> "Сумма" Тогда
			КолонкиСвертки.Добавить(КолонкаНДФЛ.Имя);
		КонецЕсли;
	КонецЦикла;
	ТаблицаНДФЛ.Свернуть(СтрСоединить(КолонкиСвертки, ","), "Сумма");
	
	Возврат ТаблицаНДФЛ;
	
КонецФункции

Функция УдержанныйНДФЛПоДокументу(Организация, ДокументСсылка) Экспорт
	
	ПереноситьСрокУплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Дата") >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж();
	
	ТаблицаНДФЛ = НоваяТаблицаУдержанногоНДФЛ();
	
	ПоследнийПериод = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СведенияОбУдержанномНДФЛНДФЛ.КодБК КАК КодБК,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	СведенияОбУдержанномНДФЛНДФЛ.КПП КАК КПП,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО КАК КодПоОКТМО,
	|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты КАК СрокУплаты,
	|	СУММА(СведенияОбУдержанномНДФЛНДФЛ.Сумма) КАК Сумма,
	|	СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания КАК ДатаУдержания
	|ИЗ
	|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
	|		ПО СведенияОбУдержанномНДФЛ.Ссылка = СведенияОбУдержанномНДФЛНДФЛ.Ссылка
	|ГДЕ
	|	СведенияОбУдержанномНДФЛ.Организация = &Организация
	|	И СведенияОбУдержанномНДФЛ.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОбУдержанномНДФЛНДФЛ.КодБК,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана,
	|	СведенияОбУдержанномНДФЛНДФЛ.КПП,
	|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты,
	|	СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания";

	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ПереноситьСрокУплаты Тогда
		ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
		ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
		ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
		ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
			ПроизводственныйКалендарь,
			ОбщегоНазначения.ВыгрузитьКолонку(Результат, "СрокУплаты", Истина),
			ПараметрыПолучения);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Результат Цикл
		
		Если СтрокаТаблицы.Сумма <> 0 Тогда
			НоваяСтрока = ТаблицаНДФЛ.Добавить();
			НоваяСтрока.Организация         = Организация;
			НоваяСтрока.Налог               = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(СтрокаТаблицы.КодБК);
			НоваяСтрока.ВидНалога           = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Налог, "ВидНалога");
			НоваяСтрока.КБК                 = СтрокаТаблицы.КодБК;
			НоваяСтрока.КодНалоговогоОргана = СтрокаТаблицы.КодНалоговогоОргана;
			НоваяСтрока.КПП                 = СтрокаТаблицы.КПП;
			НоваяСтрока.ОКАТО               = СтрокаТаблицы.КодПоОКТМО;
			НоваяСтрока.Сумма               = СтрокаТаблицы.Сумма;
			НоваяСтрока.СрокУплаты          = ?(ПереноситьСрокУплаты, ГрафикПереноса[СтрокаТаблицы.СрокУплаты], СтрокаТаблицы.СрокУплаты);
			
			ПоследнийПериод = КонецГода(СтрокаТаблицы.ДатаУдержания) = КонецМесяца(СтрокаТаблицы.ДатаУдержания)
				И День(СтрокаТаблицы.ДатаУдержания) > 22;
			НоваяСтрока.ИсходныйСрокУплаты          = ?(ПоследнийПериод, НачалоДня(КонецГода(СтрокаТаблицы.ДатаУдержания)), СтрокаТаблицы.СрокУплаты);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, ТаблицаНДФЛ);
	
	КолонкиСвертки = Новый Массив;
	Для Каждого КолонкаНДФЛ Из ТаблицаНДФЛ.Колонки Цикл
		Если КолонкаНДФЛ.Имя <> "Сумма" Тогда
			КолонкиСвертки.Добавить(КолонкаНДФЛ.Имя);
		КонецЕсли;
	КонецЦикла;
	ТаблицаНДФЛ.Свернуть(СтрСоединить(КолонкиСвертки, ","), "Сумма");
	
Возврат ТаблицаНДФЛ;
	
КонецФункции

Функция НоваяТаблицаУдержанногоНДФЛ()
	
	ТаблицаНДФЛ = Новый ТаблицаЗначений;
	ТаблицаНДФЛ.Колонки.Добавить("Организация",                 Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	ТаблицаНДФЛ.Колонки.Добавить("КодНалоговогоОргана",         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("Налог",                       Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	ТаблицаНДФЛ.Колонки.Добавить("ВидНалога",                   Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНалогов"));
	ТаблицаНДФЛ.Колонки.Добавить("КБК",                         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("КПП",                         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("ОКАТО",                       Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("Сумма",                       Новый ОписаниеТипов("Число"));
	ТаблицаНДФЛ.Колонки.Добавить("СрокУплаты",                  Новый ОписаниеТипов("Дата"));
	ТаблицаНДФЛ.Колонки.Добавить("ИсходныйСрокУплаты",          Новый ОписаниеТипов("Дата"));
	
	Возврат ТаблицаНДФЛ;
	
КонецФункции

Процедура ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, КоллекцияНачисленныйНДФЛ) Экспорт
	
	СведенияОбОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ГоловнаяОрганизация, ЮридическоеФизическоеЛицо");
	ОрганизацияЮрлицо     = СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СведенияОбОрганизации.ГоловнаяОрганизация);
	
	Если ТипЗнч(КоллекцияНачисленныйНДФЛ) = Тип("ДанныеФормыКоллекция") Тогда
		ТаблицаНДФЛ = КоллекцияНачисленныйНДФЛ.Выгрузить();
	Иначе
		// передана таблица значений
		
		ТаблицаНДФЛ = КоллекцияНачисленныйНДФЛ.Скопировать();
		
		Если КоллекцияНачисленныйНДФЛ.Колонки.Найти("РегистрацияВНалоговомОргане") = Неопределено Тогда
			КоллекцияНачисленныйНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
		КонецЕсли;
		
	КонецЕсли;
	
	ИмяКолонкиОКТМО = "КодПоОКТМО";
	Если ТаблицаНДФЛ.Колонки.Найти("КодПоОКТМО") = Неопределено Тогда
		ТаблицаНДФЛ.Колонки.ОКАТО.Имя = "КодПоОКТМО";
		ИмяКолонкиОКТМО = "ОКАТО";
	КонецЕсли;
	
	ПоляСвертки = "КодНалоговогоОргана";
	Если ОрганизацияЮрлицо Тогда
		ПоляСвертки = ПоляСвертки + ",КПП";
	КонецЕсли;
	ПоляДополнительногоОтбора = ПоляСвертки;
	ПоляСвертки = ПоляСвертки + ",КодПоОКТМО";
	
	ТаблицаНДФЛ.Свернуть(ПоляСвертки);
	ТаблицаНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацииВНалоговомОргане.КПП КАК КПП,
	|	РегистрацииВНалоговомОргане.Код КАК КодНалоговогоОргана
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &Организация";
	
	ДействующиеРегистрации = Запрос.Выполнить().Выгрузить();
	
	Отбор = Новый Структура(ПоляСвертки);
	ОтборДополнительный = Новый Структура(ПоляДополнительногоОтбора);
	
	Для каждого СтрокаТЗ Из ТаблицаНДФЛ Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		Регистрации = ДействующиеРегистрации.НайтиСтроки(Отбор);
		Если Регистрации.Количество() > 0 Тогда
			СтрокаТЗ.РегистрацияВНалоговомОргане = Регистрации[0].РегистрацияВНалоговомОргане;
		Иначе
			ЗаполнитьЗначенияСвойств(ОтборДополнительный, СтрокаТЗ);
			Регистрации = ДействующиеРегистрации.НайтиСтроки(ОтборДополнительный);
			Если Регистрации.Количество() > 0 Тогда
				СтрокаТЗ.РегистрацияВНалоговомОргане = Регистрации[0].РегистрацияВНалоговомОргане;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаКоллекции Из КоллекцияНачисленныйНДФЛ Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаКоллекции);
		Отбор.КодПоОКТМО = СтрокаКоллекции[ИмяКолонкиОКТМО];
		Регистрации = ТаблицаНДФЛ.НайтиСтроки(Отбор);
		Если Регистрации.Количество() > 0 Тогда
			СтрокаКоллекции.РегистрацияВНалоговомОргане = Регистрации[0].РегистрацияВНалоговомОргане;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсправитьСостояниеНастроекПодсистемыЗарплатаИКадры() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВестиУчетЗарплатыИКадровВоВнешнейПрограмме = Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить();
	Если ВестиУчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		
		МенеджерЗначения = Константы.ИспользоватьНачислениеЗарплаты.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Ложь;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		МенеджерЗначения = Константы.ИспользоватьКадровыйУчет.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Ложь;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		МенеджерЗначения = Константы.НеИспользоватьКадровыйУчет.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		МенеджерЗначения = Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Ложь;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		МенеджерЗначения = Константы.ИспользоватьНачисленияПоДоговорам.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Ложь;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		МенеджерЗначения = Константы.ИспользоватьРаботуНаНеполнуюСтавку.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Ложь;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		МенеджерЗначения = Константы.РасчетЗарплатыДляНебольшихОрганизаций.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Ложь;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		МенеджерЗначения = Константы.ПоддержкаСовместительства.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Ложь;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		
		НаборЗаписей = РегистрыСведений.НастройкиКадровогоУчета.СоздатьНаборЗаписей();
		НаборЗаписей.Очистить();
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
		НаборЗаписей = РегистрыСведений.НастройкиРасчетаЗарплаты.СоздатьНаборЗаписей();
		НаборЗаписей.Очистить();
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	Иначе
		
		НаборЗаписей = РегистрыСведений.НастройкиКадровогоУчета.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() <> 0 Тогда
			НаборЗаписей[0].ИспользуютсяДанныеСотрудниковВнешнихИсточников = Ложь;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоНерезидент(ФизЛицо) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних.Статус
	|ИЗ
	|	РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ.СрезПоследних(, ФизическоеЛицо = &ФизическоеЛицо) КАК
	|		СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних";
	
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизЛицо);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Статус = Справочники.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент;
	КонецЕсли;
	
	
КонецФункции

Функция ИспользуетсяТрудНаемныхРаботников(Организация) Экспорт
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Возврат Истина;
	Иначе
		Возврат ИПИспользуетТрудНаемныхРаботников(Организация);
	КонецЕсли;
	
КонецФункции

Функция ЕстьДоговорыПодряда(Организация, Период, Периодичность) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам") Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Документ.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ДоговорРаботыУслуги КАК Документ
		|ГДЕ
		|	Документ.Проведен
		|	И Документ.Организация = &Организация
		|	И Документ.ДатаНачала МЕЖДУ &НачалоПериода И &КонецПериода");
		
		Запрос.УстановитьПараметр("Организация", Организация);
		
		Если Периодичность = Перечисления.Периодичность.Год Тогда
			Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Период));
			Запрос.УстановитьПараметр("КонецПериода", КонецГода(Период));
		Иначе
			Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
			Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
		КонецЕсли;
		
		Возврат Не Запрос.Выполнить().Пустой();
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ЕстьДокументыОтпускаБезСодержания(Организация, Период, Периодичность) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Документ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтпускБезСохраненияОплаты КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Организация = &Организация
	|	И Документ.ДатаНачала МЕЖДУ &НачалоПериода И &КонецПериода");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если Периодичность = Перечисления.Периодичность.Год Тогда
		Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецГода(Период));
	Иначе
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
	КонецЕсли;
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ЕстьСотрудники_ЧАЭС(Организация, Период) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Справочник.Ссылка
	|Из
	|	Справочник.ФизическиеЛица КАК Справочник
	|Где
	|	НЕ Справочник.ПометкаУдаления
	|	И Справочник.ЛьготаПриНачисленииПособий = &Льгота");
	
	Запрос.УстановитьПараметр("Льгота", Перечисления.ОснованияФинансированияПособияФедеральнымБюджетом.Чернобыль);
	
	РезультатЗапроса =  Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Организация = Организация;
	ПараметрыПолученияСотрудниковОрганизаций.КадровыеДанные = "ТабельныйНомер,Должность,ДатаПриема";
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода = НачалоГода(Период);
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода = КонецДня(Период);
	ПараметрыПолученияСотрудниковОрганизаций.СписокФизическихЛиц = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ = Ложь;
	
	КадровыеДанныеФизЛиц = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	Возврат КадровыеДанныеФизЛиц.Количество() > 0;
	
КонецФункции

Функция ОрганизацияРаботаетВСельскойМестности(Организация, Период) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Истина
	|Из
	|	РегистрСведений.ТерриториальныеУсловияПФР.СрезПоследних(&Период, СтруктурнаяЕдиница = &Организация) КАК Регистр
	|где
	|	Регистр.ТерриториальныеУсловияПФР = &Территория");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Территория", Справочники.ТерриториальныеУсловияПФР.СЕЛО);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#Область СЭДО_ФСС

Процедура ПроверитьНеобходимостьЗапускаРегламентногоЗадания(Организации, ТребуетсяЗапуск) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты") Тогда
		ТребуетсяЗапуск = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекущемуПользователюСЭДОФССДоступен() Экспорт
	
	СЭДОФССДоступен = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты")
		И ПравоДоступа("Изменение", Метаданные.Документы.ИзвещениеФСС)
		И ДокументооборотСКО.ПолучитьОбработкуЭДО() <> Неопределено
		И ЭлектронныйДокументооборотСФСС.ОрганизацииИспользующиеОбменФСС().Количество() > 0;
	
	Возврат СЭДОФССДоступен;
	
КонецФункции

Функция ТекущемуПользователюДоступныТекущиеДелаПоСЭДО() Экспорт
	
	ДоступныТекущиеДелаПоСЭДО = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")
		И ТекущемуПользователюСЭДОФССДоступен();
	
	Возврат ДоступныТекущиеДелаПоСЭДО;
	
КонецФункции

Процедура ПроверитьВозможностьЗаписиДокумента(Источник, ТекстИсключения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеИсточника = Источник.Метаданные();
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", МетаданныеИсточника) Тогда
		РезультатПроверки = РезультатПроверкиУсловийТарификацииПоКоличествуСотрудников(
			Источник.Организация);
		ПроверкаУсловийТарификацииВыполнена = РезультатПроверки.УсловияВыполнены;
	Иначе
		ПроверкаУсловийТарификацииВыполнена = Истина;
	КонецЕсли;
	
	Если Не ПроверкаУсловийТарификацииВыполнена Тогда
		
		ДопустимоеКоличествоСотрудников = РезультатПроверки.КоличествоСотрудниковПоТарифу;
		Отказ = Истина;
		
	ИначеЕсли Не РасчетЗарплатыДляНебольшихОрганизацийВозможен() Тогда
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			ДопустимоеКоличествоСотрудников = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПорогЗапрета();
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				РасчетЗарплатыДляНебольшихОрганизацийПереопределяемый.ТекстСообщенияОНевозможностиСоздаватьДокументыПриПревышенииМаксимальноДопустимогоКоличестваРаботающихСотрудников(),
				ДопустимоеКоличествоСотрудников,
				Источник.Метаданные().Синоним);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстПредупрежденияОбОграниченияхРежимаРасчетаЗарплатыДляНебольшихОрганизаций(Организация, ПредыдущееКоличествоСотрудников) Экспорт

	ТекстПредупреждения = "";
	РезультатПроверки = РезультатПроверкиУсловийТарификацииПоКоличествуСотрудников(Организация);
	КоличествоСотрудников = РезультатПроверки.ТекущееКоличествоСотрудников;
	
	Если ПредыдущееКоличествоСотрудников >= КоличествоСотрудников Тогда
		// Количество сорудников не увеличилось, нет необходимости выводить предупреждение
		Возврат ТекстПредупреждения;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		ПорогЗапрета = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПорогЗапрета();
		
		// Максимально возможное значение разницы между максимально возможным количеством
		// сотрудников и фактическим количеством сотрудников, при достижении которого выдаются предупреждения
		// об ограничениях применения режима расчета зарплаты для небольших организаций:
		ПорогПредупреждения = 5;
		
		Если Не РезультатПроверки.УсловияВыполнены Тогда
			Если КоличествоСотрудников = РезультатПроверки.КоличествоСотрудниковПоТарифу + 1 Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					РасчетЗарплатыДляНебольшихОрганизацийПереопределяемый.ТекстСообщенияОПревышенииМаксимальноДопустимогоКоличестваРаботающихСотрудников(),
					РезультатПроверки.КоличествоСотрудниковПоТарифу);
			КонецЕсли;
		ИначеЕсли КоличествоСотрудников > ПорогЗапрета - ПорогПредупреждения И КоличествоСотрудников <= ПорогЗапрета Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				РасчетЗарплатыДляНебольшихОрганизацийПереопределяемый.ТекстСообщенияОПриближенииКМаксимальноДопустимомуКоличествуРаботающихСотрудников(),
				ПорогЗапрета, Организация, КоличествоСотрудников);
		ИначеЕсли КоличествоСотрудников = ПорогЗапрета + 1 Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				РасчетЗарплатыДляНебольшихОрганизацийПереопределяемый.ТекстСообщенияОПревышенииМаксимальноДопустимогоКоличестваРаботающихСотрудников(),
				ПорогЗапрета);
		КонецЕсли;
	КонецЕсли;

	Возврат ТекстПредупреждения;

КонецФункции

// Возвращает результат проверки выполнения условия тарифного ограничения на число сотрудников
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация, для которой рассчитывается количество сотрудников
//
// Возвращаемое значение:
//   Структура - см. ТарификацияБП.РезультатПроверкиУсловийТарификацииПоСотрудникам
//
Функция РезультатПроверкиУсловийТарификацииПоКоличествуСотрудников(Организация) Экспорт
	
	КоличествоСотрудников = 0;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		КоличествоСотрудников = МодульРасчетЗарплатыДляНебольшихОрганизаций.КоличествоСотрудниковОрганизации(Организация);
	КонецЕсли;
	
	Возврат ТарификацияБП.РезультатПроверкиУсловийТарификацииПоСотрудникам(КоличествоСотрудников);
	
КонецФункции

Функция РасчетЗарплатыДляНебольшихОрганизацийВозможен() Экспорт
	
	РасчетЗарплатыДляНебольшихОрганизацийВозможен = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		РасчетЗарплатыДляНебольшихОрганизацийВозможен = МодульРасчетЗарплатыДляНебольшихОрганизаций.РасчетЗарплатыДляНебольшихОрганизацийВозможен();
	КонецЕсли;
	
	Возврат РасчетЗарплатыДляНебольшихОрганизацийВозможен;
	
КонецФункции

Процедура НастроитьСписокНачисленийПоСистемеНалогообложения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПрименяетсяАУСН = ПолучитьФункциональнуюОпцию("ИспользуетсяАУСН");
	Если Не ПрименяетсяАУСН Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВидТарифа", Справочники.ВидыТарифовСтраховыхВзносов.АУСН);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа КАК ВидТарифа
		|ИЗ
		|	РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|ГДЕ
		|	ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа = &ВидТарифа";
		
		ПрименяетсяАУСН = Не Запрос.Выполнить().Пустой();
	КонецЕсли;
	
	Если ПрименяетсяАУСН Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			МодульРасчетЗарплатыДляНебольшихОрганизаций.ПодготовитьНачисленияАУСН();
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

Функция СписокСчетовАгентскогоНДФЛ() Экспорт
	
	СписокСчетовАгентскогоНДФЛ = Новый СписокЗначений;
	СписокСчетовАгентскогоНДФЛ.Добавить(ПланыСчетов.Хозрасчетный.НДФЛ);
	
	Возврат СписокСчетовАгентскогоНДФЛ;
	
КонецФункции

// Возвращает признак отнесения взносов за работников к деятельности на УСН и ПСН
//
// Параметры:
//   Организация   - СправочникСсылка.Организации - плательщик взносов.
//   НачалоПериода - Дата - начало периода уплаты взносов.
//   КонецПериода  - Дата - конец периода уплаты взносов.
//
// Возвращаемое значение:
//   Структура
//      ЕстьРаботникиУСН - Истина, если были рассчитаны взносы уменьшающие налог по деятельности на УСН
//      ЕстьРаботникиПСН - Истина, если были рассчитаны взносы уменьшающие налог по деятельности на ПСН
//
Функция ЕстьВзносыЗаРаботников(Организация, НачалоПериода, КонецПериода) Экспорт
	
	ВзносыЗаРаботников = Новый Структура("ЕстьРаботникиУСН, ЕстьРаботникиПСН", Истина, Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетаВзносовСФОТ", УчетУСН.СчетаУчетаВзносовСФОТ());
	Запрос.УстановитьПараметр("ВидыПлатежейНалоговые", Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	
	Запрос.УстановитьПараметр("ВидСубконтоВидыПлатежей",
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет);
	Запрос.УстановитьПараметр("ВидСубконтоСтатьиЗатрат",
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидСубконтоПрочиеДоходыРасходы",
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.НачисленоВсего), 0) - ЕСТЬNULL(СУММА(ВложенныйЗапрос.НачисленоСпецРежим), 0) > 0 КАК ЕстьРаботникиУСН,
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.НачисленоСпецРежим), 0) > 0 КАК ЕстьРаботникиПСН
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.СуммаОборот КАК НачисленоВсего,
	|		0 КАК НачисленоСпецРежим
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				,
	|				,
	|				&ВидСубконтоСтатьиЗатрат,
	|				СчетКт В (&СчетаВзносовСФОТ),
	|				&ВидСубконтоВидыПлатежей,
	|				Организация = &Организация
	|					И СубконтоКт1 В (&ВидыПлатежейНалоговые)) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		0
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				,
	|				,
	|				&ВидСубконтоПрочиеДоходыРасходы,
	|				СчетКт В (&СчетаВзносовСФОТ),
	|				&ВидСубконтоВидыПлатежей,
	|				Организация = &Организация
	|					И СубконтоКт1 В (&ВидыПлатежейНалоговые)) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				,
	|				,
	|				&ВидСубконтоСтатьиЗатрат,
	|				СчетКт В (&СчетаВзносовСФОТ),
	|				&ВидСубконтоВидыПлатежей,
	|				Организация = &Организация
	|					И СубконтоКт1 В (&ВидыПлатежейНалоговые)
	|					И СубконтоДт1 ССЫЛКА Справочник.СтатьиЗатрат
	|					И ВЫРАЗИТЬ(СубконтоДт1 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				,
	|				,
	|				&ВидСубконтоПрочиеДоходыРасходы,
	|				СчетКт В (&СчетаВзносовСФОТ),
	|				&ВидСубконтоВидыПлатежей,
	|				Организация = &Организация
	|					И СубконтоКт1 В (&ВидыПлатежейНалоговые)
	|					И СубконтоДт1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|					И ВЫРАЗИТЬ(СубконтоДт1 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)) КАК ХозрасчетныйОборотыДтКт) КАК ВложенныйЗапрос
	|
	|ИМЕЮЩИЕ
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.НачисленоВсего), 0) > 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ВзносыЗаРаботников, Выборка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВзносыЗаРаботников;
	
КонецФункции

Функция ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ() Экспорт 
	
	Возврат '20240101'
	
КонецФункции

// Определяет дату ближайшего рабочего дня для переданной в параметрах даты.
// Ближайший рабочий день вычисляется по следующим принципам:
// - определяется производственный календарь, указанный для графика работы организации;
// - при выпадении на выходной день - переносится на рабочий день предшествующий выходному.
//
// Параметры:
//  Организация - Тип СправочникСсылка.Организации
//  ПланируемаяДатыВыплаты - Тип Дата, дата для которой требуется определить рабочий день
//
// Возвращаемое значение:
//		Дата - вычисленная рабочая дата.
//
Функция БлижайшийРабочийДеньДляВыплаты(Организация, ПланируемаяДатыВыплаты) Экспорт
	
	ПроизводственныйКалендарь = Неопределено;
	ГрафикРаботы = ГрафикРаботыОрганизации(Организация);
	Если ЗначениеЗаполнено(ГрафикРаботы) Тогда
		ПроизводственныйКалендарь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГрафикРаботы, "ПроизводственныйКалендарь");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПроизводственныйКалендарь) Тогда
		ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	КонецЕсли;
	Если ЗначениеЗаполнено(ПроизводственныйКалендарь) Тогда
		ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
		ПараметрыПолучения.ПолучатьПредшествующие              = Истина;
		ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
		ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
			ПроизводственныйКалендарь,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланируемаяДатыВыплаты),
			ПараметрыПолучения);
		ПланируемаяДатыВыплаты = ГрафикПереноса[ПланируемаяДатыВыплаты];
	КонецЕсли;
	
	Возврат ПланируемаяДатыВыплаты;
	
КонецФункции

Функция ДатаПереходаНаЕдинуюДатуВыплатыДохода() Экспорт
	
	// Условное ограничение - начиная с 2024 г.
	
	Возврат '20240101';
	
КонецФункции

Функция ДатаПереходаНаЕФС1()
	
	Возврат ПерсонифицированныйУчетКлиентСервер.ДатаПостановленияЕФС1_2023();
	
КонецФункции

Функция ИспользуетсяЕдинаяДатаВыплатыДохода(Организация, Период) Экспорт
	
	Возврат Период >= ДатаПереходаНаЕдинуюДатуВыплатыДохода()
		И ИспользоватьРасчетПервойПоловиныМесяца(Организация, Период);
	
КонецФункции

Процедура ОбновитьОсновнойВариантПеречисленияСотруднику(ВладелецСчета, БанковскийСчет) Экспорт
	
	Если Не ЗначениеЗаполнено(ВладелецСчета)
		Или Не ЗначениеЗаполнено(БанковскийСчет)
		Или ТипЗнч(ВладелецСчета) <> Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВидыОпераций = Новый Массив;
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДепонентов);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыдачаЗаймаРаботнику);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ВладелецСчета);
	Запрос.УстановитьПараметр("ВидыОпераций",   ВидыОпераций);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеВариантыПеречисленияСотруднику.Организация КАК Организация,
	|	ОсновныеВариантыПеречисленияСотруднику.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОсновныеВариантыПеречисленияСотруднику.ВидОперации КАК ВидОперации,
	|	ОсновныеВариантыПеречисленияСотруднику.БанковскийСчет КАК БанковскийСчет,
	|	ОсновныеВариантыПеречисленияСотруднику.НазначениеПлатежа КАК НазначениеПлатежа
	|ИЗ
	|	РегистрСведений.ОсновныеВариантыПеречисленияСотруднику КАК ОсновныеВариантыПеречисленияСотруднику
	|ГДЕ
	|	ОсновныеВариантыПеречисленияСотруднику.ФизическоеЛицо = &ФизическоеЛицо
	|	И ОсновныеВариантыПеречисленияСотруднику.ВидОперации В(&ВидыОпераций)
	|	И ОсновныеВариантыПеречисленияСотруднику.БанковскийСчет ССЫЛКА Справочник.БанковскиеСчета";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.ОсновныеВариантыПеречисленияСотруднику.УстановитьОсновнойВариантПеречисления(Выборка.Организация,
			Выборка.ФизическоеЛицо, Выборка.ВидОперации, БанковскийСчет, Выборка.НазначениеПлатежа);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ИспользуетсяОбменЗУПБП(Организация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВариантНастройки", "ОбменЗУПБП");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.Ссылка КАК Ссылка,
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.ИспользоватьОтборПоОрганизациям КАК ИспользоватьОтборПоОрганизациям
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК СинхронизацияДанныхЧерезУниверсальныйФормат
	|ГДЕ
	|	НЕ СинхронизацияДанныхЧерезУниверсальныйФормат.ПометкаУдаления
	|	И НЕ СинхронизацияДанныхЧерезУниверсальныйФормат.ЭтотУзел
	|	И СинхронизацияДанныхЧерезУниверсальныйФормат.ВариантНастройки = &ВариантНастройки";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатаЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Если РезультатаЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Организация = Неопределено Тогда
		Возврат Истина;
	Иначе
		Выборка = РезультатаЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не Выборка.ИспользоватьОтборПоОрганизациям Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Узлы", РезультатаЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СинхронизацияДанныхЧерезУниверсальныйФорматОрганизации.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.Организации КАК СинхронизацияДанныхЧерезУниверсальныйФорматОрганизации
	|ГДЕ
	|	СинхронизацияДанныхЧерезУниверсальныйФорматОрганизации.Ссылка В(&Узлы)
	|	И СинхронизацияДанныхЧерезУниверсальныйФорматОрганизации.Организация = &Организация";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатаЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Не РезультатаЗапроса.Пустой();
	
КонецФункции

Функция ОписаниеСостоянияОплатыВедомости(ИтогКВыплате, ИтогВыплачено) Экспорт
	
	ВедомостьОплаченаПолностью = ИтогКВыплате = ИтогВыплачено И ИтогКВыплате > 0;
	
	Если ВедомостьОплаченаПолностью Тогда
		ТекстНадписи = НСтр("ru = 'Ведомость полностью оплачена.'");
	Иначе
		Если ИтогВыплачено = 0 Тогда
			ТекстНадписи = НСтр("ru = 'Ведомость не оплачена.'");
		ИначеЕсли ИтогВыплачено < ИтогКВыплате Тогда
			ТекстНадписи = СтрШаблон(НСтр("ru = 'Ведомость оплачена не полностью. Выплачено %1 из %2'"),
				Формат(ИтогВыплачено, "ЧДЦ=2; ЧН=0,00"),
				Формат(ИтогКВыплате, "ЧДЦ=2; ЧН=0,00"));
		Иначе
			ТекстНадписи = СтрШаблон(НСтр("ru = 'Суммы платежных документов превышают сумму ведомости. Выплачено %1'"),
				Формат(ИтогВыплачено, "ЧДЦ=2"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстНадписи;
	
КонецФункции

Функция ИспользованиеРегистрационногоНомераСФР() Экспорт
	
	Возврат ЗарплатаКадры.ВидимостьРегистрационногоНомераСФР();
	
КонецФункции

Функция ДанныеДляОтражениеВУчетеНДФЛ(Организация, Период, Аванс, РасчетныйДокумент = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	// Получим массив видов особых начислений удержаний, относящихся к НДФЛ.
	ВидыОсобыхНачисленийИУдержанийНДФЛ = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ(Истина);
	
	Запрос.УстановитьПараметр("НачалоПериода",
		НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",
		КонецМесяца(Период));
	Запрос.УстановитьПараметр("Организация",
		Организация);
	Запрос.УстановитьПараметр("НачислениеУдержаниеВидОперации",
		ОтражениеЗарплатыВУчетеПовтИсп.ТаблицаНачислениеУдержаниеВидОперации());
	Запрос.УстановитьПараметр("ВидыОсобыхНачисленийИУдержанийНДФЛ",
		ВидыОсобыхНачисленийИУдержанийНДФЛ);
	Запрос.УстановитьПараметр("РасчетныйДокумент",
		РасчетныйДокумент);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачисленияУдержания.Организация КАК Организация,
	|	НачисленияУдержания.НачислениеУдержание КАК НачислениеУдержание,
	|	СУММА(НачисленияУдержания.Сумма) КАК Сумма,
	|	НачисленияУдержания.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТТаблицаНДФЛ
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержания
	|ГДЕ
	|	НачисленияУдержания.Организация = &Организация
	|	И НачисленияУдержания.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НачисленияУдержания.НачислениеУдержание В(&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|	И &Условие
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияУдержания.Организация,
	|	НачисленияУдержания.НачислениеУдержание,
	|	НачисленияУдержания.РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеУдержаниеВидОперации.НачислениеУдержание КАК НачислениеУдержание,
	|	НачислениеУдержаниеВидОперации.ВидОперации КАК ВидОперации
	|ПОМЕСТИТЬ ВТНачислениеУдержаниеВидОперации
	|ИЗ
	|	&НачислениеУдержаниеВидОперации КАК НачислениеУдержаниеВидОперации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачислениеУдержание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНДФЛ.Организация КАК Организация,
	|	ТаблицаНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(ВидыОсобыхУдержанийВидОперации.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка)) КАК ВидОперации,
	|	СУММА(ТаблицаНДФЛ.Сумма) КАК Сумма
	|ИЗ
	|	ВТТаблицаНДФЛ КАК ТаблицаНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеУдержаниеВидОперации КАК ВидыОсобыхУдержанийВидОперации
	|		ПО ТаблицаНДФЛ.НачислениеУдержание = ВидыОсобыхУдержанийВидОперации.НачислениеУдержание
	|ГДЕ
	|	ТаблицаНДФЛ.Сумма <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНДФЛ.РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(ВидыОсобыхУдержанийВидОперации.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка)),
	|	ТаблицаНДФЛ.Организация";
	
	Если ЗначениеЗаполнено(РасчетныйДокумент) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "НачисленияУдержания.Регистратор = &РасчетныйДокумент");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "Истина");
	КонецЕсли;
	Если Аванс Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НачисленияУдержанияПоСотрудникам", "НачисленияУдержанияПоСотрудникамАвансом");
	КонецЕсли;
	ТаблицаОтраженияВУчете = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаОтраженияВУчете;
	
КонецФункции

Функция ПодразделенияПрименяющиеЛьготныеТарифы(Организация) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка КАК Ссылка,
	|	ПодразделенияОрганизаций.ПрименяемыйЛьготныйТерриториальныйТариф КАК ПрименяемыйЛьготныйТерриториальныйТариф
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|	И ПодразделенияОрганизаций.ПрименяемыйЛьготныйТерриториальныйТариф <> Значение(Справочник.ВидыТарифовСтраховыхВзносов.ПустаяСсылка)";
	
	РезультатЗапроса =  Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;

КонецФункции

// Обработчик подписки на событие "ПриПроведении" документа "АвансовыйПлатежИностранцаПоНДФЛ".
// Включает функциональную опцию "ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ", если она не
// была включена ранее
//
// Параметры:
//  Источник - ДокументОбъект.АвансовыйПлатежИностранцаПоНДФЛ - документ, вызвавший данное событие
//  Отказ - Булево - признак отказа от проведения
//  РежимПроведения - РежимПроведенияДокумента - режим, в котором проводится документ-источник
//
Процедура АвансовыйПлатежИностранцаПоНДФЛПриПроведении(Источник, Отказ, РежимПроведения) Экспорт
	
	Если Отказ Или ПроведениеСервер.ГрупповоеПерепроведение(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ") Тогда
		Константы.ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ.УстановитьЗначениеОпцииАвансовыеПлатежиИностранцев(Истина);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки на событие "ОбработкаУдаленияПроведения" документа "АвансовыйПлатежИностранцаПоНДФЛ".
// Выключает функциональную опцию "ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ", если в базе не осталось
// ни одного проведенного документа "АвансовыйПлатежИностранцаПоНДФЛ"
//
// Параметры:
//  Источник - ДокументОбъект.АвансовыйПлатежИностранцаПоНДФЛ - документ, вызвавший данное событие
//  Отказ - Булево - признак отказа от удаления проведения
//
Процедура АвансовыйПлатежИностранцаПоНДФЛУдалениеПроведения(Источник, Отказ) Экспорт
	
	Если Отказ Или ПроведениеСервер.ГрупповоеПерепроведение(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ")
		И Не Константы.ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ.ЕстьПлатежи() Тогда
		Константы.ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ.УстановитьЗначениеОпцииАвансовыеПлатежиИностранцев(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Функция СчетаРасчетовПоЗарплате()
	
	СчетаРасчетов = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасчетов,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасчетов,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами));
	
	Возврат СчетаРасчетов;
КонецФункции

Функция СчетаВзносов()
	
	СчетаВзносов = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ФСС));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПФР));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ФОМС));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.СтраховыеВзносыЕдиныйТариф));
	
	Возврат СчетаВзносов;
КонецФункции

Функция СчетаПрочихОпераций()
	
	СчетаПрочихОпераций = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Касса));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЦифровойРубль));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Вспомогательный));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам));
	
	Возврат СчетаПрочихОпераций;
КонецФункции

Функция ДатаНачалаПримененияНовойМетодикиУчетаРезервов() Экспорт
	
	Возврат '20260101';
	
КонецФункции

Функция ПолучитьВидПремииПоБазовомуПериоду(ДатаНачалаБазовогоПериода, ДатаОкончанияБазовогоПериода, ТипПремии, Выплата) Экспорт
	
	Отбор = Новый Структура;
	Если ТипПремии = Перечисления.ТипыПремии.РазоваяПремия Тогда
		Отбор.Вставить("КоличествоМесяцевБазовогоПериода", 0);
		Отбор.Вставить("КатегорияНачисленияИлиНеоплаченногоВремени", Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Премия);
		Отбор.Вставить("СреднийЗаработокОбщий", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать);
	Иначе
		КоличествоМесяцевБазовогоПериода = Месяц(ДатаОкончанияБазовогоПериода) - Месяц(ДатаНачалаБазовогоПериода) + 1;
		Отбор.Вставить("КоличествоМесяцевБазовогоПериода", КоличествоМесяцевБазовогоПериода);
		Если КоличествоМесяцевБазовогоПериода = 3 Тогда
			Отбор.Вставить("КатегорияНачисленияИлиНеоплаченногоВремени", Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КвартальнаяПремия);
		Иначе
			Отбор.Вставить("КатегорияНачисленияИлиНеоплаченногоВремени", Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Премия);
		КонецЕсли;
		Если КоличествоМесяцевБазовогоПериода = 12 Тогда
			Если Выплата = 0 Тогда
				Отбор.Вставить("СреднийЗаработокОбщий", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяФиксированнойСуммой);
			Иначе
				Отбор.Вставить("СреднийЗаработокОбщий", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяПроцентом);
			КонецЕсли;
		Иначе
			Если Выплата = 0 Тогда
				Отбор.Вставить("СреднийЗаработокОбщий", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремияФиксированнойСуммой);
			Иначе
				Отбор.Вставить("СреднийЗаработокОбщий", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремияПроцентом);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НайденныеНачисления = ПланыВидовРасчета.Начисления.НачисленияПоОтбору(Отбор);
	Если НайденныеНачисления.Количество() = 0 Тогда
		УстановитьПривилегированныйРежим(Истина);
		ПланыВидовРасчета.Начисления.СоздатьПремии();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	НайденныеНачисления = ПланыВидовРасчета.Начисления.НачисленияПоОтбору(Отбор);
	Если НайденныеНачисления.Количество() > 0 Тогда
		Возврат НайденныеНачисления[0].Ссылка;
	Иначе
		ВызватьИсключение НСтр("ru = 'Отсутствует вид начисления соответствующий выбранным настройкам'");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СозданиеДенежныхДокументов

Процедура СформироватьДокументыОплаты(Форма) Экспорт
	
	Основание = Форма.Объект.Ссылка;
	
	ТребуетсяЗапись     = НЕ ЗначениеЗаполнено(Форма.Параметры.Ключ) ИЛИ Форма.Модифицированность;
	ТребуетсяПроведение = ЗначениеЗаполнено(Форма.Параметры.Ключ) И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Проведен");
	
	Если ТребуетсяЗапись ИЛИ ТребуетсяПроведение Тогда
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
			ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись);
		Иначе
			ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		Форма.Записать(ПараметрыЗаписи);
	КонецЕсли;
	
	Построчно = Ложь;
	ИмяКолонкиВыплаты = "КВыплате";
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
		
		ИмяКолонкиВыплаты = "Сумма";
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ВидМестаВыплаты") = Перечисления.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект
			ИЛИ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ВидМестаВыплаты") = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет Тогда
			ИмяДокументаВыплаты = "СписаниеСРасчетногоСчета";
			Построчно = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ВидМестаВыплаты") = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет;
		Иначе
			ИмяДокументаВыплаты = "РасходныйКассовыйОрдер";
		КонецЕсли;

		СписокРеквизитовОснования = "Организация, ПодразделениеОрганизации, Дата, Проведен";
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, СписокРеквизитовОснования);
		
	Иначе
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу") Тогда
			ИмяДокументаВыплаты = "РасходныйКассовыйОрдер";
		Иначе
			ИмяДокументаВыплаты = "СписаниеСРасчетногоСчета";
			Построчно = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ВидМестаВыплаты") = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет;
		КонецЕсли;
		СписокРеквизитовОснования = "Организация, Подразделение, Дата, Проведен";
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, СписокРеквизитовОснования);
		РеквизитыОснования.Вставить("ПодразделениеОрганизации", РеквизитыОснования.Подразделение);
	КонецЕсли;
	
	СтатьяДвиженияДенежныхСредств = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатьиДвиженияДенежныхСредств.ОплатаТруда");
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
		РежимЗаписи = ?(НЕ Основание.ПометкаУдаления,
			РежимЗаписиДокумента.Проведение,
			РежимЗаписиДокумента.Запись);
	Иначе
		РежимЗаписи = ?(Основание.Проведен,
			РежимЗаписиДокумента.Проведение,
			РежимЗаписиДокумента.Запись);
	КонецЕсли;
		
	МассивДокументов= Новый Массив;
	МассивДокументов.Добавить(Основание);
	
	Если Построчно Тогда
		
		ТаблицаВыплат = Основание.Зарплата.Выгрузить();
		ТаблицаВыплат.Свернуть("ФизическоеЛицо", "КомпенсацияЗаЗадержкуЗарплаты, " + ИмяКолонкиВыплаты);
		
		Для Каждого СтрокаТаблицы ИЗ ТаблицаВыплат Цикл
			ДокументВыплаты = Документы[ИмяДокументаВыплаты].СоздатьДокумент();
			ДокументВыплаты.Заполнить(МассивДокументов);
			ДокументВыплаты.ПеречислениеЗаработнойПлаты.Очистить();
			ДокументВыплаты.ВидОперации                   = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику;
			ДокументВыплаты.Контрагент                    = СтрокаТаблицы.ФизическоеЛицо;
			ДокументВыплаты.ПлатежнаяВедомость            = Основание;
			ДокументВыплаты.СуммаДокумента                = СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты + СтрокаТаблицы[ИмяКолонкиВыплаты];
			ДокументВыплаты.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
			ДокументВыплаты.НазначениеПлатежа             = Документы.СписаниеСРасчетногоСчета.ПолучитьНазначениеПлатежа(ДокументВыплаты);
			ДокументВыплаты.Записать(РежимЗаписи);
		КонецЦикла;
		
	Иначе
		ДокументВыплаты = Документы[ИмяДокументаВыплаты].СоздатьДокумент();
		ДокументВыплаты.Заполнить(МассивДокументов);
		ДокументВыплаты.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
		ДокументВыплаты.Дата                           = Основание.Дата;
		ДокументВыплаты.Записать(РежимЗаписи);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИсполнительныеЛисты

Функция ДатаНачалаДетальногоУчетаУдержаний() Экспорт
	
	Возврат '20220101';
	
КонецФункции

Функция ИспользуетсяТиповаяАналитикаСчетаУчетаУдержаний() Экспорт
	
	СчетРасчета = ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументамРаботников;
	ВидыСубконто = СчетРасчета.ВидыСубконто;
	
	АналитикаКорректна = ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) <> Неопределено
		И ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций) <> Неопределено
		И ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ИсполнительныеЛистыРаботников) <> Неопределено;
	
	Возврат АналитикаКорректна;
	
КонецФункции

Функция ДетальныйУчетИсполнительныхЛистов(Период) Экспорт
	
	Возврат Период >= ДатаНачалаДетальногоУчетаУдержаний()
		И ИспользуетсяТиповаяАналитикаСчетаУчетаУдержаний();
	
КонецФункции

Функция ДанныеФизическогоЛицаДляИсполнительногоЛиста(ФизическоеЛицо, ДатаСреза) Экспорт
	
	ТекстПолучатель = "";
	
	ДанныеФизЛица = ДанныеФизическихЛиц( , ФизическоеЛицо, ДатаСреза, Ложь);
	
	//////////////////////////
	// Назначение платежа
	
	// Максимальная длина назначения платежа 210 символов.
	//   Резервируем символы на описание документа 60 символов
	//   Резервируем символы на стандартный подвал: сумма 20 символов
	МаксимальнаяДлинаСтроки = 130; 
	
	ДлинаСтроки = СтрДлина(ДанныеФизЛица.Представление);
	
	// Обязательно указываем ФИО должника
	ТекстПолучатель = ДанныеФизЛица.Представление;
	
	// Далее все доступные идентифицирующие должника сведения
	Если ЗначениеЗаполнено(ДанныеФизЛица.ДатаРождения) Тогда
		ДлинаСтроки = СтрДлина(ТекстПолучатель);
		ПредставлениеРеквизита = СтрШаблон(НСтр("ru = ', %1 г.р.'"), Формат(ДанныеФизЛица.ДатаРождения, "ДФ=dd.MM.yyyy"));
		Если ДлинаСтроки + СтрДлина(ПредставлениеРеквизита) <= МаксимальнаяДлинаСтроки Тогда
			ТекстПолучатель = СтрШаблон(НСтр("ru = '%1%2'"), ТекстПолучатель, ПредставлениеРеквизита);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФизЛица.ИНН) Тогда
		ДлинаСтроки = СтрДлина(ТекстПолучатель);
		ПредставлениеРеквизита = СтрШаблон(НСтр("ru = ', ИНН %1'"), ДанныеФизЛица.ИНН);
		Если ДлинаСтроки + СтрДлина(ПредставлениеРеквизита) <= МаксимальнаяДлинаСтроки Тогда
			ТекстПолучатель = СтрШаблон(НСтр("ru = '%1%2'"), ТекстПолучатель, ПредставлениеРеквизита);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФизЛица.СтраховойНомерПФР) Тогда
		ДлинаСтроки = СтрДлина(ТекстПолучатель);
		ПредставлениеРеквизита = СтрШаблон(НСтр("ru = ', СНИЛС %1'"), ДанныеФизЛица.СтраховойНомерПФР);
		Если ДлинаСтроки + СтрДлина(ПредставлениеРеквизита) <= МаксимальнаяДлинаСтроки Тогда
			ТекстПолучатель = СтрШаблон(НСтр("ru = '%1%2'"), ТекстПолучатель, ПредставлениеРеквизита);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФизЛица.ПредставлениеДокумента) Тогда
		ДлинаСтроки = СтрДлина(ТекстПолучатель);
		ПредставлениеРеквизита = СтрШаблон(НСтр("ru = '. %1.'"), ДанныеФизЛица.ПредставлениеДокумента);
		Если ДлинаСтроки + СтрДлина(ПредставлениеРеквизита) <= МаксимальнаяДлинаСтроки Тогда
			ТекстПолучатель = СтрШаблон(НСтр("ru = '%1%2'"), ТекстПолучатель, ПредставлениеРеквизита);
		КонецЕсли;
	КонецЕсли;
	
	//////////////////////////
	// Сведения о документе
	
	ПоказательНомера = "";
	Если ЗначениеЗаполнено(ДанныеФизЛица.ВидДокумента) Тогда
		Если ДанныеФизЛица.ВидДокумента = Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ Тогда
			ПоказательНомера = "01;" + СокрЛП(СтрЗаменить(ДанныеФизЛица.Серия, " ", "")) + СокрЛП(ДанныеФизЛица.Номер);
		КонецЕсли;
	КонецЕсли;
	
	ДанныеФизическогоЛица = Новый Структура();
	ДанныеФизическогоЛица.Вставить("ТекстПолучатель",  ТекстПолучатель);
	ДанныеФизическогоЛица.Вставить("ПоказательНомера", ПоказательНомера);
	
	Возврат ДанныеФизическогоЛица;
	
КонецФункции

Функция ЕдинственныйИсполнительныйЛист(Организация, Сотрудник) Экспорт
	
	ИсполнительныйЛист = Документы.ИсполнительныйЛист.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(Сотрудник) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Сотрудник",   Сотрудник);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсполнительныйЛист.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ИсполнительныйЛист КАК ИсполнительныйЛист
		|ГДЕ
		|	ИсполнительныйЛист.Организация = &Организация
		|	И ИсполнительныйЛист.ФизическоеЛицо = &Сотрудник
		|	И ИсполнительныйЛист.Проведен";
		
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() = 1 Тогда
			ИсполнительныйЛист = Результат[0].Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИсполнительныйЛист
	
КонецФункции

Функция СтруктураРеквизитовПолученияДанныхПоИСполнительномуДокументу() Экспорт
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Организация",        Справочники.Организации.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("Контрагент",         Справочники.Контрагенты.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("Сотрудник",          Справочники.ФизическиеЛица.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("ИсполнительныйЛист", Документы.ИсполнительныйЛист.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("Дата");
	СтруктураРеквизитов.Вставить("Ссылка");
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

Функция УдержаноПоИсполнительномуДокументу(РеквизитыПолученияДанных) Экспорт
	
	СуммаУдержания = 0;
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")
		И РеквизитыПолученияДанных.Ссылка <> Неопределено
		И ЗначениеЗаполнено(РеквизитыПолученияДанных.Дата)
		И ЗначениеЗаполнено(РеквизитыПолученияДанных.Организация)
		И ЗначениеЗаполнено(РеквизитыПолученияДанных.Контрагент)
		И ЗначениеЗаполнено(РеквизитыПолученияДанных.Сотрудник)
		И ЗначениеЗаполнено(РеквизитыПолученияДанных.ИсполнительныйЛист) Тогда
		
		ВидыСубконто = Новый Массив;
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ИсполнительныеЛистыРаботников);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СчетРасчетов",       ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументамРаботников);
		Запрос.УстановитьПараметр("Организация",        РеквизитыПолученияДанных.Организация);
		Запрос.УстановитьПараметр("Контрагент",         РеквизитыПолученияДанных.Контрагент);
		Запрос.УстановитьПараметр("Сотрудник",          РеквизитыПолученияДанных.Сотрудник);
		Запрос.УстановитьПараметр("ИсполнительныйЛист", РеквизитыПолученияДанных.ИсполнительныйЛист);
		Запрос.УстановитьПараметр("Период",             Новый Граница(Новый МоментВремени(РеквизитыПолученияДанных.Дата, РеквизитыПолученияДанных.Ссылка), ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("ВидыСубконто",       ВидыСубконто);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Организация КАК Организация,
		|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
		|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
		|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
		|	ХозрасчетныйОстатки.СуммаОстатокКт КАК СуммаОстатокКт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&Период,
		|			Счет = &СчетРасчетов,
		|			&ВидыСубконто,
		|			Организация = &Организация
		|				И Субконто1 = &Контрагент
		|				И Субконто2 = &Сотрудник
		|				И Субконто3 = &ИСполнительныйЛист) КАК ХозрасчетныйОстатки
		|ГДЕ
		|	ХозрасчетныйОстатки.СуммаОстатокКт > 0";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СуммаУдержания = Выборка.СуммаОстатокКт;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СуммаУдержания;
	
КонецФункции

// Предназначена для генерации движений, связанных с отражением в регл. учете перечисления по исполнительному листу
//
// Параметры:
//	ТаблицаПеречислениеПоИсполнительномуЛисту - таблица значений с колонками
//      Подразделение
//      Контрагент
//      Сотрудник
//      СуммаКВыплате
//      СтатьяДвиженияДенежныхСредств
//      СчетУчета
//      ИсполнительныйЛист
//	ТаблицаРеквизиты  - таблица значений содержит реквизиты документа
//	Движения
//	Отказ
//
Процедура СформироватьДвиженияПеречислениеПоИсполнительномуЛистуРаботника(ТаблицаПеречислениеПоИсполнительномуЛисту, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаПеречислениеПоИсполнительномуЛисту) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыПеречисленияПоИсполнительномуЛисту(ТаблицаПеречислениеПоИсполнительномуЛисту, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	СформироватьПроводкиПоПеречисленияПоИсполнительномуЛисту(Параметры.ПеречислениеПоИсполнительномуЛисту, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыПеречисленияПоИсполнительномуЛисту(ТаблицаПеречислениеПоИсполнительномуЛисту, ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата> - период движений - дата документа
	+ "Регистратор,"               // <ДокументСсылка>
	+ "Организация";               // <СправочникСсылка.Организации>
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ВыплатаДепонентов
	
	СписокОбязательныхКолонок = ""
	+ "Подразделение,"                 // <СправочникСсылка.ПодразделенияОрганизаций> - подразделение
	+ "БанковскийСчет,"                // <СправочникСсылка.БанковскиеСчета>
	+ "Контрагент,"                    // <СправочникСсылка.Контрагенты> - контрагент-получатель удержаний
	+ "Сотрудник,"                     // <СправочникСсылка.ФизическиеЛица> - физическое лицо, у которого произвели удержание
	+ "СуммаКВыплате,"                 // <Число,15,2> - сумма выплаты в валюте документа
	+ "СтатьяДвиженияДенежныхСредств," // <СправочникСсылка.СтатьиДвиженияДенежныхСредств>
	+ "СчетУчета,"                     // <ПланСчетовСсылка.Хозрасчетный> - счет учета денежных средств
	+ "ИсполнительныйЛист";            // <ДокументСсылка.ИсполнительныйЛист>
	
	Параметры.Вставить("ПеречислениеПоИсполнительномуЛисту", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПеречислениеПоИсполнительномуЛисту, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

Процедура СформироватьПроводкиПоПеречисленияПоИсполнительномуЛисту(ТаблицаВыплаты, Реквизиты, Движения, Отказ)
	
	СчетРасчетов = ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументамРаботников;
	
	ДетальныйУчетИсполнительныхЛистов = ДетальныйУчетИсполнительныхЛистов(Реквизиты.Период);
	
	Для Каждого СтрокаТаблицы Из ТаблицаВыплаты Цикл
		
		Если СтрокаТаблицы.СуммаКВыплате = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСодержания = НСтр("ru = 'Перечисление по исполнительному листу работника'");
		Если ЗначениеЗаполнено(СтрокаТаблицы.Сотрудник) Тогда
			ПредставлениеРаботника = ПредставлениеФизическогоЛица(СтрокаТаблицы.Сотрудник, Реквизиты.Период);
			ТекстСодержания = СтрШаблон(НСтр("ru = '%1 (%2)'"), ТекстСодержания, ПредставлениеРаботника.ФамилияИО);
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		
		Проводка.Период       = Реквизиты.Период;
		Проводка.Организация  = Реквизиты.Организация;
		Проводка.Содержание   = ТекстСодержания;
		
		Проводка.Сумма = СтрокаТаблицы.СуммаКВыплате;
		
		Проводка.СчетДт = СчетРасчетов;
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты", СтрокаТаблицы.Контрагент);
		
		Если ДетальныйУчетИсполнительныхЛистов Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РаботникиОрганизаций", СтрокаТаблицы.Сотрудник);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ИсполнительныеЛистыРаботников", СтрокаТаблицы.ИсполнительныйЛист);
		КонецЕсли;
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Проводка.СчетКт = СтрокаТаблицы.СчетУчета;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиДвиженияДенежныхСредств", СтрокаТаблицы.СтатьяДвиженияДенежныхСредств);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет) Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "БанковскиеСчета", СтрокаТаблицы.БанковскийСчет);
		КонецЕсли;
		
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

// Функция формирует таблицу выплаты зарплаты для передачи в подсистему УСН
//
Функция ПолучитьТаблицуВыплатыПоИСполнительномуЛистуДляУСН(ТаблицаРеквизиты, ТаблицаВыплаты) Экспорт
	
	Параметры = ПодготовитьПараметрыПеречисленияПоИсполнительномуЛисту(ТаблицаВыплаты, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ДетальныйУчетИсполнительныхЛистов = ДетальныйУчетИсполнительныхЛистов(Реквизиты.Период);
	
	ТаблицаПеречислениеПоИсполнительномуЛисту = Новый ТаблицаЗначений;
	ТаблицаПеречислениеПоИсполнительномуЛисту.Колонки.Добавить("Работник");
	ТаблицаПеречислениеПоИсполнительномуЛисту.Колонки.Добавить("Сделка");
	ТаблицаПеречислениеПоИсполнительномуЛисту.Колонки.Добавить("Контрагент");
	ТаблицаПеречислениеПоИсполнительномуЛисту.Колонки.Добавить("КурсВзаиморасчетов");
	ТаблицаПеречислениеПоИсполнительномуЛисту.Колонки.Добавить("КратностьВзаиморасчетов");
	ТаблицаПеречислениеПоИсполнительномуЛисту.Колонки.Добавить("Сумма");
	
	Если ТаблицаВыплаты = Неопределено Или Не ДетальныйУчетИсполнительныхЛистов Тогда
		Возврат ТаблицаПеречислениеПоИсполнительномуЛисту;
	КонецЕсли;
		
	Для каждого СтрокаТаблицы Из ТаблицаВыплаты Цикл
		
		НоваяСтрока = ТаблицаПеречислениеПоИсполнительномуЛисту.Добавить();
		НоваяСтрока.Работник                = СтрокаТаблицы.Сотрудник;
		НоваяСтрока.Сделка                  = СтрокаТаблицы.ИсполнительныйЛист;
		НоваяСтрока.Контрагент              = СтрокаТаблицы.Контрагент;
		НоваяСтрока.КурсВзаиморасчетов      = 1;
		НоваяСтрока.КратностьВзаиморасчетов = 1;
		НоваяСтрока.Сумма                   = СтрокаТаблицы.СуммаКВыплате;
		
	КонецЦикла;
	
	ТаблицаПеречислениеПоИсполнительномуЛисту.Свернуть("Работник, Контрагент, Сделка, КурсВзаиморасчетов, КратностьВзаиморасчетов", "Сумма");
	
	Возврат ТаблицаПеречислениеПоИсполнительномуЛисту;
	
КонецФункции

// Функция формирует единую таблицу перечисления удержаний для разных операций для подсистемы налогового учета ИП
//
Функция ПолучитьТаблицуПрочихУдержанийИП(ТаблицаРасшифровкаПлатежа, ТаблицаУдержаниеПоИсполнительномуЛисту) Экспорт

	Если ТаблицаРасшифровкаПлатежа = Неопределено
		И ТаблицаУдержаниеПоИсполнительномуЛисту = Неопределено Тогда
		Возврат Неопределено;
	ИначеЕсли ТаблицаРасшифровкаПлатежа = Неопределено Тогда
		Возврат ТаблицаУдержаниеПоИсполнительномуЛисту;
	ИначеЕсли ТаблицаУдержаниеПоИсполнительномуЛисту = Неопределено Тогда
		Возврат ТаблицаРасшифровкаПлатежа;
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаРасшифровкаПлатежа, ТаблицаУдержаниеПоИсполнительномуЛисту);
		Возврат ТаблицаУдержаниеПоИсполнительномуЛисту;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Чеки

Функция ЧекиПробитыНаФискальномУстройстве(ОбъектСсылка) Экспорт
	
	Если ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.ОтражениеЗарплатыВБухучете") Тогда
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектСсылка, "ЗарплатаОтраженаВБухучете, Проведен");
		Если НЕ (РеквизитыОбъекта.Проведен И РеквизитыОбъекта.ЗарплатаОтраженаВБухучете) Тогда
			Возврат Ложь;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.ОтражениеЗарплатыВУчете") Тогда
		Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСсылка, "Проведен") Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ОбъектСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеПоУдержаниямДляПечатиЧеков.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор КАК ДокументОснование
	|ПОМЕСТИТЬ ВТ_ИдентификаторыЧеков
	|ИЗ
	|	РегистрСведений.ДанныеПоУдержаниямДляПечатиЧеков КАК ДанныеПоУдержаниямДляПечатиЧеков
	|ГДЕ
	|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИдентификаторыЧеков.ИдентификаторЗаписи КАК ИдентификаторЗаписиПлан,
	|	ФискальныеОперации.ИдентификаторЗаписи КАК ИдентификаторЗаписиФакт
	|ПОМЕСТИТЬ ВТ_СравнениеСостояний
	|ИЗ
	|	ВТ_ИдентификаторыЧеков КАК ВТ_ИдентификаторыЧеков
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ВТ_ИдентификаторыЧеков.ДокументОснование = ФискальныеОперации.ДокументОснование
	|			И ВТ_ИдентификаторыЧеков.ИдентификаторЗаписи = ФискальныеОперации.ИдентификаторЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИдентификаторыЧеков.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ВТ_ИдентификаторыЧеков.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	ВТ_ИдентификаторыЧеков КАК ВТ_ИдентификаторыЧеков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СравнениеСостояний.ИдентификаторЗаписиПлан КАК ИдентификаторЗаписиПлан,
	|	ВТ_СравнениеСостояний.ИдентификаторЗаписиФакт КАК ИдентификаторЗаписиФакт
	|ИЗ
	|	ВТ_СравнениеСостояний КАК ВТ_СравнениеСостояний
	|ГДЕ
	|	ВТ_СравнениеСостояний.ИдентификаторЗаписиФакт ЕСТЬ NULL";
	
	Результат = Запрос.ВыполнитьПакет();
	Если Результат[2].Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Результат[3].Пустой();
	КонецЕсли;
	
КонецФункции

Процедура ПроверитьВозможностьПечатиЧеков(ОбъектСсылка, ПечататьЧек, ТекстПредупреждения = "") Экспорт
	
	Если ТипЗнч(ОбъектСсылка) <> Тип("ДокументСсылка.ОтражениеЗарплатыВБухучете")
		И ТипЗнч(ОбъектСсылка) <> Тип("ДокументСсылка.ОтражениеЗарплатыВУчете") Тогда
		ТекстПредупреждения = НСтр("ru = 'Объект не является основанием для печати чеков'");
		ПечататьЧек = Ложь;
	Иначе
		ДокументОтраженВУчете = Ложь;
		Если ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.ОтражениеЗарплатыВБухучете") Тогда
			РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектСсылка, "ЗарплатаОтраженаВБухучете, Проведен");
			Если НЕ (РеквизитыОбъекта.Проведен И РеквизитыОбъекта.ЗарплатаОтраженаВБухучете) Тогда
				ТекстПредупреждения = НСтр("ru = 'Операция возможна только после отражения в учете документа'");
				ПечататьЧек = Ложь;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.ОтражениеЗарплатыВУчете") Тогда
			Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСсылка, "Проведен") Тогда
				ТекстПредупреждения = НСтр("ru = 'Операция возможна только после проведения документа'");
				ПечататьЧек = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если ПечататьЧек Тогда
			ДокументОбъект = ОбъектСсылка.ПолучитьОбъект();
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Регистратор", ОбъектСсылка);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ДанныеПоУдержаниямДляПечатиЧеков.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
			|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор КАК Регистратор
			|ИЗ
			|	РегистрСведений.ДанныеПоУдержаниямДляПечатиЧеков КАК ДанныеПоУдержаниямДляПечатиЧеков
			|ГДЕ
			|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор = &Регистратор";
			
			Если Запрос.Выполнить().Пустой() Тогда
				ТекстПредупреждения = НСтр("ru = 'В документе нет оснований для печати чеков'");
				ПечататьЧек = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаПараметрыПечатиЧека(ДокументСсылка, ИменаТаблиц, СообщениеОбОшибке) Экспорт
	
	ТекстЗапроса = ТекстЗапросаРеквизитыПечатиЧека(ИменаТаблиц);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРеквизитыПечатиЧека(ИменаТаблиц)
	
	ИменаТаблиц.Добавить("ВТ_ВсеДанныеДляПечати");
	ИменаТаблиц.Добавить("ВТ_ПробитыеЧеки");
	ИменаТаблиц.Добавить("ВТ_ДанныеДляПечати");
	ИменаТаблиц.Добавить("ВТ_РеквизитыШапки");
	ИменаТаблиц.Добавить("РеквизитыПечатиЧека");
	ИменаТаблиц.Добавить("ОплачиваемаяНоменклатура");
	ИменаТаблиц.Добавить("ОплачиваемыеДокументы");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор КАК Регистратор,
	|	ДанныеПоУдержаниямДляПечатиЧеков.ИдентификаторЗаписи КАК ИдентификаторЗаписи
	|ПОМЕСТИТЬ ВТ_ВсеДанныеДляПечати
	|ИЗ
	|	РегистрСведений.ДанныеПоУдержаниямДляПечатиЧеков КАК ДанныеПоУдержаниямДляПечатиЧеков
	|ГДЕ
	|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФискальныеОперации.ДокументОснование КАК ДокументОснование,
	|	ФискальныеОперации.ИдентификаторЗаписи КАК ИдентификаторЗаписи
	|ПОМЕСТИТЬ ВТ_ПробитыеЧеки
	|ИЗ
	|	РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|ГДЕ
	|	ФискальныеОперации.ДокументОснование = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВсеДанныеДляПечати.Регистратор КАК Регистратор,
	|	ВТ_ВсеДанныеДляПечати.ИдентификаторЗаписи КАК ИдентификаторЗаписи
	|ПОМЕСТИТЬ ВТ_ДанныеДляПечати
	|ИЗ
	|	ВТ_ВсеДанныеДляПечати КАК ВТ_ВсеДанныеДляПечати
	|ГДЕ
	|	НЕ ВТ_ВсеДанныеДляПечати.ИдентификаторЗаписи В
	|				(ВЫБРАТЬ
	|					ВТ_ПробитыеЧеки.ИдентификаторЗаписи
	|				ИЗ
	|					ВТ_ПробитыеЧеки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДляПечати.Регистратор КАК Ссылка,
	|	&ДатаОчереди КАК Дата,
	|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор.Номер КАК Номер,
	|	ДанныеПоУдержаниямДляПечатиЧеков.Организация КАК Организация,
	|	ДанныеПоУдержаниямДляПечатиЧеков.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеПоУдержаниямДляПечатиЧеков.ФизическоеЛицо КАК Контрагент,
	|	ДанныеПоУдержаниямДляПечатиЧеков.Сумма КАК СуммаДокумента,
	|	ЕСТЬNULL(ДанныеПоУдержаниямДляПечатиЧеков.ФизическоеЛицо.Наименование, """") + "": "" + ДанныеПоУдержаниямДляПечатиЧеков.ОписаниеУдержанияДляЧека КАК ОписаниеУдержанияДляЧека,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС) КАК СтавкаНДС,
	|	ДанныеПоУдержаниямДляПечатиЧеков.Контрагент КАК ПлатежныйАгент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорПлатежногоАгента,
	|	ДанныеПоУдержаниямДляПечатиЧеков.ИдентификаторЗаписи КАК ИдентификаторФискальнойЗаписи,
	|	ЛОЖЬ КАК НесколькоДоговоровПлатежногоАгента,
	|	НЕОПРЕДЕЛЕНО КАК НомерТаможеннойДекларации,
	|	НЕОПРЕДЕЛЕНО КАК КодСтраныПроисхожденияТовара,
	|	ЛОЖЬ КАК ВыводитьКонтрагентаВЧеке
	|ПОМЕСТИТЬ ВТ_РеквизитыШапки
	|ИЗ
	|	ВТ_ДанныеДляПечати КАК ВТ_ДанныеДляПечати
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПоУдержаниямДляПечатиЧеков КАК ДанныеПоУдержаниямДляПечатиЧеков
	|		ПО ВТ_ДанныеДляПечати.Регистратор = ДанныеПоУдержаниямДляПечатиЧеков.Регистратор
	|			И ВТ_ДанныеДляПечати.ИдентификаторЗаписи = ДанныеПоУдержаниямДляПечатиЧеков.ИдентификаторЗаписи
	|ГДЕ
	|	ДанныеПоУдержаниямДляПечатиЧеков.Регистратор = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыПечатиЧека.Ссылка КАК Ссылка,
	|	РеквизитыПечатиЧека.Организация КАК Организация,
	|	РеквизитыПечатиЧека.СуммаДокумента КАК СуммаДокумента,
	|	РеквизитыПечатиЧека.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РеквизитыПечатиЧека.Контрагент КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	РеквизитыПечатиЧека.Дата КАК Дата,
	|	РеквизитыПечатиЧека.Номер КАК Номер,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОплатыККТ.ВстречноеПредоставление) КАК ТипОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств) КАК ТипРасчета,
	|	РеквизитыПечатиЧека.СтавкаНДС КАК СтавкаНДС,
	|	НЕОПРЕДЕЛЕНО КАК Патент,
	|	РеквизитыПечатиЧека.ОписаниеУдержанияДляЧека КАК ОписаниеУдержанияДляЧека,
	|	РеквизитыПечатиЧека.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	РеквизитыПечатиЧека.ПлатежныйАгент КАК ПлатежныйАгент,
	|	РеквизитыПечатиЧека.НесколькоДоговоровПлатежногоАгента КАК НесколькоДоговоровПлатежногоАгента,
	|	РеквизитыПечатиЧека.ИдентификаторФискальнойЗаписи КАК ИдентификаторФискальнойЗаписи,
	|	РеквизитыПечатиЧека.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации,
	|	РеквизитыПечатиЧека.КодСтраныПроисхожденияТовара КАК КодСтраныПроисхожденияТовара
	|ИЗ
	|	ВТ_РеквизитыШапки КАК РеквизитыПечатиЧека
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплачиваемаяНоменклатура.Ссылка КАК Документ,
	|	ОплачиваемаяНоменклатура.Дата КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	"""" КАК КодЕдиницыИзмерения,
	|	"""" КАК КодВидаТРУ,
	|	ОплачиваемаяНоменклатура.ОписаниеУдержанияДляЧека КАК Наименование,
	|	1 КАК Количество,
	|	ОплачиваемаяНоменклатура.СуммаДокумента КАК Цена,
	|	0 КАК СуммаСкидок,
	|	0 КАК СуммаНДС,
	|	ОплачиваемаяНоменклатура.СтавкаНДС КАК СтавкаНДС,
	|	ОплачиваемаяНоменклатура.СуммаДокумента КАК Сумма,
	|	1 КАК КоличествоОтгружено,
	|	ОплачиваемаяНоменклатура.ПлатежныйАгент КАК ПлатежныйАгент,
	|	ОплачиваемаяНоменклатура.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	ОплачиваемаяНоменклатура.НесколькоДоговоровПлатежногоАгента КАК НесколькоДоговоровПлатежногоАгента,
	|	НЕОПРЕДЕЛЕНО КАК Штрихкод,
	|	ОплачиваемаяНоменклатура.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОплачиваемаяНоменклатура.Контрагент КАК Контрагент,
	|	ОплачиваемаяНоменклатура.ИдентификаторФискальнойЗаписи КАК ИдентификаторФискальнойЗаписи,
	|	ОплачиваемаяНоменклатура.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации,
	|	ОплачиваемаяНоменклатура.КодСтраныПроисхожденияТовара КАК КодСтраныПроисхожденияТовара
	|ИЗ
	|	ВТ_РеквизитыШапки КАК ОплачиваемаяНоменклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплачиваемаяНоменклатура.СтавкаНДС,
	|	ОплачиваемаяНоменклатура.Ссылка,
	|	ОплачиваемаяНоменклатура.ОписаниеУдержанияДляЧека,
	|	ОплачиваемаяНоменклатура.Дата,
	|	ОплачиваемаяНоменклатура.ДоговорПлатежногоАгента,
	|	ОплачиваемаяНоменклатура.Контрагент,
	|	ОплачиваемаяНоменклатура.ФизическоеЛицо,
	|	ОплачиваемаяНоменклатура.СуммаДокумента,
	|	ОплачиваемаяНоменклатура.ИдентификаторФискальнойЗаписи,
	|	ОплачиваемаяНоменклатура.ПлатежныйАгент,
	|	ОплачиваемаяНоменклатура.НесколькоДоговоровПлатежногоАгента,
	|	ОплачиваемаяНоменклатура.НомерТаможеннойДекларации,
	|	ОплачиваемаяНоменклатура.КодСтраныПроисхожденияТовара,
	|	ОплачиваемаяНоменклатура.СуммаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплачиваемыеДокументы.Ссылка КАК Документ,
	|	ОплачиваемыеДокументы.Организация КАК Организация,
	|	ОплачиваемыеДокументы.Дата КАК Дата,
	|	0 КАК СуммаСкидкиПоДокументу,
	|	ЛОЖЬ КАК РасчетыВУсловныхЕдиницах,
	|	ОплачиваемыеДокументы.СтавкаНДС КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	ОплачиваемыеДокументы.СуммаДокумента КАК СуммаОплаты,
	|	ОплачиваемыеДокументы.СуммаДокумента КАК СуммаВзаиморасчетов,
	|	ОплачиваемыеДокументы.СуммаДокумента КАК СуммаОплатыВсего,
	|	ОплачиваемыеДокументы.ИдентификаторФискальнойЗаписи КАК ИдентификаторФискальнойЗаписи,
	|	ОплачиваемыеДокументы.ВыводитьКонтрагентаВЧеке КАК ВыводитьКонтрагентаВЧеке
	|ИЗ
	|	ВТ_РеквизитыШапки КАК ОплачиваемыеДокументы
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплачиваемыеДокументы.СтавкаНДС,
	|	ОплачиваемыеДокументы.Ссылка,
	|	ОплачиваемыеДокументы.Организация,
	|	ОплачиваемыеДокументы.Дата,
	|	ОплачиваемыеДокументы.СуммаДокумента,
	|	ОплачиваемыеДокументы.ИдентификаторФискальнойЗаписи,
	|	ОплачиваемыеДокументы.ВыводитьКонтрагентаВЧеке,
	|	ОплачиваемыеДокументы.СуммаДокумента,
	|	ОплачиваемыеДокументы.СуммаДокумента";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция НаименованиеВСтрокеЧека(СтруктураШапки) Экспорт
	
	Возврат СтруктураШапки.ОписаниеУдержанияДляЧека;
	
КонецФункции

Функция ИдентификаторЧека(ДокументСсылка, ПараметрыРасчетаХеша)
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.CRC32);
	ХешированиеДанных.Добавить(XMLСтрока(ДокументСсылка));
	ХешированиеДанных.Добавить(XMLСтрока(ПараметрыРасчетаХеша.ФизическоеЛицо));
	ХешированиеДанных.Добавить(XMLСтрока(ПараметрыРасчетаХеша.Подразделение));
	ХешированиеДанных.Добавить(XMLСтрока(ПараметрыРасчетаХеша.Контрагент));
	ХешированиеДанных.Добавить(XMLСтрока(ПараметрыРасчетаХеша.ОписаниеУдержанияДляЧека));
	ХешированиеДанных.Добавить(Формат(ПараметрыРасчетаХеша.Сумма, "ЧДЦ=2; ЧРД=.; ЧН=-; ЧГ=; ЧО=1"));
	
	Возврат ОбщегоНазначения.СократитьСтрокуКонтрольнойСуммой(Формат(ХешированиеДанных.ХешСумма, "ЧГ="), 36);
	
КонецФункции

#КонецОбласти

#Область Антикризис

// Устанавливает тариф страховых взносов для отраслей пострадавших от коронавирусной инфекции -
// включает или выключает его применение в периоде, в котором действует освобождение от налогов для них.
//
// Параметры:
//  Организация - СправочникСсылка.Организации 
//  Значение    - Булево - Истина, чтобы включить применение; Ложь - чтобы отключить
// 
// Возвращаемое значение:
//  Булево - Истина, если действие выполнено (в ходе вызова или до вызова)
//
Функция УстановитьТарифСтраховыхВзносовДляОтраслейПострадавшихОтКоронавируснойИнфекции(Организация, Значение) Экспорт
	
	ПериодОсвобождения = НалоговыйУчет.ПериодОсвобожденияОтНалоговПострадавшимОтКоронавируса();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов");
	ЭлементБлокировкиДанных.УстановитьЗначение("ГоловнаяОрганизация", Организация);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",               Организация);
	Запрос.УстановитьПараметр("НачалоПериодаОсвобождения", ПериодОсвобождения.Начало);
	Запрос.УстановитьПараметр("ПериодДоОсвобождения",      ПериодОсвобождения.Начало - 1);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ТарифыНаНачалоПериода.ВидТарифа КАК ВидТарифа,
	|	ТарифыНаНачалоПериода.ОрганизацияИнвалидов КАК ОрганизацияИнвалидов
	|ИЗ
	|	РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов.СрезПоследних(&ПериодДоОсвобождения, ГоловнаяОрганизация = &Организация) КАК ТарифыНаНачалоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 3
	|	ПослеНачалаПериодаОсвобождения.Период КАК Период,
	|	ПослеНачалаПериодаОсвобождения.ВидТарифа КАК ВидТарифа,
	|	ПослеНачалаПериодаОсвобождения.ОрганизацияИнвалидов КАК ОрганизацияИнвалидов
	|ИЗ
	|	РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПослеНачалаПериодаОсвобождения
	|ГДЕ
	|	ПослеНачалаПериодаОсвобождения.ГоловнаяОрганизация = &Организация
	|	И ПослеНачалаПериодаОсвобождения.Период >= &НачалоПериодаОсвобождения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПослеНачалаПериодаОсвобождения.Период";
	
	ТарифУстановлен = Ложь;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных.Заблокировать();
		
		РезультатЗапросов = Запрос.ВыполнитьПакет();
		ВыборкаНаНачалоПериода = РезультатЗапросов[0].Выбрать();
		Если Не ВыборкаНаНачалоПериода.Следующий() Тогда
			ТарифНаНачало        = Неопределено;
			ОрганизацияИнвалидов = Ложь;
		Иначе
			ТарифНаНачало        = ВыборкаНаНачалоПериода.ВидТарифа;
			ОрганизацияИнвалидов = ВыборкаНаНачалоПериода.ОрганизацияИнвалидов;
		КонецЕсли;
		
		ДанныеЗаПериод = РезультатЗапросов[1].Выгрузить();
		
		Если Значение Тогда
			ТарифУстановлен = ВключитьТарифСтраховыхВзносовДляОтраслейПострадавшихОтКоронавируснойИнфекции(
				Организация,
				ТарифНаНачало,
				ОрганизацияИнвалидов,
				ДанныеЗаПериод);
		Иначе
			ТарифУстановлен = ВыключитьТарифСтраховыхВзносовДляОтраслейПострадавшихОтКоронавируснойИнфекции(
				Организация,
				ТарифНаНачало,
				ОрганизацияИнвалидов,
				ДанныеЗаПериод);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			"РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов,
			Организация,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат ТарифУстановлен;
	
КонецФункции

Функция ВключитьТарифСтраховыхВзносовДляОтраслейПострадавшихОтКоронавируснойИнфекции(Организация, ТарифНаНачало, ОрганизацияИнвалидов, ДанныеЗаПериод)
	
	ПериодОсвобождения           = НалоговыйУчет.ПериодОсвобожденияОтНалоговПострадавшимОтКоронавируса();
	СледующийПериод              = ПериодОсвобождения.Конец + 1;
	ПониженныйАнтикризисныйТариф = Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства;
	ТарифДляПострадавших         = Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции;
	
	Если Не ЗначениеЗаполнено(ТарифНаНачало) Или ТарифНаНачало = ТарифДляПострадавших Тогда
		// Настройки явно некорректные, гарантировать корректность изменений не можем
		Возврат Ложь;
	КонецЕсли;
	
	КоличествоЗаписейЗаПериод = ДанныеЗаПериод.Количество();
	
	Если КоличествоЗаписейЗаПериод >= 2 Тогда
		
		// Может быть, уже установлен 
		
		Включение = ДанныеЗаПериод[0];
		ВключенКорректно  = (Включение.Период = ПериодОсвобождения.Начало И Включение.ВидТарифа = ТарифДляПострадавших);
		
		Выключение = ДанныеЗаПериод[1];
		ВыключенКорректно = (Выключение.Период = СледующийПериод И Выключение.ВидТарифа <> ТарифДляПострадавших);
		
		Возврат ВключенКорректно И ВыключенКорректно;
		
	ИначеЕсли КоличествоЗаписейЗаПериод = 1 Тогда
		
		// Допускаем единственный вариант - применен пониженный антикризисный тариф, причем с той же даты, что и освобождение
		Если ДанныеЗаПериод[0].Период <> ПериодОсвобождения.Начало
			Или ДанныеЗаПериод[0].ВидТарифа <> ПониженныйАнтикризисныйТариф Тогда
			Возврат Ложь;
		КонецЕсли;
		
		СледующийТариф = ПониженныйАнтикризисныйТариф;
		
	Иначе
		
		СледующийТариф = ТарифНаНачало;
		
	КонецЕсли;
	
	// С начала периода применяется тариф для пострадавших
	Запись = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СоздатьМенеджерЗаписи();
	Запись.Период                   = ПериодОсвобождения.Начало;
	Запись.ГоловнаяОрганизация      = Организация;
	Запись.ВидТарифа                = ТарифДляПострадавших;
	Запись.ОрганизацияИнвалидов     = ОрганизацияИнвалидов;
	Запись.ДатаРегистрацииИзменений = Запись.Период;
	Запись.Записать();
	
	// Потом применяется другой тариф
	Запись = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СоздатьМенеджерЗаписи();
	Запись.Период                   = СледующийПериод;
	Запись.ГоловнаяОрганизация      = Организация;
	Запись.ВидТарифа                = СледующийТариф;
	Запись.ОрганизацияИнвалидов     = ОрганизацияИнвалидов;
	Запись.ДатаРегистрацииИзменений = Запись.Период;
	Запись.Записать();
	
	Возврат Истина;
	
КонецФункции

Функция ВыключитьТарифСтраховыхВзносовДляОтраслейПострадавшихОтКоронавируснойИнфекции(Организация, ТарифНаНачало, ОрганизацияИнвалидов, ДанныеЗаПериод)
	
	ПериодОсвобождения           = НалоговыйУчет.ПериодОсвобожденияОтНалоговПострадавшимОтКоронавируса();
	СледующийПериод              = ПериодОсвобождения.Конец + 1;
	ПониженныйАнтикризисныйТариф = Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства;
	ТарифДляПострадавших         = Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции;
	
	Если ТарифНаНачало <> ТарифДляПострадавших И ДанныеЗаПериод.Найти(ТарифДляПострадавших, "ВидТарифа") = Неопределено Тогда
		// не включен
		Возврат Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТарифНаНачало) Тогда
		// Настройки явно некорректные, гарантировать корректность изменений не можем
		Возврат Ложь;
	КонецЕсли;
	
	КоличествоЗаписейЗаПериод = ДанныеЗаПериод.Количество();
	
	Если КоличествоЗаписейЗаПериод < 2 Тогда
		// включен неверно
		Возврат Ложь;
	КонецЕсли;
		
	Включение = ДанныеЗаПериод[0];
	ВключенКорректно  = (Включение.Период = ПериодОсвобождения.Начало И Включение.ВидТарифа = ТарифДляПострадавших);
	Если Не ВключенКорректно Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выключение = ДанныеЗаПериод[1];
	ВыключенКорректно = (Выключение.Период = СледующийПериод И Выключение.ВидТарифа <> ТарифДляПострадавших);
	Если Не ВыключенКорректно Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// При выключении может быть неопределенность в дате начала действия тарифа, который указан с даты выключения.
	// Он должен действовать с даты включения или с даты выключения?
	
	СледующийТариф = ТарифНаНачало;
	
	Если Выключение.ВидТарифа <> ТарифНаНачало Тогда
		// Есть неопределенность.
		Если Выключение.ВидТарифа = ПониженныйАнтикризисныйТариф Тогда
			// Можем решить неопределенность, ст. 5 Закона от 1 апреля 2020 г. N 102-ФЗ -
			// дата начала применения пониженного тарифа, что и дата освобождения.
			СледующийТариф = ПониженныйАнтикризисныйТариф;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Удалим запись про выключение
	Запись = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СоздатьМенеджерЗаписи();
	Запись.Период                   = СледующийПериод;
	Запись.ГоловнаяОрганизация      = Организация;
	Запись.Удалить();
	
	// Запись про включение может понадобиться, если применяется пониженный антикризисный тариф
	Запись = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СоздатьМенеджерЗаписи();
	Запись.Период                   = ПериодОсвобождения.Начало;
	Запись.ГоловнаяОрганизация      = Организация;
	
	Если СледующийТариф = ТарифНаНачало Тогда
		// Не понадобится
		Запись.Удалить();
	Иначе
		Запись.ВидТарифа                = ПониженныйАнтикризисныйТариф;
		Запись.ОрганизацияИнвалидов     = ОрганизацияИнвалидов;
		Запись.ДатаРегистрацииИзменений = Запись.Период;
		Запись.Записать();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьОкругление() Экспорт
	
	СпособОкругленияПоУмолчанию = Справочники.СпособыОкругленияПриРасчетеЗарплаты.ПоУмолчанию();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыВыплатыЗарплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыВыплатыЗарплаты КАК СпособыВыплатыЗарплаты
	|ГДЕ
	|	СпособыВыплатыЗарплаты.Округление = ЗНАЧЕНИЕ(Справочник.СпособыОкругленияПриРасчетеЗарплаты.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СпособВыплаты = Выборка.Ссылка.ПолучитьОбъект();
		СпособВыплаты.Округление = СпособОкругленияПоУмолчанию;
		СпособВыплаты.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСпособВыплаты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВБанк.СпособВыплаты = ЗНАЧЕНИЕ(Справочник.СпособыВыплатыЗарплаты.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВКассу.Ссылка
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВКассу КАК ВедомостьНаВыплатуЗарплатыВКассу
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВКассу.СпособВыплаты = ЗНАЧЕНИЕ(Справочник.СпособыВыплатыЗарплаты.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ПоУмолчанию();
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.СпособВыплаты = СпособВыплаты;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ВосстановлениеКадровойИнформации

Процедура ОбработатьКадровыеДанныеСотрудников() Экспорт
	
	НачатьТранзакцию();
	Попытка
		ЕстьДанныеДляИсправления = Ложь;
		ЗаполнитьТекущиеКадровыеДанные(ЕстьДанныеДляИсправления);
		ЗаполнитьТекущуюТарифнуюСтавку(ЕстьДанныеДляИсправления);
		ЗаполнитьОрганизацииСотрудников(ЕстьДанныеДляИсправления);
		ЗаполнитьОсновныхСотрудниковФизическихЛиц(ЕстьДанныеДляИсправления);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать кадровые данные
                               |%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаполнитьТекущиеКадровыеДанные(ЕстьДанныеДляИсправления)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТекущиеДанные.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ТекущиеДанные.ТекущаяОрганизация КАК ТекущаяОрганизация,
	|	ТекущиеДанные.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТекущиеДанные.Сотрудник КАК Сотрудник,
	|	ТекущиеДанные.ТекущееПодразделение КАК ТекущееПодразделение,
	|	ТекущиеДанные.ТекущаяДолжность КАК ТекущаяДолжность,
	|	ТекущиеДанные.ДатаУвольнения КАК ДатаУвольнения,
	|	ТекущиеДанные.ОсновноеРабочееМестоВОрганизации КАК ОсновноеРабочееМестоВОрганизации,
	|	ТекущиеДанные.ДатаПриема КАК ДатаПриема,
	|	ТекущиеДанные.ТекущийВидЗанятости КАК ТекущийВидЗанятости,
	|	ТекущиеДанные.ОформленПоТрудовомуДоговору КАК ОформленПоТрудовомуДоговору,
	|	Сотрудники.Ссылка КАК СправочникСотрудник
	|ПОМЕСТИТЬ ВТ_ЗаписиКадры
	|ИЗ
	|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ТекущиеДанные.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|			И ТекущиеДанные.ГоловнаяОрганизация = Сотрудники.ГоловнаяОрганизация
	|ГДЕ
	|	ВЫРАЗИТЬ(ТекущиеДанные.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Записи.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВТ_Записи.ТекущаяОрганизация КАК ТекущаяОрганизация,
	|	ВТ_Записи.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_Записи.Сотрудник КАК Сотрудник,
	|	ВТ_Записи.ТекущееПодразделение КАК ТекущееПодразделение,
	|	ВТ_Записи.ТекущаяДолжность КАК ТекущаяДолжность,
	|	ВТ_Записи.ДатаУвольнения КАК ДатаУвольнения,
	|	ВТ_Записи.ОсновноеРабочееМестоВОрганизации КАК ОсновноеРабочееМестоВОрганизации,
	|	ВТ_Записи.ДатаПриема КАК ДатаПриема,
	|	ВТ_Записи.ТекущийВидЗанятости КАК ТекущийВидЗанятости,
	|	ВТ_Записи.ОформленПоТрудовомуДоговору КАК ОформленПоТрудовомуДоговору,
	|	ВТ_Записи.СправочникСотрудник КАК СправочникСотрудник,
	|	ВЫБОР
	|		КОГДА ТекущиеДанные.Сотрудник ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗаписьСуществует
	|ИЗ
	|	ВТ_ЗаписиКадры КАК ВТ_Записи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеДанные
	|		ПО ВТ_Записи.ФизическоеЛицо = ТекущиеДанные.ФизическоеЛицо
	|			И ВТ_Записи.СправочникСотрудник = ТекущиеДанные.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Записи.ГоловнаяОрганизация,
	|	ВТ_Записи.ТекущаяОрганизация,
	|	ВТ_Записи.ФизическоеЛицо,
	|	ВТ_Записи.Сотрудник,
	|	ВТ_Записи.ТекущееПодразделение,
	|	ВТ_Записи.ТекущаяДолжность,
	|	ВТ_Записи.ДатаУвольнения,
	|	ВТ_Записи.ОсновноеРабочееМестоВОрганизации,
	|	ВТ_Записи.ДатаПриема,
	|	ВТ_Записи.ТекущийВидЗанятости,
	|	ВТ_Записи.ОформленПоТрудовомуДоговору,
	|	ВТ_Записи.СправочникСотрудник,
	|	ВЫБОР
	|		КОГДА ТекущиеДанные.Сотрудник ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаДанных.Количество() <> 0 Тогда
		ЕстьДанныеДляИсправления = Истина;
		ОбновитьЗаписиРегистра(ТаблицаДанных, "ТекущиеКадровыеДанныеСотрудников");
	КонецЕсли;
	КадровыйУчет.ИсключитьПовторениеЗаписейТекущихДанныхСотрудников("ТекущиеКадровыеДанныеСотрудников", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьТекущуюТарифнуюСтавку(ЕстьДанныеДляИсправления)
	
	Если Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить() Тогда
		Набор = РегистрыСведений.ТекущаяТарифнаяСтавкаСотрудников.СоздатьНаборЗаписей();
		Набор.Очистить();
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущиеДанные.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ТекущиеДанные.ТекущаяОрганизация КАК ТекущаяОрганизация,
	|	ТекущиеДанные.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТекущиеДанные.Сотрудник КАК Сотрудник,
	|	ТекущиеДанные.ТекущаяТарифнаяСтавка КАК ТекущаяТарифнаяСтавка,
	|	ТекущиеДанные.ТекущийСпособРасчетаАванса КАК ТекущийСпособРасчетаАванса,
	|	ТекущиеДанные.ТекущийАванс КАК ТекущийАванс,
	|	Сотрудники.Ссылка КАК СправочникСотрудник
	|ПОМЕСТИТЬ ВТ_ЗаписиТариф
	|ИЗ
	|	РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущиеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ТекущиеДанные.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|			И ТекущиеДанные.ГоловнаяОрганизация = Сотрудники.ГоловнаяОрганизация
	|ГДЕ
	|	ВЫРАЗИТЬ(ТекущиеДанные.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Записи.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВТ_Записи.ТекущаяОрганизация КАК ТекущаяОрганизация,
	|	ВТ_Записи.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_Записи.Сотрудник КАК Сотрудник,
	|	ВТ_Записи.ТекущаяТарифнаяСтавка КАК ТекущаяТарифнаяСтавка,
	|	ВТ_Записи.ТекущийСпособРасчетаАванса КАК ТекущийСпособРасчетаАванса,
	|	ВТ_Записи.ТекущийАванс КАК ТекущийАванс,
	|	ВТ_Записи.СправочникСотрудник КАК СправочникСотрудник,
	|	ВЫБОР
	|		КОГДА ТекущиеДанные.Сотрудник ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗаписьСуществует
	|ИЗ
	|	ВТ_ЗаписиТариф КАК ВТ_Записи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущиеДанные
	|		ПО ВТ_Записи.ФизическоеЛицо = ТекущиеДанные.ФизическоеЛицо
	|			И ВТ_Записи.СправочникСотрудник = ТекущиеДанные.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Записи.ГоловнаяОрганизация,
	|	ВТ_Записи.ТекущаяОрганизация,
	|	ВТ_Записи.ФизическоеЛицо,
	|	ВТ_Записи.Сотрудник,
	|	ВТ_Записи.ТекущаяТарифнаяСтавка,
	|	ВТ_Записи.ТекущийСпособРасчетаАванса,
	|	ВТ_Записи.ТекущийАванс,
	|	ВТ_Записи.СправочникСотрудник,
	|	ВЫБОР
	|		КОГДА ТекущиеДанные.Сотрудник ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаДанных.Количество() <> 0 Тогда
		ЕстьДанныеДляИсправления = Истина;
		ОбновитьЗаписиРегистра(ТаблицаДанных, "ТекущаяТарифнаяСтавкаСотрудников");
		КадровыйУчет.ИсключитьПовторениеЗаписейТекущихДанныхСотрудников("ТекущаяТарифнаяСтавкаСотрудников", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьЗаписиРегистра(ТаблицаДанных, ИмяРегистра)
	
	НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		
		НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(СтрокаТаблицы.ГоловнаяОрганизация);
		НаборЗаписей.Отбор.ТекущаяОрганизация.Установить(СтрокаТаблицы.ТекущаяОрганизация);
		НаборЗаписей.Отбор.ФизическоеЛицо.Установить(СтрокаТаблицы.ФизическоеЛицо);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТаблицы.Сотрудник);
		НаборЗаписей.Очистить();
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СправочникСотрудник)
			ИЛИ СтрокаТаблицы.ЗаписьСуществует Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(СтрокаТаблицы.ГоловнаяОрганизация);
		НаборЗаписей.Отбор.ТекущаяОрганизация.Установить(СтрокаТаблицы.ТекущаяОрганизация);
		НаборЗаписей.Отбор.ФизическоеЛицо.Установить(СтрокаТаблицы.ФизическоеЛицо);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТаблицы.СправочникСотрудник);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицы);
			НоваяЗапись.Сотрудник = СтрокаТаблицы.СправочникСотрудник;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОрганизацииСотрудников(ЕстьДанныеДляИсправления)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОрганизацииВКоторыхРаботалиСотрудники.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ОрганизацииВКоторыхРаботалиСотрудники КАК ОрганизацииВКоторыхРаботалиСотрудники
		|ГДЕ
		|	(ВЫРАЗИТЬ(ОрганизацииВКоторыхРаботалиСотрудники.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо ЕСТЬ NULL
		|			ИЛИ ОрганизацииВКоторыхРаботалиСотрудники.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьДанныеДляИсправления = Истина;
	
	Набор = РегистрыСведений.ОрганизацииВКоторыхРаботалиСотрудники.СоздатьНаборЗаписей();
	Набор.Очистить();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
		|	КадроваяИсторияСотрудников.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.Ссылка,
		|	Сотрудники.ГоловнаяОрганизация
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники";
	Иначе
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		КадровыйУчет.СоздатьВТТекущиеКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, Ложь);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
		|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация КАК Организация
		|ИЗ
		|	ВТТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.Ссылка,
		|	Сотрудники.ГоловнаяОрганизация
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
		
	КонецЕсли;
	
	Набор.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
	
КонецПроцедуры

Процедура ЗаполнитьОсновныхСотрудниковФизическихЛиц(ЕстьДанныеДляИсправления)
	
	Запрос = Новый Запрос;
	Если НЕ ЕстьДанныеДляИсправления Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		| ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		| РегистрСведений.ОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
		|ГДЕ
		| ЕСТЬNULL(ВЫРАЗИТЬ(ОсновныеСотрудникиФизическихЛиц.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо, НЕОПРЕДЕЛЕНО) <> ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо";
		
		РезультатЗапроса = Запрос.Выполнить();
		ЕстьДанныеДляИсправления = НЕ РезультатЗапроса.Пустой();
	КонецЕсли;
	Если НЕ ЕстьДанныеДляИсправления Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицоТекущиеДанные,
		|	ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицоСотрудника
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
		|		ПО ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо
		|ГДЕ
		|	ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо,
		|	ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.ОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо ЕСТЬ NULL";
		
		РезультатЗапроса = Запрос.Выполнить();
		ЕстьДанныеДляИсправления = НЕ РезультатЗапроса.Пустой();
	КонецЕсли;
	
	Если НЕ ЕстьДанныеДляИсправления Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ОсновныеСотрудникиФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Очистить();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МаксимальнаяДата", ЗарплатаКадрыПериодическиеРегистры.МаксимальнаяДата());
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
			|	Сотрудники.Ссылка КАК Сотрудник
			|ПОМЕСТИТЬ ВТСотрудникиПериоды
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники";
		
		Запрос.Выполнить();
		
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистра(
			Метаданные.РегистрыСведений.ВидыЗанятостиСотрудников.Имя,
			Запрос.МенеджерВременныхТаблиц,
			Ложь,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
				"ВТСотрудникиПериоды", "Сотрудник"));
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
			|	МИНИМУМ(Сотрудники.Ссылка) КАК Сотрудник,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
			|	&МаксимальнаяДата КАК ДатаОкончания
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыЗанятостиСотрудников КАК ВидыЗанятостиСотрудников
			|		ПО Сотрудники.ГоловнаяОрганизация = ВидыЗанятостиСотрудников.ГоловнаяОрганизация
			|			И Сотрудники.ФизическоеЛицо = ВидыЗанятостиСотрудников.ФизическоеЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
			|		ПО Сотрудники.ГоловнаяОрганизация = ОсновныеСотрудникиФизическихЛиц.ГоловнаяОрганизация
			|			И Сотрудники.ФизическоеЛицо = ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо
			|ГДЕ
			|	ВидыЗанятостиСотрудников.ФизическоеЛицо ЕСТЬ NULL
			|	И ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо ЕСТЬ NULL
			|
			|СГРУППИРОВАТЬ ПО
			|	Сотрудники.ГоловнаяОрганизация,
			|	Сотрудники.ФизическоеЛицо";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				НаборЗаписей = РегистрыСведений.ОсновныеСотрудникиФизическихЛиц.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
				НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(Выборка.ГоловнаяОрганизация);
				
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				
			КонецЦикла;
			
		КонецЕсли;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВидыЗанятостиСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ВидыЗанятостиСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
			|	МАКСИМУМ(ВидыЗанятостиСотрудников.Сотрудник) КАК Сотрудник,
			|	ВЫБОР
			|		КОГДА МАКСИМУМ(ВидыЗанятостиСотрудниковПредыдущие.Период) ЕСТЬ NULL
			|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
			|		ИНАЧЕ ВидыЗанятостиСотрудников.Период
			|	КОНЕЦ КАК ДатаНачала,
			|	ВЫБОР
			|		КОГДА МИНИМУМ(ВидыЗанятостиСотрудниковСледующие.Период) ЕСТЬ NULL
			|			ТОГДА &МаксимальнаяДата
			|		ИНАЧЕ ДОБАВИТЬКДАТЕ(МИНИМУМ(ВидыЗанятостиСотрудниковСледующие.Период), ДЕНЬ, -1)
			|	КОНЕЦ КАК ДатаОкончания
			|ИЗ
			|	ВТВидыЗанятостиСотрудников КАК ВидыЗанятостиСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыЗанятостиСотрудников КАК ВидыЗанятостиСотрудниковПредыдущие
			|		ПО ВидыЗанятостиСотрудников.ГоловнаяОрганизация = ВидыЗанятостиСотрудниковПредыдущие.ГоловнаяОрганизация
			|			И ВидыЗанятостиСотрудников.ФизическоеЛицо = ВидыЗанятостиСотрудниковПредыдущие.ФизическоеЛицо
			|			И (ВидыЗанятостиСотрудниковПредыдущие.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство)))
			|			И ВидыЗанятостиСотрудников.Период > ВидыЗанятостиСотрудниковПредыдущие.Период
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыЗанятостиСотрудников КАК ВидыЗанятостиСотрудниковСледующие
			|		ПО ВидыЗанятостиСотрудников.ГоловнаяОрганизация = ВидыЗанятостиСотрудниковСледующие.ГоловнаяОрганизация
			|			И ВидыЗанятостиСотрудников.ФизическоеЛицо = ВидыЗанятостиСотрудниковСледующие.ФизическоеЛицо
			|			И (ВидыЗанятостиСотрудниковСледующие.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство)))
			|			И ВидыЗанятостиСотрудников.Период < ВидыЗанятостиСотрудниковСледующие.Период
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
			|		ПО ВидыЗанятостиСотрудников.ГоловнаяОрганизация = ОсновныеСотрудникиФизическихЛиц.ГоловнаяОрганизация
			|			И ВидыЗанятостиСотрудников.ФизическоеЛицо = ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо
			|ГДЕ
			|	ВидыЗанятостиСотрудников.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
			|	И ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо ЕСТЬ NULL
			|
			|СГРУППИРОВАТЬ ПО
			|	ВидыЗанятостиСотрудников.Период,
			|	ВидыЗанятостиСотрудников.ГоловнаяОрганизация,
			|	ВидыЗанятостиСотрудников.ФизическоеЛицо
			|
			|УПОРЯДОЧИТЬ ПО
			|	ФизическоеЛицо,
			|	ГоловнаяОрганизация,
			|	ДатаНачала";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
				
				Пока Выборка.СледующийПоЗначениюПоля("ГоловнаяОрганизация") Цикл
					
					НаборЗаписей = РегистрыСведений.ОсновныеСотрудникиФизическихЛиц.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
					НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(Выборка.ГоловнаяОрганизация);
					
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
					КонецЦикла;
					
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		КадровыйУчет.СоздатьВТТекущиеКадровыеДанныеСотрудников(
			Запрос.МенеджерВременныхТаблиц, Ложь);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
			|	МИНИМУМ(ТекущиеКадровыеДанныеСотрудников.Сотрудник) КАК Сотрудник
			|ПОМЕСТИТЬ ВТРаботающиеФизическиеЛица
			|ИЗ
			|	ВТТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|ГДЕ
			|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
			|
			|СГРУППИРОВАТЬ ПО
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
			|	МИНИМУМ(ТекущиеКадровыеДанныеСотрудников.Сотрудник) КАК Сотрудник
			|ПОМЕСТИТЬ ВТРаботающиеФизическиеЛицаСовместители
			|ИЗ
			|	ВТТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаботающиеФизическиеЛица КАК РаботающиеФизическиеЛица
			|		ПО ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = РаботающиеФизическиеЛица.ФизическоеЛицо
			|			И ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = РаботающиеФизическиеЛица.ГоловнаяОрганизация
			|ГДЕ
			|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ВнутреннееСовместительство)
			|	И РаботающиеФизическиеЛица.ФизическоеЛицо ЕСТЬ NULL
			|
			|СГРУППИРОВАТЬ ПО
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РаботающиеФизическиеЛица.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	РаботающиеФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
			|	РаботающиеФизическиеЛица.Сотрудник КАК Сотрудник,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
			|	&МаксимальнаяДата КАК ДатаОкончания
			|ИЗ
			|	ВТРаботающиеФизическиеЛица КАК РаботающиеФизическиеЛица
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РаботающиеФизическиеЛица.ГоловнаяОрганизация,
			|	РаботающиеФизическиеЛица.ФизическоеЛицо,
			|	РаботающиеФизическиеЛица.Сотрудник,
			|	ДАТАВРЕМЯ(1, 1, 1),
			|	&МаксимальнаяДата
			|ИЗ
			|	ВТРаботающиеФизическиеЛицаСовместители КАК РаботающиеФизическиеЛица
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо,
			|	МИНИМУМ(ТекущиеКадровыеДанныеСотрудников.Сотрудник),
			|	ДАТАВРЕМЯ(1, 1, 1),
			|	&МаксимальнаяДата
			|ИЗ
			|	ВТТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаботающиеФизическиеЛица КАК РаботающиеФизическиеЛица
			|		ПО ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = РаботающиеФизическиеЛица.ФизическоеЛицо
			|			И ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = РаботающиеФизическиеЛица.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаботающиеФизическиеЛицаСовместители КАК РаботающиеФизическиеЛицаСовместители
			|		ПО ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = РаботающиеФизическиеЛицаСовместители.ФизическоеЛицо
			|			И ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = РаботающиеФизическиеЛицаСовместители.ГоловнаяОрганизация
			|ГДЕ
			|	РаботающиеФизическиеЛица.ФизическоеЛицо ЕСТЬ NULL
			|	И РаботающиеФизическиеЛицаСовместители.ФизическоеЛицо ЕСТЬ NULL
			|
			|СГРУППИРОВАТЬ ПО
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				НаборЗаписей = РегистрыСведений.ОсновныеСотрудникиФизическихЛиц.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
				НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(Выборка.ГоловнаяОрганизация);
				
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСпособРасчетаАванса() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриемНаРаботу.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|ГДЕ
	|	ПриемНаРаботу.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КадровыйПеревод.Ссылка
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|ГДЕ
	|	КадровыйПеревод.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СпособРасчетаАванса = Перечисления.СпособыРасчетаАванса.ПроцентомОтТарифа;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.СпособРасчетаАванса = СпособРасчетаАванса;
		ДокументОбъект.Аванс               = 40;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРолиСотрудников() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.Ссылка КАК Сотрудник
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РолиСотрудников КАК РолиСотрудников
	|		ПО Сотрудники.Ссылка = РолиСотрудников.Сотрудник
	|ГДЕ
	|	РолиСотрудников.Сотрудник ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.РолиСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Сотрудник      = Выборка.Сотрудник;
		НоваяЗапись.РольСотрудника = Перечисления.РолиСотрудников.Работник;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьКадровыйУчетВОсновномИнтерфейсе() Экспорт
	
	Если НЕ СервисЭлектронныхТрудовыхКнижек.ИспользуетсяСервисЭлектронныхТрудовыхКнижек() Тогда
		Константы.ИспользоватьКадровыйУчетОсновнойИнтерфейс.Установить(Константы.ИспользоватьКадровыйУчет.Получить());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ВосстановитьРегистрациюВНалоговомОргане() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК СтруктурнаяЕдиница,
	|	Организации.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОрганеИстория
	|ПОМЕСТИТЬ ВТ_Регистрации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
	|		ПО Организации.Ссылка = ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница
	|ГДЕ
	|	Организации.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Организации.Ссылка,
	|	Организации.РегистрацияВНалоговомОргане,
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка,
	|	ВЫБОР
	|		КОГДА ПодразделенияОрганизаций.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.Пустаяссылка)
	|			ТОГДА Организации.РегистрацияВНалоговомОргане
	|		ИНАЧЕ ПодразделенияОрганизаций.РегистрацияВНалоговомОргане
	|	КОНЕЦ,
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ПодразделенияОрганизаций.Владелец = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
	|		ПО ПодразделенияОрганизаций.Ссылка = ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница
	|ГДЕ
	|	(ПодразделенияОрганизаций.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ИЛИ Организации.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане,
	|	ПодразделенияОрганизаций.Ссылка,
	|	ВЫБОР
	|		КОГДА ПодразделенияОрганизаций.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.Пустаяссылка)
	|			ТОГДА Организации.РегистрацияВНалоговомОргане
	|		ИНАЧЕ ПодразделенияОрганизаций.РегистрацияВНалоговомОргане
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВТ_Регистрации.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|ГДЕ
	|	ВТ_Регистрации.РегистрацияВНалоговомОрганеИстория ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.РегистрацияВНалоговомОргане,
	|	ВТ_Регистрации.СтруктурнаяЕдиница";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	ДатаЗаписи = РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.ДатаОтсчетаПериодическихСведений();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			НаборЗаписей = РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Выборка.СтруктурнаяЕдиница);
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Период                      = ДатаЗаписи;
			НоваяЗапись.СтруктурнаяЕдиница          = Выборка.СтруктурнаяЕдиница;
			НоваяЗапись.РегистрацияВНалоговомОргане = Выборка.РегистрацияВНалоговомОргане;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не удалось восстановить историю регистраций в налоговом органе
			|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			Выборка.СтруктурнаяЕдиница,
			ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьФункционалВедетсяУчетЗарплатыКадров() Экспорт
	
	Если Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить()
		ИЛИ Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Получить() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		МенеджерЗначения = Константы.ВедетсяУчетЗарплатыКадров.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Истина;
		МенеджерЗначения.ОбменДанными.Загрузка = Истина;
		МенеджерЗначения.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
	Иначе
		ОтключитьПодсистемуУчетаЗарплатыИКадров();
	КонецЕсли;

КонецПроцедуры

Процедура ВключитьРасчетЗарплатыДляНебольшихОрганизаций() Экспорт

	Если НЕ ТарификацияБПВызовСервераПовтИсп.РазрешеноИспользованиеВнешнейПрограммыУчетаЗарплаты() Тогда
		Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Установить(Ложь);
	КонецЕсли;

	Если НЕ Константы.РасчетЗарплатыДляНебольшихОрганизаций.Получить() Тогда
		Константы.РасчетЗарплатыДляНебольшихОрганизаций.Установить(
			Константы.ИспользоватьНачислениеЗарплаты.Получить()
			И РасчетЗарплатыДляНебольшихОрганизацийВозможен());
	КонецЕсли;
	
	Константы.РассчитыватьДокументыПриРедактировании.Установить(Истина);
	
КонецПроцедуры

Процедура НастроитьВоинскийУчет(Значение, Отказ) Экспорт
	
	НастройкиВоинскогоУчета = РегистрыСведений.НастройкиВоинскогоУчета.СоздатьНаборЗаписей();
	НастройкиВоинскогоУчета.Прочитать();
	Если НастройкиВоинскогоУчета.Количество() = 0 Тогда
		НастройкаВоинскогоУчета = НастройкиВоинскогоУчета.Добавить();
	Иначе
		НастройкаВоинскогоУчета = НастройкиВоинскогоУчета[0];
	КонецЕсли;
	
	НастройкаВоинскогоУчета.ИспользоватьВоинскийУчет = Значение;
	НастройкаВоинскогоУчета.ИспользоватьБронированиеГраждан = Значение;
	НастройкаВоинскогоУчета.УчитыватьВнешнихСовместителей = Значение;
	НастройкаВоинскогоУчета.ЗаполнятьТолькоАдресПоПрописке = Ложь; // управляется
	НастройкаВоинскогоУчета.ИспользуетсяТрудЛетноПодъемногоСостава = Ложь; // не поддерживается
	НастройкаВоинскогоУчета.ИспользуетсяТрудПлавсостава = Ложь; // не поддерживается
	
	НастройкиВоинскогоУчета.Записать();
	
КонецПроцедуры

Процедура УточнитьКодДоходаНДФЛКомпенсацияОтпуска() Экспорт
	
	ПланыВидовРасчета.Начисления.УточнитьКодДоходаНДФЛКомпенсацияОтпуска();
	
КонецПроцедуры

Процедура УточнитьКодДоходаНДФЛОтпускаАУСН() Экспорт
	
	ПланыВидовРасчета.Начисления.УточнитьКодДоходаНДФЛОтпускаАУСН();
	
КонецПроцедуры

Процедура УточнитьКодДоходаНДФЛПремииАУСН() Экспорт
	
	ПланыВидовРасчета.Начисления.УточнитьКодДоходаНДФЛПремииАУСН();
	
КонецПроцедуры

Процедура ВосстановитьСтатьиРасходовЗарплата() Экспорт
	
	ЗарплатаКадры.СтатьиРасходовЗарплатаНачальноеЗаполнениеПоСпособамРасчетов();
	ОтражениеЗарплатыВБухучете.ЗаполнитьСтатьюРасходовВДокументеОтражениеЗарплатыВБухучете();
	
КонецПроцедуры

Процедура ИсправитьФормироватьРезервОтпусковНУВНастройкахРезервовОтпусков() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиРасчетаРезервовОтпусков.Организация КАК Организация,
	|	НастройкиРасчетаРезервовОтпусков.Период КАК Период
	|ИЗ
	|	РегистрСведений.НастройкиРасчетаРезервовОтпусков КАК НастройкиРасчетаРезервовОтпусков
	|ГДЕ
	|	НастройкиРасчетаРезервовОтпусков.ФормироватьРезервОтпусковНУ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Выборка.Организация, Выборка.Период) Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			НаборЗаписей = РегистрыСведений.НастройкиРасчетаРезервовОтпусков.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Прочитать();
			ИсправляемаяЗапись = НаборЗаписей[0];
			ИсправляемаяЗапись.ФормироватьРезервОтпусковНУ = Ложь;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не удалось скорректировать настройку резервов отпусков для организации %1 за период % 2
				|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Выборка.Организация, Выборка.Период, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				Выборка.Организация,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьОрганизационнуюСтруктуруПредприятий() Экспорт
	
	Если СервисЭлектронныхТрудовыхКнижек.ИспользуетсяСервисЭлектронныхТрудовыхКнижек() Тогда
		СервисЭлектронныхТрудовыхКнижек.НастроитьОрганизационнуюСтруктуруПредприятия();
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка КАК Ссылка,
		|	ПодразделенияОрганизаций.Владелец КАК Владелец
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций";
		
		Результат = Запрос.ВыполнитьПакет();
		ТаблицаОрганизаций   = Результат[0].Выгрузить();
		ТаблицаПодразделений = Результат[1].Выгрузить();
		
		Для Каждого СтрокаТаблицы ИЗ ТаблицаОрганизаций Цикл
			
			ИспользуетсяКадровыйУчет = Истина;
			
			МассивПодразделений = ТаблицаПодразделений.НайтиСтроки(Новый Структура("Владелец", СтрокаТаблицы.Организация));
			КоличествоПодразделений = МассивПодразделений.Количество() ;
			Если КоличествоПодразделений =0 Тогда
				ИспользуетсяКадровыйУчет = Ложь;
			ИначеЕсли КоличествоПодразделений = 1 Тогда
				РеквизитыПодразделения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МассивПодразделений[0].Ссылка, "Наименование,НаименованиеДляЗаписейОТрудовойДеятельности");
				Если РеквизитыПодразделения.Наименование = "Основное подразделение"
					И НЕ ЗначениеЗаполнено(РеквизитыПодразделения.НаименованиеДляЗаписейОТрудовойДеятельности) Тогда
					ИспользуетсяКадровыйУчет = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			ОрганизацияОбъект = СтрокаТаблицы.Организация.ПолучитьОбъект();
			ОрганизацияОбъект.НеЗаполнятьПодразделенияВМероприятияхТрудовойДеятельности = НЕ ИспользуетсяКадровыйУчет;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОрганизацияОбъект);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьГрафикиРаботыОрганизаций() Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.СоздатьГрафикиРаботыОрганизаций();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересмотретьСрокиУплатыНДФЛ() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УчетнаяПолитикаПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаПоНДФЛ КАК УчетнаяПолитикаПоНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПродленияСроковНалоговОтчетов КАК НастройкиПродленияСроковНалоговОтчетов
	|		ПО УчетнаяПолитикаПоНДФЛ.ГоловнаяОрганизация = НастройкиПродленияСроковНалоговОтчетов.Организация.ГоловнаяОрганизация
	|ГДЕ
	|	НастройкиПродленияСроковНалоговОтчетов.РаботаетВНерабочиеДни
	|	И (НЕ УчетнаяПолитикаПоНДФЛ.ИгнорироватьДниС30МартаПо3Апреля
	|			ИЛИ НЕ УчетнаяПолитикаПоНДФЛ.ИгнорироватьДниС4По30Апреля
	|			ИЛИ НЕ УчетнаяПолитикаПоНДФЛ.ИгнорироватьДниС6По8Мая)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.УчетнаяПолитикаПоНДФЛ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(Выборка.ГоловнаяОрганизация);
		НаборЗаписей.Прочитать();
		ЗаписьУчетнойПолитики = НаборЗаписей[0];
		ЗаписьУчетнойПолитики.ИгнорироватьДниС30МартаПо3Апреля = Истина;
		ЗаписьУчетнойПолитики.ИгнорироватьДниС4По30Апреля      = Истина;
		ЗаписьУчетнойПолитики.ИгнорироватьДниС6По8Мая          = Истина;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
		УчетНДФЛ.ПересмотретьСрокиУплаты(Выборка.ГоловнаяОрганизация, '20200325', '20200508');
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьПорядокЗаполненияЭТК() Экспорт
	
	НаборЗаписей = РегистрыСведений.НастройкиЭлектронныхТрудовыхКнижек.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		ТекущаяЗапись = НаборЗаписей.Добавить();
	Иначе
		ТекущаяЗапись = НаборЗаписей[0];
	КонецЕсли;
	ТекущаяЗапись.ЗаполнятьСТДРВсемиМероприятиямиДо2020Года = Истина;
	ТекущаяЗапись.ИспользоватьДляМероприятийПриемПереводУвольнениеДваДокументаОснования = Истина;
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
КонецПроцедуры

Процедура ОбновитьСоставКонтактнойИнформацииФизическогоЛица() Экспорт
	
	// ТелефонМобильныйФизическиеЛица
	СвойстваВидаКИ = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Телефон);
	СвойстваВидаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица;
	СвойстваВидаКИ.МожноИзменятьСпособРедактирования = Истина;
	СвойстваВидаКИ.РедактированиеТолькоВДиалоге = Ложь;
	СвойстваВидаКИ.ОбязательноеЗаполнение = Ложь;
	УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(СвойстваВидаКИ);
	
КонецПроцедуры

Процедура ЗаполнитьВидДеятельностиДляНалоговогоУчетаЗатрат() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпособыОтраженияЗарплатыВБухУчете.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтраженияЗарплатыВБухУчете
	|ГДЕ
	|	СпособыОтраженияЗарплатыВБухУчете.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|	И НЕ СпособыОтраженияЗарплатыВБухУчете.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПрименяетсяОсобыйПорядокНалогообложения   = Константы.ИспользуетсяЕНВД.Получить()
		Или Константы.ИспользуетсяУСНПатент.Получить();
	ПрименяетсяОсновнаяСистемаНалогообложения = Константы.ИспользуетсяОСНО.Получить()
		Или Константы.ИспользуетсяУСН.Получить();
	
	Если ПрименяетсяОсобыйПорядокНалогообложения И НЕ ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения;
	ИначеЕсли ПрименяетсяОсобыйПорядокНалогообложения И ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты;
	Иначе
		ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СпособУчетаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СпособУчетаОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = ВидДеятельностиДляНалоговогоУчетаЗатрат;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СпособУчетаОбъект);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВключитьАвтообменПоСЭДОФСС() Экспорт
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ВключитьОбменСФССПриНеобходимости();
	
КонецПроцедуры

Процедура ВключитьАналитикуНаСчетахУдержаний() Экспорт
	
	ИсправитьСостояниеНастроекПодсистемыЗарплатаИКадры();
	ПараметрыУчета = ОбщегоНазначенияБП.ОпределитьПараметрыУчета();
	ПараметрыУчета.Вставить("ВедетсяУчетЗарплатыКадров", ?(Константы.ВедетсяУчетЗарплатыКадров.Получить(), 1, 0));
	ОбщегоНазначенияБП.ПрименитьПараметрыУчета(ПараметрыУчета, Истина);
	
КонецПроцедуры

Процедура НастроитьДатыВыплатыЗарплаты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиЗарплатаКадры.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_КорректныеНастройки
	|ИЗ
	|	РегистрСведений.НастройкиЗарплатаКадры КАК НастройкиЗарплатаКадры
	|ГДЕ
	|	НастройкиЗарплатаКадры.ДатаВыплатыАвансаНеПозжеЧем <> 0
	|			И (НастройкиЗарплатаКадры.ДатаВыплатыЗарплатыНеПозжеЧем <> 0
	|				ИЛИ НастройкиЗарплатаКадры.ВыплачиватьЗарплатуВПоследнийДеньМесяца)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_КорректныеНастройки.Организация
	|				ИЗ
	|					ВТ_КорректныеНастройки)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ПорядокВыплатыЗарплатыПоУмолчанию = ПорядокВыплатыЗарплатыПоУмолчанию();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.НастройкиЗарплатаКадры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			ТекущаяЗапись = НаборЗаписей.Добавить();
			ТекущаяЗапись.Организация = Выборка.Организация;
		Иначе
			ТекущаяЗапись = НаборЗаписей[0];
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ТекущаяЗапись, ПорядокВыплатыЗарплатыПоУмолчанию);
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьНачисленияАУСН() Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ПрименяетсяАУСН") Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			МодульРасчетЗарплатыДляНебольшихОрганизаций.ПодготовитьНачисленияАУСН();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВключитьРасчетЗаПервуюПоловинуМесяца() Экспорт
	
	Константы.ИспользоватьРасчетПервойПоловиныМесяца.Установить(Истина);
	
КонецПроцедуры

Функция ИспользоватьРасчетПервойПоловиныМесяца(Организация, Период) Экспорт
	
	Возврат УдерживаетсяНДФЛПоАвансам(Период)
		Или ПолучитьФункциональнуюОпцию("ИспользоватьРасчетПервойПоловиныМесяца", Новый Структура("Организация", Организация))
		Или УчетнаяПолитика.ПрименяетсяАУСН(Организация, Период);
	
КонецФункции

Процедура УстановитьСтавкуВзносовОтНесчастныхСлучаевдляАУСН() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев КАК СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев
	|		ПО НастройкиСистемыНалогообложения.Организация = СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Организация
	|			И НастройкиСистемыНалогообложения.Период = СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период
	|ГДЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяАУСН
	|	И СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Ставка <> 0";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			НаборЗаписей = РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				ТекущаяЗапись = НаборЗаписей.Добавить();
			Иначе
				ТекущаяЗапись = НаборЗаписей[0];
			КонецЕсли;
			ТекущаяЗапись.Организация = Выборка.Организация;
			ТекущаяЗапись.Ставка      = НулеваяСтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев();
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не удалось скорректировать ставку взносов от несчастных случаев для организации %1 за период % 2
				|%3'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Выборка.Организация, Выборка.Период, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				Выборка.Организация,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура - обработчик обновления на версию 3.0.127.33.
// Дозаполняет план видов расчета начислениями, не подлежащими отражению в расходах: мат. помощь, премия из прибыли, прочая выплата.
//
Процедура СоздатьНачисленияНеВключаемыеВРасходы() Экспорт
	
	ПланыВидовРасчета.Начисления.СоздатьНачисленияНеВключаемыеВРасходы();
	
КонецПроцедуры

// Процедура - обработчик обновления на версию 3.0.127.42.
// Заполняет аналитику отражения в учете для добавленнных начислений, не включаемых в расходы - см. СоздатьНачисленияНеВключаемыеВРасходы.
// Важно: необходимо вызывать после обработчика СоздатьНачисленияНеВключаемыеВРасходы.
//
Процедура ЗаполнитьОтражениеВУчетеНачисленийНеВключаемыхВРасходы() Экспорт
	
	НаименованияНачислений = Новый Массив;
	НаименованияНачислений.Добавить(НаименованиеНачисленияМатериальнаяПомощь());
	НаименованияНачислений.Добавить(НаименованиеНачисленияПремияЗаСчетПрибыли());
	НаименованияНачислений.Добавить(НаименованиеНачисленияПрочаяВыплатаНеПредусмотреннаяДоговором());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименования", НаименованияНачислений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Начисление,
	|	Начисления.Наименование КАК Наименование
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Наименование В(&Наименования)
	|	И Начисления.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|	И НЕ Начисления.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		// Отражение начисления уже настроено.
		Возврат;
	КонецЕсли;
	
	СпособыОтраженияДополнительныхНачислений = НастроитьСпособыОтраженияДополнительныхНачислений();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Наименование = НаименованиеНачисленияПремияЗаСчетПрибыли() Тогда
			СпособОтражения = СпособыОтраженияДополнительныхНачислений.НачисленияИзПрибыли;
		Иначе
			СпособОтражения = СпособыОтраженияДополнительныхНачислений.НачисленияНеВключаемыеВРасходы;
		КонецЕсли;
		
		НачислениеОбъект = Выборка.Начисление.ПолучитьОбъект();
		НачислениеОбъект.СпособОтраженияЗарплатыВБухучете = СпособОтражения;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НачислениеОбъект);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВидДоходаИсполнительногоПроизводства() Экспорт
	
	ПланыВидовРасчета.Начисления.ЗаполнитьВидДоходаИсполнительногоПроизводства();
	ПланыВидовРасчета.Начисления.ЗаполнитьВидДоходаИсполнительногоПроизводства2022();
	
КонецПроцедуры

Процедура ПеренестиОснованиеПодписиРуководителяКадровыхДокументов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КадровыйПеревод.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|ГДЕ
	|	КадровыйПеревод.ОснованиеПодписиРуководителя <> КадровыйПеревод.УдалитьОснованиеПодписиРуководителя
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтпускБезСохраненияОплаты.Ссылка
	|ИЗ
	|	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	|ГДЕ
	|	ОтпускБезСохраненияОплаты.ОснованиеПодписиРуководителя <> ОтпускБезСохраненияОплаты.УдалитьОснованиеПодписиРуководителя";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		// Отражение начисления уже настроено.
		Возврат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ОснованиеПодписиРуководителя = ДокументОбъект.УдалитьОснованиеПодписиРуководителя;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
	КонецЦикла;

КонецПроцедуры

// Обработчик обновления. Включает функциональную опцию "ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ"
// если в базе есть проведенные документы "АвансовыйПлатежИностранцаПоНДФЛ"
//
// Параметры:
//  ПараметрыОбработчика - Структура:
//    В структуру можно добавить произвольное количество свойств произвольных типов,
//    значения которых будут автоматически запоминаться между вызовами процедуры - обработчика отложенного обновления.
//
Процедура ВключитьОпциюАвансовИностранцевПоНДФЛ(ПараметрыОбработчика) Экспорт
	
	Если Константы.ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ.ЕстьПлатежи()
		И Не ПолучитьФункциональнуюОпцию("ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ") Тогда
		Константы.ИспользуютсяАвансовыеПлатежиИностранцевПоНДФЛ.УстановитьЗначениеОпцииАвансовыеПлатежиИностранцев(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьВидыКонтактнойИнформации() Экспорт
	
	Отбор = ОбновлениеИнформационнойБазы.ПараметрыОбновленияПредопределенныхЭлементов();
	
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"ТелефонМобильныйФизическиеЛица"));
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"EMailФизическиеЛица"));
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"ТелефонДомашнийФизическиеЛица"));
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"ТелефонРабочийФизическиеЛица"));
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"АдресПоПропискеФизическиеЛица"));
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"АдресМестаПроживанияФизическиеЛица"));
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"АдресЗаПределамиРФФизическиеЛица"));
	Отбор.Элементы.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(
		"АдресДляИнформированияФизическиеЛица"));
	
	ОбновлениеИнформационнойБазы.ОбновитьПредопределенныеЭлементы(Метаданные.Справочники.ВидыКонтактнойИнформации, Отбор);
	
КонецПроцедуры

Процедура ИсправитьВидыКонтактнойИнформацииФизическогоЛица(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Родитель",
		ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.СправочникФизическиеЛица"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка,
	|	ВидыКонтактнойИнформации.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.ЭтоГруппа = ЛОЖЬ
	|	И НЕ ВидыКонтактнойИнформации.ОтображатьВсегда
	|	И ВидыКонтактнойИнформации.Родитель = &Родитель";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	МассивСсылок = Новый Массив;
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Если Не СтрНачинаетсяС(СтрокаТаблицы.ИмяПредопределенныхДанных, "Удалить") Тогда
			МассивСсылок.Добавить(СтрокаТаблицы.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСсылок.Количество() > 0 Тогда
		ПараметрыОбновленияПредопределенных = ОбновлениеИнформационнойБазы.ПараметрыОбновленияПредопределенныхЭлементов();
		ПараметрыОбновленияПредопределенных.Элементы = МассивСсылок;
		ОбновлениеИнформационнойБазы.ОбновитьПредопределенныеЭлементы(Метаданные.Справочники.ВидыКонтактнойИнформации,
			ПараметрыОбновленияПредопределенных);
	КонецЕсли;
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ПометитьНеиспользуемыеНачисленияПремия() Экспорт
	
	Описание = ПланыВидовРасчета.Начисления.ОписаниеНачисления();
	Описание.Код							= НСтр("ru = 'ПР'");
	Описание.Наименование					= НСтр("ru = 'Премия'");
	Описание.КодДоходаНДФЛ 					= ПланыВидовРасчета.Начисления.ВидыДоходовНДФЛПоКоду("2002");
	Описание.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее;
	Описание.КлючевыеСвойства				= "Код, Наименование, КодДоходаНДФЛ, КатегорияНачисленияИлиНеоплаченногоВремени";
	НачисленияПоОписанию = ПланыВидовРасчета.Начисления.НачисленияПоОписанию(Описание);
	Если НачисленияПоОписанию.Количество() > 0 Тогда
		НачислениеОбъект = НачисленияПоОписанию[0].ПолучитьОбъект();
		НачислениеОбъект.Код = НСтр("ru = 'нПР'");
		НачислениеОбъект.ВАрхиве = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НачислениеОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьКодыДоходовВычетовНДФЛНепредопределеннымиЗначениями() Экспорт
	
	Если ОбщегоНазначения.ЭтоАвтономноеРабочееМесто() Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Создаем вычеты к доходам НДФЛ
	ЗаполнитьКодыВычетовНДФЛНепредопределенных();
	
	// 2. Создаем коды доходов НДФЛ и связываем с вычетами
	ЗаполнитьКодыДоходовНДФЛНепредопределенных();
		
	// 3. Регистрируем размер вычета соответствующих кодов НДФЛ
	РегистрыСведений.ВычетыПоДоходамНДФЛ.НачальноеЗаполнение();
	
КонецПроцедуры

Процедура ЗаполнитьКодыДоходовНДФЛНепредопределенных() Экспорт
	
	Если Не ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.РегистрыСведений.ВычетыПоДоходамНДФЛ) Тогда
		Возврат;
	КонецЕсли;
	
	КодВычета = Справочники.ВидыВычетовНДФЛ.НайтиПоКоду("914");
	Если Не ЗначениеЗаполнено(КодВычета) Тогда
		ЗаполнитьКодыВычетовНДФЛНепредопределенных();
	КонецЕсли;
	КодВычета = Справочники.ВидыВычетовНДФЛ.НайтиПоКоду("914");
	
	Справочники.ВидыДоходовНДФЛ.ОписатьКодДоходаНДФЛ( ,
		Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13,
		"6014",
		НСтр("ru = 'Доходы в виде процентов по вкладам (остаткам по счетам) в банках на территории РФ'"),
		,
		Истина,
		КодВычета,
		Истина,
		Ложь,
		"-",
		"-",
		"6014");
	
КонецПроцедуры

Процедура ЗаполнитьКодыВычетовНДФЛНепредопределенных() Экспорт
	
	Если Не ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.РегистрыСведений.ВычетыПоДоходамНДФЛ) Тогда
		Возврат;
	КонецЕсли;
	
	Справочники.ВидыВычетовНДФЛ.ОписатьКодВычетаНДФЛ( ,
		"914",
		НСтр("ru = 'Необлагаемая сумма дохода в виде процентов по вкладам (остаткам на счетах) в банках, находящихся на территории РФ, для резидента'"),
		Истина,
		,
		"-",
		"-",
		"-",
		,
		Истина,
		,
		"914");
	
КонецПроцедуры

Процедура ЗаполнитьВычетыПоДоходамНДФЛНепредопределенных(НаборЗаписей) Экспорт
	
	Если Не ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.РегистрыСведений.ВычетыПоДоходамНДФЛ) Тогда
		Возврат;
	КонецЕсли;
	
	КодДохода = Справочники.ВидыДоходовНДФЛ.НайтиПоКоду("6014");
	Если Не ЗначениеЗаполнено(КодДохода) Тогда
		ЗаполнитьКодыДоходовНДФЛНепредопределенных();
	КонецЕсли;
	КодДохода = Справочники.ВидыДоходовНДФЛ.НайтиПоКоду("6014");
	Если Не ЗначениеЗаполнено(КодДохода) Тогда
		Возврат;
	КонецЕсли;
	
	// Размер вычета на 2023 г.
	РегистрыСведений.ВычетыПоДоходамНДФЛ.ДобавитьСкидкуКДоходамНДФЛ(
		НаборЗаписей,
		'20230101',
		КодДохода,
		150000,
		0);
		
	// Размер вычета на 2024 г.
	РегистрыСведений.ВычетыПоДоходамНДФЛ.ДобавитьСкидкуКДоходамНДФЛ(
		НаборЗаписей,
		'20240101',
		КодДохода,
		210000,
		0);
	
КонецПроцедуры

Процедура ВосстановитьВычетыКДоходамНДФЛ() Экспорт
	
	Если Не ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.РегистрыСведений.ВычетыПоДоходамНДФЛ) Тогда
		Возврат;
	КонецЕсли;
	
	Справочники.ВидыВычетовНДФЛ.НачальноеЗаполнение();
	Справочники.ВидыДоходовНДФЛ.НачальноеЗаполнение();
	РегистрыСведений.ВычетыПоДоходамНДФЛ.НачальноеЗаполнение();
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюСтрахователяФизическогоЛица(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Организации.Ссылка КАК Организация,
		|	Организации.СтатусФизическогоЛица = ЗНАЧЕНИЕ(Перечисление.СтатусыФизическихЛиц.ИндивидуальныйПредприниматель) КАК ИндивидуальныйПредприниматель
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|	И Организации.КатегорияСтрахователяФизическогоЛица = ЗНАЧЕНИЕ(Перечисление.КатегорииСтрахователейФизическихЛицСФР.ПустаяСсылка)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();			   
				   
	Если РезультатЗапроса.Пустой() Тогда 
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл 
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.Организации", "Ссылка", Выборка.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		ОрганизацияОбъект = Выборка.Организация.ПолучитьОбъект();
		Если Выборка.ИндивидуальныйПредприниматель Тогда
			ОрганизацияОбъект.КатегорияСтрахователяФизическогоЛица = Перечисления.КатегорииСтрахователейФизическихЛицСФР.ИП01;
		Иначе
			ОрганизацияОбъект.КатегорияСтрахователяФизическогоЛица = Перечисления.КатегорииСтрахователейФизическихЛицСФР.ФЛ01;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОрганизацияОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВключитьНастройкиВоинскогоУчетаСовместителей() Экспорт
	
	НаборЗаписей = РегистрыСведений.НастройкиВоинскогоУчета.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяЗапись = НаборЗаписей[0];
	Если ТекущаяЗапись.УчитыватьВнешнихСовместителей Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяЗапись.УчитыватьВнешнихСовместителей = Истина;
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкаНачисленийНеВключаемыхВРасходы

Функция НастроитьСпособыОтраженияДополнительныхНачислений() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("НачисленияИзПрибыли",            СпособОтраженияНачисленияИзПрибыли());
	Результат.Вставить("НачисленияНеВключаемыеВРасходы", СпособОтраженияНачисленияНеВключаемыеВРасходы());
	
	Возврат Результат;
	
КонецФункции

Функция СпособОтраженияНачисленияИзПрибыли()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", НаименованиеСпособаОтраженияЗарплатыНачисленияИзПрибыли());
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпособыОтраженияЗарплатыВБухУчете.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтраженияЗарплатыВБухУчете
	|ГДЕ
	|	СпособыОтраженияЗарплатыВБухУчете.Наименование = &Наименование
	|	И НЕ СпособыОтраженияЗарплатыВБухУчете.ЭтоГруппа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() <> 0 Тогда
		Выборка.Следующий();
		СпособОтражения = Выборка.Ссылка;
	Иначе
		НовыйСпособОтражения = Справочники.СпособыОтраженияЗарплатыВБухУчете.СоздатьЭлемент();
		НовыйСпособОтражения.Наименование = НаименованиеСпособаОтраженияЗарплатыНачисленияИзПрибыли();
		НовыйСпособОтражения.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
		НовыйСпособОтражения.Записать();
		СпособОтражения = НовыйСпособОтражения.Ссылка;
	КонецЕсли;
	
	СчетУчета = ПланыСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению;
	ОтражениеРасходов = Перечисления.ОтражениеВУСН.НеПринимаются;
	
	НастроитьСпособОтраженияЗарплаты(СпособОтражения, СчетУчета, , ОтражениеРасходов);
	
	Возврат СпособОтражения;
	
КонецФункции

Функция СпособОтраженияНачисленияНеВключаемыеВРасходы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", НаименованиеСпособаОтраженияЗарплатыНачисленияНеВключаемыеВРасходы());
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпособыОтраженияЗарплатыВБухУчете.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтраженияЗарплатыВБухУчете
	|ГДЕ
	|	СпособыОтраженияЗарплатыВБухУчете.Наименование = &Наименование
	|	И НЕ СпособыОтраженияЗарплатыВБухУчете.ЭтоГруппа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() <> 0 Тогда
		Выборка.Следующий();
		СпособОтражения = Выборка.Ссылка;
	Иначе
		НовыйСпособОтражения = Справочники.СпособыОтраженияЗарплатыВБухУчете.СоздатьЭлемент();
		НовыйСпособОтражения.Наименование = НаименованиеСпособаОтраженияЗарплатыНачисленияНеВключаемыеВРасходы();
		НовыйСпособОтражения.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
		НовыйСпособОтражения.Записать();
		СпособОтражения = НовыйСпособОтражения.Ссылка;
	КонецЕсли;
	
	СчетУчета         = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	СтатьяРасходов    = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ОплатаТрудаНеУчитываемаяДляНалогообложения");
	ОтражениеРасходов = Перечисления.ОтражениеВУСН.НеПринимаются;
	
	НастроитьСпособОтраженияЗарплаты(СпособОтражения, СчетУчета, СтатьяРасходов, ОтражениеРасходов);
	
	Возврат СпособОтражения;
	
КонецФункции

Процедура НастроитьСпособОтраженияЗарплаты(СпособОтражения, СчетУчета, СтатьяРасходов = Неопределено, ОтражениеРасходов)
	
	СпособОтраженияОбъект = СпособОтражения.ПолучитьОбъект();
	
	// В момент вызова функциональные опции еще могут быть
	// не обновлены, поэтому читаем значения констант.
	ПрименяетсяОсобыйПорядокНалогообложения   = Константы.ИспользуетсяУСНПатент.Получить();
	ПрименяетсяОсновнаяСистемаНалогообложения = Константы.ИспользуетсяОСНО.Получить()
		Или Константы.ИспользуетсяУСН.Получить() Или Константы.ИспользуетсяАУСН.Получить();
	
	Если ПрименяетсяОсобыйПорядокНалогообложения И Не ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		СпособОтраженияОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения;
	ИначеЕсли ПрименяетсяОсобыйПорядокНалогообложения И ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		СпособОтраженияОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты;
	Иначе
		СпособОтраженияОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
	КонецЕсли;
	
	СпособОтраженияОбъект.Записать();
	
	Если ПрименяетсяОсновнаяСистемаНалогообложения Тогда
		ОсобыйПорядок = Ложь;
		НаборЗаписей = РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СпособОтраженияЗарплатыВБухУчете.Установить(СпособОтражения);
		НаборЗаписей.Отбор.ЕНВД.Установить(ОсобыйПорядок);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьПоАналитике(
				СпособОтражения,
				СчетУчета,
				СтатьяРасходов,
				ОсобыйПорядок,
				ОтражениеРасходов);
		КонецЕсли;
	КонецЕсли;
	
	Если ПрименяетсяОсобыйПорядокНалогообложения Тогда
		ОсобыйПорядок = Истина;
		НаборЗаписей = РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СпособОтраженияЗарплатыВБухУчете.Установить(СпособОтражения);
		НаборЗаписей.Отбор.ЕНВД.Установить(ОсобыйПорядок);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			РегистрыСведений.ПорядокОтраженияЗарплатыВБухУчете.СоздатьПоАналитике(
				СпособОтражения,
				СчетУчета,
				СтатьяРасходов,
				ОсобыйПорядок,
				ОтражениеРасходов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция НаименованиеНачисленияМатериальнаяПомощь()
	
	Возврат НСтр("ru = 'Материальная помощь'");
	
КонецФункции

Функция НаименованиеНачисленияПремияЗаСчетПрибыли()
	
	Возврат НСтр("ru = 'Премия за счет прибыли'");
	
КонецФункции

Функция НаименованиеНачисленияПрочаяВыплатаНеПредусмотреннаяДоговором()
	
	Возврат НСтр("ru = 'Прочая выплата, не предусмотренная договором'");
	
КонецФункции

Функция НаименованиеСпособаОтраженияЗарплатыНачисленияИзПрибыли()
	
	Возврат НСтр("ru = 'Начисления за счет прибыли'");
	
КонецФункции

Функция НаименованиеСпособаОтраженияЗарплатыНачисленияНеВключаемыеВРасходы()
	
	Возврат НСтр("ru = 'Начисления, не учитываемые в расходах'");
	
КонецФункции

#КонецОбласти

#Область РазметкаОперацийАУСН

// Возвращает сведения об отражении в учете расходов на зарплату, выплаченную по переданным ведомостям,
// в разрезе подразделений, сотрудников и виддов начислений. Для выплат за месяц данные возвращаются с учетом
// "зачтенных авансов" - выплаченных начислений за первую половину месяца.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Период      - Дата - период осуществления выплаты.
//  ДанныеВедомостей - ТаблицаЗначений:
//                      * Ведомость - ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк - ведомость, для выплат по которой
//                                    требуется определить отражение расходов в учете.
//                      * ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо из ведомости, для выплат
//                                    которому требуется определить отражение расходов в учете.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//    * Ведомость - ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк - ведомость на выплату зарплаты.
//    * ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо из ведомости.
//    * Подразделение - СправочникСсылка.ПодразделенияОрганизаций - подразделение начисления.
//    * ДокументОснование - ОпределяемыйТип.ДокументОснованиеЗарплатыКВыплате - документ начисления.
//    * НачислениеУдержание - ПланВидовРасчетаСсылка.Начисления - начисление.
//    * Сумма - Число - сумма начисления.
//    * ОтражениеРасходов - ПеречислениеСсылка.ОтражениеВУСН - порядок отражения начисления в расходах АУСН.
//
Функция ДанныеОтраженияВыплатПоВедомостямВАУСН(Организация, ПериодВыплат, ДанныеВедомостей) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", ПериодВыплат);
	Запрос.УстановитьПараметр("Ведомости", ДанныеВедомостей);
	
	// При АУСН считаем, что расходы принимаются, если не указано иное.
	Запрос.УстановитьПараметр("ОтражениеРасходовПоУмолчанию", Перечисления.ОтражениеВУСН.Принимаются);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Ведомости.Ведомость КАК Ведомость,
	|	Ведомости.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТ_Ведомости
	|ИЗ
	|	&Ведомости КАК Ведомости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Ведомости.Ведомость КАК Ведомость,
	|	ЗарплатаКВыплате.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗарплатаКВыплате.Подразделение КАК Подразделение,
	|	ЗарплатаКВыплате.ДокументОснование КАК ДокументОснование,
	|	СУММА(ЗарплатаКВыплате.СуммаКВыплате) КАК СуммаКВыплате
	|ПОМЕСТИТЬ ВТ_ВыплатыПоВедомостям
	|ИЗ
	|	ВТ_Ведомости КАК ВТ_Ведомости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
	|		ПО ВТ_Ведомости.ФизическоеЛицо = ЗарплатаКВыплате.ФизическоеЛицо
	|			И ВТ_Ведомости.Ведомость = ЗарплатаКВыплате.Регистратор
	|			И (ЗарплатаКВыплате.Активность)
	|			И (ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И (ЗарплатаКВыплате.Организация = &Организация)
	|			И (ЗарплатаКВыплате.СуммаКВыплате <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Ведомости.Ведомость,
	|	ЗарплатаКВыплате.ФизическоеЛицо,
	|	ЗарплатаКВыплате.ДокументОснование,
	|	ЗарплатаКВыплате.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыплатыПоВедомостям.Ведомость КАК Ведомость,
	|	ВТ_ВыплатыПоВедомостям.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_ВыплатыПоВедомостям.Подразделение КАК Подразделение,
	|	ВТ_ВыплатыПоВедомостям.ДокументОснование КАК ДокументОснование,
	|	НачисленияУдержания.НачислениеУдержание КАК НачислениеУдержание,
	|	ВЫРАЗИТЬ(НачисленияУдержания.НачислениеУдержание КАК ПланВидовРасчета.Начисления).СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	НачисленияУдержания.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТ_ВсеНачисления
	|ИЗ
	|	ВТ_ВыплатыПоВедомостям КАК ВТ_ВыплатыПоВедомостям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержания
	|		ПО (НачисленияУдержания.Активность)
	|			И (НачисленияУдержания.Организация = &Организация)
	|			И ВТ_ВыплатыПоВедомостям.ФизическоеЛицо = НачисленияУдержания.ФизическоеЛицо
	|			И ВТ_ВыплатыПоВедомостям.Подразделение = НачисленияУдержания.Подразделение
	|			И ВТ_ВыплатыПоВедомостям.ДокументОснование = НачисленияУдержания.Регистратор
	|			И (НачисленияУдержания.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено))
	|			И (НачисленияУдержания.Сумма <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ВыплатыПоВедомостям.Ведомость,
	|	ВТ_ВыплатыПоВедомостям.ФизическоеЛицо,
	|	ВТ_ВыплатыПоВедомостям.Подразделение,
	|	ВТ_ВыплатыПоВедомостям.ДокументОснование,
	|	НачисленияУдержанияАвансом.НачислениеУдержание,
	|	ВЫРАЗИТЬ(НачисленияУдержанияАвансом.НачислениеУдержание КАК ПланВидовРасчета.Начисления).СпособОтраженияЗарплатыВБухучете,
	|	НачисленияУдержанияАвансом.Сумма * ВЫБОР
	|		КОГДА ВТ_ВыплатыПоВедомостям.СуммаКВыплате < 0
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|ИЗ
	|	ВТ_ВыплатыПоВедомостям КАК ВТ_ВыплатыПоВедомостям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НачисленияУдержанияПоСотрудникамАвансом КАК НачисленияУдержанияАвансом
	|		ПО (НачисленияУдержанияАвансом.Активность)
	|			И (НачисленияУдержанияАвансом.Организация = &Организация)
	|			И ВТ_ВыплатыПоВедомостям.ФизическоеЛицо = НачисленияУдержанияАвансом.ФизическоеЛицо
	|			И ВТ_ВыплатыПоВедомостям.Подразделение = НачисленияУдержанияАвансом.Подразделение
	|			И ВТ_ВыплатыПоВедомостям.ДокументОснование = НачисленияУдержанияАвансом.Регистратор
	|			И (НачисленияУдержанияАвансом.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено))
	|			И (НачисленияУдержанияАвансом.Сумма <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетЗарплатыОрганизацийСрезПоследних.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН ЕСТЬ NULL
	|			ТОГДА &ОтражениеРасходовПоУмолчанию
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|			ТОГДА &ОтражениеРасходовПоУмолчанию
	|		ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН
	|	КОНЕЦ КАК ОтражениеРасходов
	|ПОМЕСТИТЬ ВТ_ОтражениеНачисленийПоУмолчанию
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыОрганизаций.СрезПоследних(&Период, Организация = &Организация) КАК БухучетЗарплатыОрганизацийСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|		ПО БухучетЗарплатыОрганизацийСрезПоследних.СпособОтраженияЗарплатыВБухучете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
	|			И (НЕ ПорядокОтраженияЗарплатыВБухУчете.ЕНВД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВсеНачисления.Ведомость КАК Ведомость,
	|	ВТ_ВсеНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_ВсеНачисления.Подразделение КАК Подразделение,
	|	ВТ_ВсеНачисления.ДокументОснование КАК ДокументОснование,
	|	ВТ_ВсеНачисления.НачислениеУдержание КАК НачислениеУдержание,
	|	ВТ_ВсеНачисления.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН ЕСТЬ NULL
	|			ТОГДА ВТ_ОтражениеНачисленийПоУмолчанию.ОтражениеРасходов
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|			ТОГДА ВТ_ОтражениеНачисленийПоУмолчанию.ОтражениеРасходов
	|		ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН
	|	КОНЕЦ КАК ОтражениеРасходов
	|ИЗ
	|	ВТ_ВсеНачисления КАК ВТ_ВсеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|		ПО ВТ_ВсеНачисления.СпособОтражения = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
	|			И (НЕ ПорядокОтраженияЗарплатыВБухУчете.ЕНВД)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОтражениеНачисленийПоУмолчанию КАК ВТ_ОтражениеНачисленийПоУмолчанию
	|		ПО (ИСТИНА)";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает сведения об отражении в учете расходов на НДФЛ, перечисленый переданным документом,
// в разрезе видов начислений.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации
//  ДатаДокумента - Дата - дата уплаты НДФЛ
//  ДокументПеречисления - ДокументСсылка.СписаниеСРасчетногоСчета - документ уплаты НДФЛ
//  НалоговыйПериод - Дата - месяц, за который уплачивается НДФЛ
//  СуммаУплаты - Число - сумма перечисленного НДФЛ
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//    * ДокументНачисления - ДокументСсылка - документ начисления НДФЛ.
//    * ОтражениеРасходов  - ПеречислениеСсылка.ОтражениеВУСН - порядок отражения НДФЛ в расходах АУСН.
//    * Сумма              - Число - сумма расхода на НДФЛ.
//
Функция ДанныеОтраженияНДФЛПеречисленногоВАУСН(Организация, ДатаДокумента, ДокументПеречисления, НалоговыйПериод, СуммаУплаты) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоНалоговогоПериода", НачалоМесяца(НалоговыйПериод));
	Запрос.УстановитьПараметр("КонецНалоговогоПериода",  КонецМесяца(НалоговыйПериод));
	Запрос.УстановитьПараметр("ТекущийПериод",           ДатаДокумента);
	Запрос.УстановитьПараметр("Организация",             Организация);
	
	// Получим начисленный за месяц НДФЛ.
	// Далее определим оплаченную ранее сумму НДФЛ за данный месяц.
	// Далее распределяем сумму предыдущей уплаты на неоплаченные суммы НДФЛ на текущий момент в разрезе документов начислений.
	// Сумму текущей оплаты распределяем на неоплаченные документы по ФИФО.
	// После этого получаем отражение в расходах начислений по каждому оплачиваемому документу начислений.
	// Пропорционально разбивке отражения начислений в НУ определяем непринимаемую часть текущего оплачиваемого НДФЛ.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК ДокументНачисления,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период КАК Период,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) КАК Сумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Активность
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период МЕЖДУ &НачалоНалоговогоПериода И &КонецНалоговогоПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	ДокументНачисления,
	|	ФизическоеЛицо";
	
	НачисленныйНДФЛ = Запрос.Выполнить().Выгрузить();
	
	РанееОплаченоНДФЛ = УплаченныйНДФЛЗаПериод(Организация, НалоговыйПериод, ДатаДокумента, ДокументПеречисления);
	
	// Распределим уплаченный по начисленному и уберем оплаченные строки НДФЛ
	
	Для Каждого НачислениеНДФЛ Из НачисленныйНДФЛ Цикл
		
		Если РанееОплаченоНДФЛ = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если НачислениеНДФЛ.Сумма <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Оплачено = Мин(НачислениеНДФЛ.Сумма, РанееОплаченоНДФЛ);
		
		НачислениеНДФЛ.Сумма = НачислениеНДФЛ.Сумма - Оплачено;
		РанееОплаченоНДФЛ    = РанееОплаченоНДФЛ    - Оплачено;
		
	КонецЦикла;
	
	УдалитьСтрокиСНулевойСуммой(НачисленныйНДФЛ, "Сумма");
	
	// Распределим сумму текущей уплаты НДФЛ по неоплаченным начислениям.
	
	НераспределеннаяСуммаОплаты = СуммаУплаты;
	
	НачисленныйНДФЛ.Колонки.Добавить("СуммаОплаты", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Для Каждого НачислениеНДФЛ Из НачисленныйНДФЛ Цикл
		
		Если НераспределеннаяСуммаОплаты = 0 Тогда
			Прервать;
		КонецЕсли;
		
		СуммаТекущейОплаты = Мин(НачислениеНДФЛ.Сумма, НераспределеннаяСуммаОплаты);
		
		НачислениеНДФЛ.СуммаОплаты  = СуммаТекущейОплаты;
		НераспределеннаяСуммаОплаты = НераспределеннаяСуммаОплаты - СуммаТекущейОплаты;
		
	КонецЦикла;
	
	УдалитьСтрокиСНулевойСуммой(НачисленныйНДФЛ, "СуммаОплаты");
	
	// Вычислим отнесение к расходам оплачиваемых начислений НДФЛ
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(НалоговыйПериод));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(ДатаДокумента));
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("ДокументыНачисления",
		ОбщегоНазначения.ВыгрузитьКолонку(НачисленныйНДФЛ, "ДокументНачисления", Истина));
	// При АУСН считаем, что расходы принимаются, если не указано иное.
	Запрос.УстановитьПараметр("ОтражениеРасходовПоУмолчанию", Перечисления.ОтражениеВУСН.Принимаются);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.Регистратор КАК ДокументНачисления,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.Начисление КАК Начисление,
	|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) КАК СуммаНачисления,
	|	ВЫРАЗИТЬ(СведенияОДоходахНДФЛ.Начисление КАК ПланВидовРасчета.Начисления).СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	ВЫБОР
	|		КОГДА СведенияОДоходахНДФЛ.ЗарегистрированоПриНачисленииАванса <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоАванс
	|ПОМЕСТИТЬ ВТ_Начисления
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.Регистратор В(&ДокументыНачисления)
	|	И СведенияОДоходахНДФЛ.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СведенияОДоходахНДФЛ.Активность
	|	И СведенияОДоходахНДФЛ.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахНДФЛ.Регистратор,
	|	СведенияОДоходахНДФЛ.Начисление,
	|	ВЫБОР
	|		КОГДА СведенияОДоходахНДФЛ.ЗарегистрированоПриНачисленииАванса <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН ЕСТЬ NULL
	|			ТОГДА &ОтражениеРасходовПоУмолчанию
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|			ТОГДА &ОтражениеРасходовПоУмолчанию
	|		ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН
	|	КОНЕЦ КАК ОтражениеРасходов
	|ПОМЕСТИТЬ ВТ_ОтражениеНачисленийПоУмолчанию
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыОрганизаций.СрезПоследних(&КонецПериода, Организация = &Организация) КАК БухучетЗарплатыОрганизацийСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|		ПО БухучетЗарплатыОрганизацийСрезПоследних.СпособОтраженияЗарплатыВБухучете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Начисления.ДокументНачисления КАК ДокументНачисления,
	|	ВТ_Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_Начисления.Начисление КАК Начисление,
	|	ВТ_Начисления.СуммаНачисления КАК СуммаНачисления,
	|	ВТ_Начисления.ЭтоАванс КАК ЭтоАванс,
	|	ВЫБОР
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ОтражениеНачисленийПоУмолчанию.ОтражениеРасходов ЕСТЬ NULL
	|							ИЛИ ВТ_ОтражениеНачисленийПоУмолчанию.ОтражениеРасходов = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|						ТОГДА &ОтражениеРасходовПоУмолчанию
	|					ИНАЧЕ ВТ_ОтражениеНачисленийПоУмолчанию.ОтражениеРасходов
	|				КОНЕЦ
	|		КОГДА ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН = ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|			ТОГДА &ОтражениеРасходовПоУмолчанию
	|		ИНАЧЕ ПорядокОтраженияЗарплатыВБухУчете.ОтражениеВУСН
	|	КОНЕЦ КАК ОтражениеРасходов
	|ИЗ
	|	ВТ_Начисления КАК ВТ_Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОтражениеНачисленийПоУмолчанию КАК ВТ_ОтражениеНачисленийПоУмолчанию
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
	|		ПО ВТ_Начисления.СпособОтражения = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
	|			И (НЕ ПорядокОтраженияЗарплатыВБухУчете.ЕНВД)";
	
	ТаблицаНачисленийЗарплаты = Запрос.Выполнить().Выгрузить();
	
	ТаблицаОтраженияРасходовНаНДФЛ = Новый ТаблицаЗначений;
	ТаблицаОтраженияРасходовНаНДФЛ.Колонки.Добавить("ДокументНачисления", Документы.ТипВсеСсылки());
	ТаблицаОтраженияРасходовНаНДФЛ.Колонки.Добавить("ОтражениеРасходов", Новый ОписаниеТипов("ПеречислениеСсылка.ОтражениеВУСН"));
	ТаблицаОтраженияРасходовНаНДФЛ.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	// Распределяем оплаты НДФЛ по документам начисления пропорционально отражению начислений в расходах.
	
	ОтборНачисленийЗарплаты = Новый Структура("ДокументНачисления, ФизическоеЛицо");
	
	Для Каждого НачислениеНДФЛ Из НачисленныйНДФЛ Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборНачисленийЗарплаты, НачислениеНДФЛ);
		
		НачисленияСотрудникуПоДокументу = ТаблицаНачисленийЗарплаты.Скопировать(ОтборНачисленийЗарплаты);
		
		НачисленоВсего = НачисленияСотрудникуПоДокументу.Итог("СуммаНачисления");
		
		Если НачисленоВсего <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НачисленоНеПринимается = 0;
		Для Каждого Начисление Из НачисленияСотрудникуПоДокументу Цикл
			Если Начисление.ОтражениеРасходов = Перечисления.ОтражениеВУСН.НеПринимаются
					И (Не Начисление.ЭтоАванс Или Начисление.СуммаНачисления > 0) Тогда
				НачисленоНеПринимается = НачисленоНеПринимается + Начисление.СуммаНачисления;
			КонецЕсли;
		КонецЦикла;
		
		// Если начисление - итоговое за месяц, в нем могут быть отражены непринимаемые начисления-отклонения
		// в больше сумме, чем ранее начисленный аванс. Такие ситуации не обрабатываем.
		// Считается, что в авансе за первую половину месяца допустимо отражать только принимаемое основное начисление.
		// Такое поведение реализовано в БЗКБ - начисление зарплаты за месяц при заполнении не учитывает
		// начисления первой половины месяца. Здесь реализуем так же. При изменении поведения БЗКБ потребуется
		// поменять и данный алгоритм.
		
		НачисленоНеПринимается = Мин(НачисленоНеПринимается, НачисленоВсего);
		
		ОплаченоНеПринимаемыйНДФЛ = Окр(НачислениеНДФЛ.СуммаОплаты * (НачисленоНеПринимается / НачисленоВсего), 2);
		Если ОплаченоНеПринимаемыйНДФЛ > 0 Тогда
			УчтенныйРасходНДФЛ = ТаблицаОтраженияРасходовНаНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(УчтенныйРасходНДФЛ, НачислениеНДФЛ);
			УчтенныйРасходНДФЛ.ОтражениеРасходов = Перечисления.ОтражениеВУСН.НеПринимаются;
			УчтенныйРасходНДФЛ.Сумма = ОплаченоНеПринимаемыйНДФЛ;
		КонецЕсли;
		
		ОплаченоПринимаемыйНДФЛ = НачислениеНДФЛ.СуммаОплаты - ОплаченоНеПринимаемыйНДФЛ;
		Если ОплаченоПринимаемыйНДФЛ > 0 Тогда
			УчтенныйРасходНДФЛ = ТаблицаОтраженияРасходовНаНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(УчтенныйРасходНДФЛ, НачислениеНДФЛ);
			УчтенныйРасходНДФЛ.ОтражениеРасходов = Перечисления.ОтражениеВУСН.Принимаются;
			УчтенныйРасходНДФЛ.Сумма = ОплаченоПринимаемыйНДФЛ;
		КонецЕсли;
		
	КонецЦикла;
	
	// Нераспределенную сумму оплаты НДФЛ (переплату) отнесенм к принимаемым расходам.
	Если НераспределеннаяСуммаОплаты > 0 Тогда
		УчтенныйРасходНДФЛ = ТаблицаОтраженияРасходовНаНДФЛ.Добавить();
		УчтенныйРасходНДФЛ.ОтражениеРасходов = Перечисления.ОтражениеВУСН.Принимаются;
		УчтенныйРасходНДФЛ.Сумма = НераспределеннаяСуммаОплаты;
	КонецЕсли;
	
	Возврат ТаблицаОтраженияРасходовНаНДФЛ;
	
КонецФункции

// Определяет сумму отраженных в бухгалтерском учете (уплаченных) платежей по НДФЛ сотрудников за указанный период.
//
// Параметры:
//  Организация     - СправочникСсылка.Организации - организация-налоговый агент.
//  НалоговыйПериод - Дата - дата в месяце налогового периода, за который требуются сведения об уплате НДФЛ.
//  ДатаСведений    - Дата - Дата и время окончания анализируемого периода.
//  ИсключаемыйДокументУплаты - ДокументСсылка - документ, движения которого по уплате НДФЛ требуется исключить.
//
// Возвращаемое значение:
//  Число - сумма уплаченного налога (авансовых платежей).
//
Функция УплаченныйНДФЛЗаПериод(Организация, НалоговыйПериод, ДатаСведений, ИсключаемыйДокументУплаты = Неопределено)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	СчетаДенежныхСредств = Новый Массив;
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.КассаОрганизации)); // 50.01
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета));   // 51
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЦифровойРубль));    // 53
	
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ); // 68.01
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("ДатаСведений",          Новый Граница(ДатаСведений, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаНачала",            НачалоМесяца(НалоговыйПериод));
	Запрос.УстановитьПараметр("ДатаОкончания",         КонецМесяца(НалоговыйПериод));
	Запрос.УстановитьПараметр("СчетаНачисленияНалога", СчетаНачисленияНалога);
	Запрос.УстановитьПараметр("СчетаДенежныхСредств",  СчетаДенежныхСредств);
	Запрос.УстановитьПараметр("ВидыПлатежей",          Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот), 0) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&ДатаНачала,
	|			&ДатаСведений,
	|			Регистратор,
	|			СчетДт В (&СчетаНачисленияНалога),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|			СчетКт В (&СчетаДенежныхСредств),
	|			,
	|			Организация = &Организация
	|				И СубконтоДт1 В (&ВидыПлатежей)) КАК ХозрасчетныйОборотыДтКт
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.СписаниеСРасчетногоСчета)
	|				ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.СписаниеСРасчетногоСчета).НалоговыйПериод
	|			КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
	|				ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РасходныйКассовыйОрдер).НалоговыйПериод
	|			ИНАЧЕ ХозрасчетныйОборотыДтКт.Период
	|		КОНЕЦ МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И &УсловиеИсключаемыйДокументУплаты";
	
	Если ИсключаемыйДокументУплаты <> Неопределено Тогда
		Запрос.УстановитьПараметр("ИсключаемыйДокументУплаты", ИсключаемыйДокументУплаты);
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УсловиеИсключаемыйДокументУплаты",
			"ХозрасчетныйОборотыДтКт.Регистратор <> &ИсключаемыйДокументУплаты");
	Иначе
		Запрос.УстановитьПараметр("УсловиеИсключаемыйДокументУплаты", Истина);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Сумма;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Процедура УдалитьСтрокиСНулевойСуммой(ТаблицаДляОбработки, ИмяКолонкиСуммы)
	
	УдалитьСтроки = Новый Массив;
	
	КоличествоСтрок = ТаблицаДляОбработки.Количество();
	
	Для Счетчик = 1 По КоличествоСтрок Цикл
		СтрокаТаблицы = ТаблицаДляОбработки[КоличествоСтрок - Счетчик];
		Если СтрокаТаблицы[ИмяКолонкиСуммы] = 0 Тогда
			УдалитьСтроки.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УдаляемаяСтрока Из УдалитьСтроки Цикл
		ТаблицаДляОбработки.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РасчетНДФЛАУСН

Функция ДанныеРасчетаНДФЛАУСН(ТаблицаПеречислениеЗарплаты, ОплатаПоДоговорамПодряда, ТаблицаРеквизитов) Экспорт
	
	Если (Не ЗначениеЗаполнено(ТаблицаПеречислениеЗарплаты) И Не ЗначениеЗаполнено(ОплатаПоДоговорамПодряда))
		Или Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СоответствиеВедомостиФЛ = Новый Соответствие;
	СоответствиеВедомостей = Новый Соответствие;
	
	МассивВедомостей = Новый Массив;
	МассивФизическихЛиц = Новый Массив;
	
	Если ЗначениеЗаполнено(ТаблицаПеречислениеЗарплаты) Тогда
	
		Для Каждого СтрокаЗарплаты Из ТаблицаПеречислениеЗарплаты Цикл
			
			Если СоответствиеВедомостиФЛ[СтрокаЗарплаты.ФизическоеЛицо] = Неопределено Тогда
				МассивФизическихЛиц.Добавить(СтрокаЗарплаты.ФизическоеЛицо);
				СоответствиеВедомостиФЛ.Вставить(СтрокаЗарплаты.ФизическоеЛицо, СтрокаЗарплаты.Ведомость);
			КонецЕсли;
			
			Если СоответствиеВедомостей[СтрокаЗарплаты.Ведомость] = Неопределено Тогда
				МассивВедомостей.Добавить(СтрокаЗарплаты.Ведомость);
				СоответствиеВедомостей.Вставить(СтрокаЗарплаты.Ведомость, 1);
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОплатаПоДоговорамПодряда) Тогда
	
		Для Каждого СтрокаОплаты Из ОплатаПоДоговорамПодряда Цикл
			
			Если СоответствиеВедомостиФЛ[СтрокаОплаты.ФизическоеЛицо] = Неопределено Тогда
				МассивФизическихЛиц.Добавить(СтрокаОплаты.ФизическоеЛицо);
				СоответствиеВедомостиФЛ.Вставить(СтрокаОплаты.ФизическоеЛицо, СтрокаОплаты.Ведомость);
			КонецЕсли;
			
			Если СоответствиеВедомостей[СтрокаОплаты.Ведомость] = Неопределено Тогда
				МассивВедомостей.Добавить(СтрокаОплаты.Ведомость);
				СоответствиеВедомостей.Вставить(СтрокаОплаты.Ведомость, 1);
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизитов[0];
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",         Реквизиты.Организация);
	Запрос.УстановитьПараметр("МассивВедомостей",    МассивВедомостей);
	Запрос.УстановитьПараметр("МассивФизическихЛиц", МассивФизическихЛиц);
	Запрос.УстановитьПараметр("НачалоПериода",       НачалоМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("КонецПериода",        КонецМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("Период",              Реквизиты.Период);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.ДокументОснование КАК ДокументОснование,
		|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Сотрудник КАК Сотрудник,
		|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов
		|ПОМЕСТИТЬ ВТДокументыНачисления
		|ИЗ
		|	Документ.ВедомостьНаВыплатуЗарплатыВБанк.Зарплата КАК ВедомостьНаВыплатуЗарплатыВБанкЗарплата
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
		|		ПО ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Ссылка = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка
		|ГДЕ
		|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка В(&МассивВедомостей)
		|	И ВедомостьНаВыплатуЗарплатыВБанкЗарплата.ФизическоеЛицо В(&МассивФизическихЛиц)
		|	И ВедомостьНаВыплатуЗарплатыВБанк.Организация = &Организация
		|	И ВедомостьНаВыплатуЗарплатыВБанкЗарплата.КВыплате > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходахСтраховыеВзносы.Начисление КАК Начисление,
		|	СведенияОДоходахСтраховыеВзносы.ВидДохода КАК ВидДохода,
		|	СведенияОДоходахСтраховыеВзносы.Регистратор КАК ДокументОснование,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА СведенияОДоходахСтраховыеВзносы.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА ""01""
		|			КОГДА НЕ СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуФОМС
		|				ТОГДА ""01""
		|			КОГДА НЕ СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуФСС
		|				ТОГДА ""01""
		|			КОГДА НЕ СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуПФР
		|				ТОГДА ""01""
		|			ИНАЧЕ """"
		|		КОНЕЦ) КАК КодНеоблагаемогоВзносамиДохода,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА СведенияОДоходахСтраховыеВзносы.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА ""4902""
		|			ИНАЧЕ """"
		|		КОНЕЦ) КАК КодДохода,
		|	СУММА(СведенияОДоходахСтраховыеВзносы.Сумма) КАК СуммаВзносы,
		|	СУММА(СведенияОДоходахСтраховыеВзносы.Скидка) КАК СкидкаВзносы
		|ПОМЕСТИТЬ ВТДанныеУчетаВзносы
		|ИЗ
		|	РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыНачисления КАК ВТДокументыНачисления
		|		ПО СведенияОДоходахСтраховыеВзносы.Регистратор = ВТДокументыНачисления.ДокументОснование
		|			И СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = ВТДокументыНачисления.ФизическоеЛицо
		|ГДЕ
		|	НЕ СведенияОДоходахСтраховыеВзносы.Регистратор ССЫЛКА Документ.ОперацияУчетаПоСтраховымВзносам
		|	И СведенияОДоходахСтраховыеВзносы.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо,
		|	СведенияОДоходахСтраховыеВзносы.Начисление,
		|	СведенияОДоходахСтраховыеВзносы.ВидДохода,
		|	СведенияОДоходахСтраховыеВзносы.Регистратор
		|
		|ИМЕЮЩИЕ
		|	(СУММА(СведенияОДоходахСтраховыеВзносы.Сумма) <> 0
		|		ИЛИ СУММА(СведенияОДоходахСтраховыеВзносы.Скидка) <> 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.ДатаПолученияДохода, МЕСЯЦ) КАК Период,
		|	СведенияОДоходахНДФЛ.Начисление КАК Начисление,
		|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) КАК СуммаДоходаНДФЛ,
		|	СУММА(СведенияОДоходахНДФЛ.СуммаВычета) КАК СуммаВычетаНДФЛ,
		|	СведенияОДоходахНДФЛ.КодДохода КАК КодДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходахНДФЛ.СуммаВычета = 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.ПустаяСсылка)
		|		ИНАЧЕ СведенияОДоходахНДФЛ.КодВычета
		|	КОНЕЦ КАК КодВычета,
		|	СведенияОДоходахНДФЛ.ДокументОснование КАК ДокументОснование,
		|	СведенияОДоходахНДФЛ.КатегорияДохода КАК КатегорияДохода,
		|	ВТДокументыНачисления.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов
		|ПОМЕСТИТЬ ВТДанныеУчетаНДФЛ
		|ИЗ
		|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыНачисления КАК ВТДокументыНачисления
		|		ПО СведенияОДоходахНДФЛ.ДокументОснование = ВТДокументыНачисления.ДокументОснование
		|			И СведенияОДоходахНДФЛ.ФизическоеЛицо = ВТДокументыНачисления.ФизическоеЛицо
		|ГДЕ
		|	НЕ СведенияОДоходахНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
		|	И НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВТДокументыНачисления.ПериодВзаиморасчетов, МЕСЯЦ)
		|	И СведенияОДоходахНДФЛ.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
		|	НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.ДатаПолученияДохода, МЕСЯЦ),
		|	СведенияОДоходахНДФЛ.Начисление,
		|	СведенияОДоходахНДФЛ.КодДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходахНДФЛ.СуммаВычета = 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.ПустаяСсылка)
		|		ИНАЧЕ СведенияОДоходахНДФЛ.КодВычета
		|	КОНЕЦ,
		|	СведенияОДоходахНДФЛ.ДокументОснование,
		|	СведенияОДоходахНДФЛ.КатегорияДохода,
		|	ВТДокументыНачисления.ПериодВзаиморасчетов
		|
		|ИМЕЮЩИЕ
		|	(СУММА(СведенияОДоходахНДФЛ.СуммаДохода) <> 0
		|		ИЛИ СУММА(СведенияОДоходахНДФЛ.СуммаВычета) <> 0)";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	 	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	 	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета КАК КодВычета,
	 	|	ВТДокументыНачисления.ДокументОснование КАК ДокументОснование,
	 	|	СУММА(ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Сумма) КАК Сумма
	 	|ИЗ
	 	|	РегистрНакопления.ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ КАК ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ
	 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыНачисления КАК ВТДокументыНачисления
	 	|		ПО ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Регистратор = ВТДокументыНачисления.ДокументОснование
	 	|			И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо = ВТДокументыНачисления.ФизическоеЛицо
	 	|ГДЕ
	 	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Организация = &Организация
	 	|
	 	|СГРУППИРОВАТЬ ПО
	 	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо,
	 	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета,
	 	|	ВТДокументыНачисления.ДокументОснование
	 	|
	 	|ОБЪЕДИНИТЬ ВСЕ
	 	|
	 	|ВЫБРАТЬ
	 	|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо,
	 	|	ИмущественныеВычетыНДФЛ.КодВычета,
	 	|	ВТДокументыНачисления.ДокументОснование,
	 	|	СУММА(ИмущественныеВычетыНДФЛ.Сумма)
	 	|ИЗ
	 	|	РегистрНакопления.ИмущественныеВычетыНДФЛ КАК ИмущественныеВычетыНДФЛ
	 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыНачисления КАК ВТДокументыНачисления
	 	|		ПО ИмущественныеВычетыНДФЛ.Регистратор = ВТДокументыНачисления.ДокументОснование
	 	|			И ИмущественныеВычетыНДФЛ.ФизическоеЛицо = ВТДокументыНачисления.ФизическоеЛицо
	 	|ГДЕ
	 	|	ИмущественныеВычетыНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	 	|	И ИмущественныеВычетыНДФЛ.Организация = &Организация
	 	|
	 	|СГРУППИРОВАТЬ ПО
	 	|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо,
	 	|	ИмущественныеВычетыНДФЛ.КодВычета,
	 	|	ВТДокументыНачисления.ДокументОснование";
	
	ТаблицаВычетов = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ВЫБОР
		|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма + РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Начислено,
		|	СУММА(ВЫБОР
		|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.Удержано)
		|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма + РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
		|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.ВозвращеноНалоговымАгентом)
		|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма + РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
		|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.ЗачетАвансовыхПлатежей)
		|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма + РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Удержано,
		|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование,
		|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
		|	МАКСИМУМ(ЕСТЬNULL(АвансовыеПлатежиИностранцевПоНДФЛ.Сумма, 0)) КАК СуммаАвансовогоПлатежа,
		|	МАКСИМУМ(ЕСТЬNULL(РеквизитыУведомленияИФНСНаЗачетАвансовыхПлатежей.НомерУведомления, """")) КАК НомерУведомленияПоАвансовымПлатежам,
		|	МАКСИМУМ(ЕСТЬNULL(РеквизитыУведомленияИФНСНаЗачетАвансовыхПлатежей.ДатаУведомления, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаУведомленияПоАвансовымПлатежам,
		|	МАКСИМУМ(ЕСТЬNULL(РеквизитыУведомленияИФНСНаЗачетАвансовыхПлатежей.КодНалоговогоОргана, """")) КАК КодНалоговогоОрганаВыдавшегоУведомлениеПоАвансовымПлатежам
		|ИЗ
		|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыНачисления КАК ВТДокументыНачисления
		|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = ВТДокументыНачисления.ДокументОснование
		|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = ВТДокументыНачисления.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АвансовыеПлатежиИностранцевПоНДФЛ КАК АвансовыеПлатежиИностранцевПоНДФЛ
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыУведомленияИФНСНаЗачетАвансовыхПлатежей КАК РеквизитыУведомленияИФНСНаЗачетАвансовыхПлатежей
		|			ПО АвансовыеПлатежиИностранцевПоНДФЛ.ФизическоеЛицо = РеквизитыУведомленияИФНСНаЗачетАвансовыхПлатежей.ФизическоеЛицо
		|				И АвансовыеПлатежиИностранцевПоНДФЛ.Год = РеквизитыУведомленияИФНСНаЗачетАвансовыхПлатежей.Год
		|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = АвансовыеПлатежиИностранцевПоНДФЛ.Регистратор
		|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = АвансовыеПлатежиИностранцевПоНДФЛ.ФизическоеЛицо
		|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода = АвансовыеПлатежиИностранцевПоНДФЛ.КодДохода
		|			И (АвансовыеПлатежиИностранцевПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
		|			И (РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.ЗачетАвансовыхПлатежей))
		|ГДЕ
		|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
		|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
		|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода";
	
	ТаблицаСуммыНалогов = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТДанныеУчетаНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА ВТДанныеУчетаНДФЛ.КодДохода.НеОблагаетсяУНалоговогоАгента
		|			ТОГДА ""4903""
		|		КОГДА ВТДанныеУчетаНДФЛ.КодДохода.КодПрименяемыйВНалоговойОтчетностиС2021Года В (""2610"", ""2630"", ""2640"", ""2641"")
		|				И (ВТДанныеУчетаНДФЛ.Период МЕЖДУ ДАТАВРЕМЯ(2021, 1, 1, 0, 0, 0) И ДАТАВРЕМЯ(2023, 12, 31, 23, 59, 59))
		|			ТОГДА ""4901""
		|		КОГДА ВТДанныеУчетаНДФЛ.Начисление.КодДоходаСтраховыеВзносы = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|			ТОГДА ""4902""
		|		КОГДА ЕСТЬNULL(ВТДанныеУчетаВзносы.КодДохода, """") = """"
		|			ТОГДА ЕСТЬNULL(ВТДанныеУчетаНДФЛ.КодДохода.КодПрименяемыйВНалоговойОтчетностиС2021Года, """")
		|		ИНАЧЕ ЕСТЬNULL(ВТДанныеУчетаВзносы.КодДохода, """")
		|	КОНЕЦ КАК КодДохода,
		|	ЕСТЬNULL(ВТДанныеУчетаНДФЛ.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года, """") КАК КодВычета,
		|	ЕСТЬNULL(ВТДанныеУчетаВзносы.КодНеоблагаемогоВзносамиДохода, """") КАК КодНеоблагаемогоВзносамиДохода,
		|	СУММА(ВТДанныеУчетаНДФЛ.СуммаДоходаНДФЛ) КАК СуммаДохода,
		|	СУММА(ВТДанныеУчетаНДФЛ.СуммаВычетаНДФЛ) КАК СуммаВычета,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВТДанныеУчетаВзносы.КодНеоблагаемогоВзносамиДохода, """") = """"
		|				ТОГДА 0
		|			ИНАЧЕ ВТДанныеУчетаНДФЛ.СуммаДоходаНДФЛ
		|		КОНЕЦ) КАК СуммаНеоблагаемаяВзносами,
		|	ВТДанныеУчетаНДФЛ.ДокументОснование КАК ДокументОснование,
		|	ВТДанныеУчетаНДФЛ.КатегорияДохода КАК КатегорияДохода,
		|	ВТДанныеУчетаНДФЛ.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов
		|ПОМЕСТИТЬ ВТ_ДанныеНДФЛИтоговые
		|ИЗ
		|	ВТДанныеУчетаНДФЛ КАК ВТДанныеУчетаНДФЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеУчетаВзносы КАК ВТДанныеУчетаВзносы
		|		ПО ВТДанныеУчетаНДФЛ.ФизическоеЛицо = ВТДанныеУчетаВзносы.ФизическоеЛицо
		|			И ВТДанныеУчетаНДФЛ.Начисление = ВТДанныеУчетаВзносы.Начисление
		|			И ВТДанныеУчетаНДФЛ.ДокументОснование = ВТДанныеУчетаВзносы.ДокументОснование
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТДанныеУчетаНДФЛ.ФизическоеЛицо,
		|	ЕСТЬNULL(ВТДанныеУчетаВзносы.КодНеоблагаемогоВзносамиДохода, """"),
		|	ЕСТЬNULL(ВТДанныеУчетаНДФЛ.КодВычета.КодПрименяемыйВНалоговойОтчетностиС2016Года, """"),
		|	ВЫБОР
		|		КОГДА ВТДанныеУчетаНДФЛ.КодДохода.НеОблагаетсяУНалоговогоАгента
		|			ТОГДА ""4903""
		|		КОГДА ВТДанныеУчетаНДФЛ.КодДохода.КодПрименяемыйВНалоговойОтчетностиС2021Года В (""2610"", ""2630"", ""2640"", ""2641"")
		|				И (ВТДанныеУчетаНДФЛ.Период МЕЖДУ ДАТАВРЕМЯ(2021, 1, 1, 0, 0, 0) И ДАТАВРЕМЯ(2023, 12, 31, 23, 59, 59))
		|			ТОГДА ""4901""
		|		КОГДА ВТДанныеУчетаНДФЛ.Начисление.КодДоходаСтраховыеВзносы = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|			ТОГДА ""4902""
		|		КОГДА ЕСТЬNULL(ВТДанныеУчетаВзносы.КодДохода, """") = """"
		|			ТОГДА ЕСТЬNULL(ВТДанныеУчетаНДФЛ.КодДохода.КодПрименяемыйВНалоговойОтчетностиС2021Года, """")
		|		ИНАЧЕ ЕСТЬNULL(ВТДанныеУчетаВзносы.КодДохода, """")
		|	КОНЕЦ,
		|	ВТДанныеУчетаНДФЛ.ДокументОснование,
		|	ВТДанныеУчетаНДФЛ.КатегорияДохода,
		|	ВТДанныеУчетаНДФЛ.ПериодВзаиморасчетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеНДФЛИтоговые.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТ_ДанныеНДФЛИтоговые.КодДохода КАК КодДохода,
		|	ВЫБОР
		|		КОГДА ВТ_ДанныеНДФЛИтоговые.КодДохода В (&КодыВыплатАУСНДляВычетаВРазмереПолученнойПрибыли)
		|				И ВТ_ДанныеНДФЛИтоговые.СуммаВычета <> 0
		|			ТОГДА &КодВычетаВРазмереПолученнойПрибыли
		|		КОГДА ВТ_ДанныеНДФЛИтоговые.КодДохода = &КодДоходаНеПодлежащегоНалогообложению
		|				И ВТ_ДанныеНДФЛИтоговые.СуммаВычета <> 0
		|				И ВТ_ДанныеНДФЛИтоговые.КодВычета В (&КодыВычетовПоНеоблагаемымДоходам)
		|			ТОГДА ВТ_ДанныеНДФЛИтоговые.КодВычета
		|		КОГДА ВТ_ДанныеНДФЛИтоговые.КодДохода = &КодДоходаНеПодлежащегоНалогообложению
		|				И ВТ_ДанныеНДФЛИтоговые.СуммаВычета <> 0
		|			ТОГДА &КодВычетаВРазмереПолученнойПрибыли
		|		ИНАЧЕ ВТ_ДанныеНДФЛИтоговые.КодВычета
		|	КОНЕЦ КАК КодВычета,
		|	ВТ_ДанныеНДФЛИтоговые.КодНеоблагаемогоВзносамиДохода КАК КодНеоблагаемогоВзносамиДохода,
		|	ВТ_ДанныеНДФЛИтоговые.СуммаДохода КАК СуммаДохода,
		|	ВТ_ДанныеНДФЛИтоговые.СуммаВычета КАК СуммаВычета,
		|	ВТ_ДанныеНДФЛИтоговые.СуммаНеоблагаемаяВзносами КАК СуммаНеоблагаемаяВзносами,
		|	ВТ_ДанныеНДФЛИтоговые.ДокументОснование КАК ДокументОснование,
		|	ВТ_ДанныеНДФЛИтоговые.КатегорияДохода КАК КатегорияДохода,
		|	ВТ_ДанныеНДФЛИтоговые.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов
		|ИЗ
		|	ВТ_ДанныеНДФЛИтоговые КАК ВТ_ДанныеНДФЛИтоговые
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизическоеЛицо,
		|	СуммаДохода УБЫВ";
	
	КодДоходаНеПодлежащегоНалогообложению = ИнтеграцияАУСН.КодДоходаНеПодлежащегоНалогообложению();
	КодыДоходовСОбязательнымВычетомВРазмереПолученнойПрибыли =
		ИнтеграцияАУСН.КодыВыплатАУСНДляВычетаВРазмереПолученнойПрибыли();
	
	Запрос.УстановитьПараметр(
		"КодДоходаНеПодлежащегоНалогообложению", КодДоходаНеПодлежащегоНалогообложению);
	Запрос.УстановитьПараметр("КодыВыплатАУСНДляВычетаВРазмереПолученнойПрибыли",
		КодыДоходовСОбязательнымВычетомВРазмереПолученнойПрибыли);
	Запрос.УстановитьПараметр("КодВычетаВРазмереПолученнойПрибыли", ИнтеграцияАУСН.КодВычетаВРазмереПолученнойПрибыли());
	Запрос.УстановитьПараметр("КодыВычетовПоНеоблагаемымДоходам", ИнтеграцияАУСН.КодыВычетовПоНеоблагаемымДоходам());
	ВыборкаДанныеУчетаНДФЛ = Запрос.Выполнить().Выбрать();
	
	ТаблицаРасчетыНДФЛ = НоваяТаблицаРасчетыНДФЛАУСН();
	
	СтруктураПоискаСуммНалогов = Новый Структура("ФизическоеЛицо,ДокументОснование,КатегорияДохода");
	
	СтруктураПоискаВычетов = Новый Структура("ФизическоеЛицо,ДокументОснование");
	
	КодыДоходовСОбязательнымиВычетами = Новый Массив;
	КодыДоходовСОбязательнымиВычетами.Добавить(КодДоходаНеПодлежащегоНалогообложению);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КодыДоходовСОбязательнымиВычетами,
		КодыДоходовСОбязательнымВычетомВРазмереПолученнойПрибыли);
	
	Пока ВыборкаДанныеУчетаНДФЛ.Следующий() Цикл
		
		НоваяСтрокаРасчета = ТаблицаРасчетыНДФЛ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРасчета, ВыборкаДанныеУчетаНДФЛ);
		
		ОбщаяСуммаВычета = НоваяСтрокаРасчета.СуммаВычета;
		
		НоваяСтрокаРасчета.Ведомость = СоответствиеВедомостиФЛ[НоваяСтрокаРасчета.ФизическоеЛицо];
		НоваяСтрокаРасчета.НалоговыйПериод = НачалоМесяца(ВыборкаДанныеУчетаНДФЛ.ПериодВзаиморасчетов);
		
		ЗаполнитьЗначенияСвойств(СтруктураПоискаСуммНалогов, ВыборкаДанныеУчетаНДФЛ);
		СтрокиСуммНалогов = ТаблицаСуммыНалогов.НайтиСтроки(СтруктураПоискаСуммНалогов);
		
		Для Каждого СтрокаСуммНалогов Из СтрокиСуммНалогов Цикл
			
			Если СтрокаСуммНалогов.Удержано <> 0 ИЛИ СтрокаСуммНалогов.Начислено <> 0 Тогда
				
				НоваяСтрокаРасчета.СуммаНДФЛ = СтрокаСуммНалогов.Начислено;
				НоваяСтрокаРасчета.СуммаНДФЛУдержаный = СтрокаСуммНалогов.Удержано;
				НоваяСтрокаРасчета.СуммаНДФЛПеречисленный = СтрокаСуммНалогов.Удержано;
				НоваяСтрокаРасчета.НДФЛНерезидента = СтрокаСуммНалогов.СуммаАвансовогоПлатежа;
				НоваяСтрокаРасчета.НомерУведомленияНерезидента = СтрокаСуммНалогов.НомерУведомленияПоАвансовымПлатежам;
				НоваяСтрокаРасчета.ДатаУведомленияНерезидента = СтрокаСуммНалогов.ДатаУведомленияПоАвансовымПлатежам;
				НоваяСтрокаРасчета.КодНалоговогоОрганаНерезидента =
					СтрокаСуммНалогов.КодНалоговогоОрганаВыдавшегоУведомлениеПоАвансовымПлатежам;
				СтрокаСуммНалогов.Начислено = 0; 
				СтрокаСуммНалогов.Удержано = 0; 
				
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоискаВычетов, ВыборкаДанныеУчетаНДФЛ);
		СтрокиСуммВычетов = ТаблицаВычетов.НайтиСтроки(СтруктураПоискаВычетов);
		
		Для Каждого СтрокаСуммВычетов Из СтрокиСуммВычетов Цикл
			
			Если СтрокаСуммВычетов.Сумма <> 0 И ЗначениеЗаполнено(СтрокаСуммВычетов.КодВычета) Тогда
				
				Если ЗначениеЗаполнено(НоваяСтрокаРасчета.КодВычета) Тогда
					ПредыдущаяСтрокаРасчета = НоваяСтрокаРасчета;
					НоваяСтрокаРасчета = ТаблицаРасчетыНДФЛ.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаРасчета, ПредыдущаяСтрокаРасчета, 
						"ФизическоеЛицо,Ведомость,НалоговыйПериод,КодДохода");
				КонецЕсли;
				
				НоваяСтрокаРасчета.КодВычета = СтрокаСуммВычетов.КодВычета;
				НоваяСтрокаРасчета.СуммаВычета = СтрокаСуммВычетов.Сумма;
				СтрокаСуммВычетов.Сумма = 0;
				ОбщаяСуммаВычета = ОбщаяСуммаВычета + НоваяСтрокаРасчета.СуммаВычета;
				
			КонецЕсли;
		КонецЦикла;
		
		Если КодыДоходовСОбязательнымиВычетами.Найти(ВыборкаДанныеУчетаНДФЛ.КодДохода) <> Неопределено
			И ОбщаяСуммаВычета <> ВыборкаДанныеУчетаНДФЛ.СуммаДохода Тогда
			ОстатокВычета = ВыборкаДанныеУчетаНДФЛ.СуммаДохода - ОбщаяСуммаВычета;
			Если ЗначениеЗаполнено(НоваяСтрокаРасчета.КодВычета) Тогда
				ПредыдущаяСтрокаРасчета = НоваяСтрокаРасчета;
				НоваяСтрокаРасчета = ТаблицаРасчетыНДФЛ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаРасчета, ПредыдущаяСтрокаРасчета, 
					"ФизическоеЛицо,Ведомость,НалоговыйПериод,КодДохода");
			КонецЕсли;
			НоваяСтрокаРасчета.КодВычета = ИнтеграцияАУСН.КодВычетаВРазмереПолученнойПрибыли();
			НоваяСтрокаРасчета.СуммаВычета = ОстатокВычета;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаРасчетыНДФЛ.Свернуть(
		"Ведомость,ФизическоеЛицо,НалоговыйПериод,КодДохода,КодВычета,КодНеоблагаемогоВзносамиДохода,
		|НомерУведомленияНерезидента, ДатаУведомленияНерезидента, КодНалоговогоОрганаНерезидента",
		"СуммаДохода,СуммаВычета,СуммаНДФЛ,СуммаНДФЛУдержаный,СуммаНДФЛПеречисленный,СуммаНеоблагаемаяВзносами, НДФЛНерезидента");
	ТаблицаРасчетыНДФЛ.Сортировать("ФизическоеЛицо, СуммаДохода УБЫВ");
	
	Возврат ТаблицаРасчетыНДФЛ;

КонецФункции

Функция НоваяТаблицаРасчетыНДФЛАУСН()
	
	ТаблицаРасчетыНДФЛ = Новый ТаблицаЗначений;
	
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("Ведомость",
		Новый ОписаниеТипов("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк"));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("НалоговыйПериод", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("КодДохода", ОбщегоНазначения.ОписаниеТипаСтрока(4));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("КодВычета", ОбщегоНазначения.ОписаниеТипаСтрока(3));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("КодНеоблагаемогоВзносамиДохода", ОбщегоНазначения.ОписаниеТипаСтрока(2));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("НомерУведомленияНерезидента", ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("ДатаУведомленияНерезидента", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("КодНалоговогоОрганаНерезидента", ОбщегоНазначения.ОписаниеТипаСтрока(4));
	
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("СуммаДохода", Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("СуммаВычета", Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("СуммаНДФЛ",   Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("СуммаНДФЛУдержаный", 
		Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("СуммаНДФЛПеречисленный", 
		Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("СуммаНеоблагаемаяВзносами",
		Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	ТаблицаРасчетыНДФЛ.Колонки.Добавить("НДФЛНерезидента",
		Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	
	Возврат ТаблицаРасчетыНДФЛ;
	
КонецФункции

#КонецОбласти

#Область УдержанныйНДФЛДляЕНС

Функция УдержанныйНДФЛПоДокументуВыплаты(Движения)
	
	ТаблицаУдержанногоНДФЛПоДокументуВыплаты = Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Выгрузить();
	УдержаноНалогаЗаПериод = ТаблицаУдержанногоНДФЛПоДокументуВыплаты.Скопировать(Новый Структура("ВариантУдержания", Перечисления.ВариантыУдержанияНДФЛ.Удержано));
	
	ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
	ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
	ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
		ПроизводственныйКалендарь,
		ОбщегоНазначения.ВыгрузитьКолонку(УдержаноНалогаЗаПериод, "КрайнийСрокУплаты", Истина),
		ПараметрыПолучения);

	СчетУчетаНДФЛ = ПланыСчетов.Хозрасчетный.НДФЛ; // 68.01
	
	ТаблицаУдержанногоНДФЛ = НоваяТаблицаУдержанногоНДФЛДляОтраженияВУчете();
	Для Каждого СтрокаТаблицы Из УдержаноНалогаЗаПериод Цикл
		СуммаНалога = УчетНДФЛКлиентСервер.ИтоговаяСуммаНДФЛпоСтроке(СтрокаТаблицы, "Сумма");
		Если СуммаНалога <> 0 Тогда
			НоваяСтрока = ТаблицаУдержанногоНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Сумма      = СуммаНалога;
			НоваяСтрока.СрокУплаты = ГрафикПереноса[СтрокаТаблицы.КрайнийСрокУплаты];
			НоваяСтрока.СчетУчета  = СчетУчетаНДФЛ;
		КонецЕсли;
	КонецЦикла;
	
	КолонкиСвертки = "";
	Для Каждого Колонка Из ТаблицаУдержанногоНДФЛ.Колонки Цикл
		Если Колонка.Имя = "Сумма" Тогда
			Продолжить;
		КонецЕсли;
		КолонкиСвертки = КолонкиСвертки + "," + Колонка.Имя;
	КонецЦикла;
	ТаблицаУдержанногоНДФЛ.Свернуть(КолонкиСвертки, "Сумма");
	
	Возврат ТаблицаУдержанногоНДФЛ;
	
КонецФункции

Процедура СформироватьДвиженияПоУдержанномуНДФЛПриВыплатеДохода(Движения, Параметры, Отказ) Экспорт
	
	Реквизиты = Параметры.Реквизиты[0];
	
	Если НЕ ЕдиныйНалоговыйСчет.ЭтоПростойУчет(Реквизиты.Период, Реквизиты.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаУдержанныйНДФЛ = УдержанныйНДФЛПоДокументуВыплаты(Движения);
	
	Если Не ЗначениеЗаполнено(ТаблицаУдержанныйНДФЛ) Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьДвиженияНачислениеНалоговНаЕНС(Движения, Реквизиты, ТаблицаУдержанныйНДФЛ, Отказ);
	
КонецПроцедуры

Процедура СформироватьДвиженияПоУдержанномуНДФЛ(ТаблицаРеквизиты, ТаблицаУдержанныйНДФЛ, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаУдержанныйНДФЛ) Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьПараметрыТаблицыУдержанногоНДФЛ(ТаблицаРеквизиты, ТаблицаУдержанныйНДФЛ);
	
	Параметры = ПодготовитьПараметрыТаблицыУдержанногоНДФЛ(ТаблицаРеквизиты, ТаблицаУдержанныйНДФЛ);
	Реквизиты = Параметры.Реквизиты[0];
	
	Если НЕ (ЕдиныйНалоговыйСчет.ЭтоПростойУчет(Реквизиты.Период, Реквизиты.Организация) 
		И ЕдиныйНалоговыйСчет.ЭтоПростойУчет(Реквизиты.Месяц, Реквизиты.Организация)) Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьДвиженияНачислениеНалоговНаЕНС(Движения, Реквизиты, ТаблицаУдержанныйНДФЛ, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыТаблицыУдержанногоНДФЛ(ТаблицаРеквизитов, ТаблицаУдержанныйНДФЛ)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+"Регистратор,"					// <ДокументСсылка>
	+"Период,"						// <Дата>
	+"Организация,"					// <СправочникСсылка.Организация>
	+"Месяц";						// <Дата>
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ТаблицаУдержанныйНДФЛ
	
	СписокОбязательныхКолонок = ""
	+"СчетУчета,"					// <ПланСчетовСсылка.Хозрасчетный>
	+"СрокУплаты,"					// <Дата>
	+"РегистрацияВНалоговомОргане,"	// <СправочникСсылка.РегистрацииВНалоговомОргане>
	+"Сумма";						// <Строка>
	
	Параметры.Вставить("ТаблицаУдержанныйНДФЛ", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаУдержанныйНДФЛ, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Функция НоваяТаблицаУдержанногоНДФЛДляОтраженияВУчете() Экспорт
	
	ТаблицаУдержанногоНДФЛ = НоваяТаблицаУдержанногоНДФЛ();
	ТаблицаУдержанногоНДФЛ.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	Возврат ТаблицаУдержанногоНДФЛ;
	
КонецФункции

Процедура СформироватьДвиженияНачислениеНалоговНаЕНС(Движения, Реквизиты, ТаблицаУдержанныйНДФЛ, Отказ)
	
	НалогиКУплате = ЕдиныйНалоговыйСчет.НовыйНачислениеНалоговНаЕНС();
	
	СводнаяТаблицаНДФЛ = ТаблицаУдержанныйНДФЛ.Скопировать(, "РегистрацияВНалоговомОргане,СчетУчета,СрокУплаты,Сумма");
	СводнаяТаблицаНДФЛ.Свернуть("РегистрацияВНалоговомОргане,СчетУчета,СрокУплаты", "Сумма");
	
	Для каждого СтрокаТаблицы Из СводнаяТаблицаНДФЛ Цикл
			
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.СчетУчета)
			Или Не ЗначениеЗаполнено(СтрокаТаблицы.СрокУплаты)
			Или СтрокаТаблицы.Сумма = 0 Тогда
			// нет ключевых значений
			Продолжить;
		КонецЕсли;
		Если Не ЕдиныйНалоговыйСчет.ЭтоПростойУчет(СтрокаТаблицы.СрокУплаты, Реквизиты.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = НалогиКУплате.Добавить();
		НоваяСтрока.СчетУчета = СтрокаТаблицы.СчетУчета;
		НоваяСтрока.СрокУплаты = СтрокаТаблицы.СрокУплаты;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
		НомерСубконто = ЕдиныйНалоговыйСчет.НомерВидаСубконтоНаСчете(
			СвойстваСчета, "ВидыПлатежейВГосБюджет", "ПеречислениеСсылка.ВидыПлатежейВГосБюджет");
		Если НомерСубконто > 0 Тогда
			НоваяСтрока["Субконто" + НомерСубконто] = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		КонецЕсли;
		НомерСубконто = ЕдиныйНалоговыйСчет.НомерВидаСубконтоНаСчете(
			СвойстваСчета, "РегистрацияВНалоговомОргане", "СправочникСсылка.РегистрацииВНалоговомОргане");
		Если НомерСубконто > 0 Тогда
			НоваяСтрока["Субконто" + НомерСубконто] = СтрокаТаблицы.РегистрацияВНалоговомОргане;
		КонецЕсли;
			
		НоваяСтрока.СуммаНалога = СтрокаТаблицы.Сумма;
		НоваяСтрока.Начислено = СтрокаТаблицы.Сумма;
		НоваяСтрока.Сумма = СтрокаТаблицы.Сумма;
		
		НоваяСтрока.НачалоПериода = Реквизиты.Период;
		НоваяСтрока.КонецПериода = Реквизиты.Период;
		
		НоваяСтрока.Содержание = НСтр("ru = 'Учет удержанного НДФЛ в составе совокупной налоговой обязанности'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
	КонецЦикла;
	
	ЕдиныйНалоговыйСчет.СформироватьДвиженияНачислениеНалоговНаЕНС(НалогиКУплате, Реквизиты, Движения);
	
КонецПроцедуры

#КонецОбласти


