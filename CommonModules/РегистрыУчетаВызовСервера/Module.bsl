////////////////////////////////////////////////////////////////////////////////
// Отчетность по регистрам учета 
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Сохраняет регистр в виде табличного документа в базу данных.
//
// Параметры:
//  СвойстваРегистраУчета - Структура - см. РегистрыУчетаКлиент.СохранитьРегистрУчета().
//
// Возвращаемое значение:
//  Структура   - данные документа, сохраненного в информационной базе.
//
Функция СохранитьРегистрУчета(СвойстваРегистраУчета) Экспорт
	
	РегистрУчета = Документы.РегистрУчета.СоздатьДокумент();
	
	РегистрУчета.Заполнить(СвойстваРегистраУчета);
	
	ПечатнаяФорма = ПолучитьИзВременногоХранилища(СвойстваРегистраУчета.ТаблицаРегистра);
	
	ПутьКФайлуMXLВоВременномХранилище = СформироватьРегистрУчетаВMXL(ПечатнаяФорма, РегистрУчета.Описание);
	
	ФайлыРегистраУчета = Новый Массив;

	ОписаниеФайлаРегистраУчета = ОписаниеФайлаРегистраУчета();
	
	ОписаниеФайлаРегистраУчета.ПутьКФайлуВоВременномХранилище = ПутьКФайлуMXLВоВременномХранилище;
	ОписаниеФайлаРегистраУчета.Расширение = "mxl";
	
	ФайлыРегистраУчета.Добавить(ОписаниеФайлаРегистраУчета);
	
	Если СвойстваРегистраУчета.Подписать Тогда
		
		ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
		ФорматСохраненияФайла = ПолучитьФорматСохраненияРегистров();

		// Если пользовательский формат - mxl, то дополнительной конвертации не требуется.
		Если ФорматСохраненияФайла <> Перечисления.ФорматыСохраненияОтчетов.MXL Тогда
		
			НастройкиФормата = ТаблицаФорматов.Найти(ФорматСохраненияФайла, "Ссылка");
			
			Если НастройкиФормата = Неопределено Тогда
			
				// Запись в журнал регистрации не требуется
				ТекстСообщения = НСтр("ru = 'Задан некорректный формат сохранения регистров учета, регистр учета не сохранен.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Возврат Неопределено;
			
			КонецЕсли;

			ПечатнаяФорма = ПолучитьИзВременногоХранилища(СвойстваРегистраУчета.ТаблицаРегистра);
			
			ПутьКФайлуВоВременномХранилище = 
				СформироватьРегистрУчетаВПользовательскомФормате(ПечатнаяФорма, НастройкиФормата, РегистрУчета.Описание);
				
			ОписаниеФайлаРегистраУчета = ОписаниеФайлаРегистраУчета();
			
			ОписаниеФайлаРегистраУчета.ПутьКФайлуВоВременномХранилище = ПутьКФайлуВоВременномХранилище;
			ОписаниеФайлаРегистраУчета.Расширение = НастройкиФормата.Расширение;
			
			ФайлыРегистраУчета.Добавить(ОписаниеФайлаРегистраУчета);
			
		КонецЕсли;
	
	КонецЕсли;
	
	РегистрУчета.ДополнительныеСвойства.Вставить("ФайлыРегистраУчета", ФайлыРегистраУчета);
	
	Попытка
		РегистрУчета.Записать();
		
	Исключение
		// Запись в журнал регистрации не требуется
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Пока ИнформацияОбОшибке.Причина <> Неопределено Цикл
			ИнформацияОбОшибке = ИнформацияОбОшибке.Причина;
		КонецЦикла;
		ТекстСообщения = НСтр("ru = 'Регистр учета не сохранен'") + Символы.ПС + ИнформацияОбОшибке.Описание;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецПопытки;
	
	ДанныеСохранения = Новый Структура;
	ДанныеСохранения.Вставить("РегистрУчета",	  РегистрУчета.Ссылка);
	ДанныеСохранения.Вставить("ОписаниеРегистра", РегистрУчета.Описание);
	ДанныеСохранения.Вставить("СвойстваПрисоединенныхФайлов", 
		ПолучитьСвойстваПрисоединенныхФайловРегистра(РегистрУчета.Ссылка));
	
	Возврат ДанныеСохранения;
	
КонецФункции

// Преобразовывает сохраненный регистр в пользовательский формат.
//
// Параметры:
//  РегистрУчета - ДокументСсылка.РегистрУчета - ссылка на сохраненный регистр учета.
//
// Возвращаемое значение:
//  Структура   - данные документа, сохраненного в информационной базе.
//
Функция ФайлРегистраУчетаВФорматеПользователя(РегистрУчета) Экспорт
	
	СвойстваПрисоединенныхФайлов = ПолучитьСвойстваПрисоединенныхФайловРегистра(РегистрУчета, Истина);
	
	СообщениеОбОшибке = "";
	
	Если СвойстваПрисоединенныхФайлов.Количество() = 0 Тогда
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = '%1 не содержит присоединенных файлов'"), РегистрУчета);
		ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
	НайденФайлВMXL = Ложь;
	НайденФайлСПодписью = Ложь;
	НайденФайлВФорматеПользователя = Ложь;
	
	Для Каждого ПрисоединенныйФайл Из СвойстваПрисоединенныхФайлов Цикл
		
		СвойстваФайла = ПрисоединенныйФайл.Значение;
		
		Если СвойстваФайла.Расширение = "mxl" Тогда
			НайденФайлВMXL = Истина;
			СвойстваФайлаMXL = СвойстваФайла;
		Иначе
			НайденФайлВФорматеПользователя = Истина;
			СвойстваФайлаВФорматеПользователя = СвойстваФайла;
		КонецЕсли;
			
		Если СвойстваФайла.ПодписанЭП = Истина Тогда
			НайденФайлСПодписью = Истина;
			СвойстваФайлаСПодписью = СвойстваФайла;
		КонецЕсли;
		
		ДанныеФайлаРегистра = СвойстваФайла.ДанныеФайла;

	КонецЦикла;
	
	Если НайденФайлСПодписью Тогда
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = '%1 уже подписан ЭП'"), РегистрУчета);
		ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
	Если НайденФайлВФорматеПользователя Тогда
		Возврат СвойстваФайлаВФорматеПользователя.ДанныеФайла.Ссылка; 
	КонецЕсли;

	Если Не НайденФайлВMXL Тогда
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = '%1 - не найден файл регистра'"), РегистрУчета);
		ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке);
		Возврат Неопределено;
	КонецЕсли;

	ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
	ФорматСохраненияФайла = ПолучитьФорматСохраненияРегистров();
	
	Если ФорматСохраненияФайла = Перечисления.ФорматыСохраненияОтчетов.MXL Тогда
		Возврат СвойстваФайлаMXL.ДанныеФайла.Ссылка; 
	КонецЕсли;
		
	НастройкиФормата = ТаблицаФорматов.Найти(ФорматСохраненияФайла, "Ссылка");
	
	Если НастройкиФормата = Неопределено Тогда
	
		// Запись в журнал регистрации не требуется
		ТекстСообщения = НСтр("ru = 'Задан некорректный формат сохранения регистров учета.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	
	КонецЕсли;

	ТабличныйДокумент = ПрочитатьРегистрУчетаИзMXL(СвойстваФайлаMXL.ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	
	ПутьКФайлуВоВременномХранилище = 
		СформироватьРегистрУчетаВПользовательскомФормате(ТабличныйДокумент, НастройкиФормата, СвойстваФайлаMXL.Описание);

	ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(СвойстваФайлаMXL.Описание);
	
	ПараметрыФайла = Новый Структура();
	ПараметрыФайла.Вставить("Автор", Пользователи.ТекущийПользователь());
	ПараметрыФайла.Вставить("ВладелецФайлов", РегистрУчета);
	ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяФайла);
	ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", ТекущаяДатаСеанса());
	ПараметрыФайла.Вставить("РасширениеБезТочки", НастройкиФормата.Расширение);
	
	ДобавленныйФайл = РаботаСФайлами.ДобавитьФайл(
		ПараметрыФайла, ПутьКФайлуВоВременномХранилище, "", СвойстваФайлаMXL.Описание);
				
	Возврат ДобавленныйФайл;
		
КонецФункции

// Общий обработчик события "ПриСозданииНаСервере" форм отчетов, являющихся регистрами учета.
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения - открываемая форма, поставляющая значения свойств.
//  Отчет        - ОтчетОбъект - объект, соответствующий основному реквизиту формы.
//
Процедура ПриСозданииНаСервере(Форма, Отчет) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВидРегистра") Тогда
		
		ПолноеИмяОтчета = Отчет.Метаданные().ПолноеИмя();
		
		КлючВарианта = Неопределено;
		Форма.Параметры.Свойство("КлючВарианта", КлючВарианта);

		Если КлючВарианта = Неопределено И Отчет.СхемаКомпоновкиДанных <> Неопределено Тогда
			
			КлючВарианта = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек[0].Имя;
			
		КонецЕсли;	
		
		Форма.ВидРегистра = Справочники.ВидыРегистровУчета.ПолучитьВидРегистраБухгалтерскогоУчетаДляОтчета(ПолноеИмяОтчета, КлючВарианта);
		
		УстановитьВидимостьКомандСохранения(Форма);
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ИспользуетсяЭП") Тогда
		Форма.ИспользуетсяЭП = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает формат сохранения регистров учета в базе данных.
//
Функция ПолучитьФорматСохраненияРегистров() Экспорт
	
	Возврат Документы.РегистрУчета.ПолучитьФорматСохраненияРегистров();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция КопияДвоичныхДанныхФайла(ДанныеФайлаMXL, УникальныйИдентификатор) Экспорт

	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайлаMXL.СсылкаНаДвоичныеДанныеФайла);

	АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
	
	Возврат АдресХранилища;

КонецФункции

// Возвращает документ в формате MXL со штампом электронной подписи.
//
Функция ДокументСоШтампом(ДанныеФайлаMXL, ДанныеФайлаРегистра, Организация) Экспорт

	ТабличныйДокумент = ПрочитатьРегистрУчетаИзMXL(ДанныеФайлаMXL.СсылкаНаДвоичныеДанныеФайла);

	ПараметрыШтампа = Новый Структура;
	ПараметрыШтампа.Вставить("ТекстОтметки", "");
	ПараметрыШтампа.Вставить("Логотип");
	
	МодульЭлектроннаяПодпись = ОбщегоНазначения.ОбщийМодуль("ЭлектроннаяПодпись");
	ЭлектронныеПодписи = МодульЭлектроннаяПодпись.УстановленныеПодписи(ДанныеФайлаРегистра.Ссылка);
	
	Штампы = Новый Массив;
	
	Если ЭлектронныеПодписи.Количество() > 0 Тогда
		Подпись = ЭлектронныеПодписи[0];
		
		Сертификат = Подпись.Сертификат;
		СертификатКриптографии = Новый СертификатКриптографии(Сертификат.Получить());
		РаботаСФайламиПереопределяемый.ПриПечатиФайлаСоШтампом(ПараметрыШтампа, СертификатКриптографии);
		
		Штамп = ШтампВизуализацииЭлектроннойПодписи(СертификатКриптографии, Организация,
		Подпись.ДатаПодписи, ПараметрыШтампа.ТекстОтметки, ПараметрыШтампа.Логотип);
		
		Штампы.Добавить(Штамп);
	КонецЕсли;
	
	МодульЭлектроннаяПодпись.ДобавитьШтампыВТабличныйДокумент(ТабличныйДокумент, Штампы);
	
	Если ТабличныйДокумент.ОбластьПечати <> Неопределено Тогда
	
		ТабличныйДокумент.ОбластьПечати = ТабличныйДокумент.Область(
			ТабличныйДокумент.ОбластьПечати.Верх,
			ТабличныйДокумент.ОбластьПечати.Лево, 
			ТабличныйДокумент.ВысотаТаблицы + 1,
			ТабличныйДокумент.ОбластьПечати.Право);
	
	КонецЕсли;
	
	Возврат ТабличныйДокумент;

КонецФункции

// Возвращает документ в формате MXL без штампа электронной подписи.
//
Функция ДокументБезШтампа(ДанныеФайлаMXL) Экспорт

	ТабличныйДокумент = ПрочитатьРегистрУчетаИзMXL(ДанныеФайлаMXL.СсылкаНаДвоичныеДанныеФайла);
	
	Возврат ТабличныйДокумент;

КонецФункции

// Возвращает данные файлов подписей и сертификатов.
//
Функция ПолучитьПакетФайловИЭП(ДанныеФайла, ДанныеФайлаMXL, РегистрУчета, УникальныйИдентификатор) Экспорт
	
	Организация = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РегистрУчета, "Организация").Организация;
	
	ПерсональныеНастройки = ЭлектроннаяПодпись.ПерсональныеНастройки();
	СохранятьСертификатВместеСПодписью = ПерсональныеНастройки.СохранятьСертификатВместеСПодписью;
	РасширениеДляФайловПодписи = ПерсональныеНастройки.РасширениеДляФайловПодписи;
	
	ПараметрыПолученияПодписей = ЭлектроннаяПодпись.НовыйПараметрыПолученияПодписейОбъекта();
	ПараметрыПолученияПодписей.ВозвращатьСертификатыИзПодписей = Истина;
	КоллекцияПодписей = ЭлектроннаяПодпись.ПодписиОбъекта(ДанныеФайла.Ссылка, ПараметрыПолученияПодписей);
	
	МассивПередаваемыхФайлов = Новый Массив;
	
	ИмяФайлаРегистра = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ДанныеФайла.Наименование, ДанныеФайла.Расширение);	
	ОписаниеПередаваемогоФайлаПодписи = Новый ОписаниеПередаваемогоФайла(ИмяФайлаРегистра, ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	МассивПередаваемыхФайлов.Добавить(ОписаниеПередаваемогоФайлаПодписи);

	Если КоллекцияПодписей.Количество() > 0 Тогда

		ВсеСвойстваПодписи = КоллекцияПодписей[0];
		
		АдресПодписи = ПоместитьВоВременноеХранилище(
			ВсеСвойстваПодписи.Подпись, УникальныйИдентификатор);
			
		ИмяФайлаПодписи = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ДанныеФайла.Наименование, РасширениеДляФайловПодписи);	
		
		ОписаниеПередаваемогоФайлаПодписи = Новый ОписаниеПередаваемогоФайла(ИмяФайлаПодписи, АдресПодписи);
		МассивПередаваемыхФайлов.Добавить(ОписаниеПередаваемогоФайлаПодписи);
		
		Если СохранятьСертификатВместеСПодписью Тогда
		
			ДанныеПоСертификату = ДанныеПоСертификату(ВсеСвойстваПодписи, УникальныйИдентификатор);

			ИмяФайлаСертификата = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ДанныеФайла.Наименование,
				ДанныеПоСертификату.РасширениеСертификата);	
			
			ОписаниеПередаваемогоФайлаСертификата = Новый ОписаниеПередаваемогоФайла(ИмяФайлаСертификата, 
				ДанныеПоСертификату.АдресСертификата);
			МассивПередаваемыхФайлов.Добавить(ОписаниеПередаваемогоФайлаСертификата);
		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеФайлаMXL) Тогда
			
			ДокументСоШтампом = ДокументСоШтампом(ДанныеФайлаMXL, ДанныеФайла, Организация); 
			
			ПолноеИмяФайла = ПолучитьИмяВременногоФайла("mxl");
			ИмяВременнойПапки = ПолноеИмяФайла;
			ДокументСоШтампом.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.PDF);
			
			ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
			АдресДокументаСоШтампомЭП = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
			
			ФайловаяСистема.УдалитьВременныйКаталог(ИмяВременнойПапки);
			
			ИмяФайлаДокументаСоШтампомЭП = СтрШаблон(НСтр("ru = '%1 - со штампом ЭП.%2'"), ДанныеФайла.Наименование, "pdf");
			
			ОписаниеДокументаСоШтампомЭП = Новый ОписаниеПередаваемогоФайла(ИмяФайлаДокументаСоШтампомЭП, АдресДокументаСоШтампомЭП);
			МассивПередаваемыхФайлов.Добавить(ОписаниеДокументаСоШтампомЭП);
			
		КонецЕсли;

	КонецЕсли;
	
	Возврат МассивПередаваемыхФайлов;

КонецФункции

Функция СохранитьФайлВФорматеПользователя(ДанныеФайлаMXL) Экспорт

	ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
	ФорматСохраненияФайла = ПолучитьФорматСохраненияРегистров();
	НастройкиФормата = ТаблицаФорматов.Найти(ФорматСохраненияФайла, "Ссылка");
	
	Если НастройкиФормата = Неопределено Тогда
	
		// Запись в журнал регистрации не требуется
		ТекстСообщения = НСтр("ru = 'Задан некорректный формат сохранения регистров учета.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	
	КонецЕсли;

	ТабличныйДокумент = ПрочитатьРегистрУчетаИзMXL(ДанныеФайлаMXL.СсылкаНаДвоичныеДанныеФайла);
	
	ПутьКФайлуВоВременномХранилище = 
		СформироватьРегистрУчетаВПользовательскомФормате(ТабличныйДокумент, НастройкиФормата, ДанныеФайлаMXL.Наименование);
		
	КомпонентыИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ДанныеФайлаMXL.ИмяФайла);
	ИмяФайла = СтрШаблон("%1.%2", КомпонентыИмениФайла.ИмяБезРасширения, НастройкиФормата.Расширение);
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Адрес", ПутьКФайлуВоВременномХранилище);
	СтруктураВозврата.Вставить("ИмяФайла", ИмяФайла);
	СтруктураВозврата.Вставить("Расширение", НастройкиФормата.Расширение);
		
	Возврат СтруктураВозврата;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьРегистрУчетаВMXL(ПечатнаяФорма, Описание)
	Перем ИмяАрхива;
	
	ПолноеИмяФайла = ПолучитьИмяВременногоФайла("mxl");
	ИмяВременнойПапки = ПолноеИмяФайла;
	ПечатнаяФорма.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.MXL);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
	ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Новый УникальныйИдентификатор);
	
	ФайловаяСистема.УдалитьВременныйКаталог(ИмяВременнойПапки);
	
	Возврат ПутьВоВременномХранилище;
	
КонецФункции

Функция СформироватьРегистрУчетаВПользовательскомФормате(ПечатнаяФорма, НастройкиФормата, Описание)
	Перем ИмяАрхива;
	
	Если НастройкиФормата.ТипФайлаТабличногоДокумента = ТипФайлаТабличногоДокумента.HTML Тогда
		
		// подготовка временной папки
		ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
		СоздатьКаталог(ИмяВременнойПапки);
		
		// Формируем имя файла с учетом ограничений: 1) имя файла не длинее 150; 2) полный путь не длинее 255.
		ПолноеИмяФайла = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки);
		ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Описание);
		ИмяФайла = Лев(ИмяФайла, Мин(150, 255 - СтрДлина(ПолноеИмяФайла)));
		ПолноеИмяФайла = ПолноеИмяФайла + ИмяФайла;
		
		ПечатнаяФорма.Записать(ПолноеИмяФайла, НастройкиФормата.ТипФайлаТабличногоДокумента);
		
		ВставитьКартинкиВHTML(ПолноеИмяФайла);
		
	Иначе
		
		ПолноеИмяФайла = ПолучитьИмяВременногоФайла(Строка(НастройкиФормата.ТипФайлаТабличногоДокумента));
		ИмяВременнойПапки = ПолноеИмяФайла;
		ПечатнаяФорма.Записать(ПолноеИмяФайла, НастройкиФормата.ТипФайлаТабличногоДокумента);
		
	КонецЕсли;
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
	ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Новый УникальныйИдентификатор);
	
	ФайловаяСистема.УдалитьВременныйКаталог(ИмяВременнойПапки);
	
	Возврат ПутьВоВременномХранилище;
	
КонецФункции

Процедура ВставитьКартинкиВHTML(ИмяФайлаHTML)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать(ИмяФайлаHTML, КодировкаТекста.UTF8);
	ТекстHTML = ТекстовыйДокумент.ПолучитьТекст();
	
	ФайлHTML = Новый Файл(ИмяФайлаHTML);
	
	ИмяПапкиКартинок = ФайлHTML.ИмяБезРасширения + "_files";
	ПутьКПапкеКартинок = СтрЗаменить(ФайлHTML.ПолноеИмя, ФайлHTML.Имя, ИмяПапкиКартинок);
	
	// ожидается, что в папке будут только картинки
	ФайлыКартинок = НайтиФайлы(ПутьКПапкеКартинок, "*");
	
	Для Каждого ФайлКартинки Из ФайлыКартинок Цикл
		КартинкаТекстом = Base64Строка(Новый ДвоичныеДанные(ФайлКартинки.ПолноеИмя));
		КартинкаТекстом = "data:image/" + Сред(ФайлКартинки.Расширение,2) + ";base64," + Символы.ПС + КартинкаТекстом;
		
		ТекстHTML = СтрЗаменить(ТекстHTML, ИмяПапкиКартинок + "\" + ФайлКартинки.Имя, КартинкаТекстом);
	КонецЦикла;
		
	ТекстовыйДокумент.УстановитьТекст(ТекстHTML);
	ТекстовыйДокумент.Записать(ИмяФайлаHTML, КодировкаТекста.UTF8);
	
КонецПроцедуры

Функция ЕстьПравоНаСозданиеРегистровУчета()
	
	Возврат ПравоДоступа("Редактирование", Метаданные.Документы.РегистрУчета);
	
КонецФункции

Процедура УстановитьВидимостьКомандСохранения(Форма) Экспорт
	
	Если Не ЕстьПравоНаСозданиеРегистровУчета() Тогда
		
		СохранитьРегистрБухгалтерскогоУчета = Форма.Элементы.Найти("ГруппаСохранитьРегистрБухгалтерскогоУчета");

		Если СохранитьРегистрБухгалтерскогоУчета <> Неопределено Тогда
			
			 СохранитьРегистрБухгалтерскогоУчета.Видимость = Ложь;
			
		КонецЕсли;
		
		СохранитьРегистрБухгалтерскогоУчетаВсеДействия = Форма.Элементы.Найти("ГруппаСохранитьРегистрБухгалтерскогоУчетаВсеДействия");
		
		Если СохранитьРегистрБухгалтерскогоУчетаВсеДействия <> Неопределено Тогда
			
			 СохранитьРегистрБухгалтерскогоУчетаВсеДействия.Видимость = Ложь;
			
		 КонецЕсли;
		 
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСвойстваПрисоединенныхФайловРегистра(РегистрУчета, ПолучитьДанныеФайла = Ложь) Экспорт

	Возврат Документы.РегистрУчета.ПолучитьСвойстваПрисоединенныхФайловРегистра(РегистрУчета, ПолучитьДанныеФайла);

КонецФункции

Функция ПрочитатьРегистрУчетаИзMXL(СсылкаНаДвоичныеДанныеФайла)

	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(СсылкаНаДвоичныеДанныеФайла);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("mxl");
	
	ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
	
	Попытка
	
		ТабличныйДок = Новый ТабличныйДокумент;
		ТабличныйДок.Прочитать(ИмяВременногоФайла);
	
	Исключение
		
		ФайловаяСистема.УдалитьВременныйФайл(ИмяВременногоФайла);	
		
		ВызватьИсключение;
		
		Возврат Неопределено;
	
	КонецПопытки;
	
	ФайловаяСистема.УдалитьВременныйФайл(ИмяВременногоФайла);
	
	Возврат ТабличныйДок;

КонецФункции

// Возвращает табличный документ, содержащий штамп визуализации электронной подписи.
//
// Параметры:
//  Сертификат   - СертификатКриптографии - сертификат, которым подписан документ.
//  ДатаПодписи  - Дата - дата подписания документа.
//  ТекстОтметки - Строка - текст, выводящийся непосредственно под штампом и описывающий
//                          расположение подлинника документа.
//  ЛоготипОрганизации - Картинка - если не указан, то будет использована стандартная картинка.
//
// Возвращаемое значение:
//  ТабличныйДокумент - табличный документ, содержащий готовый штамп электронной подписи.
//
Функция ШтампВизуализацииЭлектроннойПодписи(Сертификат, Организация, ДатаПодписи = Неопределено, 
	ТекстОтметки = "", ЛоготипОрганизации = Неопределено)
	
	Если ЛоготипОрганизации = Неопределено И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Организации") Тогда
		Если Константы.ИспользоватьЛоготипОрганизацииВШтампеЭП.Получить() = Истина Тогда
			
			ФайлЛоготип = Справочники.Организации.ДвоичныеДанныеКартинкиОрганизации(
			Организация, "ФайлЛоготип");
			Если ЗначениеЗаполнено(ФайлЛоготип) Тогда
				ЛоготипОрганизации = Новый Картинка(ФайлЛоготип);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЛоготипОрганизации) Тогда
		ЛоготипОрганизации = БиблиотекаКартинок.ЭлектроннаяПодписьСиняя;
	КонецЕсли;
	
	СвойстваСертификата = ЭлектроннаяПодпись.СвойстваСертификата(Сертификат);
	
	ПериодДействия = НСтр("ru = 'с %1 по %2'");
	ПериодДействия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПериодДействия,
		Формат(СвойстваСертификата.ДатаНачала,    "ДЛФ=D"),
		Формат(СвойстваСертификата.ДатаОкончания, "ДЛФ=D"));
	
	ПараметрыШтампа = Новый Структура;
	ПараметрыШтампа.Вставить("ДатаПодписи", ДатаПодписи);
	ПараметрыШтампа.Вставить("НомерСертификата", СвойстваСертификата.СерийныйНомер);
	ПараметрыШтампа.Вставить("ВыдалСертификат", СвойстваСертификата.КемВыдан);
	ПараметрыШтампа.Вставить("ВладелецСертификата", СвойстваСертификата.КомуВыдан);
	ПараметрыШтампа.Вставить("СрокДействия", ПериодДействия);
	ПараметрыШтампа.Вставить("ТекстОтметки", ТекстОтметки);
	
	Штамп = Документы.РегистрУчета.ПолучитьМакет("ШтампЭП");
	ЗаполнитьЗначенияСвойств(Штамп.Параметры, ПараметрыШтампа);
	Если ЛоготипОрганизации <> Неопределено Тогда
		Штамп.Области.КартинкаЛоготип.Картинка = ЛоготипОрганизации;
	КонецЕсли;
	
	Возврат Штамп;
	
КонецФункции

Функция ОписаниеФайлаРегистраУчета()
	
	ОписаниеФайлаРегистраУчета = Новый Структура;
	
	ОписаниеФайлаРегистраУчета.Вставить("ПутьКФайлуВоВременномХранилище", "");
	ОписаниеФайлаРегистраУчета.Вставить("Расширение", "");
	
	Возврат ОписаниеФайлаРегистраУчета;

КонецФункции


Функция ДанныеПоСертификату(СведенияОбЭлектроннойПодписи, УникальныйИдентификатор)
	
	Результат = Новый Структура("РасширениеСертификата, АдресСертификата");
	ДанныеСертификата = СведенияОбЭлектроннойПодписи.Сертификат;
		
	Если ТипЗнч(ДанныеСертификата) = Тип("Строка") Тогда
		Результат.РасширениеСертификата = "txt";
		Результат.АдресСертификата = ПоместитьВоВременноеХранилище(
			ДвоичныеДанныеСтроки(ДанныеСертификата), УникальныйИдентификатор);
	Иначе
		Результат.РасширениеСертификата = "cer";
		Результат.АдресСертификата = ПоместитьВоВременноеХранилище(
			ДанныеСертификата, УникальныйИдентификатор);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция ДвоичныеДанныеСтроки(ДанныеСтроки)
	
	ВременныйФайл = ПолучитьИмяВременногоФайла();
	
	ЗаписьТекста = Новый ЗаписьТекста(ВременныйФайл, КодировкаТекста.UTF8);
	ЗаписьТекста.Записать(ДанныеСтроки);
	ЗаписьТекста.Закрыть();
	
	ДвоичныеДанныеСертификата = Новый ДвоичныеДанные(ВременныйФайл);
	
	УдалитьФайлы(ВременныйФайл);
	
	Возврат ДвоичныеДанныеСертификата;
	
КонецФункции

#КонецОбласти
