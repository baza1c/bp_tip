Процедура ВыполнитьЗагрузку(Форма, ПараметрыЗагрузки, ИмяПроцедурыУспешноеЗавершение = Неопределено) Экспорт
	ДлительнаяОперация = ИнтеграцияBnovoВызовСервера.НачатьЗагрузкуBNovo(Форма.УникальныйИдентификатор, ПараметрыЗагрузки);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Получаем данные из Bnovo'");
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьИзBnovoЗавершение", 
		ЭтотОбъект, 
		Новый Структура("Форма, ИмяПроцедурыУспешноеЗавершение, ПараметрыЗагрузки", Форма, ИмяПроцедурыУспешноеЗавершение, ПараметрыЗагрузки));
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
КонецПроцедуры 

Процедура ЗагрузитьИзBnovoЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполнения, "Статус") <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	Если Результат.ТребуетсяНастройка Тогда
		ВопросНастройкаИнтеграции(ДополнительныеПараметры.ПараметрыЗагрузки);
		Возврат;
	ИначеЕсли НЕ Результат.Результат Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Результат.ДатаЗагрузки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.ИмяПроцедурыУспешноеЗавершение) Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения(ДополнительныеПараметры.ИмяПроцедурыУспешноеЗавершение, ДополнительныеПараметры.Форма);
	Иначе
		ОповещениеОЗавершении = Неопределено;
	КонецЕсли;
	
	Для каждого ТипДокумента Из Результат.ИзмененныеТипыДокументов Цикл
		ОповеститьОбИзменении(ТипДокумента);
	КонецЦикла;
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.НастройкиИнтеграцииBnovo"));
	
	ПараметрыФормы = Новый Структура("ДатаЗагрузки", Результат.ДатаЗагрузки);
	
	ОткрытьФорму("Обработка.ИнтеграцияBnovo.Форма.Форма", ПараметрыФормы,,,,,ОповещениеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

Асинх Процедура ВопросНастройкаИнтеграции(ПараметрыЗагрузки)
	Перем ОтборОрганизация;
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(НСтр("ru = 'Отсутствуют настройки интеграции Bnovo'"));
	Если ПараметрыЗагрузки.Свойство("Организация", ОтборОрганизация) Тогда
		ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' по %1'"), ОтборОрганизация));
	КонецЕсли;
	ЧастиСтроки.Добавить(НСтр("ru = '.
		|
		|Настроить сейчас?'"));
	
	Ответ = Ждать ВопросАсинх(СтрСоединить(ЧастиСтроки), РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("РегистрСведений.НастройкиИнтеграцииBnovo.Форма.ПомощникПодключения",,ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры


