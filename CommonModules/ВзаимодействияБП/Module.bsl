
#Область СлужебныйПрограммныйИнтерфейс

// Заполняет во входящих письмах реквизиты Контрагент, КонтактноеЛицо и Ответственный
//
// Параметры:
//  МассивПисем - Массив из ДокументСсылка.ЭлектронноеПисьмоИсходящее - содержит ссылки на письма, которые необходимо заполнить
//
Процедура ДозаполнитьПисьма(МассивПисем) Экспорт
	
	// Из писем берем адрес отправителя и ссылку на входящее письмо.
	// По адресу ищем контрагента и контактное лицо (лид). Если нашли, подставляем в письмо.
	// Дополнительно заполняем ответственного, который указан в контрагенте или лиде.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭлектронноеПисьмоВходящее.Ссылка КАК ЭлектронноеПисьмоВходящее,
		|	ЭлектронноеПисьмоВходящее.ОтправительАдрес КАК ОтправительАдрес,
		|	ЭлектронноеПисьмоВходящее.ВзаимодействиеОснование КАК ВзаимодействиеОснование,
		|	ЕСТЬNULL(УчетныеЗаписиЭлектроннойПочты.ВладелецУчетнойЗаписи, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК ВладелецУчетнойЗаписи
		|ПОМЕСТИТЬ АдресаОтправителей
		|ИЗ
		|	Документ.ЭлектронноеПисьмоВходящее КАК ЭлектронноеПисьмоВходящее
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|		ПО ЭлектронноеПисьмоВходящее.УчетнаяЗапись = УчетныеЗаписиЭлектроннойПочты.Ссылка
		|ГДЕ
		|	ЭлектронноеПисьмоВходящее.Ссылка В(&МассивПисем)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контакты.Контрагент КАК Контрагент,
		|	Контакты.ЭлектронноеПисьмоВходящее КАК ЭлектронноеПисьмоВходящее,
		|	Контакты.КонтактноеЛицо КАК КонтактноеЛицо,
		|	ВЫБОР
		|		КОГДА Контакты.ВладелецУчетнойЗаписи <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|			ТОГДА Контакты.ВладелецУчетнойЗаписи
		|		КОГДА НЕ КонтактныеЛица.Ссылка ЕСТЬ NULL
		|				И КонтактныеЛица.Лид
		|				И КонтактныеЛица.ОбъектВладелец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА КонтактныеЛица.Ответственный
		|		ИНАЧЕ Контрагенты.Ответственный
		|	КОНЕЦ КАК Ответственный,
		|	ВЫБОР
		|		КОГДА НЕ Контрагенты.Ссылка ЕСТЬ NULL
		|				И НЕ КонтактныеЛица.Ссылка ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА НЕ Контрагенты.Ссылка ЕСТЬ NULL
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Приоритет,
		|	Контакты.ВзаимодействиеОснование КАК ВзаимодействиеОснование
		|ИЗ
		|	(ВЫБРАТЬ
		|		КонтрагентыКонтактнаяИнформация.Ссылка КАК Контрагент,
		|		Контакты.ЭлектронноеПисьмоВходящее КАК ЭлектронноеПисьмоВходящее,
		|		ЗНАЧЕНИЕ(Справочник.КонтактныеЛица.ПустаяСсылка) КАК КонтактноеЛицо,
		|		Контакты.ВзаимодействиеОснование КАК ВзаимодействиеОснование,
		|		Контакты.ВладелецУчетнойЗаписи КАК ВладелецУчетнойЗаписи
		|	ИЗ
		|		АдресаОтправителей КАК Контакты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
		|			ПО (НРЕГ(ВЫРАЗИТЬ(КонтрагентыКонтактнаяИнформация.Представление КАК СТРОКА(100))) = НРЕГ(Контакты.ОтправительАдрес))
		|				И (КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
		|	ГДЕ
		|		НЕ КонтрагентыКонтактнаяИнформация.Ссылка.ПометкаУдаления
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КонтактныеЛицаКонтактнаяИнформация.Ссылка.ОбъектВладелец,
		|		Контакты.ЭлектронноеПисьмоВходящее,
		|		КонтактныеЛицаКонтактнаяИнформация.Ссылка,
		|		Контакты.ВзаимодействиеОснование,
		|		Контакты.ВладелецУчетнойЗаписи
		|	ИЗ
		|		АдресаОтправителей КАК Контакты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
		|			ПО (НРЕГ(ВЫРАЗИТЬ(КонтактныеЛицаКонтактнаяИнформация.Представление КАК СТРОКА(100))) = НРЕГ(Контакты.ОтправительАдрес))
		|				И (КонтактныеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
		|	ГДЕ
		|		НЕ КонтактныеЛицаКонтактнаяИнформация.Ссылка.ПометкаУдаления) КАК Контакты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО Контакты.Контрагент = Контрагенты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ПО Контакты.КонтактноеЛицо = КонтактныеЛица.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|ИТОГИ ПО
		|	ЭлектронноеПисьмоВходящее";
	
	Запрос.УстановитьПараметр("МассивПисем", МассивПисем);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаЭлектронноеПисьмоВходящее = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ИмяСобытия = НСтр("ru = 'Данные.Изменение'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	// Обходим результат с группировками по электронным письмам 
	Пока ВыборкаЭлектронноеПисьмоВходящее.Следующий() Цикл
		ВходящееПисьмоОбъект = ВыборкаЭлектронноеПисьмоВходящее.ЭлектронноеПисьмоВходящее.ПолучитьОбъект();
		КонтактнаяИнформацияВыборка = ВыборкаЭлектронноеПисьмоВходящее.Выбрать();
		Если КонтактнаяИнформацияВыборка.Следующий() Тогда
			ВходящееПисьмоОбъект.Предмет = КонтактнаяИнформацияВыборка.ВзаимодействиеОснование;
			ЗаполнитьЗначенияСвойств(ВходящееПисьмоОбъект, КонтактнаяИнформацияВыборка, , "Ответственный");
			Если ЗначениеЗаполнено(КонтактнаяИнформацияВыборка.Ответственный) Тогда
				ВходящееПисьмоОбъект.Ответственный = КонтактнаяИнформацияВыборка.Ответственный;
			ИначеЕсли Не ЗначениеЗаполнено(ВходящееПисьмоОбъект.Ответственный) Тогда
				ВходящееПисьмоОбъект.Ответственный = ТекущийПользователь;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			ВходящееПисьмоОбъект.Записать();
		Исключение
			ТекстОшибки = СтрШаблон(НСтр("ru = 'При изменении клиента во входящем письме произошла ошибка:'
												|%1", КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает значение признака рассмотрено, которое определяется по условию если среди
// входящих писем есть хотя бы 1 рассмотреное письмо, то возвращаем Ложь, в противном случае Истина
// Параметры:
//  ВыделенныеСтроки - Массив из ДокументСсылка.ЭлектронноеПисьмоИсходящее - содержит ссылки на письма
//   
// Возвращаемое значение:
//   Булево - значение признака рассмотрения
//
Функция ВычислитьЗначениеПризнака(ВыделенныеСтроки) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПредметыПапкиВзаимодействий.Взаимодействие
	|ИЗ
	|	РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
	|ГДЕ
	|	ПредметыПапкиВзаимодействий.Рассмотрено
	|	И ПредметыПапкиВзаимодействий.Взаимодействие В (&МассивВзаимодействий)";

	Запрос.УстановитьПараметр("МассивВзаимодействий", ВыделенныеСтроки);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

// Возвращает параметры для исходящего электронного письма
// Параметры:
//  УчетнаяЗаписьЭлектроннойПочты - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись, с которой будет отправлено письмо.
//  ФорматСообщения - ПеречислениеСсылка.СпособыРедактированияЭлектронныхПисем - формат письма.
//  ДляНового - Булево - признак того, что исходящее письмо создается.
//
// Возвращаемое значение:
//   Структура   - структура содержащая параметры работы пользователя для исходящего письма.
//
Функция ПолучитьПараметрыРаботыПользователяДляИсходящегоЭлектронногоПисьма(УчетнаяЗаписьЭлектроннойПочты, ФорматСообщения,ДляНового) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Подпись", Неопределено);
	СтруктураВозврата.Вставить("УведомитьОДоставке", Ложь);
	СтруктураВозврата.Вставить("УведомитьОПрочтении", Ложь);
	СтруктураВозврата.Вставить("ОтображатьТелоИсходногоПисьма", Ложь);
	СтруктураВозврата.Вставить("ВключатьТелоИсходногоПисьма", Истина);
	
	Подпись = ОтправкаПочтовыхСообщенийПереопределяемый.ПодписьПисьма();

	ПодписьПростойТекст            = Взаимодействия.ПолучитьОбычныйТекстИзHTML(Подпись);
	ПодписьФорматированныйДокумент = Новый ФорматированныйДокумент();
	ПодписьФорматированныйДокумент.УстановитьHTML(Подпись, Новый Структура);
	
	Если Не ПустаяСтрока(ПодписьПростойТекст) Тогда
		Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст Тогда
			СтруктураВозврата.Подпись = Символы.ПС + Символы.ПС + ПодписьПростойТекст;
		Иначе
			ФорматированныйДокумент = ПодписьФорматированныйДокумент;
			ФорматированныйДокумент.Вставить(ФорматированныйДокумент.ПолучитьЗакладкуНачала(), ,
				ТипЭлементаФорматированногоДокумента.ПереводСтроки);
			ФорматированныйДокумент.Вставить(ФорматированныйДокумент.ПолучитьЗакладкуНачала(), ,
				ТипЭлементаФорматированногоДокумента.ПереводСтроки);
			СтруктураВозврата.Подпись = ФорматированныйДокумент;
		КонецЕсли;
	КонецЕсли;

	Возврат СтруктураВозврата;

КонецФункции

// Обработчик обновления, создает предопределенные папки для каждой настроенной учетной записи электронной почты
Процедура СоздатьПредопределенныеПапкиЭлектронныхПисем() Экспорт

	ИмяСобытия = НСтр("ru = 'Данные.Изменение'", ОбщегоНазначения.КодОсновногоЯзыка());
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка КАК УчетнаяЗапись,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПапкиЭлектронныхПисем.Ссылка) КАК КоличествоПапок
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПапкиЭлектронныхПисем КАК ПапкиЭлектронныхПисем
	|		ПО УчетныеЗаписиЭлектроннойПочты.Ссылка = ПапкиЭлектронныхПисем.Владелец
	|			И (ПапкиЭлектронныхПисем.ПредопределеннаяПапка)
	|ГДЕ
	|	НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка";

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Выборка.КоличествоПапок = 0 Тогда
		УправлениеЭлектроннойПочтой.СоздатьПредопределенныеПапкиЭлектронныхПисемДляУчетнойЗаписи(
			Выборка.УчетнаяЗапись);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// См. ВзаимодействияПереопределяемый.ПриЗаполненииНаборовЗначенийДоступа.
Процедура ПриЗаполненииНаборовЗначенийДоступаДляОбъектаВзаимодействия(Объект, Таблица) Экспорт
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ЭлектронноеПисьмоВходящее") Тогда
		ПриЗаполненииНаборовЗначенийДоступаДляЭлектронногоПисьмаВходящего(Объект, Таблица);
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ЭлектронноеПисьмоИсходящее") Тогда
		ПриЗаполненииНаборовЗначенийДоступаДляЭлектронногоПисьмаИсходящего(Объект, Таблица);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриЗаполненииНаборовЗначенийДоступаДляЭлектронногоПисьмаВходящего(Объект, Таблица)

	// Ограничение по "УчетныеЗаписиЭлектроннойПочты".
	НомерНабора = 1;

	СтрокаТаб = Таблица.Добавить();
	СтрокаТаб.НомерНабора     = НомерНабора;
	СтрокаТаб.ЗначениеДоступа = Объект.УчетнаяЗапись;

КонецПроцедуры

Процедура ПриЗаполненииНаборовЗначенийДоступаДляЭлектронногоПисьмаИсходящего(Объект, Таблица)
	
	// Ограничение по "УчетныеЗаписиЭлектроннойПочты".
	НомерНабора = 1;

	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.НомерНабора     = НомерНабора;
	СтрокаТаблицы.ЗначениеДоступа = Объект.УчетнаяЗапись;

КонецПроцедуры

#КонецОбласти
