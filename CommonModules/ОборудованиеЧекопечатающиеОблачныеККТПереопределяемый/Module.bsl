#Область ПрограммныйИнтерфейс

// Позволяет дополнить данные фискализации чека
// Параметры:
//  ОбщиеПараметры - Структура - реквизиты фискального чека
//  СтандартнаяОбработка - Булево.
Процедура ДополнитьДанныеДляОблачнойККТ(ОбщиеПараметры, СтандартнаяОбработка) Экспорт
	Если ЗначениеЗаполнено(ОбщиеПараметры.АдресРасчетов) 
		И ЗначениеЗаполнено(ОбщиеПараметры.МестоРасчетов) Тогда
	
		Возврат;
	КонецЕсли;
	
	ДанныеОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ОбщиеПараметры.Организация, ОбщиеПараметры.ДатаВремя);
	
	Если НЕ ЗначениеЗаполнено(ОбщиеПараметры.АдресРасчетов) Тогда
		ОбщиеПараметры.АдресРасчетов = ДанныеОрганизации.ЮридическийАдрес;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОбщиеПараметры.МестоРасчетов) Тогда
		ОбщиеПараметры.МестоРасчетов = ДанныеОрганизации.ЮридическийАдрес;
	КонецЕсли;
КонецПроцедуры 

Процедура ПриПолученииИдентификатораЧека(ДокументОперации, ИдентификаторЧекаВОчереди, СтандартнаяОбработка) Экспорт
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ОчередьЧековККТ.ИдентификаторЗаписи КАК ИдентификаторЧекаВОчереди,
	|	ОчередьЧековККТ.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	РегистрСведений.ОчередьЧековККТ КАК ОчередьЧековККТ
	|ГДЕ
	|	ОчередьЧековККТ.ДокументОснование = &ДокументОперации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		ИдентификаторЧекаВОчереди = Выборка.ИдентификаторЧекаВОчереди;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти
