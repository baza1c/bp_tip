#Область СлужебныеПроцедурыИФункции

Функция ПараметрыОплатыЭС(ДокументСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОплатаПлатежнойКартой.Ссылка КАК Ссылка,
	|	ОплатаПлатежнойКартой.СуммаСертификатамиНСПК КАК ОплатаСертификатНСПК,
	|	ОплатаПлатежнойКартой.СуммаДокумента КАК ОплатаВсего,
	|	ОплатаПлатежнойКартой.ИдентификаторКорзины КАК ИдентификаторКорзины,
	|	ОплатаПлатежнойКартой.ВидОплаты.ПодключаемоеОборудование КАК ИдентификаторУстройстваЭТ
	|ИЗ
	|	Документ.ОплатаПлатежнойКартой КАК ОплатаПлатежнойКартой
	|ГДЕ
	|	ОплатаПлатежнойКартой.Ссылка = &ДокументСсылка
	|	И ОплатаПлатежнойКартой.СуммаСертификатамиНСПК > 0
	|	И ОплатаПлатежнойКартой.ИдентификаторКорзины <> """"";
	
	Результат  = Новый Структура;
	Результат.Вставить("ОплатаВсего",          0);
	Результат.Вставить("ОплатаСертификатНСПК", 0);
	Результат.Вставить("ИдентификаторКорзины", "");
	Результат.Вставить("ИдентификаторУстройстваЭТ");
	
	ВыборкаРезультата = Запрос.Выполнить().Выбрать();
	Если ВыборкаРезультата.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаРезультата);
	КонецЕсли; 

	Возврат Результат;
КонецФункции 

// Преобразует таблицу значений, полученную по адресу, в массив структур.
//
// Параметры:
//   АдресХранилищаРасшифровкаПлатежа - строка.
//
// Возвращаемое значение:
//   Массив, значение - структура.
//
Функция ДанныеРасшифровкиПлатежа(АдресХранилищаРасшифровкаПлатежа) Экспорт

	ТаблицаДанных = ПолучитьИзВременногоХранилища(АдресХранилищаРасшифровкаПлатежа);
	Если ТаблицаДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаДанных);
	
КонецФункции

// Возвращает настройки системы с документом ОплатаПлатежнойКартой.
//
// Параметры:
//   Организация - СправочникСсылка.Организации
//   Дата        - Дата - Дата, для которой получается опция УчетБезЗакрывающихДокументов.
//
// Возвращаемое значение: 
//   Структура с ключами: РозницаВключена, УчетБезЗакрывающихДокументов, НапечататьЧек - Булево.
//
Функция ПараметрыНастроекДокумента(Организация, Дата) Экспорт
	
	ПараметрыРезультат = Новый Структура;
	ПараметрыРезультат.Вставить("РозницаВключена", ПолучитьФункциональнуюОпцию("ВедетсяРозничнаяТорговля"));
	ПараметрыРезультат.Вставить("УчетБезЗакрывающихДокументов",
		УчетКассовымМетодом.УчетБезЗакрывающихДокументовВозможен(Организация, Дата));
	ПараметрыРезультат.Вставить("НапечататьЧек", ПравоДоступа("Чтение", Метаданные.Справочники.ПодключаемоеОборудование));
	
	Возврат ПараметрыРезультат;
	
КонецФункции

// Возвращает данные строки документа.
//
// Параметры:
//   ПараметрыПлатежа - Структура.
//   ПолучатьДоговор  - Булево.
//
// Возвращаемое значение:
//   Структура - состав см. ОплатаПлатежнойКартойКлиентСервер.НоваяСтруктураСвойстваПлатежа().
//
Функция СвойстваСтрокРасшифровкиПлатежа(Знач ПараметрыПлатежа, Знач ПолучатьДоговор) Экспорт
	
	СвойстваПлатежа = ОплатаПлатежнойКартойКлиентСервер.НоваяСтруктураСвойстваПлатежа();
	
	Если ПолучатьДоговор Тогда
		ПараметрыПлатежа.ДоговорКонтрагента = УчетДенежныхСредствБП.ПолучитьДоговорКонтрагента(ПараметрыПлатежа);
	КонецЕсли;
	
	СвойстваПлатежа.УслугаНПД                     = ПараметрыПлатежа.УслугаНПД;
	СвойстваПлатежа.ДоговорКонтрагента            = ПараметрыПлатежа.ДоговорКонтрагента;
	СвойстваПлатежа.СчетНаОплату                  = ПараметрыПлатежа.СчетНаОплату;

	СвойстваПлатежа.СпособПогашенияЗадолженности  =
		ПредопределенноеЗначение("Перечисление.СпособыПогашенияЗадолженности.Автоматически");
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") 
		И Не ЗначениеЗаполнено(ПараметрыПлатежа.ДоговорКонтрагента) Тогда
		
		УстановитьРеквизитыРасчетовСКонтрагентом(ПараметрыПлатежа, СвойстваПлатежа);
		
	ИначеЕсли ЗначениеЗаполнено(ПараметрыПлатежа.ДоговорКонтрагента) Тогда
		
		УстановитьРеквизитыРасчетовСКонтрагентом(ПараметрыПлатежа, СвойстваПлатежа);
		
		СвойстваДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПараметрыПлатежа.ДоговорКонтрагента, "ВалютаВзаиморасчетов,РасчетыВУсловныхЕдиницах");
			
		СвойстваПлатежа.ДоговорКонтрагентаВалютаВзаиморасчетов     = СвойстваДоговора.ВалютаВзаиморасчетов;
		СвойстваПлатежа.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах = СвойстваДоговора.РасчетыВУсловныхЕдиницах;
		
		СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
			СвойстваДоговора.ВалютаВзаиморасчетов, ПараметрыПлатежа.Дата);
		СвойстваПлатежа.КурсВзаиморасчетов      = СтруктураКурса.Курс;
		СвойстваПлатежа.КратностьВзаиморасчетов = СтруктураКурса.Кратность;
		
	КонецЕсли;
	
	СвойстваПлатежа.ПорядокОтраженияАванса =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПлатежа, "ПорядокОтраженияАванса");
	Если Не ЗначениеЗаполнено(СвойстваПлатежа.ПорядокОтраженияАванса)
		И ПараметрыПлатежа.ВидОперации = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя Тогда
		УчетПСН.УстановитьПорядокОтраженияАванса(СвойстваПлатежа.ПорядокОтраженияАванса, ПараметрыПлатежа);
	КонецЕсли;
	
	Возврат СвойстваПлатежа;
	
КонецФункции

// Возвращает счета учета с контрагентом.
//
// Параметры:
//   ПараметрыПлатежа - Структура с ключами: Организация, Контрагент, ДоговорКонтрагента
//
// Возвращаемое значение:
//   Структура с ключами: СчетУчетаРасчетовСКонтрагентом, СчетУчетаРасчетовПоАвансам.
//
Функция СчетаУчетаРасчетовСКонтрагентом(ПараметрыОбъекта)
	
	СчетаУчета = Новый Структура("СчетУчетаРасчетовСКонтрагентом, СчетУчетаРасчетовПоАвансам");
	
	СтруктураСчетов = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(
		ПараметрыОбъекта.Организация, ПараметрыОбъекта.Контрагент, ПараметрыОбъекта.ДоговорКонтрагента);
	
	СчетаУчета.Вставить("СчетУчетаРасчетовСКонтрагентом", СтруктураСчетов.СчетРасчетовПокупателя);
	СчетаУчета.Вставить("СчетУчетаРасчетовПоАвансам",     СтруктураСчетов.СчетАвансовПокупателя);
	
	Возврат СчетаУчета;
	
КонецФункции

// Устанавливает реквизиты: СчетУчетаРасчетовСКонтрагентом, СчетУчетаРасчетовПоАвансам, СтавкаНДС.
//
// Параметры:
//   ПараметрыПлатежа - Структура с ключами: Организация, Контрагент, ДоговорКонтрагента, ВидОперации, Дата
//   СвойстваПлатежа  - Структура с ключами: СчетУчетаРасчетовСКонтрагентом, СчетУчетаРасчетовПоАвансам
//
Процедура УстановитьРеквизитыРасчетовСКонтрагентом(ПараметрыПлатежа, СвойстваПлатежа) Экспорт
	
	СчетаУчета = СчетаУчетаРасчетовСКонтрагентом(ПараметрыПлатежа);
	ЗаполнитьЗначенияСвойств(СвойстваПлатежа, СчетаУчета);
	
	Если ОплатаПлатежнойКартойКлиентСервер.ЭтоРасчетыСКонтрагентом(ПараметрыПлатежа.ВидОперации) Тогда
		
		СвойстваПлатежа.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.Автоматически;
		
		Если ПараметрыПлатежа.ПлательщикНДСПриУСН Тогда
			СвойстваПлатежа.СтавкаНДС = ПараметрыПлатежа.СтавкаНДСПриУСН;
		Иначе
			ПлательщикНДС = ПараметрыПлатежа.ПлательщикНДС;
			
			СвойстваПлатежа.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПользователя(ПараметрыПлатежа.Дата, ПлательщикНДС);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#Область ЧекиНПД

// Выполняет запись в журнал регистрации и вызывает исключение для пользователя.
//
// Параметры:
//   КраткоеПредставлениеОшибки   - Строка
//   ПодробноеПредставлениеОшибки - Строка
//
Процедура ПоказатьОшибкуАннулированияЧека(КраткоеПредставлениеОшибки, ПодробноеПредставлениеОшибки = Неопределено) Экспорт
	
	ЗаписьЖурналаРегистрации(
		ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		?(ПодробноеПредставлениеОшибки <> Неопределено, ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки));
	
	ВызватьИсключение КраткоеПредставлениеОшибки;
	
КонецПроцедуры

// Возвращает сведения о чеке документа-основания.
//
// Параметры:
//   ДокументОснование - ДокументСсылка - Ссылка на документ-основание.
//
// Возвращаемое значение:
//   Структура - Сведения о чеке. См. НовыйСведенияОЧеке().
//
Функция СведенияОЧекеНПД(СсылкаНаДокумент) Экспорт
	
	Возврат РегистрыСведений.ЧекиНПД.СведенияОЧеке(СсылкаНаДокумент);
	
КонецФункции

// Возвращает надпись-пояснение ставки НПД.
//
// Параметры:
//   Контрагент - СправочникСсылка.Контрагенты - контрагент-покупатель.
//
// Возвращаемое значение:
//   Строка - заголовок для реквизита.
//
Функция ОписаниеСтавкиНПД(Контрагент) Экспорт
	
	Возврат СтрШаблон(НСтр("ru = 'Ставка налога: %1%%'"),
				ИнтеграцияСПлатформойСамозанятые.СтавкаНПД(Контрагент));
	
КонецФункции

#КонецОбласти

#КонецОбласти

