
#Область ПрограммныйИнтерфейс

// Процедура заполняет реквизиты эквайринговой операции. Необходимо передавать эти параметры при оформлении возврата
//
// Параметры:
//  Основание  - <ДокументСсылка> - документ который является основанием документа по которому производится возврат.
//                     это может быть либо "Оплата платежной картой" когда отгрузки не было и просто возвращаем предоплату
//                     или "Возврат товаров от покупателя", когда возвращаем деньги за отгруженный и возвращенный товар
//  Реквизиты - <Структура> - реквизиты операции по карте по которой будет возврат
//                 платежные системы требуют при совершении возврата давать данные исходной операции
//
Процедура ЗаполнитьРеквизитыОперацииОснование(Основание, Реквизиты) Экспорт
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ОплатаПлатежнойКартой") Тогда
		
		СтруктураПолей = Новый Структура;
		Для каждого Реквизит Из Реквизиты Цикл
			// В структуре полей эквайринговой операции "СуммаОперации" соответсвует реквизиту "СуммаДокумента"
			Если Реквизит.Ключ = "СуммаОперации" Тогда
				СтруктураПолей.Вставить(Реквизит.Ключ, "СуммаДокумента");
			ИначеЕсли Реквизит.Ключ = "ДатаОплаты" Тогда
				СтруктураПолей.Вставить(Реквизит.Ключ, "Дата");
			Иначе
				СтруктураПолей.Вставить(Реквизит.Ключ);
			КонецЕсли; 
		КонецЦикла; 
		
		ЗаполнитьЗначенияСвойств(
			Реквизиты, 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, СтруктураПолей));
	Иначе
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Основание", Основание);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(СчетНаОплатуПокупателюШапка.Ссылка, РеализацияТоваровУслугТовары.СчетНаОплатуПокупателю) КАК СчетНаОплатуПокупателю,
		|	РеализацияТоваровУслугТовары.Ссылка КАК РеализацияТоваров
		|ПОМЕСТИТЬ ВТ_ОснованияОплаты
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ПО ВозвратТоваровОтПокупателяТовары.Номенклатура = РеализацияТоваровУслугТовары.Номенклатура
		|			И ВозвратТоваровОтПокупателяТовары.Ссылка.Сделка = РеализацияТоваровУслугТовары.Ссылка
		|			И ВозвратТоваровОтПокупателяТовары.Номенклатура = РеализацияТоваровУслугТовары.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателюШапка
		|		ПО (РеализацияТоваровУслугТовары.Ссылка.СчетНаОплатуПокупателю = СчетНаОплатуПокупателюШапка.Ссылка)
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
		|	ЕСТЬNULL(ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаОплаты,
		|	ЕСТЬNULL(ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.СсылочныйНомер, """") КАК СсылочныйНомер,
		|	ЕСТЬNULL(ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.НомерПлатежнойКарты, """") КАК НомерПлатежнойКарты,
		|	ЕСТЬNULL(ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.НомерЧекаЭТ, """") КАК НомерЧекаЭТ,
		|	ЕСТЬNULL(ОплатаПлатежнойКартойРасшифровкаПлатежа.Ссылка.СуммаДокумента, 0) КАК СуммаОперации
		|ИЗ
		|	ВТ_ОснованияОплаты КАК ВТ_ОснованияОплаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОплатаПлатежнойКартой.РасшифровкаПлатежа КАК ОплатаПлатежнойКартойРасшифровкаПлатежа
		|		ПО (ВТ_ОснованияОплаты.СчетНаОплатуПокупателю = ОплатаПлатежнойКартойРасшифровкаПлатежа.СчетНаОплату
		|					И ОплатаПлатежнойКартойРасшифровкаПлатежа.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
		|				ИЛИ ВТ_ОснованияОплаты.РеализацияТоваров = ОплатаПлатежнойКартойРасшифровкаПлатежа.Сделка)";
		
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(Реквизиты, Результат[0]);
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры 


// Возвращает ссылку на экземлпяр подключаемого оборудования по виду оплаты для документа "Оплата платежной картой"
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - ссылка на документ для которого надо получить идентификатор фискальной операции
//
// Возвращаемое значение:
// Структура
//  Идентифмкатор - СправочникСсылка.ПодключаемоеОборудование
//  ЭлектронныеСертификатыНСПК - возможность проверки сертификатов НСПК на данном оборудовании
Функция ПодключаемоеОборудованиеДокумента(ДокументСсылка) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторУстройстваЭТ",   Справочники.ПодключаемоеОборудование.ПустаяСсылка());
	Результат.Вставить("ЭлектронныеСертификатыНСПК", Ложь);
	
	// Для возврата
	Результат.Вставить("ИдентификаторКорзины",       "");
	Результат.Вставить("ЭтоВозврат",                 Ложь);
	Результат.Вставить("ЭтоЧекКоррекции",            Ложь);
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОплатаПлатежнойКартой") Тогда 
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, 
			"ВидОплаты.ПодключаемоеОборудование,ВидОперации, НомерЧекаККМ, ДокументОснование");
			
		Результат.ИдентификаторУстройстваЭТ = РеквизитыДокумента.ВидОплатыПодключаемоеОборудование;
		Результат.ЭтоВозврат                = РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ВозвратПокупателю;
		Результат.ЭтоЧекКоррекции           = ЗначениеЗаполнено(РеквизитыДокумента.НомерЧекаККМ);
		Если ЗначениеЗаполнено(Результат.ИдентификаторУстройстваЭТ) Тогда
			Отбор = Новый Структура("Оборудование", Результат.ИдентификаторУстройстваЭТ);
			Результат.ЭлектронныеСертификатыНСПК = (РегистрыСведений.ОборудованиеПоОрганизациям.Получить(Отбор).ЭлектронныеСертификатыНСПК = Истина);
		КонецЕсли; 
		
		// BasketID и сумму оплаты берем из основания
		Если Результат.ЭлектронныеСертификатыНСПК 
			И Результат.ЭтоВозврат 
			И ТипЗнч(РеквизитыДокумента.ДокументОснование) = Тип("ДокументСсылка.ОплатаПлатежнойКартой") Тогда
			
			Результат.ИдентификаторКорзины = 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыДокумента.ДокументОснование, "ИдентификаторКорзины");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Возвращает возможность использования подключаемого оборудования в конфигурации
//
// Параметры:
//  ТипыПО - Массив из ПеречисленияСсылка.ТипыПодключаемогоОборудования - список оборудования которое надо проверить
//
// Возвращаемое значение:
//  Булево - возможность использования оборудования в конфигурации
Функция ПоддерживаетсяПодключаемоеОборудование(ТипыПО) Экспорт
	Возврат МенеджерОборудованияБП.ИспользуетсяОборудование(ТипыПО);
КонецФункции

// Возвращает необходимость актуализации расчетов перед печатью чека
//
// Параметры:
//  Основание - ДокументСсылка - документ по которому печатается чек
//  УникальныйИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы
//
// Возвращаемое значение:
//  Структура, Неопределено 
//    * ДлительнаяОперация - структура содержащая параметры длительной операции
//    * АдресХранилищаСОшибками -  адрес временного хранилища содержащий ошибки выполнения процедуры
Функция ПроверитьНеобходимостьАктуализацииРасчетов(Основание, УникальныйИдентификаторФормы) Экспорт
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "Ссылка, Проведен, Дата, Организация"); 

	НеобходимаАктуализацияПередПечатьюЧека = Ложь;
	Если РеквизитыОснования.Проведен
		И ПолучитьФункциональнуюОпцию("ИспользоватьОтложенноеПроведение") Тогда
		
		// Проверим, есть ли необходимость актуализировать по договорам, используемым в документе.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Основание",   РеквизитыОснования.Ссылка);
		Запрос.УстановитьПараметр("Организация", РеквизитыОснования.Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетыСКонтрагентамиОтложенноеПроведение.ДоговорКонтрагента КАК ДоговорКонтрагента
		|ИЗ
		|	РегистрСведений.РасчетыСКонтрагентамиОтложенноеПроведение КАК РасчетыСКонтрагентамиОтложенноеПроведение
		|ГДЕ
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Документ = &Основание
		|	И РасчетыСКонтрагентамиОтложенноеПроведение.Организация = &Организация";
		ДоговорыДокумента = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДоговорКонтрагента");
		
		Если ДоговорыДокумента.Количество() > 0 Тогда
			
			МоментАктуальности = УчетВзаиморасчетовОтложенноеПроведение.МоментАктуальностиОтложенныхРасчетов(
				РеквизитыОснования.Организация, РеквизитыОснования.Дата, Неопределено, ДоговорыДокумента);
			Если МоментАктуальности <> Неопределено Тогда
				НеобходимаАктуализацияПередПечатьюЧека = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НеобходимаАктуализацияПередПечатьюЧека Тогда
		
		ПараметрыПроцедуры = УчетВзаиморасчетовОтложенноеПроведение.НовыеПараметрыРасчета();
		ПараметрыПроцедуры.Организация             = РеквизитыОснования.Организация;
		ПараметрыПроцедуры.ДатаОкончания           = РеквизитыОснования.Дата;
		ПараметрыПроцедуры.Документ                = РеквизитыОснования.Ссылка;
		ПараметрыПроцедуры.ОстанавливатьсяПоОшибке = Истина;
		ПараметрыПроцедуры.АдресХранилищаСОшибками = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификаторФормы);
		
		ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторФормы);
		ПараметрыВыполненияВФоне.НаименованиеФоновогоЗадания = НСтр("ru = 'Актуализация для печати чека'");
		ПараметрыВыполненияВФоне.КлючФоновогоЗадания = Основание;
		
		РезультатРасчета = ДлительныеОперации.ВыполнитьВФоне("УчетВзаиморасчетовОтложенноеПроведение.ВыполнитьОтложенныеРасчетыВФоне",
			ПараметрыПроцедуры, ПараметрыВыполненияВФоне);
			
		Результат = Новый Структура;
		Результат.Вставить("ДлительнаяОперация",      РезультатРасчета);
		Результат.Вставить("АдресХранилищаСОшибками", ПараметрыПроцедуры.АдресХранилищаСОшибками);
		
		Возврат Результат;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
КонецФункции

// Возвращает соответствие организации документа и указанной при фискализации кассы
//
// Параметры:
//  Основание - ДокументСсылка - документ по которому печатается чек
//  ИННККТ - Строка - ИНН организации полученный из параметров ККТ
//
// Возвращаемое значение:
//  Булево - организация документа соотвествует организации ККТ
Функция ОрганизацияККТСоотвествуетДокументу(ДокументСсылка, ИННККТ) Экспорт
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Организация");
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация);
	
	Возврат НЕ ЗначениеЗаполнено(ИННККТ) ИЛИ СведенияОбОрганизации.ИНН = ИННККТ;
КонецФункции

// Возвращает ИСТИНА, если по данному документу уже пробиты чеки, 
// используется если из документа можно напечать несколько чеков
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - документ по которому печатается чек
//  ПараметрыОбработчика - Структура
//   * ИмяОбъекта - Строка - имя объекта из которого печатается чек
//
// Возвращаемое значение:
//  Булево - ИСТИНА если есть операции по документу
Функция ЧекиПробитыНаФискальномУстройстве(ДокументСсылка, ПараметрыОбработчика) Экспорт
	
	Если ПараметрыОбработчика <> Неопределено Тогда
		
		Если ПараметрыОбработчика.Свойство("ТипОбъекта") Тогда
			Если ПараметрыОбработчика.ТипОбъекта = "ОбщийМодуль" Тогда
				МодульОбработчика = ОбщегоНазначения.ОбщийМодуль(ПараметрыОбработчика.ИмяОбъекта);
			Иначе // документ
				МодульОбработчика = Документы[ПараметрыОбработчика.ИмяОбъекта];
			КонецЕсли;
			Возврат МодульОбработчика.ЧекиПробитыНаФискальномУстройстве(ДокументСсылка)
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЕстьФискальныеОперацииПоДокументу(ДокументСсылка);
КонецФункции

// Возвращает ИСТИНА, если по данному документу уже пробит фискальный чек
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - документ по которому печатается чек
//
// Возвращаемое значение:
//  Булево - ИСТИНА если есть фискальные операции по документу

Функция ЧекПробитНаФискальномУстройстве(ДокументСсылка) Экспорт
	Возврат ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "НомерЧекаККМ")) 
		И НЕ ЕстьФискальныеОперацииПоДокументу(ДокументСсылка);
КонецФункции

// Запускает механизм групповой печати чеков
//
// Параметры:
// ПараметрыФормированияЧеков - структура
// ИдентификаторФормы - УникальныйИдентификатор
Функция ГрупповаяПечатьЧеков(ПараметрыФормированияЧека, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Печать чеков: формирование пакета чеков'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ПечатьФискальныхДокументов." + ПараметрыФормированияЧека.ПараметрыОбработчика.ИмяОбработчика,
		ПараметрыФормированияЧека);
	
КонецФункции 
	
Процедура ПриНачалеФискализацииЧекаВОчереди(ПараметрыОперации, ИдентификаторУстройства) Экспорт
		
	// Настройка печати и отправки чеков
	ПараметрыОборудования = МенеджерОборудованияБП.ПараметрыПодключаемогоОборудования(ИдентификаторУстройства);
	
	ПараметрыОперации.ОблачнаяККТ = ПараметрыОборудования.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ;
	
	// Заполняем настройки чеков в соответствии с настройками по умолчанию
	ОтправлятьEmail = ПараметрыОборудования.ОтправлятьEmail 
		И ЗначениеЗаполнено(ПараметрыОперации.ПокупательEmail);
		
	ОтправлятьSMS = ПараметрыОборудования.ОтправлятьSMS
		И НЕ ОтправлятьEmail // в ФФД одно поле на email и номер телефона, поэтому одновременно на почту и телефон отправить нельзя 
		И ЗначениеЗаполнено(ПараметрыОперации.ПокупательНомер);
	
	Если ОтправлятьEmail Тогда
		ПараметрыОперации.Отправляет1СEmail = ПараметрыОборудования.Отправляет1СEmail;
	Иначе
		ПараметрыОперации.ПокупательEmail = "";
	КонецЕсли;
	
	Если ОтправлятьSMS Тогда
		ПараметрыОперации.Отправляет1СSMS = ПараметрыОборудования.Отправляет1СSMS;
	Иначе
		ПараметрыОперации.ПокупательНомер = "";
	КонецЕсли; 
	
	// Если чек отправляется по email или SMS то его можно не печатать
	Если ОтправлятьEmail ИЛИ ОтправлятьSMS Тогда
		ПараметрыОперации.Электронно = НЕ ПараметрыОборудования.ПечататьЧек;
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает ИСТИНА если есть фискальные операции по документу по которым можно выбить чек коррекции
Функция ЕстьФискальныеОперацииПоДокументу(ДокументСсылка) Экспорт
	Если НЕ ЗначениеЗаполнено(ДокументСсылка) Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Возврат ОборудованиеЧекопечатающиеУстройства.ДанныеФискальнойОперации(ДокументСсылка) <> Неопределено;
КонецФункции

Функция ДобавитьЧекВОчередьЧековККТ(ПараметрыЧекаККТ, ИдентификаторУстройства) Экспорт
	ПараметрыПакетнойОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПакетнойОперацииВОчередиЧеков();
	ПараметрыЧекаККТ.КассаККМ = ИдентификаторУстройства;
	
	ИдентификаторЧека = РаспределеннаяФискализация.ДобавитьЧекВОчередьЧековККТ(ПараметрыЧекаККТ, ПараметрыПакетнойОперации, Истина);
	Возврат ЗначениеЗаполнено(ИдентификаторЧека);
КонецФункции

Процедура ПроверитьВозможностьПечатиЧеков(ОбъектСсылка, ПараметрыОбработчика, ПечататьЧек, ТекстПредупреждения) Экспорт
	
	Если ПараметрыОбработчика.Свойство("ТипОбъекта") Тогда
		Если ПараметрыОбработчика.ТипОбъекта = "ОбщийМодуль" Тогда
			МодульОбработчика = ОбщегоНазначения.ОбщийМодуль(ПараметрыОбработчика.ИмяОбъекта);
		Иначе // документ
			МодульОбработчика = Документы[ПараметрыОбработчика.ИмяОбъекта];
		КонецЕсли;
		МодульОбработчика.ПроверитьВозможностьПечатиЧеков(ОбъектСсылка, ПечататьЧек, ТекстПредупреждения)
	КонецЕсли;
	
КонецПроцедуры

Функция КлючФискальнойОперации(ДокументСсылка) Экспорт

	ПараметрыФискальнойОперации = ОборудованиеЧекопечатающиеУстройстваВызовСервера.ДанныеФискальнойОперации(ДокументСсылка);
	
	Если ПараметрыФискальнойОперации <> Неопределено Тогда
		Возврат Регистрысведений.ФискальныеОперации.СоздатьКлючЗаписи(ПараметрыФискальнойОперации);
	Иначе
		Возврат Неопределено;
	КонецЕсли; 

КонецФункции 

Функция ДлительнаяОперацияПроверкаСтатуса(ДокументОперации, ИдентификаторФормы) Экспорт
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "ПечатьФискальныхДокументов.СтатусПробитияЧека", ДокументОперации);
КонецФункции

#КонецОбласти 
