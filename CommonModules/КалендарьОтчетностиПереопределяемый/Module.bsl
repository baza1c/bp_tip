#Область ПрограммныйИнтерфейс

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ПриСозданииНаСервере
//
Процедура КалендарьОтчетностиПриСозданииНаСервере(Форма, Параметры, Отказ, СтандартнаяОбработка) Экспорт
	
	Форма.УчетЗарплатыВЭтойПрограмме = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	Форма.ПоказыватьМоиПриложения = КалендарьОтчетности.ПоказыватьМоиПриложения();
	Форма.ПравоНастройкиНалоговОтчетов = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НалогиОтчеты);
	Форма.ПоЗарплате = Ложь;
	Форма.Элементы.ГруппаПоЗарплате.Видимость = Ложь;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ЗаполнитьДоступныеОрганизации
//
Процедура ЗаполнитьДоступныеОрганизации(Форма, СписокОрганизаций) Экспорт
	
	ПодключенныеОрганизации = Неопределено;
	УстановитьПривилегированныйРежим(Истина);
	Если Форма.ПоказыватьМоиПриложения Тогда
		ПодключенныеОрганизации =
			РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
	КонецЕсли;
	
	ОтключенныеОрганизацииЭтогоПриложения = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		Форма.ТекущийПользователь,
		КалендарьОтчетностиКлиентСервер.КлючХраненияСпискаОтключенныхОрганизаций());
	УстановитьПривилегированныйРежим(Ложь);
	
	// Организации этого приложения
	ЕстьОтключенныеОрганизации = ТипЗнч(ОтключенныеОрганизацииЭтогоПриложения) = Тип("Массив");
	ОрганизацииЭтогоПриложения = Справочники.Организации.ДоступныеОрганизацииПоRLS();
	Для Каждого ОрганизацияПриложения Из ОрганизацииЭтогоПриложения Цикл
		Если ЕстьОтключенныеОрганизации И ОтключенныеОрганизацииЭтогоПриложения.Найти(ОрганизацияПриложения) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СписокОрганизаций.Добавить(ОрганизацияПриложения, Строка(ОрганизацияПриложения));
	КонецЦикла;
	
	// Организации подключенных приложений
	Если Форма.ПоказыватьМоиПриложения Тогда
		Для Каждого ПодключеннаяОрганизация Из ПодключенныеОрганизации Цикл
			Если ПодключеннаяОрганизация.ВыводитьВКалендарь Тогда
				СписокОрганизаций.Добавить(
					ПодключеннаяОрганизация.ОрганизацияИдентификатор, ПодключеннаяОрганизация.ОрганизацияНаименование);
			КонецЕсли;
		КонецЦикла;
		СписокОрганизаций.Добавить(
			"ОткрытьМоиПриложения", НСтр("ru='Добавить из Моих приложений'"), ,БиблиотекаКартинок.ДобавитьСтраницу);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ОрганизацииЭтогоПриложенияДляКалендаря
//
Процедура ЗаполнитьДоступныеОрганизацииПоRLS(СписокОрганизаций) Экспорт
	СписокОрганизаций = Справочники.Организации.ДоступныеОрганизацииПоRLS();
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ПриСозданииНаСервере
//
Процедура СобратьСтатистикуИспользования() Экспорт
	
	КалендарьОтчетности.СобратьСтатистикуИспользования();
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ПриСозданииНаСервере
//
Процедура ПодключитьКонтекстныеНовости(Форма) Экспорт
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		Форма,
		"БП.Обработка.КалендарьОтчетности",
		"Форма",
		НСтр("ru='Новости: Календарь отчетности'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ОрганизацияСсылка
//
Процедура ЗаполнитьСсылкуНаОрганизацию(Организация) Экспорт
	
	Если ТипЗнч(Организация) = Тип("Строка") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Организация = Справочники.ОрганизацииОблачныхПриложений.НайтиПоКоду(Организация);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ВремяОжиданияПолученияДанных
//
Процедура ЗаполнитьВремяОжиданияПолученияДанных(ВремяОжидания) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Организации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Организации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Организации.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	Организации.ЗаполнитьЗначения(1, "Количество");
	
	Организации.Свернуть("ПриложениеСсылка", "Количество");
	Организации.Сортировать("Количество УБЫВ");
	
	МаксимальноеКоличествоОрганизаций = Организации[0].Количество; // наибольшее число организаций в одном приложении
	
	// Времени по умолчанию всегда достаточно, чтобы получить сведения от приложения, в котором 3 организации.
	// Если организаций больше, то следует пропорционально увеличить время ожидания для получения ответа.
	КоличествоОрганизаций = 3;
	
	Если МаксимальноеКоличествоОрганизаций <= КоличествоОрганизаций Тогда
		Возврат;
	КонецЕсли;
	
	ВремяОжидания = Окр(ВремяОжидания * МаксимальноеКоличествоОрганизаций / КоличествоОрганизаций);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ПередатьРучнойСтатусОтчетаСервер
//
Процедура ПередатьСтатусОтчета(Параметры, ДлительнаяОперация, ПараметрыВыполнения) Экспорт
	
	ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьСтатусОтчета();
	КалендарьОтчетности.ПередатьСтатусКомментарийОтчета(ТипЗапроса, Параметры, ДлительнаяОперация, ПараметрыВыполнения);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ПередатьКомментарийОтчетаСервер
//
Процедура ПередатьКомментарийОтчета(Параметры, ДлительнаяОперация, ПараметрыВыполнения) Экспорт
	
	ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьКомментарийОтчета();
	КалендарьОтчетности.ПередатьСтатусКомментарийОтчета(ТипЗапроса, Параметры, ДлительнаяОперация, ПараметрыВыполнения);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.СформироватьОтчет
//
Процедура УстановитьПараметрыВыводаКалендаряОтчетности(ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ПравоНаВключениеОтчетов = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НалогиОтчеты)
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.НовыйДанныеПодключенныхПриложений
//
Процедура ИнициализироватьДанныеПодключенныхПриложений(Данные) Экспорт
	
	Данные.Вставить("ОжидающиеЗапросы", СообщенияОтчетностиОблачныхПриложений.НовыйЗапросыОблачнымПриложениям());
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ДанныеДляОтчета
//
Процедура ЗаполнитьПодключенныеОрганизации(ПодключенныеОрганизации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПодключенныеОрганизации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.НовыйДанныеОтчетности
//
Процедура ПриОпределенииСпискаТиповПравила(СписокТипов) Экспорт
	
	СписокТипов.Добавить(Тип("СправочникСсылка.ПравилаФинОтчетности"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ВходящийЗапросФССДляРасчетаПособия"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ИзвещениеФСС"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ОтказВВозмещенииВыплатРодителямДетейИнвалидов"));
	СписокТипов.Добавить(Тип("ДокументСсылка.УведомлениеОСтатусеВыплатыПособия"));
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ДанныеДляОтчета
//
Процедура ЗапроситьДанныеПодключенныхПриложений(Организации, Параметры, Результат) Экспорт
	
	ИмяКолонкиСсылкаНаПриложение = "ПриложениеСсылка";
	
	Приложения = Организации.Скопировать(, ИмяКолонкиСсылкаНаПриложение);
	Приложения.Свернуть(ИмяКолонкиСсылкаНаПриложение);
	
	ОшибкиПриложений = Новый Массив;
	Запросы = СообщенияОтчетностиОблачныхПриложений.НовыйЗапросыОблачнымПриложениям();
	
	КалендарьОтчетности.ЗапроситьДанныеОтчетности(Приложения, Организации, Параметры, Запросы, ОшибкиПриложений);
	
	Результат.Вставить("Выполнено", Запросы);
	Результат.Вставить("Ошибки", ОшибкиПриложений);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ОпроситьПодключенныеПриложения
//
Процедура ОпроситьПодключенныеПриложения(Запросы, ДанныеПриложений) Экспорт
	
	РезультатОпроса = СообщенияОтчетностиОблачныхПриложений.ПолучитьОтветыНаЗапросы(Запросы);
	
	ТаблицаДанных = Обработки.КалендарьОтчетности.НовыйДанныеОтчетности();
	ДанныеПриложений.Вставить("Данные", ТаблицаДанных);
	ДанныеПриложений.Вставить("ОжидающиеЗапросы", РезультатОпроса.ОжидающиеЗапросы);
	ДанныеПриложений.Вставить("ОшибкиПриложений", РезультатОпроса.ОшибкиПриложений);
	
	Если РезультатОпроса.Ответы.Количество() = 0 Тогда
		// Не удалось получить ответа ни от одного приложения.
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ПодключенныеОрганизации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого Ответ Из РезультатОпроса.Ответы Цикл
		
		СвойстваТипаДата = Новый Массив;
		СвойстваТипаДата.Добавить("Срок");
		СвойстваТипаДата.Добавить("НачалоВыполнения");
		СвойстваТипаДата.Добавить("КонецПериода");
	
		Данные = ОбщегоназначенияБП.СтруктураИзСтрокиJSON(Ответ.Данные, СвойстваТипаДата);
		
		Если Данные.ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаСписокОрганизаций() Тогда
			// Обработка ответа, содержащего список организаций.
			// Данные.Ответ - массив структур с полями:
			//   *Наименование - наименование организации
			//   *Идентификатор - уникальный идентификатор организации
			КалендарьОтчетности.ОбработатьОтветНаЗапросСпискаОрганизаций(
				Данные.Ответ, Ответ.Приложение, ПодключенныеОрганизации);
			
		ИначеЕсли Данные.ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаДанныеОтчетности() Тогда
			// Ответ содержит данный отчетности.
			// Данные.Ответ - таблица значений, преобразованная в массив структур,
			// см. Обработки.КалендарьОтчетности.НовыйДанныеОтчетности()
			КалендарьОтчетности.ОбработатьОтветНаЗапросДанныеОтчетности(
				ТаблицаДанных, Данные.Ответ, Ответ.Приложение, ПодключенныеОрганизации);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ОтметитьДанныеПолучены
//
Процедура ОтметитьДанныеПолучены(Запросы, ОжидающиеЗапросы, ОшибкиПриложений) Экспорт
	
	Для Каждого Запрос Из Запросы Цикл
		
		Если ОжидающиеЗапросы.Найти(Запрос.Приложение, "Приложение") <> Неопределено
			Или ОшибкиПриложений.Найти(Запрос.Приложение) <> Неопределено Тогда
			
			Продолжить;
		КонецЕсли;
		
		СообщенияОтчетностиОблачныхПриложений.ОтметитьДанныеПолучены(
			Запрос.Приложение,
			Запрос.ИдентификаторЗапроса);
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.НовыйДанныеПлатежей
//
Процедура ПриОпределенииОписанияТиповСчетУчета(ОписаниеТиповСчетУчета) Экспорт
	
	ОписаниеТиповСчетУчета = Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный");
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ДанныеПлатежей
//
Процедура ЗаполнитьДанныеПлатежей(ДанныеПлатежей, Параметры) Экспорт
	
	Если Не Параметры.ПоНалоговымПлатежам Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросПоЗадачамУплатаНалогов = Новый Запрос;
	ЗапросПоЗадачамУплатаНалогов.УстановитьПараметр("Организации", Параметры.Организации);
	ЗапросПоЗадачамУплатаНалогов.УстановитьПараметр("ДатаНачала",  Параметры.ДатаНачала);
	ЗапросПоЗадачамУплатаНалогов.УстановитьПараметр("ДатаКонца",   Параметры.ДатаКонца);
	ЗапросПоЗадачамУплатаНалогов.УстановитьПараметр(
		"ПравилаНалоговУплачиваемыхВнеЕНС", КалендарьОтчетности.ПравилаНалоговУплачиваемыхВнеЕНС(Параметры.ДатаНачала));
	
	ЗапросПоЗадачамУплатаНалогов.Текст =
	"ВЫБРАТЬ
	|	ПравилаНалоговУплачиваемыхВнеЕНС.Правило КАК Правило,
	|	ПравилаНалоговУплачиваемыхВнеЕНС.СчетУчета КАК СчетУчета
	|ПОМЕСТИТЬ ПравилаНалоговУплачиваемыхВнеЕНС
	|ИЗ
	|	&ПравилаНалоговУплачиваемыхВнеЕНС КАК ПравилаНалоговУплачиваемыхВнеЕНС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ЕСТЬNULL(ПравилаНалоговУплачиваемыхВнеЕНС.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет)) КАК СчетУчета
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаНалоговУплачиваемыхВнеЕНС КАК ПравилаНалоговУплачиваемыхВнеЕНС
	|		ПО ЗадачиБухгалтера.Правило = ПравилаНалоговУплачиваемыхВнеЕНС.Правило
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Регламент
	|		ПО ЗадачиБухгалтера.Правило = Регламент.Ссылка
	|ГДЕ
	|	ЗадачиБухгалтера.Организация В(&Организации)
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|	И ЗадачиБухгалтера.Срок МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И НЕ ЕСТЬNULL(Регламент.ВыполняетсяЕдинымПомощником, ЛОЖЬ)";
	
	Выборка = ЗапросПоЗадачамУплатаНалогов.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонЗапроса = "
	|ВЫБРАТЬ
	|	&Организация%1 КАК Организация,
	|	&СчетУчета%1 КАК СчетУчета,
	|	&Правило%1 КАК Правило,
	|	&Срок%1 КАК Срок,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстаток < 0
	|			ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстаток
	|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстаток >= 0 И ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > 0
	|			ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПлатежа,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстаток < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьЗадолженность
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНачала%1, &ДатаКонца%1, Период,, Счет = &СчетУчета%1, &ВидыСубконто, Организация = &Организация%1 И Субконто1 В (&ВидыНалоговыхПлатежей)) КАК ХозрасчетныйОстаткиИОбороты
	|";
	
	ТекстОбъединенияЗапросов = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ТекстПодвалЗапроса = "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Срок
	|";
	
	ТекстЗапроса = "";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ВидыНалоговыхПлатежей", Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.УстановитьПараметр("ВидыСубконто", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет);
	
	Счетчик = 0;
	Пока Выборка.Следующий() Цикл
		Счетчик = Счетчик + 1;
		
		Если Счетчик > 1 Тогда
			ТекстЗапроса = ТекстЗапроса + ТекстОбъединенияЗапросов;
		КонецЕсли;
		
		ТекстШаблонЗапроса = ШаблонЗапроса;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Выборка.СчетУчета);
		Если СвойстваСчета.КоличествоСубконто = 0 Тогда
			ТекстШаблонЗапроса = СтрЗаменить(ТекстШаблонЗапроса, "&ВидыСубконто", "");
			ТекстШаблонЗапроса = СтрЗаменить(ТекстШаблонЗапроса, "И Субконто1 В (&ВидыНалоговыхПлатежей)", "");
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + СтрЗаменить(ТекстШаблонЗапроса, "%1", Счетчик);
		
		Запрос.УстановитьПараметр("Организация" + Счетчик, Выборка.Организация);
		Запрос.УстановитьПараметр("Правило" + Счетчик,     Выборка.Правило);
		Запрос.УстановитьПараметр("Срок" + Счетчик,        Выборка.Срок);
		Запрос.УстановитьПараметр("ДатаНачала"+ Счетчик,   НачалоДня(Выборка.Срок));
		Запрос.УстановитьПараметр("ДатаКонца" + Счетчик,   КонецДня(Выборка.Срок));
		Запрос.УстановитьПараметр("СчетУчета" + Счетчик,   Выборка.СчетУчета);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса + ТекстПодвалЗапроса;
	
	// Среди будущих платежей будем отображать только ближайшие по каждой организации и счету
	КонтрольБудущихПлатежей = ДанныеПлатежей.СкопироватьКолонки("Организация, СчетУчета");
	
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Срок >= Параметры.НаДату Тогда
			ПараметрыОтбора = Новый Структура("Организация, СчетУчета", Выборка.Организация, Выборка.СчетУчета);
			Если КонтрольБудущихПлатежей.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
				НоваяСтрока = ДанныеПлатежей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
				НоваяСтрока = КонтрольБудущихПлатежей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыОтбора);
			КонецЕсли;
		Иначе
			НоваяСтрока = ДанныеПлатежей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			Если Выборка.ЕстьЗадолженность Тогда
				НоваяСтрока.Статус = СтатусыОтчета.Просрочено;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ВывестиДанныеПоОднойОрганизации
// См. Обработка.КалендарьОтчетности.ВывестиДанныеПоНесколькимОрганизациям
//
Процедура СтатистикаПоПоказателямДобавитьСобытие(ИмяСобытия) Экспорт
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяСобытия);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ДанныеПриложения
//
Процедура ПриУстановкеТекстаЗапросаДанныеПриложения(ТекстЗапроса) Экспорт
	
	ПодстрокаПоиска = "Расписание.Правило.Код КАК ИдентификаторПравила";
	ПодстрокаЗамены = "ВЫБОР
	|	КОГДА Расписание.Правило ССЫЛКА Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов
	|		ТОГДА Расписание.НаименованиеЗадачи
	|	КОГДА Расписание.Правило ССЫЛКА Документ.ТранспортноеСообщение
	|		ТОГДА ""Транспортное сообщение""
	|	КОГДА Расписание.Правило ССЫЛКА Документ.ВходящийЗапросФССДляРасчетаПособия
	|		ТОГДА ""ВходящийЗапросФССДляРасчетаПособия""
	|	КОГДА Расписание.Правило ССЫЛКА Документ.ИзвещениеФСС
	|		ТОГДА ""ИзвещениеФСС""
	|	КОГДА Расписание.Правило ССЫЛКА Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов
	|		ТОГДА ""ОтказВВозмещенииВыплатРодителямДетейИнвалидов""
	|	КОГДА Расписание.Правило ССЫЛКА Документ.УведомлениеОСтатусеВыплатыПособия
	|		ТОГДА ""УведомлениеОСтатусеВыплатыПособия""
	|	ИНАЧЕ Расписание.Правило.Код
	|КОНЕЦ КАК ИдентификаторПравила";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПодстрокаПоиска, ПодстрокаЗамены);
	
	ПодстрокаПоиска = "ЕСТЬNULL(Расписание.Правило.Владелец.Код, """") КАК КодНалога";
	ПодстрокаЗамены = "ВЫБОР
	|	КОГДА Расписание.Правило ССЫЛКА Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов
	|			ИЛИ Расписание.Правило ССЫЛКА Документ.ТранспортноеСообщение
	|		ТОГДА &КодНалогаТребованиеФНС
	|	КОГДА Расписание.Правило ССЫЛКА Документ.ВходящийЗапросФССДляРасчетаПособия ТОГДА
	|		&КодЗадачиДокументСЭДО
	|	КОГДА Расписание.Правило ССЫЛКА Документ.ИзвещениеФСС ТОГДА
	|		&КодЗадачиДокументСЭДО
	|	КОГДА Расписание.Правило ССЫЛКА Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов ТОГДА
	|		&КодЗадачиДокументСЭДО
	|	КОГДА Расписание.Правило ССЫЛКА Документ.УведомлениеОСтатусеВыплатыПособия ТОГДА
	|		&КодЗадачиДокументСЭДО
	|	ИНАЧЕ ЕСТЬNULL(Расписание.Правило.Владелец.Код, """")
	|КОНЕЦ КАК КодНалога";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПодстрокаПоиска, ПодстрокаЗамены);
	
	ПодстрокаПоиска = "NULL КАК Приложение";
	ПодстрокаЗамены = "ЗНАЧЕНИЕ(Справочник.ОблачныеПриложения.ПустаяСсылка) КАК Приложение";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПодстрокаПоиска, ПодстрокаЗамены);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ЗаписатьКомментарийОтчета
//
Процедура ПриЗаписиКомментарияОтчета() Экспорт
	
	КалендарьОтчетности.ЗаписатьСобытиеДобавленКомментарий();
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ДанныеДляОтчета
//
Процедура ДобавитьОрганизацииПодключенныхПриложений(ДанныеПодключенныхПриложений, ПодключенныеОрганизации) Экспорт
	
	// Добавим записи по организациям, по которым не получится уже получить данные.
	Для Каждого Запрос Из ДанныеПодключенныхПриложений.ОшибкиПриложений Цикл
		КалендарьОтчетностиКлиентСервер.ДобавитьОрганизацииПриложения(
			ДанныеПодключенныхПриложений.Данные, ПодключенныеОрганизации, Запрос);
	КонецЦикла;
	
	// Ожидающие запросы, ответы на которые ещё не удалось получить.
	Для Каждого Запрос Из ДанныеПодключенныхПриложений.ОжидающиеЗапросы Цикл
		Если Запрос.ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаДанныеОтчетности() Тогда
			КалендарьОтчетностиКлиентСервер.ДобавитьОрганизацииПриложения(
				ДанныеПодключенныхПриложений.Данные, ПодключенныеОрганизации, Запрос.Приложение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.НовыйДанныеОтчетности
//
Процедура ПриОпределенииСпискаТиповОрганизации(СписокТипов) Экспорт
	
	СписокТипов.Добавить(Тип("СправочникСсылка.ОрганизацииОблачныхПриложений"));
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.НовыйДанныеОтчетности
//
Процедура ПриОпределенииСпискаТиповПриложения(СписокТипов) Экспорт
	
	СписокТипов.Добавить(Тип("СправочникСсылка.ОблачныеПриложения"));
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ВывестиДанныеПоНесколькимОрганизациям
//
Процедура ПриЗаполненииНаименованияОрганизацииОблачногоПриложения(Организация, Наименование) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Наименование = Справочники.ОрганизацииОблачныхПриложений.НаименованиеОрганизации(Организация);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ПараметрыОбласти
//
Процедура ЗаполнитьПараметрыДействияНавигационнаяСсылка(ПараметрыДействия, Расписание) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПриложения = Справочники.ОблачныеПриложения.ДанныеПриложения(Расписание.Приложение);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Расписание.КодНалога = ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС() Тогда
		ПараметрыСсылки = КалендарьОтчетности.ПараметрыНавигационнойСсылкиТребованиеФНС(Расписание);
	ИначеЕсли Расписание.КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО()
		И ЗначениеЗаполнено(Расписание.ИдентификаторПравила) Тогда
		ПараметрыСсылки = КалендарьОтчетности.ПараметрыНавигационнойСсылкиДокументСЭДО(
			Расписание, ПараметрыДействия.КоличествоЗадач);
	Иначе
		ПараметрыСсылки = КалендарьОтчетности.ПараметрыНавигационнойСсылки(Расписание);
	КонецЕсли;
		
	ПараметрыДействия.НавигационнаяСсылка =
		КалендарьОтчетности.НавигационнаяСсылкаНаПомощник(ДанныеПриложения.Адрес, ПараметрыСсылки);
	ПараметрыДействия.АдресПриложения = ДанныеПриложения.Адрес;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.ПараметрыОбласти
//
Процедура ЗаполнитьПараметрыДействияПараметрыЗапросаИзмененияОтчета(ПараметрыЗапроса, Расписание, ЗадачиПоСроку) Экспорт
	
	ПараметрыЗапроса.ИдентификаторОрганизации = Расписание.ИдентификаторОрганизации;
	ПараметрыЗапроса.ИдентификаторЗадачи = Расписание.КодНалога;
	ПараметрыЗапроса.ИдентификаторПравила = Расписание.ИдентификаторПравила;
	ПараметрыЗапроса.ПериодСобытия = Расписание.КонецПериода;
	ПараметрыЗапроса.РегистрацияВНалоговомОргане = Расписание.КодФНС;
	ПараметрыЗапроса.Срок = Расписание.Срок;
	ПараметрыЗапроса.НачалоВыполнения = Расписание.НачалоВыполнения;
	ПараметрыЗапроса.Наименование = Расписание.НаименованиеЗадачи;
	ПараметрыЗапроса.Периодичность = Расписание.ПредставлениеПериодичности;
	ПараметрыЗапроса.НаименованиеСокращенное = Расписание.НаименованиеНалога;
	
	Если ЗначениеЗаполнено(Расписание.Действие) Тогда
		ИндексДействия = Перечисления.ВидыДействийКалендаряБухгалтера.Индекс(Расписание.Действие);
		ПараметрыЗапроса.Действие =
			Метаданные.Перечисления.ВидыДействийКалендаряБухгалтера.ЗначенияПеречисления[ИндексДействия].Имя;
		// В одной ячейке может быть несколько документов СЭДО
		Если Расписание.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО Тогда
			ПараметрыЗапроса.ИдентификаторыТребований = ЗадачиПоСроку.ВыгрузитьКолонку("ИдентификаторТребования");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти