///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.НастройкиПлатежныхСистем".
// ОбщийМодуль.НастройкиПлатежныхСистемСлужебный.
// 
// Служебные серверные методы по работе с платежными системами:
//  - методы для взаимодействия со сторонними библиотеками;
//  - методы для интеграции с другими подсистемами БИП;
//  - вспомогательные методы для формирования дерева на форме общего списка настроек подключения;
//  - методы для вывода кнопок на форму общего списка настроек платежных систем;
//  - коды ошибок, идентификаторы статусов при обмене с сервисом;
//  - общие методы платежных систем: хост сервиса, идентификатор сервиса по имени, работа с датой в JSON,
//    настройки отображения команд, длительностью операций, проверка назначения платежа;
//  - обработчики регламентных заданий, работа с журналом регистрации;
//  - получение статусов операций.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ТехнологияСервисаОчередьЗаданий

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(
		Метаданные.РегламентныеЗадания.ЗагрузкаСтатусовОперацийПлатежныхСистем.ИмяМетода);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияПодсистемИнтернетПоддержкиПользователей

#Область БазоваяФункциональностьБИП

// Вызывается при изменении логина и пароля пользователя ИПП в
// информационной базе из всех контекстов использования библиотеки.
//
// Параметры:
//  Логин - Строка - логин пользователя Интернет-поддержки.
//
Процедура ПриИзмененииДанныхАутентификации(Логин) Экспорт

	Если ЗначениеЗаполнено(Логин) Тогда
		
		Если ЕстьНастройкиПодключения() Тогда
			ДобавитьЗаданиеЗагрузкаСтатусовОпераций();
		КонецЕсли;
		
	Иначе
		УдалитьЗаданияЗагрузкаСтатусовОперацийПлатежныхСистем();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет описание используемых в подсистеме хостов сервисов Интернет-поддержки.
//   Разные платежные системы могут использовать один и тот же адрес сервиса.
//   В связи с этим, при выводе в список хостов будет отображаться только последняя из выведенных.
//   Поэтому, если в конфигурации есть подсистема НастройкиПлатежныхСистем, то она заменяет все другие подсистемы,
//   использующие этот же адрес сервиса.
//   Если зависимые подсистемы будут использовать другой адрес сервиса, то их нужно будет отключать независимо.
//
// Параметры:
//  ХостыСервисовИнтернетПоддержки - Соответствие Из КлючИЗначение - хост и название используемого сервиса:
//    * Ключ - Строка - хост.
//    * Значение - Строка - название сервиса
//
Процедура ПриЗаполненииХостовСервисовИнтернетПоддержки(
		ХостыСервисовИнтернетПоддержки) Экспорт

	НастройкиПлатежныхСистемСобытия.ПриЗаполненииХостовСервисовИнтернетПоддержки(
		ХостыСервисовИнтернетПоддержки);
		
КонецПроцедуры

#КонецОбласти

#Область ФормаОбщихНастроек

// Добавляет информацию об отборах из параметров открытия формы:
//  - в параметр УсловияЗапроса добавляются необходимые условия вида "ИмяРеквизита = &Н1" или "ИмяРеквизита В (&Н1)";
//  - для запроса устанавливается значение соответствующего параметра.
// 
// Параметры:
//  УсловияЗапроса - Массив Из Строка - условия, которые необходимо добавить в запрос.
//  Отбор - Структура - значение отбора заданное в параметрах формы при открытии.
//        - Неопределено - отбор в параметрах не задан.
//  Запрос - Запрос - объект, содержащий выполняемый запрос.
//  
Процедура ДополнитьЗапросСтандартнымОтбором(УсловияЗапроса, Отбор, Запрос) Экспорт
	
	Если Отбор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипыВСписке = Новый Массив();
	ТипыВСписке.Добавить(Тип("Массив"));
	ТипыВСписке.Добавить(Тип("ФиксированныйМассив"));
	ТипыВСписке.Добавить(Тип("СписокЗначений"));
	
	НомерПП = 0;
	Для Каждого КлючЗначение Из Отбор Цикл
		
		ИмяПараметра = "Н" + Формат(НомерПП, "ЧН=0; ЧГ=;");
		Если ТипыВСписке.Найти(ТипЗнч(КлючЗначение.Значение)) = Неопределено Тогда
			УсловияЗапроса.Добавить("" + КлючЗначение.Ключ + " = &" + ИмяПараметра);
		Иначе
			УсловияЗапроса.Добавить("" + КлючЗначение.Ключ + " В (&" + ИмяПараметра + ")");
		КонецЕсли;
		
		Запрос.УстановитьПараметр(ИмяПараметра, КлючЗначение.Значение);
		
		НомерПП = НомерПП + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет запрос и заносит его результат в дерево. При этом удаляются лишние строки при использовании иерархии.
//  Уникальность полей проверяется по полю Ссылка.
// 
// Параметры:
//  Запрос - Запрос - источник данных для дерева.
//  Дерево - ДеревоЗначений - дерево, в которое необходимо занести результат.
//
Процедура ЗапросВДерево(Запрос, Дерево) Экспорт
	
	ДеревоИсточник = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ВывестиДерево(ДеревоИсточник, Дерево, Неопределено);
	
КонецПроцедуры

// Новый настройки условного оформления.
// 
// Возвращаемое значение:
//  Структура - Новый настройки условного оформления:
// * Оформление - Массив Из Структура - см. НовыйПараметрыОформления
// * Условия - Массив из Структура - см. НовыйУсловиеОформления
// * ОформляемыеПоля - Массив Из Строка - 
Функция НовыйНастройкиУсловногоОформления() Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("Оформление",      Новый Массив); // НовыйПараметрыОформления
	Результат.Вставить("Условия",         Новый Массив); // НовыйУсловиеОформления
	Результат.Вставить("ОформляемыеПоля", Новый Массив); // Строка
	
	Возврат Результат;

КонецФункции

// Новый параметры оформления.
// 
// Возвращаемое значение:
//  Структура - Новый параметры оформления:
// * ИмяПараметра - Строка - 
// * ЗначениеПараметра - Строка - 
Функция НовыйПараметрыОформления() Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("ИмяПараметра",      "");
	Результат.Вставить("ЗначениеПараметра", "");
	
	Возврат Результат;

КонецФункции

// Новый условие оформления.
// 
// Возвращаемое значение:
//  Структура - Новый условие оформления:
// * ЛевоеЗначение - Строка - 
// * ПравоеЗначение - Строка - 
// * ВидСравнения - Строка - 
// * ЭтоГруппа - Булево - 
// * СоставГруппы - Массив Из Структура - см. НовыйУсловиеОформления
// * ТипГруппы - Строка - 
Функция НовыйУсловиеОформления() Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("ЛевоеЗначение",  "");
	Результат.Вставить("ПравоеЗначение", "");
	Результат.Вставить("ВидСравнения",   "");
	Результат.Вставить("ЭтоГруппа",      Ложь);
	Результат.Вставить("СоставГруппы",   Новый Массив);
	Результат.Вставить("ТипГруппы",      "И");
	
	Возврат Результат;

КонецФункции

// Формирует пустое дерево настроек
// 
// Возвращаемое значение:
//  ДеревоЗначений - дерево настроек:
//   * Ссылка - Строка - Группа настроек;
//            - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП;
//            - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения к Интернет-эквайрингу.
//            - СправочникСсылка.НастройкиПодключенияКПлатежнымСистемам - настройка подключения к различным платежным
//              системам.
//   * Картинка - Число - Номер картинки в коллекции.
//   * ИдентификаторУсловногоОформления - Строка - произвольное значение для условного оформления.
//   * ЭтоГруппа - Булево - признак того, что текущая ссылка является группой.
//
Функция НовыйДеревоНастроек() Экспорт
	
	ДеревоРезультат = Новый ДеревоЗначений;
	ДеревоРезультат.Колонки.Добавить(
		"Ссылка",
		Новый ОписаниеТипов(Неопределено));
	ДеревоРезультат.Колонки.Добавить(
		"Картинка",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Любой)));
	ДеревоРезультат.Колонки.Добавить(
		"ИдентификаторУсловногоОформления",
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(36)));
	ДеревоРезультат.Колонки.Добавить(
		"ЭтоГруппа",
		Новый ОписаниеТипов("Булево"));
	
	Возврат ДеревоРезультат;
	
КонецФункции

// Формирует описание параметров, которые используются при формировании дерева со списком настроек подключения по
//  каждой подсистеме.
// 
// Возвращаемое значение:
//  Структура - параметры дерева:
//   * Подсистема - Строка - имя подсистемы;
//   * ПредставлениеПодсистемы - Строка - представление подсистемы. Будет использоваться при выводе корневой строки;
//   * КартинкаПодсистемы - Число - номер картинки в коллекции ОбщаяКартинка.НастройкиПлатежныхСистем. Будет
//     использована как картинка строки для корневого элемента;
//   * УсловноеОформление - Массив Из см. НовыйУсловиеОформления - условное оформление строк по подсистеме;
//   * ВыводитьПодсистему - Булево - признак того, что подсистема должна быть выведена в общий список;
//   * ПараметрыПодсистемы - Произвольный, Неопределено - параметры вывода подсистемы, переданные при открытии формы.
//   * РежимВыбора - Булево - Истина, если форма открыта в режиме выбора.
//   * ТолькоГруппы - Булево - Истина, если при открытии формы было указано, что можно выбирать только группы.
//   * ТолькоЭлементы - Булево - Истина, если при открытии формы было указано, что можно выбирать только элементы.
// 
Функция НовыйПараметрыДерева() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Подсистема",                "");
	Результат.Вставить("ПредставлениеПодсистемы",   "");
	Результат.Вставить("КартинкаПодсистемы",        6);
	Результат.Вставить("УсловноеОформление",        Новый Массив);
	Результат.Вставить("ВыводитьПодсистему",        Ложь);
	Результат.Вставить("ПараметрыПодсистемы",       Неопределено);
	Результат.Вставить("РежимВыбора",               Ложь);
	Результат.Вставить("ТолькоГруппы",              Ложь);
	Результат.Вставить("ТолькоЭлементы",            Ложь);
	
	Возврат Результат;
	
КонецФункции

// Конструктор описания подсистемы, подчиненной подсистеме ПлатежныеСистемы
// 
// Параметры:
//   ПлатежныйСервис - ПеречислениеСсылка.ПлатежныеСервисы - платежный сервис;
//   Подсистема - Строка - имя подсистемы в формате ПлатежныеСистемы.<ИмяПодсистемы>;
//   Представление - Строка - представление для строк списка;
//   Картинка - Строка - Имя картинки из БиблиотекиКартинок, которая будет использоваться для кнопки Создать;
//   НомерКартинки - Число - номер картинки для списка.
// 
// Возвращаемое значение:
//  Структура - описание платежной системы:
//    * ПлатежныйСервис - ПеречислениеСсылка.ПлатежныеСервисы - платежный сервис;
//    * Подсистема - Строка - полное имя подсистемы;
//    * Представление - Строка - представление для строк списка;
//    * Картинка - Строка - Имя картинки из БиблиотекиКартинок, которая будет использоваться для кнопки Создать;
//    * НомерКартинки - Число - номер картинки для списка.
//    * ЗаголовокСписка - Строка - заголовок списка, если выводится только одна подсистема;
//    * ЗаголовокПодключения - Строка - заголовок формы настройки нового подключения;
//    * Описание - Строка, ФорматированнаяСтрока - описание для первой страницы мастера нового подключения;
//    * Подключение - Строка, ФорматированнаяСтрока - подсказка для страницы ввода параметров подключения;
//    * Логотип - Строка - Имя картинки из БиблиотекиКартинок, которая будет использоваться как логотип.
//        Если не заполнено, то в качестве логотипа используется значение из свойства Картинка.
//    * ИспользуетНазначениеПлатежа - Булево - Истина, если при работе с сервисом используется назначение платежа.
Функция НовыйОписаниеПлатежнойСистемы(
		ПлатежныйСервис,
		Подсистема,
		Представление,
		Картинка,
		НомерКартинки) Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПлатежныйСервис", ПлатежныйСервис);
	Результат.Вставить("Подсистема", Подсистема);
	Результат.Вставить("Представление", Представление);
	Результат.Вставить("Картинка", Картинка);
	Результат.Вставить("НомерКартинки", НомерКартинки);
	Результат.Вставить("ЗаголовокСписка", "");
	Результат.Вставить("ЗаголовокПодключения", "");
	Результат.Вставить("Описание", "");
	Результат.Вставить("Подключение", "");
	Результат.Вставить("Логотип", "");
	Результат.Вставить("ИспользуетНазначениеПлатежа", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область НастройкиПрограммы

// Возвращает параметры окружения панели администрирования БИП.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма панели настроек.
//
// Возвращаемое значение:
//  Структура:
//    * НастройкаИнтеграцияДоступна - Булево
//    * ДлительностьОперацииПлатежныхСистем - Число
//    * ЕстьДокументыСКомандой - Булево - есть прикладные документы, в которые
//      выводятся команды настраиваемых подсистем.
//    * ДоступнаНастройкаШаблонов - Булево - Есть подсистемы, которые используют шаблоны сообщений.
//    * ПредставлениеПлатежныхСистем - Строка - список платежных систем для вывода в заголовок группы.
//    * ПредставлениеПлатежныхСистемПодсказка - Строка - список платежных систем для вывода в подсказку.
//
Функция ПараметрыОкруженияФормыИнтернетПоддержкаИСервисы(Форма) Экспорт
	
	Результат = Новый Структура();
	
	НастройкаДоступна = Ложь;
	КоличествоДокументовСКомандами = 0;
	ДоступнаНастройкаШаблонов = Ложь;
	
	ВнедренныеСистемы = Новый Массив;
	ВнедренныеСистемыПодсказка = Новый Массив;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежей = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежей");
		НастройкаДоступна = МодульСистемаБыстрыхПлатежей.НастройкаПодключенияДоступна();
		
		ВнедренныеСистемы.Добавить("СБП");
		ВнедренныеСистемыПодсказка.Добавить("Система быстрых платежей");
		
		Если НастройкаДоступна Тогда
			МодульСистемаБыстрыхПлатежейСлужебный  = ОбщегоНазначения.ОбщийМодуль(
				"СистемаБыстрыхПлатежейСлужебный");
			
			ИменаДокументовСКомандой = МодульСистемаБыстрыхПлатежейСлужебный.ИменаДокументовСКомандой();
			КоличествоДокументовСКомандами = ИменаДокументовСКомандой.Количество();
			
			ПодсистемаПереводыСБПc2b = ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b"); // АПК:216 Принятое сокращение
			ПодсистемаПереводыСБПb2b = ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПb2b"); // АПК:216 Принятое сокращение
			ПодсистемаШаблоныСообщений = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений");
			ПодсистемаОтправкаSMS = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОтправкаSMS");
			ПодсистемаОтправкаПисем = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями");
			
			ДоступнаНастройкаШаблонов = (ПодсистемаШаблоныСообщений
				И (ПодсистемаПереводыСБПc2b Или ПодсистемаПереводыСБПb2b) // АПК:216 Принятое сокращение
				И (ПодсистемаОтправкаSMS Или ПодсистемаОтправкаПисем));   // АПК:216 Принятое сокращение
				
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		
		ВнедренныеСистемы.Добавить("Интернет-эквайринг");
		ВнедренныеСистемыПодсказка.Добавить("Интернет-эквайринг");
		
		МодульИнтернетЭквайринг = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайринг");
		НастройкаДоступна = НастройкаДоступна Или МодульИнтернетЭквайринг.НастройкаПодключенияДоступна();
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПлатежныеСистемы.БазоваяФункциональностьПлатежныхСистем") Тогда
		
		МодульПлатежныеСистемы = ОбщегоНазначения.ОбщийМодуль("ПлатежныеСистемы");
		НастройкаДоступна = НастройкаДоступна Или МодульПлатежныеСистемы.НастройкаПодключенияДоступна();
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПлатежныеСистемы.ОзонПэй") Тогда
			ВнедренныеСистемы.Добавить("Ozon Pay");
			ВнедренныеСистемыПодсказка.Добавить("Ozon Pay");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВнедренныеСистемы.Количество() > 0 Тогда
		Если ВнедренныеСистемы.Количество() > 2 Тогда
			ПредставлениеПлатежныхСистем = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Платежные системы (%1, %2 и др.)'"),
				ВнедренныеСистемы[0],
				ВнедренныеСистемы[1]);
		Иначе
			ПредставлениеПлатежныхСистем = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Платежные системы (%1)'"),
				СтрСоединить(ВнедренныеСистемы, ", "));
		КонецЕсли;

	Иначе
		ПредставлениеПлатежныхСистем = НСтр("ru = 'Платежные системы'");
	КонецЕсли;
	
	Если ВнедренныеСистемыПодсказка.Количество() > 0 Тогда
		ПредставлениеПлатежныхСистемПодсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настройка подключения к платежным системам (%1)'"),
			СтрСоединить(ВнедренныеСистемыПодсказка, ", "));
	Иначе
		ПредставлениеПлатежныхСистемПодсказка = НСтр("ru = 'Настройка подключения к платежным системам'");
	КонецЕсли;
	
	Результат.Вставить("НастройкаИнтеграцияДоступна"           , НастройкаДоступна И (ВнедренныеСистемы.Количество() > 0));
	Результат.Вставить("ЕстьДокументыСКомандой"                , КоличествоДокументовСКомандами > 0);
	Результат.Вставить("ДоступнаНастройкаШаблонов"             , ДоступнаНастройкаШаблонов);
	Результат.Вставить("ДлительностьОперацииПлатежныхСистем"   , ДлительностьОперацииПлатежныхСистем());
	Результат.Вставить("ПредставлениеПлатежныхСистем"          , ПредставлениеПлатежныхСистем);
	Результат.Вставить("ПредставлениеПлатежныхСистемПодсказка" , ПредставлениеПлатежныхСистемПодсказка);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РегламентныеЗадания

// Создает регламентное задание "ЗагрузкаСтатусовОперацийПлатежныхСистем" 
// при создании новой настройки подключения.
//
Процедура ДобавитьЗаданиеЗагрузкаСтатусовОпераций() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗагрузкаСтатусовОперацийПлатежныхСистем);
	ЗаданияОбновления = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	Если ЗаданияОбновления.Количество() = 0 Тогда
		
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.ПериодПовтораВТечениеДня = 60 * 60 * 3; // Попытка получения статуса выполняется каждые 3 часа.
		Расписание.ПериодПовтораДней = 1;
		
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Истина);
		ПараметрыЗадания.Вставить("Метаданные",    Метаданные.РегламентныеЗадания.ЗагрузкаСтатусовОперацийПлатежныхСистем);
		ПараметрыЗадания.Вставить("Расписание",    Расписание);
		ПараметрыЗадания.Вставить("Наименование",  НСтр("ru = 'Загрузка статусов операций платежных систем'"));
		
		Попытка
			РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		Исключение
			ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписатьИнформациюВЖурналРегистрации(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось создать регламентное задание Загрузка статусов операций платежных систем по причине:
							|%1'"),
						ИнформацияОбОшибке),
					Истина);
			ВызватьИсключение ИнформацияОбОшибке;
		КонецПопытки;
		
		ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Создано регламентное задание Загрузка статусов операций платежных систем.'"),
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие настроек подключения к СБП, Интернет-эквайрингу или ПлатежнымСистемам.
// Если настройки есть, то вызывается создание регламентного задания для
// автоматического получения статусов операций.
//
Процедура ПроверитьЗаданиеЗагрузкаСтатусовОпераций(Параметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЕстьНастройкиПодключения() Тогда
		ДобавитьЗаданиеЗагрузкаСтатусовОпераций();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаОшибок

// Возвращает код ошибки "ОшибкаПодключения".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиОшибкаПодключения() Экспорт
	
	Возврат "ОшибкаПодключения";
	
КонецФункции

// Возвращает код ошибки "НеизвестнаяОшибка".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиНеизвестнаяОшибка() Экспорт
	
	Возврат "НеизвестнаяОшибка";
	
КонецФункции

// Возвращает код ошибки "НеверныйФорматЗапроса".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиНеверныйФорматЗапроса() Экспорт
	
	Возврат "НеверныйФорматЗапроса";
	
КонецФункции

// Возвращает код ошибки "НеверныйЛогинИлиПароль".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиНеверныйЛогинИлиПароль() Экспорт
	
	Возврат "НеверныйЛогинИлиПароль";
	
КонецФункции

// Возвращает код ошибки "УжеОплачен".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиУжеОплачен() Экспорт
	
	Возврат "УжеОплачен";
	
КонецФункции

// Возвращает код ошибки "ОшибкаСервиса".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиОшибкаСервиса() Экспорт
	
	Возврат "ОшибкаСервиса";
	
КонецФункции

// Возвращает код ошибки "СервисВременноНеДоступен".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиСервисВременноНеДоступен() Экспорт
	
	Возврат "СервисВременноНеДоступен";
	
КонецФункции

// Возвращает код ошибки "БлокировкаСервисовИПП".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиБлокировкаСервисовИПП() Экспорт
	
	Возврат "БлокировкаСервисовИПП";
	
КонецФункции

// Возвращает код ошибки "ФайлНеЗагружен".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиФайлНеЗагружен() Экспорт
	
	Возврат "ФайлНеЗагружен";
	
КонецФункции

// Возвращает код ошибки "ИнтеграцияНеИспользуется".
//
// Возвращаемое значение:
//  Строка
//
Функция КодОшибкиИнтеграцияНеИспользуется() Экспорт
	
	Возврат "ИнтеграцияНеИспользуется";
	
КонецФункции

// Возвращает код ошибки "ОбъектНеНайден".
//
// Возвращаемое значение:
//  Строка
//
Функция КодОшибкиОбъектНеНайден() Экспорт
	
	Возврат "ОбъектНеНайден";
	
КонецФункции

// Возвращает код ошибки "ОперацияВыполнена".
//
// Возвращаемое значение:
//  Строка
//
Функция КодОшибкиОперацияВыполнена() Экспорт
	
	Возврат "ОперацияВыполнена";
	
КонецФункции

// Возвращает код ошибки "ОперацияВыполняется".
//
// Возвращаемое значение:
//  Строка
//
Функция КодОшибкиОперацияВыполняется() Экспорт
	
	Возврат "ОперацияВыполняется";
	
КонецФункции

// Определяет по коду состояния и коду ошибки сервиса тип ошибки для потребителей.
//
// Параметры:
//  КодСостояния - Число - код состояния ответа сервиса;
//
// Возвращаемое значение:
//  Строка - код ошибки сервиса.
//
Функция ПереопределитьКодОшибкиСервиса(КодСостояния) Экспорт
	
	Если КодСостояния = 200 Тогда
		Возврат "";
	ИначеЕсли КодСостояния = 400
		Или КодСостояния = 404
		Или КодСостояния = 422 Тогда
		Возврат КодОшибкиНеверныйФорматЗапроса();
	ИначеЕсли КодСостояния = 401 Тогда
		Возврат КодОшибкиНеверныйЛогинИлиПароль();
	ИначеЕсли КодСостояния = 402 Тогда
		Возврат КодОшибкиТребуетсяОплата();
	ИначеЕсли КодСостояния = 403 Тогда
		Возврат КодОшибкиОтсутствуетДоступКСервису();
	ИначеЕсли КодСостояния = 429 Тогда
		Возврат КодОшибкиПревышеноКоличествоПопыток();
	ИначеЕсли КодСостояния = 503 Тогда
		Возврат КодОшибкиСервисВременноНеДоступен();
	ИначеЕсли КодСостояния >= 500 Тогда
		Возврат КодОшибкиОшибкаСервиса();
	ИначеЕсли КодСостояния = 0 Тогда
		Возврат КодОшибкиОшибкаПодключения();
	ИначеЕсли КодСостояния = -1
		Или КодСостояния = -2 Тогда
		Возврат КодОшибкиБлокировкаСервисовИПП();
	Иначе
		Возврат КодОшибкиНеизвестнаяОшибка();
	КонецЕсли;
	
КонецФункции

// Определяет по коду ошибки сообщение пользователю.
//
// Параметры:
//  КодОшибки - Строка - ошибка сервиса см. функцию ПереопределитьКодОшибкиСервиса.
//  ТелоJSON  - Строка - содержимое ответа сервиса.
//  КодСостояния - Число - код ответа сервиса.
//
// Возвращаемое значение:
//  Строка - сообщение пользователю.
//
Функция ПереопределитьСообщениеПользователю(
		КодОшибки,
		ТелоJSON = "",
		КодСостояния = 0) Экспорт
	
	Если КодСостояния = -1 Тогда
		Возврат ОбщегоНазначения.ЗапрещенДоступКИнтернетСервисамТекстСообщения();
	ИначеЕсли КодСостояния = -2 Тогда
		Возврат ИнтернетПоддержкаПользователей.ЗапрещенДоступКИнтернетПоддержкеТекст();
	КонецЕсли;
	
	КодОшибкиСервиса = "";
	Если ЗначениеЗаполнено(ТелоJSON) Тогда
		КодОшибкиСервиса = ОпределитьКодОшибкиСервиса(ТелоJSON);
	КонецЕсли;
	
	Если КодОшибки = КодОшибкиНеверныйФорматЗапроса() Тогда
		Если КодОшибкиСервиса = "BAD_REQUEST_1C" Тогда
			Возврат НСтр("ru = 'Неверный набор параметров или формат запроса к сервису Портала 1С:ИТС.'");
		ИначеЕсли КодОшибкиСервиса = "BAD_REQUEST" Тогда
			Возврат НСтр("ru = 'Неверный набор параметров или формат запроса к сервису платежной системы.'");
		ИначеЕсли КодОшибкиСервиса = "LOW_AMOUNT_PAYMENT" Тогда
			Возврат НСтр("ru = 'Сумма оплаты меньше допустимой.'");
		ИначеЕсли КодОшибкиСервиса = "HIGH_AMOUNT_PAYMENT" Тогда
			Возврат НСтр("ru = 'Сумма оплаты превышает допустимую.'");
		ИначеЕсли КодОшибкиСервиса = "UNKNOWN_PROGRAM" Тогда
			Возврат НСтр("ru = 'Неизвестный идентификатор программы.
				|Обратитесь к администратору.'");
		ИначеЕсли КодОшибкиСервиса = "INTEGRATION_IS_NOT_SUPPORTED_1C" Тогда
			Возврат НСтр("ru = 'Интеграция с платежной системой не поддерживается.
				|Обратитесь к администратору.'");
		ИначеЕсли КодОшибкиСервиса = "UNKNOWN_MERCHANT" Тогда
			Возврат НСтр("ru = 'Идентификатор мерчанта не обнаружен.
				|Проверьте настройки подключения.'");
		ИначеЕсли КодОшибкиСервиса = "REGISTRATION_EXCEPTION" Тогда
			Возврат НСтр("ru = 'Регистрация кассовой ссылки запрещена.
				|Обратитесь в банк для получения кассового QR-кода.'");
		Иначе
			Возврат НСтр("ru = 'Неверный набор параметров или формат запроса.
				|Обратитесь к администратору.'");
		КонецЕсли;
	
	ИначеЕсли КодОшибки = КодОшибкиНеверныйЛогинИлиПароль() Тогда
		Если КодОшибкиСервиса = "USER_AUTHENTICATION_EXCEPTION_1C" Тогда
			Возврат НСтр("ru = 'Ошибка аутентификации на Портале 1С:ИТС.'");
		ИначеЕсли КодОшибкиСервиса = "BAD_CREDENTIALS" Тогда
			Возврат НСтр("ru = 'Ошибка аутентификации в сервисе платежной системы.'");
		Иначе
			Возврат НСтр("ru = 'Ошибка авторизации.
				|Обратитесь к администратору.'");
		КонецЕсли;
	
	ИначеЕсли КодОшибки = КодОшибкиОтсутствуетДоступКСервису() Тогда
		Если КодОшибкиСервиса = "USER_AUTHORISATION_EXCEPTION_1С" Тогда
			Возврат НСтр("ru = 'Отсутствует доступ к сервису на Портале 1С:ИТС.'");
		ИначеЕсли КодОшибкиСервиса = "AUTHORIZATION_EXCEPTION" Тогда
			Возврат НСтр("ru = 'Отсутствует доступ к сервису платежной системы.'");
		ИначеЕсли КодОшибкиСервиса = "MERCHANT_NOT_ACTIVE" Тогда
			Возврат НСтр("ru = 'Настройка подключения не активна или находится в процессе регистрации.
				|Статус подключения необходимо уточнить у банка или платежного агрегатора, с которым заключен договор.'");
		Иначе
			Возврат НСтр("ru = 'Отсутствует доступ к сервису.
				|Обратитесь к администратору.'");
		КонецЕсли;
	
	ИначеЕсли КодОшибки = КодОшибкиПревышеноКоличествоПопыток() Тогда
		Если КодОшибкиСервиса = "TOO_MANY_REQUESTS_1C" Тогда
			Возврат НСтр("ru = 'Превышено количество попыток обращения к сервису 1С с не верными данными авторизации.
				|Проверьте правильность данных авторизации и повторите попытку через 30 минут.'");
		ИначеЕсли КодОшибкиСервиса = "TOO_MANY_REQUESTS" Тогда
			Возврат НСтр("ru = 'Превышено количество попыток обращения к сервису платежной системы с не верными данными авторизации.
				|Проверьте правильность данных авторизации и повторите попытку через 30 минут.'");
		Иначе
			Возврат НСтр("ru = 'Превышено количество попыток обращения к сервису.
				|Обратитесь к администратору.'");
		КонецЕсли;
	
	ИначеЕсли КодОшибки = КодОшибкиСервисВременноНеДоступен() Тогда
		Если КодОшибкиСервиса = "SERVICE_UNAVAILABLE_PAYMENT" Тогда
			Возврат НСтр("ru = 'Не удалось подключиться к сервису платежной системы.
				|Сервис временно недоступен. Повторите попытку подключения позже.'");
		Иначе
			Возврат НСтр("ru = 'Не удалось подключиться к сервису Портала 1С:ИТС.
				|Сервис временно недоступен. Повторите попытку подключения позже.'");
		КонецЕсли;
	
	ИначеЕсли КодОшибки = КодОшибкиТребуетсяОплата() Тогда
		Если КодОшибкиСервиса = "PAYMENT_REQUIRED" Тогда
			Возврат НСтр("ru = 'Требуется оплата сервиса.
				|Обратитесь к обслуживающему партнеру.'");
		Иначе
			Возврат НСтр("ru = 'Требуется оплата сервиса.
				|Обратитесь к администратору.'");
		КонецЕсли;
	
	ИначеЕсли КодОшибки = КодОшибкиОшибкаСервиса() Тогда
		Если КодОшибкиСервиса = "UNKNOWN_EXCEPTION" Тогда
			Возврат НСтр("ru = 'Сервис банка вернул неизвестную ошибки при выполнении операции.
				|Обратитесь к администратору.'");
		ИначеЕсли КодОшибкиСервиса = "UNKNOWN_EXCEPTION_1С" Тогда
			Возврат НСтр("ru = 'Сервис вернул неизвестную ошибки при выполнении операции.
				|Обратитесь к администратору.'");
		ИначеЕсли КодОшибкиСервиса = "BACKEND_CONNECTION_ERROR" Тогда
			Возврат НСтр("ru = 'Сервис банка не ответил в установленное время.
				|Повторите операцию или обратитесь в техническую поддержку банка.'");
		Иначе
			Возврат НСтр("ru = 'Ошибка работы с сервисом платежной системы.'");
		КонецЕсли;
	
	ИначеЕсли КодОшибки = КодОшибкиОшибкаПодключения() Тогда
		Возврат НСтр("ru = 'Не удалось подключиться к платежной системы.'");
		
	Иначе
		Возврат НСтр("ru = 'Неизвестная ошибка при подключении к сервису.'");
		
	КонецЕсли;
	
КонецФункции

// Формирует представление технической информации об ошибке.
//
// Параметры:
//  URLОперации - Строка - URL метода сервиса;
//  КодОшибки - Строка - код ошибки сервиса;
//  Заголовки - Соответствие Из КлючИЗначение - заголовки запроса;
//  ИнформацияОбОшибке - Строка - подробная информация об ошибке.
//
// Возвращаемое значение:
//  Строка - техническая информация об ошибке.
//
Функция ТехническаяИнформацияОбОшибке(
		URLОперации,
		КодОшибки,
		Заголовки,
		ИнформацияОбОшибке) Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Техническая информация об ошибке:
				|URL: %1
				|Код ошибки: %2
				|X-Correlation-ID: %3
				|Подробная информация:
				|%4'"),
			URLОперации,
			КодОшибки,
			Заголовки.Получить("X-Correlation-ID"),
			ИнформацияОбОшибке);
	
КонецФункции

#КонецОбласти

#Область ИдентификаторыОперацийИСтатусовСервиса

// Определяет статус операции по ее идентификатору
//
// Параметры:
//  ИдентификаторСтатусаОперации - Строка - идентификатор статуса операции;
//
// Возвращаемое значение:
//  Структура - результат выполнения операции в сервисе:
//    * СтатусОперации - Строка - текущее состояние операции оплаты. Возможные значения:
//      - "Выполняется" - подтверждение оплаты не получено;
//      - "Отменена" - оплата по ранее сформированной ссылке невозможна;
//      - "Выполнена" - участник Интернет-эквайринга подтвердил оплату;
//      - "Отклонена" - оплата по ранее сформированной ссылке невозможна;
//      - "Ошибка" - неизвестный статус операции.
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом:
//        - <Пустая строка> - статус успешно определен;
//        - "НеизвестныйСтатус" - неизвестный идентификатор статуса;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//
Функция ПолучитьСтатусОперацииПоИдентификатору(
		ИдентификаторСтатусаОперации) Экспорт
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("СтатусОперации",    "");
	РезультатОперации.Вставить("КодОшибки",         "");
	РезультатОперации.Вставить("СообщениеОбОшибке", "");
	
	Если ИдентификаторСтатусаОперации = ИдентификаторСтатусаВПроцессе() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется();
	ИначеЕсли ИдентификаторСтатусаОперации = ИдентификаторСтатусаВыполнена() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена();
	ИначеЕсли ИдентификаторСтатусаОперации = ИдентификаторСтатусаОтменена() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтменена();
	ИначеЕсли ИдентификаторСтатусаОперации = ИдентификаторСтатусаОтклонена() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтклонена();
	Иначе
		РезультатОперации.КодОшибки = "НеизвестныйСтатус";
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Не удалось определить статус операции.'");
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует идентификатор статуса отмененной операции.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаОтменена() Экспорт
	
	Возврат "REJECTED";
	
КонецФункции

// Формирует идентификатор статуса в процессе.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаВПроцессе() Экспорт
	
	Возврат "IN_PROGRESS";
	
КонецФункции

// Формирует идентификатор статуса выполненной операции.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаВыполнена() Экспорт
	
	Возврат "SUCCESS";
	
КонецФункции

// Формирует идентификатор статуса отклоненной операции.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаОтклонена() Экспорт
	
	Возврат "DECLINED";
	
КонецФункции

// Формирует идентификатор статуса ошибка.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаОшибка() Экспорт
	
	Возврат "ERROR";
	
КонецФункции

// Формирует идентификатор статуса превышения суммы.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаПревышение() Экспорт
	
	Возврат "EXCEEDED";
	
КонецФункции

// Формирует идентификатор статуса нет информации.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаНетИнформации() Экспорт
	
	Возврат "NO_INFO";
	
КонецФункции

// Формирует идентификатор статуса не оплачен.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаНеОплачен() Экспорт
	
	Возврат "NOT_PAID";
	
КонецФункции

// Формирует идентификатор статуса требуется подтверждение.
//
// Возвращаемое значение:
//  Строка - идентификатор статуса.
//
Функция ИдентификаторСтатусаТребуетсяПодтверждение() Экспорт
	
	Возврат "CONFIRMATION_REQUIRED";
	
КонецФункции

#Область ПлатежныеСистемы

// Определяет хост для вызова сервиса.
//
// Возвращаемое значение:
//  Строка - хост подключения.
//
Функция ХостСервисаОбменаДанными() Экспорт

	
	Возврат "payment-gateway.1c.ru";
	
КонецФункции

// Возвращает идентификатор сервиса по его имени.
//
// Параметры:
//  ИмяСервиса - Строка - имя сервиса.
//
// Возвращаемое значение:
//  Строка - Идентификатор сервиса.
//
Функция ИдентификаторСервисаПоИмени(ИмяСервиса) Экспорт
	
	Если ИмяСервиса = "orders.1c.ru" Тогда
		Возврат "orders";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Восстанавливает значение даты из JSON, если значение не равно Неопределено.
//
// Параметры:
//  Свойство - Строка - имя свойства;
//  Значение - Строка, Неопределено - значение свойства;
//  ДополнительныеПараметры - Структура, Неопределено - параметры восстановления.
//
// Возвращаемое значение:
//  Дата, Неопределено - результат восстановления.
//
Функция ВосстановитьДатуJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение <> Неопределено Тогда
		
		Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
		
		// Свойство operationDate необходимо обрабатывать
		// и хранить в формате UTC, для того чтобы при
		// проведении сверки взаиморасчетов из разных
		// часовых поясов получать единые выборки операций.
		Если Свойство = "operationDate" Тогда
			Результат = ДатаВUTC(Результат); // АПК:216 Принятое сокращение
		КонецЕсли;
		
		Возврат Результат;
		
	Иначе
		Возврат Значение;
	КонецЕсли;
	
КонецФункции

// Преобразует текущую локальную дату в UTC.
//
// Параметры:
//  Значение - Дата - исходная дата.
//
// Возвращаемое значение:
//  Дата - результат преобразования.
//
Функция ДатаВUTC(Значение) Экспорт // АПК:216 Принятое сокращение
	
	Возврат Значение - СмещениеСтандартногоВремени(ЧасовойПоясСеанса(), Значение);
	
КонецФункции

// Меняет параметры формы при переопределении в процедуре ОбработкаПолученияФормы.
// 
// Параметры:
//  ИмяПодсистемы - Строка - Имя подсистемы, для которой необходимо вывести список. Может принимать значения:
//                  - "Все", ПустаяСтрока - вывод всех подсистем.
//                  - "СБП" - для подсистемы "Система быстрых платежей".
//                  - "Эквайринг" - для подсистемы "Интернет-эквайринг".
//                  - "ОзонПэй" - для платежной системы Ozon Pay
//  ВидФормы - Строка - вид открываемой формы.
//  Параметры - Структура - исходные параметры открытия формы.
//
Процедура ОбработкаПолученияФормыСписка(ИмяПодсистемы, ВидФормы, Параметры) Экспорт
	
	ТекущиеПараметры = ОбщегоНазначения.СкопироватьРекурсивно(Параметры);
	
	Параметры.Очистить();
	
	Параметры.Вставить("Подсистема", ИмяПодсистемы);
	Параметры.Вставить("ПараметрыПодсистемы", ТекущиеПараметры);
	Параметры.Вставить("РежимВыбора", ВидФормы = "ФормаВыбора");
	
	Если Параметры.РежимВыбора Тогда
		РежимОткрытияОкнаПоУмолчанию = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		РежимОткрытияОкнаПоУмолчанию = РежимОткрытияОкнаФормы.Независимый;
	КонецЕсли;
	
	СтандартныеПараметрыФормы = Новый Структура;
	СтандартныеПараметрыФормы.Вставить("ЗакрыватьПриВыборе",            Параметры.РежимВыбора);
	СтандартныеПараметрыФормы.Вставить("РежимОткрытияОкна",             РежимОткрытияОкнаПоУмолчанию);
	СтандартныеПараметрыФормы.Вставить("ВыборГруппИЭлементов",          Неопределено);
	СтандартныеПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Неопределено);
	СтандартныеПараметрыФормы.Вставить("Отображение",                   Неопределено);

	ЗаполнитьЗначенияСвойств(СтандартныеПараметрыФормы, ТекущиеПараметры);
	
	Для Каждого КлючЗначение Из СтандартныеПараметрыФормы Цикл
		Если КлючЗначение.Значение <> Неопределено Тогда
			Параметры.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает значение константы ДлительностьОперацииПлатежныхСистем.
//
// Возвращаемое значение:
//  Число - длительность операции.
//
Функция ДлительностьОперацииПлатежныхСистем() Экспорт
	
	Возврат Константы.ДлительностьОперацииПлатежныхСистем.Получить();
	
КонецФункции

// Устанавливает значение константы ДлительностьОперацииПлатежныхСистем1.
//
// Параметры:
//  Значение - Число - количество секунд.
//
Процедура УстановитьДлительностьОперации(Значение) Экспорт
	
	Если ПривилегированныйРежим() Тогда
		Константы.ДлительностьОперацииПлатежныхСистем.Установить(Значение);
		Возврат;
	КонецЕсли;
	
	Если ДоступноИзменениеДлительностьОпераций() Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ДлительностьОперацииПлатежныхСистем.Установить(Значение);
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ВызватьИсключение НСтр("ru = 'Нарушение прав доступа'");
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет настройки отображения команд.
//
// Параметры:
//  НовыеНастройки - Соответствие Из КлючИЗначение - настройки отображения команд в объектах:
//    * Ключ - Строка - имя подсистемы, к которой относятся настройки.
//    * Значение - Соответствие Из КлючИЗначение - настройка отображения команд конкретной подсистемы:
//      ** Ключ - Строка - имя объекта метаданных.
//      ** Значение - Булево - признак использования команд в объекте.
//
Процедура УстановитьНастройкиОтображенияКоманд(НовыеНастройки) Экспорт
	
	// У пользователя может не быть прав на настройку отдельных подсистем, поэтому в параметре НовыеНастройки
	// могут быть не все подсистемы. Поэтому сначала получаем сохраненные и меняем в них только те значения,
	// которые есть в НовыеНастройки.
	
	ТекущиеНастройки = НастройкиОтображенияКоманд();

	Для Каждого КлючЗначение Из НовыеНастройки Цикл
		ТекущиеНастройки.Вставить(
			КлючЗначение.Ключ,
			КлючЗначение.Значение);
	КонецЦикла;
	
	Константы.НастройкиОтображенияКомандПлатежныхСистем.Установить(
		Новый ХранилищеЗначения(
			Новый ФиксированноеСоответствие(ТекущиеНастройки)));
			
КонецПроцедуры

// Отключает отображение команд подсистемы.
//
// Параметры:
//  ИдентификаторПодсистемы - Строка - определяет идентификатор подсистемы для которой выполняется отключение.
//  Допустимые значения:
//    - "СистемаБыстрыхПлатежей" - Система быстрых платежей.
//
Процедура ОтключитьКомандыПодсистемы(ИдентификаторПодсистемы) Экспорт
	
	НастройкиОтображенияКоманд = НастройкиОтображенияКоманд();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП")
		И ИдентификаторПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		
		НастройкиКоманд = Новый Соответствие;
		ИменаОбъектов = Новый Массив;
		
		МодульСистемаБыстрыхПлатежейСобытия = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСобытия");
		МодульСистемаБыстрыхПлатежейСобытия.ПриОпределенииИменДокументовСКомандой(ИменаОбъектов);
		
		Для Каждого ИмяОбъекта Из ИменаОбъектов Цикл
			НастройкиКоманд.Вставить(
				ИмяОбъекта,
				Ложь);
		КонецЦикла;
		
		НастройкиОтображенияКоманд.Вставить(
			ИдентификаторПодсистемы,
			НастройкиКоманд);
		
	КонецЕсли;
	
	УстановитьНастройкиОтображенияКоманд(НастройкиОтображенияКоманд);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

// Получает настройки отображения команд.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - настройки отображения команд в объектах:
//    * Ключ - Строка - имя подсистемы, к которой относятся настройки.
//    * Значение - Соответствие Из КлючИЗначение -- настройка отображения команд конкретной подсистемы:
//      ** Ключ - Строка - имя объекта метаданных.
//      ** Значение - Булево - признак использования команд в объекте.
//
Функция НастройкиОтображенияКоманд() Экспорт
	
	Результат = Новый Соответствие;
	Результат.Вставить(
		НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП(),
		Новый Соответствие);
	
	НастройкиОтображения = Константы.НастройкиОтображенияКомандПлатежныхСистем.Получить().Получить();
	
	// Проверяем на старую версию хранения настроек
	Если ТипЗнч(НастройкиОтображения) = Тип("Соответствие")
		Или ТипЗнч(НастройкиОтображения) = Тип("ФиксированноеСоответствие") Тогда
		
		НайденКлючНастроек = Ложь;
		
		Для Каждого КлючЗначение Из Результат Цикл
			НастройкиПодсистемы = НастройкиОтображения.Получить(КлючЗначение.Ключ);
			Если НастройкиПодсистемы <> Неопределено Тогда
				Результат.Вставить(
					КлючЗначение.Ключ,
					НастройкиПодсистемы);
				НайденКлючНастроек = Истина;
			КонецЕсли;
		КонецЦикла;
		
		// Если добавили новую платежную систему, то в настройках по ней будет пустое соответствие,
		// но по другим будет заполнено.
		// Если это первый переход на подсистему НастройкиПлатежныхСистем, то в константе 
		// хранится соответствие из подсистемы СистемаБыстрыхПлатежей, у которой нет таких ключей.
		// Преобразовываем в новую структуру хранения настроек.
		Если Не НайденКлючНастроек Тогда
			
			Результат.Вставить(
				НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП(),
				НастройкиОтображения);
			
		КонецЕсли;

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Определяет максимальное количество попыток запроса статуса
// для отложенных операций.
//
// Параметры:
//  ПлатежнаяСистема - Строка - платежная система. Доступные значения: "СБП", "ИнтернетЭквайринг"
//  Оплата - Булево - признак операции оплаты.
//
// Возвращаемое значение:
//  Число - максимальное количество попыток.
//
Функция МаксимальноеКоличествоПопытокЗапросаСтатуса(ПлатежнаяСистема, Оплата) Экспорт
	
	Если ПлатежнаяСистема = "СБП"
		Или ПлатежнаяСистема = "ИнтернетЭквайринг" Тогда
		Если Оплата Тогда
			Возврат 400;
		Иначе
			Возврат 10;
		КонецЕсли;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Создает описание настроек длительного вызова.
//
// Параметры:
//  ДлительностьОперации - Число - количество секунд отведенных на выполнение операции.
//
// Возвращаемое значение:
//  Структура - результат итеративного вызова:
//    * Длительность - Число - длительность вызова операции;
//    * НачалоОперации  - Дата - период отчета длительности вызова;
//    * Ожидание - Число - частота выполнения операций;
//    * МаксимальноеКоличествоЗапросов - Число - ограничение на максимально возможное количество запросов;
//    * НакопленноеЗначениеВремени - Дата - общее накопленное время за длительную операцию;
//    * КоличествоИтераций - Число - текущее количество выполненных запросов.
//
Функция НовыйИтеративныйВызовОперации(ДлительностьОперации = 0) Экспорт
	
	ДлительностьОперации = ?(
		ДлительностьОперации = 0,
		ДлительностьОперацииПлатежныхСистем(),
		ДлительностьОперации);
	
	НастройкиВызова = Новый Структура;
	НастройкиВызова.Вставить("Длительность",                   ДлительностьОперации);
	НастройкиВызова.Вставить("НачалоОперации",                 ТекущаяДатаСеанса());
	НастройкиВызова.Вставить("Ожидание",                       3);
	НастройкиВызова.Вставить("МаксимальноеКоличествоЗапросов", 300);
	НастройкиВызова.Вставить("НакопленноеЗначениеВремени",     НастройкиВызова.НачалоОперации - НастройкиВызова.Ожидание);
	НастройкиВызова.Вставить("КоличествоИтераций",             0);
	
	// Для небольшого значения длительности будет выполнен единственный вызов операции.
	Если НастройкиВызова.Длительность < 3 Тогда
		НастройкиВызова.МаксимальноеКоличествоЗапросов = 1;
	КонецЕсли;
	
	Возврат НастройкиВызова;
	
КонецФункции

// Проверяет доступность вызова операции в рамках длительного вызова.
//
// Параметры:
//  НастройкиВызова - см. НастройкиПлатежныхСистемСлужебный.НовыйИтеративныйВызовОперации
//
// Возвращаемое значение:
//  Булево - Истина, если выполнение операции возможно; Ложь в противном случае.
//
Функция ВозможенВызовОперации(НастройкиВызова) Экспорт
	
	// Прервать вызовы в случае превышения максимального
	// количества запросов.
	Если НастройкиВызова.МаксимальноеКоличествоЗапросов <= НастройкиВызова.КоличествоИтераций Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Прервать вызовы в случае превышения длительности.
	Если (НастройкиВызова.НачалоОперации + НастройкиВызова.Длительность) < ТекущаяДатаСеанса() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Для первой итерации время ожидания необходимо увеличить
	// т.к. маловероятно, что при первом вызове будет получен
	// терминальный статус операции.
	Ожидание = НастройкиВызова.Ожидание;
	Если НастройкиВызова.КоличествоИтераций = 0 Тогда
		Ожидание = Ожидание + 1;
	КонецЕсли;
	
	// Ожидание истечения времени задержки между вызовами.
	// Если запросы выполнять без ограничения, возрастет нагрузка на сервис,
	// при этом пользователи не почувствуют существенного изменения времени
	// выполнения операции.
	ИнтернетПоддержкаПользователей.Пауза(Ожидание);
	
	НастройкиВызова.НакопленноеЗначениеВремени = ТекущаяДатаСеанса();
	НастройкиВызова.КоличествоИтераций         = НастройкиВызова.КоличествоИтераций + 1;
	
	Возврат Истина;
	
КонецФункции

// Определяет необходимость создания идентификатора операции
// или его обновления.
//
// Параметры:
//  Идентификатор - УникальныйИдентификатор - текущий идентификатор операции;
//  СтатусОперации - Строка - текущий статус операции.
//
// Возвращаемое значение:
//  Булево  - если Истина, необходимо создать новый идентификатор.
//
Функция ТребуетсяГенерацияНовогоИдентификатора(
		Идентификатор,
		СтатусОперации) Экспорт
	
	// Если еще не был создан необходимо
	// сгенерировать новый.
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Если операция находится в терминальном статусе
	// и результат по этой операции отрицательный, необходимо
	// обновить идентификатор.
	Если СтатусОперации = ИдентификаторСтатусаПревышение()
		Или СтатусОперации = ИдентификаторСтатусаНеОплачен()
		Или СтатусОперации = ИдентификаторСтатусаНетИнформации()
		Или СтатусОперации = ИдентификаторСтатусаОтклонена()
		Или СтатусОперации = ИдентификаторСтатусаОтменена() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Выполняет проверку назначения платежа
//
// Параметры:
//  НазначениеПлатежа - Строка - назначение платежа;
//  РезультатОперации - Структура - результат выполнения операции.
//
Процедура ПроверитьНазначениеПлатежа(НазначениеПлатежа, РезультатОперации) Экспорт
	
	ЗапрещенныеСимволы = НастройкиПлатежныхСистемКлиентСервер.ЗапрещенныеСимволыНазначенияПлатежа(
		НазначениеПлатежа);
		
	Если ЗапрещенныеСимволы.Количество() <> 0 Тогда
		
		РезультатОперации.КодОшибки = КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В назначении присутствуют запрещенные символы: %1. Обратитесь к администратору.'"),
			СтрСоединить(ЗапрещенныеСимволы, ","));
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В назначении присутствуют запрещенные символы: %1. Необходимо обновить назначения платежей в настройках подключения.'"),
			СтрСоединить(ЗапрещенныеСимволы, ","));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ШаблоныНазначенийПлатежей

// Формирует таблицу шаблонов назначений платежей.
//
// Возвращаемое значение:
//  ТаблицаЗначений - настройки заполнения шаблонов платежей:
//   * ОбъектМетаданных - Строка - имя объекта метаданных операции.
//   * Идентификатор - Строка - идентификатор шаблона.
//   * Наименование - Строка - наименование шаблона для пользователя.
//   * Параметры - Структура - параметры заполнения шаблона:
//     ** Наименование - Строка - наименование параметра для пользователя.
//     ** Идентификатор - Строка - идентификатор параметра для заполнения.
//
Функция НовыйШаблоныНазначений() Экспорт
	
	ШаблоныНазначений = Новый ТаблицаЗначений;
	ШаблоныНазначений.Колонки.Добавить("ОбъектМетаданных", ОбщегоНазначения.ОписаниеТипаСтрока(80));
	ШаблоныНазначений.Колонки.Добавить("Идентификатор",    ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ШаблоныНазначений.Колонки.Добавить("Наименование",     ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ШаблоныНазначений.Колонки.Добавить("Параметры",        Новый ОписаниеТипов("Массив"));
	
	Возврат ШаблоныНазначений;
	
КонецФункции

// Проверяет корректность заполнения шаблонов назначения в описании настроек подключения
//
// Параметры:
//  ШаблоныНазначений - см. НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений;
//  Ошибки - Массив Из Строка - список ошибок проверки параметров.
//
Процедура ПроверитьШаблоныНазначений(ШаблоныНазначений, Ошибки) Экспорт
	
	Для Каждого ДанныеНазначения Из ШаблоныНазначений Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеНазначения.ОбъектМетаданных) Тогда
			Ошибки.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не указан объект метаданных в шаблоне назначения %1.'"),
					ДанныеНазначения.Наименование));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеНазначения.Идентификатор) Тогда
			Ошибки.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не указан идентификатор в шаблоне назначения %1.'"),
					ДанныеНазначения.Наименование));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеНазначения.Наименование) Тогда
			Ошибки.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не указано наименование в шаблоне назначения %1.'"),
					ДанныеНазначения.ОбъектМетаданных));
		КонецЕсли;
		
		Для Каждого ДанныеПараметра Из ДанныеНазначения.Параметры Цикл
			
			Если Не ЗначениеЗаполнено(ДанныеПараметра.Наименование) Тогда
				Ошибки.Добавить(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указано наименование параметра в шаблоне назначения %1.'"),
						ДанныеНазначения.ОбъектМетаданных));
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеПараметра.Идентификатор) Тогда
				Ошибки.Добавить(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указан идентификатор параметра в шаблоне назначения %1.'"),
						ДанныеНазначения.ОбъектМетаданных));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует текст надписи для шаблонов назначений на форме элемента справочников настроек подключения.
// 
// Параметры:
//  ШаблоныНазначений - см. НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений;
//  ДекорацияФормы - ДекорацияФормы - декорация, в которую необходимо выводить информацию;
//  Ссылка - Произвольный - Ссылка на элемент справочника, для которого проверяются шаблоны назначений.
//
Процедура ЗаполнитьДекорациюНастройкиШаблонов(ШаблоныНазначений, ДекорацияФормы, Ссылка) Экспорт
	
	Если ШаблоныНазначений = Неопределено Или ШаблоныНазначений.Количество() = 0 Тогда
		ДекорацияФормы.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныеШаблоны = РегистрыСведений.ШаблоныНазначенийПлатежей.ШаблоныНазначенийПлатежей(Ссылка);
	
	Если СохраненныеШаблоны.Количество() <> 0 Тогда
		ПредставлениеСсылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Шаблоны назначения (%1)'"),
			СохраненныеШаблоны.Количество());
	Иначе
		ПредставлениеСсылки = НСтр("ru = 'Шаблоны назначения'")
	КонецЕсли;
		
	ДекорацияФормы.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '<a href = ""open:paymentPurpose"">%1</a>'"),
			ПредставлениеСсылки));
	
КонецПроцедуры

#КонецОбласти

#Область РасчетHMAC

// Формирует строку HMAC.
//
// Параметры:
//  СекретныйКлюч - ДвоичныеДанные - секретный ключ для выполнения аутентификации;
//  ДанныеДляПодписи - Массив из Строка  - данные шифрования;
//  ЗначениеХешФункция - Строка - способ вычисления хеш-суммы.
//
// Возвращаемое значение:
//  Строка - значение HMAC.
//
Функция ЗначениеHMAC(СекретныйКлюч, Знач ДанныеДляПодписи, ЗначениеХешФункция) Экспорт

	Данные = "";
	Для Каждого Значение Из ДанныеДляПодписи Цикл
		
		Если Не ЗначениеЗаполнено(Значение) И Значение <> 0 Тогда
			Данные = Данные + "-";
		ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
			ЗначениеДата = ЗаписатьДатуJSON(
				Значение,
				ФорматДатыJSON.ISO,
				ВариантЗаписиДатыJSON.УниверсальнаяДата);
			Данные = Данные + СтрДлина(ЗначениеДата) + ЗначениеДата;
		ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
			ЗначениеЧисло = СтрЗаменить("" + Значение, ",", ".");
			ЗначениеЧисло = СтрЗаменить(ЗначениеЧисло, Символы.НПП, "");
			Данные = Данные + СтрДлина(ЗначениеЧисло) + ЗначениеЧисло;
		Иначе
			Данные = Данные + СтрДлина("" + Значение) + Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	СлучайноеЧислоHex = ДлинноеСлучайноеЧислоHEX();
	Данные = Данные + СтрДлина(СлучайноеЧислоHex) + СлучайноеЧислоHex;
	
	Хеш = РассчитатьHmac(
		СекретныйКлюч,
		ПолучитьДвоичныеДанныеИзСтроки(
			Данные,
			КодировкаТекста.UTF8),
		ЗначениеХешФункция);
	
	Префикс = ?(ЗначениеХешФункция = ХешФункция.SHA256, "HMACSHA256", "HMACSHA1");
	Возврат Префикс + " " + СлучайноеЧислоHex + "." + НРег(Хеш);

КонецФункции

// Применяет хеш функцию к переданным данным.
//
// Параметры:
//  Значение - ДвоичныеДанные  - данные хеширования;
//  ХешФункция - ХешФункция - хеш функция, например ХешФункция.SHA256.
//
// Возвращаемое значение:
//  ДвоичныеДанные  - хеш сумма;
//
Функция Хешировать(Знач Значение, Знач ХешФункция) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция);
	ХешированиеДанных.Добавить(Значение);
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции

#КонецОбласти

#Область ОбщегоНазначения

// Формирует идентификатор для отображения скрытых символов на форме.
//
//
// Возвращаемое значение:
//  Строка - служебный идентификатор.
//
Функция СтрокаСекретныхДанныхПоУмолчанию() Экспорт

	Возврат "a08bf0a2-7d7a-461f-8624-3b54f47c02a9";
	
КонецФункции

#КонецОбласти

#Область ПлатежныеСистемы

// Определяет организацию по прикладным настройкам подключения к платежной системе.
// Алгоритм определения:
//  1. Если в ПрикладныеНастройки есть параметр с именем "Организация" и его тип входит в определяемый 
//     тип Организация, то возвращается его значение.
//  2. Если не выполняется первое условие, то возвращается первое из найденных значений из параметра
//     ПрикладныеНастройки, которое имеет тип, входящий в определяемый тип Организация.
//  3. Если первое и второе условие не выполнилось, то возвращается Неопределено.
//  
// Параметры:
//  ПрикладныеНастройки - Соответствие Из КлючИЗначение - прикладные параметры для подсистемы Интернет-эквайринга.
//                      - Структура - прикладные параметры для подсистемы Платежные системы.
//
// Возвращаемое значение:
//  ОпределяемыйТип.Организация - найденное значение.
//  Неопределено - в случае, если не удалось определить организацию.
//  
Функция ОпределитьОрганизациюПоНастройкам(ПрикладныеНастройки) Экспорт
	
	ПараметрОрганизация = Неопределено;
	Если ТипЗнч(ПрикладныеНастройки) = Тип("Соответствие") Тогда
		ПараметрОрганизация = ПрикладныеНастройки.Получить("Организация");
	ИначеЕсли ТипЗнч(ПрикладныеНастройки) = Тип("Структура") Тогда
		Эталон = Новый Структура("Организация", Неопределено);
		ЗаполнитьЗначенияСвойств(Эталон, ПрикладныеНастройки);
		ПараметрОрганизация = Эталон.Организация;
	КонецЕсли;
	
	// Определяемый тип Организация входит в состав подсистемы СтандартныеПодсистемы.БазоваяФункциональность
	// поэтому не проверяем существование
	ТипыОрганизация = Метаданные.ОпределяемыеТипы.Организация.Тип.Типы();
	
	ПоискОрганизации = Неопределено;
	
	Если ЗначениеЗаполнено(ПараметрОрганизация)
		И ТипыОрганизация.Найти(ТипЗнч(ПараметрОрганизация)) <> Неопределено Тогда
		ПоискОрганизации = ПараметрОрганизация;
	Иначе
		Для Каждого Настройка Из ПрикладныеНастройки Цикл
			ЗначениеНастройки = Настройка.Значение;
			Если ТипыОрганизация.Найти(ТипЗнч(ЗначениеНастройки)) <> Неопределено Тогда
				ПоискОрганизации = ЗначениеНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПоискОрганизации;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СтандартныеПодсистемыБазоваяФункциональность

// См. РаботаВБезопасномРежимеПереопределяемый.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт

	НастройкиПлатежныхСистемСобытия.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РегламентныеЗадания

// Обработчик регламентного задания "ЗагрузкаСтатусовОперацийПлатежныхСистем".
//
Процедура ЗагрузкаСтатусовОперацийПлатежныхСистем() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ЗагрузкаСтатусовОперацийПлатежныхСистем);
	
	ОбработанныеОперации = НастройкиПлатежныхСистем.СтатусыОпераций();
	
	ТекстСообщения = Новый Массив;
	ШаблонСообщения = НСтр("ru = 'Загружены терминальные статусы %1 операций %2.'");
	Для Каждого КлючЗначение Из ОбработанныеОперации Цикл
		Если КлючЗначение.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		КоличествоОпераций = КлючЗначение.Значение.Количество();
		Если КоличествоОпераций <> 0 Тогда
			
			Если КлючЗначение.Ключ = "СБП" Тогда
				ИмяПлатежнойСистемы =  НСтр("ru = 'Системы быстрых платежей'");
			ИначеЕсли КлючЗначение.Ключ = "ИнтернетЭквайринг" Тогда
				ИмяПлатежнойСистемы = НСтр("ru = 'Интернет-эквайринга'");
			Иначе
				ИмяПлатежнойСистемы = КлючЗначение.Ключ;
			КонецЕсли;

			ТекстСообщения.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонСообщения,
					КоличествоОпераций,
					ИмяПлатежнойСистемы));
		КонецЕсли;
	КонецЦикла;
	
	Если ТекстСообщения.Количество() <> 0 Тогда
		ЗаписатьИнформациюВЖурналРегистрации(
			СтрСоединить(ТекстСообщения, Символы.ПС),
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет все задания "ЗагрузкаСтатусовОперацийПлатежныхСистем".
//
Процедура УдалитьЗаданияЗагрузкаСтатусовОперацийПлатежныхСистем()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗагрузкаСтатусовОперацийПлатежныхСистем);
		Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		Если Задания.Количество() <> 0 Тогда
			Для каждого Задание Из Задания Цикл
				РегламентныеЗаданияСервер.УдалитьЗадание(Задание);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФормаОбщегоСпискаНастроек

Процедура ВывестиДерево(Источник, Получатель, Знач Родитель)
	
	Для Каждого СтрокаИсточник Из Источник.Строки Цикл
		
		Если СтрокаИсточник.Ссылка = Родитель Тогда
			ЗаполнитьЗначенияСвойств(
				Получатель,
				СтрокаИсточник);
			Продолжить;
		КонецЕсли;
		
		СтрокаПолучатель = Получатель.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПолучатель, СтрокаИсточник);
		
		Если СтрокаИсточник.Строки.Количество() > 0 Тогда
			ВывестиДерево(
				СтрокаИсточник,
				СтрокаПолучатель,
				СтрокаИсточник.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует представление справочника для вывода в заголовок формы, если выводятся настройки только одной подсистемы.
// 
// Параметры:
//  ИмяМетаданных - Строка - полное имя объекта метаданных,
//                  например, Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.
//
// Возвращаемое значение:
//  Строка - если у объекта задано представление списка, то возвращается оно. В противном случае - значение синонима.
//
Функция ПредставлениеСписка(ИмяМетаданных) Экспорт
	
	Мета = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ИмяМетаданных);
	Результат = Мета.ПредставлениеСписка;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Результат = Мета.Синоним;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавляет на форму команду и кнопку создания настройки подключения.
//  Фактически создается одна команда с именем "Создать_<ИмяПодсистемы>" и две команды в группа ГруппаСоздать
//  и ГруппаСоздатьОдинТип, которые отображаются в зависимости от того, сколько подсистем выводится в список.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - открываемая форма;
//  ИмяПодсистемы - Строка - имя подсистемы, к которой относится команда;
//  Заголовок - Строка - заголовок команды;
//  Картинка - Картинка - картинка кнопки.
//
Процедура ДобавитьКнопкуСоздания(
		Форма,
		ИмяПодсистемы,
		Заголовок,
		Картинка) Экспорт
	
	ИмяКоманды = "Создать_" + ИмяПодсистемы;
	Форма.КешПараметров.ПодсистемыКнопкиСоздать.Вставить(ИмяКоманды, ИмяПодсистемы);
	
	НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
	НоваяКоманда.Заголовок = Заголовок;
	НоваяКоманда.Действие = "Подключаемый_СоздатьНастройку";
	НоваяКоманда.Картинка = Картинка;
	
	Кнопка = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Форма.Элементы.ГруппаСоздать);
	Кнопка.ИмяКоманды = ИмяКоманды;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	Кнопка = Форма.Элементы.Добавить(ИмяКоманды + "Одна", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСоздатьОдинТип);
	Кнопка.ИмяКоманды = ИмяКоманды;
	Кнопка.Отображение = ОтображениеКнопки.Текст;
	Кнопка.Заголовок = НСтр("ru = 'Создать'");
	
КонецПроцедуры

// Добавить кнопку в подменю ГруппаДополнительно.
//   Обработка команды выполняется в методе НастройкиПлатежныхСистемКлиентСобытия.ПриВыполненииКомандыДополнительно
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - открываемая форма;
//  ИмяКоманды - Строка - имя создаваемой команды;
//  Заголовок - Строка - заголовок команды;
//  Картинка - Неопределено, Картинка - картинка кнопки;
//  Отображение - Неопределено, ОтображениеКнопки - вариант отображения кнопки.
Процедура ДобавитьКнопкуДополнительно(
		Форма,
		ИмяКоманды,
		Заголовок,
		Картинка = Неопределено,
		Отображение = Неопределено) Экспорт
	
	НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
	НоваяКоманда.Заголовок = Заголовок;
	НоваяКоманда.Действие  = "Подключаемый_ВыполнитьКомандуДополнительно";
	Если Картинка <> Неопределено Тогда
		НоваяКоманда.Картинка = Картинка;
	КонецЕсли;
	Если Отображение <> Неопределено Тогда
		НоваяКоманда.Отображение = Отображение;
	КонецЕсли;
	
	Кнопка = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Форма.Элементы.ГруппаДополнительно);
	Кнопка.ИмяКоманды = ИмяКоманды;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиПодключения

// Проверяет, если ли подключение к любому платежному сервису: СБП, Интернет-эквайринг, Платежные системы.
// 
// Возвращаемое значение:
//  Булево - Истина, если есть хотя бы одна настройка любого типа
//  
Функция ЕстьНастройкиПодключения()
	
	ЕстьСБП = Ложь;
	ЕстьЭквайринг = Ложь;
	ЕстьПлатежныеСистемы = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульНастройкиПодключенияКСистемеБыстрыхПлатежей =
			ОбщегоНазначения.ОбщийМодуль("Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей");
		ЕстьСБП = МодульНастройкиПодключенияКСистемеБыстрыхПлатежей.ЕстьНастройкиПодключения();
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		МодульНастройкиПодключенияКИнтернетЭквайрингу =
			ОбщегоНазначения.ОбщийМодуль("Справочники.НастройкиПодключенияКИнтернетЭквайрингу");
		ЕстьЭквайринг = МодульНастройкиПодключенияКИнтернетЭквайрингу.ЕстьНастройкиПодключения();
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПлатежныеСистемы.БазоваяФункциональностьПлатежныхСистем") Тогда
		МодульНастройкиПодключенияКПлатежнымСистемам =
			ОбщегоНазначения.ОбщийМодуль("Справочники.НастройкиПодключенияКПлатежнымСистемам");
		ЕстьПлатежныеСистемы = МодульНастройкиПодключенияКПлатежнымСистемам.ЕстьНастройкиПодключения();
	КонецЕсли;
	
	Возврат ЕстьСБП Или ЕстьЭквайринг Или ЕстьПлатежныеСистемы;

КонецФункции

#КонецОбласти

#Область РасчетHMAC

// Формирование подписи для аутентификации (HMAC).
// См. RFC 2104 https://www.ietf.org/rfc/rfc2104.txt.
//
// Параметры:
//  СекретныйКлюч - ДвоичныеДанные - секретный ключ шифрования.
//  Данные - ДвоичныеДанные  - данные шифрования.
//  ХешФункция - ХешФункция  - хеш функция, например ХешФункция.SHA256.
// 
// Возвращаемое значение:
//  Строка - сформированный hmac.
//
Функция РассчитатьHmac(Знач СекретныйКлюч, Знач Данные, Знач ХешФункция)
	
	РазмерБлока = 64;
	
	Если СекретныйКлюч.Размер() > РазмерБлока Тогда
		СекретныйКлюч = Хешировать(СекретныйКлюч, ХешФункция);
	КонецЕсли;
	
	ПустыеДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки("");
	СекретныйКлюч = ОбрезатьДвоичныеДанныеСлева(СекретныйКлюч, РазмерБлока);
	
	КлючВБлоке1 = ЗабитьДвоичныеДанныеСправа(СекретныйКлюч, РазмерБлока, "00");
	
	Блок0x36 = ЗабитьДвоичныеДанныеСправа(ПустыеДвоичныеДанные, РазмерБлока, "36");
	КлючВБлоке2 = ОбъединитьДвоичныеДанныеXOR(КлючВБлоке1, Блок0x36);
	
	Блок0x5C = ЗабитьДвоичныеДанныеСправа(ПустыеДвоичныеДанные, РазмерБлока, "5C");
	КлючВБлоке3 = ОбъединитьДвоичныеДанныеXOR(КлючВБлоке1, Блок0x5C);
	
	КлючИДанные = СоединитьДвоичныеДанныеПоЗначениям(КлючВБлоке2, Данные);
	ДанныеХеш = СоединитьДвоичныеДанныеПоЗначениям(КлючВБлоке3, Хешировать(КлючИДанные, ХешФункция));
	Результат = Хешировать(ДанныеХеш, ХешФункция);
	
	Возврат ПолучитьHexСтрокуИзДвоичныхДанных(Результат);
	
КонецФункции

// Объединяет несколько объектов типа ДвоичныеДанные в один.
//
// Параметры:
//  ДвоичныеДанные1 - ДвоичныеДанные - первое значение;
//  ДвоичныеДанные2 - ДвоичныеДанные - второе значение.
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат соединения.
//
Функция СоединитьДвоичныеДанныеПоЗначениям(Знач ДвоичныеДанные1, Знач ДвоичныеДанные2)
	
	Массив = Новый Массив;
	Массив.Добавить(ДвоичныеДанные1);
	Массив.Добавить(ДвоичныеДанные2);
	
	Возврат СоединитьДвоичныеДанные(Массив);
	
КонецФункции

// Дополняет двоичные данные переданным блоком.
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные - значение для обработки;
//  Длина - Число - длина дополнения;
//  Строка16 - Строка - строка дополнения;
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат обработки.
//
Функция ЗабитьДвоичныеДанныеСправа(Знач ДвоичныеДанные, Знач Длина, Знач Строка16)
	
	Значение = ПолучитьДвоичныеДанныеИзHexСтроки(Строка16);
	РазмерБлока = Длина - ДвоичныеДанные.Размер();
	
	Блок = Значение;
	МассивБлока = Новый Массив;
	МассивБлока.Добавить(Значение);
	Пока Блок.Размер() < РазмерБлока Цикл
		МассивБлока.Добавить(Блок);
		Блок = СоединитьДвоичныеДанные(МассивБлока);
	КонецЦикла;
	
	МассивДанных = Новый Массив;
	МассивДанных.Добавить(ДвоичныеДанные);
	МассивБлоков = РазделитьДвоичныеДанные(Блок, РазмерБлока);
	Если МассивБлоков.Количество() > 0 Тогда
		МассивДанных.Добавить(МассивБлоков[0]);
	КонецЕсли;
	Результат = СоединитьДвоичныеДанные(МассивДанных);
	
	Возврат Результат;
	
КонецФункции

// Производит операцию исключающего ИЛИ для двоичных данных.
//
// Параметры:
//  ДвоичныеДанные1 - ДвоичныеДанные - первое значение;
//  ДвоичныеДанные2 - ДвоичныеДанные - второе значение.
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат операции.
//
Функция ОбъединитьДвоичныеДанныеXOR(Знач ДвоичныеДанные1, Знач ДвоичныеДанные2)
	
	ПотокВПамяти = Новый ПотокВПамяти();
	ЗаписьДанных = Новый ЗаписьДанных(ПотокВПамяти);
	
	ЧтениеДанных1 = Новый ЧтениеДанных(ДвоичныеДанные1);
	ЧтениеДанных2 = Новый ЧтениеДанных(ДвоичныеДанные2);
	
	Буфер1 = ЧтениеДанных1.ПрочитатьВБуферДвоичныхДанных();
	Буфер2 = ЧтениеДанных2.ПрочитатьВБуферДвоичныхДанных();
	
	Если Буфер1.Размер > Буфер2.Размер Тогда
		Буфер1.ЗаписатьПобитовоеИсключительноеИли(0, Буфер2, Буфер2.Размер);
		ЗаписьДанных.ЗаписатьБуферДвоичныхДанных(Буфер1);
	Иначе 
		Буфер2.ЗаписатьПобитовоеИсключительноеИли(0, Буфер1, Буфер1.Размер);
		ЗаписьДанных.ЗаписатьБуферДвоичныхДанных(Буфер2);
	КонецЕсли;
	
	Результат = ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
	
	Возврат Результат;
	
КонецФункции

// Выполняет удаление заданного количества байт из двоичных данных.
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные - значение для обработки;
//  КоличествоБайт - Число - количество байт для удаления.
//
// Возвращаемое значение:
//  ДвоичныеДанные - результат операции.
//
Функция ОбрезатьДвоичныеДанныеСлева(Знач ДвоичныеДанные, Знач КоличествоБайт)
	
	ЧтениеДанных = Новый ЧтениеДанных(ДвоичныеДанные);
	
	ПотокВПамяти = Новый ПотокВПамяти();
	ЗаписьДанных = Новый ЗаписьДанных(ПотокВПамяти);
	
	Буфер = ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных(КоличествоБайт);
	ЗаписьДанных.ЗаписатьБуферДвоичныхДанных(Буфер);
	
	Возврат ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции

// Создает случайное число длиной 16-32 разряда
// и переводит его в hex систему счисления.
//
// Возвращаемое значение:
//  Строка - случайное число в hex.
//
Функция ДлинноеСлучайноеЧислоHEX()
	
	ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел();
	ДлинаЧисла = ГенераторСлучайныхЧисел.СлучайноеЧисло(16, 32);
	
	// Для инициализации первого разряда исключается 0.
	СлучайноеЧисло = "" + ГенераторСлучайныхЧисел.СлучайноеЧисло(1, 9);
	
	// Последовательное формирование случайного числа по разрядам.
	Счетчик = 1;
	Пока Счетчик < ДлинаЧисла Цикл
		СлучайноеЧисло = СлучайноеЧисло + ГенераторСлучайныхЧисел.СлучайноеЧисло(0, 9);
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Возврат ПреобразоватьЧислоВHEX(Число(СлучайноеЧисло));
	
КонецФункции

// Переводит число в систему счисления hex.
//
// Параметры:
//  Значение - Число - исходное значения для преобразования;
//
// Возвращаемое значение:
//  Строка - результат преобразования
//
Функция ПреобразоватьЧислоВHEX(Знач Значение)
	
	Если Значение <= 0 Тогда
		ЧислоHEX = "0";
	Иначе
		Значение = Цел(Значение);
		ЧислоHEX = "";
		Пока Значение > 0 Цикл
			ЧислоHEX = Сред("0123456789ABCDEF", Значение % 16 + 1, 1) + ЧислоHEX;
			Значение = Цел(Значение / 16);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЧислоHEX;
	
КонецФункции

#КонецОбласти

#Область ОбработкаОшибок

// Возвращает код ошибки "ОтсутствуетДоступКСервису".
//
// Возвращаемое значение:
//  Строка
//
Функция КодОшибкиОтсутствуетДоступКСервису()
	
	Возврат "ОтсутствуетДоступКСервису";
	
КонецФункции

// Возвращает код ошибки "ПревышеноКоличествоПопыток".
//
// Возвращаемое значение:
//  Строка
//
Функция КодОшибкиПревышеноКоличествоПопыток()
	
	Возврат "ПревышеноКоличествоПопыток";
	
КонецФункции

// Возвращает код ошибки "ТребуетсяОплата".
//
// Возвращаемое значение:
//  Строка
//
Функция КодОшибкиТребуетсяОплата()
	
	Возврат "ТребуетсяОплата";
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

// См. НастройкиПлатежныхСистемСобытия.ПриОпределенииДоступностиИзмененияДлительностиОперации
Функция ДоступноИзменениеДлительностьОпераций()

	ИзменениеДоступно = Ложь;
	НастройкиПлатежныхСистемСобытия.ПриОпределенииДоступностиИзмененияДлительностиОперации(ИзменениеДоступно);
	
	Возврат ИзменениеДоступно;
	
КонецФункции

// Добавляет запись в журнал регистрации.
//
// Параметры:
//  СообщениеОбОшибке - Строка - комментарий к записи журнала регистрации;
//  Ошибка - Булево - если истина будет установлен уровень журнала регистрации "Ошибка".
//
Процедура ЗаписатьИнформациюВЖурналРегистрации(
		СообщениеОбОшибке,
		Ошибка = Истина)
	
	УровеньЖР = ?(Ошибка, УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖР,
		,
		,
		Лев(СообщениеОбОшибке, 5120));
	
КонецПроцедуры

// Возвращает имя события для журнала регистрации, которое используется для записи событий.
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Настройки платежных систем'",
		ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Производит чтение кода ошибки сервиса из тела ответа.
//
// Параметры:
//  ТелоJSON - Строка - тело ответа сервиса.
//
// Возвращаемое значение:
//  Строка - код ошибки сервиса.
//
Функция ОпределитьКодОшибкиСервиса(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  {
	//  "type": "string",
	//  "title": "string",
	//  "status": "string",
	//  "detail": "string",
	//  "instance": "string"
	// }
	
	// Определение ошибки выполняется через попытку, т.к. в случае ошибки сервиса
	// есть вероятность получить не формализованное сообщение.
	Попытка
		ЧтениеОтвета = ОбщегоНазначения.JSONВЗначение(ТелоJSON);
		Возврат ЧтениеОтвета.Получить("type");
	Исключение
		Возврат "";
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
