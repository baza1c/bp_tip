#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция МесяцыНерегулярногоЕдиногоНалоговогоПлатежа() Экспорт
	
	// ИП на УСН (доходы) без сотрудников пополняет ЕНС только в эти месяцы
	Результат = Новый Массив;
	
	Результат.Добавить(4); // апрель
	Результат.Добавить(7); // июль
	Результат.Добавить(10); // октябрь
	
	Возврат Результат;
	
КонецФункции

Функция УдержанныйНДФЛЗаПериод(Организация, Период, ВтораяПоловинаМесяца = Ложь, ТолькоПроверка = Ложь) Экспорт
	
	ПереноситьСрокУплаты = Период >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж();
	
	ТаблицаНДФЛ = НоваяТаблицаУдержанногоНДФЛ();
	
	ПоследнийПериод = Ложь;
	
	Если ПереноситьСрокУплаты Тогда
		
		// Федеральный закон от 31.07.2023 № 389-ФЗ:
		// 12.2. В течение 2023 года налоговые агенты могут подавать уведомления об исчисленных суммах по налогу на доходы физических лиц, 
		// подлежащему перечислению в сроки, установленные пунктом 6 статьи 226, пунктом 9 статьи 226.1, пунктом 7 статьи 226.2 Налогового кодекса Российской Федерации, 
		// до истечения 12-го числа текущего месяца, указав в таком уведомлении сумму налога, 
		// удержанную в период с 23-го числа предыдущего месяца до 9-го числа текущего месяца.
		НачалоПериодаПодачиПромежуточныхУведомлений = НачалоДня(Дата(2023, 10, 1));
		КонецПериодаПодачиПромежуточныхУведомлений = КонецДня(Дата(2023, 12, 31));
		
		Если ВтораяПоловинаМесяца Тогда // 23 число - конец месяца
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период), 23);
			ДатаОкончанияОтбора = КонецМесяца(Период);
			ПоследнийПериод = КонецМесяца(Период) = КонецГода(Период);
		ИначеЕсли НачалоМесяца(Период) = НачалоГода(Период) Тогда // Январь
			ДатаНачалаОтбора    = НачалоГода(Период);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 22);
		ИначеЕсли Период >= НачалоПериодаПодачиПромежуточныхУведомлений 
			И Период <= КонецПериодаПодачиПромежуточныхУведомлений 
			И День(Период) >= 9 И День(Период) < 23 Тогда
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период)-1, 23);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 9);
		ИначеЕсли Период < ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ() Тогда // остальные периоды 2023 г.
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период)-1, 23);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 22);
		Иначе // остальные периоды 2024 г.
			ДатаНачалаОтбора    = Дата(Год(Период), Месяц(Период), 1);
			ДатаОкончанияОтбора = Дата(Год(Период), Месяц(Период), 22);
		КонецЕсли;
	Иначе
		ДатаНачалаОтбора    = НачалоМесяца(Период);
		ДатаОкончанияОтбора = КонецМесяца(Период)
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		
		УдержаноНалогаЗаПериод = УчетНДФЛ.УдержаноНалогаЗаПериод(Организация, ДатаНачалаОтбора, ДатаОкончанияОтбора);
		
		Если ТолькоПроверка Тогда
			// Достаточно знать, что есть удержанный НДФЛ
			Возврат УдержаноНалогаЗаПериод;
		КонецЕсли;
		
		КодТерритории = Справочники.Организации.КодТерриторииМестаРегистрации(Организация);
		ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(КодТерритории, Период);
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") =
				Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			КПП = "0";
		Иначе
			КПП = Справочники.Организации.КППНаДату(Организация, КонецМесяца(Период), Истина);
		КонецЕсли;
		
		РегистрацииВНалоговомОргане = ОбщегоНазначения.ВыгрузитьКолонку(УдержаноНалогаЗаПериод, "РегистрацияВНалоговомОргане", Истина);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(
			РегистрацииВНалоговомОргане, Неопределено);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(
			РегистрацииВНалоговомОргане, Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
		
		РеквизитыРегистраций = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(РегистрацииВНалоговомОргане, "КодПоОКТМО, Код, КПП");
		
		Если ПереноситьСрокУплаты Тогда
			ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
			ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
			ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
			ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
				ПроизводственныйКалендарь,
				ОбщегоНазначения.ВыгрузитьКолонку(УдержаноНалогаЗаПериод, "СрокПеречисленияНалога", Истина),
				ПараметрыПолучения);
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из УдержаноНалогаЗаПериод Цикл
			
			ЗначениеКодТерритории = КодТерритории;
			ЗначениеКПП           = КПП;
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.РегистрацияВНалоговомОргане) Тогда
				РеквизитыРегистрации = РеквизитыРегистраций[СтрокаТаблицы.РегистрацияВНалоговомОргане];
				ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(РеквизитыРегистрации.КодПоОКТМО, Период);
				Если Не ПустаяСтрока(РеквизитыРегистрации.КодПоОКТМО) Тогда
					ЗначениеКодТерритории = РеквизитыРегистрации.КодПоОКТМО;
				КонецЕсли;
				Если Не ПустаяСтрока(РеквизитыРегистрации.КПП) Тогда
					ЗначениеКПП = РеквизитыРегистрации.КПП;
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаТаблицы.Сумма <> 0 Тогда
				НоваяСтрока = ТаблицаНДФЛ.Добавить();
				НоваяСтрока.Организация                 = Организация;
				НоваяСтрока.РегистрацияВНалоговомОргане = СтрокаТаблицы.РегистрацияВНалоговомОргане;
				НоваяСтрока.КПП                         = ЗначениеКПП;
				НоваяСтрока.ОКАТО                       = ЗначениеКодТерритории;
				НоваяСтрока.Сумма                       = СтрокаТаблицы.Сумма;
				НоваяСтрока.СрокУплаты                  = ?(ПереноситьСрокУплаты, ГрафикПереноса[СтрокаТаблицы.СрокПеречисленияНалога], СтрокаТаблицы.СрокПеречисленияНалога);
				НоваяСтрока.КБК                         = СтрокаТаблицы.КБК;
				НоваяСтрока.Налог                       = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(СтрокаТаблицы.КБК);
				НоваяСтрока.ИсходныйСрокУплаты          = ?(ПоследнийПериод, НачалоДня(КонецГода(Период)), СтрокаТаблицы.СрокПеречисленияНалога);
			КонецЕсли;
			
		КонецЦикла;

	Иначе
		
		Если Период >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж() Тогда
			
			Если ВтораяПоловинаМесяца И Месяц(Период) = 12 Тогда // 23-31 декабря
				НачалоПериодаУплаты    = Дата(Год(Период), Месяц(Период), 23);
				ОкончаниеПериодаУплаты = КонецГода(Период);
				ПоследнийПериод = КонецМесяца(Период) = КонецГода(Период);
			Иначе
				НачалоПериодаУплаты    = НачалоМесяца(Период);
				ОкончаниеПериодаУплаты = КонецМесяца(Период);
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Организация",            Организация);
			Запрос.УстановитьПараметр("НачалоПериода",          НачалоМесяца(ДатаНачалаОтбора));
			Запрос.УстановитьПараметр("КонецПериода",           КонецМесяца(ДатаОкончанияОтбора));
			Запрос.УстановитьПараметр("НачалоПериодаУплаты",    НачалоПериодаУплаты);
			Запрос.УстановитьПараметр("ОкончаниеПериодаУплаты", ОкончаниеПериодаУплаты);
			Запрос.УстановитьПараметр("ПостояннаяПередачаПромежуточныхУведомлений",
				Период >= ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ());
			
			Запрос.УстановитьПараметр("НачалоПериодаПодачиПромежуточныхУведомлений", НачалоПериодаПодачиПромежуточныхУведомлений);
			Запрос.УстановитьПараметр("КонецПериодаПодачиПромежуточныхУведомлений",  КонецПериодаПодачиПромежуточныхУведомлений);
			Запрос.УстановитьПараметр("ДатаНачалаОтбора",                            ДатаНачалаОтбора);
			Запрос.УстановитьПараметр("ДатаОкончанияОтбора",                         ДатаОкончанияОтбора);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК КАК КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана КАК КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП КАК КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО КАК КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты КАК СрокУплаты,
			|	СУММА(СведенияОбУдержанномНДФЛНДФЛ.Сумма) КАК Сумма
			|ИЗ
			|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
			|		ПО СведенияОбУдержанномНДФЛ.Ссылка = СведенияОбУдержанномНДФЛНДФЛ.Ссылка
			|ГДЕ
			|	СведенияОбУдержанномНДФЛ.Организация = &Организация
			|	И СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериода И &КонецПериода
			|	И СведенияОбУдержанномНДФЛ.Проведен
			|	И ВЫБОР
			|			КОГДА НЕ &ПостояннаяПередачаПромежуточныхУведомлений
			|				ТОГДА СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты МЕЖДУ &НачалоПериодаУплаты И &ОкончаниеПериодаУплаты
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ
			|	И ВЫБОР
			|			КОГДА СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериодаПодачиПромежуточныхУведомлений И &КонецПериодаПодачиПромежуточныхУведомлений
			|					ИЛИ &ПостояннаяПередачаПромежуточныхУведомлений
			|				ТОГДА СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания МЕЖДУ &ДатаНачалаОтбора И &ДатаОкончанияОтбора
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты";
		Иначе
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Организация",   Организация);
			Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаОтбора);
			Запрос.УстановитьПараметр("КонецПериода",  ДатаОкончанияОтбора);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК КАК КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана КАК КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП КАК КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО КАК КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты КАК СрокУплаты,
			|	СУММА(СведенияОбУдержанномНДФЛНДФЛ.Сумма) КАК Сумма
			|ИЗ
			|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
			|		ПО СведенияОбУдержанномНДФЛ.Ссылка = СведенияОбУдержанномНДФЛНДФЛ.Ссылка
			|ГДЕ
			|	СведенияОбУдержанномНДФЛ.Организация = &Организация
			|	И СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериода И &КонецПериода
			|	И СведенияОбУдержанномНДФЛ.Проведен
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОбУдержанномНДФЛНДФЛ.КодБК,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО,
			|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана,
			|	СведенияОбУдержанномНДФЛНДФЛ.КПП,
			|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты";
			
		КонецЕсли;
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		Если ТолькоПроверка Тогда
			// Достаточно знать, что есть удержанный НДФЛ
			Возврат Результат;
		КонецЕсли;
		
		Если ПереноситьСрокУплаты Тогда
			ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
			ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
			ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
			ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
				ПроизводственныйКалендарь,
				ОбщегоНазначения.ВыгрузитьКолонку(Результат, "СрокУплаты", Истина),
				ПараметрыПолучения);
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из Результат Цикл
			
			Если СтрокаТаблицы.Сумма <> 0 Тогда
				НоваяСтрока = ТаблицаНДФЛ.Добавить();
				НоваяСтрока.Организация = Организация;
				НоваяСтрока.Налог                       = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(СтрокаТаблицы.КодБК);
				НоваяСтрока.ВидНалога                   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Налог, "ВидНалога");
				НоваяСтрока.КБК                         = СтрокаТаблицы.КодБК;
				НоваяСтрока.КодНалоговогоОргана         = СтрокаТаблицы.КодНалоговогоОргана;
				НоваяСтрока.КПП                         = СтрокаТаблицы.КПП;
				НоваяСтрока.ОКАТО                       = СтрокаТаблицы.КодПоОКТМО;
				НоваяСтрока.Сумма                       = СтрокаТаблицы.Сумма;
				НоваяСтрока.СрокУплаты                  = ?(ПереноситьСрокУплаты, ГрафикПереноса[СтрокаТаблицы.СрокУплаты], СтрокаТаблицы.СрокУплаты);
				НоваяСтрока.ИсходныйСрокУплаты          = ?(ПоследнийПериод, НачалоДня(КонецГода(Период)), СтрокаТаблицы.СрокУплаты);
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, ТаблицаНДФЛ);
		
	КонецЕсли;
	
	КолонкиСвертки = Новый Массив;
	Для Каждого КолонкаНДФЛ Из ТаблицаНДФЛ.Колонки Цикл
		Если КолонкаНДФЛ.Имя <> "Сумма" Тогда
			КолонкиСвертки.Добавить(КолонкаНДФЛ.Имя);
		КонецЕсли;
	КонецЦикла;
	ТаблицаНДФЛ.Свернуть(СтрСоединить(КолонкиСвертки, ","), "Сумма");
	
	Возврат ТаблицаНДФЛ;
	
КонецФункции

Функция УдержанныйНДФЛПоДокументу(Организация, ДокументСсылка) Экспорт
	
	ПереноситьСрокУплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Дата") >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж();
	
	ТаблицаНДФЛ = НоваяТаблицаУдержанногоНДФЛ();
	
	ПоследнийПериод = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СведенияОбУдержанномНДФЛНДФЛ.КодБК КАК КодБК,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	СведенияОбУдержанномНДФЛНДФЛ.КПП КАК КПП,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО КАК КодПоОКТМО,
	|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты КАК СрокУплаты,
	|	СУММА(СведенияОбУдержанномНДФЛНДФЛ.Сумма) КАК Сумма,
	|	СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания КАК ДатаУдержания
	|ИЗ
	|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
	|		ПО СведенияОбУдержанномНДФЛ.Ссылка = СведенияОбУдержанномНДФЛНДФЛ.Ссылка
	|ГДЕ
	|	СведенияОбУдержанномНДФЛ.Организация = &Организация
	|	И СведенияОбУдержанномНДФЛ.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОбУдержанномНДФЛНДФЛ.КодБК,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодПоОКТМО,
	|	СведенияОбУдержанномНДФЛНДФЛ.КодНалоговогоОргана,
	|	СведенияОбУдержанномНДФЛНДФЛ.КПП,
	|	СведенияОбУдержанномНДФЛНДФЛ.СрокУплаты,
	|	СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания";

	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ПереноситьСрокУплаты Тогда
		ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
		ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
		ПараметрыПолучения.ПолучатьДатыЕслиКалендарьНеЗаполнен = Истина;
		ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
			ПроизводственныйКалендарь,
			ОбщегоНазначения.ВыгрузитьКолонку(Результат, "СрокУплаты", Истина),
			ПараметрыПолучения);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Результат Цикл
		
		Если СтрокаТаблицы.Сумма <> 0 Тогда
			НоваяСтрока = ТаблицаНДФЛ.Добавить();
			НоваяСтрока.Организация         = Организация;
			НоваяСтрока.Налог               = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(СтрокаТаблицы.КодБК);
			НоваяСтрока.ВидНалога           = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Налог, "ВидНалога");
			НоваяСтрока.КБК                 = СтрокаТаблицы.КодБК;
			НоваяСтрока.КодНалоговогоОргана = СтрокаТаблицы.КодНалоговогоОргана;
			НоваяСтрока.КПП                 = СтрокаТаблицы.КПП;
			НоваяСтрока.ОКАТО               = СтрокаТаблицы.КодПоОКТМО;
			НоваяСтрока.Сумма               = СтрокаТаблицы.Сумма;
			НоваяСтрока.СрокУплаты          = ?(ПереноситьСрокУплаты, ГрафикПереноса[СтрокаТаблицы.СрокУплаты], СтрокаТаблицы.СрокУплаты);
			
			ПоследнийПериод = КонецГода(СтрокаТаблицы.ДатаУдержания) = КонецМесяца(СтрокаТаблицы.ДатаУдержания)
				И День(СтрокаТаблицы.ДатаУдержания) > 22;
			НоваяСтрока.ИсходныйСрокУплаты          = ?(ПоследнийПериод, НачалоДня(КонецГода(СтрокаТаблицы.ДатаУдержания)), СтрокаТаблицы.СрокУплаты);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, ТаблицаНДФЛ);
	
	КолонкиСвертки = Новый Массив;
	Для Каждого КолонкаНДФЛ Из ТаблицаНДФЛ.Колонки Цикл
		Если КолонкаНДФЛ.Имя <> "Сумма" Тогда
			КолонкиСвертки.Добавить(КолонкаНДФЛ.Имя);
		КонецЕсли;
	КонецЦикла;
	ТаблицаНДФЛ.Свернуть(СтрСоединить(КолонкиСвертки, ","), "Сумма");
	
Возврат ТаблицаНДФЛ;
	
КонецФункции

Функция ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ() Экспорт 
	
	Возврат '20240101'
	
КонецФункции

Процедура ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, КоллекцияНачисленныйНДФЛ) Экспорт
	
	СведенияОбОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ГоловнаяОрганизация, ЮридическоеФизическоеЛицо");
	ОрганизацияЮрлицо     = СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СведенияОбОрганизации.ГоловнаяОрганизация);
	
	Если ТипЗнч(КоллекцияНачисленныйНДФЛ) = Тип("ДанныеФормыКоллекция") Тогда
		ТаблицаНДФЛ = КоллекцияНачисленныйНДФЛ.Выгрузить();
	Иначе
		// передана таблица значений
		
		ТаблицаНДФЛ = КоллекцияНачисленныйНДФЛ.Скопировать();
		
		Если КоллекцияНачисленныйНДФЛ.Колонки.Найти("РегистрацияВНалоговомОргане") = Неопределено Тогда
			КоллекцияНачисленныйНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
		КонецЕсли;
		
	КонецЕсли;
	
	ИмяКолонкиОКТМО = "КодПоОКТМО";
	Если ТаблицаНДФЛ.Колонки.Найти("КодПоОКТМО") = Неопределено Тогда
		ТаблицаНДФЛ.Колонки.ОКАТО.Имя = "КодПоОКТМО";
		ИмяКолонкиОКТМО = "ОКАТО";
	КонецЕсли;
	
	ПоляСвертки = "КодНалоговогоОргана";
	Если ОрганизацияЮрлицо Тогда
		ПоляСвертки = ПоляСвертки + ",КПП";
	КонецЕсли;
	ПоляДополнительногоОтбора = ПоляСвертки;
	ПоляСвертки = ПоляСвертки + ",КодПоОКТМО";
	
	ТаблицаНДФЛ.Свернуть(ПоляСвертки);
	ТаблицаНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацииВНалоговомОргане.КПП КАК КПП,
	|	РегистрацииВНалоговомОргане.Код КАК КодНалоговогоОргана
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &Организация";
	
	ДействующиеРегистрации = Запрос.Выполнить().Выгрузить();
	
	Отбор = Новый Структура(ПоляСвертки);
	ОтборДополнительный = Новый Структура(ПоляДополнительногоОтбора);
	
	Для каждого СтрокаТЗ Из ТаблицаНДФЛ Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		Регистрации = ДействующиеРегистрации.НайтиСтроки(Отбор);
		Если Регистрации.Количество() > 0 Тогда
			СтрокаТЗ.РегистрацияВНалоговомОргане = Регистрации[0].РегистрацияВНалоговомОргане;
		Иначе
			ЗаполнитьЗначенияСвойств(ОтборДополнительный, СтрокаТЗ);
			Регистрации = ДействующиеРегистрации.НайтиСтроки(ОтборДополнительный);
			Если Регистрации.Количество() > 0 Тогда
				СтрокаТЗ.РегистрацияВНалоговомОргане = Регистрации[0].РегистрацияВНалоговомОргане;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаКоллекции Из КоллекцияНачисленныйНДФЛ Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаКоллекции);
		Отбор.КодПоОКТМО = СтрокаКоллекции[ИмяКолонкиОКТМО];
		Регистрации = ТаблицаНДФЛ.НайтиСтроки(Отбор);
		Если Регистрации.Количество() > 0 Тогда
			СтрокаКоллекции.РегистрацияВНалоговомОргане = Регистрации[0].РегистрацияВНалоговомОргане;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НачисленыВзносы(Организация, Период, ВариантВыборкиСтраховыхВзносов, БезусловныйКонтроль = Ложь) Экспорт
	
	НачисленыСтраховыеВзносы = ВыплачиваетсяЗарплата(Организация, Период);
	Если Не НачисленыСтраховыеВзносы Или БезусловныйКонтроль Тогда
		НачисленыСтраховыеВзносы = ЕстьДвиженияРасчетовПоВзносамЗаПериод(Организация, Период, ВариантВыборкиСтраховыхВзносов);
	КонецЕсли;
	
	Возврат НачисленыСтраховыеВзносы;

КонецФункции

Функция ВыплачиваетсяЗарплата(Организация, Период) Экспорт
	
	НачалоПериода    = НачалоМесяца(Период);
	ОкончаниеПериода = КонецМесяца(Период);
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
			Возврат Истина;
		Иначе
			Возврат ИПИспользуетТрудНаемныхРаботниковЗаПериод(Организация, НачалоПериода, ОкончаниеПериода);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организация);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "Организация");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоПериода);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              ОкончаниеПериода);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("РаботникиПоДоговорамГПХ",       Истина);
	
	ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	Если ТаблицаСотрудников.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",        Организация);
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("НачалоПериода",      НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("ОкончаниеПериода",   НачалоДня(ОкончаниеПериода));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаСотрудников.Сотрудник КАК Сотрудник,
	|	ТаблицаСотрудников.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_Сотрудники
	|ИЗ
	|	&ТаблицаСотрудников КАК ТаблицаСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтпускБезСохраненияОплаты.Сотрудник КАК Сотрудник,
	|	ОтпускБезСохраненияОплаты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.ОтпускБезСохраненияОплаты КАК ОтпускБезСохраненияОплаты
	|ГДЕ
	|	ОтпускБезСохраненияОплаты.Проведен
	|	И ОтпускБезСохраненияОплаты.ДатаНачала <= &НачалоПериода
	|	И ОтпускБезСохраненияОплаты.ДатаОкончания >= &ОкончаниеПериода
	|	И ОтпускБезСохраненияОплаты.Организация = &Организация
	|	И ОтпускБезСохраненияОплаты.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТ_Сотрудники.Сотрудник
	|			ИЗ
	|				ВТ_Сотрудники)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Сотрудники.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ВТ_Документы.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетНачислений
	|ПОМЕСТИТЬ ВТ_СотрудникиДокументы
	|ИЗ
	|	ВТ_Сотрудники КАК ВТ_Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Документы КАК ВТ_Документы
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_Документы.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СотрудникиДокументы.Сотрудник КАК Сотрудник,
	|	ВТ_СотрудникиДокументы.ТребуетНачислений КАК ТребуетНачислений
	|ИЗ
	|	ВТ_СотрудникиДокументы КАК ВТ_СотрудникиДокументы
	|ГДЕ
	|	ВТ_СотрудникиДокументы.ТребуетНачислений";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяТаблицаУдержанногоНДФЛ()
	
	ТаблицаНДФЛ = Новый ТаблицаЗначений;
	ТаблицаНДФЛ.Колонки.Добавить("Организация",                 Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	ТаблицаНДФЛ.Колонки.Добавить("КодНалоговогоОргана",         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("Налог",                       Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	ТаблицаНДФЛ.Колонки.Добавить("ВидНалога",                   Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНалогов"));
	ТаблицаНДФЛ.Колонки.Добавить("КБК",                         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("КПП",                         Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("ОКАТО",                       Новый ОписаниеТипов("Строка"));
	ТаблицаНДФЛ.Колонки.Добавить("Сумма",                       Новый ОписаниеТипов("Число"));
	ТаблицаНДФЛ.Колонки.Добавить("СрокУплаты",                  Новый ОписаниеТипов("Дата"));
	ТаблицаНДФЛ.Колонки.Добавить("ИсходныйСрокУплаты",          Новый ОписаниеТипов("Дата"));
	
	Возврат ТаблицаНДФЛ;
	
КонецФункции

Функция ЕстьДвиженияРасчетовПоВзносамЗаПериод(Организация, Период, ВариантВыборкиСтраховыхВзносов)
	
	СчетаУчета = СчетаУчетаСтраховыхВзносов(Период, ВариантВыборкиСтраховыхВзносов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("СчетаУчета",    СчетаУчета);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(Период));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Хозрасчетный.Организация КАК Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.СчетКт В(&СчетаУчета)
	|	И Хозрасчетный.Организация = &Организация
	|	И Хозрасчетный.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СчетаУчетаСтраховыхВзносов(Период, ВариантВыборкиСтраховыхВзносов, ПараметрыСтраховыхВзносов = Неопределено)
	
	СчетаСтраховыхВзносов = Новый Массив;
	
	ВариантыВыборкиСтраховыхВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	
	Если ПараметрыСтраховыхВзносов = Неопределено Тогда
		ПараметрыСтраховыхВзносов = ВыполнениеЗадачБухгалтера.ПараметрыСтраховыхВзносов(Период);
	КонецЕсли;
	
	СчетУчетаТравматизм         = ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ;
	СчетУчетаДобровольныеВзносы = ПланыСчетов.Хозрасчетный.ПФР_ДОБР;
	СчетУчетаДовВзносыЛетчики   = ПланыСчетов.Хозрасчетный.ПФР_доп;
	СчетУчетаДовВзносыШахтеры   = ПланыСчетов.Хозрасчетный.ПФР_доп_шахтеры;
	Если ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.Травматизм Тогда
		СчетаСтраховыхВзносов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СчетУчетаТравматизм);
		ПараметрыСтраховыхВзносов = ПараметрыСтраховыхВзносов.Скопировать(Новый Структура("СчетУчета", СчетУчетаТравматизм));
	ИначеЕсли ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.ДобровольныеВзносы Тогда
		МассивСчетов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СчетУчетаДобровольныеВзносы);
		УсловияОтбораСубсчетов = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловияОтбораСубсчетов.ИспользоватьВПроводках = Истина;
		СчетаСтраховыхВзносов = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(МассивСчетов, УсловияОтбораСубсчетов);
	ИначеЕсли ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.ДопВзносыЛетчикиШахтеры Тогда
		МассивСчетов = Новый Массив;
		МассивСчетов.Добавить(СчетУчетаДовВзносыЛетчики);
		МассивСчетов.Добавить(СчетУчетаДовВзносыШахтеры);
		УсловияОтбораСубсчетов = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловияОтбораСубсчетов.ИспользоватьВПроводках = Истина;
		СчетаСтраховыхВзносов = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(МассивСчетов, УсловияОтбораСубсчетов);
	Иначе
		СчетаСтраховыхВзносов = ПараметрыСтраховыхВзносов.ВыгрузитьКолонку("СчетУчета");
		Если ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.СтраховыеВзносы Тогда
			СтрокаТравматизм = СчетаСтраховыхВзносов.Найти(СчетУчетаТравматизм);
			Если СтрокаТравматизм <> Неопределено Тогда
				СчетаСтраховыхВзносов.Удалить(СтрокаТравматизм);
			КонецЕсли;
			Если Период >= ДатаВыделенияДополнительныхВзносовЛетчиковИШахтеровИзСоставаЕНП() Тогда
				СтрокаДопВзносы = СчетаСтраховыхВзносов.Найти(СчетУчетаДовВзносыЛетчики);
				Если СтрокаДопВзносы <> Неопределено Тогда
					СчетаСтраховыхВзносов.Удалить(СтрокаДопВзносы);
				КонецЕсли;
				СтрокаДопВзносы = СчетаСтраховыхВзносов.Найти(СчетУчетаДовВзносыШахтеры);
				Если СтрокаДопВзносы <> Неопределено Тогда
					СчетаСтраховыхВзносов.Удалить(СтрокаДопВзносы);
				КонецЕсли;
			КонецЕсли;
			МассивСчетов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СчетУчетаДобровольныеВзносы);
			Для Каждого СчетУчета Из МассивСчетов Цикл
				СтрокаДобровольныеВзносы = СчетаСтраховыхВзносов.Найти(СчетУчета);
				Если СтрокаДобровольныеВзносы <> Неопределено Тогда
					СчетаСтраховыхВзносов.Удалить(СтрокаДобровольныеВзносы);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СчетаСтраховыхВзносов;
	
КонецФункции

Функция ДатаВыделенияДополнительныхВзносовЛетчиковИШахтеровИзСоставаЕНП()

	Возврат '20250101'

КонецФункции

Функция ЗарегистрированыРасчетыОрганизацииРаботодателя(Организация, НачалоПериода, КонецПериода)
	
	// В заданном периоде были начисления (зарплата, ГПХ, взносы)
	СчетаРасчетов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасчетов,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасчетов,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами));
		
	СчетаВзносов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ФСС));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПФР));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ФОМС));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ФСС_НСиПЗ));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВзносов ,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.СтраховыеВзносыЕдиныйТариф));
	
	// Необходимо исключить обороты операций, не являющихся начислением взносов:
	//   - возврат из фондов;
	//   - корректировки;
	//   - техническое операции:
	//       - ввод остатков;
	//       - перенос на ЕНС...
	//  и т.п.
	
	КорСчетаПрочихОпераций = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КорСчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Касса));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КорСчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КорСчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЦифровойРубль));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КорСчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Вспомогательный));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КорСчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КорСчетаПрочихОпераций,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("НачалоПериода",          НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",           КонецПериода);
	Запрос.УстановитьПараметр("СчетаРасчетов",          СчетаРасчетов);
	Запрос.УстановитьПараметр("СчетаВзносов",           СчетаВзносов);
	Запрос.УстановитьПараметр("КорСчетаПрочихОпераций", КорСчетаПрочихОпераций);
	Запрос.УстановитьПараметр("ВидыПлатежей",           Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХозрасчетныйОбороты.Организация КАК Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			Счет В (&СчетаВзносов),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосбюджет),
	|			Организация = &Организация
	|				И Субконто1 В (&ВидыПлатежей),
	|			НЕ КорСчет В (&КорСчетаПрочихОпераций),
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотКт <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Период, Счет В (&СчетаРасчетов), , Организация = &Организация, , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотКт <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СведенияОбУдержанномНДФЛ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СведенияОбУдержанномНДФЛ КАК СведенияОбУдержанномНДФЛ
	|ГДЕ
	|	СведенияОбУдержанномНДФЛ.Организация = &Организация
	|	И СведенияОбУдержанномНДФЛ.Месяц МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СведенияОбУдержанномНДФЛ.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтражениеЗарплатыВБухучете.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВБухучете КАК ОтражениеЗарплатыВБухучете
	|ГДЕ
	|	ОтражениеЗарплатыВБухучете.Организация = &Организация
	|	И ОтражениеЗарплатыВБухучете.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ОтражениеЗарплатыВБухучете.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтражениеЗарплатыВУчете.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВУчете КАК ОтражениеЗарплатыВУчете
	|ГДЕ
	|	ОтражениеЗарплатыВУчете.Организация = &Организация
	|	И ОтражениеЗарплатыВУчете.ПериодРегистрации МЕЖДУ &НачалоПериода И &НачалоПериода
	|	И ОтражениеЗарплатыВУчете.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СведенияОбУдержанномНДФЛНДФЛ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СведенияОбУдержанномНДФЛ.НДФЛ КАК СведенияОбУдержанномНДФЛНДФЛ
	|ГДЕ
	|	СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Организация = &Организация
	|	И (СведенияОбУдержанномНДФЛНДФЛ.ДатаУдержания МЕЖДУ &НачалоПериода И &КонецПериода)
	|	И СведенияОбУдержанномНДФЛНДФЛ.Ссылка.Проведен";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ЗарегистрированыРасчетыОрганизацииРаботодателя = Ложь;
	Для Каждого РезультатЗапроса Из Результат Цикл
		Если Не РезультатЗапроса.Пустой() Тогда
			ЗарегистрированыРасчетыОрганизацииРаботодателя = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗарегистрированыРасчетыОрганизацииРаботодателя;
	
КонецФункции

Функция ИПИспользуетТрудНаемныхРаботниковЗаПериод(Организация, Знач НачалоПериода, Знач КонецПериода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации")
		И НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		ОрганизацияСсылка = Организация;
	ИначеЕсли  ТипЗнч(Организация) = Тип("СправочникОбъект.Организации")
		И Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ОрганизацияСсылка = Организация.Ссылка;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	// Дальнейшая проверка только для индивидуального предпринимателя
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// В заданном периоде были сотрудники (актуально только для внутреннего учета)
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
		ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   ОрганизацияСсылка);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "");
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 НачалоПериода);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецПериода);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы",                        Неопределено);
		ПараметрыПолученияСотрудниковОрганизаций.Вставить("РаботникиПоДоговорамГПХ",       Истина);
		
		ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
		
		Если ТаблицаСотрудников.Количество() <> 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Проверка, что в заданном периоде были зарегистрированы начисления: зарплата, ГПХ, взносы, НДФЛ
	// При этом ставим "отсечку" - не ранее даты ввода начальных остатов
	ПроверятьРасчеты = Истина;
	ДатаНачалаУчета = РегистрыСведений.ДатыВводаНачальныхОстатков.ДатаВводаНачальныхОстатков(ОрганизацияСсылка);
	Если ЗначениеЗаполнено(ДатаНачалаУчета) Тогда
		// Если есть дата ввода начальных остатков, то "глубже" этой даты анализировать не будем.
		Если ДатаНачалаУчета >= КонецПериода Тогда
			ПроверятьРасчеты = Ложь;
		ИначеЕсли ДатаНачалаУчета >= НачалоПериода Тогда
			// Дата ввода остатков это предыдущий день начала учета, поэтому сдвигаем на сутки
			НачалоПериода = ДатаНачалаУчета + 86400;
		КонецЕсли;
	КонецЕсли;
	
	Если ПроверятьРасчеты Тогда
		ЗарегистрированыРасчетыОрганизацииРаботодателя = ЗарегистрированыРасчетыОрганизацииРаботодателя(ОрганизацияСсылка, НачалоПериода, КонецПериода);
		Возврат ЗарегистрированыРасчетыОрганизацииРаботодателя;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли
