#Область ПрограммныйИнтерфейс

// Конструктор проверки кодов.
// 
// Возвращаемое значение:
//  Структура - см. тело функции
//
Функция НовыйГрупповаяПроверка() Экспорт
	
	Проверка = Новый ТаблицаЗначений;
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("СправочникСсылка.Организации"));
	СписокТипов.Добавить(Тип("СправочникСсылка.ПодразделенияОрганизаций"));
	ТипОбъект = Новый ОписаниеТипов(СписокТипов);
	
	Проверка.Колонки.Добавить("Объект", Новый ОписаниеТипов(ТипОбъект));
	Проверка.Колонки.Добавить("Налог", Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	Проверка.Колонки.Добавить("ЭтоНалогНДФЛ", Новый ОписаниеТипов("Булево"));
	Проверка.Колонки.Добавить("РегистрацияФНС", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	Проверка.Колонки.Добавить("КодПоОКТМО", Новый ОписаниеТипов("Строка"));
	Проверка.Колонки.Добавить("КодБК", Новый ОписаниеТипов("Строка"));
	Проверка.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	
	Проверка.Колонки.Добавить("РезультатПроверки", Новый ОписаниеТипов("Структура")); // ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйРезультатПроверки()
	
	Возврат Проверка;
	
КонецФункции

// Выполняет проверку кодов по классификаторам.
//
// Параметры:
//  ГрупповаяПроверка - ТаблицаЗначений - см. ПроверкаКодовПоКлассификаторамБП.НовыйГрупповаяПроверка
//
Процедура ПроверитьГруппу(ГрупповаяПроверка) Экспорт
	
	ЗаполнитьЭтоНалогНДФЛ(ГрупповаяПроверка);
	ЗаполнитьОбъект(ГрупповаяПроверка);
	ИнициализироватьРезультатПроверки(ГрупповаяПроверка);
	
	КэшПроверокОКТМО = НовыйГрупповаяПроверка();
	
	Для Каждого Проверка Из ГрупповаяПроверка Цикл
		
		ПараметрыПроверки = ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйПараметрыПроверки();
		
		// Если такая проверка уже выполнялась, то в целях оптимизации следует взять результат аналогичной проверки.
		// Это позволит исключить избыточные запросы к внешнему сервису.
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("КодПоОКТМО", Проверка.КодПоОКТМО);
		ПараметрыПоиска.Вставить("ЭтоНалогНДФЛ", Проверка.ЭтоНалогНДФЛ);
		Если Проверка.ЭтоНалогНДФЛ Тогда
			ПараметрыПоиска.Вставить("РегистрацияФНС", Проверка.РегистрацияФНС);
		КонецЕсли;
		
		РезультатПоиска = КэшПроверокОКТМО.НайтиСтроки(ПараметрыПоиска);
		ПроверкаВыполнялась = РезультатПоиска.Количество() > 0;
		Если ПроверкаВыполнялась Тогда
			Результат = РезультатПоиска[0];
			ЗаполнитьЗначенияСвойств(Проверка.РезультатПроверки.КодПоОКТМО, Результат.РезультатПроверки.КодПоОКТМО);
		Иначе
			ПараметрыПроверки.КодПоОКТМО.Объект = Проверка.Объект;
			Если ТипЗнч(Проверка.Объект) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
				ПараметрыПроверки.КодПоОКТМО.ВидКонтактнойИнформации =
					Справочники.ВидыКонтактнойИнформации.ФактическийАдресПодразделенияОрганизаций;
			КонецЕсли;
			ПараметрыПроверки.КодПоОКТМО.ОКТМО = Проверка.КодПоОКТМО;
		КонецЕсли;
		
		ПараметрыПроверки.КодБК.КБК = Проверка.КодБК;
		ПараметрыПроверки.КодБК.Дата = Проверка.Дата;
		
		РезультатПроверки = ПроверкаКодовПоКлассификаторам.Проверить(ПараметрыПроверки);
		ЗаполнитьЗначенияСвойств(Проверка.РезультатПроверки.КодБК, РезультатПроверки.КодБК);
		
		Если Не ПроверкаВыполнялась Тогда
			ЗаполнитьЗначенияСвойств(Проверка.РезультатПроверки.КодПоОКТМО, РезультатПроверки.КодПоОКТМО);
			НоваяЗапись = КэшПроверокОКТМО.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Проверка);
			НоваяЗапись.РезультатПроверки = ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйРезультатПроверки();
			ЗаполнитьЗначенияСвойств(НоваяЗапись.РезультатПроверки.КодПоОКТМО, РезультатПроверки.КодПоОКТМО);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбъектыРегистрацийФНС(РегистрацииФНС)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("РегистрацииФНС", РегистрацииФНС);
	
	// Для проверки кодов по ОКТМО имеют значения только Объекты, у которых заполнен адрес.
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ОрганизацииКонтактнаяИнформация.Ссылка КАК Объект,
	|	ОрганизацииКонтактнаяИнформация.Ссылка.РегистрацияВНалоговомОргане КАК РегистрацияФНС
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|ГДЕ
	|	ОрганизацииКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации)
	|	И ОрганизацииКонтактнаяИнформация.Ссылка.РегистрацияВНалоговомОргане В(&РегистрацииФНС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодразделенияКонтактнаяИнформация.Ссылка,
	|	ПодразделенияКонтактнаяИнформация.Ссылка.РегистрацияВНалоговомОргане
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций.КонтактнаяИнформация КАК ПодразделенияКонтактнаяИнформация
	|ГДЕ
	|	ПодразделенияКонтактнаяИнформация.Ссылка.РегистрацияВНалоговомОргане В(&РегистрацииФНС)
	|	И ПодразделенияКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактическийАдресПодразделенияОрганизаций)
	|	И ПодразделенияКонтактнаяИнформация.Ссылка.ОбособленноеПодразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Объект";
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("РегистрацияФНС");
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьЭтоНалогНДФЛ(ГрупповаяПроверка)
	
	ВидыНалоговНДФЛ = Перечисления.ВидыНалогов.НалоговыеПлатежиНДФЛ();
	НалогиНДФЛ = Справочники.ВидыНалоговИПлатежейВБюджет.НалогиПоВидам(ВидыНалоговНДФЛ);
	
	Для Каждого Проверка Из ГрупповаяПроверка Цикл
		Проверка.ЭтоНалогНДФЛ = НалогиНДФЛ.Найти(Проверка.Налог) <> Неопределено;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОбъект(ГрупповаяПроверка)
	
	ОбъектыРегистраций = ОбъектыРегистрацийФНС(ГрупповаяПроверка.ВыгрузитьКолонку("РегистрацияФНС"));
	
	Для Каждого Проверка Из ГрупповаяПроверка Цикл
		Если Проверка.ЭтоНалогНДФЛ Тогда
			// Для налогов по НДФЛ необходимо получить Объект по Регистрации в ФНС. Это позволит выполнить проверку по адресу Объекта.
			ОбъектСтроки = ОбъектыРегистраций.НайтиСтроки(Новый Структура("РегистрацияФНС", Проверка.РегистрацияФНС));
			Если ОбъектСтроки.Количество() > 0 Тогда
				Проверка.Объект = ОбъектСтроки[0].Объект;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ИнициализироватьРезультатПроверки(ГрупповаяПроверка)
	
	Для Каждого Проверка Из ГрупповаяПроверка Цикл
		Проверка.РезультатПроверки = ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйРезультатПроверки();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
