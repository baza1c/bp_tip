////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиSberAPIСлужебныйКлиент: обмен со Сбербанком через сервис SberAPI.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область АутентификациюПоЛогину

// Создает структуру параметров для аутентификации по логину
//  Подробнее см. ВыполнитьАутентификациюПоЛогину()
//
Функция ПараметрыАутентификацииПоЛогину() Экспорт
	
	ПараметрыАутентификацииПоЛогину = Новый Структура();
	ПараметрыАутентификацииПоЛогину.Вставить("Оповещение", Неопределено);
	ПараметрыАутентификацииПоЛогину.Вставить("ИмяВнешнегоМодуля", "");
	ПараметрыАутентификацииПоЛогину.Вставить("НастройкаОбмена", Неопределено);
	ПараметрыАутентификацииПоЛогину.Вставить("ЛогинПароль", Неопределено);
	ПараметрыАутентификацииПоЛогину.Вставить("ТекстПояснения", "");
	ПараметрыАутентификацииПоЛогину.Вставить("Организация", Неопределено);
	ПараметрыАутентификацииПоЛогину.Вставить("Банк", Неопределено);
	ПараметрыАутентификацииПоЛогину.Вставить("НомерСчета", "");
	ПараметрыАутентификацииПоЛогину.Вставить("ЭтоТест", Ложь);
	ПараметрыАутентификацииПоЛогину.Вставить("НовыйИдентификаторОрганизации", "");
	
	Возврат ПараметрыАутентификацииПоЛогину;
	
КонецФункции

// Производит процесс аутентификации на сервере банка по логину и паролю.
//
// Параметры:
//  ПараметрыАутентификацииПоЛогину - Структура - содержит поля:
//   * Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения метода
//      * Результат - Структура - результат аутентификации, содержит поля:
//          * Успех - Булево - результат аутентификации
//          * ТребуетсяТокен - Булево - возвращает Истина, если Успех = Ложь и требуется токен.
//      * ДополнительныеПараметры - Структура - контекст выполнения метода
//   * ИмяВнешнегоМодуля - Строка - название внешней компоненты банка
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками, Неопределено - текущая настройка обмена с банком
//   * ЛогинПароль - Структура - логин и пароль для аутентификации, содержит поля:
//      * Логин - Строка - имя пользователя
//      * Пароль - Строка - пароль пользователя.
//   * ТекстПояснения - Строка - текст пояснения перед установкой внешней компоненты.
//   * Организация - ОпределяемыйТип.Организация, Неопределено - ссылка на организацию
//   * Банк - ОпределяемыйТип.БанкОбменСБанками, Неопределено - ссылка на банк.
//   * НомерСчета - Строка, Неопределено - номер счета к которому требуется получение доступа
//
Процедура ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину) Экспорт
	
	СтруктураВозврата = НовыйРезультатАутентификации();
	ДополнительныеПараметры = ОбщегоНазначенияКлиентСервер.СкопироватьРекурсивно(ПараметрыАутентификацииПоЛогину);
	ДополнительныеПараметры.Вставить("ОповещениеПослеБазовойАутентификации", ПараметрыАутентификацииПоЛогину.Оповещение);
	ДополнительныеПараметры.Вставить("СтруктураВозврата", СтруктураВозврата);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПолучитьФродПараметрыПослеПодключенияВКПоЛогину", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(
		ОповещениеПослеПодключенияВК,
		ПараметрыАутентификацииПоЛогину.ИмяВнешнегоМодуля,
		ПараметрыАутентификацииПоЛогину.ТекстПояснения);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКешем

// Выполняет кеширование данных авторизации на сервере сбербанка.
//
// Параметры:
//  Название - Строка - название кэшируемого параметра;
//  Значение - Произвольный - значение кэшируемого параметра.
//
Процедура ЗакешироватьПараметрСбербанка(Название, Значение) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
		
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
		
	КонецЕсли;
	
	ПараметрыОбменаСбербанк = ПараметрыПодсистемыОбменСБанками.Получить("Сбербанк");
	
	Если ПараметрыОбменаСбербанк = Неопределено Тогда
		ПараметрыОбменаСбербанк = Новый Структура;
	КонецЕсли;
	
	ПараметрыОбменаСбербанк.Вставить(Название, Значение);
	ПараметрыПодсистемыОбменСБанками.Вставить("Сбербанк", ПараметрыОбменаСбербанк);
	
КонецПроцедуры

// Выполняет очистку закэшированных параметров обмена со Сбербанком
//
Процедура ОчиститьДанныеАвторизацииСбербанк() Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие") Тогда
		ПараметрыПодсистемыОбменСБанками.Удалить("Сбербанк");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает закешированный параметр обмена со сбербанком.
//
// Параметры:
//  НазваниеПараметра  - Строка - название параметра.
//
// Возвращаемое значение:
// Произвольный - значение параметра.
//
Функция ЗначениеИзКешаСбербанк(НазваниеПараметра) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие") Тогда
		
		ПараметрыОбменаСбербанк = ПараметрыПодсистемыОбменСБанками.Получить("Сбербанк");
		Если ПараметрыОбменаСбербанк <> Неопределено И ПараметрыОбменаСбербанк.Свойство(НазваниеПараметра) Тогда
			Возврат ПараметрыОбменаСбербанк[НазваниеПараметра];
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбменЧерезТокен

Функция ПараметрыАутентификацииНаТокене() Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить("ОповещениеОВыполнении");
	Параметры.Вставить("ИмяВнешнегоМодуля");
	Параметры.Вставить("Организация");
	Параметры.Вставить("Банк");
	Параметры.Вставить("НастройкаОбмена");
	Параметры.Вставить("ПринудительнаяАутентификация", Ложь);
	Параметры.Вставить("ЭтоТест", Ложь);
	Параметры.Вставить("ЭтоПолучениеНастроек", Ложь);
	Параметры.Вставить("ИгнорироватьСохраненныеНастройки", Ложь);
	Параметры.Вставить("НовыйИдентификаторОрганизации", "");
	
	Возврат Параметры;
	
КонецФункции

// Производит процесс полной аутентификации на токене SberAPI, т.е. начиная от запроса пин-кода и заканчивая получением токена доступа
//
// Параметры:
//    ОповещениеОВыполнении - ОписаниеОповещения - оповещение после выполнения аутентификации на токене:
//          Результат - Булево - Истина, если аутентификация выполнена успешно, иначе Ложь;
//          ДополнительныеПараметры - Структура - параметры описания оповещения;
//    ИдентификаторВК - Строка - идентификатор внешней компоненты;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//    ПринудительнаяАутентификация - Булево - если Истина, то аутентификация будет производится даже если она была выполнена ранее;
//    ЭтоТест - Булево - если Истина, то будет недоступно сохранение пароля.
//    ЭтоПолучениеНастроек - Булево - признак получения настроек обмена с банком.
//
Процедура АутентифицироватьсяНаТокене(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Параметры);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолнойАутентификацииНаТокене", Параметры.ОповещениеОВыполнении);
	ОповещениеПослеАутентификацииНаТокене = Новый ОписаниеОповещения("УстановитьКаналПослеАутентификацииНаТокене",
		ЭтотОбъект, ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеПослеАутентификацииНаТокене);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ТихийСтартПослеПодключенияВКСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, Параметры.ИмяВнешнегоМодуля);
	
КонецПроцедуры

Процедура УстановитьКаналПослеАутентификацииНаТокене(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
		
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("АутентификацияНаСервереПослеУстановкиКанала",
		ЭтотОбъект, ДополнительныеПараметры);
	УстановитьКаналНаТокенеСбербанк(Оповещение, ДополнительныеПараметры.ИмяВнешнегоМодуля, , ДополнительныеПараметры);
	
КонецПроцедуры

Процедура АутентификацияНаСервереПослеУстановкиКанала(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ВыбратьСертификатПослеПолученияСТокена", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьДанныеСертификатовСТокена(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ВыбратьСертификатПослеПолученияСТокена(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
		
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	ДополнительныеПараметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
	
	СписокСертификатов = Новый СписокЗначений;
	Если СоответствиеСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
			ДополнительныеПараметры.Вставить("ИдентификаторСертификата", КлючЗначение.Ключ);
			Оповещение = Новый ОписаниеОповещения(
				"ПослеПолученияДвоичныхДанныхСертификата", ЭтотОбъект, ДополнительныеПараметры);
			ПолучитьДвоичныеДанныеСертификата(
				Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, КлючЗначение.Ключ, ДополнительныеПараметры);
			
			Возврат;
		КонецЦикла
	ИначеЕсли Не СоответствиеСертификатов.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствуют сертификаты на банковском ключе.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
		
		Возврат;
	КонецЕсли;
	
	ВыборкаСертификатов = Новый Соответствие;
	
	Для Каждого Элемент Из СоответствиеСертификатов Цикл
		
		ИдентификаторСертификата = Элемент.Ключ;
		ДвоичныеДанныеСертификата = Элемент.Значение;
		
		Если ДвоичныеДанныеСертификата = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(ДвоичныеДанныеСертификата);
		
		Если СтруктураСертификата.ДействителенДо < ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураСертификата) Тогда
			ШаблонПредставления = НСтр("ru = '%1, до %2'");
			ДатаСтрокой = Формат(СтруктураСертификата.ДействителенДО, "ДФ=MM.yyyy");
			Представление = СтрШаблон(ШаблонПредставления, СтруктураСертификата.КомуВыдан, ДатаСтрокой);
			ВыборкаСертификатов.Вставить(ИдентификаторСертификата, Представление);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВыборкаСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из ВыборкаСертификатов Цикл
			ДополнительныеПараметры.Вставить("ИдентификаторСертификата", КлючЗначение.Ключ);
			Оповещение = Новый ОписаниеОповещения(
				"ПослеПолученияДвоичныхДанныхСертификата", ЭтотОбъект, ДополнительныеПараметры);
			ПолучитьДвоичныеДанныеСертификата(
				Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, КлючЗначение.Ключ, ДополнительныеПараметры);
			Возврат;
		КонецЦикла
	КонецЕсли;
	
	Для Каждого Элемент Из ВыборкаСертификатов Цикл
		СписокСертификатов.Добавить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	ОповещениеПослеВыбораСертификата = Новый ОписаниеОповещения(
		"ОбработкаВыбораСертификата", ЭтотОбъект, ДополнительныеПараметры);
	
	ЗаголовокФормыВыбора = НСтр("ru = 'Выберите сертификат'");
	
	СписокСертификатов.ПоказатьВыборЭлемента(ОповещениеПослеВыбораСертификата, ЗаголовокФормыВыбора);
	
КонецПроцедуры

Процедура ОбработкаВыбораСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ИдентификаторСертификата", Результат.Значение);
	Оповещение = Новый ОписаниеОповещения(
		"ПослеПолученияДвоичныхДанныхСертификата", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьДвоичныеДанныеСертификата(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.Значение, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПослеПолученияДвоичныхДанныхСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	ДанныеСертификата = Новый Структура;
	ДанныеСертификата.Вставить("ИдентификаторСертификата", ДополнительныеПараметры.ИдентификаторСертификата);
	ДанныеСертификата.Вставить("ДвоичныеДанныеСертификата", Результат);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеАутентификацииПоСертификату",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ЭтоПолучениеНастроек = Ложь;
	ДополнительныеПараметры.Свойство("ЭтоПолучениеНастроек", ЭтоПолучениеНастроек);
		
	АутентификацияПоСертификату(
		Оповещение, ДополнительныеПараметры.ИмяВнешнегоМодуля, ДанныеСертификата,
		ДополнительныеПараметры.НастройкаОбмена, ЭтоПолучениеНастроек, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПослеАутентификацииПоСертификату(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
		
		ВидОперации = НСтр("ru = 'Авторизация в банке по сертификату'");
		ТекстСообщения = НСтр("ru = 'Неуспешная попытка авторизации в банке по сертификату'");
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстСообщения, ТекстСообщения);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
		
		Возврат;
		
	КонецЕсли;
	
	НастройкаОбмена = Неопределено;
	Если ДополнительныеПараметры.Свойство("НастройкаОбмена", НастройкаОбмена) Тогда
		
		ПараметрыСессииФиксСтруткура = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыУстановленнойСессииСбербанк(
			НастройкаОбмена);
		ПараметрыСессии = Новый Структура(ПараметрыСессииФиксСтруткура);
		ПараметрыСессии.Вставить("ТокенДоступа", Результат.ТокенДоступа);
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(НастройкаОбмена, ПараметрыСессии);
		
	КонецЕсли;
	
	// Сохраним ТокенДоступа в СлужебныеДанныеАвторизации
	СлужебныеДанныеАвторизации = ОбменСБанкамиSberAPIСлужебныйВызовСервера.СлужебныеДанныеАвторизации(
		ДополнительныеПараметры.Организация);
	Если ЗначениеЗаполнено(СлужебныеДанныеАвторизации) Тогда
		
		СлужебныеДанныеАвторизации.ТокенДоступа = Результат.ТокенДоступа;
		ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьСлужебныеДанныеАвторизации(
			ДополнительныеПараметры.Организация,
			СлужебныеДанныеАвторизации.ПредыдущийКлиентскийСекретИзФайла,
			СлужебныеДанныеАвторизации.ПредыдущийКлиентскийСекретОбновленный,
			СлужебныеДанныеАвторизации.СертификатПодключения,
			СлужебныеДанныеАвторизации.СертификатПодключенияПароль,
			СлужебныеДанныеАвторизации.ИдентификаторОрганизации,
			СлужебныеДанныеАвторизации.ТокенДоступа);
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Истина);
	
КонецПроцедуры

Процедура ТихийСтартПослеПодключенияВКСбербанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
	
	Если Не ДополнительныеПараметры.ЭтоТест И Не ДополнительныеПараметры.ПринудительнаяАутентификация
		И ЗначениеИзКешаСбербанк("АутентификацияНаТокенеВыполнена") = Истина
		И ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена)
		И ЗначениеИзКешаСбербанк("ТекущаяНастройкаОбмена") = ДополнительныеПараметры.НастройкаОбмена Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ОпределитьНеобходимостьПовторнойАутентификацииНаТокенеПослеПолученияСпискаБизнесСистемСбербанк", ЭтотОбъект,
			ДополнительныеПараметры);
		ПолучитьБизнесСистемыНаТокенеСбербанк(Оповещение, Результат.ПодключаемыйМодуль);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьПараметрыТокенаПослеТихогоСтартаСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуТихогоСтартаСбербанк", ЭтотОбъект);
	Результат.ПодключаемыйМодуль.НачатьВызовТихийСтарт(Оповещение);
	
КонецПроцедуры

Процедура ПолучитьБизнесСистемыНаТокенеСбербанк(Оповещение, ПодключаемыйМодуль)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеПолученияСпискаБизнесСистем", Оповещение);
	ОповещениеПослеПолученияНомераБизнесСистемы = Новый ОписаниеОповещения(
		"ПослеПолученияСпискаБизнесСистемСТокенаСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПолученияСпискаБизнесСистемНаТокенеСбербанк", ЭтотОбъект);
	БизнесСистемы = "";
	ПодключаемыйМодуль.НачатьВызовПолучитьСписокБизнесСистемVPNKeyTLS(
		ОповещениеПослеПолученияНомераБизнесСистемы, БизнесСистемы);
	
КонецПроцедуры

Процедура ПослеПолученияСпискаБизнесСистемСТокенаСбербанк(РезультатВызова,
														  ПараметрыВызова,
														  ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении списка бизнес систем.'");
		ТекстОшибки = НСтр("ru = 'Внешняя компонента VpnKey-TLS при получении списка бизнес систем вернула код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Получение списка бизнес систем.'");
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	Иначе
		Результат = Новый Структура("Успех, БизнесСистемы", Истина, ПараметрыВызова.Получить(0));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаБизнесСистем, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаБизнесСистемНаТокенеСбербанк(ИнформацияОбОшибке,
																	  СтандартнаяОбработка,
																	  ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение списка бизнес систем на аппаратном устройстве.'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить список бизнес систем с аппаратного устройства'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаБизнесСистем, Результат);
	
КонецПроцедуры

Процедура ПолучитьПараметрыТокенаПослеТихогоСтартаСбербанк(РезультатВызова,
														   ПараметрыВызова,
														   ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка при запуске клиента аппаратного устройства.
									|Убедитесь, что устройство подключено к компьютеру.'");
		ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при запуске Start.exe вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Запуск клиента аппаратного устройства.'");
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗарегистрироватьсяНаТокенеПослеПолученияИнформацииОТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИнформациюОТокенеСбербанк(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьИнформациюОТокенеСбербанк(Оповещение, ПодключаемыйМодуль, ДополнительныеПараметры)
	
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ОповещениеПослеПолученияИнформацииОТокене = Новый ОписаниеОповещения("ПослеПолученияИнформацииОТокенеСбербанк",
		ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияИнформацииОТокенеСбербанк", ЭтотОбъект);
	
	ВерсияПрошивки = 0;
	ТипУстройства = "";
	
	Попытка
		ПодключаемыйМодуль.НачатьВызовПолучитьПараметрыVPNKeyTLS(
			ОповещениеПослеПолученияИнформацииОТокене, ВерсияПрошивки, ТипУстройства);
	Исключение
		Операция = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонСообщения = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка:
								|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки);
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
	КонецПопытки
	
КонецПроцедуры

Процедура ПослеПолученияИнформацииОТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
		ТекстОшибки = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка.
							|Внешний модуль VpnKey-TLS вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстОшибки);
	Иначе
		Результат = Новый Структура("Успех, ТипУстройства", Истина, ПараметрыВызова.Получить(1));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияИнформацииОТокенеСбербанк(ИнформацияОбОшибке,
															 СтандартнаяОбработка,
															 ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуТихогоСтартаСбербанк(ИнформацияОбОшибке,
											   СтандартнаяОбработка,
											   ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При запуске программы банка произошла ошибка:
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Запуск программы Start.exe'");
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
	
КонецПроцедуры

Процедура ЗарегистрироватьсяНаТокенеПослеПолученияИнформацииОТокенеСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		
		Возврат
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ПринудительнаяАутентификация") Тогда
		ПринудительнаяАутентификация = ДополнительныеПараметры.ПринудительнаяАутентификация;
	Иначе
		ПринудительнаяАутентификация = Ложь;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ТипУстройства", Результат.ТипУстройства);
	
	НастройкаОбмена = ДополнительныеПараметры.НастройкаОбмена;
	
	Если ПринудительнаяАутентификация И ЗначениеИзКешаСбербанк("ЗапомнитьСессию") = Истина Тогда
		ПринудительнаяАутентификация = Ложь;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЭтоТест Или ПринудительнаяАутентификация
		Или ЗначениеИзКешаСбербанк("ЗапомнитьСессию") <> Истина
		Или Не (ЗначениеЗаполнено(ЗначениеИзКешаСбербанк("АутентификацияНаТокенеВыполнена"))
				И ЗначениеИзКешаСбербанк("ТекущаяНастройкаОбмена") = НастройкаОбмена) Тогда
		
		Если Результат.ТипУстройства <> "SCR" Тогда // не экранный токен
			Обработчик = Новый ОписаниеОповещения(
				"ПослеВводаДанныхАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПинКодаСбербанк", , , , , , Обработчик);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НомерКонтейнера = Строка(ЗначениеИзКешаСбербанк("НомерКонтейнера"));
	ПинКод = ЗначениеИзКешаСбербанк("ПинКод");
	
	ЗарегистрироватьсяНаТокенеСбербанка(
		ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене,
		ДополнительныеПараметры,
		НомерКонтейнера,
		ПинКод,
		Результат.ТипУстройства);
	
КонецПроцедуры

Процедура ПослеВводаДанныхАутентификацииСбербанк(ДанныеАутентификации, ДополнительныеПараметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено Или ДанныеАутентификации = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		
		Возврат;
	КонецЕсли;
	
	НомерКонтейнера = ДанныеАутентификации.НомерКонтейнера;
	ПинКод = ДанныеАутентификации.ПинКод;
	
	ЗакешироватьПараметрСбербанка("ЗапомнитьСессию", ДанныеАутентификации.ЗапомнитьСессию);
	ЗакешироватьПараметрСбербанка("НомерКонтейнера", НомерКонтейнера);
	ЗакешироватьПараметрСбербанка("ПинКод", ПинКод);
	
	ЗарегистрироватьсяНаТокенеСбербанка(
		ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене,
		ДополнительныеПараметры,
		НомерКонтейнера,
		ПинКод,
		ДополнительныеПараметры.ТипУстройства);
	
КонецПроцедуры

// Производит аутентификацию на токене.
//
// Параметры:
//    ОповещениеПослеАутентификацииНаТокене - ОписаниеОповещения - оповещение после выполнения процедуры:
//       * Результат - Булево - если Истина, то аутентификация успешно выполнена, иначе Ложь;
//    ДополнительныеПараметры - Структура - дополнительные параметры;
//    НомерКонтейнера - Число - номер бизнес системы. Для экранного токена значение не заполнено;
//    ПинКод - Строка - секретный код. Для экранного токена значение не заполнено;
//    ТипУстройства - Строка - тип токена (UNKNOWN - Неизвестно, ORDINARY - Обычный, BTN - Сенсорный(с кнопкой), SCR - Экранный).
//
Процедура ЗарегистрироватьсяНаТокенеСбербанка(ОповещениеПослеАутентификацииНаТокене,
											  ДополнительныеПараметры,
											  НомерКонтейнера,
											  ПинКод,
											  ТипУстройства)
	
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеПослеАутентификацииНаТокене);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ДополнительныеПараметры.ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("НомерКонтейнера", НомерКонтейнера);
	ДополнительныеПараметры.Вставить("ПинКод", ПинКод);
	ДополнительныеПараметры.Вставить("ТипУстройства", ТипУстройства);
	
	// Временно отключено, т.к. на сенсорном токене приводит к падению.
	ПредъявитьПинПослеЗавершенияСессии(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПредъявитьПинПослеЗавершенияСессии(Результат, ДополнительныеПараметры)
	
	Оповещение = Новый ОписаниеОповещения("ПослеПредъявленияПинаНаТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПослеПредъявленияПинаНаТокенеСбербанк", ЭтотОбъект);
	Если ДополнительныеПараметры.ТипУстройства = "SCR" Тогда // Экранный токен
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПредъявитьПинЭкр(Оповещение);
	Иначе
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПредъявитьПин(
			Оповещение, ДополнительныеПараметры.НомерКонтейнера, ДополнительныеПараметры.ПинКод);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПослеПредъявленияПинаНаТокенеСбербанк(ИнформацияОбОшибке,
																СтандартнаяОбработка,
																ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При аутентификации на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Предъявление кода доступа на аппаратном устройстве'");
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
	
КонецПроцедуры

Процедура ПослеПредъявленияПинаНаТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ОбработатьРезультатАутентификацииНаТокенеСбербанка(ДополнительныеПараметры.НастройкаОбмена, РезультатВызова,
		ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене);
	
КонецПроцедуры

Процедура ОбработатьРезультатАутентификацииНаТокенеСбербанка(НастройкаОбмена,
															 РезультатАвторизации,
															 ОповещениеПослеАутентификацииНаТокене)
	
	ЕстьОшибка = Ложь;
	
	Если РезультатАвторизации <> 0 Тогда
		Если РезультатАвторизации = 28 Тогда
			ШаблонСообщения = НСтр("ru = 'PIN%1 заблокирован'");
			НомерКонтейнера = ЗначениеИзКешаСбербанк("НомерКонтейнера");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НомерКонтейнера);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ИначеЕсли РезультатАвторизации = 27 Тогда
			ШаблонСообщения = НСтр("ru = 'PUK%1 заблокирован'");
			ТекстСообщения = СтрШаблон(
								ШаблонСообщения, ЗначениеИзКешаСбербанк("НомерКонтейнера"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ИначеЕсли РезультатАвторизации = 25 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Пользователь заблокирован'"));
		ИначеЕсли РезультатАвторизации = 29 Или РезультатАвторизации = 30 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Неверные данные аутентификации на токене'"));
		Иначе
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Не удалось выполнить аутентификацию на аппаратном устройстве.
										|Необходимо выполнить его перезапуск'");
			ТекстОшибки =
				НСтр("ru = 'Внешний модуль VpnKey-TLS при авторизации на аппаратном устройстве вернул код ошибки: %1'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатАвторизации);
			Операция = НСтр("ru = 'Авторизация на токене'");
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, НастройкаОбмена);
		КонецЕсли;
		
		ЕстьОшибка = Истина;
		ОчиститьДанныеАвторизацииСбербанк();
	КонецЕсли;
	
	ЗакешироватьПараметрСбербанка("АутентификацияНаТокенеВыполнена", Не ЕстьОшибка);
	ЗакешироватьПараметрСбербанка("ТекущаяНастройкаОбмена", НастройкаОбмена);
	
	ВыполнитьОбработкуОповещения(ОповещениеПослеАутентификацииНаТокене, Не ЕстьОшибка);
	
КонецПроцедуры

// Открывает канал на токене Сбербанка. Аутентификация на токене уже должна быть выполнена.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//               * РезультатВызова - Булево - Если Истина, то канал создан, иначе Ложь;
//               * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ИдентификаторВК - Строка - идентификатор внешней компоненты.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена.
//
Процедура УстановитьКаналНаТокенеСбербанк(Оповещение,
										  ИдентификаторВК,
										  НастройкаОбмена = Неопределено,
										  ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиВиртуальногоКанала", Оповещение);
	Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
		ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	КонецЕсли;
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"УстановитьКаналПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

Процедура УстановитьКаналПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
		
		Оповещение = Новый ОписаниеОповещения(
			"УстановитьКаналПослеОпределенияНомераБизнесСистемыСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
		НазваниеБизнесСистемы = ОбменСБанкамиСлужебныйВызовСервера.НазваниеБизнесСистемыСбербанк();
		ОпределитьНомерБизнесСистемыСбербанк(Оповещение, Результат.ПодключаемыйМодуль, НазваниеБизнесСистемы);
	Иначе
		Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКаналПослеОпределенияНомераБизнесСистемыСбербанк(НомерБизнесСистемы, Параметры) Экспорт
	
	Если НомерБизнесСистемы = Неопределено Тогда //не удалось определить бизнес систему
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеУстановкиКаналаСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуУстановкиКаналаНаТокенеСбербанк", ЭтотОбъект);
	
	Параметры.ПодключаемыйМодуль.НачатьВызовУстановитьTLSКаналСБизнесСистемой(Оповещение, НомерБизнесСистемы);
	
КонецПроцедуры

Процедура ПослеУстановкиКаналаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Установка виртуального канала'");
		ТекстСообщения = НСтр("ru = 'Не удалось установить связь с сервером.
									|Необходимо проверить работу VpnKeyTLS.'");
		ТекстОшибки = НСтр("ru = 'Компонента AddIn.VpnKeyTLS при установке виртуального канала вернула код ошибки'")
							+ " " + РезультатВызова;
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ОчиститьДанныеАвторизацииСбербанк();
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, РезультатВызова = 0);
	
КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиКаналаНаТокенеСбербанк(ИнформацияОбОшибке,
														  СтандартнаяОбработка,
														  ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При установке канала на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Установка канала на аппаратном устройстве.'");
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
	
КонецПроцедуры

// Определяет номер бизнес системы на банковском токене
// 
// Параметры:
//    Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//        * НомерБизнесСистемы - Строка, Неопределено - номер бизнес системы на токене, или Неопределено в случае ошибки
//        * ДополнительныеПараметры - Произвольный - контекст выполнения
//    ПодключаемыйМодуль - COMОбъект - внешняя компонента банка
//    НазваниеБизнесСистемы - Строка - название бизнес-системы.
//
Процедура ОпределитьНомерБизнесСистемыСбербанк(Оповещение, ПодключаемыйМодуль, НазваниеБизнесСистемы)
	
	Параметры = Новый Структура();
	Параметры.Вставить("ОповещенияПослеОпределенияНомераБизнесСистемы", Оповещение);
	Параметры.Вставить("НазваниеБизнесСистемы", НазваниеБизнесСистемы);
	ОповещениеПослеПолученияНомераБизнесСистемы = Новый ОписаниеОповещения(
		"НайтиБизнесСистемуПослеПолученияСпискаСТокенаСбербанк", ЭтотОбъект, Параметры);
	
	ПолучитьБизнесСистемыНаТокенеСбербанк(ОповещениеПослеПолученияНомераБизнесСистемы, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ОпределитьНеобходимостьПовторнойАутентификацииНаТокенеПослеПолученияСпискаБизнесСистемСбербанк(Результат,
																										 ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		АутентифицироватьсяНаТокенеСбербанка(
			ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене,
			РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля,
			ДополнительныеПараметры.НастройкаОбмена,
			Истина);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиБизнесСистемуПослеПолученияСпискаСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, Неопределено);
		
		Возврат;
	КонецЕсли;
	
	БизнесСистемы = Результат.БизнесСистемы;
	НазваниеБизнесСистемы = ДополнительныеПараметры.НазваниеБизнесСистемы;
	
	НомерБизнесСистемы = Неопределено;
	
	// Делаем две попытки прочитать список бизнес-систем: сначала в стандартной кодировке UTF-8, потом в UTF-16LE.
	Для НомерШага = 1 По 3 Цикл
		
		БизнесСистемыДляПоиска = БизнесСистемы;
		Если НомерШага = 2 Тогда
			// Получаем двоичное представление текущей строки с бизнес-системам из стандартной кодировки UTF-8,
			// которая используется платформой для всех строковых переменных в коде, т.е. "как есть".
			ДвоичныеДанныеБизнесСистемы = ПолучитьДвоичныеДанныеИзСтроки(БизнесСистемыДляПоиска);
			
			// Преобразуем полученное двоичное представление в новую строку, 
			// указывая, что исходно данные были в UTF-16LE.
			БизнесСистемыДляПоиска = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеБизнесСистемы, "UTF-16LE");
		КонецЕсли;
		
		Если НомерШага = 3 Тогда
			// Преобразуем полученное представление в ДвоичныеДанные,
			//  указывая, что исходно данные могли быть в кодировке windows-1251.
			ДвоичныеДанныеБизнесСистемы = ПолучитьДвоичныеДанныеИзСтроки(БизнесСистемыДляПоиска, "windows-1251");
			БизнесСистемыДляПоиска = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеБизнесСистемы);
		КонецЕсли;
		
		МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(БизнесСистемыДляПоиска, Символы.ПС);
		Для Каждого Строка Из МассивСтрок Цикл
			Если СтрНайти(ВРег(Строка), ВРег(НазваниеБизнесСистемы)) > 0 Тогда
				ПозПервойКавычки = СтрНайти(Строка, """");
				БизнесСистемаСтрокой = Сред(Строка, ПозПервойКавычки + 1);
				ПозВторойКавычки = СтрНайти(БизнесСистемаСтрокой, """");
				БизнесСистемаСтрокой = Сред(БизнесСистемаСтрокой, 1 , ПозВторойКавычки - 1);
				НомерБизнесСистемы = ОбменСБанкамиКлиентСервер.ЧислоИзСтроки(БизнесСистемаСтрокой);
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НомерБизнесСистемы <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НомерБизнесСистемы = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не найдена бизнес система на банковском ключе.
									|Необходимо проверить работу TLS VPN Key.'");
		Операция = НСтр("ru = 'Поиск бизнес системы на электронном ключе.'");
		ШаблонОшибки   = НСтр("ru = 'На электронном ключе не найдена бизнес-система %1.
									|Содержимое списка возврата: %2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, НазваниеБизнесСистемы, БизнесСистемы);
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, Неопределено);
		
		Возврат;
	КонецЕсли;
	
	БизнесСистемаСтрокой = Строка(НомерБизнесСистемы);
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, БизнесСистемаСтрокой);
	
КонецПроцедуры

// Получает идентификаторы сертификатов с токена Сбербанка.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//   которая будет вызвана после завершения вызова метода со следующими параметрами:
//      * Результат - Структура - результат выполнения метода:
//           ** Успех - Булево - признак успешности выполнения процедуры;
//           ** ТекстОшибки - Строка - содержит текст ошибки, если Успех = Ложь;
//           ** СоответствиеСертификатов - Соответствие - содержит идентификаторы сертификатов с токена:
//                 *** Ключ - Строка - идентификатор сертификата токена;
//                 *** Значение - ДвоичныеДанные - двоичные данные сертификата;
//  ПодключаемыйМодуль - COMОбъект - внешняя компонента банка.
//
Процедура ПолучитьДанныеСертификатовСТокена(Оповещение, ПодключаемыйМодуль, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСпискаСертификатов", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПолученияСпискаИдентификаторов = Новый ОписаниеОповещения(
		"ПослеПолученияСпискаСертификатовСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияСпискаИдентификаторовСертификатов", ЭтотОбъект);
		
	ИдентификаторыСертификатов = "";
	
	ПодключаемыйМодуль.НачатьВызовПолучитьСписокИдентСертификатовVPNKeyTLS(
		ОповещениеПослеПолученияСпискаИдентификаторов, "0", ИдентификаторыСертификатов);
	
КонецПроцедуры

Процедура ПослеПолученияСпискаСертификатовСТокенаСбербанк(РезультатВызова,
														  ПараметрыВызова,
														  ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ШаблонОшибки = НСтр("ru = 'При получении списка сертификатов на банковском ключе произошла ошибка.
			|Внешний модуль Сбербанка при получении списка доступных сертификатов вернул код ошибки: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Подписание электронного документа.'");
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
		
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСертификатов = ПараметрыВызова[1];
	
	СоответствиеСертификатов = Новый Соответствие;
	КоличествоСтрок = СтрЧислоСтрок(ИдентификаторыСертификатов);
	Индекс = 2;
	Пока Индекс < КоличествоСтрок Цикл
		Текст = СтрПолучитьСтроку(ИдентификаторыСертификатов, Индекс);
		Текст = СтрЗаменить(Текст, ",", "");
		Текст = СтрЗаменить(Текст, ";", "");
		СоответствиеСертификатов.Вставить(Текст);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Если СоответствиеСертификатов.Количество() Тогда
		ДополнительныеПараметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
		КопияСоответствия = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(СоответствиеСертификатов);
		ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(КопияСоответствия, ДополнительныеПараметры);
		Возврат;
	Иначе
		ТекстСообщения = НСтр("ru = 'Не найден ни один сертификат на аппаратном устройстве.'");
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаИдентификаторовСертификатов(ИнформацияОбОшибке,
																	 СтандартнаяОбработка,
																	 ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение списка сертификатов с аппаратного устройства.'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить информацию о сертификатах с аппаратного устройства.'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
	
КонецПроцедуры

Процедура ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(СоответствиеСертификатов, ДополнительныеПараметры)
	
	Если СоответствиеСертификатов.Количество() = 0 Тогда
		Результат = Новый Структура();
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("СоответствиеСертификатов", ДополнительныеПараметры.СоответствиеСертификатов);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
		
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
		ИдентификаторСертификата = КлючЗначение.Ключ;
		Прервать;
	КонецЦикла;
	
	СоответствиеСертификатов.Удалить(ИдентификаторСертификата);
	
	Параметры = Новый Структура;
	Параметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
	Параметры.Вставить("Контекст", ДополнительныеПараметры);
	Параметры.Вставить("ИдентификаторСертификата", ИдентификаторСертификата);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДвоичныхДанныхСертификатаСбербанк", ЭтотОбъект, Параметры);
	
	ПолучитьДвоичныеДанныеСертификата(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторСертификата, ДополнительныеПараметры);
	
КонецПроцедуры

// Получает двоичные данные сертификата подписи с токена.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами:
//    Результат - ДвоичныеДанные - данные сертификата;
//              - Неопределено - произошла ошибка.
//    ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта Оповещение.
//  ПодключаемыйМодуль - ComОбъект - внешняя компонента сбербанка;
//  ИдентификаторСертификата  - Строка - уникальный идентификатор сертификата на токене.
//
Процедура ПолучитьДвоичныеДанныеСертификата(Оповещение, ПодключаемыйМодуль, ИдентификаторСертификата, ДополнительныеПараметры) Экспорт
	
	СертификатBase64 = "";
	
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхСертификата", Оповещение);
	
	ОповещениеПослеПолученияДанныхСертификата = Новый ОписаниеОповещения("ПослеПолученияДанныхСертификатаСТокенаСбербанк",
		ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПослеПолученияДанныхСертификатаСТокенаСбербанк", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовПолучитьСертификатVPNKeyTLS(
		ОповещениеПослеПолученияДанныхСертификата, ИдентификаторСертификата, СертификатBase64);
	
КонецПроцедуры

Процедура ПослеПолученияДвоичныхДанныхСертификатаСбербанк(ДвоичныеДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДвоичныеДанныеСертификата =  Неопределено Тогда
		ДополнительныеПараметры.Контекст.СоответствиеСертификатов.Удалить(ДополнительныеПараметры.ИдентификаторСертификата);
	Иначе
		ДополнительныеПараметры.Контекст.СоответствиеСертификатов.Вставить(
			ДополнительныеПараметры.ИдентификаторСертификата, ДвоичныеДанныеСертификата);
	КонецЕсли;
	
	ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(
		ДополнительныеПараметры.СоответствиеСертификатов, ДополнительныеПараметры.Контекст);
	
КонецПроцедуры

// Производит аутентификацию на сервере банк по сертификату
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение после выполнения процедуры
//    * Результат - Булево - если Истина, то аутентификация выполнена успешно, иначе Ложь.
//  ИдентификаторВК - Строка - идентификатор внешней компоненты.
//  ДанныеСертификата - Структура - данные сертификата, содержит поля:
//     * ИдентификаторСертификата - Строка - идентификатор сертификата на токене
//     * ДвоичныеДанныеСертификата - ДвоичныеДанные - двоичные данные сертификата.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - используется для журналирования.
//  ЭтоПолучениеНастроек - Булево - аутентификации производится для получения настроек обмена.
//
Процедура АутентификацияПоСертификату(Оповещение,
									  ИдентификаторВК,
									  ДанныеСертификата,
									  НастройкаОбмена,
									  ЭтоПолучениеНастроек = Ложь,
									  ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("ИдентификаторВК", ИдентификаторВК);
	ДополнительныеПараметры.Вставить("ДанныеСертификата", ДанныеСертификата);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеОпределенияСертификата = Новый ОписаниеОповещения(
		"ПослеПолученияДанныхСертификатаСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ОпределитьСертификатПодписиСбербанк(
		ОповещениеПослеОпределенияСертификата, ИдентификаторВК, НастройкаОбмена, , ЭтоПолучениеНастроек);
	
КонецПроцедуры

// Осуществляет подбор сертификата подписи на основе установленного соединения.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//       * Результат - Структура, Неопределено, КодВозвратаДиалога - возвращаемое значение
//           - Структура - данные сертификата:
//                ** ИдентификаторСертификата - Строка - идентификатор сертификата на токене;
//                ** СертификатСсылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - найденный сертификат;
//                ** ДвоичныеДанныеСертификата - ДвоичныеДанные - двоичные данные сертификата.
//           - Неопределено - произошла ошибка;
//           - КодВозвратаДиалога - если КодВозвратаДиалога.Отмена, то пользователь отказался от выбора сертификата;
//       * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения;
//    ИдентификаторВК - Строка - идентификатор внешней компоненты банка;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//    ОграничениеПоСертификатам - Массив - сертификаты, которые доступны для использования в текущей операции.
//    ЭтоПолучениеНастроек - Булево - при получении настроек не нужно проверять наличие сертификата в существующей настройке обмена.
//
Процедура ОпределитьСертификатПодписиСбербанк(Оповещение,
											  ИдентификаторВК,
											  НастройкаОбмена,
											  ОграничениеПоСертификатам = Неопределено,
											  ЭтоПолучениеНастроек = Ложь) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОпределенияСертификата", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ЭтоПолучениеНастроек", ЭтоПолучениеНастроек);
	Если ОграничениеПоСертификатам <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОграничениеПоСертификатам", ОграничениеПоСертификатам);
	КонецЕсли;
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ОпределитьСертификатПодписиПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

Процедура ОпределитьСертификатПодписиПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		ОчиститьДанныеАвторизацииСбербанк();
		
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеПолученияСертификатов = Новый ОписаниеОповещения(
		"ПодобратьСертификатПослеПолученияСпискаСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеСертификатовСТокена(
		ОповещениеПослеПолученияСертификатов, Результат.ПодключаемыйМодуль, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПодобратьСертификатПослеПолученияСпискаСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		ОчиститьДанныеАвторизацииСбербанк();
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	
	СписокСертификатов = Новый СписокЗначений;
	
	Если ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
		ДоступныеСертификатыНастройкиОбмена = Новый Массив;
	ИначеЕсли ДополнительныеПараметры.Свойство("ОграничениеПоСертификатам") Тогда
		ДоступныеСертификатыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатов(
			ДополнительныеПараметры.ОграничениеПоСертификатам);
	Иначе
		ДоступныеСертификатыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовБанка(
			ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
	СоответствиеОтпечатокСертификат = Новый Соответствие;
	Для Каждого Элемент Из ДоступныеСертификатыНастройкиОбмена Цикл
		ПроверитьСрокДействияСертификатаБанка(
			Элемент.Сертификат, Элемент.ДействителенДО, Элемент.ПользовательОповещенОСрокеДействия);
		СоответствиеОтпечатокСертификат.Вставить(Элемент.Отпечаток, Элемент.Сертификат);
	КонецЦикла;
	
	ВыборкаСертификатов = Новый Соответствие;
	СоответствиеСертификатовСбербанка = Новый Соответствие;
	
	Для Каждого Элемент Из СоответствиеСертификатов Цикл
		
		ИдентификаторСертификата = Элемент.Ключ;
		ДвоичныеДанныеСертификата = Элемент.Значение;
		
		Если ДвоичныеДанныеСертификата = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(ДвоичныеДанныеСертификата);
		Исключение
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Ошибка чтения сертификата.'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Операция = НСтр("ru = 'Чтение данных сертификата.'");
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
				Операция, ТекстОшибки, ТекстСообщения, ДополнительныеПараметры.НастройкаОбмена);
			
			Продолжить;
		КонецПопытки;
		
		Если СтруктураСертификата.ДействителенДо < ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
			НайденныйСертификат = СтруктураСертификата.Представление;
		Иначе
			НайденныйСертификат = СоответствиеОтпечатокСертификат.Получить(СтруктураСертификата.Отпечаток);
			Если НайденныйСертификат = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("СертификатСсылка", НайденныйСертификат);
		СтруктураДанных.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
		СоответствиеСертификатовСбербанка.Вставить(ИдентификаторСертификата, СтруктураДанных);
		ВыборкаСертификатов.Вставить(ИдентификаторСертификата, Строка(НайденныйСертификат));
	КонецЦикла;
	
	Если ВыборкаСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из ВыборкаСертификатов Цикл
			СтруктураВозврата = Новый Структура();
			СтруктураВозврата.Вставить("ИдентификаторСертификата", КлючЗначение.Ключ);
			ДанныеСертификата = СоответствиеСертификатовСбербанка.Получить(КлючЗначение.Ключ);
			СтруктураВозврата.Вставить("ДвоичныеДанныеСертификата", ДанныеСертификата.ДвоичныеДанныеСертификата);
			Если Не ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
				СтруктураВозврата.Вставить("СертификатСсылка", ДанныеСертификата.СертификатСсылка);
			КонецЕсли;
			
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, СтруктураВозврата);
			
			Возврат;
		КонецЦикла;
	КонецЕсли;
	
	Если ВыборкаСертификатов.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат подписи.
			|Возможные причины:
			|- В настройке обмена через сервис 1С:ДиректБанк указан сертификат другой организации. Загрузите нужный сертификат с аппаратного устройства.
			|- Вставлено другое аппаратное устройство. Проверьте, какое устройство подключено к компьютеру.
			|- Указана неверная учетная запись. Выберите правильную учетную запись при входе на аппаратное устройство.
			|- Выбрана учетная запись, не соответствующая маршруту подписания документа. Проверьте настройки маршрутов подписания.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		ОчиститьДанныеАвторизацииСбербанк();
		
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из ВыборкаСертификатов Цикл
		СписокСертификатов.Добавить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	ЗаголовокФормыВыбора = НСтр("ru = 'Выберите сертификат подписи'");
	
	ДополнительныеПараметры.Вставить("СоответствиеСертификатовСбербанка", СоответствиеСертификатовСбербанка);
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораСертификата", ЭтотОбъект, ДополнительныеПараметры);
	
	СписокСертификатов.ПоказатьВыборЭлемента(Оповещение, ЗаголовокФормыВыбора);
	
КонецПроцедуры

Процедура ПослеВыбораСертификата(ВыбраннаяСтрока, ДополнительныеПараметры) Экспорт
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, КодВозвратаДиалога.Отмена);
	Иначе
		ДанныеСертификата = ДополнительныеПараметры.СоответствиеСертификатовСбербанка.Получить(ВыбраннаяСтрока.Значение);
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("ИдентификаторСертификата", ВыбраннаяСтрока.Значение);
		СтруктураВозврата.Вставить("ДвоичныеДанныеСертификата", ДанныеСертификата.ДвоичныеДанныеСертификата);
		Если Не ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
			
			СтруктураВозврата.Вставить("СертификатСсылка", ДанныеСертификата.СертификатСсылка);
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСрокДействияСертификатаБанка(Сертификат, ДействителенДо, ПользовательОповещенОСрокеДействия)
	
	Если ПользовательОповещенОСрокеДействия Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОбОкончанииСрокаДействия = ДобавитьМесяц(ОбщегоНазначенияКлиент.ДатаСеанса(), 1) > ДействителенДо;
	
	Если ОповеститьОбОкончанииСрокаДействия Тогда
		ПараметрыФормы = Новый Структура("Сертификат", Сертификат);
		ОткрытьФорму("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Форма.ОповещениеОбОкончанииСрокаДействия",
			ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатаСТокенаСбербанк(РезультатВызова,
														 ПараметрыВызова,
														 ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'При получении данных сертификата произошла ошибка.'");
		ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при получении сертификата вернул код ошибки'") + РезультатВызова;
		Операция = НСтр("ru = 'Получение сертификата криптографии.'");
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, Неопределено);
		
		Возврат;
	КонецЕсли;
	
	СертификатBase64 = ПараметрыВызова[1];
	
	СертификатBase64 = СтрЗаменить(СертификатBase64, "-----BEGIN CERTIFICATE-----" + Символы.ПС, "");
	СертификатBase64 = СтрЗаменить(СертификатBase64, "-----BEGIN CERTIFICATE-----" + Символы.ВК + Символы.ПС, "");
	СертификатBase64 = СтрЗаменить(СертификатBase64, Символы.ПС + "-----END CERTIFICATE-----", "");
	
	ДвоичныеДанныеСертификата = Base64Значение(СертификатBase64);
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, ДвоичныеДанныеСертификата);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПослеПолученияДанныхСертификатаСТокенаСбербанк(ИнформацияОбОшибке,
																		 СтандартнаяОбработка,
																		 ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении сертификата с аппаратного устройства произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение данных сертификата с аппаратного устройства.'");
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, Неопределено);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатаСбербанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ДанныеСертификата", Результат);
	
	// Проверим, получены ли уже ФродПараметры, если нет то получим их и потом вызовем ПреАутентификацмю
	ФродПараметры = Неопределено;
	Если ДополнительныеПараметры.Свойство("ФродПараметры", ФродПараметры) И ЗначениеЗаполнено(ФродПараметры) Тогда
		
		// ФродПараметры уже получены, вызовем ПреАутентификацмю
		ВыполнитьПреАутентификациюПоТокенуПослеПолученияФродПараметров(ФродПараметры, ДополнительныеПараметры);
		
	Иначе
		
		// ФродПараметры еще не получены, получим их и потом вызовем ПреАутентификацмю
		ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
			"ПолучитьФродПараметрыПослеПодключенияВКПоТокену", ЭтотОбъект, ДополнительныеПараметры);
		
		ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(
			ОповещениеПослеПодключенияВК, ДополнительныеПараметры.ИдентификаторВК);
		
	КонецЕсли;
	
КонецПроцедуры

#Область Подписание

Процедура НачатьПодписаниеЭДСбербанка(Знач ДанныеПодписи, Параметры) Экспорт
	
	МассивОбработанныхНастроекОбмена = Параметры.МассивОбработанныхНастроекОбмена;
	ТекущийИндекс = 0;
	
	Для Каждого Элемент Из ДанныеПодписи Цикл
		
		ТекущийИндекс = ТекущийИндекс + 1;
		Если ТекущийИндекс <= Параметры.ИндексТретьейИтерации Тогда
			Продолжить;
		Иначе
			Параметры.ИндексТретьейИтерации = ТекущийИндекс;
		КонецЕсли;
		
		Если Не МассивОбработанныхНастроекОбмена.Найти(Элемент.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МассивОбработанныхНастроекОбмена.Добавить(Элемент.Ключ);
		МассивСообщенийОбменаБанка = Элемент.Значение;
		Если МассивСообщенийОбменаБанка.Количество() > 0 Тогда
			Параметры.Вставить("ТекущиеДанныеПодписи", ДанныеПодписи);
			ОбработчикПослеПодписания = Новый ОписаниеОповещения("ПродолжитьПодписаниеЭДСбербанк", ЭтотОбъект, Параметры);
			ОграничениеПоСертификатам = Неопределено;
			Параметры.Свойство("МассивСертификатов", ОграничениеПоСертификатам);
			
			ПодписатьЭДСбербанк(
				ОбработчикПослеПодписания, Элемент.Ключ, МассивСообщенийОбменаБанка, ОграничениеПоСертификатам, Истина);
			
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ОбменСБанкамиСлужебныйКлиент.ПродолжитьПодписаниеБанковскихЭД(Параметры.РезультатВыбораСертификатаБанка, Параметры);
	
КонецПроцедуры

Процедура ПодписатьЭДСбербанк(Оповещение,
							  НастройкаОбмена,
							  МассивСообщенийОбмена,
							  ОграничениеПоСертификатам = Неопределено,
							  ПринудительнаяАутентификация = Ложь) Экспорт
	
	Параметры = Новый Структура;
	
	СообщенияКПодписи = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(МассивСообщенийОбмена);
	
	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("СообщенияОбменаСбербанк", СообщенияКПодписи);
	Параметры.Вставить("ОповещениеПослеПодписания", Оповещение);
	Параметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	Если ОграничениеПоСертификатам <> Неопределено Тогда
		Параметры.Вставить("ОграничениеПоСертификатам", ОграничениеПоСертификатам);
	КонецЕсли;
	
	ОповещениеПослеАутентификацииНаТокене = Новый ОписаниеОповещения(
		"ОпределитьСертификатПослеАутентификацииНаТокенеСбербанк", ЭтотОбъект, Параметры);
	
	АутентифицироватьсяНаТокенеСбербанка(
		ОповещениеПослеАутентификацииНаТокене,
		РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля,
		НастройкаОбмена,
		ПринудительнаяАутентификация);
	
КонецПроцедуры

Процедура ОпределитьСертификатПослеАутентификацииНаТокенеСбербанк(Успех, Параметры) Экспорт
	
	Если Не Успех Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Результат);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораСертификатаПодписатьЭДСбербанк", ЭтотОбъект, Параметры);
	
	ОграничениеПоСертификатам = Неопределено;
	Параметры.Свойство("ОграничениеПоСертификатам", ОграничениеПоСертификатам);
	ОпределитьСертификатПодписиСбербанк(
		ОписаниеОповещения, Параметры.ИмяВнешнегоМодуля, Параметры.НастройкаОбмена, ОграничениеПоСертификатам);
	
КонецПроцедуры

Процедура ПослеВыбораСертификатаПодписатьЭДСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Результат);
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ЗакешироватьПараметрСбербанка("СертификатПодписи", Результат.СертификатСсылка);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
	ПараметрыФормы.Вставить("ВидОперации", "ПодписаниеСбербанк");
	ПараметрыФормы.Вставить("МассивСообщенийОбмена", Параметры.СообщенияОбменаСбербанк);
	ПараметрыФормы.Вставить("ИдентификаторСертификатаСбербанк", Результат.ИдентификаторСертификата);
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияФормыПодписаниеСбербанк", ЭтотОбъект, Параметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

Процедура ПослеЗакрытияФормыПодписаниеСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Или Результат = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, Результат);
	
КонецПроцедуры

Процедура ПродолжитьПодписаниеЭДСбербанк(Результат, Параметры) Экспорт
	
	Если Результат.Успех Тогда
		Параметры.КоличествоПодписанных = Параметры.КоличествоПодписанных + Результат.Количество;
		НачатьПодписаниеЭДСбербанка(Параметры.ТекущиеДанныеПодписи, Параметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ТокенДоступа

Процедура ОтозватьТокенДоступа(НастройкаОбмена) Экспорт
	
	Успешно = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ОтозватьТокенДоступа(НастройкаОбмена);
	Если Успешно И ЗначениеИзКешаСбербанк("ЗапомнитьСессию") <> Истина Тогда
		ОчиститьДанныеАвторизацииСбербанк();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбменДанными

Процедура ПолучитьДокументы(Параметры) Экспорт
	
	Параметры.Вставить("ЕстьОшибка", Ложь);
	Параметры.Вставить("СчетаТребующиеАутентификацию", Новый Массив);
	Параметры.Вставить("КоличествоПолученных", 0);
	
	РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПолученияДокументов(
		Параметры.НастройкаОбмена);
	
	Если РезультатВыполнения.Свойство("ТребуетсяАутентификация") Тогда
		ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
			"ПолучитьДокументыПослеАутентификации", ЭтотОбъект, Параметры);
		
		РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля,ИспользуетсяКриптография");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		Параметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
		
		Если РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
			
			ПараметрыАутентификацииНаТокене = ПараметрыАутентификацииНаТокене();
			ПараметрыАутентификацииНаТокене.Вставить("ОповещениеОВыполнении", ОповещениеПослеАутентификации);
			ПараметрыАутентификацииНаТокене.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
			ПараметрыАутентификацииНаТокене.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
			
			АутентифицироватьсяНаТокене(ПараметрыАутентификацииНаТокене);
			
		Иначе
			
			ПараметрыАутентификацииПоЛогину = ПараметрыАутентификацииПоЛогину(); 
			ЗаполнитьЗначенияСвойств(
				ПараметрыАутентификацииПоЛогину,
				Параметры,
				"ИмяВнешнегоМодуля,НастройкаОбмена");
			ПараметрыАутентификацииПоЛогину.Вставить("Оповещение", ОповещениеПослеАутентификации);
			
			ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
			
		КонецЕсли;
		
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Параметры.ВыводитьОкноОжидания;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ОбменСБанкамиСлужебныйКлиент.УстановитьФормуВладельцаЧерезАктивноеОкно(ПараметрыОжидания);
		
		Оповещение = Новый ОписаниеОповещения("ПослеСинхронизации", ЭтотОбъект, Параметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, Оповещение, ПараметрыОжидания);
		ВыполнитьОбработкуОповещения(
			Параметры.ОповещениеЗапускаДлительнойОперации, РезультатВыполнения.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьПлатежныеДокументы(ДанныеДляОтправки, ДополнительныеПараметры) Экспорт
	
	Для Каждого КлючЗначение Из ДанныеДляОтправки Цикл
		
		НастройкаОбмена = КлючЗначение.Ключ;
		
		// Дополнительный флаг, означающий, что выявлена истекщая авторизация при попытке отправки документов
		ТребуетсяПовторнаяАутентификация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры,
			"ТребуетсяПовторнаяАутентификация",
			Ложь);
			
		Если Не ЗначениеЗаполнено(КлючЗначение.Значение.СчетаТребующиеАутентификации) Тогда
			
			ДанныеДляОтправки.Удалить(НастройкаОбмена);
			Продолжить;
			
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения(
			"ОтправитьДокументыПоСчетуПослеАутентификации", ЭтотОбъект, ДополнительныеПараметры);
		
		НомерСчета = КлючЗначение.Значение.СчетаТребующиеАутентификации.Получить(0);
		
		РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля, Организация, Банк, ИспользуетсяКриптография");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			НастройкаОбмена, РеквизитыНастройкиОбмена);
		
		ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
		ДополнительныеПараметры.Вставить("ДанныеДляОтправкиЧерезSberAPI", ДанныеДляОтправки);
		ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
		ДополнительныеПараметры.Вставить("ТекущаяНастройкаОбмена", НастройкаОбмена);
		
		Если Не РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
			
			ПараметрыАутентификацииПоЛогину = ПараметрыАутентификацииПоЛогину();
			ПараметрыАутентификацииПоЛогину.Вставить("Оповещение", Оповещение);
			ПараметрыАутентификацииПоЛогину.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
			ПараметрыАутентификацииПоЛогину.Вставить("НастройкаОбмена", НастройкаОбмена);
			
		КонецЕсли;
		
		Если РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
			
			ПараметрыСессии = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыУстановленнойСессииСбербанк(
				ДополнительныеПараметры.НастройкаОбмена);
			ТокенДоступа = Неопределено;
			Если ЗначениеЗаполнено(ПараметрыСессии)
				И ЗначениеЗаполнено(ПараметрыСессии.ТокенДоступа)
				И НЕ ТребуетсяПовторнаяАутентификация Тогда
				
				ВыполнитьОбработкуОповещения(Оповещение, Истина);
				
			Иначе
				
				Параметры = ПараметрыАутентификацииНаТокене();
				Параметры.Вставить("ОповещениеОВыполнении", Оповещение);
				Параметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
				Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
				
				АутентифицироватьсяНаТокене(Параметры);
				
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(НомерСчета) Тогда
			
			ЗаполнитьЗначенияСвойств(ПараметрыАутентификацииПоЛогину, РеквизитыНастройкиОбмена, "Организация,Банк");
			ПараметрыАутентификацииПоЛогину.Вставить("НомерСчета", НомерСчета);
			
			ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
			
		Иначе
			
			ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
			
		КонецЕсли;
		
		Возврат;
		
	КонецЦикла;
	
	ОбменСБанкамиСлужебныйКлиент.ОтправкаСообщенийОбмена(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОтправитьДокументыПоСчетуПослеАутентификации(Результат, ДополнительныеПараметры) Экспорт
	
	НастройкаОбмена = ДополнительныеПараметры.ТекущаяНастройкаОбмена;
	Если ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
		
		НастройкаОбмена = ДополнительныеПараметры.ТекущаяНастройкаОбмена;
		
		ДанныеДляОтправки = ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Получить(НастройкаОбмена);
		РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияОтправкиДокументовПоСчету(
			НастройкаОбмена, ДанныеДляОтправки);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПослеОтправкиДокументовПоСчету = Новый ОписаниеОповещения(
			"ПослеОтправкиДокументовПоСчету", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатВыполнения, ПослеОтправкиДокументовПоСчету, ПараметрыОжидания);
			
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("Успех")
		И Не Результат.Успех
		И Результат.Свойство("ТребуетсяТокен")
		И Результат.ТребуетсяТокен Тогда
		
		// На данный момент, нет возможности авторизоваться токенной учетной записью для ожидаемой авторизации
		// по логин/пароль. Поэтому исключим этот вариант из отправки и выведем сообщение
		ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Удалить(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ВидОперации = НСтр("ru = 'Попытка отправки документов через сервис SberAPI.'");
		ПредставлениеОшибки = НСтр("ru = 'Ожидается авторизация логин/пароль. Авторизация по токену недопустима.'");
		ОбменСБанкамиСлужебныйКлиент.ОбработатьОшибку(ВидОперации,
			ПредставлениеОшибки,
			ПредставлениеОшибки,
			ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОбменСБанкамиСлужебныйКлиент.ОтправкаСообщенийОбмена(ДополнительныеПараметры);
		
	ИначеЕсли ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
		
		НастройкаОбмена = ДополнительныеПараметры.ТекущаяНастройкаОбмена;
		
		ДанныеДляОтправки = ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Получить(НастройкаОбмена);
		РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияОтправкиДокументовПоСчету(
			НастройкаОбмена, ДанныеДляОтправки);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПослеОтправкиДокументовПоСчету = Новый ОписаниеОповещения(
			"ПослеОтправкиДокументовПоСчету", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатВыполнения, ПослеОтправкиДокументовПоСчету, ПараметрыОжидания);
		
	Иначе
		
		ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Удалить(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОбменСБанкамиСлужебныйКлиент.ОтправкаСообщенийОбмена(ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОтправкиДокументовПоСчету(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // отменено фоновое задание
		
		ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Удалить(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОтозватьТокенДоступа(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОтправитьПлатежныеДокументы(
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI, ДополнительныеПараметры);
		
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.ОтправленныеСообщенияОбмена) Тогда
			Оповестить("ОбновитьСостояниеОбменСБанками");
			ДополнительныеПараметры.КоличествоОтправленных = ДополнительныеПараметры.КоличествоОтправленных
				+ РезультатОперации.ОтправленныеСообщенияОбмена.Количество();
		КонецЕсли;
		
		// Дополнительный флаг, означающий, что выявлена истекшая авторизация при попытке отправки документов
		Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				РезультатОперации,
				"ТребуетсяПовторнаяАутентификация",
				Ложь) Тогда
			
				ДополнительныеПараметры.Вставить("ТребуетсяПовторнаяАутентификация", Истина);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РезультатОперации.НеотправленныеСообщенияОбмена) Тогда
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Вставить(
				ДополнительныеПараметры.ТекущаяНастройкаОбмена, РезультатОперации);
		Иначе
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Удалить(
				ДополнительныеПараметры.ТекущаяНастройкаОбмена);
			ОтозватьТокенДоступа(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
			Если РезультатОперации.ПоказыватьОкноПодтвержденияПлатежей Тогда
				ДополнительныеПараметры.ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК.Добавить(
					РезультатОперации.Банк);
				ДополнительныеПараметры.ПодтверждениеПлатежейВБанке.НастройкиОбмена.Добавить(
					ДополнительныеПараметры.ТекущаяНастройкаОбмена);
			КонецЕсли;
		КонецЕсли;
		
		ОтправитьПлатежныеДокументы(
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI, ДополнительныеПараметры);
			
	Иначе // Ошибка
			
		ВидОперации = НСтр("ru = 'Отправка документов через сервис SberAPI.'");
		ОбменСБанкамиСлужебныйКлиент.ОбработатьОшибку(
			ВидОперации,
			Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI.Удалить(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОтозватьТокенДоступа(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОтправитьПлатежныеДокументы(
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезSberAPI, ДополнительныеПараметры);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДокументыПослеАутентификации(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
		
		РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПолученияДокументов(
			ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.СчетаТребующиеАутентификацию);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ДополнительныеПараметры.ВыводитьОкноОжидания;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ОбменСБанкамиСлужебныйКлиент.УстановитьФормуВладельцаЧерезАктивноеОкно(ПараметрыОжидания);
		
		Оповещение = Новый ОписаниеОповещения("ПослеСинхронизации", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, Оповещение, ПараметрыОжидания);
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеЗапускаДлительнойОперации, РезультатВыполнения.ИдентификаторЗадания);
			
	Иначе
			
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеСинхронизации(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Синхронизация через сервис SberAPI'");
		ОбменСБанкамиСлужебныйКлиент.ОбработатьОшибку(ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки,
			РезультатЗадания.КраткоеПредставлениеОшибки, ДополнительныеПараметры.НастройкаОбмена);
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		ДополнительныеПараметры.КоличествоПолученных = ДополнительныеПараметры.КоличествоПолученных
													 + РезультатОперации.КоличествоПолученных;
		Если ЗначениеЗаполнено(РезультатОперации.СчетаТребующиеАутентификацию) Тогда
			ВыполнитьСинхронизациюПоСчетам(
				РезультатОперации.СчетаТребующиеАутентификацию, ДополнительныеПараметры);
		Иначе
			ОбменСБанкамиСлужебныйКлиент.ПоказатьРезультатСинхронизации(0, ДополнительныеПараметры.КоличествоПолученных);
			ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьСинхронизациюПоСчетам(МассивСчетов, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(МассивСчетов) Тогда
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеСинхронизации, ДополнительныеПараметры.ЕстьОшибка);
			
		Возврат;
			
	КонецЕсли;
		
	ДополнительныеПараметры.Вставить("СчетаТребующиеАутентификацию", МассивСчетов);
	ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
		"ПолучитьДокументыПослеАутентификацииПоСчету", ЭтотОбъект, ДополнительныеПараметры);
	
	РеквизитыНастройкиОбмена = Новый Структура("Организация, Банк, ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ПараметрыАутентификацииПоЛогину = ПараметрыАутентификацииПоЛогину();
	ЗаполнитьЗначенияСвойств(
		ПараметрыАутентификацииПоЛогину,
		ДополнительныеПараметры,
		"ИмяВнешнегоМодуля,НастройкаОбмена");
	ЗаполнитьЗначенияСвойств(
		ПараметрыАутентификацииПоЛогину,
		РеквизитыНастройкиОбмена,
		"Организация,Банк");
	ПараметрыАутентификацииПоЛогину.Вставить("Оповещение", ОповещениеПослеАутентификации);
	
	Если МассивСчетов.Количество() = 1 Тогда
		
		ПараметрыАутентификацииПоЛогину.Вставить("НомерСчета", МассивСчетов[0]);
		ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
			
	Иначе
			
		ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДокументыПослеАутентификацииПоСчету(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
		
		ПолучитьДокументыПослеАутентификации(Истина, ДополнительныеПараметры)
		
	Иначе
		
		ДополнительныеПараметры.ЕстьОшибка = Истина;
		ДополнительныеПараметры.СчетаТребующиеАутентификацию.Удалить(0);
		ВыполнитьСинхронизациюПоСчетам(
			ДополнительныеПараметры.СчетаТребующиеАутентификацию, ДополнительныеПараметры);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьВыписку(Оповещение, Параметры) Экспорт
	
	ПараметрыВыполнения = Новый Структура();
	ПараметрыВыполнения.Вставить("НастройкаОбмена", Неопределено);
	ПараметрыВыполнения.Вставить("НомерСчета", "");
	ПараметрыВыполнения.Вставить("ДатаНачала", Дата(1,1,1));
	ПараметрыВыполнения.Вставить("ДатаОкончания", Дата(1,1,1));
	
	ЗаполнитьЗначенияСвойств(ПараметрыВыполнения, Параметры);
	РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПолученияВыписки(ПараметрыВыполнения);
	
	Если РезультатВыполнения.Свойство("ТребуетсяАутентификация") Тогда
		
		Параметры.Вставить("ОповещениеПослеПолученияВыписки", Оповещение);
		Параметры.Вставить("ИмяВнешнегоМодуля", Параметры.РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
		ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
			"ПолучитьВыпискуПослеАутентификации", ЭтотОбъект, Параметры);
		
		ЗапрашиваемыеРеквизиты = Новый Структура("ИспользуетсяКриптография");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			Параметры.НастройкаОбмена, ЗапрашиваемыеРеквизиты);
		
		Если ЗапрашиваемыеРеквизиты.ИспользуетсяКриптография Тогда
			
			ПараметрыАутентификацииНаТокене = ПараметрыАутентификацииНаТокене();
			ПараметрыАутентификацииНаТокене.Вставить("ОповещениеОВыполнении", ОповещениеПослеАутентификации);
			ПараметрыАутентификацииНаТокене.Вставить("ИмяВнешнегоМодуля", Параметры.ИмяВнешнегоМодуля);
			ПараметрыАутентификацииНаТокене.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
			
			АутентифицироватьсяНаТокене(ПараметрыАутентификацииНаТокене);
			
		Иначе
			
			ПараметрыАутентификацииПоЛогину = ПараметрыАутентификацииПоЛогину(); 
			ЗаполнитьЗначенияСвойств(
				ПараметрыАутентификацииПоЛогину,
				Параметры,
				"ИмяВнешнегоМодуля,НастройкаОбмена,НомерСчета");
			ПараметрыАутентификацииПоЛогину.Вставить("Оповещение", ОповещениеПослеАутентификации);
			
			ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
			
		КонецЕсли;
		
	Иначе
		
		Если Параметры.Свойство("ФормаВладелец") И ТипЗнч(Параметры.ФормаВладелец) = Тип("ФормаКлиентскогоПриложения") Тогда
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Параметры.ФормаВладелец);
		Иначе
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		КонецЕсли;
		
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, Параметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, Оповещение, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьВыпискуПослеАутентификации(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
		
		ПараметрыВыполнения = Новый Структура("НастройкаОбмена, НомерСчета, ДатаНачала, ДатаОкончания");
		ЗаполнитьЗначенияСвойств(ПараметрыВыполнения, ДополнительныеПараметры);
		РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПолученияВыписки(
			ПараметрыВыполнения);
		
		Если ДополнительныеПараметры.Свойство("ФормаВладелец")
			И ТипЗнч(ДополнительныеПараметры.ФормаВладелец) = Тип("ФормаКлиентскогоПриложения") Тогда
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ДополнительныеПараметры.ФормаВладелец);
		Иначе
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		КонецЕсли;
		
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, Оповещение, ПараметрыОжидания);
		
	Иначе
		
		ВидОперации = НСтр("ru = 'Аутентификация через сервис SberAPI для получения выписки'");
		Ошибка = НСтр("ru = 'При аутентификации через сервис SberAPI для получения выписки произошла ошибка'");
		НастройкаОбмена = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "НастройкаОбмена", Неопределено);
		ОбменСБанкамиСлужебныйКлиент.ОбработатьОшибку(ВидОперации, Ошибка, Ошибка, НастройкаОбмена);
		СтруктураВозврата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияВыписки(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		СтруктураВозврата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение выписки через сервис SberAPI'");
		ОбменСБанкамиСлужебныйКлиент.ОбработатьОшибку(ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки,
			РезультатЗадания.КраткоеПредставлениеОшибки, ДополнительныеПараметры.НастройкаОбмена);
			СтруктураВозврата = Новый Структура("Успех", Ложь);
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если РезультатОперации.Свойство("ТребуетсяАутентификация") Тогда
			
			ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
				"ПолучитьВыпискуПослеАутентификации", ЭтотОбъект, ДополнительныеПараметры);
			РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля, ИспользуетсяКриптография");
			ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
				ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
			ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
			
			Если РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
				
				Параметры = ПараметрыАутентификацииНаТокене();
				Параметры.Вставить("ОповещениеОВыполнении", ОповещениеПослеАутентификации);
				Параметры.Вставить("ИмяВнешнегоМодуля", ДополнительныеПараметры.ИмяВнешнегоМодуля);
				Параметры.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
			
				АутентифицироватьсяНаТокене(Параметры);
				
			Иначе // Авторизация логин/пароль
				
				ПараметрыАутентификацииПоЛогину = ПараметрыАутентификацииПоЛогину();
				ЗаполнитьЗначенияСвойств(
					ПараметрыАутентификацииПоЛогину,
					ДополнительныеПараметры,
					"НастройкаОбмена,ИмяВнешнегоМодуля");
				ПараметрыАутентификацииПоЛогину.Вставить("Оповещение", ОповещениеПослеАутентификации);
				
				ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
				
			КонецЕсли;
			
		Иначе
			СтруктураВозврата = Новый Структура("Успех", Истина);
			СтруктураВозврата.Вставить("Выписки", РезультатОперации.Выписки);
			
			ИдентификаторНазначения = Неопределено;
			Если ДополнительныеПараметры.Свойство("ФормаВладелец")
				И ТипЗнч(ДополнительныеПараметры.ФормаВладелец) = Тип("ФормаКлиентскогоПриложения") Тогда
				ИдентификаторНазначения = ДополнительныеПараметры.ФормаВладелец.УникальныйИдентификатор;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(РезультатЗадания.Сообщения) Тогда
				ОбменСБанкамиСлужебныйКлиент.ВывестиСообщения(РезультатЗадания.Сообщения, ИдентификаторНазначения);
			КонецЕсли;
			
			ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
			
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ТестНастройки

Процедура ПровестиТестНастройки(Оповещение, НастройкаОбмена, ПараметрыОбмена, ВыводитьОкноОжидания) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеТеста", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ВыводитьОкноОжидания", ВыводитьОкноОжидания);
	
	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	
	Организация = Неопределено;
	Банк = Неопределено;
	ИспользуетсяКриптография = Ложь;
	ИмяВнешнегоМодуля = "";
	Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
		
		РеквизитыНастройкиОбмена = Новый Структура();
		РеквизитыНастройкиОбмена.Вставить("Организация");
		РеквизитыНастройкиОбмена.Вставить("Банк");
		РеквизитыНастройкиОбмена.Вставить("ИспользуетсяКриптография");
		РеквизитыНастройкиОбмена.Вставить("ИмяВнешнегоМодуля");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			НастройкаОбмена,
			РеквизитыНастройкиОбмена);
		Организация = РеквизитыНастройкиОбмена.Организация;
		Банк = РеквизитыНастройкиОбмена.Банк;
		ИспользуетсяКриптография = РеквизитыНастройкиОбмена.ИспользуетсяКриптография;
		ИмяВнешнегоМодуля = РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля;
		
	КонецЕсли;
	
	Результат = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПроверкиНаличияДоступаКДанным(
		НастройкаОбмена,
		Организация,
		Банк);
	
	ТребуетсяАутентификация = Ложь;
	Если Результат.Свойство("ТребуетсяАутентификация") Тогда
		ТребуетсяАутентификация = Результат.ТребуетсяАутентификация;
	ИначеЕсли Результат.Свойство("АдресРезультата") И ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОперации <> Неопределено
			И РезультатОперации.Свойство("ТребуетсяАутентификация") И РезультатОперации.ТребуетсяАутентификация Тогда
			ТребуетсяАутентификация = Истина;
			УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяАутентификация Тогда
		
		ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
			"ПровестиТестНастройкиПослеАутентификации", ЭтотОбъект, ДополнительныеПараметры);
		
		Если ИспользуетсяКриптография Тогда
			
			Параметры = ПараметрыАутентификацииНаТокене();
			Параметры.ОповещениеОВыполнении = ОповещениеПослеАутентификации;
			Параметры.ИмяВнешнегоМодуля = ИмяВнешнегоМодуля;
			Параметры.НастройкаОбмена = НастройкаОбмена;
			Параметры.ЭтоТест = Истина;
			
			АутентифицироватьсяНаТокене(Параметры);
			
		Иначе
			
			ПараметрыАутентификацииПоЛогину = ПараметрыАутентификацииПоЛогину();
			ПараметрыАутентификацииПоЛогину.Оповещение = ОповещениеПослеАутентификации;
			ПараметрыАутентификацииПоЛогину.ИмяВнешнегоМодуля = ДополнительныеПараметры.ИмяВнешнегоМодуля;
			ПараметрыАутентификацииПоЛогину.НастройкаОбмена = НастройкаОбмена;
			
			ВыполнитьАутентификациюПоЛогину(ПараметрыАутентификацииПоЛогину);
			
		КонецЕсли;
		
	Иначе
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		ПослеОтправкиДокументовПоСчету = Новый ОписаниеОповещения(
			"ПослеПроверкиДоступаКДаннымПослеТестаНастройки", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ПослеОтправкиДокументовПоСчету, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПровестиТестНастройкиПослеАутентификации(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
		
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, Ложь);
		
		Возврат;
		
	КонецЕсли;
		
	// Нужно запросить информацию по доступным счетам
	Организация = Неопределено;
	Банк = Неопределено;
	Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
		
		РеквизитыНастройкиОбмена = Новый Структура("Организация,Банк");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			ДополнительныеПараметры.НастройкаОбмена,
			РеквизитыНастройкиОбмена);
		Организация = РеквизитыНастройкиОбмена.Организация;
		Банк = РеквизитыНастройкиОбмена.Банк;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ДополнительныеПараметры.Организация) Тогда
		Организация = ДополнительныеПараметры.Организация;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Банк) И ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
		Банк = ДополнительныеПараметры.Банк;
	КонецЕсли;
	
	РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПроверкиНаличияДоступаКДанным(
		ДополнительныеПараметры.НастройкаОбмена,
		Организация,
		Банк);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПослеПроверкиДоступаКДаннымПослеТестаНастройки = Новый ОписаниеОповещения(
		"ПослеПроверкиДоступаКДаннымПослеТестаНастройки", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения, ПослеПроверкиДоступаКДаннымПослеТестаНастройки, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПослеПроверкиДоступаКДаннымПослеТестаНастройки(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, Ложь);
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		
		ВидОперации = НСтр("ru = 'Пробное получение доступа к данным через SberAPI'");
		ОбменСБанкамиСлужебныйКлиент.ОбработатьОшибку(ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки,
			РезультатЗадания.КраткоеПредставлениеОшибки, ДополнительныеПараметры.НастройкаОбмена);
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, Ложь);
		
	Иначе // выполнено
		
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		ОтозватьТокенДоступа(ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, РезультатОперации.Успех);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

 #Область Авторизация

// Функция проверяет, получен ли ранее пароль для авторизации на сервере банка.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  ДанныеАвторизации - Структура - содержит следующие записи:
//    * Логин - строка - имя пользователя;
//    * Пароль - Строка - пароль пользователя.
//
// Возвращаемое значение:
//  Булево - Истина - если пароль для сертификата ЭП получен ранее, иначе - Ложь.
//
Функция ПолученыДанныеАвторизации(НастройкаОбмена, ДанныеАвторизации) Экспорт
	
	Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеАутентификации = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса"];
	Если ТипЗнч(ДанныеАутентификации) = Тип("ФиксированноеСоответствие") Тогда
		ДанныеАвторизации = ДанныеАутентификации.Получить(НастройкаОбмена);
		Если ЗначениеЗаполнено(ДанныеАвторизации) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция НовыйРезультатАутентификации()
	
	Возврат Новый Структура("Успех, ТребуетсяТокен", Ложь, Ложь);
	
КонецФункции

#КонецОбласти

#Область АутентификацияПоЛогину

#Область АутентификацияИАвторизация

Процедура АутентификацияНаСервереПослеПолученияДанныхАутентификации(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения = Неопределено Тогда // отмена ввода логина-пароля
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("Логин", РезультатВыполнения.Логин);
	ДополнительныеПараметры.Вставить("Пароль", РезультатВыполнения.Пароль);
	
	ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры,
		"ИгнорироватьСохраненныеНастройки",
		Неопределено);
		
	Если Не ЗначениеЗаполнено(ИгнорироватьСохраненныеНастройки) Тогда
		
		ИгнорироватьСохраненныеНастройки = Ложь;
		ДопПараметрыАутентификации = ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации.ДополнительныеПараметры;
		Если ЗначениеЗаполнено(ДопПараметрыАутентификации) Тогда
			ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДопПараметрыАутентификации,
			"ИгнорироватьСохраненныеНастройки",
			Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияАутентификацииПоЛогину(
		ДополнительныеПараметры.НастройкаОбмена,
		РезультатВыполнения.Логин,
		РезультатВыполнения.Пароль,
		ДополнительныеПараметры.ФродПараметры,
		ДополнительныеПараметры.Организация,
		ДополнительныеПараметры.Банк,
		ИгнорироватьСохраненныеНастройки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	Оповещение = Новый ОписаниеОповещения("ПослеАутентификацииПоЛогину", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Процедура интерактивно запрашивает логин и пароль у пользователя.
//
// Параметры:
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения - обработчик, вызываемый при завершении процедуры со следующими параметрами:
//    * Результат - Структура - данные аутентификации, содержит поля:
//                    * Логин - Строка - логин пользователя
//                    * Пароль - Строка - пароль пользователя.
//              - Неопределено - пользователь отказался вводить данные аутентификации
//    * ДополнительныеПараметры - Произвольный - параметры, указанные при создании оповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена;
//
Процедура ПолучитьДанныеАутентификации(ОписаниеОповещенияОЗакрытии, НастройкаОбмена) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидОперации", НСтр("ru = 'Аутентификация на сервере банка'"));
	ПараметрыФормы.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПароляКСертификату",
		ПараметрыФормы, , , , , ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

Процедура ПослеАутентификацииПоЛогину(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
		
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда

		ОбменСБанкамиСлужебныйКлиент.УдалитьПарольИзСеанса(ДополнительныеПараметры.НастройкаОбмена);
		ТекстСообщения = РезультатЗадания.КраткоеПредставлениеОшибки;
		Если ТекстСообщения = НСтр("ru = 'Неверный логин/пароль или учетная запись заблокирована.'") Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Иначе
			ВидОперации = НСтр("ru = 'Аутентификация на сервере SberAPI'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ТипОшибкиИнтернетСоединения = ОбменСБанкамиСлужебныйКлиентСервер.ТипОшибкиИнтернетСоединения(
				РезультатЗадания.КраткоеПредставлениеОшибки);
			Если Не ЗначениеЗаполнено(ТипОшибкиИнтернетСоединения) Тогда
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстСообщения);
			Иначе
				ЭлементКоллекцииОшибка = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.НовыйЭлементКоллекцииОшибка();
				
				ЭлементКоллекцииОшибка.ТипОшибки = ТипОшибкиИнтернетСоединения;
				ЭлементКоллекцииОшибка.КраткоеПредставлениеОшибки = РезультатЗадания.КраткоеПредставлениеОшибки;
				ЭлементКоллекцииОшибка.НастройкаОбмена = ДополнительныеПараметры.НастройкаОбмена;
				
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДополнитьЭлементКоллекцииОшибка(
					ЭлементКоллекцииОшибка,
					ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации.ДополнительныеПараметры,
					ДополнительныеПараметры.НастройкаОбмена);
				
				ОповещениеПослеОтправки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
					ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации.ДополнительныеПараметры,
						"ОповещениеПослеОтправкиДокументов");
				Если ОповещениеПослеОтправки <> Неопределено Тогда
					ОповещениеПослеОтправки.ДополнительныеПараметры.КоллекцияОшибок.Добавить(ЭлементКоллекцииОшибка);
				Иначе
					КоллекцияОшибок = Новый Массив;
					КоллекцияОшибок.Добавить(ЭлементКоллекцииОшибка);
					ОбменСБанкамиСлужебныйКлиент.ПоказатьОшибкуСетевогоДоступа(
						КоллекцияОшибок,
						ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации,
						ДополнительныеПараметры.СтруктураВозврата);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		
	Иначе // выполнено
		
		ПараметрыВозврата = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если ПараметрыВозврата.ТребуетсяТокен Тогда
			
			ДополнительныеПараметры.СтруктураВозврата.ТребуетсяТокен = Истина;
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации,
				ДополнительныеПараметры.СтруктураВозврата);
			
		ИначеЕсли ПараметрыВозврата.ТребуетсяСМСАутентификация Тогда
			
			ПараметрыСессии = Новый Структура("ИдентификаторСессии, ФродПараметры, Логин");
			ЗаполнитьЗначенияСвойств(ПараметрыСессии, ДополнительныеПараметры);
			ПараметрыСессии.ИдентификаторСессии = ПараметрыВозврата.ИдентификаторСессии;
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(
				ДополнительныеПараметры.НастройкаОбмена, ПараметрыСессии);
			
			ДополнительныеПараметры.Вставить("ИдентификаторСессии", ПараметрыВозврата.ИдентификаторСессии);
			
			Оповещение = Новый ОписаниеОповещения(
				"ОтправитьОдноразовыйПарольПослеВвода", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", , ЭтотОбъект, , , , Оповещение);
			
		Иначе
			
			ПараметрыСессии = Новый Структура("ИдентификаторСессии, ФродПараметры, Логин");
			ЗаполнитьЗначенияСвойств(ПараметрыСессии, ДополнительныеПараметры);
			ПараметрыСессии.ИдентификаторСессии = ПараметрыВозврата.ИдентификаторСессии;
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(
				ДополнительныеПараметры.НастройкаОбмена, ПараметрыСессии);
			ДополнительныеПараметры.СтруктураВозврата.Успех = Истина;
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации,
				ДополнительныеПараметры.СтруктураВозврата);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьОдноразовыйПарольПослеВвода(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		
		Возврат;
	КонецЕсли;
	
	Ошибка = Ложь;
	ТребуетсяСменаПароля = Ложь;
	
	ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации.ДополнительныеПараметры,
		"ИгнорироватьСохраненныеНастройки",
		Ложь);
	
	Результат = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияОтправитьОдноразовыйПароль(
		ДополнительныеПараметры.НастройкаОбмена,
		ДополнительныеПараметры.ИдентификаторСессии,
		РезультатВыполнения,
		ДополнительныеПараметры.Организация,
		ИгнорироватьСохраненныеНастройки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиОдноразовогоПароля", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПослеОтправкиОдноразовогоПароля(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
			ОбменСБанкамиСлужебныйКлиент.УдалитьПарольИзСеанса(ДополнительныеПараметры.НастройкаОбмена);
		КонецЕсли;
		
		ТекстСообщения = РезультатЗадания.КраткоеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		
	Иначе // выполнено
		
		ПараметрыВозврата = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
			
			ПараметрыСессии = Новый Структура("ИдентификаторСессии, ФродПараметры, Логин");
			ЗаполнитьЗначенияСвойств(ПараметрыСессии, ДополнительныеПараметры);
			ПараметрыСессии.ИдентификаторСессии = ПараметрыВозврата.ИдентификаторСессии;
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(
				ДополнительныеПараметры.НастройкаОбмена, ПараметрыСессии);
			
		КонецЕсли;
		
		// Получим ACCES_TOKEN
		
		ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации.ДополнительныеПараметры,
			"ИгнорироватьСохраненныеНастройки",
			Ложь);
			
		НовыйИдентификаторОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры,
			"НовыйИдентификаторОрганизации",
			"");
		
		Результат = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПолучитьТокенДоступа(
			ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.ИдентификаторСессии,
			ДополнительныеПараметры.Организация,
			ИгнорироватьСохраненныеНастройки,
			НовыйИдентификаторОрганизации);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		Оповещение = Новый ОписаниеОповещения("ПослеПолучитьТокенДоступа", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолучитьТокенДоступа(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		
		Возврат;
		
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
			ОбменСБанкамиСлужебныйКлиент.УдалитьПарольИзСеанса(ДополнительныеПараметры.НастройкаОбмена);
		КонецЕсли;
		
		ТекстСообщения = РезультатЗадания.КраткоеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		
	Иначе // выполнено
		
		ПараметрыВозврата = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
			
			ПараметрыСессии = Новый Структура("ИдентификаторСессии, ФродПараметры, Логин, ТокенДоступа");
			ЗаполнитьЗначенияСвойств(ПараметрыСессии, ДополнительныеПараметры);
			ПараметрыСессии.ИдентификаторСессии = ПараметрыВозврата.ИдентификаторСессии;
			ПараметрыСессии.ТокенДоступа = ПараметрыВозврата.accessToken;
			
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(
				ДополнительныеПараметры.НастройкаОбмена, ПараметрыСессии);
			
		Иначе
			
			СлужебныеДанныеАвторизации = ОбменСБанкамиSberAPIСлужебныйВызовСервера.СлужебныеДанныеАвторизации(
				ДополнительныеПараметры.Организация);
			СлужебныеДанныеАвторизации.ТокенДоступа = ПараметрыВозврата.accessToken;
			ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьСлужебныеДанныеАвторизации(
				ДополнительныеПараметры.Организация,
				СлужебныеДанныеАвторизации.ПредыдущийКлиентскийСекретИзФайла,
				СлужебныеДанныеАвторизации.ПредыдущийКлиентскийСекретОбновленный,
				СлужебныеДанныеАвторизации.СертификатПодключения,
				СлужебныеДанныеАвторизации.СертификатПодключенияПароль,
				СлужебныеДанныеАвторизации.ИдентификаторОрганизации,
				СлужебныеДанныеАвторизации.ТокенДоступа);
			
		КонецЕсли;
		
		ДополнительныеПараметры.СтруктураВозврата.Вставить("ТокенДоступа", ПараметрыВозврата.accessToken);
		
		ДополнительныеПараметры.СтруктураВозврата.Успех = Истина;
		
		// Нужно запросить информацию по доступным счетам и сохранить ее
		Организация = Неопределено;
		Банк = Неопределено;
		Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
			РеквизитыНастройкиОбмена = Новый Структура("Организация,Банк");
			ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
				ДополнительныеПараметры.НастройкаОбмена,
				РеквизитыНастройкиОбмена);
			Организация = РеквизитыНастройкиОбмена.Организация;
			Банк = РеквизитыНастройкиОбмена.Банк;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ДополнительныеПараметры.Организация) Тогда
			Организация = ДополнительныеПараметры.Организация;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Банк) И ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
			Банк = ДополнительныеПараметры.Банк;
		КонецЕсли;
		
		ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры,
			"ИгнорироватьСохраненныеНастройки",
			Ложь);
		
		Если ДополнительныеПараметры.Свойство("НомерСчета") И ЗначениеЗаполнено(ДополнительныеПараметры.НомерСчета) Тогда
			РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПроверкиНаличияДоступаКДанным(
				ДополнительныеПараметры.НастройкаОбмена,
				Организация,
				Банк,
				ДополнительныеПараметры.НомерСчета,
				ИгнорироватьСохраненныеНастройки);
		Иначе
			РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПроверкиНаличияДоступаКДанным(
				ДополнительныеПараметры.НастройкаОбмена,
				Организация,
				Банк,
				,
				ИгнорироватьСохраненныеНастройки);
		КонецЕсли;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ОповещениеПослеПроверкиДоступаКДанным = Новый ОписаниеОповещения(
			"ПослеПроверкиДоступаКДанным", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатВыполнения, ОповещениеПослеПроверкиДоступаКДанным, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПроверкиДоступаКДанным(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		
		Если ДополнительныеПараметры.Свойство("ОповещениеПослеАутентификации") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		ИначеЕсли ДополнительныеПараметры.Свойство("ОповещениеПослеБазовойАутентификации") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, Ложь);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если Не РезультатОперации.Успех Тогда
			
			Если Не ЗначениеЗаполнено(РезультатОперации.ДоступныеСчета) Тогда
				ТекстСообщения = НСтр("ru = 'Для данной учетной записи нет доступа к банковским счетам.
											|Убедитесь в правильном указании логина, проверьте доступ к счетам в личном кабинете банка.
											|Если проблема повторяется, обратитесь в техническую поддержку.'");
			ИначеЕсли ДополнительныеПараметры.Свойство("НомерСчета") И ЗначениеЗаполнено(ДополнительныеПараметры.НомерСчета)
				И РезультатОперации.ДоступныеСчета.Найти(ДополнительныеПараметры.НомерСчета) = Неопределено Тогда
				ШаблонСообщения = НСтр("ru = 'Для данной учетной записи отсутствует доступ к счету %1,
											|либо номер счета указан некорректно.
											|Убедитесь в правильном указании логина, проверьте номер счета.
											|Если проблема повторяется, обратитесь в техническую поддержку.'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ДополнительныеПараметры.НомерСчета);
			Иначе
				ТекстСообщения = НСтр("ru = 'Для данной учетной записи отсутствует доступ к запрашиваемым данным.
											|Убедитесь в правильном указании логина, проверьте реквизиты организации и БИК банка.
											|Если проблема повторяется, обратитесь в техническую поддержку.'");
			КонецЕсли;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Если ДополнительныеПараметры.Свойство("ОповещениеПослеАутентификации") Тогда
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
			ИначеЕсли ДополнительныеПараметры.Свойство("ОповещениеПослеБазовойАутентификации") Тогда
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, Ложь);
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
				
				ОбменСБанкамиСлужебныйВызовСервера.СохранитьИнформациюОДоступныхСчетахВПараметрахСеанса(
					ДополнительныеПараметры.НастройкаОбмена, РезультатОперации.ДоступныеСчета);
					
			КонецЕсли;
			
			СтруктураВозврата = НовыйРезультатАутентификации();
			СтруктураВозврата.Успех = Истина;
			СтруктураВозврата.Вставить("ТокенДоступа", ДополнительныеПараметры.СтруктураВозврата.ТокенДоступа);
			ДополнительныеПараметры.Вставить("Успех", Истина);
			ДополнительныеПараметры.Вставить("СтруктураВозврата", СтруктураВозврата);
			
			Если ДополнительныеПараметры.Свойство("ОповещениеПослеАутентификации") Тогда
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, СтруктураВозврата);
			ИначеЕсли ДополнительныеПараметры.Свойство("ОповещениеПослеБазовойАутентификации") Тогда
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе // Ошибка
		
		ВидОперации = НСтр("ru = 'Проверка доступа к данным с помощью SberAPI.'");
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации,
			Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.НастройкаОбмена);
			
		Если ДополнительныеПараметры.Свойство("ОповещениеПослеАутентификации") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		ИначеЕсли ДополнительныеПараметры.Свойство("ОповещениеПослеБазовойАутентификации") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Производит процесс аутентификации только на токене Сбербанка.
//  (только на самом токене, без дальнейшей авторизации на сервере банка)
//
// Параметры:
//    ОповещениеОВыполнении - ОписаниеОповещения - оповещение после выполнения аутентификации на токене:
//          Результат - Булево - Истина, если аутентификация выполнена успешно, иначе Ложь;
//          ДополнительныеПараметры - Структура - параметры описания оповещения;
//    ИдентификаторВК - Строка - идентификатор внешней компоненты;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//    ПринудительнаяАутентификация - Булево - если Истина, то аутентификация будет производится даже если она была выполнена ранее;
//    ЭтоТест - Булево - если Истина, то будет недоступно сохранение пароля.
//
Процедура АутентифицироватьсяНаТокенеСбербанка(ОповещениеОВыполнении,
											   ИдентификаторВК,
											   НастройкаОбмена = Неопределено,
											   ПринудительнаяАутентификация = Ложь,
											   ЭтоТест = Ложь)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеОВыполнении);
	ДополнительныеПараметры.Вставить("ПринудительнаяАутентификация", ПринудительнаяАутентификация);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ЭтоТест", ЭтоТест);
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ТихийСтартПослеПодключенияВКСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

#КонецОбласти

#Область ФродМониторинг

Процедура ПолучитьФродПараметрыПослеПодключенияВКПоЛогину(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
		
		Оповещение = Новый ОписаниеОповещения(
			"ВыполнитьОповещениеПослеПолученияФродПараметров", ЭтотОбъект, ДополнительныеПараметры);
		
		ПолучитьПараметрыФродМониторинга(Оповещение, Результат.ПодключаемыйМодуль, ДополнительныеПараметры);
	Иначе
		Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		
		// При успешном выполнении в обработку оповещения передаем СтруктураВозврата, которая нужна для дальнейшей обработки
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПараметрыПКПослеПолученияАдресаКомпьютера(РезультатВызова,
																   ПараметрыВызова,
																   ДополнительныеПараметры) Экспорт
	
	ПараметрыФродМониторинга = Новый Структура;
	ПараметрыФродМониторинга.Вставить("IP", ПараметрыВызова[0]);
	ПараметрыФродМониторинга.Вставить("MAC", ПараметрыВызова[1]);
	ДополнительныеПараметры.Вставить("ПараметрыФродМониторинга", ПараметрыФродМониторинга);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьПараметрыDevicePrintПослеПолученияПараметровПК", ЭтотОбъект, ДополнительныеПараметры);
	
	ИдентификаторКомпьютера = "";
	ИдентификаторПроцессора = "";
	ИдентификаторBIOS = "";
	ИдентификаторДиска = "";
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПолучитьПараметрыПК(
		Оповещение, ИдентификаторКомпьютера, ИдентификаторПроцессора, ИдентификаторBIOS, ИдентификаторДиска);
	
КонецПроцедуры

Процедура ПолучитьПараметрыDevicePrintПослеПолученияПараметровПК(РезультатВызова,
																 ПараметрыВызова,
																 ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторКомпьютера", ПараметрыВызова.Получить(0));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторПроцессора", ПараметрыВызова.Получить(1));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторBIOS", ПараметрыВызова.Получить(2));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторДиска", ПараметрыВызова.Получить(3));
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияПараметровDevicePrint", ЭтотОбъект, ДополнительныеПараметры);
	
	РазрядностьОС = "";
	РазрядностьПроцессора = "";
	ГлубинаЦветаМонитора = "";
	ШиринаМонитора = "";
	ВысотаМонитора = "";
	ПлотностьПикселяX = "";
	ПлотностьПикселяY = "";
	ШиринаРабочегоСтола = "";
	СмещениеЧасовогоПояса = "";
	УстановленныеКомпоненты = "";
	Онлайн = Ложь;
	СглаживаниеШрифтов = Ложь;
	ЯзыкСистемы = "";
	ЯзыкПользователя = "";
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПолучитьПараметрыDevicePrint(Оповещение, РазрядностьОС,
		РазрядностьПроцессора, ГлубинаЦветаМонитора, ШиринаМонитора, ВысотаМонитора, ПлотностьПикселяX, ПлотностьПикселяY,
		ШиринаРабочегоСтола, СмещениеЧасовогоПояса, УстановленныеКомпоненты, Онлайн, СглаживаниеШрифтов, ЯзыкСистемы,
		ЯзыкПользователя);
	
КонецПроцедуры

Процедура ПослеПолученияПараметровDevicePrint(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("РазрядностьОС", ПараметрыВызова.Получить(0));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("РазрядностьПроцессора", ПараметрыВызова.Получить(1));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ГлубинаЦвета", ПараметрыВызова.Получить(2));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ШиринаМонитора", ПараметрыВызова.Получить(3));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ВысотаМонитора", ПараметрыВызова.Получить(4));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ПлотностьПикселяX", ПараметрыВызова.Получить(5));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ПлотностьПикселяY", ПараметрыВызова.Получить(6));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ШиринаРабочегоСтола", ПараметрыВызова.Получить(7));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("СмещениеЧасовогоПояса", ПараметрыВызова.Получить(8));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("УстановленныеКомпоненты", ПараметрыВызова.Получить(9));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("Онлайн", ПараметрыВызова.Получить(10));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("СглаживаниеШрифтов", ПараметрыВызова.Получить(11));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ЯзыкСистемы", ПараметрыВызова.Получить(12));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ЯзыкПользователя", ПараметрыВызова.Получить(13));
	
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("КонфигурационнаяСтрока", "");
	
	СтруктураНормализованная = Новый Структура;
	ФродСтрока = "";
	
	Для Каждого ЭлементКоллекции Из ДополнительныеПараметры.ПараметрыФродМониторинга Цикл
		СтрокаXML = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыXML(ЭлементКоллекции.Значение, "");
		СтруктураНормализованная.Вставить(ЭлементКоллекции.Ключ, СтрокаXML);
		
		ФродСтрока = ФродСтрока + ЭлементКоллекции.Ключ + "=" + СтрокаXML + "&";
	КонецЦикла;
	
	ФродСтрока = Сред(ФродСтрока, 1, СтрДлина(ФродСтрока) - 1);
	
	ФродПараметры = Новый ФиксированнаяСтруктура(СтруктураНормализованная);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияФродПараметров, ФродПараметры);
	
КонецПроцедуры

Процедура ВыполнитьОповещениеПослеПолученияФродПараметров(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
		
	КонецЕсли;
	
	ФродПараметры = Неопределено;
	ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗаполнитьФродПараметрыВСтруктуру(ФродПараметры, Результат);
	ДополнительныеПараметры.Вставить("ФродПараметры", ФродПараметры);
	
	ДанныеАутентификации = Неопределено;
	
	Если ДополнительныеПараметры.ЛогинПароль = Неопределено
		Или Не ЗначениеЗаполнено(ДополнительныеПараметры.ЛогинПароль.Логин) Тогда
		Если ПолученыДанныеАвторизации(
				ДополнительныеПараметры.НастройкаОбмена, ДанныеАутентификации) Тогда
			АутентификацияНаСервереПослеПолученияДанныхАутентификации(ДанныеАутентификации, ДополнительныеПараметры);
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"АутентификацияНаСервереПослеПолученияДанныхАутентификации", ЭтотОбъект, ДополнительныеПараметры);
			ПолучитьДанныеАутентификации(Оповещение, ДополнительныеПараметры.НастройкаОбмена);
		КонецЕсли;
	Иначе
		АутентификацияНаСервереПослеПолученияДанныхАутентификации(
			ДополнительныеПараметры.ЛогинПароль, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПараметрыФродМониторинга(Оповещение, ПодключаемыйМодуль, ДополнительныеПараметры)
	
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияФродПараметров", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПолученияАдресаКомпьютера = Новый ОписаниеОповещения(
		"ПолучитьПараметрыПКПослеПолученияАдресаКомпьютера", ЭтотОбъект, ДополнительныеПараметры);
	
	IP = "";
	MAC = "";
	
	ПодключаемыйМодуль.НачатьВызовПолучитьIP_MAC(ОповещениеПослеПолученияАдресаКомпьютера, IP, MAC);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбменЧерезТокен

#Область АутентификацияАвторизацияИФрод

Процедура ВыполнитьПреАутентификациюПоТокенуПослеПолученияФродПараметров(Результат, ДополнительныеПараметры) Экспорт
	
	ФродПараметры = Неопределено;
	ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗаполнитьФродПараметрыВСтруктуру(ФродПараметры, Результат);
	
	ДополнительныеПараметры.Вставить("ФродПараметры", ФродПараметры);
	
	ДополнительныеПараметры.Вставить("ВидЗапроса", "ПреАутентификация");
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("АдресСервера", ОбменСБанкамиSberAPIСлужебныйКлиентСервер.АдресТокена());
	ПараметрыЗапроса.Вставить("АдресМетодаСервера","/fintech/api/auth/v1/prelogin-sign");
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("User-Agent", "1C Enterprise");
	ПараметрыЗапроса.Вставить("Заголовки", Заголовки);
	
	ИнформацияОСертификате = ОбменСБанкамиСлужебныйВызовСервера.ИнформацияОСертификатеПодписиСбербанк(
		ДополнительныеПараметры.ДанныеСертификата.ДвоичныеДанныеСертификата);
	ДополнительныеПараметры.ДанныеСертификата.Вставить("ИнформацияОСертификате", ИнформацияОСертификате);
	СерийныйНомерСертификата = СтрЗаменить(Строка(ИнформацияОСертификате.SN), " ", "");
	
	ТекстЗапроса = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапросJSONPreLogin_sign(
		ИнформацияОСертификате.issuer, СерийныйНомерСертификата);
	ПараметрыЗапроса.Вставить("ТекстЗапроса", ТекстЗапроса);
	
	ДополнительныеПараметры.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	
	ПослеВыполненияЗапросаСКлиента = Новый ОписаниеОповещения(
		"ПослеВыполненияЗапросаСКлиента", ЭтотОбъект, ДополнительныеПараметры);
	
	Если НЕ ДополнительныеПараметры.Свойство("ОкноОповещенияОбОперацияхДействие") Тогда
			
		ДополнительныеПараметры.Вставить("ОкноОповещенияОбОперацияхТекстСообщения",
			НСтр("ru = 'Авторизация на токене'"));
		ДополнительныеПараметры.Вставить("ОкноОповещенияОбОперацияхКлючУникальности", Новый УникальныйИдентификатор());
		ДополнительныеПараметры.Вставить("ОкноОповещенияОбОперацияхДействие", "Открыть");
		ОбменСБанкамиСлужебныйКлиент.ОкноОповещенияОбменСБанками(ДополнительныеПараметры);
		
	КонецЕсли;
	
	ВыполнитьЗапросСКлиента(ПослеВыполненияЗапросаСКлиента, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ВыполнитьЗапросСКлиента(ПослеВыполненияЗапросаСКлиента, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("ПослеВыполненияЗапросаСКлиента", ПослеВыполненияЗапросаСКлиента);
	
	#Если ВебКлиент Тогда
		
		МетодВыполненияHTTPЗапросов = "HttpBridge";
		ТекущийКлиент = "ВебКлиент";
		
	#Иначе
		
		МетодВыполненияHTTPЗапросов = "Платформа1С";
		ТекущийКлиент = "ТонкийКлиент";
		
	#КонецЕсли
	
	ЗамещаемыйМетодВыполненияЗапросов = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.СпособВыполненияHTTPЗапросовНаКлиенте(
		ТекущийКлиент,
		ДополнительныеПараметры.ВидЗапроса);
	
	Если ЗначениеЗаполнено(ЗамещаемыйМетодВыполненияЗапросов) Тогда
		МетодВыполненияHTTPЗапросов = ЗамещаемыйМетодВыполненияЗапросов;
	КонецЕсли;
	
	Если МетодВыполненияHTTPЗапросов = "HttpBridge" Тогда
		
		ОбменСБанкамиHttpBridgeКлиент.ВыполнитьЗапрос(ДополнительныеПараметры);
		
	ИначеЕсли МетодВыполненияHTTPЗапросов = "Платформа1С" Тогда
		
		ВыполнитьHttpЗапросСКлиента(ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьФродПараметрыПослеПодключенияВКПоТокену(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
		
		Оповещение = Новый ОписаниеОповещения(
			"ВыполнитьПреАутентификациюПоТокенуПослеПолученияФродПараметров", ЭтотОбъект, ДополнительныеПараметры);
		
		ПолучитьПараметрыФродМониторинга(Оповещение, Результат.ПодключаемыйМодуль, ДополнительныеПараметры);
	Иначе
		Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		
		// При неуспешном выполнении в обработку оповещения передаем только Ложь, дальнейшей обработки результат не требуется
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыполненияЗапросаСКлиента(Результат, ДополнительныеПараметры) Экспорт
	
	ИспользоватьЖурналирование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры,
		"ИспользоватьЖурналирование",
		Ложь);
	
	НастройкаОбмена = Неопределено;
	ДополнительныеПараметры.Свойство("НастройкаОбмена", НастройкаОбмена);
	
	ЛогФайлHttpBridge = Неопределено;
	Если ДополнительныеПараметры.Свойство("ЛогФайлHttpBridge", ЛогФайлHttpBridge)
		И ЗначениеЗаполнено(ЛогФайлHttpBridge)
		И Не ИспользоватьЖурналирование Тогда
		
		Если Не ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
			
			Попытка
			Файл = Новый Файл(ЛогФайлHttpBridge);
			Если Файл.Существует() Тогда
				УдалитьФайлы(ЛогФайлHttpBridge);
			КонецЕсли;
			Исключение
				
				ВидОперации = НСтр("ru = 'Попытка удаления временного файла лога HttpBridge'");
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, НастройкаОбмена);
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
		
	Если ДополнительныеПараметры.ВидЗапроса = "ПреАутентификация" Тогда
		
		Если ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ВыполненоУспешно(Результат) Тогда
			
			ОтветPreLogin_sign = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ПрочитатьJSONОтветаPreLogin_sign(Результат.Тело);
			
			ДополнительныеПараметры.Вставить("sessionId", ОтветPreLogin_sign.sessionId);
			ДополнительныеПараметры.Вставить("authData", ОтветPreLogin_sign.authData);
			
			// Проверим получения ли уже ФродПараметры, если нет то получим их и потом вызовем ПреАутентификацмю
			ФродПараметры = Неопределено;
			Если ДополнительныеПараметры.Свойство("ФродПараметры", ФродПараметры) И ЗначениеЗаполнено(ФродПараметры) Тогда
				
				// ФродПараметры уже получены, подпишем соль
				ПодписатьСоль(ДополнительныеПараметры);
				
			Иначе
				
				// ФродПараметры еще не получены, получим их и потом подпишем соль
				ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
					"ПолучитьФродПараметрыПослеПодключенияВКИПодписатьСоль", ЭтотОбъект, ДополнительныеПараметры);
				
				ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(
					ОповещениеПослеПодключенияВК, ДополнительныеПараметры.ИдентификаторВК);
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Результат) = Тип("Структура")
			И Результат.КодСостояния = 0 И Результат.СообщениеОбОшибке = "" И Результат.Статус = "" Тогда
			
			ДополнительныеПараметры.Вставить("ОкноОповещенияОбОперацияхДействие", "Закрыть");
			ОбменСБанкамиСлужебныйКлиент.ОкноОповещенияОбменСБанками(ДополнительныеПараметры);
			
			ОчиститьДанныеАвторизацииСбербанк();
			
			ВидОперации = НСтр("ru = 'Пре-авторизация через токен.'");
			ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
				Результат.Тело, Результат.КодСостояния);
		
			ТекстОшибки = НСтр("ru = 'При установке компоненты HttpBridge произошла ошибка'");
			
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстОшибки, НастройкаОбмена);
			
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
			
		Иначе
			
			ДополнительныеПараметры.Вставить("ОкноОповещенияОбОперацияхДействие", "Закрыть");
			ОбменСБанкамиСлужебныйКлиент.ОкноОповещенияОбменСБанками(ДополнительныеПараметры);
			
			ОчиститьДанныеАвторизацииСбербанк();
			
			ВидОперации = НСтр("ru = 'Пре-авторизация через токен.'");
			ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
				Результат.Тело, Результат.КодСостояния);
		
			Ошибка = НСтр("ru = 'При пре-авторизации через токен произошла ошибка'");
			Шаблон = НСтр("ru = '%1:
								|Сообщение об ошибке %2
								|Тело ответа: %3'");
			ТекстОшибки = СтрШаблон(Шаблон, Ошибка, Результат.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
			
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, Ошибка, НастройкаОбмена);
			
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДополнительныеПараметры.ВидЗапроса = "Аутентификация" Тогда
		
		ДополнительныеПараметры.Вставить("ОкноОповещенияОбОперацияхДействие", "Закрыть");
		ОбменСБанкамиСлужебныйКлиент.ОкноОповещенияОбменСБанками(ДополнительныеПараметры);
		
		Если Результат.Статус Тогда
			
			ОтветLogin_sign = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ПрочитатьJSONОтветаLogin_sign(Результат.Тело);
			
			ДополнительныеПараметры.Вставить("sessionId", ОтветLogin_sign.sessionId);
			
			ИдентификаторСессииДвоичныеДанные = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(
				ДополнительныеПараметры.sessionId);
			
			ИдентификаторСессии = ПолучитьСтрокуИзДвоичныхДанных(ИдентификаторСессииДвоичныеДанные);
			
			ДополнительныеПараметры.Вставить("ИдентификаторСессии", ИдентификаторСессии);
			
			ПолучитьТокенДоступа(ДополнительныеПараметры);
			
		Иначе
			
			ОчиститьДанныеАвторизацииСбербанк();
			
			ВидОперации = НСтр("ru = 'Авторизация через токен.'");
			ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
				Результат.Тело, Результат.КодСостояния);
		
			Ошибка = НСтр("ru = 'При авторизации через токен произошла ошибка'");
			Шаблон = НСтр("ru = '%1:
								|Сообщение об ошибке %2
								|Тело ответа: %3'");
			ТекстОшибки = СтрШаблон(Шаблон, Ошибка, Результат.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
			
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, Ошибка, НастройкаОбмена);
			
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьФродПараметрыПослеПодключенияВКИПодписатьСоль(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
		
		Оповещение = Новый ОписаниеОповещения(
			"ПодписатьСольПослеПолученияФродПараметров", ЭтотОбъект, ДополнительныеПараметры);
		
		ПолучитьПараметрыФродМониторинга(Оповещение, Результат.ПодключаемыйМодуль, ДополнительныеПараметры);
	Иначе
		Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		
		
		// При неуспешном выполнении в обработку оповещения передаем только Ложь, дальнейшей обработки результат не требуется
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолнойАутентификацииНаТокене, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписатьСольПослеПолученияФродПараметров(Результат, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("ФродПараметры", Результат);
	
	ПодписатьСоль(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьТокенДоступа(ДополнительныеПараметры)
	
	Организация = Неопределено;
	Если Не ДополнительныеПараметры.Свойство("Организация", Организация) Тогда
		
		РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(
			ДополнительныеПараметры.НастройкаОбмена);
		Организация = РеквизитыНастройкиОбмена.Организация;
		ДополнительныеПараметры.Вставить("Организация", Организация);
		
	КонецЕсли;
	
	ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры,
		"ИгнорироватьСохраненныеНастройки",
		Ложь);
		
	НовыйИдентификаторОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры,
			"НовыйИдентификаторОрганизации",
			"");
	
	Результат = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПолучитьТокенДоступа(
		ДополнительныеПараметры.НастройкаОбмена,
		ДополнительныеПараметры.ИдентификаторСессии,
		ДополнительныеПараметры.Организация,
		ИгнорироватьСохраненныеНастройки,
		НовыйИдентификаторОрганизации);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	Оповещение = Новый ОписаниеОповещения(
		"ПослеПолученияТокенДоступаПослеАутентификации", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПослеПолученияТокенДоступаПослеАутентификации(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Неопределено;
	
	Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		НастройкаОбмена = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "НастройкаОбмена");
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		
		Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
			ОбменСБанкамиСлужебныйКлиент.УдалитьПарольИзСеанса(НастройкаОбмена);
		КонецЕсли;
		
		ТекстСообщения = РезультатЗадания.КраткоеПредставлениеОшибки;
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Оповещение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры,
			"ОповещениеПослеБазовойАутентификации");
		
		Если Оповещение = Неопределено Тогда
				
			Оповещение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДополнительныеПараметры,
				"ОповещениеПослеПолнойАутентификацииНаТокене");
			
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(Оповещение, РезультатЗадания);
		
	Иначе // выполнено
		
		ПараметрыВозврата = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		
		ПараметрыСессии = Новый Структура("ИдентификаторСессии, ФродПараметры, Логин, ТокенДоступа");
		ЗаполнитьЗначенияСвойств(ПараметрыСессии, ДополнительныеПараметры);
		ПараметрыСессии.ИдентификаторСессии = ПараметрыВозврата.ИдентификаторСессии;
		ПараметрыСессии.ТокенДоступа = ПараметрыВозврата.accessToken;
		
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Организация");
		Банк = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Банк");
		
		Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
			
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(НастройкаОбмена, ПараметрыСессии);
			
		КонецЕсли;
		
		Если Не ДополнительныеПараметры.Свойство("СтруктураВозврата") Тогда
			
			СтруктураВозврата = НовыйРезультатАутентификации();
			ДополнительныеПараметры.Вставить("СтруктураВозврата", СтруктураВозврата);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Организация) Тогда
			
			СлужебныеДанныеАвторизации = ОбменСБанкамиSberAPIСлужебныйВызовСервера.СлужебныеДанныеАвторизации(Организация);
			Если СлужебныеДанныеАвторизации <> Неопределено Тогда
				
				СлужебныеДанныеАвторизации.ТокенДоступа = ПараметрыВозврата.accessToken;
				ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьСлужебныеДанныеАвторизации(
					Организация,
					СлужебныеДанныеАвторизации.ПредыдущийКлиентскийСекретИзФайла,
					СлужебныеДанныеАвторизации.ПредыдущийКлиентскийСекретОбновленный,
					СлужебныеДанныеАвторизации.СертификатПодключения,
					СлужебныеДанныеАвторизации.СертификатПодключенияПароль,
					СлужебныеДанныеАвторизации.ИдентификаторОрганизации,
					СлужебныеДанныеАвторизации.ТокенДоступа);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДополнительныеПараметры.СтруктураВозврата.Вставить("ТокенДоступа", ПараметрыВозврата.accessToken);
		
		// Нужно запросить информацию по доступным счетам и сохранить ее
		Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) И
			(Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(Банк)) Тогда
			РеквизитыНастройкиОбмена = Новый Структура("Организация,Банк");
			ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
				ДополнительныеПараметры.НастройкаОбмена,
				РеквизитыНастройкиОбмена);
			Организация = РеквизитыНастройкиОбмена.Организация;
			Банк = РеквизитыНастройкиОбмена.Банк;
		КонецЕсли;
		
		ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДополнительныеПараметры,
			"ИгнорироватьСохраненныеНастройки",
			Ложь);
		
		// В процессе запроса данных из банка может произойти обращение к ресурсу, к которому больше нет доступа.
		// Проверим доступ к данным через обращение к методу Sber API client-info.
		Если ДополнительныеПараметры.Свойство("НомерСчета") И ЗначениеЗаполнено(ДополнительныеПараметры.НомерСчета) Тогда
			РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПроверкиНаличияДоступаКДанным(
				ДополнительныеПараметры.НастройкаОбмена,
				Организация,
				Банк,
				ДополнительныеПараметры.НомерСчета,
				ИгнорироватьСохраненныеНастройки);
		Иначе
			РезультатВыполнения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапускЗаданияПроверкиНаличияДоступаКДанным(
				ДополнительныеПараметры.НастройкаОбмена,
				Организация,
				Банк,
				,
				ИгнорироватьСохраненныеНастройки);
		КонецЕсли;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ОповещениеПослеПроверкиДоступаКДанным = Новый ОписаниеОповещения(
			"ПослеПроверкиДоступаКДанным", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатВыполнения, ОповещениеПослеПроверкиДоступаКДанным, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписатьСоль(ДополнительныеПараметры)
	
	Соль = ДополнительныеПараметры.authData;
	
	ИдентификаторСессииДвоичныеДанные = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(
		ДополнительныеПараметры.sessionId);
	
	ИдентификаторСессии = ПолучитьСтрокуИзДвоичныхДанных(ИдентификаторСессииДвоичныеДанные);
	
	ДополнительныеПараметры.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыполнитьАутентификациюПоТокену", ЭтотОбъект, ДополнительныеПараметры);
	
	Представление = НСтр("ru = 'Данные аутентификации'");
	
		
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписи", ОписаниеОповещения);
	ДополнительныеПараметры.Вставить("СтрокаПодписи", Соль);
	ДополнительныеПараметры.Вставить("ИдентификаторСертификата",
		ДополнительныеПараметры.ДанныеСертификата.ИдентификаторСертификата);
	ДополнительныеПараметры.Вставить("Представление", Представление);
	ДополнительныеПараметры.Вставить("ИдентификаторВК", ДополнительныеПараметры.ИдентификаторВК);
	ДополнительныеПараметры.Вставить("ДанныеСертификата",
		ДополнительныеПараметры.ДанныеСертификата.ДвоичныеДанныеСертификата);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПодписатьДанныеПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(
		ОповещениеПослеПодключенияВК, ДополнительныеПараметры.ИдентификаторВК);

	
КонецПроцедуры

Процедура ПодписатьДанныеПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		ВозвращаемоеЗначение = Новый Структура("Успех, ТекстОшибки", Ложь, Результат.ОписаниеОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, ВозвращаемоеЗначение);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПодписатьДанныеПослеПолученияИнформацииОТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИнформациюОТокенеСбербанк(Оповещение, Результат.ПодключаемыйМодуль, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПодписиСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При подписи данных произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Подпись данных.'");
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ТекстОшибки", ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
	
КонецПроцедуры

Процедура ВыполнитьАутентификациюПоТокену(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ФродПараметры = Неопределено;
	ДополнительныеПараметры.Свойство("ФродПараметры", ФродПараметры);
	
	Если Не ЗначениеЗаполнено(ФродПараметры) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ЭП = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ЭП", "");
	Если Не ЗначениеЗаполнено(ЭП) Тогда
		
		ВидОперации = НСтр("ru = 'Получение результата подписания'");
		ПодробнаяИнформация = НСтр("ru = 'Не заполнен результат подписания, реквизит ЭП'");
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
		
	КонецЕсли;
	
	ПодписьСоли = СтрЗаменить(СтрЗаменить(ЭП, Символы.ПС, ""), Символы.ВК, "");
	
	ТекстЗапроса = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗапросJSONLogin_sign(
		ДополнительныеПараметры.ИдентификаторСессии, ПодписьСоли, ФродПараметры);
	
	ДополнительныеПараметры.Вставить("ВидЗапроса", "Аутентификация");
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("АдресСервера", ОбменСБанкамиSberAPIСлужебныйКлиентСервер.АдресТокена());
	ПараметрыЗапроса.Вставить("АдресМетодаСервера", "/fintech/api/auth/v1/login-sign");
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("User-Agent", "1C Enterprise");
	ПараметрыЗапроса.Вставить("Заголовки", Заголовки);
	
	ПараметрыЗапроса.Вставить("ТекстЗапроса", ТекстЗапроса);
	
	ДополнительныеПараметры.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	
	ПослеВыполненияЗапросаСКлиента = Новый ОписаниеОповещения(
		"ПослеВыполненияЗапросаСКлиента", ЭтотОбъект, ДополнительныеПараметры);
	ВыполнитьЗапросСКлиента(ПослеВыполненияЗапросаСКлиента, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область Подписание

Процедура ПодписатьДанныеПослеПолученияИнформацииОТокенеСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ТипУстройства", Результат.ТипУстройства);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатАсинхроннойПодписиСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПодписиСбербанк", ЭтотОбъект);
		
	ПодключаемыйМодуль = ДополнительныеПараметры.ПодключаемыйМодуль;
		
	ПодключаемыйМодуль.НачатьВызовПодписатьДанныеЧерезVPNKeyTLSФорматPKCS7(
		Оповещение, ДополнительныеПараметры.СтрокаПодписи, ДополнительныеПараметры.ИдентификаторСертификата, "1", "0", "1");
	
КонецПроцедуры

Процедура ОбработатьРезультатАсинхроннойПодписиСбербанк(РезультатВызова,
														ПараметрыВызова,
														ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
							|Внешний модуль Сбербанка при подписании вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = ДополнительныеПараметры.ПодключаемыйМодуль;
	ТипУстройства = ДополнительныеПараметры.ТипУстройства;
	Представление = ДополнительныеПараметры.Представление;
	
	Если ТипУстройства = "UNKNOWN" Или ТипУстройства = "ORDINARY" Тогда
		ПолучитьПодписанныеДанныеСбербанк(ДополнительныеПараметры.ОповещениеПослеПодписи, ПодключаемыйМодуль);

	Иначе // сенсорный или экранный
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОбъектПодписи", Представление);
		ПараметрыФормы.Вставить("ТипУстройства", ТипУстройства);
		ПараметрыФормы.Вставить("ИмяМодуля", ДополнительныеПараметры.ИдентификаторВК);

		
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ОжиданиеПодписиСбербанк", ПараметрыФормы, , , , ,
			ДополнительныеПараметры.ОповещениеПослеПодписи, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПодписанныеДанныеСбербанк(Оповещение, ПодключаемыйМодуль)
	
	Состояние = 0;
	ЭП = "";
	
	Параметры = Новый Структура();
	Параметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	Параметры.Вставить("ОповещениеПослеПолученияПодписи", Оповещение);
	
	ОповещениеВызова = Новый ОписаниеОповещения("ПослеПолученияДанныхПодписиСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПолученияДанныхПодписиСбербанк", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовПолучитьДанныеПодписиИзVPNKeyTLSPKCS7(ОповещениеВызова, Состояние, ЭП);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхПодписиСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
								|Внешний модуль VpnKey-TLS при подписании вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	Состояние = ПараметрыВызова.Получить(0);
	
	Если Состояние = "COMPLETE" Тогда
		Результат = Новый Структура;
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("ЭП", ПараметрыВызова.Получить(1));
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	ИначеЕсли Состояние = "FAILED" ИЛИ Состояние = "REJECTED" Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
								|Код состояния: %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Состояние);
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	Иначе
		ПолучитьПодписанныеДанныеСбербанк(
			ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, ДополнительныеПараметры.ПодключаемыйМодуль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияДанныхПодписиСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении данных подписи произошла ошибка:
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение данных подписи'");
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
	ОчиститьДанныеАвторизацииСбербанк();
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ТекстОшибки", ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область HTTPЗапрос

Процедура ВыполнитьHttpЗапросСКлиента(ДополнительныеПараметры)
	
	// HttpСоединение от платформы
	
	ПараметрыЗапроса = ДополнительныеПараметры.ПараметрыЗапроса;
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.Вставить("ТипЗапроса", ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаКлиент());
	ПараметрыHTTPЗапроса.Вставить("ВидЗапроса", "POST");
	ПараметрыHTTPЗапроса.Вставить("АдресСервера", ПараметрыЗапроса.АдресСервера);
	ПараметрыHTTPЗапроса.Вставить("АдресРесурса", ПараметрыЗапроса.АдресМетодаСервера);
	ПараметрыHTTPЗапроса.Вставить("Заголовки", ПараметрыЗапроса.Заголовки);
	ПараметрыHTTPЗапроса.Вставить("Данные", ПараметрыЗапроса.ТекстЗапроса);
	ПараметрыHTTPЗапроса.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена); 
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ПослеВыполненияЗапросаСКлиента,
		РезультатВыполнения);
	
КонецПроцедуры

Функция ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса)
	
	ПолучитьТелоКакСтроку = Истина;
	ИспользоватьЖурналирование = Ложь;
	Если ЗначениеЗаполнено(ПараметрыHTTPЗапроса.НастройкаОбмена) Тогда
		
		ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(
			ПараметрыHTTPЗапроса.НастройкаОбмена);
		ИспользоватьЖурналирование = ПараметрыЖурналирования.ИспользоватьЖурналирование;
		
	КонецЕсли;
	
	СтруктураВозврата = ОбменСБанкамиСлужебныйКлиентСервер.СтруктураВозвратаHttpЗапроса();
	
	АдресРесурса = ПараметрыHTTPЗапроса.АдресРесурса;
	Если Сред(АдресРесурса,1,1) = "/" Или Сред(АдресРесурса,1,1) = "\" Тогда
		АдресРесурса = Сред(АдресРесурса,2);
	КонецЕсли;
	
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, ПараметрыHTTPЗапроса.Заголовки);
	
	Если ТипЗнч(ПараметрыHTTPЗапроса.Данные) = Тип("ДвоичныеДанные") Тогда
		HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(ПараметрыHTTPЗапроса.Данные);
	ИначеЕсли ТипЗнч(ПараметрыHTTPЗапроса.Данные) = Тип("Строка") Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ПараметрыHTTPЗапроса.Данные);
	КонецЕсли;
	
	Адрес = "";
	Протокол = "";
	Порт = 80;
	
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОпределитьПараметрыСайтаКлиентскийМетод(
		ПараметрыHTTPЗапроса.АдресСервера, Адрес, Протокол, Порт, Истина);
	
	Соединение = Новый HTTPСоединение(Адрес, Порт);
	
	Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ПараметрыHTTPЗапроса.НастройкаОбмена) Тогда
		
		URL = ПараметрыHTTPЗапроса.АдресСервера + "/" + ПараметрыHTTPЗапроса.АдресРесурса;
		Тело = HTTPЗапрос.ПолучитьТелоКакСтроку();
		ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPЗапросВЛог(
			ПараметрыHTTPЗапроса.ВидЗапроса,
			ПараметрыHTTPЗапроса.НастройкаОбмена,
			URL,
			Соединение.Сервер,
			Соединение.Порт,
			HTTPЗапрос.Заголовки,
			Тело);
		
	КонецЕсли;
	
	Если ПараметрыHTTPЗапроса.ВидЗапроса = "POST" Тогда
		ОтветHTTP = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
	Иначе
		ОтветHTTP = Соединение.Получить(HTTPЗапрос);
	КонецЕсли;
	
	ДвоичныеДанныеВТеле = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеВТелеHTTPОтвета(ОтветHTTP.Заголовки);
	Если ДвоичныеДанныеВТеле Тогда
		ТелоОвета = ОтветHTTP.ПолучитьТелоКакДвоичныеДанные();
	Иначе
		ТелоОвета = ОтветHTTP.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ПараметрыHTTPЗапроса.НастройкаОбмена) Тогда
		
		ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPОтветВЛог(
			ПараметрыHTTPЗапроса.НастройкаОбмена,
			ОтветHTTP.КодСостояния,
			ТелоОвета,
			ОтветHTTP.Заголовки);
		
	КонецЕсли;
	
	HTTPЗапрос = Неопределено;
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		
		СтруктураВозврата.Статус = Истина;
		Если ПолучитьТелоКакСтроку Тогда
			СтруктураВозврата.Тело = ОтветHTTP.ПолучитьТелоКакСтроку();
		Иначе
			СтруктураВозврата.Тело = ОтветHTTP.ПолучитьТелоКакДвоичныеДанные();
		КонецЕсли;
		
	Иначе
		
		СтруктураВозврата.Статус = Ложь;
		СообщениеОбОшибке = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.РасшифровкаКодаСостоянияHTTP(
			ОтветHTTP.КодСостояния);
		Если Не ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			СообщениеОбОшибке = НСтр("ru = '<Неизвестный код состояния>.'");
		КонецЕсли;
		
		СтруктураВозврата.СообщениеОбОшибке = СообщениеОбОшибке;
		СтруктураВозврата.Тело = ТелоОвета;
		
	КонецЕсли;
	
	СтруктураВозврата.КодСостояния = ОтветHTTP.КодСостояния;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#КонецОбласти
