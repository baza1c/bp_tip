////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интеграция с 1С:Документооборотом"
// Модуль ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервераПереопределяемый: сервер, вызов сервера
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается при определении имени реквизита формы, содержащего ссылку на объект, интегрируемый с 1С:Документооборотом.
// Позволяет указать имя реквизита формы, содержащего ссылку на объект, интегрируемый с 1С:Документооборотом.
// Переопределение требуется только в том случае, если имя основного реквизита формы
// отличается от стандартного имени "Объект".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой выполняется поиск реквизита.
//         - Структура - структура с реквизитами:
//     * ИмяФормы - Строка
//     * Параметры - Структура
//                 - ДанныеФормыСтруктура
//                 - Неопределено
//     * Объект - Структура
//              - ДанныеФормыСтруктура
//   РеквизитыФормы - Массив из РеквизитФормы - список всех реквизитов формы.
//                  - Неопределено - если вызов функции идет с клиента, и получить список реквизитов нельзя.
//   ТипИнтегрируемогоОбъекта - Тип - тип ссылки, которую требуется найти на форме.
//   ИмяРеквизита - Строка - неявно возвращаемое значение, имя реквизита, содержащего ссылку на интегрируемый с
//     1С:Документооборотом объект.
//
// Пример:
//	Если Форма.ИмяФормы = "Документ.ВерсияСоглашенияКоммерческийДоговор.Форма.ФормаДокумента" Тогда
//		ИмяРеквизита = "ДоговорОбъект.Ссылка";
//	КонецЕсли;
//
Процедура ПриОпределенииИмениРеквизитаОбъектаИнтегрируемогоС1СДокументооборотом(Знач Форма, Знач РеквизитыФормы,
		Знач ТипИнтегрируемогоОбъекта, ИмяРеквизита) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ВерсияСоглашенияКоммерческийДоговор.Форма.ФормаДокумента" Тогда
		ИмяРеквизита = "ДоговорОбъект.Ссылка";
	КонецЕсли;
	
КонецПроцедуры

// Заполняет предопределенные шаблоны выражений на встроенном языке.
//
// Параметры:
//   ШаблоныВыражений - Соответствие из КлючИЗначение:
//     * Ключ - Строка - имя шаблона.
//     * Значение - Строка - выражение на встроенном языке.
//
Процедура ПриОпределенииШаблоновВыраженийНаВстроенномЯзыке(ШаблоныВыражений) Экспорт
	
	ШаблоныВыражений.Вставить("ЗаполнениеЗаголовокаДО",
		"Параметры.Результат = Строка(Параметры.Источник);");
	
	ШаблоныВыражений.Вставить("ЗаполнениеГрифаДоступаДО",
		"Параметры.Результат = ""Общий"";");
	
	ШаблоныВыражений.Вставить("ЗаполнениеВопросаДеятельностиДО",
		"Параметры.Результат = ""Основная деятельность"";");
	
КонецПроцедуры

// Создает правила интеграции для указанного типа объекта ИС.
//
// Параметры:
//   ИмяТипаОбъекта - Строка, ЛюбаяСсылка - полное имя типа объекта ИС, как в метаданных, или ссылка на объект ИС.
//   ПравилаИнтеграции - Массив из см. ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеПравилаИнтеграции
//
// Пример:
//	Если ИмяТипаОбъекта = "Документ.ПоступлениеТоваровУслуг" Тогда
//		// Создаем правила интеграции и добавляем в массив
//		ПравилаИнтеграции = НачатьАвтоматическуюНастройкуИнтеграцииПоступлениеТоваровУслуг();
//	ИначеЕсли ИмяТипаОбъекта = "Документ.ЗаявкаНаРасходованиеДенежныхСредств" Тогда
//		// Создаем правила интеграции и добавляем в массив
//		ПравилаИнтеграции = НачатьАвтоматическуюНастройкуИнтеграцииЗаявкаНаОперацию();
//	КонецЕсли;
//
Процедура ПриСозданииПравилИнтеграцииАвтоматически(Знач ИмяТипаОбъекта, ПравилаИнтеграции) Экспорт
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ИмяТипаОбъекта)) Тогда
		ИмяТипаОбъекта = ИмяТипаОбъекта.Метаданные().ПолноеИмя();
	КонецЕсли;
	
	Если ИмяТипаОбъекта = "Документ.ПоступлениеТоваровУслуг" Тогда
		ПравилаИнтеграции = ИнтеграцияС1СДокументооборотБП.НачатьАвтоматическуюНастройкуИнтеграцииПоступлениеТоваровУслуг();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти