#Область ПрограммныйИнтерфейс

#Область РазрезыУчета

// Возвращает разрезы, в которых ведется бухгалтерский учет - в форме "Счет и три субконто"
// См. также методы области АналитикаЗатрат
//
// Возвращаемое значение:
//  Структура - Ключ - имя разреза, значение - его тип.
//
Функция РазрезыУчета() Экспорт
	
	ОписаниеТиповСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;
	
	РазрезыУчета = Новый Структура;
	РазрезыУчета.Вставить("Счет",          Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	РазрезыУчета.Вставить("Подразделение", БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	РазрезыУчета.Вставить("Субконто1",     ОписаниеТиповСубконто);
	РазрезыУчета.Вставить("Субконто2",     ОписаниеТиповСубконто);
	РазрезыУчета.Вставить("Субконто3",     ОписаниеТиповСубконто);
	
	Возврат РазрезыУчета;
	
КонецФункции

// Определяет разрезы, в которых конфигурация в принципе позволяет рассчитывать (и сохранять) калькуляцию себестоимости
// выпущенной продукции, оказанных услуг, незавершенного производства.
// 
// Параметры:
// 	Роль - ПеречислениеСсылка.РолиСчетовЗатрат
// 	     - Неопределено
// 	
// Возвращаемое значение:
//  Массив Из ТипЗначенияАналитикиЗатрат - разрезы учета
//
Функция РазрезыКалькуляции(Знач Роль = Неопределено) Экспорт
	
	Если Роль = Неопределено Тогда
		Роль = Перечисления.РолиСчетовЗатрат.КалькуляцияСебестоимости;
	КонецЕсли;
	
	Разрезы = Новый Массив;
	
	Разрезы.Добавить(РазрезАналитикиСчет());
	Разрезы.Добавить(РазрезАналитикиПодразделение());
	Если Роль = Перечисления.РолиСчетовЗатрат.КалькуляцияСебестоимости Тогда
		Разрезы.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
		Разрезы.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция);
	Иначе
		Разрезы.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыРемонтовОС);
		Разрезы.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	КонецЕсли;
	
	Возврат Разрезы;
	
КонецФункции

// Возвращает имена разрезов распределения, не участвующих в распределении на финансовый результат.
// 
// Возвращаемое значение:
//  Массив из Строка
//
Функция РазрезыРаспределенияНедоступныеНаФинансовыйРезультат() Экспорт
	
	НедоступныеРазрезыРаспределения = Новый Массив;
	
	// Общей логикой расчета себестоимости принято,
	// что подразделение на счете 90 остается тем же, что было на счете затрат (запасов)
	НедоступныеРазрезыРаспределения.Добавить("Подразделение");
	// На счетах финансового результата статья затрат имеет особый смысл: см. ЭлементыЗатрат
	НедоступныеРазрезыРаспределения.Добавить("СтатьяЗатрат");
	
	Возврат НедоступныеРазрезыРаспределения;
	
КонецФункции

#КонецОбласти

#Область СпискиСчетовУчетаЗатрат

// Возвращает предопределенные счета учета запасов, оценка стоимости которых выполняется в ходе закрытия месяца
//
// Возвращаемое значение:
//  Массив значений типа ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеСчетаЗапасов() Экспорт
	
	// Счета учета, образующие раздел учета Запасы
	СчетаЗапасов = Новый Массив;
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);              // 07
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеКомпонентовОсновныхСредств); // 08.04.1
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.Материалы);                           // 10.* (есть исключения)
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);                       // 21
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.Товары);                              // 41.* (есть исключения)
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);                    // 43
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);                   // 45
	СчетаЗапасов.Добавить(ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья);    // 20.02
	
	Возврат СчетаЗапасов;
	
КонецФункции

// Возвращает предопределенные счета-группы учета расходов на производство, закрываемые автоматически.
// Подчиненные счета могут содержать счета-исключения
//
// Возвращаемое значение:
//  Массив значений типа ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеСчетаРасходов() Экспорт
	
	СчетаРасходов = Новый Массив();
	
	СчетаПрямыхРасходов = ПредопределенныеСчетаПрямыхРасходов();
	Для Каждого Счет Из СчетаПрямыхРасходов Цикл
		СчетаРасходов.Добавить(Счет);
	КонецЦикла;
	
	СчетаКосвенныхРасходов = ПредопределенныеСчетаКосвенныхРасходов();
	Для Каждого Счет Из СчетаКосвенныхРасходов Цикл
		СчетаРасходов.Добавить(Счет);
	КонецЦикла;
	
	Возврат СчетаРасходов;

КонецФункции

// Возвращает предопределенные счета учета расходов основного, вспомогательного и обслуживающего производства, закрываемые автоматически
//
// Возвращаемое значение:
//  Массив значений типа ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеСчетаПрямыхРасходов() Экспорт
	
	СчетаРасходов = Новый Массив();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасходов, УчетЗатрат.ПредопределенныеКалькуляционныеСчета());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасходов, УчетЗатрат.ПредопределенныеСчетаОбслуживающиеПроизводства());
	РасширеннаяАналитикаЗатрат.ДобавитьСчетБракВПроизводстве(СчетаРасходов);
	
	Возврат СчетаРасходов;
	
КонецФункции

// Возвращает предопределенные счета учета расходов основного и вспомогательного производства, закрываемые автоматически
//
// Возвращаемое значение:
//  Массив значений типа ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеКалькуляционныеСчета() Экспорт
	
	СчетаРасходов = Новый Массив();
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_); // 20.*, есть исключения
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства_);
	
	Возврат СчетаРасходов;
	
КонецФункции

// Возвращает предопределенные счета учета расходов обслуживающего производства, закрываемые автоматически
//
// Возвращаемое значение:
//  Массив значений типа ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеСчетаОбслуживающиеПроизводства() Экспорт
	
	СчетаРасходов = Новый Массив();
	РасходыОбслуживающихПроизводств.ДобавитьСчетОбслуживающиеПроизводства(СчетаРасходов);
	
	Возврат СчетаРасходов;
	
КонецФункции

// Возвращает предопределенные счета учета общепроизводственных и общехозяйственных расходов, закрываемые автоматически
//
// Возвращаемое значение:
//  Массив значений типа ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеСчетаКосвенныхРасходов() Экспорт
	
	СчетаРасходов = Новый Массив();
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	Возврат СчетаРасходов;
	
КонецФункции

// Возвращает предопределенные счета учета расходов на продажу, закрываемые автоматически
//
// Возвращаемое значение:
//  Массив значений типа ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеСчетаРасходовНаПродажу() Экспорт
	
	СчетаРасходов = Новый Массив;
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	
	Возврат СчетаРасходов;
	
КонецФункции

// Возвращает предопределенные счета учета затрат на исследования и разработки
//
// Возвращаемое значение:
//  Массив из ПланСчетовСсылка.Хозрасчетный
//
Функция ПредопределенныеСчетаКалькуляцииЗатратНаНИОКР() Экспорт
	
	СчетаРасходов = Новый Массив();
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.СобственныеИсследованияИРазработки); // 20.04
	
	Возврат СчетаРасходов;
	
КонецФункции

// Возвращает счета учета расходов на производство и продажу, закрываемые автоматически
//
// Параметры:
//  ПредопределенныеСчета - Массив, ФиксированныйМассив - перечень предопределенных счетов,
//                          результат функций вида ПредопределенныеСчетаРасходов(),
//                          ПредопределенныеСчетаРасходовНаПродажу()
// 
// Возвращаемое значение:
//  ФиксированныйМассив - значения типа ПланСчетовСсылка.Хозрасчетный
//
Функция СчетаРасходов(ПредопределенныеСчета) Экспорт
	
	СчетаРасходов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПредопределенныеСчета", ПредопределенныеСчета);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&ПредопределенныеСчета)
	|	И НЕ Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья))
	|	И НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках
	|
	|УПОРЯДОЧИТЬ ПО
	|	Хозрасчетный.Порядок,
	|	Хозрасчетный.Код,
	|	Хозрасчетный.Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СчетаРасходов.Добавить(Выборка.Счет);
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(СчетаРасходов);
	
КонецФункции

// Определяет счета учета расходов на производство и продажу, закрываемые автоматически,
// по которым в заданном периоде отражены обороты.
//
// Параметры:
//  НачалоПериода - Дата - начало проверяемого периода
//  КонецПериода  - Дата - конец проверяемого периода; пустая дата - без ограничения
//  Организация   - СправочникСсылка.Организации - организация
//  ВсяОрганизация - Булево - признак, определяющий получение данных по всем филиалам организации или только по указанному филиалу
// 
// Возвращаемое значение:
//  Массив Из ПланСчетовСсылка.Хозрасчетный - используемые счета.
//
Функция ИспользуемыеСчетаРасходов(НачалоПериода, КонецПериода, Организация, ВсяОрганизация = Истина) Экспорт
	
	ПроверяемыеСчета = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроверяемыеСчета, ПредопределенныеСчетаРасходов());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроверяемыеСчета, ПредопределенныеСчетаРасходовНаПродажу());
	
	ОтборСчетов = СчетаРасходов(ПроверяемыеСчета);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.УстановитьПараметр("ОтборСчетов",   ОтборСчетов);
	
	Если ВсяОрганизация Тогда
		Запрос.УстановитьПараметр("Организации", БухгалтерскийУчетПереопределяемый.ВсяОрганизация(Организация));
	Иначе 
		Запрос.УстановитьПараметр("Организации", Организация);
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ХозрасчетныйОбороты.Счет КАК Счет
	|ПОМЕСТИТЬ ВсеСчета
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&ОтборСчетов), , Организация В (&Организации), , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	(ХозрасчетныйОбороты.СуммаОборотДт <> 0
	|			ИЛИ ХозрасчетныйОбороты.СуммаОборотКт <> 0
	|			ИЛИ ХозрасчетныйОбороты.СуммаНУОборотДт <> 0
	|			ИЛИ ХозрасчетныйОбороты.СуммаНУОборотКт <> 0)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ХозрасчетныйОстатки.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&ОтборСчетов), , Организация В (&Организации)) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	(ХозрасчетныйОстатки.СуммаОстатокДт <> 0
	|			ИЛИ ХозрасчетныйОстатки.СуммаОстатокКт <> 0
	|			ИЛИ ХозрасчетныйОстатки.СуммаНУОстатокДт <> 0
	|			ИЛИ ХозрасчетныйОстатки.СуммаНУОстатокКт <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеСчета.Счет КАК Счет,
	|	ВсеСчета.Счет.Порядок КАК СчетПорядок
	|ИЗ
	|	ВсеСчета КАК ВсеСчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетПорядок,
	|	Счет";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Счет");
	
КонецФункции

#КонецОбласти

#Область ОбщиеТипы

// Возвращает тип идентификатора, за которым можно скрыть набор значений аналитики учета
// Значение -1 - служебное, см. ИдентификаторВершиныНеопределено()
//
Функция ТипИдентификатораВершины() Экспорт
	
	Возврат ОбщегоНазначения.ОписаниеТипаЧисло(10, 0, ДопустимыйЗнак.Любой);
	
КонецФункции

// Возвращает служебное значение идентификатора (порядкового номера), обозначающего неопределенное (не заполненное) значение.
// Такое значение - меньше нуля.
// 
Функция ИдентификаторВершиныНеопределено() Экспорт
	
	Возврат -1;
	
КонецФункции

#КонецОбласти

#Область АналитикаЗатрат

// Методы этой области работают с коллекцией АналитикаЗатрат, описывающей некоторое состояние затрат и включающей
// счет учета, субконто, другие измерения.
// Другими словами АналитикаЗатрат - это набор значений (свойств), характеризующих затрату.
//
// Ключи этой коллекции - вид аналитики затрат, разрезы учета.
// Допустимые значения:
//  - Тип("ПланСчетовСсылка.Хозрасчетный") - указывает на то, что эта аналитика - счет учета. См. РазрезАналитикиСчет
//  - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные - вид субконто
//  - Строка - имя измерения регистра бухгалтерии, "Подразделение". См. РазрезАналитикиПодразделение
//
// Значения этой коллекции - значения каждого из разрезов учета (видов аналитики затрат).
// Эти значения можно назвать свойствами аналитики затрат.
// Тип определяется видом аналитики затрат (ключем коллекции) - см. ТипЗначенияАналитикиЗатрат()

// Определяет константу, обозначающую разрез аналитики Счет
// 
// Возвращаемое значение:
//  Тип("ПланСчетовСсылка.Хозрасчетный")
//
Функция РазрезАналитикиСчет() Экспорт
	
	Возврат Тип("ПланСчетовСсылка.Хозрасчетный");
	
КонецФункции

// Определяет константу, обозначающую разрез аналитики Подразделение
// 
// Возвращаемое значение:
//  Строка
//
Функция РазрезАналитикиПодразделение() Экспорт
	
	Возврат "Подразделение";
	
КонецФункции

// Определяет тип значения аналитики затрат по виду аналитики
//
// Параметры:
//  ВидАналитики - Тип, ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные, Строка - см. комментарий к области АналитикаЗатрат
// 
// Возвращаемое значение:
//  ОписаниеТипов - типы, допустимые для значений этого разреза учета
//
Функция ТипЗначенияАналитикиЗатрат(ВидАналитики) Экспорт
	
	Если ВидАналитики = РазрезАналитикиСчет() Тогда
		Возврат Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидАналитики));
	ИначеЕсли ВидАналитики = РазрезАналитикиПодразделение() Тогда
		Возврат Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("СправочникСсылка.ПодразделенияОрганизаций"));
	ИначеЕсли ТипЗнч(ВидАналитики) = Тип("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные") Тогда
		Возврат ВидАналитики.ТипЗначения;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Определяет значение одного из свойств аналитики затрат.
//
// Параметры:
//  АналитикаЗатрат - Соответствие - набор значений, характеризующих затрату, см. комментарий к области АналитикаЗатрат
//  ВидАналитики - Тип, ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные, Строка - запрашиваемый вид аналитики затрат,
//                                                                               см. комментарий к области АналитикаЗатрат
// 
// Возвращаемое значение:
//  Ссылка - значение аналитики, тип см. в ТипЗначенияАналитикиЗатрат.
//
Функция ЗначениеАналитикиЗатрат(АналитикаЗатрат, ВидАналитики) Экспорт
	
	ТипЗначения = ТипЗначенияАналитикиЗатрат(ВидАналитики);
	
	Если ТипЗначения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ТипЗначения.ПривестиЗначение(АналитикаЗатрат[ВидАналитики]);
	
КонецФункции

// Дополняет аналитику затрат значениями, которые пока не отображаются пользователю,
// но должны быть записаны в информационную базу, так как могут понадобиться
// (будут отображаться) после добавления записей в НСИ.
//
// Параметры:
//  АналитикаЗатрат - Соответствие - набор значений, характеризующих затрату, см. комментарий к области АналитикаЗатрат
// 
// Возвращаемое значение:
//  АналитикаЗатрат - набор значений, характеризующих затрату, дополненный значениями, не отображаемыми пользователю
//
Функция АналитикаЗатратДляЗаписи(АналитикаЗатрат) Экспорт
	
	АналитикаЗатратДляЗаписи = ОбщегоНазначения.СкопироватьРекурсивно(АналитикаЗатрат);
	ДополнитьАналитикуЗатратНоменклатурнойГруппой(АналитикаЗатратДляЗаписи);
	
	Возврат АналитикаЗатратДляЗаписи;
	
КонецФункции

Процедура ДополнитьАналитикуЗатратНоменклатурнойГруппой(АналитикаЗатрат)
	
	Если Не БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу() Тогда
		// Если пользователь управляет этим разрезом аналитики, то дополнять ничего не требуется.
		Возврат;
	КонецЕсли;
	
	АналитикаЗатрат.Вставить(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы,
		БухгалтерскийУчетВызовСервераПовтИсп.ОсновнаяНоменклатурнаяГруппа());
	
КонецПроцедуры

// Конвертирует аналитику затрат в форму "Счет и три субконто", где вид субконто определяется его порядковым номером на счете.
//
// Параметры:
//  АналитикаЗатрат - Соответствие - набор значений, характеризующих затрату, см. комментарий к области АналитикаЗатрат
// 
// Возвращаемое значение:
//  Структура - набор значений, характеризующих затрату.
//   Ключи добавляются в коллекцию при необходимости (см. описание ключей)
//   Возможные ключи:
//    * Счет           - ПланСчетовСсылка.Хозрасчетный - счет затрат, если задан в АналитикаЗатрат.
//                       Если счет затрат не задан, то коллекция не заполнена.
//    * Подразделение  - СправочникСсылка.ПодразделенияОрганизаций -
//                       подразделение, если учет на счете ведется по подразделениям.
//    * Субконто1      - Характеристика.ВидыСубконтоХозрасчетные,
//    * Субконто2      - Характеристика.ВидыСубконтоХозрасчетные,
//    * Субконто3      - Характеристика.ВидыСубконтоХозрасчетные - субконто, заданные в АналитикаЗатрат
//  См. также РазрезыУчета
//
Функция АналитикаЗатратНомераСубконто(АналитикаЗатрат) Экспорт
	
	АналитикаНомераСубконто = Новый Структура;
	
	Счет = ЗначениеАналитикиЗатрат(АналитикаЗатрат, РазрезАналитикиСчет());
		
	Если Не ЗначениеЗаполнено(Счет) Тогда
		Возврат АналитикаНомераСубконто;
	КонецЕсли;
		
	АналитикаНомераСубконто.Вставить("Счет", Счет);
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	Если СвойстваСчета.УчетПоПодразделениям Тогда
		Подразделение = ЗначениеАналитикиЗатрат(АналитикаЗатрат, РазрезАналитикиПодразделение());
		АналитикаНомераСубконто.Вставить("Подразделение", Подразделение);
	КонецЕсли;
	
	Для НомерСубконто = 1 По БухгалтерскийУчет.МаксимальноеКоличествоСубконто() Цикл
		
		КлючСубконто = XMLСтрока(НомерСубконто);
		
		Если СвойстваСчета.КоличествоСубконто < НомерСубконто Тогда
			ЗначениеСубконто = Неопределено;
		Иначе
			ВидСубконто = СвойстваСчета["ВидСубконто" + КлючСубконто];
			ЗначениеСубконто = ЗначениеАналитикиЗатрат(АналитикаЗатрат, ВидСубконто);
		КонецЕсли;
		
		АналитикаНомераСубконто.Вставить("Субконто" + КлючСубконто, ЗначениеСубконто);
		
	КонецЦикла;
	
	Возврат АналитикаНомераСубконто;
	
КонецФункции

// Возвращает соответствие для получения аналитики затрат по счету и 3 субконто.
//
// Параметры:
//  СчетИСубконто - Структура - Набор значений, характеризующих затрату.
//    * Счет           - ПланСчетовСсылка.Хозрасчетный - счет затрат, если задан в АналитикаЗатрат.
//    * Подразделение  - СправочникСсылка.ПодразделенияОрганизаций -
//                       подразделение, если учет на счете ведется по подразделениям.
//    * Субконто1      - Характеристика.ВидыСубконтоХозрасчетные,
//    * Субконто2      - Характеристика.ВидыСубконтоХозрасчетные,
//    * Субконто3      - Характеристика.ВидыСубконтоХозрасчетные - субконто, заданные в АналитикаЗатрат.
//  См. также РазрезыУчета
// 
// Возвращаемое значение:
//   - Соответствие - набор значений, характеризующих затрату, см. комментарий к области АналитикаЗатрат.
//
Функция АналитикаЗатратПоСчетуИСубконто(СчетИСубконто) Экспорт
	
	АналитикаЗатрат = Новый Соответствие;
	Если Не СчетИСубконто.Свойство("Счет")
		Или Не ЗначениеЗаполнено(СчетИСубконто.Счет) Тогда
		Возврат АналитикаЗатрат;
	КонецЕсли;
	
	АналитикаЗатрат.Вставить(РазрезАналитикиСчет(), СчетИСубконто.Счет);
	
	ОписаниеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетИСубконто.Счет);
	Для НомерСубконто = 1 По 3 Цикл
		ИдСубконто = XMLСтрока(НомерСубконто);
		ЗначениеСубконто = СчетИСубконто["Субконто" + ИдСубконто];
		Если ЗначениеЗаполнено(ЗначениеСубконто) Тогда
			ВидСубконто = ОписаниеСчета["ВидСубконто" + ИдСубконто];
			АналитикаЗатрат.Вставить(ВидСубконто, ЗначениеСубконто);
		КонецЕсли;
	КонецЦикла;
	
	Если ОписаниеСчета.УчетПоПодразделениям И ЗначениеЗаполнено(СчетИСубконто.Подразделение) Тогда
		АналитикаЗатрат.Вставить(РазрезАналитикиПодразделение(), СчетИСубконто.Подразделение);
	КонецЕсли;
	
	Возврат АналитикаЗатрат;
	
КонецФункции

#КонецОбласти

#Область Разработка

// Выполняет переданный запрос (пакет запросов, создающий и уничтожающий таблицы),
// сохраняя промежуточные результаты (данные созданных таблиц, даже если они уничтожаются тем же пакетом).
//
// После выполнения запроса состояние менеджера временных таблиц возвращается к исходному,
// которое было до вызова метода.
//
// Параметры:
//  Запрос - Запрос - выполняемый запрос
// 
// Возвращаемое значение:
//   - Массив Из Структура, по числу выполненных запросов пакета
//      * Текст - Строка - текст запроса
//      * Тип - Строка - указатель на тип запроса
//      * ИмяВременнойТаблицы - Строка - имя созданной таблицы
//      * Результат - ТаблицаЗначений - (для запросов выборки данных) - выбранные данные
//
Функция ОтладитьЗапрос(Запрос) Экспорт
	
	ИсходныйТекстЗапроса = "" + Запрос.Текст;
	
	МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	Если МенеджерВременныхТаблиц = Неопределено Тогда
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	КонецЕсли;
	
	// Разложим пакет на несколько запросов
	ПакетЗапросов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИсходныйТекстЗапроса, ";");
	
	ОписаниеЗапросов = Новый Массив;
	Для Каждого ТекстЗапроса Из ПакетЗапросов Цикл
		
		СловаЗапроса = Новый Массив;
		// Разложим запрос на слова
		Для НомерСтроки = 1 По СтрЧислоСтрок(ТекстЗапроса) Цикл
			
			Строка = СокрЛП(СтрПолучитьСтроку(ТекстЗапроса, НомерСтроки));
			Если ПустаяСтрока(Строка) Тогда
				Продолжить;
			КонецЕсли;
			
			// Отсечем заведомые комментарии (но не все комментарии удастся так найти)
			Если Лев(Строка, 2) = "//" Тогда
				Продолжить;
			КонецЕсли;
			
			// Разложим строку на слова, разделенные пробелами
			Для Каждого Слово Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Строка, " ") Цикл
				СловаЗапроса.Добавить(Слово);
			КонецЦикла;
			
		КонецЦикла;
		
		// Текст запроса должен начинаться с определенных ключевых слов
		Если СловаЗапроса.Количество() = 0 
			Или (СловаЗапроса[0] <> "ВЫБРАТЬ" И СловаЗапроса[0] <> "УНИЧТОЖИТЬ") Тогда
			Продолжить;
		КонецЕсли;
		
		// Определим тип запроса и имя временной таблицы.
		Уничтожить = СловаЗапроса.Найти("УНИЧТОЖИТЬ");
		Поместить  = СловаЗапроса.Найти("ПОМЕСТИТЬ");
		
		Если Уничтожить <> Неопределено Тогда
			
			ТипЗапроса = "УНИЧТОЖИТЬ";
			ИмяВременнойТаблицы = СловаЗапроса[Уничтожить + 1];
			
		ИначеЕсли Поместить <> Неопределено Тогда
			
			ТипЗапроса = "ПОМЕСТИТЬ";
			ИмяВременнойТаблицы = СловаЗапроса[Поместить + 1];
			
		Иначе
			
			ТипЗапроса = "ВЫБРАТЬ";
			ИмяВременнойТаблицы = "";
			
		КонецЕсли;
		
		ОписаниеЗапроса = Новый Структура;
		ОписаниеЗапроса.Вставить("Текст",               ТекстЗапроса);
		ОписаниеЗапроса.Вставить("Тип",                 ТипЗапроса);
		ОписаниеЗапроса.Вставить("ИмяВременнойТаблицы", ИмяВременнойТаблицы);
		
		ОписаниеЗапросов.Добавить(ОписаниеЗапроса);
		
	КонецЦикла;
	
	// Уничтожим временные таблицы, если они созданы (будут созданы) в этом пакете,
	// чтобы сохранить состав временных таблиц таким же, как до отладки
	ВременныеТаблицы = Новый Массив;
	Для Каждого ОписаниеЗапроса Из ОписаниеЗапросов Цикл
		Если ОписаниеЗапроса.Тип = "ПОМЕСТИТЬ" Тогда
			ВременныеТаблицы.Добавить(ОписаниеЗапроса.ИмяВременнойТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	// Последовательно выполняем каждый пакет и складываем его результат в коллекцию.
	Для Каждого ОписаниеЗапроса Из ОписаниеЗапросов Цикл
		
		// Запросы разных типов выполняем по-разному
		Если ОписаниеЗапроса.Тип = "УНИЧТОЖИТЬ" Тогда
		
			// Для того, чтобы снизить влияние принципа неопределенности,
			// выполняем этот запрос, только если уничтожается таблица, созданная в этой процедуре.
			// Иначе после отладки выполнять код будет нельзя
			
			ВременнаяТаблица = ВременныеТаблицы.Найти(ОписаниеЗапроса.ИмяВременнойТаблицы);
			
			Если ВременнаяТаблица = Неопределено Тогда
				
				ОписаниеЗапроса.Вставить("Результат", Ложь);
				
			Иначе
				
				Запрос.Текст = ОписаниеЗапроса.Текст;
				Запрос.Выполнить();
				
				ВременныеТаблицы.Удалить(ВременнаяТаблица);
				
				ОписаниеЗапроса.Вставить("Результат", Истина);
				
			КонецЕсли;
			
		ИначеЕсли ОписаниеЗапроса.Тип = "ПОМЕСТИТЬ" Тогда
		
			Запрос.Текст = ОписаниеЗапроса.Текст;
			Запрос.Выполнить();
			
			// Выгрузим результат запроса в таблицу значений
			ТаблицаЗапроса = МенеджерВременныхТаблиц.Таблицы.Найти(ОписаниеЗапроса.ИмяВременнойТаблицы);
			ОписаниеЗапроса.Вставить("Результат", ТаблицаЗапроса.ПолучитьДанные().Выгрузить());
			
		Иначе // Выборка данных
			
			Запрос.Текст = ОписаниеЗапроса.Текст;
			ОписаниеЗапроса.Вставить("Результат", Запрос.Выполнить().Выгрузить());
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Убираем следы
	// 1. Уничтожаем созданные временные таблицы, которых не было до выполнения пакета
	Запрос.Текст = "";
	Для Каждого ИмяТаблицы Из ВременныеТаблицы Цикл
		Запрос.Текст = Запрос.Текст + "УНИЧТОЖИТЬ " + ИмяТаблицы + ";";
	КонецЦикла;
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		ОписаниеЗапросов.Добавить(Запрос.Текст);
		Запрос.Выполнить();
	КонецЕсли;
	
	// 2. Восстанавлиаем текст запроса
	Запрос.Текст = ИсходныйТекстЗапроса;
	
	Возврат ОписаниеЗапросов;
	
КонецФункции

#КонецОбласти

#КонецОбласти
