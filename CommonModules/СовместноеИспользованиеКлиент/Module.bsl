#Область ПрограммныйИнтерфейс

// См. ОбщегоНазначенияКлиентПереопределяемый.ПриНачалеРаботыСистемы
//
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт
	
	#Если Не МобильныйКлиент Тогда
		Если ВнешнийСайтДоступен() Тогда
			ОбработчикСобытия = Новый ОписаниеОповещения("ПриПолученииСообщенияОтВнешнегоСайта", ЭтотОбъект);
			ОкноВнешнегоСайта.ПодключитьОбработчикСообщений(ОбработчикСобытия);
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

// См. обработчик ОбработкаПереходаПоНавигационнойСсылке модуля клиентского приложения.
//
Процедура ОбработкаПереходаПоНавигационнойСсылке(ДанныеПереходаПоНавигационнойСсылке, СтандартнаяОбработка) Экспорт
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ОтносительнаяСсылка = ДанныеПереходаПоНавигационнойСсылке.ОтносительнаяНавигационнаяСсылка;
	
	Для Каждого СоответствиеПрефикса Из ПрефиксыНавигационныхСсылок() Цикл
		ПрефиксСсылки = СоответствиеПрефикса.Ключ;
		ИдентификаторПриложения = СоответствиеПрефикса.Значение;
		
		Если Не СтрНачинаетсяС(ОтносительнаяСсылка, ПрефиксСсылки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		НавигационнаяСсылкаДляПерехода = Сред(ОтносительнаяСсылка, СтрДлина(ПрефиксСсылки) + 1);
		
		Если ИдентификаторПриложения = СовместноеИспользованиеКлиентПереопределяемый.ИдентификаторПриложения() Тогда
			ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаДляПерехода);
		Иначе
			ПерейтиПоНавигационнойСсылкеВнешнегоПриложения(
				НавигационнаяСсылкаДляПерехода,
				ИдентификаторПриложения);
		КонецЕсли;
		
		Прервать;
	КонецЦикла;
	
	Если СтандартнаяОбработка Тогда
		СовместноеИспользованиеКлиентПереопределяемый.ОбработкаПереходаПоНавигационнойСсылке(ДанныеПереходаПоНавигационнойСсылке, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПерейтиПоНавигационнойСсылкеВнешнегоПриложения(НавигационнаяСсылка, ИдентификаторПриложения) Экспорт
	
	Если ВнешнийСайтДоступен() Тогда
		ПерейтиПоНавигационнойСсылкеЧерезВнешнийСайт(НавигационнаяСсылка, ИдентификаторПриложения);
	Иначе
		АдресПриложения = АдресВнешнегоПриложения(ИдентификаторПриложения);
		Если Не ПустаяСтрока(АдресПриложения) Тогда
			ВнешняяСсылка = АдресПриложения;
			Если Не СтрЗаканчиваетсяНа(ВнешняяСсылка, "/") Тогда
				ВнешняяСсылка = ВнешняяСсылка + "/";
			КонецЕсли;
			ВнешняяСсылка = ВнешняяСсылка + "#" + НавигационнаяСсылка;
			
			ПерейтиПоНавигационнойСсылке(ВнешняяСсылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ОтправитьОповещениеВнешнемуПриложению(СвойстваОповещения, ИдентификаторПриложения, ВыполнитьПереход = Истина) Экспорт
	
	#Если Не МобильныйКлиент Тогда
		Если Не ВнешнийСайтДоступен() Тогда
			Возврат Ложь;
		КонецЕсли;
	
		Отправитель = СовместноеИспользованиеКлиентПереопределяемый.ИдентификаторПриложения();
		
		ОписаниеДействия = ОписаниеДействияВоВнешнемПриложении("Оповестить");
		ОписаниеДействия.Параметры.Вставить("ИмяСобытия", СвойстваОповещения.ИмяСобытия);
		ОписаниеДействия.Параметры.Вставить("Параметр", СвойстваОповещения.Параметр);
		
		Сообщение = НовоеСообщениеВнешнемуСайту("ОтправитьСообщение", Отправитель, ИдентификаторПриложения, ОписаниеДействия);
		ОкноВнешнегоСайта.ОтправитьСообщение(Сообщение);
		
		Если ВыполнитьПереход Тогда
			Сообщение = НовоеСообщениеВнешнемуСайту("ВыполнитьПереход", Отправитель, ИдентификаторПриложения);
			ОкноВнешнегоСайта.ОтправитьСообщение(Сообщение);
		КонецЕсли;
		
		Возврат Истина;
	#КонецЕсли
	
	Возврат Ложь;
	
КонецФункции

Функция НовоеОповещениеВнешнегоПриложения(ИмяСобытия, Параметр) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяСобытия", ИмяСобытия);
	Результат.Вставить("Параметр", Параметр);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовоеСообщениеВнешнемуСайту(Действие, Отправитель, Получатель, ДополнительныеПараметры = Неопределено)
	
	ДанныеСообщения = СовместноеИспользованиеВызовСервера.ДанныеСообщенияВнешнемуСайту(Действие, Отправитель, Получатель, ДополнительныеПараметры);
	
	Возврат Новый СообщениеВнешнемуСайту(ДанныеСообщения);
	
КонецФункции

Процедура ПриПолученииСообщенияОтВнешнегоСайта(Сообщение, ДополнительныеПараметры) Экспорт
	
	ОписаниеДействия = СовместноеИспользованиеВызовСервера.ОписаниеДействияОтВнешнегоПриложения(Сообщение.Данные);
	
	Если ОписаниеДействия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОписаниеДействия.Действие = "ПерейтиПоНавигационнойСсылке" Тогда
		ПерейтиПоНавигационнойСсылке(ОписаниеДействия.ПараметрыДействия.Получить("НавигационнаяСсылка"));
	ИначеЕсли ОписаниеДействия.Действие = "Оповестить" Тогда
		Оповестить(
			ОписаниеДействия.ПараметрыДействия.Получить("ИмяСобытия"),
			ОписаниеДействия.ПараметрыДействия.Получить("Параметр"));
	КонецЕсли;
	
КонецПроцедуры

Функция ПрефиксыНавигационныхСсылок()
	
	Префиксы = Новый Соответствие;
	
	СовместноеИспользованиеКлиентПереопределяемый.ЗаполнитьПрефиксыНавигационныхСсылок(Префиксы);
	
	Возврат Префиксы;
	
КонецФункции

Функция АдресВнешнегоПриложения(ИдентификаторПриложения)
	
	Возврат "";
	
КонецФункции

#Область ВнешнийСайт

Функция ВнешнийСайтДоступен()
	
	#Если МобильныйКлиент Тогда
		Возврат Ложь;
	#Иначе
		Возврат ОкноВнешнегоСайта.Доступно;
	#КонецЕсли
	
КонецФункции

Процедура ПерейтиПоНавигационнойСсылкеЧерезВнешнийСайт(НавигационнаяСсылка, Получатель)
	
	#Если Не МобильныйКлиент Тогда
		Отправитель = СовместноеИспользованиеКлиентПереопределяемый.ИдентификаторПриложения();
		
		ОписаниеДействия = ОписаниеДействияВоВнешнемПриложении("ПерейтиПоНавигационнойСсылке");
		ОписаниеДействия.Параметры.Вставить("НавигационнаяСсылка", НавигационнаяСсылка);
		
		Сообщение = НовоеСообщениеВнешнемуСайту("ОтправитьСообщение", Отправитель, Получатель, ОписаниеДействия);
		ОкноВнешнегоСайта.ОтправитьСообщение(Сообщение);
		
		Сообщение = НовоеСообщениеВнешнемуСайту("ВыполнитьПереход", Отправитель, Получатель);
		ОкноВнешнегоСайта.ОтправитьСообщение(Сообщение);
	#КонецЕсли
	
КонецПроцедуры

Функция ОписаниеДействияВоВнешнемПриложении(Действие)
	
	Описание = Новый Структура;
	Описание.Вставить("Действие", Действие);
	Описание.Вставить("Параметры", Новый Структура);
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти

#КонецОбласти