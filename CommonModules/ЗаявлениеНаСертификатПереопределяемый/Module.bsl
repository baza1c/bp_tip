///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается в форме добавления заявления на новый сертификат для заполнения реквизитов организации и при ее выборе.
//
// Параметры:
//  Параметры - Структура:
//
//    * ТипОрганизации - ОписаниеТипов - возвращаемое значение. Содержит ссылочные типы, из которых
//                       можно сделать выбор. Начальное значение ОпределяемыйТип.Организация.
//                     - Неопределено  - возвращаемое значение. Выбор организации не поддерживается.
//
//    * Организация - СправочникСсылка - организация из ТипОрганизации, которую нужно заполнить.
//                    Если организация уже заполнена, требуется перезаполнить ее свойства - например,
//                    при повтором вызове, когда пользователь выбрал другую организацию.
//                  - Неопределено - если ТипОрганизации не настроен.
//                    Пользователю недоступен выбор организации.
//
//    * ЭтоИндивидуальныйПредприниматель - Булево - возвращаемое значение:
//                    Ложь   - начальное значение - указанная организация является юридическим лицом;
//                    Истина - указанная организация является индивидуальным предпринимателем.
//
//    * ЭтоИностраннаяОрганизация - Булево - возвращаемое значение, когда Истина ОГРН не заполняется.
//                                - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * НаименованиеСокращенное  - Строка - возвращаемое значение. Краткое наименование организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * НаименованиеПолное       - Строка - возвращаемое значение. Краткое наименование организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ИНН                      - Строка - возвращаемое значение. ИНН организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * КПП                      - Строка - возвращаемое значение. КПП организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ОГРН                     - Строка - возвращаемое значение. ОГРН организации (кроме иностранных).
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * РасчетныйСчет            - Строка - возвращаемое значение. Основной расчетный счет организации для договора.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * БИК                      - Строка - возвращаемое значение. БИК банка расчетного счета.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * КорреспондентскийСчет    - Строка - возвращаемое значение. Корреспондентский счет банка расчетного счета.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Телефон                  - Строка - возвращаемое значение. Телефон организации в формате JSON, как его
//                                 возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//
//    * ЮридическийАдрес - Строка - возвращаемое значение. Юридический адрес организации в формате JSON, как его
//                         возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                       - Неопределено - значение не указано, не изменять имеющееся значение.
//
Процедура ПриЗаполненииРеквизитовОрганизацииВЗаявленииНаСертификат(Параметры) Экспорт
	
	ДанныеОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Параметры.Организация, ТекущаяДатаСеанса());
	Параметры.НаименованиеСокращенное          = ДанныеОбОрганизации.СокращенноеНаименование;
	Параметры.НаименованиеПолное               = ДанныеОбОрганизации.ПолноеНаименование;
	Параметры.ИНН                              = ДанныеОбОрганизации.ИНН;
	Параметры.КПП                              = ДанныеОбОрганизации.КПП;
	Параметры.ОГРН                             = ДанныеОбОрганизации.ОГРН;
	Параметры.РасчетныйСчет                    = ДанныеОбОрганизации.НомерСчета;
	Параметры.БИК                              = ДанныеОбОрганизации.БИК;
	Параметры.КорреспондентскийСчет            = ДанныеОбОрганизации.КоррСчет;
	Параметры.ЭтоИндивидуальныйПредприниматель = ДанныеОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	//Заполним контактную информацию
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	
	Объекты = Новый Массив();
	Объекты.Добавить(Параметры.Организация);
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Объекты, , ВидыКИ);
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	Строка = КонтактнаяИнформация.Найти(ВидКонтактнойИнформации, "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.ЮридическийАдрес = Строка.Значение;
	КонецЕсли;
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
	Строка = КонтактнаяИнформация.Найти(ВидКонтактнойИнформации, "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.Телефон = Строка.Значение;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в форме добавления заявления на новый сертификат для заполнении реквизитов владельца и при его выборе.
//
// Параметры:
//  Параметры - Структура:
//    * ЭтоФизическоеЛицо - Булево - если Истина, тогда заявление заполняется для физического лица, иначе для организации.
// 
//    * Организация  - СправочникСсылка - выбранная организация из ТипОрганизации, на которую оформляется сертификат.
//                   - Неопределено - если ТипОрганизации не настроен или составной и тип организации не выбран.
//
//    * ТипВладельца  - ОписаниеТипов - возвращаемое значение. Содержит ссылочные типы, из которых можно сделать выбор.
//                    - Неопределено  - возвращаемое значение. Выбор владельца не поддерживается.
//
//    * Сотрудник    - СправочникСсылка - возвращаемое значение. Владелец сертификата из ТипВладельца,
//                     которого нужно заполнить. Если уже заполнен (выбран пользователем), его не следует изменять.
//                   - Неопределено - если ТипВладельца не определен, тогда реквизит недоступен пользователю.
//
//    * Директор     - СправочникСсылка - возвращаемое значение. Директор из ТипВладельца, который может быть
//                     выбран, как владелец сертификата. Не учитывается, если заявление для ИП или физического лица.
//                   - Неопределено - начальное значение - скрыть директора из списка выбора.
//
//    * ГлавныйБухгалтер - СправочникСсылка - возвращаемое значение. Главный бухгалтер из ТипВладельца, который может
//                     быть выбран как владелец сертификата. Не учитывается, если заявление для ИП или физического лица.
//                   - Неопределено - начальное значение - скрыть главного бухгалтера из списка выбора.
//
//    * Пользователь - СправочникСсылка.Пользователи - возвращаемое значение. Пользователь - владелец сертификата.
//                     В общем случае может быть не заполнено. Рекомендуется заполнить, если есть возможность.
//                     Записывается в сертификат в поле Пользователь, может быть изменено в дальнейшем.
//
//    * Фамилия            - Строка - возвращаемое значение. Фамилия сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Имя                - Строка - возвращаемое значение. Имя сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Отчество           - Строка - возвращаемое значение. Отчество сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДатаРождения       - Дата   - возвращаемое значение. Дата рождения сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Пол                - Строка - возвращаемое значение. Пол сотрудника "Мужской" или "Женский".
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * МестоРождения      - Строка - возвращаемое значение. Описание места рождения сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Гражданство        - СправочникСсылка.СтраныМира - возвращаемое значение. Гражданство сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ИНН                - Строка - возвращаемое значение. ИНН физического лица.
//                           Учитывается только в заявлении для физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * СтраховойНомерПФР  - Строка - возвращаемое значение. СНИЛС сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Должность          - Строка - возвращаемое значение. Должность сотрудника в организации.
//                           Не учитывается, если заявление для ИП или физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Подразделение      - Строка - возвращаемое значение. Обособленное подразделение организации, в котором
//                           работает сотрудник. Не учитывается, если заявление для ИП или физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//
//    * ДокументВид        - Строка - возвращаемое значение. Строки "21" или "91". 21 - паспорт гражданина РФ,
//                           91 - иной документ, предусмотренный законодательством РФ (по СПДУЛ).
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументНомер      - Строка - возвращаемое значение. Номер документа сотрудника (серия и
//                           номер для паспорта гражданина РФ).
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументКемВыдан   - Строка - возвращаемое значение. Кем выдан документ сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументКодПодразделения - Строка - возвращаемое значение. Код подразделения, если вид документа 21.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументДатаВыдачи - Дата   - возвращаемое значение. Дата выдачи документа сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * АдресРегистрации   - Строка - возвращаемое значение. Адрес постоянной или временной регистрации
//                           физического лица (минимум регион, населенный пункт) в формате JSON, как его возвращает
//                           функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                           Учитывается только в заявлении для физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ЭлектроннаяПочта   - Строка - возвращаемое значение. Адрес электронной почты сотрудника в формате JSON, как его
//                           возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Телефон            - Строка - возвращаемое значение. Телефон физического лица в формате JSON, как его
//                           возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                           Учитывается только в заявлении для физического лица.
//
//
Процедура ПриЗаполненииРеквизитовВладельцаВЗаявленииНаСертификат(Параметры) Экспорт
	
	Параметры.ТипВладельца = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица");
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Организация, "ЮридическоеФизическоеЛицо, ИндивидуальныйПредприниматель");
	
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Параметры.Организация, ТекущаяДатаСеанса());
	
	Если РеквизитыОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Если Не ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
			Параметры.Сотрудник = РеквизитыОрганизации.ИндивидуальныйПредприниматель;
		КонецЕсли;
	Иначе
		Параметры.Директор         = ОтветственныеЛица.Руководитель;
		Параметры.ГлавныйБухгалтер = ОтветственныеЛица.ГлавныйБухгалтер;
	КонецЕсли;
	
	ТекущийВладелец = Неопределено;
	
	Если ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
		ТекущийВладелец = Параметры.Сотрудник;
	ИначеЕсли ЗначениеЗаполнено(Параметры.Директор) Тогда
		ТекущийВладелец = Параметры.Директор;
		Параметры.Сотрудник = Параметры.Директор;
		Параметры.Должность = ОтветственныеЛица.РуководительДолжностьПредставление;
	ИначеЕсли ЗначениеЗаполнено(Параметры.ГлавныйБухгалтер) Тогда
		ТекущийВладелец = Параметры.ГлавныйБухгалтер;
		Параметры.Сотрудник = Параметры.ГлавныйБухгалтер;
		Параметры.Должность = ОтветственныеЛица.ГлавныйБухгалтерДолжностьПредставление;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийВладелец) Тогда
		Возврат; // Поля можно заполнить только, если сотрудник указан.
	КонецЕсли;
	
	ДанныеФизическогоЛица = УчетЗарплаты.ДанныеФизическихЛиц(Параметры.Организация, ТекущийВладелец, ТекущаяДатаСеанса());
	
	Если ЗначениеЗаполнено(ДанныеФизическогоЛица.Должность) Тогда
		Параметры.Должность = ДанныеФизическогоЛица.Должность;
	КонецЕсли;
	
	Параметры.Фамилия  = ДанныеФизическогоЛица.Фамилия;
	Параметры.Имя      = ДанныеФизическогоЛица.Имя;
	Параметры.Отчество = ДанныеФизическогоЛица.Отчество;
	
	Если Параметры.ЭтоФизическоеЛицо Тогда
		Параметры.ИНН = ДанныеФизическогоЛица.ИНН;
	КонецЕсли;
	
	Параметры.ДатаРождения  = ДанныеФизическогоЛица.ДатаРождения;
	Параметры.Пол           = ?(ДанныеФизическогоЛица.Пол = Перечисления.ПолФизическогоЛица.Женский, "Женский", "Мужской");
	Параметры.МестоРождения = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(ДанныеФизическогоЛица.МестоРождения);
	Параметры.Гражданство   = ДанныеФизическогоЛица.Страна;
	
	Параметры.СтраховойНомерПФР  = ДанныеФизическогоЛица.СтраховойНомерПФР;
	Если ЗначениеЗаполнено(ДанныеФизическогоЛица.ВидДокумента) Тогда
		Параметры.ДокументВид = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеФизическогоЛица.ВидДокумента, "КодМВД");
	КонецЕсли;
	Параметры.ДокументНомер      = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"), ДанныеФизическогоЛица.Серия, ДанныеФизическогоЛица.Номер);
	Параметры.ДокументКемВыдан   = ДанныеФизическогоЛица.КемВыдан;
	Параметры.ДокументДатаВыдачи = ДанныеФизическогоЛица.ДатаВыдачи;
	Параметры.ДокументКодПодразделения = ДанныеФизическогоЛица.КодПодразделения;
	
	АдресаЭлектроннойПочты = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
		ТекущийВладелец, Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица, , Ложь);
	
	Если ЗначениеЗаполнено(АдресаЭлектроннойПочты) Тогда
		АдресЭлектроннойПочты = АдресаЭлектроннойПочты[0];
		Параметры.ЭлектроннаяПочта = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВXML(АдресЭлектроннойПочты.Значение);
	КонецЕсли;
	
	Если Параметры.ЭтоФизическоеЛицо Тогда
		
		Телефоны = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
			ТекущийВладелец, Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица, , Ложь);
		
		Если ЗначениеЗаполнено(Телефоны) Тогда
			Телефон = Телефоны[0];
			Параметры.Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВXML(Телефон.Значение);
		КонецЕсли;
		
		АдресаРегистрации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
			ТекущийВладелец, Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица, , Ложь);
		
		Если ЗначениеЗаполнено(АдресаРегистрации) Тогда
			АдресРегистрации = АдресаРегистрации[0];
			Параметры.АдресРегистрации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВXML(АдресРегистрации.Значение);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФизическогоЛица.ПодразделениеОрганизации) Тогда
		
		РеквизитыПодразделения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
						ДанныеФизическогоЛица.ПодразделениеОрганизации, 
						"ОбособленноеПодразделение, НаименованиеПолное");
		Если РеквизитыПодразделения.ОбособленноеПодразделение = Истина Тогда
			Параметры.Подразделение = РеквизитыПодразделения.НаименованиеПолное;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в форме добавления заявления на новый сертификат для заполнения реквизитов руководителя и при его выборе.
// Только для юридического лица. Для индивидуального предпринимателя и физического лица не требуется.
//
// Параметры:
//  Параметры - Структура:
//    * Организация   - СправочникСсылка - выбранная организация из ТипОрганизации, на которую оформляется сертификат.
//                    - Неопределено - если ТипОрганизации не настроен.
//
//    * ТипРуководителя - ОписаниеТипов - возвращаемое значение. Содержит ссылочные типы, из которых можно сделать выбор.
//                      - Неопределено  - возвращаемое значение. Выбор партнера не поддерживается.
//
//    * Руководитель  - СправочникСсылка - это значение из ТипРуководителя, выбранное пользователем,
//                      по которому нужно заполнить должность.
//                    - Неопределено - ТипРуководителя не определен.
//                    - ЛюбаяСсылка - возвращаемое значение. Руководитель, который будет подписывать документы.
//
//    * Представление - Строка - возвращаемое значение. Представление руководителя.
//                    - Неопределено - получить представление от значения Руководитель.
//
//    * Должность     - Строка - возвращаемое значение. Должность руководителя, который будет подписывать документы.
//                    - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Основание     - Строка - возвращаемое значение. Основание на котором действует
//                      должностное лицо (устав, доверенность, ...).
//                    - Неопределено - значение не указано, не изменять имеющееся значение.
//
Процедура ПриЗаполненииРеквизитовРуководителяВЗаявленииНаСертификат(Параметры) Экспорт
	
Параметры.ТипРуководителя = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица");
	
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Параметры.Организация, ТекущаяДатаСеанса());
	
	Если Не ЗначениеЗаполнено(Параметры.Руководитель) Тогда
		Параметры.Руководитель = ОтветственныеЛица.Руководитель;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Руководитель) Тогда
		Возврат; // Поля можно заполнить только, если руководитель указан.
	КонецЕсли;
	
	Если Параметры.Руководитель = ОтветственныеЛица.Руководитель Тогда
		Параметры.Должность = ОтветственныеЛица.РуководительДолжностьПредставление;
		Параметры.Основание = НСтр("ru = 'Устав'");
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в форме добавления заявления на новый сертификат для заполнения реквизитов партнера и при его выборе.
//
// Параметры:
//  Параметры - Структура:
//    * ЭтоФизическоеЛицо - Булево - если Истина, тогда заявление заполняется для физического лица, иначе для организации.
//
//    * Организация   - СправочникСсылка - выбранная организация из ТипОрганизации, на которую оформляется сертификат.
//                    - Неопределено - если ТипОрганизации не настроен.
//
//    * ТипПартнера   - ОписаниеТипов - содержит ссылочные типы из которых можно сделать выбор.
//                    - Неопределено - выбор партнера не поддерживается.
//
//    * Партнер       - СправочникСсылка - это контрагент (обслуживающая организация) из ТипПартнера,
//                      выбранный пользователем, по которому нужно заполнить реквизиты, описанные ниже.
//                    - Неопределено - ТипПартнера не определен.
//                    - ЛюбаяСсылка - возвращаемое значение. Значение, сохраняемое в заявке для истории.
//
//    * Представление - Строка - возвращаемое значение. Представление партнера.
//                    - Неопределено - получить представление от значения Партнер.
//
//    * ЭтоИндивидуальныйПредприниматель - Булево - возвращаемое значение:
//                      Ложь   - начальное значение - указанный партнер является юридическим лицом,
//                      Истина - указанный партнер является индивидуальным предпринимателем.
//
//    * ИНН           - Строка - возвращаемое значение. ИНН партнера.
//                    - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * КПП           - Строка - возвращаемое значение. КПП партнера.
//                    - Неопределено - значение не указано, не изменять имеющееся значение.
//
Процедура ПриЗаполненииРеквизитовПартнераВЗаявленииНаСертификат(Параметры) Экспорт
	
	Параметры.ТипПартнера = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	
	Если Не ЗначениеЗаполнено(Параметры.Партнер) Тогда
		Возврат; // Поля можно заполнить только, если партнер указан.
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Партнер, "ИНН, КПП, ЮридическоеФизическоеЛицо");
	
	Если Реквизиты.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Параметры.ЭтоИндивидуальныйПредприниматель = Истина;
	КонецЕсли;
	
	Параметры.ИНН = Реквизиты.ИНН;
	Параметры.КПП = Реквизиты.КПП;
	
КонецПроцедуры

#КонецОбласти
