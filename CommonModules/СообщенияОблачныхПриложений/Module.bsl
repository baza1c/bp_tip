
#Область ПрограммныйИнтерфейс

#Область ПодключениеПриложений

// Проверяет, может ли другое облачное приложение быть подключено для получения данных по отчетной кампании.
//
// Параметры:
//   Адрес - Строка - URI облачного приложения
//
// Возвращаемое значение:
//   Булево - результат проверки
//
Функция ПриложениеГотовоПриниматьЗапросы(Адрес) Экспорт
	
	Если ДоступенHTTPСервисКалендарьОтчетности(Адрес) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// В качестве проверки используется метод GET.
	// Если приложение готово, то ответ ожидается ответ 200.
	// Любой другой код ответа символизирует о неготовности приложения принимать запросы.
	
	Подключение = ЗапросыREST.НовыйПодключение();
	Подключение.АдресСервиса = Адрес;
	Подключение.ПространствоИмен = ПространствоИмен();
	Если Не ДобавитьЗаголовокJWTАутентификации(Подключение) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтветСервиса = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, "GET", "intracloud_connections");
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = (ОтветСервиса.КодСостояния = ЗапросыREST.КодСтандартногоСостояния());
	
	Если Результат Тогда
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("КалендарьОтчетности.ОбращениеКExternalAPI");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Подключает другое облачное приложение для получения данных по отчетной кампании.
// Для получение адреса и наименования облачного приложения используется
// метод СообщенияОтчетностиОблачныхПриложений.ПриложенияАбонента()
//
// Параметры:
//   Адрес - Строка - URI облачного приложения
//   Наименование - Строка - наименование приложения в облаке
//
// Возвращаемое значение:
//   Неопределено
//   Структура
//
Функция ПодключитьОблачноеПриложение(Адрес, Наименование) Экспорт
	
	РежимСовместимости = Не ДоступенHTTPСервисКалендарьОтчетности(Адрес);
	
	Подключение = ЗапросыREST.НовыйПодключение();
	Подключение.АдресСервиса = Адрес;
	Подключение.ПространствоИмен = ПространствоИмен(РежимСовместимости);
	Если Не ДобавитьЗаголовокJWTАутентификации(Подключение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Структура;
	Если РежимСовместимости Тогда
		Запрос.Вставить("Идентификатор", ИдентификаторЭтогоПриложения());
		Ресурс = "intracloud_connections";
	Иначе
		Запрос.Вставить("ТипЗапроса", СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаПодключитьОблачноеПриложение());
		Запрос.Вставить("ВыполнитьСинхронно", Истина);
		Ресурс = "requests";
	КонецЕсли;
	
	ОтветСервиса = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, "POST", Ресурс, Запрос);
	
	Если ОтветСервиса = Неопределено Тогда
		//Если запрос не удалось выполнить, то ошибку в ЖР запишет метод ЗапросыREST.ВыполнитьМетодСервиса().
		Возврат Неопределено;
	КонецЕсли;
	
	Содержимое = ?(ОтветСервиса.Содержимое.Свойство("Ответ"),
		ОтветСервиса.Содержимое.Ответ,
		ОтветСервиса.Содержимое);
	
	Если ОтветСервиса.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния()
		Или Не Содержимое.Свойство("ИдентификаторПриложения")
		Или Не Содержимое.Свойство("ДоступныеОрганизации") Тогда
		
		ЗаписатьОшибкуКодСостояния(
			ОтветСервиса,
			НСтр("ru = 'Подключение приложения'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат Неопределено;
	КонецЕсли;
	
	Если РежимСовместимости Тогда
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("КалендарьОтчетности.ОбращениеКExternalAPI");
	КонецЕсли;
	
	Идентификатор = Содержимое.ИдентификаторПриложения;
	УстановитьПривилегированныйРежим(Истина);
	Приложение = Справочники.ОблачныеПриложения.СсылкаПриложение(
		Идентификатор,
		Адрес,
		Наименование);
	УстановитьПривилегированныйРежим(Ложь);
		
	Если Приложение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДоступныеОрганизации = Новый Массив;
	Для Каждого ДанныеОрганизации Из Содержимое.ДоступныеОрганизации Цикл
		УстановитьПривилегированныйРежим(Истина);
		Организация = Справочники.ОрганизацииОблачныхПриложений.СсылкаОрганизация(
			ДанныеОрганизации.Идентификатор,
			Приложение,
			ДанныеОрганизации.Наименование);
		УстановитьПривилегированныйРежим(Ложь);
			
		Если ЗначениеЗаполнено(Организация) Тогда
			ДоступныеОрганизации.Добавить(Организация);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("ПриложениеИдентификатор, ПриложениеСсылка, ДоступныеОрганизации",
		Идентификатор, Приложение, ДоступныеОрганизации);
	
КонецФункции

// Возвращает идентификатор этого облачного приложения в механизме обмена сообщениями
//
// Возвращаемое значение:
//   Строка - идентификатор приложения
//
Функция ИдентификаторЭтогоПриложения() Экспорт

	ИдентификаторПриложения = Константы.ИдентификаторПриложенияСообщенийОблачныхПриложений.Получить();
	
	Если ЗначениеЗаполнено(ИдентификаторПриложения) Тогда
		Возврат ИдентификаторПриложения;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Константа.ИдентификаторПриложенияСообщенийОблачныхПриложений");
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		ИдентификаторПриложения = Константы.ИдентификаторПриложенияСообщенийОблачныхПриложений.Получить();
		Если ЗначениеЗаполнено(ИдентификаторПриложения) Тогда
			ОтменитьТранзакцию();
			Возврат ИдентификаторПриложения;
		КонецЕсли;
	
		ИдентификаторПриложения = XMLСтрока(Новый УникальныйИдентификатор());
		Константы.ИдентификаторПриложенияСообщенийОблачныхПриложений.Установить(ИдентификаторПриложения);
		
		ЗафиксироватьТранзакцию();
		Возврат ИдентификаторПриложения;
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Сообщения облачных приложений'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка, , , 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область ТранспортДанных

// Запрашивает у другого облачного приложения данные.
// 
// Параметры:
//   Приложение - СправочникСсылка.ОблачныеПриложения - экземпляр приложения, которому направляется запрос
//   Ресурс - Строка - имя ресурса HTTP-сервиса, обслуживающего прикладной механизм
//   ДанныеЗапроса - Структура - См. СообщенияОтчетностиОблачныхПриложений.НовыйПараметрыЗапросаДанных(),
//                                   СообщенияОтчетностиОблачныхПриложений.НовыйПараметрыЗапросаСпискаОрганизаций()
//
// Возвращаемое значение:
//   Строка - идентификатор запроса, с которым потом следует обратиться методом GET,
//            если приложение не может принять запрос (и не сможет принять в будущем),
//            возвращается пустая строка.
//   Структура - ответ на запрос, если он был обработан синхронно,
//   Неопределено - если не удалось выполнить запрос.
//
Функция ЗапроситьДанныеПриложения(Приложение, Ресурс, ДанныеЗапроса) Экспорт
	
	Подключение = ДанныеПодключения(Приложение);
	Если Подключение = Неопределено тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Подключение.РежимСовместимости Тогда
		РесурсМетода = СтрШаблон("%1/requests/add", Ресурс);
	Иначе
		РесурсМетода = "requests";
	КонецЕсли;
	
	ОтветСервиса = ЗапросыREST.ВыполнитьМетодСервиса(
		Подключение,
		"POST",
		РесурсМетода,
		ДанныеЗапроса);
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОтветСервиса.КодСостояния = КодСостоянияПриложениеУдалено()
		Или ОтветСервиса.КодСостояния = ЗапросыREST.КодСостоянияНеАвторизован() Тогда
		// В случае, если приложение больше не существует, нода отвечает кодом 420.
		// Если пользователю запрещен доступ в приложение, то ответ будет с кодом 403.
		Возврат "";
	КонецЕсли;
	
	Если ОтветСервиса.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		
		ЗаписатьОшибкуКодСостояния(
			ОтветСервиса,
			НСтр("ru = 'Запрос данных'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
		Возврат Неопределено;
	КонецЕсли;
	
	Если Подключение.РежимСовместимости Тогда
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("КалендарьОтчетности.ОбращениеКExternalAPI");
	КонецЕсли;
	
	СодержимоеОтвета = ОтветСервиса.Содержимое;
	Возврат ?(СодержимоеОтвета.Свойство("Идентификатор"), СодержимоеОтвета.Идентификатор, СодержимоеОтвета);
	
КонецФункции

// Получает у приложения данные его отчетной кампании.
// 
// Параметры:
//   Приложение - СправочникСсылка.ОблачныеПриложения - экземпляр приложения
//   Ресурс - Строка - имя ресурса HTTP-сервиса, обслуживающего прикладной механизм
//   ИдентификаторЗапроса - Строка - идентификатор запроса, полученный методом ЗапроситьДанныеПриложения()
//
// Возвращаемое значение:
//   Строка - содержимое ответа в виде строки JSON,
//            если у приложения данные ещё не готовы, то возвращается пустая строка.
//   Неопределено - если не удалось получить данные, повторять запрос не следует.
//
Функция ПолучитьДанныеПриложения(Приложение, Ресурс, ИдентификаторЗапроса) Экспорт
	
	Подключение = ДанныеПодключения(Приложение);
	Если Подключение = Неопределено тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Подключение.РежимСовместимости Тогда
		РесурсМетода = СтрШаблон("%1/requests/%2", Ресурс, ИдентификаторЗапроса);
	Иначе
		РесурсМетода = СтрШаблон("requests/%1", ИдентификаторЗапроса);
	КонецЕсли;
	
	ОтветСервиса = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, "GET", РесурсМетода);
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат Неопределено;
	ИначеЕсли ОтветСервиса.КодСостояния = ЗапросыREST.КодСтандартногоСостояния() Тогда
		Если Подключение.РежимСовместимости Тогда
			СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("КалендарьОтчетности.ОбращениеКExternalAPI");
		КонецЕсли;
		Возврат ОтветСервиса.Содержимое;
	ИначеЕсли ОтветСервиса.КодСостояния = ЗапросыREST.КодСостоянияОтсутствуетСодержимое() Тогда
		Возврат "";
	КонецЕсли;
	
	ЗаписатьОшибкуКодСостояния(
		ОтветСервиса,
		НСтр("ru = 'Получение данных'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Возврат Неопределено;
	
КонецФункции

// Подтверждает получение данных. Удаляет ответ в приложении, от которого он был получен.
// 
// Параметры:
//   Приложение - СправочникСсылка.ОблачныеПриложения - экземпляр приложения
//   Ресурс - Строка - имя ресурса HTTP-сервиса, обслуживающего прикладной механизм
//   ИдентификаторЗапроса - Строка - идентификатор запроса, полученный методом ЗапроситьДанныеПриложения()
//
Процедура ОтметитьДанныеПолучены(Приложение, Ресурс, ИдентификаторЗапроса) Экспорт
	
	Подключение = ДанныеПодключения(Приложение);
	Если Подключение = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	Если Подключение.РежимСовместимости Тогда
		РесурсМетода = СтрШаблон("%1/requests/%2", Ресурс, ИдентификаторЗапроса);
	Иначе
		РесурсМетода = СтрШаблон("requests/%1", ИдентификаторЗапроса);
	КонецЕсли;
	
	ОтветСервиса = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, "DELETE", РесурсМетода);
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтветСервиса.КодСостояния = ЗапросыREST.КодСтандартногоСостояния() Тогда
		Если Подключение.РежимСовместимости Тогда
			СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("КалендарьОтчетности.ОбращениеКExternalAPI");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ЗаписатьОшибкуКодСостояния(
		ОтветСервиса,
		НСтр("ru = 'Подтверждение получения данных'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик удаления устаревших служебных пользователей,
// в связи с ненадобностью, после перехода на JWT-аутентификацию.
//
// Параметры:
//  ПараметрыОбработчика - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура УдалитьСлужебныхПользователей(ПараметрыОбработчика) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Наименование = "Сообщения облачных приложений";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Наименование = &Наименование
	|	И Пользователи.Служебный
	|	И НЕ Пользователи.ПометкаУдаления";
	
	
	Попытка
		
		КлючХранения = "IntraCloudMessagingCredentials";
		
		СохраненныеДанные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(КлючХранения);
		Если СохраненныеДанные <> Неопределено Тогда
			Если ЗначениеЗаполнено(СохраненныеДанные.Логин) Тогда
				Пользователи.УдалитьПользователяИБ(СохраненныеДанные.Логин);
			КонецЕсли;
			ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(КлючХранения);
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СправочникОбъект.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить обработчик в процедуре Обработки.КалендарьОтчетности.УдалитьСлужебныхПользователей по причине:
			|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.Обработки.КалендарьОтчетности,, ТекстСообщения);
	КонецПопытки;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступенHTTPСервисКалендарьОтчетности(Адрес)
	
	// В качестве проверки используется метод GET.
	// Если приложение готово, то ответ ожидается ответ 200.
	// Любой другой код ответа символизирует о неготовности приложения принимать запросы.
	
	Подключение = ЗапросыREST.НовыйПодключение();
	Подключение.АдресСервиса = Адрес;
	Подключение.ПространствоИмен = ПространствоИмен(Ложь);
	Если Не ДобавитьЗаголовокJWTАутентификации(Подключение) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтветСервиса = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, "GET", "ping");
	
	Если ОтветСервиса = Неопределено 
		Или ОтветСервиса.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ДанныеПодключения(Приложение)
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПриложения = Справочники.ОблачныеПриложения.ДанныеПриложения(Приложение);
	УстановитьПривилегированныйРежим(Ложь);
	
	РежимСовместимости = Не ДоступенHTTPСервисКалендарьОтчетности(ДанныеПриложения.Адрес);
	
	Подключение = ЗапросыREST.НовыйПодключение();
	Подключение.ИсточникЗапроса = Приложение;
	Подключение.АдресСервиса = ДанныеПриложения.Адрес;
	Подключение.ПространствоИмен = ПространствоИмен(РежимСовместимости);
	Подключение.Вставить("РежимСовместимости", РежимСовместимости);
	
	Если Не ДобавитьЗаголовокJWTАутентификации(Подключение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Подключение;
	
КонецФункции

Функция ДобавитьЗаголовокJWTАутентификации(Подключение)
	
	Перем СвойстваОтвета;
	
	ТокенДоступа = ПрограммныйИнтерфейсСервиса.ДействующийТокенДоступа(, Ложь, СвойстваОтвета);
	Если СвойстваОтвета.Ошибка Тогда
		ЗаписьЖурналаРегистрации(
			ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			СвойстваОтвета.Сообщение);
		Возврат Ложь;
	КонецЕсли;
	
	Подключение.ЗаголовкиАутентификации.Вставить("Authorization", СтрШаблон("Bearer %1", ТокенДоступа));
	
	Возврат Истина;
	
КонецФункции

Функция ПространствоИмен(РежимСовместимости = Истина)
	
	Если РежимСовместимости Тогда
		Возврат СтрШаблон("/hs/%1/%2", ИмяСервиса(), ВерсияAPI());
	Иначе
		Возврат СтрШаблон("/hs/%1", ИмяСервиса(Ложь));
	КонецЕсли;
	
КонецФункции

Функция ВерсияAPI()
	
	Возврат "v1";
	
КонецФункции

Функция ИмяСервиса(РежимСовместимости = Истина)
	
	Если РежимСовместимости Тогда
		Возврат "api";
	Иначе
		Возврат "ReportingCycles";
	КонецЕсли;
	
КонецФункции

Процедура ЗаписатьОшибкуКодСостояния(ОтветСервиса, Действие)
	
	ИмяСобытия = ИмяСобытияЖурналаРегистрации(Действие);
	
	// Ответ с кодом 200 может содержать конфиденциальные сведения.
	// Например, токен доступа. Не следует такие сведения записывать в ЖР.
	Комментарий = ?(ОтветСервиса.КодСостояния = ЗапросыREST.КодСтандартногоСостояния(), "", ОтветСервиса.Текст);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Ошибка,
		,
		ОтветСервиса.КодСостояния,
		Комментарий);
	
КонецПроцедуры

Функция ИмяСобытияЖурналаРегистрации(Действие = "")
	
	ЭлементыИмени = Новый Массив;
	ЭлементыИмени.Добавить(НСтр("ru = 'Сообщения облачных приложений'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	Если ЗначениеЗаполнено(Действие) Тогда
		ЭлементыИмени.Добавить(Действие);
	КонецЕсли;
		
	Возврат СтрСоединить(ЭлементыИмени, ".");
	
КонецФункции

Функция КодСостоянияПриложениеУдалено()
	
	Возврат 420;
	
КонецФункции

#КонецОбласти