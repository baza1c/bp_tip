
#Область ПрограммныйИнтерфейс

Процедура ОткрытьРасшифровкуСтатистики(ИмяОтчета, ИмяФормы, ИдентификаторыПолей, Параметры)  Экспорт
	
	Перем Организация, НачалоПериода, КонецПериода, НомерСтроки, АдресВременногоХранилищаРасшифровки;
	
	ИмяПоля = "";
	
	Если ТипЗнч(ИдентификаторыПолей) = Тип("Массив") И ИдентификаторыПолей.Количество() > 0 Тогда
		ИмяПоля = ИдентификаторыПолей[0];
	КонецЕсли;
	
	Параметры.Свойство("Организация",              Организация);
	Параметры.Свойство("мДатаНачалаПериодаОтчета", НачалоПериода);
	Параметры.Свойство("мДатаКонцаПериодаОтчета",  КонецПериода);
	Параметры.Свойство("НомерСтроки",              НомерСтроки);
	Параметры.Свойство("АдресВременногоХранилищаРасшифровки", АдресВременногоХранилищаРасшифровки);
	
	ПараметрыОтчета = Новый Структура();
	
	ПараметрыОтчета.Вставить("Организация",   Организация);
	ПараметрыОтчета.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода",  КонецПериода);
	ПараметрыОтчета.Вставить("ИмяПоля",       ИмяПоля);
	ПараметрыОтчета.Вставить("ИмяФормы",      ИмяФормы);
	ПараметрыОтчета.Вставить("ИмяОтчета",     ИмяОтчета);
	ПараметрыОтчета.Вставить("НомерСтроки",   НомерСтроки);
	ПараметрыОтчета.Вставить("АдресВременногоХранилищаРасшифровки", АдресВременногоХранилищаРасшифровки);
	
	Если ОткрытьФорму("Отчет.РасшифровкаСтатистики.Форма.ФормаОтчета", ПараметрыОтчета,,Истина) = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Для выбранной ячейки расшифровки не существует.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли; 
	
КонецПроцедуры

// см. РегламентированнаяОтчетностьКлиентПереопределяемый.ОбработкаВыбораРаспределитьВыручкуПоОКВЭД
Процедура РаспределитьВыручкуПоОКВЭД(ПараметрыФормы, Форма, ОбработчикЗавершения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Организация",          ПараметрыФормы.Организация);
	ДополнительныеПараметры.Вставить("ИмяОтчета",            ПараметрыФормы.ИДОтчета);
	ДополнительныеПараметры.Вставить("ИмяФормы",             ПараметрыФормы.ИДРедакцииОтчета);
	ДополнительныеПараметры.Вставить("Форма",                Форма);
	ДополнительныеПараметры.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
	
	ОповещениеРаспределениеВыручкиПоОКВЭД = Новый ОписаниеОповещения(
		"ОбработатьРаспределениеВыручкиПоОКВЭД", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Обработка.КлассификацияВыручкиПоОКВЭД.Форма", ПараметрыФормы, Форма,,,,ОповещениеРаспределениеВыручкиПоОКВЭД);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьРаспределениеВыручкиПоОКВЭД(РезультатЗакрытия = Неопределено, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено И РезультатЗакрытия.Свойство("АдресРаспределениеВыручки") Тогда
		ОбработчикЗавершения = ДополнительныеПараметры.ОбработчикЗавершения;
		
		ДополнительныеПараметры.Удалить("Форма");
		ДополнительныеПараметры.Удалить("ОбработчикЗавершения");
		ЗаполнениеФормСтатистикиВызовСервера.ОбработатьРаспределениеВыручкиПоОКВЭД(РезультатЗакрытия.АдресРаспределениеВыручки, ДополнительныеПараметры);
		
		Если ОбработчикЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОбработчикЗавершения, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
