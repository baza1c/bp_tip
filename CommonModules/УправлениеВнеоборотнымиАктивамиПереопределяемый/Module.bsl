#Область ПрограммныйИнтерфейс

#Область ОтражениеАмортизацииОсновныхСредствИНМА

// Процедура ВыполнитьДополнительныеДействияНачислениеАмортизации выполняет 
// отражение начисленной амортизации по ОС и НМА в дополнительных объектах 
// конфигурации. Процедура вызывается из 
// УправлениеВнеоборотнымиАктивами.СформироватьДвиженияНачислениеАмортизации() при 
// проведении документов:
//      - ПередачаНМА
//      - ПередачаОС
//      - ПередачаОС
//      - ПеремещениеОС
//		- ПодготовкаКПередачеОС
//		- ПринятиеКУчетуОС (при включении в расходы НУ стоимости объекта ОС)
//		- РегламентнаяОперация (с видом операции "АмортизацияИИзносОС")
//		- РегламентнаяОперация (с видом операции "АмортизацияНМАИсписаниеРасходовПоНИОКР")
// При добавлении новых регистров необходимо для них назначать в качестве регистраторов указанные 
// документы.
//
// 
// Параметры:
//  ТаблицаЗатрат - ТаблицаЗначений - Колонки таблицы, формируемые по умолчанию, описаны ниже.
//						Дополнительные данные могут быть добавлены в таблицу при переопределении
//						процедуры ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл() этого модуля.
//		ОбъектУчета			- СправочникСсылка.ОсновныеСредства, СправочникСсылка.НематериальныеАктивы;
//		Подразделение 		- Ссылка на справочник подразделений, подразделение счета учета ОС и НМА;
//		ПодразделениеЗатрат	- Ссылка на справочник подразделений, подразделение, на которое
//								относятся затраты по амортизации (ПодразделениеДт);
//		Субконто1			- Значение 1-ого субконто счета затрат (счет Дт);
//		Субконто2			- Значение 2-ого субконто счета затрат (счет Дт);
//		Субконто3			- Значение 3-ого субконто счета затрат (счет Дт);
//		СуммаБУ				- Число, сумма начисленной амортизации по БУ;
//		СчетАмортизации		- ПланСчетовСсылка.Хозрасчетный, счет начисленной амортизации (Счет Кт проводки),
//								например, 02.01;
//		СчетЗатрат			- ПланСчетовСсылка.Хозрасчетный, счет отнесения затрат по амортизации, например, 20.
//		
//  ТаблицаРеквизиты - ТаблицаЗначений - таблица значений с реквизитами из шапки документа, состав граф 
//						таблицы не переопределяется:
//		Организация			- СправочникСсылка.Организации;
//		Период				- Дата проведения;
//		Регистратор			- Ссылка на документ - регистратор.
//
//  Движения - КоллекцияДвижений - коллекция наборов записей регистров.
//  Отказ - Булево - признак отказа от проведения.
//
Процедура ВыполнитьДополнительныеДействияНачислениеАмортизации(ТаблицаЗатрат, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	// РегламентированныйУчетОСиНМА
	
	// Учет доходов и расходов ИП
	ТаблицаНачисленияАмортизацииИП	= УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуНачисленияАмортизацииОС(
		ТаблицаЗатрат,
		ТаблицаРеквизиты, Отказ);
		
	ТаблицаНачисленияАмортизацииНМАИП	= УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуНачисленияАмортизацииНМА(
		ТаблицаЗатрат,
		ТаблицаРеквизиты, Отказ);
		
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаНачисленияАмортизацииНМАИП, ТаблицаНачисленияАмортизацииИП);
		
	ТаблицаРаспределенияАмортизацииИП = ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл(
		ТаблицаНачисленияАмортизацииИП,
		ТаблицаРеквизиты, Отказ);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияНачислениеАмортизации(
		ТаблицаРаспределенияАмортизацииИП,
		ТаблицаРеквизиты, Движения, Отказ);
		
	// Конец РегламентированныйУчетОСиНМА

КонецПроцедуры

#КонецОбласти

#Область РаспределениеАмортизацииПоНаправлениямЗатрат

// Функция ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл проверяется корректность данных
// и возвращает таблицу затрат по амортизации.
//
// Параметры:
//  ТаблицаАмортизации - ТаблицаЗначений - таблица с рассчитанной амортизацией со следующими колонками:
//   * НаправлениеАмортизации - СправочникСсылка.СпособыОтраженияРасходовПоАмортизации
//   * НомерСтроки - Число
//   * ОбъектУчета - СправочникСсылка.ОсновныеСредства
//                 - СправочникСсылка.НематериальныеАктивы
//   * Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//   * СуммаАмортизацииБУ - Число
//   * СуммаАмортизацииВР - Число
//   * СуммаАмортизацииНУ - Число
//   * СуммаАмортизацииПР - Число
//   * СуммаКапитальныхВложенийВключаемыхВРасходы - Число
//   * СчетНачисленияАмортизации - ПланСчетовСсылка.Хозрасчетный
//   * СчетУчета - ПланСчетовСсылка.Хозрасчетный
//  ТаблицаРеквизиты - ТаблицаЗначений - таблица с данными из шапки документа-регистратора:
//   * Организация - СправочникСсылка.Организации
//   * Период - Дата
//   * Регистратор - ДокументОбъект, ДокументСсылка - документ регламентной операции
//  Отказ - Булево
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с данными по распределению амортизации по направлениям.
//  В таблице обязательно должны содержаться колонки, определенные по тексту функции для 
//  переменной ТаблицаЗатрат, возможно добавление дополнительных колонок для последующего 
//  использования в процедуре ВыполнитьДополнительныеДействияНачислениеАмортизации(). 
//
Функция ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл(ТаблицаАмортизации, ТаблицаРеквизиты, Отказ) Экспорт
	
	// Проверим корректность настроек отражения в учете
	Если ЕстьОшибкиЗаполнения(ТаблицаАмортизации, ТаблицаРеквизиты[0]) Тогда
		Отказ = Истина;
		Возврат НовыйТаблицаЗатрат();
	КонецЕсли;
	
	Возврат ТаблицаРаспределенияАмортизацияПоНаправлениям(ТаблицаАмортизации);

КонецФункции

// Проверяет заполненность и корректность аналитик в способах отражения расходов по амортизации
//
// Параметры:
//  Ошибки - см. ВыводСообщенийОбОшибках.НовыйДетальнаяИнформацияОбОшибках
//  ТаблицаАмортизации - ТаблицаЗначений - таблица с рассчитанной амортизацией со следующими колонками:
//   * НаправлениеАмортизации - СправочникСсылка.СпособыОтраженияРасходовПоАмортизации
//   * НомерСтроки - Число
//   * ОбъектУчета - СправочникСсылка.ОсновныеСредства
//                 - СправочникСсылка.НематериальныеАктивы
//   * Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//   * СуммаАмортизацииБУ - Число
//   * СуммаАмортизацииВР - Число
//   * СуммаАмортизацииНУ - Число
//   * СуммаАмортизацииПР - Число
//   * СуммаКапитальныхВложенийВключаемыхВРасходы - Число
//   * СчетНачисленияАмортизации - ПланСчетовСсылка.Хозрасчетный
//   * СчетУчета - ПланСчетовСсылка.Хозрасчетный
//  Реквизиты - ТаблицаЗначений - таблица с данными из шапки документа-регистратора:
//   * Организация - СправочникСсылка.Организации
//   * Период - Дата
//   * Регистратор - ДокументОбъект, ДокументСсылка - документ регламентной операции
//  Отказ - Булево - возвращаемый параметр
//
Процедура ПроверитьКорректностьДанныхРегламентнойОперации(Ошибки, ТаблицаАмортизации, Реквизиты, Отказ) Экспорт
	
	Справочники.СпособыОтраженияРасходовПоАмортизации.ДобавитьОшибкиКорректностиСпособовОтражения(
		Ошибки,
		ТаблицаАмортизации.ВыгрузитьКолонку("НаправлениеАмортизации"),
		Отказ);
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьОшибкиЗаполненностьНаправленийАмортизации(Ошибки, ТаблицаАмортизации, Отказ);
	
КонецПроцедуры

// Функция ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл возвращает таблицу затрат по амортизации.
//
// Параметры:
//  ТаблицаАмортизации - ТаблицаЗначений - таблица с рассчитанной амортизацией со следующими колонками:
//   * НаправлениеАмортизации - СправочникСсылка.СпособыОтраженияРасходовПоАмортизации
//   * НомерСтроки - Число
//   * ОбъектУчета - СправочникСсылка.ОсновныеСредства
//                 - СправочникСсылка.НематериальныеАктивы
//   * Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//   * СуммаАмортизацииБУ - Число
//   * СуммаАмортизацииВР - Число
//   * СуммаАмортизацииНУ - Число
//   * СуммаАмортизацииПР - Число
//   * СуммаКапитальныхВложенийВключаемыхВРасходы - Число
//   * СчетНачисленияАмортизации - ПланСчетовСсылка.Хозрасчетный
//   * СчетУчета - ПланСчетовСсылка.Хозрасчетный
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с данными по распределению амортизации по направлениям.
//  В таблице обязательно должны содержаться колонки, определенные по тексту функции для 
//  переменной ТаблицаЗатрат, возможно добавление дополнительных колонок для последующего 
//  использования в процедуре ВыполнитьДополнительныеДействияНачислениеАмортизации(). 
//
Функция ТаблицаРаспределенияАмортизацияПоНаправлениям(ТаблицаАмортизации) Экспорт
	
	ДобавитьНеобязательныеКолонки(ТаблицаАмортизации);
	
	Параметры = ПодготовитьПараметрыРаспределениеАмортизацииПоНаправлениямРегл(ТаблицаАмортизации);
	
	ТаблицаЗатрат = НовыйТаблицаЗатрат();
	
	// РегламентированныйУчетОСиНМА
	
	ТаблицаСпособов = Параметры.ТаблицаАмортизации.Скопировать(, "НаправлениеАмортизации");
	ТаблицаСпособов.Свернуть("НаправлениеАмортизации");
	
	// Заранее считаем информацию о способах отражения расходов
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпособыОтраженияРасходов.НаправлениеАмортизации КАК Способы
	|ПОМЕСТИТЬ СпособыОтраженияРасходов
	|ИЗ
	|	&Способы КАК СпособыОтраженияРасходов
	|ГДЕ
	|	СпособыОтраженияРасходов.НаправлениеАмортизации <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияРасходовПоАмортизации.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Способы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпособыОтраженияРасходов.Способы КАК Ссылка,
	|	ЕСТЬNULL(СпособыОтражения.НомерСтроки, 0) КАК НомерСтроки,
	|	ЕСТЬNULL(СпособыОтражения.СчетЗатрат, НазначенияИспользования.Счет) КАК СчетЗатрат,
	|	ЕСТЬNULL(СпособыОтражения.ПодразделениеОрганизации, НазначенияИспользования.Подразделение) КАК Подразделение,
	|	ЕСТЬNULL(СпособыОтражения.Субконто1, НазначенияИспользования.Субконто1) КАК Субконто1,
	|	ЕСТЬNULL(СпособыОтражения.Субконто2, НазначенияИспользования.Субконто2) КАК Субконто2,
	|	ЕСТЬNULL(СпособыОтражения.Субконто3, НазначенияИспользования.Субконто3) КАК Субконто3,
	|	ЕСТЬNULL(СпособыОтражения.Коэффициент, 1) КАК Коэффициент
	|ИЗ
	|	СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК СпособыОтражения
	|		ПО СпособыОтраженияРасходов.Способы = СпособыОтражения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НазначенияИспользованияНМА КАК НазначенияИспользования
	|		ПО СпособыОтраженияРасходов.Способы = НазначенияИспользования.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("Способы", ТаблицаСпособов);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ПараметрыСпособов = Новый Соответствие;
	
	ВыборкаСпособ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСпособ.Следующий() Цикл
		
		ПараметрыСпособа = Новый Структура;
		
		Коэффициенты = Новый Массив;
		
		ПараметрыСпособа.Вставить("Коэффициенты", Коэффициенты);
		
		ТаблицаНаправлений = НовыйТаблицаНаправлений();
				
		ПараметрыСпособа.Вставить("Направления", ТаблицаНаправлений);
		
		ПараметрыСпособов.Вставить(ВыборкаСпособ.Ссылка, ПараметрыСпособа);
		
		Выборка = ВыборкаСпособ.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СтрокаНаправления = ТаблицаНаправлений.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНаправления, Выборка);
			
			Если Выборка.Коэффициент > 0 Тогда
				Коэффициенты.Добавить(Выборка.Коэффициент);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	КешПересчетАмортизации = Неопределено;
	
	// цикл по Объектам из ТаблицаАмортизации
	Для Каждого СтрокаТЗ Из ТаблицаАмортизации Цикл
		
		Если Не СуммыЗаполнены(СтрокаТЗ) Тогда
			// Распределять нечего
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.НаправлениеАмортизации) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыСпособа = ПараметрыСпособов[СтрокаТЗ.НаправлениеАмортизации];
		Коэффициенты = ПараметрыСпособа.Коэффициенты;
		Направления = ПараметрыСпособа.Направления;
		
		Если Направления.Количество() = 1 Тогда  // амортизация идет на один счет.
			
			СтрокаНапр = Направления[0];
			
			НоваяСтрока = ТаблицаЗатрат.Добавить();
			
			НоваяСтрока.ОбъектУчета              = СтрокаТЗ.ОбъектУчета;
			НоваяСтрока.Ремонт                   = СтрокаТЗ.Ремонт;
			
			НоваяСтрока.СчетЗатрат               = СтрокаНапр.СчетЗатрат;
			НоваяСтрока.ПодразделениеЗатрат      = СтрокаНапр.Подразделение;
			НоваяСтрока.Субконто1                = СтрокаНапр.Субконто1;
			НоваяСтрока.Субконто2                = СтрокаНапр.Субконто2;
			НоваяСтрока.Субконто3                = СтрокаНапр.Субконто3;
			
			АмортизацияАктивовПересчет.УстановитьАналитикуЗатрат(НоваяСтрока, СтрокаТЗ, КешПересчетАмортизации);
			
			НоваяСтрока.СчетАмортизации          = СтрокаТЗ.СчетНачисленияАмортизации;
			НоваяСтрока.СуммаБУ                  = СтрокаТЗ.СуммаАмортизацииБУ;
			НоваяСтрока.Подразделение = СтрокаТЗ.Подразделение;
			НоваяСтрока.СуммаНУ                  = СтрокаТЗ.СуммаАмортизацииНУ;
			НоваяСтрока.СуммаВР                  = СтрокаТЗ.СуммаАмортизацииВР;
			НоваяСтрока.СуммаПР                  = СтрокаТЗ.СуммаАмортизацииПР;
			НоваяСтрока.КорректировкаОбесценения = СтрокаТЗ.КорректировкаОбесценения;
			НоваяСтрока.РасчетныйДокумент        = СтрокаТЗ.РасчетныйДокумент;
			НоваяСтрока.ДокументОплаты           = СтрокаТЗ.ДокументОплаты;
			НоваяСтрока.СпособПоступления        = СтрокаТЗ.СпособПоступления;
			НоваяСтрока.Контрагент			     = СтрокаТЗ.Контрагент;
			НоваяСтрока.ДоговорКонтрагента	     = СтрокаТЗ.ДоговорКонтрагента;
			НоваяСтрока.СодержаниеОперации	     = СтрокаТЗ.СодержаниеОперации;
			
		Иначе // амортизация распределяется по весовым коэффициентам.
			
			МассивСумм = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(СтрокаТЗ.СуммаАмортизацииБУ, Коэффициенты, 2);
			МассивСуммНУ = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(СтрокаТЗ.СуммаАмортизацииНУ, Коэффициенты, 2);
			Если СтрокаТЗ.СуммаАмортизацииВР <> 0 Тогда
				МассивСуммПР = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(СтрокаТЗ.СуммаАмортизацииПР, Коэффициенты, 2);
			КонецЕсли;
			МассивКоррОбесценения = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(СтрокаТЗ.КорректировкаОбесценения, Коэффициенты, 2);
			
			Для каждого СтрокаНапр Из Направления Цикл
				
				НоваяСтрока = ТаблицаЗатрат.Добавить();
				
				НоваяСтрока.ОбъектУчета              = СтрокаТЗ.ОбъектУчета;
				НоваяСтрока.Ремонт                   = СтрокаТЗ.Ремонт;
				
				НоваяСтрока.СчетЗатрат               = СтрокаНапр.СчетЗатрат;
				НоваяСтрока.ПодразделениеЗатрат      = СтрокаНапр.Подразделение;
				НоваяСтрока.Субконто1                = СтрокаНапр.Субконто1;
				НоваяСтрока.Субконто2                = СтрокаНапр.Субконто2;
				НоваяСтрока.Субконто3                = СтрокаНапр.Субконто3;
				
				АмортизацияАктивовПересчет.УстановитьАналитикуЗатрат(НоваяСтрока, СтрокаТЗ, КешПересчетАмортизации);
				
				НоваяСтрока.СчетАмортизации          = СтрокаТЗ.СчетНачисленияАмортизации;
				НоваяСтрока.СуммаБУ                  = ?(ЗначениеЗаполнено(МассивСумм), МассивСумм[СтрокаНапр.НомерСтроки - 1], 0);
				НоваяСтрока.Подразделение = СтрокаТЗ.Подразделение;
				НоваяСтрока.СуммаНУ                  = ?(ЗначениеЗаполнено(МассивСуммНУ), МассивСуммНУ[СтрокаНапр.НомерСтроки - 1], 0);
				НоваяСтрока.РасчетныйДокумент        = СтрокаТЗ.РасчетныйДокумент;
				НоваяСтрока.ДокументОплаты           = СтрокаТЗ.ДокументОплаты;
				НоваяСтрока.СпособПоступления        = СтрокаТЗ.СпособПоступления;
				НоваяСтрока.Контрагент			     = СтрокаТЗ.Контрагент;
				НоваяСтрока.ДоговорКонтрагента	     = СтрокаТЗ.ДоговорКонтрагента;
				НоваяСтрока.СодержаниеОперации	     = СтрокаТЗ.СодержаниеОперации;
				
				НоваяСтрока.КорректировкаОбесценения = ?(ЗначениеЗаполнено(МассивКоррОбесценения), МассивКоррОбесценения[СтрокаНапр.НомерСтроки - 1], 0);
				
				Если СтрокаТЗ.СуммаАмортизацииВР = 0 Тогда
					Если СтрокаТЗ.СуммаАмортизацииПР <> 0 Тогда
						НоваяСтрока.СуммаПР = НоваяСтрока.СуммаБУ - НоваяСтрока.СуммаНУ;
					Иначе
						НоваяСтрока.СуммаПР = 0;
					КонецЕсли;
					
					НоваяСтрока.СуммаВР = 0;
				Иначе
					НоваяСтрока.СуммаПР = ?(ЗначениеЗаполнено(МассивСуммПР), МассивСуммПР[СтрокаНапр.НомерСтроки - 1], 0);
					НоваяСтрока.СуммаВР = НоваяСтрока.СуммаБУ - НоваяСтрока.СуммаНУ - НоваяСтрока.СуммаПР;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Конец РегламентированныйУчетОСиНМА
	Возврат ТаблицаЗатрат;

КонецФункции

// Конструктор таблицы затрат
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. тело функции
//
Функция НовыйТаблицаЗатрат() Экспорт
	
	// определим структуру выходной таблицы
	ТаблицаЗатрат = Новый ТаблицаЗначений;
	ТаблицаЗатрат.Колонки.Добавить("СуммаБУ",				ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаЗатрат.Колонки.Добавить("СуммаНУ",				ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаЗатрат.Колонки.Добавить("СуммаВР",				ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаЗатрат.Колонки.Добавить("СуммаПР",				ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаЗатрат.Колонки.Добавить("СуммаМЦПР",				ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаЗатрат.Колонки.Добавить("СуммаМЦВР",				ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаЗатрат.Колонки.Добавить("КорректировкаОбесценения", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаЗатрат.Колонки.Добавить("СчетЗатрат",			Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаЗатрат.Колонки.Добавить("СчетАмортизации",		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаЗатрат.Колонки.Добавить("ОбъектУчета",			Справочники.ТипВсеСсылки());
	ТаблицаЗатрат.Колонки.Добавить("Ремонт",                Метаданные.ОпределяемыеТипы.ДокументыРемонтаОС.Тип);
	ТаблицаЗатрат.Колонки.Добавить("Субконто1",				Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип);
	ТаблицаЗатрат.Колонки.Добавить("Субконто2",				Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип);
	ТаблицаЗатрат.Колонки.Добавить("Субконто3",				Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип);
	ТаблицаЗатрат.Колонки.Добавить("РасчетныйДокумент",		Документы.ТипВсеСсылки());
	ТаблицаЗатрат.Колонки.Добавить("ДокументОплаты",		Документы.ТипВсеСсылки());
	ТаблицаЗатрат.Колонки.Добавить("Подразделение",			БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	ТаблицаЗатрат.Колонки.Добавить("ПодразделениеЗатрат",	БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	ТаблицаЗатрат.Колонки.Добавить("СпособПоступления",		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыПоступленияАктивов"));
	ТаблицаЗатрат.Колонки.Добавить("Контрагент",			Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаЗатрат.Колонки.Добавить("ДоговорКонтрагента",	Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ТаблицаЗатрат.Колонки.Добавить("СодержаниеОперации",	Новый ОписаниеТипов("Строка"));
	Возврат ТаблицаЗатрат;

КонецФункции

#КонецОбласти

#Область АмортизационнаяПремия

// Процедура ВыполнитьДополнительныеДействияНачислениеАмортизационнойПремии
// вызывается из УчетОС.СформироватьДвиженияНачислениеАмортизационнойПремии() 
// после формирования проводок в регламентированном учете для отражения
// затрат НУ, связанных с амортизационной премией, в дополнительных регистрах.
// Получает те же параметры, что и УчетОС.СформироватьДвиженияНачислениеАмортизационнойПремии().
// 
// 
// Параметры:
//  ТаблицаНачисленияАмортизационнойПремии - ТаблицаЗначений - таблица значений с графами:
//		ДокументАмортизационнойПремии 	- документ, которым была отражена амортизационная премия (Дт КВ)
//		ОсновноеСредство				- объект ОС
//		Подразделение					- подразделение, на котором отражена амортизационная премия на счете КВ
//		ПодразделениеПоАмортизационнойПремии - подразделение, по которому отражены расходы по амортизационной премии (Счет Дт)
//		СубконтоПоАмортизационнойПремии1 - значение 1-го субконто счета Дт
//		СубконтоПоАмортизационнойПремии2 - значение 2-го субконто счета Дт
//		СубконтоПоАмортизационнойПремии3 - значение 3-го субконто счета Дт
//		СуммаВР
//		СуммаНУ
//		СуммаПР
//		СчетУчета						- ПланСчетовСсылка.Хозрасчетный - счет учета 
//		СчетУчетаЗатратПоАмортизационнойПремии - ПланСчетовСсылка.Хозрасчетный - счет отражения затрат по аморт.премии.
//
//	ТаблицаРеквизиты - ТаблицаЗначений - таблица значений с графами:
//		* Организация	- СправочникСсылка.Организации
//		* Период - Дата
//		* Регистратор - ДокументСсылка - Документ-регистратор
//
//  Движения - КоллекцияДвижений - коллекция наборов записей регистров.
//  Отказ - Булево - признак отказа от проведения.
//
Процедура ВыполнитьДополнительныеДействияНачислениеАмортизационнойПремии(ТаблицаНачисленияАмортизационнойПремии, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	// РегламентированныйУчетОСиНМА
	

	// Конец РегламентированныйУчетОСиНМА
	
КонецПроцедуры

#Область ВыбытиеОсновныхСредствИНМА

// Процедура ВыполнитьДополнительныеДействияСписаниеОС вызывается из 
// УчетОС.СформироватьДвиженияСписаниеОстаточнойСтоимостиОС() после определения
// остаточной стоимости объекта ОС для возможности отражения дополнительных движений
// при проведении документов:
//		- ПередачаОС
//		- СписаниеОС
// При добавлении новых регистров необходимо для них назначать в качестве регистраторов 
// указанные документы.
// 
// 
// Параметры:
//  ТаблицаСтоимости - таблица значений с графами:
//		ОсновноеСредство 	- СправочникСсылка.ОсновныеСредства
//		СтоимостьБУ			- Число, остаточная стоимость объект ОС по БУ
//		Субконто			- Значение субконто СчетаСписания
//		СчетСписания		- Счет, на который списан объект ОС 
//
//	ТаблицаРеквизиты - таблица значений с графами:
//		Номер				- Строка, номер документа-регистратора
//		Организация			- Справочник.Организации
//		Период				- Дата документа - регистратора
//		Подразделение		- подразделение, где числится ОС
//		Регистратор			- Документ - регистратор
//
Процедура ВыполнитьДополнительныеДействияСписаниеОС(ТаблицаСтоимости, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	// РегламентированныйУчетОСиНМА
	

	// Конец РегламентированныйУчетОСиНМА

КонецПроцедуры

// Процедура ВыполнитьДополнительныеДействияСписаниеНМА вызывается из 
// УчетНМА.СформироватьДвиженияВыбытиеНМА() после определения
// остаточной стоимости объекта НМА для возможности отражения дополнительных движений
// при проведении документов:
//		- ПередачаНМА
//		- СписаниеНМА
// При добавлении новых регистров необходимо для них назначать в качестве регистраторов 
// указанные документы.
// 
// 
// Параметры:
//  ТаблицаПараметрыСписания - таблица значений с графами:
//		АмортизацияНМАБУ 	- Число, накопленная амортизация по объекту
//		НематериальныйАктив	- Справочник.НематериальныеАктивы, списываемый объект
//		СтоимостьНМАБУ		- Число, первоначальная стоимость объекта НМА.
//								Разница (СтоимостьНМАБУ - АмортизацияНМАБУ) дает остаточную
//								стоимость, которая списывается на расходы по БУ.
//		СчетНачисленияАмортизации - ПланСчетов.Хозрасчетный, счет учета амортизации (например, 05)
//		СчетУчета			- ПланСчетов.Хозрасчетный, счет учета объекта НМА (например, 04)
//
//	ТаблицаРеквизиты - таблица значений с графами:
//		Организация			- Справочник.Организации
//		Период				- Дата документа - регистратора
//		Подразделение		- подразделение, в котором числился объект НМА и на расходы 
//								которого списывается его остаточная стоимость
//		Регистратор			- Документ - регистратор
//		СубконтоСписания	- Значение субконто счета списания
//		СчетСписания		- ПланСчетов.Хозрасчетный, счет списания (например, 91.02)
//
Процедура ВыполнитьДополнительныеДействияСписаниеНМА(ТаблицаПараметрыСписания, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	// РегламентированныйУчетОСиНМА
	

	// Конец РегламентированныйУчетОСиНМА

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет в дерево ошибок строки с объектами учета, в которых не заполнены способы отражения расходов по амортизации.
//
// Параметры:
//  Ошибки - см. ВыводСообщенийОбОшибках.НовыйДетальнаяИнформацияОбОшибках
//  ТаблицаАмортизации - ТаблицаЗначений - таблица с рассчитанной амортизацией со следующими колонками:
//   * НаправлениеАмортизации - СправочникСсылка.СпособыОтраженияРасходовПоАмортизации
//   * НомерСтроки - Число
//   * ОбъектУчета - СправочникСсылка.ОсновныеСредства
//                 - СправочникСсылка.НематериальныеАктивы
//   * Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//   * СуммаАмортизацииБУ - Число
//   * СуммаАмортизацииВР - Число
//   * СуммаАмортизацииНУ - Число
//   * СуммаАмортизацииПР - Число
//   * СуммаКапитальныхВложенийВключаемыхВРасходы - Число
//   * СчетНачисленияАмортизации - ПланСчетовСсылка.Хозрасчетный
//   * СчетУчета - ПланСчетовСсылка.Хозрасчетный
// Отказ - Булево - возвращаемый параметр
//
Процедура ДобавитьОшибкиЗаполненностьНаправленийАмортизации(Ошибки, ТаблицаАмортизации, Отказ)
	
	Для Каждого СтрокаТЗ Из ТаблицаАмортизации Цикл
		
		Если Не СуммыЗаполнены(СтрокаТЗ) Тогда
			// Распределять нечего
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.НаправлениеАмортизации) Тогда
			
			ШаблонОписания = НСтр("ru = 'Не указаны способы отражения расходов по амортизации для объекта ""%1"".'");
			ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписания, СтрокаТЗ.ОбъектУчета);
			
			ТекстРекомендации = НСтр("ru = 'Укажите эти данные в документах принятия к учету и изменения параметров начисления амортизации.'");
			
			Ошибка = ВыводСообщенийОбОшибках.ДобавитьПростоеОписаниеОшибки(Ошибки);
			Ошибка.Описание = ТекстОписания;
			Ошибка.Рекомендация = ТекстРекомендации;
			Ошибка.Ссылка = СтрокаТЗ.ОбъектУчета;
			Отказ = Истина;
			
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьЗаполненностьНаправленийАмортизации(ТаблицаАмортизации, Реквизиты, ЕстьОшибки)
	
	Для Каждого СтрокаТЗ Из ТаблицаАмортизации Цикл
		
		Если Не СуммыЗаполнены(СтрокаТЗ) Тогда
			// Распределять нечего
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.НаправлениеАмортизации) Тогда
			
			ШаблонОписания = НСтр("ru = 'Не указаны способы отражения расходов по амортизации для объекта ""%1"".'");
			ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписания, СтрокаТЗ.ОбъектУчета);
			
			ТекстРекомендации = НСтр("ru = 'Укажите эти данные в документах принятия к учету и изменения параметров начисления амортизации.'");
			
			ТекстыОшибок = Новый Массив;
			ТекстыОшибок.Добавить(ТекстОписания);
			ТекстыОшибок.Добавить(ТекстРекомендации);
			
			ТекстОшибки = СтрСоединить(ТекстыОшибок, Символы.ПС);
			
			Если Не ПустаяСтрока(Реквизиты.ИмяСписка) Тогда
				ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки,
					Реквизиты.Регистратор,
					Реквизиты.ИмяСписка + "["+ Формат(СтрокаТЗ.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].ОсновноеСредство",
					"Объект",
					ЕстьОшибки);
			Иначе
				
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, СтрокаТЗ.ОбъектУчета, , , ЕстьОшибки);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция СуммыЗаполнены(СтрокаТЗ)
	
	Возврат СтрокаТЗ.СуммаАмортизацииБУ <> 0
		Или СтрокаТЗ.СуммаАмортизацииНУ <> 0
		Или СтрокаТЗ.СуммаАмортизацииПР <> 0
		Или СтрокаТЗ.СуммаАмортизацииВР <> 0
		Или ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТЗ, "КорректировкаОбесценения")
			И СтрокаТЗ.КорректировкаОбесценения <> 0;

КонецФункции

Функция ЕстьОшибкиЗаполнения(ТаблицаАмортизации, Реквизиты)
	
	ЕстьОшибки = Ложь;
	ТаблицаСпособов = ТаблицаАмортизации.ВыгрузитьКолонку("НаправлениеАмортизации");
	
	Справочники.СпособыОтраженияРасходовПоАмортизации.ПроверитьПриВыполненииРегламентнойОперации(
		Реквизиты.Регистратор,
		ЕстьОшибки,
		ТаблицаСпособов);
		
	Если ЕстьОшибки Тогда
		Возврат ЕстьОшибки;
	КонецЕсли;
	
	ПроверитьЗаполненностьНаправленийАмортизации(ТаблицаАмортизации, Реквизиты, ЕстьОшибки);
	
	Возврат ЕстьОшибки;
	
КонецФункции

Процедура ДобавитьНеобязательныеКолонки(ТаблицаАмортизации)
	
	Если Не ТаблицаАмортизации = Неопределено Тогда
		Если ТаблицаАмортизации.Колонки.Найти("РасчетныйДокумент") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("РасчетныйДокумент", Документы.ТипВсеСсылки());
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("ДокументОплаты") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("ДокументОплаты", Документы.ТипВсеСсылки());
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("СпособПоступления") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("СпособПоступления", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыПоступленияАктивов"));
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("Контрагент") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("ДоговорКонтрагента", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("КорректировкаОбесценения") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("КорректировкаОбесценения", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("СодержаниеОперации") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("СодержаниеОперации", Новый ОписаниеТипов("Строка"));
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("КорректируемыйПериод") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("КорректируемыйПериод", Новый ОписаниеТипов("Дата"));
		КонецЕсли;
		Если ТаблицаАмортизации.Колонки.Найти("Ремонт") = Неопределено Тогда
			ТаблицаАмортизации.Колонки.Добавить("Ремонт", Метаданные.ОпределяемыеТипы.ДокументыРемонтаОС.Тип);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция НовыйТаблицаНаправлений()
	
	ТаблицаНаправлений = Новый ТаблицаЗначений;
	ТаблицаНаправлений.Колонки.Добавить("НомерСтроки");
	ТаблицаНаправлений.Колонки.Добавить("СчетЗатрат");
	ТаблицаНаправлений.Колонки.Добавить("Подразделение");
	ТаблицаНаправлений.Колонки.Добавить("Субконто1");
	ТаблицаНаправлений.Колонки.Добавить("Субконто2");
	ТаблицаНаправлений.Колонки.Добавить("Субконто3");
	ТаблицаНаправлений.Колонки.Добавить("Коэффициент", ОбщегоНазначения.ОписаниеТипаЧисло(6, 3));
	
	Возврат ТаблицаНаправлений;

КонецФункции

Функция ПодготовитьПараметрыРаспределениеАмортизацииПоНаправлениямРегл(ТаблицаАмортизации)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаАмортизации

	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"               // <Число, 5, 0>
	+ "НаправлениеАмортизации,"    // <СправочникСсылка.СпособыОтраженияРасходовПоАмортизации,
		// СправочникСсылка.НазначенияИспользованияНМА> - способ отражения расходов по амортизации ОС
	+ "ОбъектУчета,"               // <СправочникСсылка.ОсновныеСредства>
	+ "Подразделение,"             // <Ссылка на справочник подразделений>
	+ "СуммаАмортизацииБУ,"        // <Число, 15, 2> - сумма амортизации по данным бухгалтерского учета
	+ "СуммаАмортизацииНУ,"        // <Число, 15, 2> - сумма амортизации по данным налогового учета
	+ "СуммаАмортизацииПР,"        // <Число, 15, 2> - постоянные разницы в оценке амортизации (ПБУ 18/02)
	+ "СуммаАмортизацииВР,"        // <Число, 15, 2> - временные разницы в оценке амортизации (ПБУ 18/02)
	+ "КорректировкаОбесценения,"  // <Число, 15, 2> - корректировка обесценения
	+ "СчетНачисленияАмортизации," // <ПланСчетовСсылка.Хозрасчетный> - счет начисления амортизации ОС
	+ "РасчетныйДокумент,"         // <ДокументСсылка.*>
	+ "ДокументОплаты,"            // <ДокументСсылка.*>
	+ "СпособПоступления,"         // <ПеречислениеСсылка.СпособыПоступленияАктивов>
	+ "Ремонт,"                    // <Метаданные.ОпределяемыеТипы.ДокументыРемонтаОС.Тип>
	+ "Контрагент,"        	   	   // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента";        // <СправочникСсылка.ДоговорыКонтрагентов>
	
	Параметры.Вставить("ТаблицаАмортизации",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаАмортизации, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

#КонецОбласти