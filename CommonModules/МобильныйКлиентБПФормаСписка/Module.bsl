#Область СлужебныйПрограммныйИнтерфейс

Процедура НастроитьФормуОбъекта(Форма, ИспользоватьМеханизмВажности = Ложь) Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМК = Элементы.Добавить("ГруппаМК", Тип("ГруппаФормы"), Форма);
	ГруппаМК.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМК.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМК.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМК.ОтображатьЗаголовок = Ложь;
	
	ГруппаМККоманднаяПанель = Элементы.Добавить("ГруппаМККоманднаяПанель", Тип("ГруппаФормы"), ГруппаМК);
	ГруппаМККоманднаяПанель.Вид = ВидГруппыФормы.КоманднаяПанель;
	ГруппаМККоманднаяПанель.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	ГруппаМККомандыФормы = Элементы.Добавить("ГруппаМККомандыФормы", Тип("ГруппаФормы"), ГруппаМККоманднаяПанель);
	ГруппаМККомандыФормы.Вид = ВидГруппыФормы.ГруппаКнопок;
	ГруппаМККомандыФормы.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	ГруппаМКВерхняяПанель = Элементы.Добавить("ГруппаМКВерхняяПанель", Тип("ГруппаФормы"), ГруппаМК);
	ГруппаМКВерхняяПанель.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКВерхняяПанель.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаМКВерхняяПанель.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКВерхняяПанель.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКВерхняяПанель.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКОтборОрганизация = Элементы.Добавить("ГруппаМКОтборОрганизация", Тип("ГруппаФормы"), ГруппаМКВерхняяПанель);
	ГруппаМКОтборОрганизация.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКОтборОрганизация.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКОтборОрганизация.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКОтборОрганизация.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКОтборОрганизация.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКВерхняяПанельКнопки = Элементы.Добавить("ГруппаМКВерхняяПанельКнопки", Тип("ГруппаФормы"), ГруппаМКВерхняяПанель);
	ГруппаМКВерхняяПанельКнопки.Вид = ВидГруппыФормы.КоманднаяПанель;
	ГруппаМКВерхняяПанельКнопки.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКВерхняяПанельКнопки.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	ГруппаМКФормаПрочее = Элементы.Добавить("ГруппаМКФормаПрочее", Тип("ГруппаФормы"), ГруппаМК);
	ГруппаМКФормаПрочее.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаПрочее.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаПрочее.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	ГруппаМКФормаПрочее.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФормаПрочее.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКОтборы = Элементы.Добавить("ГруппаМКОтборы", Тип("ГруппаФормы"), ГруппаМК);
	ГруппаМКОтборы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКОтборы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКОтборы.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКОтборы.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКОтборы.ОтображатьЗаголовок = Ложь;
	ГруппаМКОтборы.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	ГруппаМКБыстрыеОтборы = Элементы.Добавить("ГруппаМКБыстрыеОтборы", Тип("ГруппаФормы"), ГруппаМКОтборы);
	ГруппаМКБыстрыеОтборы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКБыстрыеОтборы.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	ГруппаМКБыстрыеОтборы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКБыстрыеОтборы.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКБыстрыеОтборы.Видимость = Ложь;
	ГруппаМКБыстрыеОтборы.Скрыть();
	ГруппаМКБыстрыеОтборы.Заголовок = НСтр("ru = 'Отборы'");
	
	ГруппаМКСписок = Элементы.Добавить("ГруппаМКШапка", Тип("ГруппаФормы"), ГруппаМК);
	ГруппаМКСписок.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКСписок.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКСписок.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКСписок.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКСписок.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКСписок = Элементы.Добавить("ГруппаМКСписок", Тип("ГруппаФормы"), ГруппаМК);
	ГруппаМКСписок.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКСписок.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКСписок.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКСписок.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКСписок.ОтображатьЗаголовок = Ложь;

	ГруппаМКСкрыто = Элементы.Добавить("ГруппаМКСкрыто", Тип("ГруппаФормы"), ГруппаМК);
	ГруппаМКСкрыто.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКСкрыто.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКСкрыто.Видимость = Ложь;
	
	Если ИспользоватьМеханизмВажности Тогда
		Форма.СворачиваниеЭлементовПоВажности =
			СворачиваниеЭлементовФормыПоВажности.Использовать;
	Иначе
		Форма.СворачиваниеЭлементовПоВажности =
			СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	КонецЕсли;
	
	ЭлементыДоступныеВМобильномКлиенте = Новый Массив;
	ЭлементыДоступныеВМобильномКлиенте.Добавить("ГруппаМК");
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы,
		Форма.ПодчиненныеЭлементы,
		Элементы.ГруппаМКСкрыто,
		ЭлементыДоступныеВМобильномКлиенте);
	
КонецПроцедуры

Процедура АдаптироватьМККоманднаяПанельФормы(Форма, КомандыПоля) Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМККоманднаяПанель = Элементы.Найти("ГруппаМККоманднаяПанель");
	Форма.СоставКоманднойПанелиНаМобильномУстройстве.Добавить(ГруппаМККоманднаяПанель);
	
	ГруппаМКВерхняяПанельКнопки = Элементы.Найти("ГруппаМКВерхняяПанельКнопки");
	Если ПустаяСтрока(КомандыПоля.КомандаСоздать) И КомандыПоля.РазрешеноИзменение Тогда
		ГруппаМКСоздать = Элементы.Добавить("ГруппаМКСоздать", Тип("ГруппаФормы"), ГруппаМКВерхняяПанельКнопки);
		ГруппаМКСоздать.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		МобильныйКлиентБПФормаОбъекта.ДобавитьКнопкуКоманднойПанели(Форма,
			"Создать",
			"Создать",
			БиблиотекаКартинок.СоздатьЭлементСписка,
			ГруппаМКСоздать);
	Иначе
		//Для команд форм - управление доступностью - платформенное
		СписокКомандСоздать = СтрРазделить(КомандыПоля.КомандаСоздать,",", Ложь);
		Для Каждого ЭлементКомандаСоздать Из СписокКомандСоздать Цикл
			КомандаСоздать = Элементы.Найти(ЭлементКомандаСоздать);
			Если КомандаСоздать <> Неопределено Тогда
				КомандаСоздать.Картинка = БиблиотекаКартинок.ДобавитьЭлементСписка;
				КомандаСоздать.Отображение = ОтображениеКнопки.КартинкаИТекст;
				Если ТипЗнч(КомандаСоздать) = Тип("КнопкаФормы") Тогда
					КомандаСоздать.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
				КонецЕсли;
				КомандаСоздать.Видимость = Истина;
				Элементы.Переместить(КомандаСоздать, ГруппаМКВерхняяПанельКнопки);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ГруппаМККомандыФормы = Элементы.Найти("ГруппаМККомандыФормы");
	
	ГруппаМКПерваяКоманда = Элементы.Добавить("ГруппаМКПерваяКоманда", Тип("ГруппаФормы"), ГруппаМККомандыФормы);
	ГруппаМКПерваяКоманда.Вид = ВидГруппыФормы.ГруппаКнопок;
	
	Если КомандыПоля.ДоступноУправлениеОтбором Тогда
		МобильныйКлиентБПФормаОбъекта.ДобавитьКнопкуКоманднойПанели(Форма,
			"ПоказатьСкрытьОтбор",
			"",
			БиблиотекаКартинок.ФильтрМК,
			ГруппаМКПерваяКоманда);
	Иначе
		МобильныйКлиентБПФормаОбъекта.ДобавитьКнопкуКоманднойПанели(Форма,
			"КомандыЗаглушка",
			"",
			БиблиотекаКартинок.Пустая,
			ГруппаМКПерваяКоманда);
	КонецЕсли;
	
	Если КомандыПоля.СворачиватьПрочиеКолонки Тогда
		ГруппаМКПоказатьСкрытьВсеПоля = Элементы.Добавить("ГруппаМКПоказатьСкрытьВсеПоля", Тип("ГруппаФормы"), ГруппаМККоманднаяПанель);
		ГруппаМКПоказатьСкрытьВсеПоля.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		МобильныйКлиентБПФормаОбъекта.ДобавитьКнопкуКоманднойПанели(Форма,
			"ПоказатьСкрытьПрочиеКолонки",
			"Показать все поля",
			БиблиотекаКартинок.Пустая,
			ГруппаМКПоказатьСкрытьВсеПоля);
	КонецЕсли;
	
КонецПроцедуры

Функция ВстроитьЭлементВГруппуМКСоздать(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМКСоздать = Элементы.Найти("ГруппаМКСоздать");
	Если ГруппаМКСоздать = Неопределено Тогда
		ГруппаМКВерхняяПанельКнопки = Элементы.Найти("ГруппаМКВерхняяПанельКнопки");
		ГруппаМКСоздать = Элементы.Добавить("ГруппаМКСоздать", Тип("ГруппаФормы"), ГруппаМКВерхняяПанельКнопки);
		ГруппаМКСоздать.Вид = ВидГруппыФормы.ГруппаКнопок;
	КонецЕсли;
	
	Элементы.Переместить(ЭлементФормы, Элементы.ГруппаМКСоздать);
	
КонецФункции

Функция ВстроитьЭлементВГруппуМКПерваяКомандаПанели(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КомандыЗаглушка = Элементы.Найти("КнопкаМККомандыЗаглушка");
	//Ставим на место заглушки, иначе относим к прочим командам
	Если КомандыЗаглушка <> Неопределено Тогда
		КомандыЗаглушка.Видимость = Ложь;
		ГрупаКонтейнер = Элементы.Найти("ГруппаМКПерваяКоманда");
	Иначе
		ГрупаКонтейнер = Элементы.Найти("ГруппаМККомандыФормы");
	КонецЕсли;
	
	Элементы.Переместить(ЭлементФормы, ГрупаКонтейнер);
	
КонецФункции

Процедура АдаптироватьМКОтборыФормы(Форма, НаименованиеРеквизитаФормыДинамическийСписок, ИсключаемыеИзПредставленияИменаОтборов) Экспорт

	Элементы = Форма.Элементы;
	
	НастроитьФормуОтбораМК(Форма,
		НаименованиеРеквизитаФормыДинамическийСписок, 
		ИсключаемыеИзПредставленияИменаОтборов);
	
	АдаптироватьМКЭлементОтбораОрганизация(Форма, Элементы.ОтборОрганизация,
		"",
		НСтр("ru = 'Все организации'"));
	
	АдаптироватьМКЭлементОтбораБыстрыеОтборы(Форма, Элементы.ОтборКонтрагент,
		НСтр("ru = 'Контрагент'"),
		НСтр("ru = 'Все контрагенты'"));
	
	ВстроитьЭлементВГруппуМКБыстрыеОтборы(Форма, Элементы.ГруппаДополнительныеОтборы);
	
КонецПроцедуры

Процедура НастроитьФормуОтбораМК(Форма, НаименованиеРеквизитаФормыДинамическийСписок, ИсключаемыеИзПредставленияИменаОтборов) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("МК_НаименованиеРеквизитаФормыДинамическийСписок", Новый ОписаниеТипов("Строка")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("МК_СинонимыЭлементовОтбора", Новый ОписаниеТипов("СписокЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("МК_ИсключаемыеИзПредставленияИменаОтборов", Новый ОписаниеТипов("Строка")));
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Форма.МК_НаименованиеРеквизитаФормыДинамическийСписок = НаименованиеРеквизитаФормыДинамическийСписок;
	Форма.МК_ИсключаемыеИзПредставленияИменаОтборов = ИсключаемыеИзПредставленияИменаОтборов;
	
	Элементы = Форма.Элементы;
	
	Элемент = Элементы.Добавить("НадписьМКПредставлениеОтбора", Тип("ДекорацияФормы"), Элементы.ГруппаМКОтборы);
	Элемент.Вид = ВидДекорацииФормы.Надпись;
	Элемент.ЦветТекста = ЦветаСтиля.ЦветТекстаЗаголовкаМК;
	Элемент.Шрифт = ШрифтыСтиля.ШрифтТекстаЗаголовкаМК;
	Элемент.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
	Элемент.РастягиватьПоГоризонтали = Истина;
	Элемент.Видимость = Ложь;
	
	Элементы.ГруппаМКБыстрыеОтборы.Объединенная = Ложь;
	Элементы.ГруппаМКБыстрыеОтборы.Поведение = ПоведениеОбычнойГруппы.Обычное;
	
КонецПроцедуры

Процедура АдаптироватьМКЭлементОтбораОрганизация(Форма, ЭлементФормы, ЗаголовокЭлемента, ПодсказкаВводаПустоеПоле) Экспорт
	
	ГруппаМКОтборОрганизация = Форма.Элементы.Найти("ГруппаМКОтборОрганизация");
	АдаптироватьМКЭлементОтбора(Форма, ЭлементФормы, ЗаголовокЭлемента, ПодсказкаВводаПустоеПоле, ГруппаМКОтборОрганизация);
	
КонецПроцедуры

Процедура АдаптироватьМКЭлементОтбораБыстрыеОтборы(Форма, ЭлементФормы, ЗаголовокЭлемента, ПодсказкаВводаПустоеПоле) Экспорт
	
	ГруппаМКБыстрыеОтборы = Форма.Элементы.Найти("ГруппаМКБыстрыеОтборы");
	АдаптироватьМКЭлементОтбора(Форма, ЭлементФормы, ЗаголовокЭлемента, ПодсказкаВводаПустоеПоле, ГруппаМКБыстрыеОтборы);
	
КонецПроцедуры

Процедура АдаптироватьМКЭлементОтбора(Форма, ЭлементФормы, ЗаголовокЭлемента, ПодсказкаВводаПустоеПоле, ГруппаРазмещения)
	
	Элементы = Форма.Элементы;
	
	ЭлементФормы.Заголовок = ЗаголовокЭлемента;
	ЭлементФормы.ПодсказкаВвода = ПодсказкаВводаПустоеПоле;
	ЭлементФормы.РастягиватьПоГоризонтали = Истина;
	ЭлементФормы.Ширина = 0;
	
	ЭлементФормы.ПоложениеЗаголовка = ?(ЭлементФормы.Заголовок = "", ПоложениеЗаголовкаЭлементаФормы.Нет, ПоложениеЗаголовкаЭлементаФормы.Верх);
	ЭлементФормы.ШрифтЗаголовка = ШрифтыСтиля.ШрифтТекстаЗаголовкаМК;
	ЭлементФормы.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаЗаголовкаМК;
	ЭлементФормы.КнопкаОчистки = Истина;
	
	Элементы.Переместить(ЭлементФормы, ГруппаРазмещения);
	
	Если ЗначениеЗаполнено(ЗаголовокЭлемента)
		И СтрНачинаетсяС(ЭлементФормы.Имя, "Отбор") Тогда
		ИмяОтбора = Сред(ЭлементФормы.Имя, СтрДлина("Отбор")+1);
		Форма.МК_СинонимыЭлементовОтбора.Добавить(ИмяОтбора, ЗаголовокЭлемента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМКСписок(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаМКСписок");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМКШапка(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаМКШапка");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ПеренестиКомандыВМККомандыФормы(Форма, КоллекцияКоманд) Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМККомандыФормы = Элементы.Найти("ГруппаМККомандыФормы");
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы, КоллекцияКоманд, ГруппаМККомандыФормы);
	
КонецПроцедуры

Процедура ПеренестиЭлементВМККомандыФормы(Форма, ЭлементКоманды, ЭлементНиже = Неопределено) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если ЭлементКоманды = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаМККомандыФормы = Элементы.Найти("ГруппаМККомандыФормы");
	Элементы.Переместить(ЭлементКоманды, ГруппаМККомандыФормы, ЭлементНиже);
	
КонецПроцедуры

Функция НовыйОписаниеКомандТабличноеПоле() Экспорт
	
	КомандыМК = Новый Структура;
	КомандыМК.Вставить("КомандаСоздать", "");
	// Если ДоступноУправлениеОтбором - Ложь, можно занять место кнопки заглущки
	КомандыМК.Вставить("ДоступноУправлениеОтбором", Истина);
	КомандыМК.Вставить("РазрешеноИзменение", Истина);
	КомандыМК.Вставить("СворачиватьПрочиеКолонки", Истина);
	
	Возврат КомандыМК;
	
КонецФункции

Процедура ПодготовитьСписокФормы(Форма, ИмяЭлементаСписок = "Список", СтрокРасшифровки = 3) Экспорт
	
	Элементы = Форма.Элементы;
	
	ЭлементСписок = Элементы[ИмяЭлементаСписок];
	
	//Табличное поле
	ЭлементСписок.ЗапросОбновления = СпособЗапросаОбновления.ПотянутьСверху;
	ЭлементСписок.ПоведениеПриСжатииПоГоризонтали = 
		ПоведениеТаблицыПриСжатииПоГоризонтали.СкрыватьЭлементыПоВажности;
	//В мобильном Авто не всегда срабатывает, т.к. поле Дата не является первым
	ЭлементСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	//Список
	ЭлементСписок.Шапка = Ложь;
	ЭлементСписок.ПутьКДаннымКартинкиСтроки = "";
	
	ГруппаКолонокМК_Корень = МобильныйКлиентБПФормаОбъекта.ДобавитьКорневуюГруппуСписка(Форма, ЭлементСписок);
	ГруппаКолонокМК_СтатусДокумента = ДобавитьГруппуКолонокСостояниеДокумента(Форма, ГруппаКолонокМК_Корень);
	
	ГруппаКолонокМК_Строки = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Строки",
		ГруппировкаКолонок.Вертикальная,
		ГруппаКолонокМК_Корень);
		
	ГруппаКолонокМК_ДатаНомерСумма = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_ДатаНомерСумма",
		ГруппировкаКолонок.Горизонтальная,
		ГруппаКолонокМК_Строки);
		
	ГруппаКолонокМК_ДатаНомер = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_ДатаНомер",
		ГруппировкаКолонок.ВЯчейке,
		ГруппаКолонокМК_ДатаНомерСумма);
		
	ГруппаКолонокМК_СуммаДокумента = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_СуммаДокумента",
		ГруппировкаКолонок.Горизонтальная,
		ГруппаКолонокМК_ДатаНомерСумма);
		
	ГруппаКолонокМК_СуммаДокумента.РастягиватьПоГоризонтали = Ложь;
	ГруппаКолонокМК_СуммаДокумента.Ширина = 8;
	
	ГруппаКолонокМК_Важное = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Важное",
		ГруппировкаКолонок.Горизонтальная,
		ГруппаКолонокМК_Строки);
	
	Для сч =1 по СтрокРасшифровки Цикл
		ГруппаКолонокМК_Строка1 = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
			"ГруппаКолонокМК_Строка" + сч,
			ГруппировкаКолонок.Горизонтальная,
			ГруппаКолонокМК_Строки);
	КонецЦикла;
	
	ГруппаКолонокМК_Прочее = ДобавитьГруппуСпискаПрочее(Форма, ГруппаКолонокМК_Строки);
	
КонецПроцедуры

Процедура ПодготовитьСписокФормыСправочника(Форма, ИмяЭлементаСписок = "Список", СтрокРасшифровки = 3) Экспорт
	
	Элементы = Форма.Элементы;
	
	ЭлементСписок = Элементы[ИмяЭлементаСписок];
	
	//Табличное поле
	ЭлементСписок.ЗапросОбновления = СпособЗапросаОбновления.ПотянутьСверху;
	ЭлементСписок.ПоведениеПриСжатииПоГоризонтали = 
		ПоведениеТаблицыПриСжатииПоГоризонтали.СкрыватьЭлементыПоВажности;
	
	//Список
	ЭлементСписок.Шапка = Ложь;
	
	ГруппаКолонокМК_Корень = МобильныйКлиентБПФормаОбъекта.ДобавитьКорневуюГруппуСписка(Форма, ЭлементСписок);
	
	ГруппаКолонокМК_Строки = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Строки",
		ГруппировкаКолонок.Вертикальная,
		ГруппаКолонокМК_Корень);
		
	ГруппаКолонокМК_Важное = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Важное",
		ГруппировкаКолонок.Горизонтальная,
		ГруппаКолонокМК_Строки);
	
	Для сч =1 по СтрокРасшифровки Цикл
		ГруппаКолонокМК_Строка1 = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(Форма,
			"ГруппаКолонокМК_Строка" + сч,
			ГруппировкаКолонок.Горизонтальная,
			ГруппаКолонокМК_Строки);
	КонецЦикла;
	
	ГруппаКолонокМК_Прочее = ДобавитьГруппуСпискаПрочее(Форма, ГруппаКолонокМК_Строки);
	
КонецПроцедуры

Функция ДобавитьГруппуКолонокСостояниеДокумента(Форма, КорневойЭлемент)
	
	Элементы = Форма.Элементы;
	ГруппаКолонокМК_СтатусДокумента = Элементы.Добавить("ГруппаКолонокМК_СостояниеДокумента", Тип("ГруппаФормы"), КорневойЭлемент);
	ГруппаКолонокМК_СтатусДокумента.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокМК_СтатусДокумента.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонокМК_СтатусДокумента.Ширина = 1;
	ГруппаКолонокМК_СтатусДокумента.ОтображатьВШапке = Ложь;
	ГруппаКолонокМК_СтатусДокумента.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
	ГруппаКолонокМК_СтатусДокумента.РастягиватьПоГоризонтали = Ложь;
	
	Возврат ГруппаКолонокМК_СтатусДокумента;
	
КонецФункции

Процедура ДобавитьКолонкаСостояниеДокумента(Форма, ИмяРеквизитаСостоянияДокумента, ИмяРеквизитаСписок) Экспорт
	
	Элементы = Форма.Элементы;
	
	Список = Элементы[ИмяРеквизитаСписок];
	
	ПолеМК_СостояниеДокумента = Элементы.Добавить("ПолеМК_СостояниеДокумента", Тип("ПолеФормы"), Список);
	ПолеМК_СостояниеДокумента.ПутьКДанным = ИмяРеквизитаСписок + "." + ИмяРеквизитаСостоянияДокумента;
	ПолеМК_СостояниеДокумента.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеМК_СостояниеДокумента.КартинкаЗначений = БиблиотекаКартинок.СостоянияДокумента;
	ПолеМК_СостояниеДокумента.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
	
	ВстроитьЭлементВГруппуМК_СостояниеДокумента(Форма, ПолеМК_СостояниеДокумента);
	
КонецПроцедуры

Функция ДобавитьГруппуСпискаПрочее(Форма, КорневойЭлемент)
	
	Элементы = Форма.Элементы;
	
	ГруппаКолонокМК_Прочее = Элементы.Добавить("ГруппаКолонокМК_Прочее", Тип("ГруппаФормы"), КорневойЭлемент);
	ГруппаКолонокМК_Прочее.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокМК_Прочее.Группировка = ГруппировкаКолонок.ВЯчейке;
	ГруппаКолонокМК_Прочее.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	ГруппаКолонокМК_Прочее.ОтображатьВШапке = Ложь;
	ГруппаКолонокМК_Прочее.РастягиватьПоГоризонтали = Ложь;
	ГруппаКолонокМК_Прочее.Видимость = Ложь;

	Возврат ГруппаКолонокМК_Прочее;
	
КонецФункции

Функция ПолучитьКорневаяГруппаСписка(Форма, ИмяРеквизитаСписок) ЭКспорт
	
	Элементы = Форма.Элементы;
	Список = Элементы[ИмяРеквизитаСписок];
	
	Возврат Элементы.Найти("ГруппаКолонокМК_Корень_" + ИмяРеквизитаСписок);
	
КонецФункции

Процедура ВстроитьЭлементВГруппуМК_СостояниеДокумента(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_СостояниеДокумента");
	
	ЭлементФормы.Ширина = 2;
	ЭлементФормы.АвтоМаксимальнаяШирина = 2;
	ЭлементФормы.РастягиватьПоГоризонтали = Ложь;
	ЭлементФормы.КартинкаШапки = БиблиотекаКартинок.Пустая;
	ЭлементФормы.ОтображатьВШапке = Ложь;
	ЭлементФормы.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМК_ДатаНомер(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_ДатаНомер");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМК_ДатаНомерСумма(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_ДатаНомерСумма");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМК_СуммаДокумента(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_СуммаДокумента");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМК_Важное(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_Важное");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМК_Строка(Форма, ЭлементФормы, НомерСтроки) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_Строка"+НомерСтроки);
	
	Если ТипЗнч(ЭлементФормы) <> Тип("ГруппаФормы") Тогда
		ЭлементФормы.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаСпискаМК;
	КонецЕсли;
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМК_Прочее(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_Прочее");
	
	ЭлементФормы.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	ЭлементФормы.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаПрочееСпискаМК;
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура ВстроитьЭлементыВГруппуМК_Прочее(Форма, ИмяРеквизитаСписок = "Список") Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_Прочее");
	
	ЭлементСписок = Элементы.Найти(ИмяРеквизитаСписок);
	
	ИменаКолонокИсключений = Новый Массив;
	ИменаКолонокИсключений.Добавить("ГруппаКолонокМК_Корень_" + ИмяРеквизитаСписок);
	МобильныйКлиентБПФормаОбъекта.ПереместитьКолонкиСписка(Элементы, 
		ЭлементСписок,
		КорневойЭлемент,
		ИменаКолонокИсключений);
	
	УстановитьОформлениеЭлементов(КорневойЭлемент.ПодчиненныеЭлементы);
	
КонецПроцедуры

Процедура ВстроитьЭлементВГруппуМКБыстрыеОтборы(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаМКБыстрыеОтборы");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры 

Процедура УстановитьВидимостьМКБыстрыеОтборы(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМКБыстрыеОтборы = Элементы.Найти("ГруппаМКБыстрыеОтборы");
	ГруппаМКБыстрыеОтборы.Видимость = Истина;
	
КонецПроцедуры

Процедура УстановитьОформлениеЭлементов(КоллекцияКолонок) Экспорт
	Для Каждого ПодчиненныйЭлемент Из КоллекцияКолонок Цикл
		Если ПодчиненныйЭлемент.Вид = ВидПоляФормы.ПолеВвода
			Или ПодчиненныйЭлемент.Вид = ВидПоляФормы.ПолеНадписи Тогда
			ПодчиненныйЭлемент.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаПрочееСпискаМК;
			ПодчиненныйЭлемент.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
		ИначеЕсли ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок Тогда
			ПодчиненныйЭлемент.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СкрытьЭлементФормыСписка(Форма, ЭлементФормы) Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаМКСкрыто");
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

#КонецОбласти