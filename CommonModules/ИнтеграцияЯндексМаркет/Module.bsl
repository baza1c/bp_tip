#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Авторизация

// Определяет имя сервера авторизации для обновления ключей доступа к сервису.
//
// Возвращаемое значение:
//   Строка - имя сервера сервиса.
//
Функция СерверАвторизации() Экспорт
	
	Результат = "oauth.yandex.ru";
	
	Возврат Результат;
	
КонецФункции

// Определяет адрес ресурса авторизации.
//
// Возвращаемое значение:
//   Строка - адрес ресурса. 
//
Функция АдресАвторизации() Экспорт
	
	ДанныеПриложения = ДанныеПриложения();

	Результат = "https://oauth.yandex.ru/authorize?response_type=code"
		+ "&client_id=" + ДанныеПриложения.IDПриложения 
		+ "&client_secret=" + ДанныеПриложения.ПарольПриложения 
		+ "&redirect_uri=" + "https://oauth.yandex.ru/verification_code";
	
	Возврат Результат;
	
КонецФункции

// Определяет данные авторизации.
//
// Параметры:
//   СтруктураОтвета - Структура - содержит набор ключей, которые необходимо вернуть.
//
// Возвращаемое значение:
//   Структура - содержит запрошенный набор ключей:
//     * access_token         - Строка - токен авторизации;
//     * access_token_expires - Дата - период действия токена;
//     * refresh_token        - Строка - токен автообновления.
//
Функция ДанныеАвторизации(СтруктураОтвета) Экспорт
	
	СтруктураДанныхАвторизации = Новый Структура;

	Если СтруктураОтвета.Свойство("access_token") 
		 И СтруктураОтвета.Свойство("expires_in") 
		 И СтруктураОтвета.Свойство("refresh_token") Тогда
		 
			expires_in = СтруктураОтвета.expires_in;
			
			Если ЭтаСтрокаЯвляетсяЦелымНеотрицательнымЧислом(expires_in) Тогда
				СрокЖизниТокена = НачалоДня(ТекущаяДатаСеанса() + Число(expires_in));
				СтруктураДанныхАвторизации.Вставить("access_token",         СтруктураОтвета.access_token);
				СтруктураДанныхАвторизации.Вставить("access_token_expires", СрокЖизниТокена);
				СтруктураДанныхАвторизации.Вставить("refresh_token",        СтруктураОтвета.refresh_token);
			Иначе
				ТекстОписанияСобытия = НСтр("ru = 'Ошибка сохранения данных в безопасном хранилище'", 
					ОбщегоНазначения.КодОсновногоЯзыка());
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Некорректное значение свойства expires_in при получении ключа авторизации: %1';
						|en = 'Out of order property value expires_in: %1'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					СтруктураОтвета.expires_in);
					
				ЗаписьЖурналаРегистрации(ТекстОписанияСобытия, 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ТекстОшибки);
			КонецЕсли;
	КонецЕсли;
			
	Возврат СтруктураДанныхАвторизации;
	
КонецФункции

// Функция возвращает http запрос для обновления ключей доступа к сервису для организации.
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
// Возвращаемое значение:
//   HTTPЗапрос - запрос для получения ключей доступа.
//
Функция ЗапросОбновитьТокеныДоступа(ИдентификаторАккаунта) Экспорт
	
	ДанныеПриложения = ДанныеПриложения();

	СтруктураДанныхАвторизации = ПолучитьДанныеАвторизации(ИдентификаторАккаунта);
	
	Если СтруктураДанныхАвторизации <> Неопределено Тогда
		refresh_token = СтруктураДанныхАвторизации.refresh_token;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");	

	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить("grant_type=refresh_token");
	ПараметрыURL.Добавить("refresh_token" + refresh_token);
	ПараметрыURL.Добавить("client_id=" + ДанныеПриложения.IDПриложения);
	ПараметрыURL.Добавить("client_secret=" + ДанныеПриложения.ПарольПриложения);
	
	Адрес = "/token?"; 
	HTTPЗапрос = Новый HTTPЗапрос(Адрес, Заголовки);
	Тело = СтрСоединить(ПараметрыURL, "&");
	
	HTTPЗапрос.УстановитьТелоИзСтроки(Тело,"windows-1251");
	
	Возврат HTTPЗапрос;
	
КонецФункции

Функция ПолучитьТокеныПоКоду(ВременныйКод) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Результат",         Неопределено);
	Результат.Вставить("ДанныеАвторизации", Неопределено);
	
	Сервер         = ИнтеграцияЯндексМаркет.СерверАвторизации();
	ИнтернетПрокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	HTTPСоединение = Новый HTTPСоединение(Сервер,,,,ИнтернетПрокси,60,СоединениеOpenSSL);
	HTTPЗапрос = ЗапросПолучитьТокеныПоКоду(ВременныйКод);

	Попытка
		HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);  
		ДанныеОтвета   = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет);
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
			
		Возврат Результат;
	КонецПопытки;
		
	КодСостояния = HTTPОтвет.КодСостояния; 
	
	СтатусОтвета = ВРЕГ(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОтвета, "status", "OK"));

	Если КодСостояния = 200 И СтатусОтвета = "OK" Тогда
		ДанныеАвторизации = ДанныеАвторизации(ДанныеОтвета);
		
		// Если в ответе есть токен, значит она завершена успешно
		Результат.Результат = ДанныеАвторизации.Свойство("access_token");
		
		Если Результат.Результат Тогда
			ДанныеЛогина      = ПолучитьДанныеЛогина(ДанныеАвторизации.access_token);
			
			ДанныеАвторизации.Вставить("id",    СокрЛП(ДанныеЛогина.Идентификатор));
			ДанныеАвторизации.Вставить("login", СокрЛП(ДанныеЛогина.Имя));
		КонецЕсли;
		Результат.Вставить("ДанныеАвторизации", ДанныеАвторизации);
	ИначеЕсли КодСостояния = 400 Тогда
		Результат.Результат = Ложь;
	ИначеЕсли СтатусОтвета = "ERROR" Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка выполнения запроса %1: %2%3'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), 
			Сервер, 
			ТекстОшибки(ДанныеОтвета.errors),
			HTTPОтвет.ПолучитьТелоКакСтроку());
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	Иначе
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка выполнения запроса %1: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), 
			Сервер, 
			HTTPОтвет.ПолучитьТелоКакСтроку());
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	КонецЕсли;
		
	Возврат Результат;
КонецФункции 

// Функция возвращает информацию о пользователе Яндекс ID (логин и идентификатор пользователя).
//
// Параметры:
//   КлючДоступа - Строка - ключ доступа, выданный для логина пользователя.
// 
// Возвращаемое значение:
//   Структура - информацию о пользователе:
//     * Имя           - Строка - логин пользователя на Яндекс ID;
//     * Идентификатор - Строка - уникальный идентификатор пользователя на Яндекс ID.
//
Функция ПолучитьДанныеЛогина(КлючДоступа) Экспорт
		
	ДанныеПриложения = ДанныеПриложения();
	Сервер = СерверДанныхАвторизации();
	ИмяМетода = "/info";
	Адрес = ИмяМетода +"?";
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Authorization", "OAuth " + КлючДоступа);
	
	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить("format=" + "json");
	ПараметрыURL.Добавить("jwt_secret=" + ДанныеПриложения.ПарольПриложения);
	
	Попытка	
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,,ИнтернетПрокси,200,СоединениеOpenSSL);
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер);
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ТекстОшибки, 
			Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
			
		Возврат Неопределено;
	КонецПопытки;
	
	ТелоЗапроса = СтрСоединить(ПараметрыURL, "&");
	Адрес = Адрес + ТелоЗапроса;
	
	HTTPЗапрос = Новый HTTPЗапрос(Адрес, Заголовки); 
	СтруктураОтвета = Новый Структура();
	СтруктураДанныхЛогина = Новый Структура();
		
	Попытка
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос);
		СтруктураОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет);
	Исключение 
		ТекстОшибки = НСтр("ru = 'Ошибка выполнения запроса получения данных о пользователе Яндекс ID (логина и идентификатора пользователя)",
				ОбщегоНазначения.КодОсновногоЯзыка());
		
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
			
		Возврат Неопределено;
	КонецПопытки;
	
	КодСостояния = HTTPОтвет.КодСостояния;
	
	Если КодСостояния = 200 Тогда
		СтруктураДанныхЛогина.Вставить("Имя",СтруктураОтвета.login);
		СтруктураДанныхЛогина.Вставить("Идентификатор",СтруктураОтвета.id);
	Иначе
		Если СтруктураОтвета.Свойство("status") И СтруктураОтвета.status = "ERROR" Тогда
			ОписаниеОшибок = ТекстОшибки(СтруктураОтвета.errors);
		Иначе
			ОписаниеОшибок = ""; 
		КонецЕсли;
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка вызова API Яндекс Маркет: %1; %2; %3'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			КодСостояния,
			HTTPОтвет.ПолучитьТелоКакСтроку(),
			ОписаниеОшибок);

		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
			
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтруктураДанныхЛогина;
	
КонецФункции

// Определяет данные авторизации из безопасного хранилища. 
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
// Возвращаемое значение:
//   Структура    - данные авторизации для подключения к сервису (см. ИнтеграцияСЯндексМаркетСервер.ДанныеАвторизации);
//   Неопределено - данные авторизации не найдены.
//
Функция ПолучитьДанныеАвторизации(НастройкиИнтеграции) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НастройкиИнтеграции) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	АПИТокен = ПолучитьТокенАПИ(НастройкиИнтеграции.Владелец);
	Если ЗначениеЗаполнено(АПИТокен) Тогда
		ДанныеАвторизации = Новый Структура("ApiKey", АПИТокен);
	Иначе
		ДанныеАвторизации = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
							НастройкиИнтеграции, "access_token,access_token_expires, refresh_token, id, login", Ложь);
		
		Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеАвторизации, "access_token") = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеАвторизации;
	
КонецФункции
 
// Определяет данные авторизации по указанной учетной записи (магазину). 
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
// Возвращаемое значение:
//   Строка - имя аккаунта (см. ИнтеграцияСЯндексМаркетСервер.ПолучитьДанныеЛогина).
//
Функция ДанныеАвторизацииЛогин(НастройкиИнтеграции) Экспорт
	
	ТекущиеДанные = ПолучитьДанныеАвторизации(НастройкиИнтеграции);
	
	Если ТекущиеДанные <> Неопределено Тогда   
		Логин = ТекущиеДанные.login;
	КонецЕсли;
	
	Возврат Логин;
	
КонецФункции

// Добавляет данные авторизации в безопасное хранилище. 
//
// Параметры:
//   ДанныеАвторизации - Структура - данные авторизации для подключения к сервису (см. ИнтеграцияСЯндексМаркетСервер.ДанныеАвторизации);
//   ДанныеЛогина           - Структура - данные об аккаунте (см. ИнтеграцияСЯндексМаркетСервер.ПолучитьДанныеЛогина).
//
// Возвращаемое значение:
//   Булево - результат успешного выполнения операции.
//
Функция СохранитьНастройкиАвторизации(НастройкиИнтеграции, ДанныеАвторизации) Экспорт
	
	Результат = Ложь;
	
	Если ДанныеАвторизации.Свойство("access_token")
		 И ДанныеАвторизации.Свойство("access_token_expires") 
		 И ДанныеАвторизации.Свойство("refresh_token") 
		 И ДанныеАвторизации.Свойство("id")
		 И ДанныеАвторизации.Свойство("login") Тогда
				
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкиИнтеграции, ДанныеАвторизации.access_token, "access_token");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкиИнтеграции, ДанныеАвторизации.access_token_expires, "access_token_expires");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкиИнтеграции, ДанныеАвторизации.refresh_token, "refresh_token");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкиИнтеграции, ДанныеАвторизации.id, "id");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкиИнтеграции, ДанныеАвторизации.login, "login");
		УстановитьПривилегированныйРежим(Ложь);
		
		Результат = Истина;
	КонецЕсли;
	
	Если НЕ Результат Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось сохранить данные авторизации в безопасном хранилище для владельца ""%1"".'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			НастройкиИнтеграции);
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Удаляет данные авторизации из безопасного хранилища. 
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
Процедура УдалитьДанныеАвторизации(НастройкиИнтеграции) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(НастройкиИнтеграции);
	
КонецПроцедуры

Функция ИнтеграцияНастроена(НастройкаИнтеграции) Экспорт
	
	Возврат ПолучитьДанныеАвторизации(НастройкаИнтеграции) <> Неопределено;
	
КонецФункции 

#КонецОбласти

Функция ПолучитьДанныеДокумента(ПараметрыОтчета, КодОтветаСервера, СообщениеОбОшибке) Экспорт
	
	ДанныеДокумента = Неопределено;
	
	ДанныеАвторизации = ПолучитьДанныеАвторизации(ПараметрыОтчета.НастройкаИнтеграции);
	Если ДанныеАвторизации = Неопределено Тогда
		СообщениеОбОшибке = НСтр("ru = 'Требуется настройка подключения'");
		Возврат ДанныеДокумента;
	КонецЕсли;
	РезультатаЗапроса = ЗапроситьОтчетаОПродажах(ПараметрыОтчета, КодОтветаСервера);
	Если РезультатаЗапроса = Неопределено Тогда
		СообщениеОбОшибке = НСтр("ru = 'При выполнении запроса произошла ошибка, подробности в журнале регистрации.'");
		Возврат ДанныеДокумента;
	КонецЕсли;
	
	АтрибутыРезультата = Новый структура;
	АтрибутыРезультата.Вставить("СсылкаНаФайл", "result.file");
	АтрибутыРезультата.Вставить("Статус", "result.status");
	АтрибутыРезультата.Вставить("СтатусПодробно", "result.substatus");
	
	АдресЗапроса = СтрШаблон("reports/info/%1", РезультатаЗапроса.ИдентификаторЗапроса);

	ДатаОкончанияФормирования = РезультатаЗапроса.ДатаОкончанияФормирования + 1;
	
	ОбработкаЗавершена = Ложь;
	Пока НЕ ОбработкаЗавершена Цикл
		
		ОстатокСекунд = Цел(Макс(ДатаОкончанияФормирования - ТекущаяДатаСеанса(),0));
		ДлительныеОперации.СообщитьПрогресс(,СтрШаблон(НСтр("ru = 'Отчет будет готов через %1 сек...'"), ОстатокСекунд+1));
		ОбщегоНазначенияБТС.Пауза(1);
		
		Если ОстатокСекунд%10 <> 0 Тогда
			Продолжить;
		КонецЕсли;
		//Логирование
		Логирование = "";
		РезультатВыполнения = ВыполнитьGETЗапрос(АдресЗапроса, , АтрибутыРезультата, ПараметрыОтчета.НастройкаИнтеграции, Логирование);
		//Логирование конец
		Если НЕ РезультатВыполнения.Результат Тогда
			СообщениеОбОшибке = НСтр("ru = 'При выполнении запроса произошла ошибка, подробности в журнале регистрации.'");
			Прервать;
		КонецЕсли;
	
		Статус = РезультатВыполнения.Статус;
		СтатусПодробно = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполнения, "СтатусПодробно");
		Если Статус = "PENDING" ИЛИ Статус = "PROCESSING" Тогда
			Продолжить;
		КонецЕсли;
		
		// Остальные статусы - терминальные
		ОбработкаЗавершена = Истина;
		
		Если Статус = "DONE" И НЕ ЗначениеЗаполнено(СтатусПодробно) Тогда
			ДанныеДокумента = РазобратьДанныеОтчетаВСтруктуру(РезультатВыполнения.СсылкаНаФайл, СообщениеОбОшибке, Логирование);
		ИначеЕсли СтатусПодробно = "NO_DATA" Тогда
			// нет данных - это не ошибка
		ИначеЕсли СтатусПодробно = "TOO_LARGE" Тогда
			СообщениеОбОшибке = НСтр("ru = 'Отчет превысил допустимый размер - укажите меньший период времени или уточните условия запроса.'"); 
		Иначе
			СообщениеОбОшибке = НСтр("ru = 'Во время генерации произошла ошибка.'"); 
		КонецЕсли;
	КонецЦикла;
	
	//Логирование
	Если ТипЗнч(Логирование) = Тип("Структура") Тогда
		РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЗаписатьВЖурнал(Логирование);
	КонецЕсли;
	//Логирование конец 

	Возврат ДанныеДокумента;
	
КонецФункции

Функция РазобратьДанныеОтчетаВСтруктуру(СсылкаНаФайл, СообщениеОбОшибке, Логирование = Неопределено)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xlsx");
	
	ДлительныеОперации.СообщитьПрогресс(,НСтр("ru = 'Получаем данные отчета'"));
	
	ПараметрыПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.ПараметрыПолученияФайла();
	ПараметрыПолучения.ПутьДляСохранения = ИмяВременногоФайла;
	РезультатПолучения = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(СсылкаНаФайл, ПараметрыПолучения, Ложь);	
	Если НЕ РезультатПолучения.Статус Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось загрузить файл отчета о продажах. Подробности в журнале регистрации.'");
		
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			СтрШаблон("Не удалось загрузить файл отчета о продажах по причине: %1", СообщениеОбОшибке), 
			Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
			
		Возврат Неопределено;
	//Логирование
	Иначе
		Если ТипЗнч(Логирование) = Тип("Структура") Тогда
			Логирование.ИмяФайла = РезультатПолучения.Путь;
			РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЗаписатьВЖурнал(Логирование);
			Логирование = Неопределено;
		КонецЕсли;
	//Логирование конец	
	КонецЕсли;
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("ВременныйФайл",   ИмяВременногоФайла);
	ОписаниеФайла.Вставить("ИмяФайла",        "report.xlsx");
	ОписаниеФайла.Вставить("ПолноеИмяФайла",  "report.xlsx");
	ОписаниеФайла.Вставить("РасширениеФайла", "xlsx");
	
	ЗагруженныеФайлы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписаниеФайла);
	
	Возврат ЭлектронноеВзаимодействиеБП.ПолучитьДанныеФайлов(ЗагруженныеФайлы);
	
КонецФункции 


Функция СписокТоваровМаркетплейс(НастройкаИнтеграции, ТекстОшибки, Отказ) Экспорт
	АтрибутыРезультата = Новый Структура;
	АтрибутыРезультата.Вставить("ИнформацияОТоварах", "result.offerMappings");
	АтрибутыРезультата.Вставить("Пагинатор", "result.paging");
	
	ИдентификаторСтраницы = "";
	АдресЗапроса = "/{businessId}/offer-mappings.json?page_token=";
	
	ИнформацияОТоварах = Новый Массив;
	Пока ИдентификаторСтраницы <> Неопределено Цикл
		РезультатЗапроса = ВыполнитьPOSTЗапрос(АдресЗапроса+ИдентификаторСтраницы, Неопределено, АтрибутыРезультата, НастройкаИнтеграции);
		Отказ = НЕ РезультатЗапроса.Результат;
		Если Отказ Тогда
			ТекстОшибки = РезультатЗапроса.СообщениеОбОшибке;
			Возврат РезультатЗапроса; 
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИнформацияОТоварах, РезультатЗапроса.ИнформацияОТоварах);
		
		ИдентификаторСтраницы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗапроса.Пагинатор, "nextPageToken");
	КонецЦикла;
	
	Владелец = СопоставлениеНоменклатурыКонтрагентовСлужебный.ВладелецНоменклатурыКонтрагента(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "Контрагент"));
	СписокТоваров = Новый Массив;
	Для каждого ИнформацияОТоваре Из ИнформацияОТоварах Цикл
		ОсновныеПараметрыТовара = ИнформацияОТоваре.offer;
		
		НоменклатураМаркетплейса = ОбменСМаркетплейс.НовыйНоменклатураМаркетплейса(Перечисления.ВидыМаркетплейсов.ЯндексМаркет, Владелец);
		
		НоменклатураМаркетплейса.КодМагазина  = ОсновныеПараметрыТовара.offerId;
		НоменклатураМаркетплейса.Наименование = ОсновныеПараметрыТовара.name;
		НоменклатураМаркетплейса.Артикул      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОсновныеПараметрыТовара, "vendorCode");
		НоменклатураМаркетплейса.Штрихкоды    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОсновныеПараметрыТовара, "barcodes",
			НоменклатураМаркетплейса.Штрихкоды);
		
		ОбменСМаркетплейс.ДобавитьНоменклатуруВСписок(СписокТоваров, НоменклатураМаркетплейса);
	КонецЦикла;

	Возврат СписокТоваров;
КонецФункции

#Область УправлениеОстаткамиТоваров

Функция ПолучитьОстаткиТоваров(НастройкаИнтеграции, Отбор, Отказ) Экспорт
	ТаблицаТовары = ОбменСМаркетплейс.НовыйТаблицаОстатковТоваров();
	
	Если Отбор.Количество() = 0 Тогда
		Возврат ТаблицаТовары;
	КонецЕсли;
	
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("offerIds", ОбщегоНазначения.ВыгрузитьКолонку(Отбор, "ИдентификаторТовара"));
	
	АтрибутыРезультата = Новый структура;
	АтрибутыРезультата.Вставить("ОстаткиПоСкладам", "result.warehouses");
	
	РезультатВыполнения = ВыполнитьPOSTЗапрос("/{campaignId}/offers/stocks.json", ДанныеЗапроса, АтрибутыРезультата, НастройкаИнтеграции);
	Отказ = НЕ РезультатВыполнения.Результат;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для каждого СкладМаркетплейса Из РезультатВыполнения.ОстаткиПоСкладам Цикл
		ИдентификаторСклада = Формат(СкладМаркетплейса.warehouseId, "ЧГ=0");
		Для каждого ОстатокНаСкладе Из СкладМаркетплейса.offers Цикл
			ИдентификаторТовара = ОстатокНаСкладе.offerId;
			Для каждого ИнформацияОбОстатках Из ОстатокНаСкладе.stocks Цикл
				Если ИнформацияОбОстатках.type <> "AVAILABLE" Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрока = ТаблицаТовары.Добавить();
				НоваяСтрока.ИдентификаторСклада = ИдентификаторСклада;
				НоваяСтрока.ИдентификаторТовара = ИдентификаторТовара;
				НоваяСтрока.Остаток = ИнформацияОбОстатках.count;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
		
	ТаблицаТовары.Свернуть("ИдентификаторТовара, ИдентификаторСклада, Штрихкод", "Остаток");
	
	Возврат ТаблицаТовары;
КонецФункции

Процедура ПередатьОстаткиТоваров(НастройкаИнтеграции, ТаблицаТовары, СообщениеОбОшибке, Отказ, КодОтветаСервера = 0) Экспорт 
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("skus", Новый Массив);
	
	ПорцииДляВыгрузки = ОбменСМаркетплейс.ПодготовитьПорцииДляОбмена(ТаблицаТовары, 500);
	Для каждого Порция Из ПорцииДляВыгрузки Цикл
		Для каждого СтрокаТаблицыТовары Из Порция Цикл
			// Для запроса HTTP
			StockDTO = НовыйStockDTO();
			StockDTO.sku         = СтрокаТаблицыТовары.ИдентификаторТовара;
			StockDTO.warehouseId = СтрокаТаблицыТовары.ИдентификаторСклада;
			
			StockItemDTO = НовыйStockItemDTO(СтрокаТаблицыТовары.Остаток);
			StockDTO.items.Добавить(StockItemDTO);
			
			ДанныеЗапроса.skus.Добавить(StockDTO);
		КонецЦикла;
		
		РезультатЗапроса = ВыполнитьPUTЗапрос("/{campaignId}/offers/stocks.json", ДанныеЗапроса, НастройкаИнтеграции);
		Если НЕ РезультатЗапроса.Результат Тогда
			СообщениеОбОшибке = РезультатЗапроса.СообщениеОбОшибке;
			Отказ = Истина;
			КодОтветаСервера = РезультатЗапроса.КодОтветаСервера;
			Прервать;
		КонецЕсли;
		ОбменСМаркетплейс.ОбновитьДанныеОбОстатках(НастройкаИнтеграции, Порция);
		
		ДанныеЗапроса.skus.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьФайлОстатков(НастройкаИнтеграции, ТаблицаОстатки) Экспорт
	ЧастиИмени = Новый Массив;
	ЧастиИмени.Добавить("mass_shared_stocks_business");
	Если ЗначениеЗаполнено(НастройкаИнтеграции.ИдентификаторКабинета) Тогда
		ЧастиИмени.Добавить(НастройкаИнтеграции.ИдентификаторКабинета);
	КонецЕсли;
	ЧастиИмени.Добавить(Формат(ТекущаяДата(), "ДФ=dd-MM-yyyy"));
	
	СтруктураФайла = Новый Структура;
	СтруктураФайла.Вставить("ИмяФайла", СтрСоединить(ЧастиИмени, "_")+".xlsx");
	СтруктураФайла.Вставить("Страницы", Новый Соответствие);
	
	Макет = РегистрыСведений.ОстаткиТоваровМаркетплейсов.ПолучитьМакет("ФормаОстатковЯндекс");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Вывести(Макет.ПолучитьОбласть("ОстаткиНаСкладе_Шапка"));
	
	ОбластьОстатки = Макет.ПолучитьОбласть("ОстаткиНаСкладе_Строка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОстатки", ТаблицаОстатки);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОстатки.Остаток КАК Остаток,
	|	ТаблицаОстатки.ИдентификаторТовара КАК ИдентификаторТовара
	|ПОМЕСТИТЬ ВТ_ОстаткиТоваров
	|ИЗ
	|	&ТаблицаОстатки КАК ТаблицаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторМаркетплейса,
	|	НоменклатураМаркетплейсов.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	ВТ_ОстаткиТоваров.Остаток КАК Остаток
	|ИЗ
	|	ВТ_ОстаткиТоваров КАК ВТ_ОстаткиТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ПО ВТ_ОстаткиТоваров.ИдентификаторТовара = НоменклатураМаркетплейсов.ИдентификаторТовара";
	
	ВыборкаРезультатов = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаРезультатов.Следующий() Цикл
		ОбластьОстатки.Параметры.Заполнить(ВыборкаРезультатов);
		
		ТабличныйДокумент.Вывести(ОбластьОстатки);
	КонецЦикла;
	
	СтруктураФайла.Страницы.Вставить("Список товаров", ПоместитьВоВременноеХранилище(ТабличныйДокумент));
	СтруктураФайла.Страницы.Вставить("Описание полей", ПоместитьВоВременноеХранилище(Макет.ПолучитьОбласть("Инструкция")));
	
	Возврат СтруктураФайла;
КонецФункции

#КонецОбласти

#Область УправлениеМагазинами

Функция СписокСкладов(НастройкаИнтеграции, Отказ) Экспорт
	
	РезультатЗапроса = ВыполнитьGETЗапрос("", Неопределено, Неопределено, НастройкаИнтеграции);
	
	Если НЕ РезультатЗапроса.Результат Тогда
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеОтвета = РезультатЗапроса.ДанныеОтвета;
	
	СоответствиеТиповСкладов = СоответствиеТиповСкладов();
	
	АтрибутыРезультата = Новый Структура;
	АтрибутыРезультата.Вставить("ИдентификаторКабинета", "business.id");
	АтрибутыРезультата.Вставить("ИдентификаторСклада",   "id");
	АтрибутыРезультата.Вставить("ТипСклада",             "placementType");
	АтрибутыРезультата.Вставить("Наименование",          "domain");
	
	СписокСкладов = Новый СписокЗначений;
	Для каждого CampaignDTO Из ДанныеОтвета.campaigns Цикл
		ДанныеМаркетплейс = ОбменСМаркетплейс.РазобратьДанныеОтвета(CampaignDTO, АтрибутыРезультата);
		Если ДанныеМаркетплейс.Количество() <> 4 Тогда
			Продолжить;
		КонецЕсли;
		
		Идентификаторы = Новый Структура;
		Идентификаторы.Вставить("ИдентификаторКабинета", Формат(ДанныеМаркетплейс.ИдентификаторКабинета, "ЧГ=")); // businessID
		Идентификаторы.Вставить("ИдентификаторСклада",   Формат(ДанныеМаркетплейс.ИдентификаторСклада, "ЧГ=")); // campaightID
		Идентификаторы.Вставить("ТипСклада",             СоответствиеТиповСкладов[ДанныеМаркетплейс.ТипСклада]);
		Идентификаторы.Вставить("Наименование",          ДанныеМаркетплейс.Наименование);
		
		СписокСкладов.Добавить(Идентификаторы, CampaignDTO.domain);
	КонецЦикла;
	
	Возврат СписокСкладов;
	
КонецФункции

#КонецОбласти

#Область ПродажиБизнесу

Процедура ВыделитьПродажиБизнесу(ДанныеФайла, ПродажиБизнесу) Экспорт
	
	ДанныеФайла.Вставить("ЭтоПродажаБизнесу", Ложь);
	ДанныеФайла.Вставить("НомерДокумента", "");
	ДанныеФайла.ДанныеДокумента.Вставить("НаименованиеПокупателя", "");
	ДанныеФайла.ДанныеДокумента.Вставить("ИННПокупателя", "");
	ДанныеФайла.ДанныеДокумента.Вставить("КПППокупателя", "");
	ДанныеФайла.ДанныеДокумента.Вставить("НомерДокумента", "");
	ДанныеФайла.ДанныеДокумента.Вставить("ДатаДокумента", Дата(1, 1, 1));
	ДанныеФайла.ДанныеДокумента.Вставить("НомерСФ", "");
	ДанныеФайла.ДанныеДокумента.Вставить("ДатаСФ", Дата(1, 1, 1));
	ДанныеФайла.ДанныеДокумента.Вставить("НомерКоррСФ", "");
	ДанныеФайла.ДанныеДокумента.Вставить("ДатаКоррСФ", Дата(1, 1, 1));
	ДанныеФайла.ДанныеДокумента.Вставить("Контрагент", Неопределено);
	ДанныеФайла.ДанныеДокумента.Вставить("ДоговорКонтрагента", Неопределено);
	
	ТаблицаТоваров = ДанныеФайла.ДанныеДокумента.ТаблицаТоваров;
	Если ТаблицаТоваров.Колонки.Найти("ДанныеМаркетплейса") <> Неопределено Тогда
		ТаблицаТоваров.Колонки.Удалить("ДанныеМаркетплейса");
	КонецЕсли;
	
	ПервичныеДанныеМаркетплейса = ДанныеФайла.ДанныеДокумента.ПервичныеДанныеМаркетплейса;
	ДанныеФайла.ДанныеДокумента.ПервичныеДанныеМаркетплейса = ОбменСМаркетплейс.ОтобратьПервичныеДанныеМаркетплейса(
		ПервичныеДанныеМаркетплейса, "типзаказа", "Продажа физлицу");
	
	ТаблицаТоваровПродажаБизнесу = ТаблицаТоваров.Скопировать(Новый Структура("ТипЗаказа", НСтр("ru = 'Продажа бизнесу'")));
	
	Если ТаблицаТоваровПродажаБизнесу.Количество() > 0 Тогда
		
		Для Каждого СтрокаТаблицы Из ТаблицаТоваровПродажаБизнесу Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.НомерУПД) И Не ЗначениеЗаполнено(СтрокаТаблицы.НомерДокумента) Тогда
				СтрокаТаблицы.НомерДокумента = СтрокаТаблицы.НомерУПД;
				СтрокаТаблицы.НомерСФ        = СтрокаТаблицы.НомерУПД;
				СтрокаТаблицы.ДатаДокумента  = СтрокаТаблицы.ДатаУПД;
				СтрокаТаблицы.ДатаСФ         = СтрокаТаблицы.ДатаУПД;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТаблицы.НомерУКД) И Не ЗначениеЗаполнено(СтрокаТаблицы.НомерКоррСФ) Тогда
				СтрокаТаблицы.НомерКоррСФ = СтрокаТаблицы.НомерУКД;
				СтрокаТаблицы.ДатаКоррСФ  = СтрокаТаблицы.ДатаУКД;
			КонецЕсли;
		КонецЦикла;
		
		РеквизитыСчетовФактур = ТаблицаТоваровПродажаБизнесу.Скопировать(, "НомерДокумента, ДатаДокумента");
		РеквизитыСчетовФактур.Свернуть("НомерДокумента, ДатаДокумента");
		Для Каждого РеквизитыСчетаФактуры Из РеквизитыСчетовФактур Цикл
			НомерДокумента = РеквизитыСчетаФактуры.НомерДокумента;
			Если ПустаяСтрока(НомерДокумента) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяТаблицаТоваров = ТаблицаТоваровПродажаБизнесу.Скопировать(Новый Структура("НомерДокумента", НомерДокумента));
			Если НоваяТаблицаТоваров.Количество() > 0 Тогда
				
				НовыеДанныеФайла = ОбщегоНазначения.СкопироватьРекурсивно(ДанныеФайла);
				НовыеДанныеФайла.ДанныеДокумента.ТаблицаТоваров = НоваяТаблицаТоваров;
				ОбновитьДанныеФайлаЯндекс(НовыеДанныеФайла);
				НовыеДанныеФайла.ЭтоПродажаБизнесу = Истина;
				НовыеДанныеФайла.НомерДокумента = НомерДокумента;
				ЗаполнитьЗначенияСвойств(НовыеДанныеФайла.ДанныеДокумента, НоваяТаблицаТоваров[0],
					"НаименованиеПокупателя, ИННПокупателя, КПППокупателя,
					|НомерДокумента, ДатаДокумента, НомерСФ, ДатаСФ, НомерКоррСФ, ДатаКоррСФ");
				НовыеДанныеФайла.ДанныеДокумента.Вставить("КраткоеНаименованиеПокупателя",
					Справочники.Контрагенты.ПолучитьКраткоеНаименованиеКонтрагента(НовыеДанныеФайла.ДанныеДокумента.НаименованиеПокупателя));
				НовыеДанныеФайла.ДанныеДокумента.ПервичныеДанныеМаркетплейса = ОбменСМаркетплейс.ОтобратьПервичныеДанныеМаркетплейса(
					ПервичныеДанныеМаркетплейса, "номертоварнойнакладной,номерупд", НомерДокумента);
				ПродажиБизнесу.Добавить(НовыеДанныеФайла);
				
				СтрокиКУдалению = ТаблицаТоваров.НайтиСтроки(Новый Структура("НомерДокумента", НомерДокумента));
				Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
					ТаблицаТоваров.Удалить(СтрокаКУдалению);
				КонецЦикла;
				СтрокиКУдалению = ТаблицаТоваров.НайтиСтроки(Новый Структура("НомерУПД", НомерДокумента));
				Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
					ТаблицаТоваров.Удалить(СтрокаКУдалению);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		ОбновитьДанныеФайлаЯндекс(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьКонтрагентаИДоговорПоПродажеБизнесу(ДанныеДокумента, ПараметрыДокумента, ОписаниеОшибки) Экспорт
	
	ЭтоВозврат = ДанныеДокумента.ВидДокумента = "ОтчетЯндекс_Невыкуплено"
		Или ДанныеДокумента.ВидДокумента = "ОтчетЯндекс_Возвращено";
	Организация = ПараметрыДокумента.Организация;
	ГруппаДляНовыхКонтрагентов = ПараметрыДокумента.ГруппаДляНовыхКонтрагентов;
	
	РеквизитыПокупателя = Новый Структура("Наименование, НаименованиеПолное, ИНН, КПП, ЮридическоеФизическоеЛицо",
		ДанныеДокумента.КраткоеНаименованиеПокупателя,
		ДанныеДокумента.НаименованиеПокупателя,
		ДанныеДокумента.ИННПокупателя,
		ДанныеДокумента.КПППокупателя,
		?(СтрДлина(ДанныеДокумента.ИННПокупателя) = 12 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ДанныеДокумента.ИННПокупателя),
			Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо,
			Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо));
	Контрагент = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(РеквизитыПокупателя);
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		РеквизитыПокупателя.Вставить("Родитель", ГруппаДляНовыхКонтрагентов);
		Попытка
			Контрагент = Справочники.Контрагенты.СоздатьКонтрагента(РеквизитыПокупателя);
		Исключение
			ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать контрагента: %1 (ИНН: %2),
				|по причине %2'"),
				ДанныеДокумента.НаименованиеПокупателя,
				ДанныеДокумента.ИННПокупателя,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
	КонецЕсли;
	ДанныеДокумента.Контрагент = Контрагент;
	
	// Для возвратов договор берем из документа основания
	Если Не ЭтоВозврат Тогда
		
		НаименованиеДоговора = СтрШаблон(НСтр("ru = '%1 от %2'"),
			СокрЛП(ДанныеДокумента.НомерДокумента),
			Формат(ДанныеДокумента.ДатаДокумента, "ДФ=dd.MM.yyyy"));
		
		РеквизитыДоговора = Новый Структура;
		РеквизитыДоговора.Вставить("Владелец", Контрагент);
		РеквизитыДоговора.Вставить("Организация", Организация);
		РеквизитыДоговора.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		РеквизитыДоговора.Вставить("Наименование", НаименованиеДоговора);
		РеквизитыДоговора.Вставить("Номер", СокрЛП(ДанныеДокумента.НомерДокумента));
		РеквизитыДоговора.Вставить("Дата", ДанныеДокумента.ДатаДокумента);
		
		ДополнительныеПараметры = Новый Структура("Наименование, Номер, Дата",
			Новый Структура("ЗначениеОтбора", НаименованиеДоговора),
			Новый Структура("ЗначениеОтбора", СокрЛП(ДанныеДокумента.НомерДокумента)),
			Новый Структура("ЗначениеОтбора", ДанныеДокумента.ДатаДокумента));
		
		ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ЕстьДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
			ДоговорКонтрагента,
			РеквизитыДоговора.Владелец,
			РеквизитыДоговора.Организация,
			РеквизитыДоговора.ВидДоговора,
			ДополнительныеПараметры);
		Если Не ЕстьДоговорКонтрагента Тогда
			ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", РеквизитыДоговора);
			ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
			Если ДоговорКонтрагента = Неопределено Тогда
				ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать договор с покупателем контрагента: %1 (ИНН: %2).'"),
					ДанныеДокумента.НаименованиеПокупателя,
					ДанныеДокумента.ИННПокупателя);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		ДанныеДокумента.ДоговорКонтрагента = ДоговорКонтрагента;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АвтозагрузкаОтчетовМаркетплейсов

Функция ПериодыАвтозагрузки(Интеграция, ПериодыАвтозагрузки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Интеграция.Организация);
	Запрос.УстановитьПараметр("Магазин", Формат(Интеграция.ИдентификаторСклада, "ЧГ=0"));
	Запрос.УстановитьПараметр("Маркетплейс", Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	Запрос.УстановитьПараметр("ПериодыАвтозагрузки", ПериодыАвтозагрузки);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ОтчетМаркетплейса.Дата, МЕСЯЦ)), ДАТАВРЕМЯ(1, 1, 1)) КАК МаксимальныйПериод
	|ИЗ
	|	Документ.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|ГДЕ
	|	НЕ ОтчетМаркетплейса.ПометкаУдаления
	|	И ОтчетМаркетплейса.Организация = &Организация
	|	И ОтчетМаркетплейса.Маркетплейс = &Маркетплейс
	|	И ОтчетМаркетплейса.Магазин = &Магазин
	|	И НАЧАЛОПЕРИОДА(ОтчетМаркетплейса.Дата, МЕСЯЦ) В (&ПериодыАвтозагрузки)";
	
	МаксимальныйПериод = Дата(1, 1, 1);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		МаксимальныйПериод = Выборка.МаксимальныйПериод;
	КонецЕсли;
	
	Результат = Новый Массив;
	Для Каждого ПериодАвтозагрузки Из ПериодыАвтозагрузки Цикл
		Если ПериодАвтозагрузки > МаксимальныйПериод Тогда
			Результат.Добавить(Новый Структура("НачалоПериода, ОкончаниеПериода",
				НачалоМесяца(ПериодАвтозагрузки), КонецМесяца(ПериодАвтозагрузки)));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция АвтозагрузкаДокументов(Интеграция, ДанныеМаркетплейса) Экспорт
	
	УстановитьСпособЗагрузки(ДанныеМаркетплейса, ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиФоновый());
	
	МенеджерОбработки = Обработки.ЗагрузкаОтчетаЯндексМаркет;
	
	ОбработкаОбъект = МенеджерОбработки.Создать();
	ЗаполнитьЗначенияСвойств(ОбработкаОбъект, Интеграция);
	
	ОрганизацияИзОтчета = Формат(Интеграция.ИдентификаторСклада, "ЧГ=0");
	МенеджерОбработки.ЗаполнитьПериоды(ОбработкаОбъект, ДанныеМаркетплейса, ОрганизацияИзОтчета);
	ДанныеСвязанныхДокументов = МенеджерОбработки.ЗаполнитьСвязанныеДокументы(ОбработкаОбъект);
	ДеревоДокументов = ДанныеСвязанныхДокументов.Дерево;
	
	ПараметрыДокумента = МенеджерОбработки.ПараметрыСозданияДокументов(
		ОбработкаОбъект, ОрганизацияИзОтчета, Интеграция.ГруппаДляНовыхКонтрагентов);
	
	НесозданныеДокументы = МенеджерОбработки.ДеревоНеСозданныхДокументов(ДеревоДокументов);
	СохранитьНесозданныеДокументы(ПараметрыДокумента, ДанныеМаркетплейса, НесозданныеДокументы);
	
	СозданныеДокументы = Новый Массив;
	МенеджерОбработки.СоздатьДокументы(ПараметрыДокумента, ДанныеМаркетплейса, ДеревоДокументов);
	МенеджерОбработки.ДокументыДерева(СозданныеДокументы, ДеревоДокументов.Строки);
	
	СозданныеДокументы.Добавить(ПараметрыДокумента.ОтчетМаркетплейса);
	
	Возврат СозданныеДокументы;
	
КонецФункции

Процедура УстановитьСпособЗагрузки(ДанныеМаркетплейса, СпособЗагрузки) Экспорт
	
	Для Каждого СтрокаДанных Из ДанныеМаркетплейса Цикл
		СтрокаДанных.ДанныеДокумента.Вставить("СпособЗагрузки", СпособЗагрузки);
	КонецЦикла;
	
КонецПроцедуры

Процедура СохранитьНесозданныеДокументы(ПараметрыДокумента, ДанныеМаркетплейса, НесозданныеДокументы)
	Если Не ЗначениеЗаполнено(НесозданныеДокументы.Строки) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОтчета = Новый Структура("ДанныеФайла, ПараметрыДокумента, ДеревоДокументов",
		ДанныеМаркетплейса, ПараметрыДокумента, НесозданныеДокументы);
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	Параметры = Предупреждения.НовыеПараметрыПредупреждения(
		Перечисления.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НесозданыДокументы);
	Параметры.Документ = ПараметрыДокумента.ОтчетМаркетплейса;
	Параметры.Организация = ПараметрыДокумента.Организация;
	Параметры.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет;
	Параметры.ДанныеОтчета = Новый ХранилищеЗначения(ДанныеОтчета, Новый СжатиеДанных(9));
	Предупреждения.ЗаписатьПредупреждение(Параметры);
	
КонецПроцедуры

Функция СоздатьДокументы(ДанныеДокументов) Экспорт
	
	СозданныеДокументы = Новый Массив;
	
	Для Каждого СтрокаДокумента Из ДанныеДокументов Цикл
		ПараметрыДокумента = СтрокаДокумента.ПараметрыДокумента;
		Если Не ЗначениеЗаполнено(ПараметрыДокумента.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ИмяДокумента = ПараметрыДокумента.Ссылка.Метаданные().Имя;
		АдресХранилища = ПоместитьВоВременноеХранилище(СтрокаДокумента.ДанныеФайла);
		ПересозданныйДокумент = Документы[ИмяДокумента].СоздатьДокументПоОтчетуЯндекса(
			АдресХранилища, ПараметрыДокумента, ПараметрыДокумента.Ссылка.Дата);
		Если ЗначениеЗаполнено(ПересозданныйДокумент) Тогда
			СозданныеДокументы.Добавить(ПересозданныйДокумент);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СозданныеДокументы;
	
КонецФункции

#КонецОбласти

#Область ОтложенноеСопоставлениеНоменклатуры

Функция НастройкиСопоставленияНоменклатуры(ВидДокумента = Неопределено) Экспорт
	
	ВидимостьКолонокСопоставления = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ВидимостьКолонокСопоставления.Вставить("ЕдиницаИзмерения", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Упаковка", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Артикул", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Штрихкод", Ложь);
	
	ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
	ДополнительныйРеквизит.Имя = "КодМагазина";
	ДополнительныйРеквизит.Представление = "Ваш SKU";
	
	НовыеКолонкиСопоставления = Новый Массив;
	НовыеКолонкиСопоставления.Добавить(ДополнительныйРеквизит);
	
	ДополнительныеПараметрыПоиска = Новый Структура;
	ДополнительныеПараметрыПоиска.Вставить("Маркетплейс", Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	
	НастройкиСопоставления    = Новый Структура;
	НастройкиСопоставления.Вставить("ВидимостьКолонокСопоставления", ВидимостьКолонокСопоставления);
	НастройкиСопоставления.Вставить("ДополнительныеРеквизитыСопоставления", НовыеКолонкиСопоставления);
	НастройкиСопоставления.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметрыПоиска);
	НастройкиСопоставления.Вставить("ОтключитьПоискПоШтрихкодамКомбинаций", Истина);
	НастройкиСопоставления.Вставить("ОтключитьПоискПоСловарю", Истина);
	НастройкиСопоставления.Вставить("ОграничениеТипаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НастройкиСопоставления.Вставить("ТочностьПоискаПоУмолчанию", 100);
	
	Возврат НастройкиСопоставления;
	
КонецФункции

Функция ПодготовитьОтложенноеСопоставлениеНоменклатуры(Контрагент, НезаполненнаяНоменклатура) Экспорт
	
	НесопоставленнаяНоменклатура = Новый Массив;
	
	Для Каждого СтрокаКСопоставлению Из НезаполненнаяНоменклатура Цикл
		СтрокаТовара = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаКСопоставлению.СтрокаТовара);
		СтрокаТовара.Вставить("Наименование", СтрокаТовара.НаимТов);
		
		НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(Контрагент);
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, СтрокаТовара);
		
		НатуральныйИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(
			ВРег(СтрЗаменить(СтрокаТовара.Наименование, " ", ""))
			+ "#" + ВРег(СтрЗаменить(СтрокаТовара.КодМагазина, " ", "") + "#"));
		
		Если ЗначениеЗаполнено(СтрокаТовара.КодМагазина) Тогда
			НоменклатураКонтрагента.Идентификатор = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(СтрокаТовара.КодМагазина);
			НоменклатураКонтрагента.ИсторияИдентификаторов.Добавить(НатуральныйИД);
		Иначе
			НоменклатураКонтрагента.Идентификатор = НатуральныйИД;
		КонецЕсли;
		НоменклатураКонтрагента.ИдентификаторНоменклатуры = НоменклатураКонтрагента.Идентификатор;
		НоменклатураКонтрагента.Вставить("ИмяТабличнойЧасти", СтрокаКСопоставлению.ИмяТабличнойЧасти);
		НоменклатураКонтрагента.Вставить("ИндексТабличнойЧасти", СтрокаКСопоставлению.ИндексТабличнойЧасти);
		НесопоставленнаяНоменклатура.Добавить(НоменклатураКонтрагента);
		
	КонецЦикла;
	
	Возврат НесопоставленнаяНоменклатура;
	
КонецФункции

#КонецОбласти

#Область ОтложенноеСвязываниеДокументов

Процедура ПодготовитьНесозданныеДокументыОтчетаМаркетплейса(Форма) Экспорт
	
	Документ = Форма.Объект.Ссылка;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.ДанныеОтчета КАК ДанныеОтчета
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Документ = &Документ
	|	И НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)
	|	И Предупреждения.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НесозданыДокументы)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДанныеОтчета = Выборка.ДанныеОтчета.Получить();
		
		ДанныеНесозданныхДокументов = Новый Структура;
		ДанныеНесозданныхДокументов.Вставить("АдресХранилищаДереваСвязанныхДокументов",
			ПоместитьВоВременноеХранилище(ДанныеОтчета.ДеревоДокументов, Форма.УникальныйИдентификатор));
		ДанныеНесозданныхДокументов.Вставить("АдресХранилищаДанныеФайла",
			ПоместитьВоВременноеХранилище(ДанныеОтчета.ДанныеФайла, Форма.УникальныйИдентификатор));
		ДанныеНесозданныхДокументов.Вставить("ПараметрыДокумента", ДанныеОтчета.ПараметрыДокумента);
		
		Форма.ДанныеНесозданныхДокументов = ДанныеНесозданныхДокументов;
		Форма.ЕстьНесозданныеДокументы = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьРанееНесвязанныеДокументы(ПараметрыДокумента, ДанныеМаркетплейса, ДеревоДокументов) Экспорт
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	МенеджерОбработки = Обработки.ЗагрузкаОтчетаЯндексМаркет;
	
	СозданныеДокументы = Новый Массив;
	МенеджерОбработки.СоздатьДокументы(ПараметрыДокумента, ДанныеМаркетплейса, ДеревоДокументов, Ложь);
	МенеджерОбработки.ДокументыДерева(СозданныеДокументы, ДеревоДокументов.Строки);
	
	// Удаляем статус НесозданыДокументы
	Предупреждения.УдалитьПредупреждениеДляДокумента(ПараметрыДокумента.ОтчетМаркетплейса);
	
	ОбменСМаркетплейс.ПровестиДокументыАвтозагрузки(
		СозданныеДокументы,
		Перечисления.ВидыМаркетплейсов.ЯндексМаркет,
		ПараметрыДокумента.Организация);
	
	Предупреждения.ОбновитьДляОтчетовМаркетплейсов();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область СервисныеФункции

// Возвращает имя события журнала регистрации работы с торговой площадкой.
//
// Возвращаемое значение:
//   Строка - Наименование события для записей в журнале регистрации.
//
Функция СобытиеЖурналаРегистрации() Экспорт

	Возврат НСтр("ru = 'Интеграция с Яндекс Маркет'", 
				ОбщегоНазначения.КодОсновногоЯзыка());

КонецФункции

#КонецОбласти


#Область АвторизацияСлужебный

// Конструктор данных для авторизации приложения.
//
// Возвращаемое значение:
//   Структура - данные для авторизации приложения:
//     * IDПриложения     - Строка - идентификатор приложения;
//     * ПарольПриложения - Строка - пароль приложения.
//
Функция ДанныеПриложения()
	
	Результат = Новый Структура;	
	Результат.Вставить("IDПриложения",     "7482494a1165477da48a3839da02b565");
	Результат.Вставить("ПарольПриложения", "b59fc35da6fe463581be06cc2bafc3c9");
	
	Возврат Результат;
	
КонецФункции

// Определяет имя сервиса данных авторизации Яндекс.
//
// Возвращаемое значение:
//   Строка - имя сервера сервиса данных авторизации.
//
Функция СерверДанныхАвторизации()
	
	Результат = "login.yandex.ru";
	
	Возврат Результат;
	
КонецФункции

// Определяет имя сервиса партнерского API.
//
// Параметры:
//   ПоМагазину - Булево - определение имени сервиса для магазина (Истина, по умолчанию) или для кабинета (Ложь).
//
// Возвращаемое значение:
//   Строка - имя сервера сервиса.
//
Функция СерверПартнерскогоAPI(Дополнение = "")
	Возврат СтрШаблон("api.partner.market.yandex.ru/%1", Дополнение);
КонецФункции

#КонецОбласти

#Область УправлениеОстаткамиТоваровСлужебный

Функция НовыйStockItemDTO(Количество)
	Результат = Новый Структура;
	Результат.Вставить("count", Окр(Количество)); // integer<int64> количество доступного товара
	Результат.Вставить("type", "FIT"); // этот параметр пока не используется — единственное возможное значение FIT
	Результат.Вставить("updatedAt", // string<date-time> дата обновления остатков
		ЗаписатьДатуJSON(ТекущаяДатаСеанса(), ФорматДатыJSON.ISO, ВариантЗаписиДатыJSON.УниверсальнаяДата)); 
		
	Возврат Результат;
КонецФункции

Функция НовыйStockDTO()

	Результат = Новый Структура;
	Результат.Вставить("sku", ""); // string SKU товара
	Результат.Вставить("warehouseId", 0); // integer<int64> ID склада
	Результат.Вставить("items", Новый Массив);  // информация об остатках
		
	Возврат Результат;

КонецФункции 

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоответствиеТиповСкладов()
	СоответствиеТиповСкладов = Новый Соответствие;
	СоответствиеТиповСкладов.Вставить("FBS", Перечисления.ТипыСкладовМаркетплейс.FBS);
	СоответствиеТиповСкладов.Вставить("FBO", Перечисления.ТипыСкладовМаркетплейс.FBO);
	СоответствиеТиповСкладов.Вставить("DBS", Перечисления.ТипыСкладовМаркетплейс.DBS);
	
	Возврат СоответствиеТиповСкладов;
КонецФункции

Функция ЗапроситьОтчетаОПродажах(ПараметрыОтчета, КодОтветаСервера) 
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("campaignId", Число(ПараметрыОтчета.ИдентификаторСклада));
	ДанныеЗапроса.Вставить("year",       Год(ПараметрыОтчета.НачалоПериода));
	ДанныеЗапроса.Вставить("month",      Месяц(ПараметрыОтчета.НачалоПериода));
	
	АтрибутыРезультата = Новый структура;
	АтрибутыРезультата.Вставить("ИдентификаторЗапроса", "result.reportId");
	АтрибутыРезультата.Вставить("ОжидаемаяПродолжительность", "result.estimatedGenerationTime");
	
	РезультатВыполнения = ВыполнитьPOSTЗапрос("reports/goods-realization/generate?format=FILE", ДанныеЗапроса, АтрибутыРезультата, ПараметрыОтчета.НастройкаИнтеграции);
	
	КодОтветаСервера = РезультатВыполнения.КодОтветаСервера;
	
	Если НЕ РезультатВыполнения.Результат Тогда
		СообщениеОбОшибке = РезультатВыполнения.СообщениеОбОшибке;
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторЗапроса", РезультатВыполнения.ИдентификаторЗапроса);
	Результат.Вставить("ДатаОкончанияФормирования", ТекущаяДатаСеанса() + РезультатВыполнения.ОжидаемаяПродолжительность/1000);
	
	Возврат Результат;
КонецФункции

// Формирует текст ошибки по результатам работы метода сервиса.
//
// Параметры:
//   Ошибки - Массив Из Структура - ошибки сервиса:
//     * code    - Строка - код ошибки;
//     * message - Строка - описание ошибки.
//
// Возвращаемое значение:
//   Строка - описание ошибок.
//
Функция ТекстОшибки(Ошибки)
	
	Результат = "";
	
	Для каждого ЭлементКоллекции Из Ошибки Цикл
		Результат = Результат + "(" + ЭлементКоллекции.code + ") " + ЭлементКоллекции.message + "; ";
	КонецЦикла;
		
	Возврат СокрЛП(Результат);
	
КонецФункции

// Проверяет, является ли строка целым неотрицательным числом	
// Параметры:
//  ПроверяемаяСтрока - Строка - проверяемый строка.
//
// Возвращаемое значение:
//  Булево - Истина, если строка содержит только цифры.
//
Функция ЭтаСтрокаЯвляетсяЦелымНеотрицательнымЧислом(ПроверяемаяСтрока) Экспорт
	
	СтрокаЯвляетсяЧислом = Истина;
	ПроверяемаяСтрока = СокрЛП(ПроверяемаяСтрока);
	Длина = СтрДлина(ПроверяемаяСтрока);
	
	Для НомерСимвола = 1 По Длина Цикл
		Симв = Сред(ПроверяемаяСтрока, НомерСимвола, 1);
		Если Найти("0123456789", Симв) = 0 И КодСимвола(Симв)<>160 Тогда
			СтрокаЯвляетсяЧислом = Ложь;		
		КонецЕсли;		
	КонецЦикла;
		
	Возврат СтрокаЯвляетсяЧислом;
	
КонецФункции

Функция ВыполнитьPOSTЗапрос(АдресРесурса = "", ДанныеЗапроса = Неопределено, АтрибутыРезультата = Неопределено, НастройкаИнтеграции)
	Возврат ВыполнитьHTTPЗапрос(АдресРесурса, ДанныеЗапроса, АтрибутыРезультата, НастройкаИнтеграции, "POST")
КонецФункции

Функция ВыполнитьPUTЗапрос(АдресРесурса = "", ДанныеЗапроса = Неопределено, НастройкаИнтеграции)
	
	Возврат ВыполнитьHTTPЗапрос(АдресРесурса, ДанныеЗапроса, Неопределено, НастройкаИнтеграции, "PUT");
	
КонецФункции

Функция ВыполнитьGETЗапрос(АдресРесурса = "", ДанныеЗапроса = Неопределено, АтрибутыРезультата, НастройкаИнтеграции, Логирование = Неопределено)
	
	Возврат ВыполнитьHTTPЗапрос(АдресРесурса, ДанныеЗапроса, АтрибутыРезультата, НастройкаИнтеграции, "GET", Логирование)
	
КонецФункции

Функция ВыполнитьHTTPЗапрос(АдресРесурса, ДанныеЗапроса, АтрибутыРезультата, НастройкаИнтеграции, HTTPМетод, Логирование = Неопределено)
	
	РезультатЗапроса = Новый Структура;
	
	РезультатЗапроса.Вставить("Результат",         Ложь);
	РезультатЗапроса.Вставить("СообщениеОбОшибке", "");
	РезультатЗапроса.Вставить("КодОтветаСервера",  Неопределено);
	ДанныеПриложения  = ДанныеПриложения();
	
	ДанныеАвторизации = ПолучитьДанныеАвторизации(НастройкаИнтеграции);
	Если ДанныеАвторизации = Неопределено Тогда
		РезультатЗапроса.СообщениеОбОшибке = НСтр("ru = 'Не указаны данные для подключения к Яндекс.Маркет'");
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	ПараметрыИнтеграции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаИнтеграции, 
		"ИдентификаторКабинета,ИдентификаторСклада");
	
	Если СтрНайти(АдресРесурса, "{businessId}") > 0 Тогда
		// Запрос по кабинету в целом
		Сервер = СерверПартнерскогоAPI("businesses");
		АдресРесурса = СтрЗаменить(АдресРесурса, "{businessId}", ПараметрыИнтеграции.ИдентификаторКабинета);
	ИначеЕсли СтрНачинаетсяС(АдресРесурса, "reports") Тогда
		Сервер = СерверПартнерскогоAPI();
	Иначе
		// Запрос по магазину (складу)
		Сервер = СерверПартнерскогоAPI("campaigns");
		АдресРесурса = СтрЗаменить(АдресРесурса, "{campaignId}", ПараметрыИнтеграции.ИдентификаторСклада);
	КонецЕсли;
	
	ИнтернетПрокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	
	HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type",  "application/json");
	Если ДанныеАвторизации.Свойство("ApiKey") Тогда
		Заголовки.Вставить("Api-Key", ДанныеАвторизации.ApiKey);
	Иначе
		Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
	КонецЕсли;
	Заголовки.Вставить("X-Market-Integration", "vendor_1c_bp3");
	
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Если ДанныеЗапроса <> Неопределено Тогда
		ТелоЗапроса = ОбменСМаркетплейс.ВJSON(ДанныеЗапроса);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.UTF8);
	КонецЕсли;
	
	Попытка
		HTTPОтвет = HTTPСоединение.ВызватьHTTPМетод(HTTPМетод, HTTPЗапрос);
	Исключение 
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
			
		РезультатЗапроса.СообщениеОбОшибке = ТекстОшибки;
		РезультатЗапроса.Вставить("КодОтветаСервера", 500);
		Возврат РезультатЗапроса;
	КонецПопытки;
	
	//Логирование
	ЗаписыватьРезультат = Истина;
	Если Не Логирование = Неопределено Тогда
		ЗаписыватьРезультат = Ложь;
	КонецЕсли;
	
	Если РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЛогированиеВключено() Тогда
		Логирование = РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЛогирования(НастройкаИнтеграции);
		Если HTTPСоединение  = Неопределено Тогда
			Логирование.АдресСервера 	= Сервер;
		Иначе
			Логирование.АдресСервера 	= HTTPСоединение.Сервер + ":" + HTTPСоединение.Порт;
			Логирование.Соединение 		= Истина;
			Логирование.Метод 			= HTTPМетод;
			Логирование.Запрос 			= HTTPЗапрос;
			Логирование.Ответ 			= HTTPОтвет;
		КонецЕсли;
		Если ЗаписыватьРезультат Тогда
			РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЗаписатьВЖурнал(Логирование);
		КонецЕсли;
    КонецЕсли;
	//Конец Логирование
	
	Попытка
		ДанныеОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(HTTPОтвет);
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора ответа от сервера %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), 
			Сервер + АдресРесурса);
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
			
		РезультатЗапроса.СообщениеОбОшибке = ТекстОшибки;
		
		Возврат РезультатЗапроса;
	КонецПопытки;
	
	СтатусЗапроса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОтвета, "status", "OK");
	КодСостояния = HTTPОтвет.КодСостояния;
	
	РезультатЗапроса.Вставить("КодОтветаСервера", КодСостояния);
	
	РезультатЗапроса.Вставить("Результат",          КодСостояния = 200 И СтатусЗапроса = "OK");
	
	Если КодСостояния = 401 Тогда
		РезультатЗапроса.СообщениеОбОшибке = НСтр("ru = 'Ошибка авторизации в API Яндекс.Маркет. Проверьте, что пользователь имеет доступ к магазину.'");
	ИначеЕсли КодСостояния = 403 Тогда
		РезультатЗапроса.СообщениеОбОшибке = НСтр("ru = 'Ошибка авторизации в API Яндекс.Маркет. Проверьте, что пользователь имеет право на запрошенные действия.'");
	КонецЕсли;
	
	РезультатЗапроса.КодОтветаСервера = КодСостояния;
	Если НЕ РезультатЗапроса.Результат Тогда
		СообщенияОбОшибках = 
			?(СтатусЗапроса = "ERROR", ТекстОшибки(ДанныеОтвета.errors), "");
			
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка выполнения запроса %1: %2%3'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), 
			Сервер + АдресРесурса, 
			СообщенияОбОшибках,
			HTTPОтвет.ПолучитьТелоКакСтроку());
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			ТекстОшибки, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	ИначеЕсли ЗначениеЗаполнено(АтрибутыРезультата) И ДанныеОтвета <> Неопределено Тогда
		ДанныеОтветаСтруктура = ОбменСМаркетплейс.РазобратьДанныеОтвета(ДанныеОтвета, АтрибутыРезультата);
		ОбщегоНазначенияклиентСервер.ДополнитьСтруктуру(РезультатЗапроса, ДанныеОтветаСтруктура);
	Иначе
		РезультатЗапроса.Вставить("ДанныеОтвета", ДанныеОтвета);
	КонецЕсли;
	
	Возврат РезультатЗапроса;
	
КонецФункции

// Функция возвращает http запрос для получения ключей доступа к сервису по временному коду.
//
// Параметры:
//   ВременныйКод - Строка - временный код для подключения к сервису.
//
// Возвращаемое значение:
//   HTTPЗапрос - запрос для получения ключей доступа.
//
Функция ЗапросПолучитьТокеныПоКоду(ВременныйКод)
	
	ДанныеПриложения = ДанныеПриложения();
	
    Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");

	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить("grant_type=authorization_code");
	ПараметрыURL.Добавить("code=" + ВременныйКод);
    ПараметрыURL.Добавить("client_id=" + ДанныеПриложения.IDПриложения);    
    ПараметрыURL.Добавить("client_secret=" + ДанныеПриложения.ПарольПриложения);
	
	Адрес = "/token?"; 
    HTTPЗапрос = Новый HTTPЗапрос(Адрес, Заголовки);
	Тело = СтрСоединить(ПараметрыURL, "&");
	
	HTTPЗапрос.УстановитьТелоИзСтроки(Тело,"windows-1251");
    
	Возврат HTTPЗапрос;
	
КонецФункции 

Процедура ОбновитьДанныеФайлаЯндекс(ДанныеФайла)
	
	ДанныеДокумента = ДанныеФайла.ДанныеДокумента;
	СуммаПродаж = ДанныеДокумента.ТаблицаТоваров.Итог("Сумма");
	
	Если ДанныеДокумента.ВидДокумента = "ОтчетЯндекс_Отгружено" Тогда
		СтраницаОтчета = НСтр("ru='Товары, переданные в доставку'");
	ИначеЕсли ДанныеДокумента.ВидДокумента = "ОтчетЯндекс_Доставлено" Тогда
		СтраницаОтчета = НСтр("ru='Доставленные товары'");
	ИначеЕсли ДанныеДокумента.ВидДокумента = "ОтчетЯндекс_Невыкуплено" Тогда
		СтраницаОтчета = НСтр("ru='Невыкупленные товары'");
	ИначеЕсли ДанныеДокумента.ВидДокумента = "ОтчетЯндекс_Возвращено" Тогда
		СтраницаОтчета = НСтр("ru='Возвращённые товары'");
	КонецЕсли;
	
	Описание = СтрШаблон(НСтр("ru='Отчет Яндекс от %1
		|%2: %3'"), Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy"), Формат(СтраницаОтчета, "ЧДЦ=2; ЧН="), Формат(СуммаПродаж, "ЧДЦ=2; ЧН="));
			
	ДанныеФайла.СуммаПродаж = СуммаПродаж;
	ДанныеФайла.Описание    = Описание;
	
КонецПроцедуры

#КонецОбласти

#Область НоваяАвторизацияЯндексМаркет

Процедура СохранитьТокенАПИ(Организация, ApiKey) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Организация, ApiKey, "ApiKey");
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПолучитьТокенАПИ(Организация) Экспорт
	
	ApiKey = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	ApiKey = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Организация, "ApiKey", Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ApiKey;
	
КонецФункции

Процедура УдалитьТокенАПИ(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Организация, "ApiKey")) Тогда
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(Организация, "ApiKey");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли