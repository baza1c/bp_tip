#Область ПрограммныйИнтерфейс

// Возвращает настройки для баннера при смене объекта УСН
//
// Параметры:
//   Организация - СправочникСсылка.Организации - ссылка на проверяемую организацию
// 
// Возвращаемое значение:
//   Структура - см. НовыеНастройкиДляБаннераСменыОбъектаУСН
//
Функция НастройкиДляБаннераСменыОбъектаУСН(Организация) Экспорт
	// Запрос должен получить год учета, признак объекта (доход или расход) и признак напоминания
	// Можно было бы это сделать из одного регистра НастройкиУчетаУСН, но в нем нет признака объекта
	// в нем есть только ставка, а ставка подвержена изменению, поэтому связываем с регистром НастройкиСистемыНалогообложения
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиСистемыНалогообложенияСрезПоследних.Период КАК Период,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНДоходы КАК ПрименяетсяУСНДоходы
		|ПОМЕСТИТЬ втСистемаНалогообложения
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(
		|			&ДатаФормирования,
		|			Организация = &Организация
		|				И (ПрименяетсяУСНДоходы
		|					ИЛИ ПрименяетсяУСНДоходыМинусРасходы)) КАК НастройкиСистемыНалогообложенияСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиУчетаУСНСрезПоследних.НапоминатьОВведенииОстатков КАК НапоминатьОВведенииОстатков
		|ПОМЕСТИТЬ втУчетУСН
		|ИЗ
		|	РегистрСведений.НастройкиУчетаУСН.СрезПоследних(&ДатаФормирования, Организация = &Организация) КАК НастройкиУчетаУСНСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСистемаНалогообложения.Период КАК Период,
		|	втСистемаНалогообложения.ПрименяетсяУСНДоходы КАК ПрименяетсяУСНДоходы,
		|	втУчетУСН.НапоминатьОВведенииОстатков КАК НапоминатьОВведенииОстатков
		|ИЗ
		|	втСистемаНалогообложения КАК втСистемаНалогообложения,
		|	втУчетУСН КАК втУчетУСН";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаФормирования", ТекущаяДатаСеанса());
	ПериодыПерехода = Запрос.Выполнить().Выбрать();
	
	Если ПериодыПерехода.Следующий() Тогда
		Настройки = НовыеНастройкиДляБаннераСменыОбъектаУСН();
		Настройки.Год         = Формат(ПериодыПерехода.Период, "ДФ=yyyy");
		Настройки.ОбъектДоход = ПериодыПерехода.ПрименяетсяУСНДоходы;
		Настройки.Напоминать  = ПериодыПерехода.НапоминатьОВведенииОстатков;
		
		Возврат Настройки;
	КонецЕсли;
	
КонецФункции

// Возвращает настройки для баннера при переходе с УСН на ОСНО
//
// Параметры:
//   Организация - СправочникСсылка.Организации - ссылка на проверяемую организацию
// 
// Возвращаемое значение:
//   Структура - см. НовыеНастройкиДляБаннераПереходаСУСННаОСНО
//
Функция НастройкиДляБаннераПереходСУСННаОСНО(Организация) Экспорт
	
#Если Не МобильноеПриложениеСервер Или МобильноеПриложениеКлиент Тогда
	ДатыПерехода = Обработки.ПомощникПереходаСУСННаОСНО.ДатыПереходаСУСННаОСНО(Организация);
	ТекущаяДата = ТекущаяДатаСеанса();
	ЭлементТекущийГод = ДатыПерехода.НайтиПоЗначению(КонецГода(ТекущаяДата));
	ЭлементПрошлыйГод = ДатыПерехода.НайтиПоЗначению(НачалоГода(ТекущаяДата) - 1);
	
	Если ЭлементТекущийГод <> Неопределено Тогда
		ДатаПерехода = ЭлементТекущийГод.Значение;
	ИначеЕсли ЭлементПрошлыйГод <> Неопределено Тогда
		ДатаПерехода = ЭлементПрошлыйГод.Значение;
	Иначе
		ДатаПерехода = '00010101';
	КонецЕсли;
#Иначе
	ДатаПерехода = '00010101';
#КонецЕсли
	
	Настройки = НовыеНастройкиДляБаннераПереходаСУСННаОСНО();
	Настройки.ПоказыватьБаннер = ЗначениеЗаполнено(ДатаПерехода);
	Настройки.ГодПерехода = Формат(ДатаПерехода + 1, "ДФ=yyyy");
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыПериодаСистемыНалогообложения(Организация, СистемаНалогообложения) Экспорт
	
	Параметры = НовыйПараметрыПериодаСистемыНалогообложения();
	Параметры.ИспользуетсяДатаИзменения = ИспользуетсяДатаИзменения(Организация, СистемаНалогообложения);
	Параметры.ПереходСНачалаГода =
		НастройкиСистемыНалогообложенияФормыКлиентСервер.ПереходНаСистемуНалогообложенияСНачалаГода(
			СистемаНалогообложения);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИспользуетсяДатаИзменения(Организация, СистемаНалогообложения = Неопределено, Период = Неопределено) Экспорт
	
	Возврат НастройкиСистемыНалогообложенияФормы.ИспользуетсяДатаИзменения(Организация, СистемаНалогообложения, Период);
	
КонецФункции

Функция НовыеНастройкиДляБаннераСменыОбъектаУСН()
	Настройки = Новый Структура;
	Настройки.Вставить("Год", "");
	Настройки.Вставить("ОбъектДоход", Ложь);
	Настройки.Вставить("Напоминать", Ложь);
	
	Возврат Настройки;
КонецФункции

Функция НовыйПараметрыПериодаСистемыНалогообложения()
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользуетсяДатаИзменения", Ложь);
	Результат.Вставить("ПереходСНачалаГода", Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция НовыеНастройкиДляБаннераПереходаСУСННаОСНО()
	Настройки = Новый Структура;
	Настройки.Вставить("ПоказыватьБаннер", Ложь);
	Настройки.Вставить("ГодПерехода", "");
	
	Возврат Настройки;
КонецФункции

#КонецОбласти