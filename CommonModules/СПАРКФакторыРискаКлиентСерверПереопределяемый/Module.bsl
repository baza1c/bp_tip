///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "СПАРКРиски".
// ОбщийМодуль.СПАРКФакторыРискаКлиентСерверПереопределяемый.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выводит информацию о факторах риска в элемент управления.
// В случае, если информации нет в кэше, то инициируется фоновое задание.
//
// Параметры:
//  ФакторыРиска                - Структура - ключи описаны в СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
//  КонтрагентОбъект            - Объект, Неопределено - заполняется в том случае, если форма - это форма
//                                элемента справочника, а не форма документа.
//  Контрагент                  - ОпределяемыйТип.КонтрагентБИП, Строка - Контрагент или ИНН контрагента;
//  Форма                       - ФормаКлиентскогоПриложения - форма, в которой необходимо вывести
//                                информацию о факторах риска СПАРК.
//  ИспользованиеРазрешено      - Булево - признак разрешения использования функциональности;
//  Параметры                   - Структура - См СПАРКРискиКлиентСервер.ПараметрыОтображенияДанныхСПАРК;
//  СтандартнаяОбработка        - Булево - если вернуть сюда Ложь, то стандартная обработка не будет происходить.
//
Процедура ОтобразитьФакторыРиска(
			ФакторыРиска,
			КонтрагентОбъект,
			Контрагент,
			Форма,
			ИспользованиеРазрешено,
			Параметры,
			СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Элементы = Форма.Элементы;
	ИндексыСПАРКРиски = Форма.ИндексыСПАРКРиски;
	Если Не (Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаЭлемента"
		    И ЗначениеЗаполнено(ФакторыРиска)
			И Не ЗначениеЗаполнено(ФакторыРиска.ВидОшибки)
			И ЗначениеЗаполнено(ИндексыСПАРКРиски)
			И Не ЗначениеЗаполнено(ИндексыСПАРКРиски.ВидОшибки)
			И ИндексыСПАРКРиски.Активен) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСПАРКФакторыРиска", "Видимость", Ложь);
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСПАРКФакторыРиска", "Видимость", Истина);
	НегативныеСписки = ФакторыРиска.НегативныеСписки;
	Если КонтрагентОбъект.ЮридическоеФизическоеЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо") Тогда
		ФакторыРискаОбщие = ФакторыРиска.ФакторыРискаЮридическогоЛица;
		ФакторыРиска115ФЗ = ФакторыРиска.ФакторыРискаЮридическогоЛицаПо115ФЗ;
	Иначе
		ФакторыРискаОбщие = ФакторыРиска.ФакторыРискаФизическогоЛица;
		ФакторыРиска115ФЗ = ФакторыРиска.ФакторыРискаФизическогоЛицаПо115ФЗ;
	КонецЕсли;
	
	НегативныеСпискиКоличество = ?(НегативныеСписки = Неопределено, 0, НегативныеСписки.Количество());
	ФакторыРискаОбщиеКоличество = ?(ФакторыРискаОбщие = Неопределено, 0, ФакторыРискаОбщие.Количество());
	ФакторыРиска115ФЗКоличество = ?(ФакторыРиска115ФЗ = Неопределено, 0, ФакторыРиска115ФЗ.Количество());
	ВсегоФакторовРиска = НегативныеСпискиКоличество + ФакторыРискаОбщиеКоличество + ФакторыРиска115ФЗКоличество;
	
	ЦветаСтилей = СПАРКРискиВызовСервера.ЦветаСтилей();
	
	Если ВсегоФакторовРиска = 0 Тогда
		ЦветФактора = ЦветаСтилей.ЦветГрадацияСПАРКНизкийРиск;
	ИначеЕсли НегативныеСпискиКоличество >= 3
			Или ФакторыРискаОбщиеКоличество >= 5
			Или ФакторыРиска115ФЗКоличество >= 5 Тогда
		ЦветФактора = ЦветаСтилей.ЦветОсобогоТекста;
	Иначе
		ЦветФактора = ЦветаСтилей.ЦветГрадацияСПАРКСреднийРиск;
	КонецЕсли;
	
	ТекстЗначенияФактора = "";
	Если ФакторыРиска.ОжиданиеОтвета = Ложь Тогда
		ТекстЗначенияФактора = ?(ВсегоФакторовРиска = 0, НСтр("ru = 'Нет'"),
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = '; %1 фактор; ; %1 фактора; %1 факторов; %1 фактора'"), ВсегоФакторовРиска));
	КонецЕсли;
	ЗначениеФактора = Новый ФорматированнаяСтрока(ТекстЗначенияФактора,, ЦветФактора,, "SPARK:ShowExtendedInformation");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ДекорацияСПАРКФакторыРискаЗаголовок", "Заголовок", НСтр("ru = 'Факторы риска:'"));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ДекорацияСПАРКФакторыРискаЗначение", "Заголовок", ЗначениеФактора);

КонецПроцедуры

#КонецОбласти

