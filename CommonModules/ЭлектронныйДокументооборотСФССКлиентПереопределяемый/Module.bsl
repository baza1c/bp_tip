#Область ПрограммныйИнтерфейс

#Область СЭДО

// Вызывается после получении сообщений с сервера СЭДО СФР.
//
// Параметры:
//   РезультатПолучения                     -  Структура:
//      * БылиОшибки           - Булево - если Истина, то при выполнении операции возникали ошибки.
//      * ОшибкиПоОрганизациям - Соответствие - возвращаются массивы описаний ошибок в разрезе по организациям.
//      * ДанныеСообщенийПоОрганизациям - Соответствие - ключ соответствия организация, значение - массив структур с полями:
//        * Идентификатор           - Строка - идентификатор сообщения.
//        * Тип                     - Строка  - тип сообщения согласно спецификации.
//        * Получатель              - Строка - идентификатор получателя.
//        * ТребуетсяПодтверждение  - Булево - требуется подтверждение о прочтении сообщения.
//        * Содержимое              - Строка - текстовое содержимое сообщения.
//        * Новое                   - Булево - признак того, что это это новое сообщение, данные которого ещё не были загружены.
//        * РегистрационныйНомерСФР - Строка - регистрационный номер СФР организации страхователя.
//      * РезультатыПоОрганизациям - Соответствие - ключ соответствия организация, значение - массив структур с полями:
//        * Выполнено            - Булево - признак успешного выполнения операции.
//        * ОписаниеОшибки       - Строка - содержит описание ошибки в случае, если Выполнено установлено в Ложь.
//        * ДанныеСообщения      - Структура - см. описание структуры ДанныеСообщенийПоОрганизациям.
//        * Идентификатор        - Строка - идентификатор сообщения.
//
Процедура ПослеПолученияСообщенийСЭДО(РезультатПолучения) Экспорт
	
	Если Не УчетЗарплатыВызовСервера.ТекущемуПользователюДоступныТекущиеДелаПоСЭДО() Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьТекущиеДелаПоСЭДО = Ложь;
	Для Каждого КлючЗначение Из РезультатПолучения.ДанныеСообщенийПоОрганизациям Цикл
		Для Каждого Значение Из КлючЗначение.Значение Цикл
			ТипЧислом = ЭлектронныйДокументооборотСФССКлиентСервер.ТипСообщенияСЭДОВЧисло(Значение.Тип);
			Если (ТипЧислом >= 100 И ТипЧислом <=124)
				Или (ТипЧислом = 10 Или ТипЧислом = 84 Или ТипЧислом = 85 Или ТипЧислом = 320 Или ТипЧислом = 321) Тогда
				ПоказатьТекущиеДелаПоСЭДО = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ПоказатьТекущиеДелаПоСЭДО Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПоказатьТекущиеДелаПоСЭДО Тогда
		ОткрытьФорму("Обработка.ОбщиеФормыСЭДОФСС.Форма.ТекущиеДелаПоСЭДО", , , Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти