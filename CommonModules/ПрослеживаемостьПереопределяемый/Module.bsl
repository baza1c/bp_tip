
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытийДокументов

// Вызывается при вводе документа (Уведомление об остатках, Уведомление о ввозе, 
// Уведомление о перемещении) на основании,
// при выполнении метода Заполнить или при интерактивном вводе нового.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - заполняемый документ,
//  ДанныеЗаполнения - Произвольный - значение, которое используется как основание для заполнения,
//  ТекстЗаполнения - Строка, Неопределено - текст, используемый для заполнения документа,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполненияДокумента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	
	
КонецПроцедуры

// Предоставляет возможность переопределить обработку заполнения документа "Уведомление о перемещении"
//
// Параметры:
//  УведомлениеОбъект    - ДокументОбъект.УведомлениеОперемещенииПрослеживаемыхТоваров - документ, для которого
//                                                                                        выполняется заполнение.
//  ДанныеЗаполнения - Произвольный - см. описание параметра в синтаксис-помощнике к обработчику ОбработкаЗаполнения.
//  ТекстЗаполнения - Строка - см. описание параметра в синтаксис-помощнике к обработчику ОбработкаЗаполнения.
//  СтандартнаяОбработка - Булево - см. описание параметра в синтаксис-помощнике к обработчику ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполненияУведомленияОПеремещении(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ЗаполнениеДокументов.Заполнить(УведомлениеОбъект, ДанныеЗаполнения);
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) 
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("Структура")  Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров")
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		ПрослеживаемостьБП.ЗаполнитьКорректировочноеУведомлениеОПеремещенииПрослеживаемыхТоваров(
			УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	Иначе
	
		ПрослеживаемостьБРУ.ЗаполнитьУведомлениеОПеремещенииПрослеживаемыхТоваров(
			УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
		
	ОбработкаЗаполненияДокумента(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
КонецПроцедуры

// Вызывается расширением формы при необходимости проверки заполнения реквизитов при записи или при проведении документа в форме,
// а также при выполнении метода ПроверитьЗаполнение.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - проверяемый документ,
//  Отказ - Булево - признак отказа от проведения документа,
//  ПроверяемыеРеквизиты - Массив - массив путей к реквизитам, для которых будет выполнена проверка заполнения,
//  МассивНепроверяемыхРеквизитов - Массив - массив путей к реквизитам, для которых не будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(ДокументОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Переопределяемая процедура обработки проведения документов 
// "Уведомление об остатках прослеживаемых товаров" и "Уведомление о ввозе прослеживаемых товаров"
// 
// Параметры:
//  ПараметрыПроведения - Структура таблиц см. Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения и 
//                        Документы.УведомлениеОВвозеПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения
//  Движения - ссылка на движения документа
//  Отказ - Булево - признак отказа от записи
//
Процедура ОбработкаПроведенияУведомление(ПараметрыПроведения, Движения, Отказ) Экспорт
	
	Параметры = ПодготовитьПараметрыТаблицыРегистрацииПрослеживаемыхТоваров(
		ПараметрыПроведения.Реквизиты, 
		ПараметрыПроведения.ТаблицаТоваров, 
		ПараметрыПроведения.ТаблицаОС);
		
	Реквизиты = Параметры.Реквизиты[0];
	
	РегистрыСведений.РегистрацияПрослеживаемыхТоваров.ОбновитьСостояние(Реквизиты.Регистратор, Реквизиты.Основание);
	
	ПрослеживаемостьБП.ОбработкаПроведенияУведомления(Параметры, Движения, Отказ);
			
КонецПроцедуры

// Переопределяемая процедура обработки удаления проведения документов
// "Уведомление об остатках прослеживаемых товаров" и "Уведомление о ввозе прослеживаемых товаров"
// 
// Параметры:
//  Регистратор - ДокументСсылка - документ регистратор
//  Основание - ДокументСсылка - основание регистратора
//
Процедура ОбработкаУдаленияПроведенияУведомление(Регистратор, Основание) Экспорт
	
	РегистрыСведений.РегистрацияПрослеживаемыхТоваров.ОбновитьСостояние(Регистратор, Основание, Истина);
	
КонецПроцедуры

// Переопределяемая процедура обработки проведения документа Уведомление о перемещении
// 
// Параметры:
//  ПараметрыПроведения - Структура таблиц см. Документы.УведомлениеОПеремещении.ПодготовитьПараметрыПроведения
//  Движения - ссылка на движения документа
//  Отказ - Булево - признак отказа от записи
//
Процедура ОбработкаПроведенияУведомлениеОПеремещении(ПараметрыПроведения, Движения, Отказ) Экспорт
	
	
КонецПроцедуры

// Возникает перед выполнением записи документа (Уведомление об остатках, Уведомление о ввозе, 
// Уведомление о перемещении). Вызывается после начала транзакции записи, но до начала записи документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - записываемый документ,
//  Отказ - Булево - признак отказа от записи,
//  РежимЗаписи - РежимЗаписиДокумента - текущий режим записи документа,
//  РежимПроведения - РежимПроведенияДокумента - текущий режим проведения документа.
Процедура ПередЗаписью(ДокументОбъект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияПрослеживаемогоТовара

// Функция возвращает параметры документа основания
// 
// Параметры:
// ДокументОснования - ДокументСсылка - документ основание
//
// Возвращаемое значение:
// - Структура - параметры документа
//
//
Функция ПолучитьПараметрыДокументаОснования(ДокументОснования) Экспорт
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснования, "Дата, Организация");
	
КонецФункции

// Проверяет необходимость обновления регистра сведений Регистрация прослеживаемого товара
//
// Параметры:
// ДокументОснование - ДокументСсылка - Документ, по которому будет формироваться запись в регистре и расчет остатка 
// Период - Дата - Дата документа основания
// 
// Возвращаемое значение:
// Булево - Истина если не надо обновлять запись в регистре
//
Функция НеОбновлятьСостояние(ДокументОснование, Период) Экспорт
	
	Если Не ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Период) Тогда
		
		// Очистим регистр по основанию, если пользователь поменял дату документа основания 
		// и дата не попадает в период ведения прослеживаемости
		Набор = РегистрыСведений.РегистрацияПрослеживаемыхТоваров.СоздатьНаборЗаписей();
		Набор.Отбор.Основание.Установить(ДокументОснование);
		Набор.Записать(Истина);
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Считает количество товара по которому необходимо сделать запись в регистре Регистрация прослеживаемого товара
//
// Параметры:
// ДокументОснование - ДокументСсылка - Документ, по которому будет формироваться запись в регистре и расчет остатка 
// ПараметрыДокументаОснования - Структура - 
//												Дата - Дата - дата документа основания
//							 					Организация - СправочникСсылка.Организация - организация 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица готовых записей в регистр Регистрация прослеживаемого товара
//
Функция ПолучитьТаблицуДляОбновленияРегистрацииПрослеживаемогоТовара(ДокументРегистратор, ДокументОснование, УдалениеПроведения, ПараметрыДокументаОснования) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Основание", ДокументОснование);
	Запрос.УстановитьПараметр("НачалоПериода", ПрослеживаемостьБРУ.ДатаНачалаУчетаПрослеживаемыхТоваров());
	Запрос.УстановитьПараметр("КонецПериода", ПараметрыДокументаОснования.Дата);
	Запрос.УстановитьПараметр("ПустаяСсылкаРнпт", ПустоеЗначениеРНПТ());
	Запрос.УстановитьПараметр("ПустоеЗначениеНоменклатуры", Справочники.Номенклатура.ПустаяСсылка());

	Запрос.УстановитьПараметр("Организация", ПараметрыДокументаОснования.Организация);
	Запрос.УстановитьПараметр("ЭтоКорректировка", 
		ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаПоступления")
		ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровПоставщику"));
	Запрос.УстановитьПараметр("ДокументИсключение",
		?(УдалениеПроведения, ДокументРегистратор, Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПустаяСсылка()));
		
	Запрос.Текст = ТекстЗапросаПоОснованию(ДокументОснование, ПараметрыДокументаОснования)
				+ ТекстЗапросаПоУведомлениямИВычислениюОстатка();
				
	УстановитьПривилегированныйРежим(Истина);
				
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

Процедура СформироватьДвиженияКорректировкиПрослеживаемыхТоваровВЕАЭС(ТаблицаТовары,
		ТаблицаРеквизиты, Движения) Экспорт
	
	ПрослеживаемостьБП.СформироватьДвиженияКорректировкиПрослеживаемыхТоваровВЕАЭС(ТаблицаТовары, 
		ТаблицаРеквизиты, Движения);

КонецПроцедуры

// Считает количество комплектующих в составе ОС
//
// Параметры:
// ПараметрыТоваров - Структура - 
//                             - Организация - СправочникСсылка.Организация - Организация
//                             - ОсновноеСредство - СправочникСсылка.ОсновноеСредство - Основное средство 
// 
// Возвращаемое значение:
//  Число - количество комплектующих в составе ОС
//
Функция КоличествоТоваровВСоставеОС(ПараметрыТоваров) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ПараметрыТоваров.Организация );
	Запрос.УстановитьПараметр("ОсновноеСредство", ПараметрыТоваров.ОсновноеСредство );
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК ОсновноеСредство,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Количество) КАК КоличествоОбъектов
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|		ПО (РегистрацияПрослеживаемыхТоваров.ОсновноеСредство = ОсновныеСредства.Ссылка)
	|ГДЕ
	|	ОсновныеСредства.Ссылка = &ОсновноеСредство
	|	И РегистрацияПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.РегистрацияКомплектующихОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновныеСредства.Ссылка";
	
	КоличествоКомплектующихВОС = 0;
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		
		КоличествоКомплектующихВОС = Результат.КоличествоОбъектов;
		
	КонецЕсли;
	
	Возврат КоличествоКомплектующихВОС;
	
КонецФункции

#Конецобласти

#Область Уведомления

// Формирует и возвращает текст запроса для выборки данных
// необходимых для формирования движений документа
//
// Параметры:
//  НомераТаблиц - Число - Структура с таблицами параметров проведения
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаТаблицаТоваровУведомлениеОбОстатках(НомераТаблиц, ДокументСсылка) Экспорт
	
	НомераТаблиц.Вставить("ТаблицаТоваров", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Уведомление.Ссылка КАК Ссылка,
	|	Уведомление.НомерСтроки КАК НомерСтроки,
	|	Уведомление.Номенклатура КАК Номенклатура,
	|	Уведомление.Количество КАК Количество,
	|	Уведомление.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	Уведомление.Сумма КАК Сумма,
	|	Уведомление.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Уведомление.Комиссионер КАК Комиссионер
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК Уведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО Уведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Уведомление.Ссылка = &Ссылка
	|	И НЕ Реквизиты.ЭтоКорректировочноеУведомление
	|	И (Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары)
	|			ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.Номенклатура,
	|	-КорректировочноеУведомление.Количество,
	|	-КорректировочноеУведомление.КоличествоПрослеживаемости,
	|	-КорректировочноеУведомление.Сумма,
	|	КорректировочноеУведомление.СтранаПроисхождения,
	|	КорректировочноеУведомление.Комиссионер
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И (Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары)
	|			ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ПустаяСсылка))
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.Сумма <> КорректировочноеУведомление.СуммаПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения
	|			ИЛИ КорректировочноеУведомление.Комиссионер <> КорректировочноеУведомление.КомиссионерПослеИзменения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.Номенклатура,
	|	КорректировочноеУведомление.КоличествоПослеИзменения,
	|	КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения,
	|	КорректировочноеУведомление.СуммаПослеИзменения,
	|	КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения,
	|	КорректировочноеУведомление.КомиссионерПослеИзменения
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И (Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары)
	|			ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ПустаяСсылка))
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.Сумма <> КорректировочноеУведомление.СуммаПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения
	|			ИЛИ КорректировочноеУведомление.Комиссионер <> КорректировочноеУведомление.КомиссионерПослеИзменения)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Формирует и возвращает текст запроса для выборки данных
// необходимых для формирования движений документа Уведомление о перемещении
//
// Параметры:
//  НомераТаблиц - Число - Структура с таблицами параметров проведения
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаТаблицаТоваровУведомлениеОПеремещении(НомераТаблиц, ДокументСсылка) Экспорт
	
	НомераТаблиц.Вставить("ТаблицаТоваров",            НомераТаблиц.Количество());
	НомераТаблиц.Вставить("КорректировочныеДокументы", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка КАК Ссылка,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество КАК Количество,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Сумма КАК Сумма,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ КАК РНПТ,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ПорядковыйНомер КАК ПорядковыйНомер,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Контрагент КАК Контрагент,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент КАК СопроводительныйДокумент,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КлючСтроки КАК КлючСтроки,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество КАК КоличествоДоИзменения
	|ИЗ
	|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Товары КАК УведомлениеОПеремещенииПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Контрагенты КАК УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты
	|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка
	|			И УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КлючСтроки = УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.КлючСтроки
	|ГДЕ
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка = &Ссылка
	|	И УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка.НомерКорректировки = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПослеИзменения - УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения - УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СуммаПослеИзменения - УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Сумма,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ПорядковыйНомер,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Контрагент,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.НомерСтроки,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КлючСтроки,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество
	|ИЗ
	|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Товары КАК УведомлениеОПеремещенииПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Контрагенты КАК УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты
	|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка
	|			И УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КлючСтроки = УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.КлючСтроки
	|ГДЕ
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка = &Ссылка
	|	И УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка.НомерКорректировки > 0
	|	И (УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество <> УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|			ИЛИ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Сумма <> УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|			ИЛИ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество <> УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровКорректировочныеДокументы.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровКорректировочныеДокументы.Документ
	|	КОНЕЦ КАК Документ,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровКорректировочныеДокументы.КлючСтроки КАК КлючСтроки,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровКорректировочныеДокументы.ПорядковыйНомер КАК ПорядковыйНомер
	|ИЗ
	|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.КорректировочныеДокументы КАК УведомлениеОПеремещенииПрослеживаемыхТоваровКорректировочныеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров КАК УведомлениеОПеремещенииПрослеживаемыхТоваров
	|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровКорректировочныеДокументы.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровКорректировочныеДокументы.Ссылка = &Ссылка
	|	И УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

//Переопределяемые дополнительные параметры
//
// Параметры:
//  НомераТаблиц - ТаблицаЗначений - структура таблиц параметров проведения
//  Запрос - Запрос - запрос параметров проведения
//
Процедура ДополнительныеПараметрыЗапроса(НомераТаблиц, Запрос) Экспорт
	
	
КонецПроцедуры

// Формирует текст запроса по таблице товаров
// документа УведомлениеОбОстаткахПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаПоТаблицеТоваровУведомленияОбОстатках() Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Справочник.Номенклатура).НаименованиеПолное КАК НаименованиеТовара,
		|	ВЫБОР
		|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КодОКПД2 = ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КодОКПД2 КАК Справочник.КлассификаторОКПД2).Код
		|	КОНЕЦ КАК КодОКПД2,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации КАК ВидОперации,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения КАК Справочник.КлассификаторЕдиницИзмерения).Код КАК ЕдиницаИзмерения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество КАК Количество,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код КАК ЕдиницаПрослеживаемости,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма КАК Сумма,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).Код КАК КодТНВЭД,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).НаименованиеПолное КАК ТНВЭДНаименование,
		|	ДанныеПервичныхДокументов.Номер КАК НомерПервичногоДокумента,
		|	ДанныеПервичныхДокументов.Дата КАК ДатаПервичногоДокумента,
		|	"""" КАК РНПТ,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НаименованиеПервичногоДокумента КАК НаименованиеПервичногоДокументаСтрока,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерПервичногоДокумента КАК НомерПервичногоДокументаСтрока,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ДатаПервичногоДокумента КАК ДатаПервичногоДокументаСтрока
		|ПОМЕСТИТЬ ВТ_ВыборкаДанныхВременная
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = ДанныеПервичныхДокументов.Документ
		|				И УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = ДанныеПервичныхДокументов.Организация
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И (УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары)
		|			ИЛИ УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ПустаяСсылка))
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.ОсновноеСредство КАК Справочник.ОсновныеСредства).НаименованиеПолное,
		|	NULL,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	1,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	1,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.Сумма,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).Код,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).НаименованиеПолное,
		|	ДанныеПервичныхДокументов.Номер,
		|	ДанныеПервичныхДокументов.Дата,
		|	"""",
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НаименованиеПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ДатаПервичногоДокумента
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = ДанныеПервичныхДокументов.Документ
		|				И УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = ДанныеПервичныхДокументов.Организация
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.Номенклатура КАК Справочник.Номенклатура).НаименованиеПолное,
		|	NULL,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.Количество,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.КоличествоПрослеживаемости,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.Сумма,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).Код,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).НаименованиеПолное,
		|	ДанныеПервичныхДокументов.Номер,
		|	ДанныеПервичныхДокументов.Дата,
		|	"""",
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НаименованиеПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ДатаПервичногоДокумента
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = ДанныеПервичныхДокументов.Документ
		|				И УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = ДанныеПервичныхДокументов.Организация
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровОсновныеСредства.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС)
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Справочник.Номенклатура).НаименованиеПолное,
		|	ВЫБОР
		|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2ПослеИзменения = ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2ПослеИзменения КАК Справочник.КлассификаторОКПД2).Код
		|	КОНЕЦ,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).Код,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).НаименованиеПолное,
		|	ДанныеПервичныхДокументов.Номер,
		|	ДанныеПервичныхДокументов.Дата,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НаименованиеПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ДатаПервичногоДокумента
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = ДанныеПервичныхДокументов.Документ
		|				И УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = ДанныеПервичныхДокументов.Организация
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И (УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары)
		|			ИЛИ УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ПустаяСсылка))
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.ОсновноеСредство КАК Справочник.ОсновныеСредства).НаименованиеПолное,
		|	ВЫБОР
		|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2ПослеИзменения = ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2ПослеИзменения КАК Справочник.КлассификаторОКПД2).Код
		|	КОНЕЦ,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).Код,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).НаименованиеПолное,
		|	ДанныеПервичныхДокументов.Номер,
		|	ДанныеПервичныхДокументов.Дата,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НаименованиеПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ДатаПервичногоДокумента
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = ДанныеПервичныхДокументов.Документ
		|				И УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = ДанныеПервичныхДокументов.Организация
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Справочник.Номенклатура).НаименованиеПолное,
		|	ВЫБОР
		|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2ПослеИзменения = ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2ПослеИзменения КАК Справочник.КлассификаторОКПД2).Код
		|	КОНЕЦ,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).Код,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).НаименованиеПолное,
		|	ДанныеПервичныхДокументов.Номер,
		|	ДанныеПервичныхДокументов.Дата,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НаименованиеПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ДатаПервичногоДокумента
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = ДанныеПервичныхДокументов.Документ
		|				И УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = ДанныеПервичныхДокументов.Организация
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС)
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВыборкаДанных.НаименованиеТовара КАК НаименованиеТовара,
		|	ВТ_ВыборкаДанных.КодОКПД2 КАК КодОКПД2,
		|	ВТ_ВыборкаДанных.ВидОперации КАК ВидОперации,
		|	ВТ_ВыборкаДанных.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_ВыборкаДанных.Количество КАК Количество,
		|	ВТ_ВыборкаДанных.ЕдиницаПрослеживаемости КАК ЕдиницаПрослеживаемости,
		|	ВТ_ВыборкаДанных.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	ВТ_ВыборкаДанных.Сумма КАК Сумма,
		|	ВТ_ВыборкаДанных.ПервичныйДокумент КАК ПервичныйДокумент,
		|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД,
		|	ВЫРАЗИТЬ(ВТ_ВыборкаДанных.ТНВЭДНаименование КАК СТРОКА(255)) КАК ТНВЭДНаименование,
		|	ВТ_ВыборкаДанных.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
		|	ВТ_ВыборкаДанных.ДатаПервичногоДокумента КАК ДатаПервичногоДокумента,
		|	ВТ_ВыборкаДанных.РНПТ КАК РНПТ,
		|	ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока КАК НаименованиеПервичногоДокументаСтрока,
		|	ВТ_ВыборкаДанных.НомерПервичногоДокументаСтрока КАК НомерПервичногоДокументаСтрока,
		|	ВТ_ВыборкаДанных.ДатаПервичногоДокументаСтрока КАК ДатаПервичногоДокументаСтрока
		|ПОМЕСТИТЬ ВТ_ВыборкаДанных
		|ИЗ
		|	ВТ_ВыборкаДанныхВременная КАК ВТ_ВыборкаДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(1) КАК Количество,
		|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД
		|ПОМЕСТИТЬ ВТ_КоличествоСтрок
		|ИЗ
		|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВыборкаДанных.КодТНВЭД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВТ_КоличествоСтрок.Количество > 1
		|			ТОГДА ВТ_ВыборкаДанных.ТНВЭДНаименование
		|		ИНАЧЕ ВТ_ВыборкаДанных.НаименованиеТовара
		|	КОНЕЦ КАК НаимТовДок,
		|	ВТ_ВыборкаДанных.КодОКПД2 КАК КодОКПД2,
		|	ВТ_ВыборкаДанных.ЕдиницаИзмерения КАК ЕдинИзмерДок,
		|	СУММА(ВТ_ВыборкаДанных.Количество) КАК КолТоварДок,
		|	ВТ_ВыборкаДанных.ЕдиницаПрослеживаемости КАК ЕдинИзмерПер,
		|	СУММА(ВТ_ВыборкаДанных.КоличествоПрослеживаемости) КАК КоличТоварПер,
		|	СУММА(ВТ_ВыборкаДанных.Сумма) КАК СтТоварБезНДС,
		|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока = """"
		|			ТОГДА ВТ_ВыборкаДанных.НомерПервичногоДокументаСтрока
		|		ИНАЧЕ ВТ_ВыборкаДанных.НомерПервичногоДокумента
		|	КОНЕЦ КАК НомПервичДок,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока = """"
		|			ТОГДА ВТ_ВыборкаДанных.ДатаПервичногоДокументаСтрока
		|		ИНАЧЕ ВТ_ВыборкаДанных.ДатаПервичногоДокумента
		|	КОНЕЦ КАК ДатаПервичДок,
		|	ВТ_ВыборкаДанных.РНПТ КАК РегНомерТов,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока = """"
		|			ТОГДА ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока
		|		КОГДА ВТ_ВыборкаДанных.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
		|			ТОГДА ""Инвентаризация основных средств""
		|		КОГДА ВТ_ВыборкаДанных.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС)
		|			ТОГДА ""Инвентаризация основных средств""
		|		ИНАЧЕ ""ИнвентаризацияТоваров""
		|	КОНЕЦ КАК НаименованиеПервичногоДокумента
		|ИЗ
		|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоСтрок КАК ВТ_КоличествоСтрок
		|		ПО ВТ_ВыборкаДанных.КодТНВЭД = ВТ_КоличествоСтрок.КодТНВЭД
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВыборкаДанных.КодОКПД2,
		|	ВТ_ВыборкаДанных.ЕдиницаИзмерения,
		|	ВТ_ВыборкаДанных.ЕдиницаПрослеживаемости,
		|	ВТ_ВыборкаДанных.КодТНВЭД,
		|	ВТ_ВыборкаДанных.РНПТ,
		|	ВЫБОР
		|		КОГДА ВТ_КоличествоСтрок.Количество > 1
		|			ТОГДА ВТ_ВыборкаДанных.ТНВЭДНаименование
		|		ИНАЧЕ ВТ_ВыборкаДанных.НаименованиеТовара
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока = """"
		|			ТОГДА ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока
		|		КОГДА ВТ_ВыборкаДанных.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
		|			ТОГДА ""Инвентаризация основных средств""
		|		КОГДА ВТ_ВыборкаДанных.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС)
		|			ТОГДА ""Инвентаризация основных средств""
		|		ИНАЧЕ ""ИнвентаризацияТоваров""
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока = """"
		|			ТОГДА ВТ_ВыборкаДанных.НомерПервичногоДокументаСтрока
		|		ИНАЧЕ ВТ_ВыборкаДанных.НомерПервичногоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_ВыборкаДанных.НаименованиеПервичногоДокументаСтрока = """"
		|			ТОГДА ВТ_ВыборкаДанных.ДатаПервичногоДокументаСтрока
		|		ИНАЧЕ ВТ_ВыборкаДанных.ДатаПервичногоДокумента
		|	КОНЕЦ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует текст запроса по таблице товаров
// документа УведомлениеОВвозеПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаПоТаблицеТоваровУведомленияОВвозе() Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура КАК Справочник.Номенклатура).Наименование КАК НаименованиеТовара,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА ВЫБОР
		|					КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭДПослеИзменения = ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
		|						ТОГДА """"
		|					ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭДПослеИзменения
		|				КОНЕЦ
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.КодОКПД2 = ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваровТовары.КодОКПД2 КАК Справочник.КлассификаторОКПД2).Код
		|	КОНЕЦ КАК КодОКПД2,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
		|			ТОГДА ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмерения КАК Справочник.КлассификаторЕдиницИзмерения).Код
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения КАК Справочник.КлассификаторЕдиницИзмерения).Код
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
		|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
		|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
		|	КОНЕЦ КАК Количество,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
		|			ТОГДА ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемостиПослеИзменения КАК Справочник.КлассификаторЕдиницИзмерения).Код
		|	КОНЕЦ КАК ЕдиницаПрослеживаемости,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
		|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
		|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
		|	КОНЕЦ КАК КоличествоПрослеживаемости,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
		|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
		|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения
		|	КОНЕЦ КАК Сумма,
		|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
		|			ТОГДА ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).Код
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).Код
		|	КОНЕЦ КАК КодТНВЭД,
		|	ВЫБОР
		|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
		|			ТОГДА ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).Наименование
		|		ИНАЧЕ ВЫРАЗИТЬ(УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).Наименование
		|	КОНЕЦ КАК ТНВЭДНаименование,
		|	ДанныеПервичныхДокументов.Номер КАК НомерСопроводительногоДокумента,
		|	ДанныеПервичныхДокументов.Дата КАК ДатаСопроводительногоДокумента,
		|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ КАК РНПТ
		|ПОМЕСТИТЬ ВТ_ВыборкаДанных
		|ИЗ
		|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = ДанныеПервичныхДокументов.Документ
		|				И УведомлениеОВвозеПрослеживаемыхТоваров.Организация = ДанныеПервичныхДокументов.Организация
		|		ПО УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = &Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(1) КАК Количество,
		|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД
		|ПОМЕСТИТЬ ВТ_КоличествоСтрок
		|ИЗ
		|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВыборкаДанных.КодТНВЭД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВТ_КоличествоСтрок.Количество > 1
		|			ТОГДА ВТ_ВыборкаДанных.ТНВЭДНаименование
		|		ИНАЧЕ ВТ_ВыборкаДанных.НаименованиеТовара
		|	КОНЕЦ КАК НаимТовДок,
		|	ВТ_ВыборкаДанных.КодОКПД2 КАК КодОКПД2,
		|	ВТ_ВыборкаДанных.ЕдиницаИзмерения КАК ЕдинИзмерДок,
		|	СУММА(ВТ_ВыборкаДанных.Количество) КАК КолТоварДок,
		|	ВТ_ВыборкаДанных.ЕдиницаПрослеживаемости КАК ЕдинИзмерПрослеж,
		|	СУММА(ВТ_ВыборкаДанных.КоличествоПрослеживаемости) КАК КоличТоварПрослеж,
		|	СУММА(ВТ_ВыборкаДанных.Сумма) КАК СтТоварБезНДС,
		|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД,
		|	ВТ_ВыборкаДанных.НомерСопроводительногоДокумента КАК НомСопрДок,
		|	ВТ_ВыборкаДанных.ДатаСопроводительногоДокумента КАК ДатаСопрДок,
		|	ВТ_ВыборкаДанных.РНПТ КАК РНПТ
		|ИЗ
		|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоСтрок КАК ВТ_КоличествоСтрок
		|		ПО ВТ_ВыборкаДанных.КодТНВЭД = ВТ_КоличествоСтрок.КодТНВЭД
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВыборкаДанных.КодОКПД2,
		|	ВТ_ВыборкаДанных.ЕдиницаИзмерения,
		|	ВТ_ВыборкаДанных.ЕдиницаПрослеживаемости,
		|	ВЫБОР
		|		КОГДА ВТ_КоличествоСтрок.Количество > 1
		|			ТОГДА ВТ_ВыборкаДанных.ТНВЭДНаименование
		|		ИНАЧЕ ВТ_ВыборкаДанных.НаименованиеТовара
		|	КОНЕЦ,
		|	ВТ_ВыборкаДанных.КодТНВЭД,
		|	ВТ_ВыборкаДанных.ДатаСопроводительногоДокумента,
		|	ВТ_ВыборкаДанных.НомерСопроводительногоДокумента,
		|	ВТ_ВыборкаДанных.РНПТ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует текст запроса по таблице товаров
// документа УведомлениеОПеремещенииПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаПоТаблицамУведомленияОПеремещенииПрослеживаемыхТоваров() Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Контрагент КАК Контрагент,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент КАК СопроводительныйДокумент,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ПорядковыйНомерПослеИзменения
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ПорядковыйНомер
		|		КОНЕЦ) КАК ПорядковыйНомер,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура.НаименованиеПолное) КАК НоменклатураНаименование,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭДПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭД
		|	КОНЕЦ КАК КодТНВЭД,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмеренияПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемостиПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемости
		|	КОНЕЦ КАК ЕдиницаПрослеживаемости,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество
		|		КОНЕЦ) КАК Количество,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
		|		КОНЕЦ) КАК КоличествоПрослеживаемости,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СуммаПослеИзменения
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Сумма
		|		КОНЕЦ) КАК Сумма,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ
		|	КОНЕЦ КАК РНПТ,
		|	МАКСИМУМ(ДанныеПервичныхДокументов.Номер) КАК НомерСопроводительногоДокумента,
		|	МАКСИМУМ(ДанныеПервичныхДокументов.Дата) КАК ДатаСопроводительногоДокумента,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭДПослеИзменения.Код
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭД.Код
		|		КОНЕЦ) КАК КодТНВЭДКод,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмеренияПослеИзменения.Код
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмерения.Код
		|		КОНЕЦ) КАК ЕдиницаИзмеренияКод,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемостиПослеИзменения.Код
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемости.Код
		|		КОНЕЦ) КАК ЕдиницаПрослеживаемостиКод,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|				ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТПослеИзменения.Код
		|			ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ.Код
		|		КОНЕЦ) КАК РНПТКод,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.НомерСтроки КАК НомерСтроки,
		|	СтраныМира.Код КАК КодТранзитнойСтраны
		|ИЗ
		|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Контрагенты КАК УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Товары КАК УведомлениеОПеремещенииПрослеживаемыхТоваровТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|			ПО УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент = ДанныеПервичныхДокументов.Документ
		|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка
		|			И УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.КлючСтроки = УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КлючСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров КАК УведомлениеОПеремещенииПрослеживаемыхТоваров
		|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваров.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.ТранзитныеСтраныЕАЭС КАК УведомлениеОПеремещенииПрослеживаемыхТоваровТранзитныеСтраныЕАЭС
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|			ПО УведомлениеОПеремещенииПрослеживаемыхТоваровТранзитныеСтраныЕАЭС.Страна = СтраныМира.Ссылка
		|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваровТранзитныеСтраныЕАЭС.Ссылка
		|			И УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.КлючСтроки = УведомлениеОПеремещенииПрослеживаемыхТоваровТранзитныеСтраныЕАЭС.КлючСтроки
		|			И (НЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТранзитныеСтраныЕАЭС.ЭтоСтрокаСИсходнымиСтранами)
		|ГДЕ
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка = &Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Контрагент,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемостиПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемости
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмеренияПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмерения
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭДПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭД
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ПорядковыйНомерПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ПорядковыйНомер
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА УведомлениеОПеремещенииПрослеживаемыхТоваров.НомерКорректировки > 0
		|			ТОГДА УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТПослеИзменения
		|		ИНАЧЕ УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ
		|	КОНЕЦ,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.НомерСтроки,
		|	СтраныМира.Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСопроводительногоДокумента,
		|	ПорядковыйНомер
		|ИТОГИ ПО
		|	Контрагент,
		|	СопроводительныйДокумент,
		|	НомерСтроки,
		|	КодТранзитнойСтраны";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует данные по контрагенту
// документа УведомлениеОПеремещенииПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Строка - текст запроса
//
// Формирует структуру данных контрагента, необходимых для печати
// Уведомления о перемещении прослеживаемых товаров
//
// Параметры:
// Контрагент - СправочникСсылка.Контрагенты - Контрагент 
// 
// Возвращаемое значение:
//    Структура:
//		Признак - Строка - Признак физического лица, возможны значения: "V" - физ. лицо; "" - организация.
//		КодГосударства - Строка - Код государства-члена ЕАЭС по ОКСМ (3 цифры).
//		Наименование - Строка - Наименование организации (1 - 1000 символов).
//		Фамилия - Строка - Фамилия физического лица (1 - 60 символов).
//		Имя - Строка - Имя физического лица (1 - 60 символов).
//		Отчетсво - -  Отчество физического лица (1 - 60 символов).
//		ИдентификационныйКод - Строка -  Идентификационный код (номер) в государстве-члене ЕАЭС (8 - 14 символов).
//		Адрес - Строка - Адрес (Строка: 1 - 1000 символов).
//
Функция РеквизитыКонтрагента(Контрагент) Экспорт
	
	Реквизиты = Новый Структура("Признак,КодГосударства,Наименование,Фамилия,Имя,Отчетсво,ИдентификационныйКод,Адрес",
	 "","","","","","","","");
	
	ЭтоФизическоеЛицо = Справочники.Контрагенты.ЭтоФизическоеЛицо(Контрагент);
	
	ДанныеКонтрагента = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Контрагент);
	
	Если ЭтоФизическоеЛицо Тогда
		
		ЧастиФИО= ФизическиеЛицаКлиентСервер.ЧастиИмени(ДанныеКонтрагента.ПолноеНаименование);
		
		Реквизиты["Признак"] = "V";
		Реквизиты["Фамилия"] = ЧастиФИО.Фамилия;
		Реквизиты["Имя"] = ЧастиФИО.Имя;
		Реквизиты["Отчетсво"] = ЧастиФИО.Отчество;
		
	Иначе
		
		Реквизиты["Наименование"] = ДанныеКонтрагента.ПолноеНаименование;
		
	КонецЕсли;
	
	Реквизиты["КодГосударства"] = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ДанныеКонтрагента.СтранаРегистрации, "Код");
		
	Реквизиты["ИдентификационныйКод"] = ДанныеКонтрагента.НалоговыйНомерВСтранеРегистрации;
	
	Реквизиты["Адрес"] = ДанныеКонтрагента.ЮридическийАдрес;
	
	Возврат Реквизиты;
		
КонецФункции

// Формирует данные многострочной строки по таблице товаров
// документа УведомлениеОПеремещенииПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Структура:
//			- НомерПоПорядкуТоваров - Число  - графа 1 (0 - 999)
//			- НаименованиеСопроводительногоДокумента - Строка - графа 2 (1 - 255 символов)
//			- НомерСопроводительногоДокумента - Строка - графа 3 (1 - 255 символов)
//			- ДатаСопроводительногоДокумента - Дата   - графа 4.
//			- КодТНВЭД - Строка - графа 6 (6 - 10 символов)
//			- ПорядковыйНомерСопроводительногоДокумента - Число - графа 7 (0 - 9999)
//			- НаименованиеТовара - Строка - графа 8 (1 - 255 символов)
//			- Количество - Число  - графа 9 (0 - 9999999999999.999999)
//			- КодЕдиницаИзмерения - Строка - графа 10 (3 - 4 цифры)
//			- КоличествоПрослеживаемости - Число  - графа 11 (0 - 999999999999999.99999999999)
//			- ЕдиницаПрослеживаемости - Строка - графа 12 (3 - 4 цифры)
//			- РНПТ - Строка - графа 13 (27 - 29 смволов)
//			- АдресДоставки - Строка - графа 14 (1 - 255 символов)
//			- СуммаБезНДС - Число  - графа 15 (0 - 99999999999999999.99)
//
Функция ПолучитьДанныеСтрокиВыборки(ВыборкаНомерСтроки) Экспорт
	
	ДанныеСтроки = Новый Структура();
	
	НаименованиеСопроводительногоДокумента = "";
	АдресДоставки = "";
	
	Если ТипЗНЧ(ВыборкаНомерСтроки.СопроводительныйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		НаименованиеСопроводительногоДокумента = "Накладная";
		
		АдресДоставки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ВыборкаНомерСтроки.СопроводительныйДокумент, "АдресДоставки");
			
	КонецЕсли;
		
	ДанныеСтроки.Вставить("НаименованиеСопроводительногоДокумента", НаименованиеСопроводительногоДокумента);
	ДанныеСтроки.Вставить("НомерСопроводительногоДокумента", ВыборкаНомерСтроки.НомерСопроводительногоДокумента);
	ДанныеСтроки.Вставить("ДатаСопроводительногоДокумента", ВыборкаНомерСтроки.ДатаСопроводительногоДокумента);
	
	ДанныеСтроки.Вставить("КодТНВЭД", ВыборкаНомерСтроки.КодТНВЭДКод);
	ДанныеСтроки.Вставить("ПорядковыйНомерВСопроводительномДокументе", ВыборкаНомерСтроки.ПорядковыйНомер);
	ДанныеСтроки.Вставить("НаименованиеТовара", ВыборкаНомерСтроки.НоменклатураНаименование);
	ДанныеСтроки.Вставить("Количество", ВыборкаНомерСтроки.Количество);
	ДанныеСтроки.Вставить("КодЕдиницаИзмерения", ВыборкаНомерСтроки.ЕдиницаИзмеренияКод);
	ДанныеСтроки.Вставить("КоличествоПрослеживаемости", ВыборкаНомерСтроки.КоличествоПрослеживаемости);
	ДанныеСтроки.Вставить("ЕдиницаПрослеживаемости", ВыборкаНомерСтроки.ЕдиницаПрослеживаемостиКод);
	ДанныеСтроки.Вставить("РНПТ", ВыборкаНомерСтроки.РНПТКод);
	ДанныеСтроки.Вставить("СуммаБезНДС", ВыборкаНомерСтроки.Сумма);
	
	ДанныеСтроки.Вставить("АдресДоставки", АдресДоставки);
	
	Возврат ДанныеСтроки;
	
КонецФункции

// Определяет код государственного органа, администрирующего деятельность организации
//
// Параметры:
//  Организация					 - СправочникСсылка.Организации, СправочникОбъект.Организации - администрируемая организация
// 
// Возвращаемое значение:
//  Строка - значащая часть кода государственного органа, заданного в информационной базе
//
Функция КодГосударственногоОрганаОрганизации(Организация) Экспорт
	
	Возврат ДанныеГосударственныхОрганов.КодГосударственногоОрганаОрганизации(
			Организация,
			Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган,
			Истина);
	
КонецФункции

// Определяет КПП организации на заданную дату
//
// Параметры:
//  Организация					 - СправочникСсылка.Организации, СправочникОбъект.Организации - администрируемая организация
//  ДатаСведений - Дата - Дата, на которую нужно получить КПП организации.
// Возвращаемое значение:
//  Строка - КПП.
//
Функция КППНаДату(Организация, ДатаСведений) Экспорт
	
	Возврат Справочники.Организации.КППНаДату(Организация, ДатаСведений, Ложь);
	
КонецФункции

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.РегистрыНакопления.ПрослеживаемыеТоварыОтгруженныеВЕАЭС, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ОперацииСПрослеживаемымиТоварами, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РегистрацияПрослеживаемыхТоваров, Истина);
	
КонецПроцедуры
// Формирует и возвращает текст запроса для выборки данных
// необходимых для формирования движений документа
//
// Параметры:
//  НомераТаблиц - Число - Структура с таблицами параметров проведения
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаТаблицаТоваровУведомлениеОВвозе(НомераТаблиц, ДокументСсылка) Экспорт
	
	НомераТаблиц.Вставить("ТаблицаТоваров", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Уведомление.Ссылка КАК Ссылка,
	|	Уведомление.НомерСтроки КАК НомерСтроки,
	|	Уведомление.Номенклатура КАК Номенклатура,
	|	Уведомление.Количество КАК Количество,
	|	Уведомление.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	Уведомление.Сумма КАК Сумма,
	|	Уведомление.СтранаПроисхождения КАК СтранаПроисхождения,
	|	"""" КАК Комиссионер
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК Уведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО Уведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Уведомление.Ссылка = &Ссылка
	|	И НЕ Реквизиты.ЭтоКорректировочноеУведомление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.Номенклатура,
	|	-КорректировочноеУведомление.Количество,
	|	-КорректировочноеУведомление.КоличествоПрослеживаемости,
	|	-КорректировочноеУведомление.Сумма,
	|	КорректировочноеУведомление.СтранаПроисхождения,
	|	""""
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения)
	|	И КорректировочноеУведомление.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.Номенклатура,
	|	КорректировочноеУведомление.КоличествоПослеИзменения,
	|	КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения,
	|	КорректировочноеУведомление.СуммаПослеИзменения,
	|	КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения,
	|	""""
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения)
	|	И КорректировочноеУведомление.КоличествоПослеИзменения <> 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Формирует и возвращает текст запроса для выборки данных
// необходимых для формирования движений документа
//
// Параметры:
//  НомераТаблиц - Число - Структура с таблицами параметров проведения
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаТаблицаОСУведомлениеОбОстатках(НомераТаблиц, ДокументСсылка) Экспорт
	
	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Уведомление.Ссылка КАК Ссылка,
	|	Уведомление.НомерСтроки КАК НомерСтроки,
	|	Уведомление.ОсновноеСредство КАК ОсновноеСредство,
	|	Уведомление.Сумма КАК Сумма,
	|	Уведомление.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Уведомление.Номенклатура КАК Комплектующие,
	|	Уведомление.Количество КАК Количество,
	|	Уведомление.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК Уведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО Уведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Уведомление.Ссылка = &Ссылка
	|	И (Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
	|			ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС))
	|	И НЕ Реквизиты.ЭтоКорректировочноеУведомление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.ОсновноеСредство,
	|	-КорректировочноеУведомление.Сумма,
	|	КорректировочноеУведомление.СтранаПроисхождения,
	|	КорректировочноеУведомление.Номенклатура,
	|	-КорректировочноеУведомление.Количество,
	|	-КорректировочноеУведомление.КоличествоПрослеживаемости
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И (Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
	|			ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС))
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.ОсновноеСредство,
	|	КорректировочноеУведомление.СуммаПослеИзменения,
	|	КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения,
	|	КорректировочноеУведомление.Номенклатура,
	|	КорректировочноеУведомление.КоличествоПослеИзменения,
	|	КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И (Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
	|			ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС))
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И КорректировочноеУведомление.КоличествоПослеИзменения > 0
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Возвращает структуру реквизитов основного средства 
// Параметры: 
//  ОсновноеСредство - СправочникСсылка - основное средство
// 
// Возвращемое значение:
//  Структура - КодТНВЭД - СправочникСсылка.КлассификаторТНВЭД
//            - ЕдиницаИзмерения - СправочникСсылка.КлассификаторЕдиницИзмерения
//
Функция ПараметрыОС(ОсновноеСредство) Экспорт
	
	НаборПараметровОС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОсновноеСредство, "КодТНВЭД");
	НаборПараметровОС.Вставить("ЕдиницаИзмерения", 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НаборПараметровОС.КодТНВЭД, "ЕдиницаИзмерения"));
	
	Возврат НаборПараметровОС;
	
КонецФункции

// Процедура выполняет подготовку наборов записей документа к проведению документа.
// 1. Очищает наборы записей от "старых записей" (ситуация возможна только в толстом клиенте)
// 2. Взводит флаг записи у наборов, по которым документ имел движения при прошлом проведении
// 3. Устанавливает активность наборам записей документов с установленным флагом ручной корректировки
// 4. Записывает пустые наборы, если дата ранее проведенного документа была сдвинута вперед
// Вызывается из модуля документа при проведении.
//
Процедура ПодготовитьНаборыЗаписейКПроведению(Объект) Экспорт
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(Объект);
	
КонецПроцедуры

// Процедура выполняет подготовку наборов записей документа к отмене проведения документа.
// 1. Взводит флаг записи у наборов, по которым документ имел движения при прошлом проведении
// 2. Снимает активность у наборов записей документов с установленным флагом ручной корректировки
// Вызывается из модуля документа при отмене проведения.
//
Процедура ПодготовитьНаборыЗаписейКОтменеПроведения(Объект) Экспорт
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(Объект);
	
КонецПроцедуры

// Функция формирует сведения об указанном ЮрФизЛице. К сведениям относятся -
// наименование, адрес, номер телефона, банковские реквизиты.
//
// Параметры: 
//  ЮрФизЛицо   - организация или физическое лицо, о котором собираются сведения.
//  Период - дата, на которую выбираются сведения о ЮрФизЛице.
//  БанковскийСчет - банковский счет, реквизиты которого выводятся.
//
// Возвращаемое значение:
//  Сведения - собранные сведения.
//
//  Функция формирует сведения об указанном ЮрФизЛице. К сведениям относятся -
// наименование, адрес, номер телефона, банковские реквизиты.
//
// Параметры: 
//  ЮрФизЛицо   - организация или физическое лицо, о котором собираются сведения.
//  ДатаПериода - дата, на которую выбираются сведения о ЮрФизЛице.
//  БанковскийСчет - банковский счет, реквизиты которого выводятся.
//
// Возвращаемое значение:
//  Сведения - собранные сведения.
//
Функция СведенияОЮрФизЛице(ЮрФизЛицо, Период = '20300101') Экспорт

	Возврат БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
		ЮрФизЛицо, Период, , , , Истина);
	
КонецФункции

// Вызывается перед загрузкой пользовательских настроек на сервере
//
// Параметры:
//  Список      - ДинамическийСписок - Динамический список, для которого устанавливается отбор.
//  Настройки   - ПользовательскиеНастройкиКомпоновкиДанных - Восстанавливаемые настройки списка.
//  ИмяОтбора   - Строка - Имя элемента отбора.
//
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Список, Настройки, ИмяОтбора) Экспорт
	
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, ИмяОтбора);
	
КонецПроцедуры

Функция УчетнаяПолитикаСуществует(Организация, Период, ВыводитьСообщениеОбОтсутствииУчетнойПолитики = Ложь, ДокументСсылка = Неопределено) Экспорт
	
	Возврат УчетнаяПолитика.Существует(Организация, Период, ВыводитьСообщениеОбОтсутствииУчетнойПолитики, ДокументСсылка);
	
КонецФункции

Функция УстановитьСостояниеДокумента(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Форма.СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);

КонецФункции

// Возвращает перечень (массив) всех структурных частей переданной головной организации, имеющих отдельный баланс.
// В перечень входит головная организация и все ее обособленные подразделения на выделенном балансе.
// В перечень входят только те организации, данные по которым доступны текущему пользователю.
//
Функция ВсяОрганизация(Организация) Экспорт
	
	МассивОрганизаций = БухгалтерскийУчетПереопределяемый.ВсяОрганизация(Организация);

	Возврат МассивОрганизаций;

КонецФункции

Функция ЭтоЮридическоеЛицо(СведенияОПродавце) Экспорт
	
	Возврат СведенияОПродавце.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
КонецФункции

Функция ЗапросЗаполнитьДокументДаннымиУведомлениеОперемещении() Экспорт

	Возврат 
	"ВЫБРАТЬ
	|	РеализацияПрослеживаемыхТоваровОстатки.Контрагент КАК Контрагент,
	|	РеализацияПрослеживаемыхТоваровОстатки.СопроводительныйДокумент КАК СопроводительныйДокумент,
	|	РеализацияПрослеживаемыхТоваровОстатки.Номенклатура КАК Номенклатура,
	|	РеализацияПрослеживаемыхТоваровОстатки.РНПТ КАК РНПТ,
	|	РеализацияПрослеживаемыхТоваровОстатки.ПорядковыйНомер КАК ПорядковыйНомер,
	|	РеализацияПрослеживаемыхТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	РеализацияПрослеживаемыхТоваровОстатки.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемостиОстаток,
	|	РеализацияПрослеживаемыхТоваровОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ВЫРАЗИТЬ(РеализацияПрослеживаемыхТоваровОстатки.Номенклатура КАК Справочник.Номенклатура).КодТНВЭД КАК КодТНВЭД,
	|	ВЫРАЗИТЬ(РеализацияПрослеживаемыхТоваровОстатки.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ВТ_ВыборкаДанных
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТоварыОтгруженныеВЕАЭС.Остатки(&Период, Организация = &Организация) КАК РеализацияПрослеживаемыхТоваровОстатки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ДокументОснование <> НЕОПРЕДЕЛЕНО
	|				ТОГДА РеализацияПрослеживаемыхТоваровОстатки.СопроводительныйДокумент = &ДокументОснование
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";
	
КонецФункции

Функция ТекстЗапросаТаблицаПрослеживаемыхТоваровПоОснованиюУведомленияОбОстатках() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация КАК Организация,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Дата КАК ПериодСобытия,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации КАК ВидОперации,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.ОсновноеСредство КАК ОсновноеСредство,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	0 КАК Количество,
	|	0 КАК Сумма,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровВременный
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = &Основание
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Дата,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.ОсновноеСредство,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровВременный.ОсновноеСредство КАК ОсновноеСредство,
	|	0 КАК Количество,
	|	0 КАК Сумма,
	|	СправочникОсновныеСредства.КодТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваровВременный.Организация КАК Организация,
	|	ТаблицаТоваровВременный.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваровВременный.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровВременный.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ИнвентаризацияОС) КАК ВидОперации
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваров
	|ИЗ
	|	ВТ_ТаблицаТоваровВременный КАК ТаблицаТоваровВременный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК СправочникОсновныеСредства
	|		ПО ТаблицаТоваровВременный.ОсновноеСредство = СправочникОсновныеСредства.Ссылка
	|ГДЕ
	|	ТаблицаТоваровВременный.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваровВременный.Организация,
	|	ТаблицаТоваровВременный.ПериодСобытия,
	|	ТаблицаТоваровВременный.ВидОперации,
	|	СправочникОсновныеСредства.КодТНВЭД,
	|	ТаблицаТоваровВременный.ОсновноеСредство,
	|	ТаблицаТоваровВременный.Номенклатура,
	|	ТаблицаТоваровВременный.СтранаПроисхождения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровВременный.ОсновноеСредство,
	|	0,
	|	0,
	|	СправочникОсновныеСредства.КодТНВЭД,
	|	ТаблицаТоваровВременный.Организация,
	|	ТаблицаТоваровВременный.ПериодСобытия,
	|	ТаблицаТоваровВременный.Номенклатура,
	|	ТаблицаТоваровВременный.СтранаПроисхождения,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.РегистрацияКомплектующихОС)
	|ИЗ
	|	ВТ_ТаблицаТоваровВременный КАК ТаблицаТоваровВременный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК СправочникОсновныеСредства
	|		ПО ТаблицаТоваровВременный.ОсновноеСредство = СправочникОсновныеСредства.Ссылка
	|ГДЕ
	|	ТаблицаТоваровВременный.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваровВременный.Организация,
	|	ТаблицаТоваровВременный.ПериодСобытия,
	|	ТаблицаТоваровВременный.ВидОперации,
	|	СправочникОсновныеСредства.КодТНВЭД,
	|	ТаблицаТоваровВременный.ОсновноеСредство,
	|	ТаблицаТоваровВременный.Номенклатура,
	|	ТаблицаТоваровВременный.СтранаПроисхождения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровВременный.ОсновноеСредство,
	|	0,
	|	0,
	|	СправочникОсновныеСредства.КодТНВЭД,
	|	ТаблицаТоваровВременный.Организация,
	|	ТаблицаТоваровВременный.ПериодСобытия,
	|	ТаблицаТоваровВременный.Номенклатура,
	|	ТаблицаТоваровВременный.СтранаПроисхождения,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ИнвентаризацияТоваров)
	|ИЗ
	|	ВТ_ТаблицаТоваровВременный КАК ТаблицаТоваровВременный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК СправочникОсновныеСредства
	|		ПО ТаблицаТоваровВременный.ОсновноеСредство = СправочникОсновныеСредства.Ссылка
	|ГДЕ
	|	(ТаблицаТоваровВременный.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары)
	|			ИЛИ ТаблицаТоваровВременный.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваровВременный.Организация,
	|	ТаблицаТоваровВременный.ПериодСобытия,
	|	ТаблицаТоваровВременный.ВидОперации,
	|	СправочникОсновныеСредства.КодТНВЭД,
	|	ТаблицаТоваровВременный.ОсновноеСредство,
	|	ТаблицаТоваровВременный.Номенклатура,
	|	ТаблицаТоваровВременный.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.Сумма КАК Сумма,
	|	ТаблицаТоваров.Организация КАК Организация,
	|	ТаблицаТоваров.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваров.ВидОперации КАК ВидОперации,
	|	0 КАК КоличествоУчет,
	|	ТаблицаТоваров.ТНВЭД КАК ТНВЭД,
	|	ЛОЖЬ КАК РанееРегистрировалсяВПрослеживаемости,
	|	0 КАК СуммаУчет,
	|	"""" КАК СтранаПроисхождения,
	|	ЛОЖЬ КАК СчитатьОстаток
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровРазмеченный
	|ИЗ
	|	ВТ_ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.ТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваров.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.Сумма КАК Сумма,
	|	ТаблицаТоваров.Организация КАК Организация,
	|	ТаблицаТоваров.ВидОперации КАК ВидОперации,
	|	ТаблицаТоваров.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|	&ПустаяСсылкаРнпт КАК РНПТ,
	|	0 КАК ВсегоКоличествоКомплектующих,
	|	0 КАК ВсегоКомлектующихНаСумму,
	|	NULL КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_ТоварыПоОснованию
	|ИЗ
	|	ВТ_ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаТоваров";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьПоНоменклатуреЗначенияПоУмолчанию(Номенклатура) Экспорт
	
	ИзменяемыеПоля = Новый Структура("КодТНВЭД,ЕдиницаИзмерения,ЕдиницаПрослеживаемости,РНПТ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(Номенклатура.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).ЕдиницаИзмерения КАК ЕдиницаПрослеживаемости
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
	
		ЗаполнитьЗначенияСвойств(ИзменяемыеПоля, Результат);
		
	Иначе
	
		ИзменяемыеПоля.КодТНВЭД = Справочники.КлассификаторТНВЭД.ПустаяСсылка();
		ИзменяемыеПоля.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
		ИзменяемыеПоля.ЕдиницаПрослеживаемости = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
		
	КонецЕсли;
	
	ИзменяемыеПоля.РНПТ = "";
	
	Возврат ИзменяемыеПоля;
	
КонецФункции

#КонецОбласти

#Область ПомощникПолученияРНПТ

&НаСервере
Функция ПолучитьРНПТНаСервере(СтрРНПТ) Экспорт
	
	НайденныйРНПТ = Справочники.НомераГТД.НайтиПоКоду(СтрРНПТ);
	Если НайденныйРНПТ = Справочники.НомераГТД.ПустаяСсылка() Тогда
		
		НовыйЭлемент = Справочники.НомераГТД.СоздатьЭлемент();
		НовыйЭлемент.Код = СтрРНПТ;
		НовыйЭлемент.Записать();
		
		НайденныйРНПТ = НовыйЭлемент.Ссылка;
	КонецЕсли;
	
	Возврат НайденныйРНПТ;
	
КонецФункции

// Проверяет ведется учет по складам или нет на счете товары
//
// Возвращаемое значение:
// Булево - Да, по складам ведется учет
//
Функция ВедетсяУчетПоСкладам() Экспорт
	
	Возврат БухгалтерскийУчет.ВедетсяУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары);

КонецФункции

// Получаем таблицу остатков прослеживаемых товаров и ОС
// Уточнение: попадают остатки, по которым делали инвентаризацию и без инвентаризации
//
// Параметры:
//  Период - Дата - дата остатков
//  Организация - СправочникСсылка.Организация - Организация
//
// Возвращаемое значение:
//		Структура - 
//					НачальныеДанныеТоваров - ТаблицаЗначений - остатки товаров
//					НачальныеДанныеОС -      ТаблицаЗначений - остатки ОС
//
Функция ПолучитьНачальныеДанныеДляПомощникаПолученияРНПТ(Период, Организация) Экспорт
	
	Если НЕ ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Период) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	НачальныеДанные = ПрослеживаемостьБП.ПолучитьНачальныеДанныеТоваров(Период, Организация);
	
	НачальныеДанные.Колонки.Добавить("ЭтоОС", Новый ОписаниеТипов("Булево"));
	НачальныеДанные.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	НачальныеДанные.Колонки.Добавить("КоличествоСоставныхОбъектовВОС", Новый ОписаниеТипов("Число"));
	НачальныеДанные.Колонки.Добавить("ЕстьСостав", Новый ОписаниеТипов("Булево"));
	НачальныеДанные.Колонки.Добавить("ОстаточнаяСтоимостьОС", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
	
	НачальныеДанныеОС = ПрослеживаемостьБП.ПолучитьНачальныеДанныеОС(Период, Организация);
	
	Для Каждого ТекущаяСтрока Из НачальныеДанныеОС Цикл
		
		НоваяСтрока = НачальныеДанные.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока); 
		НоваяСтрока.ЭтоОС = Истина;
		
	КонецЦикла;
	
	Возврат НачальныеДанные;
	
КонецФункции

// Возвращает представление документа на форме
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ
//  
// Возвращаемое значение:
//  Строка - Текст, составленный по ссылке документа
// 
Функция ПредставлениеДокумента(Документ, ЭтоДокументОснования = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Документ) Тогда
		Возврат "";
	КонецЕсли;
	
	ПараметрыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Номер,Дата");
	ТекстОснование = "";
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Инвентаризация №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Инвентаризация ОС №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда
		
		ПараметрыДокументаДополнительные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Документ, "ВидОперации,НаименованиеПервичногоДокумента,НомерПервичногоДокумента,ДатаПервичногоДокумента");
			
		Если ЭтоДокументОснования И ЗначениеЗаполнено(ПараметрыДокументаДополнительные.НаименованиеПервичногоДокумента) Тогда
			
			ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 №%2 от %3'"),
				ПараметрыДокументаДополнительные.НаименованиеПервичногоДокумента,
				ПараметрыДокументаДополнительные.НомерПервичногоДокумента,
				Формат(ПараметрыДокументаДополнительные.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"));
			
		Иначе
			
			Если ПараметрыДокументаДополнительные.ВидОперации 
					= Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства Тогда
				
				ТекстОкончаниеДокумента = НСтр("ru='%1 об остатках ОС №%2 от %3'");
				
			ИначеЕсли ПараметрыДокументаДополнительные.ВидОперации 
					= Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС Тогда
				
				ТекстОкончаниеДокумента = НСтр("ru='%1 об остатках комплектующих ОС №%2 от %3'");
				
			Иначе
				
				ТекстОкончаниеДокумента = НСтр("ru='%1 об остатках №%2 от %3'");
				
			КонецЕсли;
		
			ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОкончаниеДокумента,
				ТекстУведомленияПоПараметрам(Документ),
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
				Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
		КонецЕсли;
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 о ввозе №%2 от %3'"),
			ТекстУведомленияПоПараметрам(Документ),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Уведомление о перемещении №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Поступление (акт, накладная) №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Возврат товаров поставщику №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Возврат товаров от покупателя №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Реализация (акты, накладные, УПД) №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Корректировка поступления №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Корректировка реализации №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	КонецЕсли;
	
	Возврат ТекстОснование;
	
КонецФункции

// Процедура в которой можно добавить новые реквизиты на форму
// 
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма помощника получения РНПТ
// 
// Пример как добавлять колонку в таблицу НачальныеДанне
// нРеквизиты = Новый Массив;
// нРеквизиты.Добавить(Новый РеквизитФормы("Тест1", Новый ОписаниеТипов("Строка"), "Объект.НачальныеДанные", "Тест3", Истина));
// Форма.ИзменитьРеквизиты(нРеквизиты);
//
// Пример как добавлять колонку в таблицу на форму
// нЭлемент = Элементы.Вставить("Тест3", Тип("ПолеФормы"), Элементы.НачальныеДанные, Элементы.НачальныеДанныеКоличество);
// нЭлемент.Вид = ВидПоляФормы.ПолеВвода;
// нЭлемент.ПутьКДанным = "Объект.НачальныеДанные.Тест1";
//
// Пример как добавлять колонку в таблицу на форму
// переопределение запроса динамического списка Функция: ТекстЗапросаДинамическогоСпискаУведомления()
// нЭлемент = Элементы.Вставить("ТестУведомления", Тип("ПолеФормы"), Элементы.Уведомления, Элементы.УведомленияПредставлениеУведомление);
// нЭлемент.Вид = ВидПоляФормы.ПолеВвода;
// нЭлемент.ПутьКДанным = "Уведомления.ТестУведомления";
//
Процедура ДобавитьДополнительныеРеквизитыНаФорму(Форма) Экспорт

	Элементы = Форма.Элементы;
	
	Возврат ;
	
КонецПроцедуры

Функция ТекстЗапросаДинамическогоСпискаУведомления(ТекстЗапроса) Экспорт
	
	ТекстЗапросаУведомлениеОбОСтатках = ИнтерфейсыВзаимодействияБРОВызовСервера.ТекстыДляЗапросаДокументовРеализацииПолномочийНО(
		"УведомлениеОбОстаткахПрослеживаемыхТоваров");
	ТекстЗапросаУведомлениеОВвозе = ИнтерфейсыВзаимодействияБРОВызовСервера.ТекстыДляЗапросаДокументовРеализацииПолномочийНО(
		"УведомлениеОВвозеПрослеживаемыхТоваров");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|		ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент 
	|	ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ОснованиеКорректировки
	|	КОНЕЦ КАК Основание,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВидОперации КАК ВидОперации,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК Уведомление,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(100)) КАК ПредставлениеУведомление,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(100)) КАК ПредставлениеОснование,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(100)) КАК ПредставлениеРНПТ,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК ТНВЭД,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ КАК РНПТ,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности КАК СостояниеСдачиОтчетности,
	|	ВЫБОР
	|		КОГДА ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности = ЗНАЧЕНИЕ(Перечисление.СостояниеСдачиОтчетности.ДокументооборотНеНачат)
	|				ИЛИ ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности = ЗНАЧЕНИЕ(Перечисление.СостояниеСдачиОтчетности.ПустаяСсылка)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ИНАЧЕ ЖурналОтчетовСтатусы.ДатаОтправки
	|	КОНЕЦ КАК ДатаОтправки,
	|	ЖурналОтчетовСтатусы.Статус КАК Статус1,
	|	ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)) КАК Статус,
	|	ЖурналОтчетовСтатусы.Ссылка КАК РегламентированныйОтчет,
	|	ЖурналОтчетовСтатусы.Организация КАК Организация,
	|	ЖурналОтчетовСтатусы.ВидКонтролирующегоОргана КАК ВидКонтролирующегоОргана,
	|	ЖурналОтчетовСтатусы.КодКонтролирующегоОргана КАК КодКонтролирующегоОргана,
	|	ЖурналОтчетовСтатусы.НаименованиеОтчета КАК НаименованиеОтчета,
	|	ЖурналОтчетовСтатусы.НеОтправляетсяВКонтролирующийОрган КАК НеОтправляетсяВКонтролирующийОрган,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(2)) КАК Отступ,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА (ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ <> """"
	|	И ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ ЕСТЬ НЕ NULL)
	|		ИНАЧЕ 
	|			ВЫБОР
	|				КОГДА СтатусыОтправки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.Сдан)
	|					ТОГДА Истина
	|				ИНАЧЕ
	|					Ложь
	|			КОНЕЦ
	|	КОНЕЦ КАК ДокументыРеализацииПолномочийНО_РНПТЗаполнен,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка КАК ДокументыРеализацииПолномочийНО_Ссылка,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ
	|	КОНЕЦ КАК ДокументыРеализацииПолномочийНО_РНПТ,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ВидУведомления КАК ДокументыРеализацииПолномочийНО_ВидУведомления,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.НомерУведомления КАК ДокументыРеализацииПолномочийНО_НомерУведомления,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ДатаУведомления КАК ДокументыРеализацииПолномочийНО_ДатаУведомления,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.НаименованиеСобственника КАК ДокументыРеализацииПолномочийНО_НаименованиеСобственника,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ФИОСобственника КАК ДокументыРеализацииПолномочийНО_ФИОСобственника,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ИННСобственника КАК ДокументыРеализацииПолномочийНО_ИННСобственника,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.КППСобственника КАК ДокументыРеализацииПолномочийНО_КППСобственника
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = ЖурналОтчетовСтатусы.Ссылка
	|	" + ТекстЗапросаУведомлениеОбОСтатках.СоединениеСДокументом + "
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен = ИСТИНА
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = &Организация
	|	И (УведомлениеОбОстаткахПрослеживаемыхТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		ИЛИ УведомлениеОбОстаткахПрослеживаемыхТоваров.ДатаСоздания МЕЖДУ &НачалоПериода И &КонецПериода)
	|	И ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ = &ПустоеЗначениеРНПТ
	|		ИНАЧЕ НЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ПолученоПодтверждениеИзФНС
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Запрос по корректировочным уведомлениям о ввозе
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ОснованиеКорректировки
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары),
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(100)),
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(100)),
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(100)),
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмерения,
	|	ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности,
	|	ВЫБОР
	|		КОГДА ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности = ЗНАЧЕНИЕ(Перечисление.СостояниеСдачиОтчетности.ДокументооборотНеНачат)
	|				ИЛИ ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности = ЗНАЧЕНИЕ(Перечисление.СостояниеСдачиОтчетности.ПустаяСсылка)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ИНАЧЕ ЖурналОтчетовСтатусы.ДатаОтправки
	|	КОНЕЦ,
	|	ЖурналОтчетовСтатусы.Статус,
	|	ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)),
	|	ЖурналОтчетовСтатусы.Ссылка,
	|	ЖурналОтчетовСтатусы.Организация,
	|	ЖурналОтчетовСтатусы.ВидКонтролирующегоОргана,
	|	ЖурналОтчетовСтатусы.КодКонтролирующегоОргана,
	|	ЖурналОтчетовСтатусы.НаименованиеОтчета,
	|	ЖурналОтчетовСтатусы.НеОтправляетсяВКонтролирующийОрган,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(2)),
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА (ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ <> """"
	|	И ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ ЕСТЬ НЕ NULL)
	|		ИНАЧЕ 
	|			ВЫБОР
	|				КОГДА СтатусыОтправки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.Сдан)
	|					ТОГДА Истина
	|				ИНАЧЕ
	|					Ложь
	|			КОНЕЦ
	|	КОНЕЦ КАК ДокументыРеализацииПолномочийНО_РНПТЗаполнен,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка КАК ДокументыРеализацииПолномочийНО_Ссылка,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ
	|	КОНЕЦ КАК ДокументыРеализацииПолномочийНО_РНПТ,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ВидУведомления КАК ДокументыРеализацииПолномочийНО_ВидУведомления,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.НомерУведомления КАК ДокументыРеализацииПолномочийНО_НомерУведомления,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ДатаУведомления КАК ДокументыРеализацииПолномочийНО_ДатаУведомления,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.НаименованиеСобственника КАК ДокументыРеализацииПолномочийНО_НаименованиеСобственника,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ФИОСобственника КАК ДокументыРеализацииПолномочийНО_ФИОСобственника,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.ИННСобственника КАК ДокументыРеализацииПолномочийНО_ИННСобственника,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.КППСобственника КАК ДокументыРеализацииПолномочийНО_КППСобственника
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = ЖурналОтчетовСтатусы.Ссылка
	|	" + ТекстЗапросаУведомлениеОВвозе.СоединениеСДокументом + "
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Проведен = ИСТИНА
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Организация = &Организация
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ = &ПустоеЗначениеРНПТ
	|		ИНАЧЕ НЕ УведомлениеОВвозеПрослеживаемыхТоваров.ПолученоПодтверждениеИзФНС
	|	КОНЕЦ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДинамическогоСпискаТоварыПоУведомлениям(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемогоТовараТовары.Ссылка КАК Уведомление,
	|	УведомлениеОбОстаткахПрослеживаемогоТовараТовары.Номенклатура КАК Номенклатура,
	|	УведомлениеОбОстаткахПрослеживаемогоТовараТовары.КодОКПД2 КАК КодОКПД2,
	|	ЛОЖЬ КАК ЗаполненоОКПД2,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемогоТовараТовары.Количество
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемогоТовараТовары.КоличествоПослеИзменения
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемогоТовараТовары.КоличествоПрослеживаемости
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемогоТовараТовары.КоличествоПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемогоТовараТовары.Сумма
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемогоТовараТовары.СуммаПослеИзменения
	|	КОНЕЦ КАК Сумма,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК ЕдиницаПрослеживаемости,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения = УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК ЕдиницыИзмеренияОдинаковы,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(1)) КАК Отступ,
	|	УведомлениеОбОстаткахПрослеживаемогоТовараТовары.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемогоТовараТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ПО УведомлениеОбОстаткахПрослеживаемогоТовараТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.КодОКПД2,
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|	КОНЕЦ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмерения,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемости,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмерения = УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемости,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(1)),
	|	NULL
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Проведен = ИСТИНА";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПустоеЗначениеРНПТ() Экспорт
	
	Возврат Справочники.НомераГТД.ПустаяСсылка();
	
КонецФункции

Функция ТекстУведомленияПоПараметрам(Документ)
	
	НомерКорректировки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "НомерКорректировки");
	
	Возврат ?(НомерКорректировки = 0, НСтр("ru='Уведомление'"), НСтр("ru='Корректировочное уведомление'"));
		
КонецФункции

// Возвращает текст представления первичного документа
// Представление документа определяется по наименованию, ввденному вручную или по ссылке документа
//
// Параметры:
//  СсылкаНаПервичныйДокумент - СсылкаНаДокумент - Ссылка на документ
//  ТекстНаименованиеПервичногоДокумента - Строка - наименование документа, заполняется при ручном вводе первичного документа
//  ТекстНомерПервичногоДокумента - Строка - номер документа, заполняется при ручном вводе первичного документа
//  ДатаПервичногоДокумента - Дата - дата документа,  заполняется при ручном вводе первичного документа
//
// Возвращаемое значение:
//		Строка - Представление первичного докуметнта
//
Функция ПредставлениеПервичногоДокументаВУведомленииОбОстатках(
			СсылкаНаПервичныйДокумент, ТекстНаименованиеПервичногоДокумента, 
			ТекстНомерПервичногоДокумента, ДатаПервичногоДокумента) Экспорт

	ТекстГиперссылки = НСтр("ru='Выбор'");
	
	Если ЗначениеЗаполнено(ТекстНаименованиеПервичногоДокумента)Тогда

		ТекстГиперссылки = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 №%2 от %3'"), 
				ТекстНаименованиеПервичногоДокумента, ТекстНомерПервичногоДокумента, 
				Формат(ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"));
		
	ИначеЕсли ЗначениеЗаполнено(СсылкаНаПервичныйДокумент) Тогда
		
		ТекстГиперссылки = ПредставлениеДокумента(СсылкаНаПервичныйДокумент);
		
	КонецЕсли;
		
	Возврат ТекстГиперссылки;
	
КонецФункции

// Возвращает строку с представлением периода.
//
// Параметры:
//	НачалоПериода - Дата - Начало периода.
//	КонецПериода - Дата - Конец периода.
//	ТолькоДаты - Булево - Если Ложь, то возвращаемая строка включает предлог "за".
//
// Возвращаемое значение:
//  Строка - Текст представления периода.
//
Функция ПолучитьПредставлениеПериода(НачалоПериода = '00010101', КонецПериода = '00010101', ТолькоДаты  = Ложь) Экспорт

	Возврат БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
				НачалоПериода, КонецПериода, ТолькоДаты);
	
КонецФункции

// Создает инвентаризацию товаров в помощнике Получения РНПТ
//
// Параметры:
// 
// Дата - Дата - Дата, на которую будет создана инвентаризация
// Организация - СправочникСсылка.Организации
// ТекущийСклад - Склад, по которому необходимо создать инвентаризацию
// СписокНоменклатуры - СписокЗначений - список товаров для инвентаризации
// 
// Возвращаемое значение:
// СсылкаОбъект.ИнвентаризацияТоваровНаСкладе - Ссылка на документ инвентаризации
Функция СоздатьИнвентаризациюТовара(Дата, Организация, ТекущийСклад, СписокНоменклатуры) Экспорт
	
	НовыйДокумент = Документы.ИнвентаризацияТоваровНаСкладе.СоздатьДокумент();
	НовыйДокумент.Дата = Дата;
	
	ДанныеЗаполнения = ДанныеЗаполненияИнвентаризации(Дата, Организация, ТекущийСклад, СписокНоменклатуры);
	
	НовыйДокумент.Заполнить(ДанныеЗаполнения);
	
	НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат НовыйДокумент;
	
КонецФункции

Функция ДанныеЗаполненияИнвентаризации(Дата, Организация, Склад, СписокНоменклатуры)
	
	ДанныеЗаполнения = Новый Структура();
	
	ДанныеЗаполнения.Вставить("Дата",                                Дата);
	ДанныеЗаполнения.Вставить("Организация",                         Организация);
	ДанныеЗаполнения.Вставить("Склад",                               Склад);
	ДанныеЗаполнения.Вставить("СписокНоменклатуры",                  СписокНоменклатуры);
	ДанныеЗаполнения.Вставить("ЗаполняетсяИзПомощникаПолученияРНПТ", Истина);
	
	Возврат ДанныеЗаполнения;
КонецФункции

#КонецОбласти

// Формирует временные таблицы СведенияОСсылкахНаДаты и СведенияОНоменклатуре со сведениями, необходимыми
// для отчета об операциях.
// СведенияОСсылкахНаДаты:
//  * Ссылка - СправочникСсылка.Контрагенты
//  * ДатаСведений - дата на которую получены сведения о контрагенте
//  * Наименование, ИНН, КПП - сведения о контрагенте
// СведенияОНоменклатуре:
//  * Ссылка - СправочникСсылка.Номенклатура
//  * КодТНВЭД, КодОКПД2, ЕдиницаИзмерения, ЕдиницаИзмеренияПрослеживаемости - сведения о номенклатуре
//
Процедура ДополнительныеСведенияДляОтчетаОбОперациях(МенеджерВременныхТаблиц) Экспорт
	
	ПрослеживаемостьБП.ДополнительныеСведенияДляОтчетаОбОперациях(МенеджерВременныхТаблиц);
	
КонецПроцедуры

// Для пустого отчета об операциях в процедуре заполняется текст предупреждения для пользователя
Процедура ПредупреждениеОбОтсутствииДанныхВОтчете(ПараметрыОтчета) Экспорт
	
	ПрослеживаемостьБП.СообщениеОбОтсутствииДанныхВОтчете(ПараметрыОтчета);
	
КонецПроцедуры

// Формирует таблицу товаров, для регистра РеализацияПрослеживаемыхТоваров в ЕАЭС. 
// Далее по этим движениям будет заполняться документ Уведомление о перемещении прослеживаемых товаров
Функция ПодготовитьТаблицуРеализацияПрослеживаемыхТоваровВЕАЭС(ПрослеживаемыеТоварыРеализованные, РеализацияТаблицаДокумента, ТаблицаРеквизитов) Экспорт
	Если Не ЗначениеЗаполнено(ПрослеживаемыеТоварыРеализованные)
		ИЛИ Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
			Возврат Неопределено;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыПрослеживаемыхТоваров(ПрослеживаемыеТоварыРеализованные, РеализацияТаблицаДокумента, ТаблицаРеквизитов);
	
	Реквизиты = Параметры.Реквизиты[0];
	
	Если НЕ (ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Реквизиты.Период))
		ИЛИ (НЕ Справочники.СтраныМира.ГосударствоЧленТаможенногоСоюза(Реквизиты.СтранаРегистрации)) Тогда
			Возврат Неопределено;
	КонецЕсли;
	
	РеализацияПрослеживаемыхТоваров = Параметры.ПрослеживаемыеТовары.Скопировать();
	РеализацияПрослеживаемыхТоваров.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	РеализацияПрослеживаемыхТоваров.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	РеализацияПрослеживаемыхТоваров.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	РеализацияПрослеживаемыхТоваров.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	РаспределитьСуммуПродажиПоПрослеживаемымТоварам(РеализацияПрослеживаемыхТоваров, Параметры.РеализацияТаблицаДокумента, Реквизиты);
	
	Возврат РеализацияПрослеживаемыхТоваров;
	
КонецФункции

Функция ПодготовитьПараметрыПрослеживаемыхТоваров(ТаблицаТовары, РеализацияТаблицаДокумента, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;

	СписокОбязательныхКолонок = ""
	+ "Номенклатура,"               // <СправочникСсылка.Номенклатура>
	+ "РНПТ,"                       // <СправочникСсылка.НомераГТД>
	+ "Количество,"                 // <Число,15,3> - количество
	+ "СопроводительныйДокумент,"   // <ДокументСсылка.РеализацияТоваровУслуг> - документ реализации
	+ "Контрагент,"                 // <СправочникСсылка.Контрагенты> - Контрагент
	+ "ПорядковыйНомер,"                 // <Число> - НомерСтроки номенклатуры в табличной части документа
	+ "КоличествоПрослеживаемости"; // <Число,26,11> - количество в единицах прослеживаемости
	
	Параметры.Вставить("ПрослеживаемыеТовары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
		
	СписокОбязательныхКолонок = ""
	+ "Номенклатура,"// <СправочникСсылка.Номенклатура>
	+ "Количество,"  // <Число,15,3> - количество
	+ "СуммаБезНДС,"    // <Число,26,11> - Сумма продажи без НДС
	+ "НомерСтроки"; // <Число> - Номер строки документа в табличной части
	
	Параметры.Вставить("РеализацияТаблицаДокумента", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		РеализацияТаблицаДокумента, СписокОбязательныхКолонок));
		
	СписокОбязательныхКолонок = ""
	+ "Период,"      // <Дата>
	+ "Регистратор," // <ДокументСсылка>
	+ "Организация," // <СправочникСсылка.Организации>
	+ "СтранаРегистрации," // <СправочникСсылка.СтраныМира>
	+ "Контрагент"; // <СправочникСсылка.Контрагенты>
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
		
	Возврат Параметры;
	
	
КонецФункции

Процедура РаспределитьСуммуПродажиПоПрослеживаемымТоварам(РеализацияПрослеживаемыхТоваров, РеализацияТаблицаДокумента, Реквизиты)
	
	РеализацияТаблицаДокумента.Свернуть("Номенклатура, НомерСтроки","Количество, СуммаБезНДС");
	РеализацияТаблицаДокумента.Колонки.Добавить("Списано", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	РеализацияТаблицаДокумента.Колонки.Добавить("СуммаСписано", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	ФильтрПоНоменклатуре = Новый Структура("Номенклатура,НомерСтроки");
	
	Для Каждого ТекСтрокаРеализацияПрослеживаемыхТоваров Из РеализацияПрослеживаемыхТоваров Цикл
		
		ЗаполнитьЗначенияСвойств(ТекСтрокаРеализацияПрослеживаемыхТоваров, Реквизиты);
		ЗаполнитьЗначенияСвойств(ФильтрПоНоменклатуре, ТекСтрокаРеализацияПрослеживаемыхТоваров, "Номенклатура");
		ФильтрПоНоменклатуре.НомерСтроки = ТекСтрокаРеализацияПрослеживаемыхТоваров.ПорядковыйНомер;
		
		НайденныеСтроки = РеализацияТаблицаДокумента.НайтиСтроки(ФильтрПоНоменклатуре);
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			
			ОстатокКоличество = ТекущаяСтрока.Количество - ТекущаяСтрока.Списано;
			
			Если ОстатокКоличество = ТекСтрокаРеализацияПрослеживаемыхТоваров.Количество Тогда
				
				ТекСтрокаРеализацияПрослеживаемыхТоваров.Сумма = 
					ТекущаяСтрока.СуммаБезНДС - ТекущаяСтрока.СуммаСписано;
			Иначе
				
				Цена = ТекущаяСтрока.СуммаБезНДС/ТекущаяСтрока.Количество;
				
				ТекСтрокаРеализацияПрослеживаемыхТоваров.Сумма = ТекСтрокаРеализацияПрослеживаемыхТоваров.Количество * Цена;
				
			КонецЕсли;
			
			ТекущаяСтрока.Списано = ТекущаяСтрока.Списано + ТекСтрокаРеализацияПрослеживаемыхТоваров.Количество;
			ТекущаяСтрока.СуммаСписано = ТекущаяСтрока.СуммаСписано + ТекСтрокаРеализацияПрослеживаемыхТоваров.Сумма;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#Область ВыбытиеВЕАЭС

Процедура СформироватьДвиженияПрослеживаемыхТоваровВЕАЭС(ПрослеживаемыеТоварыРеализованные, РеализацияТаблицаДокумента, ТаблицаРеквизитов, Движения) Экспорт
	
	РеализацияПрослеживаемыхТоваровВЕАЭС = ПодготовитьТаблицуРеализацияПрослеживаемыхТоваровВЕАЭС(
		ПрослеживаемыеТоварыРеализованные, РеализацияТаблицаДокумента, ТаблицаРеквизитов);
	
	Если НЕ ЗначениеЗаполнено(РеализацияПрослеживаемыхТоваровВЕАЭС) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из РеализацияПрослеживаемыхТоваровВЕАЭС Цикл
		Запись = Движения.ПрослеживаемыеТоварыОтгруженныеВЕАЭС.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;
	
	Движения.ПрослеживаемыеТоварыОтгруженныеВЕАЭС.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РегистрацияПрослеживаемогоТовара

Функция ПодготовитьПараметрыТаблицыРегистрацииПрослеживаемыхТоваров(ТаблицаРеквизитов, ТаблицаТоваров, ТаблицаОС)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.ТаблицаТоваров
	
	СписокОбязательныхКолонок = ""
		+ "Ссылка,"                        // <ДокументСсылка.*> - документ-регистратор движений
		+ "НомерСтроки,"                   // <Число> - номер строки в таблице товаров
		+ "Номенклатура,"                  // <СправочникСсылка.Номенклатура> - номенклатура
		+ "Количество,"                    // <Число ,15,3> - количество
		+ "КоличествоПрослеживаемости,"    // <Число, 15,3> - количество по прослеживаемости
		+ "Сумма,"                         // <Число, 15,3> - сумма
		+ "СтранаПроисхождения,"           // <СправочникСсылка.СтраныМира> - страна происхождения товара
		+ "Комиссионер";                   // <СправочникСсылка.Контрагенты> - комиссионер. Заполняется, если товар у комиссионера
		
	Параметры.Вставить("ТаблицаТоваров", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТоваров, СписокОбязательныхКолонок));
		
	// Подготовка таблицы Параметры.ТаблицаОС
	
	СписокОбязательныхКолонок = ""
		+ "Ссылка,"                        // <ДокументСсылка.*> - документ-регистратор движений
		+ "НомерСтроки,"                   // <Число> - номер строки в таблице товаров
		+ "ОсновноеСредство,"              // <СправочникСсылка.ОсновныеСредства> - основное средство
		+ "Комплектующие,"                 // <СправочникСсылка.Номенклатура> - номенклатура
		+ "Количество,"                    // <Число ,15,3> - количество
		+ "КоличествоПрослеживаемости,"    // <Число, 15,3> - количество по прослеживаемости
		+ "Сумма,"                         // <Число, 15,3> - сумма
		+ "СтранаПроисхождения";           // <СправочникСсылка.СтраныМира> - страна происхождения товара
		
	Параметры.Вставить("ТаблицаОС", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаОС, СписокОбязательныхКолонок));
		
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Период,"							// <Дата> - период движений - дата документа
		+ "Регистратор,"					// <ДокументСсылка.*> - документ-регистратор движений
		+ "Организация,"					// <СправочникСсылка.Организации> - организация
		+ "РНПТ,"							// <ОпределяемыеТипы.РНПТ> - РНПТ
		+ "ЭтоКорректировочноеУведомление,"	// <Булево> - признак, показывающий, что данные их корректировочного документа
		+ "ПолученоПодтверждениеИзФНС,"		// <Булево> - признак, показывающий, что получено подтверждение от ФНС
		+ "Основание";						//<ДокументСсылка.*> - документ-основания
		
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

Функция ТекстЗапросаПоОснованию(ДокументОснование, ПараметрыДокументаОснования)
	
	ОбъектМетаданных = ДокументОснование.Метаданные();
	
	ТекстЗапроса = Документы[ОбъектМетаданных.Имя].ТекстЗапросаТаблицаПрослеживаемыхТоваровПоОснованию();
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоУведомлениямИВычислениюОстатка()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество) КАК Количество,
	|	СУММА(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма) КАК Сумма,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК ТНВЭД,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки КАК НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК СсылкаНаУведомление,
	|	&ПустаяСсылкаРнпт КАК РНПТ,
	|	&ПустаяСсылкаРнпт КАК РНПТИзУведомления,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_ВыписанныеУведомления
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = &Основание
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка <> &ДокументИсключение
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|				ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
	|			ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество - УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|				ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
	|			ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма - УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|		КОНЕЦ),
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА &ПустаяСсылкаРнпт
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ
	|	КОНЕЦ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ,
	|	ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = &Основание
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка <> &ДокументИсключение
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА &ПустаяСсылкаРнпт
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ
	|	КОНЕЦ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|				ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество
	|			КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = УведомлениеОбОстаткахПрослеживаемыхТоваров.ОснованиеКорректировки
	|				ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения - УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество
	|			ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество - УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|				ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма
	|			КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = УведомлениеОбОстаткахПрослеживаемыхТоваров.ОснованиеКорректировки
	|				ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения - УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма
	|			ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма - УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|		КОНЕЦ),
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ,
	|	NULL,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.ОсновноеСредство
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ОснованиеКорректировки = &Основание
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка <> &ДокументИсключение
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.ОсновноеСредство,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|				ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
	|			КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = УведомлениеОВвозеПрослеживаемыхТоваров.ОснованиеКорректировки
	|				ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения - УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
	|			ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество - УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|				ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
	|			КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = УведомлениеОВвозеПрослеживаемыхТоваров.ОснованиеКорректировки
	|				ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения - УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
	|			ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма - УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|		КОНЕЦ),
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА &ПустаяСсылкаРнпт
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = УведомлениеОВвозеПрослеживаемыхТоваров.ОснованиеКорректировки
	|			ТОГДА &ПустаяСсылкаРнпт
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ
	|	КОНЕЦ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ,
	|	ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ОснованиеКорректировки = &Основание
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка <> &ДокументИсключение
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА &ПустаяСсылкаРнпт
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ
	|	КОНЕЦ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА &ПустаяСсылкаРнпт
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = УведомлениеОВвозеПрослеживаемыхТоваров.ОснованиеКорректировки
	|			ТОГДА &ПустаяСсылкаРнпт
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоОснованию.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровПоОснованию.Количество КАК Количество,
	|	ТаблицаТоваровПоОснованию.Сумма КАК Сумма,
	|	ТаблицаТоваровПоОснованию.ТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваровПоОснованию.Организация КАК Организация,
	|	ТаблицаТоваровПоОснованию.ВидОперации КАК ВидОперации,
	|	ТаблицаТоваровПоОснованию.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваровПоОснованию.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТоваровПоОснованию.РНПТ КАК РНПТ,
	|	ТаблицаТоваровПоОснованию.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ТаблицаТоваровПоОснованию.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаТоваровПоОснованию.ВсегоКоличествоКомплектующих КАК ВсегоКоличествоКомплектующих,
	|	ТаблицаТоваровПоОснованию.ВсегоКомлектующихНаСумму КАК ВсегоКомлектующихНаСумму
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровПоОснованию
	|ИЗ
	|	ВТ_ТоварыПоОснованию КАК ТаблицаТоваровПоОснованию
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.СсылкаНаУведомление) КАК СсылкаНаУведомление,
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.НомерКорректировки) КАК НомерКорректировки,
	|	ВЫБОР
	|		КОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках = ЗНАЧЕНИЕ(Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПустаяСсылка)
	|			ТОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|		ИНАЧЕ ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках
	|	КОНЕЦ КАК Основание
	|ПОМЕСТИТЬ ПоследниеУведомления
	|ИЗ
	|	ВТ_ВыписанныеУведомления КАК ВТ_ВыписанныеУведомления
	|ГДЕ
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление ССЫЛКА Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках = ЗНАЧЕНИЕ(Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПустаяСсылка)
	|			ТОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|		ИНАЧЕ ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.СсылкаНаУведомление),
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.НомерКорректировки),
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|ИЗ
	|	ВТ_ВыписанныеУведомления КАК ВТ_ВыписанныеУведомления
	|ГДЕ
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление ССЫЛКА Документ.УведомлениеОВвозеПрослеживаемыхТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоУведомлениям.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаТоваровПоУведомлениям.Количество) КАК Количество,
	|	СУММА(ТаблицаТоваровПоУведомлениям.Сумма) КАК Сумма,
	|	ТаблицаТоваровПоУведомлениям.ТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваровПоУведомлениям.РНПТ КАК РНПТ,
	|	ТаблицаТоваровПоУведомлениям.РНПТИзУведомления КАК РНПТИзУведомления,
	|	ТаблицаТоваровПоУведомлениям.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровПоУведомлениям
	|ИЗ
	|	ПоследниеУведомления КАК ПоследниеУведомления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыписанныеУведомления КАК ТаблицаТоваровПоУведомлениям
	|		ПО ПоследниеУведомления.СсылкаНаУведомление = ТаблицаТоваровПоУведомлениям.СсылкаНаУведомление
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваровПоУведомлениям.Номенклатура,
	|	ТаблицаТоваровПоУведомлениям.ТНВЭД,
	|	ТаблицаТоваровПоУведомлениям.РНПТ,
	|	ТаблицаТоваровПоУведомлениям.РНПТИзУведомления,
	|	ТаблицаТоваровПоУведомлениям.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТНВЭД,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоОснованию.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ЭтоКорректировка
	|			ТОГДА ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0)
	|		КОГДА ТаблицаТоваровПоОснованию.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС)
	|				И ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) - ТаблицаТоваровПоОснованию.Количество
	|		КОГДА ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) < 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0)
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ЭтоКорректировка
	|			ТОГДА ТаблицаТоваровПоОснованию.Сумма - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Сумма, 0)
	|		КОГДА ТаблицаТоваровПоОснованию.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС)
	|				И ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Сумма, 0) - ТаблицаТоваровПоОснованию.Сумма
	|		КОГДА ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) < 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТоваровПоОснованию.Сумма - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Сумма, 0)
	|	КОНЕЦ КАК Сумма,
	|	ТаблицаТоваровПоОснованию.ТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваровПоОснованию.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваровПоОснованию.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС)
	|				И ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) <> 0
	|				И ТаблицаТоваровПоУведомлениям.РНПТИзУведомления <> &ПустаяСсылкаРнпт
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.КорректировкаТоваровИзЕАЭС)
	|		ИНАЧЕ ТаблицаТоваровПоОснованию.ВидОперации
	|	КОНЕЦ КАК ВидОперации,
	|	&Основание КАК Основание,
	|	ТаблицаТоваровПоОснованию.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваровПоОснованию.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваровПоОснованию.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС)
	|				И ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) <> 0
	|			ТОГДА ТаблицаТоваровПоУведомлениям.РНПТИзУведомления
	|		ИНАЧЕ ТаблицаТоваровПоОснованию.РНПТ
	|	КОНЕЦ КАК РНПТ,
	|	ТаблицаТоваровПоУведомлениям.РНПТИзУведомления КАК РНПТИзУведомления,
	|	ТаблицаТоваровПоОснованию.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ТаблицаТоваровПоОснованию.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаТоваровПоОснованию.ВсегоКоличествоКомплектующих КАК ВсегоКоличествоКомплектующих,
	|	ТаблицаТоваровПоОснованию.ВсегоКомлектующихНаСумму КАК ВсегоКомлектующихНаСумму
	|ИЗ
	|	ВТ_ТаблицаТоваровПоОснованию КАК ТаблицаТоваровПоОснованию
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаТоваровПоУведомлениям КАК ТаблицаТоваровПоУведомлениям
	|		ПО ТаблицаТоваровПоОснованию.ТНВЭД = ТаблицаТоваровПоУведомлениям.ТНВЭД
	|			И ТаблицаТоваровПоОснованию.Номенклатура = ТаблицаТоваровПоУведомлениям.Номенклатура
	|			И ТаблицаТоваровПоОснованию.РНПТ = ТаблицаТоваровПоУведомлениям.РНПТ
	|			И ТаблицаТоваровПоОснованию.ОсновноеСредство = ТаблицаТоваровПоУведомлениям.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ТаблицаТоваровТолькоОприходование.Номенклатура,
	|	ВТ_ТаблицаТоваровТолькоОприходование.Количество,
	|	ВТ_ТаблицаТоваровТолькоОприходование.Сумма,
	|	ВТ_ТаблицаТоваровТолькоОприходование.ТНВЭД,
	|	ВТ_ТаблицаТоваровТолькоОприходование.Организация,
	|	ВТ_ТаблицаТоваровТолькоОприходование.ВидОперации,
	|	&Основание,
	|	ВТ_ТаблицаТоваровТолькоОприходование.ПериодСобытия,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	ВТ_ТаблицаТоваровРазмеченный КАК ВТ_ТаблицаТоваровТолькоОприходование
	|ГДЕ
	|	ВТ_ТаблицаТоваровТолькоОприходование.РанееРегистрировалсяВПрослеживаемости
	|	И ВТ_ТаблицаТоваровТолькоОприходование.Количество > ВТ_ТаблицаТоваровТолькоОприходование.КоличествоУчет";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОтключениеФункциональноастиПоОС

// Вызывается при установке отображения страниц в Помощнике получения РНПТ
//
// Параметры:
// 
// ОтображениеСтраниц - ОтображениеСтраницФормы - ссылка на отображение страниц
//
Процедура ОтображениеСтраницФормыПомощникаРНПТ(ОтображениеСтраниц) Экспорт
	
	Если ОтключитьФункциональностьПоОС() Тогда
		ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе
		ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при открытии Помощника получения РНПТ и при открытии журнала по уведомлениям
//
// Параметры:
//
// Форма - ФормаКлиентскогоПриложения - форма объекта
//
Процедура ИзменитьУсловноеОформление(Форма) Экспорт
	
	// Для УНФ сделана возможность отлючения функциональности по ОС
	
	Элементы = Форма.Элементы;
	
	Если Форма.ИмяФормы = "Обработка.ПомощникПолученияРНПТ.Форма.Форма" Тогда
		
		Если ОтключитьФункциональностьПоОС() Тогда
			Элементы.СтраницаОС.Видимость = Ложь;
			Элементы.НачальныеДанные.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		КонецЕсли;
		
	ИначеЕсли Форма.ИмяФормы = "ЖурналДокументов.ПрослеживаемостьУведомления.Форма.ФормаСписка" Тогда
		
		Если ОтключитьФункциональностьПоОС() Тогда
			
			Элементы.СоздатьУведомлениеОбОстаткахПрослеживаемыхОС.Видимость = Ложь;
			Элементы.СоздатьУведомлениеОбОстаткахПрослеживаемыхСоставныхОС.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


// Добавляет список видов уведомлений, что будут отражаться при добавлении нового документа Уведомления об остатках
//
// Возвращаемое значение:
// СписокЗначений - список видов уведомлений
//
Функция СписокВидовУведомленийОбОстатках() Экспорт
	
	СписокВидовУведомлений = Новый СписокЗначений();
	ВидыУведомлений = ПрослеживаемостьБРУ.ВидыУведомленийОбОСтатках();
	
	СписокВидовУведомлений.Добавить(ВидыУведомлений.УведомлениеПоТоварам, НСтр("ru = 'Уведомление об остатках прослеживаемых товаров'"));
	
	Если НЕ ОтключитьФункциональностьПоОС() Тогда
		СписокВидовУведомлений.Добавить(ВидыУведомлений.УведомлениеПоОС, НСтр("ru = 'Уведомление об остатках прослеживаемых ОС'"));
		СписокВидовУведомлений.Добавить(ВидыУведомлений.УведомлениеПоКомплектующимОС, НСтр("ru = 'Уведомление об остатках прослеживаемых комплектующих ОС'"));
	КонецЕсли;

	СписокВидовУведомлений.Добавить(ВидыУведомлений.КорректировочноеУведомлениеПоТоварам, НСтр("ru = 'Корректировочное уведомление об остатках прослеживаемых товаров'"));
	
	Если НЕ ОтключитьФункциональностьПоОС() Тогда
		СписокВидовУведомлений.Добавить(ВидыУведомлений.КорректировочноеУведомлениеПоОС, НСтр("ru = 'Корректировочное уведомление об остатках прослеживаемых ОС'"));
		СписокВидовУведомлений.Добавить(ВидыУведомлений.КорректировочноеУведомлениеПоКомплектующимОС, НСтр("ru = 'Корректировочное уведомление об остатках прослеживаемых комплектующих ОС'"));
	КонецЕсли;
	
	Возврат СписокВидовУведомлений;
	
КонецФункции

// Функция, которая отключает функциональность по прослеживаемым ОС
// в Помощнике получения РНПТ и в журнале уведомлений
//
// Возвращаемое значение:
//  Булево - Истина - функциональность отключить по прослеживаемы ОС, ложь - не отключать
//
Функция ОтключитьФункциональностьПоОС() Экспорт
	
	Возврат Ложь;

КонецФункции

#КонецОбласти
