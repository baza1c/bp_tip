#Область ПрограммныйИнтерфейс

// Переопределяет возможность выполнения задачи текущим пользователем
// Подробнее
// См. КалендарьБухгалтера.ПравоВыполненияЗадачи
//
Процедура ПолучитьПравоВыполненияЗадачи(ЕстьПраво, Задача) Экспорт
	
	ЕстьПраво = Ложь;
	
	Если ТипЗнч(Задача) = Тип("ПеречислениеСсылка.ЗадачиНачалаРаботы") Тогда
		
		Если Задача = Перечисления.ЗадачиНачалаРаботы.СписокНалоговОтчетов Тогда
			
			ЕстьПраво = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НалогиОтчеты);
			
		ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.ВводНачальныхОстатков Тогда
			
			ЕстьПраво = ПравоДоступа("Изменение", Метаданные.Документы.ВводНачальныхОстатков);
			
		ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.НастройкаЗаполненияФормСтатистики Тогда
			
			ЕстьПраво = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкаЗаполненияФормСтатистики);
			
		ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.Подключение1СОтчетности Тогда
			
			ЕстьПраво = ПравоДоступа("Использование", Метаданные.Обработки.ОбщиеОбъектыРеглОтчетности);
			
		ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.Подключение1СЭДО Тогда
			
			ЕстьПраво = ЭлектронноеВзаимодействиеБПВызовСервера.ЕстьПравоНастройкиЭДО();
			Если Не ЕстьПраво Тогда
				
				ТекстСообщения = НСтр("ru = 'Недостаточно прав для настройки обмена электронными документами. Обратитесь к администратору.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				
			КонецЕсли;
			
		ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.ОплатитьЗадолженностьПоНалогамВзносам Тогда
			
			ЕстьПраво = ПравоДоступа("Изменение", Метаданные.Документы.ВводНачальныхОстатков);
			
		ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.УказатьДатуПрекращенияДеятельности Тогда
			
			ЕстьПраво = ПравоДоступа("Изменение", Метаданные.Справочники.Организации);
			
		ИначеЕсли Задача = Перечисления.ЗадачиНачалаРаботы.ПроверитьОсновнойВидДеятельности Тогда
			
			ЕстьПраво = ПравоДоступа("Изменение", Метаданные.Справочники.Организации);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Задача) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей") Тогда
		
		ЕстьПраво = ПравоДоступа("Изменение", Метаданные.Документы.ПлатежноеПоручение);
		
	ИначеЕсли ТипЗнч(Задача) = Тип("СправочникСсылка.ПравилаФинОтчетности") Тогда
		
		ЕстьПраво = ПравоДоступа("Изменение", Метаданные.Документы.ФинОтчетВБанк);
		
	Иначе
		
		ЕстьПраво = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ЗадачиБухгалтера);
		
	КонецЕсли;
	
КонецПроцедуры 

// Подробнее
// См. КалендарьБухгалтера.СобытияКалендаряБухгалтера
//
Процедура ЗаполнитьСобытияКалендаряБухгалтера(ЗадачиОрганизаций, Организации, ОтборЗадач) Экспорт
	
	ЭтоИнтерфейсИнтеграцииСБанком = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
	НаименованиеЗадачиУплатаПатента      = НСтр("ru = 'уплата патента'");
	НаименованиеЗадачиУведомлениеПатента = НСтр("ru = 'уведомление по патенту'");
	
	ТаблицаСтатусовВыполненныхЗадач = Новый ТаблицаЗначений();
	ТаблицаСтатусовВыполненныхЗадач.Колонки.Добавить("Статус", Метаданные.РегистрыСведений.ЗадачиБухгалтера.Ресурсы.Статус.Тип);
	Для Каждого Статус ИЗ ВыполнениеЗадачБухгалтера.СтатусыВыполненныхЗадач() Цикл
		ТаблицаСтатусовВыполненныхЗадач.Добавить().Статус = Статус;
	КонецЦикла;
	
	ДатаНачалаСобытий = НачалоДня(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачалаСобытий",                      ДатаНачалаСобытий);
	Запрос.УстановитьПараметр("СписокОрганизаций",                      Организации);
	Запрос.УстановитьПараметр("СтатусыВыполненныхЗадач",                ТаблицаСтатусовВыполненныхЗадач);
	Запрос.УстановитьПараметр("НаименованиеЗадачиУплатаПатента",        НаименованиеЗадачиУплатаПатента);
	Запрос.УстановитьПараметр("НаименованиеЗадачиУведомлениеПатента",   НаименованиеЗадачиУведомлениеПатента);
	Запрос.УстановитьПараметр("ВыбиратьЗадачиОтчетностиИУплатыНалогов", ОтборЗадач.ЗадачиОтчетностиИУплатыНалогов);
	Запрос.УстановитьПараметр("ВыбиратьРегулярныеПлатежи",              ОтборЗадач.РегулярныеПлатежи);
	Запрос.УстановитьПараметр("ВыбиратьПроверкиКонтролирующихОрганов",  ОтборЗадач.ПроверкиКонтролирующихОрганов);
	Запрос.УстановитьПараметр("ВыбиратьФинОтчетность"                ,  ОтборЗадач.ФинОтчетность);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыВыполненныхЗадач.Статус КАК Статус
	|ПОМЕСТИТЬ СтатусыВыполненныхЗадач
	|ИЗ
	|	&СтатусыВыполненныхЗадач КАК СтатусыВыполненныхЗадач
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Статус
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.ВАрхиве КАК ВАрхиве,
	|	ВЫБОР
	|		КОГДА СтатусыВыполненныхЗадач.Статус ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Выполнено,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ЗадачиБухгалтера.НаименованиеСокращенное КАК Наименование,
	|	ЗадачиБухгалтера.Наименование КАК НаименованиеПолное,
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.ХешЗадачи КАК ХешЗадачи,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ПОМЕСТИТЬ ВТ_АктуальныеЗадачи
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатусыВыполненныхЗадач КАК СтатусыВыполненныхЗадач
	|		ПО (СтатусыВыполненныхЗадач.Статус = ЗадачиБухгалтера.Статус)
	|ГДЕ
	|	ЗадачиБухгалтера.Организация В(&СписокОрганизаций)
	|	И ЗадачиБухгалтера.Срок >= &ДатаНачалаСобытий
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Организация КАК Организация,
	|	ВТ_АктуальныеЗадачи.Срок КАК Срок,
	|	ВТ_АктуальныеЗадачи.ХешЗадачи КАК ХешЗадачи,
	|	Требования.Владелец.Наименование КАК Наименование,
	|	ВТ_АктуальныеЗадачи.НаименованиеПолное КАК НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ДействиеПорядок,
	|	Требования.Владелец.Ссылка КАК Задача,
	|	ВТ_АктуальныеЗадачи.Выполнено КАК Выполнено,
	|	ВТ_АктуальныеЗадачи.ВАрхиве КАК ВАрхиве,
	|	Требования.ОтправкаОтчетаОграничена КАК ОтправкаОтчетаОграничена,
	|	ВТ_АктуальныеЗадачи.Действие КАК Действие,
	|	Требования.ВыполняетсяЕдинымПомощником КАК ВыполняетсяЕдинымПомощником
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Требования
	|		ПО ВТ_АктуальныеЗадачи.Правило = Требования.Ссылка
	|ГДЕ
	|	НЕ Требования.ПометкаУдаления
	|	И &ВыбиратьЗадачиОтчетностиИУплатыНалогов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Организация,
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ВТ_АктуальныеЗадачи.ХешЗадачи,
	|	ВЫБОР
	|		КОГДА ВТ_АктуальныеЗадачи.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление)
	|			ТОГДА &НаименованиеЗадачиУведомлениеПатента
	|		ИНАЧЕ &НаименованиеЗадачиУплатаПатента
	|	КОНЕЦ,
	|	ВТ_АктуальныеЗадачи.НаименованиеПолное,
	|	5,
	|	Патенты.Ссылка,
	|	ВТ_АктуальныеЗадачи.Выполнено,
	|	ВТ_АктуальныеЗадачи.ВАрхиве,
	|	ЛОЖЬ,
	|	ВТ_АктуальныеЗадачи.Действие,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Патенты КАК Патенты
	|		ПО ВТ_АктуальныеЗадачи.Правило = Патенты.Ссылка
	|ГДЕ
	|	Патенты.Владелец В(&СписокОрганизаций)
	|	И НЕ Патенты.ПометкаУдаления
	|	И &ВыбиратьЗадачиОтчетностиИУплатыНалогов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Организация,
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ВТ_АктуальныеЗадачи.ХешЗадачи,
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	ВТ_АктуальныеЗадачи.НаименованиеПолное,
	|	4,
	|	ПравилаРегулярныхПлатежей.Ссылка,
	|	ВТ_АктуальныеЗадачи.Выполнено,
	|	ВТ_АктуальныеЗадачи.ВАрхиве,
	|	ЛОЖЬ,
	|	ВТ_АктуальныеЗадачи.Действие,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРегулярныхПлатежей КАК ПравилаРегулярныхПлатежей
	|		ПО ВТ_АктуальныеЗадачи.Правило = ПравилаРегулярныхПлатежей.Ссылка
	|ГДЕ
	|	НЕ ПравилаРегулярныхПлатежей.ПометкаУдаления
	|	И &ВыбиратьРегулярныеПлатежи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Организация,
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ВТ_АктуальныеЗадачи.ХешЗадачи,
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	ВТ_АктуальныеЗадачи.НаименованиеПолное,
	|	4,
	|	ПравилаФинОтчетности.Ссылка,
	|	ВТ_АктуальныеЗадачи.Выполнено,
	|	ВТ_АктуальныеЗадачи.ВАрхиве,
	|	ЛОЖЬ,
	|	ВТ_АктуальныеЗадачи.Действие,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаФинОтчетности КАК ПравилаФинОтчетности
	|		ПО ВТ_АктуальныеЗадачи.Правило = ПравилаФинОтчетности.Ссылка
	|ГДЕ
	|	НЕ ПравилаФинОтчетности.ПометкаУдаления
	|	И &ВыбиратьФинОтчетность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Организация,
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ВТ_АктуальныеЗадачи.ХешЗадачи,
	|	ПроверкиКонтролирующимиОрганами.Наименование,
	|	ВТ_АктуальныеЗадачи.НаименованиеПолное,
	|	1,
	|	ПроверкиКонтролирующимиОрганами.Ссылка,
	|	ВТ_АктуальныеЗадачи.Выполнено,
	|	ВТ_АктуальныеЗадачи.ВАрхиве,
	|	ЛОЖЬ,
	|	ВТ_АктуальныеЗадачи.Действие,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПроверкиКонтролирующимиОрганами КАК ПроверкиКонтролирующимиОрганами
	|		ПО ВТ_АктуальныеЗадачи.Правило = ПроверкиКонтролирующимиОрганами.Ссылка
	|ГДЕ
	|	НЕ ПроверкиКонтролирующимиОрганами.ПометкаУдаления
	|	И &ВыбиратьПроверкиКонтролирующихОрганов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Срок,
	|	Наименование,
	|	ДействиеПорядок
	|ИТОГИ
	|	МИНИМУМ(ДействиеПорядок),
	|	МИНИМУМ(Выполнено)
	|ПО
	|	Организация,
	|	Срок,
	|	Наименование";
	
	Результат = Запрос.Выполнить();
	
	ЗадачиОрганизаций = Новый Соответствие();
	
	ПодключенаОтправка = Неопределено; // эта проверка относительно затратная, поэтому выполним ее по месту и закешируем
	ТекстОграниченияОтправкиОтчета = ИнтерфейсыВзаимодействияБРОКлиентСервер.КраткоеПредставлениеОграниченияОтправкиОтчета();
	ТекстОграниченияОтправкиОтчета = СтрШаблон(
		НСтр("ru = 'Внимание! %1'"),
		ОбщегоНазначенияБПКлиентСервер.КапитализироватьСтроку(ТекстОграниченияОтправкиОтчета));
	
	ВыборкаПоОрганизации = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Организация");
	Пока ВыборкаПоОрганизации.Следующий() Цикл
		
		СписокЗадач = КалендарьБухгалтера.НовыйСписокЗадач();
		
		ВыборкаПоСроку = ВыборкаПоОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Срок");
		Пока ВыборкаПоСроку.Следующий() Цикл
			
			ВыборкаПоЗадаче = ВыборкаПоСроку.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Наименование");
			Пока ВыборкаПоЗадаче.Следующий() Цикл
				
				Задача = СписокЗадач.Добавить();
				НаименованиеЗадачи = ВРег(Лев(ВыборкаПоЗадаче.Наименование, 1)) + Сред(ВыборкаПоЗадаче.Наименование, 2);
				Задача.Наименование = НаименованиеЗадачи;
				Задача.ДатаЗадачи   = ВыборкаПоЗадаче.Срок;
				Задача.Выполнено    = ВыборкаПоЗадаче.Выполнено;
				
				Выборка = ВыборкаПоЗадаче.Выбрать(ОбходРезультатаЗапроса.Прямой);
				Пока Выборка.Следующий() Цикл
					
					Если ЭтоИнтерфейсИнтеграцииСБанком И Выборка.ВыполняетсяЕдинымПомощником Тогда
						Продолжить;
					КонецЕсли;
					
					ОписаниеПодзадачи = КалендарьБухгалтера.НовыйОписаниеПодзадачиСобытия();
					ОписаниеПодзадачи.Идентификатор = Выборка.ХешЗадачи;
					ОписаниеПодзадачи.Наименование = Выборка.НаименованиеПолное;
					ОписаниеПодзадачи.ВАрхиве = Выборка.ВАрхиве;
					ОписаниеПодзадачи.Выполнено = Выборка.Выполнено;
					
					Если Выборка.ОтправкаОтчетаОграничена Тогда
						
						Если ПодключенаОтправка = Неопределено Тогда
							ПодключенаОтправка = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом();
						КонецЕсли;
						
						Если ПодключенаОтправка Тогда
							ОписаниеПодзадачи.Наименование =
								ОписаниеПодзадачи.Наименование + Символы.ПС + ТекстОграниченияОтправкиОтчета;
						КонецЕсли;
						
					КонецЕсли;
					
					Задача.Подзадачи.Добавить(ОписаниеПодзадачи);
					
				КонецЦикла;
			КонецЦикла;
			
		КонецЦикла;
		
		ЗадачиОрганизаций.Вставить(ВыборкаПоОрганизации.Организация, СписокЗадач);
		
	КонецЦикла;
	
КонецПроцедуры

// Переопределяет количество просроченных не выполненных задач по организациям.
// Подробнее
// См. КалендарьБухгалтера.КоличествоПросроченныхСобытийКалендаряБухгалтера
//
// Параметры:
//   Организации - Массив организаций, по которым нужно определить количество просроченных задач.
//   ОтборЗадач - Структура - описывает отборы по типам задач, описание полей см. в НовыйНовыйОтборЗадачПоВидам
// Возвращаемое значение:
//   Соответствие - Соответствие, которое содержит в качестве ключа организацию, а в качестве значения - количество просроченных задач.
//
Процедура ПолучитьКоличествоПросроченныхСобытийКалендаряБухгалтера(КоличествоПросроченных, Организации, ОтборЗадач) Экспорт
	
	ДатаПросроченных = НачалоДня(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаПросроченных", ДатаПросроченных);
	Запрос.УстановитьПараметр("СписокОрганизаций", Организации);
	Запрос.УстановитьПараметр("СтатусыВыполненныхЗадач", ВыполнениеЗадачБухгалтера.СтатусыВыполненныхЗадач());
	Запрос.УстановитьПараметр("ВыбиратьЗадачиОтчетностиИУплатыНалогов", ОтборЗадач.ЗадачиОтчетностиИУплатыНалогов);
	Запрос.УстановитьПараметр("ВыбиратьРегулярныеПлатежи",              ОтборЗадач.РегулярныеПлатежи);
	Запрос.УстановитьПараметр("ВыбиратьПроверкиКонтролирующихОрганов",  ОтборЗадач.ПроверкиКонтролирующихОрганов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ПросроченныеЗадачи
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация В(&СписокОрганизаций)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И ЗадачиБухгалтера.Срок < &ДатаПросроченных
	|	И ЗадачиБухгалтера.Статус НЕ В (&СтатусыВыполненныхЗадач)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПросроченныеЗадачи.Организация КАК Организация,
	|	КОЛИЧЕСТВО(ВТ_ПросроченныеЗадачи.Правило) КАК Количество
	|ИЗ
	|	ВТ_ПросроченныеЗадачи КАК ВТ_ПросроченныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Требования
	|		ПО ВТ_ПросроченныеЗадачи.Правило = Требования.Ссылка
	|ГДЕ
	|	НЕ Требования.ПометкаУдаления
	|	И &ВыбиратьЗадачиОтчетностиИУплатыНалогов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПросроченныеЗадачи.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПросроченныеЗадачи.Организация,
	|	КОЛИЧЕСТВО(ВТ_ПросроченныеЗадачи.Правило)
	|ИЗ
	|	ВТ_ПросроченныеЗадачи КАК ВТ_ПросроченныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Патенты КАК Патенты
	|		ПО ВТ_ПросроченныеЗадачи.Правило = Патенты.Ссылка
	|ГДЕ
	|	Патенты.Владелец В(&СписокОрганизаций)
	|	И НЕ Патенты.ПометкаУдаления
	|	И &ВыбиратьЗадачиОтчетностиИУплатыНалогов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПросроченныеЗадачи.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПросроченныеЗадачи.Организация,
	|	КОЛИЧЕСТВО(ВТ_ПросроченныеЗадачи.Правило)
	|ИЗ
	|	ВТ_ПросроченныеЗадачи КАК ВТ_ПросроченныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРегулярныхПлатежей КАК ПравилаРегулярныхПлатежей
	|		ПО ВТ_ПросроченныеЗадачи.Правило = ПравилаРегулярныхПлатежей.Ссылка
	|ГДЕ
	|	НЕ ПравилаРегулярныхПлатежей.ПометкаУдаления
	|	И &ВыбиратьРегулярныеПлатежи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПросроченныеЗадачи.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПросроченныеЗадачи.Организация,
	|	КОЛИЧЕСТВО(ВТ_ПросроченныеЗадачи.Правило)
	|ИЗ
	|	ВТ_ПросроченныеЗадачи КАК ВТ_ПросроченныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПроверкиКонтролирующимиОрганами КАК ПроверкиКонтролирующимиОрганами
	|		ПО ВТ_ПросроченныеЗадачи.Правило = ПроверкиКонтролирующимиОрганами.Ссылка
	|ГДЕ
	|	НЕ ПроверкиКонтролирующимиОрганами.ПометкаУдаления
	|	И &ВыбиратьПроверкиКонтролирующихОрганов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПросроченныеЗадачи.Организация";
	
	ТаблицаПросроченных = Запрос.Выполнить().Выгрузить();
	ТаблицаПросроченных.Свернуть("Организация", "Количество");
	
	Для Каждого СтрокаПросроченного Из ТаблицаПросроченных Цикл
		КоличествоПросроченных.Вставить(СтрокаПросроченного.Организация, СтрокаПросроченного.Количество);
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. КалендарьБухгалтера.ДанныеЗадачиБухгалтера
//
Процедура ПолучитьДанныеЗадачиБухгалтера(ДанныеЗадачиБухгалтера, Организация) Экспорт
	
	ДанныеЗадачиБухгалтера = Новый Структура();
	ДанныеЗадачиБухгалтера.Вставить("КоличествоПросроченных", 0);
	ДанныеЗадачиБухгалтера.Вставить("Просроченнаязадача",     КалендарьБухгалтераБП.НоваяПросроченнаяЗадачаИнформационнойПанели());
	ДанныеЗадачиБухгалтера.Вставить("АктуальныеЗадачи",       КалендарьБухгалтераБП.НоваяТаблицаЗадачИнформационнойПанели());
	
	// Для увеличения производительности запросы выполняются в привилегированном режиме.
	// Выполнение проверки доступности данных по выбранной организации по RLS.
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	Если ЗначениеЗаполнено(Организация) Тогда
		СписокОрганизаций = Новый Массив;
		Если ДоступныеОрганизации.Найти(Организация) <> Неопределено Тогда
			СписокОрганизаций.Добавить(Организация);
		КонецЕсли;
	Иначе
		СписокОрганизаций = Новый Массив(ДоступныеОрганизации);
	КонецЕсли;
	
	Если СписокОрганизаций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВерхняяГраницаСроков = Перечисления.ОтносительныеСроки.ВерхняяГраница();
	
	ТекущаяДата  = НачалоДня(ТекущаяДатаСеанса());
	КонецПериода = КонецДня(ТекущаяДата + (ВерхняяГраницаСроков * 86400));
	НаименованиеЗадачиУплатаПатента = НСтр("ru = 'уплата патента'");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущаяДата",       ТекущаяДата);
	Запрос.УстановитьПараметр("КонецПериода",      КонецПериода);
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Запрос.УстановитьПараметр("НаименованиеЗадачиУплатаПатента", НаименованиеЗадачиУплатаПатента);
	Запрос.УстановитьПараметр("СтатусыВыполненныхЗадач",         ВыполнениеЗадачБухгалтера.СтатусыВыполненныхЗадач());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ЗадачиБухгалтера.НаименованиеСокращенное КАК Наименование
	|ПОМЕСТИТЬ ВТ_АктуальныеЗадачи
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация В(&СписокОрганизаций)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И НЕ ЗадачиБухгалтера.Статус В (&СтатусыВыполненныхЗадач)
	|	И ЗадачиБухгалтера.Срок МЕЖДУ &ТекущаяДата И &КонецПериода
	|	И ВЫБОР
	|			КОГДА ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПравилаПредставленияОтчетовУплатыНалогов
	|				ТОГДА НЕ ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).ВыполняетсяЕдинымПомощником
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок КАК Срок,
	|	Требования.Действие КАК Действие,
	|	Требования.Владелец.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ДействиеПорядок,
	|	Требования.Владелец.Ссылка КАК Задача,
	|	Требования.ОтправкаОтчетаОграничена КАК ОтправкаОтчетаОграничена
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Требования
	|		ПО ВТ_АктуальныеЗадачи.Правило = Требования.Ссылка
	|ГДЕ
	|	НЕ Требования.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога),
	|	&НаименованиеЗадачиУплатаПатента,
	|	5,
	|	Патенты.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Патенты КАК Патенты
	|		ПО ВТ_АктуальныеЗадачи.Правило = Патенты.Ссылка
	|ГДЕ
	|	Патенты.Владелец В(&СписокОрганизаций)
	|	И НЕ Патенты.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	4,
	|	ПравилаРегулярныхПлатежей.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРегулярныхПлатежей КАК ПравилаРегулярныхПлатежей
	|		ПО ВТ_АктуальныеЗадачи.Правило = ПравилаРегулярныхПлатежей.Ссылка
	|ГДЕ
	|	НЕ ПравилаРегулярныхПлатежей.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ФинОтчетность),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	4,
	|	ПравилаФинОтчетности.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаФинОтчетности КАК ПравилаФинОтчетности
	|		ПО ВТ_АктуальныеЗадачи.Правило = ПравилаФинОтчетности.Ссылка
	|ГДЕ
	|	НЕ ПравилаФинОтчетности.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Проверка),
	|	ПроверкиКонтролирующимиОрганами.Наименование,
	|	1,
	|	ПроверкиКонтролирующимиОрганами.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроверкиКонтролирующимиОрганами КАК ПроверкиКонтролирующимиОрганами
	|		ПО ВТ_АктуальныеЗадачи.Правило = ПроверкиКонтролирующимиОрганами.Ссылка
	|ГДЕ
	|	НЕ ПроверкиКонтролирующимиОрганами.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ТребованиеФНС),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	1,
	|	ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов КАК ДокументыРеализацииПолномочийНалоговыхОрганов
	|		ПО ВТ_АктуальныеЗадачи.Правило = ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка
	|ГДЕ
	|	НЕ ДокументыРеализацииПолномочийНалоговыхОрганов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	1,
	|	ВходящийЗапросФССДляРасчетаПособия.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФССДляРасчетаПособия
	|		ПО ВТ_АктуальныеЗадачи.Правило = ВходящийЗапросФССДляРасчетаПособия.Ссылка
	|ГДЕ
	|	НЕ ВходящийЗапросФССДляРасчетаПособия.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	1,
	|	ИзвещениеФСС.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзвещениеФСС КАК ИзвещениеФСС
	|		ПО ВТ_АктуальныеЗадачи.Правило = ИзвещениеФСС.Ссылка
	|ГДЕ
	|	НЕ ИзвещениеФСС.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	1,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов КАК ОтказВВозмещенииВыплатРодителямДетейИнвалидов
	|		ПО ВТ_АктуальныеЗадачи.Правило = ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Ссылка
	|ГДЕ
	|	НЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	1,
	|	УведомлениеОСтатусеВыплатыПособия.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОСтатусеВыплатыПособия КАК УведомлениеОСтатусеВыплатыПособия
	|		ПО ВТ_АктуальныеЗадачи.Правило = УведомлениеОСтатусеВыплатыПособия.Ссылка
	|ГДЕ
	|	НЕ УведомлениеОСтатусеВыплатыПособия.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_АктуальныеЗадачи.Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ТребованиеФНС),
	|	ВТ_АктуальныеЗадачи.Наименование,
	|	1,
	|	ТранспортноеСообщение.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_АктуальныеЗадачи КАК ВТ_АктуальныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	|		ПО ВТ_АктуальныеЗадачи.Правило = ТранспортноеСообщение.Ссылка
	|ГДЕ
	|	НЕ ТранспортноеСообщение.ПометкаУдаления
	|ИТОГИ
	|	МАКСИМУМ(ОтправкаОтчетаОграничена)
	|ПО
	|	Срок,
	|	Задача
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация В(&СписокОрганизаций)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И НЕ ЗадачиБухгалтера.Статус В (&СтатусыВыполненныхЗадач)
	|	И ЗадачиБухгалтера.Срок < &ТекущаяДата
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Перечисление.ЗадачиНачалаРаботы
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПроверкиКонтролирующимиОрганами
	|	И ВЫБОР
	|			КОГДА ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПравилаПредставленияОтчетовУплатыНалогов
	|				ТОГДА НЕ ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).ВыполняетсяЕдинымПомощником
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	// Актуальные задачи
	
	АктуальныеЗадачи = ДанныеЗадачиБухгалтера.АктуальныеЗадачи;
	
	СрокиПоДням = Перечисления.ОтносительныеСроки.СрокиПоДням();
	
	ВыборкаПоСроку = Результат[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Срок");
	Пока ВыборкаПоСроку.Следующий() Цикл
		
		ОсталосьДней = КалендарьБухгалтераБП.ОсталосьДней(ТекущаяДата, ВыборкаПоСроку.Срок);
		
		ОтносительныйСрок = СрокиПоДням.Получить(ОсталосьДней);
		Если ОтносительныйСрок = Неопределено ИЛИ ОтносительныйСрок = Перечисления.ОтносительныеСроки.ПустаяСсылка() Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаПоЗадаче = ВыборкаПоСроку.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Задача");
		Пока ВыборкаПоЗадаче.Следующий() Цикл
			
			ОтправкаОтчетаОграничена = ВыборкаПоЗадаче.ОтправкаОтчетаОграничена; // Истина, если отправка ограничена у любой из задач.
			
			// Одинаковые задачи с разными действиями сворачиваем в одну строку.
			// Большинство свойств выбираем из первой такой задачи.
			Выборка = ВыборкаПоЗадаче.Выбрать();
			Если Выборка.Следующий() Тогда
				НоваяЗадача = АктуальныеЗадачи.Добавить();
				НоваяЗадача.Наименование             = Выборка.Наименование;
				НоваяЗадача.ОтносительныйСрок        = ОтносительныйСрок;
				НоваяЗадача.ОсталосьДней             = ОсталосьДней;
				НоваяЗадача.ДействиеПорядок          = Выборка.ДействиеПорядок;
				НоваяЗадача.ОтносительныйСрокПорядок = Перечисления.ОтносительныеСроки.Порядок(ОтносительныйСрок, ОсталосьДней);
				// Если действие не одно, то указываем наименование задачи
				Если Выборка.Количество() > 1 Тогда
					НоваяЗадача.Действие = Выборка.Наименование;
				Иначе
					НоваяЗадача.Действие = Выборка.Действие;
				КонецЕсли;
				НоваяЗадача.ОтправкаОтчетаОграничена = ОтправкаОтчетаОграничена;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	АктуальныеЗадачи.Индексы.Добавить("ОтносительныйСрок");
	
	ДанныеЗадачиБухгалтера.Вставить("АктуальныеЗадачи", АктуальныеЗадачи);
	
	// Просроченные задачи
	КоличествоПросроченых = Результат[2].Выбрать();
	Если КоличествоПросроченых.Следующий() Тогда
		ДанныеЗадачиБухгалтера.КоличествоПросроченных = КоличествоПросроченых.Количество;
	КонецЕсли;
	
	// Просроченных задач может быть много, поэтому уточняем данные задачи только если она одна
	Если ДанныеЗадачиБухгалтера.КоличествоПросроченных = 1 Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗадачиБухгалтера.Правило КАК Правило,
		|	ЗадачиБухгалтера.Срок,
		|	ЗадачиБухгалтера.НаименованиеСокращенное КАК Наименование
		|ПОМЕСТИТЬ ВТ_ПросроченныеЗадачи
		|ИЗ
		|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|ГДЕ
		|	ЗадачиБухгалтера.Организация В(&СписокОрганизаций)
		|	И НЕ ЗадачиБухгалтера.ВАрхиве
		|	И НЕ ЗадачиБухгалтера.Статус В (&СтатусыВыполненныхЗадач)
		|	И ЗадачиБухгалтера.Срок < &ТекущаяДата
		|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Перечисление.ЗадачиНачалаРаботы
		|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПроверкиКонтролирующимиОрганами
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Правило
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок КАК Срок,
		|	Требования.Владелец.Наименование КАК Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Требования
		|		ПО ВТ_АктуальныеЗадачи.Правило = Требования.Ссылка
		|ГДЕ
		|	НЕ Требования.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок КАК Срок,
		|	ВТ_АктуальныеЗадачи.Наименование КАК Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФССДляРасчетаПособия
		|		ПО ВТ_АктуальныеЗадачи.Правило = ВходящийЗапросФССДляРасчетаПособия.Ссылка
		|ГДЕ
		|	НЕ ВходящийЗапросФССДляРасчетаПособия.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок КАК Срок,
		|	ВТ_АктуальныеЗадачи.Наименование КАК Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФССДляРасчетаПособия
		|		ПО ВТ_АктуальныеЗадачи.Правило = ВходящийЗапросФССДляРасчетаПособия.Ссылка
		|ГДЕ
		|	НЕ ВходящийЗапросФССДляРасчетаПособия.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок КАК Срок,
		|	ВТ_АктуальныеЗадачи.Наименование КАК Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзвещениеФСС КАК ИзвещениеФСС
		|		ПО ВТ_АктуальныеЗадачи.Правило = ИзвещениеФСС.Ссылка
		|ГДЕ
		|	НЕ ИзвещениеФСС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок КАК Срок,
		|	ВТ_АктуальныеЗадачи.Наименование КАК Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОСтатусеВыплатыПособия КАК УведомлениеОСтатусеВыплатыПособия
		|		ПО ВТ_АктуальныеЗадачи.Правило = УведомлениеОСтатусеВыплатыПособия.Ссылка
		|ГДЕ
		|	НЕ УведомлениеОСтатусеВыплатыПособия.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок,
		|	&НаименованиеЗадачиУплатаПатента
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Патенты КАК Патенты
		|		ПО ВТ_АктуальныеЗадачи.Правило = Патенты.Ссылка
		|ГДЕ
		|	Патенты.Владелец В(&СписокОрганизаций)
		|	И НЕ Патенты.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок,
		|	ВТ_АктуальныеЗадачи.Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРегулярныхПлатежей КАК ПравилаРегулярныхПлатежей
		|		ПО ВТ_АктуальныеЗадачи.Правило = ПравилаРегулярныхПлатежей.Ссылка
		|ГДЕ
		|	НЕ ПравилаРегулярныхПлатежей.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок,
		|	ВТ_АктуальныеЗадачи.Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаФинОтчетности КАК ПравилаФинОтчетности
		|		ПО ВТ_АктуальныеЗадачи.Правило = ПравилаФинОтчетности.Ссылка
		|ГДЕ
		|	НЕ ПравилаФинОтчетности.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок,
		|	ВТ_АктуальныеЗадачи.Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов КАК ДокументыРеализацииПолномочийНалоговыхОрганов
		|		ПО ВТ_АктуальныеЗадачи.Правило = ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка
		|ГДЕ
		|	НЕ ДокументыРеализацииПолномочийНалоговыхОрганов.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_АктуальныеЗадачи.Срок,
		|	ВТ_АктуальныеЗадачи.Наименование
		|ИЗ
		|	ВТ_ПросроченныеЗадачи КАК ВТ_АктуальныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
		|		ПО ВТ_АктуальныеЗадачи.Правило = ТранспортноеСообщение.Ссылка
		|ГДЕ
		|	НЕ ТранспортноеСообщение.ПометкаУдаления";
		
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ДанныеЗадачиБухгалтера.ПросроченнаяЗадача, Выборка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. КалендарьБухгалтера.ЗаполнитьВФоне
//
Процедура ПриЗаполненииВФоне(Параметры, ВременноеХранилищеРезультата, Организация, ИзмененыРегулярныеЗадачи) Экспорт
	
	МобильноеПриложениеПредприниматель.ОтправитьУведомленияОЗадачах();
	Если ИзмененыРегулярныеЗадачи Тогда
		СинхронизацияСКалендаремGoogle.СинхронизироватьЗадачи(Организация);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. КалендарьБухгалтера.ЗаполнитьРегламентнымЗаданием
//
Процедура ПриЗаполненииРегламентнымЗаданием() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Справочники.ПроверкиКонтролирующимиОрганами.СоздатьЗадачиБухгалтераПоНовымПроверкам();
	КонецЕсли;
	
	МобильноеПриложениеПредприниматель.ОтправитьУведомленияОЗадачах();
	
КонецПроцедуры

Процедура ПолучитьОрганизацииДанныеКоторыхДоступныПользователю(ДоступныеОрганизации, ПравоНаИзменение = Ложь, Пользователь = Неопределено) Экспорт
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
КонецПроцедуры

// Подробнее
// См. КалендарьБухгалтераПовтИсп.ДатаНачалаДеятельности
//
Процедура ПолучитьДатуНачалаДеятельности(Дата, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.ДатаРегистрации КАК Дата
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	
	НачалоИнтервала = '0001-01-01';
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НачалоИнтервала = Макс(НачалоИнтервала, Выборка.Дата);
	КонецЦикла;
	
	Дата = НачалоИнтервала;
	
КонецПроцедуры

// Подробнее
// См. КалендарьБухгалтераПовтИсп.ДатаЗакрытияОрганизации
//
Процедура ПолучитьДатуЗакрытияОрганизации(Дата, Организация) Экспорт
	Дата = ОбщегоНазначенияБПВызовСервераПовтИсп.ДатаЗакрытияОрганизации(Организация);
КонецПроцедуры

// Подробнее
// См. КалендарьБухгалтераПовтИсп.ДатаПостановкиНаУчетНалогоплательщика
//
Процедура ПолучитьДатуПостановкиНаУчетНалогоплательщика(Дата, Организация, КонецФинансовогоПериода) Экспорт
	Дата = УчетнаяПолитика.ДатаПостановкиНаУчетНалогоплательщика(Организация, КонецФинансовогоПериода);
КонецПроцедуры

#КонецОбласти
