///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "СПАРК".
// ОбщийМодуль.СПАРКФакторыРискаКлиентСервер.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Параметры получения факторов
// 
// Возвращаемое значение:
//  Структура - параметры получения факторов:
//  * ИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор экземпляра формы.
//
Функция ПараметрыПолученияФакторов() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторФормы", Неопределено);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает представление данных об арбитражных делах.
//
// Параметры:
//  ДанныеОбАрбитражныхДелах - Структура - Данные о государственных контрактах.
//                                     См. НовыйСводныеДанныеОГосударственныхКонтрактах;
//  ЕстьДанные - Булево - Возвращаемое. Признак наличия данных
// 
// Возвращаемое значение:
//  Строка - представление данных.
//
Функция ДанныеОбАрбитражныхДелахПредставление(
		ДанныеОбАрбитражныхДелах,
		ЕстьДанные = Ложь) Экспорт
	
	РезультатДанныхНет = НСтр("ru='данных нет'");
	Если ДанныеОбАрбитражныхДелах = Неопределено Тогда
		Возврат РезультатДанныхНет;
	КонецЕсли;
	
	Результат = Новый Массив;
	Если ДанныеОбАрбитражныхДелах.КоличествоРассматриваемыхДел > 0 Тогда
		Результат.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 рассматривается'"),
				ДанныеОбАрбитражныхДелах.КоличествоРассматриваемыхДел));
	КонецЕсли;
	Если ДанныеОбАрбитражныхДелах.КоличествоРешенийИПостановлений > 0 Тогда
		Результат.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 решений и постановлений'"),
				ДанныеОбАрбитражныхДелах.КоличествоРешенийИПостановлений));
	КонецЕсли;
	Если ДанныеОбАрбитражныхДелах.КоличествоЗавершенныхДел > 0 Тогда
		Результат.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 завершено'"),
				ДанныеОбАрбитражныхДелах.КоличествоЗавершенныхДел));
	КонецЕсли;
	
	Если Результат.Количество() > 0 Тогда
		Результат = СтрСоединить(Результат, " - ");
		ЕстьДанные = Истина;
	Иначе
		Результат = РезультатДанныхНет;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает представление данных о государственных контрактах.
//
// Параметры:
//  ДанныеГосударственныеКонтракты - Структура - Данные о государственных контрактах.
//                                   См. НовыйСводныеДанныеОГосударственныхКонтрактах;
//  ЕстьДанные - Булево - Возвращаемое. Признак наличия данных
// 
// Возвращаемое значение:
//  Строка - представление данных.
//
Функция ДанныеГосударственныеКонтрактыПредставление(
		ДанныеГосударственныеКонтракты,
		ЕстьДанные = Ложь) Экспорт
	
	РезультатДанныхНет = НСтр("ru='данных нет'");
	Если ДанныеГосударственныеКонтракты.ОбщееКоличествоТендеров = 0 Тогда
		Возврат РезультатДанныхНет;
	КонецЕсли;
	
	ЕстьДанные = Истина;
	ПредставлениеТендеров = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru=';%1 торговой процедуре;%1 торговых процедурах;%1 торговых процедурах;%1 торговых процедурах;%1 торговых процедурах'"),
		ДанныеГосударственныеКонтракты.ОбщееКоличествоТендеров);
	
	Результат = Новый Массив;
	Результат.Добавить(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Принято участие в %1.'"),
			ПредставлениеТендеров));
	
	Если ДанныеГосударственныеКонтракты.ОбщаяСумма > 0
		И ДанныеГосударственныеКонтракты.ОбщееКоличествоКонтрактов > 0 Тогда
		
		ПредставлениеКонтрактов = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';%1 контракт;%1 контракта;%1 контракта;%1 контрактов;%1 контрактов'"),
			ДанныеГосударственныеКонтракты.ОбщееКоличествоТендеров);
		
		Результат.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Заключено %1 на·%2 ₽.'"),
				ПредставлениеКонтрактов,
				ДанныеГосударственныеКонтракты.ОбщаяСумма));
	КонецЕсли;
	
	Результат = СтрСоединить(Результат, Символы.ПС);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Результат загрузки факторов риска контрагента.
// 
// Возвращаемое значение:
//  Структура - набор данных о контрагенте:
//  * ИНН                                 - Строка - ИНН контрагента;
//  * ПодлежитПроверке                    - Булево - флаг корректности к проверки контрагента;
//  * ВидКонтрагента                      - ПеречислениеСсылка.ВидыКонтрагентовСПАРКРиски - вид контрагента в СПАРК;
//  * ИспользованиеРазрешено              - Булево - признак настройки СПАРК;
//  * ДатаАктуальности                    - Дата - дата и время получения данных из СПАРК;
//  * Контрагент                          - ОпределяемыеТипы.КонтрагентБИП - контрагент;
//  * ВидОшибки                           - ПеречислениеСсылка.ВидыОшибокСПАРКРиски - вид ошибки при работе с сервисом;
//  * ТекстОшибки                         - Строка - строковое представление ошибки;
//  * ОжиданиеОтвета                      - Булево - признак готовности данных в сервисе;
//  * ДлительнаяОперация                  - Структура, Неопределено - см. ДлительныеОперации.ВыполнитьФункцию;
//  * РаботаВМоделиСервиса                - Булево - признак работы в режиме разделения данных по областям;
//  * ДанныеАутентификацииЗаполнены       - Булево - признак заполненности данных аутентификации;
//  * ДоступностьПодключенияТестовогоПериода - Строка - признак возможности подключения тестового периода;
//  * ОшибкаПриПроверкеТестовогоПериода      - Булево - признак ошибки при проверке тестового периода;
//  * Уведомление                         - Неопределено - уведомление портала 1С:ИТС;
//  * СводныеДанныеОбАрбитражныхДелах     - Структура - данные об арбитражных делах;
//  * ФакторыРискаФизическогоЛица         - Неопределено - факторы риска физического лица;
//  * ФакторыРискаФизическогоЛицаПо115ФЗ  - Неопределено - факторы риска физического лица по 115 ФЗ;
//  * ФакторыРискаЮридическогоЛица        - Неопределено - факторы риска юридического лица;
//  * ФакторыРискаЮридическогоЛицаПо115ФЗ - Неопределено - факторы риска юридического лица по 115 ФЗ;
//  * ДанныеГосударственныеКонтракты      - Структура - данные о государственных контрактах;
//  * НегативныеСписки                    - Неопределено - информация о вхождении в негативные списки.
//
Функция НовыйРезультатФакторыРиска() Экспорт
	
	Результат = Новый Структура;
	
	// Основные данные.
	Результат.Вставить("ИНН", "");
	Результат.Вставить("ПодлежитПроверке", Ложь);
	Результат.Вставить("ВидКонтрагента", Неопределено);
	Результат.Вставить("ИспользованиеРазрешено", Истина);
	Результат.Вставить("ДатаАктуальности", Неопределено);
	Результат.Вставить("Контрагент", Неопределено);
	
	// Прочее.
	Результат.Вставить(
		"ВидОшибки",
		ПредопределенноеЗначение("Перечисление.ВидыОшибокСПАРКРиски.ПустаяСсылка"));
	Результат.Вставить("ТекстОшибки", "");
	
	Результат.Вставить("ОжиданиеОтвета",     Ложь);
	Результат.Вставить("ДлительнаяОперация", Неопределено);
	
	Результат.Вставить("РаботаВМоделиСервиса",                   Истина);
	Результат.Вставить("ДанныеАутентификацииЗаполнены",          Ложь);
	Результат.Вставить("ДоступностьПодключенияТестовогоПериода", "");
	Результат.Вставить("ОшибкаПриПроверкеТестовогоПериода",      Ложь);
	
	// Уведомление Монитора Портала 1С:ИТС.
	Результат.Вставить("Уведомление", Неопределено);
	
	Результат.Вставить("СводныеДанныеОбАрбитражныхДелах",     НовыйСводныеДанныеОбАрбитражныхДелах());
	Результат.Вставить("ФакторыРискаФизическогоЛица",         Неопределено);
	Результат.Вставить("ФакторыРискаФизическогоЛицаПо115ФЗ",  Неопределено);
	Результат.Вставить("ФакторыРискаЮридическогоЛица",        Неопределено);
	Результат.Вставить("ФакторыРискаЮридическогоЛицаПо115ФЗ", Неопределено);
	Результат.Вставить("ДанныеГосударственныеКонтракты",      НовыйСводныеДанныеОГосударственныхКонтрактах());
	Результат.Вставить("НегативныеСписки",                    Неопределено);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйСводныеДанныеОбАрбитражныхДелах()
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеОДелахВКачествеИстца",      НовыйДанныеОДелах());
	Результат.Вставить("ДанныеОДелахВКачествеОтветчика",  НовыйДанныеОДелах());
	Результат.Вставить("СводныеДанныеПоТипам",            Новый Массив);
	Результат.Вставить("СводныеДанныеПоГодам",            Новый Массив);
	Результат.Вставить("ОбщееКоличествоДел",              0);
	Результат.Вставить("КоличествоРассматриваемыхДел",    0);
	Результат.Вставить("КоличествоОбжалуемыхДел",         0);
	Результат.Вставить("КоличествоРешенийИПостановлений", 0);
	Результат.Вставить("КоличествоЗавершенныхДел",        0);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйДанныеОДелах() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеОЗавершенныхДелах",        Новый Массив);
	Результат.Вставить("ДанныеОРешенияхИПостановлениях", Новый Массив);
	
	Результат.Вставить("ОбщееКоличествоДел",              0);
	Результат.Вставить("СуммаПоВсемДелам",                0);
	Результат.Вставить("КоличествоРассматриваемыхДел",    0);
	Результат.Вставить("СуммаПоРассматриваемымДелам",     0);
	Результат.Вставить("КоличествоОбжалуемыхДел",         0);
	Результат.Вставить("СуммаПоОбжалуемымДелам",          0);
	Результат.Вставить("КоличествоРешенийИПостановлений", 0);
	Результат.Вставить("СуммаПоРешениямИПостановлениям",  0);
	Результат.Вставить("КоличествоЗавершенныхДел",        0);
	Результат.Вставить("СуммаПоЗавершеннымДелам",         0);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйСводныеДанныеОГосударственныхКонтрактах()
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеОГосКонтрактахПо94ФЗИ44ФЗ", Неопределено);
	Результат.Вставить("ДанныеОГосКонтрактахПо223ФЗ",     Неопределено);
	
	Результат.Вставить("ОбщееКоличествоКонтрактов", 0);
	Результат.Вставить("ОбщаяСумма",                0);
	Результат.Вставить("ОбщееКоличествоТендеров",   0);
	
	Возврат Результат;
	
КонецФункции

#Область ОтображениеФакторовРиска

// Выводит информацию о факторах рисках СПАРК в элемент управления.
// Эта служебная процедура непосредственно заполняет элементы управления.
//
// Параметры:
//  ФакторыРиска         - Структура - См. НовыйРезультатФакторыРиска();
//  КонтрагентОбъект     - ОпределяемыйТип.КонтрагентБИПОбъект, Неопределено - заполняется в том случае, если форма
//                         это форма элемента справочника, а не форма документа.
//  Контрагент           - ОпределяемыйТип.КонтрагентБИП, Строка - Контрагент или ИНН контрагента;
//  Форма                - ФормаКлиентскогоПриложения - форма, в которой необходимо вывести информацию о факторах риска.
//  ИспользованиеРазрешено - Булево - разрешено использование;
//  ПараметрыОтображения   - Структура - прочие параметры. См. СПАРКРискиКлиентСервер.ПараметрыОтображенияДанныхСПАРК.
//
Процедура ОтобразитьФакторыРиска(
		ФакторыРиска,
		КонтрагентОбъект,
		Контрагент,
		Форма,
		ИспользованиеРазрешено,
		ПараметрыОтображения = Неопределено) Экспорт
	
	СтандартнаяОбработка = Истина;
	СПАРКФакторыРискаКлиентСерверПереопределяемый.ОтобразитьФакторыРиска(
		ФакторыРиска,
		КонтрагентОбъект,
		Контрагент,
		Форма,
		ИспользованиеРазрешено,
		ПараметрыОтображения,
		СтандартнаяОбработка);

	Если СтандартнаяОбработка <> Истина Тогда
		Возврат;
	КонецЕсли;

	Если Не ИспользованиеРазрешено Тогда
		Форма.Элементы.ГруппаФакторыРиска.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ВариантОтображения = Неопределено;
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		ВариантОтображения = ПараметрыОтображения.ВариантОтображенияФакторовРиска;
	КонецЕсли;
	
	Если ВариантОтображения = "ВКарточке" Тогда
		ВывестиПредставлениеФакторовРискаВКарточке(
			Форма,
			ФакторыРиска);
	Иначе
		
		Если ФакторыРиска.ПодлежитПроверке <> Истина Тогда
			Форма.Элементы.ДекорацияФакторыРиска.Видимость                 = Ложь;
			Форма.Элементы.КартинкаОжиданиеЗагрузкиФакторовРиска.Видимость = Ложь;
			Возврат;
		Иначе
			Форма.Элементы.ДекорацияФакторыРиска.Видимость                 = Истина;
			Форма.Элементы.КартинкаОжиданиеЗагрузкиФакторовРиска.Видимость = Истина;
		КонецЕсли;

		Если ФакторыРиска.ОжиданиеОтвета Тогда
			
			Форма.Элементы.КартинкаОжиданиеЗагрузкиФакторовРиска.Видимость = Истина;
			Форма.Элементы.КартинкаОжиданиеЗагрузкиФакторовРиска.Картинка = БиблиотекаКартинок.ДлительнаяОперация16;
			Форма.Элементы.КартинкаОжиданиеЗагрузкиФакторовРиска.Подсказка = НСтр("ru='Выполняется получение данных из Интернет.'");
			
		Иначе
			Форма.Элементы.КартинкаОжиданиеЗагрузкиФакторовРиска.Видимость = Ложь;
		КонецЕсли;

		ПредставлениеФакторов = Новый Массив;
		
		Если ВариантОтображения = "Многострочный" Тогда
			
			ВывестиМногострочноеПредставлениеФакторовРиска(
				Форма,
				ФакторыРиска,
				ПредставлениеФакторов);
				
		КонецЕсли;

		Результат = Новый ФорматированнаяСтрока(ПредставлениеФакторов);
		Форма.Элементы.ДекорацияФакторыРиска.Заголовок = Результат;

	КонецЕсли;
	
	
КонецПроцедуры

#Область ВыводФакторовРискаМногострочно

Процедура ВывестиМногострочноеПредставлениеФакторовРиска(Форма, ФакторыРиска, ПредставлениеФакторов)
	
	ЦветаСтилей = СПАРКРискиВызовСервера.ЦветаСтилей();
	
	ЕстьОшибкаПодключения = Ложь;
	
	ПредставлениеФакторов.Добавить(
		Новый ФорматированнаяСтрока(
			НСтр("ru='Факторы риска'"),
			Новый Шрифт(Форма.Элементы.ДекорацияИндексыСПАРКРиски.Шрифт, , , Истина))); // Жирный шрифт
	ПредставлениеФакторов.Добавить(Символы.ПС);
	
	Если ФакторыРиска.ВидОшибки = ПредопределенноеЗначение("Перечисление.ВидыОшибокСПАРКРиски.НеизвестныйИНН") Тогда
		ПредставлениеФакторов.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Нет информации о контрагенте'"),
				,
				,
				,
				"SPARK:NoInformation"));
	ИначеЕсли ФакторыРиска.ВидОшибки = ПредопределенноеЗначение("Перечисление.ВидыОшибокСПАРКРиски.НекорректныйИНН") Тогда
		ПредставлениеФакторов.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Нет информации о контрагенте'"),
				,
				ЦветаСтилей.ЦветОсобогоТекста,
				,
				"SPARK:NoInformation"));
	ИначеЕсли ФакторыРиска.ВидОшибки = ПредопределенноеЗначение("Перечисление.ВидыОшибокСПАРКРиски.НеизвестнаяОшибка") Тогда
		ПредставлениеФакторов.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru='При загрузке данных получена ошибка.
					|Подробную информацию см. в Журнале регистрации.'")));
	Иначе
		
		ЕстьОшибкаПодключения = СПАРКРискиКлиентСервер.ЭтоВидОшибкиПодключения(ФакторыРиска.ВидОшибки);
		ЕстьОшибкаПолученияДанных = Ложь;

		ТекстОшибки = "";
		ЦветТекстаОшибки = ЦветаСтилей.ЦветОсобогоТекста;
		Если ФакторыРиска.ОжиданиеОтвета Тогда
			ТекстОшибки = НСтр("ru='Получение данных...'");
			ЦветТекстаОшибки = Неопределено; // Авто
			ЕстьОшибкаПолученияДанных = Истина;
		КонецЕсли;
		
		Если ЕстьОшибкаПолученияДанных = Истина Тогда
			ПредставлениеФакторов.Добавить(Новый ФорматированнаяСтрока(ТекстОшибки, , ЦветТекстаОшибки));
			ПредставлениеФакторов.Добавить(Символы.ПС);
		Иначе
			
			ВывестиНегативныеСписки(ФакторыРиска, ПредставлениеФакторов, ЦветаСтилей);
			ВывестиОбщиеФакторыРиска(ФакторыРиска, ПредставлениеФакторов, ЦветаСтилей);
			ВывестиФакторыРиска115ФЗ(ФакторыРиска, ПредставлениеФакторов, ЦветаСтилей);
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьОшибкаПодключения Тогда
		ПредставлениеФакторов = Новый Массив;
		Если ФакторыРиска.ВидОшибки = ПредопределенноеЗначение("Перечисление.ВидыОшибокСПАРКРиски.ТребуетсяОплатаИлиПревышенЛимит")
			И ФакторыРиска.ДоступностьПодключенияТестовогоПериода = "Доступно" Тогда
			
			ПредставлениеФакторов.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru='Подключить'"),
					,
					,
					,
					"SPARK:ConnectTrialTariff"));
			
			ПредставлениеФакторов.Добавить(" ");
			ПредставлениеФакторов.Добавить(НСтр("ru = 'тестовый период'"));
			ПредставлениеФакторов.Добавить(Символы.ПС);
			ПредставлениеФакторов.Добавить(НСтр("ru = 'или'"));
			ПредставлениеФакторов.Добавить(" ");
			
			ПредставлениеФакторов.Добавить(
				Новый ФорматированнаяСтрока(
				НСтр("ru='купить сервис'"),
				,
				,
				,
				"SPARK:BuyService"));
			
			ПредставлениеФакторов.Добавить(" ");
			ПредставлениеФакторов.Добавить(НСтр("ru = 'проверки'"));
			ПредставлениеФакторов.Добавить(Символы.ПС);
			ПредставлениеФакторов.Добавить(НСтр("ru = 'и мониторинга контрагентов'"));
			
		ИначеЕсли ФакторыРиска.ВидОшибки = ПредопределенноеЗначение("Перечисление.ВидыОшибокСПАРКРиски.ТребуетсяОплатаИлиПревышенЛимит")
			И ФакторыРиска.ДоступностьПодключенияТестовогоПериода = "Подключение" Тогда
			ПредставлениеФакторов.Добавить(
				НСтр("ru = 'Выполняется подключение сервиса.'"));
		ИначеЕсли ФакторыРиска.ВидОшибки = ПредопределенноеЗначение("Перечисление.ВидыОшибокСПАРКРиски.ЗапрещенДоступКИнтернетСервисам") Тогда
			ПредставлениеФакторов.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru='1СПАРК Риски'"),
					Новый Шрифт(Форма.Элементы.ДекорацияИндексыСПАРКРиски.Шрифт, , , Истина))); // Жирный шрифт
			ПредставлениеФакторов.Добавить(Символы.ПС);
			ПредставлениеФакторов.Добавить(
				НСтр("ru = 'Доступ к Интернет-сервисам в приложении запрещен администратором.'"));
		Иначе
			
			Если Не ФакторыРиска.ДанныеАутентификацииЗаполнены
				Или ФакторыРиска.РаботаВМоделиСервиса Тогда
				
				ПредставлениеФакторов.Добавить(
					Новый ФорматированнаяСтрока(
						НСтр("ru='Подробнее о сервисе'"),
						,
						,
						,
						"SPARK:About"));
				
				ПредставлениеФакторов.Добавить(" ");
				ПредставлениеФакторов.Добавить(НСтр("ru = 'проверки'"));
				ПредставлениеФакторов.Добавить(Символы.ПС);
				ПредставлениеФакторов.Добавить(НСтр("ru = 'и мониторинга контрагентов'"));
				
			Иначе
				
				ПредставлениеФакторов.Добавить(
					Новый ФорматированнаяСтрока(
						НСтр("ru='Купить сервис'"),
						,
						,
						,
						"SPARK:BuyService"));
				
				ПредставлениеФакторов.Добавить(" ");
				ПредставлениеФакторов.Добавить(НСтр("ru = 'проверки и'"));
				ПредставлениеФакторов.Добавить(Символы.ПС);
				ПредставлениеФакторов.Добавить(НСтр("ru = 'мониторинга контрагентов'"));
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиНегативныеСписки(ФакторыРиска, ПредставлениеФакторов, ЦветаСтилей)
	
	НегативныеСписки = ФакторыРиска.НегативныеСписки;
	
	Если НегативныеСписки = Неопределено
		Или НегативныеСписки.Количество() = 0 Тогда
		
		ОписаниеРисков = Новый ФорматированнаяСтрока(
			НСтр("ru='нет'"),
			, // Шрифт
			ЦветаСтилей.ЦветГрадацияСПАРКНизкийРиск, // ЦветТекста
			, // ЦветФона
			"SPARK:ShowExtendedInformation");
		
	Иначе
		
		ОписаниеРисков = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = '; %1 список; ; %1 списка; %1 списков; %1 списков'"),
			НегативныеСписки.Количество());
		Если НегативныеСписки.Количество() < 3 Тогда
			ЦветФактора = ЦветаСтилей.ЦветГрадацияСПАРКСреднийРиск;
		Иначе
			ЦветФактора = ЦветаСтилей.ЦветОсобогоТекста;
		КонецЕсли;
		
		ОписаниеРисков = Новый ФорматированнаяСтрока(
			ОписаниеРисков,
			,
			ЦветФактора,
			,
			"SPARK:ShowExtendedInformation");
		
	КонецЕсли;
	
	ПредставлениеФакторов.Добавить(
		Новый ФорматированнаяСтрока(
				НСтр("ru='Негативные списки:'")));
	ПредставлениеФакторов.Добавить(Символы.НПП);
	ПредставлениеФакторов.Добавить(ОписаниеРисков);
	ПредставлениеФакторов.Добавить(Символы.ПС);
	
КонецПроцедуры

Процедура ВывестиОбщиеФакторыРиска(ФакторыРиска, ПредставлениеФакторов, ЦветаСтилей)
	
	Если ФакторыРиска.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо") Тогда
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаЮридическогоЛица;
	Иначе
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаФизическогоЛица;
	КонецЕсли;
	
	Если ДанныеФакторыРиска = Неопределено
		Или ДанныеФакторыРиска.Количество() = 0 Тогда
		
		ОписаниеРисков = Новый ФорматированнаяСтрока(
			НСтр("ru='нет'"),
			, // Шрифт
			ЦветаСтилей.ЦветГрадацияСПАРКНизкийРиск, // ЦветТекста
			, // ЦветФона
			"SPARK:ShowExtendedInformation");
	Иначе
		
		ОписаниеРисков = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = '; %1 риск; ; %1 риска; %1 рисков; %1 рисков'"),
			ДанныеФакторыРиска.Количество());
		Если ДанныеФакторыРиска.Количество() < 5 Тогда
			ЦветФактора = ЦветаСтилей.ЦветГрадацияСПАРКСреднийРиск;
		Иначе
			ЦветФактора = ЦветаСтилей.ЦветОсобогоТекста;
		КонецЕсли;
		
		ОписаниеРисков = Новый ФорматированнаяСтрока(
			ОписаниеРисков,
			,
			ЦветФактора,
			,
			"SPARK:ShowExtendedInformation");
		
	КонецЕсли;
	
	ПредставлениеФакторов.Добавить(
		Новый ФорматированнаяСтрока(
				НСтр("ru='Факторы риска:'")));
	ПредставлениеФакторов.Добавить(Символы.НПП);
	ПредставлениеФакторов.Добавить(ОписаниеРисков);
	ПредставлениеФакторов.Добавить(Символы.ПС);
	
КонецПроцедуры

Процедура ВывестиФакторыРиска115ФЗ(ФакторыРиска, ПредставлениеФакторов, ЦветаСтилей)
	
	Если ФакторыРиска.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо") Тогда
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаЮридическогоЛицаПо115ФЗ;
	Иначе
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаФизическогоЛицаПо115ФЗ;
	КонецЕсли;
	
	Если ДанныеФакторыРиска = Неопределено
		Или ДанныеФакторыРиска.Количество() = 0 Тогда
		
		ОписаниеРисков = Новый ФорматированнаяСтрока(
			НСтр("ru='нет'"),
			, // Шрифт
			ЦветаСтилей.ЦветГрадацияСПАРКНизкийРиск, // ЦветТекста
			, // ЦветФона
			"SPARK:ShowExtendedInformation");
		
	Иначе
		
		ОписаниеРисков = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = '; %1 риск; ; %1 риска; %1 рисков; %1 рисков'"),
			ДанныеФакторыРиска.Количество());
		Если ДанныеФакторыРиска.Количество() < 5 Тогда
			ЦветФактора = ЦветаСтилей.ЦветГрадацияСПАРКСреднийРиск;
		Иначе
			ЦветФактора = ЦветаСтилей.ЦветОсобогоТекста;
		КонецЕсли;
		
		ОписаниеРисков = Новый ФорматированнаяСтрока(
			ОписаниеРисков,
			,
			ЦветФактора,
			,
			"SPARK:ShowExtendedInformation");
		
	КонецЕсли;
	
	ПредставлениеФакторов.Добавить(
		Новый ФорматированнаяСтрока(
				НСтр("ru='Факторы риска по 115-ФЗ:'")));
	ПредставлениеФакторов.Добавить(Символы.НПП);
	ПредставлениеФакторов.Добавить(ОписаниеРисков);
	ПредставлениеФакторов.Добавить(Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

#Область ВыводФакторовРискаВКарточке

Процедура ВывестиПредставлениеФакторовРискаВКарточке(
		Форма,
		ФакторыРиска)
		
	Элементы = Форма.Элементы;
	Элементы.ГруппаКартинкаОжиданиеФакторыРиска.Видимость = Истина;
	Элементы.ГруппаСПАРКФакторыРиска.Видимость = Ложь;
	Элементы.ГруппаСПАРКФакторыРиска115ФЗ.Видимость = Ложь;
	Элементы.ГруппаСПАРКНегативныеСписки.Видимость = Ложь;
	Элементы.ГруппаСПАРКПризнакиХозяйственнойДеятельности.Видимость = Ложь;

	ВывестиКарточкуФакторыРиска(Форма, ФакторыРиска);
	ВывестиКарточкуФакторыРиска115ФЗ(Форма, ФакторыРиска);
	ВывестиКарточкуНегативныеСписки(Форма, ФакторыРиска);
	ВывестиКарточкуПризнакиХозяйственнойДеятельности(Форма, ФакторыРиска);
	
	Элементы.ГруппаКартинкаОжиданиеФакторыРиска.Видимость = Ложь;
	Элементы.ГруппаСПАРКФакторыРиска.Видимость = Истина;
	Элементы.ГруппаСПАРКФакторыРиска115ФЗ.Видимость = Истина;
	Элементы.ГруппаСПАРКНегативныеСписки.Видимость = Истина;
	Элементы.ГруппаСПАРКПризнакиХозяйственнойДеятельности.Видимость = Истина;
	
КонецПроцедуры

Процедура ВывестиКарточкуФакторыРиска(Форма, ФакторыРиска)
	
	Если Форма.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо") Тогда
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаЮридическогоЛица;
	Иначе
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаФизическогоЛица;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	Если ДанныеФакторыРиска = Неопределено Тогда
		Элементы.ГруппаСПАРКФакторыРиска.Видимость = Истина;
		Возврат;
	КонецЕсли;
	
	ФакторыВыведены = Ложь;
	
	ВывестиФакторыРиска(
		Форма,
		ДанныеФакторыРиска,
		"ФакторРиска",
		ФакторыВыведены);
	
	Элементы.ГруппаДанныеФакторРиска.Видимость = ФакторыВыведены;
	Элементы.ДекорацияФакторыРискаОписание.Видимость = Не ФакторыВыведены;
	
	Элементы.ГруппаСПАРКФакторыРиска.Видимость = Истина;
	
КонецПроцедуры

Процедура ВывестиКарточкуФакторыРиска115ФЗ(Форма, ФакторыРиска)
	
	Если Форма.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо") Тогда
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаЮридическогоЛицаПо115ФЗ;
	Иначе
		ДанныеФакторыРиска = ФакторыРиска.ФакторыРискаФизическогоЛицаПо115ФЗ;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	Если ДанныеФакторыРиска = Неопределено Тогда
		Элементы.ГруппаСПАРКФакторыРиска115ФЗ.Видимость = Истина;
		Возврат;
	КонецЕсли;
	
	ФакторыВыведены = Ложь;
	
	ВывестиФакторыРиска(
		Форма,
		ДанныеФакторыРиска,
		"ФакторРиска115ФЗ",
		ФакторыВыведены);
	
	Элементы.ГруппаДанныеФакторРиска115ФЗ.Видимость = ФакторыВыведены;
	Элементы.ДекорацияФакторыРиска115ФЗОписание.Видимость = Не ФакторыВыведены;
	
	Элементы.ГруппаСПАРКФакторыРиска115ФЗ.Видимость = Истина;
	
КонецПроцедуры

Процедура ВывестиКарточкуНегативныеСписки(Форма, ФакторыРиска)
	
	Элементы = Форма.Элементы;
	Если ФакторыРиска.НегативныеСписки = Неопределено
		Или ФакторыРиска.НегативныеСписки.Количество() = 0 Тогда
		
		Элементы.ДекорацияНегативныеСпискиОписание.Заголовок = НСтр("ru='Нет информации о контрагенте'");
		Элементы.ДекорацияНегативныеСпискиОписание.Видимость = Истина;
		
	Иначе
		Элементы.ДекорацияНегативныеСпискиОписание.Видимость = Ложь;
		РезультатНегативныеСписки = Новый Соответствие();
		Для Каждого НегативныеСписок Из ФакторыРиска.НегативныеСписки Цикл
			РезультатНегативныеСписки.Вставить(НегативныеСписок.Название, Истина);
		КонецЦикла;
		
		ВывестиФакторыРиска(
			Форма,
			РезультатНегативныеСписки,
			"НегативныеСписки");
		
	КонецЕсли;
	
	Элементы.ГруппаДанныеНегативныеСписки.Видимость = Истина;
	
КонецПроцедуры

Процедура ВывестиФакторыРиска(Форма, ФакторыРиска, ИмяФактора, ИнформацияВыведена = Ложь)
	
	Индекс = 1;
	Если ФакторыРиска <> Неопределено Тогда
		Для Каждого ФакторРиска Из ФакторыРиска Цикл
			ИнформацияВыведена = Истина;
			ВывестиФакторРиска(
				Форма.Элементы,
				ФакторРиска,
				ИмяФактора,
				Индекс);
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиФакторРиска(
		Элементы,
		ФакторРиска,
		ИмяФактора,
		Индекс)
		
	ГруппаФакторРиска = ЭлементПоИмени(
		Элементы,
		("Группа" + ИмяФактора + Индекс),
		Тип("ГруппаФормы"),
		Элементы["ГруппаДанные" + ИмяФактора]);
	ГруппаФакторРиска.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФакторРиска.ОтображатьЗаголовок = Ложь;
	ГруппаФакторРиска.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаФакторРиска.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	Если ИмяФактора = "НегативныеСписки" Тогда
		
		ЭлементФормы = ЭлементПоИмени(
			Элементы,
			"Декорация" + ИмяФактора + "Картинка" + Индекс,
			Тип("ДекорацияФормы"),
			ГруппаФакторРиска);
			ЭлементФормы.Вид = ВидДекорацииФормы.Картинка;
			ЭлементФормы.РазмерКартинки = РазмерКартинки.ПоРазмеруШрифта;
			ЭлементФормы.Картинка = БиблиотекаКартинок.ФакторРискаСПАРК;
		
		ЭлементФормы = ЭлементПоИмени(
			Элементы,
			"Декорация" + ИмяФактора + "Значение" + Индекс,
			Тип("ДекорацияФормы"),
			ГруппаФакторРиска);
		ЭлементФормы.Вид = ВидДекорацииФормы.Надпись;
		ЭлементФормы.Заголовок = ФакторРиска.Ключ;
		
	Иначе
		
		ЭлементФормы = ЭлементПоИмени(
			Элементы,
			"Декорация" + ИмяФактора + "Картинка" + Индекс,
			Тип("ДекорацияФормы"),
			ГруппаФакторРиска);
		ЭлементФормы.Вид = ВидДекорацииФормы.Картинка;
		ЭлементФормы.РазмерКартинки = РазмерКартинки.ПоРазмеруШрифта;
		ЭлементФормы.Картинка = БиблиотекаКартинок.ФакторРискаСПАРК;
		
		ЭлементФормы = ЭлементПоИмени(
			Элементы,
			"Декорация" + ИмяФактора + "Значение" + Индекс,
			Тип("ДекорацияФормы"),
			ГруппаФакторРиска);
		ЭлементФормы.Вид = ВидДекорацииФормы.Надпись;
		ЭлементФормы.Заголовок = ФакторРиска.Название;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЭлементПоИмени(Элементы, Имя, ТипЭлемента, Родитель)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, Имя) Тогда
		Результат = Элементы[Имя];
		Результат.Видимость = Истина;
		Возврат Элементы[Имя];
	Иначе
		Возврат Элементы.Добавить(Имя, ТипЭлемента, Родитель);
	КонецЕсли;
	
КонецФункции

Процедура ВывестиКарточкуПризнакиХозяйственнойДеятельности(Форма, ФакторыРиска)
	
	Элементы = Форма.Элементы;
	Элементы.ДекорацияДанныеОбАрбитражныхДелахЗначение.Заголовок =
		ДанныеОбАрбитражныхДелахПредставление(
			ФакторыРиска.СводныеДанныеОбАрбитражныхДелах);
	Элементы.ДекорацияДанныеГосударственныеКонтрактыЗначение.Заголовок =
		ДанныеГосударственныеКонтрактыПредставление(
			ФакторыРиска.ДанныеГосударственныеКонтракты);
	
	Элементы.ГруппаСПАРКПризнакиХозяйственнойДеятельности.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти