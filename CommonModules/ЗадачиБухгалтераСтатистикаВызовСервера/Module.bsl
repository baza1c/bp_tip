#Область ПрограммныйИнтерфейс

// Проверяет различные ограничения, которые могут препятствовать загрузке стат. форм из сервиса
// (например, нет интеренет-поддержки)
// 
// Параметры:
//  ПричинаНедоступности - Строка - детализирует (в виде кода причины), какое именно ограничение не позволяет загрузить данные
//  СообщениеОбОшибке - Строка - текстовое пояснение в случае недоступности загрузки
// 
// Возвращаемое значение:
//  Булево - Истина, если загрузка данных в данный момент возможна
Функция ДоступнаЗагрузкаФормИзСервиса(ПричинаНедоступности = "", СообщениеОбОшибке = "") Экспорт
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		ПричинаНедоступности = "ЗаблокированаРаботаСВнешнимиРесурсами";
		СообщениеОбОшибке = НСтр("ru='Заблокирована работа с внешними ресурсами'");
		Возврат Ложь;
	КонецЕсли;

	// В режиме интерфейса интеграции с банком задачи по статистике не предусмотрены
	ЭтоИнтерфейсИнтеграцииСБанком = Ложь;
	ЗадачиБухгалтераСтатистикаПереопределяемый.ОпределитьЭтоИнтерфейсИнтеграцииСБанком(ЭтоИнтерфейсИнтеграцииСБанком);
	Если ЭтоИнтерфейсИнтеграцииСБанком Тогда
		ПричинаНедоступности = "ЭтоИнтерфейсИнтеграцииСБанком";
		СообщениеОбОшибке = НСтр("ru='Загрузка списка форм статистики не предусмотрена в текущем режиме'");
		Возврат Ложь;
	КонецЕсли;

	Если ОбщегоНазначения.РазделениеВключено() Тогда
		// Если область данных еще не используется, то обращение к сервису не требуется.
		// В качестве критерия проверяем наличие хотя бы одной организации.
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации";
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если РезультатЗапроса.Пустой() Тогда
			СообщениеОбОшибке = НСтр("ru='Область данных не используется'");
			Возврат Ложь;
		КонецЕсли;
		
		Возврат Истина;
		
	КонецЕсли;
	
	// Запрос к сервису возможен только приналичии доступа к порталу ИТС
	УстановитьПривилегированныйРежим(Истина);
	ДанныеИнтернетПоддержки = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	Если ДанныеИнтернетПоддержки = Неопределено Тогда
		ПричинаНедоступности = "НеЗаполненыДанныеИнтернетПоддержки";
		СообщениеОбОшибке = НСтр("ru='Не подключена интернет-поддержка'");
		Возврат Ложь;
	КонецЕсли;
	
	// Если загрузка данных выполнялась недавно, то считаем, что логин и пароль все еще валидны - не выполняем явную
	// проверку этих данных
	Если Не ПроверитьМинимальныйИнтервалОбращенияКСервису() Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверим, что есть доступ
	РезультатПроверки = ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль(ДанныеИнтернетПоддержки.Логин, ДанныеИнтернетПоддержки.Пароль);
	Если Не РезультатПроверки.Результат Тогда
		ПричинаНедоступности = РезультатПроверки.КодОшибки;
		СообщениеОбОшибке = РезультатПроверки.СообщениеОбОшибке;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает дату (без времени) последней успешной загрузки списка форм статистики из сервиса.
// 
// Возвращаемое значение:
//  Дата
Функция ДатаПоследнейЗагрузкиСпискаФормСтатистики() Экспорт
	
	Возврат Константы.ДатаАктуальностиСпискаФормСтатистики.Получить();
	
КонецФункции

// Возвращает url-адрес интерактивного сервиса росстата, где пользователь может сверить данные в программе и в Росстате
// 
// Возвращаемое значение:
//  Строка
Функция АдресИнтерактивногоСервисаРосстата() Экспорт
	
	Возврат "https://websbor.rosstat.gov.ru/online/info";
	
КонецФункции

// Проверяет, прошло ли достаточно времени после последнего успешного запроса к сервису
// 
// Возвращаемое значение:
//  Булево - Истина, если можно отправить новый запрос в сервис
Функция ПроверитьМинимальныйИнтервалОбращенияКСервису() Экспорт
	
	МинимальныйИнтервал = 1 * 24 * 3600; // 1 день
	
	ДатаПоследнегоЗапроса = ДатаПоследнейЗагрузкиСпискаФормСтатистики();
	
	// Дата в константе устанавливается без времени. Поэтому достаточно, чтобы сменилась дата -
	// тогда возможен повторный запрос.
	Возврат (ОбщегоНазначения.ТекущаяДатаПользователя() - ДатаПоследнегоЗапроса) >= МинимальныйИнтервал;
	
КонецФункции

// Проверяет, запущено ли фоновое задание по загрузке стат. форм из сервиса, и если нет, то запускает его.
// 
// Возвращаемое значение:
//  Структура - Сведения о длительной операции получения стат форм:
// * ОжидатьЗавершение - Число - предполагаемое время в секундах до готовности результата фонового задания
// * ДлительнаяОперация - Структура - см. возвращаемое значение метода ДлительныеОперации.ВыполнитьПроцедуру()
Функция СведенияОДлительнойОперацииПолученияСтатФорм() Экспорт
	
	РегламентноеЗадание = Метаданные.РегламентныеЗадания.ПолучениеСпискаТребуемыхСтатФорм;
	ИмяМетода = РегламентноеЗадание.ИмяМетода;
	
	// Проверка, выполняется ли фоновое задание.
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяМетода", ИмяМетода);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	СведенияОДлительнойОперацииПолученияСтатФорм = НовыйСведенияОДлительнойОперацииПолученияСтатФорм();
	
	Если АктивныеФоновыеЗадания.Количество() > 0 Тогда
		// Задание уже запущено - нужно ожидать завершения.
		// Точно известно, что получение результата запроса занимает некоторое время. Закладываем стандартное время
		// подготовик результата на стороне сервиса, плюс еще секунда на обработку результата.
		СведенияОДлительнойОперацииПолученияСтатФорм.ОжидатьЗавершение = 
			ЗадачиБухгалтераСтатистика.СтандартноеВремяОжиданияРезультатаЗапроса() + 1;
		Возврат СведенияОДлительнойОперацииПолученияСтатФорм;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.ОжидатьЗавершение = 0; // сразу показываем картинку ожидания
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Запуск вручную: %1'"), РегламентноеЗадание.Синоним);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	СведенияОДлительнойОперацииПолученияСтатФорм.ДлительнаяОперация =
		ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода, Истина); // Истина, чтобы в случае ошибки в результате был статус "ошибка"
	
	Возврат СведенияОДлительнойОперацииПолученияСтатФорм;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйСведенияОДлительнойОперацииПолученияСтатФорм()
	
	СведенияОДлительнойОперацииПолученияСтатФорм = Новый Структура;
	СведенияОДлительнойОперацииПолученияСтатФорм.Вставить("ОжидатьЗавершение", 0);
	СведенияОДлительнойОперацииПолученияСтатФорм.Вставить("ДлительнаяОперация");
	
	Возврат СведенияОДлительнойОперацииПолученияСтатФорм;
	
КонецФункции

#КонецОбласти