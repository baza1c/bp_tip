#Область ПрограммныйИнтерфейс 

Функция ЗагрузитьДанные(Организация = Неопределено, Гостиница = Неопределено, Период = Неопределено) Экспорт
	Перем СообщениеОбОшибке;
	
	Результат = Новый Структура;
	Результат.Вставить("Результат",                Ложь);
	Результат.Вставить("ТребуетсяНастройка",       Ложь);
	Результат.Вставить("ИзмененныеТипыДокументов", Новый Массив);
	Результат.Вставить("ДатаЗагрузки",             Неопределено);
	Результат.Вставить("СообщениеОбОшибке",        Неопределено);

	СписокГостиниц = РегистрыСведений.НастройкиИнтеграцииBnovo.СписокГостиницДляЗагрузки(Организация, Гостиница);
	Если СписокГостиниц.Количество() = 0 Тогда
		Результат.ТребуетсяНастройка = Истина;
		Возврат Результат;
	КонецЕсли;
	
	ДатаЗагрузки = ТекущаяДатаСеанса();
	Для каждого СтрокаГостиница Из СписокГостиниц Цикл
		ЕстьЗагруженныеДокументы = Ложь;
		
		ЗагруженныеБронирования = БронированияЗаПериод(СтрокаГостиница.Организация, СтрокаГостиница.Гостиница, Период, Результат.СообщениеОбОшибке);
		Если ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ЗагруженныеБронирования.Количество() > 0 Тогда
			ЕстьЗагруженныеДокументы = Истина;
			ЗаписатьДанныеДокументов(СтрокаГостиница.Организация, СтрокаГостиница.Гостиница, ДатаЗагрузки, ЗагруженныеБронирования, Результат.ИзмененныеТипыДокументов);
		КонецЕсли;
		
		ЗагруженныеПроживания = ПроживанияЗаПериод(СтрокаГостиница.Организация, СтрокаГостиница.Гостиница, Период, Результат.СообщениеОбОшибке);
		Если ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ЗагруженныеПроживания.Количество() > 0 Тогда
			ЕстьЗагруженныеДокументы = Истина;
			ЗаписатьДанныеДокументов(СтрокаГостиница.Организация, СтрокаГостиница.Гостиница, ДатаЗагрузки, ЗагруженныеПроживания, Результат.ИзмененныеТипыДокументов);
		КонецЕсли;
		
		ЗагруженныеОплаты = ОплатыЗаПериод(СтрокаГостиница.Организация, СтрокаГостиница.Гостиница, Период, Результат.СообщениеОбОшибке);
		Если ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ЗагруженныеОплаты.Количество() > 0 Тогда
			ЕстьЗагруженныеДокументы = Истина;
			ЗаписатьДанныеДокументов(СтрокаГостиница.Организация, СтрокаГостиница.Гостиница, ДатаЗагрузки, ЗагруженныеОплаты, Результат.ИзмененныеТипыДокументов);
		КонецЕсли;
		
		Если НЕ ЕстьЗагруженныеДокументы Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаписьРегистра = РегистрыСведений.НастройкиИнтеграцииBnovo.СоздатьМенеджерЗаписи();
		
		ЗаписьРегистра.Организация = СтрокаГостиница.Организация;
		ЗаписьРегистра.Гостиница = СтрокаГостиница.Гостиница;
		ЗаписьРегистра.Прочитать();
		
		ЗаписьРегистра.ДатаПоследнейЗагрузки = ?(Период<>Неопределено, Макс(Период.ДатаОкончания, ЗаписьРегистра.ДатаПоследнейЗагрузки), ДатаЗагрузки);
		ЗаписьРегистра.Записать(Истина); 
		
		Результат.Вставить("ДатаЗагрузки", ДатаЗагрузки);
	КонецЦикла;
	
	Результат.Вставить("Результат", Истина);
	
	Возврат Результат;

КонецФункции

Функция БронированияЗаПериод(Организация, Гостиница, Период, СообщениеОбОшибке) Экспорт
	СозданныеДокументы = Новый Массив;
	
	ТипСумма         = ОбщегоНазначения.ОписаниеТипаЧисло(12, 2);
	ТипКоличество    = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);
	ТипСтрока        = ОбщегоНазначения.ОписаниеТипаСтрока(100);
	ТипИдентификатор = ОбщегоНазначения.ОписаниеТипаЧисло(15);
	
	УслугиПоБронированиям = Новый ТаблицаЗначений;
	УслугиПоБронированиям.Колонки.Добавить("ДатаБрони",                ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	УслугиПоБронированиям.Колонки.Добавить("НомерБрони",               ТипСтрока);
	УслугиПоБронированиям.Колонки.Добавить("ИмяГостя",                 ТипСтрока);
	УслугиПоБронированиям.Колонки.Добавить("ИдентификаторПлательщика", ТипИдентификатор);
	УслугиПоБронированиям.Колонки.Добавить("ИдентификаторУслуги",      ТипСтрока);
	УслугиПоБронированиям.Колонки.Добавить("ЭтоУслугаРазмещения",      Новый ОписаниеТипов("Булево"));
	УслугиПоБронированиям.Колонки.Добавить("СодержаниеУслуги",         ТипСтрока);
	УслугиПоБронированиям.Колонки.Добавить("Количество",               ТипКоличество);
	УслугиПоБронированиям.Колонки.Добавить("Сумма",                    ТипСумма);
	УслугиПоБронированиям.Колонки.Добавить("СтавкаНДС",                Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	УслугиПоБронированиям.Колонки.Добавить("СуммаНДС",                 ТипСумма);
	
	НоменклатурыДляСопоставления = НовыйТаблицаНоменклатуры();
	
	СоотвествиеПлательщик = Новый Соответствие;
	
	СоотвествиеСтавокНДС = СоотвествиеСтавокНДС();
	
	НастройкиИнтеграции = РегистрыСведений.НастройкиИнтеграцииBnovo.Получить(Новый Структура("Организация, Гостиница", Организация, Гостиница));
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиИнтеграции, "УчетнаяЗапись") = Неопределено Тогда
		СообщениеОбОшибке = НСтр("ru = 'Подключение не настроено'");
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыПолучитьБронирования = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыПолучитьБронирования(Период);
	ПараметрыПолучитьБронирования.УчетнаяЗапись = НастройкиИнтеграции.УчетнаяЗапись;
	
	РезультатЗапроса = ЗапросПолучитьДанные(ПараметрыПолучитьБронирования);
	Если РезультатЗапроса.КодСостояния <> 200 Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось выполнить запрос к Bnovo'");
		Возврат Неопределено;
	КонецЕсли; 
	
	Для каждого СтрокаУслугаПроживания Из РезультатЗапроса.Ответ.data Цикл
		
		Плательщик = СтрокаУслугаПроживания.payer;
		Бронирование = СтрокаУслугаПроживания.reservation;
		
		НоваяСтрока = УслугиПоБронированиям.Добавить(); 
		НоваяСтрока.ДатаБрони                = ПрочитатьДатуJSON(Лев(СтрокаУслугаПроживания.create_date, 10), ФорматДатыJSON.ISO);
		НоваяСтрока.НомерБрони               = Бронирование.number;
		НоваяСтрока.ИмяГостя                 = Бронирование.guest_name;
		НоваяСтрока.ИдентификаторПлательщика = Плательщик.id;
		
		ИдентификаторУслуги = СтрокаУслугаПроживания.service_id;
		
		// для услуги проживания в id запишем идентификатор номера
		НоваяСтрока.ЭтоУслугаРазмещения = ИдентификаторУслуги = "occupancy";
		Если НоваяСтрока.ЭтоУслугаРазмещения Тогда
			ИдентификаторУслуги = Бронирование.room_type_id
		КонецЕсли;
		НоваяСтрока.ИдентификаторУслуги = ПолныйИдентификаторНоменклатуры(ИдентификаторУслуги, НоваяСтрока.ЭтоУслугаРазмещения);
		
		Если НоваяСтрока.ЭтоУслугаРазмещения 
			И ЗначениеЗаполнено(Бронирование.departure) 
			И ЗначениеЗаполнено(Бронирование.arrival) Тогда
			
			НоваяСтрока.СодержаниеУслуги = СтрШаблон("%1 %2", СтрокаУслугаПроживания.service_name, СтрокаУслугаПроживания.reservation.room_type_name);
			
			// для проживания в количество всегда пишем количество суток (для турналога)
			НоваяСтрока.Количество = Цел((ПрочитатьДатуJSON(Лев(Бронирование.departure, 10), ФорматДатыJSON.ISO) - ПрочитатьДатуJSON(Лев(Бронирование.arrival, 10),ФорматДатыJSON.ISO))/86400);
		Иначе
			
			НоваяСтрока.СодержаниеУслуги = СтрокаУслугаПроживания.service_name;
			НоваяСтрока.Количество = СтрокаУслугаПроживания.service_quantity;
		КонецЕсли;
		НоваяСтрока.Сумма                    = СтрокаУслугаПроживания.amount;
		НоваяСтрока.СтавкаНДС                = Перечисления.СтавкиНДС.СтавкаНДС(СоотвествиеСтавокНДС[СтрокаУслугаПроживания.nds], НоваяСтрока.ДатаБрони);
		НоваяСтрока.СуммаНДС                 = СтрокаУслугаПроживания.nds_amount;
		
		Если СоотвествиеПлательщик[Плательщик.id] <> Неопределено 
			ИЛИ Плательщик.type <> 0 Тогда
			
			Продолжить;
		КонецЕсли;
			РеквизитыКонтрагента = Новый Структура;
			РеквизитыКонтрагента.Вставить("Наименование", Плательщик.name);
			РеквизитыКонтрагента.Вставить("ИНН", Плательщик.inn);
			РеквизитыКонтрагента.Вставить("КПП", Плательщик.kpp);
			КонтрагентСсылка = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(РеквизитыКонтрагента);
			Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
				КонтрагентСсылка = Справочники.Контрагенты.СоздатьКонтрагента(РеквизитыКонтрагента);
			КонецЕсли;
			СоотвествиеПлательщик.Вставить(Плательщик.id, КонтрагентСсылка);
	КонецЦикла;
	
	УслугиПоБронированиям.Свернуть("ДатаБрони, НомерБрони, ИмяГостя, ИдентификаторПлательщика, ИдентификаторУслуги, ЭтоУслугаРазмещения, СодержаниеУслуги, СтавкаНДС", "Количество, Сумма, СуммаНДС");
	
	ТаблицаБронирования = УслугиПоБронированиям.Скопировать();
	ТаблицаБронирования.Свернуть("ДатаБрони, НомерБрони, ИмяГостя, ИдентификаторПлательщика");
	ТаблицаБронирования.Сортировать("НомерБрони");
	
	ШаблонКомментарий = НСтр("ru = '%1 - автоматически создано загрузкой из Bnovo'");
	
	СопоставленнаяНоменклатура = НоменклатураПоИдентификаторам(ОбщегоНазначения.ВыгрузитьКолонку(УслугиПоБронированиям, "ИдентификаторУслуги", Истина));
	
	СписокБроней = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаБронирования, "НомерБрони", Истина);
	ДокументыПоНомерамБроней = СоответствиеДокументовНомерамБроней(Организация, Гостиница, СписокБроней, "СчетНаОплатуПокупателю");
	
	НачатьТранзакцию();
	Для каждого СтрокаБронирование Из ТаблицаБронирования Цикл
		НомерБрони = СтрокаБронирование.НомерБрони;
		
		НоменклатурыДляСопоставления.Очистить();
		
		ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Организация, СтрокаБронирование.ДатаБрони)
			Или УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, СтрокаБронирование.ДатаБрони);
		
		ДокументПоНомеруБрони =  ДокументыПоНомерамБроней[НомерБрони];
		Если ЗначениеЗаполнено(ДокументПоНомеруБрони) Тогда
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.СчетНаОплатуПокупателю");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокументПоНомеруБрони);
			Блокировка.Заблокировать();
			
			ДокументОбъект = ДокументПоНомеруБрони.ПолучитьОбъект();
			ДокументОбъект.ПометкаУдаления = Ложь;
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			
			ДокументОбъект.Товары.Очистить();
			
		Иначе
			ДокументОбъект = Документы.СчетНаОплатуПокупателю.СоздатьДокумент();
			
			ДанныеЗаполнения = Новый Структура;
			
			ДанныеЗаполнения.Вставить("Организация",     Организация);
			ДанныеЗаполнения.Вставить("Гостиница",       Гостиница);
			ДанныеЗаполнения.Вставить("ЗагрузкаИзBnovo", Истина);
			
			ДанныеЗаполнения.Вставить("Контрагент",      СоотвествиеПлательщик[СтрокаБронирование.ИдентификаторПлательщика]);
			
			// Физическое лицо
			Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения.Контрагент) Тогда
				ДанныеЗаполнения.Вставить("Контрагент", НастройкиИнтеграции.Покупатель);
				Если НастройкиИнтеграции.РазделятьДоговораПоНомерамБроней Тогда
					
					ЗначенияЗаполнения = Новый Структура;
					ЗначенияЗаполнения.Вставить("Организация", Организация);
					ЗначенияЗаполнения.Вставить("Владелец",    ДанныеЗаполнения.Контрагент);
					ЗначенияЗаполнения.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
					ЗначенияЗаполнения.Вставить("Номер",       НомерБрони);
					ЗначенияЗаполнения.Вставить("Дата",        СтрокаБронирование.ДатаБрони);
					
					ДанныеЗаполнения.Вставить("ДоговорКонтрагента", НайтиСоздатьДоговорКонтрагента(ЗначенияЗаполнения));
				КонецЕсли;
			КонецЕсли;
			
			ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеЗаполнения);
			
			ДокументОбъект.ОрганизацияПолучатель = ДанныеЗаполнения.Организация;
			
			текДата = ДокументОбъект.Дата;
			
			ДокументОбъект.Дата = СтрокаБронирование.ДатаБрони; 
			Если НачалоГода(текДата) <> НачалоГода(СтрокаБронирование.ДатаБрони) Тогда
				ДокументОбъект.УстановитьНовыйНомер();
			КонецЕсли;
			
			УчетДенежныхСредствБП.УстановитьБанковскийСчет(ДокументОбъект.СтруктурнаяЕдиница,
				Организация, ДокументОбъект.ВалютаДокумента);
				
			ДокументОбъект.ДокументБезНДС   = НЕ ПлательщикНДС;
			ДокументОбъект.СуммаВключаетНДС = Истина;
			
			ДокументОбъект.Комментарий = СформироватьКомментарий(НомерБрони, СтрокаБронирование.ИмяГостя);
		КонецЕсли;
		
		ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
		
		НомерСтрокиЗаказа = 0;
		
		УслугиПоБронированию = УслугиПоБронированиям.НайтиСтроки(Новый Структура("НомерБрони", НомерБрони));
		Для каждого СтрокаУслуги Из УслугиПоБронированию Цикл
			Если СтрокаУслуги.Сумма = 0 Тогда
				Продолжить;
			КонецЕсли;
		
			НоваяСтрока = ДокументОбъект.Товары.Добавить();
			
			НоваяСтрока.Номенклатура = СопоставленнаяНоменклатура[СтрокаУслуги.ИдентификаторУслуги];
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
				НоваяСтрокаНоменклатура = НоменклатурыДляСопоставления.Добавить();
				НоваяСтрокаНоменклатура.НомерСтрокиЗаказа   = НомерСтрокиЗаказа;
				НоваяСтрокаНоменклатура.Идентификатор       = СтрокаУслуги.ИдентификаторУслуги;
				НоваяСтрокаНоменклатура.Наименование        = СтрокаУслуги.СодержаниеУслуги;
				НоваяСтрокаНоменклатура.ЭтоУслугаРазмещения = СтрокаУслуги.ЭтоУслугаРазмещения;
			КонецЕсли;
			
			НоваяСтрока.Содержание = СтрокаУслуги.СодержаниеУслуги;
			НоваяСтрока.Количество = СтрокаУслуги.Количество;
			НоваяСтрока.Сумма      = СтрокаУслуги.Сумма;
			НоваяСтрока.Цена       = Окр(НоваяСтрока.Сумма/НоваяСтрока.Количество, 2);
			НоваяСтрока.СтавкаНДС  = СтрокаУслуги.СтавкаНДС;
			НоваяСтрока.СуммаНДС   = СтрокаУслуги.СуммаНДС;
			
			Если СтрокаУслуги.ЭтоУслугаРазмещения Тогда
				НоваяСтрока.ВидЛьготыПоТуристическомуНалогу = Перечисления.ВидыЛьготПоТуристическомуНалогу.НеПрименяется;
			КонецЕсли;
			
			НомерСтрокиЗаказа = НомерСтрокиЗаказа + 1;
		КонецЦикла; 
		
		Если ДокументОбъект.Товары.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ДокументОбъект);
		Попытка
			ДокументОбъект.Записать();
		Исключение
			ОтменитьТранзакцию();
			Если НЕ ДатыЗапретаИзменения.ИзменениеЗапрещено(ДокументОбъект,, СообщениеОбОшибке) Тогда
				СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'Не удалось записать документ по брони %1 от %2'"), СтрокаБронирование.НомерБрони, СтрокаБронирование.ДатаБрони);
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, ,ДокументОбъект, СообщениеОбОшибке);
			
			Возврат Неопределено;
		КонецПопытки;
		
		СтруктураДокумент = НовыйЗагруженныйДокумент();
		
		СтруктураДокумент.Вставить("НомерБрони",             НомерБрони);
		СтруктураДокумент.Вставить("ИмяГостя",               СтрокаБронирование.ИмяГостя);
		СтруктураДокумент.Вставить("Контрагент",             ДокументОбъект.Контрагент);
		СтруктураДокумент.Вставить("Документ",               ДокументОбъект.Ссылка);
		СтруктураДокумент.Вставить("Сумма",                  ДокументОбъект.СуммаДокумента);
		
		СозданныеДокументы.Добавить(СтруктураДокумент);
		
		Если НоменклатурыДляСопоставления.Количество() > 0 Тогда
			НовыеОбъекты = РегистрыСведений.НесопоставленныеОбъектыИнтернетМагазина.СоздатьМенеджерЗаписи();
			НовыеОбъекты.Документ = ДокументОбъект.Ссылка;
			НовыеОбъекты.ДанныеОбъекта = Новый ХранилищеЗначения(НоменклатурыДляСопоставления, Новый СжатиеДанных(9));
			НовыеОбъекты.Записать();
		КонецЕсли;
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	Возврат СозданныеДокументы;
КонецФункции

Функция ПроживанияЗаПериод(Организация, Гостиница, Период, СообщениеОбОшибке) Экспорт
	СозданныеДокументы = Новый Массив;
	
	ТипСумма = ОбщегоНазначения.ОписаниеТипаЧисло(12, 2);
	ТипКоличество = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);
	ТипСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(100);
	ТипИдентификатор = ОбщегоНазначения.ОписаниеТипаЧисло(15);
	
	ТаблицаПроживания = Новый ТаблицаЗначений;
	ТаблицаПроживания.Колонки.Добавить("ДатаВыезда",               ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаПроживания.Колонки.Добавить("НомерБрони",               ТипСтрока);
	ТаблицаПроживания.Колонки.Добавить("ИмяГостя",                 ТипСтрока);
	ТаблицаПроживания.Колонки.Добавить("ТипПлательщика",           ОбщегоНазначения.ОписаниеТипаЧисло(1));
	ТаблицаПроживания.Колонки.Добавить("ИдентификаторПлательщика", ТипИдентификатор);
	ТаблицаПроживания.Колонки.Добавить("ИдентификаторУслуги",      ТипСтрока);
	ТаблицаПроживания.Колонки.Добавить("ЭтоУслугаРазмещения",         Новый ОписаниеТипов("Булево"));
	ТаблицаПроживания.Колонки.Добавить("СодержаниеУслуги",         ТипСтрока);
	ТаблицаПроживания.Колонки.Добавить("Количество",               ТипКоличество);
	ТаблицаПроживания.Колонки.Добавить("Сумма",                    ТипСумма);
	ТаблицаПроживания.Колонки.Добавить("СтавкаНДС",                Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТаблицаПроживания.Колонки.Добавить("СуммаНДС",                 ТипСумма);
	
	НоменклатурыДляСоспоставления = НовыйТаблицаНоменклатуры();
	
	СоотвествиеПлательщик = Новый Соответствие;
	СоотвествиеСтавокНДС = СоотвествиеСтавокНДС();
	
	НастройкиИнтеграции = РегистрыСведений.НастройкиИнтеграцииBnovo.Получить(Новый Структура("Организация, Гостиница", Организация, Гостиница));
	
	ПараметрыПолучитьПроживания = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыПолучитьПроживания(Период);
	ПараметрыПолучитьПроживания.УчетнаяЗапись = НастройкиИнтеграции.УчетнаяЗапись;
	
	РезультатЗапроса = ЗапросПолучитьДанные(ПараметрыПолучитьПроживания);
	Если РезультатЗапроса.КодСостояния <> 200 Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось выполнить запрос к Bnovo'");
		Возврат Неопределено;
	КонецЕсли; 
	
	Для каждого СтрокаПроживание Из РезультатЗапроса.Ответ.data Цикл
		Плательщик = СтрокаПроживание.payer;
		Бронирование = СтрокаПроживание.reservation;
	
		НоваяСтрока = ТаблицаПроживания.Добавить(); 
		
		НоваяСтрока.ДатаВыезда               = ПрочитатьДатуJSON(Лев(СтрокаПроживание.reservation.departure, 10), ФорматДатыJSON.ISO);
		НоваяСтрока.НомерБрони               = Бронирование.number;
		
		Если Плательщик.type = 1 Тогда
			НоваяСтрока.ТипПлательщика = 0;  // физлицо
		Иначе
			НоваяСтрока.ТипПлательщика = ?(Плательщик.is_agency, 2, 1); // 1 - юрлицо, 2 - агент
		КонецЕсли;
		
		НоваяСтрока.ИдентификаторПлательщика = Плательщик.id;
		НоваяСтрока.ИмяГостя                 = Бронирование.guest_name;
		
		ИдентификаторУслуги = СтрокаПроживание.service_id;
		
		// для услуги проживания в id запишем идентификатор номера
		НоваяСтрока.ЭтоУслугаРазмещения = ИдентификаторУслуги = "occupancy";
		Если НоваяСтрока.ЭтоУслугаРазмещения Тогда
			ИдентификаторУслуги = Бронирование.room_type_id;
		КонецЕсли;
		НоваяСтрока.ИдентификаторУслуги = ПолныйИдентификаторНоменклатуры(ИдентификаторУслуги, НоваяСтрока.ЭтоУслугаРазмещения);
		
		Если НоваяСтрока.ЭтоУслугаРазмещения 
			И ЗначениеЗаполнено(Бронирование.departure) 
			И ЗначениеЗаполнено(Бронирование.arrival) Тогда
			
			НоваяСтрока.СодержаниеУслуги = СтрШаблон("%1 %2", СтрокаПроживание.service_name, СтрокаПроживание.reservation.room_type_name);
			
			// для проживания в количество всегда пишем количество суток (для турналога)
			НоваяСтрока.Количество = Цел((ПрочитатьДатуJSON(Лев(Бронирование.departure, 10), ФорматДатыJSON.ISO) - ПрочитатьДатуJSON(Лев(Бронирование.arrival, 10),ФорматДатыJSON.ISO))/86400);
		Иначе
			НоваяСтрока.СодержаниеУслуги = СтрокаПроживание.service_name;
			НоваяСтрока.Количество = СтрокаПроживание.service_quantity;
		КонецЕсли;
		
		НоваяСтрока.Сумма     = СтрокаПроживание.amount;
		НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(СоотвествиеСтавокНДС[СтрокаПроживание.nds], НоваяСтрока.ДатаВыезда);
		НоваяСтрока.СуммаНДС  = СтрокаПроживание.nds_amount;
		
		Если НЕ СоотвествиеПлательщик[НоваяСтрока.ИдентификаторПлательщика] = Неопределено ИЛИ Плательщик.type <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыКонтрагента = Новый Структура;
		РеквизитыКонтрагента.Вставить("Наименование", Плательщик.name);
		РеквизитыКонтрагента.Вставить("ИНН", Плательщик.inn);
		РеквизитыКонтрагента.Вставить("КПП", Плательщик.kpp);
		КонтрагентСсылка = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(РеквизитыКонтрагента);
		Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
			КонтрагентСсылка = Справочники.Контрагенты.СоздатьКонтрагента(РеквизитыКонтрагента);
		КонецЕсли;
		СоотвествиеПлательщик.Вставить(НоваяСтрока.ИдентификаторПлательщика, КонтрагентСсылка);
		
	КонецЦикла;
	
	ТаблицаПроживания.Свернуть("ДатаВыезда, НомерБрони, ИмяГостя, ТипПлательщика, ИдентификаторПлательщика, ИдентификаторУслуги, ЭтоУслугаРазмещения, СодержаниеУслуги, СтавкаНДС", "Количество, Сумма, СуммаНДС");
	
	ТаблицаБронирования = ТаблицаПроживания.Скопировать();
	ТаблицаБронирования.Свернуть("ДатаВыезда, НомерБрони, ИмяГостя, ТипПлательщика, ИдентификаторПлательщика");
	ТаблицаБронирования.Сортировать("ТипПлательщика, ИдентификаторПлательщика, НомерБрони");
	
	СопоставленнаяНоменклатура = НоменклатураПоИдентификаторам(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПроживания, "ИдентификаторУслуги", Истина));
	
	СписокБроней = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаБронирования, "НомерБрони", Истина);
	
	СчетаПоНомерамБроней      = СоответствиеДокументовНомерамБроней(Организация, Гостиница, СписокБроней, "СчетНаОплатуПокупателю");
	РеализацииПоНомерамБроней = СоответствиеДокументовНомерамБроней(Организация, Гостиница, СписокБроней, "ОтчетКомиссионераОПродажах,РеализацияТоваровУслуг");
	
	ДоговораПоСчетамНаОплату = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
		ОбщегоНазначения.ВыгрузитьКолонку(СчетаПоНомерамБроней, "Значение"), "ДоговорКонтрагента");
		
	ПлательщикНДС = Новый Соответствие;
	
	Для каждого СтрокаБронирование Из ТаблицаБронирования Цикл
		НомерБрони = СтрокаБронирование.НомерБрони;
		УслугиПоСчету = ТаблицаПроживания.НайтиСтроки(Новый Структура("НомерБрони", НомерБрони));
		
		НоменклатурыДляСоспоставления.Очистить();
		
		ДанныеЗаполнения = Новый Структура;
		
		ДанныеЗаполнения.Вставить("Контрагент",  СоотвествиеПлательщик[СтрокаБронирование.ИдентификаторПлательщика]);
		ДанныеЗаполнения.Вставить("Организация", Организация);
		ДанныеЗаполнения.Вставить("Гостиница",   Гостиница);
		ДанныеЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийРеализацияТоваров.Услуги);
		ДанныеЗаполнения.Вставить("ЗагрузкаИзBnovo", Истина);
		
		Если СтрокаБронирование.ТипПлательщика = 0 Тогда // физлицо
			ДанныеЗаполнения.Вставить("Контрагент", НастройкиИнтеграции.Покупатель);
		КонецЕсли;
		
		СчетПоНомеруБрони = СчетаПоНомерамБроней[НомерБрони];
		Если ПлательщикНДС[НачалоМесяца(СтрокаБронирование.ДатаВыезда)] = Неопределено Тогда
			ПлательщикНДС.Вставить(НачалоМесяца(СтрокаБронирование.ДатаВыезда), 
				УчетнаяПолитика.ПлательщикНДС(Организация, СтрокаБронирование.ДатаВыезда) 
				ИЛИ УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, СтрокаБронирование.ДатаВыезда));
		КонецЕсли;
		
		Если СтрокаБронирование.ТипПлательщика = 0 
			И НастройкиИнтеграции.РазделятьДоговораПоНомерамБроней 
			И ДоговораПоСчетамНаОплату[СчетПоНомеруБрони] <> Неопределено Тогда
		
			ДанныеЗаполнения.Вставить("ДоговорКонтрагента", ДоговораПоСчетамНаОплату[СчетПоНомеруБрони]);
		Иначе
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Организация", Организация);
			ЗначенияЗаполнения.Вставить("Владелец",    ДанныеЗаполнения.Контрагент);
			
			Если СтрокаБронирование.ТипПлательщика = 2 Тогда  // агент
				ЗначенияЗаполнения.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
			Иначе
				ЗначенияЗаполнения.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
			КонецЕсли;
			
			Если СтрокаБронирование.ТипПлательщика = 0 // физлицо
				И НастройкиИнтеграции.РазделятьДоговораПоНомерамБроней Тогда
				
				ЗначенияЗаполнения.Вставить("Номер",       НомерБрони);
			КонецЕсли;
			
			ДанныеЗаполнения.Вставить("ДоговорКонтрагента", НайтиСоздатьДоговорКонтрагента(ЗначенияЗаполнения));
		КонецЕсли;
		
		ТипДокумента = ?(СтрокаБронирование.ТипПлательщика = 2, "ОтчетКомиссионераОПродажах", "РеализацияТоваровУслуг");
		
		ДокументПоНомеруБрони = РеализацииПоНомерамБроней[НомерБрони];
		Если ЗначениеЗаполнено(ДокументПоНомеруБрони) 
			И ТипЗнч(ДокументПоНомеруБрони) <> Тип("ДокументСсылка."+ТипДокумента) Тогда
		
			ДокументОбъект = ДокументПоНомеруБрони.ПолучитьОбъект();
			ДокументОбъект.УстановитьПометкуУдаления();
			
			ДокументПоНомеруБрони = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДокументПоНомеруБрони) Тогда
			ДокументОбъект = ДокументПоНомеруБрони.ПолучитьОбъект();
			ДокументОбъект.Услуги.Очистить();
		Иначе
			Если СтрокаБронирование.ТипПлательщика = 2 Тогда
				ДанныеЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОПродажах);
				
				ДокументОбъект = Документы.ОтчетКомиссионераОПродажах.СоздатьДокумент();
				ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеЗаполнения);
				
				Документы.ОтчетКомиссионераОПродажах.ЗаполнитьСчетаУчетаРасчетов(,, ДокументОбъект);
				
				Если ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента) Тогда
					
					ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.ДоговорКонтрагента, "СпособРасчетаКомиссионногоВознаграждения, ПроцентКомиссионногоВознаграждения");
					ДокументОбъект.СпособРасчетаКомиссионногоВознаграждения = ЗначенияРеквизитов.СпособРасчетаКомиссионногоВознаграждения;
					ДокументОбъект.ПроцентКомиссионногоВознаграждения = ЗначенияРеквизитов.ПроцентКомиссионногоВознаграждения;
					
				КонецЕсли;
				
				СтрокаПокупатель = ДокументОбъект.Покупатели.Добавить();
				СтрокаПокупатель.Покупатель = СоотвествиеПлательщик[СтрокаБронирование.ИдентификаторПлательщика];
			Иначе
				ДокументОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
				ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеЗаполнения);
				
				ДокументОбъект.СчетНаОплатуПокупателю = СчетПоНомеруБрони;
			КонецЕсли;
			
			ДокументОбъект.Дата        = СтрокаБронирование.ДатаВыезда;
			ДокументОбъект.Комментарий = СформироватьКомментарий(НомерБрони, СтрокаБронирование.ИмяГостя);
			
			ДокументОбъект.ДокументБезНДС   = НЕ ПлательщикНДС[НачалоМесяца(ДокументОбъект.Дата)];
			ДокументОбъект.СуммаВключаетНДС = Истина;
			
		КонецЕсли;
		
		НомерСтрокиЗаказа = 0;
		
		Для каждого СтрокаУслуги Из УслугиПоСчету Цикл
			Если СтрокаУслуги.Сумма = 0 Тогда
				Продолжить;
			КонецЕсли;
		
			НоваяСтрока = ДокументОбъект.Услуги.Добавить();
			
			НоваяСтрока.Номенклатура = СопоставленнаяНоменклатура[СтрокаУслуги.ИдентификаторУслуги];
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
				НоваяСтрокаНоменклатура = НоменклатурыДляСоспоставления.Добавить();
				НоваяСтрокаНоменклатура.НомерСтрокиЗаказа   = НомерСтрокиЗаказа;
				НоваяСтрокаНоменклатура.Идентификатор = СтрокаУслуги.ИдентификаторУслуги;
				НоваяСтрокаНоменклатура.Наименование        = СтрокаУслуги.СодержаниеУслуги;
				НоваяСтрокаНоменклатура.ЭтоУслугаРазмещения = СтрокаУслуги.ЭтоУслугаРазмещения;
			КонецЕсли;
			
			НоваяСтрока.Содержание   = СтрокаУслуги.СодержаниеУслуги;
			НоваяСтрока.Количество   = СтрокаУслуги.Количество;
			НоваяСтрока.Сумма        = СтрокаУслуги.Сумма;
			НоваяСтрока.Цена         = Окр(НоваяСтрока.Сумма/НоваяСтрока.Количество, 2);
			НоваяСтрока.СтавкаНДС    = СтрокаУслуги.СтавкаНДС;
			НоваяСтрока.СуммаНДС     = СтрокаУслуги.СуммаНДС; 
			
			НомерСтрокиЗаказа = НомерСтрокиЗаказа + 1;
		КонецЦикла; 
		
		Если ДокументОбъект.Услуги.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
		ДокументОбъект.Записать();
		
		СтруктураДокумент = НовыйЗагруженныйДокумент();
		
		СтруктураДокумент.Вставить("НомерБрони", НомерБрони);
		СтруктураДокумент.Вставить("ИмяГостя",   СтрокаБронирование.ИмяГостя);
		СтруктураДокумент.Вставить("Контрагент", ДокументОбъект.Контрагент);
		СтруктураДокумент.Вставить("Документ",   ДокументОбъект.Ссылка);
		СтруктураДокумент.Вставить("Сумма",      ДокументОбъект.СуммаДокумента);
		
		СозданныеДокументы.Добавить(СтруктураДокумент);
		
		Если НоменклатурыДляСоспоставления.Количество() > 0 Тогда
			НовыеОбъекты = РегистрыСведений.НесопоставленныеОбъектыИнтернетМагазина.СоздатьМенеджерЗаписи();
			НовыеОбъекты.Документ = ДокументОбъект.Ссылка;
			НовыеОбъекты.ДанныеОбъекта = Новый ХранилищеЗначения(
				НоменклатурыДляСоспоставления, 
				Новый СжатиеДанных(9));
			НовыеОбъекты.Записать();
		КонецЕсли;
	КонецЦикла; 
	
	Возврат СозданныеДокументы;
КонецФункции

Функция ОплатыЗаПериод(Организация, Гостиница, Период, СообщениеОбОшибке) Экспорт
	СозданныеДокументы = Новый Массив;
	
	ТипСумма = ОбщегоНазначения.ОписаниеТипаЧисло(12, 2);
	ТипКоличество = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);
	ТипСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(100);
	ТипИдентификатор = ОбщегоНазначения.ОписаниеТипаЧисло(15);
	ТипСчетНаОплатуПокупателю = Тип("ДокументСсылка.СчетНаОплатуПокупателю");
	ТипПоступлениеНаличных    = Тип("ДокументСсылка.ПриходныйКассовыйОрдер");
	ТипОплатаПлатежнойКартой = Тип("ДокументСсылка.ОплатаПлатежнойКартой");
	
	
	ТаблицаОплаты = Новый ТаблицаЗначений;
	ТаблицаОплаты.Колонки.Добавить("ДатаОплаты",               ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаОплаты.Колонки.Добавить("ВидОплаты",                Новый ОписаниеТипов("ПеречислениеСсылка.ТипыОплат"));
	ТаблицаОплаты.Колонки.Добавить("ЭтоВозврат",               Новый ОписаниеТипов("Булево"));
	ТаблицаОплаты.Колонки.Добавить("НомерБрони",               ТипСтрока);
	ТаблицаОплаты.Колонки.Добавить("Плательщик",               Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаОплаты.Колонки.Добавить("Сумма",                    ТипСумма);
	
	СоотвествиеПлательщик = Новый Соответствие;
	
	НастройкиИнтеграции = РегистрыСведений.НастройкиИнтеграцииBnovo.Получить(Новый Структура("Организация, Гостиница", Организация, Гостиница));
	
	ПараметрыПолучитьБронирования = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыПолучитьПлатежи(Период);
	ПараметрыПолучитьБронирования.УчетнаяЗапись = НастройкиИнтеграции.УчетнаяЗапись;
	
	РезультатЗапроса = ЗапросПолучитьДанные(ПараметрыПолучитьБронирования);
	Если РезультатЗапроса.КодСостояния <> 200 Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось выполнить запрос к Bnovo'");
		Возврат Неопределено;
	КонецЕсли; 
	
	Для каждого СтрокаОплата Из РезультатЗапроса.Ответ.data Цикл
		ЭтоОплатаНаличными = СтрокаОплата.payment_method = 0;
		Если (НЕ НастройкиИнтеграции.ЗагружатьОплатыНаличные И ЭтоОплатаНаличными) 
			ИЛИ (НЕ НастройкиИнтеграции.ЗагружатьОплатыКарта И НЕ ЭтоОплатаНаличными) Тогда
			
			Продолжить;
		КонецЕсли;
		
		Плательщик = СтрокаОплата.payer;
		
		НоваяСтрока = ТаблицаОплаты.Добавить(); 
		НоваяСтрока.ДатаОплаты       = ПрочитатьДатуJSON(Лев(СтрокаОплата.paid_date, 10), ФорматДатыJSON.ISO);
		НоваяСтрока.НомерБрони      = СтрокаОплата.reservation_number;
		НоваяСтрока.ВидОплаты       = ?(ЭтоОплатаНаличными, Перечисления.ТипыОплат.Наличные, Перечисления.ТипыОплат.ПлатежнаяКарта);
		НоваяСтрока.ЭтоВозврат      = СтрокаОплата.payment_type = 1;
		НоваяСтрока.Сумма           = ?(НоваяСтрока.ЭтоВозврат, -СтрокаОплата.amount, СтрокаОплата.amount);
		
		Если Плательщик.type = 0 Тогда
			ИдентификаторПлательщика = Плательщик.inn+"@"+Плательщик.kpp;
			
			КонтрагентСсылка = СоотвествиеПлательщик[ИдентификаторПлательщика];
			
			Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
				РеквизитыКонтрагента = Новый Структура;
				РеквизитыКонтрагента.Вставить("Наименование", Плательщик.name);
				РеквизитыКонтрагента.Вставить("ИНН", Плательщик.inn);
				РеквизитыКонтрагента.Вставить("КПП", Плательщик.kpp);
				КонтрагентСсылка = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(РеквизитыКонтрагента);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
				КонтрагентСсылка = Справочники.Контрагенты.СоздатьКонтрагента(РеквизитыКонтрагента);
			КонецЕсли;
			
			СоотвествиеПлательщик.Вставить(ИдентификаторПлательщика, КонтрагентСсылка);
			
			НоваяСтрока.Плательщик = КонтрагентСсылка;
		Иначе
			НоваяСтрока.Плательщик = НастройкиИнтеграции.Покупатель;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаОплаты.Свернуть("ДатаОплаты, НомерБрони, Плательщик, ВидОплаты, ЭтоВозврат", "Сумма");
	ТаблицаОплаты.Сортировать("ДатаОплаты, ЭтоВозврат Убыв");
	
	СписокБроней  = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаОплаты, "НомерБрони", Истина);
	
	ТаблицаДокументыПоНомерамБроней = ТаблицаДокументовНомерамБроней(Организация, Гостиница, СписокБроней, "ПриходныйКассовыйОрдер, РасходныйКассовыйОрдер, ОплатаПлатежнойКартой, СчетНаОплатуПокупателю");
	
	Для каждого НомерБрони Из СписокБроней Цикл
		Отбор = Новый Структура("НомерБрони", НомерБрони);
		СписокДокументов = ОБщегоНазначения.ВыгрузитьКолонку(ТаблицаДокументыПоНомерамБроней.НайтиСтроки(Отбор), "Документ");
		Если СписокДокументов.Количество() = 0 Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация, , ,
					СтрШаблон(НСтр("ru = 'Не удалось загрузить оплату по брони %1. Не найдено бронирование.'"), НомерБрони));
			Продолжить;
		КонецЕсли;
		
		ДокументыПоНомеруБрони = ОбщегоНазначенияБП.РазложитьСписокПоТипамЗначенийОбъектов(СписокДокументов);
		Для каждого СтрокаОплаты Из ТаблицаОплаты.НайтиСтроки(Отбор) Цикл
			ДанныеЗаполнения = Новый Структура;
			Если СтрокаОплаты.ВидОплаты = Перечисления.ТипыОплат.Наличные И НЕ СтрокаОплаты.ЭтоВозврат Тогда
				ВидДокумента = "ПриходныйКассовыйОрдер";
				ТипОснование = ТипСчетНаОплатуПокупателю;
				КолонкиРаспределения = "СуммаПлатежа,СуммаНДС,ДоходыУСН,СуммаВзаиморасчетов";
			ИначеЕсли СтрокаОплаты.ВидОплаты = Перечисления.ТипыОплат.Наличные И СтрокаОплаты.ЭтоВозврат Тогда
				ВидДокумента = "РасходныйКассовыйОрдер";
				
				// Выдача наличных требует поступления наличных по этой брони вначале
				ТипОснование = ТипПоступлениеНаличных;
				КолонкиРаспределения = "СуммаПлатежа,СуммаНДС,РасходыУСН,СуммаВзаиморасчетов";
			Иначе
				ВидДокумента = "ОплатаПлатежнойКартой";
				
				ДанныеЗаполнения.Вставить("ВидОплаты",     НастройкиИнтеграции.ВидОплатыКартой);
				ДанныеЗаполнения.Вставить("СуммаОперации", СтрокаОплаты.Сумма);
				КолонкиРаспределения = "СуммаПлатежа,СуммаНДС,СуммаВзаиморасчетов";
				Если СтрокаОплаты.ЭтоВозврат Тогда
					ТипОснование = ТипОплатаПлатежнойКартой;
				Иначе
					ТипОснование = ТипСчетНаОплатуПокупателю;
				КонецЕсли;
			КонецЕсли;
			
			ТипДокумента = Тип("ДокументСсылка."+ВидДокумента);
			
			ДокументПоНомеруБрони = Неопределено;
			Если ДокументыПоНомеруБрони[ТипДокумента] <> Неопределено Тогда
				Для каждого Документ Из ДокументыПоНомеруБрони[ТипДокумента] Цикл
					Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Дата") = СтрокаОплаты.ДатаОплаты Тогда
						
						ДокументПоНомеруБрони = Документ;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ДокументыПоНомеруБрони.Вставить(ТипДокумента, Новый Массив);
			КонецЕсли;
			
			Если ДокументПоНомеруБрони <> Неопределено Тогда
				ДокументОбъект = ДокументПоНомеруБрони.ПолучитьОбъект();
				ИмяГостя = ТаблицаДокументыПоНомерамБроней.Найти(ДокументПоНомеруБрони, "Документ").ИмяГостя;
			ИначеЕсли ДокументыПоНомеруБрони[ТипОснование] = Неопределено Тогда
				// Для ввода новой оплаты - нужно основание, если его нет - то такую оплату не загружаем
				
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация, , ,
						СтрШаблон(НСтр("ru = 'Не удалось загрузить оплату по брони %1. Не найдено основание оплаты для %2'"), НомерБрони, ТипДокумента));
				Продолжить;
			Иначе
				Основание = ДокументыПоНомеруБрони[ТипОснование][0];
				ИмяГостя  = ТаблицаДокументыПоНомерамБроней.Найти(Основание, "Документ").ИмяГостя;
				
				ДокументОбъект = Документы[ВидДокумента].СоздатьДокумент();
				ДокументОбъект.Дата        = СтрокаОплаты.ДатаОплаты;
				ДокументОбъект.Комментарий = СформироватьКомментарий(НомерБрони, ИмяГостя);
				
				Если ДанныеЗаполнения.Количество() > 0 Тогда
					ДанныеЗаполнения.Вставить("Основание", Основание);
					ДокументОбъект.Заполнить(ДанныеЗаполнения);
				Иначе
					ДокументОбъект.Заполнить(Основание);
				КонецЕсли;
			КонецЕсли;
			
			Если ДокументОбъект.СуммаДокумента <> СтрокаОплаты.Сумма 
				И ДокументОбъект.СуммаДокумента <> 0 Тогда
				
				РасшифровкаПлатежа = ДокументОбъект.РасшифровкаПлатежа.Выгрузить();
				
				Коэф = СтрокаОплаты.Сумма/ДокументОбъект.СуммаДокумента;
				Для каждого ИмяКолонки Из СтрРазделить(КолонкиРаспределения, ",") Цикл
					Если РасшифровкаПлатежа.Итог(ИмяКолонки) = 0  Тогда
						Продолжить;
					КонецЕсли;
					
					ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(РасшифровкаПлатежа.Итог(ИмяКолонки)* Коэф, РасшифровкаПлатежа, ИмяКолонки);
				КонецЦикла;
				
				ДокументОбъект.РасшифровкаПлатежа.Загрузить(РасшифровкаПлатежа);
				ДокументОбъект.СуммаДокумента = СтрокаОплаты.Сумма;
			КонецЕсли;
			
			РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ДокументОбъект);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
			ДокументОбъект.Записать(); 
			
			// Добавляем текущий документ, чтобы можно было его в этой же загрузке как основание  использовать
			ДокументыПоНомеруБрони[ТипДокумента].Добавить(ДокументОбъект.Ссылка);
			
			СтруктураДокумент = НовыйЗагруженныйДокумент();
			
			СтруктураДокумент.Вставить("НомерБрони",             НомерБрони);
			СтруктураДокумент.Вставить("ИмяГостя",               ИмяГостя);
			СтруктураДокумент.Вставить("Документ",               ДокументОбъект.Ссылка);
			СтруктураДокумент.Вставить("Сумма",                  ДокументОбъект.СуммаДокумента);
			
			СозданныеДокументы.Добавить(СтруктураДокумент);
		КонецЦикла; 
	КонецЦикла;
	
	Возврат СозданныеДокументы;
КонецФункции

// Определяет необходимость сопоставления данных документа, загруженного из Bnovo
Функция ДозаполнитьПоДаннымBNovo(ДокументСсылка, ИдентификаторФормы) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ТребуетсяДозаполнение",              Ложь);
	Результат.Вставить("ВсеДанныеСопоставленыАвтоматически", Ложь);
	Результат.Вставить("ДанныеСопоставленныеАвтоматически");
	Результат.Вставить("ХранилищеДанныхДляСопоставления");
	
	Если НЕ ДоступенОбменСBNovo() Тогда
		Возврат Результат;
	КонецЕсли;
	
	НесопоставленныеОбъекты = РегистрыСведений.НесопоставленныеОбъектыИнтернетМагазина.СоздатьНаборЗаписей();
	НесопоставленныеОбъекты.Отбор.Документ.Установить(ДокументСсылка);
	НесопоставленныеОбъекты.Прочитать();
	
	Если НесопоставленныеОбъекты.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	НесопоставленныеДанные = НесопоставленныеОбъекты[0].ДанныеОбъекта.Получить();
	Если НЕ ЗначениеЗаполнено(НесопоставленныеДанные) Тогда
		УдалитьДанныеДляСопоставления(ДокументСсылка);
		
		Возврат Результат;
	КонецЕсли;
	
	РанееСопоставленнаяНоменклатура = НоменклатураПоИдентификаторам(ОбщегоНазначения.ВыгрузитьКолонку(НесопоставленныеДанные, "Идентификатор", Истина));
	Если РанееСопоставленнаяНоменклатура.Количество() > 0 Тогда
		СопоставленнаяНоменклатура = Новый Соответствие;
		Для Каждого РанееСопоставленнаяНоменклатураСтрока Из РанееСопоставленнаяНоменклатура Цикл
			СопоставленнаяСтрока = НесопоставленныеДанные.Найти(РанееСопоставленнаяНоменклатураСтрока.Ключ, "Идентификатор");
			СопоставленнаяНоменклатура.Вставить(СопоставленнаяСтрока.НомерСтрокиЗаказа, РанееСопоставленнаяНоменклатураСтрока.Значение);
			
			НесопоставленныеДанные.Удалить(СопоставленнаяСтрока);
		КонецЦикла;
	КонецЕсли;
	
	ВсеДанныеСопоставленыАвтоматически = НЕ ЗначениеЗаполнено(НесопоставленныеДанные);
	
	Если ВсеДанныеСопоставленыАвтоматически Тогда
		УдалитьДанныеДляСопоставления(ДокументСсылка);
	КонецЕсли;
	
	Результат.ТребуетсяДозаполнение = Истина;
	Результат.ВсеДанныеСопоставленыАвтоматически = ВсеДанныеСопоставленыАвтоматически;
	Результат.ДанныеСопоставленныеАвтоматически  = Новый Структура("Номенклатура", СопоставленнаяНоменклатура);
	Результат.ХранилищеДанныхДляСопоставления    = ПоместитьВоВременноеХранилище(НесопоставленныеДанные, ИдентификаторФормы);
	
	Возврат Результат;
КонецФункции

Функция ДоступенОбменСBNovo() Экспорт
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиИнтеграцииBnovo);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Функция СформироватьКомментарий(НомерБрони, ИмяГостя = "")
	Шаблон = НСтр("ru = '%1 %2 - автоматически создано загрузкой из Bnovo'");
	Возврат СтрШаблон(Шаблон, НомерБрони, ИмяГостя);
КонецФункции 


Функция СоотвествиеСтавокНДС()
	СоотвествиеСтавокНДС = Новый Соответствие;
	
	СоотвествиеСтавокНДС.Вставить("0", Перечисления.ВидыСтавокНДС.БезНДС);
	СоотвествиеСтавокНДС.Вставить("1", Перечисления.ВидыСтавокНДС.Нулевая);
	СоотвествиеСтавокНДС.Вставить("2", Перечисления.ВидыСтавокНДС.Пониженная);
	СоотвествиеСтавокНДС.Вставить("3", Перечисления.ВидыСтавокНДС.Общая);
	СоотвествиеСтавокНДС.Вставить("8", Перечисления.ВидыСтавокНДС.ПониженнаяУСН);
	СоотвествиеСтавокНДС.Вставить("9", Перечисления.ВидыСтавокНДС.ОбщаяУСН);
	
	Возврат СоотвествиеСтавокНДС;
КонецФункции

Процедура ЗаписатьДанныеДокументов(Организация, Гостиница, ДатаЗагрузки, ЗагруженныеДокументы, ИзмененныеТипы)
	
	ДокументыПоТипам = ОбщегоНазначенияБП.РазложитьСписокПоТипамЗначенийОбъектов(
		ОбщегоНазначения.ВыгрузитьКолонку(ЗагруженныеДокументы, "Документ"));
		
	Для каждого КлючИЗначение Из ДокументыПоТипам Цикл
		ИзмененныеТипы.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	Для каждого СтруктураДокумента Из ЗагруженныеДокументы Цикл
		НоваяЗапись = РегистрыСведений.ДокументыBnovo.СоздатьМенеджерЗаписи();
		НоваяЗапись.Организация   = Организация;
		НоваяЗапись.Гостиница     = Гостиница;
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтруктураДокумента);
		
		НоваяЗапись.ДатаЗагрузки  = ДатаЗагрузки;
		
		НоваяЗапись.Записать();
	КонецЦикла;
КонецПроцедуры

Функция НовыйЗагруженныйДокумент() Экспорт
	СтруктураДокумент = Новый Структура;
	
	СтруктураДокумент.Вставить("НомерБрони");
	СтруктураДокумент.Вставить("ИмяГостя");
	СтруктураДокумент.Вставить("Документ");
	
	Возврат СтруктураДокумент;
КонецФункции

Функция ЗапросДокументыНомерамБроней(Организация, Гостиница, СписокБроней, ВидыДокументов) 
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Гостиница",    Гостиница);
	Запрос.УстановитьПараметр("СписокБроней", СписокБроней);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыBnovo.Документ КАК Документ,
	|	ДокументыBnovo.НомерБрони КАК НомерБрони,
	|	ДокументыBnovo.ИмяГостя КАК ИмяГостя
	|ИЗ
	|	РегистрСведений.ДокументыBnovo КАК ДокументыBnovo
	|ГДЕ
	|	ДокументыBnovo.НомерБрони В(&СписокБроней)
	|	И &УсловиеВидДокумента
	|	И ДокументыBnovo.Организация = &Организация
	|	И ДокументыBnovo.Гостиница = &Гостиница";
	
	СтрокаУсловия = Новый Массив;
	Для каждого ВидДокумента Из СтрРазделить(ВидыДокументов, ",") Цикл
		СтрокаУсловия.Добавить(СтрШаблон("ДокументыBnovo.Документ ССЫЛКА Документ.%1", СокрЛП(ВидДокумента)));
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеВидДокумента", СтрСоединить(СтрокаУсловия, " ИЛИ "));
	
	Возврат Запрос;
КонецФункции

Функция СоответствиеДокументовНомерамБроней(Организация, Гостиница, СписокБроней, ВидыДокументов) 
	Запрос = ЗапросДокументыНомерамБроней(Организация, Гостиница, СписокБроней, ВидыДокументов);
	Возврат ОбщегоНазначенияБП.РезультатЗапросаВСоответствие(Запрос.Выполнить(), "НомерБрони", "Документ");
КонецФункции

Функция ТаблицаДокументовНомерамБроней(Организация, Гостиница, СписокБроней, ВидыДокументов) 
	Запрос = ЗапросДокументыНомерамБроней(Организация, Гостиница, СписокБроней, ВидыДокументов);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ПолныйИдентификаторНоменклатуры(Идентификатор, ЭтоУслугаРазмещения) Экспорт
	Возврат ?(ЭтоУслугаРазмещения, "@", "")+ Идентификатор;
КонецФункции

Функция НайтиСоздатьДоговорКонтрагента(ДанныеЗаполнения)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ДанныеЗаполнения.Организация);
	Запрос.УстановитьПараметр("Владелец",  ДанныеЗаполнения.Владелец);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	НЕ ОсновныеДоговорыКонтрагента.Договор ЕСТЬ NULL КАК ОсновнойДоговор
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеДоговорыКонтрагента КАК ОсновныеДоговорыКонтрагента
	|		ПО ДоговорыКонтрагентов.Ссылка = ОсновныеДоговорыКонтрагента.Договор
	|ГДЕ
	|	НЕ ДоговорыКонтрагентов.ПометкаУдаления
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.Владелец = &Владелец
	|	И &строкаУсловия
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновнойДоговор УБЫВ";
	
	Условия = Новый Массив;
	
	Условия.Добавить("ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора");
	Запрос.УстановитьПараметр("ВидДоговора",  ДанныеЗаполнения.ВидДоговора);
	
	Если ДанныеЗаполнения.Свойство("Номер") Тогда
		Условия.Добавить("ДоговорыКонтрагентов.Номер = &Номер");
		Запрос.УстановитьПараметр("Номер", ДанныеЗаполнения.Номер);
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Дата") Тогда
		Условия.Добавить("ДоговорыКонтрагентов.Дата = &Дата");
		Запрос.УстановитьПараметр("Дата", ДанныеЗаполнения.Дата);
	КонецЕсли;
	
	Запрос.Текст = стрЗаменить(Запрос.Текст, "&строкаУсловия", СтрСоединить(Условия, " И "));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", ДанныеЗаполнения);
		Возврат РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
	ИначеЕсли Выборка.Количество() = 2 И НЕ Выборка.ОсновнойДоговор Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Выборка.Ссылка;
КонецФункции

Процедура УдалитьДанныеДляСопоставления(ДокументСсылка) Экспорт
	НесопоставленныеОбъекты =РегистрыСведений.НесопоставленныеОбъектыИнтернетМагазина.СоздатьНаборЗаписей();
	НесопоставленныеОбъекты.Отбор.Документ.Установить(ДокументСсылка);
	НесопоставленныеОбъекты.Записать();
КонецПроцедуры

// Сопоставляет переданные идентификаторы BNovo номенклатуре в базе
//
// Параметры:
//  СписокИдентификаторов - Массив - Массив строк идентификаторов
//
// Возвращаемое значение
//   ТаблицаЗначений - Список сопоставленной номенклатуры с идентифкаторами
//      * Ссылка        - СправочникСсылка.Номенклатура - Номенклатура, соотв. идентифкатору
//      * Идентификатор - Строка - Переданный идентификатор
//
Функция НоменклатураПоИдентификаторам(СписокИдентификаторов) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокИдентификаторов", СписокИдентификаторов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураBnovo.Номенклатура КАК Ссылка,
	|	НоменклатураBnovo.ИдентификаторНоменклатуры КАК Идентификатор
	|ИЗ
	|	РегистрСведений.НоменклатураBnovo КАК НоменклатураBnovo
	|ГДЕ
	|	НоменклатураBnovo.ИдентификаторНоменклатуры В (&СписокИдентификаторов)";
	
	Возврат ОбщегоНазначенияБП.РезультатЗапросаВСоответствие(Запрос.Выполнить(), "Идентификатор", "Ссылка");
КонецФункции

Функция НоменклатураПоНаименованию(ТаблицаИдентификаторы) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаИдентификаторы", ТаблицаИдентификаторы);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаИдентификаторы.Наименование КАК Наименование,
	|	ТаблицаИдентификаторы.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_Идентификаторы
	|ИЗ
	|	&ТаблицаИдентификаторы КАК ТаблицаИдентификаторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Ссылка,
	|	ВТ_Идентификаторы.Идентификатор КАК Идентификатор
	|ИЗ
	|	ВТ_Идентификаторы КАК ВТ_Идентификаторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО (ВТ_Идентификаторы.Наименование = СправочникНоменклатура.Наименование
	|				ИЛИ ВТ_Идентификаторы.Наименование = СправочникНоменклатура.НаименованиеПолное)
	|			И (СправочникНоменклатура.Услуга)
	|			И (НЕ СправочникНоменклатура.ЭтоГруппа)
	|			И (НЕ СправочникНоменклатура.ПометкаУдаления)
	|			И (НЕ СправочникНоменклатура.ВАрхиве)
	|ГДЕ
	|	НЕ СправочникНоменклатура.Ссылка ЕСТЬ NULL";
	
	Возврат ОбщегоНазначенияБП.РезультатЗапросаВСоответствие(Запрос.Выполнить(), "Идентификатор", "Ссылка");
КонецФункции

Функция ДатаЗагрузкиBNovo(ОтборОрганизация, ОтборГостиница = Неопределено) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиИнтеграцииBnovo.ДатаПоследнейЗагрузки КАК ДатаЗагрузки
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииBnovo КАК НастройкиИнтеграцииBnovo
	|ГДЕ
	|	&СтрокаУсловия
	|
	|УПОРЯДОЧИТЬ ПО
	|	НастройкиИнтеграцииBnovo.ДатаПоследнейЗагрузки";
	
	Условия = Новый Массив;
	
	Запрос.УстановитьПараметр("СписокОрганизаций", ОбщегоНазначенияБП.СписокДоступныхОрганизаций(ОтборОрганизация));
	Условия.Добавить("НастройкиИнтеграцииBnovo.Организация В (&СписокОрганизаций)");
	
	Если ЗначениеЗаполнено(ОтборГостиница) Тогда
		Запрос.УстановитьПараметр("Гостиница", ОтборГостиница);
		Условия.Добавить("НастройкиИнтеграцииBnovo.Гостиница = &Гостиница");
	КонецЕсли; 
	
	строкаУсловия = "ИСТИНА";
	Если Условия.Количество() > 0 Тогда
		строкаУсловия = СтрСоединить(Условия, " И ");
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтрокаУсловия", строкаУсловия);
	
	ДатаЗагрузки = ТекущаяДатаСеанса();
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатаЗагрузки = Выборка.ДатаЗагрузки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат ДатаЗагрузки;
КонецФункции 

Функция НовыйТаблицаНоменклатуры() Экспорт
	ТаблицаНоменклатуры = Новый ТаблицаЗначений;
	
	ТаблицаНоменклатуры.Колонки.Добавить("Номенклатура",        Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаНоменклатуры.Колонки.Добавить("НомерСтрокиЗаказа",   ОбщегоНазначения.ОписаниеТипаЧисло(15));
	ТаблицаНоменклатуры.Колонки.Добавить("Идентификатор",       ОбщегоНазначения.ОписаниеТипаСтрока(80));
	ТаблицаНоменклатуры.Колонки.Добавить("ЭтоУслугаРазмещения", Новый ОписаниеТипов("Булево"));
	ТаблицаНоменклатуры.Колонки.Добавить("Наименование",        ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаНоменклатуры.Колонки.Добавить("Единица",             ОбщегоНазначения.ОписаниеТипаСтрока(200));
	
	Возврат ТаблицаНоменклатуры;
КонецФункции

Функция ЗаголовокПриложения()
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		ЗаголовокПриложения = Константы.ЗаголовокСистемы.Получить();
		Если ПустаяСтрока(ЗаголовокПриложения) Тогда
			ЗаголовокПриложения = Метаданные.КраткаяИнформация;
		КонецЕсли;
	Иначе
		ЗаголовокПриложения = РаботаВМоделиСервиса.ПолучитьИмяПриложения();
	КонецЕсли;  
	
	Возврат ЗаголовокПриложения;
КонецФункции


#Область РаботаССервисомИнтеграции

#Область МетодыСервиса

Функция ПодключитьСервисИнтеграции(Знач ПараметрыМетода) Экспорт 
	РезультатМетода = Новый Структура;
	РезультатМетода.Вставить("Результат", Ложь);
	РезультатМетода.Вставить("СообщениеОбОшибке", "");
	
	ЛогинПользователяИТС = ЛогинПользователяИТС();

	// Соединение уникально для каждого пользователя ИТС. К одному соединению могут быть приваязаны несколько подключений
	// перед работой с подключениями надо получить client_id, который передавать в запросы вместе с данными подключения
	ПараметрыУстановитьСоединение = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыУстановитьСоединение();
	Результат = УстановитьСоединение(ПараметрыУстановитьСоединение);
	Если Результат.КодСостояния <> 200 Тогда
		РезультатМетода.Вставить("СообщениеОбОшибке", СтрСоединить(Результат.Ошибки, Символы.ПС));
		Возврат РезультатМетода;
	КонецЕсли;
	
	ИдентификаторИнформационнойБазы = Константы.ИдентификаторИнформационнойБазы.Получить();
	
	// подключение уникально для каждой ИБ, его данные записываются в безопасное хранилище с привязкой к логину ИТС
	// все работа с СИ в последующем ведется от имени подключения из безопасного хранилища
	ПараметрыПолучитьПодключения = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыПолучитьПодключения();
	Результат = ПолучитьПодключения(ПараметрыПолучитьПодключения);
	Если Результат.КодСостояния <> 200 Тогда
		РезультатМетода.Вставить("СообщениеОбОшибке", СтрСоединить(Результат.Ошибки, Символы.ПС));
		Возврат РезультатМетода;
	КонецЕсли;
	
	ПараметрыПодключения = Неопределено;
	Для каждого КлючИЗначение Из Результат.Ответ Цикл
		// среди существующих подключений ищем то что было создано из этой ИБ
		Если КлючИЗначение.Значение.ИдентификаторИнформационнойБазы = ИдентификаторИнформационнойБазы Тогда
			ПараметрыПодключения = КлючИЗначение.Значение; 
			// client_id - идентификатор соединения, так как при предыдущем подключении он мог быть другим
			ЗаполнитьЗначенияСвойств(ПараметрыПодключения, ПараметрыУстановитьСоединение, "client_id");
			
			// эта база уже подключалась к СИ (но возможно с другим логином ИПП), 
			// запишем его в безопасное хранилище для последующих запросов
			ЗаписатьДанныеВБезопасноеХранилище(КлючИЗначение.Значение, ЛогинПользователяИТС);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыПодключения = Неопределено Тогда
		// эта база еше не разу не подключалась к СИ
		ПараметрыНовоеПодключение = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыУстановитьПодключение();
		ПараметрыНовоеПодключение.Логин = ЛогинПользователяИТС;
		ПараметрыНовоеПодключение.Представление = ЗаголовокПриложения();
		ПараметрыНовоеПодключение.КлючПодключения = Строка(Новый УникальныйИдентификатор);
		
		// подключение создаем под ранее установленное соединение, для этого переносим ранее полученный client_id
		ЗаполнитьЗначенияСвойств(ПараметрыНовоеПодключение, ПараметрыУстановитьСоединение, "client_id");
		
		Результат = УстановитьПодключение(ПараметрыНовоеПодключение);
		Если Результат.КодСостояния <> 200 Тогда
			РезультатМетода.Вставить("СообщениеОбОшибке", СтрСоединить(Результат.Ошибки, Символы.ПС));
			Возврат РезультатМетода;
		КонецЕсли;
	КонецЕсли;
	
	РезультатМетода.Вставить("Результат", Истина);
	Возврат РезультатМетода;
КонецФункции

Функция СоздатьОбновитьПодключение(Знач ПараметрыМетода) Экспорт
	РезультатМетода = Новый Структура;
	РезультатМетода.Вставить("Результат", Ложь);
	РезультатМетода.Вставить("СообщениеОбОшибке", "");
	
	ПараметрыСоздатьОбновитьУчетнуюЗапись                     = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыЗапросBNovo();
	ПараметрыСоздатьОбновитьУчетнуюЗапись.ApiKey              = ПараметрыМетода.ApiKey;
	ПараметрыСоздатьОбновитьУчетнуюЗапись.ДатаНачалаЗагрузки  = ПараметрыМетода.ДатаНачалаЗагрузки;
	ПараметрыСоздатьОбновитьУчетнуюЗапись.УчетнаяЗапись       = ПараметрыМетода.УчетнаяЗапись;
	
	Результат = ЗапросСоздатьОбновитьУчетнуюЗапись(ПараметрыСоздатьОбновитьУчетнуюЗапись);
	Если Результат.КодСостояния <> 200 Тогда
		РезультатМетода.Вставить("СообщениеОбОшибке", СтрСоединить(Результат.Ошибки, Символы.ПС));
		Возврат РезультатМетода;
	КонецЕсли;
	
	РезультатМетода.Вставить("Результат", Истина);
	Возврат РезультатМетода;
КонецФункции

// Установить соединение.
// 
// Параметры:
//  ПараметрыМетода - См. НовыеПараметрыУстановитьСоединение
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция УстановитьСоединение(ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	// Запрос для получения client_id и client_secret
	Результат = ЗапросУстановитьСоединение(ПараметрыМетода);
	
	// Данные подключения к СИ для записи в защищенное хранилище
	НовыеДанныеИнтеграции = Новый Структура; 
	
	// идентификатор конкретного токена
	НовыеДанныеИнтеграции.Вставить("id_token",             "");
	
	// идентификаторы клиента по логину ИПП, 
	// маскирует логин ИПП при работе с сервисом
	НовыеДанныеИнтеграции.Вставить("client_id",            "");
	НовыеДанныеИнтеграции.Вставить("client_secret",        "");
	НовыеДанныеИнтеграции.Вставить("ДатаОбновленияТокена", Дата(1, 1, 1));
	
	// логин ИПП
	НовыеДанныеИнтеграции.Вставить("Логин",                ПараметрыМетода.Логин);
	
	Если Результат.КодСостояния = 200 Тогда
		ЗаполнитьЗначенияСвойств(НовыеДанныеИнтеграции, Результат.Ответ);
	КонецЕсли;
	
	ПараметрыМетода.client_id = НовыеДанныеИнтеграции.client_id;
	
	ЗаписатьДанныеВБезопасноеХранилище(НовыеДанныеИнтеграции, ПараметрыМетода.Логин);
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
КонецФункции

// Обновление токена доступа.
// 
// Параметры:
//  ДанныеИнтеграции - См. ИнтеграцияBNovoСИПовтИсп.НовыеДанныеИнтеграции
//  Ошибки - Массив из Строка - Ошибки
// 
// Возвращаемое значение:
//  См. НовыеПараметрыОбновитьТокенДоступа
Функция ОбновитьТокенДоступа(ДанныеИнтеграции, Ошибки) Экспорт
	
	ПараметрыМетода = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыОбновитьТокенДоступа();
	ЗаполнитьЗначенияСвойств(ПараметрыМетода, ДанныеИнтеграции);
	
	Результат = ЗапросОбновитьТокенДоступа(ПараметрыМетода);
	
	Если Результат.КодСостояния = 200 Тогда
		ПараметрыМетода.id_token = СтрШаблон("Bearer %1", Результат.Ответ.id_token);
	Иначе
		ПараметрыМетода.id_token = "";
		Ошибки.Добавить(НСтр("ru = 'Ошибка подключения к сервису интеграции, выполните подключение повторно.'"));
	КонецЕсли;
	ПараметрыМетода.ДатаОбновленияТокена = ТекущаяДатаСеанса();
	ДанныеИнтеграции = ЗаписатьДанныеВБезопасноеХранилище(ПараметрыМетода, ДанныеИнтеграции.Логин);
	
	Возврат ПараметрыМетода;
	
КонецФункции

// Получить идентификатор ИТС.
// 
// Параметры:
//  ДанныеИнтеграции - См. ИнтеграцияBNovoСИПовтИсп.НовыеДанныеИнтеграции
// 
// Возвращаемое значение:
//  Строка - Идентификатор ИТС
Функция ПолучитьИдентификаторИТС(ДанныеИнтеграции = Неопределено) Экспорт
	
	ИдентификаторИТС = "";
	
	Если ДанныеИнтеграции = Неопределено Тогда
		ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища();
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДанныеИнтеграции.ИдентификаторИТС) Тогда
		Возврат ДанныеИнтеграции.ИдентификаторИТС;
	КонецЕсли;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("Тикет", ТикетАутентификацииНаПорталеПоддержки());
	ПараметрыМетода.Вставить("ИмяМетода", ИнтеграцияBnovoСИКлиентСервер.МетодПолучитьДанныеПользователяИТС());
	Результат = ЗапросПолучитьДанныеПользователяИТС(ПараметрыМетода);
	
	Если Результат.КодСостояния = 200 Тогда
		
		ИдентификаторИТС = ПолучитьЗначение(Результат.Ответ, "subscriberUeid", "", "");
		ИдентификаторИТС = ?(ПустаяСтрока(ИдентификаторИТС), ПолучитьЗначение(Результат.Ответ, "uin", , ""), ИдентификаторИТС);
		
		Если Не ПустаяСтрока(ИдентификаторИТС) Тогда
			ЗаписатьДанныеВБезопасноеХранилище(Новый Структура("ИдентификаторИТС", ИдентификаторИТС), ЛогинПользователяИТС());
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИдентификаторИТС;
	
КонецФункции

Функция ПолучитьПодключения(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт 
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросПолучитьПодключения(ПараметрыМетода);
	
	Ответ = Новый Соответствие;
	
	Если Результат.КодСостояния = 200 Тогда
		
		Подключения = ПолучитьЗначение(Результат, "Ответ", "Массив", Новый Массив); // Массив из Соответствие
		Для Каждого Элемент Из Подключения Цикл
			ПараметрыПодключения = ИнтеграцияBnovoСИКлиентСервер.НовыеПараметрыПодключения();
			ПараметрыПодключения.client_id = Элемент["clientId"];
			ПараметрыПодключения.КлючПодключения = Элемент["keyConnect"];
			ПараметрыПодключения.ИдентификаторИнформационнойБазы = Элемент["baseId"];
			ПараметрыПодключения.Представление = Элемент["name"];
			ПараметрыПодключения.ДатаПодключенияСИ = СтрокаВДату(Элемент["date"]);
			Ответ.Вставить(ПараметрыПодключения.КлючПодключения, ПараметрыПодключения);
		КонецЦикла;
		
	КонецЕсли;
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьПодключение(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросУстановитьПодключение(ПараметрыМетода);
	
	Ответ = Новый Структура("ДатаПодключенияСИ, КлючПодключения", Дата(1, 1, 1), "");
	
	Если Результат.КодСостояния = 200 Тогда
		Ответ.ДатаПодключенияСИ = СтрокаВДату(Результат.Ответ["date"]);
		Ответ.КлючПодключения = Результат.Ответ["keyConnect"];
		ЗаписатьДанныеВБезопасноеХранилище(Ответ, ПараметрыМетода.Логин);
	КонецЕсли;
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Удалить подключение.
// 
// Параметры:
//  ПараметрыМетода - См. НовыеПараметрыУдалитьПодключение
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция УдалитьПодключение(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросУдалитьПодключение(ПараметрыМетода);
	
	Ответ = Новый Структура;
	Ответ.Вставить("КлючПодключения", ПараметрыМетода.КлючПодключения);
	Ответ.Вставить("ЭтоТекущийКлючПодключения", ПараметрыМетода.ЭтоТекущийКлючПодключения);
	
	Если Результат.КодСостояния = 200 Тогда
		Если ПараметрыМетода.ЭтоТекущийКлючПодключения Тогда
			ЗаписатьДанныеВБезопасноеХранилище(Новый Структура("КлючПодключения, ДатаПодключенияСИ", "", Дата(1, 1, 1)), ПараметрыМетода.Логин);
		КонецЕсли;
	КонецЕсли;
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


Процедура ПередВыполнениемМетодаСервиса(ПараметрыМетода) Экспорт
	Возврат;
КонецПроцедуры

// Новые параметры запроса.
// 
// Параметры:
//  ПараметрыМетода - Структура - Параметры метода:
// * Метод - Строка - Имя процедуры 1С, которая отвечает за вызов метода запроса и обработку полученного результата
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// 
// Возвращаемое значение:
//  Структура - Параметры запроса:
// * Сервис - Строка - имя сервиса. "Доставка", "Маркетплейсы" и т.д.
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - раздел сервиса.
// * Сервер - Строка - адрес сервера (хоста) с которым осуществляется соединение. Например "1C.ru"
// * Приложение - Строка - Имя публикации сервиса. Например, "applications/IntegrationService"
// * КорневойURL - Строка - Путь к API сервиса
// * Метод - Строка - метод запроса. "GET", "POST" и т.п.
// * АдресМетода - Строка - Часть адрес метода запроса, включая параметры запроса. Например, "ping?p=1"
// * ПолныйАдресМетода - Строка - Полный адрес метода запроса, включая параметры запроса. Например, "applications/IntegrationService/api/ping?p=1"
// * Заголовки - Соответствие из строка - заголовки запроса
// * Авторизация - Булево - Признак необходимости получения токена доступа для доступа в сервис интеграции
// * ОбращениеКСервисуИнтеграции - Булево - Признак обращения к сервису интеграции
// * Данные - Строка, ДвоичныеДанные, Произвольный - Сначала данные для формирования тела запроса. А потом тело запроса
// * ДанныеИнтеграции - См. ИнтеграцияBNovoСИПовтИсп.НовыеДанныеИнтеграции
// * ТипКонтента - Строка - Тип передаваемых данных. "application/json", "application/x-www-form-urlencoded"
// * Таймаут - Число - время ожидания ответа запроса
// * ИмяМетода - Строка - Имя метода 1С, который отвечает за вызов HTTP запроса и обработку полученного результата
// * ИдентификаторИТС - Строка - Идентификатор пользователя ИТС
// * УчетнаяЗапись - Строка - Идентификатор учетной записи (организации)
// * ВыводитьСообщения - Булево - признак вывода на экран сообщений, возникающих в ходе выполнения запроса
// * ПрочитатьВСоответствие - Булево - признак чтения тела ответа в соответствие или в структуру. Чтение в структуру не рекомендуется из-за ограничения на имена ключей
// * ИдентификаторНазначения - Неопределено, УникальныйИдентификатор - Уникальный идентификатор формы, из которой произведен вызов
Функция НовыеПараметрыЗапроса(ПараметрыМетода) Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	
	// Основные параметры запроса
	ПараметрыЗапроса.Вставить("Сервер", "");
	ПараметрыЗапроса.Вставить("Приложение", "");
	ПараметрыЗапроса.Вставить("КорневойURL", "api/redirect/v1/%1/api/content");
	ПараметрыЗапроса.Вставить("Метод", "");
	ПараметрыЗапроса.Вставить("АдресМетода", "");
	ПараметрыЗапроса.Вставить("ПолныйАдресМетода", "");
	ПараметрыЗапроса.Вставить("Заголовки", Новый Соответствие);
	ПараметрыЗапроса.Вставить("Авторизация", Истина);
	ПараметрыЗапроса.Вставить("ОбращениеКСервисуИнтеграции", Истина);
	ПараметрыЗапроса.Вставить("Данные", "");
	ПараметрыЗапроса.Вставить("ТипКонтента", "application/json");
	ПараметрыЗапроса.Вставить("Таймаут", 20);
	
	// Служебные параметры, участвующие в формировании параметров запроса и в обработке результата ответа
	ПараметрыЗапроса.Вставить("Сервис", ИнтеграцияBnovoСИКлиентСервер.ИмяСервиса());
	ПараметрыЗапроса.Вставить("ИмяМетода", "");
	ПараметрыЗапроса.Вставить("ДанныеИнтеграции", ИнтеграцияBnovoСИПовтИсп.НовыеДанныеИнтеграции());
	ПараметрыЗапроса.Вставить("ИдентификаторИТС", "");
	ПараметрыЗапроса.Вставить("УчетнаяЗапись", "");
	ПараметрыЗапроса.Вставить("ВыводитьСообщения", Истина);
	ПараметрыЗапроса.Вставить("ПрочитатьВСоответствие", Истина);
	ПараметрыЗапроса.Вставить("ИдентификаторНазначения", Неопределено);
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ПараметрыМетода);
	ДополнитьПараметрыЗапроса(ПараметрыЗапроса, ПараметрыМетода);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Выполнить запрос к сервису.
// 
// Параметры:
// 	ПараметрыЗапроса - См. НовыеПараметрыЗапроса
// 	ПараметрыМетода - Структура - Параметры метода. Состав:
// 	 * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// 	Кэш - Неопределено, Структура - Кэшированные значения
// 	ПовторныйЗапрос - Булево - Признак ошибки обращения к основному серверу
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода, Кэш = Неопределено, ПовторныйЗапрос = Ложь) Экспорт
	
	РезультатЗапроса = НовыйРезультатМетодаСервиса();
	
	Если Не ПовторныйЗапрос Тогда
		ПараметрыЗапроса.ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища();
	КонецЕсли;
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	УстановитьПолныйАдресМетода(ПараметрыЗапроса, ПовторныйЗапрос, РезультатЗапроса.Ошибки);
	Если РезультатЗапроса.Ошибки.Количество() > 0 Тогда
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	ЗапросКСервису = Новый HTTPЗапрос(ПараметрыЗапроса.ПолныйАдресМетода); 
	
	Если ОбщегоНазначения.РежимОтладки() Тогда
		ЗаписьЖурналаРегистрации(ИнтеграцияBnovo, УровеньЖурналаРегистрации.Информация,,,
			СтрШаблон("Запрос %1 по адресу %2", ПараметрыЗапроса.Метод, ПараметрыЗапроса.ПолныйАдресМетода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Данные) Тогда
		Если ТипЗнч(ПараметрыЗапроса.Данные) = Тип("ДвоичныеДанные") Тогда
			ЗапросКСервису.УстановитьТелоИзДвоичныхДанных(ПараметрыЗапроса.Данные);
		ИначеЕсли ТипЗнч(ПараметрыЗапроса.Данные) = Тип("Строка") Тогда
			ЗапросКСервису.УстановитьТелоИзСтроки(ПараметрыЗапроса.Данные);
		Иначе
			Данные = ДанныеВJSON(ПараметрыЗапроса.Данные);
			ЗапросКСервису.УстановитьТелоИзСтроки(Данные);
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗапроса.Метод = "GET" Тогда
		ЗапросКСервису.Заголовки.Вставить("Accept", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "POST" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "PUT" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "PATCH" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "DELETE" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
		ДвоичныеДанные = ЗапросКСервису.ПолучитьТелоКакДвоичныеДанные();
		Если ДвоичныеДанные <> Неопределено Тогда
			ЗапросКСервису.Заголовки.Вставить("Content-Length", Формат(ДвоичныеДанные.Размер(), "ЧГ=0"));
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗапроса.Авторизация Тогда
		ЗапросКСервису.Заголовки.Вставить("Authorization", УстановитьТокенДоступа(ПараметрыЗапроса.ДанныеИнтеграции, РезультатЗапроса.Ошибки));
		ЗапросКСервису.Заголовки.Вставить("KeyConnect", ПараметрыЗапроса.ДанныеИнтеграции.КлючПодключения);
		ЗапросКСервису.Заголовки.Вставить("AccountID", XMLСтрока(ПараметрыЗапроса.УчетнаяЗапись));
		ЗапросКСервису.Заголовки.Вставить("Idempotency-Key", Новый УникальныйИдентификатор);
	КонецЕсли;
	
	ЗапросКСервису.Заголовки.Вставить("BaseID", ИдентификаторИнформационнойБазы());
	
	Для Каждого ПараметрыЗаголовка Из ПараметрыЗапроса.Заголовки Цикл
		ЗапросКСервису.Заголовки.Вставить(ПараметрыЗаголовка.Ключ, ПараметрыЗаголовка.Значение);
	КонецЦикла;
	
	Соединение = УстановитьСоединениеССервером(ПараметрыЗапроса, Прокси, РезультатЗапроса);
	Если Соединение = Неопределено Тогда
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	Попытка
		
		Ответ = ВызватьHTTPМетод(Соединение, ЗапросКСервису, ПараметрыЗапроса);
		Если Ответ = Неопределено Тогда // Таймаут
			Данные = Неопределено;
		Иначе
			Данные = Ответ.ПолучитьТелоКакСтроку();
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = НСтр("ru = 'Ошибка выполнения запроса к сервису.'");
		
		РезультатЗапроса.КодСостояния = 500;
		РезультатЗапроса.Ошибки.Добавить(ТекстОшибки);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		Возврат РезультатЗапроса;
		
	КонецПопытки;
	
	ОбработатьОтветСервиса(РезультатЗапроса, ПараметрыЗапроса, ПараметрыМетода, Кэш, ЗапросКСервису, Ответ, Данные);
	
	Если ОбщегоНазначения.РежимОтладки() Тогда
		ЗаписьЖурналаРегистрации(ИнтеграцияBnovo, УровеньЖурналаРегистрации.Информация,,,
			СтрШаблон("Получен ответ с кодом %1", РезультатЗапроса.КодСостояния));
	КонецЕсли;
	
	Возврат РезультатЗапроса;
КонецФункции

Функция СобытиеЖурналаРегистрации()
	Возврат "ИнтеграцияBnovo";
КонецФункции


// После выполнения метода сервиса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыеСлужебныеПараметрыЗапроса
//  Результат - См. ИнтеграцияСМаркетплейсамиСИ.НовыйРезультатМетодаСервиса
//  АдресРезультата - Строка - Адрес результата в который необходимо упаковать результат
Процедура ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата) Экспорт
	
	Ошибки = Результат.Ошибки; // Массив из Строка
	
	СообщенияПользователю = ПолучитьСообщенияПользователю(Не ПараметрыМетода.ВыводитьСообщения);
	Для Каждого Сообщение Из СообщенияПользователю Цикл
		Ошибки.Добавить(Сообщение.Текст);
	КонецЦикла;
	
	Если Ошибки.Количество() = 0
		И Результат.КодСостояния <> 0 // Запрос не выполнялся
		И Результат.КодСостояния <> 200 Тогда
		Ошибки.Добавить(ИнтеграцияBnovoСИКлиентСервер.ВнутренняяОшибкаСервиса());
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры 

// Прочитать данные из безопасного хранилища.
// 
// Параметры:
//  Ключ - Строка - Ключ
// 
// Возвращаемое значение:
//  См. ИнтеграцияBNovoСИПовтИсп.НовыеДанныеИнтеграции
Функция ПрочитатьДанныеИзБезопасногоХранилища(Ключ = "") Экспорт
	
	НовыеДанныеИнтеграции = ИнтеграцияBnovoСИПовтИсп.НовыеДанныеИнтеграции();
	
	Владелец = СтрШаблон("BNovo.СервисИнтеграции.%1", ?(ПустаяСтрока(Ключ), ЛогинПользователяИТС(), Ключ));
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеИнтеграции = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТипЗнч(ДанныеИнтеграции) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(НовыеДанныеИнтеграции, ДанныеИнтеграции);
	КонецЕсли;
	
	Возврат НовыеДанныеИнтеграции;
	
КонецФункции

// Записать данные в безопасное хранилище.
// 
// Параметры:
//  Источник - Неопределено, ФормаКлиентскогоПриложения, Структура - Источник:
// * id_token - Строка - 
// * client_id - Строка - 
// * client_secret - Строка - 
// * ДатаОбновленияТокена - Дата - 
// * Логин - Строка - 
//  Ключ - Строка - Ключ
// 
// Возвращаемое значение:
//  См. ИнтеграцияBNovoСИПовтИсп.НовыеДанныеИнтеграции
Функция ЗаписатьДанныеВБезопасноеХранилище(Источник = Неопределено, Ключ = "") Экспорт
	
	ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища(Ключ);
	
	Если ТипЗнч(Источник) = Тип("ФормаКлиентскогоПриложения") Или ЗначениеЗаполнено(Источник) Тогда
		ЗаполнитьЗначенияСвойств(ДанныеИнтеграции, Источник);
	КонецЕсли;
	
	Владелец = СтрШаблон("BNovo.СервисИнтеграции.%1", ?(ПустаяСтрока(Ключ), ЛогинПользователяИТС(), Ключ));
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, ДанныеИнтеграции);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеИнтеграции;
	
КонецФункции

// Идентификатор информационной базы.
// 
// Возвращаемое значение:
//  Строка - Идентификатор информационной базы
Функция ИдентификаторИнформационнойБазы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ИдентификаторИнформационнойБазы = Константы.ИдентификаторИнформационнойБазы.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИдентификаторИнформационнойБазы;
	
КонецФункции

// Данные аутентификации ИТС.
// 
// Возвращаемое значение:
//  См. ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки
Функция ДанныеАутентификацииИТС() Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		УстановитьПривилегированныйРежим(Истина);
		ДанныеАутентификацииИТС = МодульИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.'");
	КонецЕсли;
	
	Возврат ДанныеАутентификацииИТС;
	
КонецФункции

// Логин пользователя ИТС.
// 
// Возвращаемое значение:
//  Строка - Логин пользователя ИТС
Функция ЛогинПользователяИТС() Экспорт
	
	Результат = "";
	
	ДанныеАутентификацииИТС = ДанныеАутентификацииИТС();
	
	Если ТипЗнч(ДанныеАутентификацииИТС) = Тип("Структура") Тогда
		Результат = ДанныеАутентификацииИТС.Логин;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Хранилище значения из JSON.
// 
// Параметры:
//  Значение - Строка - данные в формате json
// 
// Возвращаемое значение:
//  Строка, Произвольный - Хранилище значения из JSON
Функция ХранилищеЗначенияИзJSON(Знач Значение)
	
	Результат = "";
	Значение = СокрЛП(Значение);
	
	Если Значение <> "" Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Значение);
		Попытка
			ЗначениеИзJSON = СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON);
		Исключение
			Возврат Результат;
		КонецПопытки;
		
		ЧтениеJSON.Закрыть();
	
		Если ЗначениеЗаполнено(ЗначениеИзJSON) И ТипЗнч(ЗначениеИзJSON) = Тип("ХранилищеЗначения") Тогда
			Возврат ЗначениеИзJSON.Получить();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция ДанныеВJSON(Данные) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ПроверятьСтруктуру = Ложь;
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, , , ЭкранированиеСимволовJSON.СимволыВнеASCII);
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные, , "ПреобразованиеТиповЗначений", ИнтеграцияBnovo);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

// Преобразование типов значений, не поддерживаемых JSON
// 
// Параметры:
//  Свойство - Строка - Свойство
//  Значение - Произвольный - Значение свойства
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры
//  Отказ - Булево - Отказ
// 
// Возвращаемое значение:
//  Строка - Преобразование типов значений
Функция ПреобразованиеТиповЗначений(Знач Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Неопределено") Тогда
		
		Возврат "";
		
	ИначеЕсли ТипЗнч(Значение) = Тип("УникальныйИдентификатор") Тогда
		
		Возврат Строка(Значение);
		
	ИначеЕсли ТипЗнч(Значение) = Тип("ХранилищеЗначения") Тогда
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, Значение, НазначениеТипаXML.Явное);
		
		Возврат ЗаписьJSON.Закрыть();
		
	КонецЕсли;
	
КонецФункции


#КонецОбласти 

#Область РаботаССервисомСлужебныеМетоды

#Область ОбщиеМетодыДляРаботыССервисом

Процедура ДополнитьПараметрыЗапроса(ПараметрыЗапроса, ПараметрыМетода)
	
	Если ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодОбновитьТокенДоступа() Тогда
		
		ПараметрыЗапроса.КорневойURL = "";
		ПараметрыЗапроса.АдресМетода = "sys/token/";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.Авторизация = Ложь;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ТипКонтента = "application/x-www-form-urlencoded";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
		ДанныеАвторизации = СтрЗаменить(Base64Строка(ПолучитьДвоичныеДанныеИзСтроки(СтрШаблон("%1:%2",
			ПараметрыМетода.client_id, ПараметрыМетода.client_secret))), Символы.ВК + Символы.ПС, "");
		ПараметрыЗапроса.Заголовки.Вставить("Authorization", СтрШаблон("Basic %1", ДанныеАвторизации));
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодПолучитьПодключения() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/connect";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодУстановитьСоединение() Тогда
		
		ПараметрыЗапроса.КорневойURL = "api/direct/v1/%1";
		ПараметрыЗапроса.АдресМетода = "auth";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.Авторизация = Ложь;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.Таймаут = 60;
		
		ПараметрыЗапроса.Заголовки.Вставить("Ticket", ПараметрыМетода.Тикет);
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодУстановитьПодключение() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/connect";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодУдалитьПодключение() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/connect";
		ПараметрыЗапроса.Метод = "DELETE";
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "keyConnect", ПараметрыМетода.КлючПодключения);
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодПолучитьДанныеПользователяИТС() Тогда
		
		ПараметрыЗапроса.Сервер = "login.1c.ru";
		ПараметрыЗапроса.Приложение = "";
		ПараметрыЗапроса.КорневойURL = "rest/public";
		ПараметрыЗапроса.АдресМетода = "ticket/check";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.Авторизация = Ложь;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.ОбращениеКСервисуИнтеграции = Ложь;    
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодСоздатьОбновитьУчетнуюЗапись() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/auth";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.Авторизация = Истина;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.Таймаут = 60; 
		
		
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодПолучитьБронирования() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/create";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.Авторизация = Истина;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.Таймаут = 60; 
		
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "date_from", Формат(ПараметрыМетода.ДатаНачала, "ДФ=yyyy-MM-dd"));
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "date_to",   Формат(ПараметрыМетода.ДатаОкончания, "ДФ=yyyy-MM-dd"));
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "new",       ПараметрыМетода.Новые);
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодПолучитьПроживания() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/departure";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.Авторизация = Истина;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.Таймаут = 60; 
		
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "date_from", Формат(ПараметрыМетода.ДатаНачала, "ДФ=yyyy-MM-dd"));
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "date_to",   Формат(ПараметрыМетода.ДатаОкончания, "ДФ=yyyy-MM-dd"));
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "new",       ПараметрыМетода.Новые);
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияBnovoСИКлиентСервер.МетодПолучитьПлатежи() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/payments";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.Авторизация = Истина;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.Таймаут = 60; 
		
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "date_from", Формат(ПараметрыМетода.ДатаНачала, "ДФ=yyyy-MM-dd"));
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "date_to",   Формат(ПараметрыМетода.ДатаОкончания, "ДФ=yyyy-MM-dd"));
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "new",       ПараметрыМетода.Новые);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПараметрАдресаHTML(Адрес, Имя, Знач Значение) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Значение = Формат(Значение, "ЧГ=");
	ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
		Значение = Формат(Значение, "БЛ=false; БИ=true");
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
		Значение = Формат(Значение, "ДФ=yyyy-MM-dd");
	КонецЕсли;
	
	Адрес = Адрес + ?(СтрНайти(Адрес, "?") = 0, "?", "&") + Имя + "=" + Значение;
	
КонецПроцедуры

// Установить соединение с сервером.
// 
// Параметры:
//  ПараметрыЗапроса - Структура - Параметры запроса:
// * Сервис - Строка - 
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - 
// * Сервер - Строка - 
// * Приложение - Строка - 
// * КорневойURL - Строка - 
// * Метод - Строка - 
// * АдресМетода - Строка - 
// * Заголовки - Соответствие из Строка - 
// * Авторизация - Булево - 
// * Данные - Строка, ДвоичныеДанные, Произвольный - 
// * ТипКонтента - Строка - 
// * Таймаут - Число - 
// * ИмяМетода - Строка - 
// * ИдентификаторИТС - Строка - 
// * УчетнаяЗапись - Строка - 
// * ВыводитьСообщения - Булево - 
// * ПрочитатьВСоответствие - Булево - 
// * ИдентификаторНазначения - Неопределено, УникальныйИдентификатор - 
//  Прокси - Неопределено, ИнтернетПрокси - Прокси
//  РезультатЗапроса - См. НовыйРезультатМетодаСервиса
// 
// Возвращаемое значение:
//  Структура - HTTPСоединение, Неопределено - Установить соединение с сервером:
Функция УстановитьСоединениеССервером(ПараметрыЗапроса, Прокси, РезультатЗапроса)
	
	Попытка
		Возврат Новый HTTPСоединение(ПараметрыЗапроса.Сервер, 443, , , Прокси, ПараметрыЗапроса.Таймаут,
			ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение());
	Исключение
		ТекстОшибки = НСтр("ru = 'Отсутствует соединение с сервисом интеграции. Подробности см. в журнале регистрации.'");
		РезультатЗапроса.КодСостояния = 404;
		РезультатЗапроса.Ошибки.Добавить(ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Процедура УстановитьПолныйАдресМетода(ПараметрыЗапроса, ПовторныйЗапрос, Ошибки)
	
	Если Не ПараметрыЗапроса.ОбращениеКСервисуИнтеграции Тогда
		ПараметрыЗапроса.ПолныйАдресМетода = СтрШаблон("/%1/%2/%3", ПараметрыЗапроса.Приложение,
			ПараметрыЗапроса.КорневойURL, ПараметрыЗапроса.АдресМетода);
		ПараметрыЗапроса.ПолныйАдресМетода = СтрЗаменить(ПараметрыЗапроса.ПолныйАдресМетода, "//", "/");
		Возврат;
	КонецЕсли;
	
	АдресЗапроса = "";
	
	Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
		Если ОписаниеДоступногоАдреса.Текущий Тогда
			АдресЗапроса = ОписаниеДоступногоАдреса.Адрес;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПустаяСтрока(АдресЗапроса) Тогда
		Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
			Если Не ЗначениеЗаполнено(ОписаниеДоступногоАдреса.ДатаОтказа)
				Или ТекущаяДатаСеанса() - ОписаниеДоступногоАдреса.ДатаОтказа > 60 * 60 * 12 Тогда
				АдресЗапроса = ОписаниеДоступногоАдреса.Адрес;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ПустаяСтрока(АдресЗапроса) Тогда
		АдресЗапроса = СтрЗаменить(АдресЗапроса, "https://", "");
		ИндексРазделителя = СтрНайти(АдресЗапроса, ".com/");
		ПараметрыЗапроса.Сервер = Сред(АдресЗапроса, 1, ИндексРазделителя + 3);
		ПараметрыЗапроса.Приложение = Сред(АдресЗапроса, ИндексРазделителя + 5);
	ИначеЕсли ПовторныйЗапрос Тогда
		Ошибки.Добавить(НСтр("ru = 'Нет доступных адресов для отправки запроса.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса.ПолныйАдресМетода = СтрШаблон("/%1/%2/%3", ПараметрыЗапроса.Приложение, ПараметрыЗапроса.КорневойURL,
		ПараметрыЗапроса.АдресМетода);
	
	Если СтрНайти(ПараметрыЗапроса.ПолныйАдресМетода, "/%1") > 0 Тогда
		Если ПустаяСтрока(ПараметрыЗапроса.ИдентификаторИТС) Тогда
			ПараметрыЗапроса.ИдентификаторИТС = ПолучитьИдентификаторИТС(ПараметрыЗапроса.ДанныеИнтеграции);
			Если ПустаяСтрока(ПараметрыЗапроса.ИдентификаторИТС) Тогда
				Ошибки.Добавить(НСтр("ru = 'Ошибка валидации Интернет-поддержки пользователей.'"));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		ПараметрыЗапроса.ПолныйАдресМетода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ПараметрыЗапроса.ПолныйАдресМетода, ПараметрыЗапроса.ИдентификаторИТС);
	КонецЕсли;
	
	ПараметрыЗапроса.ПолныйАдресМетода = СтрЗаменить(ПараметрыЗапроса.ПолныйАдресМетода, "//", "/");
	
КонецПроцедуры

// Вызвать HTTPМетод.
// 
// Параметры:
//  Соединение - HTTPСоединение - Соединение
//  ЗапросКСервису - HTTPЗапрос - Запрос к сервису
//  ПараметрыЗапроса - См. НовыеПараметрыЗапроса
// 
// Возвращаемое значение:
//  HTTPОтвет - Вызвать HTTPМетод
Функция ВызватьHTTPМетод(Соединение, ЗапросКСервису, ПараметрыЗапроса)
	
	Для ПопыткаПодключения = 1 По 3 Цикл
		
		Если ПопыткаПодключения = 2 Тогда
			ОбщегоНазначенияБТС.Пауза(5);
		ИначеЕсли ПопыткаПодключения = 3 Тогда
			ОбщегоНазначенияБТС.Пауза(2);
		КонецЕсли;
		
		Попытка
			Возврат Соединение.ВызватьHTTPМетод(ПараметрыЗапроса.Метод, ЗапросКСервису);
		Исключение
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ОбработатьОтветСервиса(РезультатЗапроса, ПараметрыЗапроса, ПараметрыМетода, Кэш, ЗапросКСервису, Ответ, Данные)
	
	Если ТребуетсяПереадресация(ПараметрыЗапроса, Ответ) Тогда
		РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода, Кэш, Истина);
		Возврат;
	КонецЕсли;
	
	Ответ = ?(Ответ = Неопределено, HTTPОтветТаймаут(), Ответ); // Таймаут
	
	РезультатЗапроса.КодСостояния = Ответ.КодСостояния;
	РезультатЗапроса.ВыводитьСообщения = ПараметрыЗапроса.ВыводитьСообщения;
	
	ДанныеИзJSON = "";
	Если ЗначениеЗаполнено(Данные) Тогда
		ДанныеИзJSON = ОбщегоНазначения.JSONВЗначение(Данные, ,ПараметрыЗапроса.ПрочитатьВСоответствие);
	КонецЕсли;
	
	Если РезультатЗапроса.КодСостояния <> 200 Тогда
		
		Информация = "";
		Если ТипЗнч(ДанныеИзJSON) = Тип("Соответствие") Тогда
			Информация = ДанныеИзJSON["Информация"]; // Строка
			ТехническаяИнформация = ДанныеИзJSON["ТехническаяИнформация"];
		ИначеЕсли ТипЗнч(ДанныеИзJSON) = Тип("Структура") Тогда
			Информация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеИзJSON, "Информация", ""); // Строка
			ТехническаяИнформация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеИзJSON, "ТехническаяИнформация", "");
		ИначеЕсли ТипЗнч(ДанныеИзJSON) = Тип("Строка") Тогда
			ТехническаяИнформация = ДанныеИзJSON;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Информация) Тогда
			РезультатЗапроса.Ошибки.Добавить(Информация);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТехническаяИнформация) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТехническаяИнформация);
		КонецЕсли;
		
		Возврат;
		
	ИначеЕсли ПараметрыЗапроса.Авторизация Тогда
		
		ЕстьИзменения = Ложь;
		
		ДоступныеАдреса = Ответ.Заголовки["NextUrls"];
		Если ТипЗнч(ДоступныеАдреса) = Тип("Строка") Тогда
			
			ДоступныеАдресаНовые = СтрРазделить(ДоступныеАдреса, ",");
			
			КоличествоДоступныхАдресов = ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.ВГраница();
			Для Индекс = -КоличествоДоступныхАдресов По 0 Цикл
				ОписаниеДоступногоАдреса = ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.Получить(-Индекс);
				РезультатПоиска = ДоступныеАдресаНовые.Найти(ОписаниеДоступногоАдреса.Адрес);
				Если РезультатПоиска = Неопределено Тогда
					ЕстьИзменения = Истина;
					ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.Удалить(-Индекс);
				Иначе
					ДоступныеАдресаНовые.Удалить(РезультатПоиска);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого Адрес Из ДоступныеАдресаНовые Цикл
				ЕстьИзменения = Истина;
				ОписаниеДоступногоАдреса = НовыеПараметрыДоступногоАдреса();
				ОписаниеДоступногоАдреса.Адрес = Адрес;
				ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.Добавить(ОписаниеДоступногоАдреса);
			КонецЦикла;
			
		КонецЕсли;
		
		ТекущийАдрес = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("https://%1/%2",
			ПараметрыЗапроса.Сервер, ПараметрыЗапроса.Приложение);
		Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
			Если ТекущийАдрес = ОписаниеДоступногоАдреса.Адрес И Не ОписаниеДоступногоАдреса.Текущий Тогда
				ОписаниеДоступногоАдреса.Текущий = Истина;
				ЕстьИзменения = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьИзменения Тогда
			ЗаписатьДанныеВБезопасноеХранилище(Новый Структура("ДоступныеАдреса, ДатаОбновленияТокена",
				ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса, Дата(1,1,1)));
		КонецЕсли;
		
	КонецЕсли;
	
	РезультатЗапроса.Ответ = ДанныеИзJSON;
	РезультатЗапроса.Заголовки = Ответ.Заголовки;
	
КонецПроцедуры

Функция ТребуетсяПереадресация(ПараметрыЗапроса, Ответ)
	
	Результат = Ложь;
	
	Если ПараметрыЗапроса.ОбращениеКСервисуИнтеграции
		И (Ответ = Неопределено
		Или Ответ.КодСостояния = 404
		Или Ответ.КодСостояния = 503) Тогда
		
		Результат = Истина;
		
		ТекущийАдрес = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("https://%1/%2",
			ПараметрыЗапроса.Сервер, ПараметрыЗапроса.Приложение);
		Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
			Если ТекущийАдрес = ОписаниеДоступногоАдреса.Адрес Тогда
				ОписаниеДоступногоАдреса.ДатаОтказа = ТекущаяДатаСеанса();
				ОписаниеДоступногоАдреса.Текущий = Ложь;
				ПараметрыЗапроса.ДанныеИнтеграции = ЗаписатьДанныеВБезопасноеХранилище(
					Новый Структура("ДоступныеАдреса, ДатаОбновленияТокена",
					ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса, Дата(1,1,1)));
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// HTTPОтвет таймаут.
// 
// Возвращаемое значение:
//  Структура - HTTPОтвет таймаут:
// * КодСостояния - Число - 
// * Заголовки - Соответствие из Строка - 
Функция HTTPОтветТаймаут()
	
	Результат = Новый Структура;
	
	Результат.Вставить("КодСостояния", 408);
	Результат.Вставить("Заголовки", Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

// Владелец тикета.
// 
// Возвращаемое значение:
//  Строка - Владелец тикета
Функция ВладелецТикета()
	Возврат "IntegrationService";
КонецФункции

// Тикет аутентификации на портале поддержки.
// 
// Возвращаемое значение:
//  Строка - Тикет аутентификации на портале поддержки
Функция ТикетАутентификацииНаПорталеПоддержки()
	
	Результат = "";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		УстановитьПривилегированныйРежим(Истина);
		ДанныеТикета = МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета());
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.'");
	КонецЕсли;
	
	Результат = ДанныеТикета.Тикет;
	
	Возврат Результат;
	
КонецФункции

// Установить токен доступа.
// 
// Параметры:
//  ДанныеИнтеграции - См. ИнтеграцияBNovoСИПовтИсп.НовыеДанныеИнтеграции
//  Ошибки - Массив из Строка - Ошибки
// 
// Возвращаемое значение:
//  Строка - Установить токен доступа
Функция УстановитьТокенДоступа(ДанныеИнтеграции = Неопределено, Ошибки = Неопределено)
	
	ТокенДоступа = "";
	
	Если ДанныеИнтеграции = Неопределено Тогда
		ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища();
	КонецЕсли;
	
	Если ТекущаяДатаСеанса() - ДанныеИнтеграции.ДатаОбновленияТокена > 1800 Тогда
		ТокенДоступа = ОбновитьТокенДоступа(ДанныеИнтеграции, Ошибки).id_token;
	Иначе
		ТокенДоступа = ДанныеИнтеграции.id_token;
	КонецЕсли;
	
	Возврат ТокенДоступа;
	
КонецФункции

#КонецОбласти

#Область ЗапросыКСервису

Функция ЗапросПолучитьПодключения(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Запрос установить соединение.
// 
// Параметры:
//  ПараметрыМетода - Структура, Произвольный - Параметры метода
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросУстановитьСоединение(ПараметрыМетода)
	
	ПараметрыМетода.Тикет = ТикетАутентификацииНаПорталеПоддержки();
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросУстановитьПодключение(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.Данные = Новый Соответствие;
	ПараметрыЗапроса.Данные.Вставить("login", ПараметрыМетода.Логин);
	ПараметрыЗапроса.Данные.Вставить("name", ПараметрыМетода.Представление);
	ПараметрыЗапроса.Данные.Вставить("keyConnect", ПараметрыМетода.КлючПодключения);
	ПараметрыЗапроса.Данные.Вставить("clientId", ПараметрыМетода.client_id);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросПолучитьДанные(ПараметрыМетода)
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
КонецФункции

Функция ЗапросСоздатьОбновитьУчетнуюЗапись(ПараметрыМетода)
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.Данные = Новый Соответствие;
	ПараметрыЗапроса.Данные.Вставить("organizationId",         Справочники.Организации.ПустаяСсылка().УникальныйИдентификатор());
	ПараметрыЗапроса.Данные.Вставить("hotelId",                Справочники.Гостиницы.ПустаяСсылка().УникальныйИдентификатор());
	ПараметрыЗапроса.Данные.Вставить("loadPaymentsCash",       Ложь);
	ПараметрыЗапроса.Данные.Вставить("loadPaymentsCard",       Ложь);
	ПараметрыЗапроса.Данные.Вставить("buyer",                  Справочники.Контрагенты.ПустаяСсылка().УникальныйИдентификатор()); 
	ПараметрыЗапроса.Данные.Вставить("paymentCardType",        Справочники.ВидыОплатОрганизаций.ПустаяСсылка().УникальныйИдентификатор()); 
	ПараметрыЗапроса.Данные.Вставить("singleBookingAgreement", Ложь);
	
	ПараметрыЗапроса.Данные.Вставить("name",                   "Интеграция Bnovo (новая)");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиИнтеграцииBnovo.Организация КАК Организация,
	|	НастройкиИнтеграцииBnovo.Гостиница КАК Гостиница,
	|	НастройкиИнтеграцииBnovo.Покупатель КАК Покупатель,
	|	НастройкиИнтеграцииBnovo.ВидОплатыКартой КАК ВидОплатыКартой,
	|	НастройкиИнтеграцииBnovo.ЗагружатьОплатыНаличные КАК ЗагружатьОплатыНаличные,
	|	НастройкиИнтеграцииBnovo.ЗагружатьОплатыКарта КАК ЗагружатьОплатыКарта,
	|	НастройкиИнтеграцииBnovo.РазделятьДоговораПоНомерамБроней КАК РазделятьДоговораПоНомерамБроней
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииBnovo КАК НастройкиИнтеграцииBnovo
	|ГДЕ
	|	НастройкиИнтеграцииBnovo.УчетнаяЗапись = &УчетнаяЗапись";
	Запрос.УстановитьПараметр("УчетнаяЗапись", ПараметрыМетода.УчетнаяЗапись);
	
	ПараметрыЗапроса.Данные.Вставить("name", "Интеграция Bnovo (новый)");
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		ПараметрыЗапроса.Данные["organizationId"]         = Результат.Организация.УникальныйИдентификатор();
		ПараметрыЗапроса.Данные["hotelId"]                = Результат.Гостиница.УникальныйИдентификатор();
		ПараметрыЗапроса.Данные["loadPaymentsCash"]       = Результат.ЗагружатьОплатыНаличные;
		ПараметрыЗапроса.Данные["loadPaymentsCard"]       = Результат.ЗагружатьОплатыКарта;
		ПараметрыЗапроса.Данные["buyer"]                  = Результат.Покупатель.УникальныйИдентификатор(); 
		ПараметрыЗапроса.Данные["paymentCardType"]        = Результат.ВидОплатыКартой.УникальныйИдентификатор(); 
		ПараметрыЗапроса.Данные["singleBookingAgreement"] = Результат.РазделятьДоговораПоНомерамБроней;
		
		ПараметрыЗапроса.Данные.Вставить("name", СтрШаблон("Интеграция Bnovo: %1 (%2)", Результат.Организация, Результат.Гостиница));
	КонецЕсли;
	
	
	ПараметрыЗапроса.Данные.Вставить("apiKey",    ПараметрыМетода.ApiKey);
	ПараметрыЗапроса.Данные.Вставить("date_from", Формат(ПараметрыМетода.ДатаНачалаЗагрузки, "ДФ=yyyy-MM-dd"));
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
КонецФункции

Функция ЗапросУдалитьПодключение(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросОбновитьТокенДоступа(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	ПараметрыЗапроса.Данные = "grant_type=CLIENT_CREDENTIALS";
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросПолучитьДанныеПользователяИТС(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.ИдентификаторИТС = "?";
	
	ПараметрыЗапроса.Данные = Новый Соответствие;
	ПараметрыЗапроса.Данные.Вставить("ticket", ПараметрыМетода.Тикет);
	ПараметрыЗапроса.Данные.Вставить("serviceNick", ВладелецТикета());
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

#КонецОбласти

#КонецОбласти  

#Область КонвертацияДанных

// Получает значение реквизита структуры/соответствия
// 
// Параметры:
//  Источник - Структура - источник данных.
//  ИмяРеквизита - Строка - Имя реквизита. Поддерживается разделение через "."
//  ТипЗначения - Строка - Тип значения, к которому нужно привести пустое значение реквизита
//  ЗначениеПоУмолчанию - Неопределено, Произвольный - Значение по умолчанию
// 
// Возвращаемое значение:
//  Неопределено, Произвольный, Строка, Дата, Булево, Структура, Массив из Произвольный - Полученное значение
Функция ПолучитьЗначение(Знач Источник, Знач ИмяРеквизита, ТипЗначения = "", ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Значение = ЗначениеПоУмолчанию;
	
	МассивПолей = СтрРазделить(ИмяРеквизита, ".");
	КоличествоПолей = МассивПолей.Количество()-1;
	
	Для Ид = 0 По КоличествоПолей-1 Цикл
		ИмяСвойства = МассивПолей[Ид];
		
		Если ТипЗнч(Источник) = Тип("Структура")
			И Источник.Свойство(ИмяСвойства) Тогда
			Источник = Источник[ИмяСвойства]; // Структура
		Иначе
			Возврат Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	ИмяРеквизита = МассивПолей[КоличествоПолей];
	
	ВыполнитьПреобразование = Ложь;
	
	Если ТипЗнч(Источник) = Тип("Структура") И Источник.Свойство(ИмяРеквизита) Тогда
		
		ИсходноеЗначение = Источник[ИмяРеквизита]; // Произвольный
		ВыполнитьПреобразование = Истина;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("Соответствие") Тогда
		
		ИсходноеЗначение = Источник.Получить(ИмяРеквизита);
		ВыполнитьПреобразование = Истина;
		
	КонецЕсли;
	
	Если ВыполнитьПреобразование Тогда
		
		Если ИсходноеЗначение = Неопределено Тогда
			Значение = ЗначениеПоУмолчанию;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Массив") Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Структура") Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Дата") Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Строка") И ВРег(ИсходноеЗначение) = "NONE" Тогда
			Значение = ЗначениеПоУмолчанию;
		ИначеЕсли ТипЗначения = "" Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗначения = "Дата" Тогда
			Значение = СтрокаВДату(ИсходноеЗначение);
		ИначеЕсли ТипЗначения = "Число" Тогда
			Значение = СтрокаВЧисло(ИсходноеЗначение);
		ИначеЕсли ТипЗначения = "ХранилищеЗначения" Тогда
			Значение = ХранилищеЗначенияИзJSON(ИсходноеЗначение);
		Иначе
			ТребуемыйТип = Новый ОписаниеТипов(ТипЗначения);
			Значение = ТребуемыйТип.ПривестиЗначение(ИсходноеЗначение);
		КонецЕсли;
		
		Если Значение = Неопределено Тогда
			Значение = ЗначениеПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция ПустаяДатаJSON()

	Возврат ЗаписатьДатуJSON('00010101', ФорматДатыJSON.ISO, ВариантЗаписиДатыJSON.УниверсальнаяДата);

КонецФункции

// Преобразует строку в дату.
// 
// Параметры:
//  СтрокаДанных - Строка - значение, преобразуемое в дату
// 
// Возвращаемое значение:
//  Дата - Строка в дату
Функция СтрокаВДату(СтрокаДанных)
	
	Результат = Дата(1,1,1);
	
	Если СтрокаДанных = ПустаяДатаJSON() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат = ПрочитатьДатуJSON(СтрокаДанных, ФорматДатыJSON.ISO);
	
	Возврат Результат;
	
КонецФункции

// Преобразует строку в число
// 
// Параметры:
//  Значение - Произвольный - значение, преобразуемое в число
// 
// Возвращаемое значение:
//  Число, Неопределено, Произвольный - Строка в число
Функция СтрокаВЧисло(Значение)
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Результат = Значение;
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		НовоеЗначение = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Значение);
		Если НовоеЗначение = Неопределено Тогда
			Результат = 0;
		Иначе
			Результат = НовоеЗначение;
		КонецЕсли;
		
	Иначе
		Результат = 0;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область КонструкторыДанных

// Новые параметры доступного адреса.
// 
// Возвращаемое значение:
//  Структура - Новые параметры доступного адреса:
// * Адрес - Строка - 
// * ДатаОтказа - Дата - 
// * Текущий - Булево - 
Функция НовыеПараметрыДоступногоАдреса() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Адрес", "");
	Параметры.Вставить("ДатаОтказа", Дата(1,1,1));
	Параметры.Вставить("Текущий", Ложь);
	
	Возврат Параметры;
	
КонецФункции

// Новый результат метода сервиса.
// 
// Возвращаемое значение:
//  Структура - Новый результат метода сервиса:
// * Ошибки - Массив из Строка - Тексты ошибок
// * КодСостояния - Число - Код ответа HTTP
// * ВыводитьСообщения - Булево - Признак вывода пользователю сообщений об ошибках
// * Ответ - Неопределено, Соответствие из Произвольный - Ответ HTTP запроса. Свойства:
// ** Ключ - Строка - Имя поля
// ** Значение - Произвольный - Значение поля
// * Заголовки - Неопределено, Соответствие из Строка - Заголовки ответа.
Функция НовыйРезультатМетодаСервиса() Экспорт
	
	НовыйРезультат = Новый Структура;
	
	НовыйРезультат.Вставить("Ошибки", Новый Массив);
	НовыйРезультат.Вставить("КодСостояния", 0);
	НовыйРезультат.Вставить("ВыводитьСообщения", Истина);
	НовыйРезультат.Вставить("Ответ", Неопределено);
	НовыйРезультат.Вставить("Заголовки", Неопределено);
	
	Возврат НовыйРезультат;
	
КонецФункции

#КонецОбласти
#КонецОбласти
