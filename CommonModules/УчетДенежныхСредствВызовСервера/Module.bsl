
#Область СлужебныйПрограммныйИнтерфейс

Функция ТипыКассовыхДокументов(МассивОбъектов) Экспорт
	
	СписокТиповКассовыхДокументов = Новый Массив;
	
	ДобавленаРКО = Ложь;
	ДобавленаПКО = Ложь;
	
	Для каждого КассовыйОрдер Из МассивОбъектов Цикл
		Если НЕ ДобавленаРКО И ТипЗнч(КассовыйОрдер) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			ДобавленаРКО = Истина;
			СписокТиповКассовыхДокументов.Добавить("РКО");
		ИначеЕсли НЕ ДобавленаПКО И ТипЗнч(КассовыйОрдер) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
			ДобавленаПКО = Истина;
			СписокТиповКассовыхДокументов.Добавить("ПКО");
		КонецЕсли;
		
		Если ДобавленаРКО И ДобавленаПКО Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокТиповКассовыхДокументов;
	
КонецФункции

Функция УровеньДоступаКассовыеДокументы() Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.ЖурналыДокументов.Деньги.Команды.Деньги) Тогда
		Возврат "ЖурналКассовыеДокументы";
	ИначеЕсли ПравоДоступа("Просмотр", Метаданные.Документы.ПриходныйКассовыйОрдер) Тогда
		Возврат "ПриходныйКассовыйОрдер";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Допустимо ли выполнять проверки банковского счета с такими реквизитами.
//
// Параметры:
//  ВалютаДенежныхСредств - СправочникСсылка.Валюты - валюта банковского счета.
//  ГосударствоОткрытияСчета - СправочникСсылка.СтраныМира - страна, в которой открыт счет.
//                           - Булево - Истина = страна открытия счета принадлежит к Таможенному союзу; Ложь = не принадлежит.
//
// Возвращаемое значение:
//  Булево       - Истина = проверять допустимо; иначе Ложь.
//
Функция ПроверятьРеквизитыБанковскогоСчета(Знач ВалютаДенежныхСредств, Знач ГосударствоОткрытияСчета) Экспорт
	
	ИностраннаяВалюта = ВалютаДенежныхСредств <> ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Если ТипЗнч(ГосударствоОткрытияСчета) = Тип("Булево") Тогда
		ЧленТаможенногоСоюза = ГосударствоОткрытияСчета;
	Иначе
		ЧленТаможенногоСоюза = Справочники.СтраныМира.ГосударствоЧленТаможенногоСоюза(ГосударствоОткрытияСчета);
	КонецЕсли;
	
	Возврат Не ИностраннаяВалюта И Не ЧленТаможенногоСоюза;
	
КонецФункции

Функция СтатьяДДСПоУмолчанию(КонтекстОперации, Период = '0001-01-01') Экспорт
	
	Возврат УчетДенежныхСредствБП.СтатьяДДСПоУмолчанию(КонтекстОперации, Период);
	
КонецФункции

Функция НетДокументовДиректБанкСбербанк(Знач Организация, Знач БанковскийСчет) Экспорт
	
	Возврат ЭлектронноеВзаимодействиеБП.НетДокументовДиректБанкСбербанк(Организация, БанковскийСчет);
	
КонецФункции

// Получает реквизиты первого платежного поручения
//
// Параметры:
//  МассивПлатежныхПоручений - Массив Из ДокументСсылка.ПлатежноеПоручение - массив ссылок на документы
//
// Возвращаемое значение:
//  Структура - реквизиты первого документа
//    * Дата            - Дата
//    * Организация     - СправочникСсылка.Организации
//    * СчетОрганизации - СправочникСсылка.БанковскиеСчета
//
Функция РеквизитыПервогоДокумента(Знач МассивПлатежныхПоручений) Экспорт
	
	РеквизитыПервогоДокумента = Новый Структура;
	
	Если МассивПлатежныхПоручений.Количество() = 0 Тогда
		Возврат РеквизитыПервогоДокумента;
	КонецЕсли;
	
	// Ревизиты всех документов одинаковые, поэтому получим их из первого в массиве.
	Ссылка = МассивПлатежныхПоручений[0];
	РеквизитыПервогоДокумента = Документы.ПлатежноеПоручение.РеквизитыПлатежногоПоручения(Ссылка);
	
	Возврат РеквизитыПервогоДокумента;
	
КонецФункции

Функция СтатьяЕдиныйНалоговыйПлатеж(ВидОперации) Экспорт
	
	Возврат Справочники.СтатьиДвиженияДенежныхСредств.СтатьяЕдиныйНалоговыйПлатеж(ВидОперации);
	
КонецФункции

#КонецОбласти
