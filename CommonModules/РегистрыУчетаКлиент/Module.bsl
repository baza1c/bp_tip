////////////////////////////////////////////////////////////////////////////////
// Отчетность по регистрам учета 
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Сохраняет табличный документ как регистр учета. Вызывается из формы отчета.
//
// Параметры:
//  Форма     - ФормаКлиентскогоПриложения - открытая форма, поставляющая значения свойств.
//  Подписать - Булево - нужно ли подписывать файл электронной подписью. Если не указано, то Ложь.
//
Процедура СохранитьРегистрУчета(Форма, Знач Подписать = Ложь) Экспорт
	
	Перем ДанныеСохранения;
	
	ТаблицаРегистра = Форма.Результат;
	Если Не ПроверитьВозможностьСохраненияРезультата(Форма, ТаблицаРегистра, Подписать) Тогда
		Возврат;
	КонецЕсли;
		
	Отчет = Форма.Отчет;
	
	СвойстваРегистраУчета = Новый Структура("ПодразделениеОрганизации, ВключатьОбособленныеПодразделения");
	Отчет.Свойство("Подразделение",                     СвойстваРегистраУчета.ПодразделениеОрганизации);
	Отчет.Свойство("ВключатьОбособленныеПодразделения", СвойстваРегистраУчета.ВключатьОбособленныеПодразделения);
	СвойстваРегистраУчета.Вставить("Организация",       Отчет.Организация);
	СвойстваРегистраУчета.Вставить("НачалоПериода",     Отчет.НачалоПериода);
	СвойстваРегистраУчета.Вставить("КонецПериода",      Отчет.КонецПериода);
	СвойстваРегистраУчета.Вставить("ВидРегистра",       Форма.ВидРегистра);
	СвойстваРегистраУчета.Вставить("Описание",          Форма.Заголовок);
	СвойстваРегистраУчета.Вставить("ТаблицаРегистра",
		ПоместитьВоВременноеХранилище(ТаблицаРегистра, Форма.УникальныйИдентификатор));
	СвойстваРегистраУчета.Вставить("Подписать",			Подписать);
	
	ДанныеСохранения = РегистрыУчетаВызовСервера.СохранитьРегистрУчета(СвойстваРегистраУчета);
	Если ДанныеСохранения = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого ПрисоединенныйФайл Из ДанныеСохранения.СвойстваПрисоединенныхФайлов Цикл
		
		// Подписываем файл в пользовательском формате (не mxl).
		// Если пользовательский формат - mxl, то файл один, подписываем его.
		Если Подписать 
			И (ДанныеСохранения.СвойстваПрисоединенныхФайлов.Количество() = 1 
				ИЛИ ПрисоединенныйФайл.Значение.Расширение <> "mxl") Тогда
			
			РаботаСФайламиКлиент.ПодписатьФайл(ПрисоединенныйФайл.Ключ, Форма.УникальныйИдентификатор);	
			
		Конецесли;
		
		Оповестить("Запись_Файл", Новый Структура, ПрисоединенныйФайл.Ключ);
		
	КонецЦикла;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Сохранение'"),
		ПолучитьНавигационнуюСсылку(ДанныеСохранения.РегистрУчета),
		ДанныеСохранения.ОписаниеРегистра,
		БиблиотекаКартинок.Информация32);

КонецПроцедуры

// Открывает список сохраненных регистров учета с отбором. Вызывается из формы отчета.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - открытая форма, поставляющая значения отборов.
//
Процедура ОткрытьАрхивРегистровУчета(Форма) Экспорт
	
	ВидРегистра = Форма.ВидРегистра;
	Если Не ЗначениеЗаполнено(ВидРегистра) Тогда
		Возврат;
	КонецЕсли;
	
	Отчет = Форма.Отчет;
	
	ОтборСписка = Новый Структура;
	ОтборСписка.Вставить("ВидРегистра",   ВидРегистра);
	ОтборСписка.Вставить("Организация",   Отчет.Организация);
	ОтборСписка.Вставить("НачалоПериода", Отчет.НачалоПериода);
	ОтборСписка.Вставить("КонецПериода",  Отчет.КонецПериода);
	
	ПараметрыОткрытияФормы = Новый Структура("Отбор", ОтборСписка);
	
	ИмяФормы = "Документ.РегистрУчета.Форма.АрхивРегистровУчета";
	
	ФормаСписка = ПолучитьФорму(ИмяФормы, ПараметрыОткрытияФормы);
		
	Если ФормаСписка <> Неопределено Тогда
		Если ФормаСписка.Открыта() Тогда
			Оповестить("ИзменитьОтборРегистровУчета", ПараметрыОткрытияФормы);
			ФормаСписка.Активизировать();
		Иначе
			ФормаСписка.Открыть();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Выполняет подписание файла, прикрепленного к документу РегистрУчета, электронной подписью.
// Вызывается из формы списка и формы документа РегистрУчета.
//
// Параметры:
//  РегистрУчета - ДокументСсылка.РегистрУчета - ссылка на сохраненный регистр учета.
//  Форма        - ФормаКлиентскогоПриложения - открытая форма, откуда вызывается процедура.
//  ОбработкаРезультата  - ОписаниеОповещения - при вызове передается значение типа Булево,
//                         						если Истина - файл успешно подписан, иначе не подписан,
//                       - Неопределено - оповещение не будет вызвано.
//
Процедура ПодписатьРегистрУчета(РегистрУчета, Форма, ОбработкаРезультата = Неопределено) Экспорт

	Если НЕ Форма.ИспользуетсяЭП Тогда
		СообщениеОбОшибке = НСтр("ru = 'Для подписи регистра включите использование ЭП в настройках программы'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, РегистрУчета);
		Возврат;
	КонецЕсли;
	
	ПодписываемыйФайл = РегистрыУчетаВызовСервера.ФайлРегистраУчетаВФорматеПользователя(РегистрУчета);
	
	Если ЗначениеЗаполнено(ПодписываемыйФайл) Тогда
		
		ДополнительныеПараметры = Новый Структура;
	
		Если ОбработкаРезультата <> Неопределено Тогда
		
			ДополнительныеПараметры.Вставить("ОбработкаРезультата", ОбработкаРезультата);
		
		КонецЕсли;
	
		РаботаСФайламиКлиент.ПодписатьФайл(ПодписываемыйФайл, Форма.УникальныйИдентификатор, ДополнительныеПараметры);
	
	КонецЕсли;

КонецПроцедуры

// Открывает сохраненную версию регистра учета, прикрепленную к документу РегистрУчета.
//
// Параметры:
//  РегистрУчета - ДокументСсылка.РегистрУчета - ссылка на сохраненный регистр учета.
//
Процедура ОткрытьРегистр(РегистрУчета) Экспорт

	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("ДокументРегистрУчета", РегистрУчета);
	
	ОткрытьФорму("Документ.РегистрУчета.Форма.ФормаПросмотраРегистра", ПараметрыОткрытияФормы,, РегистрУчета);
	
КонецПроцедуры

// Выполняет сохранение файла, прикрепленного к документу РегистрУчета, если есть, то с электронной подписью.
// Вызывается из формы списка и формы документа РегистрУчета.
//
// Параметры:
//  РегистрУчета - ДокументСсылка.РегистрУчета - ссылка на сохраненный регистр учета.
//  Форма        - ФормаКлиентскогоПриложения - открытая форма, откуда вызывается процедура.
//
Процедура СохранитьВместеСЭП(РегистрУчета, Форма) Экспорт

	СвойстваПрисоединенныхФайлов = РегистрыУчетаВызовСервера.ПолучитьСвойстваПрисоединенныхФайловРегистра(РегистрУчета, Истина);
	
	Если СвойстваПрисоединенныхФайлов.Количество() = 0 Тогда
		
		СообщениеОбОшибке = СтрШаблон(НСтр("ru='%1 не содержит присоединенного файла'"), РегистрУчета);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, РегистрУчета);
		Возврат;
		
	КонецЕсли;
	
	НайденФайлВMXL = Ложь;
	НайденФайлСПодписью = Ложь;
	НайденФайлВФорматеПользователя = Ложь;
	ДанныеФайлаMXL = Неопределено;
	ДанныеФайлаВФорматеПользователя = Неопределено;
	ДанныеФайлаСПодписью = Неопределено;
	
	Для Каждого ПрисоединенныйФайл Из СвойстваПрисоединенныхФайлов Цикл
		
		СвойстваФайла = ПрисоединенныйФайл.Значение;
		
		Если СвойстваФайла.Расширение = "mxl" Тогда
			НайденФайлВMXL = Истина;
			СвойстваФайлаMXL = СвойстваФайла;
			ДанныеФайлаMXL = СвойстваФайла.ДанныеФайла;
		Иначе
			НайденФайлВФорматеПользователя = Истина;
			СвойстваФайлаВФорматеПользователя = СвойстваФайла;
			ДанныеФайлаВФорматеПользователя = СвойстваФайла.ДанныеФайла;
		КонецЕсли;
			
		Если СвойстваФайла.ПодписанЭП = Истина Тогда
			НайденФайлСПодписью = Истина;
			СвойстваФайлаСПодписью = СвойстваФайла;
			ДанныеФайлаСПодписью = СвойстваФайла.ДанныеФайла;
			ФайлСПодписью = ПрисоединенныйФайл.Ключ;
		КонецЕсли;

	КонецЦикла;
	
	Если НЕ НайденФайлСПодписью Тогда
		
		Если НайденФайлВФорматеПользователя Тогда
		
			РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайлаВФорматеПользователя);
			
		Иначе
			
			ФайлВФорматеПользователя = РегистрыУчетаВызовСервера.СохранитьФайлВФорматеПользователя(ДанныеФайлаMXL);
			
			Если ФайлВФорматеПользователя <> Неопределено Тогда
				
				ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
				ПараметрыСохранения.Диалог.Расширение = ФайлВФорматеПользователя.Расширение;
				ПараметрыСохранения.Диалог.Заголовок = НСтр("ru = 'Сохранение'");
				ПараметрыСохранения.Диалог.Фильтр = СтрШаблон("Файлы (*.%1)|*.%1", ФайлВФорматеПользователя.Расширение);

				ФайловаяСистемаКлиент.СохранитьФайл(Неопределено, ФайлВФорматеПользователя.Адрес,
					ФайлВФорматеПользователя.ИмяФайла, ПараметрыСохранения);
					
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Если ДанныеФайлаMXL = Неопределено Тогда
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Регистр учета сформирован в прошлой версии программы, он не содержит визуального представления штампа ЭП.'"));
		
		КонецЕсли;
		
		ФайлыПодписей = РегистрыУчетаВызовСервера.ПолучитьПакетФайловИЭП(ДанныеФайлаСПодписью,
			ДанныеФайлаMXL,	РегистрУчета, Форма.УникальныйИдентификатор);
			
		ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайлов();
		ПараметрыСохранения.Диалог.Заголовок = НСтр("ru = 'Выбор папки (существующие файлы будут перезаписаны):'");
			
		ФайловаяСистемаКлиент.СохранитьФайлы(Неопределено, ФайлыПодписей, ПараметрыСохранения); 
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет состояние табличного документа
//
Функция ПроверитьВозможностьСохраненияРезультата(Форма, ТаблицаРегистра, ПроверитьВозможностьПодписи = Ложь)
	
	
	// Проверка формы отчета.
	// Должна быть указана Организация.
	//
	// Должны стоять флаги:
	// - Выводить заголовок (Заголовок);
	// - Выводить подписи   (Подписи);
	// - Выводить единицу измерения (Единица измерения).
	
	Если Не ЗначениеЗаполнено(Форма.Отчет.Организация) Тогда
		
		ТекстСообщения = Нстр("ru = 'Для сохранения регистра учета, отчет должен быть сформирован по организации'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПолеОрганизация");
		Возврат Ложь;
		
	КонецЕсли;
	
	СостояниеРегистра = Форма.Элементы.Результат.ОтображениеСостояния;
	
	Если СостояниеРегистра.Видимость Тогда
		ТекстСообщения = Нстр("ru = 'Отчет не может быть сохранен'");
		СостояниеРегистраТекст = СостояниеРегистра.Текст;
		Если ЗначениеЗаполнено(СостояниеРегистраТекст) Тогда
			ТекстСообщения = СостояниеРегистраТекст;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Результат");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Форма.ВыводитьЗаголовок Тогда
		
		ТекстСообщения = Нстр("ru = 'Для сохранения регистра учета, флаг ""Заголовок"" должен быть установлен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВыводитьЗаголовок");
		ПоказатьСтраницуНастроек(Форма);
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не Форма.ВыводитьПодвал Тогда
		
		ТекстСообщения = Нстр("ru = 'Для сохранения регистра учета, флаг ""Подписи"" должен быть установлен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВыводитьПодвал");
		ПоказатьСтраницуНастроек(Форма);
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не Форма.ВыводитьЕдиницуИзмерения Тогда
		
		ТекстСообщения = Нстр("ru = 'Для сохранения регистра учета, флаг ""Единица измерения"" должен быть установлен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВыводитьЕдиницуИзмерения");
		ПоказатьСтраницуНастроек(Форма);
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.ВидРегистра) Тогда
		
		ТекстСообщения = Нстр("ru = 'Для отчета не задан вид регистра в справочнике ""Виды регистров учета"", отчет не сохранен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ТаблицаРегистра.ШиринаТаблицы = 0 Тогда
		ТекстСообщения = Нстр("ru = 'Отчет не может быть сохранен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Результат");
		Возврат Ложь;
	КонецЕсли;

	Если ПроверитьВозможностьПодписи И НЕ Форма.ИспользуетсяЭП Тогда
		СообщениеОбОшибке = НСтр("ru='Для подписи регистра включите использование электронной подписи в настройках программы'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Показывает страницу настроек отчета
//
Процедура ПоказатьСтраницуНастроек(Форма)
	Если Форма.Элементы.Найти("РазделыОтчета") <> Неопределено Тогда
		Форма.Элементы.РазделыОтчета.ТекущаяСтраница		 = Форма.Элементы.НастройкиОтчета;
		Форма.Элементы.ПрименитьНастройки.КнопкаПоУмолчанию	 = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти