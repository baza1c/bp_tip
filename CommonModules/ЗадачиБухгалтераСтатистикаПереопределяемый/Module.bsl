
#Область ПрограммныйИнтерфейс

// Подробнее
// См. ЗадачиБухгалтераСтатистика.ДоступнаЗагрузкаФормИзСервиса
//
Процедура ОпределитьЭтоИнтерфейсИнтеграцииСБанком(Признак) Экспорт
	Признак = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
КонецПроцедуры

// Подробнее
// См. ЗадачиБухгалтераСтатистика.ВключитьРегламентноеЗаданиеПолучениеСпискаТребуемыхСтатФорм
//
Процедура ВключитьРегламентноеЗаданиеПолучениеСпискаТребуемыхСтатФорм(ПараметрыОбработчика) Экспорт
	
	// По умолчанию регл. задание будет запланировано на случайное время суток (но при этом будет сгенерировано
	// конкретное время). Это позволит избежать резкого всплеска числа обращений к сервису стат. форм.
	ПараметрыРасписания = ОбщегоНазначенияБП.ПараметрыРасписанияЕжедневногоРегламентногоЗадания();
	
	// В клиент-серверном варианте и в области на сервисе (Fresh) спланируем расписание так, чтобы выполнение
	// заданий в разных ИБ или в разных областях сервиса осуществлялось в разное время суток.
	//
	// В файловом варианте время запуска тоже случайное, но здесь ограничим интервал установки времени запуска
	// периодом с 9 до 17. Предполагаем, что в это время (рабочие часы) больше вероятность, что в ИБ будет подключен
	// клиентский сеанс.
	// При этом указание точного времени позволяет избежать ситуации, когда сразу при запуске клиентского
	// сеанса выполняется много регл заданий (без уточнения времени запуска), что приводит к долгому запуску.
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПараметрыРасписания.ВремяНачалаПериода = Дата(1, 1, 1, 9, 0, 0);
		ПараметрыРасписания.ВремяКонцаПериода = Дата(1, 1, 1, 17, 0, 0);
	КонецЕсли;
	
	ОбщегоНазначенияБП.ВключитьРегламентноеЗаданиеЕжедневно(
		Метаданные.РегламентныеЗадания.ПолучениеСпискаТребуемыхСтатФорм, ПараметрыРасписания);
	
КонецПроцедуры

// Подробнее
// См. ЗадачиБухгалтераСтатистика.РеспондентыРосстата
//
Процедура УстановитьТекстЗапросаРеспондентыРосстата(ТекстЗапроса) Экспорт
	
	Если БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям() Тогда
		ЗаполнениеФормСтатистикиПоОбособленнымПодразделениям.УстановитьТекстЗапросаРеспондентыРосстата(ТекстЗапроса);
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Организация,
		|	Организации.КодПоОКПО КАК ОКПО,
		|	Организации.ИНН КАК ИНН,
		|	ЛОЖЬ КАК ЕстьОбособленныеПодразделения
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	(Организации.ИНН <> """"
		|			ИЛИ Организации.КодПоОКПО <> """")
		|	И &УсловиеПоОрганизации";
	КонецЕсли;
	
	Если ОбщегоНазначенияБП.ИспользуетсяАрхив() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "НЕ Организации.ВАрхиве");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "Истина");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ЗадачиБухгалтераСтатистика.ЗаполнитьНастройкиОтчетовПоОтветуСервиса
//
Процедура ОпределитьОрганизациюПоРеспонденту(Организация, Респондент) Экспорт
	
	Организация = Неопределено;
	Если Респондент.is_branch Тогда
		Организация = Справочники.Организации.НайтиПоРеквизиту("КодПоОКПО", СокрЛП(Респондент.OKPO));
	ИначеЕсли ЗначениеЗаполнено(Респондент.INN) Тогда
		Организация = Справочники.Организации.НайтиОрганизацию(СокрЛП(Респондент.INN));
	ИначеЕсли ЗначениеЗаполнено(Респондент.OKPO) Тогда
		Организация = Справочники.Организации.НайтиПоРеквизиту("КодПоОКПО", СокрЛП(Респондент.OKPO));
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти