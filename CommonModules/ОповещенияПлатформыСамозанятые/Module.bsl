#Область СлужебныйПрограммныйИнтерфейс

// Возвращает параметры обсуждений
// 
// Параметры:
//   ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//  
// Возвращаемое значение:
//   - Неопределено
//   - Соответствие из Структура
//
Функция ПараметрыЧатов(ОтборОрганизация = Неопределено) Экспорт
	
	Перем ИдентификаторБота, ИдентификаторТекущегоПользователя;
	
	Если Не Обсуждения.ОбсужденияДоступны()
		Или Не ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые()
		Или Не ОповещенияПлатформыСамозанятыеПереопределяемый.ДоступноЧтениеОповещений() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЧатов = Новый Соответствие;
	
	ПодключенныеОрганизации = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПодключенныеОрганизации();
	Для Каждого Организация Из ПодключенныеОрганизации Цикл
		
		Если ОтборОрганизация <> Неопределено И ОтборОрганизация <> Организация Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыЧата = ПараметрыЧата(Организация, ИдентификаторБота, ИдентификаторТекущегоПользователя);
		Если ПараметрыЧата <> Неопределено Тогда
			ПараметрыЧатов.Вставить(ПараметрыЧата.ИдентификаторЧата, ПараметрыЧата);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПараметрыЧатов;
	
КонецФункции

// Функция периодического обновления списка обсуждений с проверкой актуальности данных
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//  Быстро - Булево - Истина, если нужно учитывать актуальность данных
// 
// Возвращаемое значение:
//  Булево - Истина, если функция выполнена успешно
//
Функция ОбновитьОбсуждения(ОтборОрганизация = Неопределено, Быстро = Истина) Экспорт
	
	Результат = Истина;
	
	Если ОтборОрганизация = Неопределено Тогда
		ПодключенныеОрганизации = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПодключенныеОрганизации();
		Для Каждого Организация Из ПодключенныеОрганизации Цикл
			Если Не ОбновитьОбсужденияОрганизации(Организация, Быстро) Тогда
				Результат = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат = ОбновитьОбсужденияОрганизации(ОтборОрганизация, Быстро);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает количество непрочитанных сообщений и сохраняет в регистре
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
// 
// Возвращаемое значение:
//  Булево - Истина, если функция выполнена успешно
//
Функция ПолучитьКоличествоНепрочитанных(ОтборОрганизация = Неопределено) Экспорт
	
	Результат = Истина;
	
	Если ОтборОрганизация = Неопределено Тогда
		ПодключенныеОрганизации = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПодключенныеОрганизации();
		Для Каждого Организация Из ПодключенныеОрганизации Цикл
			Если Не ПолучитьКоличествоНепрочитанныхОрганизации(Организация) Тогда
				Результат = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат = ПолучитьКоличествоНепрочитанныхОрганизации(ОтборОрганизация);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обновляет команды в обсуждениях
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//
Процедура ОбновитьКоличествоНепрочитанных(ОтборОрганизация = Неопределено) Экспорт
	
	Перем ИдентификаторБота, ИдентификаторТекущегоПользователя;
	
	Если Не Обсуждения.ОбсужденияДоступны() Тогда
		Возврат;
	Конецесли;
	
	Если ОтборОрганизация = Неопределено Тогда
		ПодключенныеОрганизации = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПодключенныеОрганизации();
		Для Каждого Организация Из ПодключенныеОрганизации Цикл
			ПараметрыЧата = ПараметрыЧата(Организация, ИдентификаторБота, ИдентификаторТекущегоПользователя);
			Если ПараметрыЧата <> Неопределено Тогда
				ПеренестиДействиеУведомитьОПрочтенииВсех(ПараметрыЧата);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ПараметрыЧата = ПараметрыЧата(ОтборОрганизация);
		Если ПараметрыЧата <> Неопределено Тогда
			ПеренестиДействиеУведомитьОПрочтенииВсех(ПараметрыЧата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Получает сообщения и сохраняет в справочнике
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//  ПолучатьПрочитанные - Булево, Неопределено - Получать прочитанные
// 
// Возвращаемое значение:
//  Булево - Истина, если функция выполнена успешно
//
Функция ПолучитьОповещения(ОтборОрганизация = Неопределено, ПолучатьПрочитанные = Неопределено) Экспорт
	
	Результат = Истина;
	
	Если ОтборОрганизация = Неопределено Тогда
		ПодключенныеОрганизации = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПодключенныеОрганизации();
		Для Каждого Организация Из ПодключенныеОрганизации Цикл
			Если Не ПолучитьОповещенияОрганизации(Организация, ПолучатьПрочитанные) Тогда
				Результат = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат = ПолучитьОповещенияОрганизации(ОтборОрганизация, ПолучатьПрочитанные);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает и добавляет новые сообщения в Систему взаимодействия
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//  ОчиститьПередЗаполнением - Булево - очистить обсуждение перед заполнением
//
Процедура ОбновитьОповещения(ОтборОрганизация = Неопределено, ОчиститьПередЗаполнением = Ложь) Экспорт
	
	Перем ИдентификаторБота, ИдентификаторТекущегоПользователя;
	
	Если Не Обсуждения.ОбсужденияДоступны() Тогда
		Возврат;
	Конецесли;
	
	Если ОтборОрганизация = Неопределено Тогда
		ПодключенныеОрганизации = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПодключенныеОрганизации();
		Для Каждого Организация Из ПодключенныеОрганизации Цикл
			ПараметрыЧата = ПараметрыЧата(Организация, ИдентификаторБота, ИдентификаторТекущегоПользователя);
			Если ПараметрыЧата <> Неопределено Тогда
				Если ОчиститьПередЗаполнением Тогда
					УдалитьВсеСообщения(ПараметрыЧата);
				КонецЕсли;
				ОбновитьОповещенияЧата(ПараметрыЧата);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ПараметрыЧата = ПараметрыЧата(ОтборОрганизация);
		Если ПараметрыЧата <> Неопределено Тогда
			Если ОчиститьПередЗаполнением Тогда
				УдалитьВсеСообщения(ПараметрыЧата);
			КонецЕсли;
			ОбновитьОповещенияЧата(ПараметрыЧата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура выполнения метода взаимодействия с сервисом интеграции в фоне.
// При вызове на клиенте должна выполняться асинхронно.
//
// Параметры:
//  ПараметрыЧата - Структура:
//    * Организация- СправочникСсылка.Организации
//    * ИдентификаторЧата - Строка
//    * ИдентификаторПомощника - Строка
//  ИсходныйСтатус - ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//  НовыйСтатус - ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//  
// Возвращаемое значение:
//  Булево - Истина, если функция выполнена успешно
//
Функция УведомитьОбИзмененииСтатусаВсех(ПараметрыЧата, ИсходныйСтатус, НовыйСтатус) Экспорт
	
	НайденныеОповещения = Справочники.ОповещенияПлатформыСамозанятые.НайтиПоСтатусу(
		ПараметрыЧата.Организация,
		ИсходныйСтатус);
	
	Возврат УведомитьОбИзмененииСтатуса(ПараметрыЧата, НайденныеОповещения, НовыйСтатус);
	
КонецФункции

// Процедура выполнения метода взаимодействия с сервисом интеграции в фоне.
// При вызове на клиенте должна выполняться асинхронно.
//
// Параметры:
//  ПараметрыЧата - Структура:
//    * Организация- СправочникСсылка.Организации
//    * ИдентификаторЧата - Строка
//    * ИдентификаторПомощника - Строка
//  СписокОповещений - Массив из СправочникСсылка.ОповещенияПлатформыСамозанятые
//  НовыйСтатус - ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//  
//  Возвращаемое значение:
//    Булево - Истина, если функция выполнена успешно
//
Функция УведомитьОбИзмененииСтатуса(ПараметрыЧата, СписокОповещений, НовыйСтатус) Экспорт
	
	Если Не ЕстьПравоИзменятьСостоянияОповещений() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МаксимальноеЧислоОповещений = ИнтеграцияСПлатформойСамозанятые.МаксимальноеЧислоОповещенийВЗапросе();
	
	ПорцияОповещений = Новый Массив;
	ВГраница = СписокОповещений.ВГраница();
	Для Индекс = 0 По ВГраница Цикл
		ПорцияОповещений.Добавить(СписокОповещений[Индекс]);
		Если ПорцияОповещений.Количество() = МаксимальноеЧислоОповещений Или Индекс = ВГраница Тогда
			Результат = УведомитьОбИзмененииСтатусаПорции(ПараметрыЧата.Организация, СписокОповещений, НовыйСтатус);
			Если Результат Тогда
				ОбновитьСтатусСообщений(ПараметрыЧата, ПорцияОповещений, НовыйСтатус);
				ПорцияОповещений.Очистить();
			Иначе
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучитьКоличествоНепрочитанных(ПараметрыЧата.Организация) Тогда
		ОбновитьКоличествоНепрочитанных(ПараметрыЧата.Организация);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Определяет идентификатор оповещения по данным оповещения.
// 
// Параметры:
//  ДанныеСообщения - Произвольный
// 
// Возвращаемое значение:
//  - Строка
//  - Неопределено
//
Функция ИдентификаторОповещения(ДанныеСообщения) Экспорт
	
	Если ТипЗнч(ДанныеСообщения) <> Тип("Строка") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтрНачинаетсяС(СокрЛ(ДанныеСообщения), "{") И СтрЗаканчиваетсяНа(СокрП(ДанныеСообщения), "}") Тогда
		// Старый формат хранения идентификатора в данных
		ДанныеСообщения = ЗначениеИзJSON(ДанныеСообщения);
		Если ТипЗнч(ДанныеСообщения) = Тип("Структура") И ДанныеСообщения.Свойство("Идентификатор") Тогда
			Возврат ДанныеСообщения.Идентификатор;
		КонецЕсли;
	Иначе
		Возврат ДанныеСообщения;
	КонецЕсли;
	
КонецФункции

// Удаляет сообщение из обсуждения.
// 
// Параметры:
//  ИдентификаторСообщения - ИдентификаторСообщенияСистемыВзаимодействия
//
Процедура УдалитьСообщение(ИдентификаторСообщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СистемаВзаимодействия.УдалитьСообщение(ИдентификаторСообщения);
	
КонецПроцедуры

// Находит элемент справочника ОповещенияПлатформыСамозанятые, соответствующий выбранному сообщению.
// 
// Параметры:
//  ДанныеСообщения - Произвольный
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  - СправочникСсылка.ОповещенияПлатформыСамозанятые
//  - Неопределено
//
Функция НайтиОповещение(ДанныеСообщения, Организация) Экспорт
	
	ИдентификаторОповещения = ИдентификаторОповещения(ДанныеСообщения);
	Если ИдентификаторОповещения <> Неопределено Тогда
		Возврат Справочники.ОповещенияПлатформыСамозанятые.НайтиПоКоду(ИдентификаторОповещения, , , Организация);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЕстьПравоПолучатьОповещения() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.Справочники.ОповещенияПлатформыСамозанятые);
	
КонецФункции

Функция ЕстьПравоИзменятьСостоянияОповещений() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбновитьОбсужденияОрганизации(Организация, Быстро)
	
	ОповещенияОбновлены = Справочники.ОповещенияПлатформыСамозанятые.Обновить(Организация, Быстро);
	
	Если ОповещенияОбновлены Тогда
		КоличествоНепрочитанныхОбновлено = РегистрыСведений.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые.Обновить(
			Организация, Ложь);
	Иначе
		КоличествоНепрочитанныхОбновлено = РегистрыСведений.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые.Обновить(
			Организация, Быстро);
	КонецЕсли;
	
	Возврат КоличествоНепрочитанныхОбновлено Или ОповещенияОбновлены;
	
КонецФункции

Функция ПараметрыЧата(Организация, ИдентификаторБота = Неопределено, ИдентификаторТекущегоПользователя = Неопределено)
	
	Если ИдентификаторБота = Неопределено Тогда
		// Находим (создаем) чат-бота - пользователя системы взаимодействия.
		Попытка
			ИдентификаторБота = ИдентификаторБота();
		Исключение
			КомментарийЖурналаРегистрации = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон(НСтр("ru = 'Ошибка при настройке бота Системы взаимодействия: %1'"), КомментарийЖурналаРегистрации));
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Если ИдентификаторТекущегоПользователя = Неопределено Тогда
		ИдентификаторТекущегоПользователя = СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
	КонецЕсли;
	
	ОбсуждениеГрупповое = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	КлючОбсуждения = КлючЧата(Строка(Организация.УникальныйИдентификатор()));
	ЗаголовокОбсуждения = ЗаголовокЧата(Организация, ОбсуждениеГрупповое);
		
	// Находим (создаем) обсуждение текущего пользователя с чат-ботом.
	Попытка
		ИдентификаторОбсуждения = НайтиОбсуждениеСистемыВзаимодействия(КлючОбсуждения, ИдентификаторТекущегоПользователя);
		Если ИдентификаторОбсуждения = Неопределено Тогда
			ИдентификаторОбсуждения = СоздатьОбсуждениеСистемыВзаимодействия(
				КлючОбсуждения, ИдентификаторБота, ИдентификаторТекущегоПользователя, ЗаголовокОбсуждения, ОбсуждениеГрупповое);
		Иначе
			УстановитьЗаголовокОбсужденияСистемыВзаимодействия(ИдентификаторОбсуждения, ЗаголовокОбсуждения);
		КонецЕсли;
	Исключение
		КомментарийЖурналаРегистрации = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			СтрШаблон(НСтр("ru = 'Ошибка при настройке обсуждения Системы взаимодействия: %1'"), КомментарийЖурналаРегистрации));
		Возврат Неопределено;
	КонецПопытки;
	
	ПараметрыЧата = НовыеПараметрыЧата();
	ПараметрыЧата.Организация = Организация;
	ПараметрыЧата.ИдентификаторПомощника = Строка(ИдентификаторБота);
	ПараметрыЧата.ИдентификаторЧата = Строка(ИдентификаторОбсуждения);
	
	Возврат ПараметрыЧата;
	
КонецФункции

Функция ИдентификаторБота()
	
	ИмяБота = ИмяПользовательИБПомощник();
	СвойстваПользователяИБ = НайтиПользователяИБДляБота(ИмяБота);
	Если СвойстваПользователяИБ = Неопределено Тогда
		СвойстваПользователяИБ = СоздатьПользователяИБДляБота(ИмяБота);
	КонецЕсли;
	
	Попытка
		ИдентификаторПользователяСистемыВзаимодействия = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(
			СвойстваПользователяИБ.УникальныйИдентификатор);
	Исключение
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтрШаблон(НСтр("ru = 'Не найден пользователь системы взаимодействия %1'"), ИмяБота));
	КонецПопытки;
	
	Если ИдентификаторПользователяСистемыВзаимодействия = Неопределено Тогда
		ИдентификаторПользователяСистемыВзаимодействия = СоздатьПользователяСистемыВзаимодействияДляБота(
			СвойстваПользователяИБ.ПользовательИБ);
	КонецЕсли;
	
	Возврат ИдентификаторПользователяСистемыВзаимодействия;
	
КонецФункции

Функция НайтиПользователяИБДляБота(ИмяБота)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Пользователи.СвойстваПользователяИБ(ИмяБота);
	
КонецФункции

Функция СоздатьПользователяИБДляБота(ИмяБота)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
	ОписаниеПользователяИБ.Имя = ИмяБота;
	ОписаниеПользователяИБ.ПолноеИмя = ПолноеИмяПользовательИБПомощник();
	ОписаниеПользователяИБ.АутентификацияСтандартная = Истина;
	ОписаниеПользователяИБ.Вставить("Действие", "Записать");
	ОписаниеПользователяИБ.Вставить("ВходВПрограммуРазрешен", Ложь);
	ОписаниеПользователяИБ.ЗапрещеноИзменятьПароль = Истина;
	ОписаниеПользователяИБ.СохраняемоеЗначениеПароля = "";
	
	НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
	НовыйПользователь.Наименование = ОписаниеПользователяИБ.ПолноеИмя;
	НовыйПользователь.Служебный = Истина;
	НовыйПользователь.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
	НовыйПользователь.Записать();
	
	Возврат Пользователи.СвойстваПользователяИБ(ИмяБота);
	
КонецФункции

Функция СоздатьПользователяСистемыВзаимодействияДляБота(ПользовательИБ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользовательВзаимодействия = СистемаВзаимодействия.СоздатьПользователя(ПользовательИБ);
	ПользовательВзаимодействия.Имя = НСтр("ru='ФНС России'");
	ПользовательВзаимодействия.ПолноеИмя = НСтр("ru='ФНС России'");
	ПользовательВзаимодействия.Картинка = БиблиотекаКартинок.ПлатформаСамозанятыеЛоготипФНС;
	ПользовательВзаимодействия.Записать();
	
	Возврат ПользовательВзаимодействия.Идентификатор;
	
КонецФункции

Функция НайтиОбсуждениеСистемыВзаимодействия(КлючЧата, ИдентификаторТекущегоПользователя)
	
	Отбор = Новый ОтборОбсужденийСистемыВзаимодействия;
	Отбор.Ключ = КлючЧата;
	Отбор.ТекущийПользовательЯвляетсяУчастником = Истина;
	
	НайденныеОбсуждения = СистемаВзаимодействия.ПолучитьОбсуждения(Отбор);
	
	ИдентификаторОбсуждения = Неопределено;
	Для Каждого Обсуждение Из НайденныеОбсуждения Цикл
		Если Обсуждение.Ключ = КлючЧата
			И Обсуждение.Участники.Содержит(ИдентификаторТекущегоПользователя) Тогда
			ИдентификаторОбсуждения = Обсуждение.Идентификатор;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИдентификаторОбсуждения;
	
КонецФункции

Функция СоздатьОбсуждениеСистемыВзаимодействия(КлючОбсуждения, ИдентификаторБота, ИдентификаторТекущегоПользователя, ЗаголовокОбсуждения, ОбсуждениеГрупповое)
	
	УстановитьПривилегированныйРежим(Истина);
	Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
	Обсуждение.Ключ = КлючОбсуждения;
	Обсуждение.Групповое = ОбсуждениеГрупповое;
	Если ОбсуждениеГрупповое Тогда
		// Заголовок может быть задан только у группового обсуждения
		Обсуждение.Заголовок = ЗаголовокОбсуждения;
	КонецЕсли;
	Обсуждение.Участники.Добавить(ИдентификаторБота);
	Обсуждение.Участники.Добавить(ИдентификаторТекущегоПользователя);
	Обсуждение.Записать();
	
	Возврат Обсуждение.Идентификатор;
	
КонецФункции

Процедура УстановитьЗаголовокОбсужденияСистемыВзаимодействия(ИдентификаторОбсуждения, ЗаголовокОбсуждения)
	
	УстановитьПривилегированныйРежим(Истина);
	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(ИдентификаторОбсуждения);
	Если Обсуждение.Групповое И Обсуждение.Заголовок <> ЗаголовокОбсуждения Тогда
		// Заголовок может быть задан только у группового обсуждения
		Обсуждение.Заголовок = ЗаголовокОбсуждения;
		Обсуждение.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьКоличествоНепрочитанныхОрганизации(Организация) Экспорт
	
	КоличествоНепрочитанныхПолучено = Ложь;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые");
		//@skip-check bsl-legacy-check-string-literal
		ЭлементБлокировкиДанных.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		ИмяМетода = "КоличествоНепрочитанныхОповещений";
		ПараметрыМетода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия(ИмяМетода);
		ПараметрыМетода.Организация = Организация;
		
		Запрос = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыМетода);
		
		Если Не Запрос.Ошибка Тогда
			РезультатЗапроса = ПолучитьРезультатВыполнения(Запрос);
		Иначе
			КомментарийЖурналаРегистрации = СтрШаблон(
				НСтр("ru = 'При получении количества непрочитанных возникла ошибка: %1'"),
				Запрос.Сообщение);
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				КомментарийЖурналаРегистрации);
		КонецЕсли;
		
		Если РезультатЗапроса <> Неопределено И ТипЗнч(РезультатЗапроса) = Тип("Структура") Тогда
			
			Количество = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗапроса, "Количество");
			
			ДанныеЗаполнения = Новый Структура();
			ДанныеЗаполнения.Вставить("Организация", Организация);
			ДанныеЗаполнения.Вставить("Количество", Количество);
			
			КоличествоНепрочитанныхПолучено = РегистрыСведений.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые.Записать(ДанныеЗаполнения);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'Не удалось получить количество непрочитанных: %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			КомментарийЖурналаРегистрации);
		
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
	Возврат КоличествоНепрочитанныхПолучено;
	
КонецФункции

Функция ПолучитьОповещенияОрганизации(Организация, Знач ПолучатьПрочитанные)
	
	ОповещенияПолучены = Ложь;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые");
		//@skip-check bsl-legacy-check-string-literal
		ЭлементБлокировкиДанных.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Если ПолучатьПрочитанные = Неопределено Тогда
			// Если получаем первый раз, получим прочитанные
			ПолучатьПрочитанные = Не Справочники.ОповещенияПлатформыСамозанятые.Получены(Организация);
		КонецЕсли;
		
		ИмяМетода = "СписокОповещений";
		ПараметрыМетода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия(ИмяМетода);
		ПараметрыМетода.Организация = Организация;
		
		ПараметрыМетода.Вставить("ПолучатьПрочитанные", ПолучатьПрочитанные);
		ПараметрыМетода.Вставить("ПолучатьАрхивные", Ложь);
		
		Запрос = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыМетода);
		
		Если Не Запрос.Ошибка Тогда
			РезультатЗапроса = ПолучитьРезультатВыполнения(Запрос);
		Иначе
			КомментарийЖурналаРегистрации = СтрШаблон(
				НСтр("ru = 'При запросе списка оповещений возникла ошибка: %1'"),
				Запрос.Сообщение);
			ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				КомментарийЖурналаРегистрации);
		КонецЕсли;
		
		Если РезультатЗапроса <> Неопределено И ТипЗнч(РезультатЗапроса) = Тип("Структура") Тогда
			
			ДоставленныеОповещения = Справочники.ОповещенияПлатформыСамозанятые.ПрочитатьРезультатЗапроса(РезультатЗапроса);
			ОбновитьСтатусыОтфильтрованныхОповещений(ДоставленныеОповещения, Организация, ПолучатьПрочитанные);
			
			УведомитьОДоставке(Организация, ДоставленныеОповещения);
			
			ОповещенияПолучены = Истина;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'Не удалось обновить список оповещений: %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			КомментарийЖурналаРегистрации);
		
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
	Возврат ОповещенияПолучены;
	
КонецФункции

Процедура ОбновитьСтатусыОтфильтрованныхОповещений(ДоставленныеОповещения, Организация, ПолучатьПрочитанные)
	
	Если ПолучатьПрочитанные Тогда
		// Все недоставленные оповещения, со статусом НеПрочитано и Прочитано, являются 
		// удаленными, т.к. были отфильтрованы параметром запроса ПолучатьАрхивные. Обновим статус в программе.
		ОтборСтатусы = Новый Массив;
		ОтборСтатусы.Добавить(Перечисления.СтатусыОповещенийПлатформыСамозанятые.НеПрочитано);
		ОтборСтатусы.Добавить(Перечисления.СтатусыОповещенийПлатформыСамозанятые.Прочитано);
		НайденныеОповещения = Справочники.ОповещенияПлатформыСамозанятые.НайтиПоСтатусам(
			Организация, ОтборСтатусы);
		УстановитьСтатусОповещений(
			Организация,
			НедоставленныеОповещения(НайденныеОповещения, ДоставленныеОповещения),
			Перечисления.СтатусыОповещенийПлатформыСамозанятые.Удалено,
			ТекущаяУниверсальнаяДата())
	Иначе
		// Все недоставленные оповещения, со статусом НеПрочитано, являются как минимум 
		// прочитанными, т.к. были отфильтрованы параметром запроса ПолучатьПрочитанные. Обновим статус в программе.
		НайденныеОповещения = Справочники.ОповещенияПлатформыСамозанятые.НайтиПоСтатусу(
			Организация, Перечисления.СтатусыОповещенийПлатформыСамозанятые.НеПрочитано);
		УстановитьСтатусОповещений(
			Организация,
			НедоставленныеОповещения(НайденныеОповещения, ДоставленныеОповещения),
			Перечисления.СтатусыОповещенийПлатформыСамозанятые.Прочитано,
			ТекущаяУниверсальнаяДата())
	КонецЕсли;
	
КонецПроцедуры

Функция НедоставленныеОповещения(НайденныеОповещения, ДоставленныеОповещения)
	
	НедоставленныеОповещения = Новый Массив;
	Для Каждого Оповещение Из НайденныеОповещения Цикл
		Если ДоставленныеОповещения.Найти(Оповещение) = Неопределено Тогда
			НедоставленныеОповещения.Добавить(Оповещение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат НедоставленныеОповещения;
	
КонецФункции

Процедура ОбновитьОповещенияЧата(ПараметрыЧата)
	
	Организация = ПараметрыЧата.Организация;
	
	// Датой последнего обновления условно считаем дату создания последнего сообщения,
	// т.к. более ранние изменения анализировать не имеет смысла
	ДатаПоследнегоОбновления = Неопределено;
	ИдентификаторЧата = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ПараметрыЧата.ИдентификаторЧата);
	ПоследнееСообщение = НайтиПоследнееСообщение(ИдентификаторЧата, 10);
	Если ПоследнееСообщение <> Неопределено Тогда
		ПоследнееОповещение = НайтиОповещение(ПоследнееСообщение.Данные, Организация);
		Если ПоследнееОповещение <> Неопределено Тогда
			ДатаПоследнегоОбновления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоследнееОповещение, "ДатаСоздания");
		КонецЕсли;
	КонецЕсли;
	
	// Если сообщений в чате нет, то анализируем изменения за один месяц
	Если Не ЗначениеЗаполнено(ДатаПоследнегоОбновления) Тогда
		ДатаПоследнегоОбновления = РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.ДатаИзменения(Организация);
		Если ДатаПоследнегоОбновления <> Неопределено Тогда
			ДатаПоследнегоОбновления = ДобавитьМесяц(ДатаПоследнегоОбновления, -1);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаПоследнегоОбновления) Тогда
		ДатаПоследнегоОбновления = ДобавитьМесяц(ТекущаяУниверсальнаяДата(), -1);
	КонецЕсли;
	
	Измененные = Справочники.ОповещенияПлатформыСамозанятые.ИзмененныеПозже(Организация, ДатаПоследнегоОбновления);
	Если Измененные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Измененные.Индексы.Добавить("Статус");
	
	СтатусУдалено = Перечисления.СтатусыОповещенийПлатформыСамозанятые.Удалено;
	Удаленные = Измененные.Скопировать(Новый Структура("Статус", СтатусУдалено));
	Если ЗначениеЗаполнено(Удаленные) Тогда
		Удаленные.Сортировать("ДатаСоздания");
		ОбновитьСтатусСообщений(ПараметрыЧата, Удаленные.ВыгрузитьКолонку("Оповещение"), СтатусУдалено);
	КонецЕсли;
	
	СтатусПрочитано = Перечисления.СтатусыОповещенийПлатформыСамозанятые.Прочитано;
	Прочитанные = Измененные.Скопировать(Новый Структура("Статус", СтатусПрочитано));
	Если ЗначениеЗаполнено(Прочитанные) Тогда
		Прочитанные.Сортировать("ДатаСоздания");
		ОбновитьСтатусСообщений(ПараметрыЧата, Прочитанные.ВыгрузитьКолонку("Оповещение"), СтатусПрочитано);
	КонецЕсли;
	
	СтатусНеПрочитано = Перечисления.СтатусыОповещенийПлатформыСамозанятые.НеПрочитано;
	Непрочитанные = Измененные.Скопировать(Новый Структура("Статус", СтатусНеПрочитано));
	
	Если ЗначениеЗаполнено(Непрочитанные) Тогда
		Непрочитанные.Сортировать("ДатаСоздания");
		СоздатьНовыеСообщения(Непрочитанные.ВыгрузитьКолонку("Оповещение"), ПараметрыЧата);
		ПеренестиДействиеУведомитьОПрочтенииВсех(ПараметрыЧата);
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьНовыеСообщения(НепрочитанныеОповещения, ПараметрыЧата)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДействияСообщений = ОповещенияПлатформыСамозанятыеКлиентСервер.ДействияСообщений();
	ИдентификаторЧата = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ПараметрыЧата.ИдентификаторЧата);
	ИдентификаторБота = Новый ИдентификаторПользователяСистемыВзаимодействия(ПараметрыЧата.ИдентификаторПомощника);
	ИдентификаторТекущегоПользователя = СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
	
	ДатаАктуальностиСообщений = ДобавитьМесяц(ТекущаяДатаСеанса(), -1);
	ПервоеСообщение = НайтиПервоеСообщение(ИдентификаторЧата, 10);
	Если ПервоеСообщение <> Неопределено Тогда
		ДатаПервогоСообщения = Макс(ПервоеСообщение.Дата, ДатаАктуальностиСообщений);
	Иначе
		ДатаПервогоСообщения = ДатаАктуальностиСообщений;
	КонецЕсли;
	
	НайденныеСообщения = НайтиСообщенияОповещений(ПараметрыЧата, НепрочитанныеОповещения);
	
	//@skip-check bsl-legacy-check-string-literal
	РеквизитыОповещений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		НепрочитанныеОповещения,
		"Код, Наименование, Сообщение, ДатаСоздания, ТипОповещения");
	РеквизитыСтатусовОповещений = РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.ПрочитатьСтатусы(
		НепрочитанныеОповещения, ПараметрыЧата.Организация);
	
	Для Каждого Оповещение Из НепрочитанныеОповещения Цикл
		
		Реквизиты = РеквизитыОповещений[Оповещение];
		Если Реквизиты = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДатаСоздания = МестноеВремя(Реквизиты.ДатаСоздания, ЧасовойПоясСеанса());
		
		Если ЗначениеЗаполнено(ДатаПервогоСообщения) И ДатаСоздания < ДатаПервогоСообщения Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыСтатусов = РеквизитыСтатусовОповещений[Оповещение];
		Статус = ?(РеквизитыСтатусов <> Неопределено, РеквизитыСтатусов.Статус, Неопределено);
		
		Сообщение = НайденныеСообщения[Оповещение];
		Если Сообщение <> Неопределено Тогда
			Если Статус = Перечисления.СтатусыОповещенийПлатформыСамозанятые.Удалено Тогда
				СистемаВзаимодействия.УдалитьСообщение(Сообщение.Идентификатор);
			Иначе
				Если НастроитьДействиеТекущийСтатус(Сообщение, Статус) Тогда
					Сообщение.Записать();
				КонецЕсли;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Сообщение = СистемаВзаимодействия.СоздатьСообщение(ИдентификаторЧата);
		Сообщение.Автор = ИдентификаторБота;
		Сообщение.Дата = ДатаСоздания;
		Сообщение.Текст = ТекстСообщения(Реквизиты.Наименование, Реквизиты.Сообщение);
		Сообщение.Получатели.Добавить(ИдентификаторТекущегоПользователя);
		Сообщение.Данные = Реквизиты.Код;
		НастроитьДействиеТекущийСтатус(Сообщение, Статус);
		
		Если Реквизиты.ТипОповещения = Перечисления.ТипыОповещенийПлатформыСамозанятые.ОплатаНалога Тогда
			Сообщение.Действия.Добавить(
				ДействияСообщений.ПерейтиКОплатеНПД,
				НСтр("ru = 'Оплатить'"));
		ИначеЕсли Реквизиты.ТипОповещения = Перечисления.ТипыОповещенийПлатформыСамозанятые.ПривязкаКПартнеру Тогда
			Сообщение.Действия.Добавить(
				ДействияСообщений.ПерейтиВЛичныйКабинетМойНалог,
				НСтр("ru = 'Личный кабинет Мой налог'"));
		КонецЕсли;
		
		Сообщение.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстСообщения(Заголовок, Тело)
	
	ЧастиСообщения = Новый Массив();
	Если ЗначениеЗаполнено(Заголовок) Тогда
		ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Заголовок, ШрифтыСтиля.ШрифтЗаголовковОповещенийПлатформыСамозанятые));
		ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Символы.ПС));
		ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Символы.ПС));
	КонецЕсли;
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Тело));
	
	Возврат Новый ФорматированнаяСтрока(ЧастиСообщения);
	
КонецФункции

Функция НастроитьДействиеТекущийСтатус(Сообщение, Статус) Экспорт
	
	ДействияСообщений = ОповещенияПлатформыСамозанятыеКлиентСервер.ДействияСообщений();
	
	ДействиеСтатусНеПрочитано = Сообщение.Действия.Найти(ДействияСообщений.СтатусНеПрочитано);
	ДействиеСтатусПрочитано = Сообщение.Действия.Найти(ДействияСообщений.СтатусПрочитано);
	
	Если Статус = Перечисления.СтатусыОповещенийПлатформыСамозанятые.НеПрочитано Тогда
		УдаляемоеДействие = ДействиеСтатусПрочитано;
		ДобавляемоеДействие = ДействиеСтатусНеПрочитано;
		ЗначениеДействия = ДействияСообщений.СтатусНеПрочитано;
		Шрифт = ШрифтыСтиля.ШрифтНепрочитанныхОповещенийПлатформыСамозанятые;
		Цвет = ЦветаСтиля.ЦветНепрочитанныхОповещенийПлатформыСамозанятые;
	Иначе
		УдаляемоеДействие = ДействиеСтатусНеПрочитано;
		ДобавляемоеДействие = ДействиеСтатусПрочитано;
		ЗначениеДействия = ДействияСообщений.СтатусПрочитано;
		Шрифт = ШрифтыСтиля.ШрифтПрочитанныхОповещенийПлатформыСамозанятые;
		Цвет = ЦветаСтиля.ЦветПрочитанныхОповещенийПлатформыСамозанятые;
	КонецЕсли;
	
	Модифицированость = Ложь;
	Если УдаляемоеДействие <> Неопределено Тогда
		Сообщение.Действия.Удалить(УдаляемоеДействие);
		Модифицированость = Истина;
	КонецЕсли;
	Если ДобавляемоеДействие = Неопределено Тогда
		Сообщение.Действия.Вставить(0, ЗначениеДействия, Новый ФорматированнаяСтрока(Строка(Статус), Шрифт, Цвет));
		Модифицированость = Истина;
	КонецЕсли;
	
	Возврат Модифицированость;
	
КонецФункции

Процедура ПеренестиДействиеУведомитьОПрочтенииВсех(ПараметрыЧата)
	
	КоличествоНепрочитанных = Справочники.ОповещенияПлатформыСамозанятые.КоличествоНепрочитанных(ПараметрыЧата.Организация);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторЧата = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ПараметрыЧата.ИдентификаторЧата);
	СообщениеУведомитьОПрочтенииВсех = НайтиСообщениеУведомитьОПрочтенииВсех(ИдентификаторЧата, 100);
	ПоследнееСообщение = НайтиПоследнееСообщение(ИдентификаторЧата, 10);
	
	ЗначениеДействия = ОповещенияПлатформыСамозанятыеКлиентСервер.ДействияСообщений().УведомитьОПрочтенииВсех;
	
	Если КоличествоНепрочитанных <> Неопределено Тогда
		ТекстДействия = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';Уведомить ФНС о прочтении %1 сообщения;;Уведомить ФНС о прочтении %1 сообщений;Уведомить ФНС о прочтении %1 сообщений;'"), 
				КоличествоНепрочитанных,, "ЧГ=0;");
	Иначе
		ТекстДействия = НСтр("ru = 'Уведомить ФНС о прочтении всех сообщений'");
	КонецЕсли;
	
	Если КоличествоНепрочитанных <> 0 Тогда
		Если СообщениеУведомитьОПрочтенииВсех <> Неопределено И ПоследнееСообщение <> Неопределено Тогда
			Если СообщениеУведомитьОПрочтенииВсех.Идентификатор = ПоследнееСообщение.Идентификатор Тогда
				ИзменяемоеДействие = СообщениеУведомитьОПрочтенииВсех.Действия.Найти(ЗначениеДействия);
				ИзменяемоеДействие.Текст = ТекстДействия;
				СообщениеУведомитьОПрочтенииВсех.Записать();
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если СообщениеУведомитьОПрочтенииВсех <> Неопределено Тогда
			УдаляемоеДействие = СообщениеУведомитьОПрочтенииВсех.Действия.Найти(ЗначениеДействия);
			Если УдаляемоеДействие <> Неопределено Тогда
				СообщениеУведомитьОПрочтенииВсех.Действия.Удалить(УдаляемоеДействие);
				СообщениеУведомитьОПрочтенииВсех.Записать();
			КонецЕсли;
		КонецЕсли;
		Если ПоследнееСообщение <> Неопределено И ЕстьПравоИзменятьСостоянияОповещений() Тогда
			ДобавляемоеДействие = ПоследнееСообщение.Действия.Найти(ЗначениеДействия);
			Если ДобавляемоеДействие = Неопределено Тогда
				ПоследнееСообщение.Действия.Добавить(ЗначениеДействия, ТекстДействия);
				ПоследнееСообщение.Записать();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если СообщениеУведомитьОПрочтенииВсех <> Неопределено Тогда
			УдаляемоеДействие = СообщениеУведомитьОПрочтенииВсех.Действия.Найти(ЗначениеДействия);
			Если УдаляемоеДействие <> Неопределено Тогда
				СообщениеУведомитьОПрочтенииВсех.Действия.Удалить(УдаляемоеДействие);
				СообщениеУведомитьОПрочтенииВсех.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция УведомитьОДоставке(Организация, СписокОповещений)
	
	МаксимальноеЧислоОповещений = ИнтеграцияСПлатформойСамозанятые.МаксимальноеЧислоОповещенийВЗапросе();
	
	ПорцияОповещений = Новый Массив;
	ВГраница = СписокОповещений.ВГраница();
	Для Индекс = 0 По ВГраница Цикл
		ПорцияОповещений.Добавить(СписокОповещений[Индекс]);
		Если ПорцияОповещений.Количество() = МаксимальноеЧислоОповещений Или Индекс = ВГраница Тогда
			Результат = УведомитьОДоставкеПорцииОповещений(Организация, ПорцияОповещений);
			Если Результат Тогда
				ПорцияОповещений.Очистить();
			Иначе
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция УведомитьОДоставкеПорцииОповещений(Организация, СписокОповещений)
	
	ПараметрыМетода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия("ПометитьОповещениеДоставленным");
	ПараметрыМетода.Организация = Организация;
	ПараметрыМетода.Оповещения = Справочники.ОповещенияПлатформыСамозанятые.ИдентификаторыОповещений(СписокОповещений);
	
	Запрос = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	
	Если Не Запрос.Ошибка Тогда
		РезультатЗапроса = ПолучитьРезультатВыполнения(Запрос);
	Иначе
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'При отправке уведомлений возникла ошибка: %1'"),
			Запрос.Сообщение);
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
	КонецЕсли;
	
	РезультатПолученный = "";
	РезультатОжидаемый = "Успешно";
	
	Если РезультатЗапроса <> Неопределено И ТипЗнч(РезультатЗапроса) = Тип("Структура") Тогда
		РезультатПолученный = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗапроса, "Результат");
		Если РезультатПолученный <> РезультатОжидаемый Тогда
			КомментарийЖурналаРегистрации = СтрШаблон(
				НСтр("ru = 'Результат обработки уведомлений: %1'"),
				РезультатПолученный);
			ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				КомментарийЖурналаРегистрации);
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатПолученный = РезультатОжидаемый;
	
КонецФункции

Функция УведомитьОбИзмененииСтатусаПорции(Организация, СписокОповещений, НовыйСтатус)
	
	МетодВзаимодействия = ИмяМетодаИзмененияСтатуса(НовыйСтатус);
	
	ПараметрыМетода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия);
	ПараметрыМетода.Организация = Организация;
	ПараметрыМетода.Оповещения = Справочники.ОповещенияПлатформыСамозанятые.ИдентификаторыОповещений(СписокОповещений);
	
	// Запоминаем время отправки уведомлений
	ДатаИзменения = ТекущаяУниверсальнаяДата();
	
	Запрос = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	
	Если Не Запрос.Ошибка Тогда
		РезультатЗапроса = ПолучитьРезультатВыполнения(Запрос);
	Иначе
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'При отправке уведомлений возникла ошибка: %1'"),
			Запрос.Сообщение);
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
	КонецЕсли;
	
	РезультатПолученный = "";
	РезультатОжидаемый = "Успешно";
	
	Если РезультатЗапроса <> Неопределено И ТипЗнч(РезультатЗапроса) = Тип("Структура") Тогда
		РезультатПолученный = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатЗапроса, "Результат");
		Если РезультатПолученный <> РезультатОжидаемый Тогда
			КомментарийЖурналаРегистрации = СтрШаблон(
				НСтр("ru = 'Результат обработки уведомлений: %1'"),
				РезультатПолученный);
			ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				КомментарийЖурналаРегистрации);
		КонецЕсли;
	КонецЕсли;
	
	Если РезультатПолученный = РезультатОжидаемый Тогда
		УстановитьСтатусОповещений(Организация, СписокОповещений, НовыйСтатус, ДатаИзменения);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьРезультатВыполнения(Запрос)
	
	МаксимальнаяДлительность = ИнтеграцияСПлатформойСамозанятые.МаксимальнаяДлительностьОжиданияРезультата();
	ВремяОжидания = 0;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	РезультатЗапроса = ИнтеграцияСПлатформойСамозанятые.НовыйРезультатЗапроса(СтатусыЗапросов.Выполняется);
	РезультатЗапроса.ВремяОжидания = Запрос.ВремяОжидания;
	
	Пока РезультатЗапроса.Статус = СтатусыЗапросов.Выполняется И ВремяОжидания < МаксимальнаяДлительность Цикл
		ОбщегоНазначенияБТС.Пауза(РезультатЗапроса.ВремяОжидания);
		ВремяОжидания = ВремяОжидания + РезультатЗапроса.ВремяОжидания;
		РезультатЗапроса = ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполнения(Запрос);
	КонецЦикла;
	
	Если РезультатЗапроса.Статус = СтатусыЗапросов.Выполнено Тогда
		
		Возврат РезультатЗапроса.Результат
		
	ИначеЕсли РезультатЗапроса.Статус = СтатусыЗапросов.Выполняется Тогда
		
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'Превышено время ожидания обработки: %1 с'"),
			ВремяОжидания);
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			КомментарийЖурналаРегистрации);
			
	ИначеЕсли РезультатЗапроса.Статус = СтатусыЗапросов.Ошибка Тогда
		
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'При обработке возникла ошибка: %1'"),
			РезультатЗапроса.Сообщение);
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
			
	КонецЕсли;
	
КонецФункции

Функция ИмяМетодаИзмененияСтатуса(Статус)
	
	Если Статус = Перечисления.СтатусыОповещенийПлатформыСамозанятые.Прочитано Тогда
		Возврат "ПометитьОповещениеПрочитанным";
	ИначеЕсли Статус = Перечисления.СтатусыОповещенийПлатформыСамозанятые.Удалено Тогда
		Возврат "ПоместитьОповещениеВАрхив";
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Невозможно установить статус: %1'"), Статус);
	КонецЕсли;
	
КонецФункции

Процедура УстановитьСтатусОповещений(Организация, СписокОповещений, НовыйСтатус, ДатаИзменения)
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого Оповещение Из СписокОповещений Цикл
			ДанныеЗаполнения = Новый Структура;
			ДанныеЗаполнения.Вставить("Организация", Организация);
			ДанныеЗаполнения.Вставить("Оповещение", Оповещение);
			ДанныеЗаполнения.Вставить("Статус", НовыйСтатус);
			ДанныеЗаполнения.Вставить("ДатаИзменения", ДатаИзменения);
			РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.ЗаписатьСтатус(ДанныеЗаполнения);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'Не удалось обновить статус оповещений: %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
		ВызватьИсключение КомментарийЖурналаРегистрации;
	КонецПопытки;
	
КонецПроцедуры

Процедура ОбновитьСтатусСообщений(ПараметрыЧата, СписокОповещений, НовыйСтатус)
	
	УстановитьПривилегированныйРежим(Истина);
	НайденныеСообщения = НайтиСообщенияОповещений(ПараметрыЧата, СписокОповещений);
	Для Каждого Оповещение Из СписокОповещений Цикл
		Сообщение = НайденныеСообщения[Оповещение];
		Если Сообщение <> Неопределено Тогда
			Если НастроитьДействиеТекущийСтатус(Сообщение, НовыйСтатус) Тогда
				Сообщение.Записать();
			КонецЕсли;
			Если НовыйСтатус = Перечисления.СтатусыОповещенийПлатформыСамозанятые.Удалено Тогда
				СистемаВзаимодействия.УдалитьСообщение(Сообщение.Идентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиСообщенияОповещений(ПараметрыЧата, СписокОповещений)
	
	НайденныеСообщения = Новый Соответствие();

	Если Не ЗначениеЗаполнено(СписокОповещений) Тогда
		Возврат НайденныеСообщения;
	КонецЕсли;
	
	//@skip-check bsl-legacy-check-string-literal
	РеквизитыОповещений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокОповещений, "Код, ДатаСоздания");
	
	ДатаСозданияПервого = ТекущаяУниверсальнаяДата();
	ИдентификаторыОповещений = Новый Массив;
	Для Каждого Оповещение Из СписокОповещений Цикл
		ИдентификаторыОповещений.Добавить(РеквизитыОповещений[Оповещение].Код);
		ДатаСозданияПервого = Мин(РеквизитыОповещений[Оповещение].ДатаСоздания, ДатаСозданияПервого);
	КонецЦикла;
	ИдентификаторыОповещений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИдентификаторыОповещений);

	ИдентификаторЧата = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ПараметрыЧата.ИдентификаторЧата);
	
	НайденныеСообщенияПоИдентификаторам = НайтиСообщенияПоИдентификаторам(
		ИдентификаторЧата,
		ИдентификаторыОповещений,
		МестноеВремя(ДатаСозданияПервого, ЧасовойПоясСеанса()));
	
	Для Каждого Оповещение Из СписокОповещений Цикл
		Идентификатор = РеквизитыОповещений[Оповещение].Код;
		Сообщение = НайденныеСообщенияПоИдентификаторам[Идентификатор];
		Если Сообщение <> Неопределено Тогда
			НайденныеСообщения.Вставить(Оповещение, Сообщение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденныеСообщения;
	
КонецФункции

Функция НайтиСообщенияПоИдентификаторам(ИдентификаторЧата, ИскомыеИдентификаторы, ДатаСозданияПервого)
	
	НайденныеСообщения = Новый Соответствие();
	
	Количество = ИскомыеИдентификаторы.Количество();
	Если Количество = 0 Тогда
		Возврат НайденныеСообщения;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	После = Неопределено;
	Пока Истина Цикл
		
		Если НайденныеСообщения.Количество() = ИскомыеИдентификаторы.Количество() Тогда
			Прервать;
		КонецЕсли;
		
		ВсеСообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений(ИдентификаторЧата, Количество * 2, После));
		Если ВсеСообщения.Количество() = 0 Тогда
			Прервать;
		КонецЕслИ;
		Если НачалоДня(ВсеСообщения[0].Дата) < НачалоДня(ДатаСозданияПервого) Тогда
			// Дальше искать не имеет смысла
			Прервать;
		КонецЕсли;
		
		Для Каждого Сообщение Из ВсеСообщения Цикл
			Идентификатор = ИдентификаторОповещения(Сообщение.Данные);
			Если Идентификатор <> Неопределено И ИскомыеИдентификаторы.Найти(Идентификатор) <> Неопределено Тогда
				НайденныеСообщения.Вставить(Идентификатор, Сообщение);
			КонецЕсли;
			После = Сообщение.Идентификатор;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат НайденныеСообщения;
	
КонецФункции

Функция НайтиПервоеСообщение(ИдентификаторЧата, Знач Количество, Знач После = Неопределено)
	
	НайденноеСообщение = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВсеСообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений(ИдентификаторЧата, Количество, После, Ложь));
	Если ВсеСообщения.Количество() = 0 Тогда
		Возврат НайденноеСообщение;
	КонецЕсли;
	
	Для Каждого Сообщение Из ВсеСообщения Цикл
		Идентификатор = ИдентификаторОповещения(Сообщение.Данные);
		Если Идентификатор <> Неопределено Тогда
			НайденноеСообщение = Сообщение;
			Прервать;
		КонецЕсли;
		После = Сообщение.Идентификатор;
	КонецЦикла;
	
	Если НайденноеСообщение = Неопределено Тогда
		Количество = Количество * 2;
		НайденноеСообщение = НайтиПервоеСообщение(ИдентификаторЧата, Количество, После);
	КонецЕсли;
	
	Возврат НайденноеСообщение;
	
КонецФункции

Функция НайтиПоследнееСообщение(ИдентификаторЧата, Знач Количество, Знач После = Неопределено)
	
	НайденноеСообщение = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВсеСообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений(ИдентификаторЧата, Количество, После));
	Если ВсеСообщения.Количество() = 0 Тогда
		Возврат НайденноеСообщение;
	КонецЕсли;
	
	Для Каждого Сообщение Из ВсеСообщения Цикл
		Идентификатор = ИдентификаторОповещения(Сообщение.Данные);
		Если Идентификатор <> Неопределено Тогда
			НайденноеСообщение = Сообщение;
			Прервать;
		КонецЕсли;
		После = Сообщение.Идентификатор;
	КонецЦикла;
	
	Если НайденноеСообщение = Неопределено Тогда
		Количество = Количество * 2;
		НайденноеСообщение = НайтиПоследнееСообщение(ИдентификаторЧата, Количество, После);
	КонецЕсли;
	
	Возврат НайденноеСообщение;
	
КонецФункции

Функция НайтиСообщениеУведомитьОПрочтенииВсех(ИдентификаторЧата, Знач Количество, Знач После = Неопределено)
	
	СообщениеУведомитьОПрочтенииВсех = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВсеСообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений(ИдентификаторЧата, Количество, После));
	Если ВсеСообщения.Количество() = 0 Тогда
		Возврат СообщениеУведомитьОПрочтенииВсех;
	КонецЕсли;
	
	ДействиеУведомитьОПрочтенииВсех = ОповещенияПлатформыСамозанятыеКлиентСервер.ДействияСообщений().УведомитьОПрочтенииВсех;
	
	Для Каждого Сообщение Из ВсеСообщения Цикл
		Идентификатор = ИдентификаторОповещения(Сообщение.Данные);
		Если Идентификатор <> Неопределено И Сообщение.Действия.Найти(ДействиеУведомитьОПрочтенииВсех) <> Неопределено Тогда
			СообщениеУведомитьОПрочтенииВсех = Сообщение;
			Прервать;
		КонецЕсли;
		После = Сообщение.Идентификатор;
	КонецЦикла;
	
	Если СообщениеУведомитьОПрочтенииВсех = Неопределено Тогда
		Количество = Количество * 2;
		СообщениеУведомитьОПрочтенииВсех = НайтиСообщениеУведомитьОПрочтенииВсех(ИдентификаторЧата, Количество, После);
	КонецЕсли;
	
	Возврат СообщениеУведомитьОПрочтенииВсех;
	
КонецФункции

Процедура УдалитьВсеСообщения(ПараметрыЧата)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторЧата = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ПараметрыЧата.ИдентификаторЧата);
	
	Пока Истина Цикл
	
		ВсеСообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений(ИдентификаторЧата, 200, Неопределено, Ложь));
		Если ВсеСообщения.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого Сообщение Из ВсеСообщения Цикл
			СистемаВзаимодействия.УдалитьСообщение(Сообщение.Идентификатор);
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

Функция ОтборСообщений(ИдентификаторЧата, Количество, После, ОбратныйПорядок = Истина)
	
	ОтборСообщений = Новый ОтборСообщенийСистемыВзаимодействия;
	ОтборСообщений.Количество = Количество;
	Если ОбратныйПорядок Тогда
		ОтборСообщений.НаправлениеСортировки = НаправлениеСортировки.Убыв;
	Иначе
		ОтборСообщений.НаправлениеСортировки = НаправлениеСортировки.Возр;
	КонецЕсли;
	ОтборСообщений.Обсуждение = ИдентификаторЧата;
	Если После <> Неопределено Тогда
		ОтборСообщений.После = После;
	КонецЕсли;
	
	Возврат ОтборСообщений;
	
КонецФункции

Функция ИмяПользовательИБПомощник()
	
	Возврат "ЧатБотПлатформыСамозанятыеСлужебный";
	
КонецФункции

Функция ПолноеИмяПользовательИБПомощник()
	
	Возврат НСтр("ru = 'Уведомления от ФНС России'");
	
КонецФункции

Функция КлючЧата(ДополнительныйИдентификатор)
	
	СоставКлюча = Новый Массив;
	СоставКлюча.Добавить(ИмяПользовательИБПомощник());
	СоставКлюча.Добавить(СистемаВзаимодействия.ИдентификаторТекущегоПользователя());
	Если ЗначениеЗаполнено(ДополнительныйИдентификатор) Тогда
		СоставКлюча.Добавить(ДополнительныйИдентификатор);
	КонецЕсли;
	
	Возврат СтрСоединить(СоставКлюча, "_");
	
КонецФункции

Функция ЗаголовокЧата(Организация, НесколькоОрганизаций)
	
	Если НесколькоОрганизаций Тогда
		//@skip-check bsl-legacy-check-string-literal
		Заголовок = СтрШаблон(
			НСтр("ru = 'ФНС России ⇒ %1'"),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "Наименование"));
	Иначе
		Заголовок = НСтр("ru = 'ФНС России'");
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

Функция НовыеПараметрыЧата()
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("ИдентификаторЧата", "");
	Результат.Вставить("ИдентификаторПомощника", "");
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеИзJSON(ТекстJSON, ВСоответствие = Ложь, СвойстваТипаДата = Неопределено)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Попытка
		ЧтениеJSON.УстановитьСтроку(ТекстJSON);
		Значение = ПрочитатьJSON(ЧтениеJSON, ВСоответствие, СвойстваТипаДата);
	Исключение
		Значение = Неопределено;
	КонецПопытки;
	ЧтениеJSON.Закрыть();
	
	Возврат Значение;
	
КонецФункции

Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации();
	
КонецФункции

#КонецОбласти
