
#Область ПрограммныйИнтерфейс

// Возвращает наименование операции для целей сверки расчетов по типу документа и дополнительным сведениям
//
// Параметры:
//  ДокументСсылка - ссылка на документ для которого требуется определить наименование операции
//  ДополнительныеСведения - Структура с ключами:
//    Счет - счет расчетов с контрагентом
//    КорСчет - корреспондирующий со счетом расчетов счет
//    Дебет - сумма по дебету
//    Кредит - сумма по кредиту
//    ВидПрочихДоходовИРасходов - вид расхода 
//    КорСубконто1 - субконто для определения наименования операции
//
// Возвращаемое значение:
//  НаименованиеОперации - Строка - наименование операции для целей сопоставления документов
//                                  на сервисе сверки, см. Перечисления.СервисСверкиРасчетовВидыОпераций
//
Функция НаименованиеОперации(ДокументСсылка, ДополнительныеСведения) Экспорт
	
	НаименованиеОперации = "";
	
	ЭтоРасчетыСПоставщиком            = БухгалтерскийУчетПовтИсп.СчетВИерархии(
		ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	ЭтоРасчетыСПокупателем            = БухгалтерскийУчетПовтИсп.СчетВИерархии(
		ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	ЭтоРасчетыСНеопределеннойСтороной = БухгалтерскийУчетПовтИсп.СчетВИерархии(
		ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	СчетаИсключения                   = ПланыСчетов.Хозрасчетный.СчетаНДСПоАренднымОбязательствам();
	
	НаименованиеОперацииРеализация = НСтр("ru='Реализация'");
	
	Если БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы) Тогда
		
		Если Не ЗначениеЗаполнено(ДополнительныеСведения.ВидПрочихДоходовИРасходов) Тогда
			НаименованиеОперации = "";
		ИначеЕсли ДополнительныеСведения.ВидПрочихДоходовИРасходов = Перечисления.ВидыПрочихДоходовИРасходов.КурсовыеРазницы Тогда
			НаименованиеОперации = НСтр("ru='КурсовыеРазницы'");
		ИначеЕсли ДополнительныеСведения.ВидПрочихДоходовИРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ШтрафыПениНеустойкиКПолучениюУплате Тогда
			НаименованиеОперации = НСтр("ru='Санкции'");
		ИначеЕсли ДополнительныеСведения.КорСубконто1 = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПрочиеДоходыИРасходы.СуммовыеРазницы") Тогда
			НаименованиеОперации = НСтр("ru='СуммовыеРазницы'");
		ИначеЕсли ДополнительныеСведения.КорСубконто1 = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПрочиеДоходыИРасходы.КурсовыеРазницыПоРасчетамВУЕ") Тогда
			НаименованиеОперации = НСтр("ru='КурсовыеРазницыУЕ'");
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ИзменениеУсловийАренды") Тогда
		НаименованиеОперации = НСтр("ru='КорректировкаПоступления'");
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеВАренду")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
		НаименованиеОперации = НСтр("ru='Поступление'");
		
	ИначеЕсли СчетаИсключения.Найти(ДополнительныеСведения.КорСчет) <> Неопределено
		И ДополнительныеСведения.Дебет > 0 Тогда
		НаименованиеОперации = НСтр("ru='КорректировкаРеализации'");
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(НаименованиеОперации) Тогда
		// Строка уже отработана
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаПоступления")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		НаименованиеОперации = НСтр("ru='КорректировкаПоступления'");

	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		НаименованиеОперации = НСтр("ru='КорректировкаРеализации'");
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоКраткосрочнымКредитамИЗаймам) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоДолгосрочнымКредитамИЗаймам) Тогда
		
		// Займы по счетам учета.
		Если ДополнительныеСведения.Дебет > 0 Тогда
			НаименованиеОперации = НСтр("ru='ВозвратЗайма'");
		ИначеЕсли ДополнительныеСведения.Кредит > 0 Тогда
			НаименованиеОперации = НСтр("ru='Займ'");
		Иначе
			НаименованиеОперации = НСтр("ru='ОперацииПоЗаймам'");
		КонецЕсли;
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоПретензиям) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоПретензиямВал) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыПоПретензиямУЕ) Тогда
		
		НаименованиеОперации = НСтр("ru='Претензия'");
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.ДенежныеДокументы)
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.ДенежныеДокументыВал) Тогда
		
		Если ДополнительныеСведения.Кредит > 0 Тогда
			НаименованиеОперации = НСтр("ru='ПоступлениеДенежныхДокументов'");
			
		ИначеЕсли ДополнительныеСведения.Дебет > 0 Тогда
			НаименованиеОперации = НСтр("ru='ВыдачаДенежныхДокументов'");
			
		КонецЕсли;
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.Касса) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.РасчетныеСчета) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.ВалютныеСчета) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.СпециальныеСчета) Тогда
		
		// Проверим операции займов, отраженные через документы со специальным видом операции,
		// для случаев когда используются нестандартные счета расчетов.
		ЭтоЗайм = Ложь;
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет")
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") 
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета")
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			
			ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидОперации");
			Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПолучениеЗайма 
				ИЛИ ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеЗайма 
				ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыдачаЗаймаКонтрагенту
				ИЛИ ВидОперации = Перечисления.ВидыОперацийРКО.ВыдачаЗаймаКонтрагенту Тогда
				ЭтоЗайм = Истина;
				НаименованиеОперации = НСтр("ru='Займ'");
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратЗаймаКонтрагентом
				ИЛИ ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратЗаймаКонтрагентом
				ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма
				ИЛИ ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратЗайма Тогда
				ЭтоЗайм = Истина;
				НаименованиеОперации = НСтр("ru='ВозвратЗайма'");
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЭтоЗайм Тогда
			Если ДополнительныеСведения.Кредит > 0 Тогда
				Если ЭтоРасчетыСПоставщиком Тогда
					НаименованиеОперации = НСтр("ru='ВозвратСредствОтПоставщика'");
				Иначе
					НаименованиеОперации = НСтр("ru='ОплатаОтПокупателя'");
				КонецЕсли;
			ИначеЕсли ДополнительныеСведения.Дебет > 0 Тогда
				Если ЭтоРасчетыСПокупателем Тогда
					НаименованиеОперации = НСтр("ru='ВозвратСредствПокупателю'");
				Иначе
					НаименованиеОперации = НСтр("ru='ОплатаПоставщику'");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.Продажи) И (ДополнительныеСведения.Дебет > 0) Тогда
		НаименованиеОперации = НаименованиеОперацииРеализация;

	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.КорСчет, ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы) 
		И НЕ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками) 
		И ДополнительныеСведения.Дебет > 0 Тогда
		НаименованиеОперации = НаименованиеОперацииРеализация;

	ИначеЕсли (БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками) 
		ИЛИ БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами))
		И ДополнительныеСведения.Кредит <> 0 Тогда
		НаименованиеОперации = НСтр("ru='Поступление'");

	ИначеЕсли БухгалтерскийУчетПовтИсп.СчетВИерархии(ДополнительныеСведения.Счет, ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками) Тогда
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И ДополнительныеСведения.Дебет <> 0 Тогда
			НаименованиеОперации = НаименованиеОперацииРеализация;

		ИначеЕсли ДополнительныеСведения.Кредит <> 0 Тогда
			НаименованиеОперации = НСтр("ru='ОплатаОтПокупателя'");
			
		КонецЕсли;
		
	// Дополнительно проверим операции займов, отраженные через документы со специальным видом операции,
	// для случаев когда используются нестандартные счета расчетов.
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") 
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		
		ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидОперации");
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПолучениеЗайма 
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеЗайма 
			ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыдачаЗаймаКонтрагенту
			ИЛИ ВидОперации = Перечисления.ВидыОперацийРКО.ВыдачаЗаймаКонтрагенту Тогда
			НаименованиеОперации = НСтр("ru='Займ'");
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратЗаймаКонтрагентом
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратЗаймаКонтрагентом
			ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма
			ИЛИ ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратЗайма Тогда
			НаименованиеОперации = НСтр("ru='ВозвратЗайма'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПустаяСтрока(НаименованиеОперации)Тогда
		НаименованиеОперации = НСтр("ru='СторноДокумента'");
	КонецЕсли;
	
	Возврат НаименованиеОперации;
	
КонецФункции

// Возвращает структуру дополнительных сведений, необходимых для определение наименования операции
//
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
//
// Возвращаемое значение:
//  ДополнительныеСведения - Стуктура - ключи см. тело функции
//
Функция ДополнительныеСведенияДляОпределенияОперации(Выборка) Экспорт
	
	ДополнительныеСведения = Новый Структура();
	ДополнительныеСведения.Вставить("Счет",                      Выборка.Счет);
	ДополнительныеСведения.Вставить("КорСчет",                   Выборка.КорСчет);
	ДополнительныеСведения.Вставить("Дебет",                     Выборка.Дебет);
	ДополнительныеСведения.Вставить("Кредит",                    Выборка.Кредит);
	ДополнительныеСведения.Вставить("ВидПрочихДоходовИРасходов", Выборка.ВидПрочихДоходовИРасходов);
	ДополнительныеСведения.Вставить("КорСубконто1",              Выборка.КорСубконто1);
	
	Возврат ДополнительныеСведения;
	
КонецФункции

Функция ДанныеИнформационнойБазыКСверке(Организация, ДатаНачала, ДатаОкончания) Экспорт
	
	ТаблицаСчетов = УчетВзаиморасчетов.ПолучитьТаблицуСчетовУчетаВзаиморасчетов();
	СчетаВзаиморасчетов = ТаблицаСчетов.ВыгрузитьКолонку("СчетРасчетов");
	
	СчетаИсключения = ПланыСчетов.Хозрасчетный.СчетаНДСПоАренднымОбязательствам();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаИсключения, ПланыСчетов.Хозрасчетный.СчетаПроцентовПоОбязательствам());
	СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.РасчетыНДСНалоговогоАгента);
	СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.КорректировкаРасчетовПрошлогоПериода);
	
	АналитикаРасчетов = Новый Массив();
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФильтрСписокСчетов",    СчетаВзаиморасчетов);
	Запрос.УстановитьПараметр("СчетаИсключения",       СчетаИсключения);
	Запрос.УстановитьПараметр("АналитикаРасчетов",     АналитикаРасчетов);
	Запрос.УстановитьПараметр("СчетаНДСПоАренде",      ПланыСчетов.Хозрасчетный.СчетаНДСПоАренднымОбязательствам());
	Запрос.УстановитьПараметр("ДатаНачала",            ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",         ДатаОкончания);
	Запрос.УстановитьПараметр("ОтбиратьПоОрганизации", ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("Организация",           Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
	|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ,
	|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	Контрагенты.ИНН КАК ИННКонтрагента,
	|	ВЫБОР
	|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА Контрагенты.КПП
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КППКонтрагента,
	|	Контрагенты.НаименованиеПолное КАК НаименованиеКонтрагентаПолное,
	|	Контрагенты.Наименование КАК НаименованиеКонтрагента,
	|	КонтрагентыБизнесСеть.Идентификатор КАК ИдентификаторКонтрагента,
	|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
	|	СервисСверкиРасчетовРеестрДокументов.НомерДокумента КАК НомерДокумента
	|ПОМЕСТИТЬ ДокументыКОтправке
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть
	|		ПО СервисСверкиРасчетовРеестрДокументов.Контрагент = КонтрагентыБизнесСеть.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО СервисСверкиРасчетовРеестрДокументов.Контрагент = Контрагенты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО СервисСверкиРасчетовРеестрДокументов.Организация = ОрганизацииБизнесСеть.Организация
	|ГДЕ
	|	НЕ ОрганизацииБизнесСеть.Идентификатор ЕСТЬ NULL
	|	И СервисСверкиРасчетовРеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке)
	|	И НЕ КонтрагентыБизнесСеть.Идентификатор ЕСТЬ NULL
	|	И Контрагенты.ИННВведенКорректно
	|	И ВЫБОР
	|			КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|				ТОГДА Контрагенты.КППВведенКорректно
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И СервисСверкиРасчетовРеестрДокументов.НомерДокумента <> """"
	|	И СервисСверкиРасчетовРеестрДокументов.ДатаДокумента >= &ДатаНачала
	|	И СервисСверкиРасчетовРеестрДокументов.ДатаДокумента <= &ДатаОкончания
	|	И ВЫБОР
	|			КОГДА &ОтбиратьПоОрганизации
	|				ТОГДА СервисСверкиРасчетовРеестрДокументов.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ФильтрСписокСчетов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&ФильтрСписокСчетов)
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКОтправке.Организация КАК Организация,
	|	ДокументыКОтправке.Контрагент КАК Контрагент,
	|	ДокументыКОтправке.Документ КАК Документ,
	|	ДокументыКОтправке.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХозрасчетныйОбороты.Организация, НЕОПРЕДЕЛЕНО) <> ДокументыКОтправке.Организация
	|				ИЛИ ЕСТЬNULL(ХозрасчетныйОбороты.Субконто1, НЕОПРЕДЕЛЕНО) <> ДокументыКОтправке.Контрагент
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.ОтправленоКУдалению)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.Получено)
	|	КОНЕЦ КАК Статус,
	|	ДокументыКОтправке.НомерДокумента КАК НомерДокумента,
	|	ДокументыКОтправке.ДатаДокумента КАК ДатаДокумента,
	|	ДанныеПервичныхДокументов.ДатаРегистратора КАК ДатаРегистратора,
	|	ДанныеПервичныхДокументов.НомерРегистратора КАК НомерРегистратора,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаНДСПоАренде)
	|				И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ИзменениеУсловийАренды
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотДт, 0)
	|	КОНЕЦ КАК Дебет,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаНДСПоАренде)
	|				И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ИзменениеУсловийАренды
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт
	|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0)
	|	КОНЕЦ КАК Кредит,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, НЕОПРЕДЕЛЕНО) КАК НомерВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ЭтоУниверсальныйДокумент, ЛОЖЬ) КАК ЭтоУниверсальныйДокумент,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ЭтоКорректировочныйДокумент, ЛОЖЬ) КАК ЭтоКорректировочныйДокумент,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.КорСчет, НЕОПРЕДЕЛЕНО) КАК КорСчет,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто1, НЕОПРЕДЕЛЕНО) КАК КорСубконто1,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто2, НЕОПРЕДЕЛЕНО) КАК КорСубконто2,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто3, НЕОПРЕДЕЛЕНО) КАК КорСубконто3,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.Валюта, НЕОПРЕДЕЛЕНО) КАК Валюта,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.ВалютнаяСуммаОборот, 0) КАК ВалютнаяСумма,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.ПрочиеДоходыИРасходы).ВидПрочихДоходовИРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидПрочихДоходовИРасходов,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.Счет, НЕОПРЕДЕЛЕНО) КАК Счет,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеПервичныхДокументов.ЭтоУниверсальныйДокумент, ЛОЖЬ)
	|				И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ПодразделениеОрганизации.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ОрганизацииБизнесСеть.Идентификатор КАК ИдентификаторОрганизации,
	|	ДокументыКОтправке.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ДокументыКОтправке.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
	|	ДокументыКОтправке.НаименованиеКонтрагентаПолное КАК НаименованиеКонтрагентаПолное,
	|	ДокументыКОтправке.ИННКонтрагента КАК ИННКонтрагента,
	|	ДокументыКОтправке.КППКонтрагента КАК КППКонтрагента
	|ПОМЕСТИТЬ ВТ_ДанныеРеестра
	|ИЗ
	|	ДокументыКОтправке КАК ДокументыКОтправке
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(
	|				&ДатаНачала,
	|				&ДатаОкончания,
	|				Регистратор,
	|				Счет В
	|					(ВЫБРАТЬ
	|						ФильтрСписокСчетов.Счет
	|					ИЗ
	|						ФильтрСписокСчетов),
	|				&АналитикаРасчетов,
	|				(Организация, Субконто1) В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ДокументыКОтправке.Организация,
	|						ДокументыКОтправке.Контрагент
	|					ИЗ
	|						ДокументыКОтправке КАК ДокументыКОтправке),
	|				,
	|				) КАК ХозрасчетныйОбороты
	|		ПО ДокументыКОтправке.Документ = ХозрасчетныйОбороты.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ДокументыКОтправке.Организация = ДанныеПервичныхДокументов.Организация
	|			И ДокументыКОтправке.Документ = ДанныеПервичныхДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО ДокументыКОтправке.Организация = ОрганизацииБизнесСеть.Организация
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК Ссылка,
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТ_ДанныеСчетовФактур
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТ_ДанныеРеестра.Документ
	|			ИЗ
	|				ВТ_ДанныеРеестра)
	|	И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры В (ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураПолученныйДокументыОснования.Ссылка,
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|ГДЕ
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТ_ДанныеРеестра.Документ
	|			ИЗ
	|				ВТ_ДанныеРеестра)
	|	И НЕ СчетФактураПолученныйДокументыОснования.Ссылка.ПометкаУдаления
	|	И СчетФактураПолученныйДокументыОснования.Ссылка.ВидСчетаФактуры В (ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаПоступление), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеСчетовФактур.Ссылка КАК Ссылка,
	|	ВТ_ДанныеСчетовФактур.ДокументОснование КАК ДокументОснование,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ, ЛОЖЬ) КАК ЭтоКорректировочныйСчетФактура,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.НомерСчетаФактуры
	|		КОНЕЦ, """") КАК НомерСчетаФактуры,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.ДатаСчетаФактуры
	|		КОНЕЦ, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСчетаФактуры,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре, 0)) КАК СуммаПоСчетуФактуре,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаНДС, 0)) КАК СуммаНДС,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение, 0)) КАК СуммаНДСРазницаУменьшение,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение, 0)) КАК СуммаНДСРазницаУвеличение
	|ПОМЕСТИТЬ ВТ_ДанныеПоНДС
	|ИЗ
	|	ВТ_ДанныеСчетовФактур КАК ВТ_ДанныеСчетовФактур
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналУчетаСчетовФактур КАК ЖурналУчетаСчетовФактур
	|		ПО ВТ_ДанныеСчетовФактур.Ссылка = ЖурналУчетаСчетовФактур.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеСчетовФактур.Ссылка,
	|	ВТ_ДанныеСчетовФактур.ДокументОснование,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ, ЛОЖЬ),
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.НомерСчетаФактуры
	|		КОНЕЦ, """"),
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.ДатаСчетаФактуры
	|		КОНЕЦ, ДАТАВРЕМЯ(1, 1, 1))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеРеестра.Организация КАК Организация,
	|	ВТ_ДанныеРеестра.Контрагент КАК Контрагент,
	|	ВТ_ДанныеРеестра.Документ КАК Документ,
	|	ВТ_ДанныеРеестра.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ВТ_ДанныеРеестра.Статус КАК Статус,
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерДокумента, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаРегистратора,
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерРегистратора, """") КАК НомерРегистратора,
	|	ВТ_ДанныеРеестра.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	Организации.ИНН КАК ИНН,
	|	ВЫБОР
	|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА Организации.КПП
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КПП,
	|	Организации.НаименованиеПолное КАК НаименованиеПолное,
	|	СУММА(ВТ_ДанныеРеестра.Дебет - ВТ_ДанныеРеестра.Кредит) КАК Сумма,
	|	Организации.Наименование КАК Наименование,
	|	ВТ_ДанныеРеестра.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
	|	МАКСИМУМ(ВТ_ДанныеРеестра.Счет) КАК Счет,
	|	МАКСИМУМ(ВТ_ДанныеРеестра.ВидПрочихДоходовИРасходов) КАК ВидПрочихДоходовИРасходов,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ДанныеРеестра.ВалютнаяСумма < 0
	|				ТОГДА -ВТ_ДанныеРеестра.ВалютнаяСумма
	|			ИНАЧЕ ВТ_ДанныеРеестра.ВалютнаяСумма
	|		КОНЕЦ) КАК ВалютнаяСумма,
	|	ВТ_ДанныеРеестра.Валюта КАК Валюта,
	|	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто3) КАК КорСубконто3,
	|	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто2) КАК КорСубконто2,
	|	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто1) КАК КорСубконто1,
	|	МАКСИМУМ(ВТ_ДанныеРеестра.КорСчет) КАК КорСчет,
	|	ВТ_ДанныеРеестра.ЭтоКорректировочныйДокумент КАК ЭтоКорректировочныйДокумент,
	|	ВТ_ДанныеРеестра.ЭтоУниверсальныйДокумент КАК ЭтоУниверсальныйДокумент,
	|	ВТ_ДанныеРеестра.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ВТ_ДанныеРеестра.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	СУММА(ВТ_ДанныеРеестра.Кредит) КАК Кредит,
	|	СУММА(ВТ_ДанныеРеестра.Дебет) КАК Дебет,
	|	ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") КАК НомерСчетаФактуры,
	|	ЕСТЬNULL(ВТ_ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСчетаФактуры,
	|	ЕСТЬNULL(ВТ_ДанныеПоНДС.ЭтоКорректировочныйСчетФактура, ЛОЖЬ) КАК ЭтоКорректировочныйСчетФактура,
	|	МАКСИМУМ(ЕСТЬNULL(ВТ_ДанныеПоНДС.СуммаНДС + ВТ_ДанныеПоНДС.СуммаНДСРазницаУменьшение + ВТ_ДанныеПоНДС.СуммаНДСРазницаУвеличение, 0)) КАК СуммаНДС,
	|	ВТ_ДанныеРеестра.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ВТ_ДанныеРеестра.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
	|	ВТ_ДанныеРеестра.НаименованиеКонтрагентаПолное КАК НаименованиеКонтрагентаПолное,
	|	ВТ_ДанныеРеестра.ИННКонтрагента КАК ИННКонтрагента,
	|	ВТ_ДанныеРеестра.КППКонтрагента КАК КППКонтрагента,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") ПОДОБНО ""%[А-Я]%""
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОбрабатыватьНомерСФ
	|ИЗ
	|	ВТ_ДанныеРеестра КАК ВТ_ДанныеРеестра
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ВТ_ДанныеРеестра.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеПоНДС КАК ВТ_ДанныеПоНДС
	|		ПО ВТ_ДанныеРеестра.Документ = ВТ_ДанныеПоНДС.ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеРеестра.Организация,
	|	ВТ_ДанныеРеестра.Контрагент,
	|	ВТ_ДанныеРеестра.Документ,
	|	ВТ_ДанныеРеестра.ИдентификаторДокумента,
	|	ВТ_ДанныеРеестра.Статус,
	|	ВТ_ДанныеРеестра.ИдентификаторОрганизации,
	|	Организации.ИНН,
	|	ВЫБОР
	|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА Организации.КПП
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	Организации.НаименованиеПолное,
	|	Организации.Наименование,
	|	ВТ_ДанныеРеестра.ЦифровойИндексОбособленногоПодразделения,
	|	ВТ_ДанныеРеестра.Валюта,
	|	ВТ_ДанныеРеестра.ЭтоКорректировочныйДокумент,
	|	ВТ_ДанныеРеестра.ЭтоУниверсальныйДокумент,
	|	ВТ_ДанныеРеестра.ДатаВходящегоДокумента,
	|	ВТ_ДанныеРеестра.НомерВходящегоДокумента,
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерДокумента, """"),
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """"),
	|	ЕСТЬNULL(ВТ_ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ВТ_ДанныеПоНДС.ЭтоКорректировочныйСчетФактура, ЛОЖЬ),
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерРегистратора, """"),
	|	ВТ_ДанныеРеестра.ИдентификаторКонтрагента,
	|	ВТ_ДанныеРеестра.НаименованиеКонтрагента,
	|	ВТ_ДанныеРеестра.НаименованиеКонтрагентаПолное,
	|	ВТ_ДанныеРеестра.ИННКонтрагента,
	|	ВТ_ДанныеРеестра.КППКонтрагента,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") ПОДОБНО ""%[А-Я]%""
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИТОГИ
	|	МАКСИМУМ(ИдентификаторОрганизации),
	|	МАКСИМУМ(ИНН),
	|	МАКСИМУМ(КПП),
	|	МАКСИМУМ(НаименованиеПолное),
	|	МАКСИМУМ(Наименование)
	|ПО
	|	Организация";
	
	Возврат Запрос.Выполнить();

КонецФункции

// Проверяет нужно ли оповещать пользователя о расхождениях.
//
// Параметры:
//  Пользователь - ссылка на пользователя, которого нужно проверить на необходимость оповестить
//
// Возвращаемое значение:
//   Булево - признак того, что указанного пользователя нужно оповещать о расхождениях.
//
Функция ОповещатьПользователяОРасхождениях(Пользователь) Экспорт
	
	// Оповещаем всех пользователей которым доступна роль ИспользованиеСервисаСверкиРасчетов
	Возврат Пользователи.РолиДоступны("ИспользованиеСервисаСверкиРасчетов", Пользователь, Ложь);
	
КонецФункции

// Возвращает организации, которые нужно подключить к сверке.
//
// Параметры:
//  ТолькоСДвижениями - булево - признак того, что нужно регистрировать только те организации
//  по которым есть движения/документы.
//
// Возвращаемое значение:
//   Массив - массив организаций для подключения.
//
Функция ОрганизацииДляПодключения(ТолькоСДвижениями) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодключенныеОрганизации", БизнесСеть.ПодключенныеОрганизации());
	Запрос.УстановитьПараметр("НачалоПериода", СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов());
	Если ТолькоСДвижениями И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		// Единственную организацию подключаем "под капотом" вне зависимости от наличия движений.
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПодключенныеОрганизации.Организация КАК Организация
		|ПОМЕСТИТЬ ПодключенныеОрганизации
		|ИЗ
		|	&ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаОрганизацииКПодключению
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|		ПО Организации.Ссылка = ПодключенныеОрганизации.Организация
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И ПодключенныеОрганизации.Организация ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ХозрасчетныйОбороты.Организация КАК Организация
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			,
		|			,
		|			,
		|			,
		|			Организация В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаОрганизацииКПодключению.Организация
		|				ИЗ
		|					ВременнаяТаблицаОрганизацииКПодключению),
		|			,
		|			) КАК ХозрасчетныйОбороты";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПодключенныеОрганизации.Организация КАК Организация
		|ПОМЕСТИТЬ ПодключенныеОрганизации
		|ИЗ
		|	&ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|		ПО Организации.Ссылка = ПодключенныеОрганизации.Организация
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И ПодключенныеОрганизации.Организация ЕСТЬ NULL";
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	ОрганизацииДляПодключения = Запрос.Выполнить().Выгрузить();
	
	Возврат ОрганизацииДляПодключения.ВыгрузитьКолонку("Организация");
	
КонецФункции

// Регистрирует документы к сверке с даты начала работы сверки расчетов (см. СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов)
//
// Параметры:
//  МассивОрганизаций  - массив - если передан, регистрируются документы только указанных организаций.
//  МассивКонтрагентов - массив - если передан, регистрируются документы только указанных контрагентов.
//
Процедура ЗарегистрироватьКСверкеРанееВведенныеДокументы(МассивОрганизаций, МассивКонтрагентов) Экспорт
	
	 УстановитьПривилегированныйРежим(Истина);
	
	// Если передан отбор, то повторно регистрируем документы к обмену,
	// иначе регистрируем только те, которые ранее не были зарегистрированы (для сценария включения\выключения сверки).
	ТолькоНеОтправленныеНаСверку = Истина;
	Если ЗначениеЗаполнено(МассивОрганизаций) Тогда
		ТолькоНеОтправленныеНаСверку = Ложь;
	Иначе
		ПодключенныеОрганизации = СервисСверкиРасчетов.ПодключенныеОрганизации();
		МассивОрганизаций = ПодключенныеОрганизации.ВыгрузитьКолонку("Организация");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивКонтрагентов) Тогда
		ТолькоНеОтправленныеНаСверку = Ложь;
	Иначе
		МассивКонтрагентов = СервисСверкиРасчетов.КонтрагентыДляСверки();
	КонецЕсли;
	
	ТаблицаСчетов = УчетВзаиморасчетов.ПолучитьТаблицуСчетовУчетаВзаиморасчетов(Истина, Ложь);
	ФильтрСписокСчетов = ТаблицаСчетов.ВыгрузитьКолонку("СчетРасчетов");
	
	АналитикаРасчетов = Новый Массив();
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	ДатаНачалаСверкиРасчетов    = СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов();
	ДатаОкончанияСверкиРасчетов = КонецКвартала(ДобавитьМесяц(ТекущаяДата(), 3));
	
	ТипыСверяемыхДокументов = Метаданные.ОпределяемыеТипы.СервисСверкиРасчетовДокументыПодлежащиеСверке.Тип.Типы();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала",              ДатаНачалаСверкиРасчетов);
	Запрос.УстановитьПараметр("ДатаОкончания",           Новый Граница(КонецДня(ДатаОкончанияСверкиРасчетов), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("МассивОрганизаций",       МассивОрганизаций);
	Запрос.УстановитьПараметр("МассивКонтрагентов",      МассивКонтрагентов);
	Запрос.УстановитьПараметр("ФильтрСписокСчетов",      ФильтрСписокСчетов);
	Запрос.УстановитьПараметр("АналитикаРасчетов",       АналитикаРасчетов);
	Запрос.УстановитьПараметр("СчетаИсключения",         Документы.АктСверкиВзаиморасчетов.СчетаИсключения());
	Запрос.УстановитьПараметр("СчетаНДСПоАренде",        ПланыСчетов.Хозрасчетный.СчетаНДСПоАренднымОбязательствам());
	Запрос.УстановитьПараметр("ТипыСверяемыхДокументов", ТипыСверяемыхДокументов);
	
	Если ТолькоНеОтправленныеНаСверку Тогда
		// Сначала убедимся, что есть хотя бы один незарегистрированный к сверке документ.
		Запрос.Текст = ТекстЗапросаНаличиеНезарегистрированныхДокументов();
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Документы к регистрации найдены, выполняем запрос по всем незарегистрированным документам.
	Запрос.УстановитьПараметр("ТолькоНеОтправленныеНаСверку", ТолькоНеОтправленныеНаСверку);
	Запрос.Текст = ТекстЗапросаРасчетыСКонтрагентами();
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
		НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация            = Выборка.Организация;
		Запись.Контрагент             = Выборка.Контрагент;
		Запись.Документ               = Выборка.Документ;
		Если ЗначениеЗаполнено(Выборка.ДатаВходящегоДокумента) Тогда
			Запись.ДатаДокумента      = Выборка.ДатаВходящегоДокумента;
		Иначе
			Запись.ДатаДокумента      = Выборка.Дата;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.НомерВходящегоДокумента) Тогда
			ПолныйНомер = Выборка.НомерВходящегоДокумента;
		Иначе
			ПолныйНомер = Выборка.Номер;
		КонецЕсли;
		Запись.НомерДокумента         = СервисСверкиРасчетов.НомерБезПрефиксовИЛидирующихНулей(ПолныйНомер);
		Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
		Запись.ИдентификаторДокумента = Выборка.Документ.УникальныйИдентификатор();
		Запись.ИдентификаторСообщения = "";
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаНаличиеНезарегистрированныхДокументов()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОрганизацииБизнесСеть.Организация КАК Организация
	|ПОМЕСТИТЬ МассивОрганизаций
	|ИЗ
	|	РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент
	|ПОМЕСТИТЬ МассивКонтрагентов
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ПометкаУдаления
	|	И Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И Контрагенты.ИННВведенКорректно
	|	И ВЫБОР
	|			КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|				ТОГДА Контрагенты.КППВведенКорректно
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ФильтрСписокСчетов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&ФильтрСписокСчетов)
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХозрасчетныйОбороты.Регистратор КАК Документ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ФильтрСписокСчетов.Счет
	|				ИЗ
	|					ФильтрСписокСчетов),
	|			&АналитикаРасчетов,
	|			Организация В
	|					(ВЫБРАТЬ
	|						МассивОрганизаций.Организация
	|					ИЗ
	|						МассивОрганизаций)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						МассивКонтрагентов.Контрагент
	|					ИЗ
	|						МассивКонтрагентов),
	|			,
	|			) КАК ХозрасчетныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ПО ХозрасчетныйОбороты.Организация = СервисСверкиРасчетовРеестрДокументов.Организация
	|			И ХозрасчетныйОбороты.Субконто1 = СервисСверкиРасчетовРеестрДокументов.Контрагент
	|			И ХозрасчетныйОбороты.Регистратор = СервисСверкиРасчетовРеестрДокументов.Документ
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСчет В
	|					(ВЫБРАТЬ
	|						ФильтрСписокСчетов.Счет
	|					ИЗ
	|						ФильтрСписокСчетов)
	|				ТОГДА НЕ ХозрасчетныйОбороты.КорСубконто1 В
	|							(ВЫБРАТЬ
	|								МассивКонтрагентов.Контрагент
	|							ИЗ
	|								МассивКонтрагентов)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И (ХозрасчетныйОбороты.СуммаОборотДт <> 0
	|			ИЛИ ХозрасчетныйОбороты.СуммаОборотКт <> 0)
	|	И СервисСверкиРасчетовРеестрДокументов.Документ ЕСТЬ NULL
	|	И ТИПЗНАЧЕНИЯ(ХозрасчетныйОбороты.Регистратор) В (&ТипыСверяемыхДокументов)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСКонтрагентами()
	
	Возврат
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ФильтрСписокСчетов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&ФильтрСписокСчетов)
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Период КАК Дата,
	|	ДанныеПервичныхДокументов.НомерРегистратора КАК Номер,
	|	ХозрасчетныйОбороты.Регистратор КАК Документ,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаНДСПоАренде)
	|				И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ИзменениеУсловийАренды
	|			ТОГДА 0
	|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.КорректировкаПоступления
	|				ИЛИ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА 0
	|		ИНАЧЕ ХозрасчетныйОбороты.СуммаОборотДт
	|	КОНЕЦ КАК Дебет,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаНДСПоАренде)
	|				И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ИзменениеУсловийАренды
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт
	|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.КорректировкаПоступления
	|				ИЛИ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт
	|		ИНАЧЕ ХозрасчетныйОбороты.СуммаОборотКт
	|	КОНЕЦ КАК Кредит,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, НЕОПРЕДЕЛЕНО) КАК НомерВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаВходящегоДокумента,
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	ХозрасчетныйОбороты.Субконто1 КАК Контрагент
	|ПОМЕСТИТЬ ДокументыПредварительная
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ФильтрСписокСчетов.Счет
	|				ИЗ
	|					ФильтрСписокСчетов),
	|			&АналитикаРасчетов,
	|			Организация В (&МассивОрганизаций)
	|				И Субконто1 В (&МассивКонтрагентов),
	|			,
	|			) КАК ХозрасчетныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация В (&МассивОрганизаций))
	|			И ХозрасчетныйОбороты.Регистратор = ДанныеПервичныхДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ПО ХозрасчетныйОбороты.Организация = СервисСверкиРасчетовРеестрДокументов.Организация
	|			И ХозрасчетныйОбороты.Субконто1 = СервисСверкиРасчетовРеестрДокументов.Контрагент
	|			И ХозрасчетныйОбороты.Регистратор = СервисСверкиРасчетовРеестрДокументов.Документ
	|			И (&ТолькоНеОтправленныеНаСверку)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСчет В
	|					(ВЫБРАТЬ
	|						ФильтрСписокСчетов.Счет
	|					ИЗ
	|						ФильтрСписокСчетов)
	|				ТОГДА НЕ ХозрасчетныйОбороты.КорСубконто1 В (&МассивКонтрагентов)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И (ХозрасчетныйОбороты.СуммаОборотДт <> 0
	|			ИЛИ ХозрасчетныйОбороты.СуммаОборотКт <> 0)
	|	И ВЫБОР
	|			КОГДА &ТолькоНеОтправленныеНаСверку
	|				ТОГДА СервисСверкиРасчетовРеестрДокументов.Документ ЕСТЬ NULL
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ТИПЗНАЧЕНИЯ(ХозрасчетныйОбороты.Регистратор) В (&ТипыСверяемыхДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПредварительная.Дата КАК Дата,
	|	ДокументыПредварительная.Номер КАК Номер,
	|	ДокументыПредварительная.Документ КАК Документ,
	|	ДокументыПредварительная.Дебет КАК Дебет,
	|	ДокументыПредварительная.Кредит КАК Кредит,
	|	ДокументыПредварительная.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ДокументыПредварительная.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ДокументыПредварительная.Организация КАК Организация,
	|	ДокументыПредварительная.Контрагент КАК Контрагент
	|ИЗ
	|	ДокументыПредварительная КАК ДокументыПредварительная
	|ГДЕ
	|	(ДокументыПредварительная.Дебет <> 0
	|			ИЛИ ДокументыПредварительная.Кредит <> 0)";
	
КонецФункции

// Возвращает признак того, что документ является счетом-фактурой.
//
// Параметры:
//  ДокументОбъект - документ, который проверяется на соответствие типу счета-фактуры.
//
// Возвращаемое значение:
//  Булево - признак того, что документ является счетом-фактурой.
//
Функция ЭтоСчетФактура(ДокументОбъект) Экспорт
	
	Возврат ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураВыданный")
		ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураПолученный");
	
КонецФункции

// Регистрирует основания счета-фактуры к обмену. Сам счет-фактура не регистрируется к обмену,
// сведения о счете-фактуре отправляются вместе со сведениями об основании.
//
// Параметры:
//  ДокументОбъект - документ-основание счета-фактуры.
//
Процедура ЗарегистрироватьОснованияСФКОбмену(ДокументОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Для указанных видов счетов-фактур регистрируем все незарегистрированные основания к отправке,
	// для выданных УПД регистрируем основание повторно для актуализации номера.
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		ВидОперации = ДокументОбъект.ВидСчетаФактуры;
		Если ВидОперации <> Перечисления.ВидСчетаФактурыВыставленного.Корректировочный
			И ВидОперации <> Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
		ВидОперации = ДокументОбъект.ВидСчетаФактуры;
		Если ВидОперации <> Перечисления.ВидСчетаФактурыПолученного.Корректировочный
			И ВидОперации <> Перечисления.ВидСчетаФактурыПолученного.НаПоступление Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТипыСверяемыхДокументов = Метаданные.ОпределяемыеТипы.СервисСверкиРасчетовДокументыПодлежащиеСверке.Тип.Типы();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СчетФактураДокументыОснования.ДокументОснование КАК Ссылка,
	|	СчетФактураДокументыОснования.Ссылка.Организация КАК Организация,
	|	СчетФактураДокументыОснования.Ссылка.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ПО СчетФактураДокументыОснования.ДокументОснование = СервисСверкиРасчетовРеестрДокументов.Документ
	|			И (СервисСверкиРасчетовРеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументовОснования
	|		ПО (&ЭтоВыданныйСФ)
	|			И СчетФактураДокументыОснования.ДокументОснование = ДанныеПервичныхДокументовОснования.Документ
	|ГДЕ
	|	(СервисСверкиРасчетовРеестрДокументов.Документ ЕСТЬ NULL
	|			ИЛИ &ЭтоВыданныйСФ
	|				И ЕСТЬNULL(ДанныеПервичныхДокументовОснования.ЭтоУниверсальныйДокумент, ЛОЖЬ))
	|	И СчетФактураДокументыОснования.Ссылка = &ДокументСсылка
	|	И ТИПЗНАЧЕНИЯ(СчетФактураДокументыОснования.ДокументОснование) В (&ТипыСверяемыхДокументов)
	|	И &ОтборПоОснованию";
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СчетФактураВыданный", "СчетФактураПолученный");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоОснованию", "ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоОснованию",
			"НЕ СчетФактураДокументыОснования.ДокументОснование ССЫЛКА Документ.ОказаниеУслуг");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ЭтоВыданныйСФ", ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураВыданный"));
	Запрос.УстановитьПараметр("ТипыСверяемыхДокументов", ТипыСверяемыхДокументов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		СписокДокументовКОбмену = РезультатЗапроса.Выгрузить();
		Для Каждого ДокументКОбмену Из СписокДокументовКОбмену Цикл
			СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(ДокументКОбмену.Организация, ДокументКОбмену.Контрагент, ДокументКОбмену.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Ищет контрагента по связке ИНН/КПП, заполняет структуру сведений о контрагенте
//
// Параметры:
//  РеквизитыКонтрагента - Структура со свойствами ИНН, КПП.
//  СведенияОКонтрагенте - Структура со свойствами:
//    ИНН - заполняется, когда не найден по ИНН
//    КПП - заполняется, когда не найден по КПП
//    Значение - ссылка на справочник, если не найден, то пустая ссылка
//
Процедура СведенияКонтрагентаПоИНН_КПП(РеквизитыКонтрагента, СведенияОКонтрагенте) Экспорт
	
	СведенияОКонтрагенте.Значение = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(РеквизитыКонтрагента); 
	
	Если Не ЗначениеЗаполнено(СведенияОКонтрагенте.Значение) Тогда
		СведенияОКонтрагенте.КППКонтрагента = РеквизитыКонтрагента.КПП;
		// Из сервиса может вернуться неактуальный КПП, произведем поиск контрагента по ИНН
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("ИНН", РеквизитыКонтрагента.ИНН);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СведенияОКонтрагенте.Значение  = Выборка.Ссылка;
		Иначе
			СведенияОКонтрагенте.ИННКонтрагента = РеквизитыКонтрагента.ИНН;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СверитьКнигиДляПомощникаПоНДСВФоне(СтруктураПараметров) Экспорт
	
	РезультатСверки = Новый Структура;
	РезультатСверки.Вставить("ЕстьРасхожденияВКнигеПокупок", Ложь);
	РезультатСверки.Вставить("ЕстьРасхожденияВКнигеПродаж",  Ложь);
	РезультатСверки.Вставить("ЕстьНеСверенныеВКнигеПокупок", Ложь);
	РезультатСверки.Вставить("ЕстьНеСверенныеВКнигеПродаж",  Ложь);
	РезультатСверки.Вставить("ЕстьДанныеВКнигеПокупок",      Ложь);
	РезультатСверки.Вставить("ЕстьДанныеВКнигеПродаж",       Ложь);
	РезультатСверки.Вставить("КоличествоРасхожденийВКнигеПокупок", 0);
	РезультатСверки.Вставить("КоличествоНеСверенныхВКнигеПокупок", 0);
	РезультатСверки.Вставить("КоличествоРасхожденийВКнигеПродаж",  0);
	РезультатСверки.Вставить("КоличествоНеСверенныхВКнигеПродаж",  0);
	
	ТаблицыСверки = Новый Структура("ТаблицаНеСверили, ТаблицаРасхождения,
		|ТаблицаНетУКонтрагента, ТаблицаНетВОрганизации, ТаблицаБезРасхождений");
	
	АдресХранилища = ПоместитьВоВременноеХранилище(,Новый УникальныйИдентификатор);
	СтруктураПараметров.ПараметрыОтчета.ДанныеДляПроверкиКонтрагентов.Вставить("РазделыОтчета", ПроверкаКонтрагентов.НоваяТаблицаРазделыОтчета());
	Если Не СтруктураПараметров.ТолькоКнигаПродаж Тогда
		Отчеты.КнигаПокупок.СформироватьОтчет(СтруктураПараметров.ПараметрыОтчета, АдресХранилища);
		ПараметрыОтчета = СтруктураПараметров.ПараметрыОтчета;
		Отчеты.КнигаПокупок.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
		
		Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
		ДанныеКнигиПокупок = Результат.ДанныеДляСверкиДеклараций.ДанныеОтчета;
		
		СтруктураПараметров.ДанныеСверки = ДанныеКнигиПокупок;
		СтруктураПараметров.КодВидаОперации = СервисСверкиРасчетовСверкаДекларацийНДС.НеСверяемыеКодыОпераций("КнигаПокупок");
		Результат = СервисСверкиРасчетовПереопределяемый.ПолучитьДанныеСверкиДекларации(СтруктураПараметров);
		
		ТаблицыСверки.ТаблицаНеСверили       = Результат[5];
		ТаблицыСверки.ТаблицаРасхождения     = Результат[6];
		ТаблицыСверки.ТаблицаБезРасхождений  = Результат[7];
		ТаблицыСверки.ТаблицаНетУКонтрагента = Результат[8];
		
		СтруктураПроверки = ПроверитьНаличиеРасхожденийДляПомощникаНДС(ТаблицыСверки);
		РезультатСверки.ЕстьРасхожденияВКнигеПокупок       = СтруктураПроверки.ЕстьРасхождения;
		РезультатСверки.ЕстьНеСверенныеВКнигеПокупок       = СтруктураПроверки.ЕстьНеСверенные;
		РезультатСверки.ЕстьДанныеВКнигеПокупок            = СтруктураПроверки.ЕстьДанные;
		РезультатСверки.КоличествоРасхожденийВКнигеПокупок = СтруктураПроверки.КоличествоРасхождений;
		РезультатСверки.КоличествоНеСверенныхВКнигеПокупок = СтруктураПроверки.КоличествоНеСверенных;
	Иначе
		РезультатСверки.ЕстьРасхожденияВКнигеПокупок       = СтруктураПараметров.ДанныеДляСверкиДеклараций.ЕстьРасхожденияВКнигеПокупок;
		РезультатСверки.ЕстьНеСверенныеВКнигеПокупок       = СтруктураПараметров.ДанныеДляСверкиДеклараций.ЕстьНеСверенныеВКнигеПокупок;
		РезультатСверки.ЕстьДанныеВКнигеПокупок            = СтруктураПараметров.ДанныеДляСверкиДеклараций.ЕстьДанныеВКнигеПокупок;
		РезультатСверки.КоличествоРасхожденийВКнигеПокупок = СтруктураПараметров.ДанныеДляСверкиДеклараций.КоличествоРасхожденийВКнигеПокупок;
		РезультатСверки.КоличествоНеСверенныхВКнигеПокупок = СтруктураПараметров.ДанныеДляСверкиДеклараций.КоличествоНеСверенныхВКнигеПокупок;
	КонецЕсли;
	
	Если Не СтруктураПараметров.ТолькоКнигаПокупок Тогда
		СтруктураПараметров.ПараметрыОтчета.Удалить("ЭтоКнигаПокупок");
		СтруктураПараметров.ПараметрыОтчета.Вставить("ЭтоКнигаПродаж", Истина);
		
		Отчеты.КнигаПродаж.СформироватьОтчет(СтруктураПараметров.ПараметрыОтчета, АдресХранилища);
		Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
		ДанныеКнигиПродаж = Результат.ДанныеДляСверкиДеклараций.ДанныеОтчета;
		
		СтруктураПараметров.ДанныеСверки            = ДанныеКнигиПродаж;
		СтруктураПараметров.КодВидаОперации         = СервисСверкиРасчетовСверкаДекларацийНДС.НеСверяемыеКодыОпераций("КнигаПродаж");
		СтруктураПараметров.ОперацияНетВОрганизации = СервисСверкиРасчетовСверкаДекларацийНДС.ОперацииПоКоторымВыводимСФОтсутствующиеВОрганизации();
		Результат = СервисСверкиРасчетовПереопределяемый.ПолучитьДанныеСверкиДекларации(СтруктураПараметров, Ложь);
		
		ТаблицыСверки.ТаблицаНеСверили       = Результат[5];
		ТаблицыСверки.ТаблицаРасхождения     = Результат[6];
		ТаблицыСверки.ТаблицаБезРасхождений  = Результат[7];
		ТаблицыСверки.ТаблицаНетУКонтрагента = Результат[8];
		ТаблицыСверки.ТаблицаНетВОрганизации = Результат[9];
		
		СтруктураПроверки = ПроверитьНаличиеРасхожденийДляПомощникаНДС(ТаблицыСверки);
		РезультатСверки.ЕстьРасхожденияВКнигеПродаж       = СтруктураПроверки.ЕстьРасхождения;
		РезультатСверки.ЕстьНеСверенныеВКнигеПродаж       = СтруктураПроверки.ЕстьНеСверенные;
		РезультатСверки.ЕстьДанныеВКнигеПродаж            = СтруктураПроверки.ЕстьДанные;
		РезультатСверки.КоличествоРасхожденийВКнигеПродаж = СтруктураПроверки.КоличествоРасхождений;
		РезультатСверки.КоличествоНеСверенныхВКнигеПродаж = СтруктураПроверки.КоличествоНеСверенных;
		
	Иначе
		РезультатСверки.ЕстьРасхожденияВКнигеПродаж       = СтруктураПараметров.ДанныеДляСверкиДеклараций.ЕстьРасхожденияВКнигеПродаж;
		РезультатСверки.ЕстьНеСверенныеВКнигеПродаж       = СтруктураПараметров.ДанныеДляСверкиДеклараций.ЕстьНеСверенныеВКнигеПродаж;
		РезультатСверки.ЕстьДанныеВКнигеПродаж            = СтруктураПараметров.ДанныеДляСверкиДеклараций.ЕстьДанныеВКнигеПродаж;
		РезультатСверки.КоличествоРасхожденийВКнигеПродаж = СтруктураПараметров.ДанныеДляСверкиДеклараций.КоличествоРасхожденийВКнигеПродаж;
		РезультатСверки.КоличествоНеСверенныхВКнигеПродаж = СтруктураПараметров.ДанныеДляСверкиДеклараций.КоличествоНеСверенныхВКнигеПродаж;
	КонецЕсли;
	
	СтруктураПараметров.ДанныеСверки    = Неопределено;
	СтруктураПараметров.ПараметрыОтчета = Неопределено;
	
	Возврат ПоместитьВоВременноеХранилище(РезультатСверки, СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления: помечает не помеченные ранее документы к обмену с сервисом сверки
//
Процедура ЗарегистрироватьДокументыКОбменуССервисомСверки() Экспорт
	
	Если Не СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ФильтрСписокСчетов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&ФильтрСписокСчетов)
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	ХозрасчетныйОбороты.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОбороты.Период КАК Дата,
	|	ДанныеПервичныхДокументов.НомерРегистратора КАК Номер,
	|	ХозрасчетныйОбороты.Регистратор КАК Документ,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаНДСПоАренде)
	|				И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ИзменениеУсловийАренды
	|			ТОГДА 0
	|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.КорректировкаПоступления
	|				ИЛИ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА 0
	|		ИНАЧЕ ХозрасчетныйОбороты.СуммаОборотДт
	|	КОНЕЦ КАК Дебет,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаНДСПоАренде)
	|				И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ИзменениеУсловийАренды
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт
	|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.КорректировкаПоступления
	|				ИЛИ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт
	|		ИНАЧЕ ХозрасчетныйОбороты.СуммаОборотКт
	|	КОНЕЦ КАК Кредит,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, НЕОПРЕДЕЛЕНО) КАК НомерВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаВходящегоДокумента
	|ПОМЕСТИТЬ ДокументыПредварительная
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ФильтрСписокСчетов.Счет
	|				ИЗ
	|					ФильтрСписокСчетов),
	|			&АналитикаРасчетов,
	|			Организация В (&МассивОрганизаций)
	|				И Субконто1 В (&МассивКонтрагентов),
	|			,
	|			) КАК ХозрасчетныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация В (&МассивОрганизаций))
	|			И ХозрасчетныйОбороты.Регистратор = ДанныеПервичныхДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ПО ХозрасчетныйОбороты.Организация = СервисСверкиРасчетовРеестрДокументов.Организация
	|			И ХозрасчетныйОбороты.Субконто1 = СервисСверкиРасчетовРеестрДокументов.Контрагент
	|			И ХозрасчетныйОбороты.Регистратор = СервисСверкиРасчетовРеестрДокументов.Документ
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСчет В
	|					(ВЫБРАТЬ
	|						ФильтрСписокСчетов.Счет
	|					ИЗ
	|						ФильтрСписокСчетов)
	|				ТОГДА ХозрасчетныйОбороты.КорСубконто1 В (&МассивКонтрагентов)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И НЕ(ХозрасчетныйОбороты.СуммаОборотДт = 0
	|				И ХозрасчетныйОбороты.СуммаОборотКт = 0)
	|	И СервисСверкиРасчетовРеестрДокументов.Документ ЕСТЬ NULL
	|	И ТИПЗНАЧЕНИЯ(ХозрасчетныйОбороты.Регистратор) В (&ТипыСверяемыхДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПредварительная.Организация КАК Организация,
	|	ДокументыПредварительная.Контрагент КАК Контрагент,
	|	ДокументыПредварительная.Документ КАК Документ,
	|	ДокументыПредварительная.Дата КАК Дата,
	|	ДокументыПредварительная.Номер КАК Номер,
	|	ДокументыПредварительная.Дебет КАК Дебет,
	|	ДокументыПредварительная.Кредит КАК Кредит,
	|	ДокументыПредварительная.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ДокументыПредварительная.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента
	|ИЗ
	|	ДокументыПредварительная КАК ДокументыПредварительная
	|ГДЕ
	|	(ДокументыПредварительная.Дебет <> 0
	|			ИЛИ ДокументыПредварительная.Кредит <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Документ";
	
	ДатаНачалаСверкиРасчетов    = СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов();
	ДатаОкончанияСверкиРасчетов = КонецКвартала(ДобавитьМесяц(ТекущаяДата(), 3));
	
	ПодключенныеОрганизации = БизнесСеть.ПодключенныеОрганизации();
	МассивОрганизаций       = ПодключенныеОрганизации.ВыгрузитьКолонку("Организация");
	
	ТаблицаСчетов = УчетВзаиморасчетов.ПолучитьТаблицуСчетовУчетаВзаиморасчетов(Истина, Ложь);
	ФильтрСписокСчетов = ТаблицаСчетов.ВыгрузитьКолонку("СчетРасчетов");
	
	АналитикаРасчетов = Новый Массив();
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	ТипыСверяемыхДокументов = Метаданные.ОпределяемыеТипы.СервисСверкиРасчетовДокументыПодлежащиеСверке.Тип.Типы();
	
	Запрос.УстановитьПараметр("ДатаНачала",              ДатаНачалаСверкиРасчетов);
	Запрос.УстановитьПараметр("ДатаОкончания",           Новый Граница(КонецДня(ДатаОкончанияСверкиРасчетов), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("МассивОрганизаций",       МассивОрганизаций);
	Запрос.УстановитьПараметр("МассивКонтрагентов",      СервисСверкиРасчетов.КонтрагентыДляСверки());
	Запрос.УстановитьПараметр("ФильтрСписокСчетов",      ФильтрСписокСчетов);
	Запрос.УстановитьПараметр("АналитикаРасчетов",       АналитикаРасчетов);
	Запрос.УстановитьПараметр("СчетаИсключения",         Документы.АктСверкиВзаиморасчетов.СчетаИсключения());
	Запрос.УстановитьПараметр("СчетаНДСПоАренде",        ПланыСчетов.Хозрасчетный.СчетаНДСПоАренднымОбязательствам());
	Запрос.УстановитьПараметр("ТипыСверяемыхДокументов", ТипыСверяемыхДокументов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
		НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация            = Выборка.Организация;
		Запись.Контрагент             = Выборка.Контрагент;
		Запись.Документ               = Выборка.Документ;
		Если ЗначениеЗаполнено(Выборка.ДатаВходящегоДокумента) Тогда
			Запись.ДатаДокумента      = Выборка.ДатаВходящегоДокумента;
		Иначе
			Запись.ДатаДокумента      = Выборка.Дата;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.НомерВходящегоДокумента) Тогда
			ПолныйНомер = Выборка.НомерВходящегоДокумента;
		Иначе
			ПолныйНомер = Выборка.Номер;
		КонецЕсли;
		Запись.НомерДокумента         = СервисСверкиРасчетов.НомерБезПрефиксовИЛидирующихНулей(ПолныйНомер);
		Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
		Запись.ИдентификаторДокумента = Выборка.Документ.УникальныйИдентификатор();
		Запись.ИдентификаторСообщения = "";
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЦикла
	
КонецПроцедуры

// Обработчик обновления: помечает документы займов к обмену с сервисом сверки
//
Процедура ЗарегистрироватьДокументыПоЗаймамКОбменуССервисомСверки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
	|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|ГДЕ
	|	ВЫБОР
	|			КОГДА СервисСверкиРасчетовРеестрДокументов.Документ ССЫЛКА Документ.ПоступлениеНаРасчетныйСчет
	|				ТОГДА ВЫРАЗИТЬ(СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ.ПоступлениеНаРасчетныйСчет).ВидОперации В (&ВидыОперацийЗаймы)
	|			КОГДА СервисСверкиРасчетовРеестрДокументов.Документ ССЫЛКА Документ.СписаниеСРасчетногоСчета
	|				ТОГДА ВЫРАЗИТЬ(СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ.СписаниеСРасчетногоСчета).ВидОперации В (&ВидыОперацийЗаймы)
	|			КОГДА СервисСверкиРасчетовРеестрДокументов.Документ ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|				ТОГДА ВЫРАЗИТЬ(СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ.ПриходныйКассовыйОрдер).ВидОперации В (&ВидыОперацийЗаймы)
	|			КОГДА СервисСверкиРасчетовРеестрДокументов.Документ ССЫЛКА Документ.РасходныйКассовыйОрдер
	|				ТОГДА ВЫРАЗИТЬ(СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ.РасходныйКассовыйОрдер).ВидОперации В (&ВидыОперацийЗаймы)
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И СервисСверкиРасчетовРеестрДокументов.Статус <> ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке)";
	
	МассивВидовОперацийЗаймы = Новый Массив();
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПолучениеЗайма);
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийПКО.ПолучениеЗайма);
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыдачаЗаймаКонтрагенту);
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийРКО.ВыдачаЗаймаКонтрагенту);
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратЗаймаКонтрагентом);
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийПКО.ВозвратЗаймаКонтрагентом);
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма);
	МассивВидовОперацийЗаймы.Добавить(Перечисления.ВидыОперацийРКО.ВозвратЗайма);
	Запрос.УстановитьПараметр("ВидыОперацийЗаймы", МассивВидовОперацийЗаймы);
	
	ЗарегистрироватьДокументыКОбменуССервисомСверкиПоУсловию(Запрос);
	
КонецПроцедуры

// Обработчик обновления: помечает документы "Поступление (акт, накладная, УПД)" с видом операции "Услуги аренды"
// и способом учета "Плата за право пользования предметами аренды" к обмену с сервисом сверки
Процедура ЗарегистрироватьДокументыПравоПользованияПредметамиАренды() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
	|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ПО СервисСверкиРасчетовРеестрДокументов.Документ = ПоступлениеТоваровУслуг.Ссылка 
	|ГДЕ
	|	СервисСверкиРасчетовРеестрДокументов.Статус <> ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке)
	|	И ПоступлениеТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды)
	|	И ПоступлениеТоваровУслуг.ВедетсяУчетПредметовАренды";
	
	ЗарегистрироватьДокументыКОбменуССервисомСверкиПоУсловию(Запрос);
	
КонецПроцедуры

// Отложенный обработчик обновления: устанавливает значения ресурсов НомерДокумента и ДатаДокумента в реестре
//
Процедура ЗаполнитьДатуНомерДокументовРеестра(ПараметрыВыполнения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
	|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ,
	|	СервисСверкиРасчетовРеестрДокументов.Статус КАК Статус,
	|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторСообщения КАК ИдентификаторСообщения,
	|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
	|	СервисСверкиРасчетовРеестрДокументов.НомерДокумента КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Номер КАК Номер,
	|	ДанныеПервичныхДокументов.Дата КАК Дата,
	|	ДанныеПервичныхДокументов.ДатаРегистратора КАК ДатаРегистратора,
	|	ДанныеПервичныхДокументов.НомерРегистратора КАК НомерРегистратора
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО СервисСверкиРасчетовРеестрДокументов.Организация = ДанныеПервичныхДокументов.Организация
	|			И СервисСверкиРасчетовРеестрДокументов.Документ = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	(СервисСверкиРасчетовРеестрДокументов.ДатаДокумента = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ СервисСверкиРасчетовРеестрДокументов.НомерДокумента = """")
	|	И НЕ ДанныеПервичныхДокументов.Документ ЕСТЬ NULL
	|	И (ДанныеПервичныхДокументов.Номер <> """"
	|			ИЛИ ДанныеПервичныхДокументов.НомерРегистратора <> """")
	|	И (ДанныеПервичныхДокументов.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ ДанныеПервичныхДокументов.ДатаРегистратора <> ДАТАВРЕМЯ(1, 1, 1))";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыВыполнения.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ПараметрыВыполнения.ОбработкаЗавершена = Ложь;
	
	ИмяПроцедуры = "РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.ЗаполнитьДатуНомерДокументовРеестра()";
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СервисСверкиРасчетовРеестрДокументов");
			ЭлементБлокировки.УстановитьЗначение("Организация", Выборка.Организация);
			ЭлементБлокировки.УстановитьЗначение("Контрагент",  Выборка.Контрагент);
			ЭлементБлокировки.УстановитьЗначение("Документ",    Выборка.Документ);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				
				Если ЗначениеЗаполнено(Выборка.Дата) Тогда
					Запись.ДатаДокумента  = Выборка.Дата;
				Иначе
					Запись.ДатаДокумента  = Выборка.ДатаРегистратора;
				КонецЕсли;
				Если ЗначениеЗаполнено(Выборка.Номер) Тогда
					ПолныйНомер = Выборка.Номер;
				Иначе
					ПолныйНомер = Выборка.НомерРегистратора;
				КонецЕсли;
				Запись.НомерДокумента     = СервисСверкиРасчетов.НомерБезПрефиксовИЛидирующихНулей(ПолныйНомер);
				Если Запись.НомерДокумента <> ПолныйНомер Тогда
					Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
					Запись.ИдентификаторСообщения = "";
				КонецЕсли;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ЗафиксироватьТранзакцию();
			
		Исключение
			// Если не удалось обработать какую-либо запись, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В процедуре %1 не удалось обработать документ %2 по причине:
					|%3'"),
					ИмяПроцедуры,
					Выборка.Документ,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.СервисСверкиРасчетовРеестрДокументов, Выборка.Документ, ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось обработать записи: в %2 из %3 возникли ошибки'"),
			ИмяПроцедуры,
			ПроблемныхОбъектов,
			ОбъектовОбработано);
		
		ВызватьИсключение ТекстСообщения;
		
	Иначе
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.РегистрыСведений.СервисСверкиРасчетовРеестрДокументов, ,
			СтрШаблон(НСтр("ru = 'Процедура %1 обработала очередную порцию записей: %2 записей'"),
				ИмяПроцедуры,
				ОбъектовОбработано));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереопределитьНомерДатуКорректировокРеализации() Экспорт
	
	Если Не СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
	|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.НомерИсправляемогоКорректировочногоДокумента <> """"
	|			ТОГДА КорректировкаРеализации.НомерИсправляемогоКорректировочногоДокумента
	|		ИНАЧЕ КорректировкаРеализации.НомерИсходногоДокумента
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.ДатаИсправляемогоКорректировочногоДокумента <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА КорректировкаРеализации.ДатаИсправляемогоКорректировочногоДокумента
	|		ИНАЧЕ КорректировкаРеализации.ДатаИсходногоДокумента
	|	КОНЕЦ КАК Дата
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|			ПО (НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления)
	|				И КорректировкаРеализации.Ссылка = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ПО СервисСверкиРасчетовРеестрДокументов.Документ = КорректировкаРеализации.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка ЕСТЬ NULL
	|	И (КорректировкаРеализации.НомерИсходногоДокумента <> """"
	|			ИЛИ КорректировкаРеализации.НомерИсправляемогоКорректировочногоДокумента <> """")
	|	И КорректировкаРеализации.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ИмяПроцедуры = "РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.ПереопределитьНомерДатуКорректировокРеализации()";
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СервисСверкиРасчетовРеестрДокументов");
			ЭлементБлокировки.УстановитьЗначение("Организация", Выборка.Организация);
			ЭлементБлокировки.УстановитьЗначение("Контрагент",  Выборка.Контрагент);
			ЭлементБлокировки.УстановитьЗначение("Документ",    Выборка.Документ);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.ДатаДокумента          = Выборка.Дата;
				Запись.НомерДокумента         = Выборка.Номер;
				Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
				Запись.ИдентификаторСообщения = "";
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В процедуре %1 не удалось обработать документ %2 по причине:
					|%3'"),
					ИмяПроцедуры,
					Выборка.Документ,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.СервисСверкиРасчетовРеестрДокументов, Выборка.Документ, ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Функция КонтрагентПодлежитСверке(Контрагент) Экспорт
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ИННВведенКорректно") = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Справочники.Контрагенты.ЭтоИностраннаяОрганизация(Контрагент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция КонтрагентыДляСверки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ПометкаУдаления
	|	И Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И Контрагенты.ИННВведенКорректно
	|	И ВЫБОР
	|			КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|				ТОГДА Контрагенты.КППВведенКорректно
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";
	ТаблицаКонтрагенты = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаКонтрагенты.ВыгрузитьКолонку("Контрагент");
	
КонецФункции

Функция ДанныеРегистраДополнительныхФайлов(ПараметрыЧтения) Экспорт
	
	ЗаписьРегистраСведений = РегистрыСведений.ДополнительныеФайлыРегламентированныхОтчетов.СоздатьМенеджерЗаписи();
	
	ЗаписьРегистраСведений.РегламентированныйОтчет = ПараметрыЧтения.РегламентированныйОтчет;
	ЗаписьРегистраСведений.ВидДополнительногоФайла = ПараметрыЧтения.ВидДополнительногоФайла;
	
	ЗаписьРегистраСведений.Прочитать();
	
	Если ЗначениеЗаполнено(ЗаписьРегистраСведений.ВидДополнительногоФайла) Тогда
		Данные = ЗаписьРегистраСведений.СодержимоеФайла.Получить();
	Иначе
		Данные = Неопределено;
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ПроверитьНаличиеРасхожденийДляПомощникаНДС(ТаблицыСверки)
	СтруктураВозврата = Новый Структура(
		"ЕстьРасхождения, ЕстьНеСверенные, ЕстьДанные, КоличествоРасхождений, КоличествоНеСверенных",
		Ложь, Ложь, Ложь, 0, 0);
		
	ТаблицаДанных = ТаблицыСверки.ТаблицаНеСверили.Выбрать();
	Если ТаблицаДанных.Следующий() Тогда
		СтруктураВозврата.ЕстьНеСверенные       = Истина;
		СтруктураВозврата.ЕстьДанные            = Истина;
		СтруктураВозврата.КоличествоНеСверенных = ТаблицаДанных.КоличествоДокументов;
	КонецЕсли;

	ТаблицаДанных = ТаблицыСверки.ТаблицаРасхождения.Выбрать();
	Если ТаблицаДанных.Следующий() Тогда
		СтруктураВозврата.ЕстьРасхождения       = Истина;
		СтруктураВозврата.ЕстьДанные            = Истина;
		СтруктураВозврата.КоличествоРасхождений = ТаблицаДанных.КоличествоДокументов;
	КонецЕсли;
	
	ТаблицаДанных = ТаблицыСверки.ТаблицаНетУКонтрагента.Выбрать();
	Если ТаблицаДанных.Следующий() Тогда
		СтруктураВозврата.ЕстьРасхождения       = Истина;
		СтруктураВозврата.ЕстьДанные            = Истина;
		СтруктураВозврата.КоличествоРасхождений = СтруктураВозврата.КоличествоРасхождений + ТаблицаДанных.КоличествоДокументов;
	КонецЕсли;
	
	Если ТаблицыСверки.ТаблицаНетВОрганизации <> Неопределено Тогда
		ТаблицаДанных = ТаблицыСверки.ТаблицаНетВОрганизации.Выбрать();
		Если ТаблицаДанных.Следующий() Тогда
			СтруктураВозврата.ЕстьРасхождения       = Истина;
			СтруктураВозврата.ЕстьДанные            = Истина;
			СтруктураВозврата.КоличествоРасхождений = СтруктураВозврата.КоличествоРасхождений + ТаблицаДанных.КоличествоДокументов;
		КонецЕсли;
	КонецЕсли;

	Если ТаблицыСверки.ТаблицаБезРасхождений.Выбрать().Следующий() Тогда
		СтруктураВозврата.ЕстьДанные = Истина;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
			
КонецФункции

// Возвращает наименование картинки, являющейся логотипом помощника конфигурации.
//
// Возвращаемое значение:
//   Строка - наименование картинки, содержащейся в коллекции БиблиотекаКартинок конфигурации.
//
Функция КартинкаЛоготипаПомощника() Экспорт
	
	Возврат "КотСервисСверкиРасчетов";
	
КонецФункции

// См. СервисСверкиРасчетов.ДанныеОРелевантныхКонтрагентах()
Функция ДанныеОРелевантныхКонтрагентах() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаПодключенныеКонтрагенты();
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВсеПодключенныеКонтрагенты = Результат.Выгрузить();
	
	ТаблицаСчетов = УчетВзаиморасчетов.ПолучитьТаблицуСчетовУчетаВзаиморасчетов(Истина, Ложь);
	ФильтрСписокСчетов = ТаблицаСчетов.ВыгрузитьКолонку("СчетРасчетов");
	
	АналитикаРасчетов = Новый Массив();
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	ДатаНачалаСверкиРасчетов    = СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов();
	ДатаОкончанияСверкиРасчетов = КонецКвартала(ДобавитьМесяц(ТекущаяДата(), 3));
	
	Запрос.УстановитьПараметр("ДатаНачала",         ДатаНачалаСверкиРасчетов);
	Запрос.УстановитьПараметр("ДатаОкончания",      Новый Граница(КонецДня(ДатаОкончанияСверкиРасчетов), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ФильтрСписокСчетов", ФильтрСписокСчетов);
	Запрос.УстановитьПараметр("СчетаИсключения",    Документы.АктСверкиВзаиморасчетов.СчетаИсключения());
	Запрос.УстановитьПараметр("АналитикаРасчетов",  АналитикаРасчетов);

	Запрос.Текст = ТекстЗапросаРелевантныеКонтрагенты();
	РелевантныеКонтрагенты = Запрос.Выполнить().Выгрузить();

	// Если к сервису подключены менее трех релевантных контрагентов, то считаем, что рассказывать об этом рано.
	МинимальноеКоличествоКонтрагентовДляПоказаБаннера = 3;
	Если РелевантныеКонтрагенты.Количество() < МинимальноеКоличествоКонтрагентовДляПоказаБаннера Тогда
		Возврат Неопределено;
	КонецЕсли;

	КоличествоПодключенныхКонтрагентов = ВсеПодключенныеКонтрагенты.Количество();

	КэшДанныхОРелевантныхКонтрагентах = Новый Структура();
	КэшДанныхОРелевантныхКонтрагентах.Вставить("РелевантныеКонтрагенты", РелевантныеКонтрагенты);
	КэшДанныхОРелевантныхКонтрагентах.Вставить("КоличествоПодключенныхКонтрагентов", КоличествоПодключенныхКонтрагентов);
	
	Возврат КэшДанныхОРелевантныхКонтрагентах;
	
КонецФункции

#КонецОбласти 

#Область ПрочиеПроцедурыФункции

Процедура ЗарегистрироватьДокументыКОбменуССервисомСверкиПоУсловию(Запрос)
	
	Если Не СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
		НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
		НаборЗаписей.Прочитать();
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Запись.Статус = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЦикла
	
КонецПроцедуры

Функция ТекстЗапросаРелевантныеКонтрагенты()
	
	Возврат 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ФильтрСписокСчетов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&ФильтрСписокСчетов)
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто1 КАК Контрагент,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ХозрасчетныйОбороты.Регистратор) КАК КоличествоДокументов,
	|	ВЫРАЗИТЬ(СУММА(ХозрасчетныйОбороты.СуммаОборотДт + ХозрасчетныйОбороты.СуммаОборотКт) КАК ЧИСЛО(19, 0)) КАК Оборот
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ФильтрСписокСчетов.Счет
	|				ИЗ
	|					ФильтрСписокСчетов),
	|			&АналитикаРасчетов,
	|			Субконто1 В
	|				(ВЫБРАТЬ
	|					МассивКонтрагентов.Контрагент
	|				ИЗ
	|					МассивКонтрагентов),
	|			,
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСчет В
	|					(ВЫБРАТЬ
	|						ФильтрСписокСчетов.Счет
	|					ИЗ
	|						ФильтрСписокСчетов)
	|				ТОГДА НЕ ХозрасчетныйОбороты.КорСубконто1 В
	|							(ВЫБРАТЬ
	|								МассивКонтрагентов.Контрагент
	|							ИЗ
	|								МассивКонтрагентов)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И (ХозрасчетныйОбороты.СуммаОборотДт <> 0
	|			ИЛИ ХозрасчетныйОбороты.СуммаОборотКт <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1
	|
	|УПОРЯДОЧИТЬ ПО
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотДт + ХозрасчетныйОбороты.СуммаОборотКт) УБЫВ";
	
КонецФункции

Функция ТекстЗапросаПодключенныеКонтрагенты()
	
	Возврат
	"ВЫБРАТЬ
	|	КонтрагентыБизнесСеть.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ МассивКонтрагентов
	|ИЗ
	|	РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МассивКонтрагентов.Контрагент КАК Контрагент
	|ИЗ
	|	МассивКонтрагентов КАК МассивКонтрагентов";
	
КонецФункции

#КонецОбласти
