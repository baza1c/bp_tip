////////////////////////////////////////////////////////////////////////////////
// ВзаиморасчетыССотрудникамиПереопределяемый: переопределяемые методы учета взаиморасчетов, работающие на стороне сервера.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура дополняет таблицу сотрудников данными о порядке расчета аванса из настроек учета.
//
// Параметры:
//		Запрос                  - Запрос.
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц.
//		ТолькоРазрешенные       - Булево - признак отбора только разрещенных данных.
//		Параметры               - Структура - параметры получения.
//		ИмяВТСотрудники         - Строка  - имя временной таблицы с данными сотрудников.
//		КадровыеДанные          - Булево -  признак получения кадровых данных.
//
Процедура ДополнитьЗапросВТПлановыйАванс(Запрос, МенеджерВременныхТаблиц, ТолькоРазрешенные, Параметры, ИмяВТСотрудники, КадровыеДанные) Экспорт
	
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
	|	КадровыеДанныеСотрудников.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	&ОплатаТруда КАК СтатьяРасходов,
	|	&Зарплата КАК ВидДоходаИсполнительногоПроизводства,
	|	ВЫБОР
	|		КОГДА НастройкиУчетаЗарплаты.ИндивидуальныйАванс
	|			ТОГДА ВЫБОР
	|					КОГДА КадровыеДанныеСотрудников.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
	|						ТОГДА ЕСТЬNULL(КадровыеДанныеСотрудников.Аванс, 0)
	|					КОГДА КадровыеДанныеСотрудников.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПроцентомОтТарифа)
	|							И ЕСТЬNULL(КадровыеДанныеСотрудников.Аванс, 0) <> 0
	|						ТОГДА ЕСТЬNULL(КадровыеДанныеСотрудников.ФОТ, 0) * КадровыеДанныеСотрудников.Аванс / 100
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НастройкиУчетаЗарплаты.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
	|					ТОГДА ЕСТЬNULL(НастройкиУчетаЗарплаты.Аванс, 0)
	|				КОГДА НастройкиУчетаЗарплаты.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПроцентомОтТарифа)
	|						И ЕСТЬNULL(НастройкиУчетаЗарплаты.Аванс, 0) <> 0
	|					ТОГДА ЕСТЬNULL(КадровыеДанныеСотрудников.ФОТ, 0) * НастройкиУчетаЗарплаты.Аванс / 100
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаКВыплате
	|ПОМЕСТИТЬ ВТПлановыйАванс
	|ИЗ
	|	#ВТСотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО Сотрудники.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаЗарплаты КАК НастройкиУчетаЗарплаты
	|		ПО (НастройкиУчетаЗарплаты.Организация = &Организация)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСотрудникиИПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКадровыеДанныеСотрудников";
	
КонецПроцедуры	

// Процедура дополняет таблицу сотрудников данными о месте выплатых зарплаты.
//
// Параметры:
//		Запрос                  - Запрос.
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц.
//		Ведомость               - ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк, ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу.
//		ИмяВТСотрудники         - Строка, имя временной таблицы с данными сотрудников.
//
Процедура ДополнитьЗапросВТСотрудникиДляВедомостиПоМестуВыплаты(Запрос, МенеджерВременныхТаблиц, МестоВыплаты, ИмяВТСотрудники) Экспорт
	
	Если МестоВыплаты.Вид = Перечисления.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект Тогда
		// Отбираем сотрудников с л/с в этом проекте.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТСотрудникиПоМестуВыплаты
		|ИЗ
		|	#ВТСотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЛицевыеСчетаСотрудников КАК ЛицевыеСчетаСотрудников
		|		ПО Сотрудники.Сотрудник = ЛицевыеСчетаСотрудников.Сотрудник
		|			И (ЛицевыеСчетаСотрудников.НомерЛицевогоСчета ЕСТЬ НЕ NULL )
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестаВыплатыЗарплатыСотрудников КАК МестаВыплатыЗарплатыСотрудников
		|		ПО (МестаВыплатыЗарплатыСотрудников.Сотрудник = Сотрудники.Сотрудник)
		|ГДЕ
		|	МестаВыплатыЗарплатыСотрудников.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект)";
	ИначеЕсли МестоВыплаты.Вид = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет Тогда
		// Отбираем тех сотрудников, у которых место выплаты - банковский счет.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТСотрудникиПоМестуВыплаты
		|ИЗ
		|	#ВТСотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестаВыплатыЗарплатыСотрудников КАК МестаВыплатыЗарплатыСотрудников
		|		ПО (МестаВыплатыЗарплатыСотрудников.Сотрудник = Сотрудники.Сотрудник)
		|ГДЕ
		|	МестаВыплатыЗарплатыСотрудников.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.БанковскийСчет)";
	Иначе
		// Отбираем тех сотрудников, у которых место выплаты - касса.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТСотрудникиПоМестуВыплаты
		|ИЗ
		|	#ВТСотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестаВыплатыЗарплатыСотрудников КАК МестаВыплатыЗарплатыСотрудников
		|		ПО (МестаВыплатыЗарплатыСотрудников.Сотрудник = Сотрудники.Сотрудник)
		|ГДЕ
		|	ЕСТЬNULL(МестаВыплатыЗарплатыСотрудников.Вид, ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.Касса)) = ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.Касса)";
	КонецЕсли;
	
КонецПроцедуры

// Процедура проверяет оплаченность расчетного документа
// и возможность сформировать документы оплаты на его основании.
//
// Параметры:
//		РасчетныйДокумент     - ДокументСсылка - расчетный документ, который требуется оплатить.
//		МожноСоздатьВедомости - Булево
//
Процедура МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент, МожноСоздатьВедомости) Экспорт
	
	МожноСоздатьВедомости = УчетЗарплаты.МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент);
	
КонецПроцедуры

// Процедура дополняет таблицу сотрудников данными о порядке расчета аванса из настроек учета.
//
// Параметры:
//		Запрос                  - Запрос.
//		Организация             - СправочникСсылка.Организации
//
Процедура ДополнитьЗапросПлановымАвансом(Запрос, Организация) Экспорт
	
	ИндивидуальныйАванс = ПолучитьФункциональнуюОпцию("ИндивидуальныйАванс", Новый Структура("Организация", Организация));
	
	Если ИндивидуальныйАванс Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПлановыеНачисления.Сотрудник КАК Сотрудник,
		|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
		|	ПлановыеНачисления.Начисление КАК Начисление,
		|	ВЫБОР
		|		КОГДА &ПерваяПоловинаМесяца
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ ЕСТЬNULL(НастройкиУчетаЗарплаты.ИндивидуальныйАванс, ЛОЖЬ)
		|						ТОГДА ВЫБОР
		|								КОГДА НастройкиУчетаЗарплаты.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
		|									ТОГДА НастройкиУчетаЗарплаты.Аванс
		|								ИНАЧЕ ПлановыеНачисления.Размер * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0) / 100 * НастройкиУчетаЗарплаты.Аванс
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА НЕ ПлановыеАвансы.СпособРасчетаАванса ЕСТЬ NULL
		|								ТОГДА ВЫБОР
		|										КОГДА ПлановыеАвансы.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
		|											ТОГДА ПлановыеАвансы.Аванс
		|										ИНАЧЕ ПлановыеНачисления.Размер * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0) / 100 * ПлановыеАвансы.Аванс
		|									КОНЕЦ
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ ПлановыеНачисления.Размер * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0)
		|	КОНЕЦ КАК Результат,
		|	КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени КАК ДоляНеполногоРабочегоВремени,
		|	ВЫБОР
		|		КОГДА ПлановыеНачисления.Начисление.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда)
		|			ТОГДА ИСТИНА
		|		КОГДА ПлановыеНачисления.Начисление.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.Прочее)
		|				И ПлановыеНачисления.Начисление.ВходитВБазуРКИСН
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВходитВБазуРКиСН,
		|	&ПланируемаяДатаВыплаты КАК ПланируемаяДатаВыплаты
		|ПОМЕСТИТЬ ВТДанныеНачисленийПоСотрудникам
		|ИЗ
		|	ВТПлановыеНачисленияСрезПоследних КАК ПлановыеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК КадровыеДанныеСотрудников
		|		ПО ПлановыеНачисления.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеАвансыСрезПоследних КАК ПлановыеАвансы
		|		ПО ПлановыеНачисления.Сотрудник = ПлановыеАвансы.Сотрудник,
		|	РегистрСведений.НастройкиУчетаЗарплаты КАК НастройкиУчетаЗарплаты
		|ГДЕ
		|	ПлановыеНачисления.Размер <> 0
		|	И НастройкиУчетаЗарплаты.Организация = &Организация";
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
		|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
		|	МИНИМУМ(Начисления.Ссылка) КАК Начисление,
		|	ВЫБОР
		|		КОГДА &ПерваяПоловинаМесяца
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ ЕСТЬNULL(НастройкиУчетаЗарплаты.ИндивидуальныйАванс, ЛОЖЬ)
		|						ТОГДА ВЫБОР
		|								КОГДА НастройкиУчетаЗарплаты.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
		|									ТОГДА НастройкиУчетаЗарплаты.Аванс
		|								ИНАЧЕ КадровыеДанныеСотрудников.ТарифнаяСтавка * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0) / 100 * НастройкиУчетаЗарплаты.Аванс
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА НЕ ТекущаяТарифнаяСтавкаСотрудников.ТекущийСпособРасчетаАванса ЕСТЬ NULL
		|								ТОГДА ВЫБОР
		|										КОГДА ТекущаяТарифнаяСтавкаСотрудников.ТекущийСпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
		|											ТОГДА ТекущаяТарифнаяСтавкаСотрудников.ТекущийАванс
		|										ИНАЧЕ КадровыеДанныеСотрудников.ТарифнаяСтавка * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0) / 100 * ТекущаяТарифнаяСтавкаСотрудников.ТекущийАванс
		|									КОНЕЦ
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ КадровыеДанныеСотрудников.ТарифнаяСтавка * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0)
		|	КОНЕЦ КАК Результат,
		|	ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0) КАК ДоляНеполногоРабочегоВремени,
		|	ИСТИНА КАК ВходитВБазуРКиСН,
		|	&ПланируемаяДатаВыплаты КАК ПланируемаяДатаВыплаты
		|ПОМЕСТИТЬ ВТДанныеНачисленийПоСотрудникам
		|ИЗ
		|	ВТСотрудникиОрганизации КАК КадровыеДанныеСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
		|		ПО (Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
		|		ПО КадровыеДанныеСотрудников.Сотрудник = ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
		|	РегистрСведений.НастройкиУчетаЗарплаты КАК НастройкиУчетаЗарплаты
		|ГДЕ
		|	НастройкиУчетаЗарплаты.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	КадровыеДанныеСотрудников.Сотрудник,
		|	КадровыеДанныеСотрудников.Подразделение,
		|	ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0),
		|	ВЫБОР
		|		КОГДА &ПерваяПоловинаМесяца
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ ЕСТЬNULL(НастройкиУчетаЗарплаты.ИндивидуальныйАванс, ЛОЖЬ)
		|						ТОГДА ВЫБОР
		|								КОГДА НастройкиУчетаЗарплаты.СпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
		|									ТОГДА НастройкиУчетаЗарплаты.Аванс
		|								ИНАЧЕ КадровыеДанныеСотрудников.ТарифнаяСтавка * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0) / 100 * НастройкиУчетаЗарплаты.Аванс
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА НЕ ТекущаяТарифнаяСтавкаСотрудников.ТекущийСпособРасчетаАванса ЕСТЬ NULL
		|								ТОГДА ВЫБОР
		|										КОГДА ТекущаяТарифнаяСтавкаСотрудников.ТекущийСпособРасчетаАванса = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ФиксированнойСуммой)
		|											ТОГДА ТекущаяТарифнаяСтавкаСотрудников.ТекущийАванс
		|										ИНАЧЕ КадровыеДанныеСотрудников.ТарифнаяСтавка * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0) / 100 * ТекущаяТарифнаяСтавкаСотрудников.ТекущийАванс
		|									КОНЕЦ
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ КадровыеДанныеСотрудников.ТарифнаяСтавка * ЕСТЬNULL(КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени, 0)
		|	КОНЕЦ";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
