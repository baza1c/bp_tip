
Функция РеквизитыДоговораСтрокой(Номер, Дата) Экспорт
	
	// 123 от 08.12.1999
	
	Слова = Новый Массив;
	Если ЗначениеЗаполнено(Номер) Тогда
		Слова.Добавить(СокрЛП(Номер));
	КонецЕсли;
	Если ЗначениеЗаполнено(Дата) Тогда
		Слова.Добавить(СтрШаблон(НСтр("ru = 'от %1'"), Формат(Дата, "ДЛФ=D")));
	КонецЕсли;
	
	Возврат СтрСоединить(Слова, " ");
	
КонецФункции

Функция НаименованиеПоУмолчаниюБезРеквизитов() Экспорт
	
	// Такое наименование по умолчанию используется только тогда, когда договор нужно сохранить, но дата и номер договора неизвестны.
	// При интерактивном вводе договора наименование по умолчанию устанавливается исходя из номера и даты - см. РеквизитыДоговораСтрокой().
	
	Возврат НСтр("ru='Основной договор'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

Функция ПорядковыйНомерПола(Пол) Экспорт
	Перем НомерПола;
	
	Если Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский") Тогда
		НомерПола = 2;
	ИначеЕсли Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской") Тогда
		НомерПола = 1;
	КонецЕсли;
	
	Возврат НомерПола;
	
КонецФункции

// По имени параметра определяет порядковый номер падежа (от 1 до 6), к которому должен быть приведен параметр.
//
Функция ПадежПоИмениПараметра(Знач ИмяПараметра) Экспорт
	
	ПорядковыйНомерПадежа = 1;
	
	Если ПустаяСтрока(ИмяПараметра)
	 Или ВРег(Прав(ИмяПараметра, 5)) <> "ПАДЕЖ" Тогда
		Возврат ПорядковыйНомерПадежа;
	КонецЕсли;
	
	ИмяПараметра = ВРег(Лев(ИмяПараметра, СтрДлина(ИмяПараметра) - 5));
	
	Если СтрЗаканчиваетсяНа(ИмяПараметра, "РОДИТЕЛЬНЫЙ") Тогда
		ПорядковыйНомерПадежа = 2;
	ИначеЕсли СтрЗаканчиваетсяНа(ИмяПараметра, "ДАТЕЛЬНЫЙ") Тогда
	    ПорядковыйНомерПадежа = 3;
	ИначеЕсли СтрЗаканчиваетсяНа(ИмяПараметра, "ВИНИТЕЛЬНЫЙ") Тогда
	    ПорядковыйНомерПадежа = 4;
	ИначеЕсли СтрЗаканчиваетсяНа(ИмяПараметра, "ТВОРИТЕЛЬНЫЙ") Тогда
	    ПорядковыйНомерПадежа = 5;
	ИначеЕсли СтрЗаканчиваетсяНа(ИмяПараметра, "ПРЕДЛОЖНЫЙ") Тогда
	    ПорядковыйНомерПадежа = 6;
	КонецЕсли;
	
	Возврат ПорядковыйНомерПадежа;
	
КонецФункции

// Возвращаем цвет фона документа по умолчанию.
//
Функция ЦветФонаПоУмолчанию() Экспорт

	// Белый, используем абсолютный цвет, чтобы корректно можно было его сохранять в HTML.
	Возврат Новый Цвет(255, 255, 255);

КонецФункции

#Область ПрограммныйИнтерфейс

// Возвращает реквизиты дополнительного соглашения для заполнения и печати в договоре контрагента
//
// Возвращаемое значение:
//  Структура с ключами:
//   * НомерСоглашения - Строка - номер соглашения
//   * ДатаСоглашения - Дата - дата соглашения
//   * СоглашениеПодписано  Булево - признак того, что соглашение подписано
//   * ФайлСоглашения - СправочникСсылка.ДоговорыКонтрагентовПрисоединенныеФайлы - ссылка на сохраненный файл соглашения
//                    - Неопределено, если файл не сформирован
//   * КомментарийСоглашения - Строка - комментарий соглашения
Функция РеквизитыДополнительногоСоглашения() Экспорт
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("НомерСоглашения", "");
	Реквизиты.Вставить("ДатаСоглашения", '00010101');
	Реквизиты.Вставить("СоглашениеПодписано", Ложь);
	Реквизиты.Вставить("ФайлСоглашения", Неопределено);
	Реквизиты.Вставить("КомментарийСоглашения", "");
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти