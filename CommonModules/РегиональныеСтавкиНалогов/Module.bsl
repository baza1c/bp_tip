// Сервис Региональные ставки налогов

#Область ПрограммныйИнтерфейс

// Возвращает признак использования режима отладки
//
// Возвращаемое значение:
//   Булево
//
Функция РежимОтладки() Экспорт
	
	ПараметрЗапускаПриложения = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ПараметрЗапуска");
	
	Возврат СтрНайти(ПараметрЗапускаПриложения, "РежимОтладкиСервисаРегиональныеСтавкиНалогов") > 0;
	
КонецФункции

// Процедура получения ставок УСН в фоновом задании
//
// Параметры:
//  ПараметрыПолучения - Структура - см. РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаСтавкиУСН()
//  АдресХранилища - Строка - адрес во временном хранилище
//
Процедура ПолучитьСтавкиУСНВФоне(ПараметрыПолучения, АдресХранилища) Экспорт
	
	Результат = ПолучитьСтавкиУСН(ПараметрыПолучения);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Получает краткую информацию о региональных особенностях применения УСН, 
// помещает ее во временное хранилище.
//
// Параметры:
//  ПараметрыПолучения - Структура - см. РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаОписаниеСтавокУСН()
//  АдресХранилища - Строка	- адрес временного хранилища
//
Процедура ПолучитьОписаниеСтавокУСНВФоне(ПараметрыПолучения, АдресХранилища) Экспорт
	
	Результат = ПолучитьОписаниеСтавокУСН(ПараметрыПолучения);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Возвращает данные для расчета потенциального дохода.
//
// Параметры:
//  ПараметрыПолучения - Структура - параметры для запроса информации в сервисе
//    * КодРегиона - Строка - код субъекта РФ
//    * КодОКВЭД - Строка - код вида деятельности по классификатору ОКВЭД (ред.2)
//    * НаДату - Дата - необязательный элемент
//  АдресХранилища - Строка - адрес временного хранилища
//
Процедура ДифференциацияПотенциальногоДохода(ПараметрыПолучения, АдресХранилища) Экспорт
	
	Результат = ПолучитьДифференциациюПотенциальногоДохода(ПараметрыПолучения);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Возвращает данные для расчета потенциального дохода.
//
// Параметры:
//  ПараметрыПолучения - Структура - параметры для запроса информации в сервисе
//    * КодВидаДеятельности - Строка
//    * ИдентификаторМуниципальногоОбразования - Строка
//    * НаДату - Дата - необязательный элемент
//  АдресХранилища - Строка - адрес временного хранилища
//
Процедура РазмерыПотенциальногоДохода(ПараметрыПолучения, АдресХранилища) Экспорт
	
	Результат = ПолучитьРазмерыПотенциальногоДохода(ПараметрыПолучения);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Процедура получения видов деятельности в фоновом задании
//
// Параметры:
//  ПараметрыПолучения - Структура - см. РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаВидыДеятельностиПСН()
//  АдресХранилища - Строка - адрес во временном хранилище
//
Процедура ПолучитьВидыДеятельностиПатентаВФоне(ПараметрыПолучения, АдресХранилища) Экспорт
	
	Результат = ПолучитьВидыДеятельностиПатента(ПараметрыПолучения);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Процедура получения ставок ПСН в фоновом задании
//
// Параметры:
//  ПараметрыПолучения - Структура - см. РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаСтавкиПСН()
//  АдресХранилища - Строка - адрес во временном хранилище
//
Процедура ПолучитьСтавкиПСНВФоне(ПараметрыПолучения, АдресХранилища) Экспорт
	
	Результат = ПолучитьСтавкиПСН(ПараметрыПолучения);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Процедура получения текстового описания ставок ПСН в фоновом задании
//
// Параметры:
//  ПараметрыПолучения - Структура - см. РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаСтавкиПСН()
//  АдресХранилища - Строка - адрес во временном хранилище
//
Процедура ПолучитьОписаниеСтавокПСНВФоне(ПараметрыПолучения, АдресХранилища) Экспорт
	
	Результат = ПолучитьОписаниеСтавокПСН(ПараметрыПолучения);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание токена
//
// Возвращаемое значение:
//   Структура - структура соответствуют описанию JWT-токена
//
Функция СвойстваВременногоТокена() Экспорт
	
	Свойства = СвойстваТокена(ВременныйТокен());
	
	Возврат Свойства;
	
КонецФункции

// Процедура обновления временного токена фоновым заданием.
// Сохраняет токен в безопасном хранилище данных и возвращает его свойства.
//
// Параметры:
//   Параметры - Произвольный - не используется
//   АдресХранилища - Строка - адрес временного хранилища, в которое будет помещены свойства полученного временного токена
//
//
Процедура ОбновитьВременныйТокен(Параметры, АдресХранилища) Экспорт
	Перем СообщениеОбОшибке;
	
	ВременныйТокен = НовыйВременныйТокен(Ложь, СообщениеОбОшибке);
	Если ЗначениеЗаполнено(ВременныйТокен) Тогда
		Свойства = СвойстваТокена(ВременныйТокен);
	Иначе
		Свойства = Новый Структура("Ошибка", СообщениеОбОшибке);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Свойства, АдресХранилища);
	
КонецПроцедуры

// Удаляет временный токен
// Параметры:
//   СообщениеОбОшибке - Строка - в нее помещается текст ошибки, если она возникнет.
// Возвращаемое значение:
//   Булево - Истина, если удалось удалить токен, Ложь - если произошла ошибка.
//
Функция УдалитьВременныйТокен(СообщениеОбОшибке = "") Экспорт
	
	КлючЗаписиБезопасногоХранилища = РегистрыСведений.БезопасноеХранилищеДанныхОбщее.НовыйКлючЗаписи(ВладелецДанныхБезопасногоХранилища());
	Если Не ЗаблокироватьВременныйТокенДляОбновления(КлючЗаписиБезопасногоХранилища) Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось заблокировать временный токен для изменения'");
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.БезопасноеХранилищеДанныхОбщее.Удалить(ВладелецДанныхБезопасногоХранилища(), КлючВременногоТокена());
	УстановитьПривилегированныйРежим(Ложь);
	
	РазблокироватьВременныйТокен(КлючЗаписиБезопасногоХранилища);
	
	Возврат Истина;
	
КонецФункции

Функция ВладелецДанныхБезопасногоХранилища() Экспорт
	
	Возврат "БухгалтерияПредприятия.РегиональныеСтавкиНалогов";
	
КонецФункции

Функция МуниципальноеОбразованиеПоАдресуРегистрацииОрганизацииПоУмолчанию(КодРегиона, СписокВариантов) Экспорт
	
	Результат = "";
	
	Если СписокВариантов.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
	Если Не ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЮрАдресОрганизации = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияНаДату(
		ОрганизацияПоУмолчанию,
		Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	Если Не ЗначениеЗаполнено(ЮрАдресОрганизации.Значение) Тогда
		Возврат Результат;
	КонецЕсли;
	
	СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(
		ЮрАдресОрганизации.Значение,
		Новый Структура("КодыАдреса", Истина));
	
	Если ЗначениеЗаполнено(СведенияОбАдресе.КодРегиона) И СведенияОбАдресе.КодРегиона <> КодРегиона Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Ищем снизу вверх
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("Территория");
	ПоляПоиска.Добавить("НаселенныйПункт");
	ПоляПоиска.Добавить("ВнутригородскойРайон");
	ПоляПоиска.Добавить("Поселение");
	ПоляПоиска.Добавить("Город");
	ПоляПоиска.Добавить("МуниципальныйРайон");
	ПоляПоиска.Добавить("Район");
	
	Для каждого Поле Из ПоляПоиска Цикл
		ИдентификаторСтрокой = Строка(СведенияОбАдресе.Идентификаторы[Поле]);
		Если Не ПустаяСтрока(ИдентификаторСтрокой)
			И СписокВариантов.НайтиПоЗначению(ИдентификаторСтрокой) <> Неопределено Тогда
			Результат = ИдентификаторСтрокой;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПериодПоУмолчанию() Экспорт
	
	Если РежимОтладки() Тогда
		Возврат ОбщегоНазначения.ТекущаяДатаПользователя();
	Иначе
		Возврат ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтавкиУСН(ПараметрыПолучения) Экспорт
	
	АдресРесурса = АдресСтавкиУСН()
		+ СтрокаПараметровЗапросаКСервису(ПараметрыПолучения, 
			СоответствиеПараметровИнформацияУСНВВашемРегионе());
		
	Возврат ИнформацияСервиса(АдресРесурса);
	
КонецФункции

Функция ПолучитьОписаниеСтавокУСН(ПараметрыПолучения)
	
	АдресРесурса = АдресОписаниеСтавокУСН()
		+ СтрокаПараметровЗапросаКСервису(ПараметрыПолучения,
			СоответствиеПараметровЗаголовокДекорацииУСНВВашемРегионе());
		
	Возврат ИнформацияСервиса(АдресРесурса);
	
КонецФункции

Функция ПолучитьДифференциациюПотенциальногоДохода(ПараметрыПолучения)
	
	АдресРесурса = АдресДифференциацияПотенциальногоДохода()
		+ СтрокаПараметровЗапросаКСервису(ПараметрыПолучения, СоответствиеПараметровПатент());
	
	Возврат ИнформацияСервиса(АдресРесурса);
	
КонецФункции

Функция ПолучитьРазмерыПотенциальногоДохода(ПараметрыПолучения)
	
	АдресРесурса = АдресРазмерыПотенциальногоДохода()
		+ СтрокаПараметровЗапросаКСервису(ПараметрыПолучения, СоответствиеПараметровПатент());
	
	Возврат ИнформацияСервиса(АдресРесурса);
	
КонецФункции

Функция ПолучитьВидыДеятельностиПатента(ПараметрыПолучения)
	
	АдресРесурса = АдресВидыДеятельностиПатента()
		+ СтрокаПараметровЗапросаКСервису(ПараметрыПолучения, СоответствиеПараметровПатент());
	
	Возврат ИнформацияСервиса(АдресРесурса);
	
КонецФункции

Функция ПолучитьСтавкиПСН(ПараметрыПолучения) Экспорт
	
	АдресРесурса = АдресСтавкиПСН()
		+ СтрокаПараметровЗапросаКСервису(ПараметрыПолучения, СоответствиеПараметровПатент());
	
	Возврат ИнформацияСервиса(АдресРесурса);
	
КонецФункции

Функция ПолучитьОписаниеСтавокПСН(ПараметрыПолучения)
	
	АдресРесурса = АдресОписаниеСтавокПСН()
		+ СтрокаПараметровЗапросаКСервису(ПараметрыПолучения, СоответствиеПараметровПатент());
	
	Возврат ИнформацияСервиса(АдресРесурса);
	
КонецФункции

#Область РаботаССервисом

Функция ИнформацияСервиса(АдресРесурса)

	Результат = НовыйРезультатЗапросаСервис();
	
	Если Не РегиональныеСтавкиНалоговВызовСервера.ИспользуетсяСервисРегиональныхСтавок() Тогда
		Результат.ОписаниеОшибки = НСтр("ru='Сервис не используется'");
		Возврат Результат;
	КонецЕсли;
	
	АдресСервисаНачальный = АдресСервиса();
	СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресСервисаНачальный);
	
	ОшибкаДоступностиСервиса = СервисДоступен(СтруктураАдреса);
	Если Не ПустаяСтрока(ОшибкаДоступностиСервиса) Тогда
		Результат.ОписаниеОшибки = ОшибкаДоступностиСервиса;
		Возврат Результат;
	КонецЕсли;
	
	АдресСервисаТекущий = АдресСервиса();
	Если АдресСервисаНачальный <> АдресСервисаТекущий Тогда
		// При получении временного токена адрес изменился из-за перенаправления в другое приложение
		СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресСервисаТекущий);
	КонецЕсли;
	
	Соединение = СоединениеССервисом(СтруктураАдреса, ТаймаутСоединения());
	Запрос = ЗапросКСервису(СтруктураАдреса, АдресРесурса);
	
	Попытка
		Ответ = Соединение.Получить(Запрос);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписатьОшибкуСервисаВЖурналРегистрации(ТекстОшибки);
		Результат.ОписаниеОшибки = НСтр("ru = 'Не удалось получить данные сервиса.'");
		Возврат Результат;
	КонецПопытки;
	
	Результат.КодСостояния = Ответ.КодСостояния;
	ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
	КодыОтветаСервиса = РегиональныеСтавкиНалоговКлиентСервер.КодыОтветаСервиса();
	
	Если Ответ.КодСостояния = КодыОтветаСервиса.ПостояннаяПереадресация Тогда
		НовыйОтветСервиса = НовыйОтветСервиса(Ответ);
		УстановитьНовыйАдресСервиса(НовыйОтветСервиса.АдресПереадресации);
		Возврат ИнформацияСервиса(АдресРесурса);
	ИначеЕсли Ответ.КодСостояния = КодыОтветаСервиса.Успех Тогда
		Результат.Ответ = ЗначениеИзJSON(ТекстОтвета, Ложь);
		Возврат Результат;
	Иначе
		ЗаписатьОшибкуСервисаВЖурналРегистрации(СтрШаблон(
			НСтр("ru = 'Код состояния %1: %2'"),
			Ответ.КодСостояния,
			ТекстОтвета));
		Результат.ОписаниеОшибки = ТекстОтвета;
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

Функция СоединениеССервисом(СтруктураАдреса, Таймаут, Пользователь = Неопределено, Пароль = Неопределено)

	Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураАдреса.Схема);
	Соединение = Новый HTTPСоединение(
		СтруктураАдреса.Хост,
		СтруктураАдреса.Порт,
		Пользователь,
		Пароль,
		Прокси,
		Таймаут,
		?(СтруктураАдреса.Схема = "http", Неопределено, Новый ЗащищенноеСоединениеOpenSSL));
		
	Возврат Соединение;

КонецФункции

Функция ТаймаутСоединения()
	
	Возврат 60;
	
КонецФункции

Функция СервисДоступен(СтруктураАдреса)

	Результат = НСтр("ru='Сервис временно недоступен'");
	
	Соединение = СоединениеССервисом(СтруктураАдреса, 7);
	Запрос = ЗапросКСервису(СтруктураАдреса, АдресУСНPing());
	Попытка
		Ответ = Соединение.Получить(Запрос);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписатьОшибкуСервисаВЖурналРегистрации(ТекстОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Если Ответ.КодСостояния <> РегиональныеСтавкиНалоговКлиентСервер.КодыОтветаСервиса().Успех Тогда
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		ТекстОшибки = "" + Ответ.КодСостояния + "; " + ТекстОтвета;
		ЗаписатьОшибкуСервисаВЖурналРегистрации(ТекстОшибки);
		Возврат Результат;
	КонецЕсли;
	
	Возврат "";

КонецФункции

Функция ЗапросКСервису(СтруктураАдреса, АдресРесурса)

	Запрос = Новый HTTPЗапрос;
	Запрос.АдресРесурса = ?(ПустаяСтрока(СтруктураАдреса.ПутьНаСервере), 
		"", 
		"/" + СтруктураАдреса.ПутьНаСервере) + АдресРесурса;
	Запрос.Заголовки.Вставить("Content-Type", "application/json");
	Запрос.Заголовки.Вставить("accept", "application/json");
	
	Токен = ВременныйТокен();
	
	Если Токен <> Неопределено Тогда
		Запрос.Заголовки.Вставить("User-Token", Токен);
	КонецЕсли;
	
	Возврат Запрос;

КонецФункции

Функция НовыйРезультатЗапросаСервис()

	Результат = Новый Структура;
	Результат.Вставить("КодСостояния", 0);
	Результат.Вставить("Ответ", "");
	Результат.Вставить("ОписаниеОшибки", "");
	Возврат Результат;

КонецФункции

Процедура ЗаписатьОшибкуСервисаВЖурналРегистрации(ТекстОшибки)
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрацииСервис(),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		ТекстОшибки);
	
КонецПроцедуры

Функция ИмяСобытияЖурналаРегистрацииСервис()
	
	Результат = НСтр("ru = 'Региональные ставки налогов'", ОбщегоНазначения.КодОсновногоЯзыка());
	Возврат Результат;
	
КонецФункции

Функция АдресСервиса()
	
	Результат = Константы.АдресСервисаРегиональныеСтавкиНалогов.Получить();
	Если ПустаяСтрока(Результат) Тогда
		Результат = АдресСервисаПоУмолчанию();
		УстановитьНовыйАдресСервиса(Результат);
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция АдресУСНPing()
	
	Возврат "/usn/ping";
	
КонецФункции

Функция АдресСтавкиУСН()
	
	Возврат "/usn/rates";
	
КонецФункции

Функция СоответствиеПараметровИнформацияУСНВВашемРегионе()
	
	Результат = Новый Соответствие;
	Результат.Вставить("КодРегиона", "region");
	Результат.Вставить("КодВидаДеятельности", "okved");
	Результат.Вставить("ЭтоЮрЛицо", "isCompany");
	Результат.Вставить("ПрименяетсяУСНДоходы", "usnD");
	Результат.Вставить("ПрименяетсяУСНДоходыРасходы", "usnDR");
	Результат.Вставить("Период", "date");
	
	Возврат Результат;
	
КонецФункции

Функция АдресОписаниеСтавокУСН()
	
	Возврат "/usn/description";
	
КонецФункции

Функция СоответствиеПараметровЗаголовокДекорацииУСНВВашемРегионе()
	
	Результат = Новый Соответствие;
	Результат.Вставить("КодРегиона", "region");
	Результат.Вставить("КодВидаДеятельности", "okved");
	Результат.Вставить("ЭтоЮрЛицо", "isCompany");
	Результат.Вставить("ПрименяетсяУСНДоходы", "usnD");
	Результат.Вставить("ПрименяетсяУСНДоходыРасходы", "usnDR");
	Результат.Вставить("Период", "date");
	
	Возврат Результат;
	
КонецФункции

Функция СтрокаПараметровЗапросаКСервису(ВходныеПараметры, СоответствиеПараметров)
	
	ПараметрыЗапроса = Новый Массив;
	
	Для Каждого Параметр Из ВходныеПараметры Цикл
		ИмяПараметра = СоответствиеПараметров.Получить(Параметр.Ключ);
		Если ИмяПараметра <> Неопределено Тогда
			Если ЗначениеЗаполнено(Параметр.Значение) Тогда
				Значение = Параметр.Значение;
			Иначе
				Значение = ЗначениеПараметраПоУмолчанию(ИмяПараметра);
			КонецЕсли;
			ПредставлениеЗначения = ПредставлениеЗначенияПараметра(ИмяПараметра, Значение);
			ПараметрыЗапроса.Добавить(СтрШаблон("%1=%2", ИмяПараметра, ПредставлениеЗначения));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ?(ПараметрыЗапроса.Количество() > 0,
		"?" + СтрСоединить(ПараметрыЗапроса, "&"),
		"");
	
КонецФункции

Функция ЗначениеПараметраПоУмолчанию(ИмяПараметра)
	
	Если ИмяПараметра = "date" Тогда
		Возврат ПериодПоУмолчанию();
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ПредставлениеЗначенияПараметра(ИмяПараметра, Значение)
	
	Если ИмяПараметра = "date" Тогда
		Возврат Формат(Значение, "ДФ=yyyyMMdd");
	Иначе
		Возврат XMLСтрока(Значение);
	КонецЕсли;
	
КонецФункции

Функция АдресДифференциацияПотенциальногоДохода()
	
	Возврат "/patent/differentiation";
	
КонецФункции

Функция АдресРазмерыПотенциальногоДохода()
	
	Возврат "/patent/potentialgross";
	
КонецФункции

Функция АдресВидыДеятельностиПатента()
	
	Возврат "/patent/activities";
	
КонецФункции

Функция АдресСтавкиПСН()
	
	Возврат "/patent/rates";
	
КонецФункции

Функция АдресОписаниеСтавокПСН()
	
	Возврат "/patent/description";
	
КонецФункции

Функция СоответствиеПараметровПатент()
	
	Результат = Новый Соответствие;
	Результат.Вставить("КодВидаДеятельности", "activity");
	Результат.Вставить("КодРегиона", "region");
	Результат.Вставить("ИдентификаторТерритории", "territory");
	Результат.Вставить("КодОКВЭД", "okved");
	Результат.Вставить("Период", "date");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РаботаСJSON

Функция ЗначениеИзJSON(ТекстJSON, ВСоответствие, СвойстваТипаДата = Неопределено)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Попытка
		ЧтениеJSON.УстановитьСтроку(ТекстJSON);
		Значение = ПрочитатьJSON(ЧтениеJSON, ВСоответствие, СвойстваТипаДата);
	Исключение
		Значение = Неопределено;
	КонецПопытки;
	ЧтениеJSON.Закрыть();
	
	Возврат Значение;
	
КонецФункции

#КонецОбласти

#Область Авторизация

// Возвращает временный токен авторизации.
// Получает новый, если срок действия старого истек.
//
// Параметры:
//   СообщениеОбОшибке - Строка - Исходящий параметр
//
// Возвращаемое значение:
//  Строка
//
Функция ВременныйТокен(СообщениеОбОшибке = "")
	
	УстановитьПривилегированныйРежим(Истина);
	ВременныйТокен = РегистрыСведений.БезопасноеХранилищеДанныхОбщее.Прочитать(
		ВладелецДанныхБезопасногоХранилища(),
		КлючВременногоТокена());
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ТокенДейсвует(ВременныйТокен, ТаймаутСоединения()) Тогда
		ВременныйТокен = НовыйВременныйТокен(Истина, СообщениеОбОшибке);
	КонецЕсли;
	
	Возврат ВременныйТокен;
	
КонецФункции

// Запрашивает и возвращает новый временный токен авторизации
//
// Параметры:
//   ПроверятьСрокДействия - Булево - Ложь, если необходимо обновить токен принудительно без проверки срока действия
//   СообщениеОбОшибке - Строка - Исходящий параметр
//
// Возвращаемое значение:
//  Строка, Неопределено - Временный токен, Неопределено, если не удалось получить новый временный токен
//
Функция НовыйВременныйТокен(ПроверятьСрокДействия = Истина, СообщениеОбОшибке = "")
	
	Результат = Неопределено;
	
	КлючЗаписиБезопасногоХранилища = РегистрыСведений.БезопасноеХранилищеДанныхОбщее.НовыйКлючЗаписи(
		ВладелецДанныхБезопасногоХранилища());
	
	Если Не ЗаблокироватьВременныйТокенДляОбновления(КлючЗаписиБезопасногоХранилища) Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось заблокировать временный токен для изменения. Повторите попытку позже.'");
		Возврат Результат;
	КонецЕсли;
	
	Если ПроверятьСрокДействия Тогда
		
		// Блокировка установленя, проверим срок действия на случай, если другой сенс уже обновил токен.
		УстановитьПривилегированныйРежим(Истина);
		ВременныйТокен = РегистрыСведений.БезопасноеХранилищеДанныхОбщее.Прочитать(
			ВладелецДанныхБезопасногоХранилища(),
			КлючВременногоТокена());
		УстановитьПривилегированныйРежим(Ложь);
		
		ОбновитьТокен = Не ТокенДейсвует(ВременныйТокен, ТаймаутСоединения());
		Результат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВременныйТокен, "ТокенBase64");
	Иначе
		ОбновитьТокен = Истина;
	КонецЕсли;
	
	Если ОбновитьТокен Тогда
		
		ВременныйТокен = НовыйТокенАвторизации();
		Результат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВременныйТокен, "ТокенBase64");
		
		Если ТипЗнч(ВременныйТокен) = Тип("Структура") Тогда
			УстановитьПривилегированныйРежим(Истина);
			РегистрыСведений.БезопасноеХранилищеДанныхОбщее.Записать(
				КлючЗаписиБезопасногоХранилища.Владелец,
				ВременныйТокен,
				КлючВременногоТокена());
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	РазблокироватьВременныйТокен(КлючЗаписиБезопасногоХранилища);
	
	Возврат Результат;
	
КонецФункции

// Ключ токена в ответе сериса авторизации,
// он же - ключ значения в безопасном хранилизе данных
//
Функция КлючВременногоТокена()
	
	Возврат "ТокенBase64";
	
КонецФункции

// Определяет действует ли токен по его описанию.
//
// Параметры:
//   Токен - Строка - JWT-токен
//   Чувствительность - Число - время, необходимое для гарантированного выполнения запроса с предъявлением токена.
//
// Возвращаемое значение:
//   Булево
//
Функция ТокенДейсвует(Токен, Чувствительность = 0)
	Перем СрокДействияUnixTime;
	
	СвойстваВременногоТокена = СвойстваТокена(Токен);
	
	Если СвойстваВременногоТокена.Свойство("exp", СрокДействияUnixTime) Тогда
		СрокДействия = ДатаИзUnixTime(СрокДействияUnixTime);
	КонецЕсли;
	
	Возврат СрокДействия <> Неопределено И СрокДействия > (ТекущаяДатаСеанса() - Чувствительность);
	
КонецФункции

Функция ДатаИзUnixTime(UnixTime)
	
	Возврат МестноеВремя(Дата(1970, 1, 1) + UnixTime);
	
КонецФункции

// Возвращает свойства JWT-токена в стуктуре
//
// Параметры:
//   Токен - Строка - JWT-токен
//
// Возвращаемое значение:
//   Структура - header + payload в соответствии с RFC 7519 (https://tools.ietf.org/html/rfc7519)
//
Функция СвойстваТокена(Токен)
	
	Если Токен = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	РазделительЧастейТокена = ".";
	ЧастиТокена = СтрРазделить(СокрЛП(Токен), РазделительЧастейТокена);
	
	Если ЧастиТокена.Количество() = 3 Тогда
		
		// Токен JWT состоит из трех частей: заголовок (header), полезная нагрузка (payload) и подпись или данные шифрования
		
		ДвоичныеДанныеЗаголовка = Base64Значение(ЧастиТокена[0]);
		ЧтениеДанных = Новый ЧтениеДанных(ДвоичныеДанныеЗаголовка);
		СтрокаЗаголовка = ЧтениеДанных.ПрочитатьСтроку();
		ЧтениеДанных.Закрыть();
		
		ДвоичныеДанныеНагрузки = Base64Значение(ДополнитьBase64Строку(ЧастиТокена[1]));
		ЧтениеДанных = Новый ЧтениеДанных(ДвоичныеДанныеНагрузки);
		СтрокаНагрузки = ЧтениеДанных.ПрочитатьСтроку();
		ЧтениеДанных.Закрыть();
		
	КонецЕсли;
	
	СвойстваВременногоТокена = Новый Структура;
	
	Если ЗначениеЗаполнено(СтрокаЗаголовка) Тогда
		СтруктураЗаголовка = ЗначениеИзJSON(СтрокаЗаголовка, Ложь);
		Если ЗначениеЗаполнено(СтруктураЗаголовка) И ТипЗнч(СтруктураЗаголовка) = Тип("Структура") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СвойстваВременногоТокена, СтруктураЗаголовка);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаНагрузки) Тогда
		СтруктураНагрузки = ЗначениеИзJSON(СтрокаНагрузки, Ложь);
		Если ЗначениеЗаполнено(СтруктураНагрузки) И ТипЗнч(СтруктураНагрузки) = Тип("Структура") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СвойстваВременногоТокена, СтруктураНагрузки);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СвойстваВременногоТокена;
	
КонецФункции

Функция ДополнитьBase64Строку(Base64Строка)
	
	ДлинаХвоста = 4 - (СтрДлина(Base64Строка) % 4);
	Если ДлинаХвоста > 0 Тогда
		Возврат СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Base64Строка, СтрДлина(Base64Строка) + ДлинаХвоста, "=", "Справа");
	Иначе
		Возврат Base64Строка;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область БлокировкаБезопасногоХранилищаВременногоТокена

Функция ЗаблокироватьВременныйТокенДляОбновления(КлючЗаписиБезопасногоХранилища)
	
	ДанныеЗаблокированы = Ложь;
	
	Для НомерПопытки = 1 По КоличествоПовторовОбновленияВременногоТокена() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(КлючЗаписиБезопасногоХранилища);
			ДанныеЗаблокированы = Истина;
			Прервать;
		Исключение
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрацииСервис(),
				УровеньЖурналаРегистрации.Информация,
				,
				,
				НСтр("ru = 'Ожидание блокировки безопасного хранилища временного токена'"));
		КонецПопытки;
		
		// Возможно другой сеанс уже обновляет временный токен.
		// Ждем возможности заблокировать, после чего снова проверим срок действия токена.
		ОбщегоНазначенияБТС.Пауза(ВремяОжиданияОбновленияВременногоТокена());
		
	КонецЦикла;
	
	Возврат ДанныеЗаблокированы;
	
КонецФункции

Процедура РазблокироватьВременныйТокен(КлючЗаписиБезопасногоХранилища)
	
	РазблокироватьДанныеДляРедактирования(КлючЗаписиБезопасногоХранилища);
	
КонецПроцедуры

Функция КоличествоПовторовОбновленияВременногоТокена()
	
	Возврат 6;
	
КонецФункции

Функция ВремяОжиданияОбновленияВременногоТокена()
	
	Возврат 20; // секунд
	
КонецФункции

#КонецОбласти

#Область ТокеныАвторизованныхПользователей

Функция АдресСервисаПоУмолчанию()
	
	Возврат "https://taxrates.1cmycloud.com/applications/taxrates/api";
	
КонецФункции

Функция ИмяСервисаНаПорталеИТС()

	Возврат "1c-regional-taxes";

КонецФункции

Функция НовыйТокенАвторизации()
	
	ПараметрыМетода = НовыеПараметры_СоздатьТокенАвторизации();
	ПараметрыМетода.Тикет = ТикетАутентификацииНаПорталеПоддержки();
	ПараметрыМетода.ИмяКонфигурации = Метаданные.Имя;
	
	ПараметрыЗапроса = ПараметрыЗапросаКСервису(ПараметрыМетода);
	ОтветСервиса = ВыполнитьЗапрос(ПараметрыЗапроса);
	ДанныеОтвета = ДанныеОтвета_СоздатьТокенАвторизации(ОтветСервиса.Данные);
	КодыОтветовСервиса = РегиональныеСтавкиНалоговКлиентСервер.КодыОтветаСервиса();
	Если ОтветСервиса.КодСостояния = КодыОтветовСервиса.ПостояннаяПереадресация Тогда
		УстановитьНовыйАдресСервиса(ОтветСервиса.АдресПереадресации);
		Возврат НовыйТокенАвторизации();
	ИначеЕсли ОтветСервиса.КодСостояния = КодыОтветовСервиса.Успех Тогда
		Возврат ДанныеОтвета.РеквизитыТокена;
	Иначе
		Причина = СтрШаблон(НСтр("ru = 'Не удалось получить токен авторизации: %1'"), ОписаниеОшибкиИзОтвета(ОтветСервиса));
		ВызватьИсключение(Причина,
			КатегорияОшибки.ОтсутствиеРазрешенияДляИспользованияФункциональности,
			ОтветСервиса.КодСостояния,
			РегиональныеСтавкиНалоговКлиентСервер.КодОшибкиПолученияТокена());
	КонецЕсли;
	
КонецФункции

Функция НовыеПараметры_СоздатьТокенАвторизации()
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ИмяКонфигурации", "");
	ПараметрыМетода.Вставить("Тикет", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция ТикетАутентификацииНаПорталеПоддержки()
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(
		ИмяСервисаНаПорталеИТС());
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЗначениеЗаполнено(ДанныеАутентификации.Тикет) Тогда
		Возврат ДанныеАутентификации.Тикет;
	Иначе
		ЗаписатьОшибкуСервисаВЖурналРегистрации(ДанныеАутентификации.ИнформацияОбОшибке);
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не удалось получить тикет на использование сервиса: %1'"),
			ДанныеАутентификации.СообщениеОбОшибке);
	КонецЕсли;
	
КонецФункции

Функция ПараметрыЗапросаКСервису(ПараметрыМетода, ТокенАвторизации = Неопределено)
	
	ПараметрыЗапроса = НовыйПараметрыЗапросаКСервису();
	ПараметрыЗапроса.МетодHttp = "POST";
	ПараметрыЗапроса.АдресРесурса = АдресРесурса("auth/token", ПараметрыМетода);
	ПараметрыЗапроса.Данные = ДанныеЗапроса_СоздатьТокенАвторизации(ПараметрыМетода);
	
	Если ТокенАвторизации <> Неопределено Тогда
		ПараметрыЗапроса.Заголовки.Вставить("", ТокенАвторизации.ТокенBase64);
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

Функция НовыйПараметрыЗапросаКСервису()
	
	Результат = Новый Структура;
	Результат.Вставить("МетодHttp", "");
	Результат.Вставить("АдресРесурса", "");
	Результат.Вставить("Заголовки", Новый Соответствие);
	Результат.Вставить("Данные", Неопределено);
	Возврат Результат;
	
КонецФункции

Функция АдресРесурса(ШаблонАдресаРесурса, ПараметрыМетода)
	
	ПараметрыПодстановки = Новый Структура;
	
	Для Каждого КлючИЗначение Из ПараметрыРесурсов() Цикл
		
		Если СтрНайти(ШаблонАдресаРесурса, СтрШаблон("[%1]", КлючИЗначение.Значение)) > 0 Тогда
			
			ЗначениеПараметра = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыМетода, КлючИЗначение.Ключ);
			Если ЗначениеПараметра <> Неопределено Тогда
				ПараметрыПодстановки.Вставить(КлючИЗначение.Значение, ЗначениеПараметра);
			Иначе
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Не заполнен параметр %1'"), КлючИЗначение.Значение);
				ЗаписатьОшибкуСервисаВЖурналРегистрации(ТекстОшибки);
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;

	Если ПараметрыМетода.Количество() > 0 Тогда
		АдресРесурса = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонАдресаРесурса, ПараметрыПодстановки);
	Иначе
		АдресРесурса = ШаблонАдресаРесурса;
	КонецЕсли;
	
	Возврат АдресРесурса;
	
КонецФункции

Функция ПараметрыРесурсов()
	
	Параметры = Новый Структура;
	Параметры.Вставить("РеквизитыТокена", "user_token");
	Параметры.Вставить("ТокенBase64", "token");
	Параметры.Вставить("СрокДействия", "expire_time");
	
	Параметры.Вставить("Ошибка", "error");
	Параметры.Вставить("Идентификатор", "ueid");
	Параметры.Вставить("Вид", "type");
	Параметры.Вставить("Описание", "detail");
	
	Возврат Параметры;
	
КонецФункции

Функция ДанныеЗапроса_СоздатьТокенАвторизации(ПараметрыМетода)
	
	ИмяКонфигурации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыМетода, "ИмяКонфигурации", "");
	Тикет = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыМетода, "Тикет", "");
	
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("application_name", ИмяКонфигурации);
	ДанныеЗапроса.Вставить("ticket", Тикет);
	
	Возврат ДанныеЗапроса;
	
КонецФункции

Функция ДанныеОтвета_СоздатьТокенАвторизации(ДанныеОтветаСервиса)
	
	Если ДанныеОтветаСервиса = Неопределено или ТипЗнч(ДанныеОтветаСервиса) <> Тип("Структура") Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ПараметрыРесурсов = ПараметрыРесурсов();
	РеквизитыТокена = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, 
		ПараметрыРесурсов.РеквизитыТокена);
	
	ДанныеОтвета = Новый Структура("РеквизитыТокена, Ошибка");
	ДанныеОтвета.РеквизитыТокена = ДанныеОтвета_РеквизитыТокена(РеквизитыТокена, ПараметрыРесурсов);
	ДанныеОтвета.Ошибка = ДанныеОтвета_Ошибка(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Ошибка), ПараметрыРесурсов);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_РеквизитыТокена(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("ТокенBase64, СрокДействия");
	
	ДанныеОтвета.ТокенBase64 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.ТокенBase64);
	
	СрокДействия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.СрокДействия);
	Если ЗначениеЗаполнено(СрокДействия) Тогда
		ДанныеОтвета.СрокДействия = УниверсальноеВремя(СрокДействия);
	Иначе
		ДанныеОтвета.СрокДействия = Дата(1, 1, 1);
	КонецеслИ;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_Ошибка(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = НоваяСтруктураТокена();
	
	ДанныеОтвета.Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Идентификатор);
	ДанныеОтвета.Вид = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Вид);
	ДанныеОтвета.Описание = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Описание);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ВыполнитьЗапрос(ПараметрыЗапроса)
	
	СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресСервиса());
	СоединениеHttp = НовоеСоединениеССервисом(СтруктураАдреса, ТаймаутСоединения());
	ЗапросHttp = НовыйЗапросКСервису(СтруктураАдреса, ПараметрыЗапроса);
	
	ОтветHttp = СоединениеHttp.ВызватьHTTPМетод(ПараметрыЗапроса.МетодHttp, ЗапросHttp);
	
	КодСостояния = ОтветHttp.КодСостояния;
	
	Если Цел(КодСостояния / 100) = 2 Тогда
		Если РежимОтладки() Тогда
			ЗаписатьИнформациюПриВыполненииМетодаВзаимодействия(
				СоединениеHttp, ПараметрыЗапроса.МетодHttp, ЗапросHttp, ОтветHttp);
		КонецЕсли;
	Иначе
		ЗаписатьИнформациюПриВыполненииМетодаВзаимодействия(
			СоединениеHttp, ПараметрыЗапроса.МетодHttp, ЗапросHttp, ОтветHttp);
		ПредставлениеОшибки = СтрШаблон(
			НСтр("ru = 'Ошибка %1 при получении токена пользователя
				|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
			КодСостояния,
			ОтветHttp.ПолучитьТелоКакСтроку());
		ЗаписатьОшибкуСервисаВЖурналРегистрации(ПредставлениеОшибки);
	КонецЕсли;
	
	ОтветСервиса = НовыйОтветСервиса(ОтветHttp);
	
	Возврат ОтветСервиса;
	
КонецФункции

Процедура ЗаписатьИнформациюПриВыполненииМетодаВзаимодействия(СоединениеHttp, МетодHttp, ЗапросHttp, ОтветHttp)
	
	Представления = Новый Массив;
	Представления.Добавить(СтрШаблон("%1 %2",
		МетодHttp,
		ПредставлениеАдресаРесурса(СоединениеHttp, ЗапросHttp.АдресРесурса)));
	Представления.Добавить(ПредставлениеЗаголовков(ЗапросHttp.Заголовки));
	
	ТелоЗапроса = ЗапросHttp.ПолучитьТелоКакСтроку();
	Если ЗначениеЗаполнено(ТелоЗапроса) Тогда
		Представления.Добавить("");
		Представления.Добавить(ТелоЗапроса);
	КонецЕсли;
	
	Представления.Добавить("");
	Представления.Добавить(СтрШаблон("HTTP %1", ОтветHttp.КодСостояния));
	Представления.Добавить(ПредставлениеЗаголовков(ОтветHttp.Заголовки));
	
	ТелоОтвета = ОтветHttp.ПолучитьТелоКакСтроку();
	Если ЗначениеЗаполнено(ТелоОтвета) Тогда
		Представления.Добавить("");
		Представления.Добавить(ТелоОтвета);
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрацииСервис(),
		УровеньЖурналаРегистрации.Информация, , , СтрСоединить(Представления, Символы.ПС));
	
КонецПроцедуры

Функция ПредставлениеАдресаРесурса(СоединениеHttp, АдресРесурса)
	
	Порт = "";
	Если СоединениеHttp.ЗащищенноеСоединение <> Неопределено Тогда
		Протокол = "https";
		ПортПоУмолчанию = 443;
	Иначе
		Протокол = "http";
		ПортПоУмолчанию = 80;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СоединениеHttp.Порт) И СоединениеHttp.Порт <> ПортПоУмолчанию Тогда
		Порт = СтрШаблон(":%1", Формат(СоединениеHttp.Порт, "ЧГ=0;"));
	КонецЕсли;
	
	Возврат СтрШаблон("%1://%2%3/%4", Протокол, СоединениеHttp.Сервер, Порт, АдресРесурса);
	
КонецФункции

Функция ПредставлениеЗаголовков(Заголовки)
	
	ЗаголовкиСекретов = ЗаголовкиСекретов();
	
	Представления = Новый Массив;
	Для Каждого Заголовок Из Заголовки Цикл
		Если ЗаголовкиСекретов.Найти(НРег(Заголовок.Ключ)) <> Неопределено Тогда
			Представления.Добавить(СтрШаблон("%1: ***", Заголовок.Ключ));
		Иначе
			Представления.Добавить(СтрШаблон("%1: %2", Заголовок.Ключ, Заголовок.Значение));
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(Представления, Символы.ПС);
	
КонецФункции

Функция НовыйОтветСервиса(ОтветHttp)
	
	ТелоОтвета = ОтветHttp.ПолучитьТелоКакСтроку();
	Если ЗначениеЗаполнено(ТелоОтвета) Тогда
		Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВJSON(ТелоОтвета) Тогда
			Данные = ОбщегоНазначения.JSONВЗначение(ТелоОтвета, ИменаСвойствСоЗначениямиДата(), Ложь);
		Иначе
			Данные = ТелоОтвета;
		КонецЕсли;
	Иначе
		Данные = Неопределено;
	КонецЕсли;
	
	Ответ = Новый Структура;
	Ответ.Вставить("КодСостояния", ОтветHttp.КодСостояния);
	Ответ.Вставить("Данные", Данные);
	Ответ.Вставить("АдресПереадресации", ОтветHttp.Заголовки.Получить("Location"));
	
	Возврат Ответ;
	
КонецФункции

Функция НовыйЗапросКСервису(СтруктураАдреса, ПараметрыЗапроса)
	
	Запрос = Новый HTTPЗапрос;
	
	Если Не ПустаяСтрока(СтруктураАдреса.ПутьНаСервере) Тогда
		Запрос.АдресРесурса = СтрШаблон("%1/%2", СтруктураАдреса.ПутьНаСервере, ПараметрыЗапроса.АдресРесурса);
	Иначе
		Запрос.АдресРесурса = ПараметрыЗапроса.АдресРесурса;
	КонецЕсли;
	
	Запрос.Заголовки.Вставить("Accept", "application/json");
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Данные) Тогда
		Запрос.Заголовки.Вставить("Content-Type", "application/json");
	КонецЕсли;

	Для Каждого Заголовок Из ПараметрыЗапроса.Заголовки Цикл
		Запрос.Заголовки.Вставить(Заголовок.Ключ, Заголовок.Значение);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Данные) Тогда
		ТелоЗапроса = ОбщегоНазначения.ЗначениеВJSON(ПараметрыЗапроса.Данные);
		Запрос.УстановитьТелоИзСтроки(ТелоЗапроса);
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

Функция НовоеСоединениеССервисом(СтруктураАдреса, Таймаут, Пользователь = Неопределено, Пароль = Неопределено)
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураАдреса.Схема);
	КонецЕсли;
	
	Если НРег(СтруктураАдреса.Схема) = "https" Тогда
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение(
		СтруктураАдреса.Хост,
		СтруктураАдреса.Порт,
		Пользователь,
		Пароль,
		Прокси,
		Таймаут,
		ЗащищенноеСоединение);
	
	Возврат Соединение;
	
КонецФункции

Функция ЗаголовкиСекретов()
	
	Ключи = Новый Массив;
	Ключи.Добавить(НРег("User-Token"));
	
	Возврат Ключи;
	
КонецФункции

Функция ИменаСвойствСоЗначениямиДата()
	
	Поля = Новый Массив;
	Поля.Добавить("expire_time");
	Поля.Добавить("date");
	
	Возврат Поля;
	
КонецФункции

Функция ОписаниеОшибкиИзОтвета(ДанныеОтвета)
	
	Если ДанныеОтвета <> Неопределено Тогда
		Ошибка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОтвета, "Ошибка");
		Если Ошибка <> Неопределено Тогда
			ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Ошибка, "Описание");
		Иначе
			ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОтвета, "Данные");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Возврат ОписаниеОшибки;
	Иначе
		Возврат НСтр("ru = 'Неизвестная ошибка'");
	КонецЕсли;
	
КонецФункции

Функция НоваяСтруктураТокена()
	
	Результат =Новый Структура;
	Результат.Вставить("ТокенBase64", "");
	Результат.Вставить("СрокДействия", '00010101');
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьНовыйАдресСервиса(АдресСервиса)

	Если Не ЗначениеЗаполнено(АдресСервиса) Тогда
		Возврат;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	Константы.АдресСервисаРегиональныеСтавкиНалогов.Установить(АдресСервиса);
	// Очистим старый временный токен от старого адреса приложения
	УдалитьВременныйТокен();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти
	
#КонецОбласти

