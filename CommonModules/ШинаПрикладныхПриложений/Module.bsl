#Область ПрограммныйИнтерфейс

// Конструктор коллекции, описывающей код доступа к мобильному приложению.
//
// Код доступа может быть получен в 1С:Бухгалтерии, затем он должен быть передан
// пользователю мобильного приложения и введен в мобильное приложение.
// После ввода кода доступа конкретный экземпляр мобильного приложения (в который введен код)
// и область 1С:Бухгалтерии (в которой был получен код) могут обмениваться сообщениями.
// 
// Возвращаемое значение:
//  Структура - см. тело функции
//
Функция НовыйКодДоступа() Экспорт
	
	КодДоступа = Новый Структура;
	
	КодДоступа.Вставить("Код",           ""); // Для использования при машинной обработке
	КодДоступа.Вставить("Представление", ""); // Для передачи пользователю

	Возврат КодДоступа;
	
КонецФункции

// Предоставляет новый код доступа в мобильное приложение.
//
// Полученный код следует сохранить в информационной базе, а его представление выдать пользователю.
//
// Параметры:
//  Приложение      - Строка - имя приложения в шине
//  НачальныеДанные - Структура - произвольные данные, которые будут доставлены в мобильное приложение
//                    (будут доступны мобильному приложению после того, как пользователь введет в него код доступа).
//                    Например, это может быть информация о предприятии, из информационной базы которого выдан код доступа,
//                    либо о лице, которому он выдан.
//                    Структура должна быть сериализуемой в JSON и не должна содержать вложенные соответствия.
// 
// Возвращаемое значение:
//  Структура - см. ШинаМобильныхПриложений.НовыйКодДоступа
//  Неопределено - не удалось предоставить код
//
Функция КодДоступа(Приложение, НачальныеДанные) Экспорт
	
	Подключение = Подключение(Приложение);
	Если Подключение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Структура;
	Запрос.Вставить("НачальныеДанные", НачальныеДанные);
	ОтветСервиса = ВыполнитьМетодСервиса(
		Приложение, Подключение, "POST", ИмяСервисаШиныПриложений(), "User", Запрос);
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОтветСервиса.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		ЗаписатьОшибкуКодСостояния(
			ОтветСервиса,
			Приложение,
			НСтр("ru = 'Регистрация кода доступа'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат Неопределено;
	КонецЕсли;
	
	КодДоступа = НовыйКодДоступа();
	КодДоступа.Код = ОтветСервиса.Содержимое.Ключ;
	КодДоступа.Представление = ОтветСервиса.Содержимое.ПредставлениеКлюча;
	
	Возврат КодДоступа;
	
КонецФункции

// Удаляет (отзывает) код доступа: после вызова метода код нельзя использовать для передачи сообщений.
//
// Параметры:
//  Приложение - Строка - имя приложения в шине
//  КодДоступа - Строка - сохраняемый код, полученный в КодДоступа
// 
// Возвращаемое значение:
//  Булево - Истина, если получено подтверждение, что код отозван (не может быть более использован)
//
Функция УдалитьКодДоступа(Приложение, КодДоступа) Экспорт
	
	Подключение = Подключение(Приложение);
	Если Подключение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Структура;
	Запрос.Вставить("Ключ", КодДоступа);
	
	ОтветСервиса = ВыполнитьМетодСервиса(
		Приложение, Подключение, "DELETE", ИмяСервисаШиныПриложений(), "User", Запрос);
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОтветСервиса.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		ШинаМобильныхПриложенийСлужебный.ЗаписатьОшибкуКодСостояния(
			ОтветСервиса,
			Приложение,
			НСтр("ru = 'Удаление кода доступа'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Запускает фоновое получения сообщений с Шины прикладных приложений
//
// Параметры:
//  Приложение - Строка - имя приложения в шине
//
Процедура ПолучитьСообщенияШиныОтложенно(Приложение) Экспорт
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		ТекстОшибки = НСтр("ru = 'Работа с внешними ресурсами заблокирована'", ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ПараметрыФоновогоВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыФоновогоВыполнения.ОжидатьЗавершение           = 0;
	ПараметрыФоновогоВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыФоновогоВыполнения.КлючФоновогоЗадания         = Приложение;
	ПараметрыФоновогоВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(
		НСтр("ru = 'Обработка сообщений мобильного приложения %1'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Приложение);
	
	ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыФоновогоВыполнения,
		"ШинаПрикладныхПриложений.ПолучитьСообщенияШины",
		Приложение);
	
КонецПроцедуры

// Возвращает адрес Шины прикладных приложений
// 
// Возвращаемое значение:
//  Строка - адрес в интернете
//
Функция АдресШиныПрикладныхПриложений() Экспорт
	
	Возврат "https://kktservice.1cmycloud.com/applications/application-bus";
	
КонецФункции

// Отправляет сообщение Шине прикладных приложений.
//
// Параметры:
//  Приложение - Строка - имя приложения в Шине
//  Сообщение - Структура
//
Процедура ОтправитьСообщениеШине(Приложение, Сообщение) Экспорт
	
	Подключение = Подключение(Приложение);
	Если Подключение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтветСервиса = ВыполнитьМетодСервиса(Приложение, Подключение, "POST", ИмяСервисаШиныПриложений(), "outbound", Сообщение);
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если ОтветСервиса.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		ЗаписатьОшибкуКодСостояния(
			ОтветСервиса,
			Приложение,
			НСтр("ru = 'Отправка сообщения Шине'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет получение сообщений с Шины прикладных приложений.
//
Процедура ПолучитьСообщенияШины(Приложение) Экспорт
	
	Подключение = Подключение(Приложение);
	Если Подключение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяДанныхПриложения = ИмяДанныхПриложения(Приложение);
	ИмяСобытияПроверкаНаличияСообщений = НСтр("ru = 'Проверка наличия сообщений'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	// Используем блокировку записи регистра сведений "ОпросШиныМобильныхПриложений", чтобы не допустить нескольких одновременных запросов.
	Блокировка = РегистрыСведений.ОпросШиныМобильныхПриложений.СоздатьКлючЗаписи(Новый Структура("ИмяПриложения", ИмяДанныхПриложения));
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Блокировка);
	Исключение
		ИмяСобытия = ШинаМобильныхПриложенийСлужебный.ИмяСобытияЖурналаРегистрации(ИмяСобытияПроверкаНаличияСообщений, ИмяДанныхПриложения);
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	ИмяСервиса = ИмяСервисаШиныПриложений();
	ТекущийИдентификаторСообщения = Неопределено;
	Пока Истина Цикл
		
		ОтветИдентификаторСообщения = ВыполнитьМетодСервиса(
			Приложение, Подключение, "GET", ИмяСервиса, "inbound", Неопределено, Ложь);
			
		Если ОтветИдентификаторСообщения = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Если ОтветИдентификаторСообщения.КодСостояния = ЗапросыREST.КодСостоянияНетДанных() Тогда
			// Все обработаны
			Прервать;
		КонецЕсли;
		
		Если ОтветИдентификаторСообщения.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
			ЗаписатьОшибкуКодСостояния(
				ОтветИдентификаторСообщения, ИмяДанныхПриложения, ИмяСобытияПроверкаНаличияСообщений);
			Прервать;
		КонецЕсли;
		
		Если ТекущийИдентификаторСообщения = ОтветИдентификаторСообщения.Содержимое.Идентификатор Тогда
			// Это сообщение было уже обработано в прошлой итерации цикла. Не следует его повторно обрабатывать.
			Прервать;
		КонецЕсли;
		
		ТалонИТС = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ТикетАутентификации();
		Если Не ЗначениеЗаполнено(ТалонИТС) Тогда
			Прервать;
		КонецЕсли;
		
		ТекущийИдентификаторСообщения = ОтветИдентификаторСообщения.Содержимое.Идентификатор;
		Подключение.ЗаголовкиАутентификации.Вставить(ИмяЗаголовкаАутентификацииИТС(), ТалонИТС);
		
		Ресурс = СтрШаблон("inbound/%1", ТекущийИдентификаторСообщения);
		ОтветСодержимоеСообщения =
			ВыполнитьМетодСервиса(Приложение, Подключение, "GET", ИмяСервиса, Ресурс, Неопределено, Ложь);
		
		Если ОтветСодержимоеСообщения = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Если ОтветСодержимоеСообщения.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния()
			И ОтветСодержимоеСообщения.КодСостояния <> ЗапросыREST.КодСостоянияНеобходимаОплата() Тогда
			ЗаписатьОшибкуКодСостояния(
				ОтветСодержимоеСообщения, ИмяДанныхПриложения, ИмяСобытияПроверкаНаличияСообщений);
			Прервать;
		КонецЕсли;
		
		Содержимое = ОтветСодержимоеСообщения.Содержимое;
		ОбработатьСообщение(
			Приложение,
			?(Содержимое.Свойство("Отправитель"), Содержимое.Отправитель, Неопределено),
			?(Содержимое.Свойство("Сообщение"), Содержимое.Сообщение, Новый Структура),
			ОтветСодержимоеСообщения.Заголовки);
			
		Если ОтветСодержимоеСообщения.КодСостояния = ЗапросыREST.КодСостоянияНеобходимаОплата() Тогда
			Прервать;
		КонецЕсли;
		
		ОтветПодтверждениеПолучения = ВыполнитьМетодСервиса(Приложение, Подключение, "DELETE", ИмяСервиса, Ресурс);
		
		Если ОтветПодтверждениеПолучения <> Неопределено
			И ОтветПодтверждениеПолучения.КодСостояния = ЗапросыREST.КодСтандартногоСостояния() Тогда
			
			ТекущийИдентификаторСообщения = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	РазблокироватьДанныеДляРедактирования(Блокировка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Авторизация

// Возвращает временный токен авторизации, получая новый при необходимости
//
// Возвращаемое значение:
//  Строка, Неопределено
//
Функция ВременныйТокен(ИмяДанныхПриложения)
	
	УстановитьПривилегированныйРежим(Истина);
	ВременныйТокен = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИмяДанныхПриложения,
		"ВременныйТокен");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ВременныйТокен = Неопределено Тогда
		ВременныйТокен = ПолучитьНовыйВременныйТокен(ИмяДанныхПриложения);
	КонецЕсли;
	
	Возврат ВременныйТокен;
	
КонецФункции

// Возвращает новый временный токен авторизации, срок действия токена при этом игнорируется.
//
// Возвращаемое значение:
//  Строка, Неопределено
//
Функция ПолучитьНовыйВременныйТокен(ИмяДанныхПриложения)
	
	ИмяСобытия = "ПолучениеВременногоТокенаШины";
	
	Подключение = СохраненныеДанныеПодключения(ИмяДанныхПриложения);
	Если Подключение = Неопределено Тогда
		ОписаниеСобытия = НСтр("ru = 'Не задана учетная запись пользователя сервиса Шины.'", ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ОписаниеСобытия);
		Возврат Неопределено;
	КонецЕсли;
	
	Подключение.ПространствоИмен = АдресСервисаАвторизации();
	Подключение.ЗаголовкиАутентификации.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	Ответ = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, "POST", "", "grant_type=client_credentials");
	
	Если Ответ = Неопределено Тогда
		// Если не удалось подключиться к сервису, ошибку уже зарегистрировал метод ВыполнитьМетодСервиса()
		Возврат Неопределено;
	КонецЕсли;
		
	Если Ответ.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		
		ОписаниеСобытия = СтрШаблон(
		НСтр("ru = 'Ошибка запроса получения временного токена шины. Код ответа: %1.
			|Описание: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Ответ.КодСостояния, Ответ.Текст);
	
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ОписаниеСобытия);
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не Ответ.Содержимое.Свойство(КлючВременногоТокена()) Тогда
		
		ОписаниеСобытия = СтрШаблон(
		НСтр("ru = 'Ошибка запроса получения временного токена шины. Код ответа: %1.
			|Описание: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Ответ.КодСостояния, Ответ.Текст);
	
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ОписаниеСобытия);
		Возврат Неопределено;
	КонецЕсли;
	
	Токен = Ответ.Содержимое[КлючВременногоТокена()];
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ИмяДанныхПриложения, Токен, "ВременныйТокен");
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Токен;
	
КонецФункции

// Ключ токена в ответе сервиса авторизации
//
Функция КлючВременногоТокена()
	
	Возврат "id_token";
	
КонецФункции

Функция АдресСервисаАвторизации()
	
	Возврат "sys/token";
	
КонецФункции

Функция ИмяЗаголовкаАутентификацииИТС()
	
	Возврат "auth-login-1c";
	
КонецФункции

#КонецОбласти

#Область ПодключениеПодсистемы

Процедура ЗаполнитьМенеджерыПриложений(МенеджерыПриложений)
	
	МенеджерыПриложений.Добавить(РегистрыСведений.МобильноеПриложениеСканированиеЧеков);
	
КонецПроцедуры

#КонецОбласти

Функция Подключение(Приложение)
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		ТекстОшибки = НСтр("ru = 'Работа с внешними ресурсами заблокирована'", ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	АдресСервиса = ШинаПрикладныхПриложений.АдресШиныПрикладныхПриложений();
	ИмяДанныхПриложения = ИмяДанныхПриложения(Приложение);
	
	Подключение = СохраненныеДанныеПодключения(ИмяДанныхПриложения);
	Если Подключение <> Неопределено Тогда
		Подключение.АдресСервиса = АдресСервиса;
		Подключение.ИсточникЗапроса = ИмяДанныхПриложения;
		Возврат Подключение;
	КонецЕсли;

	ТалонИТС = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ТикетАутентификации();
	
	Если Не ЗначениеЗаполнено(ТалонИТС) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Подключение = ЗапросыREST.НовыйПодключение();
	Подключение.ИсточникЗапроса = ИмяДанныхПриложения;
	Подключение.АдресСервиса = АдресСервиса;
	Подключение.ЗаголовкиАутентификации.Вставить(ИмяЗаголовкаАутентификацииИТС(), ТалонИТС);
	
	// Зарегистрируем приложение
	Подключение.ПространствоИмен = ПространствоИмен(ИмяСервисаРегистрацииПриложения());
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
		Подключение.ЗаголовкиАутентификации, ШинаМобильныхПриложенийСлужебный.АутентификацияИТС(), Истина);
	ОтветСервиса = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, "POST", "tenant");
	
	Если ОтветСервиса = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОтветСервиса.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		ЗаписатьОшибкуКодСостояния(
			ОтветСервиса,
			Приложение,
			НСтр("ru = 'Регистрация приложения'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат Неопределено;
	КонецЕсли;
	
	// Сохраним данные подключения
	Ответ = ОтветСервиса.Содержимое;
	Подключение.Логин = Ответ.Идентификатор;
	Подключение.Пароль = Ответ.Секрет;
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого КлючИЗначение Из Подключение Цикл
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			ИмяДанныхПриложения,
			КлючИЗначение.Значение,
			КлючИЗначение.Ключ);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Подключение;
	
КонецФункции

Функция СохраненныеДанныеПодключения(ИмяДанныхПриложения)
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПодключения = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИмяДанныхПриложения,
		"АдресСервиса, Логин, Пароль");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеПодключения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Фактически при отсутствии данных ПрочитатьДанныеИзБезопасногоХранилища зачем-то возвращает структуру,
	// в которой значения свойств будут Неопределено
	Для Каждого КлючИЗначение Из ДанныеПодключения Цикл
		Если КлючИЗначение.Значение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Подключение = ЗапросыREST.НовыйПодключение();
	Подключение.ИсточникЗапроса = ИмяДанныхПриложения;
	ЗаполнитьЗначенияСвойств(Подключение, ДанныеПодключения);
	Возврат Подключение;
	
КонецФункции

// Выполняет метод HTTP (REST) API.
//
// Параметры:
//  Приложение  - Строка - имя приложения в шине
//  Подключение - Структура - см. ЗапросыRest.НовыйПодключение
//  Метод       - Строка - имя HTTP-метода (например, "POST", "GET", "MERGE")
//  ИмяСервиса  - Строка - корневой URL HTTP-сервиса.
//  Ресурс      - Строка - имя ресурса HTTP-сервиса.
//                         В общем случае путь на сервере имеет вид "/ИмяСервиса/Ресурс".
//  Данные       - Структура, Строка - данные, передаваемые в теле запроса(в виде json-объекта, либо серилизованные в строку)
//  ВременныйТокен - Cтрока - временный токен для доступа к Шине
// 
// Возвращаемое значение:
//  Структура - см. НовыйОтветСервиса
//  Неопределено - ответ от вызываемого сервиса не получен.
//
Функция ВыполнитьМетодСервиса(Приложение, Подключение, Метод, ИмяСервиса, Ресурс, Данные = Неопределено, ЗаписыватьОшибку = Истина)
	
	ИмяДанныхПриложения = ИмяДанныхПриложения(Приложение);
	
	// Для аутентификации на Шине используется временный токен, передаваемый в заголовках. Логин и пароль не используется.
	// Временный токен валиден в течение 1 часа.
	ВременныйТокен = ВременныйТокен(ИмяДанныхПриложения);
	Если ВременныйТокен = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Подключение.ПространствоИмен = ПространствоИмен(ИмяСервиса);
	Подключение.ЗаголовкиАутентификации.Вставить("Authorization", СтрШаблон("Bearer %1", ВременныйТокен));
	Ответ = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, Метод, Ресурс, Данные);
	
	Если Ответ = Неопределено Тогда
		// Если не удалось подключиться к Шине, ошибку уже зарегистрировал метод ЗапросыREST.ВыполнитьМетодСервиса()
		Возврат Неопределено;
	КонецЕсли;
	
	// Срок действия токена не проверяется перед выполнением запроса, поэтому
	// временный токен может быть устаревшим. Тогда вернется ответ с кодом 401.
	// Так же Менеджер аутентификации в некоторых случаях может ответить кодом 400.
	// Например, при перезапуске сервера 1С Элемент не сохраняется ключ подписи токенов. Попытка
	// предоставить ранее полученный токен приведет к ответу с кодом 400 (ошибка G5RT-31950).
	// В таких случаях необходимо получить новый токен и повторить запрос.
	Если Ответ.КодСостояния = ЗапросыREST.КодСостоянияНеАутентифицирован()
		Или Ответ.КодСостояния = ЗапросыREST.КодСостоянияПлохойЗапрос() Тогда
		Возврат ОбновитьТокенВыполнитьМетодСервиса(Приложение, Подключение, Метод, Ресурс, Данные, ЗаписыватьОшибку);
	КонецЕсли;
	
	Если ЗаписыватьОшибку И Ответ.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		ЗаписатьОшибкуЗапроса(Ресурс, Приложение, Ответ.КодСостояния, Ответ.Текст);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции

Функция ОбновитьТокенВыполнитьМетодСервиса(Приложение, Подключение, Метод, Ресурс, Данные, ЗаписыватьОшибку = Истина)
	
	ИмяДанныхПриложения = ИмяДанныхПриложения(Приложение);
	
	ВременныйТокен = ПолучитьНовыйВременныйТокен(ИмяДанныхПриложения);
	Если ВременныйТокен = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Подключение.ЗаголовкиАутентификации.Вставить("Authorization", СтрШаблон("Bearer %1", ВременныйТокен));
	Ответ = ЗапросыREST.ВыполнитьМетодСервиса(Подключение, Метод, Ресурс, Данные);
	
	Если Ответ = Неопределено Тогда
		// Если не удалось подключиться к Шине, ошибку уже зарегистрировал метод ЗапросыREST.ВыполнитьМетодСервиса()
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗаписыватьОшибку И Ответ.КодСостояния <> ЗапросыREST.КодСтандартногоСостояния() Тогда
		ЗаписатьОшибкуЗапроса(Ресурс, Приложение, Ответ.КодСостояния, Ответ.Текст);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции

Процедура ОбработатьСообщение(Приложение, Отправитель, Сообщение, Заголовки)
	
	МенеджерыПриложений = Новый Массив;
	ЗаполнитьМенеджерыПриложений(МенеджерыПриложений);
	
	Сообщение.Вставить("Заголовки", Заголовки);
	МенеджерПриложения = ШинаМобильныхПриложенийСлужебный.МенеджерПриложения(Приложение, МенеджерыПриложений);
	МенеджерПриложения.ОбработатьСообщениеШиныМобильныхПриложений(Отправитель, Сообщение);
	
КонецПроцедуры

Функция ПространствоИмен(ИмяСервиса)
	
	Возврат СтрШаблон("api/%1/%2", ИмяСервиса, ВерсияAPI());
	
КонецФункции

Функция ВерсияAPI()
	
	Возврат "v1";
	
КонецФункции

Функция ИмяДанныхПриложения(Приложение)
	
	Возврат СтрШаблон("bus-%1", Приложение);
	
КонецФункции

Функция ИмяСервисаРегистрацииПриложения()
	
	Возврат "tenantauth";
	
КонецФункции

Функция ИмяСервисаШиныПриложений()
	
	Возврат "tenantbus";
	
КонецФункции

Функция ИмяСобытияЖурналаРегистрации(Действие = "", Приложение = "")
	
	ЭлементыИмени = Новый Массив;
	ЭлементыИмени.Добавить(НСтр("ru = 'Шина прикладных приложений'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	Если ЗначениеЗаполнено(Действие) Тогда
		ЭлементыИмени.Добавить(Действие);
		Если ЗначениеЗаполнено(Приложение) Тогда
			ЭлементыИмени.Добавить(Приложение);
		КонецЕсли;
	КонецЕсли;
		
	Возврат СтрСоединить(ЭлементыИмени, ".");
	
КонецФункции

Процедура ЗаписатьОшибкуКодСостояния(ОтветСервиса, Приложение, Действие)
	
	ИмяСобытия = ИмяСобытияЖурналаРегистрации(Действие, Приложение);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Ошибка,
		,
		ОтветСервиса.КодСостояния,
		ОтветСервиса.Текст);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуЗапроса(Ресурс, Приложение, КодСостояния, Текст)
	
	ОписаниеСобытия = СтрШаблон(
		НСтр("ru = 'Ошибка запроса. Код ответа: %1.
			|Описание: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		КодСостояния, Текст);
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(Ресурс, Приложение),
		УровеньЖурналаРегистрации.Ошибка, , , ОписаниеСобытия);
КонецПроцедуры

#КонецОбласти