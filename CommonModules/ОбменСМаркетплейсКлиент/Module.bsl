
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПрочитатьДанныеОтчетаВСтруктуру(ПараметрыОтчета, ПараметрыВыполнения) Экспорт
	Форма = ПараметрыВыполнения.Форма;
	
	Результат = ОбменСМаркетплейсВызовСервера.ЗагрузитьОтчетОПродажах(ПараметрыОтчета, Форма.УникальныйИдентификатор);
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.Заголовок = ПараметрыВыполнения.Заголовок;
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = ПараметрыВыполнения.ВыводитьПрогрессВыполнения;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ПараметрыВыполнения.ОповещениеОЗавершении, ПараметрыОжидания);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		ВыполнитьОбработкуОповещения(ПараметрыВыполнения.ОповещениеОЗавершении, Результат);
	КонецЕсли;
КонецПроцедуры

Процедура ЗагрузитьСписокНоменклатуры(НастройкаИнтеграции, Форма, ОповещениеОЗавершении = Неопределено) Экспорт
	РезультатВыполненияФоновогоЗадания = ОбменСМаркетплейсВызовСервера.НачатьПолучениеСпискаТоваров(
		НастройкаИнтеграции, Форма.УникальныйИдентификатор);
		
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполненияФоновогоЗадания, "Статус") = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ЗагрузитьСписокНоменклатурыЗавершение = Новый ОписаниеОповещения("ЗагрузитьСписокНоменклатурыЗавершение", ЭтотОбъект, ОповещениеОЗавершении);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполненияФоновогоЗадания, ЗагрузитьСписокНоменклатурыЗавершение, ПараметрыОжидания);
	Иначе
		ЗагрузитьСписокНоменклатурыЗавершение(РезультатВыполненияФоновогоЗадания, ОповещениеОЗавершении) 
	КонецЕсли;
	
КонецПроцедуры 

Процедура ЗагрузитьСписокНоменклатурыЗавершение(РезультатВыполненияФоновогоЗадания, ОповещениеОЗавершении) Экспорт
	
	РезультатОперации = Новый Структура;
	
	РезультатОперации.Вставить("Результат",          Ложь);
	РезультатОперации.Вставить("СообщениеОбОшибке");
	РезультатОперации.Вставить("СписокТоваров");
	РезультатОперации.Вставить("КодОтветаСервера", 0);
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполненияФоновогоЗадания, "Статус") = "Выполнено" Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации, 
			ОбменСМаркетплейсВызовСервера.ОбработатьРезультатПолученияСпискаТоваров(РезультатВыполненияФоновогоЗадания.АдресРезультата));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, РезультатОперации);
	
КонецПроцедуры

// Запускает сопоставление номенклатуры
//
// Параметры:
//  СписокНесопоставленнойНоменклатуры  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
// ПараметрыСопоставления - <Структура> 
//    НастройкаИнтеграции -  <Справочники.НастройкиИнтеграции>
//    ВидМаркетплейса     -  <Перечисления.ВидыМаркетплейсов>
//
Процедура СопоставитьНоменклатуруПриЗагрузкеКаталога(СписокНесопоставленнойНоменклатуры, ПараметрыСопоставления) Экспорт
	НастройкиСопоставления    = Новый Структура;
	НовыеКолонкиСопоставления = Новый Массив;
			
	ДополнительныеПараметрыПоиска = Новый Структура;
	ДополнительныеПараметрыПоиска.Вставить("НатуральныеКлючи", "Артикул");
	ДополнительныеПараметрыПоиска.Вставить("Маркетплейс", ПараметрыСопоставления.ВидМаркетплейса);
	
	Если ПараметрыСопоставления.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries") 
		ИЛИ ПараметрыСопоставления.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON") Тогда
			
		ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
		ДополнительныйРеквизит.Имя = "КодМагазина";
		ДополнительныйРеквизит.Представление = "Код маркетплейса";
		НовыеКолонкиСопоставления.Добавить(ДополнительныйРеквизит);
			
	ИначеЕсли ПараметрыСопоставления.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет") Тогда
		
		ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
		ДополнительныйРеквизит.Имя = "КодМагазина";
		ДополнительныйРеквизит.Представление = "Ваш SKU";
		НовыеКолонкиСопоставления.Добавить(ДополнительныйРеквизит);
	КонецЕсли;
	
	ВидимостьКолонокСопоставления = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ВидимостьКолонокСопоставления.Вставить("ЕдиницаИзмерения", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Упаковка", Ложь);
	ВидимостьКолонокСопоставления.Вставить("СтавкаНДС", Ложь);
	
	НастройкиСопоставления.Вставить("ВидимостьКолонокСопоставления", ВидимостьКолонокСопоставления);
	НастройкиСопоставления.Вставить("ДополнительныеРеквизитыСопоставления", НовыеКолонкиСопоставления);
	
	НастройкиСопоставления.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметрыПоиска);
	НастройкиСопоставления.Вставить("ОграничениеТипаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НастройкиСопоставления.Вставить("ОтключитьПоискПоСловарю", Истина);
	НастройкиСопоставления.Вставить("ТочностьПоискаПоУмолчанию", 100);
	НастройкиСопоставления.Вставить("ВладелецФормы", ЭтотОбъект);
	
	ОповещениеОЗавершенииСопоставления = Новый ОПисаниеОповещения("СопоставитьНоменклатуруЗавершение", ЭтотОбъект, ПараметрыСопоставления);
	
	СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры(СписокНесопоставленнойНоменклатуры,НастройкиСопоставления, ОповещениеОЗавершенииСопоставления);
КонецПроцедуры

Процедура ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(ВладелецФормы, ПараметрыФормы = Неопределено) Экспорт
	
	ОткрытьФорму("РегистрСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов.Форма.ФормаНастройки",
		ПараметрыФормы, ВладелецФормы,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОткрытьЖурналОбмена(ВладелецФормы, ПараметрыФормы = Неопределено) Экспорт
	
	ОткрытьФорму("РегистрСведений.ЖурналОбменаСМаркетплейсами.Форма.ФормаСписка", ПараметрыФормы, ВладелецФормы,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область НоваяАвторизацияЯндекс

//Осуществляет переход по локальной ссылке баннера
//Параметры: НавигационнаяСсылка Тип("Строка")
//
//Возвращаемое значение: нет
//
Процедура ЛокальнаяСсылкаПерейти(НавигационнаяСсылкаНастройки) Экспорт

	ИдентификаторСсылки = Прав(НавигационнаяСсылкаНастройки, СтрДлина(НавигационнаяСсылкаНастройки)-17);
	УникальныйИдентификаторНастройки = Новый УникальныйИдентификатор(ИдентификаторСсылки);
	Настройка = ОбменСМаркетплейсВызовСервера.ПолучитьНастройкуПоУникальномуИдентификатору(УникальныйИдентификаторНастройки);
	
	ОткрытьНастройкуИнтеграции(Настройка);
	
КонецПроцедуры

//Открывает форму настройки интеграции
//Параметры:
//			- НастройкаИнтеграции Тип("СправочникСсылка.НастройкиИнтеграции")
//			- ВидМаркетплейса Тип("Перечисление.ВидыМаркетплейсов")-необяз.
//			- ОшибкаАвторизации Тип("Булево") -необяз.
//			- ВидОперации Тип("Строка") -необяз.
//
//Возвращаемое значение: нет
//
Процедура ОткрытьНастройкуИнтеграции(НастройкаИнтеграции, ВидМаркетплейса = Неопределено, ОшибкаАвторизации = Ложь, ВидОперации = "") Экспорт
	
	ЗначенияЗаполнения = Новый Структура();
	ЗначенияЗаполнения.Вставить("Владелец", ОбменСМаркетплейсВызовСервера.ОрганизацияНастройки(НастройкаИнтеграции));
	Если ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		ВидМаркетплейса = ОбменСМаркетплейсВызовСервера.ВидМаркетплейсаНастройки(НастройкаИнтеграции);
	ИначеЕсли Не ЗначениеЗаполнено(ВидМаркетплейса) Тогда
		ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет");	
	КонецЕсли;		
	ЗначенияЗаполнения.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", НастройкаИнтеграции);
	ПараметрыФормы.Вставить("ВидОперации", ВидОперации);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ОшибкаАвторизации", ОшибкаАвторизации);
	
	ОткрытьФорму("Справочник.НастройкиИнтеграцииМаркетплейс.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

//Открывает форму нового элемента настройки инетграции
//Параметры:
//			- Организация Тип("СправочникСсылка.Организации")
//			- ВидМаркетплейса Тип("Перечисление.ВидыМаркетплейсов")-необяз.
//			- ОшибкаАвторизации Тип("Булево") -необяз.
//			- ВидОперации Тип("Строка") -необяз.
//
//Возвращаемое значение: нет
//
Процедура НоваяНастройкаИнтеграции(Организация, ВидМаркетплейса, ОшибкаАвторизации = Ложь, ВидОперации = "") Экспорт
	
	ЗначенияЗаполнения = Новый Структура();
	ЗначенияЗаполнения.Вставить("Владелец", Организация);
	ЗначенияЗаполнения.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Неопределено);
	ПараметрыФормы.Вставить("ВидОперации", ВидОперации);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ОшибкаАвторизации", ОшибкаАвторизации);
	
	ОткрытьФорму("Справочник.НастройкиИнтеграцииМаркетплейс.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

//Выводит предложение о переходже на новый спсоб авторизации
//Параметры: Организация Тип("СправочникСсылка.Организации")
//
//Возвращаемое значение: нет
//
Процедура ПредложитьНовуюАвторизацию(Организация) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПредложитьНовуюАвторизациюЗавершение", ЭтотОбъект, Новый Структура("Организация", Организация));
	ТекстВопроса = "Ошибка авторизации на сервисе Яндекс.Маркет.
			       |Укажите новый токен авторизации в настройках
				   |Открыть настройки интеграции?";
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

//Завершение предложения перехода на новую авторизацию
Процедура ПредложитьНовуюАвторизациюЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;	
	
	Настройка = ОбменСМаркетплейсВызовСервера.ПолучитьНастройкуИнтеграции(ДополнительныеПараметры.Организация);
	
	ОткрытьНастройкуИнтеграции(Настройка, , Истина);
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СопоставитьНоменклатуруЗавершение(РезультатСопоставления, ДополнительныеПараметры) Экспорт
	ЕстьНоваяНоменклатура = ЗначениеЗаполнено(РезультатСопоставления);
	Если ЕстьНоваяНоменклатура Тогда
		ОбменСМаркетплейсВызовСервера.ОбработатьРезультатыСопоставления(
			РезультатСопоставления, 
			ДополнительныеПараметры.НастройкаИнтеграции);
	КонецЕсли;
	
		
	Если ДополнительныеПараметры.Свойство("ОповещениеОЗавершении") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, Новый Структура("Результат", ЕстьНоваяНоменклатура));
	КонецЕсли;
КонецПроцедуры

#Область ОтложенноеСоспоставлениеНоменклатуры

// Подключает,в случае необходимости, к форме документа обработчик
// сопоставления номенклатуры, см. ПоказатьСопоставлениеНоменклатуры
//
// Параметры:
//  Форма	 - Форма - форма документа
//
Процедура ПодключитьОбработчикСопоставленияНоменклатуры(Форма) Экспорт
	
#Если МобильныйКлиент Тогда
	Возврат;
#КонецЕсли
	
	Если Форма.ТребуетсяСопоставлениеНоменклатуры Тогда
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПоказатьСопоставлениеНоменклатуры", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Открывает стандартную форму сопоставления номенклатуры
//
// Параметры:
//  Форма		 - Форма - форма документа
//  Маркетплейс	 - ПеречислениеСсылка.ВидыМаркетплейсов - маркетплейс
//
Процедура ПоказатьСопоставлениеНоменклатуры(Форма, Маркетплейс = Неопределено) Экспорт
	
	Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	
	Форма.ДанныеДляСопоставленияНоменклатуры.НастройкиСопоставленияНоменклатуры.Вставить(
		"РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	ДополнительныеПараметры = Новый Структура("Форма, Маркетплейс", Форма, Маркетплейс);
	ОбработчикОповещения = Новый ОписаниеОповещения(
		"СопоставлениеНоменклатурыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры(
		Форма.ДанныеДляСопоставленияНоменклатуры.НоменклатураКонтрагента,
		Форма.ДанныеДляСопоставленияНоменклатуры.НастройкиСопоставленияНоменклатуры,
		ОбработчикОповещения);
	
КонецПроцедуры

// Вызывается после закрытия стандартной формы сопоставления номенклатуры 
//
// Параметры:
//  РезультатЗакрытия		 - Структура - см. СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры
//  ДополнительныеПараметры	 - Структура:
//   * Форма - Форма - форма документа
//
Процедура СопоставлениеНоменклатурыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Форма = ДополнительныеПараметры.Форма;
		Форма.ТребуетсяСопоставлениеНоменклатуры = Ложь;
		
		Маркетплейс = Форма.ДанныеДляСопоставленияНоменклатуры.НастройкиСопоставленияНоменклатуры.ДополнительныеПараметрыПоиска.Маркетплейс;
		НастройкаИнтеграции = Новый Структура("Владелец, ВидМаркетплейса",
			Форма.Объект.Организация,
			Маркетплейс);
		ОбменСМаркетплейсВызовСервера.ОбработатьРезультатыСопоставления(
			РезультатЗакрытия,
			НастройкаИнтеграции);
			
		НоменклатураКонтрагента = Новый Массив;
		Для Каждого СтрокаРезультата Из РезультатЗакрытия Цикл
			Если ЗначениеЗаполнено(СтрокаРезультата.НоменклатураИБ.Номенклатура) Тогда
				НоменклатураКонтрагента.Добавить(СтрокаРезультата.НоменклатураКонтрагента.Идентификатор);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(НоменклатураКонтрагента) Тогда
			ДлительнаяОперация = ОбменСМаркетплейсВызовСервера.ОбновитьНесопоставленнуюНоменклатуру(
				Маркетплейс, НоменклатураКонтрагента, Форма.УникальныйИдентификатор, Форма.Объект.Ссылка);
			Если ДлительнаяОперация = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
			ДополнительныеПараметры = Новый Структура("Форма", Форма);
			ОповещениеОЗавершении = Новый ОписаниеОповещения(
				"ОбновитьНесопоставленнуюНоменклатуруЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Окончание отложенного сопоставления номенклатуры
//
// Параметры:
//  Результат				 - Произвольный
//  ДополнительныеПараметры	 - Структура:
//   * Форма - Форма - форма документа
//
Процедура ОбновитьНесопоставленнуюНоменклатуруЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	ОповеститьОбИзменении(Форма.Объект.Ссылка);
	Форма.Прочитать();
	
	Оповестить("ИзмененыДокументыМаркетплейсов", Неопределено, Форма.Объект.Ссылка);
	
КонецПроцедуры

// Обновление регистра НоменклатураМаркетплейсов с возможной разбивкой по организациям,
// вызывается после отложенного сопоставления номенклатуры по маркетплейсу
//
// Параметры:
//  РезультатЗакрытия		 - Массив - результат сопоставления номенклатуры
//  НоменклатураОрганизаций	 - Массив - ключи для разбивки массива РезультатЗакрытия по организациям
//  Маркетплейс				 - ПеречислениеСсылки.ВидыМаркетплейсов - маркетплейс
//
Процедура ОбновитьНоменклатуруМаркетплейсов(РезультатЗакрытия, НоменклатураОрганизаций, Маркетплейс) Экспорт
	
	Если НоменклатураОрганизаций.Количество() = 1 Тогда
		Организация = Неопределено;
		Для Каждого СтрокаДанных Из НоменклатураОрганизаций Цикл
			Организация = СтрокаДанных.Ключ;
		КонецЦикла;
		НастройкаИнтеграции = Новый Структура("Владелец, ВидМаркетплейса", Организация, Маркетплейс);
		
		ОбменСМаркетплейсВызовСервера.ОбработатьРезультатыСопоставления(
			РезультатЗакрытия,
			НастройкаИнтеграции);
	Иначе
		Для Каждого ОтборПоОрганизации Из НоменклатураОрганизаций Цикл
			РезультатЗакрытияПоОрганизации = Новый Массив;
			Для Каждого КлючПоиска Из ОтборПоОрганизации.Значение Цикл
				Для Каждого СтрокаРезультатЗакрытия Из РезультатЗакрытия Цикл
					НоменклатураКонтрагента = СтрокаРезультатЗакрытия.НоменклатураКонтрагента;
					Если НоменклатураКонтрагента.ИдентификаторНоменклатуры = КлючПоиска.ИдентификаторНоменклатуры
						И НоменклатураКонтрагента.ШтрихкодыНоменклатуры = КлючПоиска.ШтрихкодыНоменклатуры Тогда
						РезультатЗакрытияПоОрганизации.Добавить(СтрокаРезультатЗакрытия);
						ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(РезультатЗакрытия, СтрокаРезультатЗакрытия);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			НастройкаИнтеграции = Новый Структура("Владелец, ВидМаркетплейса",
				ОтборПоОрганизации.Ключ,
				Маркетплейс);
			ОбменСМаркетплейсВызовСервера.ОбработатьРезультатыСопоставления(
				РезультатЗакрытияПоОрганизации,
				НастройкаИнтеграции);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтложенноеСвязываниеДокументов

Функция НовыйПараметрыДереваСвязанныхДокументов() Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДеревоДокументов");
	ПараметрыФормы.Вставить("Период", Дата(1,1,1));
	ПараметрыФормы.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	ПараметрыФормы.Вставить("Контрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
	ПараметрыФормы.Вставить("ДоговорКонтрагента", ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
	ПараметрыФормы.Вставить("Склад", ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"));
	ПараметрыФормы.Вставить("ОтложенноеСвязываниеДокументов", Ложь);
	
	Возврат ПараметрыФормы;
	
КонецФункции

#КонецОбласти

Процедура ОповеститьОбИзмененииСтатусаПредупрежденияАвтозагрузкиМаркетплейса(Документ, ПараметрыЗаписи) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыЗаписи, "ОповеститьОбИзмененииСтатусаПредупрежденияАвтозагрузкиМаркетплейса", Ложь) Тогда
		Оповестить("ИзмененыДокументыМаркетплейсов", Неопределено, Документ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
