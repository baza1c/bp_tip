
#Область ПрограммныйИнтерфейс

// Возвращает имя сервиса тарификации в Шине прикладных приложений
//
Функция ИмяСервисаТарификации() Экспорт
	
	Возврат "ReceiptScannerTariff";
	
КонецФункции

// Возвращает остаток на балансе, начиная с которого следует уведомлять пользователя о низком уровне баланса.
//
// Возвращаемое значение:
//  Число
//
Функция ПорогУведомленияНизкийУровеньБаланса() Экспорт
	
	Возврат 50;
	
КонецФункции

// Возвращает структуру данных, описывающую состояние баланса пользователя.
//
// Возвращаемое значение:
//  Структура
//
Функция НовыйДанныеТарификации() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Остаток", Неопределено);
	Результат.Вставить("ДатаПереподключенияБесплатногоТарифа", '0001-01-01');
	Результат.Вставить("ЧековОжидаетОплаты", 0);
	Результат.Вставить("РазмерБесплатногоТарифа", 0);
	Результат.Вставить("ПредставлениеОпций", "");
	
	Возврат Результат;
КонецФункции

// Читает сохраненные ранее методом Сохранить() сведения о балансе пользователя.
//
// Возвращаемое значение:
//  Структура - см. НовыйДанныеТарификации
//  Неопределено - если нет сохраненных сведений
//
Функция Прочитать() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СохраненныеДанные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ИмяСервисаТарификации());
	УстановитьПривилегированныйРежим(Ложь);
	
	ДанныеТарификации = НовыйДанныеТарификации();
	Если СохраненныеДанные = Неопределено Тогда
		Возврат ДанныеТарификации;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеТарификации, СохраненныеДанные);
	
	Возврат ДанныеТарификации;
	
КонецФункции

// Отправляет через Шину запрос сервису тарификации на подготовку сведений о состоянии баланса
//
// Параметры:
//   Приложение - имя приложения в Шине
//
Процедура ЗапроситьСостояниеБаланса(Приложение) Экспорт
	
	ТалонИТС = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ТикетАутентификации();
	Если Не ЗначениеЗаполнено(ТалонИТС) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = НовыйЗапросДанныеТарификации();
	Запрос.ТалонИТС = ТалонИТС;
	ШинаПрикладныхПриложений.ОтправитьСообщениеШине(Приложение, Запрос);
	
КонецПроцедуры

// Обрабатывает сообщение с Шины со сведениями о балансе, полученное от сервиса тарификации
//
// Параметры:
//   Сообщение - Структура
//      Остаток - Число
//      ДатаПереподключенияБесплатногоТарифа - Дата - срок, когда будет подключен новый пакет бесплатных чеков
//      ЧековОжидаетОплаты - Число - число чеков, которые не могут быть загружены, если баланс равен нулю.
//      РазмерБесплатногоТарифа - Число - размер пакета чеков, который предоставляется бесплатно
//      ТарифныеОпции: Массив из Структура:
//         Остаток - Число
//         СрокДействия - Дата
//
Процедура ОбработатьСообщение(Сообщение) Экспорт
	
	Если Не ЗначениеЗаполнено(Сообщение) Или Не Сообщение.Свойство("Остаток")
		Или Не Сообщение.Свойство("ДатаПереподключенияБесплатногоТарифа") Тогда
		
		Возврат;
	КонецЕсли;
	
	ДанныеТарификации = НовыйДанныеТарификации();
	ДанныеТарификации.Остаток = Сообщение.Остаток;
	
	ДанныеТарификации.ДатаПереподключенияБесплатногоТарифа =
		ОбщегоНазначенияБП.ДесериализоватьДатуISO(Сообщение.ДатаПереподключенияБесплатногоТарифа);
	
	Если Сообщение.Свойство("ЧековОжидаетОплаты") Тогда
		ДанныеТарификации.ЧековОжидаетОплаты = Сообщение.ЧековОжидаетОплаты;
	КонецЕсли;
	
	Если Сообщение.Свойство("РазмерБесплатногоТарифа") Тогда
		ДанныеТарификации.РазмерБесплатногоТарифа = Сообщение.РазмерБесплатногоТарифа;
	КонецЕсли;
	
	Если Сообщение.Свойство("ТарифныеОпции") И ТипЗнч(Сообщение.ТарифныеОпции) = Тип("Массив") Тогда
		
		ПредставлениеОпций = Новый Массив;
		Для Каждого Опция Из Сообщение.ТарифныеОпции Цикл
			Если Не Опция.Свойство("Остаток") Или Не Опция.Свойство("СрокДействия") Или Опция.Остаток = 0 Тогда
				Продолжить;
			КонецЕсли;
			СрокДействия = ОбщегоНазначенияБП.ДесериализоватьДатуISO(Опция.СрокДействия);
			Если ЗначениеЗаполнено(СрокДействия) Тогда
				ПредставлениеОпций.Добавить(СтрШаблон(
					НСтр("ru='%1 до %2'"),
					XMLСтрока(Опция.Остаток),
					Формат(СрокДействия, "ДЛФ=D")));
			Иначе
				ПредставлениеОпций.Добавить(XMLСтрока(Опция.Остаток));
			КонецЕсли;
		КонецЦикла;
		ДанныеТарификации.ПредставлениеОпций = СтрСоединить(ПредставлениеОпций, Символы.ПС);
	КонецЕсли;
	
	Сохранить(ДанныеТарификации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйЗапросДанныеТарификации()
	
	Запрос = Новый Структура;
	Запрос.Вставить("Команда", "ДанныеТарификации");
	Запрос.Вставить("ТалонИТС", "");
	
	Возврат Запрос;
	
КонецФункции

Функция Сохранить(ДанныеТарификации)
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		ИмяСервисаТарификации(),
		ДанныеТарификации);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

#КонецОбласти