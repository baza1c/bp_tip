
#Область ПрограммныйИнтерфейс

// Возвращает структуру параметров, по данным которой можно:
// - открыть помощник подготовки регламентированного отчета, уведомления ЕНС, требования ФНС
// - создать/изменить задачу бухгалтера в другом приложении в режиме сервиса.
//
Функция НовыйПараметрыОтчета() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("РегистрацияВНалоговомОргане", Неопределено);
	Параметры.Вставить("Срок", Дата(1,1,1));
	Параметры.Вставить("ПериодСобытия", Дата(1,1,1));
	Параметры.Вставить("КонецПериода", Дата(1,1,1));
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("ИдентификаторЗадачи", "");
	Параметры.Вставить("Правило", Неопределено);
	Параметры.Вставить("ИдентификаторПравила", "");
	Параметры.Вставить("Действие", Неопределено);
	Параметры.Вставить("НачалоВыполнения", Дата(1,1,1));
	Параметры.Вставить("Периодичность", Неопределено);
	Параметры.Вставить("НаименованиеСокращенное", "");
	Параметры.Вставить("КоличествоЗадач", 1);
	// В одной ячейке календаря отчетности может быть несколько правил,
	// например в случае документов СЭДО.
	Параметры.Вставить("ПодчиненныеПравила", Новый Массив);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру параметров, по данным которой можно будет открыть требование ФНС
// либо запустить его расшифровку.
//
Функция НовыйПараметрыТребованияФНС() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("Правило", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру параметров, по данным которой можно будет открыть документ СЭДО.
//
Функция НовыйПараметрыДокументСЭДО() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("Правило", Неопределено);
	Параметры.Вставить("ФормаСписка", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру параметров, по которой будет заполнена боковая панель в Календаре отчетности
// с дальнейшими возможными действиями с отчетом / уведомлением / требованием.
//
Функция НовыйПараметрыДействияСОтчетом() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("СледующееДействие", "");
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("НаименованиеОрганизации", "");
	Параметры.Вставить("Срок", Дата(1,1,1));
	Параметры.Вставить("НаименованиеОтчета", "");
	Параметры.Вставить("КраткоеНаименованиеОтчета", "");
	Параметры.Вставить("ТекстСообщения", "");
	Параметры.Вставить("Комментарий", "");
	Параметры.Вставить("АдресПриложения", "");
	Параметры.Вставить("НавигационнаяСсылка", "");
	Параметры.Вставить("ПараметрыЗапросаИзмененияОтчета", НовыйПараметрыЗапросаИзмененияОтчета());
	Параметры.Вставить("Приложение", Неопределено);
	Параметры.Вставить("Статус", "");
	Параметры.Вставить("КартинкаСтатуса", Новый Картинка);
	Параметры.Вставить("СтатусУстановленВручную", Ложь);
	Параметры.Вставить("РежимСовместимости", Ложь);
	Параметры.Вставить("ЕстьЗадача", Ложь);
	Параметры.Вставить("КоличествоЗадач", 1);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру параметров, которую следует передавать в теле запроса
// для изменения статуса отчета или комментария в другом приложении в режиме сервиса.
// Параметры содержат все необходимые данные, чтобы при необходимости в другом
// приложении можно было создать новую задачу (запись в РегистрСведений.ЗадачиБухгалтера).
//
Функция НовыйПараметрыЗапросаИзмененияОтчета() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ИдентификаторОрганизации", "");
	Параметры.Вставить("ИдентификаторЗадачи", "");
	Параметры.Вставить("ИдентификаторПравила", "");
	Параметры.Вставить("ИдентификаторыТребований", Новый Массив);
	Параметры.Вставить("ПериодСобытия", Дата(1,1,1));
	Параметры.Вставить("РегистрацияВНалоговомОргане", "");
	Параметры.Вставить("Действие", "");
	Параметры.Вставить("Срок", Дата(1,1,1));
	Параметры.Вставить("НачалоВыполнения", Дата(1,1,1));
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("Периодичность", "");
	Параметры.Вставить("НаименованиеСокращенное", "");
	Параметры.Вставить("Статус", "");
	Параметры.Вставить("СтатусУстановленВручную", Ложь);
	Параметры.Вставить("Комментарий", "");
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру параметров для поиска требуемого отчета в расписании Списка задач
// в этой информационной базе. 
//
Функция НовыйПараметрыПоискаОтчета() Экспорт
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Организация", Неопределено);
	ПараметрыПоиска.Вставить("Правило", Неопределено);
	ПараметрыПоиска.Вставить("РегистрацияФНС", Неопределено);
	ПараметрыПоиска.Вставить("Срок", Дата(1,1,1));
	
	Возврат ПараметрыПоиска;
КонецФункции

// Возвращает структуру параметров для поиска требуемого отчета в расписании Задач бухгалтера,
// когда работа с Календарем отчетности выполняется в режиме сервиса. Подключенные приложения
// возвращают не Правило подготовки отчета, а его идентификатор.
//
Функция НовыйПараметрыПоискаОтчетаВСервисе() Экспорт
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Организация", Неопределено);
	ПараметрыПоиска.Вставить("КодНалога", "");
	ПараметрыПоиска.Вставить("ИдентификаторПравила", "");
	ПараметрыПоиска.Вставить("РегистрацияФНС", Неопределено);
	ПараметрыПоиска.Вставить("Срок", Дата(1,1,1));
	
	Возврат ПараметрыПоиска;
КонецФункции

// Возвращает структуру параметров, необходимых для передачи в параметрах ссылки, чтобы
// приложение могло выполнить переход в помощник подготовки отчета, уведомления ЕНС.
//
Функция НовыйПараметрыНавигационнойСсылкиПодготовкаОтчета() Экспорт
	
	Параметры = НовыйПараметрыНавигационнойСсылки();
	
	Параметры.action = ИмяДействияОткрытьПомощникОчетУведомление();
	
	Параметры.Вставить("registration", ""); // регистрация в налоговом органе (строка, 4 символа)
	Параметры.Вставить("limit", ""); // крайний срок сдачи отчета (например, "20240131")
	Параметры.Вставить("period", ""); // период, за который сдается отчет (например, "20240131")
	Параметры.Вставить("description", ""); // полное название отчета, которое будет выведено в заголовок формы
										   // помощника (например, "Бухгалтерская отчетность за 2023 г.")
	Параметры.Вставить("task_id", ""); // идентификатор задачи бухгалтера (например, "БухгалтерскаяОтчетность")
	Параметры.Вставить("rule_id", ""); // идентификатор правила сдачи отчета (например, "2013_ОбщаяФорма")
	Параметры.Вставить("is_notification", ""); // не используется, передается для своместимости с допроектными версиями.
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру параметров, необходимых для передачи в параметрах ссылки, чтобы
// приложение могло выполнить переход в требование ФНС либо расшифровку требования.
//
Функция НовыйПараметрыНавигационнойСсылкиТребованиеФНС() Экспорт
	
	Параметры = НовыйПараметрыНавигационнойСсылки();
	
	Параметры.action = ИмяДействияОткрытьТребованиеФНС();
	Параметры.Вставить("request", ""); // GUID требования (элемент справочника "Документы реализации полномочий налоговых органов")
	Параметры.Вставить("is_transport_links", ""); // признак, транспортное ли это сообщение (т.е. нерасшифрованное требование)
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру параметров, необходимых для передачи в параметрах ссылки, чтобы
// приложение могло выполнить переход в документ СЭДО.
//
Функция НовыйПараметрыНавигационнойСсылкиДокументСЭДО() Экспорт
	
	Параметры = НовыйПараметрыНавигационнойСсылки();
	
	Параметры.action = ИмяДействияОткрытьДокументСЭДО();
	Параметры.Вставить("name", ""); // имя документа СЭДО
	Параметры.Вставить("ref", ""); // GUID ссылки документа СЭДО
	Параметры.Вставить("quantity", 1); // количество задач в одной ячейке
	
	Возврат Параметры;
	
КонецФункции

Функция ПустоеЗначениеПараметраНавигационнойСсылки() Экспорт
	
	Возврат "none";
	
КонецФункции

// Возвращает ключ хранения в безопасном хранилище списка организаций этого облачного приложения,
// по которым не нужно формировать календарь отчетности.
//
Функция КлючХраненияСпискаОтключенныхОрганизаций() Экспорт
	
	Возврат "ОтключенныеОрганизацииКалендаряОтчетности";
	
КонецФункции

// Возвращает статусы, в котором может находиться регламентированный отчет/уведомление/требование
// в календаре отчетности.
//
Функция СтатусыОтчета() Экспорт
	
	Статусы = Новый Структура;
	Статусы.Вставить("Сдано", ИнтерфейсыВзаимодействияБРОКлиентСервер.ПредставлениеСтатусаСдано());
	Статусы.Вставить("Подготовлено", ИнтерфейсыВзаимодействияБРОКлиентСервер.ПредставлениеСтатусаПодготовлено());
	Статусы.Вставить("ВРаботе", ИнтерфейсыВзаимодействияБРОКлиентСервер.ПредставлениеСтатусаВРаботе());
	Статусы.Вставить("Просрочено", НСтр("ru='Просрочено'"));
	Статусы.Вставить("НеСдано", НСтр("ru='Не сдано'"));
	Статусы.Вставить("ПодтвердитеПрием", НСтр("ru='Подтвердите прием'"));
	Статусы.Вставить("ПриемПодтвержден", НСтр("ru='Прием подтвержден'"));
	Статусы.Вставить("ОтказаноВПриеме", НСтр("ru='Отказано в приеме'"));
	Статусы.Вставить("ОтправьтеОтвет", НСтр("ru='Отправьте ответ'"));
	Статусы.Вставить("ОтветОтправлен", НСтр("ru='Ответ отправлен'"));
	Статусы.Вставить("Расшифровать", НСтр("ru='Расшифровать'"));
	Статусы.Вставить("НеТребуется", НСтр("ru='Не требуется'"));
	Статусы.Вставить("Оплачено", НСтр("ru='Оплачено'"));
	Статусы.Вставить("НеОплачено", НСтр("ru='Не оплачено'"));
	Возврат Статусы;
	
КонецФункции

// Проверяет, может ли пользователель самостоятельно включить/отключить необходимость подготовки
// отчета в форме настроек налогов и отчетов.
//
// Параметры:
//   КодНалога - Строка - Код задачи, по которой требуется подготовить отчет
//   ИдентификаторПравила - Строка - Идентификатор правила задачи, по которому требуется подготовить отчет
//
// Возвращаемое значение:
//   Булево
//
Функция ДоступноУправлениеОтчетом(КодНалога, ИдентификаторПравила) Экспорт
	
	СписокЗадач = Новый Массив;
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиСтатистика());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиЭкологическийСбор());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиНДСТаможенногоСоюза());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиВодныйНалог());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИгорныйБизнес());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиЕдиныйСельхозНалог());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогПолезныеИскопаемые());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиЕдинаяУпрощеннаяДекларация());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиПлатаЗаНегативноеВоздействиеНаОкружающуюСреду());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиПрочаяОтчетностьРосприроднадзор());
	СписокЗадач.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиДекларированиеАлкогольнойПродукции());
	
	Если СписокЗадач.Найти(КодНалога) <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Правила = Новый Массив;
	Правила.Добавить(ЗадачиБухгалтераКлиентСервер.ИдентификаторПравилаСтраховыеВзносыДопВзносы());
	
	Возврат Правила.Найти(ИдентификаторПравила) <> Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет, поддерживает ли Календарь отчетности работу с передаваемым видом действия календаря бухгалтера.
//
// Параметры:
//   Действие - Перечисление.ВидыДействийКалендаряБухгалтера
//
// Возвращаемое значение:
//   Булево
//
Функция ДействиеПоддерживается(Действие) Экспорт
	
	Возврат Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Отчет")
		Или Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление")
		Или Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.ФинОтчетность")
		Или Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.ТребованиеФНС")
		Или Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога")
		Или Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО");
		
КонецФункции

Функция ИмяСобытияОповещенияСпискаЗадач() Экспорт
	
	Возврат "СписокЗадачБухгалтера_РучнойСтатус";
	
КонецФункции

Функция ИмяСобытияОповещенияКалендаряОтчетности() Экспорт
	
	Возврат "КалендарьОтчетности_РучнойСтатус";
	
КонецФункции

Функция ИмяДействияОткрытьПомощникОчетУведомление() Экспорт
	
	Возврат "open_assistant";
	
КонецФункции

Функция ИмяДействияОткрытьТребованиеФНС() Экспорт
	
	Возврат "open_tax_audit_request";
	
КонецФункции

Функция ИмяДействияОткрытьДокументСЭДО() Экспорт
	
	Возврат "open_sedo_doc";
	
КонецФункции

// Возвращает структуру поиска правил для документов СЭДО в данных 
// этого или подключенных приложений
//
// Параметры:
//  ПараметрыОтчета			 - Структура - см. НовыйПараметрыОтчета
//  ПоискВДанныхПриложения	 - Булево - Истина, для поиска в данных этого приложения,
//                                      Ложь, для поиска в данных подключенных приложений
// 
// Возвращаемое значение:
//  Структура - см. НовыйПараметрыПоискаОтчетаВСервисе
//
Функция ПараметрыПоискаДокументовСЭДО(ПараметрыОтчета, ПоискВДанныхПриложения) Экспорт
	
	ПараметрыПоиска = НовыйПараметрыПоискаОтчетаВСервисе();
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ПараметрыОтчета);
	ПараметрыПоиска.КодНалога = ПараметрыОтчета.ИдентификаторЗадачи;
	ПараметрыПоиска.РегистрацияФНС = ПараметрыОтчета.РегистрацияВНалоговомОргане;
	
	Если ПоискВДанныхПриложения Тогда
		ПараметрыПоиска.Вставить("Правило", ПараметрыОтчета.Правило);
	Иначе
		ПараметрыПоиска.Вставить("ИдентификаторТребования", ПараметрыОтчета.Правило);
	КонецЕсли;
	
	Возврат ПараметрыПоиска;
	
КонецФункции

Функция ПереданыВсеНеобходимыеПараметрыПереходаПоСсылке(ПараметрыСсылки, ТребуемыеПараметры) Экспорт
	
	Для Каждого ТребуемыйПараметр Из ТребуемыеПараметры Цикл
		ЗначениеПараметра = ПараметрыСсылки.Получить(ТребуемыйПараметр.Ключ);
		Если ЗначениеПараметра = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		ТребуемыеПараметры[ТребуемыйПараметр.Ключ] = ЗначениеПараметра;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ДобавитьОрганизацииПриложения(ДанныеПриложений, ПодключенныеОрганизации, Приложение) Экспорт
	
	ОтборПоПриложению = Новый Структура("ПриложениеСсылка,ВыводитьВКалендарь", Приложение, Истина);
	СтрокиОрганизации = ПодключенныеОрганизации.НайтиСтроки(ОтборПоПриложению);
	Для Каждого Строка Из СтрокиОрганизации Цикл
		СтрокаДанных = ДанныеПриложений.Добавить();
		СтрокаДанных.Приложение = Приложение;
		СтрокаДанных.Организация = Строка.ОрганизацияСсылка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйПараметрыНавигационнойСсылки()
	
	Параметры = Новый Структура;
	
	// Параметр ссылки, указывающий на то, как следует обработать эту внешнюю ссылку.
	Параметры.Вставить("action", "");
	
	Параметры.Вставить("application_id", ""); // идентификатор приложения, сгенерировавшего ссылку
	Параметры.Вставить("organization", ""); // GUID элемента справочника "Организации"
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти