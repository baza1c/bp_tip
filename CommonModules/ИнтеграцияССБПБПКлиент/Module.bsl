
#Область ПрограммныйИнтерфейс

// Заполняет параметры сообщения электронной почты, отправляемого без шаблона.
// См. ПереводыСБПc2bКлиентПереопределяемый.ПриЗаполненииПараметровСообщенияБезШаблонаСБП
//
Процедура ПриЗаполненииПараметровСообщенияБезШаблонаСБП(ПараметрыСообщения, ПараметрыОперации) Экспорт
	
	ИнтеграцияССБПБПВызовСервера.ПриЗаполненииПараметровСообщенияБезШаблонаСБП(ПараметрыСообщения, ПараметрыОперации);
	
КонецПроцедуры

// Определяет алгоритм, выполняющийся при открытии формы кассовых ссылок из формы настройки подключения СБП.
// См. Параметры: см. ПереводыСБПc2bКлиентПереопределяемый.ПриНастройкеКассовыхСсылок()
//
Процедура ПриНастройкеКассовыхСсылок(
		ПараметрыНастройки,
		ПараметрыОплаты,
		ОповещениеПослеЗакрытияФормы) Экспорт
	
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура);
	ПараметрыФормы.Отбор.Вставить("Организация",         ПараметрыОплаты.Получить("Организация"));
	ПараметрыФормы.Отбор.Вставить("НастройкаИнтеграции", ПараметрыНастройки.НастройкаПодключения);
	ОткрытьФорму("РегистрСведений.КассовыеСсылки.ФормаСписка",
		ПараметрыФормы,
		ОповещениеПослеЗакрытияФормы.Модуль,
		Истина,
		,
		,
		ОповещениеПослеЗакрытияФормы,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Функция ВозвратОплаты(ДокументВозврат, ДокументПродажи, ПараметрыОперации, Форма, ОповещениеОЗавершении) Экспорт
	
	РезультатВозвратОплаты = ИнтеграцияССБПБПВызовСервера.ВозвратОплаты(
		ДокументВозврат, ДокументПродажи, ПараметрыОперации);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                  Форма);
	ДополнительныеПараметры.Вставить("ДокументВозврат",        ДокументВозврат);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",      ПараметрыОперации);
	ДополнительныеПараметры.Вставить("РезультатВозвратОплаты", РезультатВозвратОплаты);
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении",  ОповещениеОЗавершении);
	
	Если РезультатВозвратОплаты.СтатусОперации = "Выполнена"
		Или РезультатВозвратОплаты.СтатусОперации = "Ошибка" Тогда
		
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, НовыйРезультатОперации(РезультатВозвратОплаты.ИнформацияОбОшибке));
	Иначе
		// запустим длительную операцию проверки статуса возврата
		ДлительнаяОперация = ИнтеграцияССБПБПВызовСервера.СтатусВозврата(
			Форма.УникальныйИдентификатор, ДокументВозврат, ПараметрыОперации.НастройкаИнтеграции);
		
		СтатусВозвратаЗавершение = Новый ОписаниеОповещения("СтатусВозвратаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Региструем возврат в СБП'");
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, СтатусВозвратаЗавершение, ПараметрыОжидания);
	КонецЕсли;
	
КонецФункции

Процедура ВозвратОплатыСчет(ДокументВозврат, Форма, ОповещениеОЗавершении) Экспорт
	
	Объект = Форма.Объект;
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("Организация",    Объект.Организация);
	ПараметрыОперации.Вставить("БанковскийСчет", Объект.БанковскийСчет);
	
	АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
	
	РезультатВозвратОплаты = ИнтеграцияССБПБПВызовСервера.ВозвратОплатыСчет(
		ДокументВозврат, ПараметрыОперации, АдресРезультата);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                  Форма);
	ДополнительныеПараметры.Вставить("ДокументВозврат",        ДокументВозврат);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",      ПараметрыОперации);
	ДополнительныеПараметры.Вставить("РезультатВозвратОплаты", РезультатВозвратОплаты);
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении",  ОповещениеОЗавершении);
	
	Если РезультатВозвратОплаты.СтатусОперации = "Выполнена"
		Или РезультатВозвратОплаты.СтатусОперации = "Ошибка" Тогда
		
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, НовыйРезультатОперации(РезультатВозвратОплаты.ИнформацияОбОшибке));
	Иначе
		// запустим длительную операцию проверки статуса возврата
		ДлительнаяОперация = ИнтеграцияССБПБПВызовСервера.СтатусВозврата(
			Форма.УникальныйИдентификатор, ДокументВозврат, ПараметрыОперации.НастройкаИнтеграции);
		
		СтатусВозвратаЗавершение = Новый ОписаниеОповещения("СтатусВозвратаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Регистрируем возврат в СБП'");
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, СтатусВозвратаЗавершение, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВозвратОплатыПодтверждениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РезультатВозвратОплаты = ИнтеграцияССБПБПВызовСервера.ПодтвердитьВозврат(
		ДополнительныеПараметры.ДокументВозврат, ДополнительныеПараметры.ПараметрыОперации.НастройкиИнтеграции);
	Если РезультатВозвратОплаты.СтатусОперации = "Выполнена" 
		ИЛИ РезультатВозвратОплаты.СтатусОперации = "Ошибка" Тогда
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, РезультатВозвратОплаты);
	Иначе
		// запустим длительную операцию проверки статуса возврата
		ДлительнаяОперация = ИнтеграцияССБПБПВызовСервера.СтатусВозврата(
			ДополнительныеПараметры.Форма.УникальныйИдентификатор,
			ДополнительныеПараметры.ДокументВозврат,
			ДополнительныеПараметры.ПараметрыОперации.НастройкаИнтеграции);
		
		СтатусВозвратаЗавершение = Новый ОписаниеОповещения("СтатусВозвратаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ДополнительныеПараметры.Форма);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Региструем возврат в СБП'");
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, СтатусВозвратаЗавершение, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

Процедура СтатусВозвратаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не удалось произвести возврат покупателю. Повторите операцию позже.'"));
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ПодробноеПредставлениеОшибки) Тогда
		Ошибка = Результат.ПодробноеПредставлениеОшибки;
	ИначеЕсли ЗначениеЗаполнено(Результат.КраткоеПредставлениеОшибки) Тогда
		Ошибка = Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ошибка) Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеОЗавершении, НовыйРезультатОперации(Ошибка));
		Возврат;
	КонецЕсли;
	
	РезультатВозврата = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Если ЗначениеЗаполнено(РезультатВозврата.СообщениеОбОшибке) Тогда
		Ошибка = РезультатВозврата.СообщениеОбОшибке;
	ИначеЕсли ЗначениеЗаполнено(РезультатВозврата.КодОшибки) Тогда
		Ошибка = РезультатВозврата.КодОшибки;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеОЗавершении, НовыйРезультатОперации(Ошибка));
	
КонецПроцедуры

Процедура ПроверитьОплату(ДокументПродажи, ПараметрыОперации, Форма, ОповещениеОЗавершении) Экспорт
	
	ДлительнаяОперация = ИнтеграцияССБПБПВызовСервера.СтатусОплаты(
		Форма.УникальныйИдентификатор, ДокументПродажи, ПараметрыОперации.НастройкаИнтеграции);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Ожидание оплаты от покупателя'");
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#Область ФормаОтображенияQRКода

Процедура ПриОткрытииФормыQRКода(
		Форма,
		ДанныеПлатежнойСсылки,
		ОповещениеПослеЗавершенияНастройкиФормы) Экспорт
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("СледующееОповещение", ОповещениеПослеЗавершенияНастройкиФормы);
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		Новый ОписаниеОповещения("ПодключениеОборудованияЗавершение", ЭтотОбъект, ДополнительныеПараметры),
		Форма,
		"ДисплейПокупателя");
КонецПроцедуры

Процедура ПодключениеОборудованияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Форма", ДополнительныеПараметры.Форма);
	ПараметрыОповещения.Вставить("Результат", Результат.Результат);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.СледующееОповещение, ПараметрыОповещения);
КонецПроцедуры

Процедура ПриОтображенииQRКода(
		ДанныеПлатежнойСсылки,
		Параметры) Экспорт
	
	Если НЕ Параметры.Результат 
		ИЛИ НЕ ОборудованиеДисплеиПокупателяКлиент.ПодключенныеДисплеиПокупателяВыводятQRКод() Тогда
		
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ОборудованиеДисплеиПокупателяКлиент.ПараметрыОперацииДисплейПокупателя();
	ПараметрыОперации.СтрокиТекста   = НСтр("ru = 'Отсканируйте для оплаты'");
	ПараметрыОперации.ЗначениеQRКода = ДанныеПлатежнойСсылки.ПлатежнаяСсылка;
	
	ОборудованиеДисплеиПокупателяКлиент.НачатьВыводQRКодаНаДисплейПокупателя(
		Неопределено,
		Параметры.Форма,
		Неопределено,
		ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НовыйРезультатОперации(СообщениеОбОшибке)
	
	Результат = Новый Структура;
	Результат.Вставить("Результат",         НЕ ЗначениеЗаполнено(СообщениеОбОшибке));
	Результат.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
