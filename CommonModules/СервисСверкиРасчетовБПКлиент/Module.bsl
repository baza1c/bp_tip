#Область ПрограммныйИнтерфейс

Процедура СверитьКнигиДляПомощникаУчетаНДС(Форма, ПараметрыОтчета, ТолькоКнигаПокупок = Ложь, ТолькоКнигаПродаж = Ложь) Экспорт
	
	Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НачалоПериода",                  ПараметрыОтчета.НачалоПериода);
	СтруктураПараметров.Вставить("КонецПериода",                   ПараметрыОтчета.КонецПериода);
	СтруктураПараметров.Вставить("ДанныеДляСверкиДеклараций",      Форма.ДанныеДляСверкиДеклараций);
	СтруктураПараметров.Вставить("Организация",                    ПараметрыОтчета.Организация);
	СтруктураПараметров.Вставить("УникальныйИдентификатор",        Форма.УникальныйИдентификатор);
	СтруктураПараметров.Вставить("Контрагент",                     Неопределено);
	СтруктураПараметров.Вставить("ВыводитьТолькоДопЛисты",         Ложь);
	СтруктураПараметров.Вставить("ФормироватьДополнительныеЛисты", Истина);
	СтруктураПараметров.Вставить("ПараметрыОтчета",                ПараметрыОтчета);
	СтруктураПараметров.Вставить("ТолькоКнигаПокупок",             ТолькоКнигаПокупок);
	СтруктураПараметров.Вставить("ТолькоКнигаПродаж",              ТолькоКнигаПродаж);
	СтруктураПараметров.Вставить("ДанныеСверки",                   Неопределено);
	СтруктураПараметров.Вставить("КодВидаОперации",                Неопределено);
	СтруктураПараметров.Вставить("ОперацияНетВОрганизации",        Неопределено);
	
	ВыполнениеСверки = СервисСверкиРасчетовБПВызовСервера.СверитьДанныеДляПомощникуПоУчетуНДС(СтруктураПараметров);

	Если ВыполнениеСверки.Статус = "Выполнено" Тогда
		РезультатыСверки = ПолучитьИзВременногоХранилища(Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
		ОтобразитьРезультатыСверкиДляПомощникаПоНДС(Форма, РезультатыСверки);
	ИначеЕсли ВыполнениеСверки.Статус = "Ошибка" Тогда
		ОтобразитьОшибкуСверкиВПомощникеПоНДС(Форма, ТолькоКнигаПокупок, ТолькоКнигаПродаж);
		
	ИначеЕсли ВыполнениеСверки.Статус = "Выполняется" Тогда
		ОтобразитьОжиданиеСверкиВПомощникеПоНДС(Форма, ТолькоКнигаПокупок, ТолькоКнигаПродаж);
		Форма.ИдентификаторЗаданияСверки = ВыполнениеСверки.ИдентификаторЗадания;
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("Форма",              Форма);
		ДополнительныеПараметры.Вставить("ТолькоКнигаПокупок", ТолькоКнигаПокупок);
		ДополнительныеПараметры.Вставить("ТолькоКнигаПродаж",  ТолькоКнигаПродаж);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСверкиВПомощникеПоНДС", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ВыполнениеСверки, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;

КонецПроцедуры

Процедура ОтменитьВыполнениеСверкиВПомощнике(Форма) Экспорт
	
	СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.ОтменитьВыполнениеСверки(Форма.ИдентификаторЗаданияСверки);
	Форма.ИдентификаторЗаданияСверки = Неопределено;
	Форма.Элементы.ГруппаОжиданиеСверки.Видимость               = Ложь;
	Форма.Элементы.ГруппаОжиданиеСверкиКнигиПокупок.Видимость   = Ложь;
	Форма.Элементы.ГруппаОжиданиеСверкиКнигиПродаж.Видимость    = Ложь;
	Форма.Элементы.ГруппаРезультатыСверки.Видимость             = Ложь;
	Форма.Элементы.ГруппаРезультатыСверкиКнигиПокупок.Видимость = Ложь;
	Форма.Элементы.ГруппаРезультатыСверкиКнигиПродаж.Видимость  = Ложь;
	
КонецПроцедуры

Процедура ПослеСверкиВПомощникеПоНДС(Результат, ДополнительныеПараметры = Неопределено) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОтобразитьОшибкуСверкиВПомощникеПоНДС(Форма,
			ДополнительныеПараметры.ТолькоКнигаПокупок,
			ДополнительныеПараметры.ТолькоКнигаПродаж);
		ИначеЕсли Форма.Открыта() Тогда
			
		РезультатыСверки = ПолучитьИзВременногоХранилища(Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
		Если Не РезультатыСверки = Неопределено Тогда
			ОтобразитьРезультатыСверкиДляПомощникаПоНДС(Форма, РезультатыСверки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтобразитьОшибкуСверкиВПомощникеПоНДС(Форма, ТолькоКнигаПокупок, ТолькоКнигаПродаж)
	ЭлементыФормы = Форма.Элементы;
	Заголовок = НСтр("ru = 'Не удалось выполнить сверку счетов-фактур'");
	
	Если ТолькоКнигаПокупок Тогда
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПокупок.Видимость    = Ложь;
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПокупок.Видимость  = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПокупок.Заголовок        = Заголовок;
		ЭлементыФормы.КартинкаРезультатыСверкиКнигиПокупок.Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
	ИначеЕсли ТолькоКнигаПродаж Тогда
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПродаж.Видимость    = Ложь;
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПродаж.Видимость  = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПродаж.Заголовок        = Заголовок;
		ЭлементыФормы.КартинкаРезультатыСверкиКнигиПродаж.Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
	Иначе
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПокупок.Видимость = Ложь;
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПродаж.Видимость  = Ложь;
		
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПокупок.Видимость  = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПокупок.Видимость        = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПокупок.Заголовок        = Заголовок;
		ЭлементыФормы.КартинкаРезультатыСверкиКнигиПокупок.Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
		
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПродаж.Видимость  = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПродаж.Видимость        = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПродаж.Заголовок        = Заголовок;
		ЭлементыФормы.КартинкаРезультатыСверкиКнигиПродаж.Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.Декларация) Тогда
		ЭлементыФормы.ГруппаОжиданиеСверки.Видимость                = Ложь;
		ЭлементыФормы.ГруппаРезультатыСверки.Видимость              = Истина;
		ЭлементыФормы.РезультатыСверки.Видимость                    = Истина;
		ЭлементыФормы.РезультатыСверки.Заголовок                    = Заголовок;
		ЭлементыФормы.КартинкаРезультатыСверкиКнигиПокупок.Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтобразитьОжиданиеСверкиВПомощникеПоНДС(Форма, ТолькоКнигаПокупок, ТолькоКнигаПродаж)
	
	ЭлементыФормы = Форма.Элементы;
	Если ЗначениеЗаполнено(Форма.Декларация) Тогда
		ЭлементыФормы.ГруппаОжиданиеСверки.Видимость             = Истина;
		ЭлементыФормы.ГруппаРезультатыСверки.Видимость           = Ложь;
	КонецЕсли;
	
	Если ТолькоКнигаПокупок Тогда
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПокупок.Видимость   = Истина;
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПокупок.Видимость = Ложь;
	ИначеЕсли ТолькоКнигаПродаж Тогда 
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПродаж.Видимость   = Истина;
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПродаж.Видимость = Ложь;
	Иначе
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПокупок.Видимость   = Истина;
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПокупок.Видимость = Ложь;
		ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПродаж.Видимость    = Истина;
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПродаж.Видимость  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтобразитьРезультатыСверкиДляПомощникаПоНДС(Форма, РезультатыСверки)
	
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПокупок.Видимость = Ложь;
	ЭлементыФормы.ГруппаОжиданиеСверкиКнигиПродаж.Видимость  = Ложь;
	ЭлементыФормы.ГруппаОжиданиеСверки.Видимость             = Ложь;
	
	ЗаполнитьЗначенияСвойств(Форма.ДанныеДляСверкиДеклараций, РезультатыСверки);
	
	Если РезультатыСверки.ЕстьДанныеВКнигеПокупок Тогда
		Если РезультатыСверки.ЕстьРасхожденияВКнигеПокупок Тогда
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Расхождения с продавцами (%1)'"),
				РезультатыСверки.КоличествоРасхожденийВКнигеПокупок);
			Картинка = БиблиотекаКартинок.ВниманиеКрасный;
		ИначеЕсли РезультатыСверки.ЕстьНеСверенныеВКнигеПокупок Тогда
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не сверено с продавцами (%1)'"),
				РезультатыСверки.КоличествоНеСверенныхВКнигеПокупок);
			Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
		Иначе
			Заголовок = НСтр("ru = 'Нет расхождений с продавцами'");
			Картинка = БиблиотекаКартинок.ВыполненоУспешно;
		КонецЕсли;
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПокупок.Видимость  = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПокупок.Видимость        = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПокупок.Заголовок        = Заголовок;
		ЭлементыФормы.КартинкаРезультатыСверкиКнигиПокупок.Картинка = Картинка;
	Иначе
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПокупок.Видимость  = Ложь;
		ЭлементыФормы.РезультатыСверкиКнигиПокупок.Видимость        = Ложь;
		ЭлементыФормы.РезультатыСверкиКнигиПокупок.Заголовок        = "";
	КонецЕсли;
	
	Если РезультатыСверки.ЕстьДанныеВКнигеПродаж Тогда
		Если РезультатыСверки.ЕстьРасхожденияВКнигеПродаж Тогда
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Расхождения с покупателями (%1)'"),
				РезультатыСверки.КоличествоРасхожденийВКнигеПродаж);
			Картинка = БиблиотекаКартинок.ВниманиеКрасный;
		ИначеЕсли РезультатыСверки.ЕстьНеСверенныеВКнигеПродаж Тогда
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не сверено с покупателями (%1)'"),
				РезультатыСверки.КоличествоНеСверенныхВКнигеПродаж);
			Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
		Иначе
			Заголовок = НСтр("ru = 'Нет расхождений с покупателями'");
			Картинка = БиблиотекаКартинок.ВыполненоУспешно;
		КонецЕсли;

		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПродаж.Видимость  = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПродаж.Видимость        = Истина;
		ЭлементыФормы.РезультатыСверкиКнигиПродаж.Заголовок        = Заголовок;
		ЭлементыФормы.КартинкаРезультатыСверкиКнигиПродаж.Картинка = Картинка;
	Иначе
		ЭлементыФормы.ГруппаРезультатыСверкиКнигиПродаж.Видимость  = Ложь;
		ЭлементыФормы.РезультатыСверкиКнигиПродаж.Видимость        = Ложь;
		ЭлементыФормы.РезультатыСверкиКнигиПродаж.Заголовок        = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.Декларация) И (РезультатыСверки.ЕстьДанныеВКнигеПродаж Или РезультатыСверки.ЕстьДанныеВКнигеПокупок) Тогда
		
		Если РезультатыСверки.ЕстьРасхожденияВКнигеПокупок Или РезультатыСверки.ЕстьРасхожденияВКнигеПродаж Тогда
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Расхождения с контрагентами (%1)'"),
				РезультатыСверки.КоличествоРасхожденийВКнигеПродаж + РезультатыСверки.КоличествоРасхожденийВКнигеПокупок);
			Картинка = БиблиотекаКартинок.ВниманиеКрасный;
		ИначеЕсли РезультатыСверки.ЕстьНеСверенныеВКнигеПокупок Или РезультатыСверки.ЕстьНеСверенныеВКнигеПродаж Тогда
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не сверено с контрагентами (%1)'"),
				РезультатыСверки.КоличествоНеСверенныхВКнигеПокупок + РезультатыСверки.КоличествоНеСверенныхВКнигеПродаж);
			Картинка = БиблиотекаКартинок.ЕдиныйНалоговыйСчетВниманиеЖелтыйТреугольник;
		Иначе
			Заголовок = НСтр("ru = 'Нет расхождений с контрагентами'");
			Картинка = БиблиотекаКартинок.ВыполненоУспешно;
		КонецЕсли;
		ЭлементыФормы.ГруппаРезультатыСверки.Видимость           = Истина;
		ЭлементыФормы.РезультатыСверки.Видимость                 = Истина;
		ЭлементыФормы.РезультатыСверки.Заголовок                 = Заголовок;
		ЭлементыФормы.КартинкаРезультатСверкиДекларации.Картинка = Картинка;
	Иначе
		ЭлементыФормы.ГруппаРезультатыСверки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
