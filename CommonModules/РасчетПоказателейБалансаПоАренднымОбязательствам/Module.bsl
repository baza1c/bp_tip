#Область СлужебныйПрограммныйИнтерфейс

// Конструктор параметра "КонтекстРасчета"
// 
// Возвращаемое значение:
//  Структура:
//   * КонецПериода - Дата, МоментВремениСУточнениемПериода
//   * КонецПериодаДата - Дата
//   * Организации - Массив из СправочникСсылка.Организации
//   * ИспользуетсяВалютныйУчет - см. БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет
//   * ЕстьДисконтирование - см. УчетнаяПолитика.ПоддерживаетсяДисконтированиеОбязательств
//
Функция НовыйКонтекстРасчета() Экспорт
	
	Контекст = Новый Структура;
	
	Контекст.Вставить("КонецПериода", '0001-01-01');
	Контекст.Вставить("КонецПериодаДата", '0001-01-01');
	Контекст.Вставить("Организации",   Новый Массив);
	Контекст.Вставить("ИспользуетсяВалютныйУчет", Ложь);
	Контекст.Вставить("ЕстьДисконтирование",      Ложь);
	
	Возврат Контекст;

КонецФункции

// Возвращает исходные данные по арендным обязательствам
//
// Параметры:
//  КонтекстРасчета - см. НовыйКонтекстРасчета
//
// Возвращаемое значение:
//  см.НовыйИсходныеДанныеПоАренде
Функция ИсходныеДанныеПоАренднымОбязательствам(КонтекстРасчета) Экспорт
	
	ИсходныеДанные = НовыйИсходныеДанныеПоАренде();
	
	// Остатки по счетам 76.07, 76.27, 76.37
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РасчетыПоАренде = РасчетыПоАренде(МенеджерВременныхТаблиц,КонтекстРасчета);
	
	// НДС с авансовых арендных платежей
	НДСсАвансовыхПлатежей = НДСсАвансовыхПлатежейПоАренде(МенеджерВременныхТаблиц, КонтекстРасчета);
	
	// Арендные обязательства по графикам платежей
	Если КонтекстРасчета.ЕстьДисконтирование Тогда
		РасчетПоказателейБалансаПоАренднымОбязательствамГрафики.ДобавитьТаблицуГрафикиПлатежейПоАренде(ИсходныеДанные, КонтекстРасчета);
	КонецЕсли;
	
	Для Каждого СтрокаРасчета Из РасчетыПоАренде Цикл
		
		СтрокаТаблицы = ИсходныеДанные.АрендныеОбязательства.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаРасчета);
		
		// заполняем НДС с выданного авансового платежа, если платеж отражен на счетах 76.07.2, 76.27.2, 76.37.2
		Если СтрокаТаблицы.ОстатокПлатежей > 0 Тогда
			ДополнитьСтрокуДаннымиНДСсПлатежейПоАренде(СтрокаТаблицы, НДСсАвансовыхПлатежей);
		КонецЕсли;
		
		// Сумма платежа всегда содержит НДС, поэтому при платежах авансом величина на счетах 76.07, 76.27, 76.37 получается искаженной
		// Для устранения искажения необходимо исключить НДС с выданного авансового платежа
		ОстатокБезНДС = СтрокаРасчета.ОстатокПоАренде - СтрокаТаблицы.НДСсПлатежей;
		
		СтрокаТаблицы.ОстатокПоАрендеДт = Макс(СтрокаРасчета.ОстатокПоАренде,  0);
		СтрокаТаблицы.ОстатокПоАрендеКт = Макс(-СтрокаРасчета.ОстатокПоАренде, 0);
		СтрокаТаблицы.Обязательство  = Макс(-ОстатокБезНДС, 0);
		СтрокаТаблицы.АвансыПоАренде = Макс(ОстатокБезНДС,  0);
		
		РасчетПоказателейБалансаПоАренднымОбязательствамГрафики.ДополнитьСтрокуДаннымиИзГрафикаПоАренде(СтрокаТаблицы, ИсходныеДанные.ГрафикиПлатежей);
		
		Если СтрокаТаблицы.ЕстьГрафикПлатежей Тогда
			// Если график предусматривает годовой платеж меньше суммы процентных расходов за год или часть обязательства погашена авансом,
			// тогда величина на счетах 76.07, 76.27, 76.37 может быть меньше, чем величина по графику через 12 месяцев от отчетной даты
			СтрокаТаблицы.ДолгосрочноеОбязательство = Мин(СтрокаТаблицы.ОбязательствоРуб12, СтрокаТаблицы.Обязательство);
		ИначеЕсли СтрокаТаблицы.Долгосрочный Тогда
			СтрокаТаблицы.ДолгосрочноеОбязательство = СтрокаТаблицы.Обязательство;
		КонецЕсли;
		СтрокаТаблицы.КраткосрочноеОбязательство = СтрокаТаблицы.Обязательство - СтрокаТаблицы.ДолгосрочноеОбязательство;
		
		// если есть договоры по арендным обязательствам в валюте
		Если СтрокаТаблицы.РасчетыВВалюте Тогда
			ИсходныеДанные.ЕстьРасчетыВВалюте = Истина;
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат ИсходныеДанные;
	
КонецФункции

// Возвращает типы документов, связанные с арендой
// Возвращаемое значение:
//  Массив - типы документов аренды
//
Функция ТипыДокументовАренды() Экспорт
	
	Типы = Новый Массив;
	РасчетПоказателейБалансаПоАренднымОбязательствамГрафики.ДобавитьТипыДокументовАренды(Типы);
	
	Возврат Типы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РасчетыПоАренде

// Конструктор исходных данных по арендным обязательствам
//
// Возвращаемое значение:
//  Структура:
//   * АрендныеОбязательства - см. РасчетПоказателейБалансаПоАренднымОбязательствам.НовыйАрендныеОбязательства
//   * ГрафикиПлатежей - см. РасчетПоказателейБалансаПоАренднымОбязательствамГрафики.НовыйГрафикиПлатежейПоАренде
//                     - Неопределено - если Не ЕстьДисконтирование
//   * ЕстьРасчетыВВалюте - Булево
//
Функция НовыйИсходныеДанныеПоАренде()
	
	ИсходныеДанные = Новый Структура;
	ИсходныеДанные.Вставить("АрендныеОбязательства", НовыйАрендныеОбязательства());
	ИсходныеДанные.Вставить("ГрафикиПлатежей");
	ИсходныеДанные.Вставить("ЕстьРасчетыВВалюте", Ложь);
	
	Возврат ИсходныеДанные;
	
КонецФункции

Функция НовыйАрендныеОбязательства()
	
	ТипБулево = Новый ОписаниеТипов("Булево");
	ТипДата = Новый ОписаниеТипов("Дата");
	ТипСумма = БухгалтерскийУчетКлиентСервер.ТипСумма();
	
	Таблица = Новый ТаблицаЗначений; 
	Таблица.Колонки.Добавить("Период",                      ТипДата);
	Таблица.Колонки.Добавить("Контрагент",                  Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	Таблица.Колонки.Добавить("ДоговорКонтрагента",          Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	Таблица.Колонки.Добавить("РасчетыВВалюте",              ТипБулево);
	Таблица.Колонки.Добавить("Валюта",                      Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	Таблица.Колонки.Добавить("Курс",                        Метаданные.ОпределяемыеТипы.КурсВалюты.Тип);
	Таблица.Колонки.Добавить("СрокДоговора",                ТипДата);
	Таблица.Колонки.Добавить("Долгосрочный",                ТипБулево);
	Таблица.Колонки.Добавить("ЕстьГрафикПлатежей",          ТипБулево);
	Таблица.Колонки.Добавить("НесколькоДокументовАренды",   ТипБулево);
	Таблица.Колонки.Добавить("ДокументАренды",              Новый ОписаниеТипов(ТипыДокументовАренды()));
	Таблица.Колонки.Добавить("ДокументАрендыПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	Таблица.Колонки.Добавить("ДатаДокументАренды",          ТипДата);
	Таблица.Колонки.Добавить("НомерДокументАренды",         ОбщегоНазначения.ОписаниеТипаСтрока(30));
	Таблица.Колонки.Добавить("ОстатокПоАрендеДт",           ТипСумма);
	Таблица.Колонки.Добавить("ОстатокПоАрендеКт",           ТипСумма);
	Таблица.Колонки.Добавить("ОстатокПлатежей",             ТипСумма);
	Таблица.Колонки.Добавить("НДСсПлатежей",                ТипСумма);
	Таблица.Колонки.Добавить("АвансыПоАренде",              ТипСумма);
	Таблица.Колонки.Добавить("Обязательство",               ТипСумма);
	Таблица.Колонки.Добавить("Обязательство12",             ТипСумма);
	Таблица.Колонки.Добавить("ОбязательствоРуб12",          ТипСумма);
	Таблица.Колонки.Добавить("ДолгосрочноеОбязательство",   ТипСумма);
	Таблица.Колонки.Добавить("КраткосрочноеОбязательство",  ТипСумма);

	Возврат Таблица;

КонецФункции

Функция РасчетыПоАренде(МенеджерВременныхТаблиц, КонтекстРасчета)
	
	СчетаРасчетовПоАренде = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			СчетаРасчетовПоАренде, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоАренде));      // 76.07
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			СчетаРасчетовПоАренде, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоАрендеВал));   // 76.27
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			СчетаРасчетовПоАренде, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоАрендеУЕ));    // 76.37
			
	КонтрагентыДоговоры = Новый Массив;
	КонтрагентыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	КонтрагентыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организации",              КонтекстРасчета.Организации);
	Запрос.УстановитьПараметр("Период",                   Новый Граница(КонтекстРасчета.КонецПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Период12",                 КонецМесяца(ДобавитьМесяц(КонтекстРасчета.КонецПериодаДата, 12)));
	Запрос.УстановитьПараметр("СчетаРасчетовПоАренде",    СчетаРасчетовПоАренде);
	Запрос.УстановитьПараметр("СчетаАрендныхПлатежей",    ПланыСчетов.Хозрасчетный.СчетаАрендныхПлатежей());
	Запрос.УстановитьПараметр("КонтрагентыДоговоры",      КонтрагентыДоговоры);
	Запрос.УстановитьПараметр("ИспользуетсяВалютныйУчет", КонтекстРасчета.ИспользуетсяВалютныйУчет);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ОстаткиБУ.Субконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ОстаткиБУ.Субконто2 КАК Справочник.ДоговорыКонтрагентов), ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК ДоговорКонтрагента,
	|	СУММА(ОстаткиБУ.СуммаОстаток) КАК ОстатокПоАренде,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиБУ.Счет В (&СчетаАрендныхПлатежей)
	|				ТОГДА ОстаткиБУ.СуммаОстатокДт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстатокПлатежей
	|ПОМЕСТИТЬ РасчетыПоАренде
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет В (&СчетаРасчетовПоАренде), &КонтрагентыДоговоры, Организация В (&Организации)) КАК ОстаткиБУ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ОстаткиБУ.Субконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ОстаткиБУ.Субконто2 КАК Справочник.ДоговорыКонтрагентов), ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоАренде.Контрагент КАК Контрагент,
	|	РасчетыПоАренде.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РасчетыПоАренде.ОстатокПоАренде КАК ОстатокПоАренде,
	|	РасчетыПоАренде.ОстатокПлатежей КАК ОстатокПлатежей,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.СрокДействия, ДАТАВРЕМЯ(1, 1, 1)) КАК СрокДоговора,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяВалютныйУчет
	|			ТОГДА ЕСТЬNULL(ДоговорыКонтрагентов.Валютный, ЛОЖЬ)
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.СрокДействия, ДАТАВРЕМЯ(1, 1, 1)) > &Период12 КАК Долгосрочный
	|ИЗ
	|	РасчетыПоАренде КАК РасчетыПоАренде
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РасчетыПоАренде.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Возвращает таблицу по договорам с незаполненным видом актива для авансов
//
// Параметры: 
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//  КонтекстРасчета - см. НовыйКонтекстРасчета
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//   Контрагент - СправочникСсылка.Контрагенты
//   ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов
//   СуммаНДС - Число
//
Функция НДСсАвансовыхПлатежейПоАренде(МенеджерВременныхТаблиц, КонтекстРасчета)
	
	// Определим типы документов, которые фактически есть в остатках 76.ВА на дату отчета.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	КонтрагентыСФПолученные = Новый Массив;
	КонтрагентыСФПолученные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	КонтрагентыСФПолученные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные);

	Запрос.УстановитьПараметр("Организации",             КонтекстРасчета.Организации);
	Запрос.УстановитьПараметр("Период",                  Новый Граница(КонтекстРасчета.КонецПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаДокумента",           КонтекстРасчета.КонецПериодаДата);
	Запрос.УстановитьПараметр("КонтрагентыСФПолученные", КонтрагентыСФПолученные);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
	|	ХозрасчетныйОстатки.Субконто2 КАК СФПолученный,
	|	ХозрасчетныйОстатки.СуммаОстатокКт КАК СуммаНДС
	|ПОМЕСТИТЬ Остатки76ВА
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным),
	|			&КонтрагентыСФПолученные,
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						РасчетыПоАренде.Контрагент
	|					ИЗ
	|						РасчетыПоАренде)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СФПолученный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТИПЗНАЧЕНИЯ(Остатки76ВА.СФПолученный) КАК ТипДокумента
	|ИЗ
	|	Остатки76ВА КАК Остатки76ВА";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	// объединим запросы в цикле по типам документов
	ШаблонЗапроса = 
	"ВЫБРАТЬ
	|	РеквизитыДокумента.Контрагент КАК Контрагент,
	|	РеквизитыДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Остатки76ВА.СуммаНДС КАК СуммаНДС
	|ИЗ
	|	&ИмяДокумента КАК РеквизитыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Остатки76ВА КАК Остатки76ВА
	|		ПО РеквизитыДокумента.Ссылка = Остатки76ВА.СФПолученный
	|			И РеквизитыДокумента.Организация = Остатки76ВА.Организация
	|			И РеквизитыДокумента.Контрагент = Остатки76ВА.Контрагент
	|			И (РеквизитыДокумента.Дата <= &ДатаДокумента)";
	
	ШаблонОбъединение =
	"
	|ОБЪЕДИНИТЬ ВСЕ
	|";
			
	ТекстыПодзапросов = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		МетаданныеДокумента = Метаданные.НайтиПоТипу(Выборка.ТипДокумента);
		Если МетаданныеДокумента = Неопределено
			Или Не Метаданные.Документы.Содержит(МетаданныеДокумента)
			Или Не ДокументСодержитНужныеРеквизиты(МетаданныеДокумента) Тогда
			Продолжить;
		КонецЕсли;
		ТекстыПодзапросов.Добавить(СтрЗаменить(ШаблонЗапроса, "&ИмяДокумента", МетаданныеДокумента.ПолноеИмя()));
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТекстыПодзапросов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, ШаблонОбъединение);
	
	// поместим запрос во временную таблицу
	ЗапросОбъединение = СхемыЗапросов.НайтиТаблицуПоПсевдониму(ТекстЗапроса, "РеквизитыДокумента");
	ЗапросОбъединение.Запрос.ТаблицаДляПомещения = "ОстаткиПоДоговорам";
	ТекстЗапроса = ЗапросОбъединение.Схема.ПолучитьТекстЗапроса();

	// добавим группировку
	ШаблонРазделитель =
	"
	|;
	|";
	
	ТекстГруппировкаСУсловием =
	"ВЫБРАТЬ
	|	ОстаткиПоДоговорам.Контрагент КАК Контрагент,
	|	ОстаткиПоДоговорам.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СУММА(ОстаткиПоДоговорам.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	ОстаткиПоДоговорам КАК ОстаткиПоДоговорам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ОстаткиПоДоговорам.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|			И (ДоговорыКонтрагентов.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляАвансов.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоДоговорам.Контрагент,
	|	ОстаткиПоДоговорам.ДоговорКонтрагента";
	
	ТекстыПодзапросов = Новый Массив; 
	ТекстыПодзапросов.Добавить(ТекстЗапроса);
	ТекстыПодзапросов.Добавить(ТекстГруппировкаСУсловием);

	Запрос.Текст = СтрСоединить(ТекстыПодзапросов, ШаблонРазделитель);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НДСсПлатежей = Результат.Выгрузить();
	НДСсПлатежей.Индексы.Добавить("Контрагент, ДоговорКонтрагента");
	Возврат НДСсПлатежей;
	
КонецФункции

Функция ДокументСодержитНужныеРеквизиты(МетаданныеДокумента)

	Возврат МетаданныеДокумента.Реквизиты.Найти("Организация") <> Неопределено
			И МетаданныеДокумента.Реквизиты.Найти("Контрагент") <> Неопределено
			И МетаданныеДокумента.Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено;
	
КонецФункции

Процедура ДополнитьСтрокуДаннымиНДСсПлатежейПоАренде(СтрокаТаблицы, НДСсАвансовыхПлатежей)
	
	Если НДСсАвансовыхПлатежей = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Контрагент, ДоговорКонтрагента");
	ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
	
	Строки = НДСсАвансовыхПлатежей.НайтиСтроки(Отбор);
	Если Не ЗначениеЗаполнено(Строки) Тогда
		Возврат;
	КонецЕсли;
	// всегда должна быть одна строка с отбором Контрагент, Договор
	СтрокаТаблицы.НДСсПлатежей = Строки[0].СуммаНДС;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
