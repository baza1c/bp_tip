
#Область ПрограммныйИнтерфейс

// Выполняет проверку кодов по классификаторам.
//
// Параметры:
//  ПараметрыПроверки - Структура - см. ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйПараметрыПроверки
//
// Возвращаемое значение:
//  РезультатПроверки - Структура - см. ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйРезультатПроверки
//
Функция Проверить(ПараметрыПроверки) Экспорт
	
	Проверка = ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйПроверка();
	ЗаполнитьЗначенияСвойств(Проверка.ПараметрыПроверки.КодПоОКТМО, ПараметрыПроверки.КодПоОКТМО);
	ЗаполнитьЗначенияСвойств(Проверка.ПараметрыПроверки.КодБК, ПараметрыПроверки.КодБК);
	
	Инициализировать(Проверка);
	ВыполнитьПроверку(Проверка);
	
	Возврат Проверка.РезультатПроверки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Инициализировать(Проверка)
	
	ПараметрыПроверки = Проверка.ПараметрыПроверки.КодПоОКТМО;
	ПараметрыПроверки.ОКТМО = СокрЛП(ПараметрыПроверки.ОКТМО);
	
	Если Не ЗначениеЗаполнено(ПараметрыПроверки.Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Объекты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыПроверки.Объект);
	ВидыКонтактнойИнформации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыПроверки.ВидКонтактнойИнформации);
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		Объекты,
		Неопределено,
		ВидыКонтактнойИнформации);
		
	Если Не КонтактнаяИнформация.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Проверка.Адрес = КонтактнаяИнформация[0].Значение;
	
КонецПроцедуры

Процедура ВыполнитьПроверку(Проверка)
	
	ПараметрыПроверки = Проверка.ПараметрыПроверки;
	
	Если ЗначениеЗаполнено(ПараметрыПроверки.КодПоОКТМО.ОКТМО) Тогда
		ВыполнитьПроверкуКодПоОКТМО(Проверка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПроверки.КодБК.КБК) Тогда
		ВыполнитьПроверкуКодБК(Проверка);
	КонецЕсли;
КонецПроцедуры

Процедура ВыполнитьПроверкуКодПоОКТМО(Проверка)
	
	ПараметрыПроверки = Проверка.ПараметрыПроверки.КодПоОКТМО;
	РезультатПроверки = Проверка.РезультатПроверки.КодПоОКТМО;
	
	ДлинаКодаОКТМО = СтрДлина(ПараметрыПроверки.ОКТМО);
	ДлинаКодаОКТМОБюджетополучателя = 8;
	Если ДлинаКодаОКТМО <> ДлинаКодаОКТМОБюджетополучателя И ДлинаКодаОКТМО <> 11 Тогда
		РезультатПроверки.Успешно = Ложь;
		РезультатПроверки.ТекстОшибки = НСтр("ru = 'Неверная длина кода по ОКТМО'");
		Возврат;
	КонецЕсли;
	
	Если Не ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Проверка.Адрес) Тогда
		
		КодыАдреса = АдресныйКлассификаторБП.КодыАдреса(Проверка.Адрес);
		
		КодОКТМОБюджетополучателя = КодыАдреса.ОКТМОБюджетополучателя;
		Если Не ЗначениеЗаполнено(КодОКТМОБюджетополучателя) И Не ЗначениеЗаполнено(КодыАдреса.ОКТМО) Тогда
			Возврат;
		КонецЕсли;
		
		Если СтрДлина(КодОКТМОБюджетополучателя) <> ДлинаКодаОКТМОБюджетополучателя Тогда
			// Если классификатор содержит ошибку в коде ОКТМО бюджетополучателя, возьмем ОКТМО муниципального образования.
			КодОКТМОБюджетополучателя = КодыАдреса.ОКТМО;
		КонецЕсли;
		
		Если СтрДлина(КодОКТМОБюджетополучателя) <> ДлинаКодаОКТМОБюджетополучателя Тогда
			Возврат;
		ИначеЕсли КодОКТМОБюджетополучателя = ПараметрыПроверки.ОКТМО Тогда
			РезультатПроверки.Успешно = Истина;
			Возврат;
		КонецЕсли;
		
		ОжидаемыйКодПоОКТМО = КодОКТМОБюджетополучателя;
		РезультатПроверки.Успешно = Ложь;
		РезультатПроверки.ОжидаемыйКодПоОКТМО = ОжидаемыйКодПоОКТМО;
		РезультатПроверки.ТекстОшибки =
			СтрШаблон(НСтр("ru = 'Действующему классификатору соответствует код %1'"), ОжидаемыйКодПоОКТМО);
			
		ПроверкаКодовПоКлассификаторамПереопределяемый.ПриЗаполненииТекстаОшибкиПроверкиОКТМОПоАдресу(
			РезультатПроверки.ТекстОшибки,
			ОжидаемыйКодПоОКТМО);
		
	Иначе
		КодыПоОКТМО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыПроверки.ОКТМО);
		РезультатПроверкиОКТМО = АдресныйКлассификатор.ПроверитьОКТМОБюджетополучателя(КодыПоОКТМО);
		Если РезультатПроверкиОКТМО.Отказ Тогда
			Возврат;
		КонецЕсли;
		
		РезультатПроверки.Успешно = РезультатПроверкиОКТМО.Данные[ПараметрыПроверки.ОКТМО].Успешно;
		Если РезультатПроверки.Успешно = Ложь Тогда
			РезультатПроверки.ТекстОшибки = НСтр("ru = 'Не найден в действующем классификаторе адресов'");
			
			ПроверкаКодовПоКлассификаторамПереопределяемый.ПриЗаполненииТекстаОшибкиПроверкиОКТМО(РезультатПроверки.ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьПроверкуКодБК(Проверка)
	
	ПроверкаКодовПоКлассификаторамПереопределяемый.ПриПроверкеКодаБК(
		Проверка.РезультатПроверки.КодБК,
		Проверка.ПараметрыПроверки.КодБК);
	
КонецПроцедуры

#КонецОбласти