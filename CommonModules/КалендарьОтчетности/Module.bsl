#Область ПрограммныйИнтерфейс

// Готовит параметры отчета для Календаря отчетности в режиме сервиса, когда запрос пришел
// от другого прикладного приложения.
//
// Параметры:
//   ПараметрыЗапроса - Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыЗапросаИзмененияОтчета()
//
// Возвращаемое значение:
//   Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыОтчета()
//   Неопределено - если по данным запроса не удалось подготовить параметры отчета
//
Функция ПодготовитьПараметрыОтчета(ПараметрыЗапроса) Экспорт
	
	ПараметрыОтчета = КалендарьОтчетностиКлиентСервер.НовыйПараметрыОтчета();
	
	Попытка
		ПараметрыОтчета.Организация = Справочники.Организации.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ПараметрыЗапроса.ИдентификаторОрганизации));
			
		ПараметрРегистрацияЗаполнен = Не ПустаяСтрока(ПараметрыЗапроса.РегистрацияВНалоговомОргане);
		ПараметрыОтчета.РегистрацияВНалоговомОргане = ?(ПараметрРегистрацияЗаполнен,
			Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(
				ПараметрыОтчета.Организация,
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыОтчета.Организация, "КПП"),
				ПараметрыЗапроса.РегистрацияВНалоговомОргане),
			Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
				
		ПараметрыОтчета.Срок = ОбщегоНазначенияБП.ДесериализоватьДатуISO(ПараметрыЗапроса.Срок);
		ПараметрыОтчета.ПериодСобытия = ОбщегоНазначенияБП.ДесериализоватьДатуISO(ПараметрыЗапроса.ПериодСобытия);
		ПараметрыОтчета.КонецПериода = ПараметрыОтчета.ПериодСобытия;
		ПараметрыОтчета.НачалоВыполнения = ОбщегоНазначенияБП.ДесериализоватьДатуISO(ПараметрыЗапроса.НачалоВыполнения);
		
		ПараметрыОтчета.Наименование = ПараметрыЗапроса.Наименование;
		ПараметрыОтчета.НаименованиеСокращенное = ПараметрыЗапроса.НаименованиеСокращенное;
		ПараметрыОтчета.ИдентификаторЗадачи = ПараметрыЗапроса.ИдентификаторЗадачи;
		
		ПараметрыОтчета.Действие = Перечисления.ВидыДействийКалендаряБухгалтера[ПараметрыЗапроса.Действие];
		
		Если ПараметрыОтчета.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО Тогда
			Для Каждого ИдентификаторТребования Из ПараметрыЗапроса.ИдентификаторыТребований Цикл
				УИПравила = Новый УникальныйИдентификатор(ИдентификаторТребования);
				Правило = Документы[ПараметрыЗапроса.ИдентификаторПравила].ПолучитьСсылку(УИПравила);
				Если ЗначениеЗаполнено(ПараметрыОтчета.Правило) Тогда
					ПараметрыОтчета.ПодчиненныеПравила.Добавить(Правило)
				Иначе
					ПараметрыОтчета.Правило = Правило;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ПараметрыОтчета.Периодичность = Перечисления.Периодичность[ПараметрыЗапроса.Периодичность];
			// Календарь отчетности работает с 2-мя видами "правил":
			// ПравилаПредставленияОтчетовУплатыНалогов и ПравилаФинОтчетности.
			// Из общего числа обрабатываемых правил доля у "ПравилаФинОтчетности" незначительна.
			// В приоритете ищем значение "ПравилаПредставленияОтчетовУплатыНалогов'
			ПараметрПравилоЗаполнен = Не ПустаяСтрока(ПараметрыЗапроса.ИдентификаторПравила);
			ПараметрыОтчета.Правило = ?(ПараметрПравилоЗаполнен,
				Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору(
					ПараметрыЗапроса.ИдентификаторЗадачи,
					ПараметрыЗапроса.ИдентификаторПравила),
				Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
				
				Если Не ЗначениеЗаполнено(ПараметрыОтчета.Правило) И ПараметрПравилоЗаполнен Тогда
				ПараметрыОтчета.Правило = Справочники.ПравилаФинОтчетности.НайтиПоКоду(ПараметрыЗапроса.ИдентификаторПравила);
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'КалендарьОтчетностиПараметрыОтчета'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, ,
			ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(ПараметрыЗапроса),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Процедура СобратьСтатистикуИспользования() Экспорт
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		// Сбор статистики выполняется только в режиме сервиса.
		Возврат;
	КонецЕсли;
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("КалендарьОтчетности.ОткрытаФормаОтчета");
	
	ИмяСобытия = "КалендарьОтчетности.СозданиеФормы";
	Попытка
		
		КоличествоПриложений = КоличествоПриложений();
		Если КоличествоПриложений = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
			Возврат;
		КонецЕсли;
		
		// Собираем использование в течение заданных интервалов
		Интервалы = Новый Массив;
		Интервалы.Добавить(Перечисления.Периодичность.Квартал);
		Интервалы.Добавить(Перечисления.Периодичность.Месяц);
		Интервалы.Добавить(Перечисления.Периодичность.Неделя);
		
		ДиапазонКоличествоПриложений = Диапазон(КоличествоПриложений);
		
		Для Каждого Интервал Из Интервалы Цикл
			
			Если Не ИспользовалсяВИнтервале(Интервал, ИмяСобытия) Тогда
				// В статистику будут записаны следующие использования в течение периода
				ЗаписатьИспользовалсяВИнтервале(Интервал, ИмяСобытия);
			Иначе
				ЗаписатьСтатистику(Интервал, ИмяСобытия, ДиапазонКоличествоПриложений);
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		
		ИмяСобытияЖурнала = СтрШаблон("СтатистикаИспользования.%1", ИмяСобытия);
		ТекстОшибки       = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			ИмяСобытияЖурнала,
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Обработки.КалендарьОтчетности,
			, // Данные
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ПередатьСтатусКомментарийОтчета(ТипЗапроса, Параметры, ДлительнаяОперация, ПараметрыВыполнения) Экспорт
	
	ПараметрыЗапроса = Параметры.ПараметрыЗапросаИзмененияОтчета;
	
	Если ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьСтатусОтчета() Тогда
		ПараметрыЗапроса.Статус = Параметры.Статус;
		ПараметрыЗапроса.СтатусУстановленВручную = Параметры.СтатусУстановленВручную;
	ИначеЕсли ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаЗаписатьКомментарийОтчета() Тогда
		ПараметрыЗапроса.Комментарий = Параметры.Комментарий;
	КонецЕсли;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"СообщенияОтчетностиОблачныхПриложений.ЗапроситьСинхронно",
		ТипЗапроса,
		Параметры.Приложение,
		ПараметрыЗапроса);
	
КонецПроцедуры

Процедура ЗапроситьДанныеОтчетности(Приложения, Организации, Параметры, Запросы, ОшибкиПриложений) Экспорт
	
	Для Каждого Приложение Из Приложения Цикл
		
		Отбор = Новый Структура("ПриложениеСсылка, ВыводитьВКалендарь", Приложение.ПриложениеСсылка, Истина);
		Если Организации.НайтиСтроки(Отбор).Количество() = 0 Тогда
			// Ни одна из организаций не включена в календарь. Отправлять запрос не нужно.
			Продолжить;
		КонецЕсли;
		
		Ответ = СообщенияОтчетностиОблачныхПриложений.ЗапроситьДанныеОтчетнойКампании(
			Приложение.ПриложениеСсылка,
			Параметры);
	
		Если Ответ = "" Тогда
			// Доступ к приложению отсутствует. Отключим приложение от Календаря отчетности.
			УстановитьПривилегированныйРежим(Истина);
			РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.ОтключитьПриложение(Приложение.ПриложениеСсылка);
			УстановитьПривилегированныйРежим(Ложь);
			Продолжить;
		КонецЕсли;
			
		Если Ответ = Неопределено Тогда
			// Запрос не был принят приложением. Данные не могут быть получены.
			ОшибкиПриложений.Добавить(Приложение.ПриложениеСсылка);
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Запросы.Добавить();
		НоваяСтрока.Приложение = Приложение.ПриложениеСсылка;
		НоваяСтрока.ИдентификаторЗапроса = Ответ;
		НоваяСтрока.ТипЗапроса = СообщенияОтчетностиОблачныхПриложенийКлиентСервер.ТипЗапросаДанныеОтчетности();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьСобытиеДобавленКомментарий() Экспорт
	
	ИдентификаторПриложения = СообщенияОблачныхПриложений.ИдентификаторЭтогоПриложения();
	ИмяСобытия = СтрШаблон("КалендарьОтчетности.ДобавленКомментарий.%1", ИдентификаторПриложения);
	
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(ИмяСобытия, 1);
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяСобытия);
	
КонецПроцедуры

Процедура ОбработатьОтветНаЗапросСпискаОрганизаций(СписокОрганизаций, Приложение, ПодключенныеОрганизации) Экспорт
	
	Если Не СписокОрганизаций.Количество() Тогда
		// Пустой ответ указывает на то, что у пользователя больше нет прав ни на одну организацию
		// из другого приложения. Отключим все ранее подключенные организации этого приложения.
		
		УстановитьПривилегированныйРежим(Истина);
		РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.ОтключитьПриложение(Приложение);
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из СписокОрганизаций Цикл
		
		// Ответ должен содержать обязательные реквизиты:
		// Идентификатор, Наименование.
		// Если хоть один реквизит отсутствует, то нет смысла обрабатывать ответ.
		Если Не ЭлементСписка.Свойство("Идентификатор")Или Не ЭлементСписка.Свойство("Наименование") Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("ОрганизацияИдентификатор, ПриложениеСсылка",
			ЭлементСписка.Идентификатор, Приложение);
			
		НайденыеСтроки = ПодключенныеОрганизации.НайтиСтроки(Отбор);
		Если НайденыеСтроки.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		Организация = Справочники.ОрганизацииОблачныхПриложений.СсылкаОрганизация(
			ЭлементСписка.Идентификатор,
			Приложение,
			ЭлементСписка.Наименование);
			
		Если ЗначениеЗаполнено(Организация) Тогда
			РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.УстановитьСостояние(Организация, Ложь);
			//Перечитаем данные подключенных организаций.
			ПодключенныеОрганизации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьОтветНаЗапросДанныеОтчетности(ТаблицаДанных, ДанныеОтчетности, Приложение, ПодключенныеОрганизации) Экспорт
	
	Если Не ДанныеОтчетности.Количество() Тогда
		// Пустой ответ указывает на то, что у пользователя больше нет прав ни на одну организацию
		// из другого приложения. Отключим все ранее подключенные организации этого приложения.
		УстановитьПривилегированныйРежим(Истина);
		РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.ОтключитьПриложение(Приложение);
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
	КонецЕсли;
	
	Для Каждого ПолученныеДанные Из ДанныеОтчетности Цикл
		
		// Ответ должен содержать обязательные реквизиты:
		// ИдентификаторОрганизации, НаименованиеОрганизации.
		// Если хоть один реквизит отсутствует, то нет смысла обрабатывать ответ.
		Если Не ПолученныеДанные.Свойство("ИдентификаторОрганизации")
			Или Не ПолученныеДанные.Свойство("НаименованиеОрганизации") Тогда
			
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("ОрганизацияИдентификатор, ПриложениеСсылка",
			ПолученныеДанные.ИдентификаторОрганизации, Приложение);
		
		НайденыеСтроки = ПодключенныеОрганизации.НайтиСтроки(Отбор);
		// От приложения могут быть получены сведения по отсутствующей организации.
		Организация = Неопределено;
		Если НайденыеСтроки.Количество() = 0 Тогда
			УстановитьПривилегированныйРежим(Истина);
			НоваяОрганизация = Справочники.ОрганизацииОблачныхПриложений.СсылкаОрганизация(
				ПолученныеДанные.ИдентификаторОрганизации,
				Приложение,
				ПолученныеДанные.НаименованиеОрганизации);
			УстановитьПривилегированныйРежим(Ложь);
			
				
			Если НоваяОрганизация = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Истина);
			РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.УстановитьСостояние(НоваяОрганизация, Истина);
			Организация = НоваяОрганизация;
			//Перечитаем данные подключенных организаций.
			ПодключенныеОрганизации = РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации();
			УстановитьПривилегированныйРежим(Ложь);
		Иначе
			СтрокаОрганизация = НайденыеСтроки[0];
			Организация = СтрокаОрганизация.ОрганизацияСсылка;
			Если СтрокаОрганизация.ОрганизацияНаименование <> СокрЛП(ПолученныеДанные.НаименованиеОрганизации) Тогда
				// Организация в другом приложении может быть переименована. Изменим наименование в этом приложении.
				УстановитьПривилегированныйРежим(Истина);
				Справочники.ОрганизацииОблачныхПриложений.Переименовать(
					Организация,
					ПолученныеДанные.НаименованиеОрганизации);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
		КонецЕсли;
		
		СтрокаДанных = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанных, ПолученныеДанные);
		
		СтрокаДанных.Организация = Организация;
		СтрокаДанных.Приложение = Приложение;
		Если ПолученныеДанные.Свойство("Действие") Тогда
			Для Каждого ВидДействия из Перечисления.ВидыДействийКалендаряБухгалтера Цикл
				Если Строка(ВидДействия) = ПолученныеДанные.Действие Тогда
					СтрокаДанных.Действие = ВидДействия;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПравилаНалоговУплачиваемыхВнеЕНС(Период) Экспорт
	
	ТаблицаНалоговПоКодамЗадач = Новый ТаблицаЗначений;
	ТаблицаНалоговПоКодамЗадач.Колонки.Добавить("КодЗадачи", ОбщегоНазначения.ОписаниеТипаСтрока(30));
	ТаблицаНалоговПоКодамЗадач.Колонки.Добавить("Налог", Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	
	Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ, Ложь, Период);
	Если ЗначениеЗаполнено(Налог) Тогда
		
		НоваяСтрока = ТаблицаНалоговПоКодамЗадач.Добавить();
		НоваяСтрока.КодЗадачи = "2023_Уплата_Травматизм";
		НоваяСтрока.Налог     = Налог;
		
		НоваяСтрока = ТаблицаНалоговПоКодамЗадач.Добавить();
		НоваяСтрока.КодЗадачи = "2022_Уплата_ФСС_НС_АУСН";
		НоваяСтрока.Налог     = Налог;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНалоговПоКодамЗадач", ТаблицаНалоговПоКодамЗадач);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНалоговПоКодамЗадач.КодЗадачи КАК КодЗадачи,
	|	ТаблицаНалоговПоКодамЗадач.Налог КАК Налог
	|ПОМЕСТИТЬ ТаблицаНалоговПоКодамЗадач
	|ИЗ
	|	&ТаблицаНалоговПоКодамЗадач КАК ТаблицаНалоговПоКодамЗадач
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Регламент.Ссылка КАК Правило,
	|	ЕСТЬNULL(ВидыНалоговИПлатежейВБюджет.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет)) КАК СчетУчета
	|ИЗ
	|	ТаблицаНалоговПоКодамЗадач КАК ТаблицаНалоговПоКодамЗадач
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|		ПО ТаблицаНалоговПоКодамЗадач.Налог = ВидыНалоговИПлатежейВБюджет.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Регламент
	|		ПО ТаблицаНалоговПоКодамЗадач.КодЗадачи = Регламент.Код";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПоказыватьМоиПриложения() Экспорт
	
	Перем СвойстваОтвета;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ПрограммныйИнтерфейсСервиса.ДействующийТокенДоступа(, Ложь, СвойстваОтвета);
		Возврат Не СвойстваОтвета.Ошибка;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция НавигационнаяСсылкаНаПомощник(АдресПриложения, ПараметрыСсылки) Экспорт
	
	СтрокаПараметров = "";
	Для Каждого Параметр Из ПараметрыСсылки Цикл
		ЗначениеПараметра = ?(ЗначениеЗаполнено(Параметр.Значение), Параметр.Значение, "none");
		КодированноеЗначение = КодироватьСтроку(ЗначениеПараметра, СпособКодированияСтроки.КодировкаURL);
		СтрокаПараметров = СтрШаблон("%1%2=%3&", СтрокаПараметров, Параметр.Ключ, КодированноеЗначение);
	КонецЦикла;
	
	// При вызове метода ПерейтиПоНавигационнойСсылке() платформа всегда сначала пробует выполнить внутренний переход по ссылке.
	// Поэтому ссылка намерененно генерируется невалидная, чтобы платформа не смогла выполнить внутренний переход,
	// а выполнила внешний переход по этой навигационной ссылке.
	Возврат СтрШаблон("%1/#e1cib/ОбработкаКалендарьОтчетности?%2", АдресПриложения, СтрокаПараметров);
	
КонецФункции

Функция ПараметрыНавигационнойСсылки(Расписание) Экспорт
	
	ПараметрыСсылки = КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиПодготовкаОтчета();
	
	ПараметрыСсылки.application_id =
		ПередаваемоеЗначениеПараметраСсылки(КалендарьОтчетностиВызовСервера.ИдентификаторЭтогоПриложения());
	
	ПараметрыСсылки.registration = ?(ЗначениеЗаполнено(Расписание.РегистрацияФНС),
		ПередаваемоеЗначениеПараметраСсылки(Расписание.КодФНС),
		КалендарьОтчетностиКлиентСервер.ПустоеЗначениеПараметраНавигационнойСсылки());
		
	ПараметрыСсылки.organization = ПередаваемоеЗначениеПараметраСсылки(Расписание.ИдентификаторОрганизации);
	ПараметрыСсылки.limit = ПередаваемоеЗначениеПараметраСсылки(Формат(Расписание.Срок, "ДФ=yyyyMMdd"));
	ПараметрыСсылки.period = ПередаваемоеЗначениеПараметраСсылки(Формат(Расписание.КонецПериода, "ДФ=yyyyMMdd"));
	ПараметрыСсылки.description = ПередаваемоеЗначениеПараметраСсылки(Расписание.НаименованиеЗадачи);
	ПараметрыСсылки.task_id = ПередаваемоеЗначениеПараметраСсылки(Расписание.КодНалога);
	ПараметрыСсылки.rule_id = ПередаваемоеЗначениеПараметраСсылки(Расписание.ИдентификаторПравила);
	
	Возврат ПараметрыСсылки;
	
КонецФункции

Функция ПараметрыНавигационнойСсылкиТребованиеФНС(Расписание) Экспорт
	
	ПараметрыСсылки = КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиТребованиеФНС();
	ПараметрыСсылки.application_id = ПередаваемоеЗначениеПараметраСсылки(КалендарьОтчетностиВызовСервера.ИдентификаторЭтогоПриложения());
	
	ПараметрыСсылки.organization = ПередаваемоеЗначениеПараметраСсылки(Расписание.ИдентификаторОрганизации);
	ПараметрыСсылки.request = ПередаваемоеЗначениеПараметраСсылки(Расписание.ИдентификаторТребования);
	ПараметрыСсылки.is_transport_links = ?(Расписание.ИдентификаторПравила = "Транспортное сообщение", "true", "false");
	
	Возврат ПараметрыСсылки;
	
КонецФункции

Функция ПараметрыНавигационнойСсылкиДокументСЭДО(Расписание, КоличествоЗадач) Экспорт
	
	ПараметрыСсылки = КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиДокументСЭДО();
	ПараметрыСсылки.application_id =ПередаваемоеЗначениеПараметраСсылки(КалендарьОтчетностиВызовСервера.ИдентификаторЭтогоПриложения());
	ПараметрыСсылки.organization = ПередаваемоеЗначениеПараметраСсылки(Расписание.ИдентификаторОрганизации);
	ПараметрыСсылки.name = ПередаваемоеЗначениеПараметраСсылки(Расписание.ИдентификаторПравила);
	ПараметрыСсылки.ref = ПередаваемоеЗначениеПараметраСсылки(Расписание.ИдентификаторТребования);
	ПараметрыСсылки.quantity= КоличествоЗадач;
	
	Возврат ПараметрыСсылки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КоличествоПриложений()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ОрганизацииОтчетности.Пользователь) КАК КоличествоПриложений
	|ИЗ
	|	РегистрСведений.ОрганизацииОтчетностиОблачныхПриложений КАК ОрганизацииОтчетности
	|ГДЕ
	|	ОрганизацииОтчетности.Пользователь = &Пользователь";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка.КоличествоПриложений;
	
КонецФункции

Функция ИспользовалсяВИнтервале(Интервал, ИмяСобытия)
	
	ПредыдущееИспользование = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяСобытия, XMLСтрока(Интервал), '0001-01-01');
	ЭтоИспользование = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Интервал, ОбщегоНазначения.ТекущаяДатаПользователя());
	
	Возврат ПредыдущееИспользование = ЭтоИспользование;
	
КонецФункции

Процедура ЗаписатьИспользовалсяВИнтервале(Интервал, ИмяСобытия)
	
	Значение = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Интервал, ОбщегоНазначения.ТекущаяДатаПользователя());
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяСобытия, XMLСтрока(Интервал), Значение);
	
КонецПроцедуры

Процедура ЗаписатьСтатистику(Интервал, ИмяСобытия, ДиапазонКоличествоПриложений)
	
	ИмяПоказателя = СтрШаблон(
		"%1.%2.%3",
		ИмяСобытия,
		XMLСтрока(Интервал),
		XMLСтрока(ДиапазонКоличествоПриложений));
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяПоказателя);

КонецПроцедуры

Функция Диапазон(Значение)
	
	КоличествоИнтервалов = 5;
	Основание = 2.71828; // число Эйлера
	
	НачалоИнтервала = 1;
	
	Для ИндексИнтервала = 0 По КоличествоИнтервалов Цикл
		КонецИнтервала = Цел(Pow(Основание, ИндексИнтервала));
		Если Значение <= КонецИнтервала Тогда
			Возврат НачалоИнтервала;
		КонецЕсли;
		НачалоИнтервала = КонецИнтервала;
	КонецЦикла;
	
	Возврат КонецИнтервала;
	
КонецФункции

Функция ПередаваемоеЗначениеПараметраСсылки(Значение)
	
	ПустоеЗначениеПараметра = КалендарьОтчетностиКлиентСервер.ПустоеЗначениеПараметраНавигационнойСсылки();
	Возврат ?(ЗначениеЗаполнено(Значение), Значение, ПустоеЗначениеПараметра);
	
КонецФункции

#КонецОбласти
