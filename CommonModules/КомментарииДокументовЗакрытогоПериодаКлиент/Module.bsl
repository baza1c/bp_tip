
#Область ПрограммныйИнтерфейс

//Открывает форму ввода комментария
//Параметры: форма Тип("ФормаКлиентскогоПриложения")
//
//Возвращаемое значение: нет
//
Процедура ИзменитьКомментарий(Форма) Экспорт
	
	СсылкаНаДокумент = Форма.Объект.Ссылка;
	Комментарий = КомментарииДокументовЗакрытогоПериодаВызовСервера.ТекущийКомментарийДокумента(СсылкаНаДокумент);
	ДопПараметры = Новый Структура("Форма, ТекущийКомментарий", Форма, Комментарий);
	Оповещение = Новый ОписаниеОповещения("РедактированиеКомментарияЗавершение", ЭтотОбъект, ДопПараметры);
	
	ПоказатьВводСтроки(Оповещение, Комментарий, "Комментарий");
	
КонецПроцедуры

//Обработчик введенного комментария в форме списка
//Параметры: СсылкаНаДокумент Тип("ДокументСсылка.***")
//
//Возвращаемое значение: нет
//
Процедура ИзменитьКомментарийВСписке(СсылкаНаДокумент) Экспорт
	
	Если НЕ КомментарииДокументовЗакрытогоПериодаВызовСервера.РазрешенаБлокировкаОбъектаДляРедактирования(СсылкаНаДокумент) Тогда
		ПоказатьПредупреждение(, "Объект редактируется другим пользователем");
		Возврат;	
	КонецЕсли;

	Если Не КомментарииДокументовЗакрытогоПериодаВызовСервера.РазрешитьИзменениеКомментарияВСписке(СсылкаНаДокумент) Тогда
	    ПоказатьПредупреждение(, "Нарушение прав доступа");
		Возврат;
	КонецЕсли;

	Комментарий = КомментарииДокументовЗакрытогоПериодаВызовСервера.ТекущийКомментарийДокумента(СсылкаНаДокумент);
	ДопПараметры = Новый Структура("Документ, ТекущийКомментарий", СсылкаНаДокумент, Комментарий);
	Оповещение = Новый ОписаниеОповещения("РедактированиеКомментарияЗавершение", ЭтотОбъект, ДопПараметры);
		
	ПоказатьВводСтроки(Оповещение, Комментарий, "Комментарий");
	
КонецПроцедуры

//Обработчик введенного коментария в форме документа и форме списка
//Параметры:
//			- ВведенаяСтрока Тип("Строка")
//			- ДополнительныеПараметры Тип("Структура")
//
//Возвращаемое значение: нет
//
Процедура РедактированиеКомментарияЗавершение(ВведеннаяСтрока, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	ТекстОшибки = "";
	//Пользователь отказался от ввода
	Если ВведеннаяСтрока = Неопределено Или 
		ДополнительныеПараметры.ТекущийКомментарий = ВведеннаяСтрока Тогда  
		Возврат;	
	КонецЕсли;
	
	ФормаДокумента = Неопределено;
	
	Если ДополнительныеПараметры.Свойство("Форма") Тогда
		ФормаДокумента = ДополнительныеПараметры.Форма;
		СсылкаНаДокумент = ФормаДокумента.Объект.Ссылка;
	ИначеЕсли ДополнительныеПараметры.Свойство("Документ") Тогда
		СсылкаНаДокумент = ДополнительныеПараметры.Документ;
	КонецЕсли;
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ЗаписатьКомментарий(СсылкаНаДокумент, ВведеннаяСтрока, Отказ, ТекстОшибки);
	
	Если Отказ Тогда
		ПоказатьПредупреждение(, ТекстОшибки);	
	Иначе
		Если Не ФормаДокумента = Неопределено Тогда
			ФормаДокумента.ЭтотОбъект.Прочитать();
		КонецЕсли;
		Оповестить("ИзмененКомментарий", СсылкаНаДокумент);//Для формы списка
	КонецЕсли;
	
КонецПроцедуры 

//Обработчик оповещения в форме списка документов
//Параметры:
//			-Форма Тип("ФормаКлиентскогоПриложения")
//			- ИмяСобытия Тип("Строка")
//			- Параметр Тип("Любой")
//			- Источник Тип("ФормаКлиентскогоПриложения")
//
//Возвращаемое значение: нет
//
Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = "ИзмененКомментарий" Тогда
		ИмяФормыСписка = КомментарииДокументовЗакрытогоПериодаВызовСервера.ПолучитьИмяФормыСпискаДокумента(Параметр);	
		Если Форма.ИмяФормы = ИмяФормыСписка Тогда
			Форма.Элементы.Список.Обновить();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры  

#КонецОбласти