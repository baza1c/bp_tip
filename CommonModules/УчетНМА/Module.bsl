
#Область ПрограммныйИнтерфейс

// Проверяет, применяется ли в конкретном периоде устаревший стандарт ПБУ 14
//
// Параметры:
//  Период - Дата
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Булево - Истина - применяется ПБУ 14, Ложь - применяется ФСБУ 14
//
Функция ПрименяетсяПБУ14(Период) Экспорт
	
	Возврат Период < НачалоОбязательногоПримененияФСБУ14();
	
КонецФункции

// Определяет дату начала обязательного применения ФСБУ 14/22 "Нематериальные активы"
// 
// Возвращаемое значение:
//  Дата
//
Функция НачалоОбязательногоПримененияФСБУ14() Экспорт
	Возврат '2024-01-01';
КонецФункции

// Определяет, нужно ли при расчете амортизации в бухгалтерском учете
// задерживать начало и конец амортизации на месяц, следующий за признанием и списанием объекта.
//
// Параметры:
//  Период      - Дата
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Булево - Истина, если нужно задерживать.
//           Ложь, если амортизация начинается со дня признания.
//
Функция ЗадерживатьАмортизацию(Период, Организация) Экспорт
	
	Возврат ПрименяетсяПБУ14(Период) Или Перечисления.АлгоритмыПериодаАмортизации.ЗадерживатьАмортизацию(Период, Организация);
	
КонецФункции

// Получает данные и формирует проводки регламентной операции "Переход на ФСБУ 14".
// 
// Параметры:
//  СтруктураШапкиДокумента - Структура
//  Отказ - Булево
//  Движения - КоллекцияДвижений
//
Процедура ПереходНаФСБУ14(СтруктураШапкиДокумента, Отказ, Движения) Экспорт
	
	ПараметрыПроведения = Документы.РегламентнаяОперация.ПодготовитьПараметрыПереходНаФСБУ14(СтруктураШапкиДокумента, Отказ);
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	
	// Если в обработке подготовки данных для перехода на ФСБУ 14 пользователем используется режим "Стоимость активов определяется: По иным данным",
	// то появляется возможность указать поля Обесценение и ОбесценениеДоступноеДляВосстановления.
	// В первой версии программы проводки с учетом указанных сумм формировались на счет амортизации, а не на счет обесценения.
	// См. ПовторноПровестиПереходФСБУ14Обесценение
	ПодготовитьДанныеПереходаНаФСБУ14(ПараметрыПроведения);
	
	ТаблицаИзменениеСтоимостиНМА = ТаблицаИзменениеСтоимостиНМА(ПараметрыПроведения);
	
	ТаблицаСтоимостиПереклассификацияНМА = ТаблицаСтоимостиПереклассификацияНМА(ПараметрыПроведения);
	
	// Формирование движений по регистрам сведений
	СформироватьДвиженияПервоначальныеСведенияНМАБухгалтерскийУчет(Движения, ПараметрыПроведения, ТаблицаСтоимостиПереклассификацияНМА);
	
	СформироватьДвиженияЭлементыАмортизацииНМАБухгалтерскийУчет(Движения, ПараметрыПроведения);
	
	СформироватьДвиженияСостоянияНМА(Движения, ПараметрыПроведения);
	
	СформироватьДвиженияСпособыОтраженияРасходов(Движения, ПараметрыПроведения);
	
	СформироватьДвиженияСчетаУчетаНМА(Движения, ПараметрыПроведения);
	
	// Формирование движений по регистру бухгалтерии
	СформироватьПроводкиПереходНаФСБУ14(Движения, ПараметрыПроведения.ТаблицаРеквизиты, ТаблицаИзменениеСтоимостиНМА, ТаблицаСтоимостиПереклассификацияНМА);
	
	Движения.Хозрасчетный.ЗаполнитьСуммыВременныхРазниц();
	
	Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("УстанавливаемоеУточнениеПериода",
		БухгалтерскийУчетКлиентСервер.ОсновноеЗначениеУточненияМежотчетногоПериода());
	
КонецПроцедуры

// Формирует представление группы бухгалтерского учета в соответствии с ФСБУ 14.
// Сейчас в программе не реализовано деление объектов НМА на группы учета,
// поэтому выводим группы, соответствующие видам.
//
// Параметры:
//  ВидНематериальногоАктива - ПеречислениеСсылка.ВидыНематериальныхАктивов - актуальный вид нематериального актива, как задано в справочнике
//  ВидНМАПоПБУ14 - ПеречислениеСсылка.ВидыНМАПоПБУ14 - применявшийся ранее вид нематериального актива, как задано в справочнике
// 
// Возвращаемое значение:
//  Строка - представление вида НМА
//
Функция ПредставлениеГруппыУчета(ВидНематериальногоАктива, ВидНМАПоПБУ14 = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ВидНематериальногоАктива) Тогда
		Возврат Строка(ВидНематериальногоАктива); // группой считается совокупность одного вида, п. 12 ФСБУ 14
	ИначеЕсли ВидНМАПоПБУ14 = Перечисления.ВидыНМАПоПБУ14.ИсключительноеПравоНаИзобретение
		Или ВидНМАПоПБУ14 = Перечисления.ВидыНМАПоПБУ14.ИсключительноеПравоНаСелекционныеДостижения
		Или ВидНМАПоПБУ14 = Перечисления.ВидыНМАПоПБУ14.ИмущественноеПравоНаТопологииИМС
		Или ВидНМАПоПБУ14 = Перечисления.ВидыНМАПоПБУ14.ИсключительноеПравоНаПрограммыДляЭВМ Тогда
		// Нет прямого соответствия между видами, приведенными в ФСБУ 14, и применявшимися ранее.
		// Поэтому не опираемся на применявшиеся ранее, а сгруппируем их более-менее произвольно
		Возврат НСтр("ru = 'Результаты интеллектуальной деятельности'");
	ИначеЕсли ВидНМАПоПБУ14 = Перечисления.ВидыНМАПоПБУ14.ИсключительноеПравоНаТоварныйЗнак Тогда
		Возврат НСтр("ru = 'Средства идивидуализации'");
	Иначе
		Возврат НСтр("ru = 'Иные объекты НМА'");
	КонецЕсли;
	
КонецФункции

// Определяет, является ли нематериальный актив малоценным в соответствии с ФСБУ 14.
// Значение зависит от стоимости приобретения и настроек учетной политики.
// 
// Параметры:
//  Контекст - Структура - КлючИЗначение:
//    * Организация - СправочникСсылка.Организации
//    * Период - Дата
//    * Стоимость - ОпределяемыйТип.ДенежнаяСуммаНеотрицательная
//    * ВалютаДокумента - СправочникСсылка.Валюты
//    * КурсВзаиморасчетов - Число
//    * КратностьВзаиморасчетов - Число
// 
// Возвращаемое значение:
//  Булево
//
Функция ЭтоМалоценныйНМА(Контекст) Экспорт
	
	НастройкиСущественностиВНА = Справочники.НастройкиСущественностиВнеоборотныхАктивов.НастройкиСущественностиВНА(Контекст);

	УказанЛимитСтоимости = (НастройкиСущественностиВНА.СпособУказанияЛимита = Перечисления.СпособыУказанияЛимитаСущественностиВНА.ПоСтоимости);
	
	Стоимость = Контекст.Стоимость;
	
	Если УказанЛимитСтоимости И Контекст.ВалютаДокумента <> Константы.ВалютаРегламентированногоУчета.Получить() Тогда
		Если Контекст.КратностьВзаиморасчетов > 0 Тогда
			Стоимость =
				Окр(Стоимость * Контекст.КурсВзаиморасчетов / Контекст.КратностьВзаиморасчетов, 2);
		Иначе
			Стоимость = 0;
		КонецЕсли;
	КонецЕсли;
		
	ЭтоМалоценныйНМА = УказанЛимитСтоимости И Стоимость < НастройкиСущественностиВНА.ЛимитСтоимости;
	
	Возврат ЭтоМалоценныйНМА;
	
КонецФункции

// Добавляет проводку по отражению убытка от обесценения (остатка обесценения, доступного к восстановлению).
// В общем случае (для корсчетов, предусматривающих учет ВР) после вызова функции следует обеспечить заполнение сумм временных разниц,
// например, с помощью ЗаполнитьСуммыВременныхРазниц.
// Для этого может использоваться возвращаемое значение.
//
// Параметры:
//  Проводки - РегистрБухгалтерииНаборЗаписей.Хозрасчетный - заполняемая коллекция проводок
//  Реквизиты - Структура - Ключ: Период, Организация
//  НематериальныйАктив - СправочникСсылка.НематериальныеАктивы - обесцениваемый актив
//  Подразделение - СправочникСсылка.ПодразделенияОрганизаций - подразделение, в котором учтен обесцениваемый актив
//  СодержаниеПроводок - Строка - содержание проводки
//  КорСчет - ПланСчетовСсылка.Хозрасчетный - счет дебета (расходов или капитала)
//  СуммаПроводки - Число
// 
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись.Хозрасчетный - добавленная проводка
//  - Неопределено - исходя из переданных параметров добавлять проводку не следует
//
Функция ДобавитьПроводкуДоступноеОбесценение(Проводки, Реквизиты, НематериальныйАктив, Подразделение, СодержаниеПроводок, КорСчет, СуммаПроводки) Экспорт
	
	Если СуммаПроводки = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Проводка = Проводки.Добавить();
	
	Проводка.Период      = Реквизиты.Период;
	Проводка.Организация = Реквизиты.Организация;
	Проводка.Содержание  = СодержаниеПроводок;
	
	Проводка.СчетДт = КорСчет;
	Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ОбесценениеНематериальныхАктивов;
	
	БухгалтерскийУчет.УстановитьПодразделенияПроводки(Проводка, Подразделение, Подразделение);
	
	БухгалтерскийУчет.УстановитьСубконто(
		Проводка.СчетКт,
		Проводка.СубконтоКт,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы,
		НематериальныйАктив);
	
	Проводка.Сумма = СуммаПроводки;
	
	Возврат Проводка;
	
КонецФункции

#Область ОбработчикиОбновления

// Вызывается в версии, в которой добавлен отдельный субсчет для учета накопленного обесценения.
// После появления субсчета рекомендуется повторно выполнить регламентную операцию перехода на ФСБУ 14 с тем,
// чтобы отразить признанное при переходе обесценение на новом субсчете.
//
// Параметры:
//  ПараметрыОбработчика - Структура - управляющая коллекция, см. документацию к подсистеме ОбновлениеИнформационнойБазы БСП.
//      Параметр фактически не используется, так как все данные обрабатываются за один раз.
//
Процедура ПовторноПровестиПереходФСБУ14Обесценение(ПараметрыОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НМАПриПереходеНаФСБУ14.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НМАПриПереходеНаФСБУ14 КАК НМАПриПереходеНаФСБУ14
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпособПереходаНаФСБУ14 КАК СпособПереходаНаФСБУ14
	|		ПО НМАПриПереходеНаФСБУ14.Организация = СпособПереходаНаФСБУ14.Организация
	|ГДЕ
	|	НЕ СпособПереходаНаФСБУ14.СтоимостьОпределяетсяПоДаннымУчета
	|	И НМАПриПереходеНаФСБУ14.Обесценение > 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// Потребуем повторно выполнить операции, если отражены данные по обесценению
	ВидОперации    = Перечисления.ВидыРегламентныхОпераций.ПереходНаФСБУ14;
	НомерГруппы    = Перечисления.ВидыРегламентныхОпераций.НомерГруппы(ВидОперации);
	ПериодОперации = НачалоОбязательногоПримененияФСБУ14() - 1;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
			Выборка.Организация,
			ПериодОперации,
			НомерГруппы,
			ВидОперации,
			Истина);
	КонецЦикла;
	
КонецПроцедуры

// Списки счетов учета, на которых ведется учет объектов в разрезе элементов справочника "Нематериальные активы".
//
// Возвращаемое значение:
//  Структура:
//   * СчетаУчетаПоНМА - СписокЗначений из ПланСчетовСсылка.Хозрасчетный - все счета учета
//   * СчетаУчетаПоПодразделениям - СписокЗначений из ПланСчетовСсылка.Хозрасчетный - счета с признаком учета по подразделениям
//   * СчетаУчетаПоСтатьямЗатрат - СписокЗначений из ПланСчетовСсылка.Хозрасчетный - счета с субконто "Статьи затрат"
//
Функция СпискиСчетовУчетаНМА() Экспорт
	
	СпискиСчетовУчетаНМА = Новый Структура;
	СпискиСчетовУчетаНМА.Вставить("СчетаУчетаПоНМА",            Новый СписокЗначений);
	СпискиСчетовУчетаНМА.Вставить("СчетаУчетаПоПодразделениям", Новый СписокЗначений);
	СпискиСчетовУчетаНМА.Вставить("СчетаУчетаПоСтатьямЗатрат",  Новый СписокЗначений);
	
	Запрос = Новый Запрос;
	
	ВидыСубконтоТипНМА = Новый Массив;
	ВидыСубконтоТипНМА.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	ВидыСубконтоТипНМА.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР);
	
	Запрос.УстановитьПараметр("ВидыСубконтоТипНМА", ВидыСубконтоТипНМА);
	
	Запрос.УстановитьПараметр("ВидСубконтоСтатьяЗатрат", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК СчетУчета,
	|	ХозрасчетныйВидыСубконто.Ссылка.УчетПоПодразделениям КАК УчетПоПодразделениям
	|ПОМЕСТИТЬ СчетаУчетаПоНМА
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.ВидСубконто В(&ВидыСубконтоТипНМА)
	|	И НЕ ХозрасчетныйВидыСубконто.Ссылка.ЗапретитьИспользоватьВПроводках
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаУчетаПоНМА.СчетУчета КАК СчетУчета,
	|	СчетаУчетаПоНМА.УчетПоПодразделениям КАК УчетПоПодразделениям,
	|	НЕ ВидСубконтоСтатьяЗатрат.Ссылка ЕСТЬ NULL КАК УчетПоСтатьямЗатрат
	|ИЗ
	|	СчетаУчетаПоНМА КАК СчетаУчетаПоНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидСубконтоСтатьяЗатрат
	|		ПО СчетаУчетаПоНМА.СчетУчета = ВидСубконтоСтатьяЗатрат.Ссылка
	|			И (ВидСубконтоСтатьяЗатрат.ВидСубконто = &ВидСубконтоСтатьяЗатрат)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СпискиСчетовУчетаНМА.СчетаУчетаПоНМА.Добавить(Выборка.СчетУчета);
		Если Выборка.УчетПоПодразделениям Тогда
			СпискиСчетовУчетаНМА.СчетаУчетаПоПодразделениям.Добавить(Выборка.СчетУчета);
		КонецЕсли;
		Если Выборка.УчетПоСтатьямЗатрат Тогда
			СпискиСчетовУчетаНМА.СчетаУчетаПоСтатьямЗатрат.Добавить(Выборка.СчетУчета);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СпискиСчетовУчетаНМА;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура СформироватьДвиженияПриобретениеПрав(ТаблицаПрава, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Или Не ЗначениеЗаполнено(ТаблицаПрава) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];

	ЭтоПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ОтражатьВНалоговомУчетеУСН = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период);
	ЭтоПлательщикНДФЛ = УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период);
	
	СчетУчетаНМА = ПланыСчетов.Хозрасчетный.НематериальныеАктивыОрганизации;

	СтатьяЗатратАмортизация = Неопределено;
	
	Настройки = Справочники.НастройкиУчетаЗатрат.БазовыеНастройкиУчетаЗатратУчетнойПолитики(Реквизиты.Период, Реквизиты.Организация);
	
	Для Каждого СтрокаТаблицы Из ТаблицаПрава Цикл
		
		// В зависимости от того, является ли приобретаемый результат интеллектуальной деятельности объектом НМА в бухгалтерском или налоговом учете,
		// проводки состоят из одного или двух этапов.
		//
		// - Один этап
		// Дт 20 (или другой счет затрат) Кт 60.01 или Дт 76.18 Кт 60.01
		//
		// - Два этапа
		// 1-й этап: Дт 08 Кт 60.01
		// 2-й этап: Дт 04 Кт 08 - обычные объекты НМА или Дт 20 (или другой счет затрат) Кт 60.01 - малоценные НМА в бухгалтерском учете.
		
		// Первый этап или отражение в один этап
		ПроводкаНУ = Неопределено;
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;

		СчетДт = СтрокаТаблицы.СчетЗатрат;
		
		АналитикаПроводки = Новый Соответствие;

		Если СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс Тогда
			СчетДт = СчетУчетаАвансы();
			АналитикаПроводки = АналитикаАвансов(СтрокаТаблицы);
		ИначеЕсли СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив Тогда
			СчетДт = СчетУчетаКапВложенияНМА();
			АналитикаПроводки = АналитикаНМА(СтрокаТаблицы, Реквизиты);
		Иначе
			АналитикаПроводки = АналитикаРасходов(СтрокаТаблицы);
		КонецЕсли;

		Проводка.Содержание  = СтрокаТаблицы.Содержание;

		Проводка.СчетДт = СчетДт;
		
		Для Каждого ЗначениеАналитики Из АналитикаПроводки Цикл
			Если ЗначениеАналитики.Ключ = "Подразделение" Тогда
				Продолжить;
			КонецЕсли;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, ЗначениеАналитики.Ключ,
				ЗначениеАналитики.Значение);
		КонецЦикла;
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);

		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = АналитикаПроводки["Подразделение"];
		КонецЕсли;

		Проводка.СчетКт = СтрокаТаблицы.КорСчет;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, СтрокаТаблицы.ВидКорСубконто1,
			СтрокаТаблицы.КорСубконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, СтрокаТаблицы.ВидКорСубконто2,
			СтрокаТаблицы.КорСубконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, СтрокаТаблицы.ВидКорСубконто3,
			СтрокаТаблицы.КорСубконто3);

		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);

		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = Реквизиты.Подразделение;
		КонецЕсли;

		Если СвойстваСчетаКт.Валютный Тогда
			Проводка.ВалютаКт        = Реквизиты.ВалютаВзаиморасчетов;
			Проводка.ВалютнаяСуммаКт = СтрокаТаблицы.СуммаВзаиморасчетов;
		КонецЕсли;
		
		Проводка.Сумма = СтрокаТаблицы.СуммаБУ;
		
		Если ЭтоПлательщикНалогаНаПрибыль Тогда
			
			ТребуетсяОтдельнаяПроводкаНУ = ТребуетсяОтдельнаяПроводкаНУ(СтрокаТаблицы);
			
			Если ТребуетсяОтдельнаяПроводкаНУ Тогда
				
				Проводка.СуммаНУДт = 0;
				Проводка.СуммаПРДт = 0;
				Проводка.СуммаВРДт = СтрокаТаблицы.СуммаНУ;
				
				Проводка.СуммаНУКт = 0;
				Проводка.СуммаПРКт = 0;
				Проводка.СуммаВРКт = СтрокаТаблицы.СуммаНУ;
				
				ПроводкаНУ = ОтдельнаяПроводкаНалоговогоУчетаАвансыКапВложения(Движения, Реквизиты, СтрокаТаблицы, Проводка);
				
			Иначе
					
				Если СвойстваСчетаДт.НалоговыйУчет Тогда
					Проводка.СуммаНУДт = СтрокаТаблицы.СуммаНУ;
					Проводка.СуммаПРДт = 0;
					Проводка.СуммаВРДт = 0;
				КонецЕсли;

				Если СвойстваСчетаКт.НалоговыйУчет Тогда
					Проводка.СуммаНУКт = СтрокаТаблицы.СуммаНУ;
					Проводка.СуммаПРКт = 0;
					Проводка.СуммаВРКт = 0;
				КонецЕсли;
				ПроводкаНУ = Проводка;
			КонецЕсли;
			
			ПрименитьПовышающийКоэффициент(ПроводкаНУ, Реквизиты.Период, СтрокаТаблицы.ПрименяетсяПовышающийКоэффициент);
		КонецЕсли;
		
		// Второй этап
		КапитальноеВложениеБУ = (Проводка.СчетДт = СчетУчетаКапВложенияНМА());
		КапитальноеВложениеНУ = (ПроводкаНУ <> Неопределено) И (ПроводкаНУ.СчетДт = СчетУчетаКапВложенияНМА());
		
		ПроводкаНМА = Неопределено;
		ТребуетсяВторойЭтап = Ложь;
		Если КапитальноеВложениеБУ Тогда
			ПроводкаНМА = Проводка;
			ТребуетсяВторойЭтап = Истина;
		ИначеЕсли КапитальноеВложениеНУ Тогда
			ПроводкаНМА = ПроводкаНУ;
			ТребуетсяВторойЭтап = Истина;
		КонецЕсли;
		
		ТребуетсяОтдельнаяПроводкаНУ = ЭтоПлательщикНалогаНаПрибыль
			И КапитальноеВложениеБУ
			И КапитальноеВложениеНУ
			И СтрокаТаблицы.ЭтоМалоценныйНМА;
		
		Если ТребуетсяВторойЭтап Тогда

			Проводка = Движения.Хозрасчетный.Добавить();

			СкопироватьДанныеПроводки(Проводка, ПроводкаНМА, "Кт", "Дт");

			Проводка.Сумма = ПроводкаНМА.Сумма;
			
			Если КапитальноеВложениеБУ И СтрокаТаблицы.ЭтоМалоценныйНМА Тогда
				Проводка.Содержание = НСтр("ru = 'Затраты на приобретение НМА признаны расходами периода'", ОбщегоНазначения.КодОсновногоЯзыка());
				Проводка.СчетДт = СтрокаТаблицы.СчетЗатрат;

				АналитикаПроводки = АналитикаРасходов(СтрокаТаблицы);
			Иначе
				Проводка.Содержание = НСтр("ru = 'Принят к учету НМА'", ОбщегоНазначения.КодОсновногоЯзыка());
				Проводка.СчетДт = СчетУчетаНМА;
				Проводка.Период = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
				
				АналитикаПроводки = АналитикаНМА(СтрокаТаблицы, Реквизиты);
			КонецЕсли;
			
			Для Каждого ЗначениеАналитики Из АналитикаПроводки Цикл
				Если ЗначениеАналитики.Ключ = "Подразделение" Тогда
					Продолжить;
				КонецЕсли;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, ЗначениеАналитики.Ключ,
					ЗначениеАналитики.Значение);
			КонецЦикла;
			
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			
			Если ЭтоПлательщикНалогаНаПрибыль Тогда
			
				Если Не ТребуетсяОтдельнаяПроводкаНУ И КапитальноеВложениеНУ Тогда
					
					Если СвойстваСчетаДт.НалоговыйУчет Тогда
						Проводка.СуммаНУДт       = СтрокаТаблицы.СуммаНУ;
						Проводка.СуммаПРДт       = 0;
						Проводка.СуммаВРДт       = Проводка.Сумма - СтрокаТаблицы.СуммаНУ;
					КонецЕсли;
										
					Если СвойстваСчетаКт.НалоговыйУчет Тогда
						Проводка.СуммаНУКт       = ПроводкаНМА.СуммаНУДт;
						Проводка.СуммаПРКт       = ПроводкаНМА.СуммаПРДт;
						Проводка.СуммаВРКт       = ПроводкаНМА.СуммаВРДт;
					КонецЕсли;
					
				Иначе
					Если СвойстваСчетаДт.НалоговыйУчет Тогда
						Проводка.СуммаНУДт       = 0;
						Проводка.СуммаПРДт       = 0;
						Проводка.СуммаВРДт       = СтрокаТаблицы.СуммаНУ;
					КонецЕсли;
										
					Если СвойстваСчетаКт.НалоговыйУчет Тогда
						Проводка.СуммаНУКт       = 0;
						Проводка.СуммаПРКт       = 0;
						Проводка.СуммаВРКт       = СтрокаТаблицы.СуммаНУ;
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;
			
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = АналитикаПроводки["Подразделение"];
			КонецЕсли;

			Если ТребуетсяОтдельнаяПроводкаНУ Тогда
				
				ПроводкаНУ = ОтдельнаяПроводкаНалоговогоУчетаНМА(Движения, Реквизиты, СтрокаТаблицы, ПроводкаНМА);
				
				Если СвойстваСчетаКт.НалоговыйУчет Тогда
					ПроводкаНУ.СуммаНУКт       = СтрокаТаблицы.СуммаНУ;
					ПроводкаНУ.СуммаПРКт       = 0;
					ПроводкаНУ.СуммаВРКт       = - СтрокаТаблицы.СуммаНУ;
				КонецЕсли;
	
			КонецЕсли;
			
			Если КапитальноеВложениеБУ И СтрокаТаблицы.ЭтоМалоценныйНМА Тогда
				ДобавитьПроводкуМалоценныйНМАСчетФинансовогоРезультата(Движения, Проводка, СтрокаТаблицы, СтрокаТаблицы.ПодразделениеЗатрат, Настройки);
			КонецЕсли;
		КонецЕсли;
		
		// Движения регистров сведений по учету НМА.
		Если СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив
				И Не СтрокаТаблицы.ЭтоМалоценныйНМА
			Или СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив
				И (ЭтоПлательщикНалогаНаПрибыль Или ЭтоПлательщикНДФЛ)
			Или СтрокаТаблицы.ПорядокВключенияСтоимостиВСоставРасходовУСН = Перечисления.ПорядокВключенияСтоимостиОСиНМАВСоставРасходовУСН.ВключитьВСоставАмортизируемогоИмущества
				И ОтражатьВНалоговомУчетеУСН Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.НазначениеИспользованияАмортизация) Тогда
				
				НазначениеИспользованияАмортизация = Справочники.НазначенияИспользованияНМА.НовыйНазначениеИспользования();
				ЗаполнитьЗначенияСвойств(НазначениеИспользованияАмортизация, СтрокаТаблицы);
				СвойствоСчетаЗатрат = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
				НомерСубконтоСтатьяЗатрат = СвойствоСчетаЗатрат.ИдентификаторыСубконто[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат];
				
				Если НомерСубконтоСтатьяЗатрат <> Неопределено Тогда
					Если СтатьяЗатратАмортизация = Неопределено Тогда
						СтатьяЗатратАмортизация = Справочники.СтатьиЗатрат.ПредопределенныйЭлемент("Амортизация");
					КонецЕсли;
					НазначениеИспользованияАмортизация["Субконто" + НомерСубконтоСтатьяЗатрат] = СтатьяЗатратАмортизация;
				КонецЕсли;
				СтрокаТаблицы.НазначениеИспользованияАмортизация =
					Справочники.НазначенияИспользованияНМА.НазначениеИспользования(НазначениеИспользованияАмортизация);
			КонецЕсли;
			
			Движение = Движения.СостоянияНМАОрганизаций.Добавить();
			Движение.Организация         = Реквизиты.Организация;
			Движение.Период              = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
			Движение.НематериальныйАктив = СтрокаТаблицы.НематериальныйАктив;
			Движение.Состояние           = Перечисления.ВидыСостоянийНМА.ПринятКУчету;

			Движение = Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Добавить();
			Движение.Организация             = Реквизиты.Организация;
			Движение.Период                  = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
			Движение.НематериальныйАктив     = СтрокаТаблицы.НематериальныйАктив;
			Движение.СпособОтраженияРасходов = СтрокаТаблицы.НазначениеИспользованияАмортизация;
			
			Движение = Движения.СчетаБухгалтерскогоУчетаНМА.Добавить();
			Движение.Организация               = Реквизиты.Организация;
			Движение.Период                    = Реквизиты.Период;
			Движение.НематериальныйАктив       = СтрокаТаблицы.НематериальныйАктив;
			Движение.СчетУчета                 = СчетУчетаНМА;
			Движение.СчетНачисленияАмортизации = ПланыСчетов.Хозрасчетный.АмортизацияНематериальныхАктивов;
			
			Движения.СостоянияНМАОрганизаций.Записывать                                   = Истина;
			Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Записывать = Истина;
			Движения.СчетаБухгалтерскогоУчетаНМА.Записывать                               = Истина;
			
		КонецЕсли;

		Если СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив
			И Не СтрокаТаблицы.ЭтоМалоценныйНМА Тогда

			Движение = Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Добавить();
			Движение.Организация                 = Реквизиты.Организация;
			Движение.Период                      = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
			Движение.НачислятьАмортизацию        = Истина;
			Движение.НематериальныйАктив         = СтрокаТаблицы.НематериальныйАктив;
			Движение.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный;
			Движение.ПервоначальнаяСтоимость     = СтрокаТаблицы.СуммаБУ;
			Движение.СпособПоступления           = Перечисления.СпособыПоступленияАктивов.ПриобретениеЗаПлату;
			Движение.СрокПолезногоИспользования  = СтрокаТаблицы.СрокИспользования;

			Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Записывать = Истина;
		Иначе
			Движение = Движения.РезультатыИнтеллектуальнойДеятельности.Добавить();
			Движение.Организация                 = Реквизиты.Организация;
			Движение.Период                      = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
			Движение.ОбъектУчета                 = СтрокаТаблицы.НематериальныйАктив;
			Движение.Стоимость                   = СтрокаТаблицы.СуммаБУ;
			Движение.ДатаОкончанияИспользования  = СтрокаТаблицы.ДатаОкончанияСписания;
			Движения.РезультатыИнтеллектуальнойДеятельности.Записывать = Истина;
		КонецЕсли;

		Если (ЭтоПлательщикНДФЛ Или ЭтоПлательщикНалогаНаПрибыль)
			И СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив Тогда

			Движение = Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Добавить();
			Движение.Организация                              = Реквизиты.Организация;
			Движение.Период                                   = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
			Движение.НематериальныйАктив                      = СтрокаТаблицы.НематериальныйАктив;
			Движение.НачислятьАмортизацию                     = Истина;
			Движение.ПервоначальнаяСтоимостьНУ                = СтрокаТаблицы.СуммаНУ;
			Движение.СрокПолезногоИспользования               = СтрокаТаблицы.СрокИспользования;
			Движение.ПорядокВключенияСтоимостиВСоставРасходов = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации;
			Движение.ДатаПриобретения                         = Реквизиты.Период;
			
			Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Записывать = Истина;
			
		КонецЕсли;
		
		Если ОтражатьВНалоговомУчетеУСН
			И (СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив
				Или СтрокаТаблицы.ПорядокВключенияСтоимостиВСоставРасходовУСН = Перечисления.ПорядокВключенияСтоимостиОСиНМАВСоставРасходовУСН.ВключитьВСоставАмортизируемогоИмущества) Тогда

			Движение = Движения.ПервоначальныеСведенияНМАНалоговыйУчетУСН.Добавить();
			Движение.Организация                                 = Реквизиты.Организация;
			Движение.Период                                      = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
			Движение.НематериальныйАктив                         = СтрокаТаблицы.НематериальныйАктив;
			Движение.ДатаПриобретения                            = Реквизиты.Период;
			Движение.ПервоначальнаяСтоимостьУСН                  = СтрокаТаблицы.СуммаНУ;
			Движение.СрокПолезногоИспользования                  = СтрокаТаблицы.СрокИспользования;
			Движение.ПорядокВключенияСтоимостиВСоставРасходовУСН =
				СтрокаТаблицы.ПорядокВключенияСтоимостиВСоставРасходовУСН;
			
			Движения.ПервоначальныеСведенияНМАНалоговыйУчетУСН.Записывать = Истина;
			
		КонецЕсли;

	КонецЦикла;

	Движения.Хозрасчетный.Записывать = Истина;

КонецПроцедуры

Процедура ПрименитьПовышающийКоэффициент(ПроводкаНУ, Период, ПрименяетсяПовышающийКоэффициент) Экспорт
	
	Если Не ПрименяетсяПовышающийКоэффициент Тогда
		Возврат;
	КонецЕсли;

	СуммаНУ = ПроводкаНУ.СуммаНУДт;
	ПроводкаНУ.СуммаНУДт = СуммаНУ * ПовышающийКоэффициентНалоговогоУчета(Период);
	ПроводкаНУ.СуммаПРДт = СуммаНУ - ПроводкаНУ.СуммаНУДт;
		
КонецПроцедуры

Процедура ДобавитьПроводкуМалоценныйНМАСчетФинансовогоРезультата(Движения, ПроводкаСчетЗатрат, СтрокаТаблицы, ПодразделениеЗатрат, Настройки) Экспорт
	
	ЭтоКапитальноеВложениеБУ = (ПроводкаСчетЗатрат.СчетКт = СчетУчетаКапВложенияНМА());
	
	Если Не ЭтоКапитальноеВложениеБУ
		Или Не СтрокаТаблицы.ЭтоМалоценныйНМА
		Или БухгалтерскийУчетПовтИсп.ЭтоСчетФинансовогоРезультата(ПроводкаСчетЗатрат.СчетДт) Тогда
		Возврат;
	КонецЕсли;
	
	РольСчета = Перечисления.РолиСчетовЗатрат.ПустаяСсылка();
	Если Настройки.Закрытие[ПроводкаСчетЗатрат.СчетДт]<> Неопределено Тогда
		РольСчета = Настройки.Закрытие[ПроводкаСчетЗатрат.СчетДт].Роль;
	Иначе
		РольСчета = Перечисления.РолиСчетовЗатрат.ОсновнаяРольСчета(ПроводкаСчетЗатрат.СчетДт);
	КонецЕсли;
	
	Если РольСчета = Перечисления.РолиСчетовЗатрат.ПрочиеРасходы Тогда
		Возврат;
	КонецЕсли;
	
	СчетФинансовогоРезультата = Перечисления.РолиСчетовЗатрат.СчетФинансовогоРезультата(РольСчета);
	
	ПроводкаРасходыПериода = Движения.Хозрасчетный.Добавить();
	
	ПроводкаРасходыПериода.Сумма = ПроводкаСчетЗатрат.Сумма;
	ПроводкаРасходыПериода.СуммаНУКт = ПроводкаСчетЗатрат.СуммаНУДт;
	ПроводкаРасходыПериода.СуммаПРКт = ПроводкаСчетЗатрат.СуммаПРДт;
	ПроводкаРасходыПериода.СуммаВРКт = ПроводкаСчетЗатрат.СуммаВРДт;
	
	ПроводкаРасходыПериода.СчетДт = СчетФинансовогоРезультата;
	
	СкопироватьДанныеПроводки(ПроводкаРасходыПериода, ПроводкаСчетЗатрат, "Кт", "Дт");
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПроводкаРасходыПериода.СчетДт);
	
	Для НомерСубконто = 1 По БухгалтерскийУчет.МаксимальноеКоличествоСубконто() Цикл
		ВидСубконто = СвойстваСчета["ВидСубконто" + XMLСтрока(НомерСубконто)];
		ЗначениеСубконто = ПроводкаРасходыПериода["СубконтоКт"][ВидСубконто];
		БухгалтерскийУчет.УстановитьСубконто(
		ПроводкаРасходыПериода.СчетДт,
		ПроводкаРасходыПериода.СубконтоДт,
		ВидСубконто,
		ЗначениеСубконто);
	КонецЦикла;
	
	СвойстваСчетаФинансовогоРезультата = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетФинансовогоРезультата);
	
	Если СвойстваСчетаФинансовогоРезультата.УчетПоПодразделениям Тогда
		
		ПроводкаРасходыПериода.ПодразделениеДт = ПодразделениеЗатрат;
		
	КонецЕсли;
	
	Если СвойстваСчетаФинансовогоРезультата.НалоговыйУчет Тогда
		ПроводкаРасходыПериода.СуммаВРДт       = ПроводкаРасходыПериода.Сумма;
	КонецЕсли;

КонецПроцедуры

Процедура СкопироватьДанныеПроводки(ПроводкаПриемник, ПроводкаИсточник, НаправлениеПриемника, НаправлениеИсточника) Экспорт
	
	ЗаполнитьЗначенияСвойств(ПроводкаПриемник, ПроводкаИсточник, "Организация, Период, Содержание");
	
	ПроводкаПриемник["Подразделение" + НаправлениеПриемника] = ПроводкаИсточник["Подразделение" + НаправлениеИсточника];
	
	ПроводкаПриемник["Счет" + НаправлениеПриемника] = ПроводкаИсточник["Счет" + НаправлениеИсточника];
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПроводкаИсточник["Счет" + НаправлениеИсточника]);

	Для НомерСубконто = 1 По БухгалтерскийУчет.МаксимальноеКоличествоСубконто() Цикл
		ВидСубконто = СвойстваСчета["ВидСубконто" + XMLСтрока(НомерСубконто)];
		ЗначениеСубконто = ПроводкаИсточник["Субконто" + НаправлениеИсточника][ВидСубконто];
		БухгалтерскийУчет.УстановитьСубконто(
			ПроводкаПриемник["Счет" + НаправлениеПриемника],
			ПроводкаПриемник["Субконто" + НаправлениеПриемника],
			ВидСубконто,
			ЗначениеСубконто);
	КонецЦикла;
	
	ПроводкаПриемник["Валюта" + НаправлениеПриемника]        = ПроводкаИсточник["Валюта" + НаправлениеИсточника];
	
КонецПроцедуры

Процедура УстановитьСубконтоНазначениеИспользования(СтрокаПрава) Экспорт

	СвойстваСчетаБУ = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаПрава.СчетОтражениеБУ);
	НомерСубконтоБУ = СвойстваСчетаБУ.ИдентификаторыСубконто[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НазначенияИспользованияНМА];

	Если ЗначениеЗаполнено(НомерСубконтоБУ)
		И СтрокаПрава["ВидСубконтоОтражениеБУ" + XMLСтрока(НомерСубконтоБУ)] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НазначенияИспользованияНМА Тогда
		СтрокаПрава["СубконтоОтражениеБУ" + XMLСтрока(НомерСубконтоБУ)] = СтрокаПрава.НазначениеИспользования;
	КонецЕсли;
	
	СвойстваСчетаНУ = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаПрава.СчетОтражениеНУ);
	НомерСубконтоНУ = СвойстваСчетаНУ.ИдентификаторыСубконто[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НазначенияИспользованияНМА];

	Если ЗначениеЗаполнено(НомерСубконтоНУ)
		И СтрокаПрава["ВидСубконтоОтражениеНУ" + XMLСтрока(НомерСубконтоНУ)] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НазначенияИспользованияНМА Тогда
		СтрокаПрава["СубконтоОтражениеНУ" + XMLСтрока(НомерСубконтоНУ)] = СтрокаПрава.НазначениеИспользования;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияУчетПрав(ПараметрыПроведения, Движения) Экспорт

	Параметры = ПодготовитьПараметрыПроведенияУчетПрав(ПараметрыПроведения);
	
	Реквизиты = Параметры.Реквизиты[0];
	СрокиИспользованияСтоимость = Параметры.СрокиИспользованияСтоимость;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПериодГраница",
		Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СрокиИспользованияСтоимость", СрокиИспользованияСтоимость);
	Запрос.УстановитьПараметр("ПроверятьЭлементыАмортизации", Реквизиты.ПроверятьЭлементыАмортизации);
	Запрос.УстановитьПараметр("ДатаДокумента", Реквизиты.Период);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	СрокиИспользованияСтоимость.ОбъектУчета КАК ОбъектУчета,
	|	СрокиИспользованияСтоимость.СрокПолезногоИспользованияПоРезультатамИнвентаризации КАК СрокПолезногоИспользованияПоРезультатамИнвентаризации,
	|	СрокиИспользованияСтоимость.НачалоДействияПрава КАК НачалоДействияПрава,
	|	СрокиИспользованияСтоимость.СрокДействияПраваКоличествоМесяцев КАК СрокДействияПраваКоличествоМесяцев,
	|	СрокиИспользованияСтоимость.СрокДействияПраваДата КАК СрокДействияПраваДата,
	|	СрокиИспользованияСтоимость.Пересмотрено КАК Пересмотрено,
	|	СрокиИспользованияСтоимость.НаличиеФактическое КАК НаличиеФактическое,
	|	СрокиИспользованияСтоимость.Стоимость КАК Стоимость
	|ПОМЕСТИТЬ СрокиИспользованияСтоимость
	|ИЗ
	|	&СрокиИспользованияСтоимость КАК СрокиИспользованияСтоимость
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатыИнтеллектуальнойДеятельности.ОбъектУчета КАК ОбъектУчета,
	|	РезультатыИнтеллектуальнойДеятельности.ДатаОкончанияИспользования КАК ДатаОкончанияИспользования,
	|	РезультатыИнтеллектуальнойДеятельности.Стоимость КАК Стоимость
	|ПОМЕСТИТЬ АктуальныеСведения
	|ИЗ
	|	РегистрСведений.РезультатыИнтеллектуальнойДеятельности.СрезПоследних(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И ОбъектУчета В
	|					(ВЫБРАТЬ
	|						СрокиИспользованияСтоимость.ОбъектУчета
	|					ИЗ
	|						СрокиИспользованияСтоимость КАК СрокиИспользованияСтоимость)) КАК РезультатыИнтеллектуальнойДеятельности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрокиИспользованияСтоимость.ОбъектУчета КАК ОбъектУчета,
	|	СрокиИспользованияСтоимость.Стоимость КАК Стоимость,
	|	ВЫБОР
	|		КОГДА НЕ СрокиИспользованияСтоимость.НаличиеФактическое
	|			ТОГДА &ДатаДокумента
	|		КОГДА &ПроверятьЭлементыАмортизации
	|				И СрокиИспользованияСтоимость.Пересмотрено
	|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаДокумента, МЕСЯЦ, СрокиИспользованияСтоимость.СрокПолезногоИспользованияПоРезультатамИнвентаризации)
	|		КОГДА АктуальныеСведения.ДатаОкончанияИспользования ЕСТЬ НЕ NULL 
	|			ТОГДА АктуальныеСведения.ДатаОкончанияИспользования
	|		КОГДА СрокиИспользованияСтоимость.СрокДействияПраваКоличествоМесяцев = 0
	|			ТОГДА СрокиИспользованияСтоимость.СрокДействияПраваДата
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(СрокиИспользованияСтоимость.НачалоДействияПрава, МЕСЯЦ, СрокиИспользованияСтоимость.СрокДействияПраваКоличествоМесяцев)
	|	КОНЕЦ КАК ДатаОкончанияИспользования
	|ИЗ
	|	СрокиИспользованияСтоимость КАК СрокиИспользованияСтоимость
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеСведения КАК АктуальныеСведения
	|		ПО СрокиИспользованияСтоимость.ОбъектУчета = АктуальныеСведения.ОбъектУчета";
	
	ВыборкаСрокиСтоимость = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаСрокиСтоимость.Следующий() Цикл
		Запись = Движения.РезультатыИнтеллектуальнойДеятельности.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Реквизиты);
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаСрокиСтоимость);
	КонецЦикла;
	
	Движения.РезультатыИнтеллектуальнойДеятельности.Записывать = Истина;
КонецПроцедуры

Процедура СформироватьДвиженияИзменениеЭлементовАмортизации(ПараметрыПроведения, Движения) Экспорт
	
	Параметры = ПодготовитьПараметрыПроведенияИзменениеЭлементовАмортизации(ПараметрыПроведения);
	
	Реквизиты = Параметры.Реквизиты[0];
	// Уникальность нематериальных активов в строках таблицы гарантируется проверкой заполнения документа.
	ИзменениеЭлементовАмортизации = Параметры.ИзменениеЭлементовАмортизации;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НематериальныеАктивы.НематериальныйАктив КАК НематериальныйАктив,
		|	НематериальныеАктивы.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации
		|ПОМЕСТИТЬ СписокОбъектовУчета
		|ИЗ
		|	&СписокОбъектовУчета КАК НематериальныеАктивы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияНМАОрганизаций.НематериальныйАктив КАК НематериальныйАктив,
		|	МАКСИМУМ(НАЧАЛОПЕРИОДА(СостоянияНМАОрганизаций.Период, ДЕНЬ)) КАК НачалоОтсчетаРесурсМесяцев
		|ПОМЕСТИТЬ НачалаОтсчетаСрокаИспользования
		|ИЗ
		|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОбъектовУчета КАК СписокОбъектовУчета
		|		ПО СостоянияНМАОрганизаций.НематериальныйАктив = СписокОбъектовУчета.НематериальныйАктив
		|ГДЕ
		|	СостоянияНМАОрганизаций.Организация = &Организация
		|	И СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
		|	И СостоянияНМАОрганизаций.Период <= &ПериодСведенийДата
		|	И СостоянияНМАОрганизаций.Активность
		|	И СписокОбъектовУчета.СпособНачисленияАмортизации В (&СпособыАмортизацииПоСрокуИспользования)
		|
		|СГРУППИРОВАТЬ ПО
		|	СостоянияНМАОрганизаций.НематериальныйАктив
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыработкаНМАОбороты.НематериальныйАктив КАК НематериальныйАктив,
		|	ВыработкаНМАОбороты.КоличествоОборот КАК ИспользованоВсего
		|ПОМЕСТИТЬ ИспользованиеРесурса
		|ИЗ
		|	РегистрНакопления.ВыработкаНМА.Обороты(
		|			,
		|			&ПериодСведений,
		|			,
		|			Организация = &Организация
		|				И НематериальныйАктив В
		|					(ВЫБРАТЬ
		|						СписокОбъектовУчета.НематериальныйАктив
		|					ИЗ
		|						СписокОбъектовУчета КАК СписокОбъектовУчета
		|					ГДЕ
		|						СписокОбъектовУчета.СпособНачисленияАмортизации В (&СпособыАмортизацииПоОбъемуПродукции))) КАК ВыработкаНМАОбороты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокОбъектовУчета.НематериальныйАктив КАК НематериальныйАктив,
		|	ЕСТЬNULL(НачалаОтсчетаСрокаИспользования.НачалоОтсчетаРесурсМесяцев, &ПериодСведенийДата) КАК НачалоОтсчетаРесурсМесяцев,
		|	ЕСТЬNULL(ИспользованиеРесурса.ИспользованоВсего, 0) КАК ИспользованоВсего
		|ИЗ
		|	СписокОбъектовУчета КАК СписокОбъектовУчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ НачалаОтсчетаСрокаИспользования КАК НачалаОтсчетаСрокаИспользования
		|		ПО СписокОбъектовУчета.НематериальныйАктив = НачалаОтсчетаСрокаИспользования.НематериальныйАктив
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИспользованиеРесурса КАК ИспользованиеРесурса
		|		ПО СписокОбъектовУчета.НематериальныйАктив = ИспользованиеРесурса.НематериальныйАктив";
	
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокОбъектовУчета", ИзменениеЭлементовАмортизации);
	Запрос.УстановитьПараметр("ПериодСведенийДата", Реквизиты.Период);
	
	МоментДокумента = Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("ПериодСведений", Новый Граница(МоментДокумента, ВидГраницы.Исключая));
	
	Запрос.УстановитьПараметр("СпособыАмортизацииПоСрокуИспользования", СпособыАмортизацииПоСрокуИспользования());
	Запрос.УстановитьПараметр("СпособыАмортизацииПоОбъемуПродукции", СпособыАмортизацииПоОбъемуПродукции());
	
	ИспользованиеНематериальныхАктивов = Запрос.Выполнить().Выгрузить();
	ИспользованиеНематериальныхАктивов.Индексы.Добавить("НематериальныйАктив");
	
	ЭлементыАмортизации = Параметры.ИзменениеЭлементовАмортизации.СкопироватьКолонки();
	МетаданныеЭлементовАмортизации = Метаданные.РегистрыСведений.ЭлементыАмортизацииНМАБухгалтерскийУчет.Ресурсы;
	ЭлементыАмортизации.Колонки.Добавить("СрокПолезногоИспользования", МетаданныеЭлементовАмортизации.СрокПолезногоИспользования.Тип);
	ЭлементыАмортизации.Колонки.Добавить("ОбъемПродукцииРабот", МетаданныеЭлементовАмортизации.ОбъемПродукцииРабот.Тип);
	
	АлгоритмПериодаАмортизации = Перечисления.АлгоритмыПериодаАмортизации.АлгоритмРасчетаАмортизации(Реквизиты.Период, Реквизиты.Организация);
	
	Для Каждого СтрокаЭлементовАмортизации Из ИзменениеЭлементовАмортизации Цикл
		
		НоваяСтрока = ЭлементыАмортизации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЭлементовАмортизации);
		
		Если Не СтрокаЭлементовАмортизации.НачислятьАмортизацию Тогда
			Продолжить;
		КонецЕсли;
		
		ОтборСтрок = Новый Структура("НематериальныйАктив", СтрокаЭлементовАмортизации.НематериальныйАктив);
		СтрокиИспользования = ИспользованиеНематериальныхАктивов.НайтиСтроки(ОтборСтрок);
		
		ИнформацияОбИспользовании = Новый Структура("НачалоОтсчетаРесурсМесяцев, ИспользованоВсего", НачалоДня(Реквизиты.Период), 0);
		Если СтрокиИспользования.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(ИнформацияОбИспользовании, СтрокиИспользования[0]);
		КонецЕсли;
		
		Если СтрокаЭлементовАмортизации.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда
			НоваяСтрока.ОбъемПродукцииРабот =
				ИнформацияОбИспользовании.ИспользованоВсего + СтрокаЭлементовАмортизации.ОстатокОбъемаПродукцииРабот;
		Иначе
			НачалоОтсчетаРесурсов = ИнформацияОбИспользовании.НачалоОтсчетаРесурсМесяцев;
			Если АлгоритмПериодаАмортизации = Перечисления.АлгоритмыПериодаАмортизации.СДатыПризнания Тогда
				// Амортизация активов, принятых к учету 1 числа, заканчивается последним днем месяца.
				НачалоОтсчетаРесурсов = НачалоДня(НачалоОтсчетаРесурсов) - 1;
			КонецЕсли;
			НоваяСтрока.СрокПолезногоИспользования = АмортизацияАктивов.РазностьМесяцев(
				ДобавитьМесяц(КонецМесяца(Реквизиты.Период), СтрокаЭлементовАмортизации.ОстатокСрокаПолезногоИспользования),
				НачалоОтсчетаРесурсов);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементАмортизации Из ЭлементыАмортизации Цикл
		Запись = Движения.ЭлементыАмортизацииНМАБухгалтерскийУчет.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Реквизиты);
		ЗаполнитьЗначенияСвойств(Запись, ЭлементАмортизации);
	КонецЦикла;
	
	Движения.ЭлементыАмортизацииНМАБухгалтерскийУчет.Записывать = Истина;
КонецПроцедуры

// Для списка НМА возвращает текст запроса получения актуальных учетных данных.
// Например, остаток стоимости, инвентарный номер, подразделение, счета учета.
// 
// Возвращаемое значение:
//  Строка
//
Функция ТекстЗапросаУчетныеДанные() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НематериальныеАктивы.Ссылка КАК Объект,
	|	НематериальныеАктивы.Код КАК ИнвентарныйНомер,
	|	НематериальныеАктивы.АмортизационнаяГруппа КАК АмортизационнаяГруппа
	|ПОМЕСТИТЬ Объекты
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|ГДЕ
	|	НематериальныеАктивы.Ссылка В(&Объекты)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчета.НематериальныйАктив КАК Объект,
	|	СчетаУчета.СчетУчета КАК СчетУчета,
	|	СчетаУчета.СчетНачисленияАмортизации КАК СчетАмортизации
	|ПОМЕСТИТЬ СчетаУчета
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						Объекты.Объект
	|					ИЗ
	|						Объекты КАК Объекты)) КАК СчетаУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	СчетАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Стоимость.Субконто1 КАК Объект,
	|	Стоимость.Счет КАК Счет,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(Стоимость.Подразделение КАК Справочник.ПодразделенияОрганизаций), ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	Стоимость.СуммаОстаток КАК ОстатокБУ,
	|	ЕСТЬNULL(Стоимость.СуммаНУОстаток, 0) КАК ОстатокНУ,
	|	ЕСТЬNULL(Стоимость.СуммаПРОстаток, 0) КАК ОстатокПР,
	|	ЕСТЬNULL(Стоимость.СуммаВРОстаток, 0) КАК ОстатокВР
	|ПОМЕСТИТЬ ОстаткиСтоимости
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет В (&СчетаУчета), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы), Организация = &Организация) КАК Стоимость
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодразделенияСтоимости.Объект КАК Объект,
	|	МАКСИМУМ(ПодразделенияСтоимости.Подразделение) КАК Подразделение
	|ПОМЕСТИТЬ ПодразделенияОбъектов
	|ИЗ
	|	СчетаУчета КАК СчетаУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиСтоимости КАК ПодразделенияСтоимости
	|		ПО СчетаУчета.Объект = ПодразделенияСтоимости.Объект
	|			И СчетаУчета.СчетУчета = ПодразделенияСтоимости.Счет
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодразделенияСтоимости.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Объект КАК Объект,
	|	СУММА(Остатки.ОстатокБУ) КАК ОстатокБУ,
	|	СУММА(Остатки.ОстатокНУ) КАК ОстатокНУ,
	|	СУММА(Остатки.ОстатокПР) КАК ОстатокПР,
	|	СУММА(Остатки.ОстатокВР) КАК ОстатокВР
	|ПОМЕСТИТЬ ОстаткиРазниц
	|ИЗ
	|	ОстаткиСтоимости КАК Остатки
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаправленияАмортизации.НематериальныйАктив КАК Объект,
	|	НаправленияАмортизации.СпособОтраженияРасходов КАК Направление
	|ПОМЕСТИТЬ НаправленияАмортизации
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						Объекты.Объект
	|					ИЗ
	|						Объекты КАК Объекты)) КАК НаправленияАмортизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Объекты.Объект КАК Объект,
	|	Объекты.Объект КАК ОбъектУчета,
	|	Объекты.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	Объекты.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ЕСТЬNULL(СчетаУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчета,
	|	ЕСТЬNULL(СчетаУчета.СчетАмортизации, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетАмортизации,
	|	ЕСТЬNULL(НаправленияАмортизации.Направление, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияРасходовПоАмортизации.ПустаяСсылка)) КАК НаправлениеАмортизации,
	|	ЕСТЬNULL(ПодразделенияОбъектов.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ОстаткиРазниц.ОстатокБУ, 0) КАК ОстатокБУ,
	|	ЕСТЬNULL(ОстаткиРазниц.ОстатокНУ, 0) КАК ОстатокНУ,
	|	ЕСТЬNULL(ОстаткиРазниц.ОстатокПР, 0) КАК ОстатокПР,
	|	ЕСТЬNULL(ОстаткиРазниц.ОстатокВР, 0) КАК ОстатокВР
	|ИЗ
	|	Объекты КАК Объекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаУчета КАК СчетаУчета
	|		ПО Объекты.Объект = СчетаУчета.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаправленияАмортизации КАК НаправленияАмортизации
	|		ПО Объекты.Объект = НаправленияАмортизации.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодразделенияОбъектов КАК ПодразделенияОбъектов
	|		ПО Объекты.Объект = ПодразделенияОбъектов.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиРазниц КАК ОстаткиРазниц
	|		ПО Объекты.Объект = ОстаткиРазниц.Объект
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИнвентарныйНомер,
	|	Объект";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СчетаУчетаНМА() Экспорт
	
	ТипОбъектов = ТипОбъектов();
	
	СчетаУчета = Новый Структура;
	СчетаУчета.Вставить("Активы",      АмортизацияАктивов.СчетаАктивов(ТипОбъектов));
	СчетаУчета.Вставить("Амортизация", АмортизацияАктивов.СчетаАмортизации(ТипОбъектов));
	СчетаУчета.Вставить("Баланс",      Новый Массив);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчета.Баланс, СчетаУчета.Активы);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчета.Баланс, СчетаУчета.Амортизация);
	
	Возврат СчетаУчета;
	
КонецФункции

Функция СпособыАмортизацииПоСрокуИспользования() Экспорт
	СпособыАмортизации = Новый Массив;
	СпособыАмортизации.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.Линейный);
	СпособыАмортизации.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка);
	Возврат СпособыАмортизации;
КонецФункции

Процедура СформироватьДвиженияНачатыеРазработки(ТаблицаРеквизиты, ТаблицаОбъектыНИОКР, Движения) Экспорт
	
	Параметры = ПодготовитьПараметрыНачатыеРазработки(ТаблицаРеквизиты, ТаблицаОбъектыНИОКР);
	
	Реквизиты = Параметры.Реквизиты[0];
	
	Для Каждого ОбъектНИОКР Из Параметры.ОбъектыНИОКР Цикл
		Движение = Движения.ДатыНачалаСтадииРазработокНИОКР.Добавить();
		Движение.НИОКР        = ОбъектНИОКР.НематериальныйАктив;
		Движение.Организация  = Реквизиты.Организация;
		Движение.ДатаНачала   = НачалоМесяца(Реквизиты.Период);
	КонецЦикла;
	
	Движения.ДатыНачалаСтадииРазработокНИОКР.Записывать = Истина;
	
КонецПроцедуры

Процедура ПроверитьНачатыеРазработки(ТаблицаРеквизиты, ТаблицаОбъектыНИОКР, Отказ) Экспорт
	
	Параметры = ПодготовитьПараметрыПроверкаНачатыеРазработки(ТаблицаРеквизиты, ТаблицаОбъектыНИОКР);
	
	Реквизиты = Параметры.Реквизиты[0];
	
	Если ТранзакцияАктивна() Тогда
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДатыНачалаСтадииРазработокНИОКР");
		ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
		ЭлементБлокировки.ИсточникДанных = Параметры.ОбъектыНИОКР;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НИОКР", "НематериальныйАктив");
		Блокировка.Заблокировать();
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектыНИОКР", Параметры.ОбъектыНИОКР);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Регистратор", Реквизиты.Регистратор);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбъектыНИОКР.НомерСтроки КАК НомерСтроки,
	|	ОбъектыНИОКР.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ ОбъектыНИОКР
	|ИЗ
	|	&ОбъектыНИОКР КАК ОбъектыНИОКР
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ДатыНачалаСтадииРазработокНИОКР.НИОКР) КАК НИОКР,
	|	ПРЕДСТАВЛЕНИЕ(ДатыНачалаСтадииРазработокНИОКР.Регистратор) КАК Регистратор,
	|	ПРЕДСТАВЛЕНИЕ(ДатыНачалаСтадииРазработокНИОКР.Организация) КАК Организация,
	|	ДатыНачалаСтадииРазработокНИОКР.ДатаНачала КАК ДатаНачала,
	|	ОбъектыНИОКР.НомерСтроки КАК НомерСтроки,
	|	НематериальныеАктивы.Код КАК Код
	|ИЗ
	|	РегистрСведений.ДатыНачалаСтадииРазработокНИОКР КАК ДатыНачалаСтадииРазработокНИОКР
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыНИОКР КАК ОбъектыНИОКР
	|		ПО ДатыНачалаСтадииРазработокНИОКР.Организация = &Организация
	|		И ДатыНачалаСтадииРазработокНИОКР.НИОКР = ОбъектыНИОКР.НематериальныйАктив
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО ДатыНачалаСтадииРазработокНИОКР.НИОКР = НематериальныеАктивы.Ссылка
	|ГДЕ
	|	ДатыНачалаСтадииРазработокНИОКР.Активность
	|	И ДатыНачалаСтадииРазработокНИОКР.Регистратор <> &Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Результат = Запрос.Выполнить();
	
	ВыборкаОбъектовНИОКР = Результат.Выбрать();
	
	ШаблонТекстаОшибки = НСтр("ru = 'По объекту %1 (%2) в организации %3 уже начата стадия разработки %4
									|Информация отражена документом %5.'");
	Пока ВыборкаОбъектовНИОКР.Следующий() Цикл
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонТекстаОшибки,
			ВыборкаОбъектовНИОКР.НИОКР,
			ВыборкаОбъектовНИОКР.Код,
			ВыборкаОбъектовНИОКР.Организация,
			Формат(ВыборкаОбъектовНИОКР.ДатаНачала, "ДЛФ=DD;"),
			ВыборкаОбъектовНИОКР.Регистратор);
		Поле = "СоздаваемыеОбъекты[" + XMLСтрока(ВыборкаОбъектовНИОКР.НомерСтроки - 1) + "].НематериальныйАктив";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор, Поле, "Объект", Отказ);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает значение повышающего коэффициента в налоговом учете, в соответствии с п.п. 1, 3 ст. 257 НК РФ,
// п. 7 ст. 262 НК РФ, подп. 26 п. 1 ст. 264 НК РФ.
// Для расходов на НИОКР применение коэффициента автоматизировано до 2025 года, когда действовал коэффициент 1.5,
// поэтому для этого вида расходов коэффициент может возвращать значение 1.5 или 2 в зависимости от периода.
// В остальных случаях возвращается значение коэффициента начиная с 2025 года.
// 
// Параметры:
//  Период - Дата
//  ЭтоРасходыНаНИОКР - Булево
// 
// Возвращаемое значение:
//  Число - Значение коэффициента.
//
Функция ПовышающийКоэффициентНалоговогоУчета(Период, ЭтоРасходыНаНИОКР = Ложь) Экспорт
	
	НачалоПримененияПовышающегоКоэффициентаДляЛицензийПО = Дата(2025, 1, 1);
	
	Если Период >= НачалоПримененияПовышающегоКоэффициентаДляЛицензийПО Тогда
		Возврат 2;
	КонецЕсли;
	
	Если ЭтоРасходыНаНИОКР Тогда
		Возврат 1.5;
	КонецЕсли;
	
	// Коэффицент 1.5 до начала 2025 года поддерживаем только для НИОКР (п.п. 7, 8 ст. 262 НК РФ), так как
	// после 2025 года Принятием к учету НМА могут приниматься к учету программы, купленные в 2024 году,
	// к которым должен быть применен коэффициент не 2, а 1.5, но определять дату приобретения мы пока не умеем.
	Возврат 1;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СпособыАмортизацииПоОбъемуПродукции()
	СпособыАмортизации = Новый Массив;
	СпособыАмортизации.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
	Возврат СпособыАмортизации;
КонецФункции

Функция ПодготовитьПараметрыПроведенияИзменениеЭлементовАмортизации(ПараметрыПроведения)

	Реквизиты = ПараметрыПроведения.Реквизиты;
	
	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты",
		Реквизиты);
	Описание.Колонки.Добавить("Период");       // <Дата> - период движений - дата документа
	Описание.Колонки.Добавить("Организация");  // <СправочникСсылка.Организации>
	Описание.Колонки.Добавить("Регистратор");  // <ДокументСсылка.ИнвентаризацияНМА>
	
	ОписаниеТаблиц.Добавить(Описание);
	
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"ИзменениеЭлементовАмортизации",
		ПараметрыПроведения.ИзменениеЭлементовАмортизации);
		
	Описание.Колонки.Добавить("НематериальныйАктив");           // <СправочникСсылка.НематериальныеАктивы>
	Описание.Колонки.Добавить("НачислятьАмортизацию");          // <Булево>
	Описание.Колонки.Добавить("СпособНачисленияАмортизации");   // <ПеречислениеСсылка.СпособыНачисленияАмортизацииНМА>
	Описание.Колонки.Добавить("ОстатокСрокаПолезногоИспользования"); // <Число>
	Описание.Колонки.Добавить("Коэффициент");                        // <Число>
	Описание.Колонки.Добавить("ЛиквидационнаяСтоимость");            // <Число>
	Описание.Колонки.Добавить("ОстатокОбъемаПродукцииРабот");        // <Число>
		
	ОписаниеТаблиц.Добавить(Описание);
	
	Возврат ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	
КонецФункции

Функция ПодготовитьПараметрыПроведенияУчетПрав(ПараметрыПроведения)

	Реквизиты = ПараметрыПроведения.Реквизиты;
	
	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты",
		Реквизиты);
	Описание.Колонки.Добавить("Период");       // <Дата> - период движений - дата документа
	Описание.Колонки.Добавить("Организация");  // <СправочникСсылка.Организации>
	Описание.Колонки.Добавить("Регистратор");                   // <ДокументСсылка.ИнвентаризацияНМА>
	Описание.Колонки.Добавить("ПроверятьЭлементыАмортизации");  // <Булево>
	
	
	ОписаниеТаблиц.Добавить(Описание);
	
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"СрокиИспользованияСтоимость",
		ПараметрыПроведения.СрокиИспользованияСтоимость);
		
	Описание.Колонки.Добавить("ОбъектУчета");                                           // <СправочникСсылка.НематериальныеАктивы>
	Описание.Колонки.Добавить("СрокПолезногоИспользованияПоРезультатамИнвентаризации"); // <Число>
	Описание.Колонки.Добавить("НачалоДействияПрава");                                   // <Дата>
	Описание.Колонки.Добавить("СрокДействияПраваКоличествоМесяцев");                    // <Число>
	Описание.Колонки.Добавить("СрокДействияПраваДата");                                 // <Дата>
	Описание.Колонки.Добавить("Пересмотрено");                                          // <Булево>
	Описание.Колонки.Добавить("НаличиеФактическое");                                    // <Булево>
	Описание.Колонки.Добавить("Стоимость");                                             // <Число>
		
	ОписаниеТаблиц.Добавить(Описание);
	
	Возврат ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
КонецФункции

Функция СчетУчетаАвансы()
	Возврат ПланыСчетов.Хозрасчетный.ПриобретениеПравНаИспользованиеРезультатовИнтеллектуальнойДеятельности;
КонецФункции

Функция СчетУчетаКапВложенияНМА()
	Возврат ПланыСчетов.Хозрасчетный.ПриобретениеНематериальныхАктивов;
КонецФункции

Функция ОтдельнаяПроводкаНалоговогоУчетаАвансыКапВложения(Движения, Реквизиты, СтрокаТаблицы, ПроводкаДляКопирования)
	
	ПроводкаНУ = Движения.Хозрасчетный.Добавить();
			
	СкопироватьДанныеПроводки(ПроводкаНУ, ПроводкаДляКопирования, "Кт", "Кт");
	
	Если СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования Тогда
		ПроводкаНУ.СчетДт = СчетУчетаАвансы();
		АналитикаПроводки = АналитикаАвансов(СтрокаТаблицы);
	ИначеЕсли СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу Тогда
		ПроводкаНУ.СчетДт = СтрокаТаблицы.СчетЗатрат;
		АналитикаПроводки = АналитикаРасходов(СтрокаТаблицы);
	Иначе
		ПроводкаНУ.СчетДт = СчетУчетаКапВложенияНМА();
		АналитикаПроводки = АналитикаНМА(СтрокаТаблицы, Реквизиты);
	КонецЕсли;

	Для Каждого ЗначениеАналитики Из АналитикаПроводки Цикл
		Если ЗначениеАналитики.Ключ = "Подразделение" Тогда
			Продолжить;
		КонецЕсли;
		БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт, ПроводкаНУ.СубконтоДт, ЗначениеАналитики.Ключ,
			ЗначениеАналитики.Значение);
	КонецЦикла;

	СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПроводкаНУ.СчетДт);

	Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
		ПроводкаНУ.ПодразделениеДт = АналитикаПроводки["Подразделение"];
	КонецЕсли;
	
	ПроводкаНУ.СуммаНУДт = СтрокаТаблицы.СуммаНУ;
	ПроводкаНУ.СуммаПРДт = 0;
	ПроводкаНУ.СуммаВРДт = - СтрокаТаблицы.СуммаНУ;
	
	ПроводкаНУ.СуммаНУКт = СтрокаТаблицы.СуммаНУ;
	ПроводкаНУ.СуммаПРКт = 0;
	ПроводкаНУ.СуммаВРКт = - СтрокаТаблицы.СуммаНУ;
	
	Возврат ПроводкаНУ;
	
КонецФункции

Функция ОтдельнаяПроводкаНалоговогоУчетаНМА(Движения, Реквизиты, СтрокаТаблицы, ПроводкаДляКопирования)
	
	ПроводкаНУ = Движения.Хозрасчетный.Добавить();

	ПроводкаНУ.Период      = Макс(Реквизиты.Период, СтрокаТаблицы.ДатаНачалаСписания);
	ПроводкаНУ.Организация = Реквизиты.Организация;
	ПроводкаНУ.Содержание = НСтр("ru = 'Принят к учету НМА'", ОбщегоНазначения.КодОсновногоЯзыка());
	ПроводкаНУ.Сумма = 0;

	ПроводкаНУ.СчетКт          = ПроводкаДляКопирования.СчетДт;
	ПроводкаНУ.ПодразделениеКт = ПроводкаДляКопирования.ПодразделениеДт;

	АналитикаПроводки = АналитикаНМА(СтрокаТаблицы, Реквизиты);
	
	Для Каждого ЗначениеАналитики Из АналитикаПроводки Цикл
		Если ЗначениеАналитики.Ключ = "Подразделение" Тогда
			Продолжить;
		КонецЕсли;
		БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетКт, ПроводкаНУ.СубконтоКт, ЗначениеАналитики.Ключ,
			ЗначениеАналитики.Значение);
	КонецЦикла;

	ПроводкаНУ.СчетДт = ПланыСчетов.Хозрасчетный.НематериальныеАктивыОрганизации;
	
	Для Каждого ЗначениеАналитики Из АналитикаПроводки Цикл
		Если ЗначениеАналитики.Ключ = "Подразделение" Тогда
			Продолжить;
		КонецЕсли;
		БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт, ПроводкаНУ.СубконтоДт, ЗначениеАналитики.Ключ,
			ЗначениеАналитики.Значение);
	КонецЦикла;

	СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПроводкаНУ.СчетДт);

	Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
		ПроводкаНУ.ПодразделениеДт = АналитикаПроводки["Подразделение"];
	КонецЕсли;

	Если СвойстваСчетаДт.НалоговыйУчет Тогда
		ПроводкаНУ.СуммаНУДт       = СтрокаТаблицы.СуммаНУ;
		ПроводкаНУ.СуммаПРДт       = 0;
		ПроводкаНУ.СуммаВРДт       = - СтрокаТаблицы.СуммаНУ;
	КонецЕсли;
	
	Возврат ПроводкаНУ;
	
КонецФункции

Функция ТребуетсяОтдельнаяПроводкаНУ(СтрокаТаблицы)

	// Отдельная проводка в НУ не нужна, если отражение в БУ и НУ одинаковое.
	// Это может быть в трех случаях:
	// 1. Объект прав - нематериальный актив и в БУ, и в НУ.
	// 2. Объект прав - авансы в БУ и расходы в НУ, признаваемые равномерно.
	// 3. Объект прав - расходы в БУ и расходы в НУ, признаваемые сразу.
	
	ОдинаковоеОтражениеБУиНУ =
		СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив
			И СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив
			Или СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс
			И (СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования
			Или СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.РасходыНаНИОКР) // такой вариант заведомо неправильный, исправляем ошибку пользователя
			Или СтрокаТаблицы.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Расходы
			И СтрокаТаблицы.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу;
	Возврат Не ОдинаковоеОтражениеБУиНУ;
	
КонецФункции

// Тип объектов, работа с которыми реализована в этом модуле - для АмортизацияАктивов
Функция ТипОбъектов()
	Возврат Тип("СправочникСсылка.НематериальныеАктивы");
КонецФункции

// Заполняет соответствие значений аналитики расходов.
// 
// Параметры:
//  СтрокаТаблицы - СтрокаТаблицыЗначений 
// 
// Возвращаемое значение:
//  Соответствие - Аналитика расходов

Функция АналитикаРасходов(СтрокаТаблицы)
	АналитикаРасходов = Новый Соответствие();
	АналитикаРасходов.Вставить("Подразделение", СтрокаТаблицы.ПодразделениеЗатрат);
	АналитикаРасходов.Вставить(1, СтрокаТаблицы.Субконто1);
	АналитикаРасходов.Вставить(2, СтрокаТаблицы.Субконто2);
	АналитикаРасходов.Вставить(3, СтрокаТаблицы.Субконто3);
	Возврат АналитикаРасходов;
КонецФункции

Функция АналитикаНМА(СтрокаТаблицы, Реквизиты)
	АналитикаНМА = Новый Соответствие();
	АналитикаНМА.Вставить("Подразделение", Реквизиты.Подразделение);
	АналитикаНМА.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы, СтрокаТаблицы.НематериальныйАктив);
	Возврат АналитикаНМА;
КонецФункции

Функция АналитикаАвансов(СтрокаТаблицы)
	
	АналитикаАвансов = Новый Соответствие();
	// Контрагенты
	АналитикаАвансов.Вставить(СтрокаТаблицы.ВидКорСубконто1, СтрокаТаблицы.КорСубконто1);
	// Договоры
	АналитикаАвансов.Вставить(СтрокаТаблицы.ВидКорСубконто2, СтрокаТаблицы.КорСубконто2);
	// Назначения использования
	АналитикаАвансов.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НазначенияИспользованияНМА, СтрокаТаблицы.НазначениеИспользования);
	
	Возврат АналитикаАвансов;
	
КонецФункции

#Область ПереходНаФСБУ14

Процедура ПодготовитьДанныеПереходаНаФСБУ14(ПараметрыПроведения)
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	СчетаНМА = Обработки.ПомощникПереходаНаФСБУ14.СчетаУчетаНМА();
	
	Запрос.УстановитьПараметр("СчетаКапитальныхВложений", СчетаНМА.СчетаКапитальныхВложений);
	Запрос.УстановитьПараметр("СтандартныйСчетАмортизации", СтандартныйСчетАмортизации(СчетаНМА.СчетаАмортизации));
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("СтоимостьОпределяетсяПоДаннымУчета", Реквизиты.СтоимостьОпределяетсяПоДаннымУчета);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДанныеПерехода(НомераТаблиц)
		+ ТекстЗапросаПервоначальныеСведенияБУ(НомераТаблиц)
		+ ТекстЗапросаЭлементыАмортизации(НомераТаблиц)
		+ ТекстЗапросаСостоянияНМА(НомераТаблиц)
		+ ТекстЗапросаСпособыОтраженияРасходов(НомераТаблиц)
		+ ТекстЗапросаСчетаУчетаНМА(НомераТаблиц)
		+ ТекстЗапросаИзменениеСтоимости(НомераТаблиц)
		+ ТекстЗапросаПереклассификация(НомераТаблиц)
		;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

КонецПроцедуры

Функция ТекстЗапросаВременныеТаблицыДанныеПерехода(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаДанныеПерехода", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НематериальныеАктивыСгруппированные", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.ПозицияСписка КАК ПозицияСписка,
	|	СписокНМА.Амортизация КАК Амортизация,
	|	СписокНМА.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
	|	СписокНМА.ДействиеПриПереходе КАК ДействиеПриПереходе,
	|	СписокНМА.Коэффициент КАК Коэффициент,
	|	СписокНМА.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.НеопределенныйСрокПолезногоИспользования КАК НеопределенныйСрокПолезногоИспользования,
	|	СписокНМА.Обесценение КАК Обесценение,
	|	СписокНМА.ОбесценениеДоступноеДляВосстановления КАК ОбесценениеДоступноеДляВосстановления,
	|	СписокНМА.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	СписокНМА.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	СписокНМА.ПодразделениеСписания КАК ПодразделениеСписания,
	|	СписокНМА.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	СписокНМА.СубконтоСписания1 КАК СубконтоСписания1,
	|	СписокНМА.СубконтоСписания2 КАК СубконтоСписания2,
	|	СписокНМА.СубконтоСписания3 КАК СубконтоСписания3,
	|	ВЫБОР
	|		КОГДА СписокНМА.СчетАмортизации = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА &СтандартныйСчетАмортизации
	|		ИНАЧЕ СписокНМА.СчетАмортизации
	|	КОНЕЦ КАК СчетАмортизации,
	|	СписокНМА.СчетСписания КАК СчетСписания,
	|	СписокНМА.СчетУчета КАК СчетУчета,
	|	СписокНМА.СчетУчета В (&СчетаКапитальныхВложений) КАК ЭтоСчетКапитальныхВложений
	|ПОМЕСТИТЬ ВременнаяТаблицаДанныеПерехода
	|ИЗ
	|	РегистрСведений.НМАПриПереходеНаФСБУ14 КАК СписокНМА
	|ГДЕ
	|	СписокНМА.Организация = &Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	ПозицияСписка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ДанныеПерехода.ПозицияСписка) КАК ПозицияСписка,
	|	ДанныеПерехода.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ НематериальныеАктивыСгруппированные
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК ДанныеПерехода
	|ГДЕ
	|	ДанныеПерехода.ДействиеПриПереходе В (ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.Пересмотрено), ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КПереклассификации))
	|	И НЕ ДанныеПерехода.ЭтоСчетКапитальныхВложений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПерехода.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	ПозицияСписка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияБУ(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаПервоначальныеСведенияБУ", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПервоначальныеСведенияБУ", НомераТаблиц.Количество());
	// Элементы амортизации - срок полезного использования, способ начисления амортизации, предполагаемый объем продукции, коэффицциент ускорения -
	// записываются в регистр "Первоначальные сведения" для совместимости. Эти же сведения, а также ликвидационная стоимость будут
	// записаны также в регистр "Элементы амортизации НМА (бухгалтерский учет)".
	// Срок полезного использования отсчитывается от даты принятия к учету, который определяется по регистру сведений "Состояния НМА организаций",
	// Движения по этому регистру также запишутся при проведении рег. операции "Переход на ФСБУ 14" (см. ТекстЗапросаСостоянияНМА, СформироватьДвиженияСостоянияНМА)
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеПерехода.НематериальныйАктив КАК НематериальныйАктив,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.Иное) КАК СпособПоступления,
	|	НЕ ДанныеПерехода.НеопределенныйСрокПолезногоИспользования КАК НачислятьАмортизацию,
	|	ДанныеПерехода.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ДанныеПерехода.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ДанныеПерехода.ОбъемПродукцииРабот КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ДанныеПерехода.Коэффициент КАК Коэффициент
	|ПОМЕСТИТЬ ВременнаяТаблицаПервоначальныеСведенияБУ
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК ДанныеПерехода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НематериальныеАктивыСгруппированные КАК НематериальныеАктивы
	|		ПО ДанныеПерехода.НематериальныйАктив = НематериальныеАктивы.НематериальныйАктив
	|			И ДанныеПерехода.ПозицияСписка = НематериальныеАктивы.ПозицияСписка
	|ГДЕ
	|	ДанныеПерехода.ДействиеПриПереходе = ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КПереклассификации)
	|	И НЕ ДанныеПерехода.ЭтоСчетКапитальныхВложений
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДанныеПерехода.ПервоначальнаяСтоимость) КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияБУ.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияБУ.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияБУ.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияБУ.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияБУ.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияБУ.Коэффициент КАК Коэффициент
	|ИЗ
	|	ВременнаяТаблицаПервоначальныеСведенияБУ КАК ПервоначальныеСведенияБУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаДанныеПерехода КАК ДанныеПерехода
	|		ПО ПервоначальныеСведенияБУ.НематериальныйАктив = ДанныеПерехода.НематериальныйАктив
	|
	|СГРУППИРОВАТЬ ПО
	|	ПервоначальныеСведенияБУ.Коэффициент,
	|	ПервоначальныеСведенияБУ.НачислятьАмортизацию,
	|	ПервоначальныеСведенияБУ.НематериальныйАктив,
	|	ПервоначальныеСведенияБУ.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияБУ.СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияБУ.СпособПоступления,
	|	ПервоначальныеСведенияБУ.СрокПолезногоИспользования";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаЭлементыАмортизации(НомераТаблиц)
	
	НомераТаблиц.Вставить("ЭлементыАмортизации", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеПерехода.ПозицияСписка КАК ПозицияСписка,
	|	ДанныеПерехода.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеПерехода.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	НЕ ДанныеПерехода.НеопределенныйСрокПолезногоИспользования КАК НачислятьАмортизацию,
	|	ДанныеПерехода.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ДанныеПерехода.Коэффициент КАК Коэффициент,
	|	ДанныеПерехода.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	ДанныеПерехода.СрокПолезногоИспользования КАК СрокПолезногоИспользования
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК ДанныеПерехода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НематериальныеАктивыСгруппированные КАК НематериальныеАктивы
	|		ПО ДанныеПерехода.НематериальныйАктив = НематериальныеАктивы.НематериальныйАктив
	|			И ДанныеПерехода.ПозицияСписка = НематериальныеАктивы.ПозицияСписка
	|ГДЕ
	|	ДанныеПерехода.ДействиеПриПереходе В (ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.Пересмотрено), ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КПереклассификации))
	|	И НЕ ДанныеПерехода.ЭтоСчетКапитальныхВложений";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСостоянияНМА(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаНовыеСостоянияНМА", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СостоянияНМА", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеПерехода.НематериальныйАктив КАК НематериальныйАктив,
	|	ВЫБОР
	|		КОГДА ДанныеПерехода.ЭтоСчетКапитальныхВложений
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|	КОНЕЦ КАК Состояние,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДанныеПерехода.ДатаПринятияКУчету <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДанныеПерехода.ДатаПринятияКУчету
	|			ИНАЧЕ &Период
	|		КОНЕЦ) КАК Период
	|ПОМЕСТИТЬ ВременнаяТаблицаНовыеСостоянияНМА
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК ДанныеПерехода
	|ГДЕ
	|	ДанныеПерехода.ДействиеПриПереходе = ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КПереклассификации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПерехода.НематериальныйАктив,
	|	ВЫБОР
	|		КОГДА ДанныеПерехода.ЭтоСчетКапитальныхВложений
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	Состояние
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаНовыеСостоянияНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ВременнаяТаблицаНовыеСостоянияНМА.Состояние КАК Состояние,
	|	ВременнаяТаблицаНовыеСостоянияНМА.Период КАК Период
	|ИЗ
	|	ВременнаяТаблицаНовыеСостоянияНМА КАК ВременнаяТаблицаНовыеСостоянияНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияНМАОрганизаций.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|					И НематериальныйАктив В
	|						(ВЫБРАТЬ
	|							ВременнаяТаблицаНовыеСостоянияНМА.НематериальныйАктив КАК НематериальныйАктив
	|						ИЗ
	|							ВременнаяТаблицаНовыеСостоянияНМА КАК ВременнаяТаблицаНовыеСостоянияНМА)) КАК СостоянияНМАОрганизаций
	|		ПО ВременнаяТаблицаНовыеСостоянияНМА.НематериальныйАктив = СостоянияНМАОрганизаций.НематериальныйАктив
	|ГДЕ
	|	ВременнаяТаблицаНовыеСостоянияНМА.Состояние <> ЕСТЬNULL(СостоянияНМАОрганизаций.Состояние, ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПустаяСсылка))";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСпособыОтраженияРасходов(НомераТаблиц)
	
	НомераТаблиц.Вставить("СпособыОтраженияРасходов", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеПерехода.НематериальныйАктив КАК НематериальныйАктив,
	|	МАКСИМУМ(ДанныеПерехода.СпособОтраженияРасходов) КАК СпособОтраженияРасходов
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК ДанныеПерехода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НематериальныеАктивыСгруппированные КАК НематериальныеАктивы
	|		ПО ДанныеПерехода.НематериальныйАктив = НематериальныеАктивы.НематериальныйАктив
	|			И ДанныеПерехода.ПозицияСписка = НематериальныеАктивы.ПозицияСписка
	|ГДЕ
	|	ДанныеПерехода.ДействиеПриПереходе = ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КПереклассификации)
	|	И НЕ ДанныеПерехода.СчетУчета В (&СчетаКапитальныхВложений)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПерехода.НематериальныйАктив";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСчетаУчетаНМА(НомераТаблиц)
	
	НомераТаблиц.Вставить("СчетаУчетаНМА", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеПерехода.НематериальныйАктив КАК НематериальныйАктив,
	|	МАКСИМУМ(ДанныеПерехода.СчетУчета) КАК СчетУчета,
	|	МАКСИМУМ(ДанныеПерехода.СчетАмортизации) КАК СчетНачисленияАмортизации
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК ДанныеПерехода
	|ГДЕ
	|	ДанныеПерехода.ДействиеПриПереходе = ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КПереклассификации)
	|	И НЕ ДанныеПерехода.СчетУчета В (&СчетаКапитальныхВложений)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПерехода.НематериальныйАктив";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

Функция ТекстЗапросаИзменениеСтоимости(НомераТаблиц)
	
	НомераТаблиц.Вставить("ИзменениеСтоимостиНМА", НомераТаблиц.Количество());
	
	// НМА со статусом Пересмотрено и К списанию заполняются автоматически, при этом в алгоритме заполнения строки группируются
	// по полям НематериальныйАктив и ЭтоСчетКапитальныхВложений, что предотвращает задваивание записей в ТаблицаИзменениеСтоимостиНМА.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СохраненныеДанные.НематериальныйАктив КАК НематериальныйАктив,
	|	СохраненныеДанные.ДействиеПриПереходе КАК ДействиеПриПереходе,
	|	МАКСИМУМ(СохраненныеДанные.СчетУчета) КАК СчетУчета,
	|	МАКСИМУМ(СохраненныеДанные.СчетАмортизации) КАК СчетАмортизации,
	|	СУММА(СохраненныеДанные.ПервоначальнаяСтоимость) КАК ПервоначальнаяСтоимость,
	|	СУММА(СохраненныеДанные.Амортизация) КАК Амортизация,
	|	СУММА(СохраненныеДанные.Обесценение) КАК Обесценение,
	|	СУММА(СохраненныеДанные.ОбесценениеДоступноеДляВосстановления) КАК ОбесценениеДоступноеДляВосстановления,
	|	СохраненныеДанные.ЭтоСчетКапитальныхВложений КАК ЭтоСчетКапитальныхВложений
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК СохраненныеДанные
	|ГДЕ
	|	СохраненныеДанные.ДействиеПриПереходе = ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.Пересмотрено)
	|				И НЕ &СтоимостьОпределяетсяПоДаннымУчета
	|			ИЛИ СохраненныеДанные.ДействиеПриПереходе = ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КСписанию)
	|
	|СГРУППИРОВАТЬ ПО
	|	СохраненныеДанные.ДействиеПриПереходе,
	|	СохраненныеДанные.НематериальныйАктив,
	|	СохраненныеДанные.ЭтоСчетКапитальныхВложений";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПереклассификация(НомераТаблиц)
	
	НомераТаблиц.Вставить("ПереклассификацияНМА", НомераТаблиц.Количество());
	// Возможна ситуация, когда один и тот же актив переклассифицируется в несколько различных НМА. В этом случае стоимость актива делится
	// между этими НМА. Если переход предусматривает указание сумм, то стоимость актива делится пропорционально указанным первоначальным стоимостям
	// новых НМА. В противном случае стоимость актива делится равномерно между указанными НМА.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СохраненныеДанные.НематериальныйАктив КАК НематериальныйАктив,
	|	МАКСИМУМ(СохраненныеДанные.СчетУчета) КАК СчетУчета,
	|	МАКСИМУМ(СохраненныеДанные.СчетАмортизации) КАК СчетАмортизации,
	|	СохраненныеДанные.СчетСписания КАК СчетСписания,
	|	СохраненныеДанные.ПодразделениеСписания КАК ПодразделениеСписания,
	|	СохраненныеДанные.СубконтоСписания1 КАК СубконтоСписания1,
	|	СохраненныеДанные.СубконтоСписания2 КАК СубконтоСписания2,
	|	СохраненныеДанные.СубконтоСписания3 КАК СубконтоСписания3,
	|	СУММА(СохраненныеДанные.ПервоначальнаяСтоимость) КАК ПервоначальнаяСтоимость,
	|	СУММА(СохраненныеДанные.Амортизация) КАК Амортизация,
	|	СУММА(СохраненныеДанные.Обесценение) КАК Обесценение,
	|	СУММА(СохраненныеДанные.ОбесценениеДоступноеДляВосстановления) КАК ОбесценениеДоступноеДляВосстановления,
	|	СохраненныеДанные.ЭтоСчетКапитальныхВложений КАК ЭтоСчетКапитальныхВложений,
	|	СУММА(ВЫБОР
	|			КОГДА &СтоимостьОпределяетсяПоДаннымУчета
	|				ТОГДА 1
	|			ИНАЧЕ СохраненныеДанные.ПервоначальнаяСтоимость
	|		КОНЕЦ) КАК КоэффициентСтоимости
	|ИЗ
	|	ВременнаяТаблицаДанныеПерехода КАК СохраненныеДанные
	|ГДЕ
	|	СохраненныеДанные.ДействиеПриПереходе = ЗНАЧЕНИЕ(Перечисление.ДействияПриПереходеНаФСБУ14.КПереклассификации)
	|
	|СГРУППИРОВАТЬ ПО
	|	СохраненныеДанные.СчетСписания,
	|	СохраненныеДанные.ПодразделениеСписания,
	|	СохраненныеДанные.СубконтоСписания1,
	|	СохраненныеДанные.СубконтоСписания2,
	|	СохраненныеДанные.СубконтоСписания3,
	|	СохраненныеДанные.НематериальныйАктив,
	|	СохраненныеДанные.ЭтоСчетКапитальныхВложений";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура СформироватьДвиженияПервоначальныеСведенияНМАБухгалтерскийУчет(Движения, ПараметрыПроведения, ТаблицаСтоимости)
	
	Если Не ЗначениеЗаполнено(ПараметрыПроведения.ПервоначальныеСведенияБУ) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	ТаблицаСтоимости.Индексы.Добавить("НематериальныйАктив");
	
	Отбор = Новый Структура("НематериальныйАктив");
	Для Каждого СтрокаТаблицы Из ПараметрыПроведения.ПервоначальныеСведенияБУ Цикл
		
		Движение = Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
		
		Если Реквизиты.СтоимостьОпределяетсяПоДаннымУчета Тогда
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
			СтрокиПервоначальнойСтоимости = ТаблицаСтоимости.НайтиСтроки(Отбор);
			Для Каждого СтрокаСтоимости Из СтрокиПервоначальнойСтоимости Цикл
				Движение.ПервоначальнаяСтоимость = Движение.ПервоначальнаяСтоимость + СтрокаСтоимости.СуммаСписания;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьДвиженияЭлементыАмортизацииНМАБухгалтерскийУчет(Движения, ПараметрыПроведения)

	Если Не ЗначениеЗаполнено(ПараметрыПроведения.ЭлементыАмортизации) Тогда
		Возврат;
	КонецЕсли;
		
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	Для Каждого СтрокаТаблицы Из ПараметрыПроведения.ЭлементыАмортизации Цикл
		
		Движение = Движения.ЭлементыАмортизацииНМАБухгалтерскийУчет.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
		
	КонецЦикла;

	Движения.ЭлементыАмортизацииНМАБухгалтерскийУчет.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьДвиженияСостоянияНМА(Движения, ПараметрыПроведения)
	
	Если Не ЗначениеЗаполнено(ПараметрыПроведения.СостоянияНМА) Тогда
		Возврат;
	КонецЕсли;
		
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	Для Каждого СтрокаТаблицы Из ПараметрыПроведения.СостоянияНМА Цикл
		
		Движение = Движения.СостоянияНМАОрганизаций.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
		ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
		
	КонецЦикла;
	
	Движения.СостоянияНМАОрганизаций.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьДвиженияСпособыОтраженияРасходов(Движения, ПараметрыПроведения)

	Если Не ЗначениеЗаполнено(ПараметрыПроведения.СпособыОтраженияРасходов) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	Для Каждого СтрокаТаблицы Из ПараметрыПроведения.СпособыОтраженияРасходов Цикл
		
		Движение = Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
		
	КонецЦикла;

	Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьДвиженияСчетаУчетаНМА(Движения, ПараметрыПроведения)

	Если Не ЗначениеЗаполнено(ПараметрыПроведения.СчетаУчетаНМА) Тогда
		Возврат;
	КонецЕсли;
		
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	Для Каждого СтрокаТаблицы Из ПараметрыПроведения.СчетаУчетаНМА Цикл
		
		Движение = Движения.СчетаБухгалтерскогоУчетаНМА.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
		
	КонецЦикла;

	Движения.СчетаБухгалтерскогоУчетаНМА.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьПроводкиПереходНаФСБУ14(Движения, ТаблицаРеквизиты, ТаблицаИзменениеСтоимостиНМА, ТаблицаСтоимостиПереклассификацияНМА)
	
	Реквизиты = ТаблицаРеквизиты[0];

	СодержаниеПроводок = НСтр("ru='Переход на ФСБУ 14'", ОбщегоНазначения.КодОсновногоЯзыка());

	СчетПрибыль = ПланыСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению;
	
	Для Каждого СтрокаИзменениеСтоимости Из ТаблицаИзменениеСтоимостиНМА Цикл
		
		// Первоначальная стоимость
		Если СтрокаИзменениеСтоимости.ПервоначальнаяСтоимостьСписание - СтрокаИзменениеСтоимости.ПервоначальнаяСтоимость <> 0 Тогда

			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;
			Проводка.Содержание  = СодержаниеПроводок;

			СвойстваСчетаУчетаНМА = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаИзменениеСтоимости.СчетУчета);

			ИмяСубконтоНМА = "НематериальныеАктивы";
			Если СвойстваСчетаУчетаНМА.ИдентификаторыСубконто[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы] = Неопределено Тогда
				ИмяСубконтоНМА = "РасходыНаНИОКР";
			КонецЕсли;

			Если СтрокаИзменениеСтоимости.ПервоначальнаяСтоимостьСписание - СтрокаИзменениеСтоимости.ПервоначальнаяСтоимость > 0 Тогда

				Проводка.СчетДт = СчетПрибыль;
				Проводка.СчетКт = СтрокаИзменениеСтоимости.СчетУчета;
				
				БухгалтерскийУчет.УстановитьПодразделенияПроводки(Проводка, СтрокаИзменениеСтоимости.Подразделение, СтрокаИзменениеСтоимости.Подразделение);

				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, ИмяСубконтоНМА,
					СтрокаИзменениеСтоимости.НематериальныйАктив);
					
				Проводка.Сумма = СтрокаИзменениеСтоимости.ПервоначальнаяСтоимостьСписание - СтрокаИзменениеСтоимости.ПервоначальнаяСтоимость;

			Иначе

				Проводка.СчетДт = СтрокаИзменениеСтоимости.СчетУчета;
				Проводка.СчетКт = СчетПрибыль;
				
				БухгалтерскийУчет.УстановитьПодразделенияПроводки(Проводка, СтрокаИзменениеСтоимости.Подразделение, СтрокаИзменениеСтоимости.Подразделение);

				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, ИмяСубконтоНМА,
					СтрокаИзменениеСтоимости.НематериальныйАктив);

				Проводка.Сумма = СтрокаИзменениеСтоимости.ПервоначальнаяСтоимость - СтрокаИзменениеСтоимости.ПервоначальнаяСтоимостьСписание;

			КонецЕсли;

		КонецЕсли;
		
		ИзменениеАмортизации = СтрокаИзменениеСтоимости.АмортизацияСписание
			- (СтрокаИзменениеСтоимости.Амортизация + СтрокаИзменениеСтоимости.Обесценение - СтрокаИзменениеСтоимости.ОбесценениеДоступноеДляВосстановления);
		
		Если ИзменениеАмортизации <> 0 Тогда

			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;
			Проводка.Содержание  = СодержаниеПроводок;

			Если ИзменениеАмортизации > 0 Тогда

				Проводка.СчетДт = СтрокаИзменениеСтоимости.СчетАмортизации;
				Проводка.СчетКт = СчетПрибыль;

				БухгалтерскийУчет.УстановитьПодразделенияПроводки(Проводка, СтрокаИзменениеСтоимости.Подразделение, СтрокаИзменениеСтоимости.Подразделение);

				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НематериальныеАктивы",
					СтрокаИзменениеСтоимости.НематериальныйАктив);
				Проводка.Сумма = ИзменениеАмортизации;

			Иначе

				Проводка.СчетДт = СчетПрибыль;
				Проводка.СчетКт = СтрокаИзменениеСтоимости.СчетАмортизации;

				БухгалтерскийУчет.УстановитьПодразделенияПроводки(Проводка, СтрокаИзменениеСтоимости.Подразделение, СтрокаИзменениеСтоимости.Подразделение);

				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "НематериальныеАктивы",
					СтрокаИзменениеСтоимости.НематериальныйАктив);

				Проводка.Сумма = - ИзменениеАмортизации;

			КонецЕсли;
			
		КонецЕсли;
		
		ДобавитьПроводкуДоступноеОбесценение(
			Движения.Хозрасчетный,
			Реквизиты,
			СтрокаИзменениеСтоимости.НематериальныйАктив,
			СтрокаИзменениеСтоимости.Подразделение,
			СодержаниеПроводок,
			СчетПрибыль,
			СтрокаИзменениеСтоимости.ОбесценениеДоступноеДляВосстановления);

	КонецЦикла;
	
	Для Каждого СтрокаПереклассификация Из ТаблицаСтоимостиПереклассификацияНМА Цикл
		
		СуммаПереклассификацииПервоначальнойСтоимости =
			Мин(СтрокаПереклассификация.СуммаСписания, СтрокаПереклассификация.ПервоначальнаяСтоимость);
		
		// Перклассифицируем сумму актива в первоначальную стоимость НМА
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Содержание  = СодержаниеПроводок;

		СвойстваСчетаУчетаНМА = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаПереклассификация.СчетУчета);
		
		ИмяСубконтоНМА = "НематериальныеАктивы";
		Если СвойстваСчетаУчетаНМА.ИдентификаторыСубконто[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы]
			= Неопределено Тогда
			ИмяСубконтоНМА = "РасходыНаНИОКР";
		КонецЕсли;
		
		Проводка.СчетДт = СтрокаПереклассификация.СчетУчета;
		Проводка.СчетКт = СтрокаПереклассификация.СчетСписания;
		
		БухгалтерскийУчет.УстановитьПодразделенияПроводки(
			Проводка,
			СтрокаПереклассификация.ПодразделениеСписания,
			СтрокаПереклассификация.ПодразделениеСписания);

		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, ИмяСубконтоНМА,
			СтрокаПереклассификация.НематериальныйАктив);
		
		Для НомерСубконто = 1 По БухгалтерскийУчет.МаксимальноеКоличествоСубконто() Цикл
			БухгалтерскийУчет.УстановитьСубконто(
				Проводка.СчетКт,
				Проводка.СубконтоКт,
				НомерСубконто,
				СтрокаПереклассификация["СубконтоСписания" + XMLСтрока(НомерСубконто)]);	
		КонецЦикла;
		
		Проводка.Сумма = СуммаПереклассификацииПервоначальнойСтоимости;
		
		// Остаток стоимости спишем на нераспределенную прибыль
		Если СтрокаПереклассификация.СуммаСписания - СуммаПереклассификацииПервоначальнойСтоимости <> 0
			Или СтрокаПереклассификация.ПервоначальнаяСтоимость - СуммаПереклассификацииПервоначальнойСтоимости <> 0 Тогда
			
			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;
			Проводка.Содержание  = СодержаниеПроводок;
			
			Если СтрокаПереклассификация.СуммаСписания - СуммаПереклассификацииПервоначальнойСтоимости <> 0 Тогда
			
				Проводка.СчетДт = СчетПрибыль;
				Проводка.СчетКт = СтрокаПереклассификация.СчетСписания;
			
				БухгалтерскийУчет.УстановитьПодразделенияПроводки(
					Проводка,
					СтрокаПереклассификация.ПодразделениеСписания,
					СтрокаПереклассификация.ПодразделениеСписания);
				
				Для НомерСубконто = 1 По БухгалтерскийУчет.МаксимальноеКоличествоСубконто() Цикл
					БухгалтерскийУчет.УстановитьСубконто(
						Проводка.СчетКт,
						Проводка.СубконтоКт,
						НомерСубконто,
						СтрокаПереклассификация["СубконтоСписания" + XMLСтрока(НомерСубконто)]);
				КонецЦикла;
			
				Проводка.Сумма = СтрокаПереклассификация.СуммаСписания - СуммаПереклассификацииПервоначальнойСтоимости;
			Иначе
				Проводка.СчетДт = СтрокаПереклассификация.СчетУчета;
				Проводка.СчетКт = СчетПрибыль;
				
				БухгалтерскийУчет.УстановитьПодразделенияПроводки(
					Проводка,
					СтрокаПереклассификация.ПодразделениеСписания,
					СтрокаПереклассификация.ПодразделениеСписания);
				
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, ИмяСубконтоНМА,
					СтрокаПереклассификация.НематериальныйАктив);
			
				Проводка.Сумма = СтрокаПереклассификация.ПервоначальнаяСтоимость - СуммаПереклассификацииПервоначальнойСтоимости;
				
			КонецЕсли;
		КонецЕсли;
		
		// Амортизации и обесценение
		ИзменениеАмортизации = СтрокаПереклассификация.Амортизация
			+ (СтрокаПереклассификация.Обесценение - СтрокаПереклассификация.ОбесценениеДоступноеДляВосстановления);
		
		Если ИзменениеАмортизации > 0 Тогда
			
			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;
			Проводка.Содержание  = СодержаниеПроводок;

			Проводка.СчетДт = СчетПрибыль;
			Проводка.СчетКт = СтрокаПереклассификация.СчетАмортизации;

			БухгалтерскийУчет.УстановитьПодразделенияПроводки(
				Проводка,
				СтрокаПереклассификация.ПодразделениеСписания,
				СтрокаПереклассификация.ПодразделениеСписания);

			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "НематериальныеАктивы",
				СтрокаПереклассификация.НематериальныйАктив);

			Проводка.Сумма = ИзменениеАмортизации;
			
		КонецЕсли;
		
		ДобавитьПроводкуДоступноеОбесценение(
			Движения.Хозрасчетный,
			Реквизиты,
			СтрокаПереклассификация.НематериальныйАктив,
			СтрокаПереклассификация.ПодразделениеСписания,
			СодержаниеПроводок,
			СчетПрибыль,
			СтрокаПереклассификация.ОбесценениеДоступноеДляВосстановления);
			
			
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаИзменениеСтоимостиНМА(ПараметрыПроведения)
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	
	ТаблицаСуммыСписанияИзменениеСтоимостиНМА = ПараметрыПроведения.ИзменениеСтоимостиНМА.СкопироватьКолонки();
	ТаблицаСуммыСписанияИзменениеСтоимостиНМА.Колонки.Добавить("ПервоначальнаяСтоимостьСписание", БухгалтерскийУчетКлиентСервер.ТипСумма());
	ТаблицаСуммыСписанияИзменениеСтоимостиНМА.Колонки.Добавить("АмортизацияСписание", БухгалтерскийУчетКлиентСервер.ТипСумма());
	ТаблицаСуммыСписанияИзменениеСтоимостиНМА.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИзменениеСтоимостиНМА", ПараметрыПроведения.ИзменениеСтоимостиНМА);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ГраницаПериода", Новый Граница(Реквизиты.Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СубконтоНМА", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	Запрос.УстановитьПараметр("СубконтоРасходыНаНИОКР", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР);
	
	СчетаУчетаНМА = Обработки.ПомощникПереходаНаФСБУ14.СчетаУчетаНМА();

	Запрос.УстановитьПараметр("СчетаКапитальныхВложений", СчетаУчетаНМА.СчетаКапитальныхВложений);
	Запрос.УстановитьПараметр("СчетаАмортизации", СчетаУчетаНМА.СчетаАмортизации);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИзменениеСтоимости.НематериальныйАктив КАК НематериальныйАктив,
	|	ИзменениеСтоимости.ДействиеПриПереходе КАК ДействиеПриПереходе,
	|	ИзменениеСтоимости.СчетУчета КАК СчетУчета,
	|	ИзменениеСтоимости.СчетАмортизации КАК СчетАмортизации,
	|	ИзменениеСтоимости.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ИзменениеСтоимости.Амортизация КАК Амортизация,
	|	ИзменениеСтоимости.Обесценение КАК Обесценение,
	|	ИзменениеСтоимости.ОбесценениеДоступноеДляВосстановления КАК ОбесценениеДоступноеДляВосстановления
	|ПОМЕСТИТЬ ИзменениеСтоимости
	|ИЗ
	|	&ИзменениеСтоимостиНМА КАК ИзменениеСтоимости
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет В (&СчетаАмортизации)
	|				ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ИНАЧЕ ХозрасчетныйОстатки.Счет
	|		КОНЕЦ) КАК Счет,
	|	МАКСИМУМ(ХозрасчетныйОстатки.Подразделение) КАК Подразделение,
	|	ХозрасчетныйОстатки.Субконто1 КАК НематериальныйАктив,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет В (&СчетаАмортизации)
	|				ТОГДА -ХозрасчетныйОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК АмортизацияОстаток,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ХозрасчетныйОстатки.Счет В (&СчетаАмортизации)
	|				ТОГДА ХозрасчетныйОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьОстаток,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаКапитальныхВложений)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСчетКапитальныхВложений
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаПериода,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ИзменениеСтоимости.СчетУчета
	|				ИЗ
	|					ИзменениеСтоимости КАК ИзменениеСтоимости
	|			
	|				ОБЪЕДИНИТЬ ВСЕ
	|			
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ИзменениеСтоимости.СчетАмортизации
	|				ИЗ
	|					ИзменениеСтоимости КАК ИзменениеСтоимости),
	|			&СубконтоНМА,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ИЗменениеСтоимости.НематериальныйАктив
	|					ИЗ
	|						ИзменениеСтоимости КАК ИзменениеСтоимости)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто1,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаКапитальныхВложений)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет В (&СчетаАмортизации)
	|				ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ИНАЧЕ ХозрасчетныйОстатки.Счет
	|		КОНЕЦ),
	|	МАКСИМУМ(ХозрасчетныйОстатки.Подразделение),
	|	ХозрасчетныйОстатки.Субконто1,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет В (&СчетаАмортизации)
	|				ТОГДА -ХозрасчетныйОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ХозрасчетныйОстатки.Счет В (&СчетаАмортизации)
	|				ТОГДА ХозрасчетныйОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ),
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаКапитальныхВложений)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаПериода,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ИзменениеСтоимости.СчетУчета
	|				ИЗ
	|					ИзменениеСтоимости КАК ИзменениеСтоимости
	|			
	|				ОБЪЕДИНИТЬ ВСЕ
	|			
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ИзменениеСтоимости.СчетАмортизации
	|				ИЗ
	|					ИзменениеСтоимости КАК ИзменениеСтоимости),
	|			&СубконтоРасходыНаНИОКР,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ИЗменениеСтоимости.НематериальныйАктив
	|					ИЗ
	|						ИзменениеСтоимости КАК ИЗменениеСтоимости)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто1,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаКапитальныхВложений)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзменениеСтоимости.НематериальныйАктив КАК НематериальныйАктив,
	|	ИзменениеСтоимости.ДействиеПриПереходе КАК ДействиеПриПереходе,
	|	ИзменениеСтоимости.СчетУчета КАК СчетУчета,
	|	ИзменениеСтоимости.СчетАмортизации КАК СчетАмортизации,
	|	ЕСТЬNULL(Остатки.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ИзменениеСтоимости.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ИзменениеСтоимости.Амортизация КАК Амортизация,
	|	ИзменениеСтоимости.Обесценение КАК Обесценение,
	|	ИзменениеСтоимости.ОбесценениеДоступноеДляВосстановления КАК ОбесценениеДоступноеДляВосстановления,
	|	Остатки.ПервоначальнаяСтоимостьОстаток КАК ПервоначальнаяСтоимостьСписание,
	|	Остатки.АмортизацияОстаток КАК АмортизацияСписание
	|ИЗ
	|	ИзменениеСтоимости КАК ИзменениеСтоимости
	|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	|		ПО ИзменениеСтоимости.НематериальныйАктив = Остатки.НематериальныйАктив
	|			И ИзменениеСтоимости.СчетУчета = Остатки.Счет";
	
	ВыборкаОстатки = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаОстатки.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаСуммыСписанияИзменениеСтоимостиНМА.Добавить(), ВыборкаОстатки);
	КонецЦикла;
	
	Возврат ТаблицаСуммыСписанияИзменениеСтоимостиНМА;
	
КонецФункции

Функция ТаблицаСтоимостиПереклассификацияНМА(ПараметрыПроведения)
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизиты[0];
	ТаблицаПереклассификацияНМА = ПараметрыПроведения.ПереклассификацияНМА;
	
	ПоляАналитикиСписания = "СчетСписания, ПодразделениеСписания, СубконтоСписания1, СубконтоСписания2, СубконтоСписания3";
	
	ТаблицаСтоимостиПереклассификацияНМА = ТаблицаПереклассификацияНМА.Скопировать();
	ТаблицаСтоимостиПереклассификацияНМА.Колонки.Добавить("СуммаСписания", БухгалтерскийУчетКлиентСервер.ТипСуммаНеотрицательная());
	
	ТаблицаСтоимостиПереклассификацияНМА.Индексы.Добавить(ПоляАналитикиСписания);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаАналитики", ТаблицаПереклассификацияНМА);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ГраницаПериода", Новый Граница(Реквизиты.Период, ВидГраницы.Включая));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаАналитики.СчетСписания КАК СчетСписания,
	|	ТаблицаАналитики.ПодразделениеСписания КАК ПодразделениеСписания,
	|	ТаблицаАналитики.СубконтоСписания1 КАК СубконтоСписания1,
	|	ТаблицаАналитики.СубконтоСписания2 КАК СубконтоСписания2,
	|	ТаблицаАналитики.СубконтоСписания3 КАК СубконтоСписания3
	|ПОМЕСТИТЬ ТаблицаАналитики
	|ИЗ
	|	&ТаблицаАналитики КАК ТаблицаАналитики
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетСписания,
	|	ПодразделениеСписания,
	|	СубконтоСписания1,
	|	СубконтоСписания2,
	|	СубконтоСписания3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет.УчетПоПодразделениям
	|			ТОГДА ХозрасчетныйОстатки.Подразделение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ВидыСубконто1.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ХозрасчетныйОстатки.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА ВидыСубконто2.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ХозрасчетныйОстатки.Субконто2
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА ВидыСубконто3.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ХозрасчетныйОстатки.Субконто3
	|	КОНЕЦ КАК Субконто3,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаПериода,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ТаблицаАналитики.СчетСписания
	|				ИЗ
	|					ТаблицаАналитики КАК ТаблицаАналитики),
	|			,
	|			Организация = &Организация) КАК ХозрасчетныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто1
	|		ПО ХозрасчетныйОстатки.Счет = ВидыСубконто1.Ссылка
	|			И (ВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто2
	|		ПО ХозрасчетныйОстатки.Счет = ВидыСубконто2.Ссылка
	|			И (ВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто3
	|		ПО ХозрасчетныйОстатки.Счет = ВидыСубконто3.Ссылка
	|			И (ВидыСубконто3.НомерСтроки = 3)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАналитики.СчетСписания КАК СчетСписания,
	|	ТаблицаАналитики.ПодразделениеСписания КАК ПодразделениеСписания,
	|	ТаблицаАналитики.СубконтоСписания1 КАК СубконтоСписания1,
	|	ТаблицаАналитики.СубконтоСписания2 КАК СубконтоСписания2,
	|	ТаблицаАналитики.СубконтоСписания3 КАК СубконтоСписания3,
	|	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК СуммаОстаток
	|ИЗ
	|	ТаблицаАналитики КАК ТаблицаАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	|		ПО ТаблицаАналитики.СчетСписания = Остатки.Счет
	|			И ТаблицаАналитики.ПодразделениеСписания = Остатки.Подразделение
	|			И ТаблицаАналитики.СубконтоСписания1 = Остатки.Субконто1
	|			И ТаблицаАналитики.СубконтоСписания2 = Остатки.Субконто2
	|			И ТаблицаАналитики.СубконтоСписания3 = Остатки.Субконто3
	|ГДЕ
	|	Остатки.СуммаОстаток > 0";

	РезультатОстатки = Запрос.Выполнить();
	
	ВыборкаОстатки = РезультатОстатки.Выбрать();
	
	Отбор = Новый Структура(ПоляАналитикиСписания);
	
	Пока ВыборкаОстатки.Следующий() Цикл
	
		ЗаполнитьЗначенияСвойств(Отбор, ВыборкаОстатки);
		
		НайденныеСтроки = ТаблицаСтоимостиПереклассификацияНМА.НайтиСтроки(Отбор);
		
		КоэффициентыРаспределения = Новый Массив;
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			КоэффициентыРаспределения.Добавить(НайденнаяСтрока.КоэффициентСтоимости);
		КонецЦикла;
		
		РезультатРаспределенияСуммы = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаОстатки.СуммаОстаток, КоэффициентыРаспределения);

		ЗаполнитьРаспределенныеЗначения(НайденныеСтроки, "СуммаСписания", РезультатРаспределенияСуммы);
		
		Если Реквизиты.СтоимостьОпределяетсяПоДаннымУчета Тогда
			ЗаполнитьРаспределенныеЗначения(НайденныеСтроки, "ПервоначальнаяСтоимость", РезультатРаспределенияСуммы);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаСтоимостиПереклассификацияНМА;
	
КонецФункции

Процедура ЗаполнитьРаспределенныеЗначения(НайденныеСтроки, ИмяПоля, РезультатРаспределения)
	Если РезультатРаспределения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для Индекс = 0 По РезультатРаспределения.ВГраница() Цикл
		НайденныеСтроки[Индекс][ИмяПоля] = РезультатРаспределения[Индекс];
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

Функция ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАВсе()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СостоянияНМАОрганизацийСрезПоследних.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ СписокНМА
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СостоянияНМАОрганизацийСрезПоследних
	|ГДЕ
	|	СостоянияНМАОрганизацийСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)";

	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАПоТаблице()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	&ТаблицаНМА КАК ТаблицаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияНМАОрганизацийСрезПоследних.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ СписокНМА
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций.СрезПоследних(
	|			&НачалоПериода,
	|			НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						ТаблицаНМА.НематериальныйАктив
	|					ИЗ
	|						ТаблицаНМА КАК ТаблицаНМА)
	|				И Организация = &Организация) КАК СостоянияНМАОрганизацийСрезПоследних
	|ГДЕ
	|	СостоянияНМАОрганизацийСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)";

	Возврат ТекстЗапроса;

КонецФункции

Процедура СообщитьОбОшибкеПриНачисленииАмортизации(Выборка, ТекстСообщения, РегОперация, Отказ, Ссылка = Неопределено)
	
	ШаблонСообщения = НСтр("ru = '%1 для нематериального актива %2 (%3).'");
	ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ТекстСообщения, 
		Выборка.НематериальныйАктивПредставление, Выборка.Код);
		
	Если Ссылка = Неопределено Тогда
		ТекстСообщенияОбОшибке = ТекстСообщенияОбОшибке + Символы.ПС
			+ НСтр("ru = 'Укажите эти данные в документах принятия к учету или изменения параметров начисления амортизации.'");
		СсылкаСообщения = БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьНавигационнуюСсылкуНаЖурналДокументыПоНМА();
	ИначеЕсли Ссылка = Ложь Тогда
		СсылкаСообщения = Неопределено;
	Иначе
		СсылкаСообщения = Ссылка;
	КонецЕсли;
	
	БухгалтерскийУчетПереопределяемый.СообщитьОбОшибкеРегОперацииСНавигацией(ТекстСообщенияОбОшибке, СсылкаСообщения, Отказ, РегОперация);
	
КонецПроцедуры // СообщитьОбОшибкеПриНачисленииАмортизации()

Функция ПодготовитьРасчетАмортизацииНМА(ТаблицаНМА, ТаблицаРеквизиты, РежимСправкиРасчета = Ложь) Экспорт
	
	Параметры = ПодготовитьПараметрыРасчетАмортизацииНМА(ТаблицаНМА, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Возврат РассчитатьАмортизацию(
		Реквизиты.Период,
		Реквизиты.Организация,
		Реквизиты.Регистратор,
		Параметры.ТаблицаНМА.ВыгрузитьКолонку("НематериальныйАктив"),
		Реквизиты.ВыдаватьСообщения,
		РежимСправкиРасчета);
		
КонецФункции

Функция РассчитатьАмортизациюПБУ14(Период, Организация, Регистратор, Отбор, ВыдаватьСообщения, РежимСправкиРасчета)

	Результат = НовыйРезультатРасчетаАмортизации();
	
	ДатаРасчета = Период;
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период);
	ПоддержкаПБУ18 = УчетнаяПолитика.ПоддержкаПБУ18(Организация, Период);
	МетодНачисленияАмортизацииНУ = УчетнаяПолитика.МетодНачисленияАмортизацииНУ(Организация, Период);
	
	Если Не ЗначениеЗаполнено(МетодНачисленияАмортизацииНУ) Тогда
		МетодНачисленияАмортизацииНУ = Перечисления.МетодыНачисленияАмортизации.Линейный;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация",                  Организация);
	Запрос.УстановитьПараметр("НачалоПериодаДата",            НачалоМесяца(ДатаРасчета));
	Запрос.УстановитьПараметр("НачалоПериода",                Новый Граница(НачалоМесяца(ДатаРасчета), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецПериода",                 КонецМесяца(ДатаРасчета));
	Запрос.УстановитьПараметр("Период",                       ДатаРасчета);
	Запрос.УстановитьПараметр("ДатаПереходаКЛинейномуМетоду", УправлениеВнеоборотнымиАктивами.ПолучитьДатуПереходаКЛинейномуМетоду(ДатаРасчета, Организация));
	Запрос.УстановитьПараметр("Регистратор",                  Регистратор);
	Запрос.УстановитьПараметр("МетодНачисленияАмортизацииНУ", МетодНачисленияАмортизацииНУ);
	
	// Получим список нематериальных активов для расчета амортизации
	Если Не ЗначениеЗаполнено(Отбор) Тогда
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАВсе();
	Иначе
		ТаблицаНМА = Новый ТаблицаЗначений;
		ТаблицаНМА.Колонки.Добавить("НематериальныйАктив", Новый ОписаниеТипов("СправочникСсылка.НематериальныеАктивы"));
		Для Каждого Объект Из Отбор Цикл
			ТаблицаНМА.Добавить().НематериальныйАктив = Объект;
		КонецЦикла;
		Запрос.УстановитьПараметр("ТаблицаНМА", ТаблицаНМА);
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАПоТаблице();
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Период КАК Период,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Коэффициент КАК Коэффициент,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|ГДЕ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.Период КАК Период,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|ГДЕ
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов КАК СпособОтраженияРасходов
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка КАК СпособОтраженияРасходов,
	|	КОЛИЧЕСТВО(СпособыОтраженияРасходовПоАмортизацииСпособы.Коэффициент) КАК КоличествоКоэффициентов
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизацииСпособы
	|ИЗ
	|	Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
	|ГДЕ
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов
	|			ИЗ
	|				СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОтраженияРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.СпециальныйКоэффициент КАК СпециальныйКоэффициент
	|ПОМЕСТИТЬ НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета
	|		ИНАЧЕ СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации
	|	КОНЕЦ КАК СчетНачисленияАмортизации
	|ПОМЕСТИТЬ СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив";
	Запрос.Выполнить();
	
	// С целью оптимизации производительности формируем массивы счетов (а не временные таблицы) для отборов в виртуальных таблицах.
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета КАК СчетУчета,
	|	ЛОЖЬ КАК ЭтоСчетНачисленияАмортизации
	|ПОМЕСТИТЬ СписокСчетовНМА
	|ИЗ
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации,
	|	ИСТИНА
	|ИЗ
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСчетовНМА.СчетУчета КАК СчетУчета
	|ИЗ
	|	СписокСчетовНМА КАК СписокСчетовНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСчетовНМА.СчетУчета КАК СчетУчета
	|ИЗ
	|	СписокСчетовНМА КАК СписокСчетовНМА
	|ГДЕ
	|	СписокСчетовНМА.ЭтоСчетНачисленияАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СписокСчетовНМА";
	Результаты = Запрос.ВыполнитьПакет();
	Запрос.УстановитьПараметр("СчетаБухгалтерскогоУчетаДляНМА", Результаты[1].Выгрузить().ВыгрузитьКолонку("СчетУчета"));
	Запрос.УстановитьПараметр("СчетаНачисленияАмортизацииНМА", Результаты[2].Выгрузить().ВыгрузитьКолонку("СчетУчета"));
	
	Если РежимСправкиРасчета Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка) КАК НематериальныйАктив,
		|	0 КАК СуммаОборотКт,
		|	0 КАК СуммаНУОборотКт
		|ПОМЕСТИТЬ ХозрасчетныйОбороты";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХозрасчетныйОбороты.Субконто1 КАК НематериальныйАктив,
		|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК СуммаОборотКт,
		|	СУММА(ХозрасчетныйОбороты.СуммаНУОборотКт) КАК СуммаНУОборотКт
		|ПОМЕСТИТЬ ХозрасчетныйОбороты
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Регистратор,
		|			Счет В (&СчетаНачисленияАмортизацииНМА),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
		|			Организация = &Организация
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						СписокНМА.НематериальныйАктив
		|					ИЗ
		|						СписокНМА КАК СписокНМА),
		|			,
		|			) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	ХозрасчетныйОбороты.Регистратор <> &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Субконто1
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() + 
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК НематериальныйАктив,
	|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
	|	ХозрасчетныйОстаткиИОбороты.Подразделение КАК Подразделение,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СуммаНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУНачальныйОстатокДт КАК СуммаНУНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРНачальныйОстатокДт КАК СуммаПРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокДт КАК СуммаВРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт КАК СуммаНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУНачальныйОстатокКт КАК СуммаНУНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРНачальныйОстатокКт КАК СуммаПРНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокКт КАК СуммаВРНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУКонечныйОстатокДт КАК СуммаНУКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРКонечныйОстатокДт КАК СуммаПРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокДт КАК СуммаВРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаКонечныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУКонечныйОстатокКт КАК СуммаНУКонечныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРКонечныйОстатокКт КАК СуммаПРКонечныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокКт КАК СуммаВРКонечныйОстатокКт
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОбороты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			,
	|			Счет В (&СчетаБухгалтерскогоУчетаДляНМА),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ХозрасчетныйОстаткиИОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаНМАОбороты.НематериальныйАктив КАК НематериальныйАктив,
	|	ВыработкаНМАОбороты.КоличествоОборот КАК КоличествоОборот
	|ПОМЕСТИТЬ ВыработкаНМАОбороты
	|ИЗ
	|	РегистрНакопления.ВыработкаНМА.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ВыработкаНМАОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияНМАОрганизаций.НематериальныйАктив КАК НематериальныйАктив,
	|	МАКСИМУМ(СостоянияНМАОрганизаций.Период) КАК ДатаВводаВЭксплуатацию
	|ПОМЕСТИТЬ СостоянияНМАОрганизаций
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|ГДЕ
	|	СостоянияНМАОрганизаций.Организация = &Организация
	|	И СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	СостоянияНМАОрганизаций.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НематериальныеАктивы.Ссылка КАК НематериальныйАктив
	|ПОМЕСТИТЬ НМАТолькоЛинейныйМетодНУ
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|ГДЕ
	|	НематериальныеАктивы.Ссылка В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокНМА КАК СписокНМА)
	|	И НематериальныеАктивы.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АмортизацияНМАНачалоПериода.Субконто1 КАК НематериальныйАктив,
	|	АмортизацияНМАНачалоПериода.СуммаНУОстатокКт КАК АмортизацияНачальныйОстаток
	|ПОМЕСТИТЬ АмортизацияНМАПриПереходеКЛинейномуМетоду
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаПереходаКЛинейномуМетоду,
	|			Счет В (&СчетаНачисленияАмортизацииНМА),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И НЕ Субконто1 В
	|						(ВЫБРАТЬ
	|							НМАТолькоЛинейныйМетодНУ.НематериальныйАктив
	|						ИЗ
	|							НМАТолькоЛинейныйМетодНУ)) КАК АмортизацияНМАНачалоПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ПРЕДСТАВЛЕНИЕ(СписокНМА.НематериальныйАктив) КАК НематериальныйАктивПредставление,
	|	СписокНМА.НематериальныйАктив.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	СписокНМА.НематериальныйАктив.Код КАК Код,
	|	СтоимостьНМА.Подразделение КАК ПодразделениеОрганизации,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНачальныйОстатокДт, 0) КАК СтоимостьНачальныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНУНачальныйОстатокДт, 0) КАК СтоимостьНУНачальныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаПРНачальныйОстатокДт, 0) КАК СтоимостьПРНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаНачальныйОстатокКт, 0) КАК АмортизацияНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0) КАК АмортизацияОборот,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаНУНачальныйОстатокКт, 0) КАК АмортизацияНУНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаПРНачальныйОстатокКт, 0) КАК АмортизацияПРНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотКт, 0) КАК АмортизацияНУОборот,
	|	ЕСТЬNULL(ВыработкаНМАОбороты.КоличествоОборот, 0) КАК Количество,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость, 0) КАК ПервоначальнаяСтоимость,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования, 0) КАК СрокИспользованияДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость, 0) КАК СтоимостьДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Коэффициент, 0) КАК Коэффициент,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК Период,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) КАК НачислятьАмортизациюБУ,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета КАК СчетУчета,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
	|	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов КАК СпособыОтраженияРасходовПоАмортизации,
	|	ЕСТЬNULL(СпособыОтраженияРасходовПоАмортизацииСпособы.КоличествоКоэффициентов, 0) КАК КоличествоКоэффициентов,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ПервоначальнаяСтоимостьНУ, 0) КАК ПервоначальнаяСтоимостьНУ,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПринятияКУчету,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) КАК НачислятьАмортизациюНУ,
	|	ВЫБОР
	|		КОГДА ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив В
	|						(ВЫБРАТЬ
	|							НМАТолькоЛинейныйМетодНУ.НематериальныйАктив
	|						ИЗ
	|							НМАТолькоЛинейныйМетодНУ)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|				ИНАЧЕ &МетодНачисленияАмортизацииНУ
	|			КОНЕЦ
	|	КОНЕЦ КАК МетодНачисленияАмортизации,
	|	ЕСТЬNULL(НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.СпециальныйКоэффициент, 0) КАК СпециальныйКоэффициент,
	|	ЕСТЬNULL(СостоянияНМАОрганизаций.ДатаВводаВЭксплуатацию, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВводаВЭксплуатацию,
	|	ВЫБОР
	|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.АмортизацияДо2009, 0)
	|		ИНАЧЕ ЕСТЬNULL(АмортизацияНМАПриПереходеКЛинейномуМетоду.АмортизацияНачальныйОстаток, 0)
	|	КОНЕЦ КАК АмортизацияПриПереходеКЛинейномуМетоду,
	|	ВЫБОР
	|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ФактическийСрокИспользованияДо2009, 0)
	|		ИНАЧЕ (ГОД(&ДатаПереходаКЛинейномуМетоду) - ГОД(СостоянияНМАОрганизаций.ДатаВводаВЭксплуатацию)) * 12 + (МЕСЯЦ(&ДатаПереходаКЛинейномуМетоду) - МЕСЯЦ(СостоянияНМАОрганизаций.ДатаВводаВЭксплуатацию)) - 1
	|	КОНЕЦ КАК ФактическийСрокПриПереходеКЛинейномуМетоду
	|ИЗ
	|	СписокНМА КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних КАК СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовПоАмортизацииСпособы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
	|		ПО (СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииСпособы.СпособОтраженияРасходов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаБухгалтерскогоУчетаНМАСрезПоследних КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК СтоимостьНМА
	|			ПО СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив = СтоимостьНМА.НематериальныйАктив
	|				И СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета = СтоимостьНМА.Счет
	|			ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК АмортизацияНМА
	|			ПО СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив = АмортизацияНМА.НематериальныйАктив
	|				И СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации = АмортизацияНМА.Счет
	|		ПО СписокНМА.НематериальныйАктив = СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ПО СписокНМА.НематериальныйАктив = ХозрасчетныйОбороты.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаНМАОбороты КАК ВыработкаНМАОбороты
	|		ПО СписокНМА.НематериальныйАктив = ВыработкаНМАОбороты.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних КАК ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних КАК НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияНМАПриПереходеКЛинейномуМетоду КАК АмортизацияНМАПриПереходеКЛинейномуМетоду
	|		ПО СписокНМА.НематериальныйАктив = АмортизацияНМАПриПереходеКЛинейномуМетоду.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|		ПО СписокНМА.НематериальныйАктив = СостоянияНМАОрганизаций.НематериальныйАктив
	|ГДЕ
	|	НЕ(ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) = ЛОЖЬ
	|				И ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) = ЛОЖЬ)";
	
	ВыборкаПоНМА = Запрос.Выполнить().Выбрать();
	
	// Чтобы во время выполнения дальнейшего алгоритма не использовались ненужные уже ресурсы (временные таблицы).
	МенеджерВременныхТаблиц.Закрыть();
	
	Пока ВыборкаПоНМА.Следующий() Цикл

		СуммаАмортизацииБУ                  = 0;
		СуммаАмортизацииНУ                  = 0;
		СуммаАмортизацииПР                  = 0;
		СуммаАмортизацииВР                  = 0;
		НормаАмортизации                    = 0;
		СтоимостьДляВычисленияАмортизацииНУ = 0;
		ОстаточнаяСтоимость                 = 0;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СчетНачисленияАмортизации) Тогда
			Если ВыдаватьСообщения Тогда
				СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан счет для начисления амортизации'"),
					Регистратор, Результат.Отказ);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Пропустить = Ложь;
		
		Если ВыборкаПоНМА.НачислятьАмортизациюБУ Тогда

			Если ВыборкаПоНМА.АмортизацияОборот <> 0 Тогда
				Если ВыдаватьСообщения И Отбор.Количество() > 0 Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Амортизация в этом месяце уже начислялась '"),
						Регистратор, Результат.Отказ);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СпособНачисленияАмортизации) Тогда
				Если ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан способ амортизации '"),
						Регистратор, Результат.Отказ);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации = 0 Тогда
				Если ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Стоимость равна нулю '"),
						Регистратор, Результат.Отказ);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации = 0 Тогда
				Если ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Срок использования равен нулю '"),
						Регистратор, Результат.Отказ);
				КонецЕсли;
				Пропустить = Истина;
			КонецЕсли;

			Если НЕ Пропустить Тогда
			
				СуммаАмортизации = 0;

				Если ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный Тогда
		 
					СуммаГодовойАмортизации = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации / ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации * 12;
					СуммаАмортизации        = СуммаГодовойАмортизации / 12;

				ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда

					Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.Количество) Тогда
						Пропустить = Истина;
					КонецЕсли;

					Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации) Тогда
						Если ВыдаватьСообщения Тогда
							СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Объем продукции для вычисления не определен '"),
								Регистратор, Результат.Отказ);
						КонецЕсли; 
						Пропустить = Истина;
					КонецЕсли;

					Если Пропустить Тогда
						СуммаАмортизации = 0 ;
					Иначе
						СуммаАмортизации = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации * ВыборкаПоНМА.Количество / ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации;
					КонецЕсли;

				ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка Тогда

					ОстаточнаяСтоимость = Макс(0, ВыборкаПоНМА.СтоимостьНачальныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток);
					ОставшийсяСрокПолезногоИспользования = ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации 
														 - УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВыборкаПоНМА.Период, ДатаРасчета)
														 + 1;
					Коэффициент = ?(ВыборкаПоНМА.Коэффициент = 0, 1, ВыборкаПоНМА.Коэффициент);
					
					СуммаАмортизации = ?(ОставшийсяСрокПолезногоИспользования > 0,
										 ОстаточнаяСтоимость * (Коэффициент / ОставшийсяСрокПолезногоИспользования),
										 0);

				КонецЕсли;

				ВсегоОсталосьСписать = ВыборкаПоНМА.СтоимостьНачальныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток;
				Если ВсегоОсталосьСписать - СуммаАмортизации < 0.01 
					И ВсегоОсталосьСписать - СуммаАмортизации > 0 Тогда 
					//погрешности при вычислениях
					СуммаАмортизации = ВсегоОсталосьСписать;
					
				КонецЕсли;
				
				БУНеОкругленная = Мин(СуммаАмортизации, ВсегоОсталосьСписать);
				Если ВыборкаПоНМА.СтоимостьНачальныйОстаток = 0 Тогда
					ПРНеОкругленная = ВыборкаПоНМА.СтоимостьПРНачальныйОстаток;
				Иначе
					ПРНеОкругленная = ВыборкаПоНМА.СтоимостьПРНачальныйОстаток * (БУНеОкругленная / ВыборкаПоНМА.СтоимостьНачальныйОстаток);
				КонецЕсли;
				РазницаНеОкругленная = БУНеОкругленная - ПРНеОкругленная;
				
				СуммаАмортизацииБУ = Окр(БУНеОкругленная, 2);
				
				Если ВыборкаПоНМА.СтоимостьПРНачальныйОстаток = ВыборкаПоНМА.АмортизацияПРНачальныйОстаток Тогда 
					СуммаАмортизацииПР = 0;
				ИначеЕсли РазницаНеОкругленная < 0.005 И РазницаНеОкругленная > -0.005 Тогда 
					СуммаАмортизацииПР = СуммаАмортизацииБУ;
				Иначе
					СуммаАмортизацииПР = Окр(ПРНеОкругленная, 2);
				КонецЕсли;
				
			КонецЕсли;

		КонецЕсли;
		
		Пропустить = Ложь;
		
		Если ОтражатьВНалоговомУчете Тогда
			
			Если ВыборкаПоНМА.НачислятьАмортизациюНУ Тогда
				
				Если ВыборкаПоНМА.АмортизацияНУОборот <> 0 Тогда
					Если ВыдаватьСообщения И Отбор.Количество() > 0 Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Амортизация (НУ) в этом месяце уже начислялась '"),
							Регистратор, Результат.Отказ);
					КонецЕсли; 
					Пропустить = Истина; 
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.МетодНачисленияАмортизации) Тогда
					Если ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан метод начисления амортизации (НУ) в настройках налога на прибыль '"),
							Регистратор, Результат.Отказ);
					КонецЕсли;
					Пропустить = Истина;                                                                                     
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СтоимостьНУНачальныйОстаток) Тогда
					Пропустить = Истина;
				КонецЕсли;
				
				Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный
					И ВыборкаПоНМА.СрокПолезногоИспользования = 0 Тогда
					Если ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Срок использования (НУ) равен нулю '"),
							Регистратор, Результат.Отказ);
					КонецЕсли; 
					Пропустить = Истина;
				КонецЕсли;
				
				Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный
					И НЕ ЗначениеЗаполнено(ВыборкаПоНМА.АмортизационнаяГруппа) Тогда
					Если ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'В справочнике не указана амортизационная группа '"),
							Регистратор, Результат.Отказ);
					КонецЕсли; 
					Пропустить = Истина;
				КонецЕсли;
				
				Если НЕ Пропустить Тогда
					
					СуммаАмортизации = 0;
					
					Если ВыборкаПоНМА.СчетУчета = ВыборкаПоНМА.СчетНачисленияАмортизации Тогда // НИОКР.
						СтоимостьДляВычисленияАмортизацииНУ = ВыборкаПоНМА.ПервоначальнаяСтоимостьНУ;
					Иначе
						СтоимостьДляВычисленияАмортизацииНУ = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток;
					КонецЕсли; 
					
					СрокИспользованияДляВычисления = Макс(1, ВыборкаПоНМА.СрокПолезногоИспользования);
					
					СпециальныйКоэффициент = ?(ВыборкаПоНМА.СпециальныйКоэффициент = 0, 1, ВыборкаПоНМА.СпециальныйКоэффициент);
					
					Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный Тогда
						
						СтоимостьДляВычисленияАмортизацииНУ = СтоимостьДляВычисленияАмортизацииНУ - ВыборкаПоНМА.АмортизацияПриПереходеКЛинейномуМетоду;
						ФактическийСрокПриПереходеКЛинейномуМетоду = Макс(ВыборкаПоНМА.ФактическийСрокПриПереходеКЛинейномуМетоду, 0);
						СрокИспользованияДляВычисления = СрокИспользованияДляВычисления - ФактическийСрокПриПереходеКЛинейномуМетоду;
						
						СуммаАмортизации = СтоимостьДляВычисленияАмортизацииНУ / СрокИспользованияДляВычисления * СпециальныйКоэффициент;
						
					ИначеЕсли ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный Тогда
						
						НормаАмортизации = УправлениеВнеоборотнымиАктивами.ПолучитьНормуПоАмортизационнойГруппе(ВыборкаПоНМА.АмортизационнаяГруппа);
						
						Если НЕ (НормаАмортизации = Неопределено) Тогда
							
							СуммаАмортизации = (ВыборкаПоНМА.СтоимостьНУНачальныйОстаток - ВыборкаПоНМА.АмортизацияНУНачальныйОстаток)
							* НормаАмортизации / 100 
							* СпециальныйКоэффициент;
							
						КонецЕсли;
						
					КонецЕсли;
					
					ВсегоОсталосьСписать = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток - ВыборкаПоНМА.АмортизацияНУНачальныйОстаток;
					Если ВсегоОсталосьСписать - СуммаАмортизации < 0.01 
						И ВсегоОсталосьСписать - СуммаАмортизации > 0 Тогда
						//погрешности при вычислениях
						СуммаАмортизации = ВсегоОсталосьСписать;
						
					КонецЕсли;
					
					СуммаАмортизацииНУ = Окр(Мин(ВсегоОсталосьСписать, СуммаАмортизации), 2);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СуммаАмортизацииВР = СуммаАмортизацииБУ - СуммаАмортизацииНУ - СуммаАмортизацииПР;			
		
		Если СуммаАмортизацииБУ > 0
		 ИЛИ СуммаАмортизацииНУ > 0
		 ИЛИ СуммаАмортизацииПР <> 0
		 ИЛИ СуммаАмортизацииВР <> 0 Тогда
	
			СтрокаАмортизации = Результат.ТаблицаАмортизации.Добавить();

			СтрокаАмортизации.ОбъектУчета               = ВыборкаПоНМА.НематериальныйАктив;
			СтрокаАмортизации.СчетУчета                 = ВыборкаПоНМА.СчетУчета;
			СтрокаАмортизации.СчетНачисленияАмортизации = ВыборкаПоНМА.СчетНачисленияАмортизации;
			СтрокаАмортизации.НаправлениеАмортизации    = ВыборкаПоНМА.СпособыОтраженияРасходовПоАмортизации;
			СтрокаАмортизации.СуммаАмортизацииБУ        = СуммаАмортизацииБУ;
			СтрокаАмортизации.СуммаАмортизацииНУ        = СуммаАмортизацииНУ;
			СтрокаАмортизации.СуммаАмортизацииПР        = ?(ПоддержкаПБУ18, СуммаАмортизацииПР, 0);
			СтрокаАмортизации.СуммаАмортизацииВР        = ?(ПоддержкаПБУ18, СуммаАмортизацииВР, 0);
			СтрокаАмортизации.Подразделение             = ВыборкаПоНМА.ПодразделениеОрганизации;
			
			//Данные для справки-расчета
			СтрокаСправкаРасчет = Результат.СправкаРасчет.Добавить();
			
			ОстаточнаяСтоимостьБУ = ВыборкаПоНМА.СтоимостьНачальныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток;
			ОстаточнаяСтоимостьНУ = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток - ВыборкаПоНМА.АмортизацияНУНачальныйОстаток;
				
			ФактическийСрокИспользованияБУ = УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(
				ВыборкаПоНМА.ДатаВводаВЭксплуатацию, ДатаРасчета) - 1;
			ФактическийСрокИспользованияНУ = УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(
				ВыборкаПоНМА.ДатаВводаВЭксплуатацию, ДатаРасчета) - 1;
				
			ОстатокСрокаПолезногоИспользованияБУ = Макс(0, ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации - ФактическийСрокИспользованияБУ);
			ОстатокСрокаПолезногоИспользованияНУ = Макс(0, ВыборкаПоНМА.СрокПолезногоИспользования - ФактическийСрокИспользованияНУ);
			
			Если ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный Тогда
				СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.Линейный;
				СтоимостьДляВычисленияАмортизацииБУ = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации;
			ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка Тогда
				СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка;
				СтоимостьДляВычисленияАмортизацииБУ = ОстаточнаяСтоимость;
			ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда
				СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции;
				СтоимостьДляВычисленияАмортизацииБУ = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации;
			КонецЕсли;
			
			СтрокаСправкаРасчет.ПериодРасчета                         = КонецМесяца(ДатаРасчета);
			СтрокаСправкаРасчет.Объект                                = ВыборкаПоНМА.НематериальныйАктив;
			СтрокаСправкаРасчет.СпособНачисленияАмортизацииБУ         = СпособНачисленияАмортизацииБУ;
			СтрокаСправкаРасчет.СпособНачисленияАмортизацииНУ         = ВыборкаПоНМА.МетодНачисленияАмортизации;
			СтрокаСправкаРасчет.АмортизационнаяГруппа                 = ВыборкаПоНМА.АмортизационнаяГруппа;
			СтрокаСправкаРасчет.ИнвентарныйНомер                      = ВыборкаПоНМА.Код;
			СтрокаСправкаРасчет.ДатаВводаВЭксплуатацию                = ВыборкаПоНМА.ДатаВводаВЭксплуатацию;
			СтрокаСправкаРасчет.НачислятьАмортизациюБУ                = ВыборкаПоНМА.НачислятьАмортизациюБУ;
			СтрокаСправкаРасчет.НачислятьАмортизациюНУ                = ВыборкаПоНМА.НачислятьАмортизациюНУ;
			СтрокаСправкаРасчет.НормаАмортизации                      = НормаАмортизации;
			СтрокаСправкаРасчет.СтоимостьБУ                           = ВыборкаПоНМА.СтоимостьНачальныйОстаток;
			СтрокаСправкаРасчет.СтоимостьНУ                           = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток;
			СтрокаСправкаРасчет.ОстаточнаяСтоимостьБУ                 = ОстаточнаяСтоимостьБУ;
			СтрокаСправкаРасчет.ОстаточнаяСтоимостьНУ                 = ОстаточнаяСтоимостьНУ;
			СтрокаСправкаРасчет.СтоимостьДляАмортизацииБУ             = СтоимостьДляВычисленияАмортизацииБУ;
			СтрокаСправкаРасчет.СтоимостьДляАмортизацииНУ             = СтоимостьДляВычисленияАмортизацииНУ;
			СтрокаСправкаРасчет.СуммаАмортизацииБУ                    = СуммаАмортизацииБУ;
			СтрокаСправкаРасчет.СуммаАмортизацииНУ                    = СуммаАмортизацииНУ;
			СтрокаСправкаРасчет.СрокПолезногоИспользованияБУ          = ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации;
			СтрокаСправкаРасчет.СрокПолезногоИспользованияНУ          = ВыборкаПоНМА.СрокПолезногоИспользования;
			СтрокаСправкаРасчет.ОстатокСрокаПолезногоИспользованияБУ  = ОстатокСрокаПолезногоИспользованияБУ;
			СтрокаСправкаРасчет.ОстатокСрокаПолезногоИспользованияНУ  = ОстатокСрокаПолезногоИспользованияНУ;
			СтрокаСправкаРасчет.КоэффициентАмортизации                = ВыборкаПоНМА.Коэффициент;
			СтрокаСправкаРасчет.КоэффициентАмортизацииНУ              = ВыборкаПоНМА.СпециальныйКоэффициент;
			СтрокаСправкаРасчет.СпособыОтраженияРасходовПоАмортизации = ВыборкаПоНМА.СпособыОтраженияРасходовПоАмортизации;
			СтрокаСправкаРасчет.ОбъемПродукцииРабот                   = ВыборкаПоНМА.Количество;
			СтрокаСправкаРасчет.ОбъемПродукцииРаботДляАмортизации     = ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации;
			
			Если ПоддержкаПБУ18 Тогда
				
				СтоимостьВРНачальныйОстаток = ВыборкаПоНМА.СтоимостьНачальныйОстаток
					- ВыборкаПоНМА.СтоимостьНУНачальныйОстаток
					- ВыборкаПоНМА.СтоимостьПРНачальныйОстаток;
					
				ОстаточнаяСтоимостьПР = ВыборкаПоНМА.СтоимостьПРНачальныйОстаток - ВыборкаПоНМА.АмортизацияПРНачальныйОстаток;
				ОстаточнаяСтоимостьВР = ОстаточнаяСтоимостьБУ - ОстаточнаяСтоимостьНУ - ОстаточнаяСтоимостьПР;
				
				СтрокаСправкаРасчет.СтоимостьПР               = ВыборкаПоНМА.СтоимостьПРНачальныйОстаток;
				СтрокаСправкаРасчет.СтоимостьВР               = СтоимостьВРНачальныйОстаток;
				СтрокаСправкаРасчет.ОстаточнаяСтоимостьПР     = ОстаточнаяСтоимостьПР;
				СтрокаСправкаРасчет.ОстаточнаяСтоимостьВР     = ОстаточнаяСтоимостьВР;
				СтрокаСправкаРасчет.СтоимостьДляАмортизацииПР = ВыборкаПоНМА.СтоимостьПРНачальныйОстаток;
				СтрокаСправкаРасчет.СуммаАмортизацииПР        = СуммаАмортизацииПР;
				СтрокаСправкаРасчет.СуммаАмортизацииВР        = СуммаАмортизацииВР;
			
			КонецЕсли;
	
		КонецЕсли;
			
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

Функция ПодготовитьПараметрыРасчетАмортизацииНМА(ТаблицаНМА, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаНМА

	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"         // <Число, 5, 0>
	+ "НематериальныйАктив"; // <СправочникСсылка.НематериальныйАктив>

	Параметры.Вставить("ТаблицаНМА",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"            // <Дата>
	+ "Организация,"       // <СправочникСсылка.Организации>
	+ "Регистратор,"       // <ДокументСсылка.*>
	+ "ВыдаватьСообщения"; // <Булево>

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

Функция ПодготовитьТаблицыАмортизацииНМА(ТаблицаНМА, ТаблицаРеквизиты, Отказ, Ошибки) Экспорт

	Параметры = ПодготовитьПараметрыАмортизацияНМА(ТаблицаНМА, ТаблицаРеквизиты);

	ПараметрыНачисленияАмортизации = Новый Структура;

	РасчетАмортизации = ПодготовитьРасчетАмортизацииНМА(Параметры.ТаблицаНМА, Параметры.Реквизиты);
	ТаблицаАмортизации = РасчетАмортизации.ТаблицаАмортизации;

	ПараметрыАмортизации = Новый Структура;
	ПараметрыАмортизации.Вставить("ТаблицаАмортизации", ТаблицаАмортизации);
	ПараметрыАмортизации.Вставить("СправкаРасчет", РасчетАмортизации.СправкаРасчет);
	ПараметрыАмортизации.Вставить("ТаблицаРеквизиты", Параметры.Реквизиты);
	ПараметрыНачисленияАмортизации.Вставить("Амортизация", ПараметрыАмортизации);
	
	УправлениеВнеоборотнымиАктивамиПереопределяемый.ПроверитьКорректностьДанныхРегламентнойОперации(
		Ошибки,
		ТаблицаАмортизации,
		Параметры.Реквизиты,
		Отказ);
		
	ТаблицаРаспределения = УправлениеВнеоборотнымиАктивамиПереопределяемый.НовыйТаблицаЗатрат();
	
	Если Не Отказ Тогда
		
		ТаблицаРаспределения =
			УправлениеВнеоборотнымиАктивамиПереопределяемый.ТаблицаРаспределенияАмортизацияПоНаправлениям(ТаблицаАмортизации);
	
	КонецЕсли;

	ПараметрыРаспределения = Новый Структура;
	ПараметрыРаспределения.Вставить("ТаблицаРеквизиты", Параметры.Реквизиты);
	ПараметрыРаспределения.Вставить("ТаблицаЗатрат", ТаблицаРаспределения);
	ПараметрыНачисленияАмортизации.Вставить("РаспределениеАмортизации", ПараметрыРаспределения);
	
	Возврат ПараметрыНачисленияАмортизации;

КонецФункции

Функция ПодготовитьПараметрыАмортизацияНМА(ТаблицаНМА, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаНМА

	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"         // <Число, 5, 0>
	+ "НематериальныйАктив"; // <СправочникСсылка.НематериальныйАктив>

	Параметры.Вставить("ТаблицаНМА",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"      // <Дата>
	+ "Организация," // <СправочникСсылка.Организации>
	+ "Регистратор," // <ДокументСсылка.*>
	+ "ИмяСписка,"   // <Строка>
	+ "ВыдаватьСообщения," // <Булево>
	+ "Содержание";  // <Строка, 150>

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

#Область РасчетАмортизации // С учетом ФСБУ 14, с 2024 года

Функция РассчитатьАмортизацию(Период, Организация, Регистратор, Отбор, ВыдаватьСообщения, РежимСправкиРасчета)

	Если ПрименяетсяПБУ14(Период) Тогда
		// Единый расчет в бухгалтерском и налоговом учете
		Возврат РассчитатьАмортизациюПБУ14(Период, Организация, Регистратор, Отбор, ВыдаватьСообщения, РежимСправкиРасчета);
	Иначе
		
		// Расчет, используемый с 2024 года - раздельный для бухгалтерского и налогового учета
		
		ОтборОбъектов = Отбор;
		
		ПериодРасчета = КонецМесяца(Период);
		Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.РегламентнаяОперация") Тогда
			// Отбор, как правило, не используется
			Если Не ЗначениеЗаполнено(ОтборОбъектов) Тогда
				ОтборОбъектов = Неопределено;
			КонецЕсли;
		Иначе
			// Выбытие актива
			ПериодРасчета = Новый МоментВремени(Период, Регистратор);
		КонецЕсли;
		
		РасчетБухгалтерскийУчет = АмортизацияАктивов.РассчитатьАмортизацию(
			ТипОбъектов(),
			ПериодРасчета,
			Организация,
			ОтборОбъектов);
		
		СчетаУчета = СчетаУчетаНМА();
		
		РасчетНалоговыйУчет = РассчитатьАмортизациюНалогНаПрибыль(ПериодРасчета, Организация, СчетаУчета, ОтборОбъектов);
		
		Возврат ОбъединитьРасчетАмортизации(РасчетБухгалтерскийУчет, РасчетНалоговыйУчет, ПериодРасчета, Организация, СчетаУчета);
		
	КонецЕсли;
	
КонецФункции

Функция НовыйРезультатРасчетаАмортизации()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ТаблицаАмортизации", ПолучитьПустуюТаблицуАмортизацииНМА());
	Результат.Вставить("Отказ",              Ложь);
	Результат.Вставить("СправкаРасчет",      УправлениеВнеоборотнымиАктивами.ПустаяСправкаРасчет("РасчетАмортизации"));
	Результат.СправкаРасчет.Колонки.Удалить("Период");
	
	Возврат Результат;
	
КонецФункции

Функция ОбъединитьРасчетАмортизации(РасчетБухгалтерскийУчет, РасчетНалоговыйУчет, Период, Организация, СчетаУчета)
	
	Результат = НовыйРезультатРасчетаАмортизации();
	
	// Получаем сведения для проводок и справки-расчета, не связанные с расчетом амортизации
	Объекты = РасчетБухгалтерскийУчет.ОценкаСтоимости.ВыгрузитьКолонку("Объект");
	Если РасчетНалоговыйУчет <> Неопределено Тогда
		ОбъектыНУ = РасчетНалоговыйУчет.ТаблицаАмортизации.ВыгрузитьКолонку("ОбъектУчета");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Объекты, ОбъектыНУ, Истина);
	КонецЕсли;
	
	Если ТипЗнч(Период) = Тип("МоментВремени") Тогда
		// Расчет при выбытии - не должен захватывать движения документа выбытия
		ДатаРасчета = Период.Дата;
		ГраницаРасчета = Новый Граница(Период, ВидГраницы.Исключая);
	Иначе
		// Расчет за месяц - должен захватывать месяц целиком
		ДатаРасчета = Период;
		ГраницаРасчета = Новый Граница(ДатаРасчета, ВидГраницы.Включая);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объекты",     Объекты);
	Запрос.УстановитьПараметр("Период",      ГраницаРасчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетаУчета",  СчетаУчета.Баланс);
	Запрос.Текст = ТекстЗапросаУчетныеДанные();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтандартныйСчетАмортизации = СтандартныйСчетАмортизации(СчетаУчета.Амортизация);
	ВидИспользованияСумм = БухгалтерскийУчет.ИспользоватьСуммыНалогНаПрибыль(Организация, ДатаРасчета);
	ИспользоватьСуммыРазниц = БухгалтерскийУчетКлиентСервер.ИспользоватьСуммыРазниц(ВидИспользованияСумм);
	
	Пока Выборка.Следующий() Цикл
		
		ЗаписьБУ = РасчетБухгалтерскийУчет.ОценкаСтоимости.Найти(Выборка.Объект, "Объект");
		Если РасчетНалоговыйУчет = Неопределено Тогда
			ЗаписьПроводкиНУ = Неопределено;
			ЗаписьРасчетНУ   = Неопределено;
		Иначе
			ЗаписьПроводкиНУ = РасчетНалоговыйУчет.ТаблицаАмортизации.Найти(Выборка.Объект, "ОбъектУчета");
			ЗаписьРасчетНУ   = РасчетНалоговыйУчет.СправкаРасчет.Найти(Выборка.Объект, "Объект");
		КонецЕсли;
		
		ЗаписьПроводкиОбщая = Результат.ТаблицаАмортизации.Добавить();
		ЗаписьРасчетОбщая = Результат.СправкаРасчет.Добавить();
		
		// В первую очередь заполняем данные НУ, так как ее коллекции содержат заведомо незаполненные поля для БУ
		Если ЗаписьПроводкиНУ <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЗаписьПроводкиОбщая, ЗаписьПроводкиНУ);
		КонецЕсли;
		
		Если ЗаписьРасчетНУ <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЗаписьРасчетОбщая, ЗаписьРасчетНУ);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЗаписьПроводкиОбщая, Выборка);
		ЗаписьПроводкиОбщая.СчетНачисленияАмортизации = Выборка.СчетАмортизации;
		Если Не ЗначениеЗаполнено(ЗаписьПроводкиОбщая.СчетНачисленияАмортизации) Тогда
			ЗаписьПроводкиОбщая.СчетНачисленияАмортизации = СтандартныйСчетАмортизации;
		КонецЕсли;
		
		ЗаписьРасчетОбщая.СпособыОтраженияРасходовПоАмортизации = ЗаписьПроводкиОбщая.НаправлениеАмортизации;
		
		ЗаполнитьЗначенияСвойств(ЗаписьРасчетОбщая, Выборка);
		ЗаполнитьЗначенияСвойств(ЗаписьРасчетОбщая, ЗаписьПроводкиОбщая);
		ЗаписьРасчетОбщая.ПериодРасчета = КонецМесяца(ДатаРасчета);
		ЗаписьРасчетОбщая.Организация   = Организация;
		
		Если ЗаписьБУ = Неопределено Тогда
			ЗаписьРасчетОбщая.ОграничениеРаздела = Перечисления.РазделыРасчетаАмортизации.ТолькоНалоговыйУчет;
		Иначе
			
			// Пояснения по проводкам корректировки обесценения см. в АмортизацияАктивов.СкорректироватьДоступноеОбесценение
			ЗаписьПроводкиОбщая.СуммаАмортизацииБУ = ЗаписьБУ.СуммаПроводки + ЗаписьБУ.КорректировкаОбесценения;
			ЗаписьПроводкиОбщая.КорректировкаОбесценения = ЗаписьБУ.КорректировкаОбесценения;
			
			КоличествоЗаписейРасчета = ЗаписьБУ.РасчетАмортизации.Количество();
			Если КоличествоЗаписейРасчета = 1 Тогда
				ЗаполнитьЗаписьСправкиРасчетаАмортизации(ЗаписьРасчетОбщая, ЗаписьБУ, ЗаписьБУ.РасчетАмортизации[0], Истина);
			ИначеЕсли КоличествоЗаписейРасчета = 0 Тогда
				ЗаполнитьЗаписьСправкиРасчетаАмортизации(ЗаписьРасчетОбщая, ЗаписьБУ, Неопределено, Истина);
			Иначе
				// Эта запись только для налогового учета
				ЗаписьРасчетОбщая.ОграничениеРаздела = Перечисления.РазделыРасчетаАмортизации.ТолькоНалоговыйУчет;
				// Для бухгалтерского создаем отдельные записи
				Основная = Истина;
				Для Каждого ЗаписьРасчетАмортизации Из ЗаписьБУ.РасчетАмортизации Цикл
					ЗаписьРасчетБухгалтерскийУчет = Результат.СправкаРасчет.Добавить();
					ЗаписьРасчетБухгалтерскийУчет.ПериодРасчета      = ЗаписьРасчетАмортизации.Начало;
					ЗаписьРасчетБухгалтерскийУчет.КонецПериодаЗатрат = ЗаписьРасчетАмортизации.Конец;
					ЗаписьРасчетБухгалтерскийУчет.ОграничениеРаздела = Перечисления.РазделыРасчетаАмортизации.ТолькоБухгалтерскийУчет;
					ЗаполнитьЗаписьСправкиРасчетаАмортизации(ЗаписьРасчетБухгалтерскийУчет, ЗаписьБУ, ЗаписьРасчетАмортизации, Основная);
					Основная = Ложь;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		РассчитатьРазницыПБУ18(ЗаписьПроводкиОбщая, ЗаписьРасчетОбщая, Выборка, ИспользоватьСуммыРазниц);
			
	КонецЦикла;
	
	// В отдельных случаях требуется разделить проводки в БУ и НУ:
	// НМА, признаваемый в бухгалтерском учете, может, по выбору организации, погашаться в налоговом учете
	// в специальном порядке, установленном п. 9 ст. 262 - в программе в этом случае принято,
	// что накопленная амортизация не отражается в учете.
	
	Отбор = Новый Структура;
	Отбор.Вставить("ВыбраноПогашениеСтоимостиНУ", Истина);
	ЗаписиПогашениеСтоимости = Результат.ТаблицаАмортизации.НайтиСтроки(Отбор);
	
	Для Каждого ЗаписьНУ Из ЗаписиПогашениеСтоимости Цикл
		
		Если ЗаписьНУ.СуммаАмортизацииБУ <> 0 И ЗаписьНУ.СуммаАмортизацииНУ <> 0 Тогда
			
			ИндексЗаписиБУ = Результат.ТаблицаАмортизации.Индекс(ЗаписьНУ); // БУ ставим перед НУ
			ЗаписьБУ = Результат.ТаблицаАмортизации.Вставить(ИндексЗаписиБУ);
			ЗаполнитьЗначенияСвойств(ЗаписьБУ, ЗаписьНУ, , "СуммаАмортизацииБУ,СуммаАмортизацииНУ,СуммаАмортизацииПР,СуммаАмортизацииВР");
			ЗаписьНУ.КорректировкаОбесценения = 0; // применима только в БУ
			
			// ЗаписьНУ содержит два комплекта сумм: БУ и НУ.
			// Перенесем из нее суммы, которые не относятся к НУ (БУ и ПР), сопровождая это корректировкой ВР по формуле БУ = НУ+ПР+ВР
			ПереносимаяСумма = ЗаписьНУ.СуммаАмортизацииБУ;
			ЗаписьБУ.СуммаАмортизацииБУ = ЗаписьБУ.СуммаАмортизацииБУ + ПереносимаяСумма;
			ЗаписьБУ.СуммаАмортизацииВР = ЗаписьБУ.СуммаАмортизацииВР + ПереносимаяСумма;
			ЗаписьНУ.СуммаАмортизацииБУ = ЗаписьНУ.СуммаАмортизацииБУ - ПереносимаяСумма;
			ЗаписьНУ.СуммаАмортизацииВР = ЗаписьНУ.СуммаАмортизацииВР - ПереносимаяСумма;
			
			ПереносимаяСумма = ЗаписьНУ.СуммаАмортизацииПР;
			ЗаписьБУ.СуммаАмортизацииПР = ЗаписьБУ.СуммаАмортизацииПР + ПереносимаяСумма;
			ЗаписьБУ.СуммаАмортизацииВР = ЗаписьБУ.СуммаАмортизацииВР - ПереносимаяСумма;
			ЗаписьНУ.СуммаАмортизацииПР = ЗаписьНУ.СуммаАмортизацииПР - ПереносимаяСумма;
			ЗаписьНУ.СуммаАмортизацииВР = ЗаписьНУ.СуммаАмортизацииВР + ПереносимаяСумма;
			
		КонецЕсли;
		
		ЗаписьНУ.СчетНачисленияАмортизации = ЗаписьНУ.СчетУчета;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьЗаписьСправкиРасчетаАмортизации(СправкаРасчет, ОценкаСтоимости, РасчетАмортизации, Основная)
	
	СправкаРасчет.НачислятьАмортизациюБУ        = ОценкаСтоимости.ПодлежитАмортизации;
	СправкаРасчет.СпособНачисленияАмортизацииБУ = ОценкаСтоимости.СпособАмортизацииЛокальный;
	СправкаРасчет.ЛиквидационнаяСтоимость       = ОценкаСтоимости.ЛиквидационнаяСтоимость;
	
	Если РасчетАмортизации = Неопределено Тогда
		СправкаРасчет.СтоимостьБУ                   = ОценкаСтоимости.ЗатратыВсего;
		СправкаРасчет.ОстаточнаяСтоимостьБУ         = ОценкаСтоимости.БалансоваяСтоимость;
		СправкаРасчет.СтоимостьДляАмортизацииБУ     = ОценкаСтоимости.АмортизируемаяВеличина;
		СправкаРасчет.СуммаАмортизацииБУ            = ОценкаСтоимости.Амортизация;
		СправкаРасчет.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный; // справка-расчет требует заполнения поля
		Возврат;
	КонецЕсли;
	
	СправкаРасчет.СтоимостьБУ                   = РасчетАмортизации.ЗатратыВсего;
	СправкаРасчет.ОстаточнаяСтоимостьБУ         = РасчетАмортизации.БалансоваяСтоимость;
	СправкаРасчет.СтоимостьДляАмортизацииБУ     = РасчетАмортизации.АмортизируемаяВеличина;
	СправкаРасчет.СуммаАмортизацииБУ            = РасчетАмортизации.Амортизация;
	
	Если Основная Тогда
		СправкаРасчет.Обесценение = ОценкаСтоимости.ДоступноеОбесценение;
		// Пояснения по проводкам корректировки обесценения см. в АмортизацияАктивов.СкорректироватьДоступноеОбесценение
		СправкаРасчет.КорректировкаОбесценения = ОценкаСтоимости.КорректировкаОбесценения;
		СправкаРасчет.СуммаАмортизацииБУ = СправкаРасчет.СуммаАмортизацииБУ + СправкаРасчет.КорректировкаОбесценения;
	КонецЕсли;
	
	РегистрыСведений.РасчетАмортизации.УстановитьНеполныйПериодЗаписи(
		СправкаРасчет,
		РасчетАмортизации.Начало,
		РасчетАмортизации.Конец);
		
	СправкаРасчет.ДатаВводаВЭксплуатацию = ОценкаСтоимости.ДатаПринятияКУчету;
		
	Если ОценкаСтоимости.СпособАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.Линейный Тогда
		СправкаРасчет.ОстатокСрокаПолезногоИспользованияБУ = РасчетАмортизации.СрокИспользованияОставшийся;
		СправкаРасчет.СрокПолезногоИспользованияБУ         = РасчетАмортизации.ОбщийРесурсМесяцев;
	ИначеЕсли ОценкаСтоимости.СпособАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции Тогда
		СправкаРасчет.ОстатокОбъемаПродукции            = РасчетАмортизации.РесурсОставшийся;
		СправкаРасчет.ОбъемПродукцииРаботДляАмортизации = РасчетАмортизации.РесурсОставшийся;
		СправкаРасчет.ОбъемПродукцииРабот               = РасчетАмортизации.РесурсИспользованный;
	ИначеЕсли ОценкаСтоимости.СпособАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка Тогда
		СправкаРасчет.ОстатокСрокаПолезногоИспользованияБУ = РасчетАмортизации.СрокИспользованияОставшийся;
		СправкаРасчет.СрокПолезногоИспользованияБУ         = РасчетАмортизации.ОбщийРесурсМесяцев;
		СправкаРасчет.КоэффициентУскорения                 = РасчетАмортизации.КоэффициентУскорения;
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьРазницыПБУ18(ЗаписьПроводки, ЗаписьРасчет, ДанныеПостоянныхРазниц, ИспользоватьСуммыРазниц)
	
	Если Не ИспользоватьСуммыРазниц Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеВнеоборотнымиАктивами.РассчитатьПостоянныеРазницы(
		ЗаписьРасчет,
		ЗаписьПроводки.СуммаАмортизацииБУ,
		ДанныеПостоянныхРазниц);
	ЗаполнитьЗначенияСвойств(ЗаписьПроводки, ЗаписьРасчет, "СуммаАмортизацииВР, СуммаАмортизацииПР");
	
КонецПроцедуры

Функция СтандартныйСчетАмортизации(СчетаАмортизации)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетаАмортизации", СчетаАмортизации);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Хозрасчетный.Ссылка КАК Ссылка,
	|	Хозрасчетный.ЗапретитьИспользоватьВПроводках КАК ЗапретитьИспользоватьВПроводках,
	|	Хозрасчетный.Порядок КАК Порядок
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В(&СчетаАмортизации)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗапретитьИспользоватьВПроводках,
	|	Порядок,
	|	Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат ПланыСчетов.Хозрасчетный.АмортизацияНематериальныхАктивов;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьПустуюТаблицуАмортизацииНМА()
	
	ТаблицаАмортизации = Новый ТаблицаЗначений();

	ТаблицаАмортизации.Колонки.Добавить("ОбъектУчета",                                Новый ОписаниеТипов("СправочникСсылка.НематериальныеАктивы"));
	ТаблицаАмортизации.Колонки.Добавить("СчетУчета",                                  Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаАмортизации.Колонки.Добавить("СчетНачисленияАмортизации",                  Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаАмортизации.Колонки.Добавить("НаправлениеАмортизации",                     Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияРасходовПоАмортизации, СправочникСсылка.НазначенияИспользованияНМА"));
	ТаблицаАмортизации.Колонки.Добавить("ВыбраноПогашениеСтоимостиНУ",                Новый ОписаниеТипов("Булево"));// п. 9 ст. 262
	ТаблицаАмортизации.Колонки.Добавить("СуммаАмортизацииБУ",                         ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаАмортизации.Колонки.Добавить("СуммаАмортизацииНУ",                         ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаАмортизации.Колонки.Добавить("СуммаАмортизацииВР",                         ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаАмортизации.Колонки.Добавить("СуммаАмортизацииПР",                         ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаАмортизации.Колонки.Добавить("КорректировкаОбесценения",                   ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаАмортизации.Колонки.Добавить("Подразделение",                              БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	ТаблицаАмортизации.Колонки.Добавить("НомерСтроки",                                ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
	
	Возврат ТаблицаАмортизации;
	
КонецФункции

#Область НалогНаПрибыль

Функция РассчитатьАмортизациюНалогНаПрибыль(Период, Организация, СчетаУчета, Отбор = Неопределено)
	
	Если ТипЗнч(Период) = Тип("МоментВремени") Тогда
		ДатаРасчета = Период.Дата;
		КонецПериодаРасчета = Новый Граница(Период, ВидГраницы.Исключая);
	Иначе
		ДатаРасчета = Период;
		КонецПериодаРасчета = КонецМесяца(ДатаРасчета) + 1;
	КонецЕсли;
	
	Если Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, ДатаРасчета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = НовыйРезультатРасчетаАмортизации();
	// Поля ОбъектУчета, Объект в Результат - ключевые:
	// - обеспечивается их уникальность в таблице
	// - после заполнения по ним создаются индексы
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация",         Организация);
	Запрос.УстановитьПараметр("НачалоПериода",       Новый Граница(НачалоМесяца(ДатаРасчета), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ДатаРасчета",         ДатаРасчета);
	Запрос.УстановитьПараметр("КонецПериодаРасчета", КонецПериодаРасчета);
	Запрос.УстановитьПараметр("СчетаАктивов",             СчетаУчета.Активы);
	Запрос.УстановитьПараметр("СчетаАмортизации",         СчетаУчета.Амортизация);
	Запрос.УстановитьПараметр("СчетаБалансовойСтоимости", СчетаУчета.Баланс);
	
	// Получим список нематериальных активов для расчета амортизации - переиспользуем код для ПБУ 14
	Если Отбор = Неопределено Тогда
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАВсе();
	Иначе
		ТаблицаНМА = Новый ТаблицаЗначений;
		ТаблицаНМА.Колонки.Добавить("НематериальныйАктив", Новый ОписаниеТипов("СправочникСсылка.НематериальныеАктивы"));
		Для Каждого Объект Из Отбор Цикл
			ТаблицаНМА.Добавить().НематериальныйАктив = Объект;
		КонецЦикла;
		Запрос.УстановитьПараметр("ТаблицаНМА", ТаблицаНМА);
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАПоТаблице();
	КонецЕсли;
	Запрос.Выполнить();
	
	// Амортизация линейным методом в налоговом учете ведется, как правило, исходя из первоначальной стоимости объекта
	// (суммы затрат на создание) и срока полезного использования, отсчитываемого от даты его создания.
	//
	// Однако, есть исключения.
	// Так, по основным средствам, которые ранее амортизировались нелинейным методом,
	// расчет ведется исходя из стоимости, сформировавшейся на дату перехода (формула в п. 1 ст. 257 НК РФ)
	// и срока от даты перехода на линейный метод (п. 13 ст. 259.2).
	//
	// При применении этих положений принимается во внимание, что до 2009 года нелинейным назывался
	// существенно другой метод амортизации (изменения внесены законами от 22.07.2008 № 158-ФЗ
	// и от 26.11.2008 № 224-ФЗ). Отсылки в коде к 2009 году связаны именно с этим.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сведения.НематериальныйАктив КАК Объект,
	|	Сведения.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимость,
	|	Сведения.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	Сведения.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	Сведения.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	Сведения.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009
	|ПОМЕСТИТЬ ПервоначальныеСведения
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК Сведения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БалансоваяСтоимость.Субконто1 КАК Объект,
	|	СУММА(ВЫБОР
	|			КОГДА БалансоваяСтоимость.Счет В (&СчетаАктивов)
	|				ТОГДА ЕСТЬNULL(БалансоваяСтоимость.СуммаНУОстатокДт, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПервоначальнаяСтоимость,
	|	СУММА(ЕСТЬNULL(БалансоваяСтоимость.СуммаНУОстаток, 0)) КАК ОстаточнаяСтоимость
	|ПОМЕСТИТЬ СтоимостьНачалоПериода
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоПериода,
	|			Счет В (&СчетаБалансовойСтоимости),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК БалансоваяСтоимость
	|
	|СГРУППИРОВАТЬ ПО
	|	БалансоваяСтоимость.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БалансоваяСтоимость.Субконто1 КАК Объект,
	|	СУММА(ЕСТЬNULL(БалансоваяСтоимость.СуммаНУОстаток, 0)) КАК ОстаточнаяСтоимость
	|ПОМЕСТИТЬ СтоимостьКонецПериода
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&КонецПериодаРасчета,
	|			Счет В (&СчетаБалансовойСтоимости),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК БалансоваяСтоимость
	|
	|СГРУППИРОВАТЬ ПО
	|	БалансоваяСтоимость.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	События.НематериальныйАктив КАК Объект,
	|	МАКСИМУМ(НАЧАЛОПЕРИОДА(События.Период, ДЕНЬ)) КАК Дата
	|ПОМЕСТИТЬ НачалоЭксплуатации
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций КАК События
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокНМА КАК СписокНМА
	|		ПО События.НематериальныйАктив = СписокНМА.НематериальныйАктив
	|ГДЕ
	|	События.Период <= &ДатаРасчета
	|	И События.Организация = &Организация
	|	И События.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	События.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.Субконто1 КАК Объект,
	|	СУММА(Амортизация.СуммаНУОборотКт) КАК Сумма
	|ПОМЕСТИТЬ АмортизацияПредварительная
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаРасчета,
	|			,
	|			Счет В (&СчетаАмортизации),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА),
	|			,
	|			) КАК Амортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	Амортизация.Субконто1
	|
	|ИМЕЮЩИЕ
	|	СУММА(Амортизация.СуммаНУОборотКт) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпециальныйКоэффициентАмортизации.НематериальныйАктив КАК Объект,
	|	СпециальныйКоэффициентАмортизации.СпециальныйКоэффициент КАК СпециальныйКоэффициент
	|ПОМЕСТИТЬ СпециальныйКоэффициентАмортизации
	|ИЗ
	|	РегистрСведений.НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК СпециальныйКоэффициентАмортизации
	|ГДЕ
	|	СпециальныйКоэффициентАмортизации.СпециальныйКоэффициент > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект";
	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("ДатаПереходаКЛинейномуМетоду", УправлениеВнеоборотнымиАктивами.ПолучитьДатуПереходаКЛинейномуМетоду(ДатаРасчета, Организация));
	
	ОбщийМетодНачисленияАмортизации = УчетнаяПолитика.МетодНачисленияАмортизацииНУ(Организация, ДатаРасчета);
	
	ДатаОтказаНелинейныйМетод = '0001-01-01';
	Если ОбщийМетодНачисленияАмортизации <> Перечисления.МетодыНачисленияАмортизации.Нелинейный Тогда
		ДатаОтказаНелинейныйМетод = УправлениеВнеоборотнымиАктивами.ПолучитьДатуПереходаКЛинейномуМетоду(
			ДатаРасчета,
			Организация,
			Истина);
	КонецЕсли;
	
	СоздатьКорректировкаПереходаНаЛинейныйМетод(
		МенеджерВременныхТаблиц,
		ДатаОтказаНелинейныйМетод,
		Организация,
		СчетаУчета.Амортизация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокНМА.НематериальныйАктив КАК Объект,
	|	СписокНМА.НематериальныйАктив.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	СписокНМА.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР) КАК ВыбраноПогашениеСтоимости,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимостьЗафиксированная,
	|	ПервоначальныеСведения.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведения.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	НачалоЭксплуатации.Дата КАК ДатаНачалаЭксплуатации,
	|	СтоимостьНачалоПериода.ПервоначальнаяСтоимость КАК СуммаЗатрат,
	|	СтоимостьНачалоПериода.ОстаточнаяСтоимость КАК ОстаточнаяСтоимостьНачалоМесяца,
	|	СтоимостьКонецПериода.ОстаточнаяСтоимость КАК ОстаточнаяСтоимостьКонецМесяца,
	|	ЕСТЬNULL(АмортизацияПредварительная.Сумма, 0) КАК АмортизацияПредварительная,
	|	ЕСТЬNULL(СпециальныйКоэффициентАмортизации.СпециальныйКоэффициент, 0) КАК СпециальныйКоэффициент,
	|	ЕСТЬNULL(КорректировкаПереходаНаЛинейныйМетод.Амортизация, 0) КАК КорректировкаАмортизацииПереходНаЛинейныйМетод,
	|	ПервоначальныеСведения.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведения.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009
	|ИЗ
	|	СписокНМА КАК СписокНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПервоначальныеСведения КАК ПервоначальныеСведения
	|		ПО СписокНМА.НематериальныйАктив = ПервоначальныеСведения.Объект
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтоимостьНачалоПериода КАК СтоимостьНачалоПериода
	|		ПО СписокНМА.НематериальныйАктив = СтоимостьНачалоПериода.Объект
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтоимостьКонецПериода КАК СтоимостьКонецПериода
	|		ПО СписокНМА.НематериальныйАктив = СтоимостьКонецПериода.Объект
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачалоЭксплуатации КАК НачалоЭксплуатации
	|		ПО СписокНМА.НематериальныйАктив = НачалоЭксплуатации.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияПредварительная КАК АмортизацияПредварительная
	|		ПО СписокНМА.НематериальныйАктив = АмортизацияПредварительная.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпециальныйКоэффициентАмортизации КАК СпециальныйКоэффициентАмортизации
	|		ПО СписокНМА.НематериальныйАктив = СпециальныйКоэффициентАмортизации.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорректировкаПереходаНаЛинейныйМетод КАК КорректировкаПереходаНаЛинейныйМетод
	|		ПО СписокНМА.НематериальныйАктив = КорректировкаПереходаНаЛинейныйМетод.Объект";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КонецМесяцаРасчета = КонецМесяца(ДатаРасчета);
	ПрошлыйПериод = НачалоМесяца(ДатаРасчета) - 1;
	
	Пока Выборка.Следующий() Цикл
		
		МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный;
		АмортизируемаяВеличина     = Выборка.СуммаЗатрат;
		СрокПолезногоИспользования = Выборка.СрокПолезногоИспользования;
		НачалоАмортизации          = КонецМесяца(Выборка.ДатаНачалаЭксплуатации) + 1; // п. 4 ст. 259 НК
		
		Если Выборка.ВыбраноПогашениеСтоимости Тогда
			// п. 9 ст. 262 - вместо амортизации предприятие может выбрать равномерное списание,
			// что по сути - амортизация линейным методом.
			// Однако, техника учета в этом случае отличается: 
			// на счетах учета хранится только остаточная стоимость, но не первоначальная.
			АмортизируемаяВеличина = Выборка.ПервоначальнаяСтоимостьЗафиксированная;
		Иначе
			МетодНачисленияАмортизации = ПрименяемыйМетодНачисленияАмортизации(
				ОбщийМетодНачисленияАмортизации,
				Выборка.АмортизационнаяГруппа);
				
			Если МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный Тогда
				
				АмортизируемаяВеличина = Выборка.ОстаточнаяСтоимостьНачалоМесяца;
				
			ИначеЕсли ЗначениеЗаполнено(ДатаОтказаНелинейныйМетод) Тогда
				
				// Амортизируемую величину и срок корректируем, исключив период применения нелинейного метода
				Если ДатаОтказаНелинейныйМетод <= '2009-01-01' Тогда
					АмортизируемаяВеличина     = АмортизируемаяВеличина     - Выборка.АмортизацияДо2009;
					СрокПолезногоИспользования = СрокПолезногоИспользования - Выборка.ФактическийСрокИспользованияДо2009;
				Иначе
					АмортизируемаяВеличина = АмортизируемаяВеличина - Выборка.КорректировкаАмортизацииПереходНаЛинейныйМетод;
					Если ДатаОтказаНелинейныйМетод > НачалоАмортизации Тогда
						СрокПолезногоИспользования = СрокПолезногоИспользования
							- АмортизацияАктивов.РазностьМесяцев(ДатаОтказаНелинейныйМетод, НачалоАмортизации);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		НормаАмортизации = 1;
		Если Не Выборка.НачислятьАмортизацию Тогда
			НормаАмортизации = 0;
		ИначеЕсли МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный Тогда
			// В программе учет амортизации нелинейным методом выполняется по каждому объекту,
			// однако так, чтобы результат по группе в целом соответствовал требованиям НК.
			НормаАмортизации = УправлениеВнеоборотнымиАктивами.ПолучитьНормуПоАмортизационнойГруппе(Выборка.АмортизационнаяГруппа);
			Если НормаАмортизации = Неопределено Тогда
				НормаАмортизации = 0;
			КонецЕсли;
		Иначе // Линейный
			СрокПолезногоИспользования = Макс(1, СрокПолезногоИспользования);
			НормаАмортизации = 1 / СрокПолезногоИспользования * 100; // п. 2 ст. 259.1 НК РФ
		КонецЕсли;
		
		СуммаАмортизации = АмортизируемаяВеличина * НормаАмортизации / 100;
		
		// Специальные коэффициенты предусмотрены ст. 259.3 НК РФ
		Если Выборка.СпециальныйКоэффициент <> 0 И Выборка.СпециальныйКоэффициент <> 1 Тогда
			СуммаАмортизации = СуммаАмортизации * Выборка.СпециальныйКоэффициент;
		КонецЕсли;
		
		СуммаАмортизации = Окр(СуммаАмортизации, БухгалтерскийУчетКлиентСервер.РазрядностьДробнойЧастиСумм());
		СуммаАмортизации = Макс(0, Мин(СуммаАмортизации, Выборка.ОстаточнаяСтоимостьКонецМесяца));
		
		//Данные для справки-расчета
		Расчет = Результат.СправкаРасчет.Добавить();
		Расчет.ПериодРасчета = КонецМесяцаРасчета;
		Расчет.Объект        = Выборка.Объект;
		Расчет.НачислятьАмортизациюНУ        = Выборка.НачислятьАмортизацию;
		Расчет.СпособНачисленияАмортизацииНУ = МетодНачисленияАмортизации;
		Расчет.АмортизационнаяГруппа         = Выборка.АмортизационнаяГруппа;
		Расчет.ДатаВводаВЭксплуатацию        = Выборка.ДатаНачалаЭксплуатации;
		Расчет.СрокПолезногоИспользованияНУ  = СрокПолезногоИспользования;
		
		Расчет.СтоимостьНУ               = Выборка.СуммаЗатрат;
		Расчет.ОстаточнаяСтоимостьНУ     = Выборка.ОстаточнаяСтоимостьНачалоМесяца;
		Расчет.СтоимостьДляАмортизацииНУ = АмортизируемаяВеличина;
		Расчет.НормаАмортизации          = НормаАмортизации;
		Расчет.КоэффициентАмортизацииНУ  = Выборка.СпециальныйКоэффициент;
		Расчет.СуммаАмортизацииНУ        = СуммаАмортизации;
		
		РанееПрошедшийСрокАмортизации = АмортизацияАктивов.РазностьМесяцев(ПрошлыйПериод, Выборка.ДатаНачалаЭксплуатации); // до начала этого месяца
		Расчет.ОстатокСрокаПолезногоИспользованияНУ = Макс(0, Выборка.СрокПолезногоИспользования - РанееПрошедшийСрокАмортизации);
		
		// Данные для проводок
		СуммаПроводки = Макс(0, СуммаАмортизации - Выборка.АмортизацияПредварительная);
		Если СуммаПроводки > 0 Тогда
			СтрокаАмортизации = Результат.ТаблицаАмортизации.Добавить();
			СтрокаАмортизации.ОбъектУчета        = Выборка.Объект;
			СтрокаАмортизации.СуммаАмортизацииНУ = СуммаПроводки;
			Если Выборка.ВыбраноПогашениеСтоимости Тогда
				СтрокаАмортизации.ВыбраноПогашениеСтоимостиНУ = Истина;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Результат.ТаблицаАмортизации.Индексы.Добавить("ОбъектУчета");
	Результат.СправкаРасчет.Индексы.Добавить("Объект");
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьКорректировкаПереходаНаЛинейныйМетод(МенеджерВременныхТаблиц, ДатаПерехода, Организация, СчетаАмортизации)
	
	Если Не ЗначениеЗаполнено(ДатаПерехода) Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка) КАК Объект,
		|	0 КАК Амортизация
		|ПОМЕСТИТЬ КорректировкаПереходаНаЛинейныйМетод
		|ГДЕ
		|	ЛОЖЬ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ПереходЛинейныйМетод.Субконто1 КАК Справочник.НематериальныеАктивы), ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка)) КАК Объект,
		|	СУММА(ЕСТЬNULL(ПереходЛинейныйМетод.СуммаНУОстатокКт, 0)) КАК Амортизация
		|ПОМЕСТИТЬ КорректировкаПереходаНаЛинейныйМетод
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ДатаПерехода,
		|			Счет В (&СчетаАмортизации),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
		|			Организация = &Организация
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						СписокНМА.НематериальныйАктив
		|					ИЗ
		|						СписокНМА КАК СписокНМА)) КАК ПереходЛинейныйМетод
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ПереходЛинейныйМетод.Субконто1 КАК Справочник.НематериальныеАктивы), ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаПерехода",     ДатаПерехода);
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("СчетаАмортизации", СчетаАмортизации);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ПрименяемыйМетодНачисленияАмортизации(ОбщийМетод, АмортизационнаяГруппа)
	
	Если ОбщийМетод <> Перечисления.МетодыНачисленияАмортизации.Нелинейный Тогда
		Возврат Перечисления.МетодыНачисленияАмортизации.Линейный;
	КонецЕсли;
	
	Если АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ВосьмаяГруппа
		Или АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДевятаяГруппа
		Или АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДесятаяГруппа
		Или АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ОтдельнаяГруппа Тогда
		
		// п. 3 ст. 259 НК РФ
		
		Возврат Перечисления.МетодыНачисленияАмортизации.Линейный;
		
	КонецЕсли;
	
	Возврат ОбщийМетод;
	
КонецФункции

#КонецОбласти

#КонецОбласти

// ВЫБЫТИЕ НМА

Функция ПодготовитьТаблицыСведенийПоВыбытиюНМА(ТаблицаНМА, ТаблицаРеквизиты, Отказ, ВыдаватьСообщения = Ложь) Экспорт
	
	// параметр ВыдаватьСообщения не используется, предполагается к удалению

	Параметры = ПодготовитьПараметрыПодготовитьТаблицыСведенийПоВыбытиюНМА(ТаблицаНМА, ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];

	// Проинициализируем структуру, чтобы в случае "досрочного" выхода в структуре были все таблицы, хоть и пустые
	ПараметрыВыбытия = ПолучитьИнициализированнуюСтруктуруПараметровВыбытия();

	Параметры.Реквизиты.Колонки.Добавить("ВыдаватьСообщения", Новый ОписаниеТипов("Булево"));
	Параметры.Реквизиты.ЗаполнитьЗначения(Ложь, "ВыдаватьСообщения");
	Если Параметры.ТаблицаНМА.Количество() = 0 Тогда
		ТаблицаАмортизации = ПолучитьПустуюТаблицуАмортизацииНМА();
	Иначе
		РасчетАмортизации = ПодготовитьРасчетАмортизацииНМА(Параметры.ТаблицаНМА, Параметры.Реквизиты);
		ТаблицаАмортизации = РасчетАмортизации.ТаблицаАмортизации;
	КонецЕсли;

	ТаблицаЗатрат = УправлениеВнеоборотнымиАктивамиПереопределяемый.ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл(
		ТаблицаАмортизации,
		Параметры.Реквизиты,
		Отказ);
		
	Если Отказ Тогда
		Возврат ПараметрыВыбытия;
	КонецЕсли;

	ПараметрыНачисленияАмортизации = Новый Структура;
	ПараметрыНачисленияАмортизации.Вставить("ТаблицаЗатрат", ТаблицаЗатрат);
	ПараметрыНачисленияАмортизации.Вставить("ТаблицаРеквизиты", Параметры.Реквизиты);
	ПараметрыНачисленияАмортизации.Вставить("СправкаРасчет", РасчетАмортизации.СправкаРасчет);

	// Параметры для процедуры СформироватьДвиженияНачислениеАмортизации
	ПараметрыВыбытия.Вставить("НачислениеАмортизации", ПараметрыНачисленияАмортизации);

	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Дата", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("ТаблицаНМА", Параметры.ТаблицаНМА);
	Запрос.УстановитьПараметр("СчетНачисленияАмортизацииПоУмолчанию", ПланыСчетов.Хозрасчетный.АмортизацияНематериальныхАктивов);
	СчетаУчетаНМА = СчетаУчетаНМА();
	Запрос.УстановитьПараметр("СчетаАктивов", СчетаУчетаНМА.Активы);
	Запрос.УстановитьПараметр("СчетаАмортизации", СчетаУчетаНМА.Амортизация);
	Запрос.УстановитьПараметр("СчетОбесценения", ПланыСчетов.Хозрасчетный.ОбесценениеНематериальныхАктивов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	&ТаблицаНМА КАК ТаблицаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СчетаУчетаНМА.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА СчетаУчетаНМА.СчетНачисленияАмортизации = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА &СчетНачисленияАмортизацииПоУмолчанию
	|		ИНАЧЕ СчетаУчетаНМА.СчетНачисленияАмортизации
	|	КОНЕЦ КАК СчетНачисленияАмортизации
	|ПОМЕСТИТЬ СчетаУчетаНМА
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|			&Дата,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						ТаблицаНМА.НематериальныйАктив
	|					ИЗ
	|						ТаблицаНМА КАК ТаблицаНМА)) КАК СчетаУчетаНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаУчетаНМА.СчетУчета КАК Счет
	|ПОМЕСТИТЬ РазличныеСчетаУчетаНМА
	|ИЗ
	|	СчетаУчетаНМА КАК СчетаУчетаНМА
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаУчетаНМА.СчетНачисленияАмортизации
	|ИЗ
	|	СчетаУчетаНМА КАК СчетаУчетаНМА
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Хозрасчетный.Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В (&СчетаАктивов, &СчетаАмортизации, &СчетОбесценения)";
	
	Запрос.Выполнить();

	Если ТранзакцияАктивна() Тогда
		// Блокировка регистра бухгалтерии.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Хозрасчетный");
		ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
		ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, КонецМесяца(Реквизиты.Период)));
		
		Запрос.Текст = "ВЫБРАТЬ РазличныеСчетаУчетаНМА.Счет ИЗ РазличныеСчетаУчетаНМА";
		ЭлементБлокировки.ИсточникДанных = Запрос.Выполнить();
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Счет", "Счет");
		
		Блокировка.Заблокировать();
		
	КонецЕсли;

	Запрос.УстановитьПараметр("ВидСубконтоНМА", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	Запрос.УстановитьПараметр("ТаблицаАмортизации", ТаблицаАмортизации);
	Запрос.УстановитьПараметр("СчетВыбытия", ПланыСчетов.Хозрасчетный.ВыбытиеНематериальныхАктивов);
	Запрос.УстановитьПараметр("СчетСписания", Реквизиты.СчетСписания);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаАмортизации.ОбъектУчета КАК ОбъектУчета,
	|	ТаблицаАмортизации.СчетНачисленияАмортизации КАК СчетУчета,
	|	ТаблицаАмортизации.Подразделение КАК Подразделение,
	|	ТаблицаАмортизации.СуммаАмортизацииБУ КАК СуммаАмортизацииБУ,
	|	ТаблицаАмортизации.СуммаАмортизацииНУ КАК СуммаАмортизацииНУ,
	|	ТаблицаАмортизации.СуммаАмортизацииПР КАК СуммаАмортизацииПР,
	|	ТаблицаАмортизации.СуммаАмортизацииВР КАК СуммаАмортизацииВР,
	|	ТаблицаАмортизации.КорректировкаОбесценения КАК КорректировкаОбесценения
	|ПОМЕСТИТЬ ТаблицаАмортизации
	|ИЗ
	|	&ТаблицаАмортизации КАК ТаблицаАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтоимостьНМА.Субконто1 КАК НематериальныйАктив,
	|	СтоимостьНМА.Счет КАК Счет,
	|	ЕСТЬNULL(СтоимостьНМА.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	СтоимостьНМА.СуммаОстаток КАК СуммаОстаток,
	|	СтоимостьНМА.СуммаНУОстаток КАК СуммаНУОстаток,
	|	СтоимостьНМА.СуммаПРОстаток КАК СуммаПРОстаток,
	|	ВЫБОР
	|		КОГДА СтоимостьНМА.Счет = &СчетОбесценения
	|			ТОГДА СтоимостьНМА.СуммаОстаток
	|		ИНАЧЕ СтоимостьНМА.СуммаВРОстаток
	|	КОНЕЦ КАК СуммаВРОстаток
	|ПОМЕСТИТЬ СтоимостьНМА
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В
	|				(ВЫБРАТЬ
	|					РазличныеСчетаУчетаНМА.Счет
	|				ИЗ
	|					РазличныеСчетаУчетаНМА КАК РазличныеСчетаУчетаНМА),
	|			&ВидСубконтоНМА,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ТаблицаНМА.НематериальныйАктив
	|					ИЗ
	|						ТаблицаНМА КАК ТаблицаНМА)) КАК СтоимостьНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаАмортизации.ОбъектУчета,
	|	ТаблицаАмортизации.СчетУчета,
	|	ТаблицаАмортизации.Подразделение,
	|	СУММА(-ТаблицаАмортизации.СуммаАмортизацииБУ),
	|	СУММА(-ТаблицаАмортизации.СуммаАмортизацииНУ),
	|	СУММА(-ТаблицаАмортизации.СуммаАмортизацииПР),
	|	СУММА(-ТаблицаАмортизации.СуммаАмортизацииВР)
	|ИЗ
	|	ТаблицаАмортизации КАК ТаблицаАмортизации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаАмортизации.ОбъектУчета,
	|	ТаблицаАмортизации.СчетУчета,
	|	ТаблицаАмортизации.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаАмортизации.ОбъектУчета,
	|	&СчетОбесценения,
	|	ТаблицаАмортизации.Подразделение,
	|	СУММА(ТаблицаАмортизации.КорректировкаОбесценения),
	|	0,
	|	0,
	|	СУММА(ТаблицаАмортизации.КорректировкаОбесценения)
	|ИЗ
	|	ТаблицаАмортизации КАК ТаблицаАмортизации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаАмортизации.Подразделение,
	|	ТаблицаАмортизации.ОбъектУчета
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаАмортизации.КорректировкаОбесценения) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СтоимостьНМА.Подразделение КАК Подразделение,
	|	&СчетВыбытия КАК СчетДт,
	|	СтоимостьНМА.Счет КАК СчетКт,
	|	СУММА(СтоимостьНМА.СуммаОстаток) КАК СуммаБУ,
	|	СУММА(СтоимостьНМА.СуммаНУОстаток) КАК СуммаНУ,
	|	СУММА(СтоимостьНМА.СуммаПРОстаток) КАК СуммаПР,
	|	СУММА(СтоимостьНМА.СуммаВРОстаток) КАК СуммаВР,
	|	ЛОЖЬ КАК ЭтоПроводкаСписанияСтоимости
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтоимостьНМА КАК СтоимостьНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = СтоимостьНМА.НематериальныйАктив
	|ГДЕ
	|	СтоимостьНМА.Счет В(&СчетаАктивов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНМА.НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив,
	|	СтоимостьНМА.Счет,
	|	СтоимостьНМА.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив,
	|	СтоимостьНМА.Подразделение,
	|	СтоимостьНМА.Счет,
	|	&СчетВыбытия,
	|	СУММА(-СтоимостьНМА.СуммаОстаток),
	|	СУММА(-СтоимостьНМА.СуммаНУОстаток),
	|	СУММА(-СтоимостьНМА.СуммаПРОстаток),
	|	СУММА(-СтоимостьНМА.СуммаВРОстаток),
	|	ЛОЖЬ
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтоимостьНМА КАК СтоимостьНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = СтоимостьНМА.НематериальныйАктив
	|ГДЕ
	|	СтоимостьНМА.Счет В (&СчетаАмортизации, &СчетОбесценения)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНМА.НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив,
	|	СтоимостьНМА.Подразделение,
	|	СтоимостьНМА.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив,
	|	СтоимостьНМА.Подразделение,
	|	&СчетСписания,
	|	&СчетВыбытия,
	|	СУММА(СтоимостьНМА.СуммаОстаток),
	|	СУММА(СтоимостьНМА.СуммаНУОстаток),
	|	СУММА(СтоимостьНМА.СуммаПРОстаток),
	|	СУММА(СтоимостьНМА.СуммаВРОстаток),
	|	ИСТИНА
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтоимостьНМА КАК СтоимостьНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = СтоимостьНМА.НематериальныйАктив
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНМА.НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив,
	|	СтоимостьНМА.Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТаблицаПараметрыСписания = Запрос.Выполнить().Выгрузить();
	ПараметрыВыбытия.Вставить("ТаблицаПараметрыСписания", ТаблицаПараметрыСписания);

	Возврат ПараметрыВыбытия;

КонецФункции

Функция ПодготовитьПараметрыПодготовитьТаблицыСведенийПоВыбытиюНМА(ТаблицаНМА, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаНМА
	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"         // <Число, 5, 0>
	+ "НематериальныйАктив," // <СправочникСсылка.НематериальныеАктивы>
	+ "Регистратор";         // <ДокументСсылка.*>

	Параметры.Вставить("ТаблицаНМА",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты
	СписокОбязательныхКолонок = ""
	+ "Период,"        // <Дата>
	+ "Организация,"   // <СправочникСсылка.Организации>
	+ "Подразделение," // <Ссылка на справочник подразделений>
	+ "СчетСписания,"  // <ПланСчетовСсылка.Хозрасчетный>
	+ "Содержание,"    // <Строка, 150>
	+ "ИмяСписка,"     // <Строка>
	+ "Регистратор";   // <ДокументСсылка.*>

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ПолучитьИнициализированнуюСтруктуруПараметровВыбытия()

	ПараметрыВыбытия = Новый Структура;
	
	// НачислениеАмортизации, структура из двух таблиц: ТаблицаЗатрат и ТаблицаРеквизиты
	ПараметрыВыбытия.Вставить("НачислениеАмортизации",
		Новый Структура("ТаблицаЗатрат, ТаблицаРеквизиты, СправкаРасчет",
			УправлениеВнеоборотнымиАктивами.ПолучитьПустуюТаблицуЗначенийСКолонками(Новый Структура("ОбъектУчета, ПодразделениеЗатрат, Подразделение,
				|Субконто1, Субконто2, Субконто3, СуммаБУ, СуммаНУ, СуммаПР, СуммаВР, СчетЗатрат, СчетАмортизации")), // ТаблицаЗатрат
			УправлениеВнеоборотнымиАктивами.ПолучитьПустуюТаблицуЗначенийСКолонками(Новый Структура("Период, Организация, Содержание")),  // ТаблицаРеквизиты
			УправлениеВнеоборотнымиАктивами.ПустаяСправкаРасчет("РасчетАмортизации")
		)
	);

	// ТаблицаПараметрыСписания, таблица значений
	ПараметрыВыбытия.Вставить("ТаблицаПараметрыСписания", Новый ТаблицаЗначений); // Достаточно указать ТЗ без строк, колонки прописывать не нужно

	Возврат ПараметрыВыбытия;

КонецФункции

Процедура СформироватьДвиженияВыбытиеНМА(ТаблицаРеквизиты, ПараметрыВыбытия, Движения, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыВыбытиеНМА(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	УправлениеВнеоборотнымиАктивами.СформироватьДвиженияНачислениеАмортизации(
		ПараметрыВыбытия.НачислениеАмортизации.ТаблицаЗатрат,
		ПараметрыВыбытия.НачислениеАмортизации.ТаблицаРеквизиты,
		Движения, Отказ);
		
	ЗакрытиеМесяца.ЗаписьВоВспомогательныеРегистрыСведений(Движения,
		ПараметрыВыбытия.НачислениеАмортизации.СправкаРасчет,
		ПараметрыВыбытия.НачислениеАмортизации.ТаблицаРеквизиты,
		"РасчетАмортизации");
	
	ОтражатьВНалоговомУчете  = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ВидИспользованияСумм = БухгалтерскийУчет.ИспользоватьСуммыНалогНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ИспользоватьСуммыРазниц = БухгалтерскийУчетКлиентСервер.ИспользоватьСуммыРазниц(ВидИспользованияСумм);
	
	Для Каждого СтрокаТаблицы Из ПараметрыВыбытия.ТаблицаПараметрыСписания Цикл

		СуммаПроводки   = СтрокаТаблицы.СуммаБУ;
		СуммаПроводкиНУ = СтрокаТаблицы.СуммаНУ;
		СуммаПроводкиПР = СтрокаТаблицы.СуммаПР;
		СуммаПроводкиВР = СтрокаТаблицы.СуммаВР;

		Если СуммаПроводки = 0 И СуммаПроводкиНУ = 0
			И (Не ИспользоватьСуммыРазниц Или (СуммаПроводкиПР = 0 И СуммаПроводкиВР = 0)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Реквизиты.Подразделение <> Неопределено Тогда
			ПодразделениеПроводки = Реквизиты.Подразделение;
		Иначе
			ПодразделениеПроводки = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Содержание  = Реквизиты.Содержание;
		Проводка.Сумма       = СуммаПроводки;

		Проводка.СчетДт = СтрокаТаблицы.СчетДт;
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = ПодразделениеПроводки;
		КонецЕсли;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,
											 Проводка.СубконтоДт,
											 "НематериальныеАктивы",
											 СтрокаТаблицы.НематериальныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РасходыНаНИОКР", СтрокаТаблицы.НематериальныйАктив);
		
		Проводка.СчетКт = СтрокаТаблицы.СчетКт;
		
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = ПодразделениеПроводки;
		КонецЕсли;
		
		Если СтрокаТаблицы.ЭтоПроводкаСписанияСтоимости Тогда
			
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,
												 Проводка.СубконтоДт,
												 СвойстваСчетаДт.ВидСубконто1,
												 Реквизиты.СубконтоСписания);
												 
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,
												 Проводка.СубконтоДт,
												 "РеализуемыеАктивы",
												 СтрокаТаблицы.НематериальныйАктив);
		КонецЕсли;

		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,
											 Проводка.СубконтоКт,
											 "НематериальныеАктивы",
											 СтрокаТаблицы.НематериальныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РасходыНаНИОКР", СтрокаТаблицы.НематериальныйАктив);

		Если ОтражатьВНалоговомУчете Тогда
			Если СвойстваСчетаДт.НалоговыйУчет Тогда
				Проводка.СуммаНУДт = СуммаПроводкиНУ;
				Если ИспользоватьСуммыРазниц Тогда
					Проводка.СуммаПРДт = СуммаПроводкиПР;
					Проводка.СуммаВРДт = СуммаПроводкиВР;
				КонецЕсли;
			КонецЕсли;

			Если СвойстваСчетаКт.НалоговыйУчет Тогда
				Проводка.СуммаНУКт = СуммаПроводкиНУ;
				Если ИспользоватьСуммыРазниц Тогда
					Проводка.СуммаПРКт = СуммаПроводкиПР;
					Проводка.СуммаВРКт = СуммаПроводкиВР;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
	// Вызов переопределяемой процедуры для возможности выполнения дополнительных действий при списании НМА.
	УправлениеВнеоборотнымиАктивамиПереопределяемый.ВыполнитьДополнительныеДействияСписаниеНМА(ПараметрыВыбытия.ТаблицаПараметрыСписания, ТаблицаРеквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыВыбытиеНМА(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата>
	+ "Организация,"               // <СправочникСсылка.Организации>
	+ "Подразделение,"             // <Ссылка на справочник подразделений>
	+ "Содержание,"                // <Строка, 150>
	+ "СчетСписания,"              // <ПланСчетовСсылка.Хозрасчетный>
	+ "СубконтоСписания"           // 
	;
	
	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// ВЫРАБОТКА НМА

Процедура СформироватьДвиженияВыработкаНМА(ТаблицаНМА, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНМА) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВыработкаНМА(ТаблицаНМА);
	Движения.ВыработкаНМА.Загрузить(Параметры.ТаблицаНМА);
	Движения.ВыработкаНМА.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыВыработкаНМА(ТаблицаНМА)

	Параметры = Новый Структура;
	
	// Подготовка таблицы ТаблицаНМА:
	СписокОбязательныхКолонок = ""
	+ "Период,"
	+ "Регистратор,"
	+ "Организация,"
	+ "НомерСтроки,"
	+ "НематериальныйАктив,"
	+ "Количество,"
	;
	Параметры.Вставить("ТаблицаНМА", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

// Изменение способов отражения расходов по амортизации НМА

Процедура СформироватьДвиженияИзменениеСпособовОтраженияРасходовПоАмортизацииНМА(ТаблицаНМА, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНМА) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыИзменениеСпособовОтраженияРасходовПоАмортизацииНМА(ТаблицаНМА);
	
	Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Загрузить(Параметры.ТаблицаНМА);
	Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыИзменениеСпособовОтраженияРасходовПоАмортизацииНМА(ТаблицаНМА)

	Параметры = Новый Структура;
	
	// Подготовка таблицы ТаблицаНМА:
	СписокОбязательныхКолонок = ""
	+ "Период,"
	+ "Регистратор,"
	+ "Организация,"
	+ "НомерСтроки,"
	+ "СпособОтраженияРасходов,"
	+ "НематериальныйАктив";
	
	Параметры.Вставить("ТаблицаНМА", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

// Изменение специального коэфициента для НМА

Процедура СформироватьДвиженияИзменениеСпециальногоКоэффициентаНМА(ТаблицаНМА, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНМА) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыИзменениеСпециальногоКоэффициентаНМА(ТаблицаНМА);
	
	Движения.НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет.Загрузить(Параметры.ТаблицаНМА);
	Движения.НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыИзменениеСпециальногоКоэффициентаНМА(ТаблицаНМА)

	Параметры = Новый Структура;
	
	// Подготовка таблицы ТаблицаНМА:
	СписокОбязательныхКолонок = ""
	+ "Период,"
	+ "Регистратор,"
	+ "Организация,"
	+ "НомерСтроки,"
	+ "НематериальныйАктив,"
	+ "СпециальныйКоэффициент";
	
	Параметры.Вставить("ТаблицаНМА", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

// ПОСТУПЛЕНИЕ НМА

Процедура СформироватьДвиженияПоступлениеНМА(ТаблицаНМА, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНМА) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыПоступлениеНМА(ТаблицаНМА, ТаблицаРеквизиты);	
	Реквизиты = Параметры.Реквизиты[0];
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаНМА Цикл
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Содержание  = СтрокаТаблицы.Содержание;
		
		Проводка.СчетДт      = СтрокаТаблицы.СчетУчета;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НематериальныеАктивы", СтрокаТаблицы.НематериальныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "РасходыНаНИОКР", СтрокаТаблицы.НематериальныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиЗатрат", СтрокаТаблицы.СтатьяЗатрат);
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.ПодразделениеЗатрат;
		КонецЕсли;
		
		Проводка.СчетКт      = Реквизиты.СчетУчетаРасчетовСКонтрагентом;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"Контрагенты", Реквизиты.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"Договоры", Реквизиты.ДоговорКонтрагента);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"ДокументыРасчетовСКонтрагентами", Реквизиты.Регистратор);
		
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = Реквизиты.Подразделение;
		КонецЕсли;
		Если СвойстваСчетаКт.Валютный Тогда
			Проводка.ВалютаКт        = Реквизиты.ВалютаВзаиморасчетов;
			Проводка.ВалютнаяСуммаКт = СтрокаТаблицы.СуммаВзаиморасчетов;
		КонецЕсли;
		
		Проводка.Сумма =  СтрокаТаблицы.СуммаБУ;
		
		Если ОтражатьВНалоговомУчете Тогда
			
			Если СвойстваСчетаДт.НалоговыйУчет Тогда
				Проводка.СуммаНУДт = СтрокаТаблицы.СуммаНУ;
				Проводка.СуммаПРДт = СтрокаТаблицы.СуммаБУ - СтрокаТаблицы.СуммаНУ;
				Проводка.СуммаВРДт = 0;
			КонецЕсли;
			Если СвойстваСчетаКт.НалоговыйУчет Тогда
				Проводка.СуммаНУКт = СтрокаТаблицы.СуммаНУ;
				Проводка.СуммаПРКт = СтрокаТаблицы.СуммаБУ - СтрокаТаблицы.СуммаНУ;
				Проводка.СуммаВРКт = 0;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыПоступлениеНМА(ТаблицаНМА, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаНМА

	СписокОбязательныхКолонок = ""
	+ "НематериальныйАктив," // <СправочникСсылка.НематериальныеАктивы> - поступающий НМА
	+ "СуммаВзаиморасчетов," // <Число,15,2> - сумма в валюте взаиморасчетов с поставщиком
	+ "СуммаБУ,"             // <Число,15,2> - сумма в рублях
	+ "СуммаНУ,"             // <Число,15,2> - сумма в рублях (НУ)
	+ "СчетУчета,"           // <ПланСчетовСсылка.Хозрасчетный> - счет учета, на который приходуется НМА
	+ "ПодразделениеЗатрат," // <СправочникСсылка.ПодразделенияОрганизаций> - подразделение, в которое поступает НМА
	+ "СтатьяЗатрат,"        // <СправочникСсылка.СтатьиЗатрат> - статья затрат
	+ "Содержание";          // <Строка,150> - содержание проводки

	Параметры.Вставить("ТаблицаНМА", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка.*> - документ-регистратор движений
	+ "Период,"                         // <Дата> - период движений - дата документа
	+ "Организация,"                    // <СправочникСсылка.Организации> - организация, в которую поступает НМА
	+ "Подразделение,"                  // <СправочникСсылка.ПодразделенияОрганизаций> - подразделение взаиморасчетов с поставщиком
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты> - поставщик НМА
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов> - договор, по которому поступает НМА
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный> - счет учета расчетов с поставщиком, обычно 60.01
	+ "ВалютаВзаиморасчетов";           // <СправочникСсылка.Валюты> - валюта взаиморасчетов с поставщиком

	Параметры.Вставить("Реквизиты",  ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// Движения по состоянию НМА

Процедура СформироватьДвиженияСостоянияНМАОрганизаций(ТаблицаНМА, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНМА) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыСостоянияНМАОрганизаций(ТаблицаНМА, ТаблицаРеквизиты);

	Движения.СостоянияНМАОрганизаций.Загрузить(Параметры.СостоянияНМАОрганизаций);
	Движения.СостоянияНМАОрганизаций.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыСостоянияНМАОрганизаций(ТаблицаНМА, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы СостоянияНМАОрганизаций:
	СписокОбязательныхКолонок = ""
	+ "НематериальныйАктив,"	// <СправочникСсылка.НематериальныйАктив>
	+ "Состояние"				// <ПеречислениеСсылка.ВидыСостоянийНМА>
	;
	
	СостоянияНМАОрганизаций = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок);
	
	// Подготовка реквизитов
	СписокОбязательныхКолонок = ""
	+ "Период,"                 // <Дата> - Дата движения
	+ "Организация";			// <СправочникСсылка.Организации> - организация документа
	
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок);
	
	СостоянияНМАОрганизаций.Колонки.Добавить("Организация", Реквизиты.Колонки.Организация.ТипЗначения);
	СостоянияНМАОрганизаций.Колонки.Добавить("Период", Реквизиты.Колонки.Период.ТипЗначения);
	СостоянияНМАОрганизаций.ЗаполнитьЗначения(Реквизиты[0].Организация, "Организация");
	СостоянияНМАОрганизаций.ЗаполнитьЗначения(Реквизиты[0].Период, "Период");
	
	Параметры.Вставить("СостоянияНМАОрганизаций", СостоянияНМАОрганизаций);
	
	Возврат Параметры;
	
КонецФункции

// ПРИНЯТИЕ К УЧЕТУ НМА

// Регистрация состояний НМА

Процедура ПроверитьВозможностьИзмененияСостоянияНМА(ТаблицаРеквизиты, ТаблицаНМА, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Или Не ЗначениеЗаполнено(ТаблицаНМА) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВозможностьИзмененияСостоянияНМА(ТаблицаРеквизиты, ТаблицаНМА);
	
	Реквизиты = Параметры.Реквизиты[0];

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	&ТаблицаНМА КАК ТаблицаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СостоянияНМА.НематериальныйАктив.Код КАК КодНМА,
	|	СостоянияНМА.Период КАК ДатаСостояния,
	|	СостоянияНМА.Регистратор КАК Регистратор,
	|	ТаблицаНМА.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНМА КАК ТаблицаНМА
	|		ПО СостоянияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	СостоянияНМА.Организация = &Организация
	|	И СостоянияНМА.Регистратор <> &Документ
	|	И СостоянияНМА.Состояние = &Состояние
	|	И СостоянияНМА.Активность
	|	И СостоянияНМА.Период <= &Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНМА.НомерСтроки";

	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация );
	Запрос.УстановитьПараметр("Состояние", Реквизиты.Состояние);
	Запрос.УстановитьПараметр("Документ", Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("ТаблицаНМА", Параметры.ТаблицаНМА);
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);

	Если ТранзакцияАктивна() Тогда

		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияНМАОрганизаций");
		ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
		ЭлементБлокировки.ИсточникДанных = Параметры.ТаблицаНМА;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НематериальныйАктив", "НематериальныйАктив");
		Блокировка.Заблокировать();

	КонецЕсли;

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

		ШаблонСообщения = НСтр("ru = 'Для нематериального актива <%1 (%2)> в организации <%3> уже зафиксировано состояние <%4>
							   |документом <%5>. Дата состояния: %6'");

	Пока Выборка.Следующий() Цикл
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
			Выборка.НематериальныйАктив, Выборка.КодНМА, Реквизиты.Организация, Реквизиты.Состояние,
			Выборка.Регистратор, Формат(Выборка.ДатаСостояния, "ДФ=dd.MM.yyyy"));
		Поле = ?(ЗначениеЗаполнено(Реквизиты.ИмяСписка), Реквизиты.ИмяСписка + "[" + Формат(Выборка.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].", "")
			+ Реквизиты.ИмяПоля;
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор, Поле, "Объект", Отказ);
	КонецЦикла;

КонецПроцедуры

Функция ПодготовитьПараметрыВозможностьИзмененияСостоянияНМА(ТаблицаРеквизиты, ТаблицаНМА)

	Параметры = Новый Структура;

	СписокОбязательныхКолонок = ""
	+ "Период,"              // <Дата>
	+ "ИмяСписка,"           // <Строка, 0>
	+ "ИмяПоля,"             // <Строка, 0> - имя реквизита документа или табличной части, в котором хранится ссылка на объект НМА.
	+ "Организация,"         // <СправочникСсылка.Организации>
	+ "Состояние,"           // <ПеречислениеСсылка.СостоянияНМА> - состояние НМА, которое требуется установить
	+ "Регистратор";         // <ДокументСсылка.*>

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"         // <Число, 5, 0>
	+ "НематериальныйАктив"; // <СправочникСсылка.НематериальныхАктивов>
	
	Параметры.Вставить("ТаблицаНМА",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаНМА, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

// Проводки НМА по счетам

Процедура СформироватьДвиженияПринятиеКУчетуНМА(ТаблицаРеквизиты, ТаблицаСпособОтраженияРасходов, Движения, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыПринятиеКУчетуНМА(ТаблицаРеквизиты, ТаблицаСпособОтраженияРасходов);

	Реквизиты = Параметры.Реквизиты[0];

	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаПБУ18          = УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период);
	
	Проводка = Движения.Хозрасчетный.Добавить();

	Проводка.Период      = Реквизиты.Период;
	Проводка.Организация = Реквизиты.Организация;
	Проводка.Сумма       = Реквизиты.СтоимостьБУ;
	
	Проводка.СчетДт          = Реквизиты.СчетУчета;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НематериальныеАктивы", Реквизиты.НематериальныйАктив);
	
	СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);

	Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
		Проводка.ПодразделениеДт = Реквизиты.ПодразделениеДт;
	КонецЕсли;
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНаОснованииИнвентаризации Тогда
		Проводка.СчетКт          = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
	Иначе
		Проводка.СчетКт          = Реквизиты.СчетУчетаВнеоборотногоАктива;
	КонецЕсли;
	
	СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
	
	Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
		Проводка.ПодразделениеКт = Реквизиты.ПодразделениеКт;
	КонецЕсли;
	
	Если Реквизиты.НематериальныйАктив.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив Тогда
		Проводка.Содержание = "Принят к учету НМА";
	Иначе
		Проводка.Содержание = "Приняты к учету расходы на НИОКР";
	КонецЕсли;
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНаОснованииИнвентаризации Тогда
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ПрочиеДоходыИРасходы", Реквизиты.СтатьяДоходов);
	Иначе
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "НематериальныеАктивы", Реквизиты.НематериальныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РасходыНаНИОКР", Реквизиты.НематериальныйАктив);
	КонецЕсли;
	
	Если ОтражатьВНалоговомУчете Тогда
		
		Если СвойстваСчетаДт.НалоговыйУчет Тогда
			Проводка.СуммаНУДт = Реквизиты.СтоимостьНУ;
			Если ПоддержкаПБУ18 Тогда
				Проводка.СуммаПРДт = Реквизиты.СтоимостьПР;
				Проводка.СуммаВРДт = Реквизиты.СтоимостьВР;
			КонецЕсли;
		КонецЕсли;
		
		Если СвойстваСчетаКт.НалоговыйУчет Тогда
			Проводка.СуммаНУКт = Реквизиты.СтоимостьНУ;
			Если ПоддержкаПБУ18 Тогда
				Проводка.СуммаПРКт = Реквизиты.СтоимостьПР;
				Проводка.СуммаВРКт = Реквизиты.СтоимостьВР;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтражатьВНалоговомУчете Тогда
		Для Каждого СтрокаСписания Из Параметры.ТаблицаСпособОтраженияРасходов Цикл
			
			Проводка             = Движения.Хозрасчетный.Добавить();
			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;
			Проводка.Сумма       = 0;
			Если Реквизиты.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив Тогда
				Проводка.Содержание = НСтр("ru = 'Включение стоимости в состав расходов (НУ)'", ОбщегоНазначения.КодОсновногоЯзыка());
			Иначе
				Проводка.Содержание = НСтр("ru = 'Приняты к учету расходы на НИОКР'", ОбщегоНазначения.КодОсновногоЯзыка());
			КонецЕсли;
			
			Проводка.СчетДт      = СтрокаСписания.СчетЗатрат;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаСписания.Субконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаСписания.Субконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаСписания.Субконто3);

			Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНаОснованииИнвентаризации Тогда
				Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
			ИначеЕсли Реквизиты.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив Тогда
				Проводка.СчетКт          = Реквизиты.СчетУчета;
			Иначе
				Проводка.СчетКт      = Реквизиты.СчетУчетаВнеоборотногоАктива;
			КонецЕсли;

			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаСписания.Подразделение;
			КонецЕсли;
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = Реквизиты.ПодразделениеКт;
			КонецЕсли;
			
			Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНаОснованииИнвентаризации Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ПрочиеДоходыИРасходы", Реквизиты.СтатьяДоходов);
			Иначе
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "НематериальныеАктивы", Реквизиты.НематериальныйАктив);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РасходыНаНИОКР", Реквизиты.НематериальныйАктив);
			КонецЕсли;

			Проводка.СуммаНУДт = СтрокаСписания.СуммаНУДт;
			Проводка.СуммаНУКт = СтрокаСписания.СуммаНУКт;

			Если ПоддержкаПБУ18 Тогда
				
				Проводка.СуммаПРДт = СтрокаСписания.СуммаПРДт;
				Проводка.СуммаПРКт = СтрокаСписания.СуммаПРКт;
				Проводка.СуммаВРДт = СтрокаСписания.СуммаВРДт;
				Проводка.СуммаВРКт = СтрокаСписания.СуммаВРКт;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Движения.Хозрасчетный.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьПараметрыПринятиеКУчетуНМА(ТаблицаРеквизиты, ТаблицаСпособОтраженияРасходов)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                       // <Дата>
	+ "НематериальныйАктив,"          // <СправочникСсылка.НематериальныеАктивы>
	+ "Организация,"                  // <СправочникСсылка.Организации>
	+ "ВидОперации,"                  // <ПеречислениеСсылка.ВидыОперацийПринятияКУчетуНМА>
	+ "СтатьяДоходов,"                // <СправочникСсылка.ПрочиеДоходыИРасходы>
	+ "ПодразделениеДт,"              // <Ссылка на справочник подразделений>
	+ "ПодразделениеКт,"              // <Ссылка на справочник подразделений>
	+ "Регистратор,"                  // <ДокументСсылка.*>
	+ "СчетУчета,"                    // <ПланСчетовСсылка.Хозрасчетный> - счет на который принимается к учету НМА
	+ "СчетУчетаВнеоборотногоАктива," // <ПланСчетовСсылка.Хозрасчетный> - счет учета нематериального актива
	+ "СтоимостьБУ,"
	+ "СтоимостьНУ,"
	+ "СтоимостьПР,"
	+ "СтоимостьВР,"
	+ "ВидОбъектаУчета,"
	+ "ПорядокСписанияНИОКРНаРасходыНУ"
	;

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"                  // <Число>
	+ "СчетЗатрат,"                   // <ПланСчетовСсылка.Хозрасчетный>
	+ "Подразделение,"                // <Ссылка на справочник подразделений>
	+ "Субконто1,"                    //
	+ "Субконто2,"                    //
	+ "Субконто3,"                    //
	+ "Коэффициент,"                  // <Число>
	+ "ВидРасходовНУ,"                // <ПеречислениеСсылка.ВидыРасходовНУ>
	+ "СуммаНУДт,"                    // <Число>
	+ "СуммаПРДт,"                    // <Число>
	+ "СуммаВРДт,"                    // <Число>
	+ "СуммаНУКт,"                    // <Число>
	+ "СуммаПРКт,"                    // <Число>
	+ "СуммаВРКт"                    // <Число>
	;

	Параметры.Вставить("ТаблицаСпособОтраженияРасходов",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаСпособОтраженияРасходов, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

// Отражение НМА в учете

Процедура СформироватьДвиженияРегистрацияСчетовБухгалтерскогоУчетаНМА(ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыРегистрацияСчетовБухгалтерскогоУчетаНМА(ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];

	Движение = Движения.СчетаБухгалтерскогоУчетаНМА.Добавить();
	Движение.Период                    = Реквизиты.Период;
	Движение.НематериальныйАктив       = Реквизиты.НематериальныйАктив;
	Движение.Организация               = Реквизиты.Организация;
	Движение.СчетУчета                 = Реквизиты.СчетУчета;
	Движение.СчетНачисленияАмортизации = Реквизиты.СчетНачисленияАмортизации;

	Движения.СчетаБухгалтерскогоУчетаНМА.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьПараметрыРегистрацияСчетовБухгалтерскогоУчетаНМА(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                    // <Дата>
	+ "Организация,"               // <СправочникСсылка.Организации>
	+ "НематериальныйАктив,"       // <СправочникСсылка.НематериальныеАктивы>
	+ "СчетНачисленияАмортизации," // <ПланСчетовСсылка.Хозрасчетный> - счет начисления амортизации ОС
	+ "СчетУчета";                 // <ПланСчетовСсылка.Хозрасчетный> - счет учета ОС

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияИзменениеСпособовОтраженияРасходовПоАмортизацииНМАБУ(ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыИзменениеСпособовОтраженияРасходовПоАмортизацииНМАБУ(ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];

	Движение = Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Добавить();
	Движение.Период                  = Реквизиты.Период;
	Движение.НематериальныйАктив     = Реквизиты.НематериальныйАктив;
	Движение.Организация             = Реквизиты.Организация;
	Движение.СпособОтраженияРасходов = Реквизиты.СпособОтраженияРасходов;

	Движения.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьПараметрыИзменениеСпособовОтраженияРасходовПоАмортизацииНМАБУ(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                                // <Дата>
	+ "Организация,"                           // <СправочникСсылка.Организации>
	+ "НематериальныйАктив,"                           // <СправочникСсылка.НематериальныеАктивы
	+ "СпособОтраженияРасходов"; // <СправочникСсылка.СпособыОтраженияРасходовПоАмортизации> - способы
		// отражения расходов по амортизации ОС

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// Первоначальниые сведения НМА

Процедура СформироватьДвиженияРегистрацияПервоначальныхСведенийНМАБУ(ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыРегистрацияПервоначальныхСведенийНМАБУ(ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];

	Движение = Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Добавить();
	Движение.Период                      = Реквизиты.Период;
	Движение.НематериальныйАктив         = Реквизиты.НематериальныйАктив;
	Движение.Организация                 = Реквизиты.Организация;
	Движение.ПервоначальнаяСтоимость     = Реквизиты.СтоимостьБУ;
	Движение.НачислятьАмортизацию        = Реквизиты.НачислятьАмортизациюБУ;
	Движение.СпособНачисленияАмортизации = Реквизиты.СпособНачисленияАмортизацииБУ;
	Движение.СпособПоступления           = Реквизиты.СпособПоступления;
	Движение.СрокПолезногоИспользования  = Реквизиты.СрокПолезногоИспользованияБУ;
	Движение.Коэффициент                 = Реквизиты.КоэффициентБУ;
	Если Реквизиты.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции тогда
		Движение.ОбъемПродукцииРаботДляВычисленияАмортизации = Реквизиты.ОбъемПродукцииРаботДляВычисленияАмортизации;
	КонецЕсли;

	Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьПараметрыРегистрацияПервоначальныхСведенийНМАБУ(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                      // <Дата>
	+ "НематериальныйАктив,"           
	+ "Организация,"                 // <СправочникСсылка.Организации>
	+ "СтоимостьБУ,"           
	+ "НачислятьАмортизациюБУ,"           
	+ "СпособНачисленияАмортизацииБУ," // <ПеречислениеСсылка.СпособыНачисленияАмортизацииНМА> - способ начисления
		// амортизации НМА по бухгалтерскому учету
	+ "ОбъемПродукцииРаботДляВычисленияАмортизации,"           
	+ "СрокПолезногоИспользованияБУ,"   
	+ "КоэффициентБУ,"   
	+ "СпособПоступления";           // <ПеречислениеСсылка.СпособыПоступленияАктивов> - способ поступления НМА

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияРегистрацияПервоначальныхСведенийНМАНУ(ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыРегистрацияПервоначальныхСведенийНМАНУ(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Движение = Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Добавить();
	Движение.Период                                        = Реквизиты.Период;
	Движение.Организация                                   = Реквизиты.Организация;
	Движение.НематериальныйАктив                           = Реквизиты.НематериальныйАктив;
	Движение.НачислятьАмортизацию                          = Реквизиты.НачислятьАмортизациюНУ;
	Движение.ПервоначальнаяСтоимостьНУ                     = Реквизиты.СтоимостьНУ;
	Движение.СрокПолезногоИспользования                    = Реквизиты.СрокПолезногоИспользованияНУ;
	Движение.ПорядокВключенияСтоимостиВСоставРасходов      = Реквизиты.ПорядокВключенияСтоимостиВСоставРасходов;
	Движение.СпособОтраженияРасходовПриВключенииВСтоимость = Реквизиты.СпособОтраженияРасходовПриВключенииВСтоимость;

	Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьПараметрыРегистрацияПервоначальныхСведенийНМАНУ(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                                        // <Дата>
	+ "Организация,"                                   // <СправочникСсылка.Организации>
	+ "НачислятьАмортизациюНУ,"                        // <Булево>
	+ "СрокПолезногоИспользованияНУ,"                  // <Число, 4, 0> - срок полезного использования ОС для целей начисления
	                                                   // амортизации по налоговому учету
	+ "НематериальныйАктив,"                           // <СправочникСсылка.НематериальныхАктивов>
	+ "СтоимостьНУ,"                                   // <Число, 15, 2> - первоначальная стоимость ОС по данным налогового учета
	+ "ПорядокВключенияСтоимостиВСоставРасходов,"      // <ПеречислениеСсылка.ПорядокВключенияСтоимостиОСВСоставРасходовНУ>
	+ "СпособОтраженияРасходовПриВключенииВСтоимость"; // <СправочникСсылка.СпособыОтраженияРасходовПоАмортизации>

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ПодготовитьТаблицуПервоначальныеСведенияНМАУСН(ТаблицаПервоначальныеСведенияНМА, ТаблицаРеквизиты, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыПервоначальныеСведенияНМАУСН(ТаблицаПервоначальныеСведенияНМА, ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];

	ОтражатьВНалоговомУчетеУСН = УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период);
	
	Если ОтражатьВНалоговомУчетеУСН Тогда
		ТаблицаПервоначальныеСведенияНМАУСН = Параметры.ТаблицаПервоначальныеСведенияНМАУСН.Скопировать();
	Иначе
		ТаблицаПервоначальныеСведенияНМАУСН = Параметры.ТаблицаПервоначальныеСведенияНМАУСН.СкопироватьКолонки();
	КонецЕсли;

	Возврат ТаблицаПервоначальныеСведенияНМАУСН;

КонецФункции

Функция ПодготовитьПараметрыПервоначальныеСведенияНМАУСН(ТаблицаПервоначальныеСведенияНМА, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаПервоначальныеСведенияНМА

	СписокОбязательныхКолонок = ""
	+ "НематериальныйАктив," // <СправочникСсылка.НематериальныхАктивов>
	+ "СуммаУСН";            // <Число, 0, 0> - стоимость НМА по данным УСН

	Параметры.Вставить("ТаблицаПервоначальныеСведенияНМАУСН",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаПервоначальныеСведенияНМА, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                                      // <Дата>
	+ "Организация,"                                 // <СправочникСсылка.Организации>
	+ "ПорядокВключенияСтоимостиВСоставРасходовУСН," // <ПеречислениеСсылка.ПорядокВключенияСтоимостиОСиНМАВСоставРасходовУСН> -
	                                                 // порядок включения стоимости ОС в состав расходов по УСН
	+ "ДатаПриобретения,"							 // <Дата> Дата приобретения ОС					 
	+ "СрокПолезногоИспользованияУСН";               // <Число, 4, 0> - срок полезного использования для целей
	                                                 // начисления амортизации по УСН

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияРегистрацияПервоначальныхСведенийНМАУСН(ТаблицаОплатНМАУСН, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;

	Параметры = ПодготовитьПараметрыРегистрацияПервоначальныхСведенийНМАУСН(ТаблицаОплатНМАУСН, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ПлательщикНДС = Реквизиты.Период >= УчетУСНКлиентСервер.ДатаНачалаПримененияНДС();
	ПрименяетсяОбщаяСтавкаНДС = УчетНДС.ПрименяетсяОбщаяСтавкаНДС(Реквизиты.Организация, Реквизиты.Период);
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	ИсключатьНДС = ПрименяетсяОбщаяСтавкаНДС И (Не РаздельныйУчетНДСНаСчете19 Или (РаздельныйУчетНДСНаСчете19
			И Реквизиты.СпособУчетаНДС <> Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости));
	
	Движение = Движения.ПервоначальныеСведенияНМАНалоговыйУчетУСН.Добавить();
	Движение.Период                     = Реквизиты.Период;
	Движение.НематериальныйАктив        = Реквизиты.НематериальныйАктив;
	Движение.Организация                = Реквизиты.Организация;
	Движение.ПервоначальнаяСтоимостьУСН = Реквизиты.СтоимостьУСН;
	Движение.СрокПолезногоИспользования = Реквизиты.СрокПолезногоИспользованияУСН;
	Движение.ДатаПриобретения           = Реквизиты.ДатаПриобретения;
	Движение.ПорядокВключенияСтоимостиВСоставРасходовУСН = Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовУСН;

	Движения.ПервоначальныеСведенияНМАНалоговыйУчетУСН.Записывать = Истина;

	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаОплатНМА Цикл

		Если СтрокаТаблицы.СуммаОплаты <> 0 Тогда

			Движение = Движения.ОплатыНМАДляУСН.Добавить();
			Движение.Период              = СтрокаТаблицы.ДатаОплаты;
			Движение.Организация         = Реквизиты.Организация;
			Движение.НематериальныйАктив = Реквизиты.НематериальныйАктив;
			Движение.ДатаОплаты          = СтрокаТаблицы.ДатаОплаты;
			Движение.СуммаОплаты         = СтрокаТаблицы.СуммаОплаты - ?(ИсключатьНДС, СтрокаТаблицы.СуммаНДС, 0);

		КонецЕсли;

	КонецЦикла;

	Движения.ОплатыНМАДляУСН.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьПараметрыРегистрацияПервоначальныхСведенийНМАУСН(ТаблицаОплатНМАУСН, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаПервоначальныеСведенияОСУСН
	СписокОбязательныхКолонок = ""
	+ "ДатаОплаты,"
	+ "СуммаОплаты," // <Число, 0, 0> - стоимость НМА по данным УСН
	+ "СуммаНДС";    // <Число, 0, 0> - НДС в стоимости НМА по данным УСН

	Параметры.Вставить("ТаблицаОплатНМА",
	ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаОплатНМАУСН, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                                      // <Дата>
	+ "Организация,"                                 // <СправочникСсылка.Организации>
	+ "НематериальныйАктив,"                         // <СправочникСсылка.НематериальныхАктивов>
	+ "ПорядокВключенияСтоимостиВСоставРасходовУСН," // <ПеречислениеСсылка.ПорядокВключенияСтоимостиОСиНМАВСоставРасходовУСН> -
	// порядок включения стоимости ОС в состав расходов по УСН
	+ "СтоимостьУСН,"
	+ "ДатаПриобретения,"                            // <Дата> Дата приобретения ОС
	+ "СрокПолезногоИспользованияУСН,"               // <Число, 4, 0> - срок полезного использования для целей
	// начисления амортизации по УСН
	+ "СпособУчетаНДС";                              // <ПеречислениеСсылка.СпособыУчетаНДС>

	Параметры.Вставить("Реквизиты",
	ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// Параметры амортизации НМА (НУ)

Процедура СформироватьДвиженияНачислениеАмортизацииНМАСпециальныйКоэффициентНУ(ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыНачислениеАмортизацииНМАСпециальныйКоэффициентНУ(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	Движение = Движения.НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет.Добавить();
	Движение.Период                  = Реквизиты.Период;
	Движение.Организация             = Реквизиты.Организация;
	Движение.НематериальныйАктив     = Реквизиты.НематериальныйАктив;
	Движение.СпециальныйКоэффициент  = Реквизиты.СпециальныйКоэффициентНУ;

	Движения.НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьПараметрыНачислениеАмортизацииНМАСпециальныйКоэффициентНУ(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                   // <Дата>
	+ "Организация,"              // <СправочникСсылка.Организации>
	+ "НематериальныйАктив,"      // <СправочникСсылка.НематериальныхАктивов>
	+ "СпециальныйКоэффициентНУ"; // <Число, 5, 2> - специальный коэффциент для начисления амортизации НМА (НУ)

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// Списание НМА на расходы (НУ) при принятии к учету

Функция ПодготовитьТаблицуСписаниеНаРасходыНУ(ТаблицаПервоначальныеСведенияНМА, ТаблицаРеквизиты, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыСписаниеНаРасходыНУ(ТаблицаПервоначальныеСведенияНМА, ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];

	Если Не Реквизиты.ПлательщикНалогаНаПрибыль
		Или Реквизиты.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР
		Или Реквизиты.ПорядокВключенияСтоимостиВСоставРасходов <> Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету Тогда
		ТаблицаДляСписания = Параметры.ТаблицаПервоначальныеСведенияНМА.СкопироватьКолонки();
	Иначе
		ТаблицаДляСписания = Параметры.ТаблицаПервоначальныеСведенияНМА;
	КонецЕсли;

	ТаблицаДляСписания.Колонки.НематериальныйАктив.Имя = "ОбъектУчета";
	ТаблицаДляСписания.Колонки.Добавить("НаправлениеАмортизации",
		Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияРасходовПоАмортизации"));
	ТаблицаДляСписания.ЗаполнитьЗначения(Реквизиты.СпособыОтраженияРасходовПоАмортизации, "НаправлениеАмортизации");
	ТаблицаДляСписания.Колонки.Добавить("СчетНачисленияАмортизации");
	ТаблицаДляСписания.ЗаполнитьЗначения(Реквизиты.СчетУчета, "СчетНачисленияАмортизации");
	ТаблицаДляСписания.Колонки.Добавить("СуммаАмортизацииБУ", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДляСписания.Колонки.Добавить("СуммаАмортизацииПР", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДляСписания.Колонки.Добавить("СуммаАмортизацииВР", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДляСписания.Колонки.СтоимостьНУ.Имя = "СуммаАмортизацииНУ";
	ТаблицаДЛяСписания.Колонки.Добавить("Подразделение");
	ТаблицаДляСписания.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
	ТаблицаДляСписания.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));

	// Распределение стоимости по направлениям.
	ТаблицаЗатратПоАмортизации = 
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл(
		ТаблицаДляСписания, Параметры.Реквизиты, Отказ);

	Для каждого СтрокаНМА Из ТаблицаЗатратПоАмортизации Цикл
		СтрокаНМА.СуммаВР = СтрокаНМА.СуммаВР - СтрокаНМА.СуммаНУ;
	КонецЦикла;

	Возврат ТаблицаЗатратПоАмортизации;

КонецФункции

Функция ПодготовитьПараметрыСписаниеНаРасходыНУ(ТаблицаПервоначальныеСведенияОС, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаПервоначальныеСведенияОС

	СписокОбязательныхКолонок = ""
	+ "НематериальныйАктив," // <СправочникСсылка.НематериальныеАктивы>
	+ "СтоимостьНУ";         // <Число, 15, 2> - сумма, относимая на расходы (НУ)

	Параметры.Вставить("ТаблицаПервоначальныеСведенияНМА",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаПервоначальныеСведенияОС, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                                   // <Дата>
	+ "Организация,"                              // <СправочникСсылка.Организации>
	+ "ПлательщикНалогаНаПрибыль,"                // <Булево>
	+ "Подразделение,"                            // <Ссылка на справочник подразделений>
	+ "ПорядокВключенияСтоимостиВСоставРасходов," // <ПеречислениеСсылка.ПорядокВключенияСтоимостиОСВСоставРасходовНУ> -
	                                              // порядок включения стоимости НМА в состав расходов (НУ)
	+ "СпособыОтраженияРасходовПоАмортизации,"    // <СправочникСсылка.СпособыОтраженияРасходовПоАмортизации> - способ
	                                              // отрадения расходов по списанию НМА (НУ)
	+ "ВидОбъектаУчета,"                          // <ПеречислениеСсылка.ВидыОбъектовУчетаНМА>
	+ "Регистратор,"                              // <ДокументСсылка.*>
	+ "СчетУчета,"                                 // <ПланСчетовСсылка.Хозрасчетный> - счет на котором формируется стоимость НМА
	+ "ИмяСписка";                                // <Строка>
	
	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// ПЕРЕДАЧА НМА

// Параметры выручки НМА

Процедура СформироватьДвиженияПоВыручкеНМА(ТаблицаРеквизиты, ТаблицаРеализованныеТоварыУслуги, Движения, Отказ) Экспорт

	Если ТаблицаРеализованныеТоварыУслуги[0].СуммаРуб = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВыручкиНМА(ТаблицаРеквизиты, ТаблицаРеализованныеТоварыУслуги);
	Реквизиты = Параметры.Реквизиты[0];
	СтрокаТаблицыНМА = Параметры.РеализованныеТоварыУслуги[0];
	
	ПрименениеПБУ18 = УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период);
	
	Проводка = Движения.Хозрасчетный.Добавить();
	
	Проводка.Период       = Реквизиты.Период;
	Проводка.Организация  = Реквизиты.Организация;
	Проводка.Сумма        = СтрокаТаблицыНМА.СуммаБУ;
	Проводка.Содержание   = СтрокаТаблицыНМА.Содержание;
	
	Проводка.СчетДт      = Реквизиты.СчетУчетаРасчетовСКонтрагентом;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"Контрагенты",        Реквизиты.Контрагент);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"Договоры",           Реквизиты.ДоговорКонтрагента);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ДокументыРасчетовСКонтрагентами", Реквизиты.Регистратор);
	
	Если Реквизиты.СчетУчетаРасчетовСКонтрагентом.Валютный Тогда
		//Если документ в иностранной валюте, он может быть выписан только в валюте взаиморасчетов
		Проводка.ВалютаДт        = Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов;
		Проводка.ВалютнаяСуммаДт = СтрокаТаблицыНМА.СуммаВзаиморасчетов;
		
	КонецЕсли;
	
	Проводка.СчетКт = СтрокаТаблицыНМА.СчетДоходов;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицыНМА.Субконто);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтавкиНДС", СтрокаТаблицыНМА.СтавкаНДС);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "РеализуемыеАктивы", Реквизиты.НематериальныйАктив);
	
	БухгалтерскийУчет.УстановитьПодразделенияПроводки(
		Проводка, Реквизиты.ПодразделениеОрганизации, Реквизиты.ПодразделениеОрганизации);
	
	Если Реквизиты.ОтражатьВНалоговомУчете Тогда
		
		СуммаНУДт = СтрокаТаблицыНМА.СуммаБУ;
		СуммаНУКт = СтрокаТаблицыНМА.СуммаБУ - СтрокаТаблицыНМА.СуммаНДСРуб;
		НалоговыйУчет.ЗаполнитьНалоговыеСуммыПроводки(
			СуммаНУДт, СуммаНУКт, Проводка.Сумма - СуммаНУДт, Проводка.Сумма - СуммаНУДт, , , Проводка, ПрименениеПБУ18);
		
	КонецЕсли;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьПараметрыВыручкиНМА(ТаблицаРеквизиты, ТаблицаРеализованныеТоварыУслуги)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"              // <Дата>
	+ "Регистратор,"         // <ДокументСсылка.*>
	+ "Организация,"         // <СправочникСсылка.Организации>
	+ "ПодразделениеОрганизации,"
	+ "НематериальныйАктив," // <СправочникСсылка.НематериальныеАктивы>
	+ "Контрагент,"
	+ "ДоговорКонтрагента,"
	+ "ОтражатьВНалоговомУчете,"
	+ "СчетУчетаРасчетовСКонтрагентом,"
	+ "СчетРасходов,"
	+ "СчетУчетаНДСПоРеализации";

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
		
	// Подготовка таблицы реализованных товаров и услуг:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "НомерСтроки,"
	+ "Содержание,"
	+ "Количество,"
	+ "СуммаВзаиморасчетов,"
	+ "СуммаРуб,"
	+ "СуммаБУ,"
	+ "СуммаНДСРуб,"
	+ "СчетУчета,"
	+ "СчетДоходов,"
	+ "Субконто,"
	+ "СтавкаНДС,"
	+ "СчетУчетаНДСПоРеализации,"
	+ "Контрагент,"
	+ "ВалютаВзаиморасчетов,"
	+ "Подразделение"
	;
	Параметры.Вставить("РеализованныеТоварыУслуги", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеализованныеТоварыУслуги, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// СОСТОЯНИЕ НМА

//Получает документ и дату для указанного состояния нематериального актива по бух учету
//
// Параметры
//
//
// Вовзаращаемое значение
//  Дата и документ - через указанные параметры процедуры.
//
Процедура ПолучитьДокументБухСостоянияНМА(НематериальныйАктив, Организация, Состояние, ДокРегистратор, ДатаДокРегистратора) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СостояниеНМА", 	Состояние);
	Запрос.УстановитьПараметр("НМА",            НематериальныйАктив);
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СостоянияНМАОрганизаций.Регистратор КАК Документ,
		|	СостоянияНМАОрганизаций.Период КАК Период,
		|	ВЫРАЗИТЬ(СостоянияНМАОрганизаций.Регистратор КАК Документ.ПоступлениеТоваровУслуг).ВидОперации КАК ВидОперацииПоступления
		|ИЗ
		|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
		|ГДЕ
		|	СостоянияНМАОрганизаций.НематериальныйАктив = &НМА
		|	И СостоянияНМАОрганизаций.Организация = &Организация
		|	И СостоянияНМАОрганизаций.Состояние = &СостояниеНМА
		|
		|УПОРЯДОЧИТЬ ПО
		|	СостоянияНМАОрганизаций.Период УБЫВ";
	ВыборкаДоков = Запрос.Выполнить().Выбрать();

	Если ВыборкаДоков.Следующий()  Тогда
		ДокРегистратор      = ВыборкаДоков.Документ;
		Если ДатаСостоянияНеСовпадаетСДатойДокумента(ВыборкаДоков.ВидОперацииПоступления) Тогда
			ДатаДокРегистратора = ВыборкаДоков.Период;
		Иначе
			ДатаДокРегистратора = ВыборкаДоков.Документ.Дата;
		КонецЕсли;

	Иначе
		ДокРегистратор      = Неопределено;
		ДатаДокРегистратора = '00010101';
	КонецЕсли;

КонецПроцедуры

Функция ОбработатьПодборНематериальныхАктивов(Знач КоллекцияНМА, Знач ВыбранноеЗначение) Экспорт
	
	ТаблицаНМА = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресВХранилище);
	
	ИсходныеДанные = КоллекцияНМА.Выгрузить();
	ИсходныеДанные.Индексы.Добавить("НематериальныйАктив");
	
	ДобавленныеСтроки = Новый Массив;
	
	КоллекцияНМА.Очистить();
	Для каждого СтрокаТаблицы Из ТаблицаНМА Цикл
		ЭлементКоллекции = КоллекцияНМА.Добавить();
		
		СтрокаИсходныхДанных = ИсходныеДанные.Найти(СтрокаТаблицы.НематериальныйАктив, "НематериальныйАктив");
		Если СтрокаИсходныхДанных <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаИсходныхДанных);
		Иначе
			ДобавленныеСтроки.Добавить(ЭлементКоллекции);
		КонецЕсли;
			
		ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаТаблицы);
	КонецЦикла;
	
	Возврат ДобавленныеСтроки;
	
КонецФункции

Функция ДатаСостоянияНеСовпадаетСДатойДокумента(ВидОперацииПоступления)

	Возврат ВидОперацииПоступления = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права;

КонецФункции

Функция ПодготовитьПараметрыПроверкаНачатыеРазработки(Реквизиты, ОбъектыНИОКР)

	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();

	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
			ОписаниеТаблиц,
			"Реквизиты",
			Реквизиты);

	Описание.Колонки.Добавить("Период"); // <Дата>
	Описание.Колонки.Добавить("Организация"); // <СправочникСсылка.Организации>
	Описание.Колонки.Добавить("Регистратор"); // <ДокументСсылка.НачалоСтадииРазработокНМА>

	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
			ОписаниеТаблиц,
			"ОбъектыНИОКР",
			ОбъектыНИОКР);
			
	Описание.Колонки.Добавить("НомерСтроки"); // <Число>
	Описание.Колонки.Добавить("НематериальныйАктив"); // <СправочникСсылка.НематериальныеАктивы>

	Возврат ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);

КонецФункции

Функция ПодготовитьПараметрыНачатыеРазработки(Реквизиты, ОбъектыНИОКР)

	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();

	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
			ОписаниеТаблиц,
			"Реквизиты",
			Реквизиты);
			
	Описание.Колонки.Добавить("Период"); // <Дата>
	Описание.Колонки.Добавить("Организация"); // <СправочникСсылка.Организации>

	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
			ОписаниеТаблиц,
			"ОбъектыНИОКР",
			ОбъектыНИОКР);
			
	Описание.Колонки.Добавить("НематериальныйАктив"); // <СправочникСсылка.НематериальныеАктивы>

	Возврат ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);

КонецФункции

#КонецОбласти