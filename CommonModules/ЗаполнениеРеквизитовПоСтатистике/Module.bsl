
#Область ПрограммныйИнтерфейс

// Возвращает значение искомого реквизита, используемого в аналогичных документах с аналогичными реквизитами,
// указанными в РеквизитыОпределители
//
// Реквизиты определители должны идти в порядке убывания приоритета: сначала самый главный, в конце самый общий
// Если вес полученных результатов для более важных реквизитов определителей будет менее порога, то будет возвращен
// реквизит с самым большим весом из выборки для самого общего реквизита определителя
//
// Имя реквизитов может быть составным, если они используются из табличной части
// например, РасшифровкаПлатежа.ДоговорКонтрагента
//
// Параметры:
//  Организация           - СправочникСсылка.Организация - организация по которой нужно выбрать
//  ДокументСтатистики    - ДокументСсылка - документ по которому необходимо сделать выборку
//  ДатаДокумента         - Дата - дата документа, установленная в форме
//  РеквизитыОпределители - Соответсвие - ключ - имя реквизита, значение - искомое значение
//  ОпределяемыйРеквизит  - Строка - имя искомого реквизита
//  РазмерВыборки         - Число - количество документо в выборке
// 
// Возвращаемое значение:
//  Ссылка, Неопределено - если реквизит найден - ссылка на его значение, иначе Неопределено
//
Функция РеквизитПоСтатистике(Организация, ДокументСтатистики, ДатаДокумента, РеквизитыОпределители, ОпределяемыйРеквизит, РазмерВыборки = 5) Экспорт
	
	ПорогВеса = 0.5; // 50% - доля документов с определяемым реквизитом, выше которой значение принимается
	МетаданныеОбъекта = ДокументСтатистики.Метаданные();
	
	Если Не ЕстьРеквизиты(МетаданныеОбъекта, РеквизитыОпределители, ОпределяемыйРеквизит) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса();
	ПараметрыЗапроса.Организация = Организация;
	ПараметрыЗапроса.ДокументСтатистики = ДокументСтатистики;
	ПараметрыЗапроса.ДатаДокумента = ДатаДокумента;
	ПараметрыЗапроса.МетаданныеОбъекта = МетаданныеОбъекта;
	ПараметрыЗапроса.РеквизитыОпределители = РеквизитыОпределители;
	ПараметрыЗапроса.ОпределяемыйРеквизит = ОпределяемыйРеквизит;
	ПараметрыЗапроса.РазмерВыборки = РазмерВыборки;
	
	Результат = РезультатЗапроса(ПараметрыЗапроса);
		
	Если Результат = Неопределено Или Результат.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НачальныйИндексРезультата = Результат.ВГраница() - РеквизитыОпределители.Количество() + 1;
	Для ИндексЗапроса = НачальныйИндексРезультата По Результат.ВГраница() Цикл
		Выборка = Результат[ИндексЗапроса].Выбрать();
	
		Если Выборка.Следующий() Тогда
			Если Выборка.Вес > ПорогВеса Или ИндексЗапроса = Результат.ВГраница() Тогда
				Возврат Выборка.ОпределяемыйРеквизит;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеПараметрыЗапроса()
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Организация", Неопределено);
	ПараметрыЗапроса.Вставить("ДокументСтатистики", Неопределено);
	ПараметрыЗапроса.Вставить("ДатаДокумента", Неопределено);
	ПараметрыЗапроса.Вставить("МетаданныеОбъекта", Неопределено);
	ПараметрыЗапроса.Вставить("РеквизитыОпределители", Неопределено);
	ПараметрыЗапроса.Вставить("ОпределяемыйРеквизит", Неопределено);
	ПараметрыЗапроса.Вставить("РазмерВыборки", 5);

	Возврат ПараметрыЗапроса;
КонецФункции

Функция РезультатЗапроса(ПараметрыЗапроса)
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 5
		|	ТаблицаДокумента.Ссылка КАК ОпределяемыйРеквизит
		|ПОМЕСТИТЬ вт_ДокументыВыборки
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ТаблицаДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокументыВыборки.ОпределяемыйРеквизит КАК ОпределяемыйРеквизит,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ вт_КоличествоОтражений
		|ИЗ
		|	вт_ДокументыВыборки КАК вт_ДокументыВыборки
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_ДокументыВыборки.ОпределяемыйРеквизит
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(вт_КоличествоОтражений.Количество) КАК Количество
		|ПОМЕСТИТЬ вт_ОбщееКоличество
		|ИЗ
		|	вт_КоличествоОтражений КАК вт_КоличествоОтражений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_КоличествоОтражений.ОпределяемыйРеквизит КАК ОпределяемыйРеквизит,
		|	ВЫБОР
		|		КОГДА вт_ОбщееКоличество.Количество > 0
		|			ТОГДА вт_КоличествоОтражений.Количество / вт_ОбщееКоличество.Количество
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Вес
		|ИЗ
		|	вт_КоличествоОтражений КАК вт_КоличествоОтражений,
		|	вт_ОбщееКоличество КАК вт_ОбщееКоличество
		|
		|УПОРЯДОЧИТЬ ПО
		|	Вес УБЫВ";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ЗапросОтбора = СхемаЗапроса.ПакетЗапросов[0];
	Оператор = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	
	// Установим количество получаемых записей
	Оператор.КоличествоПолучаемыхЗаписей = ПараметрыЗапроса.РазмерВыборки;
	
	// Установим главную таблицу документа
	ИсточникСхемыЗапроса = ЗапросОтбора.ДоступныеТаблицы.Найти(СтрШаблон("Документ.%1", ПараметрыЗапроса.МетаданныеОбъекта.Имя));
	Оператор.Источники.Заменить(0, ИсточникСхемыЗапроса);
	ПсевдонимТаблицы = Оператор.Источники[0].Источник.Псевдоним;
	
	ЧастиОпределяемогоРеквизита = СтрРазделить(ПараметрыЗапроса.ОпределяемыйРеквизит, ".");
	Если ЧастиОпределяемогоРеквизита.Количество() = 2 Тогда
		ИмяТаблицыЗапроса = СтрШаблон("Документ.%1.%2", ПараметрыЗапроса.МетаданныеОбъекта.Имя, ЧастиОпределяемогоРеквизита[0]);
		ИсточникСхемыЗапроса = ЗапросОтбора.ДоступныеТаблицы.Найти(ИмяТаблицыЗапроса);
		Если ИсточникСхемыЗапроса <> Неопределено Тогда
			Оператор.Источники.Добавить(ИсточникСхемыЗапроса);
			
			// Соединим таблицы по ссылке
			Оператор.Источники[1].Соединения.Очистить();
			УсловиеСоединения = СтрШаблон(
				"%1.Ссылка = %2.Ссылка",
				ПсевдонимТаблицы,
				Оператор.Источники[1].Источник.Псевдоним);
			Оператор.Источники[1].Соединения.Добавить(
				Оператор.Источники[0], УсловиеСоединения);
			// Для ускорения запроса, необходимо чтобы табличная часть была справа при соединении
			Оператор.Источники[1].Соединения[0].ТипСоединения = ТипСоединенияСхемыЗапроса.ПравоеВнешнее;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;

	// Установим поля определяемого реквизита
	Если ЧастиОпределяемогоРеквизита.Количество() = 1 Тогда
		ИндексРеквизита = 0;
	Иначе
		ИндексРеквизита = 1;
	КонецЕсли;
	Оператор.ВыбираемыеПоля[0] = Новый ВыражениеСхемыЗапроса(СтрШаблон(
		"%1.%2",
		Оператор.Источники[ИндексРеквизита].Источник.Псевдоним,
		ЧастиОпределяемогоРеквизита[ИндексРеквизита]));
		
	// Проверка на проведение документа
	ВыражениеПроведен = Новый ВыражениеСхемыЗапроса(СтрШаблон("%1.Проведен", ПсевдонимТаблицы));
	Оператор.Отбор.Добавить(ВыражениеПроведен);

	// Организация
	ВыражениеОрганизация = Новый ВыражениеСхемыЗапроса(
		СтрШаблон("%1.Организация = &Организация", ПсевдонимТаблицы));
	Оператор.Отбор.Добавить(ВыражениеОрганизация);
	Запрос.УстановитьПараметр("Организация", ПараметрыЗапроса.Организация);

	// Дата
	Оператор.ВыбираемыеПоля.Добавить(СтрШаблон("%1.Дата", ПсевдонимТаблицы));

	// Исключить текущий документ
	ВыражениеТекущийДокумент = Новый ВыражениеСхемыЗапроса(
		СтрШаблон("%1.Ссылка <> &ТекущийДокумент", ПсевдонимТаблицы));
	Оператор.Отбор.Добавить(ВыражениеТекущийДокумент);
	Запрос.УстановитьПараметр("ТекущийДокумент", ПараметрыЗапроса.ДокументСтатистики);
	
	// Выборка документов до текущего
	ВыражениеДата = Новый ВыражениеСхемыЗапроса(
		СтрШаблон("%1.Дата <= &ДатаДокумента", ПсевдонимТаблицы));
	Оператор.Отбор.Добавить(ВыражениеДата);
	Запрос.УстановитьПараметр("ДатаДокумента", ПараметрыЗапроса.ДатаДокумента);

	// Упорядочим по дате по убыванию
	ЗапросОтбора.Порядок.Добавить(ЗапросОтбора.Колонки[1]);
	ЗапросОтбора.Порядок[0].Направление = НаправлениеПорядкаСхемыЗапроса.ПоУбыванию;
	
	// Установим параметры отбора
	Для Каждого РеквизитОпределитель Из ПараметрыЗапроса.РеквизитыОпределители Цикл
		ЧастиРеквизитаОпределителя = СтрРазделить(РеквизитОпределитель.Ключ, ".");
		РеквизитОпределительИмя = ЧастиРеквизитаОпределителя[ЧастиРеквизитаОпределителя.ВГраница()];
		Если ЧастиОпределяемогоРеквизита.Количество() = 1 Тогда
			ВыражениеУсловие = Новый ВыражениеСхемыЗапроса(
				СтрШаблон("%1.%2 = &%2", ПсевдонимТаблицы, РеквизитОпределительИмя));
			Оператор.Отбор.Добавить(ВыражениеУсловие);
			Запрос.УстановитьПараметр(РеквизитОпределительИмя, РеквизитОпределитель.Значение);
		Иначе
			ВыражениеУсловие = Новый ВыражениеСхемыЗапроса(
				СтрШаблон("%1.Ссылка.%2 = &%2", ПсевдонимТаблицы, РеквизитОпределительИмя));
			Оператор.Отбор.Добавить(ВыражениеУсловие);
			Запрос.УстановитьПараметр(РеквизитОпределительИмя, РеквизитОпределитель.Значение);
		КонецЕсли;
	КонецЦикла;
	
	// В сложных случаях, когда определителей больше одного создадим запрос для каждого реквизита
	Если ПараметрыЗапроса.РеквизитыОпределители.Количество() > 1 Тогда
		
		// Запомним имена таблиц и присвоим им индекс 0
		ИмяТаблицыДокументы           = СхемаЗапроса.ПакетЗапросов[0].ТаблицаДляПомещения;
		ИмяТаблицыКоличествоОтражений = СхемаЗапроса.ПакетЗапросов[1].ТаблицаДляПомещения;
		ИмяТаблицыОбщееКоличество     = СхемаЗапроса.ПакетЗапросов[2].ТаблицаДляПомещения;
		
		СхемаЗапроса.ПакетЗапросов[0].ТаблицаДляПомещения = ИмяТаблицыДокументы + "0";
		СхемаЗапроса.ПакетЗапросов[1].ТаблицаДляПомещения = ИмяТаблицыКоличествоОтражений + "0";
		СхемаЗапроса.ПакетЗапросов[2].ТаблицаДляПомещения = ИмяТаблицыОбщееКоличество + "0";
		
		Для ИндексЗапроса = 1 По ПараметрыЗапроса.РеквизитыОпределители.Количество() - 1 Цикл
			вт_Документы = СхемаЗапроса.ПакетЗапросов.ДобавитьКопию(0);
			вт_Документы.ТаблицаДляПомещения = ИмяТаблицыДокументы + Строка(ИндексЗапроса);
			КоличествоОтборов = вт_Документы.Операторы[0].Отбор.Количество();
			Для ИндексУсловия = 1 По ИндексЗапроса Цикл
				вт_Документы.Операторы[0].Отбор.Удалить(КоличествоОтборов - ИндексУсловия);
			КонецЦикла;
			
			вт_КоличествоОтражений = СхемаЗапроса.ПакетЗапросов.ДобавитьКопию(ИндексЗапроса + 1);
			вт_КоличествоОтражений.ТаблицаДляПомещения = ИмяТаблицыКоличествоОтражений + Строка(ИндексЗапроса);
			ИсточникСхемыЗапроса = вт_КоличествоОтражений.ДоступныеТаблицы.Найти(ИмяТаблицыДокументы + Строка(ИндексЗапроса));
			вт_КоличествоОтражений.Операторы[0].Источники.Заменить(0, ИсточникСхемыЗапроса);
			
			вт_ОбщееКоличество = СхемаЗапроса.ПакетЗапросов.ДобавитьКопию((ИндексЗапроса + 1) * 2);
			вт_ОбщееКоличество.ТаблицаДляПомещения = ИмяТаблицыОбщееКоличество + Строка(ИндексЗапроса);
			ИсточникСхемыЗапроса = вт_ОбщееКоличество.ДоступныеТаблицы.Найти(ИмяТаблицыКоличествоОтражений + Строка(ИндексЗапроса));
			вт_ОбщееКоличество.Операторы[0].Источники.Заменить(0, ИсточникСхемыЗапроса);
			
			Основная = СхемаЗапроса.ПакетЗапросов.ДобавитьКопию(СхемаЗапроса.ПакетЗапросов.Количество() - 1);
			ИсточникСхемыЗапроса = Основная.ДоступныеТаблицы.Найти(ИмяТаблицыКоличествоОтражений + Строка(ИндексЗапроса));
			Основная.Операторы[0].Источники.Заменить(0, ИсточникСхемыЗапроса);
			ИсточникСхемыЗапроса = Основная.ДоступныеТаблицы.Найти(ИмяТаблицыОбщееКоличество + Строка(ИндексЗапроса));
			Основная.Операторы[0].Источники.Заменить(1, ИсточникСхемыЗапроса);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	
	Возврат Результат;
	
КонецФункции

Функция ЕстьРеквизиты(МетаданныеОбъекта, РеквизитыОпределители, ОпределяемыйРеквизит)
	
	ЕстьВсеРеквизиты = Истина;
	Для Каждого РеквизитОпределитель Из РеквизитыОпределители Цикл
		ЕстьВсеРеквизиты = ЕстьВсеРеквизиты
			И ЕстьРеквизитУОбъектаИлиТабличнойЧасти(РеквизитОпределитель.Ключ, МетаданныеОбъекта);
	КонецЦикла;
	
	ЕстьВсеРеквизиты = ЕстьВсеРеквизиты
		И ЕстьРеквизитУОбъектаИлиТабличнойЧасти(ОпределяемыйРеквизит, МетаданныеОбъекта);
	
	Возврат ЕстьВсеРеквизиты;
	
КонецФункции

Функция ЕстьРеквизитУОбъектаИлиТабличнойЧасти(ИскомыйРеквизит, МетаданныеОбъекта)
	
	ЧастиРеквизита = СтрРазделить(ИскомыйРеквизит, ".");
	Если ЧастиРеквизита.Количество() = 1 Тогда
		ЕстьРеквизит = ОбщегоНазначения.ЕстьРеквизитОбъекта(ИскомыйРеквизит, МетаданныеОбъекта);
	Иначе
		ЕстьРеквизит = ЕстьТабличнаяЧастьОбъекта(ЧастиРеквизита[0], МетаданныеОбъекта)
			И ЕстьРеквизитТабличнойЧастиОбъекта(ЧастиРеквизита[1], ЧастиРеквизита[0], МетаданныеОбъекта);
	КонецЕсли;
		
	Возврат ЕстьРеквизит;
	
КонецФункции

Функция ЕстьТабличнаяЧастьОбъекта(ИмяРеквизита, МетаданныеОбъекта)

	ТабличныеЧасти = МетаданныеОбъекта.ТабличныеЧасти;
	Возврат Не (ТабличныеЧасти.Найти(ИмяРеквизита) = Неопределено);

КонецФункции

Функция ЕстьРеквизитТабличнойЧастиОбъекта(ИмяРеквизита, ИмяТабличнойЧасти, МетаданныеОбъекта)

	Реквизиты = МетаданныеОбъекта.ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты;
	Возврат Не (Реквизиты.Найти(ИмяРеквизита) = Неопределено);

КонецФункции

#КонецОбласти