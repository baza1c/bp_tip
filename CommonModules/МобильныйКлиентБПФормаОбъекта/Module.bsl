#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

Процедура НастроитьФорму(Форма) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "МКАдаптацияИнициализровано")
		И Форма.МКАдаптацияИнициализровано Тогда
		Возврат;
	КонецЕсли;
	
	Форма.СворачиваниеЭлементовПоВажности =
			СворачиваниеЭлементовФормыПоВажности.Использовать;
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("МКАдаптацияИнициализровано", Новый ОписаниеТипов("Булево")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("МКИменаЭлементовИсключеныИзАдаптации", Новый ОписаниеТипов("Строка")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("МКАдаптацияЦветТекстаЗаголовка", Новый ОписаниеТипов("Цвет")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("МКАдаптацияШрифтЗаголовка", Новый ОписаниеТипов("Шрифт")));
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Форма.МКАдаптацияЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаЗаголовкаМК;
	Форма.МКАдаптацияШрифтЗаголовка = ШрифтыСтиля.ШрифтТекстаЗаголовкаМК;
	Форма.МКАдаптацияИнициализровано = Истина;
	
	Элементы = Форма.Элементы;
	
	//Важность для верхней группы не устанавливать
	ГруппаМКФорма = Элементы.Добавить("ГруппаМКФорма", Тип("ГруппаФормы"), Форма);
	ГруппаМКФорма.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФорма.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФорма.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФорма.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКФормаВажное = Элементы.Добавить("ГруппаМКФормаВажное", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМКФормаВажное.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаВажное.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаВажное.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКФормаВажное.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФормаВажное.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКФормаТабличныеЧасти = Элементы.Добавить("ГруппаМКФормаТабличныеЧасти", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМКФормаТабличныеЧасти.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаТабличныеЧасти.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаТабличныеЧасти.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКФормаТабличныеЧасти.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФормаТабличныеЧасти.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКФормаПрочее = Элементы.Добавить("ГруппаМКФормаПрочее", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМКФормаПрочее.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаПрочее.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаПрочее.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	ГруппаМКФормаПрочее.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФормаПрочее.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКФормаПодвал = Элементы.Добавить("ГруппаМКФормаПодвал", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМКФормаПодвал.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаПодвал.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаПодвал.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКФормаПодвал.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКФормаПодвал.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКФормаСкрыто = Элементы.Добавить("ГруппаМКФормаСкрыто", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМКФормаСкрыто.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКФормаСкрыто.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКФормаСкрыто.Видимость = Ложь;
	
КонецПроцедуры

Процедура АдаптироватьМККоманднаяПанель(Форма, ИменаКомандМК = "") Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМКФорма = Элементы.Найти("ГруппаМКФорма");
	
	ГруппаМККомандыФормы = Элементы.Добавить("ГруппаМККомандыФормы", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМККомандыФормы.Вид = ВидГруппыФормы.КоманднаяПанель;
	ГруппаМККомандыФормы.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	ПодменюМКПоделиться = Элементы.Добавить("ПодменюМКПоделиться", Тип("ГруппаФормы"), ГруппаМККомандыФормы);
	ПодменюМКПоделиться.Вид = ВидГруппыФормы.Подменю;
	ПодменюМКПоделиться.Картинка = БиблиотекаКартинок.ПоделитьсяМК;
	ПодменюМКПоделиться.Отображение = ОтображениеКнопки.Картинка;
	ПодменюМКПоделиться.Заголовок = НСтр("ru = 'Поделиться'");
	
	Форма.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	Форма.СоставКоманднойПанелиНаМобильномУстройстве.Добавить(ГруппаМККомандыФормы);
	
	КоманднаяПанельФормыВерхняя = Элементы.Добавить("ГруппаМККоманднаяПанельФормыВерхняя", Тип("ГруппаФормы"), Форма);
	КоманднаяПанельФормыВерхняя.РастягиватьПоГоризонтали = Истина;
	КоманднаяПанельФормыВерхняя.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	КоманднаяПанельФормыВерхняя.Вид = ВидГруппыФормы.КоманднаяПанель;
	КоманднаяПанельФормыВерхняя.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	Элементы.Переместить(КоманднаяПанельФормыВерхняя, Форма, ГруппаМКФорма);
	
	Если ИменаКомандМК = "" Тогда
		ИменаКомандМК = Новый Массив;
		ИменаКомандМК.Добавить("ФормаПровестиИЗакрыть");
		ИменаКомандМК.Добавить("ФормаЗаписатьИЗакрыть");
		ИменаКомандМК.Добавить("ФормаЗаписать");
	Иначе
		ИменаКомандМК = СтрРазделить(ИменаКомандМК, ",", Ложь);
	КонецЕсли;
	Для Каждого ИмяКоманды из ИменаКомандМК Цикл
		МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормыПоИмени(Элементы, ИмяКоманды, КоманднаяПанельФормыВерхняя);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяКоманды,
			"Отображение",
			ОтображениеКнопки.КартинкаИТекст);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяКоманды,
			"ПоложениеВКоманднойПанели",
			ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели);
	КонецЦикла;
	
	Если ИменаКомандМК.Найти("ФормаПровестиИЗакрыть") <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаПровестиИЗакрыть", "Картинка", БиблиотекаКартинок.ЗаписатьИЗакрыть);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиКомандыВПодменюМКПоделиться(Элементы, КоллекцияКоманд) Экспорт
	
	ПодменюМКПоделиться = Элементы.Найти("ПодменюМКПоделиться");
	ПереместитьЭлементыФормы(Элементы, КоллекцияКоманд, ПодменюМКПоделиться);
	
КонецПроцедуры

Процедура ПеренестиКомандуВПодменюМКПоделиться(Элементы, ЭлементКоманды, ЭлементНиже = Неопределено) Экспорт
	
	Если ЭлементКоманды = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПодменюМКПоделиться = Элементы.Найти("ПодменюМКПоделиться");
	Элементы.Переместить(ЭлементКоманды, ПодменюМКПоделиться, ЭлементНиже);
	
КонецПроцедуры

Процедура ПеренестиКомандыВМККомандыФормы(Элементы, КоллекцияКоманд) Экспорт
	
	ГруппаМККомандыФормы = Элементы.Найти("ГруппаМККомандыФормы");
	
	ПереместитьЭлементыФормы(Элементы, КоллекцияКоманд, ГруппаМККомандыФормы);
	
КонецПроцедуры

Процедура ПеренестиКомандуВМККомандыФормы(Элементы, ЭлементКоманды, ЭлементНиже = Неопределено) Экспорт
	
	Если ЭлементКоманды = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаМККомандыФормы = Элементы.Найти("ГруппаМККомандыФормы");
	Элементы.Переместить(ЭлементКоманды, ГруппаМККомандыФормы, ЭлементНиже);
	
КонецПроцедуры

Процедура ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, ЭлементФормы) Экспорт
	
	ГруппаПолучатель = Элементы.Найти("ГруппаМКФормаВажное");
	
	Элементы.Переместить(ЭлементФормы, ГруппаПолучатель);
	
КонецПроцедуры

Процедура ВстроитьЭлементФормыВГруппаМКФормаТабличныеЧасти(Элементы, ЭлементФормы, ОписаниеГруппыПоля, ЭлементСписок) Экспорт
	
	Если ОписаниеГруппыПоля.ЭлементГруппаПоля <> Неопределено Тогда
		ЭлементГруппаСписка = ОписаниеГруппыПоля.ЭлементГруппаПоля;
	Иначе
		ИмяТабличногоПоля = ЭлементСписок.Имя;
		ЭлементГруппаСписка = Элементы.Найти("ГруппаТабличногоПоля" + ИмяТабличногоПоля);
	КонецЕсли;
	Элементы.Переместить(ЭлементФормы, ЭлементГруппаСписка, ЭлементСписок);
	
КонецПроцедуры

Процедура ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, ЭлементФормы) Экспорт
	
	ГруппаПолучатель = Элементы.Найти("ГруппаМКФормаПрочее");
	
	Элементы.Переместить(ЭлементФормы, ГруппаПолучатель);
	
КонецПроцедуры

Процедура ПеренестиЭлементыВГруппаМКФормаВажное(Элементы, ГруппаЭлементов, ИменаЭлементовИсключений = "") Экспорт
	
	ГруппаПолучатель = Элементы.Найти("ГруппаМКФормаВажное");
	
	КоллекцияЭлементов = ГруппаЭлементов.ПодчиненныеЭлементы;
	
	ЭлементыИсключаемые = СтрРазделить(ИменаЭлементовИсключений, ",", Ложь);
	//Корневой элемент мобильного интерфейса формы
	ЭлементыИсключаемые.Добавить("ГруппаМКФорма");
	
	ПереместитьПодчиненныеЭлементы(Элементы, КоллекцияЭлементов, ГруппаПолучатель, ЭлементыИсключаемые);
	
КонецПроцедуры

Процедура ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы, ГруппаЭлементов, ИменаЭлементовИсключений = "") Экспорт
	
	ГруппаПолучатель = Элементы.Найти("ГруппаМКФормаПрочее");
	
	КоллекцияЭлементов = ГруппаЭлементов.ПодчиненныеЭлементы;
	ГруппаЭлементов.ОтображатьЗаголовок = Ложь;
	
	ЭлементыИсключаемые = СтрРазделить(ИменаЭлементовИсключений, ",", Ложь);
	//Корневой элемент мобильного интерфейса формы
	ЭлементыИсключаемые.Добавить("ГруппаМКФорма");
	
	ПереместитьПодчиненныеЭлементы(Элементы, КоллекцияЭлементов, ГруппаПолучатель, ЭлементыИсключаемые);
	
КонецПроцедуры

Процедура ПеренестиЭлементыВГруппаМКФормаПодвал(Элементы, ГруппаЭлементов, ИменаЭлементовИсключений = "") Экспорт
	
	ГруппаПолучатель = Элементы.Найти("ГруппаМКФормаПодвал");
	
	КоллекцияЭлементов = ГруппаЭлементов.ПодчиненныеЭлементы;
	
	ЭлементыИсключаемые = СтрРазделить(ИменаЭлементовИсключений, ",", Ложь);
	//Корневой элемент мобильного интерфейса формы
	ЭлементыИсключаемые.Добавить("ГруппаМКФорма");
	ЭлементыИсключаемые.Добавить("ГруппаМККоманднаяПанельФормыВерхняя");
	
	ПереместитьПодчиненныеЭлементы(Элементы, КоллекцияЭлементов, ГруппаПолучатель, ЭлементыИсключаемые);
	
КонецПроцедуры

Процедура ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, ЭлементФормы) Экспорт
	
	ГруппаПолучатель = Элементы.Найти("ГруппаМКФормаПодвал");
	
	Элементы.Переместить(ЭлементФормы, ГруппаПолучатель);
	
КонецПроцедуры

Процедура ВстроитьЭлементыВГруппаМКФормаПодвал(Элементы, КоллекцияЭлементов, ИменаЭлементовИсключений = "") Экспорт
	
	ГруппаПолучатель = Элементы.Найти("ГруппаМКФормаПодвал");
	
	ЭлементыИсключаемые = СтрРазделить(ИменаЭлементовИсключений, ",", Ложь);
	//Корневой элемент мобильного интерфейса формы
	ЭлементыИсключаемые.Добавить("ГруппаМКФорма");
	ЭлементыИсключаемые.Добавить("ГруппаМККоманднаяПанельФормыВерхняя");
	
	Для Каждого ВерхняяГруппа из КоллекцияЭлементов Цикл
		Если ТипЗнч(ВерхняяГруппа) = Тип("ГруппаФормы")
			И ЭлементыИсключаемые.Найти(ВерхняяГруппа.Имя) = Неопределено Тогда
			ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, ВерхняяГруппа);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СкрытьЭлементФормы(Элементы, ЭлементФормы) Экспорт
	
	ГруппаМКФормаСкрыто = Элементы.Найти("ГруппаМКФормаСкрыто");
	
	Элементы.Переместить(ЭлементФормы, ГруппаМКФормаСкрыто);
	
КонецПроцедуры

Процедура СкрытьПрочиеЭлементыФормы(Форма, ИменаЭлементовДоступныхВМК = "") Экспорт
	
	ГруппаМКФормаСкрыто = Форма.Элементы.Найти("ГруппаМКФормаСкрыто");
	
	ЭлементыДоступныеВМобильномКлиенте = СтрРазделить(ИменаЭлементовДоступныхВМК, ",", Ложь);
	ЭлементыДоступныеВМобильномКлиенте.Добавить("ГруппаМКФорма");
	ЭлементыДоступныеВМобильномКлиенте.Добавить("ГруппаМККоманднаяПанельФормыВерхняя");
	
	ПереместитьЭлементыФормы(Форма.Элементы,
		Форма.ПодчиненныеЭлементы,
		ГруппаМКФормаСкрыто,
		ЭлементыДоступныеВМобильномКлиенте);
	
КонецПроцедуры

Функция ДобавитьНижняяКоманднаяПанельТабличногоПоля(Форма, ЭлементГруппаСписка)
	
	Элементы = Форма.Элементы;
	
	ИмяКоманднойПанели = "ГруппаМККоманднаяПанельНижняя" + ЭлементГруппаСписка.Имя;
	ГруппаМККоманднаяПанель = Элементы.Добавить(ИмяКоманднойПанели,
		Тип("ГруппаФормы"),
		ЭлементГруппаСписка);
	ГруппаМККоманднаяПанель.Вид = ВидГруппыФормы.КоманднаяПанель;
	ГруппаМККоманднаяПанель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	Возврат ГруппаМККоманднаяПанель;
	
КонецФункции

Функция ПодготовитьТабличноеПолеФормыДокумента(Форма, ЭлементСписок, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля) Экспорт
	
	ЭлементСписок.Шапка = Ложь;
	ЭлементСписок.ПоведениеПриСжатииПоГоризонтали = ПоведениеТаблицыПриСжатииПоГоризонтали.ПереноситьЭлементыПоВажности;
	
	ЭлементГруппаСписка = ПодготовитьТабличноеПолеФормыДокументаСписок(Форма, ЭлементСписок, ОписаниеГруппыПоля);
	
	ПодготовитьТабличноеПолеФормыКоманднаяПанель(Форма, ЭлементСписок, ЭлементГруппаСписка, КомандыПоля);
	
	ПодготовитьТабличноеПолеФормыКолонки(Форма, ЭлементСписок, ОписаниеКолонокПоля);
	
	Возврат ЭлементГруппаСписка;
	
КонецФункции

Процедура ПодготовитьТабличноеПолеФормыКолонки(Форма, ЭлементСписок, ОписаниеКолонокПоля) Экспорт
	
	ИмяТабличногоПоля = ЭлементСписок.Имя;
	
	Элементы = Форма.Элементы;
	
	//Адаптируем колонки
	АдаптироватьМККолонкиТабличногоПоляФормы(ЭлементСписок,
		ОписаниеКолонокПоля.ВажныеКолонки,
		ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки);
	
	//Скроем "лишние" колонки
	ИмяГруппаКолонокМКСкрыто = "ГруппаКолонокМКСкрыто" + ИмяТабличногоПоля;
	ГруппаКолонокМКСкрыто = Элементы.Добавить(ИмяГруппаКолонокМКСкрыто, Тип("ГруппаФормы"), ЭлементСписок);
	ГруппаКолонокМКСкрыто.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокМКСкрыто.Группировка = ГруппировкаКолонок.ВЯчейке;
	ГруппаКолонокМКСкрыто.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	ГруппаКолонокМКСкрыто.ОтображатьВШапке = Ложь;
	ГруппаКолонокМКСкрыто.РастягиватьПоГоризонтали = Ложь;
	ГруппаКолонокМКСкрыто.Видимость = Ложь;
	
	Если Не ПустаяСтрока(ОписаниеКолонокПоля.СкрытьКолонки) Тогда
		ИменаСкрываемыхЭлементов = СтрРазделить(ОписаниеКолонокПоля.СкрытьКолонки,",", Ложь);
		Для Каждого ИмяЭлемента Из ИменаСкрываемыхЭлементов Цикл
			ЭлементКолонкаСписка = Элементы.Найти(ИмяЭлемента);
			Если ЭлементКолонкаСписка = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементКолонкаСписка.Вид = ВидГруппыФормы.ГруппаКолонок Тогда
				//Группы колонок нужно перемещать, начиная с подчиненных
				ПереместитьКолонкиСписка(Элементы,
				ЭлементКолонкаСписка, ГруппаКолонокМКСкрыто, Новый Массив);
			Иначе
				Элементы.Переместить(ЭлементКолонкаСписка, ГруппаКолонокМКСкрыто);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОписаниеКолонокПоля.КолонкаНомерСтроки) Тогда
		ЭлементКолонкаНомерСтроки = Элементы.Найти(ОписаниеКолонокПоля.КолонкаНомерСтроки);
		Если ЭлементКолонкаНомерСтроки <> Неопределено Тогда
			ЭлементКолонкаНомерСтроки.Ширина = 2;
			ЭлементКолонкаНомерСтроки.ОтображатьВШапке = Ложь;
			ЭлементКолонкаНомерСтроки.РастягиватьПоГоризонтали = Ложь;
			ЭлементКолонкаНомерСтроки.Формат = "ЧФ='№ Ч'";
			ЭлементКолонкаНомерСтроки.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПодготовитьТабличноеПолеФормыКоманднаяПанель(Форма, ЭлементСписок, ЭлементГруппаСписка, КомандыПоля) Экспорт
	
	Элементы = Форма.Элементы;
	
	НижняяКоманднаяПанель = ДобавитьНижняяКоманднаяПанельТабличногоПоля(Форма, ЭлементГруппаСписка);
	
	Если КомандыПоля.ЭлементГруппаКоманднойПанели = Неопределено Тогда
		КоманднаяПанель = ЭлементСписок.КоманднаяПанель;
		ЭлементСписок.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Авто;
		ЭлементСписок.РастягиватьПоГоризонтали = Истина;
	Иначе
		КоманднаяПанель = КомандыПоля.ЭлементГруппаКоманднойПанели;
	КонецЕсли;
	КоманднаяПанель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	КомандыМК = Новый Массив;
	КомандаДобавитьСтроку = Элементы.Найти(КомандыПоля.ИмяКомандыДобавитьСтроку);
	Если КомандаДобавитьСтроку <> Неопределено Тогда
		КомандаДобавитьСтроку.Картинка = БиблиотекаКартинок.ДобавитьЭлементСписка;
		КомандаДобавитьСтроку.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КомандаДобавитьСтроку.Заголовок = НСтр("ru = 'Добавить строку'");
		КомандаДобавитьСтроку.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
		Элементы.Переместить(КомандаДобавитьСтроку, НижняяКоманднаяПанель);
		КомандыМК.Добавить(КомандаДобавитьСтроку.Имя);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КомандыПоля.ИмяКомандыПодбор) Тогда
		КомандаПодбор = Элементы.Найти(КомандыПоля.ИмяКомандыПодбор);
		Если КомандаПодбор <> Неопределено Тогда
			КомандаПодбор.Картинка = БиблиотекаКартинок.ПодборМК;
			КомандаПодбор.Отображение = ОтображениеКнопки.КартинкаИТекст;
			КомандаПодбор.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
			КомандыМК.Добавить(КомандаПодбор.Имя);
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КомандыМК, КомандыПоля.КомандыТаблицыПрочие);
	
	СкрытьВидимостьКомандРекурсивно(КоманднаяПанель, КомандыМК)
	
КонецПроцедуры

Функция ПодготовитьТабличноеПолеФормыДокументаСписок(Форма, ЭлементСписок, ОписаниеГруппыПоля)
	
	ИмяТабличногоПоля = ЭлементСписок.Имя;
	
	Элементы = Форма.Элементы;
	ГруппаМКФормаТабличныеЧастиЭлементы = Элементы.Найти("ГруппаМКФормаТабличныеЧасти");
	
	Если ОписаниеГруппыПоля.ЭлементГруппаПоля <> Неопределено Тогда
		ЭлементГруппаСписка = ОписаниеГруппыПоля.ЭлементГруппаПоля;
		Элементы.Переместить(ЭлементГруппаСписка, ГруппаМКФормаТабличныеЧастиЭлементы);
	Иначе
		ИмяГруппаТабличногоПоля = "ГруппаТабличногоПоля" + ИмяТабличногоПоля;
		ЭлементГруппаСписка = Элементы.Добавить(ИмяГруппаТабличногоПоля, Тип("ГруппаФормы"), ГруппаМКФормаТабличныеЧастиЭлементы);
		ЭлементГруппаСписка.Заголовок = ОписаниеГруппыПоля.ЗаголовокГруппы;
		ЭлементГруппаСписка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Элементы.Переместить(ЭлементСписок, ЭлементГруппаСписка);
	КонецЕсли;
	ЭлементГруппаСписка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементГруппаСписка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ЭлементГруппаСписка.ОтображатьЗаголовок = Истина;
	ЭлементГруппаСписка.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	ЭлементГруппаСписка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ЭлементГруппаСписка.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	
	Если ЗначениеЗаполнено(ОписаниеГруппыПоля.ПутьКДаннымЗаголовка) Тогда
		ЭлементГруппаСписка.ПутьКДаннымЗаголовка = ОписаниеГруппыПоля.ПутьКДаннымЗаголовка;
	КонецЕсли;
	
	Объект = Форма.Объект;
	Если Объект.Ссылка.Пустая() И ОписаниеГруппыПоля.ПоказатьГруппуНового Тогда
		ЭлементГруппаСписка.Показать();
	Иначе
		ЭлементГруппаСписка.Скрыть();
	КонецЕсли;
	
	Возврат ЭлементГруппаСписка;

КонецФункции

Функция ПодготовитьТабличноеПолеФормыРасшифровки(Форма, ЭлементСписок, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля) Экспорт
	
	ИмяТабличногоПоля = ЭлементСписок.Имя;
	
	Элементы = Форма.Элементы;
	
	ЭлементСписок.Шапка = Ложь;
	ЭлементСписок.ПоведениеПриСжатииПоГоризонтали = ПоведениеТаблицыПриСжатииПоГоризонтали.ПереноситьЭлементыПоВажности;
	
	ЭлементГруппаСписка = ОписаниеГруппыПоля.ЭлементГруппаПоля;
	
	ПодготовитьТабличноеПолеФормыКоманднаяПанель(Форма, ЭлементСписок, ЭлементГруппаСписка, КомандыПоля);
	
	ПодготовитьТабличноеПолеФормыКолонки(Форма, ЭлементСписок, ОписаниеКолонокПоля);
	
	Возврат ЭлементГруппаСписка;
	
КонецФункции

Процедура СкрытьВидимостьКомандРекурсивно(ГруппаКоманднойПанели, КомандыМК) Экспорт
	
	Для Каждого Элемент Из ГруппаКоманднойПанели.ПодчиненныеЭлементы Цикл
		Если КомандыМК.Найти(Элемент.Имя) = Неопределено Тогда
			//Управляемой видимостью, т.к. проблема с переносом подчиненных команд
			Если Элемент.Вид = ВидГруппыФормы.ГруппаКнопок
				Или Элемент.Вид = ВидГруппыФормы.Подменю Тогда
				Для Каждого ПодчиненныйЭлемент Из Элемент.ПодчиненныеЭлементы Цикл
					Если КомандыМК.Найти(ПодчиненныйЭлемент.Имя) = Неопределено Тогда
						ПодчиненныйЭлемент.Видимость = Ложь;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Элемент.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйОписаниеКомандТабличноеПолеФормы() Экспорт
	
	КомандыМК = Новый Структура;
	КомандыМК.Вставить("ЭлементГруппаКоманднойПанели");
	КомандыМК.Вставить("ИмяКомандыПодбор", "");
	КомандыМК.Вставить("ИмяКомандыДобавитьСтроку", "");
	КомандыМК.Вставить("КомандыТаблицыПрочие", Новый Массив);
	
	Возврат КомандыМК;
	
КонецФункции

Функция НовыйОписаниеГруппыТабличноеПолеФормы() Экспорт
	
	ОписаниеГруппы = Новый Структура;
	ОписаниеГруппы.Вставить("ЭлементГруппаПоля");
	//Заполнить если элемент списка не обернут группой
	ОписаниеГруппы.Вставить("ЗаголовокГруппы");
	ОписаниеГруппы.Вставить("ПутьКДаннымЗаголовка");
	
	ОписаниеГруппы.Вставить("ПоказатьГруппуНового", Ложь); //Разворачивать группу поля для новых объектов
	
	Возврат ОписаниеГруппы;
	
КонецФункции

Функция НовыйОписаниеКолонокПоляТабличноеПолеФормы() Экспорт
	
	ОписаниеКолонокПоля = Новый Структура;
	ОписаниеКолонокПоля.Вставить("КолонкаНомерСтроки");
	ОписаниеКолонокПоля.Вставить("ВажныеКолонки", "");
	ОписаниеКолонокПоля.Вставить("УстановитьВерхниеЗаголовки", "");
	ОписаниеКолонокПоля.Вставить("СкрытьКолонки", "");
	
	Возврат ОписаниеКолонокПоля;
	
КонецФункции

Функция УстановитьИсключенияИзАдаптации(Форма, Исключения = "") Экспорт
	Форма.МКИменаЭлементовИсключеныИзАдаптации = Исключения;
КонецФункции

Процедура ОформитьАкцентнуюКнопкуФормы(КоллекцияЭлементов, ШиринаЭлемента = 0, РастягиватьПоГоризонтали = Истина) Экспорт
	
	Для каждого ЭлементФормы Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементФормы) <> Тип("КнопкаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементФормы.ЦветФона = ЦветаСтиля.ЦветТекстаКнопки;
		ЭлементФормы.ЦветТекста = ЦветаСтиля.ЦветФонаКнопки;
		ЭлементФормы.Фигура = ФигураКнопки.Овал;
		ЭлементФормы.Шрифт = ШрифтыСтиля.КрупныйШрифтТекста;
		ЭлементФормы.Ширина = ШиринаЭлемента;
		ЭлементФормы.РастягиватьПоГоризонтали = РастягиватьПоГоризонтали;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОформитьНейтральнуюКнопкуФормы(КоллекцияЭлементов) Экспорт
	
	Для каждого ЭлементФормы Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементФормы) <> Тип("КнопкаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементФормы.Фигура = ФигураКнопки.Овал;
		ЭлементФормы.Ширина = 20;
		ЭлементФормы.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
		ЭлементФормы.ЦветФона = ЦветаСтиля.ЦветФонаКнопки;
		ЭлементФормы.ЦветТекста = ЦветаСтиля.ЦветТекстаКнопки;
	КонецЦикла;
	
КонецПроцедуры

Процедура УправлениеДоступностьюПолейСИсключениямиРекурсивно(ЭлементыФормы, КоллекцияЭлементовИсключений) Экспорт
	
	Для каждого Элемент Из ЭлементыФормы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			Если КоллекцияЭлементовИсключений.Найти(Элемент) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Элемент.Доступность = Ложь;
		КонецЕсли;
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			УправлениеДоступностьюПолейСИсключениямиРекурсивно(Элемент.ПодчиненныеЭлементы, КоллекцияЭлементовИсключений);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьПоложениеЗаголовкаФлажкаРекурсивноМК(ЭлементыФормы) Экспорт
	
	Для каждого Элемент Из ЭлементыФормы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			Если Элемент.Вид = ВидПоляФормы.ПолеФлажка Тогда
				Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
				Элемент.ВидФлажка = ВидФлажка.Выключатель;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			УстановитьПоложениеЗаголовкаФлажкаРекурсивноМК(Элемент.ПодчиненныеЭлементы);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьПодсказкиФлажковВДекорациюРекурсивно(ВсеЭлементыФормы, КоллекцияЭлементов) Экспорт
	
	Для каждого Элемент Из КоллекцияЭлементов Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			Если Элемент.Вид = ВидПоляФормы.ПолеФлажка Тогда
				Если ЗначениеЗаполнено(Элемент.Подсказка) Тогда
					Элемент.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
					
					ИмяГруппы = СтрШаблон(НСтр("ru='ГруппаМК%1'"), Элемент.Имя);
					ИмяДекорации = СтрШаблон(НСтр("ru='ПодсказкаМК%1'"), Элемент.Имя);
					
					НоваяГруппа = ВсеЭлементыФормы.Добавить(ИмяГруппы,
								Тип("ГруппаФормы"),
								Элемент.Родитель);
					НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
					НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
					НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
					НоваяГруппа.ОтображатьЗаголовок = Ложь;
					
					ВсеЭлементыФормы.Переместить(НоваяГруппа, Элемент.Родитель, Элемент);
					ВсеЭлементыФормы.Переместить(Элемент, НоваяГруппа);
					
					НоваяДекорация = ВсеЭлементыФормы.Добавить(ИмяДекорации,
								Тип("ДекорацияФормы"),
								НоваяГруппа);
					НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
					НоваяДекорация.Заголовок = Элемент.Подсказка;
					НоваяДекорация.ЦветТекста = ЦветаСтиля.ЦветРамкиКнопки;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			УстановитьПодсказкиФлажковВДекорациюРекурсивно(ВсеЭлементыФормы, Элемент.ПодчиненныеЭлементы);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область ФормаДокумента

Процедура АдаптироватьНомерДатаФормаДокумента(Элементы, ГруппаНомерДата) Экспорт
	
	ГруппаНомерДата.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаНомерДата.СквозноеВыравнивание = СквозноеВыравнивание.НеИспользовать;
	ГруппаНомерДата.Объединенная = Ложь;
	
	ГруппаМКДатаНомер = Элементы.Добавить("ГруппаМКДатаНомер", Тип("ГруппаФормы"), ГруппаНомерДата);
	ГруппаМКДатаНомер.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКДатаНомер.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаМКДатаНомер.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКДатаНомер.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКДатаНомер.ОтображатьЗаголовок = Ложь;
	
	ГруппаМКДатаСостояниеДокумента = Элементы.Добавить("ГруппаМКДатаСостояниеДокумента", Тип("ГруппаФормы"), ГруппаМКДатаНомер);
	ГруппаМКДатаСостояниеДокумента.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМКДатаСостояниеДокумента.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаМКДатаСостояниеДокумента.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	ГруппаМКДатаСостояниеДокумента.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМКДатаСостояниеДокумента.ОтображатьЗаголовок = Ложь;
	
	Элементы.Дата.Заголовок = НСтр("ru = 'Дата'");
	
	Элементы.Номер.РастягиватьПоГоризонтали = Истина;
	Элементы.Номер.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	Элементы.Переместить(Элементы.Номер, ГруппаМКДатаНомер, ГруппаМКДатаСостояниеДокумента);
	Элементы.Переместить(Элементы.Дата, ГруппаМКДатаСостояниеДокумента);
	Элементы.Переместить(Элементы.СостояниеДокумента, ГруппаМКДатаСостояниеДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область МногострочноеТабличноеПоле

Процедура ПодготовитьТабличноеПолеФормыДокументаМногострочное(Форма, ИмяРеквизитаСписок = "Список") Экспорт
	
	Элементы = Форма.Элементы;
	
	ЭлементСписок = Элементы[ИмяРеквизитаСписок];
	
	//Табличное поле
	ЭлементСписок.ЗапросОбновления = СпособЗапросаОбновления.ПотянутьСверху;
	ЭлементСписок.ПоведениеПриСжатииПоГоризонтали = 
		ПоведениеТаблицыПриСжатииПоГоризонтали.СкрыватьЭлементыПоВажности;
	
	//Список
	ЭлементСписок.ПутьКДаннымКартинкиСтроки = "";
	
	ГруппаКолонокМК_Корень = ДобавитьКорневуюГруппуСписка(Форма, ЭлементСписок);
	
	ГруппаКолонокМК_Строки = ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Строки_" + ИмяРеквизитаСписок,
		ГруппировкаКолонок.Вертикальная,
		ГруппаКолонокМК_Корень);
	
	ГруппаКолонокМК_Строка1_ = ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Строка1_" + ИмяРеквизитаСписок,
		ГруппировкаКолонок.Горизонтальная,
		ГруппаКолонокМК_Строки);
		
	ГруппаКолонокМК_Строка1_.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	ГруппаКолонокМК_Строка2 = ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Строка2_" + ИмяРеквизитаСписок,
		ГруппировкаКолонок.Горизонтальная,
		ГруппаКолонокМК_Строки);
	ГруппаКолонокМК_Строка2.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	ГруппаКолонокМК_Строка3 = ДобавитьГруппуКолонок(Форма,
		"ГруппаКолонокМК_Строка3_" + ИмяРеквизитаСписок,
		ГруппировкаКолонок.ВЯчейке,
		ГруппаКолонокМК_Строки);
	ГруппаКолонокМК_Строка3.ВажностьПриОтображении = ВажностьПриОтображении.ОченьНизкая;
	
КонецПроцедуры

#КонецОбласти

#Область ФормаОтчета

Процедура АдаптироватьМККоманднаяПанельФормаОтчета(Форма, ИменаКомандМК = "") Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМКФорма = Элементы.Найти("ГруппаМКФорма");
	
	ГруппаМККомандыФормы = Элементы.Добавить("ГруппаМККомандыФормы", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМККомандыФормы.Вид = ВидГруппыФормы.КоманднаяПанель;
	ГруппаМККомандыФормы.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	ПодменюМКПоделиться = Элементы.Добавить("ПодменюМКПоделиться", Тип("ГруппаФормы"), ГруппаМККомандыФормы);
	ПодменюМКПоделиться.Вид = ВидГруппыФормы.Подменю;
	ПодменюМКПоделиться.Картинка = БиблиотекаКартинок.Поделиться;
	ПодменюМКПоделиться.Отображение = ОтображениеКнопки.Картинка;
	ПодменюМКПоделиться.Заголовок = НСтр("ru = 'Поделиться'");

	Форма.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	Форма.СоставКоманднойПанелиНаМобильномУстройстве.Добавить(ГруппаМККомандыФормы);
	
	КоманднаяПанельФормыВерхняя = Элементы.Добавить("ГруппаМККоманднаяПанельФормыВерхняя", Тип("ГруппаФормы"), Форма);
	КоманднаяПанельФормыВерхняя.РастягиватьПоГоризонтали = Истина;
	КоманднаяПанельФормыВерхняя.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	КоманднаяПанельФормыВерхняя.Вид = ВидГруппыФормы.КоманднаяПанель;
	КоманднаяПанельФормыВерхняя.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	Элементы.Переместить(КоманднаяПанельФормыВерхняя, Форма, ГруппаМКФорма);
	
	ИмяКоманыдБыстрыеНастройки = "ПоказатьБыстрыеНастройки";
	
	ИменаКомандМК = СтрРазделить(ИменаКомандМК, ",", Ложь);
	ИменаКомандМК.Добавить("Сформировать");
	ИменаКомандМК.Добавить("СформироватьОтчет");
	ИменаКомандМК.Добавить(ИмяКоманыдБыстрыеНастройки);
	Для Каждого ИмяКоманды из ИменаКомандМК Цикл
		МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормыПоИмени(Элементы, ИмяКоманды, КоманднаяПанельФормыВерхняя);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяКоманды,
			"Отображение",
			ОтображениеКнопки.КартинкаИТекст);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяКоманды,
			"ПоложениеВКоманднойПанели",
			ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели);
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяКоманыдБыстрыеНастройки,
			"Заголовок",
			НСтр("ru = 'Настройки'"));
	
КонецПроцедуры

Процедура АдаптироватьКомандуСохранитьВМенюФормаОтчета(Элементы, ИмяЭлемента = "Сохранить") Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		ИмяЭлемента,
		"Картинка",
		БиблиотекаКартинок.ПередачаДанныхНаУстройствеМКБелая);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		ИмяЭлемента,
		"Заголовок",
		НСтр("ru = 'Отправить в ...'"));
	
КонецПроцедуры

#КонецОбласти

#Область ФормаПечатиДокумента

Процедура АдаптироватьМККоманднаяПанельФормаПечатиДокументов(Форма, ИменаКомандМК = "") Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМКФорма = Элементы.Найти("ГруппаМКФорма");
	
	ГруппаМККомандыФормы = Элементы.Добавить("ГруппаМККомандыФормы", Тип("ГруппаФормы"), ГруппаМКФорма);
	ГруппаМККомандыФормы.Вид = ВидГруппыФормы.КоманднаяПанель;
	ГруппаМККомандыФормы.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	Форма.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	Форма.СоставКоманднойПанелиНаМобильномУстройстве.Добавить(ГруппаМККомандыФормы);
	
	КоманднаяПанельФормыВерхняя = Элементы.Добавить("ГруппаМККоманднаяПанельФормыВерхняя", Тип("ГруппаФормы"), Форма);
	КоманднаяПанельФормыВерхняя.РастягиватьПоГоризонтали = Истина;
	КоманднаяПанельФормыВерхняя.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	КоманднаяПанельФормыВерхняя.Вид = ВидГруппыФормы.КоманднаяПанель;
	КоманднаяПанельФормыВерхняя.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	Элементы.Переместить(КоманднаяПанельФормыВерхняя, Форма, ГруппаМКФорма);
	
	ИменаКомандМК = СтрРазделить(ИменаКомандМК, ",", Ложь);
	Для Каждого ИмяКоманды из ИменаКомандМК Цикл
		МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормыПоИмени(Элементы, ИмяКоманды, КоманднаяПанельФормыВерхняя);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяКоманды,
			"Отображение",
			ОтображениеКнопки.КартинкаИТекст);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяКоманды,
			"ПоложениеВКоманднойПанели",
			ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеМетоды

Функция ДобавитьКорневуюГруппуСписка(Форма, ЭлементСписок) Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаКолонокМК_Корень = Элементы.Добавить("ГруппаКолонокМК_Корень_" + ЭлементСписок.Имя, Тип("ГруппаФормы"), ЭлементСписок);
	ГруппаКолонокМК_Корень.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокМК_Корень.Группировка = ГруппировкаКолонок.Горизонтальная;
	
	Возврат ГруппаКолонокМК_Корень;
	
КонецФункции

#КонецОбласти

Функция ДобавитьГруппуКолонок(Форма, ИмяГруппы, Группировка, КорневойЭлемент) Экспорт
	
	Элементы = Форма.Элементы;
	ГруппаКолонокМК = Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), КорневойЭлемент);
	ГруппаКолонокМК.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокМК.Группировка = Группировка;
	ГруппаКолонокМК.РастягиватьПоГоризонтали = Истина;
	
	Возврат ГруппаКолонокМК;
	
КонецФункции

Процедура ВстроитьЭлементВГруппуМК_ТабличноеПоле_Строка(Форма, ЭлементФормы, НомерСтроки, ИмяРеквизитаСписок = "Список") Экспорт
	
	Элементы = Форма.Элементы;
	
	КорневойЭлемент = Элементы.Найти("ГруппаКолонокМК_Строка" + НомерСтроки + "_" + ИмяРеквизитаСписок);
	
	Элементы.Переместить(ЭлементФормы, КорневойЭлемент);
	
КонецПроцедуры

Процедура АдаптироватьМККолонкиТабличногоПоляФормы(ЭлементФормы, ВажныеКолонки = "", КолонкиВерхнийЗаголовок = "") Экспорт
	
	ИменаВажныхЭлементов = СтрРазделить(ВажныеКолонки, ",", Ложь);
	ИменаЭлементовВерхнийЗаголовок = СтрРазделить(КолонкиВерхнийЗаголовок, ",", Ложь);
	
	АдаптироватьМККолонкиТабличногоПоляФормыРекурсивно(ЭлементФормы.ПодчиненныеЭлементы, ИменаЭлементовВерхнийЗаголовок, ИменаВажныхЭлементов)
	
КонецПроцедуры

Процедура УстановитьВажностьКолонокТабличногоПоляФормы(КоллекцияЭлементов, ИменаВажныхЭлементов) Экспорт
	
	Для каждого ПодчиненныйЭлемент Из КоллекцияЭлементов Цикл
		
		Если ИменаВажныхЭлементов.Найти(ПодчиненныйЭлемент.Имя) <> Неопределено Тогда
			Важность = ВажностьПриОтображении.ОченьВысокая;
		Иначе
			Важность = ВажностьПриОтображении.ОченьНизкая;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура АдаптироватьМККолонкиТабличногоПоляФормыРекурсивно(КоллекцияЭлементов, ИменаЭлементовВерхнийЗаголовок, ИменаВажныхЭлементов)
	
	Для каждого ПодчиненныйЭлемент Из КоллекцияЭлементов Цикл
		
		Если ИменаВажныхЭлементов.Найти(ПодчиненныйЭлемент.Имя) <> Неопределено Тогда
			Важность = ВажностьПриОтображении.ОченьВысокая;
		Иначе
			Важность = ВажностьПриОтображении.ОченьНизкая;
		КонецЕсли;
		
		Если ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок Тогда
			АдаптироватьМКГруппуКолонокТабличногоПоля(ПодчиненныйЭлемент, Важность);
			АдаптироватьМККолонкиТабличногоПоляФормыРекурсивно(ПодчиненныйЭлемент.ПодчиненныеЭлементы,
				ИменаЭлементовВерхнийЗаголовок,
				ИменаВажныхЭлементов);
		Иначе
			ЭтоПолеВерхнийЗаголовок = ИменаЭлементовВерхнийЗаголовок.Найти(ПодчиненныйЭлемент.Имя) <> Неопределено;
			АдаптироватьМККолонкуТабличногоПоля(ПодчиненныйЭлемент,
					ЭтоПолеВерхнийЗаголовок,
					Важность);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура АдаптироватьМКГруппуКолонокТабличногоПоля(ЭлементФормы, ВажностьПриОтображении)
	
	ЭлементФормы.ОтображатьВШапке = Ложь;
	ЭлементФормы.РастягиватьПоГоризонтали = Истина;
	ЭлементФормы.ВажностьПриОтображении = ВажностьПриОтображении;
	
КонецПроцедуры

Процедура АдаптироватьМККолонкуТабличногоПоля(ЭлементФормы, ЭтоПолеВерхнийЗаголовок, ВажностьПриОтображении) Экспорт
	
	//Длинное поле, обычно ссылочное
	Если ЭтоПолеВерхнийЗаголовок Тогда 
		ЭлементФормы.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаЗаголовкаМК;
		ЭлементФормы.ШрифтЗаголовка = ШрифтыСтиля.ШрифтТекстаЗаголовкаМК;
		ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		ЭлементФормы.РастягиватьПоГоризонтали = Истина;
		ЭлементФормы.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Авто;
		
	ИначеЕсли ЭлементФормы.Вид <> ВидПоляФормы.ПолеФлажка Тогда
		ЭлементФормы.АвтоВысотаЯчейки = Ложь;
		ЭлементФормы.Ширина = 0;
		ЭлементФормы.Высота = 0;
		
		ЭлементФормы.РастягиватьПоГоризонтали = Ложь;
		ЭлементФормы.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	КонецЕсли;
	
	
	ЭлементФормы.ОтображатьВШапке = Истина;
	ЭлементФормы.ВажностьПриОтображении = ВажностьПриОтображении;
	
КонецПроцедуры

Процедура АдаптироватьКонтекстноеМеню(Форма, ИмяЭлементаГруппаКонтекстногоМеню = "", ИмяЭлементаСписок = "Список") Экспорт
	
	Элементы = Форма.Элементы;
	ЭлементСписок = Элементы[ИмяЭлементаСписок];
	
	ИмяГруппыКнопок = ИмяЭлементаСписок + "ГруппаКнопокМККонтекстСкрыто";
	ГруппаКнопокМККонтекстСкрыто = Элементы.Добавить(ИмяГруппыКнопок, Тип("ГруппаФормы"), ЭлементСписок.КонтекстноеМеню);
	ГруппаКнопокМККонтекстСкрыто.Вид = ВидГруппыФормы.ГруппаКнопок;
	ГруппаКнопокМККонтекстСкрыто.Видимость = Ложь;
	
	ИменаКонтекстныхКомандМК = Новый Массив;
	ИменаКонтекстныхКомандМК.Добавить(ИмяЭлементаСписок + "КонтекстноеМенюСкопировать");
	ИменаКонтекстныхКомандМК.Добавить(ИмяЭлементаСписок + "КонтекстноеМенюУстановитьПометкуУдаления");
	ИменаКонтекстныхКомандМК.Добавить(ИмяЭлементаСписок + "КонтекстноеМенюУдалить");
	ИменаКонтекстныхКомандМК.Добавить(ИмяГруппыКнопок);
	
	Если ЗначениеЗаполнено(ИмяЭлементаГруппаКонтекстногоМеню) Тогда
		ЭлементГруппаКонтекстногоМеню = Элементы.Найти(ИмяЭлементаГруппаКонтекстногоМеню);
		Если ЭлементГруппаКонтекстногоМеню = Неопределено Тогда
			ВызватьИсключение "Не найдена группа контекстного меню с именем: " + ИмяЭлементаГруппаКонтекстногоМеню;
		КонецЕсли;
		ИменаКонтекстныхКомандМК.Добавить(ИмяЭлементаГруппаКонтекстногоМеню);
		ПереместитьПодчиненныеЭлементы(Элементы,
			ЭлементГруппаКонтекстногоМеню.ПодчиненныеЭлементы,
			ГруппаКнопокМККонтекстСкрыто,
			ИменаКонтекстныхКомандМК);
	КонецЕсли;
		
	ПереместитьПодчиненныеЭлементы(Элементы,
		ЭлементСписок.КонтекстноеМеню.ПодчиненныеЭлементы,
		ГруппаКнопокМККонтекстСкрыто,
		ИменаКонтекстныхКомандМК);
		
КонецПроцедуры

Процедура ПереместитьКолонкиСписка(ЭлементыФормы, ЭлементСписок, ГруппаПолучатель, ИменаЭлементовИсключений) Экспорт
	
	ПереместитьПодчиненныеЭлементыРекурсивно(ЭлементыФормы,
		ЭлементСписок.ПодчиненныеЭлементы, ГруппаПолучатель, ИменаЭлементовИсключений);
		
КонецПроцедуры

Процедура ПереместитьЭлементФормыПоИмени(ЭлементыФормы, ИмяЭлемента, ГруппаПолучатель, ЭлементНиже = Неопределено) Экспорт
	
	ЭлементФормы = ЭлементыФормы.Найти(ИмяЭлемента);
	Если ЭлементФормы <> Неопределено Тогда
		ЭлементыФормы.Переместить(ЭлементФормы, ГруппаПолучатель, ЭлементНиже);
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПереместитьЭлементФормы(ЭлементыФормы, Элемент, ГруппаПолучатель, ЭлементНиже = Неопределено) Экспорт
	
	Если Элемент = Неопределено Или ГруппаПолучатель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.Переместить(Элемент, ГруппаПолучатель, ЭлементНиже);
	
КонецПроцедуры

Процедура ПереместитьЭлементыФормы(ЭлементыФормы, КоллекцияЭлементов, ГруппаПолучатель, ИменаЭлементовИсключений = Неопределено) Экспорт
	
	Если ИменаЭлементовИсключений = Неопределено Тогда
		ИменаЭлементовИсключений = Новый Массив;
	КонецЕсли;
	
	ПереместитьПодчиненныеЭлементы(ЭлементыФормы,
		КоллекцияЭлементов,
		ГруппаПолучатель,
		ИменаЭлементовИсключений);
	
КонецПроцедуры

Процедура ПереместитьПодчиненныеЭлементы(Элементы, КоллекцияЭлементов,
		ГруппаПолучатель,
		ИменаЭлементовИсключений)
	
	КоличествоЭлементов = КоллекцияЭлементов.Количество();
	ТекущийИндекс = 0;
	Пока ТекущийИндекс < КоличествоЭлементов Цикл
		ПодчиненныйЭлемент = КоллекцияЭлементов.Получить(ТекущийИндекс);
		Если ИменаЭлементовИсключений.Найти(ПодчиненныйЭлемент.Имя) <> Неопределено Тогда
			ТекущийИндекс = ТекущийИндекс + 1;
			Продолжить;
		КонецЕсли;
		
		Элементы.Переместить(ПодчиненныйЭлемент, ГруппаПолучатель);
		КоличествоЭлементов = КоллекцияЭлементов.Количество();
	КонецЦикла;
	
КонецПроцедуры

Процедура ПереместитьПодчиненныеЭлементыРекурсивно(Элементы, КоллекцияЭлементов,
		ГруппаПолучатель,
		ИменаЭлементовИсключений)
		
	КоличествоЭлементов = КоллекцияЭлементов.Количество();
	ТекущийИндекс = 0;
	Пока ТекущийИндекс < КоличествоЭлементов Цикл
		ПодчиненныйЭлемент = КоллекцияЭлементов.Получить(ТекущийИндекс);
		Если ИменаЭлементовИсключений.Найти(ПодчиненныйЭлемент.Имя) <> Неопределено
			Или Типзнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы") Тогда
			ТекущийИндекс = ТекущийИндекс + 1;
			Продолжить;
		КонецЕсли;
		
		Если ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок
			Или ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа
			Или ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ГруппаКнопок Тогда
			ПереместитьПодчиненныеЭлементыРекурсивно(Элементы,
				ПодчиненныйЭлемент.ПодчиненныеЭлементы,
				ГруппаПолучатель,
				ИменаЭлементовИсключений);
			//Группы не переносим - оставляем пустыми
			ТекущийИндекс = ТекущийИндекс + 1;
		Иначе
			Элементы.Переместить(ПодчиненныйЭлемент, ГруппаПолучатель);
		КонецЕсли;
		КоличествоЭлементов = КоллекцияЭлементов.Количество();
	КонецЦикла;
	
КонецПроцедуры

Процедура СнятьДоступностьЭлементовФормы(КоллекцияЭлементов, ИменаЭлементовИсключений = "") Экспорт
	
	Исключения = СтрРазделить(ИменаЭлементовИсключений, ",", Ложь);
	Для Каждого Элемент Из КоллекцияЭлементов Цикл
		Если Исключения.Найти(Элемент.Имя) = Неопределено Тогда
			Элемент.Доступность = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьКнопкуКоманднойПанели(Форма, ИмяКоманды, Заголовок, Картинка, ГруппаРазмещения, Доступность = Истина) Экспорт
	
	Элементы = Форма.Элементы;
	Команды = Форма.Команды;
	
	//Командная панель
	КомандаМК = Команды.Добавить("КомандаМК" + ИмяКоманды);
	КомандаМК.Заголовок = Заголовок;
	КомандаМК.Действие  = "Подключаемый_КомандаМК" + ИмяКоманды;
	
	КнопкаМК = Элементы.Добавить("КнопкаМК" + ИмяКоманды,
		Тип("КнопкаФормы"),
		ГруппаРазмещения);
	КнопкаМК.ИмяКоманды = КомандаМК.Имя;
	КнопкаМК.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаМК.Отображение = ?(Заголовок = "", ОтображениеКнопки.Картинка, ОтображениеКнопки.КартинкаИТекст);
	КнопкаМК.Доступность = Доступность;
	КнопкаМК.Картинка = ?(Не Доступность, БиблиотекаКартинок.Пустая, Картинка);
	
	Возврат КнопкаМК;
КонецФункции

#КонецОбласти

Процедура ОформитьАкцентнуюКнопкуПанели(КоллекцияЭлементов, ШиринаЭлемента = 0) Экспорт
	
	Для каждого ЭлементФормы Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементФормы) <> Тип("КнопкаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементФормы.ЦветФона = ЦветаСтиля.ЦветТекстаКнопки;
		ЭлементФормы.ЦветТекста = ЦветаСтиля.ЦветФонаКнопки;
		ЭлементФормы.Фигура = ФигураКнопки.Овал;
		ЭлементФормы.Шрифт = ШрифтыСтиля.КрупныйШрифтТекста;
		ЭлементФормы.Ширина = ШиринаЭлемента;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОформитьНейтральнуюКнопкуПанели(КоллекцияЭлементов, ШиринаЭлемента = 0) Экспорт
	
	Для каждого ЭлементФормы Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементФормы) <> Тип("КнопкаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементФормы.Фигура = ФигураКнопки.Овал;
		ЭлементФормы.Ширина = ШиринаЭлемента;
		ЭлементФормы.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
		ЭлементФормы.ЦветФона = ЦветаСтиля.ЦветФонаКнопки;
		ЭлементФормы.ЦветТекста = ЦветаСтиля.ЦветТекстаКнопки;
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьПредупреждениеОбОграничениях(Форма, Текст = "") Экспорт
	
	Элементы = Форма.Элементы;
	
	ГруппаМК = Элементы.Добавить("ГруппаМКПредупреждениеКонтейнер", Тип("ГруппаФормы"), Форма);
	ГруппаМК.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМК.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаМК.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМК.ОтображатьЗаголовок = Ложь;
	ГруппаМК.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
	
	Элемент = Элементы.Добавить("НадписьМКПредупреждениеОбОграниченияхКартинка", Тип("ДекорацияФормы"), ГруппаМК);
	Элемент.Вид = ВидДекорацииФормы.Картинка;
	Элемент.Картинка = БиблиотекаКартинок.Вопрос32;
	
	Элемент = Элементы.Добавить("НадписьМКПредупреждениеОбОграничениях", Тип("ДекорацияФормы"), ГруппаМК);
	Элемент.Вид = ВидДекорацииФормы.Надпись;
	Элемент.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
	Элемент.РастягиватьПоГоризонтали = Истина;
	Если ПустаяСтрока(Текст) тогда
		ТекстЗаголовка = Новый ФорматированнаяСтрока("Некоторые функции доступны только в версии для компьютера");
	Иначе
		ТекстЗаголовка = Новый ФорматированнаяСтрока(Текст);
	КонецЕсли;
	Элемент.Заголовок = ТекстЗаголовка;
	
	Возврат ГруппаМК;
	
КонецФункции

// Процедура вызывается при создании на сервере формы регламентированного отчета.
//
// Параметры:
//  Форма     - УправляемаФорма - форма, из которой вызывается процедура.
//
Процедура ФормаРегламентированнойОтчетности_ПриСозданииНаСервере(Форма) Экспорт
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	Форма.СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ГруппаГлавногоМеню = Элементы.Найти("ГруппаГлавногоМеню");
	Если ГруппаГлавногоМеню <> Неопределено Тогда
		Предупреждение = МобильныйКлиентБПФормаОбъекта.СоздатьПредупреждениеОбОграничениях(Форма,
			"Сформировать отчетность и прочесть письма от контролирующих органов можно в версии для компьютера");
		Элементы.Переместить(Предупреждение, Форма, ГруппаГлавногоМеню);
		ГруппаСсылокРазделы = Элементы.Найти("ГруппаСсылокРазделы");
		Если ГруппаСсылокРазделы <> Неопределено Тогда
			ГруппаСсылокРазделы.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
			ГруппаСсылокРазделы.ОтображатьЗаголовок = Истина;
			ГруппаСсылокРазделы.Заголовок = НСтр("ru = 'Разделы'");
		КонецЕсли;
	КонецЕсли;
	
	ИменаНедоступныеТабличныеПоля = "Новое,Отчеты,Уведомления,Письма,Сверки,ЕГРЮЛ,Требования,ВходящиеПисьма,Рассылки";
	Для Каждого ИмяТабличногоПоля Из СтрРазделить(ИменаНедоступныеТабличныеПоля,",", Ложь) Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяТабличногоПоля,
			"ПоложениеКоманднойПанели",
			ПоложениеКоманднойПанелиЭлементаФормы.Нет);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяТабличногоПоля + "КонтекстноеМеню",
			"Доступность",
			Ложь);
	КонецЦикла;
	
	ИменаНедоступныеРазделы = "СтраницаНастройки,СтраницаЕГРЮЛ";
	Для Каждого ИмяРаздела Из СтрРазделить(ИменаНедоступныеРазделы,",", Ложь) Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			ИмяРаздела,
			"Доступность",
			Ложь);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 // Формирует текстовое описание технической информации.
// 
// Возвращаемое значение:
//  Строка - содержание файла технической информации.
//
Функция ТекстТехническойИнформации() Экспорт
	
	Результат = Новый Массив;
	
	ДанныеТехническойИнформации    = СообщенияВСлужбуТехническойПоддержкиКлиентСервер.ОписаниеТехническойИнформации();
	НастройкиТехническойИнформации = СообщенияВСлужбуТехническойПоддержкиКлиентСервер.НастройкиТехническойИнформации();
	
	// Обращение в тех поддержку может формировать не только администратор ИБ.
	УстановитьПривилегированныйРежим(Истина);
	ДанныеТехническойИнформации.ОписаниеПрограммы          = ОписаниеПрограммы();
	ДанныеТехническойИнформации.ТекущаяСессия              = ОписаниеТекущейСессии();
	ДанныеТехническойИнформации.ДанныеРасширений           = РасширенияКонфигурации.Получить();
	СообщенияВСлужбуТехническойПоддержкиКлиентСервер.ЗаполнитьСистемнуюИнформацию(
		ДанныеТехническойИнформации.СистемнаяИнформацияСервера);
	ЗаполнитьЗначенияСвойств(
		ДанныеТехническойИнформации.СистемнаяИнформацияКлиента,
		ОбщегоНазначения.СистемнаяИнформацияКлиента());
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ШаблонТекста = НСтр("ru = 'Техническая информация о программе:
		|Имя программы: %1 <%2>;
		|Версия Платформы 1С:Предприятие на сервере: %3 <%4>
		|Версия Платформы 1С:Предприятие на клиенте: %5 <%6>
		|Версия ОС: %7'");
	
	Результат.Добавить(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонТекста,
			ДанныеТехническойИнформации.ОписаниеПрограммы.ИмяКонфигурации,
			ДанныеТехническойИнформации.ОписаниеПрограммы.Версия,
			ДанныеТехническойИнформации.СистемнаяИнформацияСервера.ВерсияПриложения,
			ДанныеТехническойИнформации.СистемнаяИнформацияСервера.ТипПлатформы,
			ДанныеТехническойИнформации.СистемнаяИнформацияКлиента.ВерсияПриложения,
			ДанныеТехническойИнформации.СистемнаяИнформацияКлиента.ТипПлатформы,
			ДанныеТехническойИнформации.СистемнаяИнформацияКлиента.ВерсияОС));
			
	ШаблонТекста = НСтр("ru = 'Информация об экране: %1;'");
	Результат.Добавить(
		СтрШаблон(ШаблонТекста, ТекстИнформацияОбЭкранеКлиента()));
	
	Результат.Добавить(
		НСтр("ru = 'Адрес публикации:'"));
	Результат.Добавить(
		ОбщегоНазначения.АдресПубликацииИнформационнойБазыВЛокальнойСети());
	
	Результат.Добавить(
		НСтр("ru = 'Установленные расширения:'"));
	Результат.Добавить(
		ТекстОписаниеУстановленныхРасширений(
			ДанныеТехническойИнформации.ДанныеРасширений));
			
	ШаблонТекста = НСтр("ru = 'Режим: %1;
		|Полные права: %2;
		|Права администратора: %3'");
	
	Результат.Добавить(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонТекста,
			ДанныеТехническойИнформации.ТекущаяСессия.РежимРаботы,
			ДанныеТехническойИнформации.ТекущаяСессия.ЕстьПолныеПрава,
			ДанныеТехническойИнформации.ТекущаяСессия.ЕстьПравоАдминистрирования));
			
	
	Возврат СтрСоединить(Результат, Символы.ПС);
	
КонецФункции

Функция ТекстИнформацияОбЭкранеКлиента()
	
	ОписаниеЭкрана = Новый Массив;
	
	ИнформацияЭкрана = ПолучитьИнформациюЭкрановКлиента();
	Если ИнформацияЭкрана <> Неопределено 
		И ИнформацияЭкрана.Количество() >0 Тогда
		ОписаниеЭкрана.Добавить("DPI:");
		ОписаниеЭкрана.Добавить(ИнформацияЭкрана[0].DPI);
		
		ОписаниеЭкрана.Добавить("Высота:");
		ОписаниеЭкрана.Добавить(ИнформацияЭкрана[0].Высота);
		
		ОписаниеЭкрана.Добавить("Ширина:");
		ОписаниеЭкрана.Добавить(ИнформацияЭкрана[0].Ширина);
	КонецЕсли;
	
	Возврат СтрСоединить(ОписаниеЭкрана, " ");
	
КонецФункции

// Определяет описание программы.
//
// Возвращаемое значение:
//  Структура - описание программы.
//
Функция ОписаниеПрограммы()
	
	ИмяПрограммы = Строка(ИнтернетПоддержкаПользователей.СлужебнаяИмяПрограммы());
	Если ИмяПрограммы = "Unknown" Тогда
		ИмяПрограммы = НСтр("ru = '<Не заполнено>'");
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ОписаниеКонфигурации", Метаданные.Синоним);
	Результат.Вставить("Версия", ИнтернетПоддержкаПользователей.ВерсияКонфигурации());
	Результат.Вставить("ИмяКонфигурации", ИнтернетПоддержкаПользователей.ИмяКонфигурации());
	
	Возврат Результат;
	
КонецФункции

Функция ТекстОписаниеУстановленныхРасширений(ДанныеРасширений)
	
	ОписаниеРасширений = Новый Массив;
	
	НазначенияРасширенияКонфигурации = Метаданные.СвойстваОбъектов.НазначениеРасширенияКонфигурации;
	Для Каждого ДанныеРасширения Из ДанныеРасширений Цикл
		ЭтоПатч = ДанныеРасширения.Назначение = НазначенияРасширенияКонфигурации.Исправление
			И СтрНачинаетсяС(ДанныеРасширения.Имя, "EF");
		
		Если ЭтоПатч ИЛИ Не ДанныеРасширения.Активно Тогда
			Продолжить;
		КонецЕсли;
		
		Назначение = ДанныеРасширения.Назначение;
		Если Назначение = НазначенияРасширенияКонфигурации.Адаптация Тогда
			ЗначениеНазначение = "Адаптация";
		ИначеЕсли Назначение = НазначенияРасширенияКонфигурации.Дополнение Тогда
			ЗначениеНазначение = "Дополнение";
		ИначеЕсли Назначение = НазначенияРасширенияКонфигурации.Исправление Тогда
			ЗначениеНазначение = "Исправление";
		Иначе
			Продолжить;
		КонецЕсли;
		
		ОписаниеРасширений.Добавить(
			Символы.Таб
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (%2)'"),
				ДанныеРасширения.Имя,
				ЗначениеНазначение));
	КонецЦикла;
	
	Если ОписаниеРасширений.Количество() = 0 Тогда
		ОписаниеРасширений.Добавить(НСтр("ru = '<Нет>'"));
	КонецЕсли;
	
	Возврат СтрСоединить(ОписаниеРасширений, Символы.ПС);
	
КонецФункции

// Определяет описание текущей сессии пользователя.
//
// Возвращаемое значение:
//  Структура - информация о сессии.
//
Функция ОписаниеТекущейСессии()
	
	ЭтоФайловаяИБ = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяПользователя", ИмяПользователя());
	Результат.Вставить(
		"РежимРаботы",
		?(ЭтоФайловаяИБ, "FILE", "SRVR"));
	Результат.Вставить(
		"ЕстьПравоАдминистрирования",
		Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь));
	Результат.Вставить(
		"ЕстьПолныеПрава",
		Пользователи.ЭтоПолноправныйПользователь(, Ложь, Ложь));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти