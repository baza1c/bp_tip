
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров расчета налога для одной строки услуг.
//
Функция ПараметрыРасчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ставка",           0);
	Результат.Вставить("СпособРасчета",    ПредопределенноеЗначение("Перечисление.СпособыРасчетаТуристическогоНалога.ПоСтавке"));
	Результат.Вставить("УслугаРазмещения", ПредопределенноеЗначение("Перечисление.ВидыУслугРазмещения.УслугаРазмещения"));
	Результат.Вставить("ВидЛьготы",        ПредопределенноеЗначение("Перечисление.ВидыЛьготПоТуристическомуНалогу.НеПрименяется"));
	Результат.Вставить("СуммаНалога",      0);
    Результат.Вставить("КоличествоУслуг",  0);
    Результат.Вставить("СтоимостьУслуг",   0);
	
	Возврат Результат;
	
КонецФункции

// Заполняет способа расчета налога и суммы налога в переданной коллекции.
//
// Параметры:
//  СтрокаУслуг   - СтрокаТаблицыЗначений - см. ПараметрыРасчета()
//
Процедура ЗаполнитьСпособИСуммуНалога(СтрокаУслуг) Экспорт
	
	Если Не ЗначениеЗаполнено(СтрокаУслуг.ВидЛьготы) Тогда
		СтрокаУслуг.ВидЛьготы = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоТуристическомуНалогу.НеПрименяется");
	КонецЕсли;
	
	Если СтрокаУслуг.ВидЛьготы <> ПредопределенноеЗначение("Перечисление.ВидыЛьготПоТуристическомуНалогу.НеПрименяется")
		И СтрокаУслуг.ВидЛьготы <> ПредопределенноеЗначение("Перечисление.ВидыЛьготПоТуристическомуНалогу.ОплатаИзБюджета") Тогда
		СтрокаУслуг.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаТуристическогоНалога.НеОблагается");
		СтрокаУслуг.СуммаНалога = 0;
		Возврат; 
	КонецЕсли;
	
	// Округляем всегда до целого количества суток в большую строну.
	// Письмо Минфина России от 24.10.2024 № 03-05-08/103501
	КоличествоУслуг = ?(СтрокаУслуг.КоличествоУслуг = 0, 1, 
		Окр(СтрокаУслуг.КоличествоУслуг + 0.5, 0, РежимОкругления.Окр15как10));

	НалогПоСтавке = Окр(СтрокаУслуг.СтоимостьУслуг * СтрокаУслуг.Ставка / (100 + СтрокаУслуг.Ставка), 2);
	НалогПоМинимальному = КоличествоУслуг * МинимальныйНалог();

	Если СтрокаУслуг.УслугаРазмещения = ПредопределенноеЗначение("Перечисление.ВидыУслугРазмещения.СанаторноКурортноеЛечение")
		Или НалогПоСтавке < НалогПоМинимальному Тогда
		СтрокаУслуг.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаТуристическогоНалога.МинимальныйНалог");
		СтрокаУслуг.СуммаНалога = НалогПоМинимальному;
        СтрокаУслуг.КоличествоУслуг = КоличествоУслуг;
	Иначе
		СтрокаУслуг.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаТуристическогоНалога.ПоСтавке");
		СтрокаУслуг.СуммаНалога = НалогПоСтавке;
		СтрокаУслуг.КоличествоУслуг = ?(СтрокаУслуг.КоличествоУслуг = 0, 1, СтрокаУслуг.КоличествоУслуг);
	КонецЕсли;
		
КонецПроцедуры

// Возвращает значение минимального налога.
//
// Возвращаемое значение:
//   Число	– значение минимального налога
//
Функция МинимальныйНалог() Экспорт
	Возврат 100;
КонецФункции  

// Рассчитывает налог по переданной строке услуги в документе НачислениеТуристическогоНалога.
//
// Параметры:
//	СтрокаУслуг - СтрокаТабличнойЧасти
//	СуммаВключаетНДС - Булево - признак включения НДС в сумму. 
//	ДопУслугаРазмещения - ПеречислениеСсылка.ВидыУслугРазмещения - Вид услуги Ранний заезд / Поздний выезд.
//
Процедура РассчитатьСуммуНалога(Услуги, СтрокаУслуг, СуммаВключаетНДС, ДопУслугаРазмещения) Экспорт
	
	Если СтрокаУслуг.УслугаРазмещения = ДопУслугаРазмещения Тогда
		
		// Для услуги Ранний заезд / Поздний выезд пересчитаем тур.налог в родительской строке
		Если ЗначениеЗаполнено(СтрокаУслуг.ИдентификаторРодительскойСтроки) Тогда 
			Отбор = Новый Структура("ИдентификаторСтроки", СтрокаУслуг.ИдентификаторРодительскойСтроки);
			РодительскиеСтроки = Услуги.НайтиСтроки(Отбор); 
            Если РодительскиеСтроки.Количество() > 0 Тогда
				РассчитатьСуммуНалога(Услуги, РодительскиеСтроки[0], СуммаВключаетНДС, ДопУслугаРазмещения);
			КонецЕсли;
		КонецЕсли;
		
		СтрокаУслуг.ВидЛьготыПоТуристическомуНалогу = Неопределено;
		СтрокаУслуг.СпособРасчета = Неопределено;
        СтрокаУслуг.Ставка = Неопределено;
		
		Возврат;

	КонецЕсли;
		
	СуммаДопУслуг = 0;
	
	Если ЗначениеЗаполнено(СтрокаУслуг.ИдентификаторСтроки) Тогда
		
		Отбор = Новый Структура("ИдентификаторРодительскойСтроки", СтрокаУслуг.ИдентификаторСтроки);
		СтрокиДопУслуг = Услуги.НайтиСтроки(Отбор); 
		
		Для Каждого СтрокаДопУслуг Из СтрокиДопУслуг Цикл
			
			СуммаДопУслуг = СуммаДопУслуг + СтоимостьУслуг(СтрокаДопУслуг, СуммаВключаетНДС);
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураРасчета = ПараметрыРасчета();  
	СтруктураРасчета.Ставка = СтрокаУслуг.Ставка;
	СтруктураРасчета.ВидЛьготы = СтрокаУслуг.ВидЛьготыПоТуристическомуНалогу;
	СтруктураРасчета.УслугаРазмещения = СтрокаУслуг.УслугаРазмещения;
	
	СтруктураРасчета.КоличествоУслуг = СтрокаУслуг.Количество;
	СтруктураРасчета.СтоимостьУслуг = СтрокаУслуг.Сумма + СуммаДопУслуг; 
	Если СуммаВключаетНДС Тогда
		СтруктураРасчета.СтоимостьУслуг = СтруктураРасчета.СтоимостьУслуг - СтрокаУслуг.СуммаНДС; 
	КонецЕсли;
	
	Если СтруктураРасчета.КоличествоУслуг = 0 Тогда
		СтрокаУслуг.СуммаНалога = 0;
	Иначе
		ЗаполнитьСпособИСуммуНалога(СтруктураРасчета);
		СтрокаУслуг.СуммаНалога = СтруктураРасчета.СуммаНалога;
		СтрокаУслуг.СпособРасчета = СтруктураРасчета.СпособРасчета;
	КонецЕсли;
	
КонецПроцедуры

Функция СтоимостьУслуг(СтрокаУслуг, СуммаВключаетНДС)
	
	Если СуммаВключаетНДС Тогда
		СтоимостьУслуг = СтрокаУслуг.Сумма - СтрокаУслуг.СуммаНДС;
	Иначе
		СтоимостьУслуг = СтрокаУслуг.Сумма;
	КонецЕсли;
	
	Возврат СтоимостьУслуг;
	
КонецФункции

Процедура ОбновитьПредставлениеИДРодСтрок(Форма, ИмяТабличнойЧасти) Экспорт
	
	Объект = Форма.Объект;
	Для Каждого СтрокаТовары Из Объект[ИмяТабличнойЧасти] Цикл
		ПредставлениеИДРодСтроки(СтрокаТовары, Форма.ТаблицаОсновныхУслугРазмещения);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(Форма, ИмяТабличнойЧасти) Экспорт
	
	Объект = Форма.Объект; 
	ЗаполнитьТаблицуОсновныхУслугРазмещения(Форма, ИмяТабличнойЧасти);
	ДанныеВыбора = ДанныеВыбораИдентификаторРодительскойСтроки(Форма, ИмяТабличнойЧасти, "");

КонецПроцедуры

Процедура УдалитьНесвязанныеИдентификаторы(Форма, ИмяТаблицы) Экспорт
	
	Объект = Форма.Объект;
	Отбор = Новый Структура("ИдентификаторСтроки");

	Для Каждого СтрокаТаблицы Из Объект[ИмяТаблицы] Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторРодительскойСтроки) Тогда
			Продолжить;
		КонецЕсли;
		Отбор.ИдентификаторСтроки = СтрокаТаблицы.ИдентификаторРодительскойСтроки;

		Если Объект[ИмяТаблицы].НайтиСтроки(Отбор).Количество() = 0 Тогда
			СтрокаТаблицы.ИдентификаторРодительскойСтроки = Неопределено;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(Форма, ИмяТаблицы);
	ОбновитьПредставлениеИДРодСтрок(Форма, ИмяТаблицы);

КонецПроцедуры

Функция ИмяРеквизитаТабличнойЧасти(ИмяТабличнойЧасти) Экспорт
	
	Возврат СтрШаблон("%1%2", ИмяТабличнойЧасти, "ИдентификаторРодительскойСтроки");
	
КонецФункции

Процедура ЗаполнитьТаблицуОсновныхУслугРазмещения(Форма, ИмяТабличнойЧасти) Экспорт
	
	Объект = Форма.Объект;
	Форма.ТаблицаОсновныхУслугРазмещения.Очистить();
    Отбор = Новый Структура("ИдентификаторРодительскойСтроки");

	Для Каждого СтрокаТовары Из Объект[ИмяТабличнойЧасти] Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТовары.ИдентификаторСтроки) Тогда 
			СтрокаТовары.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТовары.УслугаРазмещения)
			И Не СтрокаТовары.УслугаРазмещения = Форма.ДопУслугаРазмещения Тогда
			
			СтрокаТаблицы = Форма.ТаблицаОсновныхУслугРазмещения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаТовары);
			
			Отбор.ИдентификаторРодительскойСтроки = СтрокаТовары.ИдентификаторСтроки;
			СтрокаТаблицы.УслугаВыбрана = Объект[ИмяТабличнойЧасти].НайтиСтроки(Отбор).Количество() <> 0;
			СтрокаТаблицы.Представление = СтрШаблон(НСтр("ru = '%1 (Строка №%2)'"), СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки);
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеВыбораИдентификаторРодительскойСтроки(Форма, ИмяТабличнойЧасти, СтрокаПоиска) Экспорт
	
	ДанныеВыбора = Форма.Элементы[ИмяРеквизитаТабличнойЧасти(ИмяТабличнойЧасти)].СписокВыбора;
	ДанныеВыбора.Очистить();
	
	Для Каждого СтрокаТаблицы Из Форма.ТаблицаОсновныхУслугРазмещения Цикл 
		
		ЦветСтроки = ?(СтрокаТаблицы.УслугаВыбрана, Форма.ЦветНеактивнойСтроки, Новый Цвет());
		Представление = Новый ФорматированнаяСтрока(СтрокаТаблицы.Представление, , ЦветСтроки);
		Если Не ЗначениеЗаполнено(СтрокаПоиска) Тогда
			
			ДанныеВыбора.Добавить(СтрокаТаблицы.ИдентификаторСтроки, Представление);
			
		ИначеЕсли СтрНайти(НРег(Представление), НРег(СтрокаПоиска)) <> 0 Тогда
			
			ОформленноеПредставление = СтрНайтиИВыделитьОформлением(Представление, СтрокаПоиска);
			Представление = ?(ОформленноеПредставление <> Неопределено, ОформленноеПредставление, Представление);
			ДанныеВыбора.Добавить(СтрокаТаблицы.ИдентификаторСтроки, Представление);
			
		КонецЕсли; 

	КонецЦикла;
	
	Если ДанныеВыбора.Количество() > 1 Тогда  
		НадписьПоказатьВсе = Новый ФорматированнаяСтрока(НСтр("ru = 'Показать все'"), Форма.ГиперссылкаШрифт, Форма.ГиперссылкаЦвет);
		ДанныеВыбора.Добавить("ПоказатьВсе", НадписьПоказатьВсе);
	КонецЕсли;
	
	Возврат ДанныеВыбора;
		
КонецФункции

Процедура ПредставлениеИДРодСтроки(ТекущиеДанные, ТаблицаОсновныхУслугРазмещения) Экспорт
	
	ТекущиеДанные.ПредставлениеИДРодСтроки = "";
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторРодительскойСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ИдентификаторСтроки", ТекущиеДанные.ИдентификаторРодительскойСтроки);
	НайденныеСтроки = ТаблицаОсновныхУслугРазмещения.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		ТекущиеДанные.ПредставлениеИДРодСтроки = "<...>";	
	Иначе
		ТекущиеДанные.ПредставлениеИДРодСтроки = НайденныеСтроки[0].Представление;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодобратьСвободнуюСтрокуСУслугойРазмещения(Форма, ИмяТаблицы) Экспорт
	
	Объект = Форма.Объект; 
	ТекущиеДанные = Форма.Элементы[ИмяТаблицы].ТекущиеДанные;
		
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ИдентификаторРодительскойСтроки");
    Индекс = ТекущиеДанные.НомерСтроки - 2;
	Пока Индекс >= 0 Цикл
		
		СтрокаТаблицы = Объект[ИмяТаблицы][Индекс];
		Индекс = Индекс - 1;

		Если Не ЗначениеЗаполнено(СтрокаТаблицы.УслугаРазмещения)
			Или СтрокаТаблицы.УслугаРазмещения = Форма.ДопУслугаРазмещения Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор.ИдентификаторРодительскойСтроки = СтрокаТаблицы.ИдентификаторСтроки;
		СвязанныеСтроки = Объект[ИмяТаблицы].НайтиСтроки(Отбор);
		Если СвязанныеСтроки.Количество() = 0 Тогда  
			Если СтрокаТаблицы.Свойство("Гостиница") Тогда
				ТекущиеДанные.Гостиница = СтрокаТаблицы.Гостиница;
			КонецЕсли;
			ТекущиеДанные.ИдентификаторРодительскойСтроки = СтрокаТаблицы.ИдентификаторСтроки;
			ТекущиеДанные.ПредставлениеИДРодСтроки = 
				СтрШаблон(НСтр("ru = '%1 (Строка №%2)'"), СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки);
            Прервать;
		КонецЕсли;
				
	КонецЦикла;
				
КонецПроцедуры

Процедура УдалитьСтарыеДанныеИдентификаторРодительскойСтроки(Форма, ИмяТабличнойЧасти, ИдентификаторСтроки) Экспорт
	
	Объект = Форма.Объект;
	Отбор = Новый Структура("ИдентификаторРодительскойСтроки", ИдентификаторСтроки);
	НайденныеСтроки = Объект[ИмяТабличнойЧасти].НайтиСтроки(Отбор);
	
	Для Каждого СтрокаУслуги Из НайденныеСтроки Цикл
		СтрокаУслуги.ИдентификаторРодительскойСтроки = Неопределено; 
		СтрокаУслуги.ПредставлениеИДРодСтроки = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
