#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

Процедура ОткрытиеФормыСоздаваемогоДокумента(ТипДокумента, ПараметрыЗаполнения, Владелец) Экспорт
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ТипДокумента", ТипДокумента);
	ПараметрыОбработчика.Вставить("ПараметрыЗаполнения", ПараметрыЗаполнения);
	ПараметрыОбработчика.Вставить("Владелец", Владелец);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораЗначения", ЭтотОбъект, ПараметрыОбработчика);
	
	Заголовок = НСтр("ru = 'Выберите вид документа'");
	
	Если ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		НаРеализацию = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию");
		ПоказатьВводЗначения(Оповещение, НаРеализацию, Заголовок);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		НаПоступление = ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыПолученного.НаПоступление");
		ПоказатьВводЗначения(Оповещение, НаПоступление, Заголовок);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытиеФормыРедактированияСтроки(Форма, ДанныеСтроки) Экспорт
	
	Объект = Форма.Объект;
	
	Дата = Объект.РеквизитыДокумента[1].Значение;
	Организация = Форма.ИсточникПоляДоговорОрганизация;
	ИмяТаблицы = ?(ДанныеСтроки.Услуга, "Услуги", "Товары");
	ДокументБезНДС = Форма.ДокументБезНДС;
	ПрименяетсяМалоценка = Форма.ПрименяетсяМалоценка;
	
	Малоценка = ПрименяетсяМалоценка И (ДанныеСтроки.СчетУчета = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПриобретениеМалоценногоОборудованияИЗапасов"));
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
		// Для реализации
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ТолькоПросмотр",	Форма.ТолькоПросмотр);
		ПараметрыФормы.Вставить("Дата", 			Дата);
		ПараметрыФормы.Вставить("Организация", 		Организация);
		ПараметрыФормы.Вставить("ИмяТаблицы",		ИмяТаблицы);
		ПараметрыФормы.Вставить("ДокументБезНДС", 	ДокументБезНДС);
		
		ПараметрыФормы.Вставить("СчетУчета", 	ДанныеСтроки.СчетУчета);
		ПараметрыФормы.Вставить("СчетДоходов", 	ДанныеСтроки.СчетДоходов);
		ПараметрыФормы.Вставить("Субконто", 	ДанныеСтроки.Субконто);
		ПараметрыФормы.Вставить("СчетРасходов", ДанныеСтроки.СчетРасходов);
		ПараметрыФормы.Вставить("СчетУчетаНДСПоРеализации", ДанныеСтроки.СчетУчетаНДСПоРеализации);
		ПараметрыФормы.Вставить("Номенклатура", ДанныеСтроки.Номенклатура);
		ПараметрыФормы.Вставить("СуммаНДС", 	ДанныеСтроки.СуммаНДС);
		
		ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма.ФормаРедактированияСтрокиТоварыУслуги", ПараметрыФормы, Форма);
		
	Иначе
		
		Если Малоценка Тогда
			
			// Для поступления малоценки
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ТолькоПросмотр",       Форма.ТолькоПросмотр);
			ПараметрыФормы.Вставить("ДатаДокумента",        Дата);
			ПараметрыФормы.Вставить("Организация",          Организация);
			
			ПараметрыФормы.Вставить("СчетУчетаНДС",         ДанныеСтроки.СчетУчетаНДС);
			ПараметрыФормы.Вставить("СпособУчетаНДС",       ДанныеСтроки.СпособУчетаНДС);
			ПараметрыФормы.Вставить("СуммаНДС",             ДанныеСтроки.СуммаНДС);
			ПараметрыФормы.Вставить("НДСВключенВСтоимость", Форма.НДСВключенВСтоимость);
			ПараметрыФормы.Вставить("УчетАгентскогоНДС",    Форма.УчетАгентскогоНДС);
			ПараметрыФормы.Вставить("ОтражениеВУСН",        ДанныеСтроки.ОтражениеВУСН);
			
			ПараметрыФормы.Вставить("СчетЗатрат",          ДанныеСтроки.СчетЗатрат);
			ПараметрыФормы.Вставить("ПодразделениеЗатрат", ДанныеСтроки.ПодразделениеЗатрат);
			ПараметрыФормы.Вставить("Субконто1",           ДанныеСтроки.Субконто1);
			ПараметрыФормы.Вставить("Субконто2",           ДанныеСтроки.Субконто2);
			ПараметрыФормы.Вставить("Субконто3",           ДанныеСтроки.Субконто3);
			
			ПараметрыФормы.Вставить("Номенклатура",        ДанныеСтроки.Номенклатура);
			ПараметрыФормы.Вставить("Контрагент",          Форма.Контрагент);
			ПараметрыФормы.Вставить("ДоговорКонтрагента",  Форма.ДоговорКонтрагента);
			
			ОткрытьФорму("ОбщаяФорма.РедактированиеСчетаЗатрат", ПараметрыФормы, Форма);
		
		ИначеЕсли ДанныеСтроки.Услуга Тогда
			
			// Для поступления услуги
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ТолькоПросмотр",       Форма.ТолькоПросмотр);
			ПараметрыФормы.Вставить("ДатаДокумента",        Дата);
			ПараметрыФормы.Вставить("Организация",          Организация);
			
			ПараметрыФормы.Вставить("СчетУчетаНДС",         ДанныеСтроки.СчетУчетаНДС);
			ПараметрыФормы.Вставить("СпособУчетаНДС",       ДанныеСтроки.СпособУчетаНДС);
			ПараметрыФормы.Вставить("СуммаНДС",             ДанныеСтроки.СуммаНДС);
			ПараметрыФормы.Вставить("НДСВключенВСтоимость", Форма.НДСВключенВСтоимость);
			ПараметрыФормы.Вставить("УчетАгентскогоНДС",    Форма.УчетАгентскогоНДС);
			ПараметрыФормы.Вставить("ОтражениеВУСН",        ДанныеСтроки.ОтражениеВУСН);
			
			ПараметрыФормы.Вставить("СчетЗатрат",          ДанныеСтроки.СчетЗатрат);
			ПараметрыФормы.Вставить("ПодразделениеЗатрат", ДанныеСтроки.ПодразделениеЗатрат);
			ПараметрыФормы.Вставить("Субконто1",           ДанныеСтроки.Субконто1);
			ПараметрыФормы.Вставить("Субконто2",           ДанныеСтроки.Субконто2);
			ПараметрыФормы.Вставить("Субконто3",           ДанныеСтроки.Субконто3);
			
			ПараметрыФормы.Вставить("СчетЗатратНУ",        ДанныеСтроки.СчетЗатратНУ);
			ПараметрыФормы.Вставить("СубконтоНУ1",         ДанныеСтроки.СубконтоНУ1);
			ПараметрыФормы.Вставить("СубконтоНУ2",         ДанныеСтроки.СубконтоНУ2);
			ПараметрыФормы.Вставить("СубконтоНУ3",         ДанныеСтроки.СубконтоНУ3);
			
			ПараметрыФормы.Вставить("Номенклатура",        ДанныеСтроки.Номенклатура);
			ПараметрыФормы.Вставить("Контрагент",          Форма.Контрагент);
			ПараметрыФормы.Вставить("ДоговорКонтрагента",  Форма.ДоговорКонтрагента);
			
			ОткрытьФорму("ОбщаяФорма.РедактированиеСчетаЗатрат", ПараметрыФормы, Форма);
			
		Иначе
			
			// Для поступления товара
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ТолькоПросмотр",       Форма.ТолькоПросмотр);
			ПараметрыФормы.Вставить("Дата",                 Дата);
			ПараметрыФормы.Вставить("Организация",          Организация);
			ПараметрыФормы.Вставить("СпособУчетаНДС",       ДанныеСтроки.СпособУчетаНДС);
			ПараметрыФормы.Вставить("СчетУчета",            ДанныеСтроки.СчетУчета);
			ПараметрыФормы.Вставить("СчетУчетаНДС",         ДанныеСтроки.СчетУчетаНДС);
			ПараметрыФормы.Вставить("ОтражениеВУСН",        ДанныеСтроки.ОтражениеВУСН);
			
			ОткрытьФорму("Обработка.РаспознаваниеДокументовБП.Форма.ФормаРедактированиеСчетаУчета", ПараметрыФормы, Форма);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеВыбораЗначения(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипДокумента = ДополнительныеПараметры.ТипДокумента;
	ПараметрыЗаполнения = ДополнительныеПараметры.ПараметрыЗаполнения;
	Владелец = ДополнительныеПараметры.Владелец;
	
	Если ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		
		ПараметрыЗаполнения.Вставить("ВидСчетаФактуры", ВыбранноеЗначение);
		ПараметрыЗаполнения.Вставить("ПоРаспознанномуДокументу", Истина);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
		
		ИменаФорм = РаспознаваниеДокументовБПВызовСервера.ФормыСоздаваемогоДокумента(ТипДокумента);
		Если ИменаФорм = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ПрефиксИмени = "Документ.СчетФактураВыданный.Форма.";
		Иначе
			ПрефиксИмени = "Документ.СчетФактураПолученный.Форма.";
		КонецЕсли;
		
		ИмяФормы = ПрефиксИмени + ИменаФорм[ВыбранноеЗначение];
		
		ОткрытьФорму(ИмяФормы, ПараметрыФормы, Владелец, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти