
#Область ПрограммныйИнтерфейс

// Параметры:
//  Идентификатор           - Строка - идентификатор классификатора в сервисе классификаторов.
//                            Определяется в процедуре ПриДобавленииКлассификаторов.
//  Версия                  - Число - номер загруженной версии;
//  Адрес                   - Строка - адрес двоичных данных файла обновления во
//                            временном хранилище;
//  Обработан               - Булево - если Ложь, при обработке файла обновления были ошибки,
//                            и его необходимо загрузить повторно;
//  ДополнительныеПараметры - Структура - содержит дополнительные параметры обработки.
//                            Необходимо использовать для передачи значений в переопределяемый
//                            метод РаботаСКлассификаторамиВМоделиСервисаПереопределяемый.ПриОбработкеОбластиДанных
//                            и метод ИнтеграцияПодсистемБИП.ПриОбработкеОбластиДанных.
Процедура ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Загружает в фоне актуальный классификатор ОКВЭД2 из сервиса классификаторов
// Параметры см. в комментарии к ДлительныеОперации.ВыполнитьВФоне()
//
Процедура ЗагрузитьДанныеИзСервисаКлассификаторов(Параметры, АдресРезультата) Экспорт
	
	Классификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов());
	РезультатЗагрузки = РаботаСКлассификаторами.ОбновитьКлассификаторы(Классификаторы);
	
	Если Не ПустаяСтрока(РезультатЗагрузки.КодОшибки) Тогда
		
		ШаблонСообщения = НСтр("ru = 'При загрузке классификатора ОКВЭД ред. 2 из сервиса классификаторов возникла ошибка:
			|%1'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		ТекстСообщения = СтрШаблон(ШаблонСообщения, РезультатЗагрузки.ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Обновление классификатора ОКВЭД ред. 2'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.ДанныеОКВЭД, ,
			ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

// Производит поиск в ОКВЭД по строке поиска, которое может содержать код или наименование элемента классификатора
//
// Параметры:
//  СтрокаПоиска - Строка - строка, содержащая ключевые слова для поиска (код и/или наименование ОКВЭД)
//  Разрешенные - Булево - выбирать только значения, длина кода которых больше или равна 4, без учета точек
// 
// Возвращаемое значение:
//  ТаблицаЗначений - найденные элементы, либо пустая таблица:
//    * Код - Строка - код по классификатору
//    * Наименование - Строка - наименование по классификатору
//
Функция НайтиДанные(СтрокаПоиска, Разрешенные = Ложь) Экспорт
	
	Данные = Новый ТаблицаЗначений;
	Данные.Колонки.Добавить("Код");
	Данные.Колонки.Добавить("Наименование");
	Данные.Колонки.Добавить("КоличествоСовпадений");
	
	Слова = СтрРазделить(ВРег(СтрЗаменить(СтрокаПоиска, """", "")), " ", Ложь);
	КоличествоСлов = Слова.Количество();
	Для Каждого Слово ИЗ Слова Цикл
		ПараметрыПодбора = КлассификаторОКВЭД.НовыйОтбор();
		ПараметрыПодбора.СтрокаПоиска = Слово;
		ПараметрыПодбора.Режим = "КодИНаименование";
		ПараметрыПодбора.Разрешенные = Разрешенные;
		ДанныеПодбора = КлассификаторОКВЭД.Подбор(ПараметрыПодбора);
		Для Каждого СтрокаПодбора Из ДанныеПодбора Цикл
			СтрокаРезультата = Данные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтрокаПодбора);
			СтрокаРезультата.КоличествоСовпадений = 1;
		КонецЦикла;
	КонецЦикла;
	
	Данные.Свернуть("Код, Наименование", "КоличествоСовпадений");
	Данные.Сортировать("Код");
	
	РезультатПоиска = Новый ТаблицаЗначений;
	РезультатПоиска.Колонки.Добавить("Код");
	РезультатПоиска.Колонки.Добавить("Наименование");
	
	Для Каждого СтрокаДанных Из Данные Цикл
		Если СтрокаДанных.КоличествоСовпадений >= КоличествоСлов Тогда
			ЗаполнитьЗначенияСвойств(РезультатПоиска.Добавить(), СтрокаДанных);
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатПоиска;
	
КонецФункции

// Возвращает полный список данных классификатора
//
// Параметры:
//   Разрешенные - Булево - выбирать только значения, длина кода которых больше или равна 4, без учета точек
// Возвращаемое значение:
//  ТаблицаЗначений - список данных:
//    * Код - Строка - код по классификатору
//    * Наименование - Строка - наименование по классификатору
//    * ДоступенДляВыбора - Булево - код можно выбрать для указания вида деятельности организации
//
Функция Данные(Разрешенные = Ложь) Экспорт
	
	РезультатВыборки = Новый ТаблицаЗначений;
	РезультатВыборки.Колонки.Добавить("Код");
	РезультатВыборки.Колонки.Добавить("Наименование");
	РезультатВыборки.Колонки.Добавить("ДоступенДляВыбора");
	
	ПараметрыПодбора = КлассификаторОКВЭД.НовыйОтбор();
	ПараметрыПодбора.Разрешенные = Разрешенные;
	ПодобранныеДанные = КлассификаторОКВЭД.Подбор(ПараметрыПодбора);
	Для Каждого СтрокаПодобранныхДанных Из ПодобранныеДанные Цикл
		СтрокаВыборки = РезультатВыборки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВыборки, СтрокаПодобранныхДанных);
		СтрокаВыборки.ДоступенДляВыбора = ДоступенДляВыбора(СтрокаВыборки.Код);
	КонецЦикла;
	
	Возврат РезультатВыборки;
	
КонецФункции

// Возвращает соответствие кодов и наименований классификатора
// 
// Возвращаемое значение:
//  ФиксированноеСоответствие из КлючИЗначение- соответствие кодов и наименований:
//  * Ключ - Строка - код из классификатора
//  * Значение - Строка - наименование из классификатора
//
Функция СоответствиеКодовИНаименований() Экспорт
	
	СоответствиеКодовИНаименований = Новый Соответствие;
	
	Данные = Данные();
	Для Каждого СтрокаДанных Из Данные Цикл
		СоответствиеКодовИНаименований.Вставить(СтрокаДанных.Код, СтрокаДанных.Наименование);
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(СоответствиеКодовИНаименований);
	
КонецФункции

// Получает таблицу о соответствии кода (первых 2 цифр - класса) и буквенного кода раздела ОКВЭД
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица соответствия:
//    * Раздел - Строка - первые 2 цифры числового кода из классификатора ОКВЭД (класс)
//    * Код - Строка - буквенное обозначение раздела, в который входит указанный класс
//
Функция РазделыКодов() Экспорт
	
	СоответствиеРазделовКодов = КлассификаторОКВЭД.РазделыКодов();
	КодыРазделов = Новый ТаблицаЗначений;
	КодыРазделов.Колонки.Добавить("Раздел");
	КодыРазделов.Колонки.Добавить("Код");
	Для Каждого СтрокаСоответствия Из СоответствиеРазделовКодов Цикл
		СтрокаКодовРазделов = КодыРазделов.Добавить();
		СтрокаКодовРазделов.Раздел = СтрокаСоответствия.Значение;
		СтрокаКодовРазделов.Код = СтрокаСоответствия.Ключ;
	КонецЦикла;
	КодыРазделов.Сортировать("Раздел, Код");
	
	Возврат КодыРазделов
	
КонецФункции

// Возвращает наименование кода ОКВЭД2
//
// Параметры:
//   Код - Строка - код, наименование которого необходимо получить
//
// Возвращаемое значение:
//   Строка - наименование, соответствующее коду ОКВЭД2
//
Функция НаименованиеПоКоду(Код) Экспорт
	
	Наименование = "";
	
	Данные = КлассификаторОКВЭД.ЗначениеПоКоду(Код, Ложь);
	Для Каждого СтрокаДанных Из Данные Цикл
		Наименование = СтрокаДанных.Наименование;
		Прервать;
	КонецЦикла;
	
	Возврат Наименование;
	
КонецФункции

// Формирует массив кодов ОКВЭД, исходя из возможной иерархии - от исходного к наиболее общему.
//
// Параметры:
//  Код - Строка - код ОКВЭД.
//
// Возвращаемое значение:
//  Массив из Строка - возможные коды ОКВЭД с учетом иерархии, от исходного к общему.
//
// Пример:
//  КодВИерархии("01.11.15") вернет массив ["01.11.15", "01.11.1", "01.11", "01.1", "01"]
//
Функция КодВИерархии(Знач Код) Экспорт
	
	Код = СокрЛП(Код);
	Результат = Новый Массив;
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(Код, ".", "")) Тогда 
		Возврат Результат; // Корректный код может содержать только цифры и точки
	КонецЕсли;
	
	Данные = КлассификаторОКВЭД.ЗначениеПоКоду(Код, Истина);
	Для Каждого СтрокаДанных Из Данные Цикл
		Результат.Добавить(СтрокаДанных.Код);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Настройки формы ВыборВидаДеятельности, соответствующие определенному пользователю по выбранной организации.
// 
// Возвращаемое значение:
//  Структура:
// * ИзбранноеВключено - СписокЗначений из Строка
// * ИзбранноеИсключено - СписокЗначений из Строка
// * ИсторияПросмотра - СписокЗначений из Строка
Функция НовыйНастройкиФормыВыборВидаДеятельности() Экспорт
	
	НастройкиФормы = Новый Структура;
	
	НастройкиФормы.Вставить("ИзбранноеВключено", Новый СписокЗначений);
	НастройкиФормы.Вставить("ИзбранноеИсключено", Новый СписокЗначений);
	НастройкиФормы.Вставить("ИсторияПросмотра", Новый СписокЗначений);
	
	Возврат НастройкиФормы;
	
КонецФункции

// Возвращает идентификатор настройки формы выбора вида деятельности.
//
// Возвращаемое значение:
//   Строка
Функция ИдентификаторНастройкиФормыВыборВидаДеятельности() Экспорт
	
	Возврат "НастройкаФормыВыбораВидаДеятельности";
	
КонецФункции

// Получает настройку "избранных" и историю просмотренных кодов ОКВЭД для текущего пользователя по указанной организации.
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация, для которой загружается настройка
//
// Возвращаемое значение:
//   см. КлассификаторОКВЭДБП.НовыйНастройкиФормыВыборВидаДеятельности
Функция НастройкаФормыВыбораКодов(Организация) Экспорт
	
	КлючОбъекта = ИдентификаторНастройкиФормыВыборВидаДеятельности();
	КлючНастройки = XMLСтрока(Организация);
	НастройкаФормыВыбораКодов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, КлючНастройки);

	Если НастройкаФормыВыбораКодов = Неопределено Тогда
		НастройкаФормыВыбораКодов = НовыйНастройкиФормыВыборВидаДеятельности();
	КонецЕсли;
	
	Возврат НастройкаФормыВыбораКодов;
	
КонецФункции

// Сохраняет настройку "избранных" кодов ОКВЭД для текущего пользователя по указанной организации.
// 
// Параметры:
//  НастройкаФормы - см. НовыйНастройкиФормыВыборВидаДеятельности
//  Организация - СправочникСсылка.Организации - организация, для которой сохраняется настройка
Процедура СохранитьНастройкуФормыВыбораКодов(НастройкаФормы, Организация) Экспорт

	КлючОбъекта = ИдентификаторНастройкиФормыВыборВидаДеятельности();
	КлючНастройки = XMLСтрока(Организация);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючОбъекта,
		КлючНастройки,
		НастройкаФормы);
		
КонецПроцедуры

// HTML-текст описания.
// 
// Параметры:
//  ПараметрыОписания - см. НовыйПараметрыОписания
// 
// Возвращаемое значение:
//  Строка - HTMLТекст описания
Функция HTMLТекстОписания(ПараметрыОписания) Экспорт

	Код = ПараметрыОписания.Код;
	
	Если Не ЗначениеЗаполнено(Код) Тогда
		Возврат "";
	КонецЕсли;
	
	СсылкиДляВозврата = ПараметрыОписания.СсылкиДляВозврата;
	ЭтоИзбранное = ПараметрыОписания.ЭтоИзбранное;
	
	ЧастиТекста = Новый Массив;
	Заголовок =
	"<html>
	|<head>
	|  <style>
	|    body {
	|      font-family: Arial;
	|      margin-top: 1%;
	|    }
	|    .grouping {
	|      padding: 6px;
	|      max-width: 1000px;
	|      font-size: 14px;
	|    }
	|    .description {
	|      margin-left: 1%;
	|      /* Отступ слева */
	|      background-color: #fffff0;
	|      /* Цвет фона */
	|      padding: 10px;
	|      /* Поля вокруг текста */
	|      border-radius: 5px;
	|      font-size: 12px;
	|      max-width: 1000px;
	|    }
	|    .note {
	|      font-size: 10px
	|    }
	|  </style>
	|</head>";
	ЧастиТекста.Добавить(Заголовок);
	ЧастиТекста.Добавить("<body>");
	
	ЧастиТелаОписания = Новый Массив;
	
	Выборка = ИерархияКодовСОписанием(Код, ПараметрыОписания.СократитьИерархию);
	Если Выборка.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	// Вывод начинаем не с заголовка, а с иерархии кодов, так как последний код иерархии совпадает с заголовком
	ОписаниеИерархии = Новый Массив;
	ИндексПоследнегоБлокаСОписанием = 0;
	Пока Выборка.Следующий() Цикл
		
		// Например, "45.1 - Подкласс "Торговля автотранспортными средствами""
		ШаблонГруппировки = "<b> %1 </b> - %2 <b>""%3""</b>";
		
		ТекстГруппировки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонГруппировки,
			Выборка.Код, // Например, "45.1"
			ИмяГруппировки(Выборка.ДлинаКодаБезТочек), // Например, "Подкласс"
			Выборка.Наименование); // Например, "Торговля автотранспортными средствами"
			
		ТекстОписания = "";
		
		Если Не ЗначениеЗаполнено(Выборка.Описание) Тогда
			// Применяем форматирование и выравнивание
			ШаблонОписания = "
			|<div class=""grouping"">
			|  &nbsp;&nbsp;&nbsp;&nbsp;%1
			|</div>";
			ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписания, ТекстГруппировки);
		Иначе
			
			ШаблонОписания = "
			|<div class=""grouping"">
			|  <details>
			|    <summary>
			|      %1
			|    </summary>
			|    <div class=""description"">
			|      %2
			|    </div>
			|  </details>
			|</div>";
			
			ФорматированноеОписание = ФорматированноеОписание(Выборка.Описание);
			ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОписания,
				ТекстГруппировки,
				ФорматированноеОписание);
				
			ИндексПоследнегоБлокаСОписанием = ОписаниеИерархии.Количество();
			
		КонецЕсли;
		
		ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОписания,
			Выборка.Код,
			ИмяГруппировки(Выборка.ДлинаКодаБезТочек),
			Выборка.Наименование);
		ОписаниеИерархии.Добавить(ТекстОписания);
	КонецЦикла;
	
	ШаблонЗаголовка = "<h3>img_ %1 | %2 </h3>";

	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонЗаголовка,
		Выборка.Код, 
		Выборка.Наименование);
	
	ЧастиТелаОписания.Добавить(ТекстЗаголовка);
	
	РаскрытьДеталиПоследнегоБлока(ОписаниеИерархии, ИндексПоследнегоБлокаСОписанием);
	
	ЧастиТелаОписания.Добавить(СтрСоединить(ОписаниеИерархии, Символы.ПС));
	
	ТелоОписания = СтрСоединить(ЧастиТелаОписания, Символы.ПС);
	
	АдресРезультатаПоиска = ПараметрыОписания.АдресРезультатаПоиска;
	Если ЭтоАдресВременногоХранилища(АдресРезультатаПоиска) Тогда
		НайденныеСлова = ПолучитьИзВременногоХранилища(АдресРезультатаПоиска);
		Если ТипЗнч(НайденныеСлова) = Тип("Соответствие") Тогда
			Для Каждого НайденноеСлово Из НайденныеСлова Цикл
				ТелоОписания = СтрЗаменитьПоРегулярномуВыражению(ТелоОписания, НайденноеСлово.Ключ, НайденноеСлово.Значение, Ложь);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
	ТекстКартинки = "";
	ИдентификаторыГиперссылок = КлассификаторОКВЭДБПКлиентСервер.ИдентификаторыГиперссылок();
	
	Если ЭтоИзбранное <> Неопределено Тогда
		
		Картинка = БиблиотекаКартинок.ВыборОКВЭДМои;
		ТекстПодсказки = НСтр("ru = 'Добавить  в ""Мои виды деятельности""'");
		
		Если ЭтоИзбранное Тогда
			Картинка = БиблиотекаКартинок.ЗвездаИзбранноеЖелтая;
			ТекстПодсказки = НСтр("ru = 'Исключить из ""Моих видов деятельности""'");
		КонецЕсли;
		
		ДвоичныеДанныеКартинки = Картинка.ПолучитьДвоичныеДанные();
		Base64ДанныеКартинки = Base64Строка(ДвоичныеДанныеКартинки);
		ТекстКартинки = СтрШаблон(
			"<a href=""%1"">
			|  <img src='data:image/%2;base64,%3' title='%4' align='texttop' style='margin-right:5px; position: relative;'></a>",
			СтрШаблон("%1%2", ИдентификаторыГиперссылок.ИзменитьИзбранное, Выборка.Код),
			 "svg+xml",
			Base64ДанныеКартинки,
			ТекстПодсказки);
		
	КонецЕсли;
	
	ТелоОписания = СтрЗаменить(ТелоОписания, "img_", ТекстКартинки);
	ЧастиТекста.Добавить(ТелоОписания);
	
	КоличествоСсылокДляВозврата = СсылкиДляВозврата.Количество();
	Если КоличествоСсылокДляВозврата >= 2
		И СсылкиДляВозврата[КоличествоСсылокДляВозврата- 1].Значение = Код Тогда
			
		ТекстСсылкиНазад = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<div class=""grouping"">
			|  <a href=""%1%2"">&lt;&lt; %2 | %3</a>
			|</div>",
			ИдентификаторыГиперссылок.ВозвратПоСсылке,
			СсылкиДляВозврата[КоличествоСсылокДляВозврата- 2].Значение,
			Выборка.Наименование);
			
		ЧастиТекста.Добавить(ТекстСсылкиНазад);

	КонецЕсли;
	
	ЧастиТекста.Добавить("</body>");
	ЧастиТекста.Добавить("</html>");
	HTMLТекстОписания = СтрСоединить(ЧастиТекста, Символы.ПС);
	
	Возврат HTMLТекстОписания;
	
КонецФункции

// Возвращает список параметров, влияющих на формирование html-описания
// 
// Возвращаемое значение:
//  Структура:
// * Код - Строка 
// * СсылкиДляВозврата - СписокЗначений из Строка
// * ЭтоИзбранное - Неопределено - код не доступен для выбора
//                - Булево
// * СократитьИерархию - Булево - если Истина, то в описании отсутствуют описания и названия вышестоящих группировок
// * АдресРезультатаПоиска - Строка
Функция НовыйПараметрыОписания() Экспорт
	
	ПараметрыОписания = Новый Структура;
	
	ПараметрыОписания.Вставить("Код", "");
	ПараметрыОписания.Вставить("СсылкиДляВозврата", Новый СписокЗначений);
	ПараметрыОписания.Вставить("ЭтоИзбранное", Неопределено);
	ПараметрыОписания.Вставить("СократитьИерархию", Ложь);
	ПараметрыОписания.Вставить("АдресРезультатаПоиска", "");

	Возврат ПараметрыОписания;
	
КонецФункции

// Выполняет поиск по строке в классификаторе ОКВЭД и возвращает результат поиска.
// Поиск выполняется с помощью механизма полнотекстового поиска данных.
// Если полнотекстовый отключен или не дал результата, поиск осуществляется запросом.
// Для этого предварительно строка поиска разбивается на отдельные слова.
//
// Параметры:
//   СтрокаПоиска - Строка - строка, по которой необходимо выполнить поиск
//
// Возвращаемое значение:
//  см. НовыйРезультатПоискаПоСтроке
Функция РезультатПоискаПоСтроке(СтрокаПоиска) Экспорт
	
	РезультатПоискаПоСтроке = НовыйРезультатПоискаПоСтроке();
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		Возврат РезультатПоискаПоСтроке;
	КонецЕсли;
	
	СостояниеПолнотекстовогоПоиска = ПолнотекстовыйПоискСервер.СостояниеПолнотекстовогоПоиска();

	Если СостояниеПолнотекстовогоПоиска <> "ПоискЗапрещен" Тогда
		
		РазмерПорции = 200;
		// Не все слова хорошо ищутся полнотекстовым поиском. Например, сами коды ОКВЭД при поиске разбиваются на слова, разделителями которых являются точки.
		// Например, при поиске кода "01.21" будет найден в том числе и код "01.49.21".
		// Чтобы этого избежать, ищем слова с точками в середине слова по точному совпадению, окружив его кавычками.
		СписокСлов = СтрРазделить(СтрокаПоиска, " ", Ложь);
		СписокСловПолнотекстовыйПоиск = Новый Массив;
		Для Каждого Слово Из СписокСлов Цикл
			Если СтрНайти(Слово, ".") <> 0
				И Не СтрНачинаетсяС(Слово, ".")
				И Не СтрЗаканчиваетсяНа(Слово, ".")
				И Не СтрНачинаетсяС(Слово, """") Тогда
				СписокСловПолнотекстовыйПоиск.Добавить(СтрШаблон("""%1""", Слово));
			Иначе
				СписокСловПолнотекстовыйПоиск.Добавить(Слово);
			КонецЕсли;
		КонецЦикла;
		
		СтрокаПоискаПолнотекстовыйПоиск = СтрСоединить(СписокСловПолнотекстовыйПоиск, " ");
		
		// Основным считаем полнотекстовый поиск и пробуем использовать его.
		СписокПоиска = ПолнотекстовыйПоиск.СоздатьСписок(СтрокаПоискаПолнотекстовыйПоиск);
		СписокПоиска.ИспользованиеМетаданных = ИспользованиеМетаданныхПолнотекстовогоПоиска.НеИспользовать;
		СписокПоиска.ПолучатьОписание = Ложь;
		СписокПоиска.ПолучатьПредставление = Ложь;
		СписокПоиска.ОбластьПоиска = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Метаданные.РегистрыСведений.ДанныеОКВЭД);
		СписокПоиска.ПолучатьОписание = Истина;
		СписокПоиска.ПолучатьПредставление = Истина;
		СписокПоиска.РазмерПорции = РазмерПорции;

		Попытка
		
			СписокПоиска.ПерваяЧасть();
			КоличествоНайденныхЭлементов = СписокПоиска.ПолноеКоличество();
		
			// Обработка данных
			НачальнаяПозиция    = 0;
			ЕстьСледующаяПорция = Истина;
	
			Пока ЕстьСледующаяПорция Цикл
				
				ДобавитьНайденноеВРезультатПоиска(СписокПоиска, РезультатПоискаПоСтроке);
				
				НачальнаяПозиция    = НачальнаяПозиция + РазмерПорции;
				ЕстьСледующаяПорция = (НачальнаяПозиция < КоличествоНайденныхЭлементов - 1);
				
				Если ЕстьСледующаяПорция Тогда
					
					СписокПоиска.СледующаяЧасть();
				
				КонецЕсли;
			КонецЦикла

		Исключение

			ИнформацияОбОшибке = ИнформацияОбОшибке();
		
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка при использовании полнотекстового поиска'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ДанныеОКВЭД,
				,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
		КонецПопытки;
	КонецЕсли;
	
	// Полнотекстовый поиск не всегда дает нужные результаты. Например, поиск по коду "01.1" отбрасывает коды "01.11", "01.11.1" и т. д.
	// Поэтому пробуем поискать обычным поиском.
	Слова = СтрРазделить(СтрЗаменить(СтрокаПоиска, """", ""), " ", Ложь);
		
	Если ПустаяСтрока(СтрСоединить(Слова)) Тогда
		Возврат РезультатПоискаПоСтроке;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ
	|	ДанныеОКВЭД.Код КАК Код,
	|	ДанныеОКВЭД.КодБезТочек КАК КодБезТочек,
	|	ДанныеОКВЭД.Наименование КАК Наименование,
	|	ДанныеОКВЭД.Описание КАК Описание
	|ИЗ
	|	РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭД
	|ГДЕ
	|	&УсловиеПоискНайденныхСлов = &КоличествоНайденныхСлов";
	
	ШаблонУсловиеКоличествоСлов =
	"	ВЫБОР
	|		КОГДА СТРНАЙТИ(ДанныеОКВЭД.Наименование, &Слово[НомерСлова]) <> 0
	|		ИЛИ СТРНАЙТИ(ДанныеОКВЭД.Описание, &Слово[НомерСлова]) <> 0
	|		ИЛИ СТРНАЙТИ(ДанныеОКВЭД.Код, &Слово[НомерСлова]) <> 0
	|		ИЛИ СТРНАЙТИ(ДанныеОКВЭД.КодБезТочек, &Слово[НомерСлова]) <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ";
	
	ЧастиУсловияКоличествоСлов = Новый Массив;
	ПараметрЗамены = Новый Структура("НомерСлова", "");
	КоличествоСлов = 0;
	Для НомерСлова = 0 По Слова.Количество() - 1 Цикл
		Если ПустаяСтрока(Слова[НомерСлова]) Тогда
			Продолжить;
		КонецЕсли;
		ПараметрЗамены.НомерСлова = XMLСтрока(НомерСлова);
		Условие = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонУсловиеКоличествоСлов, ПараметрЗамены);
		ЧастиУсловияКоличествоСлов.Добавить(Условие);
		ИмяПараметра = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку("Слово[НомерСлова]", ПараметрЗамены);
		Запрос.УстановитьПараметр(ИмяПараметра, Слова[НомерСлова]);
		КоличествоСлов = КоличествоСлов + 1;
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(ШаблонТекстаЗапроса, "&УсловиеПоискНайденныхСлов", СтрСоединить(ЧастиУсловияКоличествоСлов, " + "));
	Запрос.УстановитьПараметр("КоличествоНайденныхСлов", КоличествоСлов);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаКодов = Результат.Выбрать();
	
	ЧастиТекста = Новый Массив;
	Пока ВыборкаКодов.Следующий() Цикл
		РезультатПоискаПоСтроке.СписокКодов.Вставить(ВыборкаКодов.Код, Истина);
		ЧастиТекста.Добавить(ВыборкаКодов.Код);
		ЧастиТекста.Добавить(ВыборкаКодов.Наименование);
		ЧастиТекста.Добавить(ВыборкаКодов.Описание);
	КонецЦикла;
	
	ТаблицаЗамены = РезультатПоискаПоСтроке.ТекстыФорматирования;
	
	ВсеНайденныеТексты = СтрСоединить(ЧастиТекста, " ");
	СловаДляФорматирования = Новый Массив;
	Для Каждого Слово Из Слова Цикл
		ЭтоЧисло = ОбщегоНазначенияКлиентСервер.ЭтоЧисло(Слово);
		Если СтрДлина(СокрЛП(Слово)) <= 2 И Не ЭтоЧисло Тогда
			Продолжить;
		КонецЕсли;
		Если ТаблицаЗамены[Слово] = Неопределено Тогда
			СловаДляФорматирования.Добавить(Слово);
		КонецЕсли;
		
		Если ЭтоЧисло И СтрДлина(СокрЛП(Слово)) > 2  Тогда
			КодИзЧисла = КодИзЧисла(Слово);
			Если ТаблицаЗамены[КодИзЧисла] = Неопределено Тогда
				СловаДляФорматирования.Добавить(КодИзЧисла);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если СловаДляФорматирования.Количество() > 0 Тогда
		ПоискЛюбоеИзСлов = СтрШаблон("(%1)", СтрСоединить(СловаДляФорматирования, "|"));
		РезультатПоискаЛюбоеИзСлов = СтрНайтиВсеПоРегулярномуВыражению(ВсеНайденныеТексты, ПоискЛюбоеИзСлов, Истина);
		
		Для Каждого ЭлементРезультата Из РезультатПоискаЛюбоеИзСлов Цикл
			ТаблицаЗамены.Вставить(
				ЭлементРезультата.Значение,
				СтрШаблон("<span style='background-color:#FFFF99'>%1</span>", ЭлементРезультата.Значение));
		КонецЦикла;
	КонецЕсли;
	
	Возврат РезультатПоискаПоСтроке;
	
КонецФункции

// Формирует представление разделов ОКВЭД для отображения в дереве.
//
// Возвращаемое значение:
//   Соответствие из КлючИЗначение:
//     * Ключ - Строка - код раздела (заглавная латинская буква)
//     * Значение - Строка
Функция ПредставлениеРазделов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(ВЫРАЗИТЬ(ДанныеОКВЭД.Код КАК СТРОКА(2))) КАК МинимальныйКодРаздела,
		|	МАКСИМУМ(ВЫРАЗИТЬ(ДанныеОКВЭД.Код КАК СТРОКА(2))) КАК МаксимальныйКодРаздела,
		|	ДанныеОКВЭД.Раздел КАК Раздел
		|ИЗ
		|	РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭД
		|ГДЕ
		|	ДанныеОКВЭД.Раздел <> ДанныеОКВЭД.Код
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОКВЭД.Раздел";
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ПредставлениеРазделов = Новый Соответствие();
	Пока Выборка.Следующий() Цикл
		ШаблонПредставления = НСтр("ru = '%1 (коды %2–%3)'");
		Если Выборка.МинимальныйКодРаздела = Выборка.МаксимальныйКодРаздела Тогда
			ШаблонПредставления = НСтр("ru = '%1 (код %2)'");
		КонецЕсли;
		ТекстПредставления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонПредставления,
			Выборка.Раздел,
			Выборка.МинимальныйКодРаздела,
			Выборка.МаксимальныйКодРаздела);
		ПредставлениеРазделов.Вставить(Выборка.Раздел, ТекстПредставления);
	КонецЦикла;
	
	Возврат ПредставлениеРазделов;
	
КонецФункции

// Формирует временные таблицы для СКД
// 
// Возвращаемое значение:
//  МенеджерВременныхТаблиц - временные таблицы.
//  
Функция МенеджерВременныхТаблицРазделов() Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	ТаблицаРазделов = КлассификаторОКВЭД.Разделы();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗРазделы.Раздел КАК Раздел,
		|	ТЗРазделы.Наименование КАК Наименование,
		|	ТЗРазделы.Описание КАК Описание
		|ПОМЕСТИТЬ ВТРазделы
		|ИЗ
		|	&ТЗРазделы КАК ТЗРазделы";
	Запрос.УстановитьПараметр("ТЗРазделы", ТаблицаРазделов);
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступенДляВыбора(Код)
	
	КоличествоЦифрВКоде = СтрДлина(СтрЗаменить(Код, ".", ""));
	Возврат КоличествоЦифрВКоде >= 4;
	
КонецФункции

// Возвращает иерархию кодов начиная с самого общего (раздела).
// 
// Параметры:
//  Код - Строка
//  СократитьИерархию - Булево
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   * Код - Строка
//   * Наименование - Строка
//   * Описание - Строка
//   * ДлинаКодБезТочек - Число
Функция ИерархияКодовСОписанием(Код, СократитьИерархию)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблицРазделов();
		
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("ИерархияКодов", ИерархияКодов(Код));

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеОКВЭД.Раздел КАК Раздел
		|ПОМЕСТИТЬ РазделКода
		|ИЗ
		|	РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭД
		|ГДЕ
		|	ДанныеОКВЭД.Код = &Код
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеОКВЭД.Раздел КАК Раздел
		|ИЗ
		|	РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭД
		|ГДЕ
		|	ДанныеОКВЭД.Раздел = &Код
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОКВЭД.Код КАК Код,
		|	ДанныеОКВЭД.Наименование КАК Наименование,
		|	ДанныеОКВЭД.Описание КАК Описание,
		|	ДанныеОКВЭД.ДлинаКодаБезТочек КАК ДлинаКодаБезТочек
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеОКВЭД.Код КАК Код,
		|		ДанныеОКВЭД.Наименование КАК Наименование,
		|		ДанныеОКВЭД.Описание КАК Описание,
		|		ДЛИНАСТРОКИ(ДанныеОКВЭД.КодБезТочек) КАК ДлинаКодаБезТочек
		|	ИЗ
		|		РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭД
		|	ГДЕ
		|		ДанныеОКВЭД.Код В (&ИерархияКодов)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ДанныеОКВЭДРазделы.Раздел,
		|		ДанныеОКВЭДРазделы.Наименование,
		|		ДанныеОКВЭДРазделы.Описание,
		|		1
		|	ИЗ
		|		ВТРазделы КАК ДанныеОКВЭДРазделы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазделКода КАК РазделКода
		|			ПО ДанныеОКВЭДРазделы.Раздел = РазделКода.Раздел) КАК ДанныеОКВЭД
		|ГДЕ
		|	&УсловиеСократитьИерархию
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДлинаКодаБезТочек";
	
	Если СократитьИерархию Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСократитьИерархию", "ДанныеОКВЭД.Код = &Код");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСократитьИерархию", XMLСтрока(Истина));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Возврат Выборка;
	
КонецФункции

Функция ИмяГруппировки(ДлинаКодаБезТочек)
	
	ИмяГруппировки = "";
	Если ДлинаКодаБезТочек = 1 Тогда
		ИмяГруппировки =  НСтр("ru = 'Раздел'");
	ИначеЕсли ДлинаКодаБезТочек = 2 Тогда
		ИмяГруппировки =  НСтр("ru = 'Класс'");
	ИначеЕсли ДлинаКодаБезТочек = 3 Тогда
		ИмяГруппировки =  НСтр("ru = 'Подкласс'");
	ИначеЕсли ДлинаКодаБезТочек = 4 Тогда
		ИмяГруппировки =  НСтр("ru = 'Группа'");
	ИначеЕсли ДлинаКодаБезТочек = 5 Тогда
		ИмяГруппировки =  НСтр("ru = 'Подгруппа'");
	ИначеЕсли ДлинаКодаБезТочек = 6 Тогда
		ИмяГруппировки =  НСтр("ru = 'Вид'");
	КонецЕсли;
	
	Возврат ИмяГруппировки;
	
КонецФункции

Функция ИерархияКодов(Код)
	
	ИерархияКодов = Новый Массив;
	Для ПозицияСимвола = 1 По СтрДлина(Код) Цикл
		КодИерархии = Лев(Код, ПозицияСимвола);
		Если СтрЗаканчиваетсяНа(КодИерархии, ".") Тогда
			Продолжить;
		КонецЕсли;
		ИерархияКодов.Добавить(КодИерархии);
	КонецЦикла;
	
	Возврат ИерархияКодов;
	
КонецФункции

Процедура РаскрытьДеталиПоследнегоБлока(Знач ОписаниеИерархии, Знач ИндексПоследнегоБлокаСОписанием)
	ОписаниеИерархии[ИндексПоследнегоБлокаСОписанием] =
		СтрЗаменить(ОписаниеИерархии[ИндексПоследнегоБлокаСОписанием], "<details>", "<details open = """">");
КонецПроцедуры

Функция ФорматированноеОписание(Знач Описание)
	
	ИдентификаторГиперссылки = КлассификаторОКВЭДБПКлиентСервер.ИдентификаторыГиперссылок().ПереходПоСсылке;
	
	// Заменяем строки в формате "пробел01" или "пробел01.1" или "пробел01.11" или "пробел01.11.1" или "пробел01.11.11"
	// Если встречаются выражения в виде цифр, которые не нужно заменять гиперссылками, например, "от 20 октября", "60%"
	// то в классификаторе перед такими числами должен стоять неразрывный пробел (Alt+0160)
	РегулярноеВыражениеПоискКодов = " ((\d){2}(\.(\d){1,2})?(\.(\d){1,2})?)"; // первый символ - обычный пробел
	Описание = СтрЗаменитьПоРегулярномуВыражению(
		Описание,
		РегулярноеВыражениеПоискКодов,
		СтрШаблон(" <a href=""%1$1"">$1</a>", ИдентификаторГиперссылки)); // $0 - все выражение целиком, $1 - первое выражение в скобках,
		                                                                  // $2 - второе и т. д.
	
	// Добавим ссылку на раздел
	// выражение "раздел[\S]{0,2}" - означает слово "раздел" и максимум два непробельных символа до следующего выражения.
	// Например, "раздел A", "разделу A", "разделом A"
	РегулярноеВыражениеПоискРазделов = "раздел[\S]{0,2}+\s([A-Z])";
	Описание = СтрЗаменитьПоРегулярномуВыражению(
		Описание,
		РегулярноеВыражениеПоискРазделов,
		СтрШаблон("<a href=""%1$1"">$0</a>", ИдентификаторГиперссылки)); 

	СоставСтрок = СтрРазделить(Описание, Символы.ПС);
	НачатСписок = Ложь;
	ФорматированныйСоставСтрок = Новый Массив;
	
	Для ИндексСтроки = 0 По СоставСтрок.ВГраница() Цикл
		
		СтрокаОписания = СоставСтрок[ИндексСтроки];
		ЭтоПоследняяСтрока = ИндексСтроки = СоставСтрок.ВГраница();
		ЗавершитьСписок = Ложь;
		
		ТекстПримечание = "Примечание -";
		Если СтрНайти(СтрокаОписания, ТекстПримечание) <> 0 Тогда
			СтрокаОписания = СтрШаблон(
				"%1 %2 %3",
				"<div class=""note"">",
				СокрЛП(СтрЗаменить(СтрокаОписания, ТекстПримечание, "Примечание: ")),
				"</div>");
		КонецЕсли;
		
		ЭтоПунктСписка = Ложь;
		Если НачатСписок Тогда
			
			Если СтрНачинаетсяС(СокрЛП(СтрокаОписания), "-") Тогда
				СтрокаОписания = СтрШаблон("%1 %2 %3", "<li>", СокрЛП(Сред(СокрЛП(СтрокаОписания), 2)), "</li>");
				ЭтоПунктСписка = Истина;
			Иначе
				НачатСписок = Ложь;
				ЗавершитьСписок = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		ИсходнаяСтрока = СтрокаОписания;
		Если Не ЭтоПунктСписка Тогда
			// выделение красным цветом "не включает"
			СтрокаОписания = СтрЗаменитьПоРегулярномуВыражению(
				СтрокаОписания,
				"(.*)(не (включ(ают|ает|ено|ена)|входят|подразумевает))(.*(?<!\.).*:$)",
				"<b>$1<font color=""#ff0000"">$2</font>$5</b><ul>");
			// выделение зеленым цветом "включает"
			Если ИсходнаяСтрока = СтрокаОписания
				И СтрНайтиПоРегулярномуВыражению(
					СтрокаОписания,
					"(включ(ают|ает|ено|ена)|входят|подразумевает).*:$").НачальнаяПозиция <> 0 Тогда // оптимизация для длинных строк
					
				СтрокаОписания = СтрЗаменитьПоРегулярномуВыражению(
					СтрокаОписания,
					"(.*)((?<!не) (включ(ают|ает|ено|ена)|входят|подразумевает))(.*(?<!\.).*:$)",
					"<b>$1<font color=""#009646"">$2</font>$5</b><ul>");
					
			КонецЕсли;
			
		КонецЕсли;
		
		ЭтоНачалоСписка = ИсходнаяСтрока <> СтрокаОписания;
		
		НачатСписок = НачатСписок Или ЭтоНачалоСписка;
			
		Если Не ЭтоПоследняяСтрока И Не ЭтоНачалоСписка Тогда
			СтрокаОписания = СтрШаблон("%1 %2", СтрокаОписания, "<p></p>");
		КонецЕсли;
		
		Если ЗавершитьСписок Тогда
			СтрокаОписания = СтрШаблон("%1 %2", "</ul>", СтрокаОписания);
		КонецЕсли;
		
		ФорматированныйСоставСтрок.Добавить(СтрокаОписания);
		
	КонецЦикла;
	
	ФорматированноеОписание = СтрСоединить(ФорматированныйСоставСтрок);
	
	Возврат ФорматированноеОписание;
КонецФункции

// Конструктор результата поиска по строке.
// 
// Возвращаемое значение:
//  Структура:
// * СписокКодов - Соответствие из КлючИЗначение:
//   ** Ключ - Строка - Код из классификатора
//   ** Значение - Булево - может принимать только значение Истина
// * ТекстыФорматирования - Соответствие из КлючИЗначение - замена для форматирования описания:
//   ** Ключ - Строка
//   ** Значение - Строка
Функция НовыйРезультатПоискаПоСтроке()
	РезультатПоискаПоСтроке = Новый Структура;
	РезультатПоискаПоСтроке.Вставить("СписокКодов", Новый Соответствие);
	РезультатПоискаПоСтроке.Вставить("ТекстыФорматирования", Новый Соответствие);
	Возврат РезультатПоискаПоСтроке;
КонецФункции

Процедура ДобавитьНайденноеВРезультатПоиска(СписокПоиска, РезультатПоискаПоСтроке)

	Для НомерПорции = 0 По СписокПоиска.Количество() - 1 Цикл
		ЭлементСписка = СписокПоиска.Получить(НомерПорции);
		РезультатПоискаПоСтроке.СписокКодов.Вставить(ЭлементСписка.Значение.Код, Истина);
	КонецЦикла;
	
	ОтображениеПоиска = СписокПоиска.ПолучитьОтображение(ВидОтображенияПолнотекстовогоПоиска.HTMLТекст);
	Построитель = Новый ПостроительDOM;
	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.УстановитьСтроку(ОтображениеПоиска);
	ДокументHTML = Построитель.Прочитать(ЧтениеHTML);
	НайденныеСловаСФорматированием = ДокументHTML.НайтиПоФильтру(
		"
		|{ 
		| ""type"": ""elementname"",
		| ""value"": {
		|   ""value"": ""span"", 
		|   ""operation"": ""equals""}
		|}");
		
	Для Каждого ЭлементHTML Из НайденныеСловаСФорматированием Цикл
		ТекстЗамены = ЭлементHTML.ТекстовоеСодержимое;
		// Маленькие слова цветом не выделяем, поскольку они с большой вероятностью могут войти в состав других слов.
		ЭтоЧисло = ОбщегоНазначенияКлиентСервер.ЭтоЧисло(ТекстЗамены);
		Если СтрДлина(СокрЛП(ТекстЗамены)) <= 2 И Не ЭтоЧисло Тогда
			Продолжить;
		КонецЕсли;
		ЗначениеЦвета = ЭлементHTML.ПолучитьАтрибут("style");
		ТаблицаЗамены = РезультатПоискаПоСтроке.ТекстыФорматирования;
		ТаблицаЗамены.Вставить(ТекстЗамены, СтрШаблон("<span style='%1'>%2</span>", ЗначениеЦвета, ТекстЗамены));
		Если ЭтоЧисло И СтрДлина(СокрЛП(ТекстЗамены)) > 2 Тогда
			КодИзКодаБезТочек = КодИзЧисла(ТекстЗамены);
			ТаблицаЗамены.Вставить(КодИзКодаБезТочек, СтрШаблон("<span style='%1'>%2</span>", ЗначениеЦвета, КодИзКодаБезТочек));
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Функция КодИзЧисла(КодБезТочек)
	
	ЧастиКода = Новый Массив;
	Пока СтрДлина(КодБезТочек) > 0 Цикл
		ЧастиКода.Добавить(Лев(КодБезТочек, 2));
		КодБезТочек = Сред(КодБезТочек, 3);
	КонецЦикла;
	
	Возврат СтрСоединить(ЧастиКода, ".");
	
КонецФункции

#КонецОбласти
