
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриПроверкеГотовностиКДокументообороту(ОбъектыУчета, ОбработчикПродолжения, СтандартнаяОбработка = Истина) Экспорт
	
	Если Не ТребуетсяПроверкаСчетовНаОплатуСоСкидкамиПоДокументу(ОбъектыУчета) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ОбработатьСчетаНаОплатуСоСкидкамиПоДокументу(ОбъектыУчета, ОбработчикПродолжения);
	
КонецПроцедуры

Функция КлючОбъектаСчетНаОплату() Экспорт
	
	Возврат "Документ_СчетНаОплатуПокупателю";
	
КонецФункции

Функция КлючНастроекВсегдаРаспределятьСкидкуВСчетеНаОплату() Экспорт
	
	Возврат "ВсегдаРаспределятьСкидкуПередФормированиемЭД";
	
КонецФункции

Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	Таймаут = 30;
	ПодключитьОбработчикОжидания("Подключаемый_НастройкаАвтоматическогоПриемаПриглашенийЭДО", Таймаут, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьСчетаНаОплатуСоСкидкамиПоДокументу(ОбъектыУчета, ОбработчикПродолжения)
	
	СчетаНаОплатуСоСкидкойПоДокументу = ОбменСКонтрагентамиБПВызовСервера.СчетаТребующиеРаспределенияСкидокПоСтрокам(ОбъектыУчета);
	Если Не ЗначениеЗаполнено(СчетаНаОплатуСоСкидкойПоДокументу) Тогда
		ВыполнитьОбработкуОповещения(ОбработчикПродолжения, ОбъектыУчета);
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекЗагрузить(КлючОбъектаСчетНаОплату(), КлючНастроекВсегдаРаспределятьСкидкуВСчетеНаОплату(), Ложь) Тогда
		РаспределитьСкидкиСчетовНаОплатуПоСтрокам(СчетаНаОплатуСоСкидкойПоДокументу, ОбъектыУчета, ОбработчикПродолжения);
	Иначе
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОбработчикПродолжения", ОбработчикПродолжения);
		ДополнительныеПараметры.Вставить("ОбъектыУчета", ОбъектыУчета);
		ДополнительныеПараметры.Вставить("СчетаНаОплатуСоСкидкойПоДокументу", СчетаНаОплатуСоСкидкойПоДокументу);
		ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ПослеВопросаОРаспределенииСкидокВСчетеНаОплату",
			ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СчетаНаОплатуСоСкидкойПоДокументу", СчетаНаОплатуСоСкидкойПоДокументу);
		ПараметрыФормы.Вставить("ВыделеноНесколькоСчетов", ОбъектыУчета.Количество() > 1);
		ОткрытьФорму("Документ.СчетНаОплатуПокупателю.Форма.ВопросОРаспределенииСкидок",
			ПараметрыФормы,,,,, ОбработчикЗакрытияФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВопросаОРаспределенииСкидокВСчетеНаОплату(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПродолжения, Новый Массив);
		Возврат;
	КонецЕсли;
	
	РаспределитьСкидкиСчетовНаОплатуПоСтрокам(ДополнительныеПараметры.СчетаНаОплатуСоСкидкойПоДокументу,
		ДополнительныеПараметры.ОбъектыУчета, ДополнительныеПараметры.ОбработчикПродолжения);
	
КонецПроцедуры

Функция ТребуетсяПроверкаСчетовНаОплатуСоСкидкамиПоДокументу(ОбъектыУчета)
	
	Если Не ЗначениеЗаполнено(ОбъектыУчета) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого ОбъектУчета Из ОбъектыУчета Цикл
		Если ТипЗнч(ОбъектУчета) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура РаспределитьСкидкиСчетовНаОплатуПоСтрокам(СчетаНаОплатуСоСкидкойПоДокументу, ОбъектыУчета, ОбработчикПродолжения)
	
	ОбменСКонтрагентамиБПВызовСервера.РаспределитьСкидкиСчетовНаОплатуПоСтрокам(СчетаНаОплатуСоСкидкойПоДокументу);
	Оповестить("СчетНаОплату_СкидкаРаспределенаПоСтрокам", СчетаНаОплатуСоСкидкойПоДокументу);
	
	// Обновим динамические списки
	Для Каждого СчетНаОплату Из СчетаНаОплатуСоСкидкойПоДокументу Цикл
		ОповеститьОбИзменении(СчетНаОплату);
	КонецЦикла;
	
	ВыполнитьОбработкуОповещения(ОбработчикПродолжения, ОбъектыУчета);
	
КонецПроцедуры

#КонецОбласти