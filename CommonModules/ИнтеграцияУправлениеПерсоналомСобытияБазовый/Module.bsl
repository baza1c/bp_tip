
#Область СлужебныйПрограммныйИнтерфейс

#Область РегистрацияПубликуемыхОбъектов

Процедура ТекущиеКадровыеДанныеСотрудниковПередЗаписью(НаборЗаписей, Замещение) Экспорт
	
	ПрежниеДанные = РегистрыБЗК.ЗаписиНабораИзБазыДанных(НаборЗаписей, Замещение, ИменаПолейОтбора());
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТЗ Из ПрежниеДанные Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ТекущаяОрганизация) Тогда
			СтрокиКУдалению.Добавить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПрежниеДанные", ПрежниеДанные);
	

КонецПроцедуры

Процедура ТекущиеКадровыеДанныеСотрудниковПриЗаписи(НаборЗаписей, Замещение) Экспорт
	
	ПрежниеДанные = Неопределено;
	НаборЗаписей.ДополнительныеСвойства.Свойство("ПрежниеДанные", ПрежниеДанные);
	Если ПрежниеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовыеДанные = РегистрыБЗК.ЗаписиНабораИзБазыДанных(НаборЗаписей, Замещение, ИменаПолейОтбора());
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТЗ Из НовыеДанные Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ТекущаяОрганизация) Тогда
			СтрокиКУдалению.Добавить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(НовыеДанные, "Сотрудник", Истина);
	
	Отбор = Новый Структура("Сотрудник");
	СписокСотрудников = Новый Массив;
	
	Для каждого Сотрудник Из Сотрудники Цикл
		Отбор.Сотрудник = Сотрудник;
		ПрежниеДанныеПоСотруднику 	= ПрежниеДанные.Скопировать(Отбор);
		НовыеДанныеПоСотруднику 	= НовыеДанные.Скопировать(Отбор);
		Если Не ОбщегоНазначения.КоллекцииИдентичны(ПрежниеДанныеПоСотруднику, НовыеДанныеПоСотруднику) Тогда
			СписокСотрудников.Добавить(Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокСотрудников.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&СписокСотрудников)
		|	И Сотрудники.ГоловнойСотрудник = Сотрудники.Ссылка";
		ТаблицаСотрудников  = Запрос.Выполнить().Выгрузить();
		
		Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		ИнтеграцияУправлениеПерсоналом.ЗарегистрироватьИзменениеКадровойИстории(ТаблицаСотрудников, Приложение);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИменаКонтролируемыхРеквизитов(Объект) Экспорт

	ИменаКонтролируемыхРеквизитов = "";
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.СтруктураПредприятия") Тогда
		ИменаКонтролируемыхРеквизитов = "Наименование,Код,Родитель";
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ШтатноеРасписание") Тогда
		ИменаКонтролируемыхРеквизитов = "Наименование,ПометкаУдаления";
	КонецЕсли;
	
	Возврат ИменаКонтролируемыхРеквизитов;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИменаПолейОтбора()

	ИменаПолей = "Сотрудник,ДатаПриема,ДатаУвольнения,ГоловнаяОрганизация,ТекущаяОрганизация,ТекущееПодразделение,ТекущаяДолжность,ТекущийВидЗанятости";
	
	Возврат ИменаПолей;

КонецФункции

#КонецОбласти