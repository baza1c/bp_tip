#Область ПрограммныйИнтерфейс

// Возвращает признак необходимости контролировать право применения спецрежима для организации
// 
// Параметры:
//   Организация - СправочникСсылка.Организации
//   НаДату - Дата, Неопределено - дата проверки
// 
// Возвращаемое значение:
//   Булево - Истина, если нужно контролировать
//
Функция Контролировать(Организация, Знач НаДату = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаДату) Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ПрименяемыйСпецрежим = ПрименяемыйСпецрежим(Организация, НаДату);
	
	Возврат ПрименяемыйСпецрежим.УСН Или ПрименяемыйСпецрежим.АУСН Или ПрименяемыйСпецрежим.Патент Или ПрименяемыйСпецрежим.НПД;
	
КонецФункции

// Возвращает структуру, с информацией о праве применения спецрежима
// по показателю.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация, для которой получаем информацию
//  ИмяПоказателя - Строка - см. процедуры ИмяПоказателяСпецрежимаДоходы() и прочие
//
// Возвращаемое значение:
//  Структура - См. НовыйИнформацияОПравеПримененияСпецрежима()
//
Функция ИнформацияОПравеПримененияСпецрежима(Организация, ИмяПоказателя) Экспорт
	
	ДатаПроверки = ОбщегоНазначения.ТекущаяДатаПользователя();
	ПрименяемыйСпецрежим = ПрименяемыйСпецрежим(Организация, ДатаПроверки);

	// Если это не спецрежим, тогда не показываем информацию
	Если Не ПрименяемыйСпецрежим.УСН И Не ПрименяемыйСпецрежим.Патент И Не ПрименяемыйСпецрежим.АУСН И Не ПрименяемыйСпецрежим.НПД Тогда
		Возврат НовыйИнформацияОПравеПримененияСпецрежима();
	КонецЕсли;
	
	ИнформацияПоПоказателю = ИнформацияПоПоказателю(ИмяПоказателя, Организация, ДатаПроверки);
	ИнформацияПоПоказателю.ПрименяетсяУСНДоходы = ПрименяемыйСпецрежим.УСНДоходы;
	
	ИнформацияОПравеПримененияСпецрежима = НовыйИнформацияОПравеПримененияСпецрежима();
	
	ПрименяетсяПатент = ПрименяемыйСпецрежим.Патент Или ПрименяемыйСпецрежим.ТолькоПатент;
	ИнформацияОПравеПримененияСпецрежима.Вставить(ИмяНастройкиПравоПримененияСпецрежимаПатент(), ПрименяетсяПатент);
	
	ИмяПоказателяДополнительнойИнформации = "";
	Если ИмяПоказателя = ИмяПоказателяСпецрежимаДоходы() Тогда
		ИмяПоказателяДополнительнойИнформации = ИмяПоказателяСпецрежимаДоходыДополнительно();
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИнформацияПоПоказателю.ВидДополнительногоСообщения)
		И Не ПустаяСтрока(ИмяПоказателяДополнительнойИнформации)
		И ДостигнутоСледующееЗначениеНапоминания(ИмяПоказателяДополнительнойИнформации,
			ИнформацияПоПоказателю.ПроцентДополнительно, Организация, ДатаПроверки) Тогда
		
		ДополнительнаяИнформация = НовыйИнформацияОПравеПримененияСпецрежима();
		ДополнительнаяИнформация.Показать = Истина;
		ДополнительнаяИнформация.СледующееЗначениеНапоминания = ЗначениеНеНапоминать();
		ДополнительнаяИнформация.ОтображатьНапоминание = Истина;
		ДополнительнаяИнформация.СсылкаНаПояснение = СсылкаНаПояснениеПоПравуПримененияСпецрежима(
			ИнформацияПоПоказателю,
			ПрименяемыйСпецрежим,
			ИмяПоказателяДополнительнойИнформации);
		ДополнительнаяИнформация.ЗаголовокПояснения = ЗаголовокПоясненияПоПравуПримененияСпецрежима(
			ДополнительнаяИнформация.СсылкаНаПояснение);
		
		ДополнительнаяИнформация.ТекстИнформации = ТекстИнформацияОПоказателе(
			ИнформацияПоПоказателю,
			ИмяПоказателяДополнительнойИнформации,
			ПрименяемыйСпецрежим,
			ДатаПроверки);
		ДополнительнаяИнформация.ПодсказкаТекстИнформации = ПодсказкаИнформацииОПоказателе(
			ИнформацияПоПоказателю, ИмяПоказателяДополнительнойИнформации, ДатаПроверки, ПрименяемыйСпецрежим);
		ДополнительнаяИнформация.ТекстНапомнитьПозже = ТекстНапомнитьПозже(
			ДополнительнаяИнформация.СледующееЗначениеНапоминания);
		ДополнительнаяИнформация.ЦветФонаГруппы = ЦветФонаГруппыПоясненияПоПравуПримененияСпецрежима(
			ИнформацияПоПоказателю, ИмяПоказателяДополнительнойИнформации);
		ДополнительнаяИнформация.ИмяПоказателя = ИмяПоказателяДополнительнойИнформации;
		
		ИнформацияОПравеПримененияСпецрежима.ДополнительнаяИнформация = ДополнительнаяИнформация;
	КонецЕсли;
	
	Если ПрименяетсяПатент Тогда
		ИмяПоказателяДляПроверкиНапоминания = ИмяНастройкиПравоПримененияСпецрежимаПатент();
	Иначе
		ИмяПоказателяДляПроверкиНапоминания = ИмяПоказателя;
	КонецЕсли;
	
	ДостигнутоСледующееЗначениеНапоминания = ДостигнутоСледующееЗначениеНапоминания(ИмяПоказателяДляПроверкиНапоминания,
		ИнформацияПоПоказателю.Процент,
		Организация,
		ДатаПроверки);
	Если Не ДостигнутоСледующееЗначениеНапоминания Тогда
		Возврат ИнформацияОПравеПримененияСпецрежима;
	КонецЕсли;
	
	ИнформацияОПравеПримененияСпецрежима.УтраченоПравоПрименения      = ИнформацияПоПоказателю.УтраченоПравоПрименения;
	ИнформацияОПравеПримененияСпецрежима.Показать                     = Истина;
	ИнформацияОПравеПримененияСпецрежима.СледующееЗначениеНапоминания =
		СледующееЗначениеНапоминания(ИнформацияПоПоказателю.Процент);
	ИнформацияОПравеПримененияСпецрежима.ОтображатьНапоминание        = 
		ИнформацияОПравеПримененияСпецрежима.СледующееЗначениеНапоминания < 100;
	ИнформацияОПравеПримененияСпецрежима.СсылкаНаПояснение = СсылкаНаПояснениеПоПравуПримененияСпецрежима(
		ИнформацияПоПоказателю,
		ПрименяемыйСпецрежим,
		ИмяПоказателя);
	ИнформацияОПравеПримененияСпецрежима.ЗаголовокПояснения = ЗаголовокПоясненияПоПравуПримененияСпецрежима(
		ИнформацияОПравеПримененияСпецрежима.СсылкаНаПояснение);
	
	ИнформацияОПравеПримененияСпецрежима.ТекстИнформации = ТекстИнформацияОПоказателе(
		ИнформацияПоПоказателю,
		ИмяПоказателя,
		ПрименяемыйСпецрежим,
		ДатаПроверки);
	ИнформацияОПравеПримененияСпецрежима.ПодсказкаТекстИнформации = ПодсказкаИнформацииОПоказателе(
		ИнформацияПоПоказателю, ИмяПоказателя, ДатаПроверки, ПрименяемыйСпецрежим);
	ИнформацияОПравеПримененияСпецрежима.ТекстНапомнитьПозже = ТекстНапомнитьПозже(
		ИнформацияОПравеПримененияСпецрежима.СледующееЗначениеНапоминания);
	ИнформацияОПравеПримененияСпецрежима.ЦветФонаГруппы = ЦветФонаГруппыПоясненияПоПравуПримененияСпецрежима(
		ИнформацияПоПоказателю, ИмяПоказателя);
	
	Возврат ИнформацияОПравеПримененияСпецрежима;
	
КонецФункции

// Возвращает структуру, с информацией о праве применения патента
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация, для которой получаем информацию
//  ДатаПроверки - Дата - дата проверки
//
// Возвращаемое значение:
//  Структура - См. НовыйИнформацияОПравеПримененияСпецрежима()
//
Функция ИнформацияОПравеПримененияПатента(Организация, ДатаПроверки) Экспорт
	
	ИмяПоказателя = ИмяПоказателяСпецрежимаДоходы();
	
	ПрименяемыйСпецрежим = Новый Структура;
	ПрименяемыйСпецрежим.Вставить("УСН", Ложь);
	ПрименяемыйСпецрежим.Вставить("УСНДоходы", Ложь);
	ПрименяемыйСпецрежим.Вставить("АУСН", Ложь);
	ПрименяемыйСпецрежим.Вставить("Патент", Истина);
	ПрименяемыйСпецрежим.Вставить("ТолькоПатент", Ложь);
	ПрименяемыйСпецрежим.Вставить("НПД", Ложь);
	
	ИнформацияОПравеПримененияПатента = НовыйИнформацияОПравеПримененияСпецрежима();
	
	ИнформацияПоПоказателю = ИнформацияПоПоказателюДоходы(Организация, ДатаПроверки, ПрименяемыйСпецрежим);
	
	ИнформацияОПравеПримененияПатента.Показать = ЗначениеЗаполнено(ИнформацияПоПоказателю.ВидСообщения) 
		И ПоказыватьНапоминание(ИнформацияПоПоказателю.Процент);
	ИнформацияОПравеПримененияПатента.УтраченоПравоПрименения = ИнформацияПоПоказателю.УтраченоПравоПрименения;
	ИнформацияОПравеПримененияПатента.СсылкаНаПояснение = СсылкаНаПояснениеПоПравуПримененияСпецрежима(
		ИнформацияПоПоказателю,
		ПрименяемыйСпецрежим,
		ИмяПоказателя);
	ИнформацияОПравеПримененияПатента.ЗаголовокПояснения = ЗаголовокПоясненияПоПравуПримененияСпецрежима(
		ИнформацияОПравеПримененияПатента.СсылкаНаПояснение);
	ИнформацияОПравеПримененияПатента.ТекстИнформации = ТекстИнформацияОПоказателе(
		ИнформацияПоПоказателю,
		ИмяПоказателя,
		ПрименяемыйСпецрежим,
		ДатаПроверки);
	ИнформацияОПравеПримененияПатента.ПодсказкаТекстИнформации = ПодсказкаИнформацииОПоказателе(
		ИнформацияПоПоказателю, ИмяПоказателя, ДатаПроверки, ПрименяемыйСпецрежим);
	ИнформацияОПравеПримененияПатента.ЦветФонаГруппы = ЦветФонаГруппыПоясненияПоПравуПримененияСпецрежима(
		ИнформацияПоПоказателю, ИмяПоказателя);
	
	Возврат ИнформацияОПравеПримененияПатента;
	
КонецФункции

// Откладывает показ напоминание по показателю
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация, для которой получаем информацию
//  ИмяПоказателя - Строка - см. процедуры ИмяПоказателяСпецрежимаДоходы() и прочие
//  СледующееЗначениеНапоминания - Число - Процент, при котором в следующий раз будет показана информация
//
Процедура ОтложитьПоказНапоминания(Организация, ИмяПоказателя, СледующееЗначениеНапоминания) Экспорт
	
	ИмяНастройкиПоказателя = ИмяНастройкиПравоПримененияСпецрежима() + ИмяПоказателя;
	
	// Получим ранее сохраненные данные
	СледующееНапоминание = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПравоПримененияСпецрежима(),
		ИмяНастройкиПоказателя);
	
	// Если их нет, то создадим новое соответствие
	Если СледующееНапоминание = Неопределено Тогда
		СледующееНапоминание = Новый Соответствие;
	КонецЕсли;
	
	Если СледующееЗначениеНапоминания <> ЗначениеНеНапоминать() Тогда
		// Процент не должен быть больше 90%
		СледующееЗначениеНапоминания = Мин(СледующееЗначениеНапоминания, 90);
	КонецЕсли;
	ПериодНапоминания = Год(ОбщегоНазначения.ТекущаяДатаПользователя());
	
	// Получим соответствие по организациям
	НапоминаниеПоОрганизации = СледующееНапоминание[Организация];
	Если НапоминаниеПоОрганизации = Неопределено Тогда
		НапоминаниеПоДате = Новый Соответствие;
		НапоминаниеПоДате.Вставить(ПериодНапоминания, СледующееЗначениеНапоминания);
		СледующееНапоминание.Вставить(Организация, НапоминаниеПоДате);
	Иначе
		НапоминаниеПоОрганизации.Вставить(ПериодНапоминания, СледующееЗначениеНапоминания);
	КонецЕсли;
	
	// Сохраним новые настройки
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПравоПримененияСпецрежима(),
		ИмяНастройкиПоказателя,
		СледующееНапоминание);
	
КонецПроцедуры

// Возвращает стоимость основных средств на указанную дату
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ДатаПроверки - Дата
//
// Возвращаемое значение:
//  Число, Неопределено
//
Функция СтоимостьОсновныхСредств(Организация, ДатаПроверки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонецМесяца(ДатаПроверки), ВидГраницы.Включая));
	
	// Арендованное имущество учитывается на 01.03 и 01.К но не включается в сумму для расчета лимита
	УсловияОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбора.СчетаИсключения = Новый Массив();
	УсловияОтбора.СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.АрендованноеИмущество);
	УсловияОтбора.СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества);
		
	СчетаОсновныхСредств = Новый Массив;
	СчетаОсновныхСредств.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	СчетаОсновныхСредств = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаОсновныхСредств, УсловияОтбора);

	Запрос.УстановитьПараметр("СчетОС", СчетаОсновныхСредств);
	Запрос.УстановитьПараметр("СчетаАмортизации",
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.АмортизацияОсновныхСредств));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка
	|ПОМЕСТИТЬ НеАмортизируемыеОС
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) КАК СуммаОС
	|ПОМЕСТИТЬ ОС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&КонецПериода,
	|			Счет В (&СчетОС),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|			Организация = &Организация
	|				И НЕ Субконто1 В
	|						(ВЫБРАТЬ
	|							НеАмортизируемыеОС.Ссылка
	|						ИЗ
	|							НеАмортизируемыеОС КАК НеАмортизируемыеОС)) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0) КАК СуммаАмортизации
	|ПОМЕСТИТЬ Амортизация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет В (&СчетаАмортизации), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОС.СуммаОС - Амортизация.СуммаАмортизации КАК Показатель
	|ИЗ
	|	ОС КАК ОС,
	|	Амортизация КАК Амортизация";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Показатель;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает сумму годового дохода по организации на спецрежиме.
//  Если организация не применяет спецрежим вернется 0.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ДатаПроверки - Дата
//
// Возвращаемое значение:
//  Число
//
Функция СуммаГодовогоДохода(Организация, ДатаПроверки) Экспорт
	
	ПрименяемыйСпецрежим = ПрименяемыйСпецрежим(Организация, ДатаПроверки);
	
	НачалоПериода = НачалоГода(ДатаПроверки);
	КонецПериода = КонецМесяца(ДатаПроверки);
	
	СуммаДоходов = СуммаДоходов(ПрименяемыйСпецрежим, Организация, НачалоПериода, КонецПериода);
	
	Возврат СуммаДоходов;
	
КонецФункции

// Возвращает идентификатор модуля для вызова через внешний программный интерфейс.
//
// Возвращаемое значение:
//   Строка - идентификатор модуля.
//
Функция ПубликуемыйИдентификатор() Экспорт
	
	Возврат "usn_application_info";
	
КонецФункции

// Возвращает данные по идентификатору данных и переданным параметрам.
//
// Параметры:
//  ИдентификаторДанных - Строка - идентификатор данных. Может быть переопределено при обработке.
//                        Указывается в качестве имени файла, возвращаемого в результате.
//  Параметры - ДвоичныеДанные - переданные параметры получения данных.
//  КодВозврата - Строка - код возврата обработчика получения данных (по умолчанию - 10200).
//  Ошибка - Булево - признак завершения выполнения метода с ошибкой.
//  СообщениеОбОшибке - Строка - текст сообщения об ошибке.
// 
// Возвращаемое значение:
//   ДвоичныеДанные - возвращаемые данные.
//
Функция ВозвращаемыеДанные(ИдентификаторДанных, Параметры, КодВозврата, Ошибка, СообщениеОбОшибке) Экспорт
	
	Если ВРег(ИдентификаторДанных) <> ВРег(ПубликуемыйИдентификатор()) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры) Тогда
		
		СообщениеОбОшибке = НСтр("ru = 'Не переданы входящие настройки.'");
		КодВозврата = АсинхронноеПолучениеДанныхСловарь.КодВозвратаОшибкаДанных();
		Ошибка = Истина;
		Возврат Неопределено;
		
	КонецЕсли;
	
	НастройкиСтрока = ПолучитьСтрокуИзДвоичныхДанных(Параметры);
	Настройки = РаботаВМоделиСервисаБТС.СтруктураИзСтрокиJSON(НастройкиСтрока);
	
	Если Настройки.Свойство("organization") Тогда
		
		Если Настройки.organization.Свойство("inn") Тогда
			
			КПП = Неопределено;
			Настройки.organization.Свойство("kpp", КПП);
			Организация = Справочники.Организации.НайтиОрганизацию(Настройки.organization.inn, КПП, Ложь);
			Если Организация.Пустая() Тогда
				СообщениеОбОшибке = НСтр("ru = 'Организация с ИНН %1 КПП %2 отсутствует в информационной базе.'");
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеОбОшибке,
					Настройки.organization.inn, Настройки.organization.kpp);
			КонецЕсли;
			
		Иначе
			
			СообщениеОбОшибке = НСтр("ru = 'В настройках группа organization должна содержать поля inn и kpp.'");
			
		КонецЕсли;
		
	Иначе
		
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		Если Организация.Пустая() Тогда
			СообщениеОбОшибке = НСтр("ru = 'В информационной базе не задана организация по-умолчанию.'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(СообщениеОбОшибке) Тогда
		
		КодВозврата = АсинхронноеПолучениеДанныхСловарь.КодВозвратаОшибкаДанных();
		Ошибка = Истина;
		Возврат Неопределено;
		
	КонецЕсли;
	
	ДатаПроверки = ТекущаяДатаСеанса();
	Процент = 0;
	ИнформацияПоПоказателюДоходы = ИнформацияПоПоказателюДоходы(Организация, ДатаПроверки);
	Процент = ИнформацияПоПоказателюДоходы.Процент;
	
	Результат = Новый Структура;
	Результат.Вставить("VAT_exemption",
		ИнформацияПоПоказателюДоходы.Показатель <= ИнформацияПоПоказателюДоходы.ГраницаПримененияНДС);
	Результат.Вставить("income_percent_of_exemption_limit", Процент);
	
	ИдентификаторДанных = ПубликуемыйИдентификатор() + ".json";
	
	ИтоговыеДанные = РаботаВМоделиСервисаБТС.СтрокаИзСтруктурыJSON(Результат);
	
	Возврат ПолучитьДвоичныеДанныеИзСтроки(ИтоговыеДанные);
	
КонецФункции

// Заполняет перечень доступных возвращаемых данных.
//
// Параметры:
//  ДоступныеВозвращаемыеДанные - Соответствие - данные, доступные для получения через API
//    * Ключ - Строка - Идентификатор данных
//    * Значение - Структура - см. АсинхронноеПолучениеДанных.НовыйОписаниеВозвращаемыхДанных().
//
Процедура УстановитьДоступныеВозвращаемыеДанные(ДоступныеВозвращаемыеДанные) Экспорт
	
	ОписаниеВозвращаемыхДанных = АсинхронноеПолучениеДанных.НовыйОписаниеВозвращаемыхДанных();
	ОписаниеВозвращаемыхДанных.Наименование = НСтр("ru = 'Право применения УСН.'");
	ОписаниеВозвращаемыхДанных.Описание = НСтр("ru = 'Право применения УСН.'"); 
	ОписаниеВозвращаемыхДанных.МодульМенеджер = КонтрольПраваПримененияСпецрежима;
	ОписаниеВозвращаемыхДанных.ТипыРезультата.Добавить(АсинхронноеПолучениеДанныхСловарь.ТипJSON());
	
	ДоступныеВозвращаемыеДанные.Вставить(ПубликуемыйИдентификатор(), ОписаниеВозвращаемыхДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнформацияОПравеПримененияСпецрежима

Функция ПрименяемыйСпецрежим(Организация, НаДату)
	
	ПрименяетсяУСН = УчетнаяПолитика.ПрименяетсяУСН(Организация, НаДату);
	ПрименяетсяУСНДоходы = ПрименяетсяУСН И УчетнаяПолитика.ПрименяетсяУСНДоходы(Организация, НаДату);
	ПрименяетсяАУСН = УчетнаяПолитика.ПрименяетсяАУСН(Организация, НаДату);
	ПрименяетсяПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, НаДату)
		И НеобходимоКонтролироватьПравоПримененияПатента(Организация, НаДату);
	ТолькоПатент = ПрименяетсяПатент И УчетнаяПолитика.ПрименяетсяОсобыйПорядокНалогообложения(Организация, НаДату);
	ПрименяетсяНПД = УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(Организация, НаДату);
	
	ПрименяемыйСпецрежим = Новый Структура;
	ПрименяемыйСпецрежим.Вставить("УСН", ПрименяетсяУСН);
	ПрименяемыйСпецрежим.Вставить("УСНДоходы", ПрименяетсяУСНДоходы);
	ПрименяемыйСпецрежим.Вставить("АУСН", ПрименяетсяАУСН);
	ПрименяемыйСпецрежим.Вставить("Патент", ПрименяетсяПатент); // Совмещается с ОСНО или УСН
	ПрименяемыйСпецрежим.Вставить("ТолькоПатент", ТолькоПатент);
	ПрименяемыйСпецрежим.Вставить("НПД", ПрименяетсяНПД);

	Возврат ПрименяемыйСпецрежим;
	
КонецФункции

Функция НовыйИнформацияОПравеПримененияСпецрежима()

	Структура = Новый Структура;
	Структура.Вставить("Показать",                     Ложь);
	Структура.Вставить("УтраченоПравоПрименения",      Ложь);
	Структура.Вставить("ОтображатьНапоминание",        Ложь);
	Структура.Вставить("ТекстИнформации",              "");
	Структура.Вставить("ПодсказкаТекстИнформации",     "");
	Структура.Вставить("СсылкаНаПояснение",            "");
	Структура.Вставить("ЗаголовокПояснения",           "");
	Структура.Вставить("СледующееЗначениеНапоминания", НижняяГраницаНапоминания());
	Структура.Вставить("ТекстНапомнитьПозже",          "");
	Структура.Вставить("ЦветФонаГруппы",               ЦветаСтиля.БыстрыеОтборыФонГруппы);
	Структура.Вставить("ИмяПоказателя",                "");
	Структура.Вставить("ДополнительнаяИнформация",     Неопределено);
	
	Возврат Структура;

КонецФункции

Функция ИнформацияПоПоказателю(ИмяПоказателя, Организация, ДатаПроверки)
	
	ИнформацияПоПоказателю = НовыйИнформацияПоПоказателю();
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	Если ДоступныеОрганизации.Найти(Организация) = Неопределено Тогда
		// Организация не доступна, выполнять ничего не будем.
		Возврат ИнформацияПоПоказателю;
	КонецЕсли;
	
	Если ИмяПоказателя = ИмяПоказателяСпецрежимаДоходы() Тогда
		ИнформацияПоПоказателю = ИнформацияПоПоказателюДоходы(Организация, ДатаПроверки);
	ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаОсновныеСредства() Тогда
		ИнформацияПоПоказателю = ИнформацияПоПоказателюОсновныеСредства(Организация, ДатаПроверки);
	ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаРаботники() Тогда
		ИнформацияПоПоказателю = ИнформацияПоПоказателюРаботники(Организация, ДатаПроверки);
	ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаТовары() Тогда
		ИнформацияПоПоказателю = ИнформацияПоПоказателюТовары(Организация, ДатаПроверки);
	КонецЕсли;
	
	Возврат ИнформацияПоПоказателю;

КонецФункции

Функция ИнформацияПоПоказателюДоходы(Организация, ДатаПроверки, ПрименяемыйСпецрежим = Неопределено)
	
	Если ПрименяемыйСпецрежим = Неопределено Тогда
		ПрименяемыйСпецрежим = ПрименяемыйСпецрежим(Организация, ДатаПроверки);
	КонецЕсли;
	
	Если ПрименяемыйСпецрежим.Патент Тогда
		ГраницаДоходов = ГраницаДоходовОграничивающаяПравоПримененияПСН(ДатаПроверки);
	ИначеЕсли ПрименяемыйСпецрежим.УСН Тогда
		ГраницаДоходов =
			КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаДоходовДляПримененияОсновнойСтавкиУСН(ДатаПроверки);
	ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
		ГраницаДоходов = УчетАУСН.ГраницаДоходов();
	ИначеЕсли ПрименяемыйСпецрежим.НПД Тогда
		ГраницаДоходов = НалогНаПрофессиональныйДоходКлиентСервер.ПределДоходов(ДатаПроверки);
	Иначе
		Возврат НовыйИнформацияПоПоказателю();
	КонецЕсли;

	НачалоПериода = НачалоГода(ДатаПроверки);
	КонецПериода = КонецМесяца(ДатаПроверки);
	
	Если ПрименяемыйСпецрежим.УСН Тогда
		НалоговыйПериодУСН = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(Организация, ДатаПроверки,
			Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВДекабре);
		
		Если НалоговыйПериодУСН.Начало <= ДатаПроверки Тогда
			// Доходы контролируются с начала налогового периода.
			НачалоПериода = НалоговыйПериодУСН.Начало;
		Иначе
			// Проверка на дату ранее начала первого налогового периода не имеет смысла - деятельность отсутствует.
			Возврат НовыйИнформацияПоПоказателю();
		КонецЕсли;
	КонецЕсли;
	
	СуммаДоходов = СуммаДоходов(ПрименяемыйСпецрежим, Организация, НачалоПериода, КонецПериода);
	
	ИнформацияПоПоказателю = НовыйИнформацияПоПоказателю();
	ИнформацияПоПоказателю.Граница = ГраницаДоходов;
	
	Если СуммаДоходов > 0 Тогда
		
		ИнформацияПоПоказателю.Показатель = СуммаДоходов;
		
		Если ПрименяемыйСпецрежим.УСН И Не ПрименяемыйСпецрежим.Патент Тогда
			// Повышенная ставка применяется только для УСН
			ИнформацияПоПоказателю.ГраницаПовышеннойСтавки =
				КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаДоходовДляПримененияПовышеннойСтавкиУСН(ДатаПроверки);
		Иначе
			ИнформацияПоПоказателю.ГраницаПовышеннойСтавки = ГраницаДоходов;
		КонецЕсли;
		
		Если ИнформацияПоПоказателю.Показатель > ИнформацияПоПоказателю.Граница Тогда
			ИнформацияПоПоказателю.Процент =
				Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.ГраницаПовышеннойСтавки);
		Иначе
			ИнформацияПоПоказателю.Процент = 
				Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.Граница);
		КонецЕсли;
		
		ИнформацияПоПоказателю.УтраченоПравоПрименения = 
			ИнформацияПоПоказателю.Показатель > ИнформацияПоПоказателю.ГраницаПовышеннойСтавки;
		
		ИнформацияПоПоказателю.ИспользуетсяКоэффициентДефлятор = ПрименяемыйСпецрежим.УСН
			И УчетУСНКлиентСервер.ИспользуетсяКоэффициентДефлятор(ДатаПроверки);
		
		Если ИнформацияПоПоказателю.УтраченоПравоПрименения Тогда
			ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().УтратаПраваПрименения;
		ИначеЕсли КонтрольПраваПримененияСпецрежимаКлиентСервер.ПредупреждатьОПриближенииКНижнейГранице(
			ИнформацияПоПоказателю.Показатель,
			ИнформацияПоПоказателю.Граница,
			ИнформацияПоПоказателю.ГраницаПовышеннойСтавки) Тогда
			
			ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеПовышеннойСтавки;
		Иначе
			ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеСпецрежима;
		КонецЕсли;
		
	КонецЕсли;
	
	ДополнитьИнформациюПоПоказателюДоходыПатент(ИнформацияПоПоказателю, Организация, ДатаПроверки, ПрименяемыйСпецрежим);
	ДополнитьИнформациюПоПоказателюДоходыСНДС(ИнформацияПоПоказателю, Организация, ДатаПроверки, ПрименяемыйСпецрежим);
	
	Если ИнформацияПоПоказателю.УтраченоПравоПрименения
		Или ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПрименениеНДССНачалаГода
		Или ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПрименениеНДС
		Или ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().УтратаПраваПониженнойСтавкиНДС Тогда
		ИнформацияПоПоказателю.ВидДополнительногоСообщения = "";
	КонецЕсли;
	
	Возврат ИнформацияПоПоказателю;
	
КонецФункции

Функция СуммаДоходов(ПрименяемыйСпецрежим, Организация, НачалоПериода, КонецПериода)
	
	УстановитьПривилегированныйРежим(Истина);
	Если ПрименяемыйСпецрежим.ТолькоПатент Тогда
		СуммаДоходов = СуммаДоходовПатент(Организация, НачалоПериода, КонецПериода);
	ИначеЕсли ПрименяемыйСпецрежим.Патент Или ПрименяемыйСпецрежим.УСН Тогда
		СуммаДоходов = СуммаДоходовУСН(Организация, НачалоПериода, КонецПериода);
	ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
		СуммаДоходов = УчетАУСН.СуммаДоходов(Организация, НачалоПериода, КонецПериода);
		Если УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоходЗаПериод(
			Организация, НачалоПериода, КонецПериода) Тогда
			СуммаДоходов = СуммаДоходов + НалогНаПрофессиональныйДоходВызовСервера.СуммаДоходов(
				Организация, НачалоПериода, КонецПериода);
		КонецЕсли;
		Если УчетнаяПолитика.ПрименяетсяУСНЗаПериод(Организация, НачалоПериода, КонецПериода)
			Или УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
			СуммаДоходов = СуммаДоходов + СуммаДоходовУСН(Организация, НачалоПериода, КонецПериода);
		КонецЕсли;
	ИначеЕсли ПрименяемыйСпецрежим.НПД Тогда
		СуммаДоходов = НалогНаПрофессиональныйДоходВызовСервера.СуммаДоходов(Организация, НачалоПериода, КонецПериода);
	Иначе
		СуммаДоходов = 0;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СуммаДоходов;
	
КонецФункции

Процедура ДополнитьИнформациюПоПоказателюДоходыСНДС(ИнформацияПоПоказателю, Организация, ДатаПроверки, ПрименяемыйСпецрежим)
	
	Если Не ПрименяемыйСпецрежим.УСН
		Или ИнформацияПоПоказателю.УтраченоПравоПрименения Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияПоПоказателю.ДатаНачалаПримененияНДС = УчетУСНКлиентСервер.ДатаНачалаПримененияНДС();
	ИнформацияПоПоказателю.ГраницаПримененияНДС = УчетУСНКлиентСервер.ГраницаПримененияНДС(ДатаПроверки);
	ИнформацияПоПоказателю.ГраницаПримененияПониженнойСтавкиНДС = УчетУСНКлиентСервер.ГраницаПримененияПониженнойСтавкиНДС(ДатаПроверки);
	
	ПрименяетсяНДСВСледующемГоду = (Год(ДатаПроверки) = Год(ИнформацияПоПоказателю.ДатаНачалаПримененияНДС) - 1);
	ПрименяетсяНДС = (Год(ДатаПроверки) >= Год(ИнформацияПоПоказателю.ДатаНачалаПримененияНДС));
	Если ПрименяетсяНДСВСледующемГоду Тогда
		ПлательщикНДС = Ложь;
		ПлательщикНДСВСледующемМесяце = Ложь;
	ИначеЕсли ПрименяетсяНДС Тогда
		ПлательщикНДС = УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, ДатаПроверки);
		// Проверяем с 1-го числа месяца, следующего за месяцем превышения границ применения НДС
		// согласно п. 5 ст. 145 НК РФ и пп. 1 п. 8 ст. 164 НК РФ
		ПлательщикНДСВСледующемМесяце = УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, КонецМесяца(ДатаПроверки) + 1);
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ИнформацияПоПоказателю.Показатель > 0 И ПрименяетсяНДС Тогда
		ПрименяетсяПониженнаяСтавкаНДС = УчетНДС.ПрименяетсяПониженнаяСтавкаНДС(Организация, ДатаПроверки);
		ПрименяетсяПониженнаяСтавкаНДСВСледующемМесяце = УчетНДС.ПрименяетсяПониженнаяСтавкаНДС(Организация, КонецМесяца(ДатаПроверки) + 1);
		Если Не ПлательщикНДС
			И Не ПлательщикНДСВСледующемМесяце Тогда
			ТребуетсяПрименятьНДС = ИнформацияПоПоказателю.Показатель > ИнформацияПоПоказателю.ГраницаПримененияНДС;
			ПроцентПримененияНДС = Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.ГраницаПримененияНДС);
			Если ТребуетсяПрименятьНДС Тогда
				ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПрименениеНДС;
				ИнформацияПоПоказателю.Процент = ПроцентПримененияНДС;
			ИначеЕсли КонтрольПраваПримененияСпецрежимаКлиентСервер.ПредупреждатьОПриближенииКНижнейГранице(
				ИнформацияПоПоказателю.Показатель,
				ИнформацияПоПоказателю.ГраницаПримененияНДС,
				ИнформацияПоПоказателю.Граница) Тогда
				
				ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеНДС;
				ИнформацияПоПоказателю.Процент = ПроцентПримененияНДС;
			КонецЕсли;
		ИначеЕсли ПлательщикНДС
			И ПрименяетсяПониженнаяСтавкаНДС
			И ПрименяетсяПониженнаяСтавкаНДСВСледующемМесяце Тогда
			
			ПроцентПриближенияСтавкиНДС = Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.ГраницаПримененияПониженнойСтавкиНДС);
			Если ИнформацияПоПоказателю.Показатель > ИнформацияПоПоказателю.ГраницаПримененияПониженнойСтавкиНДС Тогда
				ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().УтратаПраваПониженнойСтавкиНДС;
				ИнформацияПоПоказателю.Процент = ПроцентПриближенияСтавкиНДС;
			ИначеЕсли КонтрольПраваПримененияСпецрежимаКлиентСервер.ПредупреждатьОПриближенииКНижнейГранице(
				ИнформацияПоПоказателю.Показатель,
				ИнформацияПоПоказателю.ГраницаПримененияПониженнойСтавкиНДС,
				ИнформацияПоПоказателю.Граница) Тогда
				
				ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеПониженнойСтавкиНДС;
				ИнформацияПоПоказателю.Процент = ПроцентПриближенияСтавкиНДС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Для УСН с 2025 года дополнительно проверим, что в прошлом году доходы не превышали границу применения НДС
	Если Не ПлательщикНДС И ПрименяетсяНДС Тогда
		ДатаПроверкиВПрошломГоду = НачалоГода(ДатаПроверки) - 1;
		ПрименяемыйСпецрежимВПрошломГоду = ПрименяемыйСпецрежим(Организация, ДатаПроверкиВПрошломГоду);
		Если ПрименяемыйСпецрежимВПрошломГоду.УСН Тогда
			НачалоПрошлогоПериода = НачалоГода(ДатаПроверкиВПрошломГоду);
			КонецПрошлогоПериода = КонецГода(ДатаПроверкиВПрошломГоду);
			УстановитьПривилегированныйРежим(Истина);
			СуммаДоходовВПрошломГоду = СуммаДоходовУСН(Организация, НачалоПрошлогоПериода, КонецПрошлогоПериода);
			УстановитьПривилегированныйРежим(Ложь);
			ТребуетсяПрименятьНДССНачалаГода = СуммаДоходовВПрошломГоду > ИнформацияПоПоказателю.ГраницаПримененияНДС;
			Если ТребуетсяПрименятьНДССНачалаГода Тогда
				ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПрименениеНДССНачалаГода;
				ИнформацияПоПоказателю.Показатель = СуммаДоходовВПрошломГоду;
				ИнформацияПоПоказателю.Процент =
					Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.ГраницаПримененияНДС);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПлательщикНДС
		И Не ПлательщикНДСВСледующемМесяце
		И ВыводитьДополнительноеСообщение(ДатаПроверки) Тогда
		
		ГраницаПримененияНДСВСледующемГоду = УчетУСНКлиентСервер.ГраницаПримененияНДС(КонецГода(ДатаПроверки) + 1);
		Процент = Цел(ИнформацияПоПоказателю.Показатель * 100 / ГраницаПримененияНДСВСледующемГоду);
		Если ПоказыватьНапоминание(Процент) Тогда
			Если ИнформацияПоПоказателю.ВидДополнительногоСообщения = ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно().УтратаПраваПримененияВСледующемГоду Тогда
				ИнформацияПоПоказателю.ВидДополнительногоСообщения = ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно().УтратаПраваПримененияИНДСВСледующемГоду;
			Иначе
				ИнформацияПоПоказателю.ВидДополнительногоСообщения = ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно().ПрименениеНДСВСледующемГоду;
			КонецЕсли;
			ИнформацияПоПоказателю.ПроцентДополнительно = Процент;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьИнформациюПоПоказателюДоходыПатент(ИнформацияПоПоказателю, Организация, ДатаПроверки, ПрименяемыйСпецрежим)
	
	Если Год(ДатаПроверки) < Год('20250101')
		Или (Не ПрименяемыйСпецрежим.Патент И Не ПрименяемыйСпецрежим.ТолькоПатент)
		Или ИнформацияПоПоказателю.УтраченоПравоПрименения Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПроверкиВПрошломГоду = НачалоГода(ДатаПроверки) - 1;
	НачалоПрошлогоПериода = НачалоГода(ДатаПроверкиВПрошломГоду);
	КонецПрошлогоПериода = КонецГода(ДатаПроверкиВПрошломГоду);
	
	УстановитьПривилегированныйРежим(Истина);
	Если УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоПрошлогоПериода, КонецПрошлогоПериода) Тогда
		
		СуммаДоходовВПрошломГоду = СуммаДоходовУСН(Организация, НачалоПрошлогоПериода, КонецПрошлогоПериода);
		
		УтратаПраваПримененияПатентаПрошлыйГод = СуммаДоходовВПрошломГоду > ИнформацияПоПоказателю.Граница;
		Если УтратаПраваПримененияПатентаПрошлыйГод Тогда
			ИнформацияПоПоказателю.УтраченоПравоПрименения = Истина;
			ИнформацияПоПоказателю.ВидСообщения =
				ВидыСообщенийПоказателяСпецрежимаДоходы().УтратаПраваПримененияПатентаПрошлыйГод;
			ИнформацияПоПоказателю.Показатель = СуммаДоходовВПрошломГоду;
			ИнформацияПоПоказателю.Процент =
				Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.Граница);
		КонецЕсли;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ИнформацияПоПоказателю.УтраченоПравоПрименения
		И ВыводитьДополнительноеСообщение(ДатаПроверки) Тогда
		ГраницаПримененияВСледующемГоду = ГраницаДоходовОграничивающаяПравоПримененияПСН(КонецГода(ДатаПроверки) + 1);
		Процент = Цел(ИнформацияПоПоказателю.Показатель * 100 / ГраницаПримененияВСледующемГоду);
		Если ПоказыватьНапоминание(Процент) Тогда
			ИнформацияПоПоказателю.ВидДополнительногоСообщения = ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно().УтратаПраваПримененияВСледующемГоду;
			ИнформацияПоПоказателю.ПроцентДополнительно = Цел(ИнформацияПоПоказателю.Показатель * 100 / ГраницаПримененияВСледующемГоду);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СуммаДоходовУСН(Организация, НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КнигаУчетаДоходовИРасходовОбороты.Графа5Оборот КАК Показатель
	|ПОМЕСТИТЬ втДоходы
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовИРасходов.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК КнигаУчетаДоходовИРасходовОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаУчетаДоходовПатентОбороты.Графа4Оборот
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовПатент.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК КнигаУчетаДоходовПатентОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(втДоходы.Показатель), 0) КАК Показатель
	|ИЗ
	|	втДоходы КАК втДоходы";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Окр(Выборка.Показатель, УчетУСНКлиентСервер.РазрядностьОкругленияПоказателей(КонецПериода));
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция СуммаДоходовУСНПомесячно(Организация, НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КнигаУчетаДоходовИРасходовОбороты.Период КАК Месяц,
	|	КнигаУчетаДоходовИРасходовОбороты.Графа5Оборот КАК Показатель
	|ПОМЕСТИТЬ втДоходы
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовИРасходов.Обороты(&НачалоПериода, &КонецПериода, Месяц, Организация = &Организация) КАК КнигаУчетаДоходовИРасходовОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаУчетаДоходовПатентОбороты.Период,
	|	КнигаУчетаДоходовПатентОбороты.Графа4Оборот
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовПатент.Обороты(&НачалоПериода, &КонецПериода, Месяц, Организация = &Организация) КАК КнигаУчетаДоходовПатентОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДоходы.Месяц КАК Месяц,
	|	СУММА(втДоходы.Показатель) КАК Показатель
	|ИЗ
	|	втДоходы КАК втДоходы
	|
	|СГРУППИРОВАТЬ ПО
	|	втДоходы.Месяц
	|
	|УПОРЯДОЧИТЬ ПО
	|	Месяц";
	
	ДоходыПомесячно = Запрос.Выполнить().Выгрузить();
	Для Каждого Доход Из ДоходыПомесячно Цикл
		Доход.Показатель = Окр(Доход.Показатель, УчетУСНКлиентСервер.РазрядностьОкругленияПоказателей(КонецПериода));
	КонецЦикла;
	
	Возврат ДоходыПомесячно;
	
КонецФункции

Функция МесяцПревышенияГраницыПримененияНДС(Организация, НаДату = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	НачалоПериода = НачалоГода(НаДату);
	КонецПериода = КонецМесяца(НаДату);
	
	ДоходыПомесячно = СуммаДоходовУСНПомесячно(Организация, НачалоПериода, КонецПериода);
	
	Граница = УчетУСНКлиентСервер.ГраницаПримененияНДС(НаДату);
	
	Итог = 0;
	
	Для Каждого Доход Из ДоходыПомесячно Цикл
		Итог = Итог + Доход.Показатель;
		Если Итог > Граница Тогда
			Возврат Доход.Месяц;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция СуммаДоходовПатент(Организация, НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КнигаУчетаДоходовПатентОбороты.Графа4Оборот КАК Показатель
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовПатент.Обороты(&НачалоПериода, &КонецПериода,, Организация = &Организация) КАК
	|		КнигаУчетаДоходовПатентОбороты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Показатель;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция ИнформацияПоПоказателюОсновныеСредства(Организация, ДатаПроверки)
	
	ПрименяемыйСпецрежим = ПрименяемыйСпецрежим(Организация, ДатаПроверки);
	
	Если ПрименяемыйСпецрежим.УСН Тогда
		ГраницаСтоимостиОсновныхСредств = ГраницаСтоимостиОсновныхСредствОграничивающаяПравоПримененияУСН(ДатаПроверки);
	ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
		ГраницаСтоимостиОсновныхСредств = УчетАУСН.ГраницаСтоимостиОсновныхСредств();
	Иначе
		Возврат НовыйИнформацияПоПоказателю();
	КонецЕсли;
	
	Показатель = СтоимостьОсновныхСредств(Организация, ДатаПроверки);
	
	ИнформацияПоПоказателю = НовыйИнформацияПоПоказателю();
	
	Если Показатель <> Неопределено Тогда
		
		ИнформацияПоПоказателю.Показатель = Показатель;
		ИнформацияПоПоказателю.Граница = ГраницаСтоимостиОсновныхСредств;
		ИнформацияПоПоказателю.Процент = Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.Граница);
		ИнформацияПоПоказателю.УтраченоПравоПрименения = (ИнформацияПоПоказателю.Показатель > ИнформацияПоПоказателю.Граница);
		
	КонецЕсли;
	
	Возврат ИнформацияПоПоказателю;
	
КонецФункции

Функция ИнформацияПоПоказателюРаботники(Организация, ДатаПроверки)
	
	ПрименяемыйСпецрежим = ПрименяемыйСпецрежим(Организация, ДатаПроверки);
	
	Если ПрименяемыйСпецрежим.ТолькоПатент Тогда
		ГраницаЧисленностиРаботников = ГраницаСреднесписочнойЧисленностиРаботниковОграничивающаяПравоПримененияПатент();
	ИначеЕсли ПрименяемыйСпецрежим.УСН Тогда
		ГраницаЧисленностиРаботников =
			КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаЧисленностиРаботниковДляПримененияОсновнойСтавкиУСН(ДатаПроверки);
	ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
		ГраницаЧисленностиРаботников =
			УчетАУСН.ГраницаЧисленностиРаботников();
	ИначеЕсли ПрименяемыйСпецрежим.НПД Тогда
		ГраницаЧисленностиРаботников = 0;
	Иначе
		Возврат НовыйИнформацияПоПоказателю();
	КонецЕсли;
	
	НачалоПериода = НачалоГода(ДатаПроверки);
	
	Если ПрименяемыйСпецрежим.УСН Тогда
		НалоговыйПериодУСН = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(Организация, ДатаПроверки,
			Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВДекабре);
		
		Если НалоговыйПериодУСН.Начало <= ДатаПроверки Тогда
			// Численность работников контролируется с начала налогового периода.
			НачалоПериода = НалоговыйПериодУСН.Начало;
		Иначе
			// Проверка на дату ранее начала первого налогового периода не имеет смысла - деятельность отсутствует.
			Возврат НовыйИнформацияПоПоказателю();
		КонецЕсли;
	КонецЕсли;
	
	СведенияОЧисленностиРаботников = УчетЗарплаты.СреднесписочнаяЧисленность(Организация, НачалоПериода, ДатаПроверки);
	
	ИнформацияПоПоказателю = НовыйИнформацияПоПоказателю();
	ИнформацияПоПоказателю.Показатель = СведенияОЧисленностиРаботников.ЧисленностьРаботников;
	ИнформацияПоПоказателю.Граница = ГраницаЧисленностиРаботников;
	Если ПрименяемыйСпецрежим.УСН Тогда
		ИнформацияПоПоказателю.ГраницаПовышеннойСтавки =
			КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаЧисленностиРаботниковДляПримененияПовышеннойСтавкиУСН(ДатаПроверки);
	Иначе
		ИнформацияПоПоказателю.ГраницаПовышеннойСтавки = ГраницаЧисленностиРаботников;
	КонецЕсли;
	
	Если ПрименяемыйСпецрежим.НПД Тогда
		ЕстьДоговорыПодряда = УчетЗарплаты.ЕстьДоговорыПодряда(Организация, НачалоПериода, Перечисления.Периодичность.Год);
		Если ЕстьДоговорыПодряда Или СведенияОЧисленностиРаботников.Численность > 0 Тогда
			ИнформацияПоПоказателю.Показатель = 1; // Показатель не выводится для НПД: учитываются и работники, и физлица на ГПХ
		Иначе
			ИнформацияПоПоказателю.Показатель = 0;
		КонецЕсли;
		ИнформацияПоПоказателю.Процент = ИнформацияПоПоказателю.Показатель * 100;
	ИначеЕсли ИнформацияПоПоказателю.Показатель > ИнформацияПоПоказателю.Граница Тогда
		ИнформацияПоПоказателю.Процент =
			Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.ГраницаПовышеннойСтавки);
	Иначе
		ИнформацияПоПоказателю.Процент = Цел(ИнформацияПоПоказателю.Показатель * 100 / ИнформацияПоПоказателю.Граница);
	КонецЕсли;
	
	ИнформацияПоПоказателю.УтраченоПравоПрименения =
		ИнформацияПоПоказателю.Показатель > ИнформацияПоПоказателю.ГраницаПовышеннойСтавки;
	
	Возврат ИнформацияПоПоказателю;
	
КонецФункции

Функция ТекстИнформацияОПоказателе(РезультатыПоПоказателю, ИмяПоказателя, ПрименяемыйСпецрежим, ДатаПроверки)
	
	ЧастиШаблона = Новый Массив;
	ЧастиШаблонаФорматированнойСтроки = Новый Массив;
	УтратаПраваПримененияПатентаПрошлыйГод =
		ВидыСообщенийПоказателяСпецрежимаДоходы().УтратаПраваПримененияПатентаПрошлыйГод;
	
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("Показатель", 0);
	ПараметрыТекста.Вставить("Граница", 0);
	ПараметрыТекста.Вставить("ГраницаПовышеннойСтавки", 0);
	ПараметрыТекста.Вставить("ГраницаПримененияНДС", 0);
	ПараметрыТекста.Вставить("ГраницаПримененияПониженнойСтавкиНДС", 0);
	
	Если ИмяПоказателя = ИмяПоказателяСпецрежимаДоходыДополнительно() Тогда
		// Федеральный закон от 28.11.2025 № 425-ФЗ
		СледующийГод = КонецГода(ДатаПроверки) + 1;
		ГраницаПримененияНДСВСледующемГоду = УчетУСНКлиентСервер.ГраницаПримененияНДС(СледующийГод);
		ГраницаПримененияВСледующемГоду = ГраницаДоходовОграничивающаяПравоПримененияПСН(СледующийГод);
		ПараметрыТекста.Показатель = Окр(РезультатыПоПоказателю.Показатель / 1000000, 2); // в млн. рублей
		
		ШаблонНалоговаяРеформа = Новый ФорматированнаяСтрока(
			НСтр("ru='Налоговой реформе - 2026'"), , , , 
			"https://its.1c.ru/db/newscomm#content:497430:hdoc");
		Если ПравоДоступа("Просмотр", Метаданные.Обработки.СравнениеРежимовНалогообложения)
			И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
			ШаблонСравнениеРежимовНалогообложения = Новый ФорматированнаяСтрока(
				НСтр("ru='Сравнение режимов налогообложения'"), , , , "СравнениеРежимовНалогообложенияВСледующемГоду");
		Иначе
			ШаблонСравнениеРежимовНалогообложения = "";
		КонецЕсли;
		
		Если РезультатыПоПоказателю.ВидДополнительногоСообщения = ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно().ПрименениеНДСВСледующемГоду Тогда
			ЧастиШаблона.Добавить(
				НСтр("ru='Ваши доходы [Показатель] млн рублей. Согласно '"));
			ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонНалоговаяРеформа);
			ЧастиШаблонаФорматированнойСтроки.Добавить(СтрШаблон(
				НСтр("ru=' превышая порог в %1 млн, с %2 года необходимо уплачивать НДС на УСН'"),
				Окр(ГраницаПримененияНДСВСледующемГоду / 1000000, 2), // в млн. рублей
				Формат(СледующийГод, "ДФ=yyyy")));
			Если ЗначениеЗаполнено(ШаблонСравнениеРежимовНалогообложения) Тогда
				ЧастиШаблонаФорматированнойСтроки.Добавить(Символы.ПС);
				ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонСравнениеРежимовНалогообложения);
			КонецЕсли;
		ИначеЕсли РезультатыПоПоказателю.ВидДополнительногоСообщения = ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно().УтратаПраваПримененияВСледующемГоду Тогда
			ЧастиШаблона.Добавить(
				НСтр("ru='Ваши доходы [Показатель] млн рублей. Согласно '"));
			ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонНалоговаяРеформа);
			ЧастиШаблонаФорматированнойСтроки.Добавить(СтрШаблон(
				НСтр("ru=' при превышении порога в %1 млн предприниматели не смогут получить патент на %2 год'"),
				Окр(ГраницаПримененияВСледующемГоду / 1000000, 2), // в млн. рублей
				Формат(СледующийГод, "ДФ=yyyy")));
			Если ЗначениеЗаполнено(ШаблонСравнениеРежимовНалогообложения) Тогда
				ЧастиШаблонаФорматированнойСтроки.Добавить(Символы.ПС);
				ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонСравнениеРежимовНалогообложения);
			КонецЕсли;
		ИначеЕсли РезультатыПоПоказателю.ВидДополнительногоСообщения = ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно().УтратаПраваПримененияИНДСВСледующемГоду Тогда
			ЧастиШаблона.Добавить(
				НСтр("ru='Ваши доходы [Показатель] млн рублей. Согласно '"));
			ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонНалоговаяРеформа);
			ЧастиШаблонаФорматированнойСтроки.Добавить(СтрШаблон(
				НСтр("ru=' при превышении порога в %1 млн предприниматели не смогут получить патент на %3 год.
				|Также, превышая порог в %2 млн, с %3 года необходимо уплачивать НДС на УСН  '"),
				Окр(ГраницаПримененияВСледующемГоду / 1000000, 2), // в млн. рублей
				Окр(ГраницаПримененияНДСВСледующемГоду / 1000000, 2), // в млн. рублей
				Формат(СледующийГод, "ДФ=yyyy")));
			Если ЗначениеЗаполнено(ШаблонСравнениеРежимовНалогообложения) Тогда
				ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонСравнениеРежимовНалогообложения);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаДоходы() Тогда
		Если РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().УтратаПраваПрименения Тогда
			Если ПрименяемыйСпецрежим.Патент Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы превысили границу применения патента'"));
			ИначеЕсли ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы превысили границу применения УСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы превысили границу применения АУСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.НПД Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы превысили границу применения Налога на профессиональный доход'"));
			КонецЕсли;
			ЧастиШаблона.Добавить(НСтр("ru='([Показатель] при допустимых [ГраницаПовышеннойСтавки] млн руб)'"));
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = УтратаПраваПримененияПатентаПрошлыйГод Тогда
			ЧастиШаблона.Добавить(НСтр("ru='Доходы прошлого года превысили границу применения патента ([Показатель] при допустимых [Граница] млн руб)'"));
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПрименениеНДССНачалаГода Тогда
			ЧастиШаблона.Добавить(НСтр("ru='Доходы прошлого года превысили границу, после которой необходимо уплачивать НДС ([Показатель] при допустимых [ГраницаПримененияНДС] млн руб)'"));
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПрименениеНДС Тогда
			ЧастиШаблона.Добавить(НСтр("ru='Доходы превысили границу, после которой необходимо уплачивать НДС ([Показатель] при допустимых [ГраницаПримененияНДС] млн руб).
				|Необходимо выбрать ставку НДС, которая будет применяться с месяца, следующего за тем, в котором произошло превышение. '"));
			Если ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.НалогиИОтчеты)
				И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
				ШаблонФорматированнойСтроки = Новый ФорматированнаяСтрока(НСтр("ru='Настроить НДС'"), , , , "ВключитьНДС");
				ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонФорматированнойСтроки);
			КонецЕсли;
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеНДС Тогда
			ЧастиШаблона.Добавить(НСтр("ru='Доходы достигли [Процент] от границы, при превышении которой необходимо уплачивать НДС ([Показатель] при допустимых [ГраницаПримененияНДС] млн руб).
				|При превышении границы необходимо применять ставку НДС 5%, 7% или [ОбщаяСтавкаНДС].'"));
			ОбщаяСтавкаНДС = УчетНДСКлиентСервер.ОбщаяСтавкаНДС(ДатаПроверки);
			ПараметрыТекста.Вставить("ОбщаяСтавкаНДС", ОбщаяСтавкаНДС);
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().УтратаПраваПониженнойСтавкиНДС Тогда
			ЧастиШаблона.Добавить(НСтр("ru='Доходы превысили границу применения пониженной ставки НДС 5% ([Показатель] при допустимых [ГраницаПримененияПониженнойСтавкиНДС] млн руб).
				|Необходимо применять ставку НДС 7% с месяца, следующего за тем, в котором произошло превышение. '"));
			Если ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.НалогиИОтчеты)
				И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
				ШаблонФорматированнойСтроки = Новый ФорматированнаяСтрока(НСтр("ru='Настроить НДС'"), , , , "НастроитьНДС");
				ЧастиШаблонаФорматированнойСтроки.Добавить(ШаблонФорматированнойСтроки);
			КонецЕсли;
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеПониженнойСтавкиНДС Тогда
			ЧастиШаблона.Добавить(НСтр("ru='Доходы достигли [Процент] от границы применения пониженной ставки НДС 5% ([Показатель] при допустимых [ГраницаПримененияПониженнойСтавкиНДС] млн руб).
				|При превышении границы необходимо применять ставку НДС 7%.'"));
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеПовышеннойСтавки Тогда
			Если ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы достигли [Процент] от границы применения основной ставки [Спецрежим] ([Показатель] при допустимых [Граница] млн руб).
					|При превышении границы будет применяться повышенная ставка [ПовышеннаяСтавка]% (до [ГраницаПовышеннойСтавки] млн руб).'"));
				СтавкиУСН = УчетУСНКлиентСервер.НалоговыеСтавкиПоУмолчанию();
				ПараметрыТекста.Вставить("ПовышеннаяСтавка",
					?(РезультатыПоПоказателю.ПрименяетсяУСНДоходы,
					СтавкиУСН.СтавкаУСНДоходыПовышенная,
					СтавкиУСН.СтавкаУСНДоходыМинусРасходыПовышенная));
				ПараметрыТекста.Вставить("Спецрежим",
					?(РезультатыПоПоказателю.ПрименяетсяУСНДоходы,
					"УСН (доходы)",
					"УСН (доходы минус расходы)"));
			КонецЕсли;
		ИначеЕсли РезультатыПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеСпецрежима Тогда
			Если ПрименяемыйСпецрежим.Патент Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы достигли [Процент] от границы применения патента'"));
			ИначеЕсли ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы достигли [Процент] от границы применения УСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы достигли [Процент] от границы применения АУСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.НПД  И ИмяПоказателя = ИмяПоказателяСпецрежимаДоходы() Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Доходы достигли [Процент] от границы применения Налога на профессиональный доход'"));
			КонецЕсли;
			ЧастиШаблона.Добавить(НСтр("ru='([Показатель] при допустимых [ГраницаПовышеннойСтавки] млн руб)'"));
		КонецЕсли;
		
		ПараметрыТекста.Показатель = Окр(РезультатыПоПоказателю.Показатель / 1000000, 2); // в млн. рублей
		ПараметрыТекста.Граница = Окр(РезультатыПоПоказателю.Граница / 1000000, 2); // в млн. рублей
		ПараметрыТекста.ГраницаПовышеннойСтавки = Окр(РезультатыПоПоказателю.ГраницаПовышеннойСтавки / 1000000, 2); // в млн. рублей
		ПараметрыТекста.ГраницаПримененияНДС = Окр(РезультатыПоПоказателю.ГраницаПримененияНДС/ 1000000, 2); // в млн. рублей
		ПараметрыТекста.ГраницаПримененияПониженнойСтавкиНДС = Окр(РезультатыПоПоказателю.ГраницаПримененияПониженнойСтавкиНДС/ 1000000, 2); // в млн. рублей
		
	ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаОсновныеСредства() Тогда
		
		Если РезультатыПоПоказателю.УтраченоПравоПрименения Тогда
			Если ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Стоимость основных средств превысила границу применения УСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Стоимость основных средств превысила границу применения АУСН'"));
			КонецЕсли;
		Иначе
			Если ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Стоимость основных средств достигла [Процент] от границы применения УСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Стоимость основных средств достигла [Процент] от границы применения АУСН'"));
			КонецЕсли;
		КонецЕсли;
		ЧастиШаблона.Добавить(НСтр("ru='([Показатель] при допустимых [Граница] млн руб)'"));
		
		ПараметрыТекста.Показатель = Окр(РезультатыПоПоказателю.Показатель / 1000000, 0); // в млн. рублей
		ПараметрыТекста.Граница = Окр(РезультатыПоПоказателю.Граница / 1000000, 2); // в млн. рублей
		ПараметрыТекста.ГраницаПовышеннойСтавки = Окр(РезультатыПоПоказателю.ГраницаПовышеннойСтавки / 1000000, 2); // в млн. рублей
		
	ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаРаботники() Тогда
		
		Если РезультатыПоПоказателю.УтраченоПравоПрименения Тогда
			Если ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Средняя численность работников превысила границу применения УСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Средняя численность работников превысила границу применения АУСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.Патент Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Средняя численность работников превысила границу применения патента'"));
			ИначеЕсли ПрименяемыйСпецрежим.НПД Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Приём сотрудников по трудовому договору нарушает условия применения Налога на профессиональный доход'"));
			КонецЕсли;
			
			Если Не ПрименяемыйСпецрежим.НПД Тогда
				ЧастиШаблона.Добавить(НСтр("ru='([Показатель] при допустимых [ГраницаПовышеннойСтавки] человек)'"));
			КонецЕсли;
			
		ИначеЕсли КонтрольПраваПримененияСпецрежимаКлиентСервер.ПредупреждатьОПриближенииКНижнейГранице(
			РезультатыПоПоказателю.Показатель,
			РезультатыПоПоказателю.Граница,
			РезультатыПоПоказателю.ГраницаПовышеннойСтавки) Тогда
			
			Если ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр(
					"ru='Средняя численность работников достигла [Процент] от границы применения основной ставки [Спецрежим] ([Показатель] при допустимых [Граница] человек).
						|При превышении границы будет применяться повышенная ставка [ПовышеннаяСтавка]% (до [ГраницаПовышеннойСтавки] человек).'"));
				СтавкиУСН = УчетУСНКлиентСервер.НалоговыеСтавкиПоУмолчанию();
				ПараметрыТекста.Вставить("ПовышеннаяСтавка",
					?(РезультатыПоПоказателю.ПрименяетсяУСНДоходы,
					СтавкиУСН.СтавкаУСНДоходыПовышенная,
					СтавкиУСН.СтавкаУСНДоходыМинусРасходыПовышенная));
				ПараметрыТекста.Вставить("Спецрежим",
					?(РезультатыПоПоказателю.ПрименяетсяУСНДоходы,
					"УСН (доходы)",
					"УСН (доходы минус расходы)"));
			КонецЕсли;
		Иначе
			Если ПрименяемыйСпецрежим.УСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Средняя численность работников достигла [Процент] от границы применения УСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.АУСН Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Средняя численность работников достигла [Процент] от границы применения АУСН'"));
			ИначеЕсли ПрименяемыйСпецрежим.Патент Тогда
				ЧастиШаблона.Добавить(НСтр("ru='Средняя численность работников достигла [Процент] от границы применения патента'"));
			КонецЕсли;
			ЧастиШаблона.Добавить(НСтр("ru='([Показатель] при допустимых [ГраницаПовышеннойСтавки] человек)'"));
		КонецЕсли;
		
		ПараметрыТекста.Показатель = РезультатыПоПоказателю.Показатель;
		ПараметрыТекста.Граница = РезультатыПоПоказателю.Граница;
		ПараметрыТекста.ГраницаПовышеннойСтавки = РезультатыПоПоказателю.ГраницаПовышеннойСтавки;
		
	ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаТовары() Тогда
		
		Если ПрименяемыйСпецрежим.НПД Тогда
			ЧастиШаблона.Добавить(НСтр("ru='Продажа маркируемых товаров нарушает условия применения Налога на профессиональный доход'"));
		КонецЕсли;
		
		ПараметрыТекста.Показатель = РезультатыПоПоказателю.Показатель;
		ПараметрыТекста.Граница = РезультатыПоПоказателю.Граница;
		ПараметрыТекста.ГраницаПовышеннойСтавки = РезультатыПоПоказателю.ГраницаПовышеннойСтавки;
		
	КонецЕсли;
	
	ПараметрыТекста.Вставить("Процент", СтрШаблон(НСтр("ru = '%1%%'"), Строка(РезультатыПоПоказателю.Процент)));
	ПараметрыТекста.Вставить("ПроцентДополнительно", СтрШаблон(НСтр("ru = '%1%%'"), Строка(РезультатыПоПоказателю.ПроцентДополнительно)));
	
	ТекстИнформации = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
		СтрСоединить(ЧастиШаблона, " "), 
		ПараметрыТекста);
	
	Если ЧастиШаблонаФорматированнойСтроки.Количество() > 0 Тогда
		ЧастиШаблона = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекстИнформации);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЧастиШаблона, ЧастиШаблонаФорматированнойСтроки);
		Возврат Новый ФорматированнаяСтрока(ЧастиШаблона);
	Иначе
		Возврат ТекстИнформации;
	КонецЕсли;

КонецФункции

Функция ПодсказкаИнформацииОПоказателе(ИнформацияПоПоказателю, ИмяПоказателя, ДатаПроверки, ПрименяемыйСпецрежим)
	
	Если ИмяПоказателя <> ИмяПоказателяСпецрежимаДоходы()
		Или Не ПрименяемыйСпецрежим.УСН 
		Или ПрименяемыйСпецрежим.Патент Тогда
		Возврат "";
	КонецЕсли;
	
	// При наличии коэффициента-дефлятора показываем подсказку, объясняющую повышенные лимиты.
	Если Не ИнформацияПоПоказателю.ИспользуетсяКоэффициентДефлятор
		И Год(ДатаПроверки) <> Год(УчетУСНКлиентСервер.ДатаНачалаПрогрессивнойШкалы()) Тогда
		Возврат "";
	КонецЕсли;
	
	КоэффициентДефлятор = УчетУСНКлиентСервер.КоэффициентДефлятор(ДатаПроверки);
	Подсказка = "";
	
	Если ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеПониженнойСтавкиНДС Тогда
		БазоваяГраницаПримененияПониженнойСтавкиНДС = УчетУСНКлиентСервер.БазоваяГраницаПримененияПониженнойСтавкиНДС();
		
		ШаблонПодсказки = НСтр("ru = 'В %1 году коэффициент-дефлятор составляет %2.
			|Граница применения пониженной ставки НДС 5%%: %3 млн руб * %2 = %4 млн руб.'");
		
		Подсказка = СтрШаблон(ШаблонПодсказки,
			Формат(ДатаПроверки, "ДФ=гггг;"),
			КоэффициентДефлятор,
			Окр(БазоваяГраницаПримененияПониженнойСтавкиНДС / 1000000, 2), // в млн. рублей
			Окр(ИнформацияПоПоказателю.ГраницаПримененияПониженнойСтавкиНДС / 1000000, 2)); // в млн. рублей
		
	ИначеЕсли ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеПовышеннойСтавки Тогда
		
		Если Год(ДатаПроверки) = Год(УчетУСНКлиентСервер.ДатаНачалаПрогрессивнойШкалы()) Тогда
			
			Подсказка = СтроковыеФункции.ФорматированнаяСтрока(
				СтрШаблон(НСтр("ru = 'В %1 году границы применения основной и повышенной ставок УСН не индексируются.
				|<a href=""https://its.1c.ru/bmk/usn/limit_index_2021"">Подробнее на ИТС</a>'"), Формат(ДатаПроверки, "ДФ=гггг;")));
			
		Иначе
			
			// Подсказку показываем до тех пор, пока не превышен лимит применения основной ставки.
			БазовыйЛимитОсновнойСтавки =
				КонтрольПраваПримененияСпецрежимаКлиентСервер.БазовыйЛимитДоходовДляПримененияОсновнойСтавкиУСН(ДатаПроверки);
			БазовыйЛимитПовышеннойСтавки =
				КонтрольПраваПримененияСпецрежимаКлиентСервер.БазовыйЛимитДоходовДляПримененияПовышеннойСтавкиУСН(ДатаПроверки);
			
			ШаблонПодсказки = НСтр("ru = 'В %1 году коэффициент-дефлятор составляет %2.
				|Граница применения основной ставки УСН: %3 млн руб * %2 = %4 млн руб.
				|Граница применения повышенной ставки УСН: %5 млн руб * %2 = %6 млн. руб.'");
			
			Подсказка = СтрШаблон(ШаблонПодсказки,
				Формат(ДатаПроверки, "ДФ=гггг;"),
				КоэффициентДефлятор,
				Окр(БазовыйЛимитОсновнойСтавки / 1000000, 2), // в млн. рублей
				Окр(ИнформацияПоПоказателю.Граница / 1000000, 2), // в млн. рублей
				Окр(БазовыйЛимитПовышеннойСтавки / 1000000, 2), // в млн. рублей
				Окр(ИнформацияПоПоказателю.ГраницаПовышеннойСтавки / 1000000, 2)); // в млн. рублей
			
		КонецЕсли;
		
	ИначеЕсли ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеСпецрежима Тогда
		БазовыйЛимитДоходов = КонтрольПраваПримененияСпецрежимаКлиентСервер.БазовыйЛимитДоходовДляПримененияОсновнойСтавкиУСН(ДатаПроверки);
		ШаблонПодсказки = НСтр("ru = 'В %1 году коэффициент-дефлятор составляет %2.
			|Граница применения УСН: %3 млн руб * %2 = %4 млн руб.'");
		Подсказка = СтрШаблон(ШаблонПодсказки,
			Формат(ДатаПроверки, "ДФ=гггг;"),
			КоэффициентДефлятор,
			Окр(БазовыйЛимитДоходов / 1000000, 2), // в млн. рублей
			Окр(ИнформацияПоПоказателю.ГраницаПовышеннойСтавки / 1000000, 2)); // в млн. рублей
		
	КонецЕсли;
	
	Возврат Подсказка;
	
КонецФункции

Функция НовыйИнформацияПоПоказателю()

	Структура = Новый Структура;
	Структура.Вставить("Показатель", 0);
	Структура.Вставить("Процент", 0);
	Структура.Вставить("Граница", 0);
	Структура.Вставить("ГраницаПовышеннойСтавки", 0);
	Структура.Вставить("УтраченоПравоПрименения", Ложь);
	Структура.Вставить("ПрименяетсяУСНДоходы", Ложь);
	Структура.Вставить("ИспользуетсяКоэффициентДефлятор", Ложь);
	Структура.Вставить("ДатаНачалаПримененияНДС", '00010101');
	Структура.Вставить("ГраницаПримененияНДС", 0);
	Структура.Вставить("ГраницаПримененияПониженнойСтавкиНДС", 0);
	Структура.Вставить("ПроцентДополнительно", 0);
	Структура.Вставить("ВидСообщения", "");
	Структура.Вставить("ВидДополнительногоСообщения", "");
	
	Возврат Структура;

КонецФункции

Функция ИнформацияПоПоказателюТовары(Организация, ДатаПроверки)
	
	ПрименяемыйСпецрежим = ПрименяемыйСпецрежим(Организация, ДатаПроверки);
	
	Если Не ПрименяемыйСпецрежим.НПД Тогда
		Возврат НовыйИнформацияПоПоказателю();
	КонецЕсли;

	НачалоПериода = НачалоГода(ДатаПроверки);
	КонецПериода = КонецМесяца(ДатаПроверки);
	
	УстановитьПривилегированныйРежим(Истина);
	Если ПрименяемыйСпецрежим.НПД Тогда
		КоличествоДокументовСЗапрещеннымиТоварами = КоличествоДокументовСЗапрещеннымиТоварами(Организация, НачалоПериода, КонецПериода);
	Иначе
		КоличествоДокументовСЗапрещеннымиТоварами = 0;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ИнформацияПоПоказателю = НовыйИнформацияПоПоказателю();
	
	Если КоличествоДокументовСЗапрещеннымиТоварами > 0 Тогда
		
		ИнформацияПоПоказателю.Показатель = КоличествоДокументовСЗапрещеннымиТоварами;
		ИнформацияПоПоказателю.УтраченоПравоПрименения = Истина;
		ИнформацияПоПоказателю.Процент = ИнформацияПоПоказателю.Показатель * 100;
		
	КонецЕсли;
	
	Возврат ИнформацияПоПоказателю;
	
КонецФункции

Функция КоличествоДокументовСЗапрещеннымиТоварами(Организация, НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВТ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ МаркируемыеТовары
	|ИЗ
	|	Справочник.Номенклатура КАК ВТ
	|ГДЕ
	|	НЕ ВТ.ПометкаУдаления
	|	И (НЕ ВТ.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИспользуемыеМаркируемыеТовары.Ссылка), 0) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		МаркируемыеТовары.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаркируемыеТовары КАК МаркируемыеТовары
	|			ПО РеализацияТоваровУслугТовары.Номенклатура = МаркируемыеТовары.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|			ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслуг.Ссылка
	|				И (РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|				И (РеализацияТоваровУслуг.Проведен)
	|				И (РеализацияТоваровУслуг.Организация = &Организация)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		МаркируемыеТовары.Ссылка
	|	ИЗ
	|		Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателюТовары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаркируемыеТовары КАК МаркируемыеТовары
	|			ПО СчетНаОплатуПокупателюТовары.Номенклатура = МаркируемыеТовары.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|			ПО СчетНаОплатуПокупателюТовары.Ссылка = СчетНаОплатуПокупателю.Ссылка
	|				И (СчетНаОплатуПокупателю.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|				И (СчетНаОплатуПокупателю.Проведен)
	|				И (СчетНаОплатуПокупателю.Организация = &Организация)) КАК ИспользуемыеМаркируемыеТовары";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Возврат Запрос.Выполнить().Выгрузить()[0].Количество;
	
КонецФункции

Функция ПараметрыУстановкиСтавкиНДС(Организация, НаДату = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	НачалоПериода = НачалоГода(НаДату);
	КонецПериода = КонецМесяца(НаДату);
	
	ДоходыПомесячно = СуммаДоходовУСНПомесячно(Организация, НачалоПериода, КонецПериода);
	Итог = 0;
	ГраницаПримененияПониженнойСтавкиНДС = УчетУСНКлиентСервер.ГраницаПримененияПониженнойСтавкиНДС(НаДату);
	ГраницаПримененияПовышеннойСтавкиНДС = УчетУСНКлиентСервер.ГраницаПримененияПовышеннойСтавкиНДС(НаДату);
	
	МесяцПревышения = '00010101';
	ТребуемаяСтавка = Неопределено;
	
	Для Каждого Доход Из ДоходыПомесячно Цикл
		Итог = Итог + Доход.Показатель;
		Если Итог > ГраницаПримененияПониженнойСтавкиНДС Тогда
			МесяцПревышения = Доход.Месяц;
			ТребуемаяСтавка = Перечисления.ВидыСтавокНДС.ОбщаяУСН;
		ИначеЕсли Итог > ГраницаПримененияПовышеннойСтавкиНДС Тогда
			МесяцПревышения = Доход.Месяц;
			ТребуемаяСтавка = Перечисления.ВидыСтавокНДС.Общая;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("МесяцПревышения,ТребуемаяСтавка",
		МесяцПревышения, ТребуемаяСтавка);
	
КонецФункции

Функция ВыводитьДополнительноеСообщение(ДатаПроверки)
	
	//Выводим сообщение о предстоящем изменении границы применения согласно Федеральному закону от 28.11.2025 № 425-ФЗ
	Возврат Год(ДатаПроверки) >= Год('20250101')
		И Год(ДатаПроверки) <= Год('20270101');
	
КонецФункции

#КонецОбласти

#Область Напоминания

// Получим процент, при котором следующий раз покажем предупреждение пользователю
//
Функция СледующееЗначениеНапоминания(ТекущееЗначение)
	
	// Определяется шкала показа предупреждений
	Если ТекущееЗначение < 60 Тогда
		Возврат 60;
	ИначеЕсли ТекущееЗначение < 70 Тогда
		Возврат 70;
	ИначеЕсли ТекущееЗначение < 80 Тогда
		Возврат 80;
	ИначеЕсли ТекущееЗначение < 90 Тогда
		Возврат 90;
	Иначе
		Возврат 100;
	КонецЕсли;

КонецФункции

Функция ДостигнутоСледующееЗначениеНапоминания(ИмяПоказателя, ТекущееЗначение, Организация, ДатаПроверки)

	Если Не ПоказыватьНапоминание(ТекущееЗначение) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяНастройкиПоказателя = ИмяНастройкиПравоПримененияСпецрежима() + ИмяПоказателя;
	
	СледующееНапоминание = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПравоПримененияСпецрежима(), 
		ИмяНастройкиПоказателя);
	
	Если СледующееНапоминание = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	НапоминаниеПоОрганизации = СледующееНапоминание[Организация];
	Если НапоминаниеПоОрганизации = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	СледующееЗначениеНапоминания = НапоминаниеПоОрганизации[Год(ДатаПроверки)];
	Если СледующееЗначениеНапоминания = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СледующееЗначениеНапоминания = ЗначениеНеНапоминать() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СледующееЗначениеНапоминания <= ТекущееЗначение Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ТекстНапомнитьПозже(СледующееЗначениеНапоминания)
	
	Если СледующееЗначениеНапоминания = ЗначениеНеНапоминать() Тогда
		Возврат НСтр("ru='Не напоминать'");
	Иначе
		Возврат СтрШаблон(
			НСтр("ru='Напомнить при %1'"),
			Строка(СледующееЗначениеНапоминания) + "%");
	КонецЕсли;
	
КонецФункции

Функция ЗначениеНеНапоминать()
	
	Возврат "НеНапоминать";
	
КонецФункции

Функция ПоказыватьНапоминание(ЗначениеНапоминания)
	
	Возврат ЗначениеНапоминания >= НижняяГраницаНапоминания();
	
КонецФункции

Функция НижняяГраницаНапоминания()
	
	Возврат 50;
	
КонецФункции

#КонецОбласти

#Область НормативноСправочнаяИнформацияПоПравуПримененияСпецрежима

// Возвращает лимит доходов налогоплательщика, ограничивающий его право на применение патентной системы налогообложения
//
// Параметры:
//  Дата - Дата - дата, на которую необходимо вернуть границу
//
// Возвращаемое значение:
//  Число - Число - Граница доходов, ограничивающая право применения ПСН
//
Функция ГраницаДоходовОграничивающаяПравоПримененияПСН(Дата) Экспорт

	// Лимит доходов налогоплательщика, ограничивающий его право на применение ПСН
	
	Если Год(Дата) >= 2028 Тогда
		// Федеральный закон от 28.11.2025 № 425-ФЗ
		ГраницаДоходов = 10000000;
	ИначеЕсли Год(Дата) >= 2027 Тогда
		// Федеральный закон от 28.11.2025 № 425-ФЗ
		ГраницаДоходов = 15000000;
	ИначеЕсли Год(Дата) >= 2026 Тогда
		// Федеральный закон от 28.11.2025 № 425-ФЗ
		ГраницаДоходов = 20000000;
	Иначе
		// пп. 1 п. 6 ст. 346.45 НК РФ
		ГраницаДоходов = 60000000;
	КонецЕсли;
	
	Возврат ГраницаДоходов;

КонецФункции

// Возвращает лимит доходов налогоплательщика, ограничивающий его право на применение УСН
// с учетом коэффициента дефлятора для указанной даты
//
// Параметры:
//  Дата - Дата - Дата, на которую получаем границу.
//
// Возвращаемое значение:
//  Число - Число - Граница стоимости основных средств, ограничивающая право применения УСН 
//
Функция ГраницаСтоимостиОсновныхСредствОграничивающаяПравоПримененияУСН(Дата) Экспорт

	// Лимит остаточной стоимости основных средств, определяемой по правилам бухгалтерского учета.
	// См. пп. 16 п. 3 ст. 346.12 НК РФ
	ПредельнаяСтоимостьОС = 100000000;
	
	Если Год(Дата) >= 2025 Тогда
		БазоваяПредельнаяСтоимостьОС = 200000000; // Федеральный закон от 12.07.2024 № 176-ФЗ
		
		// Коэффициент ежегодной индексации
		КоэффициентДефлятор = УчетУСНКлиентСервер.КоэффициентДефлятор(Дата);
		
		ПредельнаяСтоимостьОС = БазоваяПредельнаяСтоимостьОС * КоэффициентДефлятор;
	ИначеЕсли Год(Дата) >= 2017 Тогда
		ПредельнаяСтоимостьОС = 150000000; // Федеральный закон от 03.07.2016 N 243-ФЗ
	КонецЕсли;
	
	Возврат ПредельнаяСтоимостьОС;
	
КонецФункции

// Возвращает лимит остаточной стоимости основных средств налогоплательщика,
// ограничивающий его право на применение Автоматизированной УСН
// 
// Возвращаемое значение:
//  Число - Лимит остаточной стоимости основных средств 
//
Функция ГраницаСтоимостиОсновныхСредствОграничивающаяПравоПримененияАУСН() Экспорт

	Возврат 150000000;
	
КонецФункции

// Возвращает лимит средней численности работников
//
// Возвращаемое значение:
//  Число - Число - Граница среднесписочной численности работников, ограничивающая право применения патента 
//
Функция ГраницаСреднесписочнойЧисленностиРаботниковОграничивающаяПравоПримененияПатент() Экспорт
	
	// Лимит средней численности работников за налоговый период.
	// см. п. 5 ст. 346.43 НК РФ.
	Возврат 15;
	
КонецФункции

// Возвращает имя показателя "Доходы"
//
// Возвращаемое значение:
//   Строка - Имя показателя "Доходы"
//
Функция ИмяПоказателяСпецрежимаДоходы() Экспорт
	Возврат "Доходы";
КонецФункции

// Возвращает имя показателя "Товары"
//
// Возвращаемое значение:
//   Строка - Имя показателя "Товары"
//
Функция ИмяПоказателяСпецрежимаТовары() Экспорт
	Возврат "Товары";
КонецФункции

// Возвращает имя показателя "ОсновныеСредства"
//
// Возвращаемое значение:
//   Строка - Имя показателя "ОсновныеСредства"
//
Функция ИмяПоказателяСпецрежимаОсновныеСредства() Экспорт
	Возврат "ОсновныеСредства";
КонецФункции

// Возвращает имя показателя "Работники"
//
// Возвращаемое значение:
//   Строка - Имя показателя "Работники"
//
Функция ИмяПоказателяСпецрежимаРаботники() Экспорт
	Возврат "Работники";
КонецФункции

// Возвращает имя настройки, где хранятся напоминания
//
// Возвращаемое значение:
//   Строка - Имя настройки
//
Функция ИмяНастройкиПравоПримененияСпецрежима() Экспорт
	Возврат "ПравоПримененияСпецрежима";
КонецФункции

// Возвращает имя настройки, где хранятся напоминания
//
// Возвращаемое значение:
//   Строка - Имя настройки
//
Функция ИмяНастройкиПравоПримененияСпецрежимаПатент() Экспорт
	Возврат "ПравоПримененияСпецрежимаПатент";
КонецФункции

Функция ИмяПоказателяСпецрежимаДоходыДополнительно()
	
	Возврат "ДоходыДоп";
	
КонецФункции

Функция СсылкаНаПояснениеПоПравуПримененияСпецрежима(ИнформацияПоПоказателю, ПрименяемыйСпецрежим, ИмяПоказателя = "")
	
	СсылкаНаПояснение = "";
	
	Если ПрименяемыйСпецрежим.Патент Тогда
		Если ПрименяемыйСпецрежим.УСН И ИмяПоказателя = ИмяПоказателяСпецрежимаОсновныеСредства() Тогда
			Если ИнформацияПоПоказателю.УтраченоПравоПрименения Тогда
				СсылкаНаПояснение = "http://its.1c.ru/db/taxusn#content:65:1"; // Статья "Налогообложение при утрате права на применение УСН"
			Иначе
				СсылкаНаПояснение = "http://its.1c.ru/db/taxusn#content:64:1:issogl2201"; // Статья "Предельная остаточная стоимость основных средств"
			КонецЕсли;
		ИначеЕсли ИмяПоказателя <> ИмяПоказателяСпецрежимаДоходыДополнительно() Тогда
			СсылкаНаПояснение = "http://its.1c.ru/db/taxpsn#content:9:hdoc"; // Статья "Утрата права на патентную систему налогообложения"
		КонецЕсли;
	ИначеЕсли ПрименяемыйСпецрежим.УСН Тогда
		Если ИнформацияПоПоказателю.УтраченоПравоПрименения Тогда
			СсылкаНаПояснение = "http://its.1c.ru/db/taxusn#content:65:1"; // Статья "Налогообложение при утрате права на применение УСН"
		ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаДоходы() Тогда
			Если ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПрименениеНДССНачалаГода Тогда
				Если ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.НалогиИОтчеты)
					И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
					СсылкаНаПояснение = "ВключитьНДССНачалаГода";
				КонецЕсли;
			ИначеЕсли ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы().ПриближениеКГраницеСпецрежима Тогда
				СсылкаНаПояснение = "http://its.1c.ru/db/taxusn#content:64:1:issogl2209"; // Статья "Предельный размер доходов для сохранения права на применение УСН"
			КонецЕсли;
		ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаОсновныеСредства() Тогда
			СсылкаНаПояснение = "http://its.1c.ru/db/taxusn#content:64:1:issogl2201"; // Статья "Предельная остаточная стоимость основных средств"
		ИначеЕсли ИмяПоказателя = ИмяПоказателяСпецрежимаРаботники() Тогда
			СсылкаНаПояснение = "http://its.1c.ru/db/taxusn#content:64:1:issogl2202"; // Статья "Предельная средняя численность работников"
		ИначеЕсли ИмяПоказателя <> ИмяПоказателяСпецрежимаДоходыДополнительно() Тогда
			СсылкаНаПояснение = "http://its.1c.ru/db/taxusn#content:64:1:issogl1102"; // Статья "Обязательное прекращение применения УСН"
		КонецЕсли;
	ИначеЕсли ИнформацияПоПоказателю.УтраченоПравоПрименения
		И ПрименяемыйСпецрежим.НПД
		И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения) Тогда
		СсылкаНаПояснение = "УтратаПраваПримененияНПД";
	КонецЕсли;
	
	Возврат СсылкаНаПояснение;

КонецФункции

Функция ЗаголовокПоясненияПоПравуПримененияСпецрежима(СсылкаНаПояснение)

	Если СсылкаНаПояснение = "ВключитьНДССНачалаГода" Тогда
		Возврат НСтр("ru='Настроить НДС'");
	Иначе
		Возврат НСтр("ru='Подробнее'");
	КонецЕсли;
	
КонецФункции

Функция ЦветФонаГруппыПоясненияПоПравуПримененияСпецрежима(ИнформацияПоПоказателю, ИмяПоказателя)
	
	ЦветФонаГруппы = ЦветаСтиля.БыстрыеОтборыФонГруппы;
	ВидыСообщенийПоказателяСпецрежимаДоходы = ВидыСообщенийПоказателяСпецрежимаДоходы();
	УтратаПраваПримененияПатентаПрошлыйГод =
		ВидыСообщенийПоказателяСпецрежимаДоходы.УтратаПраваПримененияПатентаПрошлыйГод;
	
	Если ИмяПоказателя = ИмяПоказателяСпецрежимаДоходыДополнительно() Тогда
		Возврат ЦветФонаГруппы;
	КонецЕсли;
	
	Если ИнформацияПоПоказателю.УтраченоПравоПрименения
		Или ИнформацияПоПоказателю.ВидСообщения = УтратаПраваПримененияПатентаПрошлыйГод
		Или ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы.ПрименениеНДССНачалаГода
		Или ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы.ПрименениеНДС
		Или ИнформацияПоПоказателю.ВидСообщения = ВидыСообщенийПоказателяСпецрежимаДоходы.УтратаПраваПониженнойСтавкиНДС Тогда
		ЦветФонаГруппы = ЦветаСтиля.ЦветФонаНекорректногоКонтрагентаВДокументе;
	КонецЕсли;
	
	Возврат ЦветФонаГруппы;

КонецФункции

Функция ВидыСообщенийПоказателяСпецрежимаДоходы()
	
	Префикс = ИмяПоказателяСпецрежимаДоходы();
	
	ВидыСообщений = Новый Структура;
	ВидыСообщений.Вставить("УтратаПраваПрименения", Префикс + "УтратаПраваПрименения");
	ВидыСообщений.Вставить("ПрименениеНДССНачалаГода", Префикс + "ПрименениеНДССНачалаГода");
	ВидыСообщений.Вставить("ПрименениеНДС", Префикс + "ПрименениеНДС");
	ВидыСообщений.Вставить("ПриближениеКГраницеНДС", Префикс + "ПриближениеКГраницеНДС");
	ВидыСообщений.Вставить("УтратаПраваПониженнойСтавкиНДС", Префикс + "УтратаПраваПониженнойСтавкиНДС");
	ВидыСообщений.Вставить("ПриближениеКГраницеПониженнойСтавкиНДС", Префикс + "ПриближениеКГраницеПониженнойСтавкиНДС");
	ВидыСообщений.Вставить("ПриближениеКГраницеПовышеннойСтавки", Префикс + "ПриближениеКГраницеПовышеннойСтавки");
	ВидыСообщений.Вставить("ПриближениеКГраницеСпецрежима", Префикс + "ПриближениеКГраницеСпецрежима");
	ВидыСообщений.Вставить("УтратаПраваПримененияПатентаПрошлыйГод", Префикс + "УтратаПраваПримененияПатентаПрошлыйГод");
	
	Возврат ВидыСообщений;
	
КонецФункции

Функция ВидыСообщенийПоказателяСпецрежимаДоходыДополнительно()
	
	Префикс = ИмяПоказателяСпецрежимаДоходыДополнительно();
	
	ВидыСообщений = Новый Структура;
	ВидыСообщений.Вставить("ПрименениеНДСВСледующемГоду", Префикс + "ПрименениеНДСВСледующемГоду");
	ВидыСообщений.Вставить("УтратаПраваПримененияВСледующемГоду", Префикс + "УтратаПраваПримененияВСледующемГоду");
	ВидыСообщений.Вставить("УтратаПраваПримененияИНДСВСледующемГоду", Префикс + "УтратаПраваПримененияИНДСВСледующемГоду");
	
	Возврат ВидыСообщений;
	
КонецФункции

// Возвращает лимит средней численности работников.
//
// Возвращаемое значение:
//  Число - Граница среднесписочной численности работников, ограничивающая право применения ЕНВД.
//
Функция ГраницаСреднесписочнойЧисленностиРаботниковОграничивающаяПравоПримененияЕНВД() Экспорт
	
	// Лимит средней численности работников за налоговый период.
	// см. п. 2.3 ст. 346.26 НК РФ.
	Возврат 100;
	
КонецФункции

#КонецОбласти

Функция НеобходимоКонтролироватьПравоПримененияПатента(Организация, ДатаПроверки)
	
	ИмеютсяДействующиеПатенты = УчетПСН.ИмеютсяДействующиеПатенты(ДатаПроверки, Организация);
	
	ИмяНастройкиПоказателя = ИмяНастройкиПравоПримененияСпецрежимаПатент();
	
	СохраненныеДанные = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПравоПримененияСпецрежима(),
		ИмяНастройкиПоказателя);
		
	ПредупреждениеОтключено = СохраненныеДанные <> Неопределено
		И СохраненныеДанные[Организация] <> Неопределено
		И СохраненныеДанные[Организация] > ДатаПроверки;
	
	Возврат ИмеютсяДействующиеПатенты И Не ПредупреждениеОтключено;
	
КонецФункции

#КонецОбласти