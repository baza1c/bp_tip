#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьДекорациюСостояния(Форма, СтатусДокументаБП) Экспорт
	
	ИмяДекорацииСостоянияОригиналовДокументов = УчетОригиналовПервичныхДокументовБПКлиентСервер.ИмяДекорацииСостояния();
	ДекорацияСостояниеОригинала = Форма.Элементы.Найти(ИмяДекорацииСостоянияОригиналовДокументов);
	Если ДекорацияСостояниеОригинала = Неопределено
		Или Не ДекорацияСостояниеОригинала.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваДекорации = УчетОригиналовПервичныхДокументовБПВызовСервера.СвойстваДекорацииСостояния(
		Форма.Объект.Ссылка, СтатусДокументаБП);
	
	ЗаполнитьЗначенияСвойств(ДекорацияСостояниеОригинала, СвойстваДекорации);
	
КонецПроцедуры

Функция ИмяСобытияИзменениеСостоянияОригиналаПервичногоДокумента() Экспорт
	Возврат "Запись_РегистрСведенийСостоянияОригиналовПервичныхДокументов";
КонецФункции

Функция ЭтоСобытиеИзмененияСостоянияОриганалаПервичногоДокумента(ДокументСсылка, ИмяСобытия, Источник) Экспорт
	
	Если ИмяСобытия <> ИмяСобытияИзменениеСостоянияОригиналаПервичногоДокумента() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник) Тогда
		
		Если ТипЗнч(Источник) = Тип("Массив") Тогда
			
			Возврат Источник.Найти(ДокументСсылка) <> Неопределено;
			
		Иначе
			
			Возврат Источник = ДокументСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти