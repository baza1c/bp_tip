Функция НовыйРезультатПроверки() Экспорт
	
	Результат = Новый ТаблицаЗначений; // Порядок строк важен - в этом порядке выводим сообщения пользователю
	Результат.Колонки.Добавить("ПолноеИмяРеквизита", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("НомерСтроки",        Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("Ошибка",             Новый ОписаниеТипов("Строка"));
	
	Возврат Результат;
	
КонецФункции

// Процедура выводит сообщения пользователю
//
// Параметры:
//  РезультатПроверки	 - ТаблицаЗначений	- см. НовыйРезультатПроверки()
//  Отказ				 - Булево - устанавливается Истина, если РезультатПроверки непустой
//  МетаданныеДокумента	 - ОбъектМетаданных: Документ - метаданные документа, для которого вызвана проверка
//  РеквизитыЗаСсылками	 - Соответствие - 
//							Ключ     - Путь к проверяемому реквизиту документа
//							Значение - Путь к данным ссылки на форме. Символ "/" в начале означает, что путь является абсолютным (будет передан в сообщение пользователю как есть),
//									   в ином случае путь относительный (например, поле ссылки находится в той же табличной части, что и проверяемый реквизит - 
//									   тогда путь к ссылке в сообщении пользователю будет сформирован относительно проверяемого реквизита)
//
Процедура СообщитьРезультатПроверки(РезультатПроверки, Отказ, МетаданныеДокумента, РеквизитыЗаСсылками) Экспорт
	
	СинонимыСписков    = Новый Соответствие;
	СинонимыРеквизитов = Новый Соответствие; // Кэшируем только реквизиты списков, так как только для таких в результате может быть несколько записей
	
	Для Каждого Сообщение Из РезультатПроверки Цикл
		
		Если ПустаяСтрока(Сообщение.Ошибка) Тогда
			ВидСообщения = "Заполнение";
		Иначе
			ВидСообщения = "Корректность";
		КонецЕсли;
		
		ПолноеИмяРеквизита = Сообщение.ПолноеИмяРеквизита;
		
		МаскировочноеПоле = РеквизитыЗаСсылками[ПолноеИмяРеквизита];
		УказанПолныйПутьКМаскировочномуПолю = Ложь;
		
		Если МаскировочноеПоле <> Неопределено И СтрНачинаетсяС(МаскировочноеПоле, "/") Тогда
			УказанПолныйПутьКМаскировочномуПолю = Истина;
			МаскировочноеПоле = СтрЗаменить(МаскировочноеПоле, "/", "");
		КонецЕсли;	
		
		ИмяРеквизитаДетально = ОбщегоНазначенияБПКлиентСервер.РазложитьПолноеИмяРеквизита(ПолноеИмяРеквизита);
		
		Если ПустаяСтрока(ИмяРеквизитаДетально.ТабличнаяЧасть) Тогда
			
			// Поле шапки
			СинонимРеквизита = МетаданныеДокумента.Реквизиты.Найти(ИмяРеквизитаДетально.Реквизит).Синоним;
			
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения, СинонимРеквизита, , , Сообщение.Ошибка);
			
			Если МаскировочноеПоле = Неопределено Тогда
				ПолеСообщения = ПолноеИмяРеквизита;
				ПутьКДанным   = "Объект";
			Иначе
				ПолеСообщения = "";
				ПутьКДанным   = МаскировочноеПоле;
			КонецЕсли;
			
		Иначе // Поле списка
			
			ИмяСписка = ИмяРеквизитаДетально.ТабличнаяЧасть;
			ИмяПоля   = ИмяРеквизитаДетально.Реквизит;
			
			СинонимСписка = СинонимыСписков[ИмяСписка];
			Если СинонимСписка = Неопределено Тогда
				МетаданныеТабличнаяЧасть = МетаданныеДокумента.ТабличныеЧасти.Найти(ИмяСписка);
				СинонимСписка            = МетаданныеТабличнаяЧасть.Синоним;
				СинонимыСписков.Вставить(ИмяСписка, СинонимСписка);
			КонецЕсли;
			
			СинонимРеквизита  = СинонимыРеквизитов[ПолноеИмяРеквизита];
		
			Если СинонимРеквизита = Неопределено Тогда
				МетаданныеТабличнаяЧасть = МетаданныеДокумента.ТабличныеЧасти.Найти(ИмяСписка);
				МетаданныеРеквизиты = МетаданныеТабличнаяЧасть.Реквизиты;
				СинонимРеквизита = МетаданныеРеквизиты.Найти(ИмяПоля).Синоним;
				СинонимыРеквизитов.Вставить(ПолноеИмяРеквизита, СинонимРеквизита);
			КонецЕсли;
			
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				ВидСообщения,
				СинонимРеквизита,
				Сообщение.НомерСтроки,
				СинонимСписка,
				Сообщение.Ошибка);
							
			Если УказанПолныйПутьКМаскировочномуПолю Тогда
				ПолеСообщения = "";
				ПутьКДанным   = МаскировочноеПоле;
			Иначе
				Если МаскировочноеПоле = Неопределено Тогда
					ПолеСообщения = ИмяПоля;
				Иначе
					ПолеСообщения = МаскировочноеПоле;
				КонецЕсли;
				ИндексЭлементаСтрокой = Формат(Сообщение.НомерСтроки - 1, "ЧН=0; ЧГ=0");
				ПолеСообщения = ИмяСписка + "[" +  ИндексЭлементаСтрокой + "]." + ПолеСообщения;
				ПутьКДанным = "Объект";
			КонецЕсли;	
				
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПолеСообщения, ПутьКДанным, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьРеквизитыЗаСсылками(Объект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками) Экспорт
	
	Сообщения = НовыйРезультатПроверки();
	
	Проверенные = Новый Массив;
	Для Каждого ПолноеИмяРеквизита Из ПроверяемыеРеквизиты Цикл
		
		Если РеквизитыЗаСсылками[ПолноеИмяРеквизита] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Проверенные.Добавить(ПолноеИмяРеквизита);
		
		ПозицияТочки = СтрНайти(ПолноеИмяРеквизита, ".");
		Если ПозицияТочки = 0 Тогда
			
			Если ЗначениеЗаполнено(Объект[ПолноеИмяРеквизита]) Тогда
				Продолжить;
			КонецЕсли;
			
			Сообщения.Добавить().ПолноеИмяРеквизита = ПолноеИмяРеквизита;
			
		Иначе
			
			ИмяСписка = Лев(ПолноеИмяРеквизита, ПозицияТочки - 1);
			ИмяПоля   = Сред(ПолноеИмяРеквизита, ПозицияТочки + 1);
			
			Для Каждого СтрокаСписка Из Объект[ИмяСписка] Цикл
				
				Если ЗначениеЗаполнено(СтрокаСписка[ИмяПоля]) Тогда
					Продолжить;
				КонецЕсли;
				
				Сообщение = Сообщения.Добавить();
				Сообщение.ПолноеИмяРеквизита = ПолноеИмяРеквизита;
				Сообщение.НомерСтроки        = СтрокаСписка.НомерСтроки;				
				
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Если Сообщения.Количество() > 0 Тогда
		СообщитьРезультатПроверки(Сообщения, Отказ, Объект.Метаданные(), РеквизитыЗаСсылками);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, Проверенные);
	
КонецПроцедуры

// Позволяет определить есть ли в табличной части документа строки с дублирующимися
// значениями заданных реквизитов (всех одновременно).
// При нахождении дублей формирует сообщения пользователю.
//
// Параметры
//  Объект            - ДокументСсылка.*, СправочникСсылка.* - Объект ссылочного типа
//  ИмяТабличнойЧасти - Строка - Имя табличной части, в которой нужно искать дубли
//  ПоляПроверки      - Структура - Структура с перечнем реквизитов, по которым нужно искать дубли.
//                      Ключ структуры - имя реквизита, значение - признак необходимости
//                      поиска дублей в том числе по пустым значениям. Истина - искать
//                      дубли по пустым значениям реквизита, любое другое значение - 
//                      игнорировать строки, в которых реквизит не заполнен
//  Отказ             - Булево - Признак отказа от дальнейшей обработки. В случае нахождения дублей
//                      выставляется в Истина.
//
// Возвращаемое значение:
//   Булево - Истина - ошибок (дублей) не обнаружено, Ложь - в противном случае.
//
Функция ПроверитьОтсутствиеДублейВТабличнойЧасти(Объект, ИмяТабличнойЧасти, ПоляПроверки, Отказ) Экспорт
	
	СтруктураОтбора = Новый Структура;
	
	ПоляПроверкиСтрокой = "";
	Для Каждого КлючИЗначение Из ПоляПроверки Цикл
		ПоляПроверкиСтрокой = ПоляПроверкиСтрокой + ", " + КлючИЗначение.Ключ;
		СтруктураОтбора.Вставить(КлючИЗначение.Ключ);
	КонецЦикла;
	ПоляПроверкиСтрокой = Сред(ПоляПроверкиСтрокой, 3);
	
	НаборыЗначений = Объект[ИмяТабличнойЧасти].Выгрузить(, ПоляПроверкиСтрокой);
	НаборыЗначений.Колонки.Добавить("__КоличествоВхождений", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	НаборыЗначений.ЗаполнитьЗначения(1, "__КоличествоВхождений");
	НаборыЗначений.Свернуть(ПоляПроверкиСтрокой, "__КоличествоВхождений");
	
	НайденыДубли = Ложь;
	
	Если ПоляПроверки.Количество() > 1 Тогда
		ШаблонСообщенияОбОшибке = НСтр("ru = 'Значения %1 повторяются в строках %2.'");
	Иначе
		ШаблонСообщенияОбОшибке = НСтр("ru = 'Значение %1 повторяется в строках %2.'");
	КонецЕсли;
	ТекстСообщения = "";
	
	Если НаборыЗначений.Количество() <> Объект[ИмяТабличнойЧасти].Количество() Тогда
		// Есть дубли
		Для Каждого НаборЗначений Из НаборыЗначений Цикл
			Если НаборЗначений.__КоличествоВхождений = 1 Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначенияСтрокой = "";
			
			ПропуститьНаборЗначений = Ложь;
			Для Каждого КлючИЗначение Из ПоляПроверки Цикл
				Если КлючИЗначение.Значение <> Истина Тогда
					Если Не ЗначениеЗаполнено(НаборЗначений[КлючИЗначение.Ключ]) Тогда
						ПропуститьНаборЗначений = Истина;
						Прервать;
					КонецЕсли;
				КонецЕсли;
				
				ЗначенияСтрокой = ЗначенияСтрокой + ", """ + НаборЗначений[КлючИЗначение.Ключ] + """";
			КонецЦикла;
			Если ПропуститьНаборЗначений Тогда
				Продолжить;
			КонецЕсли;
			
			НайденыДубли = Истина;
			
			ЗначенияСтрокой = Сред(ЗначенияСтрокой, 3);
			
			МассивНомеровСтрок = Новый Массив;
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, НаборЗначений);
			СтрокиДубли = Объект[ИмяТабличнойЧасти].НайтиСтроки(СтруктураОтбора);
			Для Каждого СтрокаДубль Из СтрокиДубли Цикл
				МассивНомеровСтрок.Добавить(Формат(СтрокаДубль.НомерСтроки, "ЧГ="));
			КонецЦикла;
			НомераСтрок = СтрСоединить(МассивНомеровСтрок, ", ");
			
			ТекстСообщения = ТекстСообщения + Символы.ПС + СтрШаблон(ШаблонСообщенияОбОшибке, ЗначенияСтрокой, НомераСтрок);
		КонецЦикла;
	КонецЕсли;
	
	Если НайденыДубли Тогда
		МассивРеквизитов = Новый Массив;
		МетаданныеТабличнойЧасти = Объект.Метаданные().ТабличныеЧасти[ИмяТабличнойЧасти];
		Для Каждого КлючИЗначение Из ПоляПроверки Цикл
			МассивРеквизитов.Добавить(МетаданныеТабличнойЧасти.Реквизиты[КлючИЗначение.Ключ].Представление());
		КонецЦикла;
		ПредставленияРеквизитов = СтрСоединить(МассивРеквизитов, ", ");
		
		Если ПоляПроверки.Количество() > 1 Тогда
			ШаблонСообщения = НСтр("ru = 'Значения в колонках %1 не должны повторяться.'");
		Иначе
			ШаблонСообщения = НСтр("ru = 'Значения в колонке %1 не должны повторяться.'");
		КонецЕсли;
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПредставленияРеквизитов) + ТекстСообщения;
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("СПИСОК", "КОРРЕКТНОСТЬ", , ,
			МетаданныеТабличнойЧасти.Представление(), ТекстСообщения);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, ИмяТабличнойЧасти);
		
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Не НайденыДубли;
	
КонецФункции
