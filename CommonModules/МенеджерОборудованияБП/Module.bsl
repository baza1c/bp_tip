
#Область ПрограммныйИнтерфейс

Процедура ЭкземплярОборудованияПриСозданииНаСервере(ТекущийОбъект, ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт
	
	ДобавитьОбщиеРеквизиты(ЭтаФорма, ТекущийОбъект);
	
	ДобавитьРеквизитыККТ(ЭтаФорма, ТекущийОбъект);
	ДобавитьРеквизитыЭТ(ЭтаФорма, ТекущийОбъект);
	
	ПараметрыОборудования = ПараметрыПодключаемогоОборудования(ТекущийОбъект.Ссылка);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыОборудования);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтаФорма.Элементы, "Склад", "ПодсказкаВвода", НСтр("ru='Если оборудование используется в розничном магазине'"));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтаФорма.Элементы, "Склад", "АвтоОтметкаНезаполненного", Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтаФорма.Элементы, "Склад", "ОтметкаНезаполненного", Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтаФорма.Элементы, "АдресСайта", "Доступность", ЭтаФорма.РасчетыВИнтернет);
	
	ЭтаФорма.ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	Если НЕ МенеджерОборудованияБПВызовСервера.ОборудованиеТребуетОрганизацию(ТекущийОбъект.ТипОборудования) Тогда
		ТекущийОбъект.Организация = Справочники.Организации.ПустаяСсылка();
		
		ЭтаФорма.ОрганизацияВидимость = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтаФорма.Элементы, "Организация", "АвтоОтметкаНезаполненного", Ложь);
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.Организация) Тогда
		ТекущийОбъект.Организация = ОбщегоНазначенияБПВызовСервера.ОсновнаяОрганизация();
	КонецЕсли;
	
	ЭтаФорма.ОрганизацияВидимость = ЭтаФорма.ИспользуетсяНесколькоОрганизаций;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтаФорма.Элементы, "Организация", "АвтоОтметкаНезаполненного", ЭтаФорма.ИспользуетсяНесколькоОрганизаций);
КонецПроцедуры

Процедура ЭкземплярОборудованияПриЧтенииНаСервере(ТекущийОбъект, ЭтаФорма) Экспорт

	ПараметрыОборудования = ПараметрыПодключаемогоОборудования(ТекущийОбъект.Ссылка);
	
	ТекущийОбъект.Организация                 = ПараметрыОборудования.Организация; 
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыОборудования);
КонецПроцедуры

Процедура ЭкземплярОборудованияПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	ПараметрыОборудования = НовыйПараметрыОборудования(ТекущийОбъект.ТипОборудования);
	
	ПараметрыОборудования.Организация  = ТекущийОбъект.Организация;
	
	ЗаполнитьЗначенияСвойств(ПараметрыОборудования, ПараметрыЗаписи);
	
	Если ТекущийОбъект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ Тогда
		ПараметрыОборудования.ПечататьЧек = Ложь;
	КонецЕсли;
	
	// если склады не используются в свойство оборудования склад не пишем, 
	// чтобы не срабатывал лишний отбор в документах
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет") Тогда
		ПараметрыОборудования.Склад = Неопределено;
	КонецЕсли;
	
	Если ПараметрыОборудования.Свойство("ЭлектронныеСертификатыНСПК") Тогда
		ПараметрыОборудования.ЭлектронныеСертификатыНСПК = ПараметрыЗаписи.ЭлектронныеСертификатыНСПК;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОплатуСертификатамиНСПК") Тогда
			
			ПараметрыПодключенияНСПК = ПараметрыЗаписи.ПараметрыПодключенияНСПК;
			
			ПараметрыОборудования.ПротоколОбменаФЭСНСПК = ПараметрыПодключенияНСПК.ПротоколОбменаФЭСНСПК;
			
			УстановитьПривилегированныйРежим(Истина);
			
			// Общий параметр для ИБ
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище("ЭС_НСПК_МИР", ПараметрыПодключенияНСПК.АдресСервисаНСПК, "АдресСервисаНСПК");
			
			// Ключ доступа для конкретной организации
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Организация, ПараметрыПодключенияНСПК.КлючОрганизацииНСПК, "КлючОрганизацииНСПК");
			
			// Идентификатор и ключ доступа для конкретной торговой точки
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, ПараметрыПодключенияНСПК.ИдентификаторНСПК,            "ИдентификаторНСПК");
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, ПараметрыПодключенияНСПК.КлючКассыНСПК,                "КлючКассыНСПК");
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, ПараметрыПодключенияНСПК.ПроверятьКорневыеСертификаты, "ПроверятьКорневыеСертификаты");
			
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли; 
	КонецЕсли; 
	
	РегистрыСведений.ОборудованиеПоОрганизациям.УстановитьПараметрыОборудования(ТекущийОбъект.Ссылка, ПараметрыОборудования);
	
	Если ТекущийОбъект.УстройствоИспользуется Тогда
		Если ТекущийОбъект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал 
			И Не ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПоПлатежнымКартам")
			И ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьОплатуПоПлатежнымКартам) Тогда
			
			Константы.ИспользоватьОплатуПоПлатежнымКартам.Установить(Истина);
			ПараметрыЗаписи.Вставить("НужноОбновитьИнтерфейс", Истина);
			
		ИначеЕсли ЭтоКассовоеОборудование(ТекущийОбъект.ТипОборудования, Истина)
			И Не ПолучитьФункциональнуюОпцию("ИспользоватьФискальныйРегистратор") 
			И ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьФискальныйРегистратор) Тогда
			
			Константы.ИспользоватьФискальныйРегистратор.Установить(Истина);
			ПараметрыЗаписи.Вставить("НужноОбновитьИнтерфейс", Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЭкземплярФискальныеОперацииПриСозданииНаСервере(Запись, ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт
	Элементы = ЭтаФорма.Элементы;
	
	ГруппаБаннер = Элементы.Вставить("ПредупреждениеОПробитомЧеке", Тип("ГруппаФормы"),ЭтаФорма, Элементы.Найти("ГруппаШапка"));
	
	ГруппаБаннер.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаБаннер.РастягиватьПоГоризонтали = Истина;
	ГруппаБаннер.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаБаннер.ОтображатьЗаголовок = ЛОЖЬ;
	ГруппаБаннер.ЦветФона = ЦветаСтиля.БыстрыеОтборыФонГруппы;
	
	НадписьПробитьЧек = Элементы.Добавить("НадписьПробитьЧек", Тип("ДекорацияФормы"), ГруппаБаннер); 
	
	ШаблонСтроки = НСтр("ru = 'Для пробития чека коррекции перейдите в документ основание'");
	НадписьПробитьЧек.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки);
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.Представление2;
	ГруппаБаннер.Видимость = НЕ ЭтаФорма.Параметры.Свойство("НеПоказыватьБаннер");
КонецПроцедуры

Процедура ПриЗаполненииОграниченияДоступа(Менеджер, Ограничение) Экспорт
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(Менеджер);
	Если МетаданныеОбъекта = Неопределено Тогда
		
		ЕстьРеквизитОрганизация = Ложь;
		
	ИначеЕсли ОбщегоНазначения.ЭтоРегистр(МетаданныеОбъекта) Тогда
		
		ЕстьРеквизитОрганизация =  НЕ (МетаданныеОбъекта.Реквизиты.Найти("Организация") = Неопределено) 
			ИЛИ НЕ (МетаданныеОбъекта.Ресурсы.Найти("Организация") = Неопределено)
			ИЛИ НЕ (МетаданныеОбъекта.Измерения.Найти("Организация") = Неопределено);
	Иначе
		
		ЕстьРеквизитОрганизация =  ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", МетаданныеОбъекта);
		
	КонецЕсли;
	
	Если НЕ ЕстьРеквизитОрганизация  Тогда
		Возврат;
	КонецЕсли; 
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации 
	|	ПО СправочникОрганизации.Ссылка = ЭтотСписок.Организация
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|ЗначениеРазрешено(Организация, ПустаяСсылка КАК Истина)
	|ИЛИ ЗначениеРазрешено(СправочникОрганизации.ГоловнаяОрганизация, ПустаяСсылка КАК Истина)
	|";
	
КонецПроцедуры

// Возвращает наличие подключенного оборудования в ИБ 
// без учета рабочего места пользователя. 
//
// Параметры:
//  ТипыПО - Строка, Массив - типы оборудования строкой 
//
// Возвращаемое значение:
//  Булево - возможность использования оборудования в конфигурации
Функция ИспользуетсяОборудование(ТипыПО) Экспорт
	Если НЕ ИспользоватьПодключаемоеОборудование() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СписокОборудованияОтбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
	СписокОборудованияОтбор.ТипыПО = ТипыПО;
	
	Возврат МенеджерОборудованияВызовСервера.СписокОборудования(СписокОборудованияОтбор).Количество() > 0;
КонецФункции 

Функция ЭтоКассовоеОборудование(ТипОборудования, ВключаяНефискальные = Ложь) Экспорт
	Возврат ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ
		ИЛИ ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ 
		ИЛИ (ВключаяНефискальные 
			И (ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ФискальныйРегистратор ИЛИ ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков));
КонецФункции


#Область ОфлайнОборудование

Процедура ЭкземплярОфлайнОборудованияПриСозданииНаСервере(ТекущийОбъект, ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт
	ДобавляемыеРеквизиты = Новый Массив;
	
	ТипБулево = Новый ОписаниеТипов("Булево");
	ТипЧисло = Новый ОписаниеТипов("Число");
	ТипСтрока = Новый ОписаниеТипов("ФорматированнаяСтрока");
	ТипСклад = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("СправочникСсылка.Склады")));
	ТипГостиница = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("СправочникСсылка.Гостиницы")));
	ТипУзелОбмена = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("ПланОбменаСсылка.ОбменСПодключаемымОборудованиемOffline")));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Склад", ТипСклад, , НСтр("ru = 'Склад'")));
	
	УплачиваетсяТуристическийНалог = ПолучитьФункциональнуюОпцию("УплачиваетсяТуристическийНалог");
	Если УплачиваетсяТуристическийНалог Тогда
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Гостиница", ТипГостиница, , НСтр("ru = 'Гостиница'")));
	КонецЕсли;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("УзелОбмена", ТипУзелОбмена, , "УзелОбмена"));
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Элемент = ЭтаФорма.Элементы.Добавить("Склад", Тип("ПолеФормы"));
	Элемент.ПутьКДанным = "Склад";
	Элемент.Вид         = ВидПоляФормы.ПолеВвода;
	Элемент.ПодсказкаВвода = НСтр("ru='Если оборудование используется в розничном магазине'");
	Элемент.АвтоОтметкаНезаполненного = Ложь;
	Элемент.ОтметкаНезаполненного = Ложь;
	
	Если УплачиваетсяТуристическийНалог Тогда
		Элемент = ЭтаФорма.Элементы.Добавить("Гостиница", Тип("ПолеФормы"));
		Элемент.ПутьКДанным = "Гостиница";
		Элемент.Вид         = ВидПоляФормы.ПолеВвода; 
		Элемент.ПодсказкаВвода = НСтр("ru='Гостиница для уплаты туристического налога'");
		
		СвязьПоОрганизации = Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация");
		СвязиПараметровВыбора = Новый Массив();
		СвязиПараметровВыбора.Добавить(СвязьПоОрганизации);
		Элемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() 
		И Параметры.Свойство("Склад") Тогда
		
		ЭтаФорма.Склад = Параметры.Склад;
	КонецЕсли;
	
	ПолеТипОборудования = ЭтаФорма.Элементы.Найти("ТипОборудования");
	Если ПолеТипОборудования <> Неопределено И НЕ ПолеТипОборудования.ТолькоПросмотр Тогда
		ПолеТипОборудования.РежимВыбораИзСписка = Истина;
		ПолеТипОборудования.СписокВыбора.Очистить();
		ПолеТипОборудования.СписокВыбора.Добавить(Перечисления.ТипыОфлайнОборудования.ККМ);
	КонецЕсли;
	
	КнопкаВыгрузитьНастройки = ЭтаФорма.Элементы.Найти("ФормаОбработкаПодключенныеКассыККМОфлайнВыгрузитьНастройки");
	Если КнопкаВыгрузитьНастройки <> Неопределено Тогда
		КнопкаВыгрузитьНастройки.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	КонецЕсли;
	
	КнопкаПолныйПрайсЛист = ЭтаФорма.Элементы.Найти("ФормаОбработкаПодключенныеКассыККМОфлайнВыгрузитьПолныйПрайсЛист");
	Если КнопкаПолныйПрайсЛист <> Неопределено Тогда
		КнопкаПолныйПрайсЛист.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	КонецЕсли;
	
	КнопкаОчиститьПрайсЛист = ЭтаФорма.Элементы.Найти("ФормаОбработкаПодключенныеКассыККМОфлайнОчиститьПрайсЛист");
	Если КнопкаОчиститьПрайсЛист <> Неопределено Тогда
		КнопкаОчиститьПрайсЛист.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	КонецЕсли;
	
	Если Параметры.Свойство("РабочееМесто") Тогда
		ТекущийОбъект.РабочееМесто = Параметры.РабочееМесто;
	КонецЕсли;
	
	ЭтаФорма.Элементы.Организация.АвтоОтметкаНезаполненного = Истина;
	
	ПараметрыОборудования = ПараметрыПодключаемогоОборудования(ТекущийОбъект.Ссылка);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыОборудования);
КонецПроцедуры

Процедура ЭкземплярОфлайнОборудованияПриЧтенииНаСервере(ТекущийОбъект, ЭтаФорма) Экспорт

	ПараметрыОборудования = ПараметрыПодключаемогоОборудования(ТекущийОбъект.Ссылка);
	
	ТекущийОбъект.Организация                 = ПараметрыОборудования.Организация;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыОборудования);
	
КонецПроцедуры

Процедура ЭкземплярОфлайнОборудованияОбработкаПроверкиЗаполненияНаСервере(ТекущийОбъект, Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Склад") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Форма.Склад) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Склад'")),
			ТекущийОбъект.Ссылка ,, "Склад", Отказ);
		Возврат;
	КонецЕсли;

	ТипЦенРозничнойТорговли = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Склад, "ТипЦенРозничнойТорговли");
	Если НЕ ЗначениеЗаполнено(ТипЦенРозничнойТорговли) Тогда
		СообщениеПользователю = НСтр("ru = 'Для обмена с офлайн оборудованием необходимо указать тип цен для склада %1.'");
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеПользователю, Форма.Склад), 
			ТекущийОбъект.Ссылка, ,"Склад", Отказ);
		Возврат;
	КонецЕсли; 
КонецПроцедуры


Процедура ЭкземплярОфлайнОборудованияПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяРозничнаяТорговля") 
		И ПравоДоступа("Изменение", Метаданные.Константы.ВедетсяРозничнаяТорговля) Тогда
		
		Константы.ВедетсяРозничнаяТорговля.Установить(Истина);
		ПараметрыЗаписи.Вставить("НужноОбновитьИнтерфейс", Истина);
		
	КонецЕсли;
	
	Если НЕ ПараметрыЗаписи.Свойство("Организация") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ОборудованиеПоОрганизациям.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Оборудование = ТекущийОбъект.Ссылка;
	
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗаписи);
	
	Если ПараметрыЗаписи.Свойство("УзелОбмена") 
		И ПараметрыЗаписи.Свойство("Склад") Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЗаписи.УзелОбмена) Тогда
			МенеджерЗаписи.УзелОбмена = ПараметрыЗаписи.УзелОбмена;
		ИначеЕсли ЗначениеЗаполнено(ПараметрыЗаписи.Склад) Тогда
			УзелОбъект = ПланыОбмена.ОбменСПодключаемымОборудованиемOffline.СоздатьУзел();
			УзелОбъект.УстановитьНовыйКод();
			УзелОбъект.Наименование = ТекущийОбъект.Наименование;
			УзелОбъект.Записать();
			
			МенеджерЗаписи.УзелОбмена = УзелОбъект.Ссылка;
			
			ПараметрыОбъекта = Новый Структура("УзелОбмена, Склад", УзелОбъект.Ссылка, ПараметрыЗаписи.Склад);
			ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
			
			ДлительныеОперации.ВыполнитьВФоне("ПланыОбмена.ОбменСПодключаемымОборудованиемOffline.ОбновитьРегистрКодовНоменклатуры",
				ПараметрыОбъекта, ПараметрыВыполнения);
		КонецЕсли;
			
	КонецЕсли;
	
	
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти 

#Область РаботаСПерсональнымиДанными

Процедура ОбработкаЗаполненияПерсональныхДанных(ПерсональныеДанные, СубъектПерсональныхДанных, ТипПерсональныхДанных, НаДату) Экспорт
	Если НЕ ЗначениеЗаполнено(СубъектПерсональныхДанных) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СубъектПерсональныхДанных) = Тип("СправочникСсылка.Контрагенты") Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("КонтрагентСсылка", СубъектПерсональныхДанных);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(ФизическиеЛица.Ссылка, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ФизическоеЛицо
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО Контрагенты.ИНН = ФизическиеЛица.ИНН
		|ГДЕ
		|	Контрагенты.Ссылка = &КонтрагентСсылка";
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			ФизическоеЛицо = Результат.ФизическоеЛицо;
		Иначе
			ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		КонецЕсли; 
	Иначе
		ФизическоеЛицо = СубъектПерсональныхДанных;
	КонецЕсли; 
	
	Если ТипПерсональныхДанных = Перечисления.ТипыПерсональныхДанныхККТ.ИНН Тогда
		ПерсональныеДанные.ИНН = ПокупательИНН(СубъектПерсональныхДанных);
	Иначе
		ПерсональныеДанные.ДатаРождения = ПокупательДатаРождения(ФизическоеЛицо);
		ПерсональныеДанные.ДанныеДокумента = ПокупательПаспортныеДанные(ФизическоеЛицо, НаДату);
	КонецЕсли;
	
	ПокупательГражданство = ПокупательГражданство(ФизическоеЛицо, НаДату);
	Если ПокупательГражданство.Пустая()
		ИЛИ ПокупательГражданство = Справочники.СтраныМира.Россия Тогда
		ПерсональныеДанные.ВидДокумента = Перечисления.ВидДокументаУдостоверяющегоЛичностьККТ.ПаспортГражданинаРФ;
	Иначе
		ГражданствоКод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПокупательГражданство, "Код");
		
		ПерсональныеДанные.Гражданство = ГражданствоКод;
		ПерсональныеДанные.ВидДокумента = Перечисления.ВидДокументаУдостоверяющегоЛичностьККТ.ПаспортИностранногоГражданина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

Функция НовыйПараметрыПроверкиЗаполнения() Экспорт
	ПараметрыОбъекта = Новый Структура;
	
	ПараметрыОбъекта.Вставить("ПечататьЧек",           Ложь);
	ПараметрыОбъекта.Вставить("ОблачнаяККТ", Ложь);
	
	ПараметрыОбъекта.Вставить("ОтправлятьSMS",         Ложь);
	ПараметрыОбъекта.Вставить("НомерТелефона",         "");
	
	ПараметрыОбъекта.Вставить("ОтправлятьEmail",       Ложь);
	ПараметрыОбъекта.Вставить("АдресЭлектроннойПочты", "");
	
	ПараметрыОбъекта.Вставить("РасчетыВИнтернет",      Ложь);
	ПараметрыОбъекта.Вставить("АдресСайта",            "");
	
	Возврат ПараметрыОбъекта;
КонецФункции 

// ОбработкаПроверкиЗаполненияПечатьЧека - проверяет заполненность реквизитов на форме оборудования
// при необходимости выдается ошибка
//
// Параметры:
//  ПараметрыОбъекта  - Структура - параметры объекта для проверки
//                 см. МенеджерОборудованияБП.НовыйПараметрыПроверкиЗаполнения()
//  ПроверятьКонтактныеДанные  - Булево - флаг необходимости проверки корректности контактных данных
//  Отказ  - Булево- Истина, если в результате проверки выявляены ошибки
//
Процедура ОбработкаПроверкиЗаполненияПечатьЧека(ПараметрыОбъекта, ПроверятьКонтактныеДанные, Отказ) Экспорт
	
	// Чтобы можно было не печатать чек, он должен быть отправлен по email/sms 
	ЭлектронныйЧек = ПараметрыОбъекта.ОтправлятьSMS ИЛИ ПараметрыОбъекта.ОтправлятьEmail;
	
	Если ПараметрыОбъекта.ОблачнаяККТ И НЕ ПараметрыОбъекта.ОтправлятьEmail И НЕ ПараметрыОбъекта.ОтправлятьSMS Тогда
		ОписаниеОшибки = НСтр("ru = 'Для пробития чека на облачной кассе укажите номер телефона или email покупателя.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, ,"ПечататьЧек", , Отказ);
	ИначеЕсли НЕ ПараметрыОбъекта.ПечататьЧек И НЕ ЭлектронныйЧек Тогда
		ОписаниеОшибки = НСтр("ru = 'Печать чека обязательна, если он не отправляется в электронном виде по email/sms.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, ,"ПечататьЧек", , Отказ);
	ИначеЕсли ПроверятьКонтактныеДанные Тогда
		Если ПараметрыОбъекта.ОтправлятьEmail Тогда
			Если НЕ ЗначениеЗаполнено(ПараметрыОбъекта.АдресЭлектроннойПочты) Тогда
				ТекстСообщения = "";
				ВидСообщения   = "Заполнение";
				
				ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения,"Адрес электронной почты",,,ТекстСообщения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , "АдресЭлектроннойПочты", , Отказ);
			ИначеЕсли НЕ ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(ПараметрыОбъекта.АдресЭлектроннойПочты) Тогда 
				ТекстСообщения = НСтр("ru = 'Неверный формат адреса электронной почты.'");
				ВидСообщения   = "Корректность";
				
				ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения,"Адрес электронной почты",,,ТекстСообщения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , "АдресЭлектроннойПочты",, Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыОбъекта.ОтправлятьSMS Тогда
			НомерТелефонаБезПрефикса = ?(СтрНайти(ПараметрыОбъекта.НомерТелефона, "+") = 1, Сред(ПараметрыОбъекта.НомерТелефона, 2), ПараметрыОбъекта.НомерТелефона);
			Если НЕ ЗначениеЗаполнено(ПараметрыОбъекта.НомерТелефона) Тогда
				ТекстСообщения = "";
				ВидСообщения   = "Заполнение";
				
				ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения, "Номер телефона",,,ТекстСообщения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , "НомерТелефона", , Отказ);
			ИначеЕсли НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерТелефонаБезПрефикса) 
				ИЛИ НЕ ОрганизацииФормыКлиентСервер.ТелефонСоответствуетТребованиям(НомерТелефонаБезПрефикса) Тогда
				
				ТекстСообщения = НСтр("ru = 'Неверный формат номера телефона.'");
				ВидСообщения   = "Корректность";
				
				ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения, "Номер телефона",,,ТекстСообщения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , "НомерТелефона",, Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


// Конструктор структуры параметров в зависимости от типа оборудования
//
// Параметры:
//  ТипОборудования  - <Перечисление.ТипыПодключаемогоОборудования, Неопределено> - тип оборудования, 
//						если передано НЕОПРЕДЕЛЕНО то возвращается структура для инициализации нового экземпляра справочника
//
// Возвращаемое значение:
//   <Структура>
//
Функция НовыйПараметрыОборудования(ТипОборудования) Экспорт
	ПараметрыОборудования = Новый Структура;
	
	ПараметрыОборудования.Вставить("Организация",     БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
	ПараметрыОборудования.Вставить("Склад",           БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнойСклад"));
	ПараметрыОборудования.Вставить("Гостиница",       БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяГостиница"));
	ПараметрыОборудования.Вставить("ТипОборудования", ТипОборудования);
	
	Если НЕ ЗначениеЗаполнено(ТипОборудования) 
		ИЛИ ЭтоКассовоеОборудование(ТипОборудования) Тогда
		
		ПараметрыОборудования.Вставить("Отправляет1СSMS",              0);
		ПараметрыОборудования.Вставить("Отправляет1СEmail",            0);
		ПараметрыОборудования.Вставить("ЗаполнятьКонтактныеДанные",    Ложь);
		ПараметрыОборудования.Вставить("ПечататьЧек",                  Истина);
		ПараметрыОборудования.Вставить("ОтправлятьEMail",              Ложь);
		ПараметрыОборудования.Вставить("ОтправлятьSMS",                Ложь);
		ПараметрыОборудования.Вставить("РасчетыВИнтернет",             Ложь);
		ПараметрыОборудования.Вставить("АдресСайта",                   "");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипОборудования) 
		ИЛИ ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ Тогда
		ПараметрыОборудования.Вставить("ИспользоватьДляСБП",           Ложь);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипОборудования) 
		ИЛИ ТипОборудования = Перечисления.ТипыОфлайнОборудования.ККМ Тогда
		
		ПараметрыОборудования.Вставить("УзелОбмена",                   НЕОПРЕДЕЛЕНО);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипОборудования) 
		ИЛИ ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал Тогда
		
		ПараметрыОборудования.Вставить("ЭлектронныеСертификатыНСПК",   Ложь);
		ПараметрыОборудования.Вставить("ПротоколОбменаФЭСНСПК",        Перечисления.ПротоколОбменаФЭСНСПК.ВерсияV2);
		ПараметрыОборудования.Вставить("ПроверятьКорневыеСертификаты", Истина);
	КонецЕсли;
	
	Возврат ПараметрыОборудования;
КонецФункции

Функция ВедетсяОбъемноСортовойУчет() Экспорт
	
	ВедетсяОбъемноСортовойУчет = Ложь;
	
	ВидыПродукцииОбъемноСортовогоУчета = ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииОбъемноСортовогоУчета();
	Для каждого ВидПродукции Из ВидыПродукцииОбъемноСортовогоУчета Цикл
		ВедетсяОбъемноСортовойУчет = ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(ВидПродукции);
		Если ВедетсяОбъемноСортовойУчет Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ВедетсяОбъемноСортовойУчет;
	
КонецФункции 

Функция ПараметрыПодключаемогоОборудования(ПодключаемоеОборудование) Экспорт
	ДанныеУстройства = МенеджерОборудования.ДанныеУстройства(ПодключаемоеОборудование);
	ТипОборудования = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеУстройства, "ТипОборудования");
	
	ПараметрыОборудования = НовыйПараметрыОборудования(ТипОборудования);
	Если НЕ ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		Возврат ПараметрыОборудования;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ПодключаемоеОборудование", ПодключаемоеОборудование);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОборудованиеПоОрганизациям.Организация КАК Организация,
	|	ОборудованиеПоОрганизациям.Склад КАК Склад,
	|	ЕСТЬNULL(ОборудованиеПоОрганизациям.УзелОбмена.Ссылка, НЕОПРЕДЕЛЕНО) КАК УзелОбмена,
	|	ВЫБОР
	|		КОГДА ОборудованиеПоОрганизациям.Отправляет1СSMS
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Отправляет1СSMS,
	|	ВЫБОР
	|		КОГДА ОборудованиеПоОрганизациям.Отправляет1СEmail
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Отправляет1СEmail,
	|	ОборудованиеПоОрганизациям.ЗаполнятьКонтактныеДанные КАК ЗаполнятьКонтактныеДанные,
	|	ОборудованиеПоОрганизациям.ПечататьЧек
	|		ИЛИ НЕ ОборудованиеПоОрганизациям.ОтправлятьEMail
	|			И НЕ ОборудованиеПоОрганизациям.ОтправлятьSMS КАК ПечататьЧек,
	|	ОборудованиеПоОрганизациям.ОтправлятьEMail КАК ОтправлятьEMail,
	|	ОборудованиеПоОрганизациям.ЭлектронныеСертификатыНСПК КАК ЭлектронныеСертификатыНСПК,
	|	ОборудованиеПоОрганизациям.ОтправлятьSMS КАК ОтправлятьSMS,
	|	ОборудованиеПоОрганизациям.Гостиница КАК Гостиница,
	|	ОборудованиеПоОрганизациям.ПротоколОбменаФЭСНСПК КАК ПротоколОбменаФЭСНСПК,
	|	ОборудованиеПоОрганизациям.РасчетыВИнтернет КАК РасчетыВИнтернет,
	|	ОборудованиеПоОрганизациям.АдресСайта КАК АдресСайта,
	|	ОборудованиеПоОрганизациям.ИспользоватьДляСБП КАК ИспользоватьДляСБП
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|ГДЕ
	|	ОборудованиеПоОрганизациям.Оборудование = &ПодключаемоеОборудование";
	
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		Если РезультатЗапроса.ЭлектронныеСертификатыНСПК Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыОборудования, РезультатЗапроса,,"ПротоколОбменаФЭСНСПК");
			
			УстановитьПривилегированныйРежим(Истина);
			
			ПараметрыОборудования.Вставить("ИдентификаторНСПК",            ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ПодключаемоеОборудование, "ИдентификаторНСПК")); 
			ПараметрыОборудования.Вставить("КлючКассыНСПК",                ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ПодключаемоеОборудование, "КлючКассыНСПК")); 
			
			ПроверятьКорневыеСертификаты = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ПодключаемоеОборудование, "ПроверятьКорневыеСертификаты");
			ПараметрыОборудования.Вставить("ПроверятьКорневыеСертификаты", ?(ПроверятьКорневыеСертификаты = Неопределено, Истина, ПроверятьКорневыеСертификаты)); 
			
			ПараметрыОборудования.Вставить("КлючОрганизацииНСПК", ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ПараметрыОборудования.Организация, "КлючОрганизацииНСПК")); 
			ПараметрыОборудования.Вставить("АдресСервисаНСПК",    ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища("ЭС_НСПК_МИР", "АдресСервисаНСПК")); 
			
			УстановитьПривилегированныйРежим(Ложь);
			
			Если ЗначениеЗаполнено(РезультатЗапроса.ПротоколОбменаФЭСНСПК) Тогда
				ПараметрыОборудования.Вставить("ПротоколОбменаФЭСНСПК", РезультатЗапроса.ПротоколОбменаФЭСНСПК);
			КонецЕсли;
		Иначе
			ЗаполнитьЗначенияСвойств(ПараметрыОборудования, РезультатЗапроса);
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат ПараметрыОборудования;
КонецФункции

Функция ИспользоватьПодключаемоеОборудование() Экспорт
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ПравоДоступа("Чтение", Метаданные.Справочники.ПодключаемоеОборудование);
	
КонецФункции

Функция ТребуетсяОбновлениеДрайверов() Экспорт
	Перем ТекущаяВерсия;
	
	// Не полноправный пользователь или разделенный режим - проверку не проводим
	Если НЕ ПолучениеВнешнихКомпонент.ДоступнаИнтерактивнаяЗагрузкаВнешнихКомпонент() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	Для каждого КлючИЗначение Из ТипыОборудования Цикл
		ТипыОборудования[КлючИЗначение.Ключ] = Истина;
	КонецЦикла;
	
	ИспользуетсяОборудование = ИспользуетсяОборудование(ТипыОборудования);
	Если НЕ ИспользуетсяОборудование Тогда
		
		// оборудования нет - проверку не проводим
		Возврат Ложь;
	КонецЕсли;
	
	ВариантОбновления  = ПолучениеВнешнихКомпонентВызовСервера.НастройкиОбновленияВнешнихКомпонент().ВариантОбновления;
	Если ВариантОбновления = 2 
		ИЛИ ВариантОбновления = 1 И ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		
		// настроено автоматическое обновление с портала или из каталога - проверку не проводим
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ВерсияПроверки = Константы.ВерсияПредупрежденияДрайвераОборудования.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ДрайвераАктуальны = ДрайверАктуален(ВерсияПроверки, ТекущаяВерсия);
	Если ДрайвераАктуальны И ВерсияПроверки <> ТекущаяВерсия Тогда
		УстановитьПривилегированныйРежим(Истина);
		Константы.ВерсияПредупрежденияДрайвераОборудования.Установить(ТекущаяВерсия);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат НЕ ДрайвераАктуальны;
КонецФункции 

Функция ТребуетсяОбновлениеДрайвера(ИдентификаторОборудования) Экспорт
	Перем ТекущаяВерсия;
	
	// Не полноправный пользователь или разделенный режим - проверку не проводим
	Если НЕ ПолучениеВнешнихКомпонент.ДоступнаИнтерактивнаяЗагрузкаВнешнихКомпонент() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторОборудования", ИдентификаторОборудования);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОборудованиеПоОрганизациям.ВерсияПроверкиДрайверов КАК ПровереннаяВерсия
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|ГДЕ
	|	ОборудованиеПоОрганизациям.Оборудование = &ИдентификаторОборудования";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПровереннаяВерсия = "";
	Если Выборка.Следующий() Тогда
		ПровереннаяВерсия = Выборка.ПровереннаяВерсия;
	КонецЕсли;
	
	ДрайверАктуален = ДрайверАктуален(ПровереннаяВерсия, ТекущаяВерсия, ИдентификаторОборудования);
	Если ДрайверАктуален И ПровереннаяВерсия <> ТекущаяВерсия Тогда
		РегистрыСведений.ОборудованиеПоОрганизациям.УстановитьВерсиюПроверкиДрайверов(ИдентификаторОборудования, ТекущаяВерсия);
	КонецЕсли;
	
	Возврат НЕ ДрайверАктуален;
КонецФункции

Функция ДрайверАктуален(ВерсияПроверки, ТекущаяВерсия, ИдентификаторОборудования = Неопределено)
	ТекущаяВерсия = ОбновлениеИнформационнойБазы.ВерсияИБ("БиблиотекаПодключаемогоОборудования");
	Если ЗначениеЗаполнено(ВерсияПроверки) И НЕ ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ТекущаяВерсия, ВерсияПроверки) > 0 Тогда
		// мы уже проверяли в этой версии
		Возврат Истина;
	КонецЕсли;
	
	Возврат МенеджерОборудования.ПроверитьОбновление(ИдентификаторОборудования).Количество() = 0;
КонецФункции

Функция ОблачнаяКассаДляАвтоматическогоПробитияЧеков(Организация) Экспорт
	Если НЕ ОблачныеКассы.ПробитиеЧековДоступно() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
	Отбор.Организация = Организация;
	Отбор.ТипыПО.ОблачнаяККТ = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОборудования", ОбщегоНазначения.ВыгрузитьКолонку(МенеджерОборудования.СписокОборудования(Отбор), "Ссылка"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОборудованиеПоОрганизациям.Оборудование КАК Оборудование
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|ГДЕ
	|	ОборудованиеПоОрганизациям.Оборудование В(&СписокОборудования)
	|	И ОборудованиеПоОрганизациям.ИспользоватьДляСБП";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка.Следующий();
	Возврат Выборка.Оборудование;
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьРеквизитыККТ(ЭтаФорма, Объект)
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ТипБулево = Новый ОписаниеТипов("Булево");
	ТипЧисло = Новый ОписаниеТипов("Число");
	ТипСтрока = Новый ОписаниеТипов("ФорматированнаяСтрока");
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Отправляет1СSMS", ТипЧисло));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Отправляет1СEmail", ТипЧисло));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Отправляет1СSMS_Надпись", ТипСтрока));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Отправляет1СEmail_Надпись", ТипСтрока));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ЗаполнятьКонтактныеДанные", ТипБулево, ,НСтр("ru = 'Заполнять электронную почту и телефон из данных контрагента'")));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("РасчетыВИнтернет", ТипБулево, ,НСтр("ru = 'Расчеты через интернет'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("АдресСайта", ОбщегоНазначения.ОписаниеТипаСтрока(100), ,НСтр("ru = 'Адрес сайта'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИспользоватьДляСБП", ТипБулево, ,НСтр("ru = 'Использовать для пробития чеков при оплате по СБП'")));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПечататьЧек", ТипБулево, ,НСтр("ru = 'Печатать чек'")));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОтправлятьEMail", ТипБулево, ,НСтр("ru = 'Отправить email'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОтправлятьSMS",   ТипБулево, ,НСтр("ru = 'Отправить SMS'")));
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ЭтаФорма.Отправляет1СEmail_Надпись = Новый ФорматированнаяСтрока("Настроить",,,,"НастроитьEmail");
	ЭтаФорма.Отправляет1СSMS_Надпись   = Новый ФорматированнаяСтрока("Настроить",,,,"НастроитьSMS");
	
	Элементы = ЭтаФорма.Элементы;
	
	ГруппаНастройкиККТ = Элементы.Добавить("ГруппаНастройкиККТ", Тип("ГруппаФормы"));
	ГруппаНастройкиККТ.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаНастройкиККТ.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаНастройкиККТ.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаНастройкиККТ.ОтображатьЗаголовок = Ложь;
	ГруппаНастройкиККТ.Видимость = ЭтоКассовоеОборудование(Объект.ТипОборудования);
	
	ГруппаОсновное = Элементы.Найти("Основное");
	Если ГруппаОсновное <> Неопределено Тогда
		Элементы.Переместить(ГруппаНастройкиККТ, ГруппаОсновное);
	КонецЕсли; 
	
	// ГруппаРасчетыЧерезИнтернет
	ГруппаРасчетыЧерезИнтернет = Элементы.Добавить("ГруппаРасчетыЧерезИнтернет", Тип("ГруппаФормы"), ГруппаНастройкиККТ);
	ГруппаРасчетыЧерезИнтернет.Заголовок = НСтр("ru = 'Настройки пробития чеков'");
	ГруппаРасчетыЧерезИнтернет.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаРасчетыЧерезИнтернет.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаРасчетыЧерезИнтернет.ОтображатьЗаголовок = Ложь;
	ГруппаРасчетыЧерезИнтернет.СквозноеВыравнивание = СквозноеВыравнивание.Использовать;
	
	Элемент = Элементы.Добавить("РасчетыВИнтернет", Тип("ПолеФормы"), ГруппаРасчетыЧерезИнтернет);
	Элемент.ПутьКДанным = "РасчетыВИнтернет";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	Элемент.Подсказка = Нстр("ru = 'Включите, если по кассе могут пробиваться чеки по оплатам, принятым через интернет'");
	Элемент.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииЭлементаФормы");
	
	Элемент = Элементы.Добавить("АдресСайта", Тип("ПолеФормы"), ГруппаРасчетыЧерезИнтернет);
	Элемент.ПутьКДанным = "АдресСайта";
	Элемент.Вид = ВидПоляФормы.ПолеВвода; 
	Элемент.ПодсказкаВвода = НСтр("ru = 'URL сайта на котором происходит оплата'");
	
	ГруппаОтправлятьЧеки = Элементы.Добавить("ГруппаОтправлятьЧеки", Тип("ГруппаФормы"), ГруппаНастройкиККТ);
	ГруппаОтправлятьЧеки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОтправлятьЧеки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаОтправлятьЧеки.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ГруппаОтправлятьЧеки.Заголовок = НСтр("ru = 'Отправка электронных чеков'");
	
	ГруппаНастройкиОтправки = Элементы.Добавить("ГруппаНастройкиОтправкиЧеки", Тип("ГруппаФормы"), ГруппаОтправлятьЧеки);
	ГруппаНастройкиОтправки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаНастройкиОтправки.ОтображатьЗаголовок = Ложь;
	ГруппаНастройкиОтправки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	НоваяГруппа(ЭтаФорма, "Отправляет1СEmail", НСтр("ru = 'Отправка по электронной почте'"), ГруппаНастройкиОтправки);
	НоваяГруппа(ЭтаФорма, "Отправляет1СSMS",   НСтр("ru = 'Отправка по SMS'"),               ГруппаНастройкиОтправки);
	
	Элемент = Элементы.Добавить("ЗаполнятьКонтактныеДанные", Тип("ПолеФормы"), ГруппаОтправлятьЧеки);
	Элемент.ПутьКДанным = "ЗаполнятьКонтактныеДанные";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.Вид = ВидПоляФормы.ПолеФлажка; 
	
	Элемент.Подсказка = НСТр("ru = 'Автоматически заполнять номер телефона или электронный адрес из данных контрагента при подготовке чека.");
	Если Объект.ТипОборудования <> Перечисления.ТипыПодключаемогоОборудования.ККТ Тогда
		Элемент.Подсказка = Элемент.Подсказка + Символы.ПС + НСТр("ru = 'Установите этот флаг, если планируете печатать чеки на удержание из зарплаты на облачной кассе'");
	КонецЕсли;
	
	Элемент.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
	Элемент = Элементы.Добавить("ИспользоватьДляСБП", Тип("ПолеФормы"), ГруппаНастройкиККТ);
	Элемент.ПутьКДанным = "ИспользоватьДляСБП";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	Элемент.Видимость = Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ;
	
	Элементы.Добавить("Разделитель3", Тип("ДекорацияФормы"), ГруппаНастройкиККТ);
	
	ГруппаЗначенияПоУмолчанию = Элементы.Добавить("ГруппаЗначенияПоУмолчанию", Тип("ГруппаФормы"), ГруппаНастройкиККТ);
	ГруппаЗначенияПоУмолчанию.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаЗначенияПоУмолчанию.Заголовок = НСтр("ru = 'Настройки печати чека по умолчанию'");
	ГруппаЗначенияПоУмолчанию.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ГруппаЗначенияПоУмолчанию.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	
	ГруппаПечататьЧек = Элементы.Добавить("ГруппаПечататьЧек", Тип("ГруппаФормы"), ГруппаЗначенияПоУмолчанию);
	ГруппаПечататьЧек.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПечататьЧек.ОтображатьЗаголовок = Ложь;
	ГруппаПечататьЧек.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаПечататьЧек.Видимость = Объект.ТипОборудования <> Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ;
	
	Элемент = Элементы.Добавить("ПечататьЧек", Тип("ПолеФормы"), ГруппаПечататьЧек);
	Элемент.ПутьКДанным = "ПечататьЧек";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	
	Элемент = Элементы.Добавить("Разделитель1", Тип("ДекорацияФормы"), ГруппаПечататьЧек);
	Элемент.Ширина = 3;
	
	Элемент = Элементы.Добавить("ОтправлятьEMail", Тип("ПолеФормы"), ГруппаЗначенияПоУмолчанию);
	Элемент.ПутьКДанным = "ОтправлятьEMail";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	
	Элемент = Элементы.Добавить("Разделитель2", Тип("ДекорацияФормы"), ГруппаЗначенияПоУмолчанию);
	Элемент.Ширина = 3;
	
	Элемент = Элементы.Добавить("ОтправлятьSMS", Тип("ПолеФормы"), ГруппаЗначенияПоУмолчанию);
	Элемент.ПутьКДанным = "ОтправлятьSMS";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	
	
	// по умолчанию для новых объектов ПечататьЧек = Истина
	ЭтаФорма.ПечататьЧек = Истина;
	
КонецПроцедуры

Процедура ДобавитьРеквизитыЭТ(ЭтаФорма, Объект)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОплатуСертификатамиНСПК") Тогда
		Возврат;
	КонецЕсли; 
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ТипСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255));
	
	// Общие параметры
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("АдресСервисаНСПК",      ТипСтрока, , "Адрес сервиса ФЭС НСПК"));
	// зависит от конкретного оборудования (драйвера)
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПротоколОбменаФЭСНСПК", Новый ОписаниеТипов("ПеречислениеСсылка.ПротоколОбменаФЭСНСПК"), , "Протокол обмена с сервисом"));
	
	// Ключ доступа (в целом на организацию)
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КлючОрганизацииНСПК", ТипСтрока, , "Ключ организации (API-KEY)"));
	
	// Параметры для каждой торговой точки
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИдентификаторНСПК",   ТипСтрока, , "Идентификатор кассы (ID кассы)"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КлючКассыНСПК",       ТипСтрока, , "Ключ кассы (MAC-KEY)"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПроверятьКорневыеСертификаты", Новый ОписаниеТипов("Булево"), , "Проверять корневые сертификаты"));
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	СтраницаОсновное = ЭтаФорма.Элементы.Найти("Основное");

	ГруппаСертификатыНСПК = ЭтаФорма.Элементы.Добавить("ГруппаЭлектронныеСертификатыНСПК", Тип("ГруппаФормы"), СтраницаОсновное);
	ГруппаСертификатыНСПК.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСертификатыНСПК.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаСертификатыНСПК.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ГруппаСертификатыНСПК.Заголовок = НСтр("ru = 'Электронные сертификаты на социально значимые товары (491-ФЗ)'");
	ГруппаСертификатыНСПК.Видимость = (Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал);
	
	Элемент = ЭтаФорма.Элементы.Добавить("АдресСервисаНСПК", Тип("ПолеФормы"), ГруппаСертификатыНСПК);
	Элемент.ПутьКДанным = "АдресСервисаНСПК";
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	
	Элемент = ЭтаФорма.Элементы.Добавить("ПротоколОбменаФЭСНСПК", Тип("ПолеФормы"), ГруппаСертификатыНСПК);
	Элемент.ПутьКДанным = "ПротоколОбменаФЭСНСПК";
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	
	Элемент = ЭтаФорма.Элементы.Добавить("КлючОрганизацииНСПК", Тип("ПолеФормы"), ГруппаСертификатыНСПК);
	Элемент.ПутьКДанным = "КлючОрганизацииНСПК";
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.РежимПароля = НЕ Пользователи.ЭтоПолноправныйПользователь();
	
	ГруппаПараметрыПодключения = ЭтаФорма.Элементы.Добавить("ГруппаПараметрыПодключения", Тип("ГруппаФормы"), ГруппаСертификатыНСПК);
	ГруппаПараметрыПодключения.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПараметрыПодключения.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаПараметрыПодключения.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	ГруппаПараметрыПодключения.Заголовок = НСтр("ru = 'Параметры подключения к кассе'");
	
	Элемент = ЭтаФорма.Элементы.Добавить("ИдентификаторНСПК", Тип("ПолеФормы"), ГруппаПараметрыПодключения);
	Элемент.ПутьКДанным = "ИдентификаторНСПК";
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.РежимПароля = НЕ Пользователи.ЭтоПолноправныйПользователь();
	
	Элемент = ЭтаФорма.Элементы.Добавить("КлючКассыНСПК", Тип("ПолеФормы"), ГруппаПараметрыПодключения);
	Элемент.ПутьКДанным = "КлючКассыНСПК";
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.РежимПароля = НЕ Пользователи.ЭтоПолноправныйПользователь();
	
	Элемент = ЭтаФорма.Элементы.Добавить("ПроверятьКорневыеСертификаты", Тип("ПолеФормы"), ГруппаПараметрыПодключения);
	Элемент.ПутьКДанным = "ПроверятьКорневыеСертификаты";
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	Элемент.Подсказка = НСтр("ru = 'Проверять наличие корневого сертификата НУЦ при обращении к серверам НСПК (рекомендуется)'");
	Элемент.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
КонецПроцедуры

Функция ИмяРеквизита()
	УИД = СтрЗаменить(Новый УникальныйИдентификатор, "-", "");

	Возврат СтрШаблон("Элемент_%1", УИД);
КонецФункции 

Процедура НоваяГруппа(ЭтаФорма, ПутьКДанным, Заголовок, Родитель)
	Группа = ЭтаФорма.Элементы.Добавить(ИмяРеквизита(), Тип("ГруппаФормы"), Родитель);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	Группа.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСверху;
	Группа.Подсказка = Заголовок;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НовыйПереключатель(ЭтаФорма, Группа, ПутьКДанным).СписокВыбора.Добавить(0, НСТр("ru = 'Через ОФД'"));
	
	ГруппаНастройка = ЭтаФорма.Элементы.Добавить(ИмяРеквизита(), Тип("ГруппаФормы"), Группа);
	ГруппаНастройка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаНастройка.ОтображатьЗаголовок = Ложь;
	ГруппаНастройка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	НовыйПереключатель(ЭтаФорма, ГруппаНастройка, ПутьКДанным).СписокВыбора.Добавить(1, НСТр("ru = 'Из программы'"));
	
	НоваяДекорация(ЭтаФорма, ГруппаНастройка);
	
	ПолеНадписи = ЭтаФорма.Элементы.Добавить(ИмяРеквизита(), Тип("ПолеФормы"), ГруппаНастройка);
	ПолеНадписи.Вид = ВидПоляФормы.ПолеНадписи;
	ПолеНадписи.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеНадписи.ПутьКДанным = СтрШаблон("%1_%2", ПутьКДанным, "Надпись");

КонецПроцедуры 

Процедура НоваяДекорация(ЭтаФорма, Родитель)

	ГруппаНастройка = ЭтаФорма.Элементы.Добавить(ИмяРеквизита(), Тип("ДекорацияФормы"), Родитель);

КонецПроцедуры 

Функция НовыйПереключатель(ЭтаФорма, Группа, ПутьКДанным)
	ИмяРеквизита = ИмяРеквизита();
	
	Элемент = ЭтаФорма.Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Группа);
	Элемент.ПутьКДанным = ПутьКДанным;
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Элемент.Вид = ВидПоляФормы.ПолеПереключателя;
	
	Возврат Элемент
КонецФункции 

#Область РаботаСПерсональнымиДанными


Функция ПокупательГражданство(ФизическоеЛицо, НаДату = Неопределено)
	
	ПокупательГражданство = Справочники.СтраныМира.ПустаяСсылка();
	НаДату = ?(НаДату = Неопределено, ТекущаяДатаСеанса(), НаДату);
	
	КодыМВД = Новый Массив;
	КодыМВД.Добавить("10");
	КодыМВД.Добавить("21");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыДокументовФизическихЛиц.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВидыДокументовФизическихЛиц
	|ИЗ
	|	Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|ГДЕ
	|	ВидыДокументовФизическихЛиц.КодМВД В(&КодыМВД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыФизическихЛицСрезПоследних.СтранаВыдачи КАК Страна
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(&ДатаСреза, Физлицо = &ФизЛицо) КАК ДокументыФизическихЛицСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|		ПО ДокументыФизическихЛицСрезПоследних.ВидДокумента = ВидыДокументовФизическихЛиц.Ссылка";
	Запрос.УстановитьПараметр("ДатаСреза", КонецДня(НаДату));
	Запрос.УстановитьПараметр("ФизЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("КодыМВД", КодыМВД);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПокупательГражданство = Выборка.Страна;
	КонецЕсли;
	
	Возврат ПокупательГражданство;
	
КонецФункции

Функция ПокупательДатаРождения(ФизическоеЛицо)
	
	Возврат СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "ДатаРождения"));
	
КонецФункции

Функция ПокупательИНН(ФизическоеЛицо)
	
	Возврат СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "ИНН"));
	
КонецФункции

Функция ПокупательПаспортныеДанные(ФизическоеЛицо, НаДату = Неопределено)
	
	ПокупательПерсональныеДанные = "";
	НаДату = ?(НаДату = Неопределено, ТекущаяДатаСеанса(), НаДату);
	
	КодыМВД = Новый Массив;
	КодыМВД.Добавить("10");
	КодыМВД.Добавить("21");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыДокументовФизическихЛиц.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВидыДокументовФизическихЛиц
	|ИЗ
	|	Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|ГДЕ
	|	ВидыДокументовФизическихЛиц.КодМВД В(&КодыМВД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыФизическихЛицСрезПоследних.Период КАК Период,
	|	ДокументыФизическихЛицСрезПоследних.Физлицо КАК Физлицо,
	|	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокумента,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
	|	ДокументыФизическихЛицСрезПоследних.СрокДействия КАК СрокДействия,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан,
	|	ДокументыФизическихЛицСрезПоследних.КодПодразделения КАК КодПодразделения,
	|	ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность КАК ЯвляетсяДокументомУдостоверяющимЛичность,
	|	ДокументыФизическихЛицСрезПоследних.Представление КАК Представление,
	|	ДокументыФизическихЛицСрезПоследних.УдалитьВидДокумента КАК УдалитьВидДокумента,
	|	ДокументыФизическихЛицСрезПоследних.ФамилияЛатиницей КАК ФамилияЛатиницей,
	|	ДокументыФизическихЛицСрезПоследних.ИмяЛатиницей КАК ИмяЛатиницей,
	|	ДокументыФизическихЛицСрезПоследних.СтранаВыдачи КАК СтранаВыдачи
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(&ДатаСреза, Физлицо = &ФизЛицо) КАК ДокументыФизическихЛицСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|		ПО ДокументыФизическихЛицСрезПоследних.ВидДокумента = ВидыДокументовФизическихЛиц.Ссылка";
	Запрос.УстановитьПараметр("ДатаСреза", КонецДня(НаДату));
	Запрос.УстановитьПараметр("ФизЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("КодыМВД", КодыМВД);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПокупательПерсональныеДанные = СокрЛП(Выборка.Серия) + " " + СокрЛП(Выборка.Номер) + " " + СокрЛП(Выборка.КемВыдан)
			+ " " + Формат(Выборка.ДатаВыдачи, "ДФ=dd.MM.yyyy") + " " + Формат(Выборка.СрокДействия, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
	Возврат ПокупательПерсональныеДанные;
	
КонецФункции

#КонецОбласти 

Процедура ДобавитьОбщиеРеквизиты(ЭтаФорма, ТекущийОбъект) 
	Элементы = ЭтаФорма.Элементы;

	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИспользуетсяНесколькоОрганизаций", Новый ОписаниеТипов("Булево")));
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет") Тогда
		ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		Возврат;
	КонецЕсли;
	
	ТипСклад = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("СправочникСсылка.Склады")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Склад", ТипСклад, , "Склад"));
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ЭлементСклад = Элементы.Добавить("Склад", Тип("ПолеФормы"));
	
	ЭлементСклад.ПутьКДанным            = "Склад";
	ЭлементСклад.Вид                    = ВидПоляФормы.ПолеВвода;
	ЭлементСклад.АвтоМаксимальнаяШирина = Ложь;
	
	ЭлементОрганизация = Элементы.Найти("Организация");
	Если ЭлементОрганизация <> Неопределено Тогда
		Элементы.Переместить(ЭлементСклад,       ЭлементОрганизация.Родитель, ЭлементОрганизация);
		Элементы.Переместить(ЭлементОрганизация, ЭлементОрганизация.Родитель, ЭлементСклад);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 