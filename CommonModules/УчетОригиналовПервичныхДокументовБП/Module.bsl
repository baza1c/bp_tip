#Область СлужебныйПрограммныйИнтерфейс

Функция ИдентификаторОтслеживатьОригинал() Экспорт
	Возврат "ОтслеживатьОригинал";
КонецФункции

Процедура ЗаполнитьТаблицуУчетаОригиналов(ТаблицаУчетаОригиналов) Экспорт
	
	Для Каждого ТипОбъекта Из Метаданные.ОпределяемыеТипы.ОбъектСУчетомОригиналовПервичныхДокументов.Тип.Типы() Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипОбъекта);
		Если МетаданныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Метаданные.Документы.Содержит(МетаданныеОбъекта)
			Или Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
			ДобавитьФормыВТаблицуОригиналов(МетаданныеОбъекта, ТаблицаУчетаОригиналов);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ИзменитьСостояниеОригиналовДокументовПриИзмененииСтатуса(ДокументСсылка, НовыйСтатус) Экспорт
	
	// Поддерживаем изменение только для статусов документов реализации и документов поступления.
	// Если будут передаваться другие статусы, то ничего не делаем.
	Если ТипЗнч(НовыйСтатус) <> Тип("ПеречислениеСсылка.СтатусыДокументовРеализации")
		И ТипЗнч(НовыйСтатус) <> Тип("ПеречислениеСсылка.СтатусыДокументовПоступления") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не УчетОригиналовПервичныхДокументов.ЭтоОбъектУчета(ДокументСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	// Меняем состояние оригинала только в том случае, если статус уже установлен.
	ТекущееСостояниеОригиналаПоСсылке = УчетОригиналовПервичныхДокументов.СведенияОСостоянииОригиналаПоСсылке(
		ДокументСсылка);
	ТекущееСостояниеОригинала = Неопределено;
	Если ЗначениеЗаполнено(ТекущееСостояниеОригиналаПоСсылке) Тогда
		ТекущееСостояниеОригинала = ТекущееСостояниеОригиналаПоСсылке.СостояниеОригиналаПервичногоДокумента;
	КонецЕсли;
	
	НовоеСостояниеОригинала = Неопределено;
	
	СостояниеОригиналовДокументПолучен = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияОригиналовПервичныхДокументов.ОригиналПолучен");
	Если НовыйСтатус = Перечисления.СтатусыДокументовРеализации.Подписан
		Или НовыйСтатус = Перечисления.СтатусыДокументовПоступления.ОригиналПолучен Тогда
		НовоеСостояниеОригинала = СостояниеОригиналовДокументПолучен;
	Иначе
		Если ЗначениеЗаполнено(ТекущееСостояниеОригинала) Тогда
			Если ТекущееСостояниеОригинала = СостояниеОригиналовДокументПолучен Тогда
				// В состояниях оргиналов указано, что документы уже получены.
				// Поэтому нужно поменять это состояние на "Форма напечатана"
				НовоеСостояниеОригинала = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияОригиналовПервичныхДокументов.ФормаНапечатана");
			Иначе
				// В состояниях оргиналов указано, что документы не получены.
				// Поэтому сохраним текущее состояние оригиналов.
				НовоеСостояниеОригинала = ТекущееСостояниеОригинала;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовоеСостояниеОригинала)
		Или НовоеСостояниеОригинала = ТекущееСостояниеОригинала Тогда
		Возврат;
	КонецЕсли;
	
	Изменен = УчетОригиналовПервичныхДокументов.УстановитьНовоеСостояниеОригинала(
		ДокументСсылка, НовоеСостояниеОригинала);
	
КонецПроцедуры

Процедура УстановитьСостояниеДокументаПоСостояниюОригинала(ДокументСсылка, СостояниеОригинала) Экспорт
	
	Если НЕ Метаданные.РегистрыСведений.СтатусыДокументов.Измерения.Документ.Тип.СодержитТип(ТипЗнч(ДокументСсылка)) Тогда
		Возврат;
	КонецЕсли;
	
	ОригиналПолучен = СостояниеОригинала = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияОригиналовПервичныхДокументов.ОригиналПолучен");
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка);
	НовыйСтатус = МенеджерДокумента.СтатусДокументаПеречислением(ОригиналПолучен);
	
	СуществующийСтатус = РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокумента(ДокументСсылка, Неопределено).Статус;
	
	Если СуществующийСтатус = НовыйСтатус Тогда
		Возврат;
	КонецЕсли;
	
	СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
	СтатусыДокумента.Статус = НовыйСтатус;
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ДокументСсылка, СтатусыДокумента);
	
КонецПроцедуры

// Обработчик события "ПриСозданииНаСервере" формы документа.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//   * Объект - ДанныеФормыСтруктура, ДокументОбъект - основной реквизит формы.
Процедура ПриСозданииНаСервере_ФормаДокумента(Форма, СтатусДокументаБП) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетОригиналовПервичныхДокументов") = Ложь
		Или Не ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СостоянияОригиналовПервичныхДокументов) Тогда
		
		ИмяДекорацииСостоянияОригиналовДокументов = УчетОригиналовПервичныхДокументовБПКлиентСервер.ИмяДекорацииСостояния();
		ОтключаемаяДекорация = Форма.Элементы.Найти(ИмяДекорацииСостоянияОригиналовДокументов);
		Если Не ОтключаемаяДекорация = Неопределено Тогда
			ОтключаемаяДекорация.Видимость = Ложь;
		КонецЕсли;
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьДекорациюСостоянияОригиналовПервичныхДокументов(Форма, Форма.Объект.Ссылка, СтатусДокументаБП);
	
КонецПроцедуры

Функция СвойстваДекорацииСостояния(ДокументСсылка, СтатусДокументаБП) Экспорт
	
	// Если статус документа в механизме БП установлен в Истина, то уже не важно, что указано в механизме БСП
	// Мы все равно будем выводить "Получено".
	// Это связано с тем, что в форме можно установит флаг "Оригинал получен", и в этом случае нужно показывать
	// ссылку с надписью "Оригинал получен", даже если состояние отличается, т.к. при записи будет приведено состояние
	// оригиналов из БСП к статусу "Оригинал получен".
	Если СтатусДокументаБП Тогда
		Возврат СвойстваДекорацииСостоянияПолучено();
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(ДокументСсылка) Тогда
		Возврат СвойстваДекорацииСостоянияНеИзвестно();
	КонецЕсли;
	
	Возврат СвойстваДекорацииСостоянияПоДокументу(ДокументСсылка);
	
КонецФункции

Функция ТипыДокументовСОригиналамиПервичныхДокументов() Экспорт
	
		// В журнале показываем только документы, для которых могут быть оригиналы первичных документов.
	ТипыДокументовСОригиналамиПервичныхДокументов = Новый Массив;
	
	Для Каждого ТипОригинала Из Метаданные.ОпределяемыеТипы.ОбъектСУчетомОригиналовПервичныхДокументов.Тип.Типы() Цикл
		Если ТипОригинала = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
			Продолжить;
		КонецЕсли;
		ТипыДокументовСОригиналамиПервичныхДокументов.Добавить(ТипОригинала);
	КонецЦикла;
	
	Возврат ТипыДокументовСОригиналамиПервичныхДокументов;
	
КонецФункции

Функция ТипыДокументовСОригиналамиПервичныхДокументовПокупателей() Экспорт
	
	ТипыДокументовСОригиналамиПервичныхДокументовПокупателей = Новый Массив;
	ТипыДокументовСОригиналамиПервичныхДокументовПокупателей.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	ТипыДокументовСОригиналамиПервичныхДокументовПокупателей.Добавить(Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг"));
	Возврат ТипыДокументовСОригиналамиПервичныхДокументовПокупателей;
	
КонецФункции

Функция ТипыДокументовСОригиналамиПервичныхДокументовПоставщиков() Экспорт
	
	ТипыДокументовСОригиналамиПервичныхДокументовВсе = ТипыДокументовСОригиналамиПервичныхДокументов();
	ТипыДокументовСОригиналамиПервичныхДокументовПокупателей = ТипыДокументовСОригиналамиПервичныхДокументовПокупателей();
	ТипыДокументовСОригиналамиПервичныхДокументовПоставщиков = Новый Массив;
	
	Для Каждого ТипДокумента Из ТипыДокументовСОригиналамиПервичныхДокументовВсе Цикл
		Если ТипыДокументовСОригиналамиПервичныхДокументовПокупателей.Найти(ТипДокумента) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ТипыДокументовСОригиналамиПервичныхДокументовПоставщиков.Добавить(ТипДокумента);
	КонецЦикла;
	
	Возврат ТипыДокументовСОригиналамиПервичныхДокументовПоставщиков;
	
КонецФункции

Функция НомерДатаДокументаДляОтчета(НомерДок, ДатаДок) Экспорт
	
	ЧастиПредставления = Новый Массив;
	Если ЗначениеЗаполнено(НомерДок) Тогда
		ЧастиПредставления.Добавить(СокрЛП(НомерДок));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НомерДок) И ЗначениеЗаполнено(ДатаДок) Тогда
		ЧастиПредставления.Добавить(" ");
		ЧастиПредставления.Добавить("от");
		ЧастиПредставления.Добавить(" ");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаДок) Тогда
		ЧастиПредставления.Добавить(Формат(ДатаДок, "ДЛФ=D")); 
	КонецЕсли;
	
	Возврат СтрСоединить(ЧастиПредставления);
	
КонецФункции

Функция ДополнитьОбъектыПечатиИМакетыПриЗаписиСостоянийОригиналовПослеПечати(ОбъектыПечати, СписокПечати) Экспорт
	
	Если СредиМакетовЕстьУПД(СписокПечати) Тогда
		// Среди макетов есть макет для УПД.
		// Он может печататься из Реализации, но в качестве объекта печати у него - Счет-фактура.
		// Кроме этого, идентификатор печатной формы макета зависит от версии УПД. Подробнее см. УчетНДС.ТаблицаМакетовУПД()
		// Поэтому, чтобы при печати УПД из реализации устанавливался признак печати у УПД,
		// нужно добавить в объекты печати все реализации для счета-фактуры, а также в СписокПечати - имя макета из команд печати.
		Если СписокПечати.НайтиПоЗначению("УниверсальныйПередаточныйДокумент") = Неопределено Тогда
			СписокПечати.Добавить("УниверсальныйПередаточныйДокумент", НСтр("ru = 'Универсальный передаточный документ (УПД)'"));
		КонецЕсли;
		
		СписокДляДобавления = Новый СписокЗначений;
		Для Каждого ЭлементСписка Из ОбъектыПечати Цикл
			Если ЗначениеЗаполнено(ЭлементСписка.Значение)
				И ТипЗнч(ЭлементСписка.Значение) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
				СписокДляДобавления.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление, ЭлементСписка.Пометка, ЭлементСписка.Картинка);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(СписокДляДобавления) Тогда
			ОснованияСчетовФактур = Документы.СчетФактураВыданный.ОснованияСчетовФактур(СписокДляДобавления.ВыгрузитьЗначения());
			
			Для Каждого ЭлементСписка Из СписокДляДобавления Цикл
				
				Если ОснованияСчетовФактур.Получить(ЭлементСписка.Значение) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ОснованияСчетаФактуры = ОснованияСчетовФактур.Получить(ЭлементСписка.Значение);
				Для Каждого ДокументОснование Из ОснованияСчетаФактуры Цикл
					ОбъектыПечати.Добавить(ДокументОснование, ЭлементСписка.Представление, ЭлементСписка.Пометка, ЭлементСписка.Картинка);
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

Процедура ДополнитьСписокПечатиПриОпределенииСпискаПечатныхФорм(ОбъектыПечати, СписокПечати) Экспорт
	
	МетаданныеСоответствие = Новый Соответствие();
	Для Каждого ОбъектПечати Из ОбъектыПечати Цикл
		МетаданныеСоответствие.Вставить(ОбъектПечати.Значение.Метаданные());
	КонецЦикла;
	
	Для Каждого КлючЗначение Из МетаданныеСоответствие Цикл
		 ДополнитьСписокПечатиПриОпределенииСпискаПечатныхФормОбъектаМетаданных(КлючЗначение.Ключ, СписокПечати);
	КонецЦикла;
	
КонецПроцедуры

#Область ОбработчикиОбновления

Процедура УстановитьПредставленияПредопределенныхЭлементовСостояний() Экспорт
	
	ФормаНапечатана = Справочники.СостоянияОригиналовПервичныхДокументов.ФормаНапечатана;
	ФормаНапечатанаОбъект = ФормаНапечатана.ПолучитьОбъект();
	ФормаНапечатанаОбъект.Наименование = ТекстНадписиСостояниеОригиналовДокументаФормаНапечтана();
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ФормаНапечатанаОбъект);
	
КонецПроцедуры

Функция ЕстьОжидаемыеОригиналыДокументов(ДокументСсылка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДокументСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("СостояниеПолучено", Справочники.СостоянияОригиналовПервичныхДокументов.ОригиналПолучен);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
	|	СостоянияОригиналовПервичныхДокументов.Состояние КАК СостояниеОригинала
	|ИЗ
	|	РегистрСведений.СостоянияОригиналовПервичныхДокументов КАК СостоянияОригиналовПервичныхДокументов
	|ГДЕ
	|	СостоянияОригиналовПервичныхДокументов.Владелец = &Ссылка
	|	И НЕ СостоянияОригиналовПервичныхДокументов.ОбщееСостояние
	|	И СостоянияОригиналовПервичныхДокументов.Состояние <> &СостояниеПолучено";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьФормыВТаблицуОригиналов(МетаданныеОбъекта, ТаблицаУчетаОригиналов)
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеОбъекта.ПолноеИмя());
	
	ДобавляемыеКомандыПечати = УправлениеПечатью.СоздатьКоллекциюКомандПечати();
	Если УправлениеПечатью.НастройкиПечатиОбъекта(МенеджерОбъекта).ПриДобавленииКомандПечати Тогда
		МенеджерОбъекта.ДобавитьКомандыПечати(ДобавляемыеКомандыПечати);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДобавляемыеКомандыПечати) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОтслеживатьОригинал = ИдентификаторОтслеживатьОригинал();
	
	ЕстьКомандыПечатиДляОригиналов = Ложь;
	Для Каждого КомандаПечати Из ДобавляемыеКомандыПечати Цикл
		
		Если Не ЗначениеЗаполнено(КомандаПечати.ДополнительныеПараметры)
			Или Не КомандаПечати.ДополнительныеПараметры.Свойство(ИдентификаторОтслеживатьОригинал)
			Или КомандаПечати.ДополнительныеПараметры[ИдентификаторОтслеживатьОригинал] <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаУчетаОригиналов.Добавить();
		НоваяСтрока.ОбъектМетаданных = МетаданныеОбъекта;
		НоваяСтрока.Идентификатор = КомандаПечати.Идентификатор;
		
		ЕстьКомандыПечатиДляОригиналов = Истина;
		
	КонецЦикла;
	
	Если Не ЕстьКомандыПечатиДляОригиналов Тогда
		// В том случае, если не добавить в таблицу ТаблицаУчетаОригиналов ни одной строки,
		// то механизм БСП добавит в список отслеживаемых печатных форм все возможные формы.
		// Нам такое поведение не подходит, поэтому добавим одну строку с несуществующей командой.
		// В этом случае список отслеживаемых форм будет пуст.
		НоваяСтрока = ТаблицаУчетаОригиналов.Добавить();
		НоваяСтрока.ОбъектМетаданных = МетаданныеОбъекта;
		НоваяСтрока.Идентификатор = ИдентификаторНесуществующейКомандыПечати();
	КонецЕсли;
	
КонецПроцедуры

Функция ИдентификаторНесуществующейКомандыПечати()
	
	Возврат "НесуществующаяКомандаПечатиДляОригиналовДокументов";
	
КонецФункции

Функция ТекстНадписиСостояниеОригиналовДокументаНеизвестно()
	
	Возврат НСтр("ru='<Состояние оригинала неизвестно>'");
	
КонецФункции

Функция ТекстНадписиСостояниеОригиналовДокументаФормаНапечтана()
	
	Возврат НСтр("ru='Ожидается получение'");
	
КонецФункции

Функция ТекстНадписиСостояниеОригиналовДокументаОригиналПолучен()
	
	Возврат НСтр("ru='Оригинал получен'");
	
КонецФункции

Процедура ЗаполнитьДекорациюСостоянияОригиналовПервичныхДокументов(Форма, ДокументСсылка, СтатусДокументаБП)
	
	ИмяДекорацииСостоянияОригиналовДокументов = УчетОригиналовПервичныхДокументовБПКлиентСервер.ИмяДекорацииСостояния();
	ДекорацияСостояниеОригинала = Форма.Элементы.Найти(ИмяДекорацииСостоянияОригиналовДокументов);
	Если ДекорацияСостояниеОригинала = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваДекорации = СвойстваДекорацииСостояния(ДокументСсылка, СтатусДокументаБП);
	ЗаполнитьЗначенияСвойств(ДекорацияСостояниеОригинала, СвойстваДекорации);
	
КонецПроцедуры

Функция НовыйСвойстваДекорацииСостояния()
	
	СвойстваДекорации = Новый Структура;
	СвойстваДекорации.Вставить("Заголовок", "");
	Возврат СвойстваДекорации;
	
КонецФункции

Функция СвойстваДекорацииСостоянияПолучено()
	
	СвойстваДекорации = НовыйСвойстваДекорацииСостояния();
	
	СостояниеОригиналаПолучено = ОбщегоНазначения.ПредопределенныйЭлемент(
		"Справочник.СостоянияОригиналовПервичныхДокументов.ОригиналПолучен");
	Если ЗначениеЗаполнено(СостояниеОригиналаПолучено) Тогда
		ТекстСостояниеОригинала = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СостояниеОригиналаПолучено, "Наименование");
	Иначе
		ТекстСостояниеОригинала = ТекстНадписиСостояниеОригиналовДокументаОригиналПолучен();
	КонецЕсли;
	СвойстваДекорации.Заголовок = ТекстСостояниеОригинала;
	
	Возврат СвойстваДекорации;
	
КонецФункции

Функция СвойстваДекорацииСостоянияНапечатано()
	
	СвойстваДекорации = НовыйСвойстваДекорацииСостояния();
	
	СостояниеОригиналаПолучено = ОбщегоНазначения.ПредопределенныйЭлемент(
		"Справочник.СостоянияОригиналовПервичныхДокументов.ФормаНапечатана");
	Если ЗначениеЗаполнено(СостояниеОригиналаПолучено) Тогда
		ТекстСостояниеОригинала = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СостояниеОригиналаПолучено, "Наименование");
	Иначе
		ТекстСостояниеОригинала = ТекстНадписиСостояниеОригиналовДокументаФормаНапечтана();
	КонецЕсли;
	СвойстваДекорации.Заголовок = ТекстСостояниеОригинала;
	
	Возврат СвойстваДекорации;
	
КонецФункции

Функция СвойстваДекорацииСостоянияНеИзвестно()
	СвойстваДекорации = НовыйСвойстваДекорацииСостояния();
	СвойстваДекорации.Заголовок = ТекстНадписиСостояниеОригиналовДокументаНеизвестно();
	Возврат СвойстваДекорации;
КонецФункции

Функция СвойстваДекорацииСостоянияПоДокументу(ДокументСсылка)
	
	// Этот статус мы будем получать в том случае, если СтатусДокументаБП = Ложь.
	// При этом может быть так, что общий статус БСП может быть "Оригинал получен".
	// Например, если в форме был записан статус, что все оригиналы получены, а потом снята галка "Оригинал получен",
	// но документ не записан. Состояние БП и БСП статусов будут синхронизированы при записи, но до записи нужно
	// ориентироватся на флаг. Если флаг не установлен, и статус БСП - "Оригинал получен", то нужно вывести текст
	// "Документ напечатан", т.к. именно он будет установлен при записи.
	
	ТекущиеСостояниеОригинала = УчетОригиналовПервичныхДокументов.СведенияОСостоянииОригиналаПоСсылке(
		ДокументСсылка);
	
	Если ТекущиеСостояниеОригинала.Количество() = 0 Тогда
		Возврат СвойстваДекорацииСостоянияНеИзвестно();
	КонецЕсли;
	
	Если ТекущиеСостояниеОригинала.СостояниеОригиналаПервичногоДокумента = 
		ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияОригиналовПервичныхДокументов.ОригиналПолучен") Тогда
		Возврат СвойстваДекорацииСостоянияНапечатано();
	КонецЕсли;
	
	СвойстваДекорации = НовыйСвойстваДекорацииСостояния();
	СвойстваДекорации.Заголовок = ТекущиеСостояниеОригинала.СостояниеОригиналаПервичногоДокумента;
	Возврат СвойстваДекорации;
	
КонецФункции

Функция СредиМакетовЕстьУПД(СписокПечати)
	
	ТаблицаМакетовУПД = УчетНДС.ТаблицаМакетовУПД();
	Для Каждого ЭлементСписка Из СписокПечати Цикл
		Если ТаблицаМакетовУПД.Найти(ЭлементСписка.Значение, "ИДМакета") <> Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ДополнитьСписокПечатиПриОпределенииСпискаПечатныхФормОбъектаМетаданных(ОбъектМетаданных, СписокПечати)
	
	Если ОбъектМетаданных = Метаданные.Документы.ПоступлениеТоваровУслуг Тогда
		СписокПечати.Добавить("ТОРГ12", НСтр("ru = 'Товарная накладная (ТОРГ-12)'"));
		СписокПечати.Добавить("Акт", НСтр("ru = 'Акт об оказании услуг'"));
		СписокПечати.Добавить("УниверсальныйПередаточныйДокумент", НСтр("ru = 'Универсальный передаточный документ (УПД)'"));
		СписокПечати.Добавить("ТранспортнаяНакладная", НСтр("ru = 'Транспортная накладная'"));
	ИначеЕсли ОбъектМетаданных = Метаданные.Документы.ПоступлениеДопРасходов Тогда
		СписокПечати.Добавить("Акт", НСтр("ru = 'Акт об оказании услуг'"));
		СписокПечати.Добавить("УниверсальныйПередаточныйДокумент", НСтр("ru = 'Универсальный передаточный документ (УПД)'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

