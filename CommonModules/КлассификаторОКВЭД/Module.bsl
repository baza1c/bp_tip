///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ОКВЭД".
// ОбщийМодуль.КлассификаторОКВЭД.
//
// Серверные процедуры получения информации из ОКВЭД:
//  - поиск значений по указанному коду/кодам;
//  - подбор возможных значений по определенным параметрам;
//  - получение списка кодов с разделами;
//  - начальное заполнение классификатора;
//  - взаимодействие с подсистемой РаботаСКлассификаторами
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет поиск в классификаторе значения по указанному коду и всех его родительских значений. Результат будет
// упорядочен по уровню иерархии от самого нижнего к самому верхнему. Поиск выполняется по точному соответствию.
// Если не найдено значений с указанным кодом:
//   * Иерархия = Ложь - будет возвращен пустой массив.
//   * Иерархия = Истина - будут возвращены вышестоящие элементы, если такие найдены. Определение вышестоящих элементов
//     выполняется разбором переданного кода.
//
// Параметры:
//  Код - Строка - код, который необходимо найти.
//  Иерархия - Булево - Истина, если нужно дополнить результат вышестоящими элементами.
//
// Возвращаемое значение:
//  Массив из Структура - найденные элементы, либо пустой массив:
//   * Раздел - Строка - раздел классификатора, к которому относится найденное значение.
//   * Код - Строка - код по классификатору.
//   * Наименование - Строка - текстовое описание выбранного значения.
//   * Представление - Строка - включает код и наименование.
//   * Описание - Строка - описание элемента.
//
Функция ЗначениеПоКоду(Знач Код, Иерархия) Экспорт
	
	Код = СокрЛП(Код);
	
	КодыДляПоиска = Новый Массив;
	
	Если Иерархия Тогда
		КодыДляПоиска = КодВИерархии(Код);
	Иначе
		КодыДляПоиска.Добавить(Код);
	КонецЕсли;
	
	Результат = ЗначенияПоКодам(КодыДляПоиска, Истина);
	
	Возврат Результат;
	
КонецФункции

// Получает информацию о соответствии кода (первых 2 цифр - класса) и буквенного кода раздела ОКВЭД.
//
// Возвращаемое значение:
//   Соответствие Из КлючИЗначение - разделы классификатора:
//     * Ключ - Строка - Первые 2 цифры числового кода из классификатора ОКВЭД (класс).
//     * Значение - Строка - Буквенное обозначение раздела, в который входит указанный класс.
//
Функция РазделыКодов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеОКВЭД.Раздел КАК Раздел,
	|	ПОДСТРОКА(ДанныеОКВЭД.Код, 0, 2) КАК Код
	|ИЗ
	|	РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭД";
	
	Результат = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Код, Выборка.Раздел);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выполняет подбор данных из классификатора по указанному отбору.
// Все значения отборов складываются логическим оператором "И".
// Для формирования структуры отбора по умолчанию рекомендуется использовать КлассификаторОКВЭД.НовыйОтбор.
//
// Параметры:
//  Отбор - Структура - ограничения, накладываемые на выбираемые данные. См. КлассификаторОКВЭД.НовыйОтбор.
//
// Возвращаемое значение:
//  Массив из Структура - результат подбора:
//   * Раздел - Строка - раздел классификатора, к которому относится найденное значение.
//   * Код - Строка - код по классификатору.
//   * Наименование - Строка - текстовое описание выбранного значения.
//   * Представление - Строка - включает код и наименование.
//   * Описание - Строка - описание элемента.
//
Функция Подбор(Отбор) Экспорт
	
	Запрос = Новый Запрос;
	
	ПравильныйОтбор = НовыйОтбор();
	ЗаполнитьЗначенияСвойств(ПравильныйОтбор, Отбор);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	ВременнаяТаблицаПодборОКВЭД(Запрос, ПравильныйОтбор);
	
	Если ПравильныйОтбор.БезОписания Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВТДанныеОКВЭД.Код КАК Код,
		|	ВТДанныеОКВЭД.Наименование КАК Наименование,
		|	ВТДанныеОКВЭД.Раздел КАК Раздел,
		|	ВТДанныеОКВЭД.КодБезТочек КАК КодБезТочек,
		|	"""" КАК Описание
		|ИЗ
		|	ВТДанныеОКВЭД КАК ВТДанныеОКВЭД
		|
		|УПОРЯДОЧИТЬ ПО
		|	Раздел,
		|	Код";
	Иначе
		// Не индексируем временную таблицу поскольку это практически не влияет на производительность запроса.
		// В регистре чуть больше 3000 записей, маловероятно значительное увеличение. По рекомендациям из
		// https://its.1c.ru/db/pubqlang/content/147/hdoc "на маленьких таблицах (порядка нескольких тысяч записей
		// и меньше) СУБД практически всегда использует сканирование таблицы либо сканирование кластерного
		// индекса... Так что нет смысла дополнительно индексировать заведомо небольшие таблицы".
		// Кроме того, по рекомендациям из статьи https://its.1c.ru/db/v8std/content/658/hdoc
		// при ЛЕВОМ соединении рекомендуется индексировать правую таблицу, а это сам регистр, в котором требуемое
		// поле уже проиндексировано.
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВТДанныеОКВЭД.Код КАК Код,
		|	ВТДанныеОКВЭД.Наименование КАК Наименование,
		|	ВТДанныеОКВЭД.Раздел КАК Раздел,
		|	ВТДанныеОКВЭД.КодБезТочек КАК КодБезТочек,
		|	ЕСТЬNULL(ДанныеОКВЭДОписания.Описание, """") КАК Описание
		|ИЗ
		|	ВТДанныеОКВЭД КАК ВТДанныеОКВЭД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭДОписания
		|		ПО ДанныеОКВЭДОписания.Код = ВТДанныеОКВЭД.Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	Раздел,
		|	Код";
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Массив;
	
	Если ПравильныйОтбор.Разрешенные Тогда
		Пока Выборка.Следующий() Цикл
			Если Не КлассификаторОКВЭДСлужебныйКлиентСервер.КодРазрешенДляВыбора(Выборка.КодБезТочек) Тогда
				Продолжить;
			КонецЕсли;
			ДобавитьВыборкуВРезультатПодбора(Результат, Выборка);
		КонецЦикла;
		
	Иначе
		Пока Выборка.Следующий() Цикл
			ДобавитьВыборкуВРезультатПодбора(Результат, Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует структуру, содержащую элементы, необходимые для функции КлассификаторОКВЭД.Подбор
//
// Возвращаемое значение:
//  Структура - параметры отбора, которые необходимо использовать в процедуре КлассификаторОКВЭД.Подбор:
//    * СтрокаПоиска - Строка - текст, который необходимо найти. Если не заполнена, то выводятся все данные с учетом
//      дополнительных отборов. Поиск выполняется по любому вхождению искомой строки в данные классификатора.
//    * Режим - Строка, Неопределено - Вариант поиска информации:
//        "Код" - поиск строки поиска выполняется только по измерению Код и реквизиту КодБезТочек;
//        "Наименование" - поиск строки поиска выполняется только по ресурсу Наименование;
//        "Описание" - поиск строки поиска выполняется по реквизиту Описание;
//        "КодИНаименование" - поиск строки поиска выполняется по измерению Код, ресурсу Наименование и реквизиту
//          КодБезТочек;
//        "КодНаименованиеОписание" - поиск строки поиска выполняется по измерению Код, ресурсу Наименование и
//          реквизитам КодБезТочек и Описание;
//        "Раздел" - поиск строки поиска выполняется только по ресурсу Раздел;
//        "" или Неопределено - поиск выполняется по всем измерениям и ресурсам, кроме Описания
//          (для обратной совмести).
//    * Разрешенные - Булево - Выбирать только значения, длина кода которых больше или равна 4, без учета точек.
//    * Раздел - Строка - дополнительный отбор по разделу. Поиск выполняется только в данных, относящихся к указанному
//      в этом параметре разделу.
//    * Код - Строка - дополнительный отбор по коду. Поиск выполняется только в данных, код которых начинается с
//      указанного в этом параметре.
//    * Наименование - Строка - дополнительный отбор по наименованию. Поиск выполняется только в данных, наименование
//      которых содержит указанную в этом параметре строку.
//    * БезОписания - Булево - если Истина, то в поле Описание всегда будет передаваться пустая строка, для уменьшения
//      объема передаваемых данных. Значение по умолчанию - Ложь.
//
Функция НовыйОтбор() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("СтрокаПоиска", "");
	Результат.Вставить("Режим", Неопределено);
	Результат.Вставить("Разрешенные", Ложь);
	Результат.Вставить("Раздел", "");
	Результат.Вставить("Код", "");
	Результат.Вставить("Наименование", "");
	Результат.Вставить("БезОписания", Ложь);

	Возврат Результат;

КонецФункции

// Выполняет начальное заполнение пустого регистра или его обновление при переходе на новую версию.
// Метод не предназначен для вызова из разделенного сеанса при работе в модели сервиса.
// В случае вызова из разделенного сеанса в модели сервиса будет вызвано исключение.
//
Процедура НачальноеЗаполнениеКлассификатора() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ВызватьИсключение НСтр("ru = 'Использование метода КлассификаторОКВЭД.НачальноеЗаполнениеКлассификатора запрещено в разделенном сеансе.'");
	КонецЕсли;
	
	РегистрыСведений.ДанныеОКВЭД.НачальноеЗаполнение();
	
КонецПроцедуры

// Выполняет обновление классификатора ОКВЭД.
// Если в базе заполнена версия загруженного классификатора, но при этом регистр пустой,
// то выполняется обнуление информации о загруженной версии.
// В остальных случаях просто запускается процесс обновления классификатора.
// При работе в модели сервиса данные классификатора получаются из кэша.
// Если файл классификатора не был загружен из Менеджера сервиса, метод вернет ошибку.
//
// Возвращаемое значение:
//  см. РаботаСКлассификаторами.ОбновитьКлассификаторы
//
Функция ОбновитьКлассификатор() Экспорт
	
	ИдентификаторВСервисе = КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Код
	|ИЗ
	|	РегистрСведений.ДанныеОКВЭД";
	
	РегистрПустой = Запрос.Выполнить().Пустой();
	
	ЗагруженнаяВерсия = РаботаСКлассификаторами.ВерсияКлассификатора(
		ИдентификаторВСервисе);
	ЕстьДанныеОЗагрузке = (ЗагруженнаяВерсия <> Неопределено И ЗагруженнаяВерсия > 0);
	
	Если ЕстьДанныеОЗагрузке И РегистрПустой Тогда
		РаботаСКлассификаторами.УстановитьВерсиюКлассификатора(
			ИдентификаторВСервисе,
			0);
		РаботаСКлассификаторами.УстановитьДатуОбновленияКлассификатора(
			ИдентификаторВСервисе,
			Дата(1, 1, 1));
	КонецЕсли;
	
	Идентификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		ИдентификаторВСервисе);
	
	Возврат РаботаСКлассификаторами.ОбновитьКлассификаторы(Идентификаторы);
	
КонецФункции

// Формирует описание всех разделов ОКВЭД
// 
// Возвращаемое значение:
//  см. НовыйТаблицаРазделов
//
Функция Разделы() Экспорт
	
	ТаблицаРазделов = НовыйТаблицаРазделов();
	
	// Локализация не требуется, поскольку классификатор относится к российскому законодательству
	// (ОКВЭД - Общероссийский классификатор видов экономической деятельности)
	
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"A",
		НСтр("ru = 'СЕЛЬСКОЕ, ЛЕСНОЕ ХОЗЯЙСТВО, ОХОТА, РЫБОЛОВСТВО И РЫБОВОДСТВО'"),
		НСтр("ru = 'Этот раздел включает:
		|- использование растительных и животных природных ресурсов, включая выращивание зерновых, содержание и разведение животных;
		|- получение древесины и других растений, животных или продуктов животного происхождения на ферме или в естественной среде обитания'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		НСтр("ru = 'B'"),
		НСтр("ru = 'ДОБЫЧА ПОЛЕЗНЫХ ИСКОПАЕМЫХ'"),
		НСтр("ru = 'Данный раздел включает:
		|- добычу полезных ископаемых, встречающихся в природе в виде твердых пород (уголь и руда), в жидком состоянии (нефть) или в газообразном состоянии (природный газ)
		|Добыча может осуществляться различными методами, такими как подземная или открытая разработка месторождений, бурение скважин, разработка морского дна и т. д.
		|Данный раздел также включает:
		|- дополнительные виды деятельности с целью подготовки сырья к реализации: дробление, измельчение, очистка, просушка, сортировка, обогащение руды, обогащение угля, сжижение природного газа и агломерация твердого топлива
		|Перечисленные виды работ обычно выполняются хозяйствующими субъектами, которые сами занимаются добычей полезных ископаемых и/или расположены в районе добычи полезных ископаемых. Добыча полезных ископаемых классифицируется в группировках по виду основного добываемого минерального сырья
		|Группировки 05 и 06 включают:
		|- добычу топливно-энергетических полезных ископаемых (каменного угля, бурого угля (лигнита), нефти, газа);
		|Группировки 07 и 08 включают:
		|- добычу металлических руд, различных минералов и нерудных полезных ископаемых
		|Некоторые технологические процессы, относящиеся к данному разделу, в частности процессы, связанные с добычей углеводородов, могут также осуществляться специализированными предприятиями по заказу третьих сторон в качестве производственных услуг, описанных в группировке 09
		|Этот раздел не включает:
		|- переработку извлеченных/добытых полезных ископаемых, см. раздел C (Обрабатывающие производства);
		|- использование извлеченных материалов без дальнейшей обработки в строительных целях, см. раздел F (Строительство);
		|- розлив в бутылки ключевой и минеральной воды из источников и скважин, см. 11.07;
		|- дробление, измельчение и другие виды обработки некоторых грунтов, горных пород и минералов, не связанные с операциями по добыче полезных ископаемых, см. 23.9'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"C",
		НСтр("ru = 'ОБРАБАТЫВАЮЩИЕ ПРОИЗВОДСТВА'"),
		НСтр("ru = 'Этот раздел включает:
		|- физическую и/или химическую обработку материалов, веществ или компонентов с целью их преобразования в новые продукты, хотя это нельзя использовать как единый универсальный критерий для определения производства (см. ниже ""переработка отходов"")
		|Материалы, вещества или преобразованные компоненты являются сырьем, т. е. продуктами сельского хозяйства, лесного хозяйства, рыболовства, горных пород и минералов и продуктов других обрабатывающих производств. Считается, что значимые периодические изменения, обновление или преобразование продуктов относятся к производству.
		|Произведенная продукция может быть готовой к потреблению или может представлять полуфабрикат для последующей обработки. Например, продукт очистки алюминия используется как сырье для первичного производства алюминиевых изделий, например алюминиевой проволоки, которая в свою очередь будет использована в необходимых конструкциях; производства техники и оборудования, для которого данные запасные части и аксессуары предназначены. Производство неспециализированных компонентов и деталей машин и оборудования, например двигателей, поршней, электромоторов, клапанов, шестеренок, подшипников, классифицируется в соответствующей группировке раздела C ""Обрабатывающие производства"", независимо от того, в состав каких машин и оборудования могут входить эти предметы. Однако производство специализированных компонентов и аксессуаров посредством отливки/формовки или штамповки пластиковых материалов включает в себя группировка 22.2. Сборка комплектующих частей и деталей также отнесена к производству. Данный раздел включает сборку целостных конструкций из составляющих компонентов, произведенных самостоятельно или приобретенных. Переработка отходов, т. е. переработка отходов для производства вторичного сырья вошла в группировку 38.3 (деятельность по обработке вторичного сырья). Хотя может производиться физическая и химическая переработка, это не считается частью обрабатывающего производства. Первичной целью данных видов деятельности является основная переработка или переработка отходов, которая классифицирована в разделе E (водоснабжение; канализация, организация сбора и утилизации отходов, деятельность по ликвидации загрязнений). Однако производство новых готовых изделий (в противовес продукции, произведенной из вторичного сырья) относится ко всему производству в целом, даже если в данных процессах использованы отходы. Например, производство серебра из отходов пленки считается производственным процессом. Специальное техобслуживание и ремонт промышленных, коммерческих и подобных машин и оборудования в целом указаны в группировке 33 (ремонт и установка машин и оборудования). Однако ремонт компьютеров, бытовых устройств указан в группировке 95 (ремонт компьютеров, предметов личного пользования и хозяйственно-бытового назначения), в то же самое время ремонт автомобилей описывается в группировке 45 (оптовая и розничная торговля и ремонт автотранспортных средств и мотоциклов). Установка техники и оборудования как узкоспециализированная деятельность классифицирована в группировке 33.20
		|Примечание - Границы обрабатывающего производства с другими разделами данного классификатора могут не иметь четкой однозначной спецификации. Как правило, обрабатывающие производства включают переработку материалов для производства новой продукции. Обычно это совершенно новая продукция. Однако определение того, что представляет собой новая продукция, может быть несколько субъективным
		|Переработка подразумевает следующие виды деятельности, задействованные в производстве и определенные в данном классификаторе:
		|- переработка свежей рыбы (извлечение устриц из раковин, филетирование рыбы), выполняемые не на борту рыболовецкого судна, см. 10.20;
		|- пастеризация молока и розлив по бутылкам, см. 10.51;
		|- выделка кожи, см. 15.11;
		|- распиловка и строгание древесины; пропитка древесины, см. 16.10;
		|- печать и родственные ей виды деятельности, см. 18.1;
		|- восстановление протектора шин, см. 22.11;
		|- производство готовых к применению бетонных смесей, см. 23.63;
		|- гальванопокрытие, металлизация и тепловая обработка металла, см. 25.61;
		|- механическое оборудование для ремонта или переборки (например, двигателей автомобилей), см. 29.10
		|Также существуют виды деятельности, включенные в процесс переработки, которые отражены в других разделах классификатора, т. е. они не классифицируются как обрабатывающие производства.
		|Они включают:
		|- лесозаготовки, классифицированные в разделе A (СЕЛЬСКОЕ, ЛЕСНОЕ ХОЗЯЙСТВО, ОХОТА, РЫБОЛОВСТВО И РЫБОВОДСТВО);
		|- модификацию сельскохозяйственной продукции, классифицированную в разделе A;
		|- подготовку пищевых продуктов для немедленного потребления в помещениях, классифицированную в группировке 56 (деятельность предприятий общественного питания и баров);
		|- обогащение руды и прочих минералов, классифицированную в разделе B (ДОБЫЧА ПОЛЕЗНЫХ ИСКОПАЕМЫХ);
		|- строительные и сборочные работы, выполняемые на строительных площадках, классифицированные в разделе F (СТРОИТЕЛЬСТВО);
		|- деятельность по разбивке крупных партий товаров на мелкие группы и вторичный сбыт более мелких партий, включая упаковку, переупаковку или розлив в бутылки такой продукции, как алкогольные напитки или химикаты;
		|- сортировку твердых отходов;
		|- смешивание красок по заказу клиента;
		|- резку металлов по заказу клиента;
		|- пояснения к различным товарам, отнесенные к разделу G (ТОРГОВЛЯ ОПТОВАЯ И РОЗНИЧНАЯ; РЕМОНТ АВТОТРАНСПОРТНЫХ СРЕДСТВ И МОТОЦИКЛОВ)'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"D",
		НСтр("ru = 'ОБЕСПЕЧЕНИЕ ЭЛЕКТРИЧЕСКОЙ ЭНЕРГИЕЙ, ГАЗОМ И ПАРОМ; КОНДИЦИОНИРОВАНИЕ ВОЗДУХА'"),
		НСтр("ru = 'Этот раздел включает:
		|- обеспечение электрической и тепловой энергией, природным газом, паром, горячей водой и т. п. через действующую инфраструктуру (сеть) распределительных линий проводов и трубопроводов
		|Параметры и протяженность электрической и тепловой сети не являются решающим фактором; в данный раздел также включено распределение электрической энергии, газа, тепла, горячей воды и т. п. в промышленных зонах или жилых зданиях. Поэтому данный раздел включает: виды деятельности предприятий, которые вырабатывают электрическую и тепловую энергию или газ, управляют распределением электроэнергии или газа
		|В него также включено:
		|- обеспечение подачи тепла и кондиционированного воздуха
		|Этот раздел не включает:
		|- работу систем водоснабжения и канализационной системы, см. 36, 37, транспортировку газа по газопроводу (обычно на далекие расстояния)'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"E",
		НСтр("ru = 'ВОДОСНАБЖЕНИЕ; ВОДООТВЕДЕНИЕ, ОРГАНИЗАЦИЯ СБОРА И УТИЛИЗАЦИИ ОТХОДОВ, ДЕЯТЕЛЬНОСТЬ ПО ЛИКВИДАЦИИ ЗАГРЯЗНЕНИЙ'"),
		НСтр("ru = 'Этот раздел включает:
		|- работу с отходами (включая их сбор, обработку и утилизацию), такими как твердые или нетвердые, промышленные или бытовые, а также с загрязненными участками земной поверхности
		|Продукт, полученный от переработки отходов или сточных вод, может быть утилизирован или использован в качестве сырья в других производственных процессах. Деятельность по водоснабжению также добавлена в данный раздел, так как водообеспечение осуществляется с помощью или посредством компонентов, также включенных в процесс переработки сточных вод'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"F",
		НСтр("ru = 'СТРОИТЕЛЬСТВО'"),
		НСтр("ru = 'Этот раздел включает:
		|- общее строительство и специальную строительную деятельность в части зданий и сооружений
		|В него включено строительство новых объектов, реконструкция, капитальный ремонт, текущий ремонт и дополнительные работы, монтаж готовых зданий или сооружений на строительном участке, включая строительство временных сооружений. В данном разделе также классифицирована деятельность по сохранению и воссозданию объектов культурного наследия
		|Общее строительство включает:
		|- строительство жилья, офисных зданий, складов и прочих общественных зданий, фермерских построек и т. д. или строительство сооружений, таких как автомобильные дороги и улицы, искусственные сооружения на них, объекты дорожного сервиса, железные дороги, взлетно-посадочные полосы аэродромов и космодромов, прочие водные объекты, ирригационные системы, системы водоснабжения и сетей водоотведения, промышленные предприятия, трубопроводы, линии связи и электропередачи, спортивные сооружения и т. д. Строительные работы могут быть выполнены за свой счет, за вознаграждение или на договорной основе. Часть работ или все работы могут передаваться в субподряд. В данный раздел включены работы, выполняемые строительными компаниями, которые несут полную ответственность за строительный проект. Этот раздел включает полное строительство зданий (группировка 41), выполнение полного объема строительных работ (группировка 42), а также специальную строительную деятельность, если она выполняется только как часть строительного процесса по субподряду (группировка 43). В него включена аренда строительного оборудования с оператором
		|Этот раздел также включает:
		|- осуществление проектов по строительству зданий или гражданских объектов с использованием финансовых, технических и физических ресурсов для их реализации с целью последующей продажи построенных зданий или объектов
		|Если данная деятельность осуществляется для эксплуатации построенных объектов, то все виды работ относятся к строительству
		|Этот раздел не включает:
		| - проведение научно-исследовательских работ (в области естественных и технических наук) по сохранению и воссозданию объектов культурного наследия и археологии, см. 72;
		| - разработку проектной документации, государственную экспертизу проектной документации и результатов инженерных изысканий, авторский и технический надзор, проведение изыскательских работ по сохранению и воссозданию объектов культурного наследия, см. 71'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"G",
		НСтр("ru = 'ТОРГОВЛЯ ОПТОВАЯ И РОЗНИЧНАЯ; РЕМОНТ АВТОТРАНСПОРТНЫХ СРЕДСТВ И МОТОЦИКЛОВ'"),
		НСтр("ru = 'Этот раздел включает:
		|- оптовую и розничную торговлю (т. е. продажу без преобразования) любого вида товаров, а также различные виды услуг, сопровождающие продажу товаров. Оптовая и розничная торговля являются конечными этапами распространения товаров. В эту группу включен также ремонт автомобилей и мотоциклов. Под продажей без преобразования принято понимать стандартные действия (операции), связанные с торговлей, такие как сортировка, классификация, упорядочивание товаров, смешивание (перемешивание) товаров (например, песка), розлив в бутылки (с предшествующим мытьем или без предшествующего мытья бутылок), упаковка, разделение оптовых партий, переупаковка в более мелкие партии для распространения, хранение (охлажденной или замороженной продукции)
		|Группировка 45 включает:
		|- все действия, связанные с продажей и ремонтом автомобилей и мотоциклов, в то время как группировки 46 и 47 включают все остальные действия, связанные с продажами
		|Основное различие между группировкой 46 (оптовая торговля) и группировкой 47 (розничная торговля) основываются на преобладающем типе покупателя
		|Оптовая торговля - это перепродажа (без преобразования) новых или бывших в употреблении товаров розничным продавцам, продажа юридическим лицам, таким как производственные, коммерческие, институционные или профессиональные пользователи, либо перепродажа другим оптовым торговцам, либо привлечение агентов или брокеров для покупки или продажи товаров
		|Основные типы компаний, включенных в группировку 46 - товарные оптовые торговцы, т. е. оптовые торговцы, приобретающие права на продаваемые товары, такие как товарные оптовики или посредники, распространители, работающие на производителей, экспортеры, импортеры и объединения покупателей, офисы продаж (но не розничные магазины), которые поддерживаются изготовителями или добывающими предприятиями, не являющимися их частями, в маркетинговых целях и не только; принимают заказы на прямые отгрузки с заводов или шахт. В нее также включены брокеры, агенты, комиссионеры, покупатели и объединения покупателей, связанные с рынком сельскохозяйственной продукции. Оптовые торговцы часто сами собирают, сортируют и приводят в надлежащий вид товары в больших партиях, разбивают крупные, переупаковывают в более мелкие партии (например, лекарства). Хранят, охлаждают, доставляют и устанавливают товары, стимулируют сбыт и создают торговые марки'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"H",
		НСтр("ru = 'ТРАНСПОРТИРОВКА И ХРАНЕНИЕ'"),
		НСтр("ru = 'Этот раздел включает:
		|- перевозку грузов и пассажиров, подчиняющуюся либо не подчиняющуюся расписанию по железной дороге, трубопроводам, автомобильной дороге, водным или воздушным транспортом, а также сопряженную с ней деятельность, такую как деятельность вокзалов и терминалов, стоянок для транспортных средств, обработку и хранение груза и т. д.
		|Этот раздел также включает:
		|- аренду транспортных средств с водителем или оператором
		|Этот раздел не включает:
		|- капитальный ремонт или обслуживание транспортных средств, кроме автомобильных, см. 33.1;
		|- строительство, содержание и ремонт автомобильных дорог, железнодорожных путей, портов, аэродромов, см. 42;
		|- техническое обслуживание и ремонт автотранспортных средств, см. 45.20;
		|- аренду транспортных средств без водителя либо оператора, см. 77.1, 77.3
		|Этот раздел также включает:
		|- деятельность почтовой связи'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"I",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ ГОСТИНИЦ И ПРЕДПРИЯТИЙ ОБЩЕСТВЕННОГО ПИТАНИЯ'"),
		НСтр("ru = 'Этот раздел включает:
		|- предоставление мест для краткосрочного проживания, а также предоставление полного ассортимента продуктов питания и напитков, пригодных для непосредственного потребления. Объем и тип дополнительных услуг, предоставляемых в рамках данного раздела, может значительно варьироваться
		|Этот раздел не включает:
		|- предоставление услуг долгосрочного проживания, так как это классифицируется в разделе, описывающем операции по недвижимости (раздел L);
		|- приготовление пищи или напитков, которые либо не являются пригодными для непосредственного потребления, либо продаются через независимые каналы распределения, т. е. при помощи оптовой или розничной торговли
		|Классификация приготовления данных продуктов питания приводится в разделе C (ОБРАБАТЫВАЮЩИЕ ПРОИЗВОДСТВА)'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"J",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ В ОБЛАСТИ ИНФОРМАЦИИ И СВЯЗИ'"),
		НСтр("ru = 'Этот раздел включает:
		|- производство и передачу материалов информационного и культурного назначения, предоставление средств передачи и размещения этих материалов, а также деятельность в области связи, информационных технологий и технологий обработки данных и прочую деятельность по предоставлению информационных услуг
		|Главными элементами этого раздела являются издательская деятельность, включая выпуск программного обеспечения (группировка 58), съемка кинофильмов и звукозапись (группировка 59), деятельность в области теле- и радиовещания (группировка 60), деятельность в области телекоммуникаций (группировка 61), информационные технологии (группировка 62) и прочие информационные услуги (группировка 63). Издательская деятельность включает приобретение авторских прав на содержание материала (информационных продуктов) и распространение данного содержания широкой общественности путем организации или участия в воспроизведении и распространении этого содержания в различных формах. Все возможные формы издания (включая печатную, электронную и звуковую форму, информационно-коммуникационную сеть Интернет, создание мультимедийных продуктов, например справочников на CD-ROM и т. д.) включены в данный раздел.
		|Деятельность, связанная с производством и распространением телерадиопрограмм, включена в группировки 59, 60 и 61, которые посвящены различным стадиям данного процесса. Отдельные элементы, такие как производство кинофильмов, телесериалов и т. д., отражены в группировке 59, тогда как производство готовых программ для телерадиоканалов из компонентов, произведенных в рамках группировки 59 или прочих компонентов (например, программы новостей в прямом эфире), включено в группировку 60 . Трансляция готовых телерадиопрограмм без любого изменения содержания включена в группировку 61. Такая трансляция в соответствии с описанием группировки 61 может осуществляться через системы наземного эфирного, спутникового, кабельного телерадиовещания, проводного радиовещания или с использованием информационно-коммуникационной сети Интернет'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"K",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ ФИНАНСОВАЯ И СТРАХОВАЯ'"),
		НСтр("ru = 'Этот раздел включает:
		|- финансовые услуги, включая страхование, перестрахование, пенсионное страхование, а также деятельность по предоставлению финансовых услуг
		|Этот раздел также включает:
		|- деятельность, связанную с финансовыми активами, деятельность холдинговых компаний, трастов, различного рода фондов и подобных им финансовых организаций'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"L",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ ПО ОПЕРАЦИЯМ С НЕДВИЖИМЫМ ИМУЩЕСТВОМ'"),
		НСтр("ru = 'Этот раздел включает:
		|- деятельность арендодателей, агентов или брокеров в одной или нескольких из следующих областей: покупка или продажа недвижимости, сдача внаем недвижимости, предоставление других услуг в сфере недвижимости, таких как оценивание, страхование или деятельность доверенных лиц
		|Деятельность по предоставлению посреднических услуг в этом разделе также может осуществляться с собственной или арендованной недвижимостью и может осуществляться за вознаграждение или на договорной основе
		|Этот раздел также включает:
		|- информацию о владельцах недвижимости'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"M",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ ПРОФЕССИОНАЛЬНАЯ, НАУЧНАЯ И ТЕХНИЧЕСКАЯ'"),
		НСтр("ru = 'Этот раздел включает:
		|- специализированную профессиональную, научную и техническую деятельность
		|Эта деятельность требует длительного обучения и предоставления специализированных знаний и навыков'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"N",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ АДМИНИСТРАТИВНАЯ И СОПУТСТВУЮЩИЕ ДОПОЛНИТЕЛЬНЫЕ УСЛУГИ'"),
		НСтр("ru = 'Этот раздел включает:
		|- различную деятельность для поддержки основной деятельности предприятий
		|Эта деятельность отличается от видов деятельности, перечисленных в разделе M, так как ее основной целью не является передача специализированных знаний'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"O",
		НСтр("ru = 'ГОСУДАРСТВЕННОЕ УПРАВЛЕНИЕ И ОБЕСПЕЧЕНИЕ ВОЕННОЙ БЕЗОПАСНОСТИ; СОЦИАЛЬНОЕ ОБЕСПЕЧЕНИЕ'"),
		НСтр("ru = 'Этот раздел включает:
		|- деятельность органов государственной власти и местного самоуправления
		|Он включает:
		|- принятие законов и соответствующее им регулирование таких сфер, как налогообложение, национальная оборона, общественный порядок и безопасность, миграция, международная деятельность, финансы, природные ресурсы и охрана окружающей природной среды, образование, наука, культура, здравоохранение, физическая культура и спорт, туризм. Юридический или институциональный статус не является определяющим при отнесении вида деятельности к данному разделу. Это значит, что виды деятельности, классифицированные в других разделах классификатора, не включаются в данный раздел, даже если они осуществляются органами государственной власти и местного самоуправления. Например, государственное регулирование деятельности в области образования включено в данный раздел, сам же процесс обучения в данный раздел не включен (см. раздел P). Государственное регулирование деятельности в области здравоохранения включено в данный раздел, в то время как деятельность лечебных учреждений в данный раздел не включена (см. раздел Q)
		|Этот раздел также включает:
		|- деятельность в сфере социального обеспечения'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"P",
		НСтр("ru = 'ОБРАЗОВАНИЕ'"),
		НСтр("ru = 'Этот раздел включает:
		|- образование как для школьников, так и для подготовки для разных профессий
		|В Российской Федерации устанавливаются следующие типы образовательных организаций, реализующих основные образовательные программы:
		|1) дошкольная образовательная организация - образовательная организация, осуществляющая в качестве основной цели ее деятельности образовательную деятельность по образовательным программам дошкольного образования, присмотр и уход за детьми;
		|2) общеобразовательная организация - образовательная организация, осуществляющая в качестве основной цели ее деятельности образовательную деятельность по образовательным программам начального общего, основного общего и (или) среднего общего образования;
		|3) профессиональная образовательная организация - образовательная организация, осуществляющая в качестве основной цели ее деятельности образовательную деятельность по образовательным программам среднего профессионального образования;
		|4) образовательная организация высшего образования - образовательная организация, осуществляющая в качестве основной цели ее деятельности образовательную деятельность по образовательным программам высшего образования и научную деятельность
		|В Российской Федерации устанавливаются следующие типы образовательных организаций, реализующих дополнительные образовательные программы
		|1) организация дополнительного образования - образовательная организация, осуществляющая в качестве основной цели ее деятельности образовательную деятельность по дополнительным общеобразовательным программам;
		|2) организация дополнительного профессионального образования - образовательная организация, осуществляющая в качестве основной цели ее деятельности образовательную деятельность по дополнительным профессиональным программам
		|Этот раздел включает:
		|- государственные, муниципальные, негосударственные (частные) образовательные организации всех видов
		|Система является многоуровневой: образование как для взрослых, так и для тех, кто осваивает азы грамотности. Может быть использована в образовательных организациях, реализующих военные профессиональные образовательные программы, в образовательных организациях, находящихся в ведении Федеральной службы исполнения наказаний
		|Для каждого уровня предусмотрены свои наборы программ
		|Отдельно включены занятия для учащихся с ограниченными возможностями здоровья
		|В Российской Федерации образование может быть получено:
		|- в организациях, осуществляющих образовательную деятельность;
		|- вне организаций, осуществляющих образовательную деятельность (в форме семейного образования и самообразования)
		|Обучение в организациях, осуществляющих образовательную деятельность, с учетом потребностей, возможностей личности и в зависимости от объема обязательных занятий педагогического работника с обучающимися осуществляется в очной, очно-заочной или заочной форме
		|Обучение в форме семейного образования и самообразования осуществляется с правом последующего прохождения промежуточной и государственной итоговой аттестации в организациях, осуществляющих образовательную деятельность
		|Допускается сочетание различных форм получения образования и форм обучения
		|Формы получения образования и формы обучения по основной образовательной программе по каждому уровню образования, профессии, специальности и направлению подготовки определяются соответствующими федеральными государственными образовательными стандартами, образовательными стандартами, если иное не установлено настоящим Федеральным законом от 29 декабря 2012 г. № 273-ФЗ ""Об образовании в Российской Федерации"". Формы обучения по дополнительным образовательным программам и основным программам профессионального обучения определяются организацией, осуществляющей образовательную деятельность, самостоятельно, если иное не установлено законодательством Российской Федерации
		|Каждый уровень образования включает деятельность специальных (коррекционных) образовательных организаций (классов, групп), обеспечивающих лечение, воспитание и обучение, социальную адаптацию и интеграцию в общество детей и подростков с ограниченными возможностями здоровья
		|В данной группировке классифицируется образование взрослых, по содержанию соответствующее определенным уровням общего образования
		|Этот раздел также включает:
		|- прочие виды образования и обучения, например обучение в школах водителей транспортных средств;
		|- обучение, в основном связанное со спортивной и оздоровительной деятельностью, такой как теннис или гольф, а также образовательной деятельностью, получаемое в общеобразовательных организациях (школе, школе-интернате, гимназии и др.), являющееся базой для среднего профессионального и высшего образования;
		|- среднее общее образование, получаемое одновременно (средние музыкальные, хореографические, художественные школы, школы искусств и т. п.)'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"Q",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ В ОБЛАСТИ ЗДРАВООХРАНЕНИЯ И СОЦИАЛЬНЫХ УСЛУГ'"),
		НСтр("ru = 'Этот раздел включает:
		|- предоставление деятельности в области здравоохранения и социальных услуг
		|Эта деятельность включает:
		|- широкий диапазон мероприятий - от медицинской помощи, которую обеспечивает обученный медицинский персонал в больницах и других организациях, а также мероприятий по уходу по месту жительства, которые включают некоторые мероприятия по охране здоровья человека, до социальных услуг без привлечения специалистов в области здравоохранения'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"R",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ В ОБЛАСТИ КУЛЬТУРЫ, СПОРТА, ОРГАНИЗАЦИИ ДОСУГА И РАЗВЛЕЧЕНИЙ'"),
		НСтр("ru = 'Данный раздел включает:
		|- широкий спектр видов деятельности в области культуры, развлечения, отдыха, включая живые выступления, работу выставок, азартные игры, спортивные мероприятия и мероприятия, связанные с отдыхом'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"S",
		НСтр("ru = 'ПРЕДОСТАВЛЕНИЕ ПРОЧИХ ВИДОВ УСЛУГ'"),
		НСтр("ru = 'Этот раздел включает:
		|- деятельность общественных объединений, ремонт и обслуживание вычислительной техники, предметов домашнего обихода и личных вещей, а также предоставление различного вида персональных услуг по обслуживанию населения, не включенные в другие группировки'"));
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"T",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ ДОМАШНИХ ХОЗЯЙСТВ КАК РАБОТОДАТЕЛЕЙ; НЕДИФФЕРЕНЦИРОВАННАЯ ДЕЯТЕЛЬНОСТЬ ЧАСТНЫХ ДОМАШНИХ ХОЗЯЙСТВ ПО ПРОИЗВОДСТВУ ТОВАРОВ И ОКАЗАНИЮ УСЛУГ ДЛЯ СОБСТВЕННОГО ПОТРЕБЛЕНИЯ'"),
		"");
	ДобавитьСтрокуВТаблицуРазделов(
		ТаблицаРазделов,
		"U",
		НСтр("ru = 'ДЕЯТЕЛЬНОСТЬ ЭКСТЕРРИТОРИАЛЬНЫХ ОРГАНИЗАЦИЙ И ОРГАНОВ'"),
		"");

	Возврат ТаблицаРазделов;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ИнтеграцияСБиблиотекойСтандартныхПодсистем

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "";
	Обработчик.Процедура = "КлассификаторОКВЭД.НачальноеЗаполнениеКлассификатора";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Комментарий = НСтр("ru='ОКВЭД. Начальное заполнение.'");
	Обработчик.РежимВыполнения = "Оперативно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.8.1.12";
	Обработчик.Процедура = "КлассификаторОКВЭД.НачальноеЗаполнениеКлассификатора";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Комментарий = НСтр("ru='ОКВЭД. Начальное заполнение.'");
	Обработчик.РежимВыполнения = "Оперативно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.8.4.17";
	Обработчик.Процедура = "КлассификаторОКВЭД.АнализКлассификатораПослеОбновления";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Комментарий =
		НСтр("ru='ОКВЭД. Проверка возможности загрузки классификатора из макета после обновлении версии БИП.'");
	Обработчик.РежимВыполнения = "Оперативно";
	
КонецПроцедуры

// Выполняет сравнение загруженной в базу версии классификатора и версию в макете.
// Если версия макета больше или равна загруженной, то данные обновляются из макета, чтобы загрузить данные,
// которые не использовались в предыдущих версиях библиотеки.
// Если версия в макете меньше загруженной, то выполняется замена номера загруженной версии для повторной загрузки
// по расписанию.
// 
Процедура АнализКлассификатораПослеОбновления() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		// Общие данные обновляются только в неразделенном сеансе.
		Возврат;
	КонецЕсли;
	
	Если Не ЗагрузитьИзМакета() Тогда
		
		ИдентификаторВСервисе = КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов();
		РаботаСКлассификаторами.УстановитьВерсиюКлассификатора(
			ИдентификаторВСервисе,
			ВерсияКлассификатораВМакете());
		РаботаСКлассификаторами.УстановитьДатуОбновленияКлассификатора(
			ИдентификаторВСервисе,
			ТекущаяДатаСеанса());

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияПодсистемИнтернетПоддержкиПользователей

// См. РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов.
//
Процедура ПриДобавленииКлассификаторов(Классификаторы) Экспорт
	
	Описатель = РаботаСКлассификаторами.ОписаниеКлассификатора();
	Описатель.Наименование           = НСтр("ru = 'Общероссийский классификатор видов экономической деятельности (ОКВЭД)'");
	Описатель.Идентификатор          = КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов();
	Описатель.ОбновлятьАвтоматически = Истина;
	Описатель.ОбщиеДанные            = Истина;
	
	Классификаторы.Добавить(Описатель);
	
КонецПроцедуры

// См. РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора.
//
Процедура ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан) Экспорт
	
	Если Идентификатор <> КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		ДвоичныеДанныеXML = ПолучитьИзВременногоХранилища(Адрес);
		ДанныеXML = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеXML);
		РезультатЧтенияXML = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ДанныеXML);
		
		УстановитьПривилегированныйРежим(Истина);
		
		НаборЗаписей = РегистрыСведений.ДанныеОКВЭД.СоздатьНаборЗаписей(); // АПК:1444 Полная загрузка данных.
		НаборЗаписей.Загрузить(РезультатЧтенияXML.Данные);
		НаборЗаписей.Записать();
		
		Обработан = Истина;
		
	Исключение
		
		Обработан = Ложь;
		
		ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина,
			РегистрыСведений.ДанныеОКВЭД);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Подбор

// Формирует временную таблицу с информацией о данных классификатора, удовлетворяющих параметрам отбора.
// Если в отборе указано, что данные регистра не должны выводиться, то создается пустая временная таблица.
// 
// Параметры:
//  Запрос - Запрос - объект Запрос, который используется для формирования результата.
//  Отбор - см. НовыйОтбор.
//  
Процедура ВременнаяТаблицаПодборОКВЭД(Запрос, Отбор)
	
	// Не индексируем временную таблицу. Обоснование - в функции Подбор
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|	ДанныеОКВЭД.Код КАК Код,
	|	ДанныеОКВЭД.Наименование КАК Наименование,
	|	ДанныеОКВЭД.Раздел КАК Раздел,
	|	ДанныеОКВЭД.КодБезТочек КАК КодБезТочек
	|ПОМЕСТИТЬ ВТДанныеОКВЭД
	|ИЗ
	|	РегистрСведений.ДанныеОКВЭД КАК ДанныеОКВЭД
	|ГДЕ
	|	&УсловиеОтбора";
	
	// Формируем дополнительные условия
	ДопУсловие = Новый Массив;

	Если ЗначениеЗаполнено(Отбор.Раздел) Тогда
		ДопУсловие.Добавить("Раздел = &Раздел");
		Запрос.УстановитьПараметр("Раздел", Отбор.Раздел);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отбор.Код) Тогда
		ДопУсловие.Добавить("Код ПОДОБНО &Код");
		Запрос.УстановитьПараметр("Код", Отбор.Код + "%");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отбор.Наименование) Тогда
		ДопУсловие.Добавить("Наименование ПОДОБНО &Наименование");
		Запрос.УстановитьПараметр("Наименование", "%" + Отбор.Наименование + "%");
	КонецЕсли;
	
	Если ДопУсловие.Количество() > 0 Тогда
		ДопУсловие = СтрСоединить(ДопУсловие, " И ");
	Иначе
		ДопУсловие = "Истина";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Отбор.СтрокаПоиска) Тогда
		ТекстЗапроса = СтрЗаменить(
			ШаблонЗапроса,
			"&УсловиеОтбора",
			ДопУсловие);
	Иначе
		// Если строка поиска указана, то определяем где искать
		Запрос.УстановитьПараметр("СтрокаПоиска", "%" + Отбор.СтрокаПоиска + "%");
		
		ЗапросыДляОбъединения = Новый Массив;
		
		РежимУказан = ЗначениеЗаполнено(Отбор.Режим);
		
		ДопУсловие = ?(ДопУсловие = "Истина", "", " И " + ДопУсловие);
		
		Если Не РежимУказан Или Отбор.Режим = "Раздел" Тогда
			ЗапросыДляОбъединения.Добавить(
				СтрЗаменить(
					ШаблонЗапроса,
					"&УсловиеОтбора",
					"Раздел = &СтрокаПоискаТочно
					|	" + ДопУсловие));
			Запрос.УстановитьПараметр("СтрокаПоискаТочно", Отбор.СтрокаПоиска);
		КонецЕсли;
		
		Если Не РежимУказан
			Или Отбор.Режим = "Код"
			Или Отбор.Режим = "КодИНаименование"
			Или Отбор.Режим = "КодНаименованиеОписание" Тогда
			ЗапросыДляОбъединения.Добавить(
				СтрЗаменить(
					ШаблонЗапроса,
					"&УсловиеОтбора",
					"Код ПОДОБНО &СтрокаПоиска
					|	" + ДопУсловие));
			ЗапросыДляОбъединения.Добавить(
				СтрЗаменить(
					ШаблонЗапроса,
					"&УсловиеОтбора",
					"КодБезТочек ПОДОБНО &СтрокаПоиска
					|	" + ДопУсловие));
		КонецЕсли;
		
		Если Не РежимУказан
			Или Отбор.Режим = "Наименование"
			Или Отбор.Режим = "КодИНаименование"
			Или Отбор.Режим = "КодНаименованиеОписание" Тогда
			ЗапросыДляОбъединения.Добавить(
				СтрЗаменить(
					ШаблонЗапроса,
					"&УсловиеОтбора",
					"Наименование ПОДОБНО &СтрокаПоиска
					|	" + ДопУсловие));
		КонецЕсли;
		
		// Поиск по описанию только при четком указании. Для обратной совместимости.
		Если Отбор.Режим = "Описание"
			Или Отбор.Режим = "КодНаименованиеОписание" Тогда
			ЗапросыДляОбъединения.Добавить(
				СтрЗаменить(
					ШаблонЗапроса,
					"&УсловиеОтбора",
					"Описание ПОДОБНО &СтрокаПоиска
					|	" + ДопУсловие));
		КонецЕсли;

		Если ЗапросыДляОбъединения.Количество() > 0 Тогда
			Для НомерЗапроса = 1 По ЗапросыДляОбъединения.ВГраница() Цикл
				ЗапросыДляОбъединения[НомерЗапроса] = 
					СтрЗаменить(
						ЗапросыДляОбъединения[НомерЗапроса],
						"ПОМЕСТИТЬ ВТДанныеОКВЭД",
						"");
			КонецЦикла;
		КонецЕсли;
		
		ТекстЗапроса = СтрСоединить(
			ЗапросыДляОбъединения,
			Символы.ПС + " ОБЪЕДИНИТЬ " + Символы.ПС);
			
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область Разделы

// Добавляет строку в таблицу с описанием разделов.
// 
// Параметры:
//  ТаблицаРазделов - см. НовыйТаблицаРазделов.
//  Раздел - Строка - код раздела.
//  Наименование - Строка - наименование раздела.
//  Описание - Строка - описание раздела.
// 
Процедура ДобавитьСтрокуВТаблицуРазделов(ТаблицаРазделов, Раздел, Наименование, Описание)
	
	НоваяСтрока = ТаблицаРазделов.Добавить();
	НоваяСтрока.Раздел = Раздел;
	НоваяСтрока.Наименование = Наименование;
	НоваяСтрока.Описание = Описание;
	
КонецПроцедуры

// Новый таблица разделов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый таблица разделов:
//    * Раздел - Строка - код раздела.
//    * Наименование - Строка - наименование раздела.
//    * Описание - Строка - описание раздела.
// 
Функция НовыйТаблицаРазделов()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить(
		"Раздел",
		ОбщегоНазначения.ОписаниеТипаСтрока(1));
	Результат.Колонки.Добавить(
		"Наименование",
		ОбщегоНазначения.ОписаниеТипаСтрока(500));
	Результат.Колонки.Добавить(
		"Описание",
		Новый ОписаниеТипов("Строка"));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеВерсииБИП

Функция ЗагрузитьИзМакета()
	
	Попытка
	
		ИдентификаторВСервисе = КлассификаторОКВЭДСлужебныйКлиентСервер.ИдентификаторВСервисеКлассификаторов();
	
		ЗагруженнаяВерсия = РегистрыСведений.ДанныеОКВЭД.ЗагруженнаяВерсия(ИдентификаторВСервисе); 
		
		ВерсияВМакете = ВерсияКлассификатораВМакете();
		
		Если ЗагруженнаяВерсия > ВерсияКлассификатораВМакете() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Макет = РегистрыСведений.ДанныеОКВЭД.ПолучитьМакет("ОКВЭД");
		РезультатЧтенияXML = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст());
		
		НаборЗаписей = РегистрыСведений.ДанныеОКВЭД.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(РезультатЧтенияXML.Данные);
		
		РегистрыСведений.ДанныеОКВЭД.ОбработатьНаборПередЗаписью(НаборЗаписей);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
		РаботаСКлассификаторами.УстановитьВерсиюКлассификатора(
			ИдентификаторВСервисе,
			ВерсияВМакете);
		РаботаСКлассификаторами.УстановитьДатуОбновленияКлассификатора(
			ИдентификаторВСервисе,
			ТекущаяДатаСеанса());
	
		Возврат Истина;
		
	Исключение
		
		ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить заполнение регистра сведений Классификатор ОКВЭД из макета:
					|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())),
			Истина,
			РегистрыСведений.ДанныеОКВЭД);
		
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

// Возвращает версию классификатора, распространяемую с текущей версией библиотеки.
//
// Возвращаемое значение:
//   Число - Версия классификатора в макете.
//
Функция ВерсияКлассификатораВМакете() Экспорт
	
	Возврат 8;
	
КонецФункции

// Возвращает данные регистра по переданному списку кодов. Результат сортируется по Разделу, затем по Коду.
//
// Параметры:
//  Коды - Массив из Строка - список кодов, данные по которым необходимо получить.
//  ПоУбыванию - Булево - Истина, если нужно сортировать элементы по коду в порядке возрастания.
//
// Возвращаемое значение:
//  Массив из Структура - найденные элементы, либо пустой массив:
//   * Раздел - Строка - раздел классификатора, к которому относится найденное значение.
//   * Код - Строка - код по классификатору.
//   * Наименование - Строка - текстовое описание выбранного значения.
//   * Представление - Строка - включает код и наименование.
//   * Описание - Строка - описание элемента.
//
Функция ЗначенияПоКодам(Коды, ПоУбыванию = Ложь) Экспорт
	
	РезультатЗапроса = РегистрыСведений.ДанныеОКВЭД.ПоискПоКодам(Коды, ПоУбыванию);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Результат = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ДобавитьВыборкуВРезультатПодбора(Результат, Выборка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Формирует иерархию кодов элементов ОКВЭД от исходного к наиболее общему.
//
// Параметры:
//  Код - Строка - код ОКВЭД.
//
// Возвращаемое значение:
//  Массив Из Строка - возможные коды ОКВЭД с учетом иерархии, от исходного к общему.
//
// Пример:
//  КодВИерархии("01.11.15") вернет массив ["01.11.15", "01.11.1", "01.11", "01.1", "01"]
//
Функция КодВИерархии(Знач Код) Экспорт
	
	Код = СокрЛП(Код);
	Результат = Новый Массив;
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(Код, ".", "")) Тогда
		Возврат Результат; // Корректный код может содержать только цифры и точки
	КонецЕсли;
	
	ДлинаКода = СтрДлина(Код);
	Для Позиция = 0 По ДлинаКода Цикл
		
		ЧастьКода = Лев(Код, ДлинаКода - Позиция);
		Если СтрЗаканчиваетсяНа(ЧастьКода, ".") Тогда
			Продолжить; // На точку код оканчиваться не может
		КонецЕсли;
		
		Результат.Добавить(ЧастьКода);
		Если ДлинаКода - Позиция <= 2 Тогда
			Прервать; // Кодов из 1 символа не бывает
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Дополняет результат методов КлассификаторОКВЭД.ЗначенияПоКодам И КлассификаторОКВЭД.Подбор переданным значением.
//
// Параметры:
//  Результат - Массив Из Структура - результат метода, который нужно дополнить.
//  Выборка - Структура, ВыборкаИзРезультатаЗапроса - данные, которые необходимо добавить в результат:
//   * Раздел - Строка - раздел классификатора, к которому относится найденное значение.
//   * Код - Строка - код по классификатору.
//   * Наименование - Строка - текстовое описание выбранного значения.
//   * Описание - Строка - описание элемента.
//
Процедура ДобавитьВыборкуВРезультатПодбора(Результат, Выборка)
	
	ДанныеРегистра = НовыйДанныеРегистра(
		Выборка.Раздел,
		Выборка.Код,
		Выборка.Наименование,
		Выборка.Описание);
	
	Результат.Добавить(ДанныеРегистра);
	
КонецПроцедуры

// Формирует описание элемента ОКВЭД
// 
// Параметры:
//  Раздел - Строка - раздел классификатора, к которому относится элемент.
//  Код - Строка - код по классификатору.
//  Наименование - Строка - текстовое описание элемента.
//  Описание - Строка - описание элемента.
// 
// Возвращаемое значение:
//  Структура - Новый данные регистра:
//   * Раздел - Строка - раздел классификатора, к которому относится элемент.
//   * Код - Строка - код по классификатору.
//   * Наименование - Строка - текстовое описание элемента.
//   * Представление - Строка - код и наименование.
//   * Описание - Строка - описание элемента.
// 
Функция НовыйДанныеРегистра(Раздел, Код, Наименование, Описание)
	
	ДанныеРегистра = Новый Структура;
	ДанныеРегистра.Вставить("Раздел", Раздел);
	ДанныеРегистра.Вставить("Код", Код);
	ДанныеРегистра.Вставить("Наименование", Наименование);
	ДанныеРегистра.Вставить("Описание", Описание);
	
	Если ЗначениеЗаполнено(Код) Тогда
		Представление = Код + " " + Наименование;
	Иначе
		Представление = Раздел + " " + Наименование;
	КонецЕсли;
	ДанныеРегистра.Вставить("Представление", Представление);
	
	Возврат ДанныеРегистра;
	
КонецФункции

// Добавляет запись в журнал регистрации.
//
// Параметры:
//  СообщениеОбОшибке - Строка - комментарий к записи журнала регистрации.
//  Ошибка - Булево - если истина будет установлен уровень журнала регистрации "Ошибка".
//  ОбъектМетаданных - ОбъектМетаданных - объект метаданных для которого регистрируется ошибка.
//
Процедура ЗаписатьИнформациюВЖурналРегистрации(
		СообщениеОбОшибке,
		Ошибка = Истина,
		ОбъектМетаданных = Неопределено) Экспорт
	
	УровеньЖР = ?(Ошибка,
		УровеньЖурналаРегистрации.Ошибка,
		УровеньЖурналаРегистрации.Информация);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖР,
		ОбъектМетаданных,
		,
		Лев(СообщениеОбОшибке, 5120));
	
КонецПроцедуры

// Возвращает имя события для журнала регистрации
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'ОКВЭД'",
		ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

#КонецОбласти