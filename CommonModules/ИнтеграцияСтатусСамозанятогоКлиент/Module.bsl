#Область ПрограммныйИнтерфейс

// Запускает получение статуса самозанятого из сервиса в фоне.
// 
// Параметры:
//  Источник - Произвольный - используется в обработке оповещения при завершении длительной операции
//  ФормаВладелец - ФормаКлиентскогоПриложения
//  ИНН - Строка - ИНН физического лица
//  НаДату - Дата - дата в часовом поясе "Europe/Moscow", на которую нужно получить статус
//  
//  Возвращаемое значение:
//   Булево - Истина, если длительная операция запущена
//
Функция НачатьПолучениеСтатусаСамозанятого(Источник, ФормаВладелец, ИНН, НаДату) Экспорт
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МетодВзаимодействия = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия().СоздатьЗадание;
	ПараметрыМетода = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия);
	
	СтрокаСодержанияЗадания = ИнтеграцияСтатусСамозанятогоКлиентСервер.НоваяСтрокаСодержанияЗадания(ИНН, НаДату);
	ПараметрыМетода.СодержаниеЗадания.Добавить(СтрокаСодержанияЗадания);
	
	ДлительнаяОперация = ИнтеграцияСтатусСамозанятогоВызовСервера.ВыполнитьВФоне(
		ПараметрыМетода, ФормаВладелец.УникальныйИдентификатор);
	
	ДополнительныеПараметры = Новый Структура("Источник", Источник);
	
	Если ДлительнаяОперация <> Неопределено И ДлительнаяОперация.Статус <> "Ошибка" Тогда
		Обработчик = Новый ОписаниеОповещения("ПолучитьСтатусСамозанятогоЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ФормаВладелец);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	Иначе
		ПолучитьСтатусСамозанятогоОшибка(ДлительнаяОперация, ДополнительныеПараметры);
	КонецЕсли;
	
	Возврат ДлительнаяОперация <> Неопределено И ДлительнаяОперация.Статус = "Выполняется";
	
КонецФункции

// Имя события, которое будет вызвано при успешном получении статуса самозанятого.
// 
// Возвращаемое значение:
//  Строка - Имя события статус самозанятого получен
//
Функция ИмяСобытия_СтатусСамозанятогоПолучен() Экспорт
	
	Возврат "СтатусСамозанятогоПолучен";
	
КонецФункции

// Имя события, которое будет вызвано, если при получении самозанятого возникла ошибка.
// 
// Возвращаемое значение:
//  Строка - имя события
//
Функция ИмяСобытия_СтатусСамозанятогоОшибка() Экспорт
	
	Возврат "СтатусСамозанятогоОшибка";
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПолучитьСтатусСамозанятогоЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Источник = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры, "Источник");
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		ДанныеОтвета = ИнтеграцияСтатусСамозанятогоКлиентСервер.НоваяСтрокаРезультатаВыполненияЗадания();
		Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			СтатусыСамозанятости = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				Результат, "СтатусыСамозанятости", Новый Массив);
			Если ЗначениеЗаполнено(СтатусыСамозанятости) Тогда
				ЗаполнитьЗначенияСвойств(ДанныеОтвета, СтатусыСамозанятости[0]);
				ИнтеграцияСтатусСамозанятогоКлиентПереопределяемый.ПриПолученииСтатусовСамозанятости(СтатусыСамозанятости);
			КонецЕсли;
		КонецЕсли;
		Оповестить(ИмяСобытия_СтатусСамозанятогоПолучен(), ДанныеОтвета, Источник);
	Иначе
		Оповестить(ИмяСобытия_СтатусСамозанятогоОшибка(), ДлительнаяОперация.КраткоеПредставлениеОшибки, Источник);
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(ДлительнаяОперация.АдресРезультата) Тогда
		УдалитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьСтатусСамозанятогоОшибка(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Источник = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры, "Источник", Неопределено);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		КраткоеПредставлениеОшибки = ДлительнаяОперация.КраткоеПредставлениеОшибки;
	Иначе
		КраткоеПредставлениеОшибки = НСтр("ru = 'Неизвестная ошибка при работе с сервисом'");
	КонецЕсли;
	
	Оповестить(ИмяСобытия_СтатусСамозанятогоОшибка(), КраткоеПредставлениеОшибки, Источник);
	
КонецПроцедуры

// Открывает журнал регистрации с отбором по имени события
//
// Параметры:
//   Владелец - ФормаКлиентскогоПриложения
//   Уровни - Массив из Строка, Неопределено - отбор по уровням
//
Процедура ОткрытьЖурналРегистрации(Владелец, Уровни = Неопределено) Экспорт
	
	ОтборЖурнала = Новый Структура;
	ОтборЖурнала.Вставить("СобытиеЖурналаРегистрации",
		ИнтеграцияСтатусСамозанятогоКлиентСервер.ИмяСобытияЖурналаРегистрации());
	
	Если Уровни <> Неопределено Тогда
		ОтборЖурнала.Вставить("Уровень", Уровни);
	КонецЕсли;
	
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(ОтборЖурнала, Владелец);
	
КонецПроцедуры

#КонецОбласти
