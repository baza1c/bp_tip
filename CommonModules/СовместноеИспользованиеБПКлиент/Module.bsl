#Область СлужебныйПрограммныйИнтерфейс

// См. СовместноеИспользованиеКлиент.ОбработкаПереходаПоНавигационнойСсылке
//
Процедура ОбработкаПереходаПоНавигационнойСсылке(ДанныеПереходаПоНавигационнойСсылке, СтандартнаяОбработка) Экспорт
	
	ОтносительнаяСсылка = ДанныеПереходаПоНавигационнойСсылке.ОтносительнаяНавигационнаяСсылка;
	
	Если СтрНачинаетсяС(ОтносительнаяСсылка, "СовместноеИспользование/") Тогда
		СтандартнаяОбработка = Ложь;
		
		Если СтрНачинаетсяС(ОтносительнаяСсылка, "СовместноеИспользование/ОткрытьПомощникСопоставленияОбъектов?") Тогда
			ИдентификаторУзла = ДанныеПереходаПоНавигационнойСсылке.ПараметрыНавигационнойСсылки.Получить("node-id");
			
			ПараметрыПомощника = Новый Структура;
			ПараметрыПомощника.Вставить("ЭтоОбменСПриложениемВСервисе", Ложь);
			ПараметрыПомощника.Вставить("ОбластьДанныхКорреспондента", 0);
			ПараметрыПомощника.Вставить("ОтправитьДанные", Ложь);
			ПараметрыПомощника.Вставить("РежимДополненияВыгрузки", Истина);
			ПараметрыПомощника.Вставить("НастройкаРасписания", Ложь);
			
			ОповещениеПриЗакрытии = Новый ОписаниеОповещения(
				"ПомощникСопоставленияОбъектовПриЗакрытии",
				СовместноеИспользованиеБПКлиент,
				Новый Структура("ИдентификаторУзла", ИдентификаторУзла));
			
			ВспомогательныеПараметры = Новый Структура;
			ВспомогательныеПараметры.Вставить("ПараметрыПомощника", ПараметрыПомощника);
			ВспомогательныеПараметры.Вставить("ОповещениеОЗакрытии", ОповещениеПриЗакрытии);
			
			УзелОбмена = СовместноеИспользованиеВызовСервера.УзелОбменаПоИдентификатору(ИдентификаторУзла);
			
			ОбменДаннымиКлиент.ОткрытьПомощникСопоставленияОбъектовОбработкаКоманды(
				УзелОбмена,
				Неопределено,
				ВспомогательныеПараметры);	
		ИначеЕсли СтрНачинаетсяС(ОтносительнаяСсылка, "СовместноеИспользование/НастройкиУчетнойПолитикиОрганизации?") Тогда
			ИдентификаторОрганизации = ДанныеПереходаПоНавигационнойСсылке.ПараметрыНавигационнойСсылки.Получить("uuid");
			ПериодСтрокой = ДанныеПереходаПоНавигационнойСсылке.ПараметрыНавигационнойСсылки.Получить("period");
			
			Организация = СовместноеИспользованиеВызовСервера.ОрганизацияПоИдентификатору(ИдентификаторОрганизации);
			
			ЧастиПериода = СтрРазделить(ПериодСтрокой, "-");
			ДатаУчетнойПолитики = Дата(Число(ЧастиПериода[0]), Число(ЧастиПериода[1]), Число(ЧастиПериода[2]));
			
			Ключ = СовместноеИспользованиеВызовСервера.КлючЗаписиДействующейУчетнойПолитики(Организация, ДатаУчетнойПолитики);
			
			ПараметрыФормы = Новый Структура;
			Если Ключ <> Неопределено Тогда
				ПараметрыФормы.Вставить("Ключ", Ключ);
			Иначе
				ЗначенияЗаполнения = Новый Структура("Организация, Период", Организация, ДатаУчетнойПолитики);
				ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			КонецЕсли;
			
			ОповещениеПриЗакрытии = Новый ОписаниеОповещения(
				"НастройкиУчетнойПолитикиОрганизацииПриЗакрытии",
				СовместноеИспользованиеБПКлиент,
				Новый Структура("ИдентификаторОрганизации", ИдентификаторОрганизации));
			
			ОткрытьФорму("РегистрСведений.УчетнаяПолитика.ФормаЗаписи", ПараметрыФормы, , , , , ОповещениеПриЗакрытии);
		ИначеЕсли СтрНачинаетсяС(ОтносительнаяСсылка, "СовместноеИспользование/НастройкиСистемыНалогобложенияОрганизации?") Тогда
			ИдентификаторОрганизации = ДанныеПереходаПоНавигационнойСсылке.ПараметрыНавигационнойСсылки.Получить("uuid");
			
			ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
			ПараметрыОткрытия.Заголовок = НСтр("ru = 'Настройки налогов и отчетов'");
			ПараметрыОткрытия.ИмяФормы = "ОбщаяФорма.НалогиИОтчеты";
			ПараметрыОткрытия.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
				"НастройкиСистемыНалогобложенияОрганизацииПриЗакрытии",
				СовместноеИспользованиеБПКлиент,
				Новый Структура("ИдентификаторОрганизации", ИдентификаторОрганизации));
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", СовместноеИспользованиеВызовСервера.ОрганизацияПоИдентификатору(ИдентификаторОрганизации));
			
			ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
		ИначеЕсли СтрНачинаетсяС(ОтносительнаяСсылка, "СовместноеИспользование/АльтернативнаяСсылка?") Тогда
			ИсходнаяСсылка = ДанныеПереходаПоНавигационнойСсылке.ПараметрыНавигационнойСсылки.Получить("src-link");
			Если ЗначениеЗаполнено(ИсходнаяСсылка) Тогда
				ПерейтиПоНавигационнойСсылке(ИсходнаяСсылка);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПомощникСопоставленияОбъектовПриЗакрытии(Результат, ДополнительныеПараметры) Экспорт
	
	Оповещение = СовместноеИспользованиеКлиент.НовоеОповещениеВнешнегоПриложения(
		"СовместноеИспользование.ЗакрытПомощникСопоставленияОбъектов",
		ДополнительныеПараметры);
		
	СовместноеИспользованиеКлиент.ОтправитьОповещениеВнешнемуПриложению(Оповещение, "УправлениеНебольшойФирмой");
	
КонецПроцедуры

Процедура НастройкиУчетнойПолитикиОрганизацииПриЗакрытии(Результат, ДополнительныеПараметры) Экспорт
	
	Оповещение = СовместноеИспользованиеКлиент.НовоеОповещениеВнешнегоПриложения(
		"СовместноеИспользование.ЗакрытаФормаНастройкиУчетнойПолитикиОрганизации",
		ДополнительныеПараметры);
		
	СовместноеИспользованиеКлиент.ОтправитьОповещениеВнешнемуПриложению(Оповещение, "УправлениеНебольшойФирмой");
	
КонецПроцедуры

Процедура НастройкиСистемыНалогобложенияОрганизацииПриЗакрытии(Результат, ДополнительныеПараметры) Экспорт
	
	Оповещение = СовместноеИспользованиеКлиент.НовоеОповещениеВнешнегоПриложения(
		"СовместноеИспользование.ЗакрытаФормаНастройкиСистемыНалогобложенияОрганизации",
		ДополнительныеПараметры);
		
	СовместноеИспользованиеКлиент.ОтправитьОповещениеВнешнемуПриложению(Оповещение, "УправлениеНебольшойФирмой");
	
КонецПроцедуры

#КонецОбласти