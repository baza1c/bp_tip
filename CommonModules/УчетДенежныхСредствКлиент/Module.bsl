
#Область ПрограммныйИнтерфейс

Функция ВыполнитьКомандуПечатиКассовыйОрдер(ОписаниеКоманды) Экспорт
	
	ОбъектыПечати = ОписаниеКоманды.ОбъектыПечати;
	СписокМакетов = УчетДенежныхСредствВызовСервера.ТипыКассовыхДокументов(ОбъектыПечати);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("ЖурналДокументов.Деньги",
		СписокМакетов,
		ОбъектыПечати,
		ОписаниеКоманды.Форма,
		ОбщегоНазначенияБПКлиент.ПолучитьЗаголовокПечатнойФормы(ОбъектыПечати));
	
КонецФункции

// Набор прав может предоставлять пользователю доступ 
// - как к полному набору документов, входящих в журнал Кассовые документы - ПКО и РКО
// - так и только к одному виду документов (ПКО).
//
// Удобно, если вне зависимости от набора прав
// в командном интерфейсе
// - присутствует команда списка документов
// - нет сильно похожих команд.
//
// То есть, если есть права и к ПКО и РКО, то должна быть команда, ведущая к журналу,
// если только к ПКО - то к списку ПКО.
//
// Для этого в командном интерфейсе показываем команду ЖурналКассовыеДокументы,
// которая в зависимости от прав пользователя открывает тот или иной список.
//
// У самого же журнала стандартные команды отключены.
//
Процедура ОткрытьФормуСпискаДоступныхКассовыхДокументов(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	УровеньДоступа = УчетДенежныхСредствВызовСервера.УровеньДоступаКассовыеДокументы();
	
	ИмяФормы = "";
	Если УровеньДоступа = "ЖурналКассовыеДокументы" Тогда
		ИмяФормы = "ЖурналДокументов.Деньги.Форма.КассовыеДокументы";
	ИначеЕсли УровеньДоступа = "ПриходныйКассовыйОрдер" Тогда
		ИмяФормы = "Документ.ПриходныйКассовыйОрдер.ФормаСписка";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием(ПараметрыВыполненияКоманды);
	ПараметрыОткрытия.ИмяФормы = ИмяФормы;
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
КонецПроцедуры

// Переходит к форме выгрузки документов вида "Платежное поручение"
//
// Параметры:
//  Форма     - ФормаКлиентскогоПриложения - форма, из которой выполняется открытие сверки
//  МассивПлатежныхПоручений - Массив из Строка - навигационные ссылки на сформированные документы платежей в потребителе
//
Процедура СформироватьВыгрузкуПлатежныхПоручений(Форма, МассивПлатежныхПоручений) Экспорт
	
	Если МассивПлатежныхПоручений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПервогоДокумента = УчетДенежныхСредствВызовСервера.РеквизитыПервогоДокумента(
		МассивПлатежныхПоручений);
	
	Если Не ЗначениеЗаполнено(РеквизитыПервогоДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	СписокДокументов = Новый СписокЗначений;
	СписокДокументов.ЗагрузитьЗначения(МассивПлатежныхПоручений);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НачалоПериода",    НачалоДня(РеквизитыПервогоДокумента.Дата));
	ПараметрыФормы.Вставить("КонецПериода",     КонецДня(РеквизитыПервогоДокумента.Дата));
	ПараметрыФормы.Вставить("Организация",      РеквизитыПервогоДокумента.Организация);
	ПараметрыФормы.Вставить("БанковскийСчет",   РеквизитыПервогоДокумента.СчетОрганизации);
	ПараметрыФормы.Вставить("СписокДокументов", СписокДокументов);
	
	ОткрытьФорму("Обработка.КлиентБанк.Форма", ПараметрыФормы, Форма);
	
КонецПроцедуры

#Область СогласованиеПлатежей  

// Возвращает список команд, которые не должны выполняться для несогласованных платежй
//
// Возвращаемое значение:
//  ПроверяемыеКоманды - Массив из Строка - имена подключаемых команд.
//
Функция ПроверяемыеКомандыЭДОНесогласованныхПлатежей() Экспорт 
	
	ПроверяемыеКоманды = Новый Массив;     
	ПроверяемыеКоманды.Добавить("ПодменюДиректБанкОбычное__ДиректБанкСоздать"); 
	ПроверяемыеКоманды.Добавить("ПодменюДиректБанкОбычное__ДиректБанкПросмотр"); 
	ПроверяемыеКоманды.Добавить("ПодменюДиректБанкОбычное__ДиректБанкОтправка");      
	
	Возврат ПроверяемыеКоманды;
	
КонецФункции

// Возвращает список команд, которые не должны выполняться для отклоненных платежй
//
// Возвращаемое значение:
//  ПроверяемыеКоманды - Массив из Строка - имена подключаемых команд.
//
Функция ПроверяемыеКомандыЭДООтклоненныхПлатежей() Экспорт 
	
	ПроверяемыеКоманды = Новый Массив;  
	ПроверяемыеКоманды.Добавить("ПодменюДиректБанкОбычное__ДиректБанкОтправка");
	ПроверяемыеКоманды.Добавить("ПодменюДиректБанкОбычное__ДиректБанкСоздать");  
	
	Возврат ПроверяемыеКоманды;
	
КонецФункции  

#КонецОбласти  

#КонецОбласти
