#Область СлужебныйПрограммныйИнтерфейс

Функция КодМетодаСопоставимыхРыночныхЦен() Экспорт
	
	Возврат "01";
	
КонецФункции

Функция КодМетодаЦеныПоследующейРеализации() Экспорт
	
	Возврат "02";
	
КонецФункции

Функция КодМетодаЗатратного() Экспорт
	
	Возврат "03";
	
КонецФункции

Функция КодМетодаСопоставимойРентабельности() Экспорт
	
	Возврат "04";
	
КонецФункции

Функция КодМетодаРаспределенияПрибыли() Экспорт
	
	Возврат "05";
	
КонецФункции

Функция КодМетодаКомбинацияМетодов() Экспорт
	
	Возврат "06";
	
КонецФункции

Функция ПоляСвойствМетодаЦенообразования(КодМетодаЦенообразования) Экспорт
	
	ПоляСвойств = Новый ТаблицаЗначений;
	ПоляСвойств.Колонки.Добавить("ИмяПоля", Новый ОписаниеТипов("Строка"));
	ПоляСвойств.Колонки.Добавить("ОписаниеПоля", Новый ОписаниеТипов("Строка"));
	ПоляСвойств.Колонки.Добавить("Обязательный", Новый ОписаниеТипов("Булево"));
	
	Если КодМетодаЦенообразования = КодМетодаСопоставимыхРыночныхЦен() Тогда
		
		ДобавитьПолеСвойств(ПоляСвойств, "ИсточникИнформацииСопоставимыхЦен", НСтр("ru = 'Источник информации'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "НаименованиеКотировкиСопоставимыхЦен", НСтр("ru = 'Наименование котировки'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "ПорядокПримененияКотировкиСопоставимыхЦен", НСтр("ru = 'Порядок применения котировки'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "НаименованиеДокументаЦеныСопоставимыхЦен", НСтр("ru = 'Наименование документа, которым фиксируется цена'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен", НСтр("ru = 'Наименование корректировки для сопоставимости'"), Истина);
	ИначеЕсли КодМетодаЦенообразования = КодМетодаЦеныПоследующейРеализации()
		Или КодМетодаЦенообразования = КодМетодаЗатратного()
		Или КодМетодаЦенообразования = КодМетодаСопоставимойРентабельности()Тогда
		
		ДобавитьПолеСвойств(ПоляСвойств, "ПоказательРентабельности", НСтр("ru = 'Показатель рентабельности'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "ТестируемаяСторона", НСтр("ru = 'Тестируемая сторона'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "Рентабельность", НСтр("ru = 'Рентабельность'"), Ложь);
		ДобавитьПолеСвойств(ПоляСвойств, "РыночнаяРентабельность", НСтр("ru = 'Рыночная рентабельность'"), Ложь);
	ИначеЕсли КодМетодаЦенообразования = КодМетодаРаспределенияПрибыли() Тогда
		ДобавитьПолеСвойств(ПоляСвойств, "ОписаниеМетодаРаспределенияПрибыли", НСтр("ru = 'Описание метода'"), Истина);
	КонецЕсли;
	
	Возврат ПоляСвойств;
	
КонецФункции

Функция ОписаниеСвойствМетодаЦенообразования(КодМетодаЦенообразования, СвойстваМетода) Экспорт
	
	ОписаниеСвойств = Новый Структура;
	ОписаниеСвойств.Вставить("ЗначенияСвойствСтрокой", "");
	ОписаниеСвойств.Вставить("ЕстьОшибкиЗаполнения", Ложь);
	ОписаниеСвойств.Вставить("ТекстОшибкиЗаполнения", "");
	
	КодыМетодовЦенообразования = Документы.ДокументацияПоКонтролируемымСделкам.КодыМетодовЦенообразования();
	Если КодыМетодовЦенообразования.НайтиПоЗначению(КодМетодаЦенообразования) = Неопределено Тогда
		
		ОписаниеСвойств.ТекстОшибкиЗаполнения = СтрШаблон(
			НСтр("ru = 'Код метода ценообразования %1 нельзя использовать в документации'"),
			КодМетодаЦенообразования);
		ОписаниеСвойств.ЕстьОшибкиЗаполнения = Истина;
		Возврат ОписаниеСвойств;
		
	КонецЕсли;
	
	Если СвойстваМетода.Количество() = 0 Тогда
		
		Если КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
			ОписаниеСвойств.ТекстОшибкиЗаполнения = НСтр("ru = 'Не заданы методы ценообразования для комбинации методов'");
		Иначе
			ОписаниеСвойств.ТекстОшибкиЗаполнения = НСтр("ru = 'Не заполнены поля свойств метода ценообразования'");
		КонецЕсли;
		
		ОписаниеСвойств.ЕстьОшибкиЗаполнения = Истина;
		Возврат ОписаниеСвойств;
		
	КонецЕсли;
	
	ЧастиОписания = Новый Массив;
	НезаполненныеПоля = Новый Массив;
	
	Для Каждого СтрокаСвойств Из СвойстваМетода Цикл
		ОписаниеКода = "";
		Если КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
			ОписаниеКода = СтрокаСвойств.КодМетодаЦенообразования + ":" + " ";
		КонецЕсли;
		
		ПоляМетода = ПоляСвойствМетодаЦенообразования(СтрокаСвойств.КодМетодаЦенообразования);
		
		ЧастиОписания.Добавить(ОписаниеКода + ОписаниеМетодаСтрокиСвойствЦенообразования(СтрокаСвойств, ПоляМетода));
		
		ДополнитьНезаполненныеПоля(НезаполненныеПоля, СтрокаСвойств, ПоляМетода);
		
	КонецЦикла;
	
	ОписаниеСвойств.ЗначенияСвойствСтрокой = СтрСоединить(ЧастиОписания, Символы.ПС);
	Если НезаполненныеПоля.Количество() > 0 Тогда
		ОписаниеСвойств.ТекстОшибкиЗаполнения = СтрШаблон(
			НСтр("ru = 'Нужно заполнить: %1'"),
			СтрСоединить(НезаполненныеПоля, ", "));
		ОписаниеСвойств.ЕстьОшибкиЗаполнения = Истина;
	КонецЕсли;
	
	Возврат ОписаниеСвойств;
	
КонецФункции

Функция ОписаниеМетодаСтрокиСвойствЦенообразования(Строка, ПоляМетода) Экспорт
	
	Если Строка = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставленияСвойств = Новый Массив;
	Для Каждого СтрокаПоля Из ПоляМетода Цикл
		ИмяПоля = СтрокаПоля.ИмяПоля;
		ЗначениеПоля = Строка[ИмяПоля];
		
		Если Не ЗначениеЗаполнено(ЗначениеПоля) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИмяПоля = "ТестируемаяСторона" Тогда
			
			СписокВыбораТестируемойСтороны = Документы.ДокументацияПоКонтролируемымСделкам.СписокВыбораТестируемойСтороны();
			ЭлементСписка = СписокВыбораТестируемойСтороны.НайтиПоЗначению(ЗначениеПоля);
			Если ЭлементСписка <> Неопределено Тогда
				ПредставленияСвойств.Добавить(ЭлементСписка.Представление);
			КонецЕсли;
			
		ИначеЕсли ИмяПоля = "Рентабельность"
			Или ИмяПоля = "РыночнаяРентабельность" Тогда
			
			ПредставленияСвойств.Добавить(Формат(ЗначениеПоля, "ЧЦ=6; ЧДЦ=3"));
			
		Иначе
			
			ПредставленияСвойств.Добавить(ЗначениеПоля);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтрСоединить(ПредставленияСвойств, ", ");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеСвойств(ПоляСвойств, ИмяПоля, ОписаниеПоля, Обязательный)
	
	НоваяСтрока = ПоляСвойств.Добавить();
	НоваяСтрока.ИмяПоля = ИмяПоля;
	НоваяСтрока.ОписаниеПоля = ОписаниеПоля;
	НоваяСтрока.Обязательный = Обязательный;
	
КонецПроцедуры

Процедура ДополнитьНезаполненныеПоля(НезаполненныеПоля, СтрокаСвойств, ПоляМетода)
	
	Для Каждого СтрокаПоля Из ПоляМетода Цикл
		Если Не СтрокаПоля.Обязательный Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПоля = СтрокаПоля.ИмяПоля;
		ЗначениеПоля = СтрокаСвойств[ИмяПоля];
		
		Если Не ЗначениеЗаполнено(ЗначениеПоля) Тогда
			НезаполненныеПоля.Добавить(СтрокаПоля.ОписаниеПоля);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

