#Область ПрограммныйИнтерфейс

// Дополняет соответствие ВидыМестВыплатыРегистраторов сведениями о виде места выплаты зарплаты по переданным документам.
//
// Параметры:
// 		ВидыМестВыплатыРегистраторов - Соответствие - дополняется видом места выплаты документов,
// 		переданных в параметре РегистраторыВыплат
// 			* Ключ - ДокументСсылка
// 			* Значение - ПеречислениеСсылка.ВидыМестВыплатыЗарплаты.
// 		РегистраторыВыплат - Массив - ссылки на документы, для которых необходимо определить вид места выплаты зарплаты.
//
Процедура ДополнитьВидыМестВыплатыРегистраторов(ВидыМестВыплатыРегистраторов, РегистраторыВыплат) Экспорт
	
	Для каждого Регистратор Из РегистраторыВыплат Цикл
		
		Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
			
			ВидОперацииДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "ВидОперации");
			
			Если ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
				Или ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда Тогда
				ВидыМестВыплатыРегистраторов.Вставить(Регистратор, Перечисления.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект);
			ИначеЕсли ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
				Или ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда
				ВидыМестВыплатыРегистраторов.Вставить(Регистратор, Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Регистратор) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			
			ВидыМестВыплатыРегистраторов.Вставить(Регистратор, Перечисления.ВидыМестВыплатыЗарплаты.Касса);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти