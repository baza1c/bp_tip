
#Область ПрограммныйИнтерфейс

// Перед определением параметров интеграции.
// Определяет необходимость встраивания гиперссылки в форме документа
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ВстроитьГиперссылку - Булево
Процедура ПередОпределениемПараметровИнтеграции(Форма, ВстроитьГиперссылку) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокументаТовары"
		ИЛИ Форма.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокументаОбщая" Тогда
		ВстроитьГиперссылку = Истина;
	ИначеЕсли Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаТовары"
		ИЛИ Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаОбщая" Тогда
		ВстроитьГиперссылку = Истина;
	КонецЕсли;
	
КонецПроцедуры

// При определении параметров интеграции.
// Определяет группу для размещения статуса проверки документа в ГИС МТ
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ПараметрыИнтеграции - Структура:
// * ИмяРеквизитаФормыОбъект - Строка - Имя реквизита объекта в форме
// * РазмещатьЭлементыИнтерфейса - Булево - Размещать элементы интерфейса
// * ИмяРодительскойГруппыФормы - Строка - Имя группы, в которую будет встроена гиперссылка
// * ИмяКомандыСоответствиеТребованиямГИСМТ - Строка - Дата документа
// * ЗапуститьПроверкуДокументаВФоне - Булево - значение константы ИспользоватьФоновуюПроверкуДокументовГИСМТ
// * ИнтервалОбработкиОжидания - Строка - интервал обработки ожидания при фоновой проверке
Процедура ПриОпределенииПараметровИнтеграции(Форма, ПараметрыИнтеграции) Экспорт
	
	ПараметрыИнтеграции.ИмяРодительскойГруппыФормы  = "ГруппаСостояниеГИСМТ";//"ГруппаСостояниеЭДО";
	ПараметрыИнтеграции.РазмещатьЭлементыИнтерфейса = Истина;

КонецПроцедуры

// При определении реквизитов документа.
// Необходимо заполнить в структуре РеквизитыДокумента из прикладного документа поля:
// Дата, Организация, Контрагент
// Параметры:
//  Документ - ОпределяемыйТип.ПрикладныеДокументыГИСМТ
//  РеквизитыДокумента - Структура:
// * Дата - Дата - Дата документа
// * Организация - Неопределено, ОпределяемыйТип.Организация - Организация
// * Контрагент - Неопределено, ОпределяемыйТип.КонтрагентГосИС - Контрагент
// * ДанныеУчастниковМОД - Структура - см. ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена - структура с дополнительными данными реквизитов, влияющих
//		на расчет статусов МОДов - ИНН/КПП грузоотправителя/грузополучателя, адреса МОД индивидуальных предпринимателей
//		Заполнять только при указании параметра ДополнитьРеквизитамиРасчетаМОД = Истина для уменьшения нагрузки на систему.
// ДополнитьРеквизитамиРасчетаМОД - Булево - если Истина, заполнить в РеквизитыДокумента поле ДанныеУчастниковМОД
//		данными, влияющими на перерасчет статуса МОД (см. ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена).
Процедура ПриОпределенииРеквизитовДокумента(Документ, РеквизитыДокумента, ДополнитьРеквизитамиРасчетаМОД = Ложь) Экспорт
	
	Если Не ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И Не ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = "Дата, Организация, Контрагент";
	Если ДополнитьРеквизитамиРасчетаМОД Тогда
		Реквизиты = Реквизиты + ", Склад, Грузоотправитель, Грузополучатель, ПодразделениеОрганизации, ПодразделениеОрганизации.ОбособленноеПодразделение" + 
					?(ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг"),", АдресДоставки, АдресДоставкиЗначение", "");
	КонецЕсли;	
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, Реквизиты);
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ЗначенияРеквизитов);
		
	Если ДополнитьРеквизитамиРасчетаМОД Тогда
		
		ДанныеУчастниковМОД = ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена();
		ЭтоИП = Ложь;

		ОбъектПокупатель = Неопределено;
		ОбъектПродавец   = Неопределено;
		ВидКИПокупателя  = Неопределено;
		ВидКИПродавца    = Неопределено;

		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузополучатель) Тогда
			ОбъектПокупатель                                              			= ЗначенияРеквизитов.Грузополучатель;
			ДанныеУчастниковМОД.Покупатель.УполномоченноеЛицоЗаПоставкуГруза 		= ОбъектПокупатель;
			ВидКИПокупателя                                               			= Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
		Иначе
			Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				ОбъектПокупатель 													= ЗначенияРеквизитов.Контрагент;
				ВидКИПокупателя  													= Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
			Иначе
				ОбъектПродавец 														= ЗначенияРеквизитов.Организация;
				ВидКИПродавца  														= Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации;
			КонецЕсли;
		КонецЕсли;

		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузоотправитель) Тогда
			ОбъектПродавец 															= ЗначенияРеквизитов.Грузоотправитель;
			ДанныеУчастниковМОД.Продавец.УполномоченноеЛицоЗаПоставкуГруза 			= ОбъектПродавец;
			ВидКИПродавца 															= Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
		Иначе
			Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				ОбъектПродавец 														= ЗначенияРеквизитов.Организация;
				ВидКИПродавца  														= Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации;
				ДанныеУчастниковМОД.Продавец.УполномоченноеЛицоЗаПоставкуГруза		= ?(ЗначениеЗаполнено(ЗначенияРеквизитов.ПодразделениеОрганизацииОбособленноеПодразделение) И ЗначенияРеквизитов.ПодразделениеОрганизацииОбособленноеПодразделение
																						, ЗначенияРеквизитов.ПодразделениеОрганизации, ЗначенияРеквизитов.Организация);
			Иначе
				ОбъектПокупатель 													= ЗначенияРеквизитов.Контрагент;
				ВидКИПокупателя  													= Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ДанныеУчастниковМОД.Продавец.УчастникОбмена   							= ЗначенияРеквизитов.Организация;
			ДанныеУчастниковМОД.Покупатель.УчастникОбмена 							= ЗначенияРеквизитов.Контрагент;
		Иначе
			ДанныеУчастниковМОД.Продавец.УчастникОбмена   							= ЗначенияРеквизитов.Контрагент;
			ДанныеУчастниковМОД.Покупатель.УчастникОбмена 							= ЗначенияРеквизитов.Организация;
		КонецЕсли;
		
		Если ЗначенияРеквизитов.Свойство("АдресДоставкиЗначение") И ЗначениеЗаполнено(ЗначенияРеквизитов.АдресДоставкиЗначение) Тогда
			ДанныеУчастниковМОД.Покупатель.АвтоматическоеЗаполнение 			= Ложь;
			ДанныеУчастниковМОД.Покупатель.АдресСтрокой             			= ЗначенияРеквизитов.АдресДоставки;
			ДанныеУчастниковМОД.Покупатель.АдресЗначенияПолей       			= ЗначенияРеквизитов.АдресДоставкиЗначение;
		Иначе
			ДанныеУчастниковМОД.Покупатель.АвтоматическоеЗаполнение     		= Истина;
			ДанныеУчастниковМОД.Покупатель.ВладелецКонтактнойИнформации 		= ОбъектПокупатель;
			ДанныеУчастниковМОД.Покупатель.ВидКонтактнойИнформации      		= ВидКИПокупателя;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Документ.ПодразделениеОрганизации) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ.ПодразделениеОрганизации, "ОбособленноеПодразделение") Тогда
			Если ЗначениеЗаполнено(Документ.Склад) ТОгда
				ВладелецКИ = Документ.Склад;
				ВидКИ = Справочники.ВидыКонтактнойИнформации.АдресСклада;
    		Иначе
				ВладелецКИ = Документ.ПодразделениеОрганизации;
				ВидКИ = Справочники.ВидыКонтактнойИнформации.ФактическийАдресПодразделенияОрганизаций;
			КонецЕсли;
		Иначе
			ОбщегоНазначенияИСПереопределяемый.ПартнерОрганизацияЭтоИндивидуальныйПредприниматель(ОбъектПродавец, ЭтоИП);
			Если ЭтоИП Тогда
				ВладелецКИ = Документ.Склад;
				ВидКИ = Справочники.ВидыКонтактнойИнформации.АдресСклада;
			Иначе
				ВладелецКИ = ОбъектПродавец;
				ВидКИ = ВидКИПродавца;
			КонецЕсли;
		КонецЕсли;
	
		ДанныеУчастниковМОД.Продавец.АвтоматическоеЗаполнение     				= Истина;
		ДанныеУчастниковМОД.Продавец.ВладелецКонтактнойИнформации 				= ВладелецКИ;
		ДанныеУчастниковМОД.Продавец.ВидКонтактнойИнформации      				= ВидКИ; 
		
		РеквизитыДокумента.ДанныеУчастниковМОД 									= ДанныеУчастниковМОД;	
	КонецЕсли;
	
КонецПроцедуры       

// При определении типа документа.
// Необходимо заполнить тип документа ГИС МТ
// Дата, Организация, Контрагент
// Параметры:
//  Документ - ОпределяемыйТип.ПрикладныеДокументыГИСМТ
//  ТипДокументаГИСМТ - ПеречислениеСсылка.ТипыДокументовГИСМТ - Тип документа
Процедура ПриОпределенииТипаДокументаГИСМТ(Документ, ТипДокументаГИСМТ) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УПД;
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") 
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		Если Документ.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УКД;
		Иначе
			ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УПДИсправительный;	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// При выполнении отдельных проверок продавца и покупателя.
// Вызывается во время отдельных проверок покупателя и продавца, до формирования УПД
// Параметры:
//  ПараметрыОбработки - см. СоответствиеТребованиямГИСМТ.ИнициализироватьПараметрыОбработки
Процедура ПриВыполненииОтдельныхПроверокПродавцаИПокупателя(ПараметрыОбработки) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

// После проверки статуса ГИС МТ.
// Вызывается после проверки статуса документа в ГИС МТ. 
// Позволяет выполнить обработку, после получения статуса документа из сервиса ГИС МТ.
// Параметры:
//  ПараметрыОбработки - см. СоответствиеТребованиямГИСМТ.ИнициализироватьПараметрыОбработки
Процедура ПослеПроверкиСтатусаГИСМТ(ПараметрыОбработки) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

// При получении статуса документа из очереди.
// Вызывается после проверки статуса документа в ГИС МТ. 
// Позволяет выполнить обработку, после получения статуса документа из сервиса ГИС МТ.
// Параметры:
//  СтрокаОчереди - СтрокаТаблицыЗначений из см.СоответствиеТребованиямГИСМТ.ИнициализироватьОчередьДокументов.
Процедура ПриПолученииСтатусаДокументаИзОчереди(СтрокаОчереди) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

// При определении адресов для проверки МОД (не используется)
// 
// Параметры:
//  Документ - ОпределяемыйТип.ПрикладныеДокументыГИСМТ
//  РеквизитыАдресов - Структура:
// * ДанныеЭлектронногоДокумента - Неопределено
Процедура ПриЗаполненииАдресовДляПроверкиМОД(Документ, РеквизитыАдресов) Экспорт
	
	//++ НЕ ГОСИС
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(Документ);
		ТаблицаДанных = УчетНДС.ПолучитьДанныеДляПечатиУниверсальногоПередаточногоДокумента(
				МассивДокументов, Документы[Документ.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(), Истина); 
				
		УстановитьПривилегированныйРежим(Ложь);
		
		ДанныеДокументаУчета = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаДанных[0]);
		РеквизитыАдресов.ДанныеЭлектронногоДокумента = ДанныеДокументаУчета;
	
	КонецЕсли;
	
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти
