
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиентские методы формы записи регистра сведений НастройкиСистемыНалогообложения
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ПоложенияПереходногоПериодаУСНПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиСистемыНалогообложения;
	
	Если Форма.ПоложенияПереходногоПериодаУСН Тогда
		Если Не ЗначениеЗаполнено(Запись.ДатаПереходаНаУСН) Тогда
			Запись.ДатаПереходаНаУСН = Запись.ДатаИзменения;
		КонецЕсли;
	Иначе
		Запись.ДатаПереходаНаУСН = '00010101';
	КонецЕсли;
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура СистемаНалогообложенияПредставлениеПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиСистемыНалогообложения;
	
	Запись.СистемаНалогообложения =
		НастройкиСистемыНалогообложенияФормыКлиентСервер.СистемаНалогообложенияПоИндексу(Форма.СистемаНалогообложенияПредставление);
	
	Если Не НастройкиСистемыНалогообложенияФормыВызовСервера.ИспользуетсяДатаИзменения(
		Запись.Организация, Запись.СистемаНалогообложения, Запись.Период) Тогда
		Запись.ДатаИзменения = НачалоМесяца(Запись.ДатаИзменения);
	КонецЕсли;
	
	Если НастройкиСистемыНалогообложенияФормыКлиентСервер.ПереходНаСистемуНалогообложенияСНачалаГода(
			Запись.СистемаНалогообложения) Тогда
		Запись.Период = НачалоГода(Запись.Период);
	КонецЕсли;
	
	Если Форма.СистемаНалогообложенияПредставление = 3 Тогда
		Запись.ПлательщикЕНВД           = Ложь;
		Запись.ПрименяетсяУСНПатент     = Истина;
		Запись.ПлательщикТорговогоСбора = Ложь;
		Запись.ПрименяетсяНалогНаПрофессиональныйДоход = Ложь;
		Запись.ПрименяетсяАУСН = Ложь;
	КонецЕсли;
	Если Форма.СистемаНалогообложенияПредставление = 4 Тогда
		Запись.ПлательщикЕНВД           = Истина;
		Запись.ПрименяетсяУСНПатент     = Ложь;
		Запись.ПлательщикТорговогоСбора = Ложь;
		Запись.ПрименяетсяНалогНаПрофессиональныйДоход = Ложь;
		Запись.ПрименяетсяАУСН = Ложь;
	КонецЕсли;
	Если Форма.СистемаНалогообложенияПредставление = 0 Тогда
		Запись.ПлательщикЕНВД           = Ложь;
		Запись.ПрименяетсяУСНПатент     = Ложь;
		Запись.ПлательщикТорговогоСбора = Ложь;
		Запись.ПрименяетсяНалогНаПрофессиональныйДоход = Истина;
		Запись.ПрименяетсяАУСН = Ложь;
		Запись.ПлательщикЕНП = Ложь;
	КонецЕсли;
	Если Форма.СистемаНалогообложенияПредставление = 6 Или Форма.СистемаНалогообложенияПредставление = 7 Тогда
		Запись.ПлательщикЕНВД           = Ложь;
		Запись.ПрименяетсяУСНПатент     = Ложь;
		Запись.ПлательщикТорговогоСбора = Ложь;
		Запись.ПрименяетсяНалогНаПрофессиональныйДоход = Ложь;
		Запись.ПрименяетсяАУСН = Истина;
		Запись.ПлательщикЕНП = Ложь;
	КонецЕсли;
	Если Форма.СистемаНалогообложенияПредставление = 1 Или Форма.СистемаНалогообложенияПредставление = 6 Тогда
		Запись.ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы");
	ИначеЕсли Форма.СистемаНалогообложенияПредставление = 2 Или Форма.СистемаНалогообложенияПредставление = 7 Тогда
		Запись.ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы");
	КонецЕсли;
	
	Если НастройкиСистемыНалогообложенияФормыКлиентСервер.ПолучитьПериодФормы(Форма) >= УчетЕНВДКлиентСервер.ДатаОтменыЕНВД() Тогда
		Запись.ПлательщикЕНВД = Ложь;
	КонецЕсли;
	
	СистемаНалогообложенияПриИзмененииНаКлиенте(Форма);
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура ПлательщикНДСПрименяющийУСНПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиСистемыНалогообложения;
	
	Если Запись.ПлательщикНДСПрименяющийУСН Тогда
		Если Запись.ДатаИзменения < УчетУСНКлиентСервер.ДатаНачалаПримененияНДС()
			Или Форма.ДатаИзменения < УчетУСНКлиентСервер.ДатаНачалаПримененияНДС() Тогда
			Запись.ДатаИзменения = УчетУСНКлиентСервер.ДатаНачалаПримененияНДС();
			Форма.ДатаИзменения = УчетУСНКлиентСервер.ДатаНачалаПримененияНДС();
			Форма.ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
				Форма.ДатаИзменения, КонецМесяца(Форма.ДатаИзменения), Истина);
		КонецЕсли;
		Форма.ПрименениеНДСНаУСН = Истина;
	Иначе
		Форма.ПрименениеНДСНаУСН = Ложь;
	КонецЕсли;
	
	НастройкиСистемыНалогообложенияФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

#КонецОбласти

Процедура ПослеЗаписи(ПараметрыЗаписи) Экспорт
	
	ОбновитьИнтерфейсПослеЗаписиСистемыНалогообложения(ПараметрыЗаписи);
	
	ПараметрыОповещения = Новый Структура("Организация, Период", ПараметрыЗаписи.Организация, ПараметрыЗаписи.Период);
	Оповестить("Запись_НастройкиСистемыНалогообложения", ПараметрыОповещения);
	
	Оповестить("ИзменениеУчетнойПолитики", ПараметрыЗаписи.Организация);
	ОповеститьОбИзменении(Тип("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	
	Если ПараметрыЗаписи.Свойство("РезультатВыполненияЗаданияКалендаряБухгалтера") Тогда
		КалендарьБухгалтераКлиент.ОжидатьЗавершениеЗаполненияВФоне(ПараметрыЗаписи.РезультатВыполненияЗаданияКалендаряБухгалтера);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьИнтерфейсПослеЗаписиСистемыНалогообложения(ПараметрыЗаписи) Экспорт
	
	Перем ИспользуемыеСистемыНалогообложенияПередЗаписью, ИспользуемыеСистемыНалогообложенияПослеЗаписи;
	
	ОбновитьИнтерфейс = Ложь;
	Если ПараметрыЗаписи.Свойство("ИспользуемыеСистемыНалогообложенияПередЗаписью", ИспользуемыеСистемыНалогообложенияПередЗаписью)
		И ПараметрыЗаписи.Свойство("ИспользуемыеСистемыНалогообложенияПослеЗаписи", ИспользуемыеСистемыНалогообложенияПослеЗаписи) Тогда
		
		Для Каждого КлючИЗначение Из ИспользуемыеСистемыНалогообложенияПередЗаписью Цикл
			СтароеЗначение = КлючИЗначение.Значение;
			НовоеЗначение = Неопределено;
			Если НЕ ИспользуемыеСистемыНалогообложенияПослеЗаписи.Свойство(КлючИЗначение.Ключ, НовоеЗначение)
				ИЛИ СтароеЗначение <> НовоеЗначение Тогда
				ОбновитьИнтерфейс = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОбновитьИнтерфейс Тогда
		ОбновитьИнтерфейс();
	КонеЦесли;
	
КонецПроцедуры

Процедура СистемаНалогообложенияПриИзмененииНаКлиенте(Форма)
	
	Запись = Форма.НастройкиСистемыНалогообложения;
	
	Запись.ПлательщикНалогаНаПрибыль =
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая")
		И Форма.ЭтоЮрЛицо;
		
	Запись.ПрименяетсяАУСН =
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.АУСН");
	
	Запись.ПрименяетсяУСН =
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная");
		
	Запись.ПрименяетсяУСНДоходы = 
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная")
		И Запись.ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы");
		
	Запись.ПрименяетсяУСНДоходыМинусРасходы =
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная")
		И Запись.ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы");
		
	Запись.ПлательщикНДФЛ =
		Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая") И НЕ Форма.ЭтоЮрЛицо;

	Если Запись.ПрименяетсяУСН Или Запись.ПрименяетсяАУСН Тогда
		Запись.ПлательщикНДС = Ложь;
	Иначе
		Запись.ПлательщикНДС = Истина;
	КонецЕсли;

	Если НЕ Запись.ПрименяетсяУСН Тогда
		Запись.ДатаПереходаНаУСН = Дата('00010101');
		Форма.ПоложенияПереходногоПериодаУСН = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Запись.ОбъектНалогообложенияУСН) Тогда
		Запись.ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы");
	КонецЕсли;
	
КонецПроцедуры

// Обновляет интерфейс в случае изменения настроек, влияющих на функциональные опции
// 
// Параметры:
//  ПараметрыЗаписи - см. НовыйИзменениеНастроек
//
Процедура ОбновитьИнтерфейсПослеЗаписиНастройкиУчета(ПараметрыЗаписи) Экспорт
			
	Если ПараметрыЗаписи.НастройкаПередЗаписью <> ПараметрыЗаписи.НастройкаПослеЗаписи Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

// Конструктор описания изменения настроек
// 
// Возвращаемое значение:
//  Структура:
// * НастройкаПередЗаписью - Неопределено
//                         - Булево
// * НастройкаПослеЗаписи  - Неопределено
//                         - Булево
//
Функция НовыйИзменениеНастроек() Экспорт
	ИзменениеНастроек = Новый Структура;
	ИзменениеНастроек.Вставить("НастройкаПередЗаписью", Неопределено);
	ИзменениеНастроек.Вставить("НастройкаПослеЗаписи", Неопределено);
	Возврат ИзменениеНастроек;
КонецФункции

#КонецОбласти