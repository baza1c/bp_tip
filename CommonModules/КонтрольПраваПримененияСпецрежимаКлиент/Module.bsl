#Область ПрограммныйИнтерфейс

// Возвращает признак необходимости контролировать право применения спецрежима для организации
// 
// Параметры:
//   Организация - СправочникСсылка.Организации
//   НаДату - Дата, Неопределено - дата проверки
// 
// Возвращаемое значение:
//   Булево - Истина, если нужно контролировать
//
Функция Контролировать(Организация, Знач НаДату = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаДату) Тогда
		НаДату = ОбщегоНазначенияКлиент.ДатаСеанса();
	КонецЕсли;
	
	Возврат КонтрольПраваПримененияСпецрежимаКлиентПовтИсп.Контролировать(Организация, НачалоМесяца(НаДату));
	
КонецФункции

// Обработчик нажатия гиперссылки "Подробнее" в баннере контроля права применения спецрежима
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма, из которой открывается пояснение ссылки
//   СсылкаНаПояснение - Строка - гиперссылка. Если не указана,
//                                берется из реквизита формы СсылкаНаПояснение
//
Процедура ПодробнееНажатие(Форма, Знач СсылкаНаПояснение = "") Экспорт
	
	Если ПустаяСтрока(СсылкаНаПояснение) Тогда
		СсылкаНаПояснение = Форма.СсылкаНаПояснение;
	КонецЕсли;
	
	Если СсылкаНаПояснение = "УтратаПраваПримененияНПД" Тогда
		
		ПараметрыВыполнения = ПараметрыПодробнее(Форма);
		ПараметрыВыполнения.Вставить("ВидОрганизации", ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"));
		ПараметрыВыполнения.Вставить("УтратаПраваНПД", Истина);
		
		ОткрытьФорму("Обработка.СравнениеРежимовНалогообложения.Форма",
			ПараметрыВыполнения,
			Форма,
			Форма.УникальныйИдентификатор);
		
	ИначеЕсли СсылкаНаПояснение = "СравнениеРежимовНалогообложенияВСледующемГоду" Тогда
		
		ПараметрыВыполнения = ПараметрыПодробнее(Форма);
		ПараметрыВыполнения.Вставить("НалоговыйПериодСледующегоГода", Истина);
		ОткрытьФорму("Обработка.СравнениеРежимовНалогообложения.Форма",
			ПараметрыВыполнения,
			Форма,
			Форма.УникальныйИдентификатор);
		
	ИначеЕсли СсылкаНаПояснение = "ВключитьНДССНачалаГода" Тогда
		
		ПараметрыВыполнения = ПараметрыПодробнее(Форма);
		ПараметрыВыполнения.Вставить("ПрименениеНДСНаУСН", Истина);
		ДатаПримененияНДС = НачалоГода(ОбщегоНазначенияКлиент.ДатаСеанса());
		ПараметрыВыполнения.Вставить("ДатаПримененияНДСНаУСН", ДатаПримененияНДС);
		ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты",
			ПараметрыВыполнения,
			Форма,
			Форма.УникальныйИдентификатор);
			
	ИначеЕсли СсылкаНаПояснение = "ВключитьНДС" Тогда
		
		ПараметрыВыполнения = ПараметрыПодробнее(Форма);
		ПараметрыВыполнения.Вставить("ПрименениеНДСНаУСН", Истина);
		Если ПараметрыВыполнения.Свойство("Организация")Тогда
			МесяцПревышенияГраницыПримененияНДС = КонтрольПраваПримененияСпецрежимаВызовСервера.МесяцПревышенияГраницыПримененияНДС(
				ПараметрыВыполнения.Организация);
			Если МесяцПревышенияГраницыПримененияНДС <> Неопределено Тогда
				ПараметрыВыполнения.Вставить("ДатаПримененияНДСНаУСН", КонецМесяца(МесяцПревышенияГраницыПримененияНДС) + 1);
			КонецЕсли;
		КонецЕсли;
		ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты",
			ПараметрыВыполнения,
			Форма,
			Форма.УникальныйИдентификатор);
		
	ИначеЕсли СсылкаНаПояснение = "НастроитьНДС" Тогда
		
		ПараметрыВыполнения = ПараметрыПодробнее(Форма);
		Если ПараметрыВыполнения.Свойство("Организация")Тогда
			ПараметрыУстановкиСтавкиНДС = КонтрольПраваПримененияСпецрежимаВызовСервера.ПараметрыУстановкиСтавкиНДС(
				ПараметрыВыполнения.Организация);
			Если ЗначениеЗаполнено(ПараметрыУстановкиСтавкиНДС.МесяцПревышения) Тогда
				ПараметрыВыполнения.Вставить("ДатаИзмененияСтавкиНДСПриУСН", КонецМесяца(ПараметрыУстановкиСтавкиНДС.МесяцПревышения) + 1);
				ПараметрыВыполнения.Вставить("СтавкаНДСПриУСН", ПараметрыУстановкиСтавкиНДС.ТребуемаяСтавка);
			КонецЕсли;
		КонецЕсли;
		ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты",
			ПараметрыВыполнения,
			Форма,
			Форма.УникальныйИдентификатор);
		
	Иначе
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СсылкаНаПояснение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыПодробнее(Форма)
	
	ПараметрыВыполнения = Новый Структура;
	Если Форма.Элементы.Найти("ОтборОрганизация") <> Неопределено Тогда
		ПараметрыВыполнения.Вставить("Организация", Форма.ОтборОрганизация);
	ИначеЕсли Форма.Элементы.Найти("ОтборТекущаяОрганизация") <> Неопределено Тогда
		ПараметрыВыполнения.Вставить("Организация", Форма.ОтборТекущаяОрганизация);
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОсновнаяОрганизация") Тогда
		ПараметрыВыполнения.Вставить("Организация", Форма.ОсновнаяОрганизация);
	КонецЕсли;
	
	Возврат ПараметрыВыполнения;
	
КонецФункции

#КонецОбласти
