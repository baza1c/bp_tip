#Область ПрограммныйИнтерфейс

// Возвращает соответствие вида транспортного средства и сведений, связанных с ним
// 
// Возвращаемое значение:
//  Соответствие - ключ - вид транспортного средства, значение - структура сведений
//                        (см. РасчетИмущественныхНалогов.ВидыИКатегорииТранспортныхСредств())
//
Функция СведенияОВидахТранспортныхСредств() Экспорт
	
	Сведения = Новый Соответствие; // Ключ - вид транспортного средства, значение - структура со сведениями
	
	ВидыИКатегорииТС = РасчетИмущественныхНалогов.ВидыИКатегорииТранспортныхСредств();
	ИменаСвойств = Новый Массив;
	Для Каждого Колонка Из ВидыИКатегорииТС.Колонки Цикл
		Если Колонка.Имя = "Вид" Тогда
			Продолжить;
		КонецЕсли;
		ИменаСвойств.Добавить(Колонка.Имя);
	КонецЦикла;
	
	ИменаСвойствСтрока = СтрСоединить(ИменаСвойств, ",");
	
	Для Каждого СтрокаВидаТС Из ВидыИКатегорииТС Цикл
		
		ЗначенияСвойствВидаТС = Новый Структура(ИменаСвойствСтрока);
		ЗаполнитьЗначенияСвойств(ЗначенияСвойствВидаТС, СтрокаВидаТС);
		Сведения.Вставить(
			Перечисления.ВидыТранспортныхСредств[СтрокаВидаТС.Вид],
			ЗначенияСвойствВидаТС);
		
	КонецЦикла;
	
	Возврат Сведения;
	
КонецФункции

// Возвращает 2-значный номер категории, применяемый в формализованных формах ФНС, по переданному виду транспортного средства
//
// Параметры:
//  ВидТранспортногоСредства - ПеречислениеСсылка.ВидыТранспортныхСредств 
//  СведенияОВидахТранспортныхСредств - Соответствие - см. СведенияОВидахТранспортныхСредств()
// 
// Возвращаемое значение:
//   - Строка, Неопределено - 2-значный номер категории
//
Функция НомерКатегорииПоВидуТранспортногоСредства(ВидТранспортногоСредства, СведенияОВидахТранспортныхСредств) Экспорт
	
	КатегорияТС = Неопределено;
	
	Сведения = СведенияОВидахТранспортныхСредств[ВидТранспортногоСредства];
	Если Сведения <> Неопределено Тогда
		КатегорияТС = Сведения.НомерКатегории;
	КонецЕсли;
	
	Возврат КатегорияТС;
	
КонецФункции

#КонецОбласти
