
#Область СлужебныйПрограммныйИнтерфейс

// Открывает подчиненную форму в уже открытом окне.
//  Предварительно ищет уже открытую форму по переданному имени и ключу уникальности в кэше
//  и если такая есть, то активизирует ее.
//
// Параметры:
//  ИмяОткрываемойФормы - Строка - полное имя открываемой формы 
//  Форма - ФормаКлиентскогоПриложения - форма, в окне которой требуется открыть подчиненную форму
//  ПараметрыФормы - Структура - параметры формы
//  КлючУникальности - Строка - ключ уникальности открываемой формы.
//                              Требуется для открытия нескольких форм с одинаковым именем
//  ОткрытыеФормы - Соответствие, Неопределено - кэш открытых форм, ускоряет поиск ранее открытых форм
//
Процедура ОткрытьФормуВОкнеФормы(ИмяОткрываемойФормы, Форма, ПараметрыФормы = Неопределено, КлючУникальности = "", ОткрытыеФормы = Неопределено) Экспорт
	
	//В мобильном клиенте все формы открываются в одном окне-экране.
	//Вернуться в родительскую форму из подчиненной невозможно.
	Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
		ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, Форма);
		Возврат;
	КонецЕсли;
	
	Если ОткрытыеФормы = Неопределено Тогда
		ОткрытыеФормы = Новый Соответствие;
	КонецЕсли;
	
	// При открытии форм внутри существующего окна ключ уникальности не используется для поиска уже открытой
	// подчиненной формы, поэтому перед открытием проверяем наличие открытой ранее формы через кэш ОткрытыеФормы
	ОткрываемаяФорма = ОткрытыеФормы.Получить(ИмяОткрываемойФормы + КлючУникальности);
	
	Если ОткрываемаяФорма = Неопределено Тогда
		
		ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
		ПараметрыОткрытия.ИмяФормы = ИмяОткрываемойФормы;
		ПараметрыОткрытия.Владелец = Форма;
		ПараметрыОткрытия.Уникальность = КлючУникальности;
		ПараметрыОткрытия.Окно = Форма.Окно;
		ПараметрыОткрытия.Вставить("ОткрытыеФормы", ОткрытыеФормы);
		
		ПараметрыОткрытия.ОбработчикОткрытияФормы = Новый ОписаниеОповещения(
			"ПослеПоказаФормыОжидания", ЭтотОбъект, ПараметрыОткрытия);
		ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
		
	Иначе
		
		ОткрытьФорму(ОткрываемаяФорма, Форма.Окно);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеПоказаФормыОжидания(ПараметрыФормы, ПараметрыОткрытия) Экспорт
	
	ОткрываемаяФорма = ПолучитьФорму(ПараметрыОткрытия.ИмяФормы,
		ПараметрыФормы,
		ПараметрыОткрытия.Владелец,
		ПараметрыОткрытия.Уникальность,
		ПараметрыОткрытия.Окно,
		ПараметрыОткрытия.НавигационнаяСсылка);
	
	ОткрытьФорму(ОткрываемаяФорма, ПараметрыОткрытия.Окно);
	
	ПараметрыОткрытия.ОткрытыеФормы.Вставить(ПараметрыОткрытия.ИмяФормы + ПараметрыОткрытия.Уникальность,
		ОткрываемаяФорма);
	
КонецПроцедуры

#КонецОбласти