#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьДекларируемоеИмущество
//
Процедура ОпределитьЕстьИмуществоПоКадастровойСтоимости(ЕстьИмуществоПоКадастровойСтоимости, Организация, НачалоПериода, КонецПериода) Экспорт
	
	ЕстьИмуществоПоКадастровойСтоимости = УправлениеВнеоборотнымиАктивамиПовтИсп.ЕстьИмуществоПоКадастровойСтоимости(Организация, НачалоПериода, КонецПериода)
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьДекларируемоеИмущество
//
Процедура ОпределитьЕстьИмуществоПоСреднегодовойСтоимости(ЕстьИмуществоПоСреднегодовойСтоимости, Организация, НачалоПериода, КонецПериода) Экспорт
	
	ЕстьИмуществоПоСреднегодовойСтоимости = УправлениеВнеоборотнымиАктивамиПовтИсп.ЕстьИмуществоПоСреднегодовойСтоимости(Организация, НачалоПериода, КонецПериода);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.КатегорииПодчиненияЗемельныхУчастковПоВидамНалогов
//
Процедура ЗаполнитьКатегорииПодчиненияЗемельныхУчастковПоВидамНалогов(КатегорииПодчинения) Экспорт
	
	КатегорииПодчинения = Новый Массив;
	КатегорииПодчинения.Добавить("103"); // ЗемельныйНалог_ГородФедеральногоЗначения
	КатегорииПодчинения.Добавить("204"); // ЗемельныйНалог_ГородскойОкруг
	КатегорииПодчинения.Добавить("211"); // ЗемельныйНалог_ГородскойОкругСВнутригородскимДелением
	КатегорииПодчинения.Добавить("212"); // ЗемельныйНалог_ВнутригородскойОкруг
	КатегорииПодчинения.Добавить("214"); // ЗемельныйНалог_МуниципальныйОкруг
	КатегорииПодчинения.Добавить("305"); // ЗемельныйНалог_МежселеннаяТерритория
	КатегорииПодчинения.Добавить("310"); // ЗемельныйНалог_СельскоеПоселение
	КатегорииПодчинения.Добавить("313"); // ЗемельныйНалог_ГородскоеПоселение
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КатегорииПодчинения", КатегорииПодчинения);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПОДСТРОКА(РегистрацияЗемельныхУчастков.КБК, 11, 3) КАК КатегорияПодчинения
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|ГДЕ
	|	ПОДСТРОКА(РегистрацияЗемельныхУчастков.КБК, 11, 3) В (&КатегорииПодчинения)
	|	И ПОДСТРОКА(РегистрацияЗемельныхУчастков.КБК, 18, 3) = ""110""
	|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу";
	
	УстановитьПривилегированныйРежим(Истина);
	
	КатегорииПодчинения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КатегорияПодчинения");
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.КатегорииПодчиненияПатентовПоВидамНалогов
//
Процедура ЗаполнитьКатегорииПодчиненияПатентовПоВидамНалогов(КатегорииПодчинения) Экспорт
	
	КатегорииПодчинения = Новый Массив;
	КатегорииПодчинения.Добавить("010"); // ПСН_ГородскойОкруг
	КатегорииПодчинения.Добавить("020"); // ПСН_МуниципальныйРайон
	КатегорииПодчинения.Добавить("030"); // ПСН_ГородФедеральногоЗначения
	КатегорииПодчинения.Добавить("040"); // ПСН_ГородскойОкругСВнутригородскимДелением
	КатегорииПодчинения.Добавить("050"); // ПСН_ВнутригородскойРайон
	КатегорииПодчинения.Добавить("060"); // ПСН_МуниципальныйОкруг
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КатегорииПодчинения", КатегорииПодчинения);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПОДСТРОКА(ОперацияСПатентом.КБК, 9, 3) КАК КатегорияПодчинения
	|ИЗ
	|	Документ.ОперацияСПатентом КАК ОперацияСПатентом
	|ГДЕ
	|	ПОДСТРОКА(ОперацияСПатентом.КБК, 9, 3) В (&КатегорииПодчинения)
	|	И ПОДСТРОКА(ОперацияСПатентом.КБК, 18, 3) = ""110""
	|	И ОперацияСПатентом.Проведен";
	
	УстановитьПривилегированныйРежим(Истина);
	
	КатегорииПодчинения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КатегорияПодчинения");
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьИмпортТоваровИзТаможенногоСоюза
//
Процедура ОпределитьЕстьИмпортТоваровИзТаможенногоСоюза(ЕстьИмпорт, Период, Периодичность, Организация) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетИмпортныхТоваров") Тогда
		ЕстьИмпорт = Ложь;
		Возврат;
	КонецЕсли; 

	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИмпортТоваровТаможенныйСоюз.Организация
	|ИЗ
	|	РегистрСведений.ИмпортТоваровТаможенныйСоюз КАК ИмпортТоваровТаможенныйСоюз";
	
	МассивУсловий = Новый Массив;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивУсловий.Добавить("ИмпортТоваровТаможенныйСоюз.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Период) И ЗначениеЗаполнено(Периодичность) Тогда
		МассивУсловий.Добавить("ИмпортТоваровТаможенныйСоюз.ДатаПериода = &Период");
		Запрос.УстановитьПараметр("Период", ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, Период));
	КонецЕсли;
	
	Если МассивУсловий.Количество() > 0 Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ
			| "+ СтрСоединить(МассивУсловий, " И ");
	КонецЕсли;
		
	Запрос.Текст = ТекстЗапроса;
	
	ЕстьИмпорт = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьЗаписиКнигиПродаж
//
Процедура ОпределитьЕстьЗаписиКнигиПродаж(ЕстьЗаписи, Период, Периодичность, Организация) Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НДСЗаписиКнигиПродаж.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж";
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить("НДСЗаписиКнигиПродаж.НДС <> 0");
	МассивУсловий.Добавить("НЕ НДСЗаписиКнигиПродаж.ВидЦенности В (&ВидыЦенностей)");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивУсловий.Добавить("НДСЗаписиКнигиПродаж.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Период) И ЗначениеЗаполнено(Периодичность) Тогда
		МассивУсловий.Добавить("НДСЗаписиКнигиПродаж.Период МЕЖДУ &НачалоПериода И  &КонецПериода");
		Запрос.УстановитьПараметр("НачалоПериода", ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, Период));
		Запрос.УстановитьПараметр("КонецПериода", ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период));
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидыЦенностей", Перечисления.ВидыЦенностей.МассивВидовЦенностиНалоговыйАгент());
	
	Если МассивУсловий.Количество() > 0 Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ
			| "+ СтрСоединить(МассивУсловий, " И ");
	КонецЕсли;
		
	Запрос.Текст = ТекстЗапроса;
	
	ЕстьЗаписи = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьЗаписиКнигиПродажНалоговогоАгента
//
Процедура ОпределитьЕстьЗаписиКнигиПродажНалоговогоАгента(ЕстьЗаписи, Период, Периодичность, Организация) Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НДСЗаписиКнигиПродаж.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж";
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить("НДСЗаписиКнигиПродаж.НДС <> 0");
	МассивУсловий.Добавить("НДСЗаписиКнигиПродаж.ВидЦенности В (&ВидыЦенностей)");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивУсловий.Добавить("НДСЗаписиКнигиПродаж.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Период) И ЗначениеЗаполнено(Периодичность) Тогда
		МассивУсловий.Добавить("НДСЗаписиКнигиПродаж.Период МЕЖДУ &НачалоПериода И  &КонецПериода");
		Запрос.УстановитьПараметр("НачалоПериода", ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, Период));
		Запрос.УстановитьПараметр("КонецПериода", ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период));
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидыЦенностей", Перечисления.ВидыЦенностей.МассивВидовЦенностиНалоговыйАгент());
	
	Если МассивУсловий.Количество() > 0 Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ
			| "+ СтрСоединить(МассивУсловий, " И ");
	КонецЕсли;
		
	Запрос.Текст = ТекстЗапроса;
	
	ЕстьЗаписи = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьИмуществоЕСГС
//
Процедура ОпределитьЕстьИмуществоЕСГС(ЕстьИмуществоЕСГС) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодВидаИмущества = ""01""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КБК = ""18210602020021000110""";
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьИмуществоЕСГС = Не Запрос.Выполнить().Пустой();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьОбъектыТорговогоСбора
//
Процедура ОпределитьЭтоПлательщикТорговогоСбора(ПлательщикТорговогоСбора, Организация, Период) Экспорт
	
	ПлательщикТорговогоСбора = УчетнаяПолитика.ПлательщикТорговогоСбора(Организация, Период);
	
КонецПроцедуры 

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловияИсключенияПоТипуОрганизации
//
Процедура ОпределитьЭтоПлательщикНалогаНаПрибыль(ПлательщикНалогаНаПрибыль, Организация, Период) Экспорт
	
	ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьИмущество
//
Процедура ОпределитьЕстьИмущество(ЕстьИмущество, Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если УправлениеВнеоборотнымиАктивамиПовтИсп.ЕстьИмуществоПоКадастровойСтоимости(Организация, НачалоПериода, КонецПериода) Тогда
		ЕстьИмущество = Истина;
	ИначеЕсли Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, НачалоПериода) Тогда
		ЕстьИмущество = Ложь;
	Иначе
		ЕстьИмущество = УправлениеВнеоборотнымиАктивамиПовтИсп.ЕстьИмуществоПоСреднегодовойСтоимости(Организация, НачалоПериода, КонецПериода);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьАкцизыИныеТовары
//
Процедура ОпределитьЕстьАкцизыИныеТовары(ЕстьАкцизыИныеТовары, Организация) Экспорт
	
	ВидыТоваровСпециальныеФормы = Новый Массив;
	// см. АкцизыФормаАлкоголь
	ВидыТоваровСпециальныеФормы.Добавить(Перечисления.ВидыПодакцизныхТоваров.Спирт);
	ВидыТоваровСпециальныеФормы.Добавить(Перечисления.ВидыПодакцизныхТоваров.СпиртосодержащаяПродукция);
	ВидыТоваровСпециальныеФормы.Добавить(Перечисления.ВидыПодакцизныхТоваров.АлкогольнаяПродукция);
	// см. АкцизыФормаТабак
	ВидыТоваровСпециальныеФормы.Добавить(Перечисления.ВидыПодакцизныхТоваров.Табак);
	// см. АкцизыФормаНефтяноеСырье
	ВидыТоваровСпециальныеФормы.Добавить(Перечисления.ВидыПодакцизныхТоваров.НефтяноеСырье);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("ИсключаемыеТовары", ВидыТоваровСпециальныеФормы);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Результат
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|	И НЕ ПорядокУплатыАкцизов.ВидТовара В (&ИсключаемыеТовары)";
	
	ЕстьАкцизыИныеТовары = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьАкцизыПоВидуТоваров
//
Процедура ОпределитьЕстьАкцизыПоВидуТоваров(ЕстьАкцизыПоВидуТоваров, Организация, Условие) Экспорт
	
	ВидыТоваров = Новый Массив;
	Если Условие = УсловияПримененияТребованийЗаконодательства.УсловиеАкцизыФормаАлкоголь() Тогда
		ВидыТоваров.Добавить(Перечисления.ВидыПодакцизныхТоваров.Спирт);
		ВидыТоваров.Добавить(Перечисления.ВидыПодакцизныхТоваров.СпиртосодержащаяПродукция);
		ВидыТоваров.Добавить(Перечисления.ВидыПодакцизныхТоваров.АлкогольнаяПродукция);
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеАкцизыФормаТабак() Тогда
		ВидыТоваров.Добавить(Перечисления.ВидыПодакцизныхТоваров.Табак);
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеАкцизыФормаНефтяноеСырье() Тогда
		ВидыТоваров.Добавить(Перечисления.ВидыПодакцизныхТоваров.НефтяноеСырье);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидыТоваров", ВидыТоваров);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Результат
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|	И ПорядокУплатыАкцизов.ВидТовара В (&ВидыТоваров)";
	
	ЕстьАкцизыПоВидуТоваров = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьАвансыПоАкцизам
//
Процедура ОпределитьЕстьАкцизыОбщийСрок(ЕстьАкцизыОбщийСрок, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Результат
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|	И НЕ ПорядокУплатыАкцизов.ЛьготныйСрокУплаты";
	
	ЕстьАкцизыОбщийСрок = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьАвансыПоАкцизам
//
Процедура ОпределитьЕстьАкцизыЛьготныйСрок(ЕстьАкцизыЛьготныйСрок, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Результат
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|	И ПорядокУплатыАкцизов.ЛьготныйСрокУплаты";
	
	ЕстьАкцизыЛьготныйСрок = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьАвансыПоАкцизам
//
Процедура ОпределитьЕстьАвансыПоАкцизам(ЕстьАвансыПоАкцизам, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Результат
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Организация = &Организация
	|	И ПорядокУплатыАкцизов.АвансыПриЗакупкеСырья";
	
	ЕстьАвансыПоАкцизам = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьОстаткиИОборотыПоСчетуНДФЛ
//
Процедура ОпределитьЕстьОстаткиИОборотыПоСчетуНДФЛ(ЕстьОстаткиИОбороты, НачалоПериода, КонецПериода, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.УстановитьПараметр("Счета",         БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ));
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Счет В (&Счета), , Организация = &Организация) КАК ХозрасчетныйОстаткиИОбороты
	|ГДЕ
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт <> 0
	|	ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт <> 0
	|	ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаОборотДт <> 0
	|	ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаОборотКт <> 0
	|	ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт <> 0
	|	ИЛИ ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт <> 0";
	
	ЕстьОстаткиИОбороты = НЕ Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьНалогКУплатеПоДекларации3НДФЛ
//
Процедура ОпределитьЭтоПлательщикНДФЛ(ПлательщикНДФЛ, Организация, Период) Экспорт
	
	ПлательщикНДФЛ = УчетнаяПолитика.ПлательщикНДФЛ(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьНалогКУплатеПоДекларации3НДФЛ
//
Процедура УточнитьЕстьНалогКУплатеПоДекларации3НДФЛ(ЕстьНалог, Организация, Период) Экспорт
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ОрганизацияЗарегистрированаТекущимДнем
//
Процедура УточнитьДатуРегистрацииОрганизации(ДатаРегистрации, Организация) Экспорт
	
	ДатаРегистрации = ОбщегоНазначенияБПВызовСервераПовтИсп.ДатаРегистрацииОрганизации(Организация);
	
	Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда 
		// Проверяем текущую дату. Считаем, что если дата регистрации не указана, то функционал выполнения правила,
		// для которого проверяется текущее условие, должен обеспечивать проверку на заполнение необходимых 
		// реквизитов организации
		ДатаРегистрации = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьЗадолженностьУчредителейПоОплатеУставногоКапитала
//
Процедура ОпределитьЭтоОбособленноеПодразделение(ЭтоОбособленноеПодразделение, Организация) Экспорт
	
	ЭтоОбособленноеПодразделение = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоОбособленноеПодразделение(Организация);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьЗадолженностьУчредителейПоОплатеУставногоКапитала
//
Процедура ОпределитьКодОКОПФНекорректен(КодОКОПФНекорректен, Организация) Экспорт
	
	КодОКОПФ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "КодОКОПФ");
	КодыСтатистикиООО = Справочники.Организации.КодыСтатистикиПоОрганизационноПравовойФорме("ОБЩЕСТВО С ОГРАНИЧЕННОЙ ОТВЕТСТВЕННОСТЬЮ");
	Если КодОКОПФ <> КодыСтатистикиООО.КодОКОПФ Тогда 
		КодОКОПФНекорректен = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ЕстьЗадолженностьУчредителейПоОплатеУставногоКапитала
//
Процедура ОпределитьЕстьЗадолженностьУчредителей(ЕстьЗадолженностьУчредителей, Организация, ТекущаяДата) Экспорт
	
	ЗадолженностьУчредителей = Справочники.Организации.ЗадолженностьУчредителей(Организация, ТекущаяДата);
	ЕстьЗадолженностьУчредителей = ЗадолженностьУчредителей.Количество() > 0;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.ПрименяетсяАУСН
//
Процедура ОпределитьПрименяетсяАУСН(ПрименяетсяАУСН, Организация, Период, Периодичность) Экспорт
	
	ЗначениеАУСН = Перечисления.СистемыНалогообложения.АУСН;
	
	ПрименяетсяАУСН = УчетнаяПолитика.СистемаНалогообложения(Организация, Период) = ЗначениеАУСН
		Или УчетнаяПолитика.СистемаНалогообложения(Организация, ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период)) = ЗначениеАУСН;
		
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.СдаетсяОтчетностьСЗВ_СТАЖ
//
Процедура ОпределитьОрганизацияПрименяетРайонныйКоэффициент(ПрименятьРайонныйКоэффициент, Организация, Период) Экспорт
	
	ПараметрыВыполнения = Новый Структура("Организация", Организация);
	ПрименятьРайонныйКоэффициент = ПолучитьФункциональнуюОпцию("ПрименятьРайонныйКоэффициент", ПараметрыВыполнения)
		Или ПолучитьФункциональнуюОпцию("ПрименятьРайонныйКоэффициентВПодразделениях", ПараметрыВыполнения);
		
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.СдаетсяОтчетностьСЗВ_СТАЖ
//
Процедура ОпределитьОрганизацияПрименяетСевернуюНадбавку(ПрименятьСевернуюНадбавку, Организация, Период) Экспорт
	
	ПараметрыВыполнения = Новый Структура("Организация", Организация);
	ПрименятьСевернуюНадбавку = ПолучитьФункциональнуюОпцию("ПрименятьСевернуюНадбавку", ПараметрыВыполнения)
		Или ПолучитьФункциональнуюОпцию("ПрименятьСевернуюНадбавкуВПодразделениях", ПараметрыВыполнения);
		
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.СдаетсяОтчетностьСЗВ_СТАЖ
//
Процедура ОпределитьОрганизацияРаботаетВСельскойМестности(РаботаетВСельскойМестности, Организация, Период) Экспорт
	
	РаботаетВСельскойМестности = УчетЗарплаты.ОрганизацияРаботаетВСельскойМестности(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.СдаетсяОтчетностьСЗВ_СТАЖ
//
Процедура ОпределитьЕстьДоговорыПодряда(ДоговорыПодряда, Организация, Период, Периодичность) Экспорт
	
	ДоговорыПодряда = УчетЗарплаты.ЕстьДоговорыПодряда(Организация, Период, Периодичность);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.СдаетсяОтчетностьСЗВ_СТАЖ
//
Процедура ОпределитьЕстьДокументыОтпускаБезСодержания(ОтпускаБезСодержания, Организация, Период, Периодичность) Экспорт
	
	ОтпускаБезСодержания = УчетЗарплаты.ЕстьДокументыОтпускаБезСодержания(Организация, Период, Периодичность);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.СдаетсяОтчетностьСЗВ_СТАЖ
//
Процедура ОпределитьЕстьСотрудники_ЧАЭС(Сотрудники_ЧАЭС, Организация, Период) Экспорт
	
	Сотрудники_ЧАЭС = УчетЗарплаты.ЕстьСотрудники_ЧАЭС(Организация, КонецГода(Период));
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьИспользуетсяТрудНаемныхРаботников(Признак, УсловияПримененияПравил, Организация) Экспорт
	
	Признак = УсловияПримененияПравил.Получить("ИспользуетсяТрудНаемныхРаботников");
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьВыплачиваетсяЗарплата(Признак, УсловияПримененияПравил, Организация, Период) Экспорт
	
	ПериодыВыплатыЗарплаты = УсловияПримененияПравил.Получить("ПериодыВыплатыЗарплаты");
	Признак = ПериодыВыплатыЗарплаты.Найти(Период) <> Неопределено;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьЗарегистрированыКадровыеМероприятия(Признак, УсловияПримененияПравил, Организация, Период, Периодичность) Экспорт
	
	УсловияПоиска = Новый Структура;
	УсловияПоиска.Вставить("Период", НачалоДня(Период));
	УсловияПоиска.Вставить("Периодичность", Периодичность);
	КадровыеМероприятия = УсловияПримененияПравил.Получить("КадровыеМероприятия");
	Признак = КадровыеМероприятия.НайтиСтроки(УсловияПоиска).Количество() > 0;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьСистемуНалогообложения(СистемаНалогообложенияСовпадает, Условие, Организация, Период, Периодичность) Экспорт
	
	Если Условие = УсловияПримененияТребованийЗаконодательства.УсловиеОбщаяСистемаНалогообложения() Тогда
		СистемаНалогообложенияСовпадает = УчетнаяПолитика.СистемаНалогообложения(Организация, Период) = Перечисления.СистемыНалогообложения.Общая
			Или УчетнаяПолитика.СистемаНалогообложения(Организация, ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период)) = Перечисления.СистемыНалогообложения.Общая;
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеУпрощеннаяСистемаНалогообложения() Тогда
		СистемаНалогообложенияСовпадает = УчетнаяПолитика.СистемаНалогообложения(Организация, Период) = Перечисления.СистемыНалогообложения.Упрощенная
			Или УчетнаяПолитика.СистемаНалогообложения(Организация, ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период)) = Перечисления.СистемыНалогообложения.Упрощенная;
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеНалогНаПрофессиональныйДоход() Тогда
		СистемаНалогообложенияСовпадает = УчетнаяПолитика.СистемаНалогообложения(Организация, Период) = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход
			Или УчетнаяПолитика.СистемаНалогообложения(Организация, ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период)) = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьПлательщикОтдельныхВидовНалогов(ПлательщикНалога, Условие, Организация, Период, Периодичность) Экспорт
	
	Если Условие = УсловияПримененияТребованийЗаконодательства.УсловиеПлательщикиЕНВД() Тогда
		ПлательщикНалога = УчетнаяПолитика.ПлательщикЕНВД(Организация, Период) 
			Или УчетнаяПолитика.ПлательщикЕНВД(Организация, ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период));
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеПлательщикиНДС() Тогда
		КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период);
		ПлательщикНалога = УчетнаяПолитика.ПлательщикНДС(Организация, Период)
			Или УчетнаяПолитика.ПлательщикНДС(Организация, ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период));
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеПлательщикиНДСНаУСН() Тогда
		КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период);
		ПлательщикНалога = (УчетнаяПолитика.СистемаНалогообложения(Организация, Период) = Перечисления.СистемыНалогообложения.Упрощенная
			И УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, Период)
			Или УчетнаяПолитика.СистемаНалогообложения(Организация, КонецПериода) = Перечисления.СистемыНалогообложения.Упрощенная
			И УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, КонецПериода))
			И Не (УчетнаяПолитика.ПлательщикНДС(Организация, Период)
			Или УчетнаяПолитика.ПлательщикНДС(Организация, КонецПериода));
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьПорядокУплатыАвансовПоНалогуНаПрибыль(УсловиеВыполняется, Условие, Организация, Период) Экспорт
	
	Если Условие = УсловияПримененияТребованийЗаконодательства.УсловиеНалогНаПрибыльАвансыЕжеквартально() Тогда
		УсловиеВыполняется = УчетнаяПолитика.ПорядокУплатыАвансов(Организация, Период) = Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.Ежеквартально;
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеНалогНаПрибыльАвансыЕжемесячно() Тогда
		УсловиеВыполняется = УчетнаяПолитика.ПорядокУплатыАвансов(Организация, Период) = Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.Ежемесячно;
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеНалогНаПрибыльАвансыПоФактическойПрибыли() Тогда
		УсловиеВыполняется = УчетнаяПолитика.ПорядокУплатыАвансов(Организация, Период) = Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.ПоФактическойПрибыли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьВариантБухгалтерскойОтчетности(УсловиеВыполняется, Условие, Организация, Период) Экспорт
	
	Если Условие = УсловияПримененияТребованийЗаконодательства.УсловиеБухгалтерскаяОтчетностьМалыеПредприятия() Тогда
		УсловиеВыполняется = УчетнаяПолитика.ВариантБухгалтерскойОтчетности(Организация, Период) = Перечисления.ВариантыБухгалтерскойОтчетности.ДляМалыхПредприятий;
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеБухгалтерскаяОтчетностьНекоммерческиеОрганизации() Тогда
		УсловиеВыполняется = УчетнаяПолитика.ВариантБухгалтерскойОтчетности(Организация, Период) = Перечисления.ВариантыБухгалтерскойОтчетности.ДляНекоммерческихОрганизаций;
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеБухгалтерскаяОтчетностьОбщаяФорма() Тогда
		УсловиеВыполняется = УчетнаяПолитика.ВариантБухгалтерскойОтчетности(Организация, Период) = Перечисления.ВариантыБухгалтерскойОтчетности.ОбщиеПравила;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьЕстьТранспортныеСредства(ЕстьТранспортныеСредства, Организация, Период, Периодичность) Экспорт
	
	ЕстьТранспортныеСредства = РегистрыСведений.РегистрацияТранспортныхСредств.ЕстьТранспортныеСредства(Период, Периодичность, Организация);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьЕстьЗемельныеУчастки(ЕстьЗемельныеУчастки, Организация, Период, Периодичность) Экспорт
	
	ЕстьЗемельныеУчастки = РегистрыСведений.РегистрацияЗемельныхУчастков.ЕстьЗемельныеУчастки(Период, Периодичность, Организация);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьЕстьЛьготыПоИмуществу(ЕстьЛьготыПоИмуществу, Организация, Период, Периодичность) Экспорт
	
	ЕстьЛьготыПоИмуществу = РегистрыСведений.РегистрацияТранспортныхСредств.ЕстьЛьготыПоТранспортномуНалогу(Организация, Период, Периодичность)
		Или РегистрыСведений.РегистрацияЗемельныхУчастков.ЕстьЛьготыПоЗемельномуНалогу(Организация, Период, Периодичность);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьПлательщикФиксированныхВзносов(Плательщик, Организация, Период) Экспорт
	
	Плательщик = УчетнаяПолитика.ПлательщикФиксированныхВзносовЗаПериод(Организация, НачалоГода(Период), КонецГода(Период));
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьПорядокУплатыФиксированныхСтраховыхВзносов(УсловиеВыполняется, Условие, Организация, Период) Экспорт
	
	Если Условие = УсловияПримененияТребованийЗаконодательства.УсловиеФиксированныеСтраховыеВзносы_Квартал() Тогда
		ПериодичностьУплатыФиксированныхСтраховыхВзносов = 
			УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период);
		УсловиеВыполняется = (ПериодичностьУплатыФиксированныхСтраховыхВзносов = Перечисления.Периодичность.Квартал);
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеФиксированныеСтраховыеВзносы_Год() Тогда
		ПериодичностьУплатыФиксированныхСтраховыхВзносов = 
			УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, КонецГода(Период));
		УсловиеВыполняется = (ПериодичностьУплатыФиксированныхСтраховыхВзносов = Перечисления.Периодичность.Год);
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеФиксированныеСтраховыеВзносы_Месяц() Тогда
		ПериодичностьУплатыФиксированныхСтраховыхВзносов =
			УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период);
		УсловиеВыполняется = (ПериодичностьУплатыФиксированныхСтраховыхВзносов = Перечисления.Периодичность.Месяц);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьУплачиватьДобровольныеВзносыВФСС(ДобровольныеВзносыВФСС, Организация, Период) Экспорт
	
	ДобровольныеВзносыВФСС = УчетнаяПолитика.УплачиватьДобровольныеВзносыВФСС(Организация, НачалоГода(Период));
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьУпрощенныйЗачетФиксированныхСтраховыхВзносов(УпрощенныйЗачет, Организация, Период) Экспорт
	
	УпрощенныйЗачет = УчетСтраховыхВзносовИППовтИсп.УпрощенныйЗачетФиксированныхВзносов(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьПлательщикФиксированныхВзносовНаДатуЗакрытия(Плательщик, Организация, Период, ДатаЗакрытия) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаЗакрытия) Тогда
		Плательщик = Ложь;
		Возврат;
	КонецЕсли;
	
	ПлательщикФиксированныхВзносов = УчетнаяПолитика.ПлательщикФиксированныхВзносовЗаПериод(
			Организация,
			НачалоГода(Период),
			НачалоДня(ДатаЗакрытия) - 1);
	Плательщик = НачалоГода(ДатаЗакрытия) = НачалоГода(Период) И ПлательщикФиксированныхВзносов;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьВозможнаСверкаНДСЗаПериод(ВозможнаСверкаНДСЗаПериод, Условие, Период, Периодичность, Организация) Экспорт
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, Период);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период);
		
	ВозможнаСверкаНДСЗаПериод = Ложь;
	Если Условие = УсловияПримененияТребованийЗаконодательства.УсловиеСверкаДанныхНДС() Тогда
		ВозможнаСверкаНДСЗаПериод = Обработки.СверкаДанныхУчетаНДС.ВозможнаСверкаНДСЗаПериод(НачалоПериода, КонецПериода, Организация);
		
	ИначеЕсли Условие = УсловияПримененияТребованийЗаконодательства.УсловиеЖурналСчетовФактур() Тогда
		Если УчетнаяПолитика.СистемаНалогообложения(Организация, Период) = Перечисления.СистемыНалогообложения.Упрощенная
			И Не УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, Период) Тогда
			
			ВозможнаСверкаНДСЗаПериод = Обработки.СверкаДанныхУчетаНДС.ВозможнаСверкаНДСЗаПериод(НачалоПериода, КонецПериода, Организация);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьВыставлениеСчетовФактурНеплательщикамиНДС(ВыставлениеСчетовФактурНеплательщикамиНДС, Период, Периодичность, Организация) Экспорт
	
	КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период);
	ВыставлениеСчетовФактурНеплательщикамиНДС = УчетнаяПолитика.СистемаНалогообложения(Организация, Период) = Перечисления.СистемыНалогообложения.Упрощенная
		И УчетнаяПолитика.СистемаНалогообложения(Организация, КонецПериода) = Перечисления.СистемыНалогообложения.Упрощенная
		И Не УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, Период)
		И Не УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, КонецПериода);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьСведенияОДоходахНДФЛ(ЕстьДоходы, Организация, НачалоПериода, КонецПериода) Экспорт
	
	ЕстьДоходы = УчетЗарплаты.ЕстьСведенияОДоходахНДФЛ(Организация, НачалоПериода, КонецПериода);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ПроверитьЭтоНекоммерческаяОрганизация(НекоммерческаяОрганизация, Организация) Экспорт
	
	НекоммерческаяОрганизация = Справочники.Организации.ЭтоНекоммерческаяОрганизация(Организация);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьПрименяетсяУСНПатент(ПрименяетсяУСНПатент, Организация, Период) Экспорт
	
	ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьПлательщикЕНП(ПлательщикЕНП, Организация, Период) Экспорт
	
	ПлательщикЕНП = УчетнаяПолитика.ПлательщикЕНП(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьЕстьВыплатыСотрудникамАУСН(ВыплатыСотрудникамАУСН, Организация, Период) Экспорт
	
	ВыплатыСотрудникамАУСН = УчетЗарплаты.ОформленыПлатежныеПорученияНаВыплатыСотрудникамАУСН(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьНапоминатьОСменеОбъектаУСН(СменаОбъектаУСН, Организация) Экспорт
	
	// 1. Проверка ввода остатков
	Переход = НастройкиСистемыНалогообложенияФормыВызовСервера.НастройкиДляБаннераСменыОбъектаУСН(Организация);
	Если ЗначениеЗаполнено(Переход) Тогда
		НужноНапоминать = Переход.Напоминать;
	Иначе
		НужноНапоминать = Ложь;
	КонецЕсли;
	
	// 2. Проверка даты
	// Если дата меньше равно 25 апреля (дата первого авансового платежа) то ложь
	ДатаОкончанияНапоминания = (Месяц(ТекущаяДатаСеанса()) <= 4) И (День(ТекущаяДатаСеанса()) <= 25);
	
	СменаОбъектаУСН = НужноНапоминать И ДатаОкончанияНапоминания;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьНужноЗаявлениеОЗачетеВзносов(ЗаявлениеОЗачетеВзносов, Организация, Период) Экспорт
	
	// Заявление ИП подает для резерва денег на ЕНС для УСН доходы-расходы и ОСНО только в 2023 г.
	// С 2024 г. в расчете налога можно зачесть суммы взноса до срока списания
	ЗаявлениеОЗачетеВзносов = Не УчетСтраховыхВзносовИППовтИсп.УпрощенныйЗачетФиксированныхВзносов(Организация, Период, Ложь)
		Или Период >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьУплачиваетсяВзносСДоходов(УплачиваетсяВзносСДоходов, Организация, Период) Экспорт
	
	УплачиваетсяВзносСДоходов = УчетСтраховыхВзносовИППовтИсп.УплачиваетсяВзносСДоходов(Организация, НачалоГода(Период), КонецГода(Период));
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьЕжемесячныйПлатежНаЕНС(ЕжемесячныйПлатежНаЕНС, УсловияПримененияПравил, Организация, Период, Периодичность, ИспользуетсяТрудНаемныхРаботников) Экспорт
	
	ЕжемесячныйПлатежНаЕНС = Истина;
	
	// С 2025 г. для ИП взнос за себя по единому тарифу нужно заплатить до 28 декабря
	ЕдиныйТарифИПВключенВЕНП = УчетСтраховыхВзносовИПКлиентСервер.ЕдиныйТарифВключенВЕдиныйНалоговыйПлатеж(Период)
		И КонецМесяца(ДобавитьМесяц(Период, 1)) = КонецГода(Период);
	
	// Для тарифа "нулевой" отчетности ежемесячные платежи по ЕНС в общем случае не производятся,
	// оплачивается только в конце года единый тариф по страховому взносу ИП
	Если Не ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности()
		И ОбщегоНазначения.РазделениеВключено()
		И ВРег(Метаданные.Имя) = ВРег("БухгалтерияПредприятияБазовая1")
		И Не ТарификацияБП.РазрешенТолькоИнтерфейсЭлектронныхТрудовыхКнижек() Тогда
		
		ЕжемесячныйПлатежНаЕНС = ЕдиныйТарифИПВключенВЕНП;
		Возврат;
	КонецЕсли;
	
	ЭтоФизЛицо = Не УсловияПримененияПравил.Получить("ЭтоЮридическоеЛицо");
	СистемаНалогообложения = УчетнаяПолитика.СистемаНалогообложения(Организация, Период);
	СистемаНалогообложенияКонецПериода = УчетнаяПолитика.СистемаНалогообложения(Организация, ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, Период));
	ИспользуетсяТрудНаемныхРаботников = УсловияПримененияПравил.Получить("ИспользуетсяТрудНаемныхРаботников");
	
	Если ЭтоФизЛицо И (СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная
		Или СистемаНалогообложенияКонецПериода = Перечисления.СистемыНалогообложения.Упрощенная)
		И Не ИспользуетсяТрудНаемныхРаботников Тогда
		
		Если ЕдиныйТарифИПВключенВЕНП Тогда
			ЕжемесячныйПлатежНаЕНС = Истина;
		Иначе
			ЕжемесячныйПлатежНаЕНС = УсловияПримененияТребованийЗаконодательстваБП.МесяцыНерегулярногоЕдиногоНалоговогоПлатежа().Найти(Месяц(Период) + 1) <> Неопределено;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если ЭтоФизЛицо И УчетнаяПолитика.ПрименяетсяТолькоУСНПатент(Организация, Период)
		И Не ИспользуетсяТрудНаемныхРаботников Тогда
		
		Если ЕдиныйТарифИПВключенВЕНП Тогда
			ЕжемесячныйПлатежНаЕНС = Истина;
		Иначе
			// Для ИП только на патенте добавлять задачи по уведомлениям и единому налоговому платежу не требуется
			ЕжемесячныйПлатежНаЕНС = Ложь;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если ЭтоФизЛицо И СистемаНалогообложения = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход Тогда
		// Для самозанятых добавлять задачи по уведомлениям и единому налоговому платежу не требуется
		ЕжемесячныйПлатежНаЕНС = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьВыплатыИностраннымКонтрагентам(ВыплатыИностраннымКонтрагентам, Организация, НачалоПериода, КонецПериода) Экспорт
	
	ВыплатыИностраннымКонтрагентам = УчетДенежныхСредствБП.ПроизводилисьВыплатыИностраннымКонтрагентам(Организация, НачалоПериода, КонецПериода);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.НалогиОтчеты.ПодготовитьДанные
//
Процедура ОпределитьНалогиПорядокКоторыхУстанавливаетсяНаМестах(СписокНалогов) Экспорт
	
	СписокНалогов = РегистрыСведений.ПорядокУплатыНалоговНаМестах.НалогиПорядокКоторыхУстанавливаетсяНаМестах();
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.НалогиОтчеты.ПодготовитьДанные
//
Процедура ОпределитьЕстьНастройкиУчетаНалогаНаПрибыль(ЕстьНастройкиУчетаНалогаНаПрибыль, Организация) Экспорт
	
	ЕстьНастройкиУчетаНалогаНаПрибыль = Ложь;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(НастройкиУчетаНалогаНаПрибыль.Период) КАК КоличествоНастроек
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК НастройкиУчетаНалогаНаПрибыль
	|ГДЕ
	|	НастройкиУчетаНалогаНаПрибыль.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЕстьНастройкиУчетаНалогаНаПрибыль = (Выборка.КоличествоНастроек > 0);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.НалогиОтчеты.ПодготовитьДанные
//
Процедура ОпределитьВариантБухгалтерскойОтчетности(ВариантБухгалтерскойОтчетности, Организация, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УчетнаяПолитикаСрезПоследних.ВариантБухгалтерскойОтчетности
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитикаСрезПоследних";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ВариантБухгалтерскойОтчетности = Выборка.ВариантБухгалтерскойОтчетности;
	Иначе
		ВариантБухгалтерскойОтчетности = Перечисления.ВариантыБухгалтерскойОтчетности.ОбщиеПравила;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.Форма.Настройка.ОбновитьНастройкиУчетнойПолитики
//
Процедура ОпределитьВариантБухгалтерскойОтчетностиДействующейНастройки(Вариант, Организация) Экспорт
	
	Вариант = РегистрыСведений.УчетнаяПолитика.ВариантБухгалтерскойОтчетностиДействующейУчетнойПолитики(Организация);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.НалогиОтчеты.ПодготовитьДанные
//
Процедура ОпределитьПорядокУплатыАвансовПоНалогуНаПрибыль(ПорядокУплаты, Организация, Период) Экспорт
	
	ПорядокУплаты = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.ПорядокУплатыАвансовДействующейНастройки(
		Организация,
		Период);
	
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.Форма.Настройка.ОбновитьНастройкиУчетаНалогаНаПрибыль
//
Процедура ОпределитьПорядокУплатыАвансовДействующейНастройки(Порядок, Организация) Экспорт
	
	Порядок = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.ПорядокУплатыАвансовДействующейНастройки(Организация);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.НалогиОтчеты.ПодготовитьДанные
//
Процедура ОпределитьПорядокУплатыРегиональныхМестныхНалогов(ПорядокУплаты, Организация, ТекущаяДата) Экспорт
	
	ПорядокУплаты = РегистрыСведений.ПорядокУплатыНалоговНаМестах.ПорядокУплатыРегиональныхМестныхНалогов(Организация, ТекущаяДата);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловияИсключенияПоТипуОрганизации
//
Процедура ДобавитьУсловияВидыПодакцизныхТоваров(УсловияИсключения) Экспорт
	
	УсловияИсключения.Добавить(Перечисления.ВидыПодакцизныхТоваров.АлкогольнаяПродукция);
	УсловияИсключения.Добавить(Перечисления.ВидыПодакцизныхТоваров.Спирт);
	УсловияИсключения.Добавить(Перечисления.ВидыПодакцизныхТоваров.СпиртосодержащаяПродукция);
	УсловияИсключения.Добавить(Перечисления.ВидыПодакцизныхТоваров.НефтяноеСырье);
	
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.Форма.ДетальнаяНастройка.ПриСозданииНаСервере
//
Процедура ПроверитьЭтоВидПодакцизныхТоваровАлкогольнаяПродукция(Признак, Ссылка) Экспорт
	
	Признак = (Ссылка = Перечисления.ВидыПодакцизныхТоваров.АлкогольнаяПродукция);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловияКоторымиПользовательУправляетНапрямую
//
Процедура ДополнитьУсловияКоторымиПользовательУправляетНапрямую(Условия) Экспорт
	
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеПроизводствоАлгоколя());
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеПеревозкаАлкоголя());
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеРозничнаяПродажаАлкоголя());
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеРозничнаяПродажаПива());
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеОптоваяТорговляАлкоголем());
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловияЗадачаВключаетсяПользователем
//
Процедура ДополнитьУсловияЗадачаВключаетсяПользователем(Условия) Экспорт
	
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеПлательщикЕСХН());
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеПлательщикНДПИ());
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеПлательщикНалогаНаИгорныйБизнес());
	Условия.Добавить(УсловияПримененияТребованийЗаконодательства.УсловиеПлательщикВодногоНалога());
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьУсловиеВыполняется(УсловиеВыполняется, Условие, Период, Периодичность, Организация) Экспорт
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьУчетЗарплатыИКадровВЭтойПрограмме(УсловиеВыполняется) Экспорт
	
	УсловиеВыполняется = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьНачисляютсяДополнительныеВзносыЧленамЛетныхЭкипажейИлиШахтерам(УсловиеВыполняется, Организация) Экспорт
	
	УсловиеВыполняется = УчетЗарплаты.НачисляютсяДополнительныеВзносыЧленамЛетныхЭкипажейИлиШахтерам(Организация);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьЗарегистрированУдержанныйАгентскийНДФЛ(УсловиеВыполняется, УсловияПримененияПравил, Организация, Период, ВтораяПоловинаМесяца) Экспорт
	
	УсловияПоиска = Новый Структура;
	УсловияПоиска.Вставить("Период", НачалоМесяца(Период));
	УсловияПоиска.Вставить("ВтораяПоловинаМесяца", ВтораяПоловинаМесяца);
	ПериодыУдержанияНДФЛ = УсловияПримененияПравил.Получить("ПериодыУдержанияНДФЛ");
	УсловиеВыполняется = ПериодыУдержанияНДФЛ.НайтиСтроки(УсловияПоиска).Количество() > 0;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьНачисленыВзносыНаТравматизм(УсловиеВыполняется, УсловияПримененияПравил, Организация, Период) Экспорт
	
	ПериодыВыплатыЗарплаты = УсловияПримененияПравил.Получить("ПериодыВыплатыЗарплаты");
	ВыплачиваетсяЗарплата = ПериодыВыплатыЗарплаты.Найти(НачалоМесяца(Период)) <> Неопределено;
	
	Если ВыплачиваетсяЗарплата Тогда
		УсловиеВыполняется = Истина;
	Иначе
		УсловиеВыполняется = УчетЗарплаты.НачисленыВзносыНаТравматизм(УсловияПримененияПравил, НачалоМесяца(Период));
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьНачисленыСтраховыеВзносы(УсловиеВыполняется, УсловияПримененияПравил, Организация, Период) Экспорт
	
	ПериодыВыплатыЗарплаты = УсловияПримененияПравил.Получить("ПериодыВыплатыЗарплаты");
	ВыплачиваетсяЗарплата = ПериодыВыплатыЗарплаты.Найти(НачалоМесяца(Период)) <> Неопределено;
	
	Если ВыплачиваетсяЗарплата Тогда
		УсловиеВыполняется = Истина;
	Иначе
		УсловиеВыполняется = УчетЗарплаты.НачисленыСтраховыеВзносы(УсловияПримененияПравил, НачалоМесяца(Период));
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьНачисленыСтраховыеВзносыЧленовЛетныхЭкипажейИлиШахтеров(УсловиеВыполняется, УсловияПримененияПравил, Организация, Период) Экспорт
	
	УсловиеВыполняется = УчетЗарплаты.НачисленыСтраховыеВзносыЧленовЛетныхЭкипажейИлиШахтеров(УсловияПримененияПравил, НачалоМесяца(Период));
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьНачисленыСтраховыеВзносыДобровольные(УсловиеВыполняется, УсловияПримененияПравил, Организация, Период) Экспорт
	
	УсловиеВыполняется = УчетЗарплаты.НачисленыСтраховыеВзносыДобровольные(УсловияПримененияПравил, НачалоМесяца(Период));
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьСтраховойСлучайВНиМ(УсловиеВыполняется, Организация, Период) Экспорт
	
	УсловиеВыполняется = УчетСтраховыхВзносовИП.ИПУплачиваетДобровольныеВзносыНаВНиМ(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьСтраховойСлучайБольничный(УсловиеВыполняется, Организация, Период) Экспорт
	
	УсловиеВыполняется = УчетСтраховыхВзносовИП.СамозанятыйУплачиваетДобровольныеВзносыСФР(Организация, Период);
	
КонецПроцедуры

// Подробнее
// См. УсловияПримененияТребованийЗаконодательства.УсловиеВыполняется
//
Процедура ОпределитьДобровольныеПенсионныеВзносы(УсловиеВыполняется, Организация, Период) Экспорт
	
	УсловиеВыполняется = УчетСтраховыхВзносовИП.СамозанятыйУплачиваетДобровольныеПенсионныеВзносы(Организация, Период);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
