#Область ПрограммныйИнтерфейс

// Выполняет обработку оповещения о записи печатных форм объектов, обновляет
// информацию о блокировке.
//
// Параметры:
//  УправляемаяФорма - УправляемаяФорма
//  ИмяСобытия       - Строка
//   Параметр        - Произвольный - См. описание одноименного параметра в синтакс-помощнике
//                      "ФормаКлиентскогоПриложения.ОбработкаОповещения".
//   Источник        - Произвольный - См. описание одноименного параметра в синтакс-помощнике
//                      "ФормаКлиентскогоПриложения.ОбработкаОповещения".
//
Процедура ОбработкаОповещения(УправляемаяФорма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = ИмяСобытияПриЗаписиПечатныхФорм() Тогда
		
		Если УправляемаяФорма.Объект.Ссылка = Источник Тогда
			
			Структура = Новый Структура("ИдентификаторыФайловСЭЦП,РеквизитыИдентификаторов", Null, Null);
			ЗаполнитьЗначенияСвойств(Структура, УправляемаяФорма);
			Если Структура.ИдентификаторыФайловСЭЦП <> Null
				И Структура.РеквизитыИдентификаторов <> Null Тогда
				
				Если Структура.ИдентификаторыФайловСЭЦП = Неопределено Тогда
					ИдентификаторыФайловСЭЦП = Новый Соответствие;
				Иначе
					ИдентификаторыФайловСЭЦП = Новый Соответствие(Структура.ИдентификаторыФайловСЭЦП);
				КонецЕсли;
				
				Для Каждого ЭлементСтруктуры Из Параметр Цикл
					ИдентификаторКоманды = Структура.РеквизитыИдентификаторов.Получить(ЭлементСтруктуры.Ключ);
					Если ИдентификаторКоманды <> Неопределено Тогда
						ИдентификаторыФайловСЭЦП.Вставить(ИдентификаторКоманды, Истина);
					КонецЕсли;
				КонецЦикла;
				
				УправляемаяФорма.ИдентификаторыФайловСЭЦП = Новый ФиксированноеСоответствие(ИдентификаторыФайловСЭЦП);
				
				ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(УправляемаяФорма);
				
			КонецЕсли;
			
			БлокировкаИзмененияОбъектовКлиентСервер.ОбновитьГруппуБлокировкиИзмененияОбъекта(УправляемаяФорма, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет подключаемые команды в форме списка при обработке оповещения о записи печатных форм.
//
// Параметры:
//  УправляемаяФорма - УправляемаяФорма
//  ИмяСобытия       - Строка
//  Параметр         - Произвольный - См. описание одноименного параметра в синтакс-помощнике
//                      "ФормаКлиентскогоПриложения.ОбработкаОповещения".
//  Источник         - Произвольный - См. описание одноименного параметра в синтакс-помощнике
//                      "ФормаКлиентскогоПриложения.ОбработкаОповещения".
//
Процедура ОбработкаОповещенияВФормеСписка(УправляемаяФорма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = ИмяСобытияПриЗаписиПечатныхФорм() Тогда
		
		ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(УправляемаяФорма);
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.УправлениеПечатью

// См. УправлениеПечатьюКлиентПереопределяемый.ПечатьДокументовВыполнитьКоманду
//
Процедура ПечатьДокументовВыполнитьКоманду(Форма, Команда, ПродолжитьВыполнениеНаСервере, ДополнительныеПараметры) Экспорт
	
	Если Команда.Имя = "СохранитьВPDFИПодписатьФайл" Тогда
		ПодписатьПоНастройкам(Форма,
			ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ЗаписатьНаДиск"));
	ИначеЕсли Команда.Имя = "ОтправитьПодписанныеPDF" Тогда
		ПодписатьПоНастройкам(Форма,
			ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ОтправитьПоПочте"));
	ИначеЕсли Команда.Имя = "ПодписатьПечатныеФормы" Тогда
		Если Форма.Параметры.ПараметрыПечати.Свойство("ЦельПодписания") Тогда
			ПодписатьПечатныеФомыПриПечати(Форма, Форма.Параметры.ПараметрыПечати.ЦельПодписания);
		Иначе
			ПодписатьПечатныеФомыПриПечати(Форма,
				ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.Подписать"));
		КонецЕсли;
	ИначеЕсли Команда.Имя = "НачатьИзменениеПечатнойФормы" Тогда
		ВключитьРедактированиеПечатнойФормы(Форма);
	ИначеЕсли Команда.Имя = "НастроитьИПодписать" Тогда
		ЦельПодписания = Неопределено;
		Форма.Параметры.ПараметрыПечати.Свойство("ЦельПодписания", ЦельПодписания);
		НастроитьИПодписать(Форма, ЦельПодписания);
	ИначеЕсли Команда.Имя = "ОтказатьВПодписанииПечатныхФорм" Тогда
		ОтказатьВПодписании(Форма);
	ИначеЕсли Команда.Имя = "ОзнакомленоСКомментариямиКПечатнойФорме" Тогда
		ОзнакомитьсяСКомментариямиКПечатнойФорме(Форма);
	КонецЕсли;
	
	ИнтеграцияСРаботаВРоссииКлиент.ПечатьДокументовВыполнитьКоманду(Форма, Команда, ПродолжитьВыполнениеНаСервере, ДополнительныеПараметры);
	ИнтеграцияКабинетСотрудникаКлиент.ПечатьДокументовВыполнитьКоманду(Форма, Команда, ПродолжитьВыполнениеНаСервере, ДополнительныеПараметры);
	
КонецПроцедуры

// См. УправлениеПечатьюКлиентПереопределяемый.ПечатьДокументовПослеОткрытия
Процедура ПечатьДокументовПослеОткрытия(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПечатнаяФормаНедоступна Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"НастроитьИПодписать",
			"Доступность",
			Ложь);
		
		ДоступностьКоманд = Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаПечатнаяФормаНедоступна;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СохранитьВPDFИПодписатьФайл",
			"Доступность",
			Ложь);
		
		ДоступностьКоманд = Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаПечатнаяФормаНедоступна;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОтправитьПодписанныеPDF",
			"Доступность",
			Ложь);
		
		ДоступностьКоманд = Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаПечатнаяФормаНедоступна;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПередатьПодписанныеPDFВСервисКабинетСотрудника",
			"Доступность",
			Ложь);
			
		ДоступностьКоманд = Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаПечатнаяФормаНедоступна;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПередатьПодписанныеPDFНаРаботаВРоссии",
			"Доступность",
			Ложь);
			
		ДоступностьКоманд = Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаПечатнаяФормаНедоступна;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			ЭДОБЗККлиентСервер.ПрефиксЭлементовОВозможностиРедактирования() + "КомандаРедактирования",
			"Доступность",
			Ложь);
			
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеПечатью

// Выполняет подписание печатных форм, выведенных в общую форму ПечатьДокументов.
//
// Параметры:
//  УправляемаяФорма              - УправляемаяФорма, общая форма ПечатьДокументов
//  ЦельПодписания                - ПеречислениеСсылка.ДействияСФайламиДокументовЭДОБЗК
//  ДанныеСертификатовОрганизаций - Соответствие
//										* Ключ     - Организация
//										* Значение - Массив, ссылок на
//											- СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//											- СправочникСсылка.Пользователи
//
Процедура ПодписатьПечатныеФомыПриПечати(УправляемаяФорма, ЦельПодписания, НастройкиПодписания = Неопределено) Экспорт
	
	// Сохранение отредактированной печатной формы
	Если УправляемаяФорма.Элементы.Найти("НастройкиПечатныхФорм") <> Неопределено Тогда
		НастройкаПечатнойФормы = УправлениеПечатьюКлиент.НастройкаТекущейПечатнойФормы(УправляемаяФорма);
		Если НастройкаПечатнойФормы <> Неопределено Тогда
			ЭлементТабличныйДокумент = УправляемаяФорма.Элементы.Найти(НастройкаПечатнойФормы.ИмяРеквизита);
			Если ЭлементТабличныйДокумент <> Неопределено И Не ЭлементТабличныйДокумент.Защита Тогда
				УправляемаяФорма[НастройкаПечатнойФормы.ИмяРеквизита] = УправляемаяФорма.ТекущаяПечатнаяФорма;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПечатныеФормы = Новый Массив;
	ВнешниеФайлы = Новый Массив;
	Для Каждого НастройкаПечатнойФормы Из УправляемаяФорма.НастройкиПечатныхФорм Цикл
		
		Если НастройкаПечатнойФормы.Печатать Тогда
			
			Если ТипЗнч(УправляемаяФорма[НастройкаПечатнойФормы.ИмяРеквизита]) = Тип("ТабличныйДокумент") Тогда
				ОписаниеФормы = Новый Структура;
				ОписаниеФормы.Вставить("ПечатнаяФорма", УправляемаяФорма[НастройкаПечатнойФормы.ИмяРеквизита]);
				ОписаниеФормы.Вставить("ИдентификаторПечатнойФормы", НастройкаПечатнойФормы.ИмяМакета);
				ОписаниеФормы.Вставить("Название", НастройкаПечатнойФормы.Название);
				
				ПечатныеФормы.Добавить(ОписаниеФормы);
			ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(УправляемаяФорма, "ФайлыРеквизитовФормы") Тогда
				ВнешниеФайлы.Добавить(УправляемаяФорма.ФайлыРеквизитовФормы.Получить(НастройкаПечатнойФормы.ИмяРеквизита));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если УправляемаяФорма.ПараметрыВывода.Свойство("ПараметрыПечатиВнешнейПечатнойФормы")
		И ЗначениеЗаполнено(УправляемаяФорма.ПараметрыВывода.ПараметрыПечатиВнешнейПечатнойФормы) Тогда
		
		Если Не УправляемаяФорма.Параметры.ПараметрыПечати.Свойство("ДополнительныеПараметры")
			Или Не ЗначениеЗаполнено(УправляемаяФорма.Параметры.ПараметрыПечати.ДополнительныеПараметры) Тогда
			
			УправляемаяФорма.Параметры.ПараметрыПечати.Вставить("ДополнительныеПараметры", Новый Структура);
		КонецЕсли;
		
		Для Каждого ОписаниеПараметра Из УправляемаяФорма.ПараметрыВывода.ПараметрыПечатиВнешнейПечатнойФормы Цикл
			Если ЗначениеЗаполнено(ОписаниеПараметра.Значение)
				И Не УправляемаяФорма.Параметры.ПараметрыПечати.ДополнительныеПараметры.Свойство(ОписаниеПараметра.Ключ) Тогда
				
				УправляемаяФорма.Параметры.ПараметрыПечати.ДополнительныеПараметры.Вставить(
					ОписаниеПараметра.Ключ, ОписаниеПараметра.Значение);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ПутьКПодписантам = "";
	Если УправляемаяФорма.Параметры.ПараметрыПечати.ДополнительныеПараметры.Свойство("ПутьКПодписантам") Тогда
		ПутьКПодписантам = УправляемаяФорма.Параметры.ПараметрыПечати.ДополнительныеПараметры.ПутьКПодписантам;
	КонецЕсли;
	
	РеквизитыОбъектов = ЭДОБЗКВызовСервера.РеквизитыАдресацииОбъектов(УправляемаяФорма.ОбъектыПечати.ВыгрузитьЗначения(), ПутьКПодписантам);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", УправляемаяФорма);
	ДополнительныеПараметры.Вставить("ЦельПодписания", ЦельПодписания);
	ДополнительныеПараметры.Вставить("ПечатныеФормы", ПечатныеФормы);
	ДополнительныеПараметры.Вставить("УникальныйИдентификатор", УправляемаяФорма.УникальныйИдентификатор);
	ДополнительныеПараметры.Вставить("РеквизитыОбъектов", РеквизитыОбъектов);
	ДополнительныеПараметры.Вставить("НастройкиПодписания", НастройкиПодписания);
	ДополнительныеПараметры.Вставить("ВнешниеФайлы", ВнешниеФайлы);
	
	Если ПолучитьФункциональнуюОпциюИнтерфейса("ИспользуетсяЭДОБЗККабинетСотрудника") Тогда
		РезультатПроверки = ВсеФизическиеЛицаДокументовВыгружаются(РеквизитыОбъектов);
	Иначе
		РезультатПроверки = Новый Структура("ЕстьФизическиеЛицаБезКабинетаСотрудника", Ложь);
	КонецЕсли;
	
	Если Не РезультатПроверки.ЕстьФизическиеЛицаБезКабинетаСотрудника Тогда
		ПодписатьПечатныеФомыПриПечатиПродолжение(Новый Структура("Значение", "ПередатьВсе"), ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ЗапомненныйОтвет = ЭДОБЗКВызовСервера.ОтветНаВопросОПередачеДокументовТолькоВСуществующиеКабинеты();
	Если ЗапомненныйОтвет <> Неопределено Тогда
		ПодписатьПечатныеФомыПриПечатиПродолжение(Новый Структура("Значение", ЗапомненныйОтвет), ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ФизическиеЛицаБезКабинетов", РезультатПроверки.ФизическиеЛицаБезКабинетов);
	
	ТекстВопроса = НСтр("ru = 'В документах есть сотрудники, непубликуемые в 1С:Кабинет сотрудника.
		|Продолжить?'");
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("ПередатьВсе", НСтр("ru = 'Передать все'"));
	Кнопки.Добавить("ПередатьТолькоСКабинетами", НСтр("ru = 'Передать только в существующие кабинеты'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
	Оповещение = Новый ОписаниеОповещения("ПодписатьПечатныеФомыПриПечатиПродолжение", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыВопроса.КнопкаПоУмолчанию = Кнопки[1].Значение;
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыВопроса);
	
КонецПроцедуры

Функция ВсеФизическиеЛицаДокументовВыгружаются(РеквизитыОбъектов)
	
	ФизическиеЛицаБезКабинетов = Новый Соответствие;
	Для Каждого РеквзитыОбъекта Из РеквизитыОбъектов Цикл
		
		ФизическиеЛицаДокументаБезКабинетов = Новый Массив;
		Если ТипЗнч(РеквзитыОбъекта.Значение.ФизическоеЛицо) = Тип("Массив") Тогда
			Если РеквзитыОбъекта.Значение.ФизическоеЛицо.Количество() <> РеквзитыОбъекта.Значение.ВыгружаемостьСотрудников.Количество() Тогда
				Для Каждого ФизическоеЛицо Из РеквзитыОбъекта.Значение.ФизическоеЛицо Цикл
					Если РеквзитыОбъекта.Значение.ВыгружаемостьСотрудников.Получить(ФизическоеЛицо) <> Истина Тогда
						ФизическиеЛицаДокументаБезКабинетов.Добавить(ФизическоеЛицо);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе
			Если РеквзитыОбъекта.Значение.ВыгружаемостьСотрудников.Получить(РеквзитыОбъекта.Значение.ФизическоеЛицо) <> Истина Тогда
				ФизическиеЛицаДокументаБезКабинетов.Добавить(РеквзитыОбъекта.Значение.ФизическоеЛицо);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФизическиеЛицаДокументаБезКабинетов) Тогда
			ФизическиеЛицаБезКабинетов.Вставить(РеквзитыОбъекта.Ключ, ФизическиеЛицаДокументаБезКабинетов);
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьФизическиеЛицаБезКабинетаСотрудника", Ложь);
	
	Если ЗначениеЗаполнено(ФизическиеЛицаБезКабинетов) Тогда
		Результат.ЕстьФизическиеЛицаБезКабинетаСотрудника = Истина;
		Результат.Вставить("ФизическиеЛицаБезКабинетов", ФизическиеЛицаБезКабинетов);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПодписатьПечатныеФомыПриПечатиПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат.Свойство("БольшеНеЗадаватьЭтотВопрос")
			И Результат.БольшеНеЗадаватьЭтотВопрос = Истина Тогда
			
			Если Результат.Значение = "ПередатьВсе"
				Или Результат.Значение = "ПередатьТолькоСКабинетами" Тогда
				
				ЭДОБЗКВызовСервера.ЗапомнитьОтветНаВопросОПередачеДокументовТолькоВСуществующиеКабинеты(Результат.Значение);
			КонецЕсли;
		КонецЕсли;
		Если Результат.Значение = "ПередатьВсе"
			Или Результат.Значение = "ПередатьТолькоСКабинетами" Тогда
			
			ПечатныеФормыОбъектов = ЭДОБЗКВызовСервера.ПечатныеФормыОбъектов(
				ДополнительныеПараметры.ПечатныеФормы,
				ДополнительныеПараметры.Форма.ОбъектыПечати,
				ДополнительныеПараметры.Форма.Параметры.ПараметрыПечати,
				ДополнительныеПараметры.УникальныйИдентификатор,
				ДополнительныеПараметры.РеквизитыОбъектов,
				Результат.Значение = "ПередатьТолькоСКабинетами");
				
			Если ЗначениеЗаполнено(ДополнительныеПараметры.ВнешниеФайлы) Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПечатныеФормыОбъектов,
					ЭДОБЗКВызовСервера.ДанныеФайловНаПодпись(ДополнительныеПараметры.ВнешниеФайлы, Новый УникальныйИдентификатор));
			КонецЕсли;
		
			Если Не ЗначениеЗаполнено(ПечатныеФормыОбъектов) Тогда
				Возврат;
			КонецЕсли;
			
			ДополнительныеПараметры.Вставить("ПечатныеФормыОбъектов", ПечатныеФормыОбъектов);
			
			Если ДополнительныеПараметры.Форма.Параметры.ПараметрыПечати.ДополнительныеПараметры.Свойство("ТребуетсяПечатнаяФормаБезПодписи")
				И ДополнительныеПараметры.Форма.Параметры.ПараметрыПечати.ДополнительныеПараметры.ТребуетсяПечатнаяФормаБезПодписи = Истина Тогда
				
				ПоказатьСостояниеСохраненныхБезЦифровыхПодписей(ПечатныеФормыОбъектов.Количество());
				Для Каждого ОписаниеПечатнойФормы Из ПечатныеФормыОбъектов Цикл
					ДанныеИдентификаторовПечатныхФорм = Новый Соответствие;
					ДанныеИдентификаторовПечатныхФорм.Вставить(ОписаниеПечатнойФормы.ИдентификаторПечатнойФормы, Истина);
					ОповеститьОбИзмененииПечатнойФормы(ОписаниеПечатнойФормы.Владелец, ДанныеИдентификаторовПечатныхФорм);
					ПослеПодписанияСохраненныхФорм(Истина, ДополнительныеПараметры); 
				КонецЦикла;
				Возврат;
			КонецЕсли;
			
			Оповещение = Новый ОписаниеОповещения("ПослеПодписанияСохраненныхФорм", ЭтотОбъект, ДополнительныеПараметры);
			ПодписатьФайлы(ПечатныеФормыОбъектов, Оповещение, ДополнительныеПараметры.Форма,
				ДополнительныеПараметры.ЦельПодписания, ДополнительныеПараметры.НастройкиПодписания);
			
		КонецЕсли;
		Если Результат.Значение = "ПередатьТолькоСКабинетами"
			Или Результат.Значение = КодВозвратаДиалога.Отмена Тогда
			
			Если ДополнительныеПараметры.Свойство("ФизическиеЛицаБезКабинетов")
				И ЗначениеЗаполнено(ДополнительныеПараметры.ФизическиеЛицаБезКабинетов) Тогда
				
				ТекстовыйДоукумент = Новый ТекстовыйДокумент;
				ТекстовыйДоукумент.ДобавитьСтроку(НСтр("ru = 'Документы, сотрудники которых не пользуются личными кабинетами:'"));
				Для Каждого ДанныеДокументов Из ДополнительныеПараметры.ФизическиеЛицаБезКабинетов Цикл
					ТекстовыйДоукумент.ДобавитьСтроку(ДанныеДокументов.Ключ);
					Для Каждого ФизическоеЛицо Из ДанныеДокументов.Значение Цикл
						ТекстовыйДоукумент.ДобавитьСтроку(СтрШаблон(" - %1", ФизическоеЛицо));
					КонецЦикла;
				КонецЦикла;
				ТекстовыйДоукумент.Показать(НСтр("ru = 'Протокол проверки подключения к личному кабинету сотрудников'"));
				
			КонецЕсли;
		КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ВыполнитьКомандуОткрытияПодписанныхФайлов(КомандаПечати) Экспорт
	
	Если ЭлектроннаяПодписьКлиент.ИспользоватьЭлектронныеПодписи() Тогда
		ПроверитьСертификатыПодписей(КомандаПечати);
		Возврат Неопределено;
	КонецЕсли;
	
	УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати(КомандаПечати);
	
КонецФункции

Функция РазблокироватьФормуОбъекта(УправляемаяФорма, СсылкаНаОбъект) Экспорт
	
	Результат = ЭДОБЗКВызовСервера.РазблокироватьФормуОбъекта(СсылкаНаОбъект);
	Если ПустаяСтрока(Результат) Тогда
		
		Структура = Новый Структура("ИдентификаторыФайловСЭЦП", Null);
		ЗаполнитьЗначенияСвойств(Структура, УправляемаяФорма);
		Если Структура.ИдентификаторыФайловСЭЦП <> Null Тогда
			УправляемаяФорма.ИдентификаторыФайловСЭЦП = Неопределено;
		КонецЕсли;
		
		ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(УправляемаяФорма);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДополнительныеПараметрыФормыПодписанияДокументов(ОписаниеОповещения = Неопределено, ЗаполнитьПараметрыКоманды = Ложь, ИнициацияПодписания = Ложь) Экспорт
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	ДополнительныеПараметры.Вставить("ЗаполнитьПараметрыКоманды", ЗаполнитьПараметрыКоманды);
	ДополнительныеПараметры.Вставить("ИнициацияПодписания", ИнициацияПодписания);
	Возврат ДополнительныеПараметры;
КонецФункции

Процедура ОткрытьФормуПодписанияВсехПечатныхФорм(ЭлектронныеДокументы, ВладелецФормы = Неопределено, ЦельПодписания = Неопределено, ДополнительныеПараметрыФормы = Неопределено)
	
	Если ДополнительныеПараметрыФормы = Неопределено Тогда
		ДополнительныеПараметрыФормы = ДополнительныеПараметрыФормыПодписанияДокументов();
	КонецЕсли;
	
	ПараметрыОткрытия = ЭДОБЗКВызовСервера.ПараметрыОткрытияФормыПодписанияПрисоединенныхФайлов(
		ЭлектронныеДокументы.ПечатныеФормы,
		ДополнительныеПараметрыФормы.ЗаполнитьПараметрыКоманды);
	
	Если ЗначениеЗаполнено(ЭлектронныеДокументы.ПрисоединенныеФайлы) Тогда
		Для Каждого ВнешнийДокумент Из ЭлектронныеДокументы.ПрисоединенныеФайлы Цикл
			ПараметрыОткрытия.КоллекцияПечатныхФорм.Добавить(Новый Структура("СинонимМакета, ТабличныйДокумент", Строка(ВнешнийДокумент), ВнешнийДокумент));
		КонецЦикла;
	КонецЕсли;
	
	Если ЦельПодписания <> Неопределено Тогда
		
		Если Не ПараметрыОткрытия.Свойство("ПараметрыПечати") Тогда
			ПараметрыОткрытия.Вставить("ПараметрыОткрытия", Новый Структура);
		КонецЕсли;
		ПараметрыОткрытия.ПараметрыПечати.Вставить("ЦельПодписания", ЦельПодписания);
		
	КонецЕсли;
	
	Если ДополнительныеПараметрыФормы.ИнициацияПодписания Тогда
		ПараметрыОткрытия.Вставить("ИнициацияПодписания", Истина);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ПодписаниеДокументов", ПараметрыОткрытия, ВладелецФормы, Истина, , , ДополнительныеПараметрыФормы.ОписаниеОповещения);
	
КонецПроцедуры

Процедура ОткрытьФормуПодписанияПечатныхФорм(ПечатныеФормы, ВладелецФормы = Неопределено, ЦельПодписания = Неопределено, ДополнительныеПараметрыФормы = Неопределено, ПрисоединенныеФайлы = Неопределено) Экспорт
	
	ЭлектронныеДокументы = Новый Структура("ПечатныеФормы,ПрисоединенныеФайлы", ПечатныеФормы, ПрисоединенныеФайлы);
	ОткрытьФормуПодписанияВсехПечатныхФорм(ЭлектронныеДокументы, ВладелецФормы, ЦельПодписания, ДополнительныеПараметрыФормы);
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыПодписанияПечатныхФорм(ДокументыЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументыЭДОБЗК) Тогда
		
		ЭлектронныеДокументы =
			ЭДОБЗКВызовСервера.ЭлектронныеДокументыПоСсылкамНаДокументыЭДОБЗК(ДокументыЭДОБЗК);
		
		Если ЭлектронныеДокументы.ГоловныеОрганизации.Количество() > 1 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для подписания необходимо выбрать документы одной организации'"));
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("ЭлектронныеДокументы", ЭлектронныеДокументы);
		
		Если ЭлектронныеДокументы.Организации.Количество() > 1 Тогда
			ТекстВопроса =
			НСтр("ru = 'Для подписания выбраны документы разных филиалов.
				|Все документы смогут быть подписаны подписью одного из филиалов.
				|Продолжить?'");
			Оповещение = Новый ОписаниеОповещения(
				"ОбработчикПодключаемойКомандыПодписанияПечатныхФормПродолжение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Иначе
			ОбработчикПодключаемойКомандыПодписанияПечатныхФормПродолжение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыСохранитьЭД(ДокументыЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументыЭДОБЗК) Тогда
		СохранитьДокументыЭДОБЗКНаДиск(ДокументыЭДОБЗК);
		ОповеститьОНеобходимостиОбновленияФорм(ДокументыЭДОБЗК);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыОткрытияЭлектронногоДокумента(ДокументЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументЭДОБЗК) Тогда
		
		ЭлектронныйДокумент = ЭДОБЗКВызовСервера.ЭлектронныйДокументЭДОБЗК(ДокументЭДОБЗК);
		Если ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
			
			ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(
				ЭлектронныйДокумент, ДополнительныеПараметры.Форма.УникальныйИдентификатор, Ложь);
			
			Если ДополнительныеПараметры.Форма.ЗапрещенныеРасширения.НайтиПоЗначению(ДанныеФайла.Расширение) <> Неопределено Тогда
				ДополнительныеПараметрыОповещения = Новый Структура;
				ДополнительныеПараметрыОповещения.Вставить("ЭлектронныйДокумент", ЭлектронныйДокумент);
				ДополнительныеПараметрыОповещения.Вставить("Форма", ДополнительныеПараметры.Форма);
				Оповещение = Новый ОписаниеОповещения(
					"ОткрытьЭлектронныйДокументПослеПодтверждения", ЭтотОбъект, ДополнительныеПараметрыОповещения);
				ПараметрыФормы = Новый Структура("Ключ", "ПередОткрытиемФайла");
				ОткрытьФорму("ОбщаяФорма.ПредупреждениеБезопасности", ПараметрыФормы, , , , , Оповещение);
				Возврат;
			КонецЕсли;
			
			ОткрытьЭлектронныйДокумент(ЭлектронныйДокумент,
				ДополнительныеПараметры.Форма.УникальныйИдентификатор);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыОткрытияВерсииЭлектронногоДокументаДляПечати(ДокументЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументЭДОБЗК) Тогда
		
		ЭлектронныйДокумент = ЭДОБЗКВызовСервера.ЭлектронныйДокументЭДОБЗК(ДокументЭДОБЗК);
		Если ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
			ДанныеФайлаНаПечать = ЭДОБЗКВызовСервера.ДанныеЭлектронногоДокументаСоШтампамиПодписей(
				ЭлектронныйДокумент, ДополнительныеПараметры.Форма.УникальныйИдентификатор);
			Если ДанныеФайлаНаПечать = Неопределено Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Не удалось получить версию файла для печати'"));
			Иначе
				ФайловаяСистемаКлиент.ОткрытьФайл(ДанныеФайлаНаПечать.АдресВХранилище, , ДанныеФайлаНаПечать.ИмяФайла);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыОбновитьВ1СКабинетСотрудника(ДокументыЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если Не ЗарплатаКадрыВызовСервера.ЭтоПолноправныйПользователь() Тогда
		ТекстПредупреждения = НСтр("ru = 'Для выполнения команды необходимы полные права на базу данных'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ОбрабатываемыеФайлы = Новый Массив;
	Если ЗначениеЗаполнено(ДокументыЭДОБЗК) Тогда
		
		ЭлектронныеДокументы = ЭДОБЗКВызовСервера.ЭлектронныеДокументыПоСсылкамНаДокументыЭДОБЗК(ДокументыЭДОБЗК);
		ДокументыЗаявлений = ЭлектронныеДокументы.ДокументыПоКатегориям.Получить(ПредопределенноеЗначение("Перечисление.КатегорииДокументовЭДОБЗК.ЗаявлениеСотрудника"));
		Если ДокументыЗаявлений <> Неопределено
			И ЗначениеЗаполнено(ДокументыЗаявлений) Тогда
			
			ТекстПредупреждения =
				НСтр("ru = 'Среди выбранных документов есть документы,
					|полученные от сотрудников.
					|Обновлять можно только документы подготовленные
					|организацией.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;
		ОбрабатываемыеФайлы = ОбщегоНазначенияКлиентСервер.СкопироватьРекурсивно(ЭлектронныеДокументы.ПрисоединенныеФайлы);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбрабатываемыеФайлы, ЭлектронныеДокументы.ПечатныеФормы);
		
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ОбрабатываемыеФайлы, Неопределено);
		
		ЭДОБЗКВызовСервера.ЗапланироватьДействияСПечатнымиФормами(ОбрабатываемыеФайлы,
			ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников"));
		
	КонецЕсли;
	ПредставлениеДокументов = СтрокаСЧислом(
		НСтр("ru = ';%1 документа; ;%1 документов;%1 документов;%1 документа'"),
			ОбрабатываемыеФайлы.Количество(), ВидЧисловогоЗначения.Количественное);
	Состояние(СтрШаблон(
		НСтр("ru = 'Запланировано обновление %1'"),
		ПредставлениеДокументов));
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыКонвертироватьДокументыВPDF_A_1(ДокументыЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если Не ЗарплатаКадрыВызовСервера.ЭтоПолноправныйПользователь() Тогда
		ТекстПредупреждения = НСтр("ru = 'Для конвертации электронных документов печатных форм необходимы полные права на базу данных'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ДокументыЭДОБЗК.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Выберите строки документы печатных форм которых хотите сконвертировать'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
	ТекстВопроса =
		НСтр("ru = 'При проведении конвертации файлы печатных форм будут ПЕРЕЗАПИСАНЫ в требуемом формате (PDF/A-1A).
		|Ранее установленные цифровые подписи перестанут действовать и поэтому будут УДАЛЕНЫ.
		|По окончании конвертации будет произведено повторное подписание печатных форм ответственным лицом организации, после чего печатные формы будут отправлены в кабинеты сотрудникам для повторного ознакомления.
		|
		|Перед проведением конвертации настоятельно рекомендуем СДЕЛАТЬ КОПИЮ БАЗЫ ДАННЫХ!
		|Проводите конвертацию маленькими ПОРЦИЯМИ документов, во избежание неприятностей, связанных с нехваткой памяти.
		|
		|Конвертировать?'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДокументыЭДОБЗК", ДокументыЭДОБЗК);
	Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОКонвертацииДокументов", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыПроверитьФорматНаСоответствиеPDF_A_1(ДокументыЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если Не ЗарплатаКадрыВызовСервера.ЭтоПолноправныйПользователь() Тогда
		ТекстПредупреждения = НСтр("ru = 'Для конвертации электронных документов необходимы полные права на базу данных'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ДокументыЭДОБЗК.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Выберите строки документы которых хотите сконвертировать'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
	Если ДокументыЭДОБЗК.Количество() = 1 Тогда
		ДанныеДокумента = ЭДОБЗКВызовСервера.ДанныеЭлектронногоДокумента(
			ДокументыЭДОБЗК[0], ДополнительныеПараметры.Форма.УникальныйИдентификатор);
		Если ЭДОБЗКВызовСервера.ФорматЭлектронногоДокументаPDF_A_1(ДанныеДокумента) Тогда
			Если ЭДОБЗКВызовСервера.УстановитьПризнакФорматФайлаPDF_A_1(ДокументыЭДОБЗК[0]) Тогда
				Если ДополнительныеПараметры.Форма.ИмяФормы = "Документ.ДокументЭДОБЗК.Форма.ФормаДокумента" Тогда
					ДополнительныеПараметры.Форма.Прочитать();
				Иначе
					ДополнительныеПараметры.Источник.Обновить();
				КонецЕсли;
			КонецЕсли;
			ПоказатьПредупреждение(, НСтр("ru = 'Формат документа соответствует требуемому формату (PDF/A-1A).'"));
		Иначе
			ТекстВопроса = НСтр("ru = 'При конвертации данные файла будут ПЕРЕЗАПИСАНЫ в требуемом формате (PDF/A-1A).
				|По окончании конвертации будет необходимо произвести повторное подписание ответственным лицом организации, после чего документ будет отправлен в кабинеты сотрудникам для повторного ознакомления.
				|
				|Выполнить конвертацию?'");
			ДополнительныеПараметры.Вставить("ДокументЭДОБЗК", ДокументыЭДОБЗК[0]);
			ДополнительныеПараметры.Вставить("ДанныеДокументаЭДОБЗК", ДанныеДокумента);
			Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОКонвертацииПриПроверкеСоответствияФорматаДокумента", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли;
	Иначе
		ТекстВопроса =
			НСтр("ru = 'После проверки, будет предпринята попытка конвертации файлов формат которых не соответствует требованиям.
			|При этом, данные файлов будут ПЕРЕЗАПИСАНЫ в требуемом формате (PDF/A-1A).
			|Ранее установленные цифровые подписи перестанут действовать и поэтому будут УДАЛЕНЫ.
			|По окончании конвертации будет необходимо произвести повторное подписание печатных форм ответственным лицом организации, после чего печатные формы будут отправлены в кабинеты сотрудникам для повторного ознакомления.
			|
			|Перед проведением конвертации настоятельно рекомендуем СДЕЛАТЬ КОПИЮ БАЗЫ ДАННЫХ!
			|Проводите конвертацию маленькими ПОРЦИЯМИ документов, во избежание неприятностей, связанных с длительной обработкой и нехваткой памяти.
			|
			|Проверить и сконвертировать?'");
		ДополнительныеПараметры.Вставить("ДокументыЭДОБЗК", ДокументыЭДОБЗК);
		Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОКонвертацииПриПроверкеСоответствияФорматаДокументов", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОтветаНаВопросОКонвертацииПриПроверкеСоответствияФорматаДокумента(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЭДОБЗКВызовСервера.СконвертироватьДанныеДокументаВФорматPDF_A_1(
			ДополнительныеПараметры.ДокументЭДОБЗК,
			ДополнительныеПараметры.ДанныеДокументаЭДОБЗК,
			ДополнительныеПараметры.Форма.УникальныйИдентификатор);
		Если ДополнительныеПараметры.Форма.ИмяФормы = "Документ.ДокументЭДОБЗК.Форма.ФормаДокумента" Тогда
			ДополнительныеПараметры.Форма.Прочитать();
		Иначе
			ДополнительныеПараметры.Источник.Обновить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаОтветаНаВопросОКонвертацииПриПроверкеСоответствияФорматаДокументов(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		РезультатПроверки = ЭДОБЗКВызовСервера.СконвертироватьДанныеДокументовВФорматPDF_A_1(
			ДополнительныеПараметры.ДокументыЭДОБЗК,
			ДополнительныеПараметры.Форма.УникальныйИдентификатор);
		Если РезультатПроверки <> Неопределено Тогда
			РезультатПроверки.Показать(
				СтрШаблон(НСтр("ru = 'Протокол проверки (конвертации) документов ЭДО на соответсвие формату PDF/A-1A от %1'"), Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДЛФ=DT")));
		КонецЕсли;
		ДополнительныеПараметры.Источник.Обновить();
	КонецЕсли;
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыОбновитьСостоянияДокументов(ДокументыЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = ЭДОБЗКВызовСервера.ОбновитьСостоянияДокументовЭДОБЗКВФоне(ДокументыЭДОБЗК);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеОбновленияСостояний", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ДополнительныеПараметры.Форма);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыПроверитьНеПроверенныеПодписи(ДокументыЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ДокументыЭДОБЗК.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выбраны документы для проверки непроверенных подписей'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметрыОбработкиОтвета = Новый Структура;
	ДополнительныеПараметрыОбработкиОтвета.Вставить("ДокументыЭДОБЗК", ДокументыЭДОБЗК);
	ДополнительныеПараметрыОбработкиОтвета.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	Если ДокументыЭДОБЗК.Количество() > 10 Тогда
		
		ТекстВопроса =
			НСтр("ru = 'Проверка подписей электронных документов может занимать продолжительное время.
				|Во избежание неприятностей, связанных с нехваткой памяти - предлагается проводить проверку небольшими порциями документов.
				|
				|Продолжить?'");
		
		Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОПроверкеПодписей", ЭтотОбъект, ДополнительныеПараметрыОбработкиОтвета);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ОбработкаОтветаНаВопросОПроверкеПодписей(КодВозвратаДиалога.Да, ДополнительныеПараметрыОбработкиОтвета);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьСостояниеОтправленныхНаПодпись(ПараметрыОповещения) Экспорт
	
	Если ПараметрыОповещения.Свойство("ДанныеФайловНаПодпись")
		И ПараметрыОповещения.ДанныеФайловНаПодпись.Количество() > 0 Тогда
		
		КоличествоФайловНаправленныхНаПодпись = 0;
		Для Каждого ФайлыНаПодписьОрганизации Из ПараметрыОповещения.ДанныеФайловНаПодпись Цикл
			КоличествоФайловНаправленныхНаПодпись = КоличествоФайловНаправленныхНаПодпись + ФайлыНаПодписьОрганизации.Значение.Количество();
		КонецЦикла;
		
		УведомитьОКоличествоДокументовОтправленныхНаПодписание(КоличествоФайловНаправленныхНаПодпись);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьСостояниеОтправленныхНаПодписьПоКоллекцииНаправлений(НаправленияНаПодписание) Экспорт
	
	КоличествоФайловНаправленныхНаПодпись = 0;
	Для Каждого НаправленияПоОрганизации Из НаправленияНаПодписание Цикл
		Для Каждого НаправленияПоИдентификатору Из НаправленияПоОрганизации.Значение Цикл
			Для Каждого НаправленияПоТипуОбъекта Из НаправленияПоИдентификатору.Значение Цикл
				Для Каждого НаправлениеПоПодписантам Из НаправленияПоТипуОбъекта.Значение Цикл
					КоличествоФайловНаправленныхНаПодпись = КоличествоФайловНаправленныхНаПодпись
						+ НаправлениеПоПодписантам.Значение.ПрисоединенныеФайлы.Количество();
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	УведомитьОКоличествоДокументовОтправленныхНаПодписание(КоличествоФайловНаправленныхНаПодпись);
	
КонецПроцедуры

Процедура УведомитьОКоличествоДокументовОтправленныхНаПодписание(КоличествоФайловНаправленныхНаПодпись)
	
	Если КоличествоФайловНаправленныхНаПодпись > 0 Тогда
		Состояние(СтрокаСЧислом(
			НСтр("ru = ';%1 документ направлен на подпись; ;%1 документа направлено на подпись;%1 документов направлены на подпись;%1 направлено на подпись'"),
			КоличествоФайловНаправленныхНаПодпись, ВидЧисловогоЗначения.Количественное));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормыВыбораПодписантовПечатныхФорм(ВладелецФормы, Организация, Оповещение, ПодписантыПечатныхФорм = Неопределено, ВыборЕдинственногоПодписанта = Ложь) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организации", Организация);
	ПараметрыОткрытия.Вставить("ВыборЕдинственногоПодписанта", ВыборЕдинственногоПодписанта);
	
	Если ПодписантыПечатныхФорм <> Неопределено Тогда
		ПараметрыОткрытия.Вставить("ПодписантыПечатныхФорм", Новый ФиксированныйМассив(ПодписантыПечатныхФорм));
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ВыборПодписантовПечатныхФорм", ПараметрыОткрытия, ВладелецФормы, Истина, , ,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// См. ОбщегоНазначенияКлиентПереопределяемый.ПриНачалеРаботыСистемы.
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт 
	
	Если Не СистемаВзаимодействия.ИспользованиеДоступно() Тогда
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбработатьДействияСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикДействияСообщения(Обработчик);
	
КонецПроцедуры

Процедура ОчиститьНастройкиПодписанияПечатныхФорм() Экспорт
	
	ЭДОБЗКВызовСервера.ОчиститьНастройкиПодписанияПечатныхФорм();
	ПоказатьПредупреждение(, НСтр("ru = 'Настройки подписания ЭЦП (электронными цифровыми подписями) очищены.'"));
	
КонецПроцедуры

Процедура ПодписатьФайлы(ПодписываемыеФайлы, ОповещениеЗавершения, ФормаВладелец, ЦельПодписания, НастройкиПодписания = Неопределено) Экспорт
	
	Организации = Новый Массив;
	ФайлыБезВладельца = Истина;
	ИсключаемыеИзЛичногоПодписания = Новый Массив;
	НаправленияНаПодписание = Новый Соответствие;
	ДанныеФайловНаПодпись = Новый Соответствие;
	Для Каждого ДанныеФайла Из ПодписываемыеФайлы Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеФайла.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		Организации.Добавить(ДанныеФайла.Организация);
		Если ЗначениеЗаполнено(ДанныеФайла.ФайлОбъекта) Тогда
			ФайлыБезВладельца = Ложь;
		КонецЕсли;
		
		Если НастройкиПодписания <> Неопределено Тогда
			НастройкиОрганизации = НастройкиПодписания.Получить(ДанныеФайла.Организация);
			Если НастройкиОрганизации <> Неопределено Тогда
				НастройкиИдентификатора = НастройкиОрганизации.Получить(ДанныеФайла.ИдентификаторПечатнойФормы);
				Если НастройкиИдентификатора <> Неопределено Тогда
					НастройкиТипаОбъекта = НастройкиИдентификатора.Получить(ТипЗнч(ДанныеФайла.Владелец));
					Если НастройкиТипаОбъекта <> Неопределено Тогда
						Если Не НастройкиТипаОбъекта.Инициатор Тогда
							ИсключаемыеИзЛичногоПодписания.Добавить(ДанныеФайла);
						КонецЕсли;
						Если НастройкиТипаОбъекта.ОтветственныеЛица.Количество() > 0 Тогда
							
							НаправленияПоОрганизации = НаправленияНаПодписание.Получить(ДанныеФайла.Организация);
							Если НаправленияПоОрганизации = Неопределено Тогда
								НаправленияПоОрганизации = Новый Соответствие;
								НаправленияНаПодписание.Вставить(ДанныеФайла.Организация, НаправленияПоОрганизации);
							КонецЕсли;
							НаправленияПоИдентификатору = НаправленияПоОрганизации.Получить(ДанныеФайла.ИдентификаторПечатнойФормы);
							Если НаправленияПоИдентификатору = Неопределено Тогда
								НаправленияПоИдентификатору = Новый Соответствие;
								НаправленияПоОрганизации.Вставить(ДанныеФайла.ИдентификаторПечатнойФормы, НаправленияПоИдентификатору);
							КонецЕсли;
							ТипВладельца = ТипЗнч(ДанныеФайла.Владелец);
							НаправленияПоТипуОбъекта = НаправленияПоИдентификатору.Получить(ТипВладельца);
							Если НаправленияПоТипуОбъекта = Неопределено Тогда
								НаправленияПоТипуОбъекта = Новый Соответствие;
								НаправленияПоИдентификатору.Вставить(ТипВладельца, НаправленияПоТипуОбъекта);
							КонецЕсли;
							
							НаправленияПоПодписантам = Неопределено;
							Для Каждого НастройкиОтветственныхЛиц Из НастройкиТипаОбъекта.ОтветственныеЛица Цикл
								Если НастройкиОтветственныхЛиц.Значение.Найти(ДанныеФайла.Владелец) <> Неопределено Тогда
									НаправленияПоПодписантам = НаправленияПоТипуОбъекта.Получить(НастройкиОтветственныхЛиц.Ключ);
									Если НаправленияПоПодписантам = Неопределено Тогда
										НаправленияПоПодписантам = Новый Структура("ПрисоединенныеФайлы,ОтветственныеЛица", Новый Массив, НастройкиОтветственныхЛиц.Ключ);
										ОтветственныеЛица = НастройкиОтветственныхЛиц.Ключ;
										Если НастройкиТипаОбъекта.Инициатор Тогда
											ОтветственныеЛица.Вставить(0, ПользователиКлиент.ТекущийПользователь());
										КонецЕсли;
										НаправленияПоТипуОбъекта.Вставить(ОтветственныеЛица, НаправленияПоПодписантам);
									КонецЕсли;
									Прервать;
								КонецЕсли;
							КонецЦикла;
							
							НаправленияПоПодписантам.ПрисоединенныеФайлы.Добавить(ДанныеФайла.ФайлОбъекта);
							
							// Подготовка коллекции к обновлению
							ДанныеФайлов = ДанныеФайловНаПодпись.Получить(ДанныеФайла.Организация);
							Если ДанныеФайлов = Неопределено Тогда
								ДанныеФайлов = Новый Массив;
								ДанныеФайловНаПодпись.Вставить(ДанныеФайла.Организация, ДанныеФайлов);
							КонецЕсли;
							
							Если ДанныеФайлов.Найти(ДанныеФайла) = Неопределено Тогда
								ДанныеФайлов.Добавить(ДанныеФайла);
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НаправленияНаПодписание.Количество() > 0 Тогда
		
		ЭДОБЗКВызовСервера.НаправитьНаПодписаниеПоКоллекцииНаправлений(НаправленияНаПодписание, ЦельПодписания);
		ПоказатьСостояниеОтправленныхНаПодписьПоКоллекцииНаправлений(НаправленияНаПодписание);
		
		Если ДанныеФайловНаПодпись.Количество() > 0 Тогда
			Если ОповещениеЗавершения <> Неопределено Тогда
				ОповещениеЗавершения.ДополнительныеПараметры.Вставить("ДанныеФайловНаПодпись", ДанныеФайловНаПодпись);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого ИсключениеИзПодписания Из ИсключаемыеИзЛичногоПодписания Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(
			ПодписываемыеФайлы, ИсключениеИзПодписания);
	КонецЦикла;
	
	Организации = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Организации);
	ДанныеСертификатовОрганизаций = ЭДОБЗКВызовСервера.ДанныеСертификатовОрганизаций(Организации);
	
	ПараметрыПодписания = Новый Структура;
	ПараметрыПодписания.Вставить("ПодписываемыеФайлы", ПодписываемыеФайлы);
	ПараметрыПодписания.Вставить("Организации", Организации);
	ПараметрыПодписания.Вставить("СертификатыОрганизаций", ДанныеСертификатовОрганизаций.Сертификаты);
	ПараметрыПодписания.Вставить("ГоловныеОрганизации", ДанныеСертификатовОрганизаций.ГоловныеОрганизации);
	ПараметрыПодписания.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	ПараметрыПодписания.Вставить("ЦельПодписания", ЦельПодписания);
	
	Если Не ЗначениеЗаполнено(ПодписываемыеФайлы) Тогда
		ПодписатьФайлыСертификатами(Истина, ПараметрыПодписания);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПодписатьФайлыСертификатами", ЭтотОбъект, ПараметрыПодписания);
	
	ПараметрыПодготовки = Новый Структура;
	ПараметрыПодготовки.Вставить("Организации", Организации);
	ПараметрыПодготовки.Вставить("ДанныеСертификатовОрганизаций", ДанныеСертификатовОрганизаций);
	ПараметрыПодготовки.Вставить("ОповещениеЗавершения", Оповещение);
	ПараметрыПодготовки.Вставить("ФормаВладелец", ФормаВладелец);
	ПараметрыПодготовки.Вставить("ИндексОрганизации", 0);
	ПараметрыПодготовки.Вставить("ФайлыБезВладельца", ФайлыБезВладельца);
	
	ПодготовитьСертификатыОрганизации(ПараметрыПодготовки);
	
КонецПроцедуры

Процедура ПоказатьСостояниеСохраненныхБезЦифровыхПодписей(КоличествоФайлов) Экспорт
	
	Если КоличествоФайлов > 0 Тогда
		
		Состояние(СтрокаСЧислом(
			НСтр("ru = ';%1 документ сохранен без цифровой подписи; ;%1 документа сохранено без цифровой подписи;%1 документов сохранено без цифровой подписи;%1 сохранено без цифровой подписи'"),
			КоличествоФайлов, ВидЧисловогоЗначения.Количественное));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписатьПоНастройкам(УправляемаяФорма, ЦельПодписания = Неопределено) Экспорт
	
	ПараметрыОткрытия = ПараметрыОткрытияФормыНастройкиПодписанияПоНастройкам(УправляемаяФорма, ЦельПодписания);
	ОткрытьФормуНастройкиПодписания(ПараметрыОткрытия, УправляемаяФорма);
	
КонецПроцедуры

Процедура ПодписатьПоНастройкамДокумент(УправляемаяФорма, ДокументЭДО, Идентификатор, ЦельПодписания, ОповещениеЗавершения) Экспорт
	ПараметрыОткрытия = ПараметрыОткрытияФормыНастройкиПодписания(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументЭДО),
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Идентификатор),
		ЦельПодписания);
	ОткрытьФормуНастройкиПодписания(ПараметрыОткрытия, УправляемаяФорма, ОповещениеЗавершения);
	
КонецПроцедуры

Процедура НастроитьИПодписать(УправляемаяФорма, ЦельПодписания = Неопределено) Экспорт
	
	ПараметрыОткрытия = ПараметрыОткрытияФормыНастройкиПодписанияПоНастройкам(УправляемаяФорма, ЦельПодписания, Истина);
	ОткрытьФормуНастройкиПодписания(ПараметрыОткрытия, УправляемаяФорма);
	
КонецПроцедуры

Процедура НастроитьИПодписатьДокумент(УправляемаяФорма, ДокументЭДО, Идентификатор, ЦельПодписания, ОповещениеЗавершения = Неопределено) Экспорт
	
	ПараметрыОткрытия = ПараметрыОткрытияФормыНастройкиПодписания(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументЭДО),
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Идентификатор),
		ЦельПодписания,
		Истина);
	ОткрытьФормуНастройкиПодписания(ПараметрыОткрытия, УправляемаяФорма, ОповещениеЗавершения);
	
КонецПроцедуры

Процедура ОткрытьФормуНастройкиПодписания(ПараметрыОткрытия, ВладелецФормы, ОповещениеЗавершения = Неопределено)
	ОткрытьФорму("ОбщаяФорма.НастройкаПодписания", ПараметрыОткрытия, ВладелецФормы, Истина, , , ОповещениеЗавершения);
КонецПроцедуры

Функция ПараметрыОткрытияФормыНастройкиПодписанияПоНастройкам(УправляемаяФорма, ЦельПодписания, ВыполнитьНастройку = Ложь)
	
	Идентификаторы = Новый Массив;
	Для Каждого НастройкаПечатнойФормы Из УправляемаяФорма.НастройкиПечатныхФорм Цикл
		Если НастройкаПечатнойФормы.Печатать Тогда
			Идентификаторы.Добавить(НастройкаПечатнойФормы.ИмяМакета);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПараметрыОткрытияФормыНастройкиПодписания(
		УправляемаяФорма.ОбъектыПечати.ВыгрузитьЗначения(),
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(Идентификаторы),
		ЦельПодписания,
		ВыполнитьНастройку);
	
КонецФункции

Функция ПараметрыОткрытияФормыНастройкиПодписания(ОбъектыПечати, Идентификаторы, ЦельПодписания, ВыполнитьНастройку = Ложь)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ОбъектыПечати", ОбъектыПечати);
	ПараметрыОткрытия.Вставить("Идентификаторы", Идентификаторы);
	ПараметрыОткрытия.Вставить("ЦельПодписания", ЦельПодписания);
	ПараметрыОткрытия.Вставить("Настроить", ВыполнитьНастройку);
	Возврат ПараметрыОткрытия;
	
КонецФункции

Процедура ОтказатьВПодписании(УправляемаяФорма) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОтказатьВПодписанииЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура("ФайлыДокументовЭДОБЗК", ФайлыДокументовЭДОБЗКФормыПечатьДокументов(УправляемаяФорма));
	ОткрытьФорму("ОбщаяФорма.ОтказВПодписании", ПараметрыОткрытия, УправляемаяФорма, Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОтказатьВПодписанииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПриОбновленииДанныхДокументовЭДОБЗК();
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыСохранитьМЧД(СписокМЧД, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(СписокМЧД) Тогда
		СохранитьМЧДНаДиск(СписокМЧД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОповеститьОНеобходимостиОбновленияФорм(Источник) Экспорт
	Если ТипЗнч(Источник) = Тип("ДокументСсылка.ДокументЭДОБЗК") Тогда
		ОповеститьОбИзменении(Источник);
		ОповеститьОбОбновленииДанныхДокументовЭДОБЗК(Источник);
	Иначе
		ОповеститьОбИзменении(Тип("ДокументСсылка.ДокументЭДОБЗК"));
		ОповеститьОбОбновленииДанныхДокументовЭДОБЗК();
	КонецЕсли;
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыВизуализаторШаблонаДокумента(ШаблонДокумента, ДополнительныеПараметры) Экспорт
	
	ПараметрыОткрытия = Новый Структура("ШаблонДокумента", ШаблонДокумента);
	ОткрытьФорму("Справочник.ШаблоныДокументов.Форма.ВизуализаторДокумента",
		ПараметрыОткрытия, ЭтотОбъект, Истина);
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыПодписанияПечатныхФормПродолжение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатыПодписанияДокументовСФайлами", ЭтотОбъект, Новый Структура);
		ДополнительныеПараметрыФормы = ЭДОБЗККлиент.ДополнительныеПараметрыФормыПодписанияДокументов(Оповещение);
		ОткрытьФормуПодписанияВсехПечатныхФорм(
			ДополнительныеПараметры.ЭлектронныеДокументы,
			ДополнительныеПараметры.Форма,
			ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.Подписать"),
			ДополнительныеПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыВерсииШаблонаДокумента(ШаблонДокумента, ДополнительныеПараметры) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ШаблонДокумента", ШаблонДокумента);
	
	ОткрытьФорму("Справочник.ШаблоныДокументов.Форма.ФормаСпискаВерсий", ПараметрыОткрытия, ДополнительныеПараметры.Форма,
		Истина, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыПодписатьСогласиеНаПрисоединениеКЭДОБЗК(СогласияНаПрисоединениеКЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(СогласияНаПрисоединениеКЭДОБЗК) Тогда
		Результат = ЭДОБЗКВызовСервера.ЭлектронныеДокументыСогласийНаПрисоединениеКЭДОБЗКСостоянияПриема(
			СогласияНаПрисоединениеКЭДОБЗК, ПредопределенноеЗначение("Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ПодписанУКЭПСотрудника"));
		Если Результат.Ошибка Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для подписания необходимо выбрать ""Согласия на присоединение к ЭДО""
				|с состоянием приема ""Подписан УКЭП сотрудника"". '"));
		Иначе
			Оповещение = Новый ОписаниеОповещения("ОбработчикПодключаемойКомандыПодписатьСогласиеНаПрисоединениеКЭДОБЗКПродолжение",
				ЭтотОбъект, ДополнительныеПараметры);
			ДополнительныеПараметрыФормы = ЭДОБЗККлиент.ДополнительныеПараметрыФормыПодписанияДокументов(Оповещение);
			ОткрытьФормуПодписанияПечатныхФорм(
				ОбщегоНазначенияБЗККлиентСервер.ВыгрузитьКолонку(Результат.ЭлектронныеДокументы, "Значение"),
				ДополнительныеПараметры.Форма,
				ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.Подписать"),
				ДополнительныеПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикПодключаемойКомандыПодписатьСогласиеНаПрисоединениеКЭДОБЗКПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	ЭДОБЗКВызовСервера.ПодтвердитьПолучениеСогласийНаПрисоединениеКЭДОБЗКПослеПодписанияЦП(ДополнительныеПараметры.МассивСсылок);
	ОбновитьИзменениеСостояниеПриема(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработчикИзменениеСостоянияПолученияПодтвердитьПолучениеСканкопий(СогласияНаПрисоединениеКЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(СогласияНаПрисоединениеКЭДОБЗК) Тогда
		Результат = ЭДОБЗКВызовСервера.ЭлектронныеДокументыСогласийНаПрисоединениеКЭДОБЗКСостоянияПриема(
			СогласияНаПрисоединениеКЭДОБЗК, ПредопределенноеЗначение("Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ПолученыСканкопии"));
		Если Результат.Ошибка Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для подтверждения получения сканкопий необходимо выбрать
				|""Согласия на присоединение к ЭДО"" с состоянием приема ""Получены сканкопии"". '"));
		Иначе
			ЭДОБЗКВызовСервера.УстановитьСостояниеПриемаСогласийНаПрисоединениеКЭДОБЗК(
				СогласияНаПрисоединениеКЭДОБЗК, ПредопределенноеЗначение("Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ОжиданиеОригинала"));
			ОбновитьИзменениеСостояниеПриема(ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикИзменениеСостоянияПолученияОтправитьЗаново(СогласияНаПрисоединениеКЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(СогласияНаПрисоединениеКЭДОБЗК) Тогда
		ЭДОБЗКВызовСервера.ОтправитьЗановоСогласияНаПрисоединениеКЭДОБЗК(СогласияНаПрисоединениеКЭДОБЗК);
		ОбновитьИзменениеСостояниеПриема(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикИзменениеСостоянияПолученияПодтвердитьПолучениеОригинала(СогласияНаПрисоединениеКЭДОБЗК, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(СогласияНаПрисоединениеКЭДОБЗК) Тогда
		Результат = ЭДОБЗКВызовСервера.ЭлектронныеДокументыСогласийНаПрисоединениеКЭДОБЗКСостоянияПриема(
			СогласияНаПрисоединениеКЭДОБЗК, ПредопределенноеЗначение("Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ОжиданиеОригинала"));
		Если Результат.Ошибка Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для подтверждения получения оригиналов необходимо выбрать
				|""Согласия на присоединение к ЭДО"" с состоянием приема ""Ожидание оригинала"". '"));
		Иначе
			ЭДОБЗКВызовСервера.УстановитьСостояниеПриемаСогласийНаПрисоединениеКЭДОБЗК(
				СогласияНаПрисоединениеКЭДОБЗК, ПредопределенноеЗначение("Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Подтверждено"));
			ОбновитьИзменениеСостояниеПриема(ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьИзменениеСостояниеПриема(ДополнительныеПараметры)
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.СогласиеНаПрисоединениеКЭДОБЗК"));
	Если ТипЗнч(ДополнительныеПараметры.Источник) = Тип("ТаблицаФормы") Тогда
		ЭДОБЗККлиентСервер.ОбновитьКоманды(ДополнительныеПараметры.Форма, ДополнительныеПараметры.Источник, Истина, Ложь);
	ИначеЕсли ДополнительныеПараметры.ЭтоФормаОбъекта Тогда
		Оповестить("ИзмененоСостояниеСогласияНаПрисоединениеКЭДОБЗК");
		ДополнительныеПараметры.Форма.Прочитать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодписаниеВФормеПечатьДокументов

Процедура ПодписатьФайлыСертификатами(Результат, ДополнительныеПараметры) Экспорт
	
	ПодписываемыеФайлы = ДополнительныеПараметры.ПодписываемыеФайлы;
	СертификатыОрганизаций = ДополнительныеПараметры.СертификатыОрганизаций;
	
	ДанныеФайловНаПодпись = Новый Соответствие;
	ДанныеФайловПоСертификатам = Новый Соответствие;
	ПодписываемыеДанные = Новый Массив;
	ОрганизацияОтбораМЧД = Неопределено;
	Сертификаты = Новый Массив;
	Для Каждого ДанныеФайла Из ПодписываемыеФайлы Цикл
		
		Если ПустаяСтрока(ДанныеФайла.АдресВХранилище)
			И Не ЗначениеЗаполнено(ДанныеФайла.ФайлОбъекта) Тогда
			
			Продолжить;
		КонецЕсли;
		
		ОрганизацияОтбораМЧД = ?(ДанныеФайла.Организация = Неопределено, "Неопределено", ДанныеФайла.Организация);
		СертификатыОрганизации = СертификатыОрганизаций.Получить(ОрганизацияОтбораМЧД);
		
		Если Не ЗначениеЗаполнено(СертификатыОрганизации) И ЗначениеЗаполнено(ДанныеФайла.Организация) Тогда
			ГоловнаяОрганизация = ДополнительныеПараметры.ГоловныеОрганизации.Получить(ДанныеФайла.Организация);
			Если ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
				ОрганизацияОтбораМЧД = ГоловнаяОрганизация;
				СертификатыГоловнойОрганизации = СертификатыОрганизаций.Получить(ГоловнаяОрганизация);
				СертификатыОрганизации = СертификатыГоловнойОрганизации;
			КонецЕсли;
		КонецЕсли;
		
		Если СертификатыОрганизации = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(СертификатыОрганизации[0]) = Тип("СправочникСсылка.Пользователи") Тогда
			
			ДанныеФайлов = ДанныеФайловНаПодпись.Получить(ДанныеФайла.Организация);
			Если ДанныеФайлов = Неопределено Тогда
				ДанныеФайлов = Новый Массив;
				ДанныеФайловНаПодпись.Вставить(ДанныеФайла.Организация, ДанныеФайлов);
			КонецЕсли;
			
			Если ДанныеФайлов.Найти(ДанныеФайла) = Неопределено Тогда
				ДанныеФайлов.Добавить(ДанныеФайла);
			КонецЕсли;
			
		Иначе
			
			Сертификаты = СертификатыОрганизации;
			
			Если ЗначениеЗаполнено(ДанныеФайла.АдресВХранилище) Тогда
				АдресВХранилище = ДанныеФайла.АдресВХранилище;
			Иначе
				ДанныеПрисоединенногоФайла = РаботаСФайламиКлиент.ДанныеФайла(ДанныеФайла.ФайлОбъекта, Новый УникальныйИдентификатор, Истина);
				АдресВХранилище = ДанныеПрисоединенногоФайла.СсылкаНаДвоичныеДанныеФайла;
			КонецЕсли;
			
			Данные = Новый Структура;
			ПодписываемыеДанные.Добавить(Данные);
			
			Данные.Вставить("Данные",        АдресВХранилище);
			Данные.Вставить("Представление", ДанныеФайла.ИмяФайла);
			Данные.Вставить("ИсходнаяСтрока", ДанныеФайла);
			
			Если ЗначениеЗаполнено(ДанныеФайла.ФайлОбъекта) Тогда
				Данные.Вставить("Объект", ДанныеФайла.ФайлОбъекта);
			Иначе
				Продолжить;
			КонецЕсли;
			
			Для Каждого Сертификат Из СертификатыОрганизации Цикл
				
				Если ДанныеФайла.УстановленныеПодписи <> Неопределено Тогда
					
					ДанныеСертификата = ДанныеФайла.УстановленныеПодписи.Получить(Сертификат);
					Если ДанныеСертификата <> Неопределено Тогда
					
						НаборДанных = ДанныеФайловПоСертификатам.Получить(Сертификат);
						Если НаборДанных = Неопределено Тогда
							НаборДанных = Новый Соответствие;
							ДанныеФайловПоСертификатам.Вставить(Сертификат, НаборДанных);
						КонецЕсли;
						
						НаборДанных.Вставить(ДанныеФайла.ФайлОбъекта, ДанныеСертификата);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДанныеФайловНаПодпись.Количество() > 0 Тогда
		НаправитьНаПодпись(ДанныеФайловНаПодпись, ДополнительныеПараметры.ЦельПодписания, СертификатыОрганизаций);
		Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
			Если ДополнительныеПараметры.ОповещениеЗавершения.ДополнительныеПараметры.Свойство("ДанныеФайловНаПодпись") Тогда
				ДанныеФайловНаПодписьПараметров = ДополнительныеПараметры.ОповещениеЗавершения.ДополнительныеПараметры.ДанныеФайловНаПодпись;
				Для Каждого ДанныеФайловНаПодписьОрганизации Из ДанныеФайловНаПодпись Цикл
					ДанныеФайлов = ДанныеФайловНаПодписьПараметров.Получить(ДанныеФайловНаПодписьОрганизации.Ключ);
					Если ДанныеФайлов = Неопределено Тогда
						ДанныеФайловНаПодписьПараметров.Вставить(ДанныеФайловНаПодписьОрганизации.Ключ, ДанныеФайловНаПодписьОрганизации.Значение);
					Иначе
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеФайлов, ДанныеФайловНаПодписьОрганизации.Значение, Истина);
					КонецЕсли;
				КонецЦикла;
			Иначе
				ДополнительныеПараметры.ОповещениеЗавершения.ДополнительныеПараметры.Вставить("ДанныеФайловНаПодпись", ДанныеФайловНаПодпись);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Сертификаты.Количество() > 0 Тогда
		ПодписатьФайлыСертификатом(ПодписываемыеДанные, ДанныеФайловПоСертификатам, Сертификаты,
			ДополнительныеПараметры.ОповещениеЗавершения, ОрганизацияОтбораМЧД);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ДополнительныеПараметры.ПодписываемыеФайлы);
	КонецЕсли;
	
КонецПроцедуры

#Область ПодготовкаСпискаСертификатовОрганизаций

Процедура ПодготовитьСертификатыОрганизации(ПараметрыПодготовки)
	
	ФормаВладелец = ПараметрыПодготовки.ФормаВладелец;
	Организация = ПараметрыПодготовки.Организации[ПараметрыПодготовки.ИндексОрганизации];
	ДанныеСертификатовОрганизаций = ПараметрыПодготовки.ДанныеСертификатовОрганизаций;
	
	СертификатыОрганизации = ДанныеСертификатовОрганизаций.Сертификаты.Получить(
		?(Организация = Неопределено, "Неопределено", Организация));
	
	Если Не ЗначениеЗаполнено(СертификатыОрганизации) И ЗначениеЗаполнено(Организация) Тогда
		Если ДанныеСертификатовОрганизаций.Свойство("ГоловныеОрганизации") Тогда
			ГоловнаяОрганизация = ДанныеСертификатовОрганизаций.ГоловныеОрганизации.Получить(Организация);
			Если ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
				СертификатыОрганизации = ДанныеСертификатовОрганизаций.Сертификаты.Получить(ГоловнаяОрганизация);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПараметрыПодготовки.ФайлыБезВладельца
		И Не ЗначениеЗаполнено(СертификатыОрганизации) Тогда
		
		Если ЭДОБЗКВызовСервера.ДоступенВыборПользователей() Тогда
			Оповещение = Новый ОписаниеОповещения("ПриВыбореОтветственныхЛиц", ЭтотОбъект, ПараметрыПодготовки);
			ОткрытьФормыВыбораПодписантовПечатныхФорм(ФормаВладелец, Организация, Оповещение);
		Иначе
			ТекстПредупреждения = СтрШаблон(
				НСтр("ru = 'Не найдено сертификатов %1 для подписания документов.'"),
				Организация);
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	Иначе
		ПодготовитьСертификатыСледующейОрганизации(ПараметрыПодготовки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьСертификатыСледующейОрганизации(ПараметрыПодготовки)
	
	ПараметрыПодготовки.ИндексОрганизации = ПараметрыПодготовки.ИндексОрганизации + 1;
	Если ПараметрыПодготовки.ИндексОрганизации < ПараметрыПодготовки.Организации.Количество() Тогда
		ПодготовитьСертификатыОрганизации(ПараметрыПодготовки);
	Иначе
		ВыполнитьОбработкуОповещения(ПараметрыПодготовки.ОповещениеЗавершения, Истина);
	КонецЕсли;
	
КонецПроцедуры

#Область ВыборСертификатов

Процедура ПриВыбореСертификатов(ВыбранныеСертификаты, ПараметрыПодготовки) Экспорт
	
	Если ВыбранныеСертификаты <> Неопределено Тогда
		
		Если ТипЗнч(ВыбранныеСертификаты) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
			СертификатыОрганизации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранныеСертификаты);
		Иначе
			СертификатыОрганизации = ВыбранныеСертификаты;
		КонецЕсли;
		
		Организация = ПараметрыПодготовки.Организации[ПараметрыПодготовки.ИндексОрганизации];
		ПараметрыПодготовки.ДанныеСертификатовОрганизаций.Сертификаты.Вставить(
			?(Организация = Неопределено, "Неопределено", Организация), СертификатыОрганизации);
		
		Если ЭДОБЗКВызовСервера.НеобходимостьЗадаватьВопросПриВыбореСертификатовОрганизации(Организация) Тогда
			
			ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
			ПараметрыВопроса.Заголовок = НСтр("ru = 'Подписание документов'");
			ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
			
			Если СертификатыОрганизации.Количество() = 1 Тогда
				ТекстВопроса = НСтр("ru = 'Всегда подписывать выбранным сертификатом?'");
			Иначе
				ТекстВопроса = НСтр("ru = 'Всегда подписывать выбранными сертификатами?'");
			КонецЕсли;
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Да'"));
			Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Нет'"));
			Кнопки.Добавить("ВсегдаНет", НСтр("ru = 'Всегда нет'"));
			
			Оповещение = Новый ОписаниеОповещения("ОбработатьОтветОПодписанииВыбраннымиСертификатами", ЭтотОбъект, ПараметрыПодготовки);
			СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыВопроса);
		Иначе
			ПодготовитьСертификатыСледующейОрганизации(ПараметрыПодготовки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтветОПодписанииВыбраннымиСертификатами(Результат, ПараметрыПодготовки) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ПараметрыПодготовки.Организации[ПараметрыПодготовки.ИндексОрганизации];
	Если Результат.Значение = КодВозвратаДиалога.Да Тогда
		СертификатыОрганизации = ПараметрыПодготовки.ДанныеСертификатовОрганизаций.Сертификаты.Получить(
			?(Организация = Неопределено, "Неопределено", Организация));
		ЭДОБЗКВызовСервера.ЗапомнитьСертификатыОрганизации(Организация, СертификатыОрганизации, Не Результат.БольшеНеЗадаватьЭтотВопрос);
	ИначеЕсли Результат.Значение = "ВсегдаНет" Тогда
		ЭДОБЗКВызовСервера.ЗапомнитьНеобходимостьЗадаватьВопросПриВыбореСертификатовОрганизации(
			Организация, Ложь);
	КонецЕсли;
	
	ПодготовитьСертификатыСледующейОрганизации(ПараметрыПодготовки);
	
КонецПроцедуры

#КонецОбласти

#Область ВыборОтветственныхЛиц

Процедура ПриВыбореОтветственныхЛиц(ВыбранныеПодписанты, ПараметрыПодготовки) Экспорт
	
	Если ВыбранныеПодписанты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ПараметрыПодготовки.Организации[ПараметрыПодготовки.ИндексОрганизации];
	ПараметрыПодготовки.ДанныеСертификатовОрганизаций.Сертификаты.Вставить(Организация, ВыбранныеПодписанты);
	
	ВсегдаСпрашиватьПодписантов = ЭДОБЗКВызовСервера.ЗначениеВсегдаСпрашиватьПодписантовОрганизации(Организация) = Истина;
	Если Не ВсегдаСпрашиватьПодписантов Тогда
		
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.Заголовок = НСтр("ru = 'Подписание документов'");
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		
		Если ВыбранныеПодписанты.Количество() = 1 Тогда
			
			ТекстВопроса = СтрШаблон(
				НСтр("ru = 'Всегда направлять документы организации %1 на подписание выбранному
					|ответственному лицу (%2)?'"),
				Организация,
				ВыбранныеПодписанты[0]);
			
		Иначе
			
			ТекстВопроса = СтрШаблон(
				НСтр("ru = 'Всегда направлять документы организации %1 на подписание выбранным ответственным лицам?'"),
				Организация);
			
		КонецЕсли;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да);
		Кнопки.Добавить(КодВозвратаДиалога.Нет);
		Кнопки.Добавить("ВсегдаСпрашивать", НСтр("ru = 'Всегда спрашивать'"));
		Оповещение = Новый ОписаниеОповещения("ОбработатьОтветОВыбореОтветственныхЛиц", ЭтотОбъект, ПараметрыПодготовки);
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыВопроса);
	Иначе
		ОбработатьОтветОВыбореОтветственныхЛиц(Неопределено, ПараметрыПодготовки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтветОВыбореОтветственныхЛиц(Результат, ПараметрыПодготовки) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Организация = ПараметрыПодготовки.Организации[ПараметрыПодготовки.ИндексОрганизации];
		Если Результат.Значение = КодВозвратаДиалога.Да Тогда
			ВыбранныеПодписанты = ПараметрыПодготовки.ДанныеСертификатовОрганизаций.Сертификаты.Получить(Организация);
			ЭДОБЗКВызовСервера.ЗапомнитьПодписантов(Организация, ВыбранныеПодписанты);
		ИначеЕсли Результат.Значение = "ВсегдаСпрашивать" Тогда
			ЭДОБЗКВызовСервера.ЗапомнитьВсегдаСпрашиватьПодписантовОрганизации(Организация)
		КонецЕсли;
		
	КонецЕсли;
	
	ПодготовитьСертификатыСледующейОрганизации(ПараметрыПодготовки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПодписаниеФайловПечатныхФорм

Процедура ПодписатьФайлыСертификатом(ПодписываемыеФайлы, ДанныеФайловПоСертификатам, СертификатыОрганизаций, ОповещениеЗавершения, ОрганизацияОтбораМЧД)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеФайловПоСертификатам", ДанныеФайловПоСертификатам);
	ДополнительныеПараметры.Вставить("СертификатыОрганизаций", СертификатыОрганизаций);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	ПередПодписанием = Новый ОписаниеОповещения("ПередПодписаниемФайловСертификатом", ЭтотОбъект, ДополнительныеПараметры);
	
	ОтборДоверенностей = Новый Структура;
	ОтборДоверенностей.Вставить("Организация", ОрганизацияОтбораМЧД);
	ОтборДоверенностей.Вставить("КлючОтбора", "ЭДОБЗК");
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("ОтборСертификатов", СертификатыОрганизаций);
	ОписаниеДанных.Вставить("ПередВыполнением", ПередПодписанием);
	ОписаниеДанных.Вставить("ОтборДоверенностей", ОтборДоверенностей);
	
	Если ПодписываемыеФайлы.Количество() = 1 Тогда
		ОписаниеДанных.Вставить("Операция",            НСтр("ru = 'Подписание файла'"));
		ОписаниеДанных.Вставить("ЗаголовокДанных",     НСтр("ru = 'Файл'"));
		ОписаниеДанных.Вставить("ПредставлениеНабора", ПодписываемыеФайлы[0].Представление);
	Иначе
		ОписаниеДанных.Вставить("Операция",            НСтр("ru = 'Подписание файлов'"));
		ОписаниеДанных.Вставить("ЗаголовокДанных",     НСтр("ru = 'Файлы'"));
		ОписаниеДанных.Вставить("ПредставлениеНабора", НСтр("ru = 'Печатные формы (%1)'"));
	КонецЕсли;
	ОписаниеДанных.Вставить("НаборДанных",         ПодписываемыеФайлы);
	
	Состояние(ОписаниеДанных.Операция);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодписанияФайловСертификатом", ЭтотОбъект, ДополнительныеПараметры);
	ТипПодписи = ЭлектроннаяПодписьКлиент.НовыйТипПодписи(ПредопределенноеЗначение("Перечисление.ТипыПодписиКриптографии.СМеткойДоверенногоВремениCAdEST"));
	ТипПодписи.ВыборДоверенности = Истина;
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, ЭтотОбъект, Оповещение, ТипПодписи);
	
КонецПроцедуры

Процедура ПослеПодписанияФайловСертификатом(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Успех Тогда
		
		Сертификат = Результат.ВыбранныйСертификат.Ссылка;
		Результат.ПередВыполнением = Неопределено;
		ЭДОБЗКВызовСервера.ПослеПодписанияФайловСертификатом(Результат);
		Если ДополнительныеПараметры.Свойство("ОповещениеЗавершения")
			И ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
			
			ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
			ДополнительныеПараметрыОповещениеЗавершения = ОповещениеЗавершения.ДополнительныеПараметры;
			Если ДополнительныеПараметрыОповещениеЗавершения <> Неопределено
				И ДополнительныеПараметрыОповещениеЗавершения.Свойство("Форма") Тогда
				
				Если Не ДополнительныеПараметрыОповещениеЗавершения.Свойство("ОбновленныеФайлы") Тогда
					ДополнительныеПараметрыОповещениеЗавершения.Вставить("ОбновленныеФайлы", Новый Массив);
				КонецЕсли;
				
				ФайлыКОбработке = Новый Массив;
				
				Форма = ДополнительныеПараметрыОповещениеЗавершения.Форма;
				ОписанияФайлов = ЭДОБЗККлиентСервер.ОписанияФайловПечатныхФормИзПараметровФормы(Форма.Параметры);
				Если Результат.Свойство("НаборДанных") Тогда
					НаборДанных = Результат.НаборДанных;
				Иначе
					НаборДанных = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Результат);
				КонецЕсли;
	
				Для Каждого ДанныеФайла Из НаборДанных Цикл
					
					Если ДанныеФайла.Свойство("ИсходнаяСтрока") И ЗначениеЗаполнено(ДанныеФайла.ИсходнаяСтрока.ФайлОбъекта) Тогда
						ФайлыКОбработке.Добавить(ДанныеФайла.ИсходнаяСтрока.ФайлОбъекта);
					ИначеЕсли ДанныеФайла.Свойство("Объект") И ЗначениеЗаполнено(ДанныеФайла.Объект) Тогда
						ФайлыКОбработке.Добавить(ДанныеФайла.Объект);
					Иначе
						Если Результат.Свойство("Данные") Тогда
							ДанныеФайла.ИсходнаяСтрока.Вставить("СвойстваПодписи", Результат.СвойстваПодписи);
						КонецЕсли;
					КонецЕсли;
					
					Если ОписанияФайлов <> Неопределено
						И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеФайла.ИсходнаяСтрока, "ИдентификаторПечатнойФормы") Тогда
						
						ОписаниеОригиналовИдентификатора = ОписанияФайлов.ОригиналыПечатныхФорм.Получить(
							ДанныеФайла.ИсходнаяСтрока.ИдентификаторПечатнойФормы);
						
						Если ОписаниеОригиналовИдентификатора <> Неопределено Тогда
							
							Оригиналы = ОписаниеОригиналовИдентификатора.Оригиналы;
							
							ИсходнаяСтрока = Оригиналы.Получить(ДанныеФайла.ИсходнаяСтрока.ФайлОбъекта);
							Если ИсходнаяСтрока <> Неопределено Тогда
								
								ЭДОБЗККлиентСервер.ДобавитьДанныеПодписи(
									ИсходнаяСтрока.УстановленныеПодписи,
									Сертификат,
									Результат);
								
								ДополнительныеПараметрыОповещениеЗавершения.ОбновленныеФайлы.Добавить(ИсходнаяСтрока);
								
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		Иначе
			ОповещениеЗавершения = Неопределено;
		КонецЕсли;
		
		Если ОповещениеЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередПодписаниемФайловСертификатом(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ОписаниеДанных.Свойство("НаборДанных") Тогда
		НаборДанных = Результат.ОписаниеДанных.НаборДанных;
	Иначе
		НаборДанных = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Результат.ОписаниеДанных);
	КонецЕсли;
	
	УдаляемыеЭлементы = Новый Массив;
	Для Каждого ОписаниеДанных Из НаборДанных Цикл
		ДанныеСертификата = ДополнительныеПараметры.ДанныеФайловПоСертификатам.Получить(Результат.ОписаниеДанных.ВыбранныйСертификат.Ссылка);
		Если ДанныеСертификата <> Неопределено Тогда
			ДанныеПодписи = ДанныеСертификата.Получить(ОписаниеДанных.Объект);
			Если ДанныеПодписи <> Неопределено Тогда
				Для Каждого МашиночитаемаяДоверенность Из ДанныеПодписи Цикл
					Если Не ЗначениеЗаполнено(Результат.ОписаниеДанных.ВыбраннаяДоверенность)
						И МашиночитаемаяДоверенность = Неопределено Тогда
						
						УдаляемыеЭлементы.Добавить(ОписаниеДанных);
						Прервать
					ИначеЕсли МашиночитаемаяДоверенность <> Неопределено
						И Результат.ОписаниеДанных.ВыбраннаяДоверенность = МашиночитаемаяДоверенность Тогда
						
						УдаляемыеЭлементы.Добавить(ОписаниеДанных);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Для Каждого ОписаниеДанных Из УдаляемыеЭлементы Цикл
		НаборДанных.Удалить(НаборДанных.Найти(ОписаниеДанных));
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(НаборДанных) Тогда
		Результат.ОписаниеДанных.Вставить("ПрекратитьВыполнение");
		Состояние(НСтр("ru = 'Подпись уже установлена'"));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Результат.Оповещение, Результат.ОписаниеДанных);
	
КонецПроцедуры

#КонецОбласти

Процедура ПослеПодписанияСохраненныхФорм(РезультатПодписания, ДополнительныеПараметры) Экспорт
	
	Если РезультатПодписания <> Неопределено Тогда
		
		РезультатПодписанияФайлов = ДополнительныеПараметры.ПечатныеФормыОбъектов;
		
		ФормаПечатьДокументов = ДополнительныеПараметры.Форма;
		Если ДополнительныеПараметры.ЦельПодписания = ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.Подписать") Тогда
			
			Если ФормаПечатьДокументов.Открыта() Тогда
				ФормаПечатьДокументов.Закрыть();
			КонецЕсли;
			
		Иначе
			
			ОбрабатываемыеФайлы = Новый Массив;
			ЗаписанныеОбъекты = Новый Массив;
			ВладельцыФайлов = Новый Соответствие;
			
			Если ДополнительныеПараметры.Свойство("ДанныеФайловНаПодпись") Тогда
				ДанныеФайловНаПодпись = ДополнительныеПараметры.ДанныеФайловНаПодпись;
			Иначе
				ДанныеФайловНаПодпись = Новый Соответствие;
			КонецЕсли;
			
			Для Каждого ОписаниеФайла Из РезультатПодписанияФайлов Цикл
				
				ЗаписанныеОбъекты.Добавить(ОписаниеФайла.ФайлОбъекта);

				ФайлыНаПодписьОрганизации = ДанныеФайловНаПодпись.Получить(ОписаниеФайла.Организация);
				Если ФайлыНаПодписьОрганизации = Неопределено
					Или ФайлыНаПодписьОрганизации.Найти(ОписаниеФайла) = Неопределено Тогда
					
					Если ЗначениеЗаполнено(ОписаниеФайла.ФайлОбъекта) Тогда
						ОбрабатываемыеФайлы.Добавить(ОписаниеФайла.ФайлОбъекта);
					Иначе
						ОписаниеФайлаПодписанногоФайла = Новый Структура;
						ОписаниеФайлаПодписанногоФайла.Вставить("ПечатнаяФорма", ОписаниеФайла.ПечатнаяФорма);
						ОписаниеФайлаПодписанногоФайла.Вставить("СвойстваПодписи", ОписаниеФайла.СвойстваПодписи);
						ОписаниеФайлаПодписанногоФайла.Вставить("ИдентификаторПечатнойФормы", ОписаниеФайла.ИдентификаторПечатнойФормы);
						ОписаниеФайлаПодписанногоФайла.Вставить("ИмяФайла", ОписаниеФайла.ИмяФайла);
						ОписаниеФайлаПодписанногоФайла.Вставить("Название", ОписаниеФайла.Название);
						ОписаниеФайлаПодписанногоФайла.Вставить("АдресВХранилище", ОписаниеФайла.АдресВХранилище);
						ОписаниеФайлаПодписанногоФайла.Вставить("ФизическоеЛицо", ОписаниеФайла.ФизическоеЛицо);
						ОбрабатываемыеФайлы.Добавить(ОписаниеФайлаПодписанногоФайла);
					КонецЕсли;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ОписаниеФайла.Владелец) И ТипЗнч(ОписаниеФайла.Владелец) <> Тип("Строка") Тогда
					
					ИдентификаторыВладельца = ВладельцыФайлов.Получить(ОписаниеФайла.Владелец);
					Если ИдентификаторыВладельца = Неопределено Тогда
						ИдентификаторыВладельца = Новый Соответствие;
						ВладельцыФайлов.Вставить(ОписаниеФайла.Владелец, ИдентификаторыВладельца);
					КонецЕсли;
					
					ИдентификаторыВладельца.Вставить(ОписаниеФайла.ИдентификаторПечатнойФормы, Истина);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого ДанныеВладельца Из ВладельцыФайлов Цикл
				ОповеститьОбИзмененииПечатнойФормы(ДанныеВладельца.Ключ, ДанныеВладельца.Значение);
			КонецЦикла;
			
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
				МодульРаботаСФайламиСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиСлужебныйКлиент");
				ПараметрыОповещенияЗаписиФайла = МодульРаботаСФайламиСлужебныйКлиент.ПараметрыОповещенияЗаписиФайла();
				ПараметрыОповещенияЗаписиФайла.Вставить("РезультатПодписанияФайлов", РезультатПодписанияФайлов);
				МодульРаботаСФайламиСлужебныйКлиент.ОповеститьОбИзмененииФайлов(ЗаписанныеОбъекты, ПараметрыОповещенияЗаписиФайла);
			КонецЕсли;
			
			Если ОбрабатываемыеФайлы.Количество() > 0 Тогда
				
				Если ДополнительныеПараметры.ЦельПодписания = ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ЗаписатьНаДиск") Тогда
					
					СохранитьПечатныеФормыНаДиск(ОбрабатываемыеФайлы);
					
				ИначеЕсли ДополнительныеПараметры.ЦельПодписания = ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ОтправитьПоПочте") Тогда
					
					ПараметрыОтправки = ФормаПечатьДокументов.ПараметрыВывода.ПараметрыОтправки;
					
					ПараметрыОткрытияФормы = Новый Структура;
					ПараметрыОткрытияФормы.Вставить("Получатели", ПараметрыОтправки.Получатель);
					ПараметрыОткрытияФормы.Вставить("Тема", ПараметрыОтправки.Тема);
					ПараметрыОткрытияФормы.Вставить("Текст", ПараметрыОтправки.Текст);
					ПараметрыОткрытияФормы.Вставить("СписокФайлов", ОбрабатываемыеФайлы);
					
					ОтправитьПечатныеФормыПоЭлектроннойПочте(ФормаПечатьДокументов, ПараметрыОткрытияФормы);
					
				ИначеЕсли ДополнительныеПараметры.ЦельПодписания = ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ПередатьНаРаботаВРоссии") Тогда
					
					ИнтеграцияСРаботаВРоссииКлиент.ПередатьДокументы(ОбрабатываемыеФайлы);
					
				ИначеЕсли ДополнительныеПараметры.ЦельПодписания = ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников") Тогда
					
					ЭДОБЗКВызовСервера.ЗапланироватьДействияСПечатнымиФормами(ОбрабатываемыеФайлы, ДополнительныеПараметры.ЦельПодписания);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ДополнительныеПараметры.Свойство("ОбновленныеФайлы") Тогда
				ОбновленныеФайлы = ДополнительныеПараметры.ОбновленныеФайлы;
			Иначе
				ОбновленныеФайлы = Новый Массив;
			КонецЕсли;
			
			Если ДанныеФайловНаПодпись.Количество() > 0 Тогда
				Для Каждого ФайлыНаПодписьОрганизации Из ДанныеФайловНаПодпись Цикл
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбновленныеФайлы, ФайлыНаПодписьОрганизации.Значение);
				КонецЦикла;
			КонецЕсли;
			
			ПоказатьСостояниеОтправленныхНаПодпись(ДополнительныеПараметры);
			
			Если ОбновленныеФайлы.Количество() > 0 Тогда
				ЭДОБЗКВызовСервера.ЗаменитьПечатныеФормы(
					ОбновленныеФайлы, ДополнительныеПараметры.ПечатныеФормы, ДополнительныеПараметры.Форма.ОбъектыПечати);
			КонецЕсли;
			
			Для Каждого ОписаниеПечатнойФормы Из ДополнительныеПараметры.ПечатныеФормы Цикл
				
				НайденныеФормы = ФормаПечатьДокументов.НастройкиПечатныхФорм.НайтиСтроки(Новый Структура("ИмяМакета", ОписаниеПечатнойФормы.ИдентификаторПечатнойФормы));
				Если НайденныеФормы.Количество() > 0 Тогда
					ФормаПечатьДокументов[НайденныеФормы[0].ИмяРеквизита] = ОписаниеПечатнойФормы.ПечатнаяФорма;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ФормаПечатьДокументов.ИмяФормы = "ОбщаяФорма.ПечатьДокументов" Тогда
				ФормаПечатьДокументов.ПодключитьОбработчикОжидания("УстановитьТекущуюСтраницу", 0.1, Истина);
				ТекущаяНастройка = УправлениеПечатьюКлиент.НастройкаТекущейПечатнойФормы(ФормаПечатьДокументов);
			КонецЕсли;
			Если ТекущаяНастройка <> Неопределено Тогда
				
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					ФормаПечатьДокументов.Элементы,
					"КнопкаРедактирование",
					"Пометка",
					Ложь);
				
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					ФормаПечатьДокументов.Элементы,
					"КнопкаРедактирование",
					"Доступность",
					Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПриОбновленииДанныхДокументовЭДОБЗК(ФормаПечатьДокументов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СохранениеНаНосители

Процедура СохранитьПечатныеФормыНаДиск(СписокФайлов, ОповещениеЗавершения = Неопределено) Экспорт
	
	СохранитьФайлыНаДиск(
		ЭДОБЗКВызовСервера.ФайлыПечатныхФормДляСохраненияНаДиск(СписокФайлов), ОповещениеЗавершения);
	
КонецПроцедуры

Процедура СохранитьДокументыЭДОБЗКНаДиск(ДокументыЭДОБЗК, ОповещениеЗавершения = Неопределено) Экспорт
	
	Состояние(НСтр("ru = 'Подготовка файлов к записи на диск'"));
	
	Если ЗначениеЗаполнено(ДокументыЭДОБЗК) Тогда
		
		ФайлыЭлектронныхДокументов = ЭДОБЗКВызовСервера.ФайлыЭлектронныхДокументовДляСохраненияНаДиск(ДокументыЭДОБЗК);
		Если Не ЗначениеЗаполнено(ФайлыЭлектронныхДокументов) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не задан электронный документ'"), ДокументыЭДОБЗК[0], "ИмяФайлаСРасширением", "");
			Возврат;
		КонецЕсли;
		
		СохранитьФайлыНаДиск(ФайлыЭлектронныхДокументов, ОповещениеЗавершения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьМЧДНаДиск(СписокМЧД, ОповещениеЗавершения = Неопределено) Экспорт
	
	Состояние(НСтр("ru = 'Подготовка файлов к записи на диск'"));
	
	СохранитьФайлыНаДиск(
		ЭДОБЗКВызовСервера.ФайлыМЧДДляСохранениеНаДиск(СписокМЧД), ОповещениеЗавершения);
	
КонецПроцедуры

Процедура СохранитьФайлыНаДиск(СохраняемыеФайлы, ОповещениеЗавершения)
	
	Если ОповещениеЗавершения = Неопределено Тогда
		ОповещениеЗавершения = Новый ОписаниеОповещения("ПослеСохраненияФайловЭлектронныхДокументовНаДиск", ЭтотОбъект);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	ДополнительныеПараметры.Вставить("СохраняемыеФайлы", СохраняемыеФайлы);
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораКаталогаДляСохранениеФайлов", ЭтотОбъект, ДополнительныеПараметры);
	ФайловаяСистемаКлиент.ВыбратьКаталог(Оповещение);
	
КонецПроцедуры

Процедура ОбработкаВыбораКаталогаДляСохранениеФайлов(ВыбранныйКаталог, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранныйКаталог) Тогда
		ДополнительныеПараметры.Вставить("ВыбранныйКаталог", ВыбранныйКаталог);
		НачатьПоискФайлов(
			Новый ОписаниеОповещения("НайтиФайлыЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			ВыбранныйКаталог, "*.*", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиФайлыЗавершение(ФайлыВПапке, ДополнительныеПараметры) Экспорт
	Если ФайлыВПапке <> Неопределено Тогда
		ИменаФайлов = Новый Соответствие;
		Для Каждого НайденныйФайл Из ФайлыВПапке Цикл
			ИменаФайлов.Вставить(
				СтрЗаменить(Врег(НайденныйФайл.ПолноеИмя), ВРег(ДополнительныеПараметры.ВыбранныйКаталог) + ПолучитьРазделительПути(), "") , Истина);
		КонецЦикла;
		
		Для Каждого СохраняемыйФайл Из ДополнительныеПараметры.СохраняемыеФайлы Цикл
			Если ИменаФайлов.Получить(ВРег(СохраняемыйФайл.Имя)) = Истина Тогда
				СохраняемыйФайл.Имя = ЭДОБЗККлиентСервер.УникальноеИмяФайлаСРасширением(
					СохраняемыйФайл.Имя, ИменаФайлов);
			КонецЕсли;
			ИменаФайлов.Вставить(ВРег(СохраняемыйФайл.Имя), Истина);
			СохраняемыйФайл.Имя = ДополнительныеПараметры.ВыбранныйКаталог + ПолучитьРазделительПути() + СохраняемыйФайл.Имя;
		КонецЦикла;
		
		ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайлов();
		ПараметрыСохранения.Интерактивно = Ложь;
		ФайловаяСистемаКлиент.СохранитьФайлы(
			ДополнительныеПараметры.ОповещениеЗавершения, ДополнительныеПараметры.СохраняемыеФайлы, ПараметрыСохранения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеСохраненияФайловЭлектронныхДокументовНаДиск(СохраненныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если СохраненныеФайлы <> Неопределено Тогда
		
		Если СохраненныеФайлы.Количество() = 1 Тогда
			ТекстСообщения = НСтр("ru = 'Записан на диск:'");
			Пояснение = СохраненныеФайлы[0].Имя;
		Иначе
			ТекстСообщения = НСтр("ru = 'Записано на диск'");
			Пояснение = СтрокаСЧислом(
				НСтр("ru = ';%1 файл; ;%1 файла;%1 файлов;%1 файлов'"),
				СохраненныеФайлы.Количество(),
				ВидЧисловогоЗначения.Количественное);
		КонецЕсли;
		
		Состояние(ТекстСообщения, , Пояснение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтправкаНаЭлАдрес

Процедура ОтправитьПечатныеФормыПоЭлектроннойПочте(ВладелецФормы, ПараметрыОткрытияФормы)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПараметрыОткрытияФормы", ПараметрыОткрытияФормы);
	ДополнительныеПараметры.Вставить("ВладелецФормы", ВладелецФормы);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОтправитьПечатныеФормыПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
		МодульРаботаСПочтовымиСообщениямиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСПочтовымиСообщениямиКлиент");
		МодульРаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьПечатныеФормыПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ОповещениеЗавершения") Тогда
		ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	Иначе
		ОповещениеЗавершения = Неопределено;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ОтправкаПодписанныхФайлов",
		ДополнительныеПараметры.ПараметрыОткрытияФормы,
		ДополнительныеПараметры.ВладелецФормы,
		Истина, , , ОповещениеЗавершения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

Процедура ОповеститьОбОбновленииДанныхДокументовЭДОБЗК(Источник = Неопределено) Экспорт
	
	Оповестить("ОбновленыДанныеДокументовЭДОБЗК", , Источник);
	
КонецПроцедуры

Процедура НаправитьНаПодпись(ДанныеФайловНаПодпись, Действие, ИсполнителиОрганизаций) Экспорт
	
	Для Каждого ОписаниеДанных Из ДанныеФайловНаПодпись Цикл
		
		Организация = ОписаниеДанных.Ключ;
		Исполнители = ИсполнителиОрганизаций.Получить(Организация);
		
		ДанныеФайлов = Новый Массив;
		Для Каждого ОписаниеФайла Из ОписаниеДанных.Значение Цикл
			ДанныеФайлов.Добавить(ОписаниеФайла.ФайлОбъекта)
		КонецЦикла;
		
		ЭДОБЗКВызовСервера.НаправитьНаПодпись(ДанныеФайлов, Действие, Исполнители);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСертификатыПодписей(КомандаПечати)
	
	ПодписиБезСертификатов = ЭДОБЗКВызовСервера.ПодписиБезСертификатов(КомандаПечати.ОбъектыПечати, КомандаПечати.Идентификатор);
	Если ЗначениеЗаполнено(ПодписиБезСертификатов) Тогда
		ДополнительныеПараметры = Новый Структура("КомандаПечати", КомандаПечати);
		ДополнительныеПараметры.Вставить("ПодписиБезСертификатов", ПодписиБезСертификатов);
		Оповещение = Новый ОписаниеОповещения("ПолучитьСведенияОСертификатеПослеСозданияМенеджера", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "ПолучениеСертификатов");
		Возврат;
	КонецЕсли;
	
	УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати(КомандаПечати);
	
КонецПроцедуры

#Область ПолучениеДанныхСертификатаИзПодписи

Функция ДанныеСертификатаПодписи(МенеджерКриптографии, ДвоичныеДанныеПодписи) Экспорт
	
	ДанныеСертификата = Новый Структура("Сертификат,Отпечаток,АдресСертификата");
	Попытка
		Сертификаты = МенеджерКриптографии.ПолучитьСертификатыИзПодписи(ДвоичныеДанныеПодписи);
		Если Сертификаты.Количество() > 0 Тогда
			
			ДанныеСертификата.Сертификат = Сертификаты[Сертификаты.Количество() - 1];
			ДанныеСертификата.Отпечаток = Base64Строка(ДанныеСертификата.Сертификат.Отпечаток);
			ДанныеСертификата.АдресСертификата = ПоместитьВоВременноеХранилище(
				ДанныеСертификата.Сертификат.Выгрузить(), Новый УникальныйИдентификатор);
			
		КонецЕсли;
	Исключение
		Ошибка = ИнформацияОбОшибке();
	КонецПопытки;
	
	Возврат ДанныеСертификата;
	
КонецФункции

Процедура НачатьПолучениеДанныхСертификатаПодписи(МенеджерКриптографии, ДвоичныеДанныеПодписи, ОповещениеЗавершения) Экспорт
	
	Попытка
		ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
		Оповещение = Новый ОписаниеОповещения("ДанныеСертификатаПодписиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		МенеджерКриптографии.НачатьПолучениеСертификатовИзПодписи(Оповещение, ДвоичныеДанныеПодписи);
	Исключение
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Неопределено);
	КонецПопытки;
	
КонецПроцедуры

Процедура ДанныеСертификатаПодписиЗавершение(Сертификаты, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Сертификаты) Тогда
		
		ДанныеСертификата = Новый Структура("Сертификат,Отпечаток,АдресСертификата");
		ДанныеСертификата.Сертификат = Сертификаты[Сертификаты.Количество() - 1];
		ДанныеСертификата.Отпечаток = Base64Строка(ДанныеСертификата.Сертификат.Отпечаток);
		
		ДополнительныеПараметры.Вставить("ДанныеСертификата", ДанныеСертификата);
		Оповещение = Новый ОписаниеОповещения("ПоОкончанииВыгрузкиСертификата", ЭтотОбъект, ДополнительныеПараметры);
		ДанныеСертификата.Сертификат.НачатьВыгрузку(Оповещение);
		
		Возврат;
		
	Иначе
		ДанныеСертификата = Неопределено;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ДанныеСертификата);
	
КонецПроцедуры

Процедура ПоОкончанииВыгрузкиСертификата(ДвоичныеДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДвоичныеДанныеСертификата <> Неопределено Тогда
		
		ДанныеСертификата = ДополнительныеПараметры.ДанныеСертификата;
		ДанныеСертификата.АдресСертификата = ПоместитьВоВременноеХранилище(
			ДвоичныеДанныеСертификата, Новый УникальныйИдентификатор);
		
	Иначе
		ДанныеСертификата = Неопределено;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ДанныеСертификата);
	
КонецПроцедуры

#КонецОбласти

Процедура ПолучитьСведенияОСертификатеПослеСозданияМенеджера(МенеджерКриптографии, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(МенеджерКриптографии) <> Тип("Строка") Тогда
		
		ДополнительныеПараметры.Вставить("МенеджерКриптографии", МенеджерКриптографии);
		ДополнительныеПараметры.Вставить("ИндексТекущейПодписи", 0);
		
		ИтерацияПолученияДанныхСертификатаБезПодписи(ДополнительныеПараметры);
		
	Иначе
		УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати(ДополнительныеПараметры.КомандаПечати);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИтерацияПолученияДанныхСертификатаБезПодписи(ДополнительныеПараметры)
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьДанныеСертификатаОчереднойПодписи", ЭтотОбъект, ДополнительныеПараметры);
	
	ДвоичныеДанныеПодписи = ПолучитьИзВременногоХранилища(
		ДополнительныеПараметры.ПодписиБезСертификатов[ДополнительныеПараметры.ИндексТекущейПодписи].Подпись);
	
	Если ТипЗнч(ДвоичныеДанныеПодписи) = Тип("ДвоичныеДанные") Тогда
		НачатьПолучениеДанныхСертификатаПодписи(
			ДополнительныеПараметры.МенеджерКриптографии,
			ДвоичныеДанныеПодписи,
			Оповещение);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатаОчереднойПодписи(ДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДанныеСертификата <> Неопределено Тогда
		ПодписьБезСертификата = ДополнительныеПараметры.ПодписиБезСертификатов[ДополнительныеПараметры.ИндексТекущейПодписи];
		ПодписьБезСертификата.Отпечаток = ДанныеСертификата.Отпечаток;
		ПодписьБезСертификата.Сертификат = ДанныеСертификата.АдресСертификата;
	КонецЕсли;
	
	ДополнительныеПараметры.ИндексТекущейПодписи = ДополнительныеПараметры.ИндексТекущейПодписи + 1;
	Если ДополнительныеПараметры.ИндексТекущейПодписи < ДополнительныеПараметры.ПодписиБезСертификатов.Количество() Тогда
		ИтерацияПолученияДанныхСертификатаБезПодписи(ДополнительныеПараметры);
	Иначе
		
		ДополнительныеПараметрыКоманды = Новый Структура;
		Если ДополнительныеПараметры.КомандаПечати.Свойство("ДополнительныеПараметры") Тогда
			ДополнительныеПараметрыКоманды = ДополнительныеПараметры.КомандаПечати.ДополнительныеПараметры;
		КонецЕсли;
		
		ДополнительныеПараметрыКоманды.Вставить("ПодписиБезСертификатов", ДополнительныеПараметры.ПодписиБезСертификатов);
		ДополнительныеПараметры.КомандаПечати.Вставить("ДополнительныеПараметры", ДополнительныеПараметрыКоманды);
		
		УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати(ДополнительныеПараметры.КомандаПечати);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДействияСообщения(Сообщение, Действие, ДополнительныеПараметры) Экспорт
	
	Если Действие = ЭДОБЗККлиентСервер.ИмяДействияУведомленияОзнакомитьсяИПодписать() Тогда
		ОткрытьСписокДокументовЭДОНаПодпись();
	ИначеЕсли Действие = ЭДОБЗККлиентСервер.ИмяДействияУведомленияОзнакомитьсяСОтказомПодписать() Тогда
		ПараметрыОткрытия = Новый Структура("Отбор",
			Новый Структура("ЕстьЗапланированныеДействияТекущегоИсполнителя", Истина));
		ОткрытьФорму("Документ.ДокументЭДОБЗК.ФормаСписка", ПараметрыОткрытия, , "48bd2282-a817-4403-b2cf-ada0147db374");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьСписокДокументовЭДОНаПодпись() Экспорт
	ПараметрыОткрытия = Новый Структура("ДокументыНаПодпись", Истина);
	ОткрытьФорму("Документ.ДокументЭДОБЗК.ФормаСписка", ПараметрыОткрытия, , "f6aa6282-3adc-413c-a92a-bb0fbfa5313c");
КонецПроцедуры

Процедура ВключитьРедактированиеПечатнойФормы(УправляемаяФорма)
	
	Элементы = УправляемаяФорма.Элементы;
	
	НастройкаПечатнойФормы = УправлениеПечатьюКлиент.НастройкаТекущейПечатнойФормы(УправляемаяФорма);
	Если НастройкаПечатнойФормы <> Неопределено Тогда
		
		Редактирование = Не Элементы[ЭДОБЗККлиентСервер.ПрефиксЭлементовОВозможностиРедактирования() + "КомандаРедактирования"].Пометка;
		
		ПолеТабличногоДокумента = Элементы[НастройкаПечатнойФормы.ИмяРеквизита];
		ПолеТабличногоДокумента.Редактирование = Редактирование;
		Элементы.ТекущаяПечатнаяФорма.Редактирование = ПолеТабличногоДокумента.Редактирование;
		Элементы.ТекущаяПечатнаяФорма.ОтображатьСетку = Элементы.ТекущаяПечатнаяФорма.Редактирование;
		Элементы.ТекущаяПечатнаяФорма.ОтображатьЗаголовки = Элементы.ТекущаяПечатнаяФорма.Редактирование;
		
		ПечатнаяФормаДоступна = Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаПечатнаяФормаНедоступна;
		РедактированиеВозможно = ПечатнаяФормаДоступна И Не ПолеТабличногоДокумента.Защита;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			ЭДОБЗККлиентСервер.ПрефиксЭлементовОВозможностиРедактирования() + "КомандаРедактирования",
			"Пометка",
			Редактирование);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			ЭДОБЗККлиентСервер.ПрефиксЭлементовОВозможностиРедактирования() + "КомандаРедактирования",
			"Доступность",
			РедактированиеВозможно);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИмяСобытияПриЗаписиПечатныхФорм()
	Возврат "ЗаписьПечатныхФормСЭЦП";
КонецФункции

Процедура ОповеститьОбИзмененииПечатнойФормы(Владелец, ДанныеИдентификаторовПечатныхФорм)
	
	ОповеститьОбИзменении(Владелец);
	Оповестить(ИмяСобытияПриЗаписиПечатныхФорм(), ДанныеИдентификаторовПечатныхФорм, Владелец);
	
КонецПроцедуры

Процедура ОткрытьЭлектронныйДокументПослеПодтверждения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат = "Продолжить" Тогда
		ОткрытьЭлектронныйДокумент(ДополнительныеПараметры.ЭлектронныйДокумент,
			ДополнительныеПараметры.Форма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьЭлектронныйДокумент(ЭлектронныйДокумент, ИдентификаторХранилища)
	
	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляОткрытия(
		ЭлектронныйДокумент, Неопределено, ИдентификаторХранилища);
	
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	
КонецПроцедуры

Функция ФайлыДокументовЭДОБЗКФормыПечатьДокументов(УправляемаяФорма)
	
	ФайлыДокументовЭДОБЗК = Новый Массив;
	
	ОписанияФайлов = ЭДОБЗККлиентСервер.ОписанияФайловПечатныхФормИзПараметровФормы(УправляемаяФорма.Параметры);
	Если ОписанияФайлов <> Неопределено Тогда
		Для Каждого ОписаниеИдентификатора Из ОписанияФайлов.ОригиналыПечатныхФорм Цикл
			НастройкиИдентификатора = УправляемаяФорма.НастройкиПечатныхФорм.НайтиСтроки(Новый Структура("ИмяМакета", ОписаниеИдентификатора.Ключ));
			Если ЗначениеЗаполнено(НастройкиИдентификатора)
				И НастройкиИдентификатора[0].Печатать Тогда
				
				Для Каждого ОписаниеОригиналов Из ОписаниеИдентификатора.Значение.Оригиналы Цикл
					ФайлыДокументовЭДОБЗК.Добавить(ОписаниеОригиналов.Ключ);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(УправляемаяФорма, "ФайлыРеквизитовФормы") Тогда
		Для Каждого НастройкаФормы Из УправляемаяФорма.НастройкиПечатныхФорм Цикл
			Если Не НастройкаФормы.Печатать Тогда
				Продолжить;
			КонецЕсли;
			ВнешнийФайл = УправляемаяФорма.ФайлыРеквизитовФормы.Получить(НастройкаФормы.ИмяРеквизита);
			Если ВнешнийФайл <> Неопределено Тогда
				ФайлыДокументовЭДОБЗК.Добавить(ВнешнийФайл);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ФайлыДокументовЭДОБЗК;
	
КонецФункции

Процедура ОзнакомитьсяСКомментариямиКПечатнойФорме(УправляемаяФорма)
	
	ЭДОБЗКВызовСервера.УдалитьФайлыИзОбработкиПользователя(
		ФайлыДокументовЭДОБЗКФормыПечатьДокументов(УправляемаяФорма),
		ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.Ознакомиться"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ОзнакомленоСКомментариямиКПечатнойФорме",
		"Доступность",
		Ложь);
	
	УправляемаяФорма.Закрыть();
	ПриОбновленииДанныхДокументовЭДОБЗК();
	
КонецПроцедуры

#Область КонвертацияДокументов

Процедура ОбработкаОтветаНаВопросОКонвертацииДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполнитьКонвертациюДокументов(ДополнительныеПараметры.ДокументыЭДОБЗК);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонвертациюДокументов(ДокументыЭДОБЗК)
	
	Результат = ЭДОБЗКВызовСервера.КонвертироватьДокументы(ДокументыЭДОБЗК);
	Если Не ЗначениеЗаполнено(Результат.ДокументыСПечатнымиФормами) Тогда
		ТекстПредупреждения = НСтр("ru = 'Не найдено электронных документов печатных форм'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		ПоказатьПротоколКонвертацииДокументов(Результат);
		Возврат;
	КонецЕсли;
	
	ТекстыСообщения = Новый Массив;
	Если Результат.ЕстьОшибки Тогда
		ТекстыСообщения.Добавить(НСтр("ru = 'Среди выбранных документов есть документы с ошибками.'"));
	КонецЕсли;
	
	Если Результат.ЕстьДругиеДокументы Тогда
		ТекстыСообщения.Добавить(НСтр("ru = 'Среди выбранных документов есть документы не являющиеся печатными формами.'"));
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Результат", Результат);
	Оповещение = Новый ОписаниеОповещения("ВыполнитьКонвертациюДокументовЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	Если ТекстыСообщения.Количество() > 0
		Или Результат.ДокументыНеТребующиеКонвертации.Количество() > 0
		Или Результат.ДокументыПрошлыхПериодов.Количество() > 0 Тогда
		
		ДокументыСПечатнымиФормамиКоличество = СтрокаСЧислом(
			НСтр("ru = ';%1 электронный документ печатной формы;;%1 электронных документа печатных форм;%1 электронных документов печатных форм;%1 электронных документа печатных форм'"),
			Результат.ДокументыСПечатнымиФормами.Количество(),
			ВидЧисловогоЗначения.Количественное);
		Если Результат.ДокументыНеТребующиеКонвертации.Количество() > 0
			Или Результат.ДокументыПрошлыхПериодов.Количество() > 0 Тогда
			
			ТекстыЭтогоСообщения = Новый Массив;
			ТекстыЭтогоСообщения.Добавить(
				СтрШаблон(НСтр("ru = 'Можно сконвертировать %1, в том числе:'"),
					ДокументыСПечатнымиФормамиКоличество));
			Если Результат.ДокументыНеТребующиеКонвертации.Количество() Тогда
				ДокументыНеТребующиеКонвертацииКоличество = СтрокаСЧислом(
					НСтр("ru = ';%1 электронный документ возможно не требует;;%1 электронных документа возможно не требуют;%1 электронных документов возможно не требуют;%1 электронных документа возможно не требуют'"),
					Результат.ДокументыНеТребующиеКонвертации.Количество(),
					ВидЧисловогоЗначения.Количественное);
				ТекстСообщенияОКоличестве = СтрШаблон(НСтр("ru = '- %1 конвертации'"), ДокументыНеТребующиеКонвертацииКоличество);
				Если Результат.ДокументыПрошлыхПериодов.Количество() > 0 Тогда
					ТекстСообщенияОКоличестве = ТекстСообщенияОКоличестве + ";";
				Иначе
					ТекстСообщенияОКоличестве = ТекстСообщенияОКоличестве + ".";
				КонецЕсли;
				ТекстыЭтогоСообщения.Добавить(ТекстСообщенияОКоличестве);
			КонецЕсли;
			Если Результат.ДокументыПрошлыхПериодов.Количество() > 0 Тогда
				ДокументыПрошлыхПериодовКоличество = СтрокаСЧислом(
					НСтр("ru = ';%1 электронный документ;;%1 электронных документа;%1 электронных документов;%1 электронных документа'"),
					Результат.ДокументыПрошлыхПериодов.Количество(),
					ВидЧисловогоЗначения.Количественное);
				ТекстыЭтогоСообщения.Добавить(СтрШаблон(НСтр("ru = '- %1 из периода, когда не требовалось применение формата PDF/A-1A.'"), ДокументыПрошлыхПериодовКоличество));
			КонецЕсли;
			ТекстыСообщения.Добавить(СтрСоединить(ТекстыЭтогоСообщения, Символы.ПС));
			
		Иначе
			ТекстыСообщения.Добавить(СтрШаблон(НСтр("ru = 'Можно сконвертировать %1.'"), ДокументыСПечатнымиФормамиКоличество));
		КонецЕсли;
		ТекстыСообщения.Добавить(НСтр("ru = 'Продолжить?'"));
		ПоказатьВопрос(Оповещение, СтрСоединить(ТекстыСообщения, Символы.ПС), РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонвертациюДокументовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодписанияФайловПриКонвертацииДокументов", ЭтотОбъект, ДополнительныеПараметры);
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ЭДОБЗКВызовСервера.УдалитьПодписиИОбновитьОсновныеДокументыПриКонвертацииДокументов(
			ДополнительныеПараметры.Результат);
		
		ПодписатьФайлы(
			ЭДОБЗКВызовСервера.ДанныеФайловНаПодпись(ДополнительныеПараметры.Результат.ЭлектронныеДокументыНаПодпись, Новый УникальныйИдентификатор),
			Оповещение, ЭтотОбъект, ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников"));
		
	Иначе
		ПоказатьПротоколКонвертацииДокументов(ДополнительныеПараметры.Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПодписанияФайловПриКонвертацииДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	ЭДОБЗКВызовСервера.ЗапланироватьДействияСПечатнымиФормами(ДополнительныеПараметры.Результат.ЭлектронныеДокументы,
		ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников"));
	
	ПоказатьПротоколКонвертацииДокументов(ДополнительныеПараметры.Результат);
	
КонецПроцедуры

Процедура ПоказатьПротоколКонвертацииДокументов(Результат)
	Если Результат.Протокол.КоличествоСтрок() > 0 Тогда
		Результат.Протокол.Показать(
			НСтр("ru = 'Протокол конвертации электронных документов печатных форм в формат PDF/A-1A'"), НСтр("ru = 'Протокол конвертации.txt'"));
	КонецЕсли;
КонецПроцедуры

Процедура ПослеОбновленияСостояний(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.ДокументЭДОБЗК"));
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПодписейЭлектронныхДокументов

Процедура ОбработкаОтветаНаВопросОПроверкеПодписей(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Состояние(НСтр("ru = 'Получаю сведения'"), , НСтр("ru = 'о непроверенных подписях'"));
		ДанныеДляПроверкиПодписей = ЭДОБЗКВызовСервера.ДанныеДляПроверкиНепроверенныхПодписей(
			ДополнительныеПараметры.ДокументыЭДОБЗК,
			ДополнительныеПараметры.ДополнительныеПараметры.Форма.УникальныйИдентификатор);
		Если ЗначениеЗаполнено(ДанныеДляПроверкиПодписей) Тогда
			НачатьПроверкуПодписей(ДанныеДляПроверкиПодписей, 0);
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Непроверенных подписей не найдено'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПроверкуПодписей(ДанныеДляПроверкиПодписей, Индекс) Экспорт
	
	Если Индекс >= ДанныеДляПроверкиПодписей.Количество() Тогда
		ДокументыЭДОБЗК = Новый Массив;
		Для Каждого ДанныеЭлектронногоДокумента Из ДанныеДляПроверкиПодписей Цикл
			ДокументыЭДОБЗК.Добавить(ДанныеЭлектронногоДокумента.ДокументЭДОБЗК);
		КонецЦикла;
		ЭДОБЗКВызовСервера.ПроверитьГотовностьДокументовДляАрхивирования(ДокументыЭДОБЗК);
		ОповеститьОНеобходимостиОбновленияФорм(ДокументыЭДОБЗК);
		Если ДанныеДляПроверкиПодписей.Количество() > 1 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Проверка подписей завершена'"));
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ДанныеЭлектронногоДокумента = ДанныеДляПроверкиПодписей[Индекс];
	ВыводитьСообщения = ДанныеДляПроверкиПодписей.Количество() < 10;
	Если ВыводитьСообщения Тогда
		Состояние(НСтр("ru = 'Проверка подписей'"), , Строка(ДанныеЭлектронногоДокумента.ДокументЭДОБЗК));
	Иначе
		Состояние(НСтр("ru = 'Проверка подписей'"), Цел(Индекс / ДанныеДляПроверкиПодписей.Количество() * 100), Строка(ДанныеЭлектронногоДокумента.ДокументЭДОБЗК));
	КонецЕсли;
	
	ПараметрыПроверки = ЭлектроннаяПодписьКлиент.ПараметрыПроверкиПодписи();
	ПараметрыПроверки.РезультатВВидеСтруктуры = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеДляПроверкиПодписей", ДанныеДляПроверкиПодписей);
	ДополнительныеПараметры.Вставить("Индекс", Индекс);
	ДополнительныеПараметры.Вставить("СсылкаНаДвоичныеДанные", ДанныеЭлектронногоДокумента.СсылкаНаДвоичныеДанныеФайла);
	ДополнительныеПараметры.Вставить("ПодписиИБ", ДанныеЭлектронногоДокумента.ПодписиИБ);
	ДополнительныеПараметры.Вставить("ПодписиСервиса", ДанныеЭлектронногоДокумента.ПодписиСервиса);
	ДополнительныеПараметры.Вставить("ПараметрыПроверки", ПараметрыПроверки);
	
	Если ЗначениеЗаполнено(ДанныеЭлектронногоДокумента.ПодписиИБ) Тогда
		ПроверитьПодписиИБ(ДополнительныеПараметры.СсылкаНаДвоичныеДанные, ДополнительныеПараметры);
	ИначеЕсли ЗначениеЗаполнено(ДанныеЭлектронногоДокумента.ПодписиСервиса) Тогда
		ДополнительныеПараметры.Удалить("ИндексТекущейПодписи");
		ДополнительныеПараметры.ПараметрыПроверки.ПроверятьСертификат = ЭлектроннаяПодписьСлужебныйКлиентСервер.НеПроверятьСертификат();
		ПроверитьПодписиИБ(ДополнительныеПараметры.СсылкаНаДвоичныеДанные, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПроверкиПодписей(Результат, ДополнительныеПараметры) Экспорт
	
	НачатьПроверкуПодписей(ДополнительныеПараметры.ДанныеДляПроверкиПодписей, ДополнительныеПараметры.Индекс + 1);
	
КонецПроцедуры

#Область ПроверкаПодписейИБЭлектронныхДокументов

Процедура ПроверитьПодписиИБ(СсылкаНаДвоичныеДанные, ДополнительныеПараметры)
	
	Если Не ДополнительныеПараметры.Свойство("ИндексТекущейПодписи") Тогда
		ДополнительныеПараметры.Вставить("ИндексТекущейПодписи", 0);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПроверкиПодписиИБ", ЭтотОбъект, ДополнительныеПараметры);
	Если ДополнительныеПараметры.ПараметрыПроверки.ПроверятьСертификат = ЭлектроннаяПодписьСлужебныйКлиентСервер.ПроверятьКвалифицированные() Тогда
		ЭлектроннаяПодписьКлиент.ПроверитьПодпись(Оповещение, СсылкаНаДвоичныеДанные,
			ДополнительныеПараметры.ПодписиИБ[ДополнительныеПараметры.ИндексТекущейПодписи].АдресПодписи, , ,
			ДополнительныеПараметры.ПараметрыПроверки);
	Иначе
		ЭлектроннаяПодписьКлиент.ПроверитьПодпись(Оповещение, СсылкаНаДвоичныеДанные,
			ДополнительныеПараметры.ПодписиСервиса[ДополнительныеПараметры.ИндексТекущейПодписи].АдресПодписи, , ,
			ДополнительныеПараметры.ПараметрыПроверки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПроверкиПодписиИБ(Результат, ДополнительныеПараметры) Экспорт
	
	ПослеПроверкиПодписи(Результат, ДополнительныеПараметры);
	
	ИндексТекущейПодписи = ДополнительныеПараметры.ИндексТекущейПодписи + 1;
	Если ДополнительныеПараметры.ПараметрыПроверки.ПроверятьСертификат = ЭлектроннаяПодписьСлужебныйКлиентСервер.ПроверятьКвалифицированные()
		И ИндексТекущейПодписи < ДополнительныеПараметры.ПодписиИБ.Количество() Тогда
		
		ДополнительныеПараметры.ИндексТекущейПодписи = ИндексТекущейПодписи;
		ПроверитьПодписиИБ(ДополнительныеПараметры.СсылкаНаДвоичныеДанные, ДополнительныеПараметры)
	ИначеЕсли ЗначениеЗаполнено(ДополнительныеПараметры.ПодписиСервиса) Тогда
		Если ДополнительныеПараметры.ПараметрыПроверки.ПроверятьСертификат = ЭлектроннаяПодписьСлужебныйКлиентСервер.ПроверятьКвалифицированные() Тогда
			ИндексТекущейПодписи = 0;
			ДополнительныеПараметры.Вставить("ИндексТекущейПодписи", ИндексТекущейПодписи);
			ДополнительныеПараметры.ПараметрыПроверки.ПроверятьСертификат = ЭлектроннаяПодписьСлужебныйКлиентСервер.НеПроверятьСертификат();
		КонецЕсли;
		Если ИндексТекущейПодписи < ДополнительныеПараметры.ПодписиСервиса.Количество() Тогда
			ДополнительныеПараметры.ИндексТекущейПодписи = ИндексТекущейПодписи;
			ПроверитьПодписиИБ(ДополнительныеПараметры.СсылкаНаДвоичныеДанные, ДополнительныеПараметры)
		Иначе
			ПослеПроверкиПодписей(Результат, ДополнительныеПараметры);
		КонецЕсли;
	Иначе
		ПослеПроверкиПодписей(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ПослеПроверкиПодписи(Результат, ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ПараметрыПроверки.ПроверятьСертификат = ЭлектроннаяПодписьСлужебныйКлиентСервер.ПроверятьКвалифицированные() Тогда
		ДанныеСтроки = ДополнительныеПараметры.ПодписиИБ[ДополнительныеПараметры.ИндексТекущейПодписи];
	Иначе
		ДанныеСтроки = ДополнительныеПараметры.ПодписиСервиса[ДополнительныеПараметры.ИндексТекущейПодписи];
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеСтроки.ДатаПроверкиПодписи) Тогда
		ДанныеСтроки.ДатаПроверкиПодписи = ОбщегоНазначенияКлиент.ДатаСеанса();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, Результат);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеСтроки, "РезультатПроверки") Тогда
		ЗаполнитьЗначенияСвойств(ДанныеСтроки.РезультатПроверки, Результат);
		ЭлектроннаяПодписьКлиентСервер.ЗаполнитьСтатусПодписи(ДанныеСтроки, ОбщегоНазначенияКлиент.ДатаСеанса());
	КонецЕсли;
	
	Если ДанныеСтроки.ПодписьВерна Тогда
		ПодписанныйОбъект = ДополнительныеПараметры.ДанныеДляПроверкиПодписей[ДополнительныеПараметры.Индекс].ЭлектронныйДокумент;
		ЭДОБЗКВызовСервера.ОбновитьРезультатПроверкиПодписи(
			ПодписанныйОбъект, ДанныеСтроки.ПорядковыйНомер, ДанныеСтроки.ДатаПроверкиПодписи, ДанныеСтроки.ПодписьВерна);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ПриОбновленииДанныхДокументовЭДОБЗК(УправляемаяФорма = Неопределено)
	ОповеститьОбОбновленииДанныхДокументовЭДОБЗК(УправляемаяФорма);
	ЭДОБЗКВызовСервера.ОбновитьУведомленияВФоне();
	ПодключитьОбработчикОжидания("ОбновитьДанныеОДокументахЭДОБЗК", 2, Истина);
КонецПроцедуры

Процедура ПодписатьЭлектронныеДокументы(УправляемаяФорма, ЭлектронныеДокументы) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатыПодписанияДокументовСФайлами", ЭтотОбъект, Новый Структура);
	ДополнительныеПараметрыФормы = ЭДОБЗККлиент.ДополнительныеПараметрыФормыПодписанияДокументов(Оповещение);
	ОткрытьФормуПодписанияВсехПечатныхФорм(
		ЭлектронныеДокументы,
		УправляемаяФорма,
		ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовЭДОБЗК.Подписать"),
		ДополнительныеПараметрыФормы);
	
КонецПроцедуры

Процедура ОбработатьРезультатыПодписанияДокументовСФайлами(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ДополнительныеПараметры.Свойство("ПодписанныеФайлы") Тогда
			ЭДОБЗКВызовСервера.УдалитьФайлыИзОбработкиПользователя(ДополнительныеПараметры.ПодписанныеФайлы);
		КонецЕсли;
		ПоказатьСостояниеОтправленныхНаПодпись(ДополнительныеПараметры);
		ОповеститьОбОбновленииДанныхДокументовЭДОБЗК();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
