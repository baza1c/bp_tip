
#Область ПрограммныйИнтерфейс

// Возвращает наименование операции для целей сверки расчетов по типу документа и дополнительным сведениям
//
// Параметры:
//  ДокументСсылка - ссылка на документ для которого требуется определить наименование операции
//  ДополнительныеСведения - Структура - ключи определяются конфигурацией-потребителем для точного определения операции
//
// Возвращаемое значение:
//  НаименованиеОперации - Строка - наименование операции для целей сопоставления документов
//                                  на сервисе сверки, см. Перечисления.СервисСверкиРасчетовВидыОпераций
//
Функция НаименованиеОперации(ДокументСсылка, ДополнительныеСведения) Экспорт
	
	Возврат СервисСверкиРасчетовБП.НаименованиеОперации(ДокументСсылка, ДополнительныеСведения);
	
КонецФункции

// Возвращает структуру дополнительных сведений, необходимых для определение наименования операции
//
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
//
// Возвращаемое значение:
//  ДополнительныеСведения - Стуктура - ключи определяются конфигурацией-потребителем для точного определения операции
//
Функция ДополнительныеСведенияДляОпределенияОперации(Выборка) Экспорт
	
	Возврат СервисСверкиРасчетовБП.ДополнительныеСведенияДляОпределенияОперации(Выборка);
	
КонецФункции

// Получает сведения к отправке по документам для сверки.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, по которой отправлять документы, если не заполнено, то по всем подключенным 
//  ДатаНачала - дата, начиная с которой нужно получить сведения
//  ДатаОкончания - дата по которую нужно получить сведения
//
// Возвращаемое значение:
//  РезультатЗапроса, пример запроса:
//
//  "ВЫБРАТЬ
//  |	ВТ_ДанныеРеестра.Организация КАК Организация,
//  |	ВТ_ДанныеРеестра.Контрагент КАК Контрагент,
//  |	ВТ_ДанныеРеестра.Документ КАК Документ,
//  |	ВТ_ДанныеРеестра.ИдентификаторДокумента КАК ИдентификаторДокумента,
//  |	ВТ_ДанныеРеестра.Статус КАК Статус,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерДокумента, """") КАК НомерДокумента,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаРегистратора,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерРегистратора, """") КАК НомерРегистратора,
//  |	ВТ_ДанныеРеестра.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
//  |	Организации.ИНН КАК ИНН,
//  |	ВЫБОР
//  |		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
//  |			ТОГДА Организации.КПП
//  |		ИНАЧЕ """"
//  |	КОНЕЦ КАК КПП,
//  |	Организации.НаименованиеПолное КАК НаименованиеПолное,
//  |	СУММА(ВТ_ДанныеРеестра.Дебет - ВТ_ДанныеРеестра.Кредит) КАК Сумма,
//  |	Организации.Наименование КАК Наименование,
//  |	ВТ_ДанныеРеестра.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.Счет) КАК Счет,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.ВидПрочихДоходовИРасходов) КАК ВидПрочихДоходовИРасходов,
//  |	СУММА(ВЫБОР
//  |			КОГДА ВТ_ДанныеРеестра.ВалютнаяСумма < 0
//  |				ТОГДА -ВТ_ДанныеРеестра.ВалютнаяСумма
//  |			ИНАЧЕ ВТ_ДанныеРеестра.ВалютнаяСумма
//  |		КОНЕЦ) КАК ВалютнаяСумма,
//  |	ВТ_ДанныеРеестра.Валюта КАК Валюта,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто3) КАК КорСубконто3,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто2) КАК КорСубконто2,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто1) КАК КорСубконто1,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСчет) КАК КорСчет,
//  |	ВТ_ДанныеРеестра.ЭтоКорректировочныйДокумент КАК ЭтоКорректировочныйДокумент,
//  |	ВТ_ДанныеРеестра.ЭтоУниверсальныйДокумент КАК ЭтоУниверсальныйДокумент,
//  |	ВТ_ДанныеРеестра.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
//  |	ВТ_ДанныеРеестра.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
//  |	СУММА(ВТ_ДанныеРеестра.Кредит) КАК Кредит,
//  |	СУММА(ВТ_ДанныеРеестра.Дебет) КАК Дебет,
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") КАК НомерСчетаФактуры,
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСчетаФактуры,
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ЭтоКорректировочныйСчетФактура, ЛОЖЬ) КАК ЭтоКорректировочныйСчетФактура,
//  |	МАКСИМУМ(ЕСТЬNULL(ВТ_ДанныеПоНДС.СуммаНДС + ВТ_ДанныеПоНДС.СуммаНДСРазницаУменьшение + ВТ_ДанныеПоНДС.СуммаНДСРазницаУвеличение, 0)) КАК СуммаНДС,
//  |	ВТ_ДанныеРеестра.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагентаПолное КАК НаименованиеКонтрагентаПолное,
//  |	ВТ_ДанныеРеестра.ИННКонтрагента КАК ИННКонтрагента,
//  |	ВТ_ДанныеРеестра.КППКонтрагента КАК КППКонтрагента,
//  |	ВЫБОР
//  |		КОГДА ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") ПОДОБНО ""%[А-Я]%""
//  |			ТОГДА ИСТИНА
//  |		ИНАЧЕ ЛОЖЬ
//  |	КОНЕЦ КАК ОбрабатыватьНомерСФ
//  |ИЗ
//  |	ВТ_ДанныеРеестра КАК ВТ_ДанныеРеестра
//  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
//  |		ПО ВТ_ДанныеРеестра.Организация = Организации.Ссылка
//  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеПоНДС КАК ВТ_ДанныеПоНДС
//  |		ПО ВТ_ДанныеРеестра.Документ = ВТ_ДанныеПоНДС.ДокументОснование
//  |ГДЕ
//  |	(&ЗаВесьПериод
//  |			ИЛИ ВТ_ДанныеРеестра.ДатаДокумента >= &ДатаНачала
//  |				И ВТ_ДанныеРеестра.ДатаДокумента <= &ДатаОкончания)
//  |
//  |СГРУППИРОВАТЬ ПО
//  |	ВТ_ДанныеРеестра.Организация,
//  |	ВТ_ДанныеРеестра.Контрагент,
//  |	ВТ_ДанныеРеестра.Документ,
//  |	ВТ_ДанныеРеестра.ИдентификаторДокумента,
//  |	ВТ_ДанныеРеестра.Статус,
//  |	ВТ_ДанныеРеестра.ИдентификаторОрганизации,
//  |	Организации.ИНН,
//  |	ВЫБОР
//  |		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
//  |			ТОГДА Организации.КПП
//  |		ИНАЧЕ """"
//  |	КОНЕЦ,
//  |	Организации.НаименованиеПолное,
//  |	Организации.Наименование,
//  |	ВТ_ДанныеРеестра.ЦифровойИндексОбособленногоПодразделения,
//  |	ВТ_ДанныеРеестра.Валюта,
//  |	ВТ_ДанныеРеестра.ЭтоКорректировочныйДокумент,
//  |	ВТ_ДанныеРеестра.ЭтоУниверсальныйДокумент,
//  |	ВТ_ДанныеРеестра.ДатаВходящегоДокумента,
//  |	ВТ_ДанныеРеестра.НомерВходящегоДокумента,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерДокумента, """"),
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1)),
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """"),
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)),
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ЭтоКорректировочныйСчетФактура, ЛОЖЬ),
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)),
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерРегистратора, """"),
//  |	ВТ_ДанныеРеестра.ИдентификаторКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагентаПолное,
//  |	ВТ_ДанныеРеестра.ИННКонтрагента,
//  |	ВТ_ДанныеРеестра.КППКонтрагента,
//  |	ВЫБОР
//  |		КОГДА ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") ПОДОБНО ""%[А-Я]%""
//  |			ТОГДА ИСТИНА
//  |		ИНАЧЕ ЛОЖЬ
//  |	КОНЕЦ
//  |ИТОГИ
//  |	МАКСИМУМ(ИдентификаторОрганизации),
//  |	МАКСИМУМ(ИНН),
//  |	МАКСИМУМ(КПП),
//  |	МАКСИМУМ(НаименованиеПолное),
//  |	МАКСИМУМ(Наименование)
//  |ПО
//  |	Организация";
//
Функция ДанныеИнформационнойБазыКСверке(Организация = Неопределено, ДатаНачала = '00010101', ДатаОкончания = '00010101') Экспорт
	
	Возврат СервисСверкиРасчетовБП.ДанныеИнформационнойБазыКСверке(Организация, ДатаНачала, ДатаОкончания);
	
КонецФункции

// Возвращает данные из хранилища.
//
// Параметры:
//  Владелец    - ПланОбменаСсылка, СправочникСсылка, Строка
//  Ключи       - Строка - Содержит список имен сохраненных данных, указанных через запятую.
// 
// Возвращаемое значение:
//  Произвольный, Структура, Неопределено - Данные из хранилища. Если указан один ключ,
//                            то возвращается его значение, иначе структура.
//                            Если данные отсутствуют - Неопределено.
//
Функция ПрочитатьДанныеИзХранилища(Владелец, Ключ) Экспорт
	
	Возврат ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(Владелец, Ключ);
	
КонецФункции

// Записывает данные в хранилище.
//
// Параметры:
//  Владелец - ПланОбменаСсылка, СправочникСсылка, Строка
//
//  Данные  - Произвольный - Данные помещаемые хранилище. Неопределено - удаляет все данные.
//             Для удаления данных по ключу следует использовать процедуру УдалитьДанныеИзХранилища.
//  Ключ    - Строка       - Ключ сохраняемых настроек
//                           Ключ должен соответствовать правилам, установленным для идентификаторов:
//                           * Первым символом ключа должна быть буква или символ подчеркивания (_).
//                           * Каждый из последующих символов может быть буквой, цифрой или символом подчеркивания (_).
//
Процедура ЗаписатьДанныеВХранилище(Владелец, Данные, Ключ) Экспорт
	
	ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(Владелец, Данные, Ключ);
	
КонецПроцедуры

// Ищет контрагента по связке ИНН/КПП, заполняет структуру сведений о контрагенте
//
// Параметры:
//  РеквизитыКонтрагента - Структура со свойствами ИНН, КПП.
//  СведенияОКонтрагенте - Структура со свойствами:
//    ИНН - заполняется, когда не найден по ИНН
//    КПП - заполняется, когда не найден по КПП
//    Значение - ссылка на справочник, если не найден, то пустая ссылка
//
Процедура СведенияКонтрагентаПоИНН_КПП(РеквизитыКонтрагента, СведенияОКонтрагенте) Экспорт
	
	СервисСверкиРасчетовБП.СведенияКонтрагентаПоИНН_КПП(РеквизитыКонтрагента, СведенияОКонтрагенте);
	
КонецПроцедуры

Процедура УстановитьПраваСлужебногоПользователяСверкиРасчетов(Пользователь) Экспорт
	
	УправлениеДоступомБП.УстановитьПраваСлужебногоПользователяСверкиРасчетов(Пользователь);
	
КонецПроцедуры

// Проверяет нужно ли оповещать пользователя о расхождениях.
//
// Параметры:
//  Пользователь - ссылка на пользователя, которого нужно проверить на необходимость оповестить
//
// Возвращаемое значение:
//   Булево - признак того, что указанного пользователя нужно оповещать о расхождениях.
//
Функция ОповещатьПользователяОРасхождениях(Пользователь) Экспорт
	
	Возврат СервисСверкиРасчетовБП.ОповещатьПользователяОРасхождениях(Пользователь);
	
КонецФункции

// Возвращает организации, которые нужно подключить к сверке.
//
// Параметры:
//  ТолькоСДвижениями - булево - признак того, что нужно регистрировать только те организации
//  по которым есть движения/документы.
//
// Возвращаемое значение:
//   Массив - массив организаций для подключения.
//
Функция ОрганизацииДляПодключения(ТолькоСДвижениями = Ложь) Экспорт
	
	Возврат СервисСверкиРасчетовБП.ОрганизацииДляПодключения(ТолькоСДвижениями);
	
КонецФункции

// Регистрирует документы к сверке с даты начала работы сверки расчетов (см. СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов)
//
// Параметры:
//  МассивОрганизаций  - массив - если передан, регистрируются документы только указанных организаций.
//  МассивКонтрагентов - массив - если передан, регистрируются документы только указанных контрагентов.
//
Процедура ЗарегистрироватьКСверкеРанееВведенныеДокументы(МассивОрганизаций, МассивКонтрагентов) Экспорт
	
	СервисСверкиРасчетовБП.ЗарегистрироватьКСверкеРанееВведенныеДокументы(МассивОрганизаций, МассивКонтрагентов);
	
КонецПроцедуры

// Возвращает признак того, что документ является счетом-фактурой.
//
// Параметры:
//  ДокументОбъект - документ, который проверяется на соответствие типу счета-фактуры.
//
// Возвращаемое значение:
//  Булево - признак того, что документ является счетом-фактурой.
//
Функция ЭтоСчетФактура(ДокументОбъект) Экспорт
	
	Возврат СервисСверкиРасчетовБП.ЭтоСчетФактура(ДокументОбъект);
	
КонецФункции

// Регистрирует основания счета-фактуры к обмену. Сам счет-фактура не регистрируется к обмену,
// сведения о счете-фактуре отправляются вместе со сведениями об основании.
//
// Параметры:
//  ДокументОбъект - документ-основание счета-фактуры.
//
Процедура ЗарегистрироватьОснованияСФКОбмену(ДокументОбъект) Экспорт
	
	СервисСверкиРасчетовБП.ЗарегистрироватьОснованияСФКОбмену(ДокументОбъект);
	
КонецПроцедуры

// Удаляет повторяющиеся элементы массива.
//
// Параметры:
//  ОбрабатываемыйМассив - Массив - элементы произвольных типов, из которых удаляются неуникальные.
//
// Возвращаемое значение:
//   Массив - элементы ОбрабатываемыйМассив после удаления лишних.
//
Функция УдалитьПовторяющиесяЭлементыМассива(ОбрабатываемыйМассив) Экспорт
	
	Возврат ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(ОбрабатываемыйМассив);
	
КонецФункции

// Возвращает сведения для регистрации организации на сервисе сверки.
//
// Параметры:
//  Организация - Справочник.Ссылка - ссылка на организацию, которую требуется зарегистрировать на сервисе
//  Период - Дата - дата получения сведений по организации
//
// Возвращаемое значение:
//  РеквизитыОрганизации - структура с ключами:
//   *НаименованиеДляПечатныхФорм
//   *ЮридическоеФизическоеЛицо
//   *ИНН
//   *КПП
//   *ДолжностьРуководителяПредставление
//   *Руководитель
//   *ФИОФизлица
//   *Телефоны
//   *Email
//
Функция СведенияОЮрФизЛице(Организация, Период = '00010101') Экспорт
	
	Возврат БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Период);
	
КонецФункции

Функция ИспользоватьНесколькоОрганизаций() Экспорт
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций")
		И ДоступныеОрганизации.Количество() > 1;
	
КонецФункции

Функция НастройкаОбсужденийДоступна() Экспорт
	
	Возврат ПравоДоступа("АдминистрированиеДанных", Метаданные);
	
КонецФункции

Функция ОрганизацияПоУмолчанию() Экспорт
	
	Возврат Справочники.Организации.ОрганизацияПоУмолчанию();
	
КонецФункции

Функция РеквизитыОрганизацииДляПодключения(Организация) Экспорт
	
	РеквизитыОрганизацииДляПодключения = Новый Структура();
	РеквизитыОрганизацииДляПодключения.Вставить("ЭтоЮрЛицо", Справочники.Организации.ЭтоЮрЛицо(Организация));
	НаименованияНаДату = Справочники.Организации.НаименованияНаДату(Организация, ТекущаяДата());
	РеквизитыОрганизацииДляПодключения.Вставить("Наименование", НаименованияНаДату.СокращенноеНаименование);
	
	Возврат РеквизитыОрганизацииДляПодключения;
	
КонецФункции

Функция ЭтоПолныйИнтерфейс() Экспорт
	
	Возврат ОбщегоНазначенияБП.ЭтоПолныйИнтерфейс();
	
КонецФункции

Функция КонтрагентПодлежитСверке(Контрагент) Экспорт
	
	Возврат СервисСверкиРасчетовБП.КонтрагентПодлежитСверке(Контрагент);
	
КонецФункции

Функция ДанныеПервичногоДокумента(ДокументСсылка) Экспорт
	
	Возврат РегистрыСведений.ДанныеПервичныхДокументов.ДанныеПервичногоДокумента(ДокументСсылка);
	
КонецФункции

Функция КонтрагентыДляСверки() Экспорт
	
	Возврат СервисСверкиРасчетовБП.КонтрагентыДляСверки();
	
КонецФункции

Процедура ИнициализацияКомпоновщикаНастроек(Форма, ОрганизацияИзменилась = Ложь, ИмяВариантаНастроек = "") Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ИнициализацияКомпоновщикаНастроек(Форма, ОрганизацияИзменилась, ИмяВариантаНастроек);
	
	
КонецПроцедуры

Процедура ОтчетПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ЗаполнитьСписокОрганизаций(ЭлементПолеОрганизация, СоответствиеОрганизаций, Период = '00010101', ИсключитьФилиалы = Ложь) Экспорт
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(ЭлементПолеОрганизация, СоответствиеОрганизаций);
	
КонецПроцедуры 

Процедура ОбработкаРезультатаОтчета(ИдентификаторОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ИдентификаторОтчета, Результат);
	
КонецПроцедуры

Функция ПодготовитьОтчет(ПараметрыОтчета) Экспорт
	
	Возврат БухгалтерскиеОтчетыВызовСервера.ПодготовитьОтчет(ПараметрыОтчета);
	
КонецФункции

Функция ПолучитьДанныеСверкиДекларации(СтруктураПараметров, КнигаПокупок = Истина, ЭтоДекларация = Ложь, ТолькоДопЛисты = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                    СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ИдентификаторСверкиКонтрагенту", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("НачалоПериода",                  СтруктураПараметров.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",                   СтруктураПараметров.КонецПериода);
	Запрос.УстановитьПараметр("ОтборПоКонтрагенту",             ?(ЗначениеЗаполнено(СтруктураПараметров.Контрагент), Истина, Ложь));
	Запрос.УстановитьПараметр("Контрагент",                     СтруктураПараметров.Контрагент);
	Запрос.УстановитьПараметр("ДанныеСверки",                   СтруктураПараметров.ДанныеСверки);
	Запрос.УстановитьПараметр("КодВидаОперации",                СтруктураПараметров.КодВидаОперации);
	Запрос.УстановитьПараметр("ОперацияНетВОрганизации",        СтруктураПараметров.ОперацияНетВОрганизации);
	Если КнигаПокупок Тогда
		Запрос.Текст = ТекстЗапросаДляКнигиПокупок(ЭтоДекларация);
	Иначе
		Запрос.Текст = ТекстЗапросаДляКнигиПродаж(ТолькоДопЛисты, ЭтоДекларация);
	КонецЕсли;
	
	Возврат Запрос.ВыполнитьПакет();

КонецФункции

Функция ТекстЗапросаДляКнигиПродаж(ТолькоДопЛисты = Ложь, ЭтоДекларация = Ложь) Экспорт
	
	Если ЭтоДекларация Тогда
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Покупатель КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПокупательИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПокупательКПП КАК КонтрагентКПП,
		|	ДанныеСверки.НДС20 + ДанныеСверки.НДС18 + ДанныеСверки.НДС10 + ДанныеСверки.НДС7 + ДанныеСверки.НДС5 КАК НДС,
		|	ДанныеСверки.ВсегоПродаж КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПродаж > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
		|			ТОГДА СчетФактураВыданный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	Контрагенты.ИНН КАК КонтрагентИНН,
		|	Контрагенты.КПП КАК КонтрагентКПП,
		|	ДанныеДляСверкиПредварительные.НДС КАК НДС,
		|	ДанныеДляСверкиПредварительные.Всего КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураВыданный.Ссылка,
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) <> ТИП(Документ.СчетФактураПолученный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
		|			ТОГДА СчетФактураПолученный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.ИНН
		|		ИНАЧЕ Контрагенты.ИНН
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.КПП
		|		ИНАЧЕ Контрагенты.КПП
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.НДС,
		|	ДанныеДляСверкиПредварительные.Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|		ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураПолученный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка,
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) = ТИП(Документ.СчетФактураПолученный)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактураДокумент";
	Иначе
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Покупатель КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПокупательИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПокупательКПП КАК КонтрагентКПП,
		|	ДанныеСверки.НДС20 + ДанныеСверки.НДС18 + ДанныеСверки.НДС10 + ДанныеСверки.НДС7 + ДанныеСверки.НДС5 КАК НДС,
		|	ДанныеСверки.ВсегоПродаж КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПродаж > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверкиПредварительные.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП КАК КонтрагентКПП,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.НДС) КАК НДС,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.Всего) КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.СчетФактура,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактураДокумент";
		
	КонецЕсли;
	
	Текст = Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() +
		"ВЫБРАТЬ
		|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
		|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
		|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ,
		|	СервисСверкиРасчетовРеестрДокументов.Статус КАК Статус,
		|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторДокумента КАК ИдентификаторДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.НомерДокумента КАК НомерДокумента
		|ПОМЕСТИТЬ ДанныеРеестрДокументов
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
		|ГДЕ
		|	СервисСверкиРасчетовРеестрДокументов.Организация = &Организация
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовРеестрДокументов.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И СервисСверкиРасчетовРеестрДокументов.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента КАК СуммаПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента КАК НДСПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации КАК СуммаПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации КАК НДСПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымКонтрагента КАК СуммаВалПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымОрганизации КАК СуммаВалПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИННКонтрагента КАК ИННКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КППКонтрагента КАК КППКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку КАК КонтрагентНеПроизводилСверку
		|ПОМЕСТИТЬ ДанныеОбнаруженныеРасхождения
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИННКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КППКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку = ИСТИНА
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ДанныеРеестрДокументов.Организация, ДанныеДляСверки.Организация) КАК Организация,
		|	ЕСТЬNULL(ДанныеРеестрДокументов.Контрагент, ДанныеДляСверки.Контрагент) КАК Контрагент,
		|	ЕСТЬNULL(ДанныеРеестрДокументов.Документ, ДанныеДляСверки.СчетФактура) КАК Документ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ДанныеРеестрДокументов.ИдентификаторСообщения КАК СТРОКА(36)), """") КАК ИдентификаторСообщения,
		|	ДанныеРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
		|	ДанныеРеестрДокументов.НомерДокумента КАК НомерДокумента,
		|	ДанныеРеестрДокументов.Статус КАК Статус,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.Всего КАК Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	ЕСТЬNULL(ДанныеОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку, ЛОЖЬ) КАК КонтрагентНеПроизводилСверку
		|ПОМЕСТИТЬ ЧтоНеСверилиПредварительнаяТаблица
		|ИЗ
		|	ДанныеРеестрДокументов КАК ДанныеРеестрДокументов
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеДляСверки КАК ДанныеДляСверки
		|		ПО (ДанныеДляСверки.СчетФактура = ДанныеРеестрДокументов.Документ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|		ПО ДанныеРеестрДокументов.Контрагент = ДанныеОбнаруженныеРасхождения.Контрагент
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Организация КАК Организация,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаДокумента КАК ДатаДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерДокумента КАК НомерДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	ЧтоНеСверилиПредварительнаяТаблица.Всего КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ЧтоНеСверилиПредварительнаяТаблица.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	НЕ(Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|					И Контрагенты.ИндивидуальныйПредприниматель = ЛОЖЬ
		|				ИЛИ ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации В (&КодВидаОперации))
		|	И (ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения = """"
		|			ИЛИ ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку)
		|ИТОГИ
		|	СУММА(НДС),
		|	СУММА(Всего),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	СервисСверкиРасчетовДанныеСервиса.Сумма КАК СуммаКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НДС КАК НДСКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерДокумента КАК НомерДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры КАК НомерСчетФактурыК,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры КАК ДатаСчетФактурыК,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.Всего КАК Всего,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО ДанныеОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И (СервисСверкиРасчетовДанныеСервиса.Сумма <> ДанныеОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НДС <> ДанныеОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры <> ДанныеДляСверки.НомерСчетаФактуры
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры <> НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ))
		|ИТОГИ
		|	СУММА(СуммаКонтрагента),
		|	СУММА(НДСКонтрагента),
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	ЧтоНеСверилиПредварительнаяТаблица.Всего КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|		ПО ЧтоНеСверилиПредварительнаяТаблица.Документ = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения <> """"
		|	И ДанныеОбнаруженныеРасхождения.Документ ЕСТЬ NULL
		|	И ЧтоНеСверилиПредварительнаяТаблица.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры,
		|	1
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверки.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	(Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|				И Контрагенты.ИндивидуальныйПредприниматель = ЛОЖЬ
		|			ИЛИ ДанныеДляСверки.КодВидаОперации В (&КодВидаОперации))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры,
		|	1
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО ДанныеОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И СервисСверкиРасчетовДанныеСервиса.Сумма = ДанныеОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|	И СервисСверкиРасчетовДанныеСервиса.НДС = ДанныеОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|	И СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры = ДанныеДляСверки.НомерСчетаФактуры
		|	И СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры = НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ)
		|ИТОГИ
		|	СУММА(НДС),
		|	СУММА(Всего),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверки.Организация КАК Организация,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.Всего КАК Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|		ПО ДанныеДляСверки.СчетФактура = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ДанныеОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = &ИдентификаторСверкиКонтрагенту
		|	И НЕ ДанныеДляСверки.КодВидаОперации В (&КодВидаОперации)
		|ИТОГИ
		|	СУММА(НДС),
		|	СУММА(Всего),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ";
	
	Если Не ТолькоДопЛисты Тогда
		Текст = Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() +
		"ВЫБРАТЬ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
		|	СервисСверкиРасчетовДанныеСервиса.Сумма КАК Всего,
		|	СервисСверкиРасчетовДанныеСервиса.НДС КАК НДС,
		|	СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры КАК НомерСчетаФактуры,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры КАК ДатаСчетаФактуры,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИННКонтрагента КАК КонтрагентИНН,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КППКонтрагента КАК КонтрагентКПП,
		|	Контрагенты.НаименованиеПолное КАК КонтрагентНаименование,
		|	1 КАК КоличествоДокументов,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактураДокумент,
		|	"""" КАК КодВидаОперации
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|		ПО СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ = НЕОПРЕДЕЛЕНО
		|	И СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовДанныеСервиса.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И СервисСверкиРасчетовДанныеСервиса.Операция В(&ОперацияНетВОрганизации)
		|ИТОГИ
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ";
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

Функция ТекстЗапросаДляКнигиПокупок(ЭтоДекларация = Ложь) Экспорт
	
	Если ЭтоДекларация Тогда
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ДанныеСверки.ВсегоПокупок КАК СуммаБезНДС,
		|	ДанныеСверки.НДС КАК НДС,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Продавец КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПродавецИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПродавецКПП КАК КонтрагентКПП,
		|	ДанныеСверки.ВсегоПокупок КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПокупок > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
		|			ТОГДА СчетФактураПолученный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	ДанныеДляСверкиПредварительные.СуммаБезНДС КАК СуммаБезНДС,
		|	ДанныеДляСверкиПредварительные.НДС КАК НДС,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.ИНН
		|		ИНАЧЕ Контрагенты.ИНН
		|	КОНЕЦ КАК КонтрагентИНН,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.КПП
		|		ИНАЧЕ Контрагенты.КПП
		|	КОНЕЦ КАК КонтрагентКПП,
		|	ДанныеДляСверкиПредварительные.Всего КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|		ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураПолученный.Ссылка,
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) <> ТИП(Документ.СчетФактураВыданный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
		|			ТОГДА СчетФактураВыданный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.СуммаБезНДС,
		|	ДанныеДляСверкиПредварительные.НДС,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.ИНН
		|		ИНАЧЕ Контрагенты.ИНН
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.КПП
		|		ИНАЧЕ Контрагенты.КПП
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры
		|ИЗ
		|	Справочник.Организации КАК Организации,
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураВыданный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) = ТИП(Документ.СчетФактураВыданный)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактураДокумент";
	Иначе
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ДанныеСверки.ВсегоПокупок КАК СуммаБезНДС,
		|	ДанныеСверки.НДС КАК НДС,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Продавец КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПродавецИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПродавецКПП КАК КонтрагентКПП,
		|	ДанныеСверки.ВсегоПокупок КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПокупок > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверкиПредварительные.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.СуммаБезНДС) КАК СуммаБезНДС,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.НДС) КАК НДС,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП КАК КонтрагентКПП,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.Всего) КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДляСверкиПредварительные.СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	СчетФактура";
		
	КонецЕсли;
	
	Текст = Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() +
		"ВЫБРАТЬ
		|	СервисСверкиРасчетовРеестрДокументов.Организация КАК Организация,
		|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент,
		|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ,
		|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторДокумента КАК ИдентификаторДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.НомерДокумента КАК НомерДокумента
		|ПОМЕСТИТЬ ДанныеРеестрДокументов
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
		|ГДЕ
		|	СервисСверкиРасчетовРеестрДокументов.Организация = &Организация
		|	И СервисСверкиРасчетовРеестрДокументов.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовРеестрДокументов.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ,
		|	Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента КАК СуммаПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента КАК НДСПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации КАК СуммаПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации КАК НДСПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымКонтрагента КАК СуммаВалПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымОрганизации КАК СуммаВалПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИННКонтрагента КАК ИННКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КППКонтрагента КАК КППКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку КАК КонтрагентНеПроизводилСверку
		|ПОМЕСТИТЬ ДанныеОбнаруженныеРасхождения
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИННКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КППКонтрагента,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку = ИСТИНА
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ,
		|	Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ДанныеРеестрДокументов.Организация, ДанныеДляСверки.Организация) КАК Организация,
		|	ЕСТЬNULL(ДанныеРеестрДокументов.Документ, ДанныеДляСверки.СчетФактура) КАК Документ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ДанныеРеестрДокументов.ИдентификаторСообщения КАК СТРОКА(36)), """") КАК ИдентификаторСообщения,
		|	ДанныеРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
		|	ДанныеРеестрДокументов.НомерДокумента КАК НомерДокумента,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДС,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДСКнига,
		|	ДанныеДляСверки.НДС КАК НДСКнига,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЕСТЬNULL(ДанныеРеестрДокументов.Контрагент, ДанныеДляСверки.Контрагент) КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	ЕСТЬNULL(ДанныеОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку, ЛОЖЬ) КАК КонтрагентНеПроизводилСверку
		|ПОМЕСТИТЬ ЧтоНеСверилиПредварительнаяТаблица
		|ИЗ
		|	ДанныеРеестрДокументов КАК ДанныеРеестрДокументов
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеДляСверки КАК ДанныеДляСверки
		|		ПО (ДанныеДляСверки.СчетФактура = ДанныеРеестрДокументов.Документ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|		ПО ДанныеРеестрДокументов.Контрагент = ДанныеОбнаруженныеРасхождения.Контрагент
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Организация КАК Организация,
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаДокумента КАК ДатаДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерДокумента КАК НомерДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДС КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	1 КАК КоличествоДокументов,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДСКнига КАК СуммаБезНДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДСКнига КАК НДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|ГДЕ
		|	ЧтоНеСверилиПредварительнаяТаблица.Организация = &Организация
		|	И ЧтоНеСверилиПредварительнаяТаблица.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И (ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения = """"
		|			ИЛИ ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку)
		|	И НЕ ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации В (&КодВидаОперации)
		|ИТОГИ
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов),
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	СервисСверкиРасчетовДанныеСервиса.Сумма КАК СуммаКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НДС КАК НДСКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерДокумента КАК НомерДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры КАК НомерСчетФактурыК,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры КАК ДатаСчетФактурыК,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДСКнига,
		|	ДанныеДляСверки.НДС КАК НДСКнига,
		|	1 КАК КоличествоДокументов,
		|	ДанныеОбнаруженныеРасхождения.СуммаПоДаннымОрганизации КАК Всего,
		|	ДанныеОбнаруженныеРасхождения.НДСПоДаннымОрганизации КАК НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО ДанныеОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ДанныеОбнаруженныеРасхождения.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И (СервисСверкиРасчетовДанныеСервиса.Сумма <> ДанныеОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НДС <> ДанныеОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры <> ДанныеДляСверки.НомерСчетаФактуры
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры <> НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ))
		|ИТОГИ
		|	СУММА(СуммаКонтрагента),
		|	СУММА(НДСКонтрагента),
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига),
		|	СУММА(КоличествоДокументов),
		|	СУММА(Всего),
		|	СУММА(НДС)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДС КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	1 КАК КоличествоДокументов,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДСКнига КАК СуммаБезНДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДСКнига КАК НДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|		ПО ЧтоНеСверилиПредварительнаяТаблица.Документ = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ЕСТЬNULL(ДанныеОбнаруженныеРасхождения.Организация, &Организация) = &Организация
		|	И ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения <> """"
		|	И ДанныеОбнаруженныеРасхождения.Документ ЕСТЬ NULL
		|	И ЧтоНеСверилиПредварительнаяТаблица.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НДС,
		|	1,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|ГДЕ
		|	ДанныеДляСверки.КодВидаОперации В(&КодВидаОперации)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	ДанныеОбнаруженныеРасхождения.СуммаПоДаннымОрганизации,
		|	ДанныеОбнаруженныеРасхождения.НДСПоДаннымОрганизации,
		|	1,
		|	ДанныеДляСверки.СуммаБезНДС,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО ДанныеОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И ДанныеОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовДанныеСервиса.Сумма = ДанныеОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|	И СервисСверкиРасчетовДанныеСервиса.НДС = ДанныеОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|	И ДанныеДляСверки.НомерСчетаФактуры = СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры
		|	И СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры = НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ)
		|ИТОГИ
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов),
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверки.Организация КАК Организация,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДСКнига,
		|	ДанныеДляСверки.НДС КАК НДСКнига,
		|	1 КАК КоличествоДокументов,
		|	ДанныеОбнаруженныеРасхождения.СуммаПоДаннымОрганизации КАК Всего,
		|	ДанныеОбнаруженныеРасхождения.НДСПоДаннымОрганизации КАК НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбнаруженныеРасхождения КАК ДанныеОбнаруженныеРасхождения
		|		ПО ДанныеДляСверки.СчетФактура = ДанныеОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ДанныеДляСверки.Организация = &Организация
		|	И НЕ ДанныеДляСверки.КодВидаОперации В (&КодВидаОперации)
		|	И ДанныеОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = &ИдентификаторСверкиКонтрагенту
		|	И ДанныеОбнаруженныеРасхождения.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	Всего УБЫВ
		|ИТОГИ
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига),
		|	СУММА(КоличествоДокументов),
		|	СУММА(Всего),
		|	СУММА(НДС)
		|ПО
		|	ОБЩИЕ";
	
	Возврат Текст;
	
КонецФункции

// Считывает из регистра сегмент данных раздела для выгрузки электронного представления декларации по НДС.
// Параметры:
//   ДекларацияНДС - ДокументСсылка.РегламентированныйОтчет - ссылка на документ, в котором сохраняются данные декларации;
//   Раздел - Строка - имя раздела;
//   НомерПервойСтроки - Число - номер первой строки, отраженной в сегменте;
// Возвращаемое значение:
//   Таблица значений - сегмент данных раздела для выгрузки.
//
Функция СегментДанныхРазделаДекларацииНДС(ДекларацияНДС, ИмяРаздела, НомерПервойСтроки) Экспорт
	
	ВидДополнительногоФайла = "Данные" + ИмяРаздела + "." + Формат(НомерПервойСтроки, "ЧН=; ЧГ=0");
	
	ПараметрыЧтения = Новый Структура;
	ПараметрыЧтения.Вставить("РегламентированныйОтчет", ДекларацияНДС);
	ПараметрыЧтения.Вставить("ВидДополнительногоФайла", ВидДополнительногоФайла);
	
	Возврат СервисСверкиРасчетовБП.ДанныеРегистраДополнительныхФайлов(ПараметрыЧтения);
	
КонецФункции

// Возвратает наименование картинки, являющейся логотипом помощника конфигурации.
//
// Возвращаемое значение:
//   Массив - элементы ОбрабатываемыйМассив после удаления лишних.
//
Функция КартинкаЛоготипаПомощника() Экспорт
	
	Возврат СервисСверкиРасчетовБП.КартинкаЛоготипаПомощника();
	
КонецФункции

// См. СервисСверкиРасчетов.ДанныеОРелевантныхКонтрагентах()
Функция ДанныеОРелевантныхКонтрагентах() Экспорт
	
	Возврат СервисСверкиРасчетовБП.ДанныеОРелевантныхКонтрагентах();
	
КонецФункции

// Очищает накопленный кэш данных контрагентов 1С:Бизнес-сеть при включении сервиса.
// До включения сервиса кэш данных не актуализировался в случае изменения КПП контрагента
// После включения сервиса кэш будет сформирован заново по актуальным контрагентам (не по всем).
Процедура СброситьКэшДанныхКонтрагентов() Экспорт
	
	НаборЗаписей = РегистрыСведений.КонтрагентыБизнесСеть.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Проверяет по переданному признаку является ли организация или контрагент юридическим лицом.  
// Параметры:
//   ПризнакДляОпределенияЮрЛица - в зависимости от потребителя может быть перечислением или строкой.
/// Возвращаемое значение:
//   Булево:
//  Истина - является юридическим лицом
//  Ложь - не явлется юридическим лицом
//
Функция ЭтоЮридическоеЛицо(ПризнакДляОпределенияЮрЛица) Экспорт
	
	Возврат ПризнакДляОпределенияЮрЛица = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
КонецФункции


#КонецОбласти







