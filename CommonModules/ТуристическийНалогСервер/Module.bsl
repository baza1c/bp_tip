
 #Область СлужебныйПрограммныйИнтерфейс

// Устанавливает реквизит Гостиница значением в зависимости от организации.
//
// Параметры
//  Гостиница - СправочникСсылка.Гостиницы - устанавливаемый реквизит
//  Организация   - СправочникСсылка.Организации - реквизит Организация
//
Процедура УстановитьГостиницу(Гостиница, Организация) Экспорт
			
	Если ЗначениеЗаполнено(Гостиница) Тогда
		ЗначениеГостиница = Гостиница;
	Иначе
		ЗначениеГостиница = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяГостиница"); 
	КонецЕсли;
	
	Если Не ГостиницаПринадлежитОрганизации(ЗначениеГостиница, Организация) Тогда

		ЗначениеГостиница = ЕдинственнаяГостиницаПоОрганизации(Организация);
				
	КонецЕсли;
	
	Гостиница = ЗначениеГостиница;
	
КонецПроцедуры

// Возвращает принадлежность Гостиницы к организации
//
// Параметры
//  Гостиница - СправочникСсылка.Гостиницы - проверяемый реквизит
//  Организация   - СправочникСсылка.Организации - реквизит Организация
//
// Возвращаемое значение:
//   Булево			– принадлежность Гостиницы к организации
//
Функция ГостиницаПринадлежитОрганизации(Гостиница, Организация) Экспорт

	Результат = Ложь;

	Если ЗначениеЗаполнено(Гостиница) И ЗначениеЗаполнено(Организация) Тогда
		
		РеквизитОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Гостиница, "Организация");
		Результат = РеквизитОрганизация = Организация;
			
	КонецЕсли;

	Возврат Результат;

КонецФункции 

// Возвращает значение гостиницы по организации, если оно единственное
//
// Параметры
//  Организация   - СправочникСсылка.Организации - реквизит Организация
//
// Возвращаемое значение:
//   СправочникСсылка.Гостиницы	–Ссылка на гостиницу или пустую ссылку,
//									в случае, если гостиница не единственная 
//
Функция ЕдинственнаяГостиницаПоОрганизации(Организация) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	Гостиницы.Ссылка КАК Гостиница
		|ИЗ
		|	Справочник.Гостиницы КАК Гостиницы
		|ГДЕ
		|	Гостиницы.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Гостиница;
	КонецЕсли;
	
	Возврат Справочники.Гостиницы.ПустаяСсылка();
	
КонецФункции

// Процедура для вызова из регламентной операции
// Параметры
//  ТаблицаРеквизиты   - ТаблицаЗначений - реквизиты, необходимые для формирования движений.
//  Отказ              - Булево - флаг отказа от проведения
//
Функция ПодготовитьТаблицыРасчета(ТаблицаРеквизиты, Отказ, Ошибки) Экспорт
	
	СтруктураТаблиц = Новый Структура();
	СтруктураТаблиц.Вставить("ТаблицаПроводок" , Неопределено);
	СтруктураТаблиц.Вставить("ТаблицаПроводокИсключениеТурНалога" , Неопределено);
	СтруктураТаблиц.Вставить("ТаблицаСправкиРасчета", Неопределено);
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыРасчета(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаПроводок  = НовыйТаблицаПроводок();  
	ТаблицаПроводокИсключениеТурНалога  = НовыйТаблицаПроводокИсключениеТурНалога();
	ТаблицаСправкиРасчета = НовыйТаблицаСправкиРасчета();
	
	РассчитатьТуристическийНалог(Реквизиты, ТаблицаПроводок, ТаблицаПроводокИсключениеТурНалога, 
		ТаблицаСправкиРасчета, Ошибки, Отказ);
	
	Если Не Отказ Тогда
		
		СтруктураТаблиц.Вставить("ТаблицаПроводок", ТаблицаПроводок);
		СтруктураТаблиц.Вставить("ТаблицаПроводокИсключениеТурНалога", ТаблицаПроводокИсключениеТурНалога);
		СтруктураТаблиц.Вставить("ТаблицаСправкиРасчета", ТаблицаСправкиРасчета);
		
	КонецЕсли;
	
	Возврат СтруктураТаблиц;
	
КонецФункции

// Процедура для вызова из регламентной операции
// Параметры
//  Реквизиты   - ТаблицаЗначений - см. ДанныеИсчисленногоТуристическогоНалога()
//
Функция ТуристическийНалогИсчисленный(Реквизиты) Экспорт 
	
	ДанныеДляРасчетаНалога = ДанныеИсчисленногоТуристическогоНалога(Реквизиты); 
	
	ДанныеДляРасчетаНалога.Колонки.Добавить("НалогИсчисляется", Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаУслуг Из ДанныеДляРасчетаНалога Цикл  
		Если Не СтрокаУслуг.НалогРассчитанРанее Тогда
			ТуристическийНалогКлиентСервер.ЗаполнитьСпособИСуммуНалога(СтрокаУслуг);
		КонецЕсли;
		Если СтрокаУслуг.ВидЛьготы = Перечисления.ВидыЛьготПоТуристическомуНалогу.НеПрименяется
			Или СтрокаУслуг.ВидЛьготы = Перечисления.ВидыЛьготПоТуристическомуНалогу.ОплатаИзБюджета Тогда
			СтрокаУслуг.НалогИсчисляется = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеДляРасчетаНалога.Свернуть(
		"ВидЛьготы, Гостиница, СпособРасчета, Организация, Номенклатура, УслугаРазмещения,
		|РасчетныйДокумент, ДокументОснование, Ставка, ПодразделениеОрганизации, 
		|СтавкаНДС, ВидСтавкиНДС, НоменклатурнаяГруппа, Контрагент, 
		|ДоговорКонтрагента, НалогРассчитанРанее, Склад, ИдентификаторСтроки, РасчетПогрешностиОкругления, НалогИсчисляется", 
		"КоличествоЛиц, КоличествоУслуг, СтоимостьУслуг, СтоимостьДопУслуг, СуммаНалога");
	
	Возврат ДанныеДляРасчетаНалога;
	
КонецФункции

// Возвращает признак видимости баннера с сообщением о туристическом налоге
//
// Параметры:
//   КодЗадачи   - Строка - код задачи по уплате налога
//   Период      - Дата - период, для которого требуется проверить видимость баннера, 
//				   Неопределено - если проверка периода не требуется
//
// Возвращаемое значение:
//   Булево
//
Функция ВидимостьБаннераИзмененияВУплатеНалога(КодЗадачи, Организация, Период = Неопределено) Экспорт

	ЧастиКлюча = Новый Массив;
	ЧастиКлюча.Добавить(КодЗадачи);
	ЧастиКлюча.Добавить(Организация.УникальныйИдентификатор());
	
	КлючНастроек = СтрСоединить(ЧастиКлюча, "_");
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ВидимостьБаннераИзмененияВУплатеНалога",
		КлючНастроек,
		Истина);
	
КонецФункции

Функция ПолучитьСведенияОПоказателяхОтчетаТуристическийНалог(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П00001М100500", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П00001М201000", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П00001М202000", Истина, Ложь);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020000101", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020001001", Истина, Ложь); 
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П00002УИД2000", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ПС00020002001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002002", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ПС00020002002", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002003", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002004", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ПС00020002004", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002005", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002006", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ПС00020002006", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002007", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002008", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ПС00020002008", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002009", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002010", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "ПС00020002010", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002011", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002012", Истина, Ложь);	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П00002М102013", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П00002М102014", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002015", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020002016", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020004001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020005001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020006001", Истина, Ложь);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020007003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020007503", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020008003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020008503", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020008603", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020008703", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020009003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020010003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020011003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020012003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020013003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020014003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000020015003", Истина, Истина);
	
	Возврат ПоказателиОтчета;
	
КонецФункции

Процедура ЗаполнитьРегламентированныйОтчетТуристическийНалог(ПараметрыОтчета, Контейнер) Экспорт
	
	Перем ТаблицаРасшифровки; // Таблица для сбора сведений о расшифровке
	
	ПараметрыЗаполнения = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыОтчета);
	ПараметрыЗаполнения.Вставить("ЗнакиДробнойЧасти", 5); 
	
	КодыКатегорий = СоответствиеКодыКатегорийСредствРазмещения();
	Раздел2 = Контейнер.Раздел2; 
	СтрокиРаздел2 = Раздел2.Строки[0];
	СтраницаРаздел2 = СтрокиРаздел2.Данные;
	Для Каждого Показатель Из СтраницаРаздел2 Цикл
		СтраницаРаздел2[Показатель.Ключ] = ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение));
	КонецЦикла;
	
	СтрокиЗданий = СтрокиРаздел2.ДанныеМногострочныхЧастей.П00002М1.Строки;
	Пока СтрокиЗданий.Количество() > 1 Цикл
		СтрокиЗданий.Удалить(1);
	КонецЦикла;

	СтраницаРаздел2Здание = СтрокиЗданий[0].Данные;
	Для Каждого Показатель Из СтраницаРаздел2Здание Цикл
		СтраницаРаздел2Здание[Показатель.Ключ] = ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение));
	КонецЦикла;

	ПерваяСтраница = Истина;

	Пока Раздел2.Строки.Количество() > 1 Цикл
		Раздел2.Строки.Удалить(1);
	КонецЦикла;

	ДанныеОтчета = ДанныеРегОтчетаТуристическийНалог(ПараметрыЗаполнения);
	
	ДанныеАдресов = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияОбъектовНаДату(
		ДанныеОтчета.ИтогиПоГостиницамСтавкам.ВыгрузитьКолонку("Гостиница"));
    ДанныеАдресов.Индексы.Добавить("Объект");

	Для Каждого ДанныеСтраницаРаздел2 Из ДанныеОтчета.ИтогиПоГостиницамСтавкам Цикл
		
		Если Не ПерваяСтраница Тогда
			// Создаем дополнительную страницу в декларации
			СтруктураНовойТаблицы = Новый Структура();
			СтруктураДанныхМногострочныхЧастей = Новый Структура();

			Для Каждого Показатель Из СтраницаРаздел2 Цикл
				СтруктураНовойТаблицы.Вставить(Показатель.Ключ, 
					ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение)));
			КонецЦикла;
			
			Для Каждого Показатель Из СтраницаРаздел2Здание Цикл
				СтруктураДанныхМногострочныхЧастей.Вставить(Показатель.Ключ, 
					ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение)));
			КонецЦикла;

			НоваяСтраницаРаздела2 = Раздел2.Строки.Добавить();		
			НоваяСтраницаРаздела2.Данные = СтруктураНовойТаблицы;
			НоваяСтраницаРаздела2.ДанныеМногострочныхЧастей = Новый Структура("П00002М1", ДеревоЗначенийРегОтчет());
			
			СтрокиРаздел2 = Раздел2.Строки[Раздел2.Строки.Количество()-1];
			СтраницаРаздел2 = СтрокиРаздел2.Данные;

			НоваяСтрокаРаздел2Здание = 
				НоваяСтраницаРаздела2.ДанныеМногострочныхЧастей.П00002М1.Строки.Добавить();
			НоваяСтрокаРаздел2Здание.Данные = СтруктураДанныхМногострочныхЧастей;
			НоваяСтрокаРаздел2Здание.ДанныеМногострочныхЧастей = Новый Структура;
			
	        СтраницаРаздел2Здание = СтрокиРаздел2.ДанныеМногострочныхЧастей.П00002М1.Строки[0].Данные;

		Иначе
			// На последующих выполнениях цикла нужно будет создавать дополнительные страницы
			ПерваяСтраница = Ложь; 
		КонецЕсли;
		
		
		ЗаполнитьСведенияОбОбъектеРазмещения(Раздел2, СтраницаРаздел2, СтраницаРаздел2Здание, СтрокиРаздел2, 
			ДанныеСтраницаРаздел2, ДанныеАдресов, КодыКатегорий, ТаблицаРасшифровки);
			
		Отбор = Новый Структура("Гостиница, Ставка", ДанныеСтраницаРаздел2.Гостиница, ДанныеСтраницаРаздел2.Ставка); 
		ДанныеПоказателей = ДанныеОтчета.ДанныеПоказателей.Скопировать(Отбор);
		
		Если ДанныеПоказателей.Количество() > 0 Тогда
			ЗаполнитьПоказателиРаздела2РегОтчета(Раздел2, СтраницаРаздел2, ДанныеПоказателей[0], ТаблицаРасшифровки);
		КонецЕсли;
		
	КонецЦикла;
		
	ПоместитьВоВременноеХранилище(Контейнер, ПараметрыОтчета.АдресВоВременномХранилище);
	ПоместитьВоВременноеХранилище(ТаблицаРасшифровки, ПараметрыОтчета.АдресВременногоХранилищаРасшифровки);

КонецПроцедуры

// Возвращает массив основных услуг размещения
//
Функция МассивУслугРазмещения() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ВидыУслугРазмещения.УслугаРазмещения);
	Результат.Добавить(Перечисления.ВидыУслугРазмещения.СанаторноКурортноеЛечение);
	
	Возврат Результат;
	
КонецФункции

// Возвращает гостинцу в родительской строке
// Параметры
//  Товары                          - табличная часть Товары.
//  ИдентификаторРодительскойСтроки - идентификатор родительской строки.
//
Функция ГостиницаСвязаннойУслуги(Товары, ИдентификаторРодительскойСтроки) Экспорт
	
	Отбор = Новый Структура("ИдентификаторСтроки", ИдентификаторРодительскойСтроки); 
	СтрокиПоИдентификатору = Товары.НайтиСтроки(Отбор);
	Если СтрокиПоИдентификатору.Количество() > 0 Тогда
		Возврат СтрокиПоИдентификатору[0].Гостиница;
	КонецЕсли;
	
	Возврат Справочники.Гостиницы.ПустаяСсылка();
	
КонецФункции

Процедура ПроверитьПрименимостьЛьготы(Дата, Ошибки, ДанныеДляРасчетаНалога, Отказ) Экспорт
	
	Если Дата >= ДатаПримененияЛьготыОплатаИзБюджета() Тогда
		Возврат;
	КонецЕсли; 
	
	ВидЛьготы = Перечисления.ВидыЛьготПоТуристическомуНалогу.ОплатаИзБюджета;
	Отбор = Новый Структура("ВидЛьготы", ВидЛьготы);
	СтрокиСоЛьготой = ДанныеДляРасчетаНалога.НайтиСтроки(Отбор);
	
	Если СтрокиСоЛьготой.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("ОплатаИзБюджета", ВидЛьготы);
		
	Ошибка = ВыводСообщенийОбОшибках.ДобавитьПростоеОписаниеОшибки(Ошибки);
	Ошибка.Описание = НСтр("ru = 'Неверно указана льгота по туристическому налогу.'");
	Рекомендация    = НСтр("ru = 'Льгота ""[ОплатаИзБюджета]"" может быть применена к услугам, оплачиваемым с начала 2026 года.'");
	Ошибка.Рекомендация = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Рекомендация, ПараметрыТекста);
		
	Для Каждого СтрокаСоЛьготой Из  СтрокиСоЛьготой Цикл
			
		ТекстОшибки = НСтр("ru = 'В документе ""[Документ]"" по услуге ""[Номенклатура]"" указана льгота ""[ОплатаИзБюджета]""'");
		
		ПараметрыТекста.Вставить("Документ", СтрокаСоЛьготой.ДокументОснование);
		ПараметрыТекста.Вставить("Номенклатура", СтрокаСоЛьготой.Номенклатура);
						
		МестоОшибки = Ошибка.ЛокализацияДетально.Добавить();
		МестоОшибки.Локализация = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ТекстОшибки, ПараметрыТекста);
		МестоОшибки.Ссылка      = СтрокаСоЛьготой.ДокументОснование;
		
		
	КонецЦикла;

КонецПроцедуры

Функция НовыйПараметрыПроверкиЛьготы() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("УслугаРазмещения", Справочники.Номенклатура.ПустаяСсылка());
	Результат.Вставить("МассивУслуг", Новый Массив);
    Результат.Вставить("СтрокаУслуги");
    Результат.Вставить("ИмяТабличнойЧасти", "");
	
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьКорректностьЗаполненияВидаЛьготы(Объект, ПараметрыПроверки, Отказ) Экспорт
	
	Если Объект.Дата < ДатаПримененияЛьготыОплатаИзБюджета()
		И ПараметрыПроверки.МассивУслуг.Найти(ПараметрыПроверки.УслугаРазмещения) <> Неопределено 
		И ПараметрыПроверки.СтрокаУслуги.ВидЛьготыПоТуристическомуНалогу = 
			Перечисления.ВидыЛьготПоТуристическомуНалогу.ОплатаИзБюджета Тогда
		
		ТекстОшибки = НСтр("ru = 'Вид льготы ""%1"" может применяться только с 2026 года'");
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность",
			НСтр("ru = 'Льгота по тур.налогу'"),
			ПараметрыПроверки.СтрокаУслуги.НомерСтроки,
			"Услуги",
			СтрШаблон(ТекстОшибки, ПараметрыПроверки.СтрокаУслуги.ВидЛьготыПоТуристическомуНалогу));

		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыПроверки.ИмяТабличнойЧасти, 
			ПараметрыПроверки.СтрокаУслуги.НомерСтроки, "ВидЛьготыПоТуристическомуНалогу");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "Объект", Отказ);
	КонецЕсли;

КонецПроцедуры

Функция ДатаПримененияЛьготыОплатаИзБюджета() Экспорт
	Возврат '20260101';
КонецФункции

Функция ДатаИзмененияПериодичностиРегОперации() Экспорт
	Возврат '20260101';
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоказателиРаздела2РегОтчета(Раздел2, СтраницаРаздел2, СтрокаДанныеПоказателей, ТаблицаРасшифровки) 
	
	// Подготовим параметры отчета расшифровки - справки-расчета по туристическому налогу
	ДопПараметры = ДополнительныеПараметрыСправкиРасчет(СтрокаДанныеПоказателей, Неопределено, Неопределено); 
	ДопПараметрыФедеральнаяЛьгота = ДополнительныеПараметрыСправкиРасчет(СтрокаДанныеПоказателей, "ВидЛьготыСсылка",
		Перечисления.ВидыЛьготПоТуристическомуНалогу.Федеральная); 
    ДопПараметрыМестнаяЛьгота = ДополнительныеПараметрыСправкиРасчет(СтрокаДанныеПоказателей, "ВидЛьготыСсылка", 
		Перечисления.ВидыЛьготПоТуристическомуНалогу.Местная); 
    ДопПараметрыМинимальныйНалог =  ДополнительныеПараметрыСправкиРасчет(СтрокаДанныеПоказателей, "СпособРасчета",
		Перечисления.СпособыРасчетаТуристическогоНалога.МинимальныйНалог); 	
	ДопПараметрыПоСтавке = ДополнительныеПараметрыСправкиРасчет(СтрокаДанныеПоказателей, "ВыводитьСтавку", Истина); 
		
	// Стоимость услуг всего
	СтраницаРаздел2.П000020007003 = СтрокаДанныеПоказателей.СтоимостьУслуг;
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020007003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020007003, "", ДопПараметры);
	
	// Количество лиц
	СтраницаРаздел2.П000020007503 = СтрокаДанныеПоказателей.КоличествоЛиц; 
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020007503_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020007503, "", ДопПараметры);
	
	// Стоимость по федеральной льготе
	СтраницаРаздел2.П000020008003 = СтрокаДанныеПоказателей.СтоимостьУслугФедеральнаяЛьгота; 
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020008003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020008003, "", ДопПараметрыФедеральнаяЛьгота);
	
	// Количество лиц по федеральной льготе
	СтраницаРаздел2.П000020008503 = СтрокаДанныеПоказателей.КоличествоЛицФедеральнаяЛьгота;
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020008503_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020008503, "", ДопПараметрыФедеральнаяЛьгота);
	
	// Стоимость по местной льготе
	СтраницаРаздел2.П000020008603 = СтрокаДанныеПоказателей.СтоимостьУслугМестнаяЛьгота;
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020008603_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020008603, "", ДопПараметрыМестнаяЛьгота);
	
	// Количество лиц по местной льготе
	СтраницаРаздел2.П000020008703 = СтрокаДанныеПоказателей.КоличествоЛицМестнаяЛьгота;
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020008703_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020008703, "", ДопПараметрыМестнаяЛьгота);
	
	// Стоимость услуг по минимальному налогу
    СтраницаРаздел2.П000020009003 = СтрокаДанныеПоказателей.СтоимостьУслугМинимальныйНалог;
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020009003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020009003, "", ДопПараметрыМинимальныйНалог);
	
	// Ставка
	СтраницаРаздел2.П000020011003 = СтрокаДанныеПоказателей.Ставка;	
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020011003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020011003, "", ДопПараметрыПоСтавке);
	
	// Сумма минимального налога
    СтраницаРаздел2.П000020013003 = СтрокаДанныеПоказателей.СуммаНалогаМинимальныйНалог;
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020013003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020013003, "", ДопПараметрыМинимальныйНалог);
	
	// Налоговая база по ставке
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020010003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020010003, "", ДопПараметрыПоСтавке);
	
	// Сумма налога по ставке
    ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020012003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020012003, "", ДопПараметрыПоСтавке);

	// Сумма налога к уплате
    ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000020015003_" + Раздел2.Строки.Количество(),
		"", "", "", СтраницаРаздел2.П000020015003, "", ДопПараметры);
	
КонецПроцедуры

Функция ДополнительныеПараметрыСправкиРасчет(СтрокаДанныеПоказателей, ИмяДополнительноеЗначение, ДополнительноеЗначение) 
	
	ДополнительныеПараметры = Новый Структура;
	ПараметрыРасшифровки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	Отбор = ПараметрыРасшифровки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
	
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Отбор, 
		"Гостиница", СтрокаДанныеПоказателей.Гостиница, ВидСравненияКомпоновкиДанных.Равно);
    БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Отбор, 
		"Ставка", СтрокаДанныеПоказателей.Ставка, ВидСравненияКомпоновкиДанных.Равно);

	Если ДополнительноеЗначение <> Неопределено Тогда   
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Отбор, ИмяДополнительноеЗначение,
			ДополнительноеЗначение, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
		
	ДополнительныеПараметры.Вставить("ПараметрыРасшифровки", ПараметрыРасшифровки);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Процедура ЗаполнитьСведенияОбОбъектеРазмещения(Раздел2, СтраницаРаздел2, СтраницаРаздел2Здание, СтрокиРаздел2,
	ДанныеСтраницаРаздел2, ДанныеАдресов, КодыКатегорий, ТаблицаРасшифровки) 
	
	ДополнительныеПараметрыАдреса = Новый Структура;
	ДополнительныеПараметрыАдреса.Вставить("ВерсияАдреса", 3);

	АдресМестонахождения = ДанныеАдресов.Найти(ДанныеСтраницаРаздел2.Гостиница, "Объект");
	
	Если АдресМестонахождения <> Неопределено И НЕ ПустаяСтрока(АдресМестонахождения.Значение) Тогда
		
		ПоляАдреса = ИнтерфейсыВзаимодействияБРО.АдресВФорматеФИАС(АдресМестонахождения.Значение, ДополнительныеПараметрыАдреса);
		
		СтраницаРаздел2.П00002УИД2000 = ПоляАдреса.ИдентификаторАдресногоОбъекта;
		СтраницаРаздел2.П000020002001 = ПоляАдреса.КодРегиона;
		СтраницаРаздел2.ПС00020002001 = ПоляАдреса.РегионСокращение;
		СтраницаРаздел2.П000020002002 = ПоляАдреса.МуниципальныйРайонВид;
		СтраницаРаздел2.ПС00020002002 = ПоляАдреса.МуниципальныйРайонСокращение;
		СтраницаРаздел2.П000020002003 = ПоляАдреса.МуниципальныйРайон;
		СтраницаРаздел2.П000020002004 = ПоляАдреса.ПоселениеВид;
		СтраницаРаздел2.ПС00020002004 = ПоляАдреса.ПоселениеСокращение;
		СтраницаРаздел2.П000020002005 = ПоляАдреса.Поселение;
		СтраницаРаздел2.П000020002006 = ПоляАдреса.НаселенныйПунктВид;
		СтраницаРаздел2.ПС00020002006 = ПоляАдреса.НаселенныйПунктСокращение;
		СтраницаРаздел2.П000020002007 = ПоляАдреса.НаселенныйПункт;
		СтраницаРаздел2.П000020002008 = ПоляАдреса.ТерриторияТип;
		СтраницаРаздел2.ПС00020002008 = ПоляАдреса.ТерриторияСокращение;
		СтраницаРаздел2.П000020002009 = ПоляАдреса.Территория;
		СтраницаРаздел2.П000020002010 = ПоляАдреса.УлицаТип;
		СтраницаРаздел2.ПС00020002010 = ПоляАдреса.УлицаСокращение;
		СтраницаРаздел2.П000020002011 = ПоляАдреса.Улица;   
		СтраницаРаздел2.П000020002012 = ПоляАдреса.НомерЗемельногоУчастка;
		СтраницаРаздел2.П000020002015 = ПоляАдреса.ПомещениеВЗданииТип;
		СтраницаРаздел2.П000020002016 = ПоляАдреса.ПомещениеВЗданииНомер;
					
		ПервоеЗдание = Истина;
		Для Каждого Здание Из ПоляАдреса.Здания Цикл
			
			Если Не ПервоеЗдание Тогда
				СтруктураДанныхМногострочныхЧастей = Новый Структура();
				Для Каждого Показатель Из СтраницаРаздел2Здание Цикл
					СтруктураДанныхМногострочныхЧастей.Вставить(
						Показатель.Ключ, ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение)));
				КонецЦикла;
				
				НоваяСтрокаРаздел2Здание = 
					СтрокиРаздел2.ДанныеМногострочныхЧастей.П00002М1.Строки.Добавить();
				НоваяСтрокаРаздел2Здание.Данные = СтруктураДанныхМногострочныхЧастей;
				НоваяСтрокаРаздел2Здание.ДанныеМногострочныхЧастей = Новый Структура;
				
				СтраницаРаздел2Здание = НоваяСтрокаРаздел2Здание.Данные;
			Иначе
				ПервоеЗдание = Ложь;
			КонецЕсли;
			
			СтраницаРаздел2Здание.П00002М102013 = Здание.ЗданиеТип;
			СтраницаРаздел2Здание.П00002М102014 = Здание.ЗданиеНомер;
			
		КонецЦикла;
					
	КонецЕсли;

	СтраницаРаздел2.П000020000101 = ДанныеСтраницаРаздел2.НомерВРеестре;
	СтраницаРаздел2.П000020001001 = ДанныеСтраницаРаздел2.Наименование;
    СтраницаРаздел2.П000020004001 = КодыКатегорий[ДанныеСтраницаРаздел2.КатегорияСредстваРазмещения];
    СтраницаРаздел2.П000020005001 = "18210303000011000110";
	СтраницаРаздел2.П000020006001 = ДанныеСтраницаРаздел2.КодПоОКТМО;
    СтраницаРаздел2.П000020011003 = ДанныеСтраницаРаздел2.Ставка;
	
	КоординатыСтраницы = "Раздел2_" + Раздел2.Строки.Количество();
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Действие", "Гостиница");
	ДополнительныеПараметры.Вставить("Гостиница", ДанныеСтраницаРаздел2.Гостиница); 
    ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, КоординатыСтраницы,
		"", "", "", "", "", ДополнительныеПараметры);

КонецПроцедуры

Функция ДеревоЗначенийРегОтчет()
	
	НовоеДеревоЗначений = Новый ДеревоЗначений();
	НовоеДеревоЗначений.Колонки.Добавить("Данные");
	НовоеДеревоЗначений.Колонки.Добавить("ДанныеМногострочныхЧастей");
	
	Возврат НовоеДеревоЗначений;
	
КонецФункции

Процедура ДобавитьСтрокуРасшифровки(ТаблицаРасшифровок, ИмяПоказателя, 
	НаименованиеПоказателя, ЗнакОперации, НаименованиеСлагаемого, Сумма, ИмяРаздела, ДополнительныеПараметры = Неопределено)
	
	ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(ТаблицаРасшифровок, ИмяПоказателя,
		НаименованиеПоказателя, ЗнакОперации, НаименованиеСлагаемого, Сумма, ИмяРаздела, ДополнительныеПараметры);

КонецПроцедуры

Функция СоответствиеКодыКатегорийСредствРазмещения() 
	
	Результат = Новый Соответствие;
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.БезЗвезд, "0");
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.ОднаЗвезда, "1");
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.ДвеЗвезды, "2");
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.ТриЗвезды, "3");
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.ЧетыреЗвезды, "4");
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.ПятьЗвезд, "5");
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.Иное, "6");
	Результат.Вставить(Перечисления.КодыКатегорийСредствРазмещения.ПустаяСсылка(), "");

	Возврат Результат;
	
КонецФункции

Функция ДанныеРегОтчетаТуристическийНалог(ПараметрыОтчета)
	
	ДанныеТуристическийНалог = Новый Структура();
	ДанныеТуристическийНалог.Вставить("ИтогиПоГостиницамСтавкам");
    ДанныеТуристическийНалог.Вставить("ДанныеПоказателей");
   
	Организация              = ПараметрыОтчета.Организация;
	КодНалоговогоОргана      = ПараметрыОтчета.КодНалоговогоОргана;
	КПП                      = ПараметрыОтчета.КПП;
	ДатаНачала = ПараметрыОтчета.мДатаНачалаПериодаОтчета;
	ДатаКонца  = ПараметрыОтчета.мДатаКонцаПериодаОтчета;
	
	НалоговыйОрган = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(Организация, КПП, КодНалоговогоОргана);	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ТуристическийНалог.КоличествоЛиц) КАК КоличествоЛиц,
		|	СУММА(ВЫБОР
		|			КОГДА ТуристическийНалог.СтоимостьУслуг > ТуристическийНалог.СуммаНалога
		|					ИЛИ ТуристическийНалог.СтоимостьУслуг < 0
		|				ТОГДА ТуристическийНалог.СтоимостьУслуг - ТуристическийНалог.СуммаНалога
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьУслуг,
		|	СУММА(ТуристическийНалог.СуммаНалога) КАК СуммаНалога,
		|	ТуристическийНалог.СпособРасчета КАК СпособРасчета,
		|	ТуристическийНалог.ВидЛьготы КАК ВидЛьготы,
		|	ТуристическийНалог.Гостиница КАК Гостиница,
		|	ВЫБОР
		|		КОГДА Гостиницы.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации)
		|			ТОГДА СОКРЛП(Организации.РегистрацияВНалоговомОргане.КодПоОКТМО)
		|		ИНАЧЕ СОКРЛП(Гостиницы.КодПоОКТМО)
		|	КОНЕЦ КАК КодПоОКТМО,
		|	ТуристическийНалог.Ставка КАК Ставка,
		|	МАКСИМУМ(Гостиницы.КатегорияСредстваРазмещения) КАК КатегорияСредстваРазмещения,
		|	МАКСИМУМ(Гостиницы.НомерВРеестре) КАК НомерВРеестре,
		|	МАКСИМУМ(Гостиницы.Наименование) КАК Наименование
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	РегистрНакопления.ТуристическийНалог КАК ТуристическийНалог
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Гостиницы КАК Гостиницы
		|		ПО ТуристическийНалог.Гостиница = Гостиницы.Ссылка
		|			И (ТуристическийНалог.Период МЕЖДУ &ДатаНачала И &ДатаКонца)
		|			И (Гостиницы.НалоговыйОрган = &НалоговыйОрган
		|				ИЛИ ТуристическийНалог.Организация.КрупнейшийНалогоплательщик
		|				ИЛИ Гостиницы.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации)
		|					И Гостиницы.Организация.РегистрацияВНалоговомОргане = &НалоговыйОрган)
		|			И (ТуристическийНалог.Организация = &Организация)
		|			И (ТуристическийНалог.КоличествоУслуг <> 0)
		|			И (ТуристическийНалог.Активность = ИСТИНА)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ТуристическийНалог.Организация = Организации.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ТуристическийНалог.СпособРасчета,
		|	ТуристическийНалог.ВидЛьготы,
		|	ТуристическийНалог.Гостиница,
		|	ТуристическийНалог.Ставка,
		|	ВЫБОР
		|		КОГДА Гостиницы.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации)
		|			ТОГДА СОКРЛП(Организации.РегистрацияВНалоговомОргане.КодПоОКТМО)
		|		ИНАЧЕ СОКРЛП(Гостиницы.КодПоОКТМО)
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Данные.Гостиница КАК Гостиница,
		|	ВТ_Данные.Ставка КАК Ставка,
		|	МАКСИМУМ(ВТ_Данные.НомерВРеестре) КАК НомерВРеестре,
		|	МАКСИМУМ(ВТ_Данные.КатегорияСредстваРазмещения) КАК КатегорияСредстваРазмещения,
		|	МАКСИМУМ(ВТ_Данные.Наименование) КАК Наименование,
		|	МАКСИМУМ(ВТ_Данные.КодПоОКТМО) КАК КодПоОКТМО
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Данные.Гостиница,
		|	ВТ_Данные.Ставка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Гостиница,
		|	Ставка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТ_Данные.СтоимостьУслуг) КАК СтоимостьУслуг,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Данные.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.Федеральная)
		|				ТОГДА ВТ_Данные.СтоимостьУслуг
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьУслугФедеральнаяЛьгота,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Данные.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.Местная)
		|				ТОГДА ВТ_Данные.СтоимостьУслуг
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьУслугМестнаяЛьгота,
		|	СУММА(ВТ_Данные.КоличествоЛиц) КАК КоличествоЛиц,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Данные.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.Федеральная)
		|				ТОГДА ВТ_Данные.КоличествоЛиц
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоЛицФедеральнаяЛьгота,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Данные.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.Местная)
		|				ТОГДА ВТ_Данные.КоличествоЛиц
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоЛицМестнаяЛьгота,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Данные.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаТуристическогоНалога.МинимальныйНалог)
		|					И ВТ_Данные.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.НеПрименяется)
		|				ТОГДА ВТ_Данные.СтоимостьУслуг
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьУслугМинимальныйНалог,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_Данные.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаТуристическогоНалога.МинимальныйНалог)
		|					И ВТ_Данные.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.НеПрименяется)
		|				ТОГДА ВТ_Данные.СуммаНалога
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНалогаМинимальныйНалог,
		|	ВТ_Данные.Ставка КАК Ставка,
		|	ВТ_Данные.Гостиница КАК Гостиница
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Данные.Ставка,
		|	ВТ_Данные.Гостиница
		|
		|УПОРЯДОЧИТЬ ПО
		|	Гостиница,
		|	Ставка";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца", КонецДня(ДатаКонца));
	Запрос.УстановитьПараметр("НалоговыйОрган", НалоговыйОрган);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.ВыполнитьПакет();
	НомерЗапросДанные = Результат.Количество() - 1; 
	НомерЗапросИтоги = Результат.Количество() - 2;

	ДанныеТуристическийНалог.ИтогиПоГостиницамСтавкам = Результат[НомерЗапросИтоги].Выгрузить();
    ДанныеТуристическийНалог.ДанныеПоказателей = Результат[НомерЗапросДанные].Выгрузить();
	
	Возврат ДанныеТуристическийНалог;

КонецФункции

Функция ПодготовитьПараметрыРасчета(ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Период,"      // <Дата>
		+ "НачДата,"     // <Дата>
		+ "КонДата,"     // <Дата>
		+ "Организация," // <СправочникСсылка.Организации>
		+ "Регистратор," // <ДокументСсылка.*>
		+ "Содержание"   // <Строка, 150>
		;
	
	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Процедура РассчитатьТуристическийНалог(Реквизиты, ТаблицаПроводок, ТаблицаПроводокИсключениеТурНалога,
	ТаблицаСправкиРасчета, Ошибки, Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОплатаИзБюджета = Перечисления.ВидыЛьготПоТуристическомуНалогу.ОплатаИзБюджета;
	РасчетТуристическийНалог = ТуристическийНалогИсчисленный(Реквизиты);
	ПроверитьПрименимостьЛьготы(Реквизиты.НачДата, Ошибки, РасчетТуристическийНалог, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если РасчетТуристическийНалог.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкорректироватьПогрешностьРасчетаНалога(Реквизиты, РасчетТуристическийНалог);
	
	Счет44_02 = ПланыСчетов.Хозрасчетный.КоммерческиеРасходы;
	СвойстваСчета44_02 = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет44_02);
	
	Счет62_01 = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
	СвойстваСчета62_01 = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет62_01);

	Счет62_Р = ПланыСчетов.Хозрасчетный.РасчетыСРозничнымиПокупателями;
	СвойстваСчета62_Р = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет62_Р);

	Счет90_01_1 = ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД;
	СвойстваСчета90_01_1 = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет90_01_1);
	
	СтатьяЗатратПоТуристическомуНалогу = Справочники.СтатьиЗатрат.ПредопределенныйЭлемент("ТуристическийНалог");

	ПредставлениеПериода = ПредставлениеПериода(Реквизиты.НачДата, Реквизиты.КонДата, "ФП=Истина");

	СодержаниеПроводки = СтрШаблон(
		НСтр("ru = 'Исключение туристического налога из доходов по налогу на прибыль за %1'"),
		ПредставлениеПериода);
	
	Для Каждого СтрокаТаблицы Из РасчетТуристическийНалог Цикл
		
		Если Не СтрокаТаблицы.НалогРассчитанРанее Тогда
			НоваяСтрокаСправкаРасчет = ТаблицаСправкиРасчета.Добавить();
			НоваяСтрокаСправкаРасчет.Организация               = Реквизиты.Организация;		
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСправкаРасчет, СтрокаТаблицы);
		КонецЕсли;

		Если СтрокаТаблицы.СуммаНалога = 0 Или СтрокаТаблицы.ВидЛьготы = ОплатаИзБюджета Тогда
			Продолжить;
		КонецЕсли;
		
		// Дт 44.02 - Кт 68.17
		НоваяСтрока = ТаблицаПроводок.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизиты);
		
		НоваяСтрока.СчетКт               = ПланыСчетов.Хозрасчетный.ТуристическийНалог; // 68.17
		НоваяСтрока.ВидПлатежаВГосБюджет = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		
		НоваяСтрока.СчетДт = Счет44_02;
		
		Если СвойстваСчета44_02.УчетПоПодразделениям Тогда
			НоваяСтрока.ПодразделениеДт = СтрокаТаблицы.ПодразделениеОрганизации;
		КонецЕсли;
		НоваяСтрока.СубконтоДт1 = СтатьяЗатратПоТуристическомуНалогу;
		НоваяСтрока.Сумма       = СтрокаТаблицы.СуммаНалога;      
		
		
		// Согласно п.1 ст.248 НК из суммы доходов по налогу на прибыль и УСН необходимо исключить суммы туристического налога.
		//
		// Для налога на прибыль делаем сторирование суммы туристического налога проводкой
		// Дт 62.01, 62.Р - Кт 90.01.1 только по НУ и ПР по Кт.
		// Счет и аналитика взаиморасчетов в данном случае неважна, т.к. нулевые суммы и БУ и НУ Дт,
		// фактически счет Дт нужен только для корреспонденции с балансовым счетом 90.01.1.
		// Но для более наглядного отражения в карточке счета заполняем контрагента и договор для 62.01, 
		// документ расчетов не заполняем (он может еще отсутствовать, если была 100% предоплата, а проживание будет в следующих периодах),
		// для 62.Р заполняем склад.
		//
		// Для УСН вместо этих проводок сторнируем сумму из КУДиР.
		
		НоваяСтрока = ТаблицаПроводокИсключениеТурНалога.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизиты);
		
		НоваяСтрока.СчетКт               = Счет90_01_1;
		НоваяСтрока.НоменклатурнаяГруппа = СтрокаТаблицы.НоменклатурнаяГруппа;
		НоваяСтрока.Номенклатура         = СтрокаТаблицы.Номенклатура;
		Если ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
			НоваяСтрока.СтавкаНДС = СтрокаТаблицы.СтавкаНДС;
		Иначе
			НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(СтрокаТаблицы.ВидСтавкиНДС, Реквизиты.КонДата);
		КонецЕсли;
	
		Если СвойстваСчета90_01_1.УчетПоПодразделениям Тогда
			НоваяСтрока.ПодразделениеКт = СтрокаТаблицы.ПодразделениеОрганизации;
		КонецЕсли;
		НоваяСтрока.СуммаНУКт       = - СтрокаТаблицы.СуммаНалога;
		НоваяСтрока.СуммаПРКт       = СтрокаТаблицы.СуммаНалога;

		Если ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
			НоваяСтрока.СчетДт             = Счет62_01;
			НоваяСтрока.Контрагент         = СтрокаТаблицы.Контрагент;
			НоваяСтрока.ДоговорКонтрагента = СтрокаТаблицы.ДоговорКонтрагента;	
			
			Если СвойстваСчета62_01.УчетПоПодразделениям Тогда
				НоваяСтрока.ПодразделениеДт = СтрокаТаблицы.ПодразделениеОрганизации;
			КонецЕсли;

		Иначе
			НоваяСтрока.СчетДт = Счет62_Р;
			НоваяСтрока.Склад  = СтрокаТаблицы.Склад;
			
			Если СвойстваСчета62_Р.УчетПоПодразделениям Тогда
				НоваяСтрока.ПодразделениеДт = СтрокаТаблицы.ПодразделениеОрганизации;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Содержание = СодержаниеПроводки;
		
	КонецЦикла;
	
	ТаблицаПроводок.Свернуть("Период,Организация,Содержание,
		|СчетДт,ПодразделениеДт, СубконтоДт1,СубконтоДт2,СубконтоДт3,
		|СчетКт,ВидПлатежаВГосБюджет", "Сумма");
	ТаблицаПроводокИсключениеТурНалога.Свернуть("Период,Организация,Содержание,
		|СчетДт, ПодразделениеДт, Контрагент, ДоговорКонтрагента, Склад,
		|СчетКт, ПодразделениеКт, НоменклатурнаяГруппа, Номенклатура, СтавкаНДС", 
		"СуммаНУКт, СуммаПРКт");
	
КонецПроцедуры

Процедура СкорректироватьПогрешностьРасчетаНалога(Реквизиты, РасчетТуристическийНалог)
	
	Если Реквизиты.НачДата < ДатаИзмененияПериодичностиРегОперации() Тогда
		Возврат;
	КонецЕсли;
	РасчетТуристическийНалог.Индексы.Добавить("НалогИсчисляется, СпособРасчета");
	Отбор = Новый Структура("НалогИсчисляется, СпособРасчета", Истина, Перечисления.СпособыРасчетаТуристическогоНалога.ПоСтавке);     

	Если НачалоКвартала(Реквизиты.НачДата) <> НачалоМесяца(Реквизиты.НачДата) Тогда 
		// В последующих месяцах может возникнуть погрешность
		// по причине ежемесячного формирования рег.операции 
		СуммыНалогаПредыдущиеМесяцы = СуммыНалогаПредыдущиеМесяцы(Реквизиты, Отбор);
		ИзмененияНалоговойСтавки = ИзмененияНалоговойСтавкиГостиниц(Реквизиты);
	КонецЕсли;
	
   	ТаблицаПоГостиницамИСтавкам = РасчетТуристическийНалог.Скопировать(Отбор);
	ТаблицаПоГостиницамИСтавкам.Свернуть("Гостиница, Ставка", "СтоимостьУслуг");
	РасчетТуристическийНалог.Индексы.Добавить("НалогИсчисляется, СпособРасчета, Гостиница, Ставка");
	
	Для Каждого СтрокаТаблицы Из ТаблицаПоГостиницамИСтавкам Цикл
		
		Отбор.Вставить("Гостиница", СтрокаТаблицы.Гостиница);
		Отбор.Вставить("Ставка", СтрокаТаблицы.Ставка);
		ОтборПоЛьготе = Новый Структура("ВидЛьготы", Перечисления.ВидыЛьготПоТуристическомуНалогу.ОплатаИзБюджета); 
		ОтборБезЛьгот = Новый Структура("ВидЛьготы", Перечисления.ВидыЛьготПоТуристическомуНалогу.НеПрименяется); 

		СтавкаНеИзменяласьЗаПериод = ИзмененияНалоговойСтавки <> Неопределено
			И ИзмененияНалоговойСтавки.Найти(СтрокаТаблицы.Гостиница) = Неопределено;
		Если СуммыНалогаПредыдущиеМесяцы <> Неопределено  
			И СуммыНалогаПредыдущиеМесяцы.Количество() > 0 
			И СтавкаНеИзменяласьЗаПериод Тогда 
			СуммаНалога = СуммаНалогаСУчетомПогрешностиРасчета(СуммыНалогаПредыдущиеМесяцы, Отбор, СтрокаТаблицы.СтоимостьУслуг);
		Иначе
			СуммаНалога = Окр(СтрокаТаблицы.СтоимостьУслуг * СтрокаТаблицы.Ставка / (100 + СтрокаТаблицы.Ставка)); 
		КонецЕсли; 
		
		ТаблицаСтрок = РасчетТуристическийНалог.Скопировать(Отбор);
		ТаблицаСтрок.Индексы.Добавить("ВидЛьготы");
		
		СуммаНалогаПострочно = ТаблицаСтрок.Итог("СуммаНалога");
		
		ТаблицаСтрокПоЛьготе = ТаблицаСтрок.Скопировать(ОтборПоЛьготе); 
		ТаблицаСтрокБезЛьгот = ТаблицаСтрок.Скопировать(ОтборБезЛьгот);

        СуммаНалогаПоЛьготе = ТаблицаСтрокПоЛьготе.Итог("СуммаНалога"); 
        ПогрешностьПоЛьготе = Окр(СуммаНалогаПоЛьготе) - СуммаНалогаПоЛьготе;
		
		Если ПогрешностьПоЛьготе <> 0 Тогда
			РаспределитьСуммыНалога(ТаблицаСтрокПоЛьготе, РасчетТуристическийНалог, ПогрешностьПоЛьготе);	
		КонецЕсли;
		
		СуммаНалогаПострочно = ТаблицаСтрок.Итог("СуммаНалога");
		Погрешность = СуммаНалога - СуммаНалогаПострочно - ПогрешностьПоЛьготе;
		РаспределитьСуммыНалога(ТаблицаСтрокБезЛьгот, РасчетТуристическийНалог, Погрешность);

	КонецЦикла;
	
КонецПроцедуры

Процедура РаспределитьСуммыНалога(ТаблицаСтрок, РасчетТуристическийНалог, Погрешность)
	
	ТаблицаСтрок.Сортировать("СтоимостьУслуг Убыв");
	Распределение = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(Погрешность, ТаблицаСтрок.ВыгрузитьКолонку("СуммаНалога")); 
	Если Распределение <> Неопределено Тогда
		
		ТаблицаСтрок.ЗагрузитьКолонку(Распределение, "СуммаНалога");
		ОбработатьСтрокиПоРасчетуПогрешности(ТаблицаСтрок); 
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаСтрок, РасчетТуристическийНалог);
		
	КонецЕсли; 

КонецПроцедуры

Функция ИзмененияНалоговойСтавкиГостиниц(Реквизиты) 
	
	Запрос = Новый Запрос;
	
	НачалоПериода = КонецМесяца(НачалоКвартала(Реквизиты.КонДата)) + 1;
	Запрос.УстановитьПараметр("НачалоПериода",    НачалоПериода); 
	Запрос.УстановитьПараметр("КонецПериода",     КонецКвартала(Реквизиты.КонДата));
	Запрос.УстановитьПараметр("Организация",      Реквизиты.Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГостиницыИсторияСтавок.Ссылка КАК Гостиница,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ГостиницыИсторияСтавок.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СтавкаИзмененаЗаПериод,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ГостиницыИсторияСтавок.Ставка) КАК КоличествоСтавок
	|ПОМЕСТИТЬ ВТ_ИзмененныеСтавки
	|ИЗ
	|	Справочник.Гостиницы.ИсторияСтавок КАК ГостиницыИсторияСтавок
	|ГДЕ
	|	ГостиницыИсторияСтавок.Ссылка.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ГостиницыИсторияСтавок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИзмененныеСтавки.Гостиница КАК Гостиница,
	|	ВТ_ИзмененныеСтавки.СтавкаИзмененаЗаПериод КАК СтавкаИзмененаЗаПериод
	|ИЗ
	|	ВТ_ИзмененныеСтавки КАК ВТ_ИзмененныеСтавки
	|ГДЕ
	|	ВТ_ИзмененныеСтавки.КоличествоСтавок > 1 И ВТ_ИзмененныеСтавки.СтавкаИзмененаЗаПериод"; 
	
	ИзмененияСтавки = Запрос.Выполнить().Выгрузить();
	
	Возврат ИзмененияСтавки;

	
КонецФункции

Функция СуммыНалогаПредыдущиеМесяцы(Реквизиты, Отбор) 
	
	Запрос = Новый Запрос;
	
	ВидыЛьгот = Новый Массив;
	ВидыЛьгот.Добавить(Перечисления.ВидыЛьготПоТуристическомуНалогу.НеПрименяется);
	ВидыЛьгот.Добавить(Перечисления.ВидыЛьготПоТуристическомуНалогу.ОплатаИзБюджета);

	КонДата = Реквизиты.НачДата - 1;
	Запрос.УстановитьПараметр("ТекущийДокумент",  Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("НачалоПериода",    НачалоКвартала(КонДата)); 
	Запрос.УстановитьПараметр("КонецПериода",     КонДата);
	Запрос.УстановитьПараметр("Организация",      Реквизиты.Организация);
	Запрос.УстановитьПараметр("ВидыЛьгот",        ВидыЛьгот);
	Запрос.УстановитьПараметр("СпособРасчета",    Отбор.СпособРасчета);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ТуристическийНалог.СтоимостьУслуг) КАК СтоимостьУслуг,
	|	ТуристическийНалог.Гостиница КАК Гостиница,
	|	СУММА(ТуристическийНалог.СуммаНалога) КАК СуммаНалога,
	|	ТуристическийНалог.Ставка КАК Ставка,
	|	ИСТИНА КАК НалогИсчисляется,
	|	ТуристическийНалог.СпособРасчета КАК СпособРасчета
	|ИЗ
	|	РегистрНакопления.ТуристическийНалог КАК ТуристическийНалог
	|ГДЕ
	|	ТуристическийНалог.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТуристическийНалог.Организация = &Организация
	|	И ТуристическийНалог.ВидЛьготы В(&ВидыЛьгот)
	|	И ТуристическийНалог.СпособРасчета = &СпособРасчета
	|	И ТуристическийНалог.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	ТуристическийНалог.Гостиница,
	|	ТуристическийНалог.Ставка,
	|	ТуристическийНалог.СпособРасчета"; 
	
	ТаблицаСуммНалога = Запрос.Выполнить().Выгрузить();
	ТаблицаСуммНалога.Индексы.Добавить("НалогИсчисляется, СпособРасчета, Гостиница, Ставка");

	Возврат ТаблицаСуммНалога;

	
КонецФункции

Функция СуммаНалогаСУчетомПогрешностиРасчета(СуммыНалогаПредыдущеМесяцы, Отбор, СтоимостьУслуг) 
	
	СуммыНалогаПоОтбору = СуммыНалогаПредыдущеМесяцы.Скопировать(Отбор);
	
	Если СуммыНалогаПоОтбору.Количество() = 0 Тогда
		Возврат Окр(СтоимостьУслуг * Отбор.Ставка / (100 + Отбор.Ставка)); 
	КонецЕсли;
	
    СтоимостьУслугПредыдущиеМесяцы = СуммыНалогаПоОтбору.Итог("СтоимостьУслуг");
	СуммаНалогаПредыдущиеМесяцы = СуммыНалогаПоОтбору.Итог("СуммаНалога"); 
	СтоимостьУслугКвартал = СтоимостьУслугПредыдущиеМесяцы + СтоимостьУслуг;
    СуммаНалогаКвартал  = Окр(СтоимостьУслугКвартал * Отбор.Ставка / (100 + Отбор.Ставка)); 
	
	Результат = СуммаНалогаКвартал - СуммаНалогаПредыдущиеМесяцы;
	
	Возврат Результат;
	
КонецФункции

Процедура ОбработатьСтрокиПоРасчетуПогрешности(ТаблицаСтрок) 
	
	Для Каждого СтрокаТаблицы Из ТаблицаСтрок Цикл
		
		СтрокаТаблицы.РасчетПогрешностиОкругления = Истина;
		СтрокаТаблицы.КоличествоЛиц = 0;
		СтрокаТаблицы.КоличествоУслуг = 0;
		СтрокаТаблицы.СтоимостьУслуг = 0;
		СтрокаТаблицы.СтоимостьДопУслуг = 0;
		Если СтрокаТаблицы.СуммаНалога <> 0 Тогда
			СтрокаТаблицы.НалогРассчитанРанее = Ложь;
		Иначе
			СтрокаТаблицы.НалогРассчитанРанее = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйТаблицаПроводок()

	ТипСумма = БухгалтерскийУчетКлиентСервер.ТипСумма();
	
	ТаблицаПроводок = Новый ТаблицаЗначений;
	
	ТаблицаПроводок.Колонки.Добавить("Период",
		ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ТаблицаПроводок.Колонки.Добавить("Организация",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаПроводок.Колонки.Добавить("СчетДт",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаПроводок.Колонки.Добавить("ПодразделениеДт",
		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаПроводок.Колонки.Добавить("СубконтоДт1");
	ТаблицаПроводок.Колонки.Добавить("СубконтоДт2");
	ТаблицаПроводок.Колонки.Добавить("СубконтоДт3");
	ТаблицаПроводок.Колонки.Добавить("СчетКт",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаПроводок.Колонки.Добавить("ВидПлатежаВГосБюджет",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПлатежейВГосБюджет"));
	ТаблицаПроводок.Колонки.Добавить("Сумма", ТипСумма);
	ТаблицаПроводок.Колонки.Добавить("Содержание",
		ОбщегоНазначения.ОписаниеТипаСтрока(150));
	
	Возврат ТаблицаПроводок;
	
КонецФункции

Функция НовыйТаблицаПроводокИсключениеТурНалога()
	
	ТипСумма = БухгалтерскийУчетКлиентСервер.ТипСумма();
	
	ТаблицаПроводок = Новый ТаблицаЗначений;
	
	ТаблицаПроводок.Колонки.Добавить("Период",
		ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ТаблицаПроводок.Колонки.Добавить("Организация",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаПроводок.Колонки.Добавить("СчетДт",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаПроводок.Колонки.Добавить("ПодразделениеДт",
		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаПроводок.Колонки.Добавить("ПодразделениеКт",
		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаПроводок.Колонки.Добавить("Склад",
		Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаПроводок.Колонки.Добавить("Контрагент",
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаПроводок.Колонки.Добавить("ДоговорКонтрагента",
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ТаблицаПроводок.Колонки.Добавить("СчетКт",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаПроводок.Колонки.Добавить("НоменклатурнаяГруппа",
		Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ТаблицаПроводок.Колонки.Добавить("СтавкаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
    ТаблицаПроводок.Колонки.Добавить("Номенклатура",
		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаПроводок.Колонки.Добавить("СуммаНУКт", ТипСумма);
	ТаблицаПроводок.Колонки.Добавить("СуммаПРКт", ТипСумма);
	ТаблицаПроводок.Колонки.Добавить("Содержание",
		ОбщегоНазначения.ОписаниеТипаСтрока(150));
	
	Возврат ТаблицаПроводок;
	
КонецФункции

Функция ПодготовитьПараметрыДвиженийРасчета(ТаблицаПроводок, ТаблицаПроводокИсключениеТурНалога, ТаблицаСправкиРасчета, ТаблицаРеквизитов)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Период,"      // <Дата>
		+ "Организация," // <СправочникСсылка.Организации>
		+ "Регистратор," // <ДокументСсылка.*>
		;
	
	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизитов, СписокОбязательныхКолонок));
	
	СписокОбязательныхКолонок = ""
		+ "Период,"                 // <Дата> - период проводок
		+ "Организация,"            // <СправочникСсылка.Организации>
		+ "СчетДт,"                 // <ПланСчетовСсылка.Хозрасчетный>
		+ "ПодразделениеДт,"        // <СправочникСсылка.ПодразделенияОрганизаций>
		+ "СубконтоДт1,"            // <Характеристика.ВидыСубконтоХозрасчетные>
		+ "СубконтоДт2,"            // <Характеристика.ВидыСубконтоХозрасчетные>
		+ "СубконтоДт3,"            // <Характеристика.ВидыСубконтоХозрасчетные>
		+ "СчетКт,"                 // <ПланСчетовСсылка.Хозрасчетный>
		+ "ВидПлатежаВГосБюджет,"   // <ПеречислениеСсылка.ВидыПлатежейВГосБюджет>
		+ "Сумма,"                  // <Число, 15, 0> - сумма проводки
		+ "Содержание";             // <Строка, 150> - содержание проводки
	
	Параметры.Вставить("Проводки", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПроводок, СписокОбязательныхКолонок));
		
	СписокОбязательныхКолонок = ""
		+ "Период,"                 // <Дата> - период проводок
		+ "Организация,"            // <СправочникСсылка.Организации>
		+ "СчетДт,"                 // <ПланСчетовСсылка.Хозрасчетный>
		+ "ПодразделениеДт,"        // <СправочникСсылка.ПодразделенияОрганизаций>
		+ "Склад,"                  // <Характеристика.ВидыСубконтоХозрасчетные>
		+ "Контрагент,"             // <Характеристика.ВидыСубконтоХозрасчетные>
		+ "ДоговорКонтрагента,"     // <Характеристика.ВидыСубконтоХозрасчетные>
		+ "СчетКт,"                 // <ПланСчетовСсылка.Хозрасчетный>
		+ "ПодразделениеКт,"        // <СправочникСсылка.ПодразделенияОрганизаций>
		+ "НоменклатурнаяГруппа,"   // <Характеристика.ВидыСубконтоХозрасчетные>      
		+ "СтавкаНДС,"              // <Характеристика.ВидыСубконтоХозрасчетные>          
		+ "Номенклатура,"           // <Характеристика.ВидыСубконтоХозрасчетные>
		+ "СуммаНУКт,"              // <Число, 15, 0> - сумма проводки
		+ "СуммаПРКт,"              // <Число, 15, 0> - сумма проводки
		+ "Содержание";             // <Строка, 150> - содержание проводки
	
	Параметры.Вставить("ПроводкиИсключениеТурНалога", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПроводокИсключениеТурНалога, СписокОбязательныхКолонок));

		
	СписокОбязательныхКолонок = ""
		+ "Организация,"          // <СправочникСсылка.Организации>
		+ "Гостиница,"            // <СправочникСсылка.Гостиницы> 
		+ "Номенклатура,"         // <СправочникСсылка.Номенклатура> 
		+ "ИдентификаторСтроки,"  //  <Строка>
		+ "СпособРасчета,"        // <ПеречислениеСсылка.СпособыРасчетаТуристическогоНалога>  
		+ "ВидЛьготы,"            //  <ПеречислениеСсылка.ВидыЛьготПоТуристическомуНалогу>  
		+ "ДокументОснование,"    //  <ДокументСсылка.СчетНаОплатуПокупателю, ДокументСсылка.ОтчетОРозничныхПродажах>
		+ "РасчетныйДокумент,"    //  <ДокументСсылка.ПоступлениеНаРасчетныйСчет, ДокументСсылка.ОплатаПлатежнойКартой,
							      // ДокументСсылка.ОтчетОРозничныхПродажах, ДокументСсылка.ПриходныйКассовыйОрдер>
		+ "РасчетПогрешностиОкругления,"  //  <Булево>
		+ "СуммаНалога," // <Число>
		+ "КоличествоЛиц,"        // <Число>
		+ "КоличествоУслуг,"      // <Число>
		+ "Ставка,"               // <Число> 
		+ "СтоимостьДопУслуг,"    // <Число>
		+ "СтоимостьУслуг";       // <Число>
	
	Параметры.Вставить("СправкаРасчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСправкиРасчета, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Процедура СформироватьДвиженияРасчета(ТаблицаПроводок, ТаблицаПроводокИсключениеТурНалога,  ТаблицаСправкиРасчета, ТаблицаРеквизитов, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыДвиженийРасчета(ТаблицаПроводок, ТаблицаПроводокИсключениеТурНалога, ТаблицаСправкиРасчета, ТаблицаРеквизитов);
	Реквизиты = Параметры.Реквизиты[0];
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаПБУ18 = УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период);
	
	// Проводка по начислению налога
	Для Каждого СтрокаТаблицы Из Параметры.Проводки Цикл
	
		Если СтрокаТаблицы.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		ЗаполнитьЗначенияСвойств(Проводка, СтрокаТаблицы);
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Для Ном = 1 По СвойстваСчетаДт.КоличествоСубконто Цикл
			
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
				СвойстваСчетаДт["ВидСубконто" + Ном], СтрокаТаблицы["СубконтоДт" + Ном]);
			
		КонецЦикла;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"ВидыПлатежейВГосБюджет", СтрокаТаблицы.ВидПлатежаВГосБюджет);
			
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
	
		Если ОтражатьВНалоговомУчете Тогда
			
			ЭтоНеПринимаемыйРасходНУ = НалоговыйУчет.ЭтоНепринимаемыйРасходНУ(СтрокаТаблицы.СубконтоДт1,
				СтрокаТаблицы.СубконтоДт2, СтрокаТаблицы.СубконтоДт3);
				
			Если СвойстваСчетаДт.НалоговыйУчет Тогда
								
				Если ПоддержкаПБУ18 Тогда
					Проводка.СуммаПРДт = ?(ЭтоНеПринимаемыйРасходНУ, СтрокаТаблицы.Сумма, 0);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Запись Из Параметры.СправкаРасчет Цикл
		Движение = Движения.ТуристическийНалог.Добавить();
		Движение.Период = Реквизиты.Период;
		ЗаполнитьЗначенияСвойств(Движение, Запись);
	КонецЦикла; 
	
	Если Параметры.Проводки.Количество() > 0 Тогда
		Движения.Хозрасчетный.Записывать = Истина;
	КонецЕсли; 
	
	Движения.ТуристическийНалог.Записывать = Параметры.СправкаРасчет.Количество() > 0;
	
	Если Не ОтражатьВНалоговомУчете Тогда
		Возврат;
	КонецЕсли;
	
	// Проводка по исключению налога из доходов
	Для Каждого СтрокаТаблицы Из Параметры.ПроводкиИсключениеТурНалога Цикл
	
		Если СтрокаТаблицы.СуммаНУКт = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		ЗаполнитьЗначенияСвойств(Проводка, СтрокаТаблицы);
		
		Если Проводка.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСРозничнымиПокупателями Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
				"Склады", СтрокаТаблицы.Склад);
		Иначе
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
				"Контрагенты", СтрокаТаблицы.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
				"Договоры", СтрокаТаблицы.ДоговорКонтрагента);
		КонецЕсли;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"НоменклатурныеГруппы", СтрокаТаблицы.НоменклатурнаяГруппа);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СтавкиНДС", СтрокаТаблицы.СтавкаНДС);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"Номенклатура", СтрокаТаблицы.Номенклатура);
		
	КонецЦикла;
	
	Если Параметры.ПроводкиИсключениеТурНалога.Количество() > 0 Тогда
		Движения.Хозрасчетный.Записывать = Истина;
	КонецЕсли; 

КонецПроцедуры

Функция НовыйТаблицаСправкиРасчета()

	Возврат ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("ТуристическийНалог");

КонецФункции
	
Функция ДанныеИсчисленногоТуристическогоНалога(Реквизиты)

	// Выбираем из регистра ОплатаСчетов информацию о счетах, которые были оплачены в течение квартала,
	// и по ним проверяем, поступила ли полная оплата (всего счета целиком без деления по услугам размещениям и прочим).
	// Если счет оплачен частично, то считаем, что каждая строка счета оплачена частично в соответствующей пропорции,
	// поэтому момент полного расчета еще не наступил и начислять туристический налог еще рано.
	// Если по счету поступила полная оплата, то включаем услуги размещения из него в таблицу для начисления налога.
	//
	// Для формирования всех проводок по начислению налога Дт 44.02 - Кт 68.17 также добавляем данные из регистра ТуристическийНалог,
	// сформированные за квартал отчетами о розничных продажах и ручными операциями по регистру.
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("ТекущийДокумент",  Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("НачалоПериода",    Реквизиты.НачДата); 
	Запрос.УстановитьПараметр("КонецПериода",     Реквизиты.КонДата);
	Запрос.УстановитьПараметр("Организация",      Реквизиты.Организация);
	Запрос.УстановитьПараметр("ВалютаРеглУчета",  ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	Запрос.УстановитьПараметр("ТаблицаМесяцы",    ТаблицаМесяцы(Реквизиты.НачДата, Реквизиты.КонДата));
	Запрос.УстановитьПараметр("УслугиРазмещения", МассивУслугРазмещения());

	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ОплатаСчетовОбороты.СчетНаОплату КАК Документ.СчетНаОплатуПокупателю) КАК СчетНаОплату,
	|	ОплатаСчетовОбороты.Период КАК Период,
	|	ОплатаСчетовОбороты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ОплатыСчетовЗаПериод
	|ИЗ
	|	РегистрНакопления.ОплатаСчетов КАК ОплатаСчетовОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТуристическийНалог КАК РанееНачисленныйТуристическийНалог
	|		ПО ОплатаСчетовОбороты.СчетНаОплату = РанееНачисленныйТуристическийНалог.ДокументОснование
	|			И (РанееНачисленныйТуристическийНалог.Активность)
	|			И (РанееНачисленныйТуристическийНалог.Период <= &КонецПериода)
	|			И (РанееНачисленныйТуристическийНалог.Регистратор <> &ТекущийДокумент)
	|ГДЕ
	|	ОплатаСчетовОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ОплатаСчетовОбороты.Организация = &Организация
	|	И ОплатаСчетовОбороты.Активность
	|	И ОплатаСчетовОбороты.Валюта = &ВалютаРеглУчета
	|	И ТИПЗНАЧЕНИЯ(ОплатаСчетовОбороты.СчетНаОплату) = ТИП(Документ.СчетНаОплатуПокупателю)
	|	И РанееНачисленныйТуристическийНалог.ДокументОснование ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетНаОплату,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ОплатыСчетовЗаПериод.Период) КАК Период,
	|	ВТ_ОплатыСчетовЗаПериод.СчетНаОплату КАК СчетНаОплату
	|ПОМЕСТИТЬ ВТ_ПоследниеДатыОплаты
	|ИЗ
	|	ВТ_ОплатыСчетовЗаПериод КАК ВТ_ОплатыСчетовЗаПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОплатыСчетовЗаПериод.СчетНаОплату
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетНаОплату,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоследниеДатыОплаты.СчетНаОплату КАК СчетНаОплату,
	|	МАКСИМУМ(Гостиницы.Ссылка) КАК Гостиница,
	|	МАКСИМУМ(ВТ_ОплатыСчетовЗаПериод.Регистратор) КАК РасчетныйДокумент,
	|	МАКСИМУМ(НАЧАЛОПЕРИОДА(ВТ_ОплатыСчетовЗаПериод.Период, МЕСЯЦ)) КАК Период
	|ПОМЕСТИТЬ ВТ_ПоследниеДокументыОплаты
	|ИЗ
	|	ВТ_ПоследниеДатыОплаты КАК ВТ_ПоследниеДатыОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОплатыСчетовЗаПериод КАК ВТ_ОплатыСчетовЗаПериод
	|		ПО ВТ_ПоследниеДатыОплаты.СчетНаОплату = ВТ_ОплатыСчетовЗаПериод.СчетНаОплату
	|			И ВТ_ПоследниеДатыОплаты.Период = ВТ_ОплатыСчетовЗаПериод.Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Гостиницы КАК Гостиницы
	|		ПО ВТ_ПоследниеДатыОплаты.СчетНаОплату.Гостиница = Гостиницы.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПоследниеДатыОплаты.СчетНаОплату
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетНаОплату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаМесяцы.НачалоМесяца КАК НачалоМесяца,
	|	ТаблицаМесяцы.КонецМесяца КАК КонецМесяца
	|ПОМЕСТИТЬ ВТ_ТаблицаМесяцы
	|ИЗ
	|	&ТаблицаМесяцы КАК ТаблицаМесяцы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЧИстория.Ссылка КАК Гостиница,
	|	ВТ_ТаблицаМесяцы.НачалоМесяца КАК НачалоМесяца,
	|	МАКСИМУМ(ТЧИстория.Период) КАК МаксПериод
	|ПОМЕСТИТЬ ВТ_ПоследниеДатыИзмененияСтавки
	|ИЗ
	|	ВТ_ТаблицаМесяцы КАК ВТ_ТаблицаМесяцы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Гостиницы.ИсторияСтавок КАК ТЧИстория
	|		ПО (ТЧИстория.Период <= ВТ_ТаблицаМесяцы.КонецМесяца)
	|ГДЕ
	|	ТЧИстория.Ссылка.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧИстория.Ссылка,
	|	ВТ_ТаблицаМесяцы.НачалоМесяца
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТЧИстория.Ссылка,
	|	МаксПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоследниеДатыИзмененияСтавки.Гостиница КАК Гостиница,
	|	ВТ_ПоследниеДатыИзмененияСтавки.НачалоМесяца КАК Период,
	|	ТЧИстория.Ставка КАК Ставка
	|ПОМЕСТИТЬ СтавкиТуристическогоНалога
	|ИЗ
	|	ВТ_ПоследниеДатыИзмененияСтавки КАК ВТ_ПоследниеДатыИзмененияСтавки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Гостиницы.ИсторияСтавок КАК ТЧИстория
	|		ПО ВТ_ПоследниеДатыИзмененияСтавки.Гостиница = ТЧИстория.Ссылка
	|			И ВТ_ПоследниеДатыИзмененияСтавки.МаксПериод = ТЧИстория.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВТ_ПоследниеДатыИзмененияСтавки.Гостиница,
	|	ВТ_ПоследниеДатыИзмененияСтавки.НачалоМесяца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатаСчетовОбороты.СчетНаОплату КАК СчетНаОплату,
	|	ОплатаСчетовОбороты.СуммаОборот КАК ОплаченоПоСчету
	|ПОМЕСТИТЬ ВТ_ВсеОплатыСчетов
	|ИЗ
	|	РегистрНакопления.ОплатаСчетов.Обороты(
	|			,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И СчетНаОплату В
	|					(ВЫБРАТЬ
	|						ВТ_ПоследниеДокументыОплаты.СчетНаОплату КАК СчетНаОплату
	|					ИЗ
	|						ВТ_ПоследниеДокументыОплаты КАК ВТ_ПоследниеДокументыОплаты)) КАК ОплатаСчетовОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетНаОплату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК КоличествоЛиц,
	|	СчетНаОплатуПокупателюТовары.Количество КАК КоличествоУслуг,
	|	ВЫБОР
	|		КОГДА СчетНаОплатуПокупателюШапка.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателюТовары.Сумма - СчетНаОплатуПокупателюТовары.СуммаНДС - СчетНаОплатуПокупателюТовары.СуммаСкидки
	|		ИНАЧЕ СчетНаОплатуПокупателюТовары.Сумма - СчетНаОплатуПокупателюТовары.СуммаСкидки
	|	КОНЕЦ КАК СтоимостьУслуг,
	|	СчетНаОплатуПокупателюТовары.ВидЛьготыПоТуристическомуНалогу КАК ВидЛьготы,
	|	СчетНаОплатуПокупателюШапка.Гостиница КАК Гостиница,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаТуристическогоНалога.ПоСтавке) КАК СпособРасчета,
	|	СчетНаОплатуПокупателюШапка.Организация КАК Организация,
	|	СчетНаОплатуПокупателюТовары.Номенклатура КАК Номенклатура,
	|	СпрНоменклатура.УслугаРазмещения КАК УслугаРазмещения,
	|	ВТ_ПоследниеДокументыОплаты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВТ_ВсеОплатыСчетов.СчетНаОплату КАК ДокументОснование,
	|	ЕСТЬNULL(СтавкиТуристическогоНалога.Ставка, СчетНаОплатуПокупателюШапка.Гостиница.СтавкаТуристическогоНалога) КАК Ставка,
	|	СчетНаОплатуПокупателюШапка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	СчетНаОплатуПокупателюТовары.СтавкаНДС КАК СтавкаНДС,
	|	СпрНоменклатура.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	СпрНоменклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	СчетНаОплатуПокупателюШапка.Контрагент КАК Контрагент,
	|	СчетНаОплатуПокупателюШапка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СчетНаОплатуПокупателюТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СчетНаОплатуПокупателюШапка.Ссылка КАК Ссылка,
	|	СчетНаОплатуПокупателюШапка.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ПОМЕСТИТЬ ВТ_ТуристическийНалогОсновныеУслуги
	|ИЗ
	|	ВТ_ВсеОплатыСчетов КАК ВТ_ВсеОплатыСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПоследниеДокументыОплаты КАК ВТ_ПоследниеДокументыОплаты
	|		ПО ВТ_ВсеОплатыСчетов.СчетНаОплату = ВТ_ПоследниеДокументыОплаты.СчетНаОплату
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателюШапка
	|		ПО ВТ_ВсеОплатыСчетов.СчетНаОплату = СчетНаОплатуПокупателюШапка.Ссылка
	|			И ВТ_ВсеОплатыСчетов.ОплаченоПоСчету >= СчетНаОплатуПокупателюШапка.СуммаДокумента
	|			И (СчетНаОплатуПокупателюШапка.Гостиница <> ЗНАЧЕНИЕ(Справочник.Гостиницы.ПустаяСсылка))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателюТовары
	|		ПО (СчетНаОплатуПокупателюШапка.Ссылка = СчетНаОплатуПокупателюТовары.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (СчетНаОплатуПокупателюТовары.Номенклатура = СпрНоменклатура.Ссылка)
	|			И (СпрНоменклатура.УслугаРазмещения В (&УслугиРазмещения))
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиТуристическогоНалога КАК СтавкиТуристическогоНалога
	|		ПО (ВТ_ПоследниеДокументыОплаты.Гостиница = СтавкиТуристическогоНалога.Гостиница)
	|			И (ВТ_ПоследниеДокументыОплаты.Период = СтавкиТуристическогоНалога.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТуристическийНалогОсновныеУслуги.КоличествоЛиц КАК КоличествоЛиц,
	|	ВТ_ТуристическийНалогОсновныеУслуги.КоличествоУслуг КАК КоличествоУслуг,
	|	ВТ_ТуристическийНалогОсновныеУслуги.СтоимостьУслуг КАК СтоимостьУслуг,
	|	0 КАК СтоимостьДопУслуг,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ВидЛьготы КАК ВидЛьготы,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Гостиница КАК Гостиница,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СуммаНалога,
	|	ВТ_ТуристическийНалогОсновныеУслуги.СпособРасчета КАК СпособРасчета,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Организация КАК Организация,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Номенклатура КАК Номенклатура,
	|	ВТ_ТуристическийНалогОсновныеУслуги.УслугаРазмещения КАК УслугаРазмещения,
	|	ВТ_ТуристическийНалогОсновныеУслуги.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ДокументОснование КАК ДокументОснование,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Ставка КАК Ставка,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВТ_ТуристическийНалогОсновныеУслуги.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ВТ_ТуристическийНалогОсновныеУслуги.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Контрагент КАК Контрагент,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЛОЖЬ КАК НалогРассчитанРанее,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЛОЖЬ КАК РасчетПогрешностиОкругления
	|ПОМЕСТИТЬ ВТ_ТуристическийНалог
	|ИЗ
	|	ВТ_ТуристическийНалогОсновныеУслуги КАК ВТ_ТуристическийНалогОсновныеУслуги
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ВТ_ТуристическийНалогОсновныеУслуги.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателюДопУслуги.Сумма - СчетНаОплатуПокупателюДопУслуги.СуммаНДС - СчетНаОплатуПокупателюДопУслуги.СуммаСкидки
	|		ИНАЧЕ СчетНаОплатуПокупателюДопУслуги.Сумма - СчетНаОплатуПокупателюДопУслуги.СуммаСкидки
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_ТуристическийНалогОсновныеУслуги.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателюДопУслуги.Сумма - СчетНаОплатуПокупателюДопУслуги.СуммаНДС - СчетНаОплатуПокупателюДопУслуги.СуммаСкидки
	|		ИНАЧЕ СчетНаОплатуПокупателюДопУслуги.Сумма - СчетНаОплатуПокупателюДопУслуги.СуммаСкидки
	|	КОНЕЦ,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ВидЛьготы,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Гостиница,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)),
	|	ВТ_ТуристическийНалогОсновныеУслуги.СпособРасчета,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Организация,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Номенклатура,
	|	ВТ_ТуристическийНалогОсновныеУслуги.УслугаРазмещения,
	|	ВТ_ТуристическийНалогОсновныеУслуги.РасчетныйДокумент,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ДокументОснование,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Ставка,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ПодразделениеОрганизации,
	|	ВТ_ТуристическийНалогОсновныеУслуги.СтавкаНДС,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ВидСтавкиНДС,
	|	ВТ_ТуристическийНалогОсновныеУслуги.НоменклатурнаяГруппа,
	|	ВТ_ТуристическийНалогОсновныеУслуги.Контрагент,
	|	ВТ_ТуристическийНалогОсновныеУслуги.ДоговорКонтрагента,
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	ВТ_ТуристическийНалогОсновныеУслуги.ИдентификаторСтроки,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_ТуристическийНалогОсновныеУслуги КАК ВТ_ТуристическийНалогОсновныеУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателюДопУслуги
	|		ПО (СчетНаОплатуПокупателюДопУслуги.ИдентификаторРодительскойСтроки = ВТ_ТуристическийНалогОсновныеУслуги.ИдентификаторСтроки)
	|			И (СчетНаОплатуПокупателюДопУслуги.Ссылка = ВТ_ТуристическийНалогОсновныеУслуги.Ссылка)
	|			И (СчетНаОплатуПокупателюДопУслуги.ИдентификаторРодительскойСтроки <> """")
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТуристическийНалог.КоличествоЛиц,
	|	ТуристическийНалог.КоличествоУслуг,
	|	ТуристическийНалог.СтоимостьУслуг,
	|	ТуристическийНалог.СтоимостьДопУслуг,
	|	ТуристическийНалог.ВидЛьготы,
	|	ТуристическийНалог.Гостиница,
	|	ТуристическийНалог.СуммаНалога,
	|	ТуристическийНалог.СпособРасчета,
	|	ТуристическийНалог.Организация,
	|	ТуристическийНалог.Номенклатура,
	|	ТуристическийНалог.Номенклатура.УслугаРазмещения,
	|	ТуристическийНалог.РасчетныйДокумент,
	|	ТуристическийНалог.ДокументОснование,
	|	ТуристическийНалог.Ставка,
	|	ВЫБОР
	|		КОГДА НЕ ОтчетОРозничныхПродажахШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА ОтчетОРозничныхПродажахШапка.ПодразделениеОрганизации
	|		КОГДА НЕ СчетНаОплатуПокупателюШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА СчетНаОплатуПокупателюШапка.ПодразделениеОрганизации
	|		КОГДА НЕ НачислениеТуристическогоНалогаШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА НачислениеТуристическогоНалогаШапка.ПодразделениеОрганизации
	|		КОГДА НЕ РеализацияТоваровУслугШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА РеализацияТоваровУслугШапка.ПодразделениеОрганизации
	|		КОГДА НЕ ОтчетКомиссионераОПродажахШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА ОтчетКомиссионераОПродажахШапка.ПодразделениеОрганизации
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка),
	|	ТуристическийНалог.Номенклатура.ВидСтавкиНДС,
	|	ТуристическийНалог.Номенклатура.НоменклатурнаяГруппа,
	|	ВЫБОР
	|		КОГДА НЕ СчетНаОплатуПокупателюШапка.Контрагент ЕСТЬ NULL
	|			ТОГДА СчетНаОплатуПокупателюШапка.Контрагент
	|		КОГДА НЕ НачислениеТуристическогоНалогаШапка.Контрагент ЕСТЬ NULL
	|			ТОГДА НачислениеТуристическогоНалогаШапка.Контрагент
	|		КОГДА НЕ РеализацияТоваровУслугШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА РеализацияТоваровУслугШапка.Контрагент
	|		КОГДА НЕ ОтчетКомиссионераОПродажахШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА ОтчетКомиссионераОПродажахШапка.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ СчетНаОплатуПокупателюШапка.ДоговорКонтрагента ЕСТЬ NULL
	|			ТОГДА СчетНаОплатуПокупателюШапка.ДоговорКонтрагента
	|		КОГДА НЕ НачислениеТуристическогоНалогаШапка.ДоговорКонтрагента ЕСТЬ NULL
	|			ТОГДА НачислениеТуристическогоНалогаШапка.ДоговорКонтрагента
	|		КОГДА НЕ РеализацияТоваровУслугШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА РеализацияТоваровУслугШапка.ДоговорКонтрагента
	|		КОГДА НЕ ОтчетКомиссионераОПродажахШапка.Ссылка ЕСТЬ NULL
	|			ТОГДА ОтчетКомиссионераОПродажахШапка.ДоговорКонтрагента
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ,
	|	ИСТИНА,
	|	ЕСТЬNULL(ОтчетОРозничныхПродажахШапка.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)),
	|	ТуристическийНалог.ИдентификаторСтроки,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрНакопления.ТуристическийНалог КАК ТуристическийНалог
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажахШапка
	|		ПО ТуристическийНалог.ДокументОснование = ОтчетОРозничныхПродажахШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателюШапка
	|		ПО ТуристическийНалог.ДокументОснование = СчетНаОплатуПокупателюШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеТуристическогоНалога КАК НачислениеТуристическогоНалогаШапка
	|		ПО ТуристическийНалог.ДокументОснование = НачислениеТуристическогоНалогаШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажахШапка
	|		ПО ТуристическийНалог.ДокументОснование = ОтчетКомиссионераОПродажахШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслугШапка
	|		ПО ТуристическийНалог.ДокументОснование = РеализацияТоваровУслугШапка.Ссылка
	|ГДЕ
	|	(ТуристическийНалог.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ИЛИ ТуристическийНалог.Регистратор ССЫЛКА Документ.ОперацияБух
	|			ИЛИ ТуристическийНалог.Регистратор ССЫЛКА Документ.НачислениеТуристическогоНалога)
	|	И ТуристическийНалог.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТуристическийНалог.Организация = &Организация
	|	И ТуристическийНалог.КоличествоУслуг <> 0
	|	И ТуристическийНалог.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_ТуристическийНалог.КоличествоЛиц) КАК КоличествоЛиц,
	|	СУММА(ВТ_ТуристическийНалог.КоличествоУслуг) КАК КоличествоУслуг,
	|	СУММА(ВТ_ТуристическийНалог.СтоимостьУслуг) КАК СтоимостьУслуг,
	|	СУММА(ВТ_ТуристическийНалог.СтоимостьДопУслуг) КАК СтоимостьДопУслуг,
	|	ВТ_ТуристическийНалог.ВидЛьготы КАК ВидЛьготы,
	|	ВТ_ТуристическийНалог.Гостиница КАК Гостиница,
	|	СУММА(ВТ_ТуристическийНалог.СуммаНалога) КАК СуммаНалога,
	|	ВТ_ТуристическийНалог.СпособРасчета КАК СпособРасчета,
	|	ВТ_ТуристическийНалог.Организация КАК Организация,
	|	ВТ_ТуристическийНалог.Номенклатура КАК Номенклатура,
	|	ВТ_ТуристическийНалог.УслугаРазмещения КАК УслугаРазмещения,
	|	ВТ_ТуристическийНалог.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВТ_ТуристическийНалог.ДокументОснование КАК ДокументОснование,
	|	МАКСИМУМ(ВТ_ТуристическийНалог.Ставка) КАК Ставка,
	|	ВТ_ТуристическийНалог.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВТ_ТуристическийНалог.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_ТуристическийНалог.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ВТ_ТуристическийНалог.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ВТ_ТуристическийНалог.Контрагент КАК Контрагент,
	|	ВТ_ТуристическийНалог.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ТуристическийНалог.НалогРассчитанРанее КАК НалогРассчитанРанее,
	|	ВТ_ТуристическийНалог.Склад КАК Склад,
	|	ВТ_ТуристическийНалог.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_ТуристическийНалог.РасчетПогрешностиОкругления КАК РасчетПогрешностиОкругления
	|ИЗ
	|	ВТ_ТуристическийНалог КАК ВТ_ТуристическийНалог
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТуристическийНалог.ИдентификаторСтроки,
	|	ВТ_ТуристическийНалог.УслугаРазмещения,
	|	ВТ_ТуристическийНалог.РасчетныйДокумент,
	|	ВТ_ТуристическийНалог.ДокументОснование,
	|	ВТ_ТуристическийНалог.ПодразделениеОрганизации,
	|	ВТ_ТуристическийНалог.СтавкаНДС,
	|	ВТ_ТуристическийНалог.ВидСтавкиНДС,
	|	ВТ_ТуристическийНалог.НоменклатурнаяГруппа,
	|	ВТ_ТуристическийНалог.Контрагент,
	|	ВТ_ТуристическийНалог.ДоговорКонтрагента,
	|	ВТ_ТуристическийНалог.НалогРассчитанРанее,
	|	ВТ_ТуристическийНалог.Склад,
	|	ВТ_ТуристическийНалог.ВидЛьготы,
	|	ВТ_ТуристическийНалог.Гостиница,
	|	ВТ_ТуристическийНалог.СпособРасчета,
	|	ВТ_ТуристическийНалог.Организация,
	|	ВТ_ТуристическийНалог.Номенклатура,
	|	ВТ_ТуристическийНалог.РасчетПогрешностиОкругления"; 
	
	ТаблицаОплаченныхУслуг = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаОплаченныхУслуг;
	
КонецФункции

Функция ТаблицаМесяцы(НачДата, КонДата)
	
	Периоды = КалендарьБухгалтера.Периоды(НачДата, КонДата, Перечисления.Периодичность.Месяц);
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("НачалоМесяца", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Таблица.Колонки.Добавить("КонецМесяца", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));

	Для Каждого ЭлементМассива Из Периоды Цикл
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.НачалоМесяца = ЭлементМассива;
		НоваяСтрока.КонецМесяца = КонецМесяца(ЭлементМассива);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти