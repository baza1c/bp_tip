#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеПредложенийИКреативов(Организация, Банк, УникальныйИдентификатор, ИмяФормы, ДатаПолучения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Предложения");
	Результат.Вставить("Креативы");

	СведенияОбОрганизациях = ФинУслуги.СведенияОбОрганизациях(ДатаПолучения, Организация);
	Предложения = Обработки.ФинансыОрганизации.СведенияОПредложенияхБанков(СведенияОбОрганизациях, Банк, ДатаПолучения, ИмяФормы);
	
	МассивБанков = ОбщегоНазначения.ВыгрузитьКолонку(Предложения, "Банк", Истина); 
	Креативы = ФинУслуги.ДанныеКреативов(МассивБанков, УникальныйИдентификатор);

	Результат.Предложения = ОбщегоНазначения.ТаблицаЗначенийВМассив(Предложения);
    Результат.Креативы = ОбщегоНазначения.ТаблицаЗначенийВМассив(Креативы);
	
	Возврат Результат;
	
КонецФункции

Процедура СкрытьБаннерПоИдентификатору(Идентификатор, ТекущаяПозицияБаннеры) Экспорт
	
	ФинУслуги.СкрытьБаннерПоИдентификатору(Идентификатор, ТекущаяПозицияБаннеры);
	
КонецПроцедуры

Функция РолиДоступны() Экспорт
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ЭтоГлавныйБухгалтер = РольДоступна("ДобавлениеИзменениеНастроекБухгалтерии");
	
	Возврат (ЭтоПолноправныйПользователь ИЛИ ЭтоГлавныйБухгалтер);

КонецФункции

Функция БанкУниверсальногоОбменаПоБанку(Банк) Экспорт
	
	ИННСбербанка = "7707083893";
	ДанныеБанка = РаботаСБанкамиБП.ПолучитьДанныеКонтрагентаБанка(Банк, Ложь);
	
	Если ДанныеБанка.ИНН = ИННСбербанка Тогда
		Возврат Банк;
	Иначе
		Возврат УниверсальныйОбменСБанками.БанкУниверсальногоОбменаПоИНН(ДанныеБанка.ИНН, 
			Перечисления.СервисыОбменаСБанками.ФинансоваяОтчетность);
	КонецЕсли;

КонецФункции

Процедура ОчиститьАдресВоВременномХранилище(МассивАдресов) Экспорт
	
	Для Каждого АдресКреатива Из МассивАдресов Цикл 
		Если ЭтоАдресВременногоХранилища(АдресКреатива) Тогда
			УдалитьИзВременногоХранилища(АдресКреатива);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти
