#Область СлужебныйПрограммныйИнтерфейс

Функция ОтправлятьУведомления() Экспорт

	РезультатЧтения = Неопределено;
	СписокЗадачУведомленияПоЭлектроннойПочтеПереопределяемый.ОпределитьОтправлятьУведомления(РезультатЧтения, ВладелецДанныхВХранилище());
	
	Если РезультатЧтения = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат РезультатЧтения;
	
КонецФункции

Функция ОтправлятьУведомленияСЭДО() Экспорт
	
	УчетЗарплатыВЭтойПрограмме = Истина;
	ЗадачиБухгалтераПереопределяемый.ПроверитьУчетЗарплатыВЭтойПрограмме(УчетЗарплатыВЭтойПрограмме);
	Если Не УчетЗарплатыВЭтойПрограмме Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РезультатЧтения = Неопределено;
	СписокЗадачУведомленияПоЭлектроннойПочтеПереопределяемый.ОпределитьОтправлятьУведомленияСЭДО(РезультатЧтения, ВладелецДанныхВХранилище(), 
		ОтправлятьУведомленияСЭДОПоЭлектроннойПочте());
	
	Если РезультатЧтения = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат РезультатЧтения;
	
КонецФункции

Функция Получатель() Экспорт
	
	Получатель = "";
	СписокЗадачУведомленияПоЭлектроннойПочтеПереопределяемый.ОпределитьПолучателя(Получатель, ВладелецДанныхВХранилище());
	Возврат Получатель;
	
КонецФункции

Функция АдресИнформационнойБазы() Экспорт
	
	АдресИнформационнойБазы = "";
	
	СписокЗадачУведомленияПоЭлектроннойПочтеПереопределяемый.ЗаполнитьАдресИнформационнойБазы(АдресИнформационнойБазы, ВладелецДанныхВХранилище());
	
	Возврат АдресИнформационнойБазы;
	
КонецФункции

Процедура УстановитьОтправлятьУведомления(ОтправлятьУведомления) Экспорт
	
	КлючДанных = "ОтправлятьУведомленияПоЭлектроннойПочте";
	СписокЗадачУведомленияПоЭлектроннойПочтеПереопределяемый.УстановитьОтправлятьУведомления(ОтправлятьУведомления, ВладелецДанныхВХранилище(), КлючДанных);

КонецПроцедуры

Процедура УстановитьОтправлятьУведомленияСЭДО(ОтправлятьУведомления) Экспорт
	
	КлючДанных = ОтправлятьУведомленияСЭДОПоЭлектроннойПочте();
	СписокЗадачУведомленияПоЭлектроннойПочтеПереопределяемый.УстановитьОтправлятьУведомления(ОтправлятьУведомления, ВладелецДанныхВХранилище(), КлючДанных);
	
КонецПроцедуры

Процедура УстановитьПоУмолчаниюОтправлятьУведомленияСЭДО() Экспорт
	
	Если КорректностьНастройки() И Не ПустаяСтрока(Получатель()) Тогда
		УстановитьОтправлятьУведомленияСЭДО(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПолучателя(Получатель) Экспорт
	
	СписокЗадачУведомленияПоЭлектроннойПочтеПереопределяемый.УстановитьПолучателя(Получатель, ВладелецДанныхВХранилище());
	
КонецПроцедуры

Функция КорректностьНастройки() Экспорт
	
	Если Не ОтправлятьУведомления() И Не ОтправлятьУведомленияСЭДО() Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка заполненности получателя
	Получатель = Получатель();
	Если ПустаяСтрока(Получатель) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Проверка корректности получателя
	Адреса = ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки(Получатель);
	Для каждого Адрес Из Адреса Цикл
		Если Не ПустаяСтрока(Адрес.ОписаниеОшибки) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Проверка системной записи электронной почты
	УстановитьПривилегированныйРежим(Истина);
	СистемнаяУчетнаяЗапись = РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись();
	УчетнаяЗаписьНастроена = РаботаСПочтовымиСообщениями.УчетнаяЗаписьНастроена(СистемнаяУчетнаяЗапись, Истина, Ложь);
	Если Не УчетнаяЗаписьНастроена Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВладелецДанныхВХранилище()
	
	Возврат "УведомленияОЗадачах";
	
КонецФункции

Функция ОтправлятьУведомленияСЭДОПоЭлектроннойПочте()
	
	Возврат "ОтправлятьУведомленияСЭДОПоЭлектроннойПочте";
	
КонецФункции

#КонецОбласти