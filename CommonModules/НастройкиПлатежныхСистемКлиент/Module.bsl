///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.НастройкиПлатежныхСистем".
// ОбщийМодуль.НастройкиПлатежныхСистемКлиент.
// 
// Клиентские методы работы с платежными системами:
//  - открытие списка настроек платежных систем и создание новой настройки;
//  - открытие формы пользовательского соглашения;
//  - конструкторы параметров методов;
//  - открытие форм настройки шаблонов назначений и шаблонов сообщений;
//  - проверка доступности команды для строки формы настроек.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет открытие формы общего списка настроек подключения к платежным системам.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма которая будет установлена в качестве владельца.
//  ПлатежнаяСистема - Строка - определяет, какие подсистемы должны выводится в списке. Допустимые значения:
//    - <Пустая строка>, "Все" - выводит все внедренные подсистемы;
//    - "СистемаБыстрыхПлатежей" - только Система быстрых платежей;
//    - "ИнтернетЭквайринг" - только Интернет-эквайринг.
//    - "ОзонПэй" - только Озон Пэй.
//
Процедура НастройкиПодключения(Владелец, ПлатежнаяСистема = "") Экспорт
	
	ПараметрыОткрытия = НовыйПараметрыОткрытияСпискаНастроек();
	ПараметрыОткрытия.РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый;
	ПараметрыОткрытия.Владелец   = Владелец;
	ПараметрыОткрытия.Подсистема = ПлатежнаяСистема;
	
	НастройкиПодключенияСлужебный(ПараметрыОткрытия);
	
КонецПроцедуры

// Открывает форму мастера настройки подключения к платежной системе.
// 
// Параметры:
//  ПлатежнаяСистема - Строка - платежная система, для которой необходимо выполнить настройку подключения.
//    Может принимать значения "СистемаБыстрыхПлатежей", "ИнтернетЭквайринг", "ОзонПэй".
//  ПараметрыПодключения - см. НовыйПараметрыПодключения.
//  
Процедура ПодключитьПлатежнуюСистему(ПлатежнаяСистема, ПараметрыПодключения) Экспорт
	
	Эталон = НовыйПараметрыПодключения();
	Если ТипЗнч(ПараметрыПодключения) <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Эталон, ПараметрыПодключения);
	КонецЕсли;
	
	НастройкиПлатежныхСистемКлиентСобытия.ПриПодключенииПлатежнойСистемы(
		ПлатежнаяСистема,
		Эталон);
	
КонецПроцедуры

// АПК:142-выкл параметры зависят от подсистемы

// Формирует необходимые параметры для открытия формы настройки подключения к платежным системам.
// 
// Параметры:
//  БИК - Строка, Неопределено - идентификатор банка. Используется для автоматического выбора
//    участника в мастере подключения.
//  ДополнительныеПараметры - Неопределено, Произвольный - любые дополнительные параметры подключения.
//    В зависимости от платежной системы, значение будет передано в методы:
//     ИнтернетЭквайрингПереопределяемый.ПриНастройкеЭлементовФормыПодключения;
//     СистемаБыстрыхПлатежейПереопределяемый.ПриНастройкеЭлементовФормыПодключения;
//     СистемаБыстрыхПлатежейПереопределяемый.ПриЗаполненииФормыНастройкиПодключения;
//     ПлатежныеСистемыПереопределяемый.ПриНастройкеЭлементовФормыПодключения;
//     ПлатежныеСистемыПереопределяемый.ПриЗаполненииФормыНастройкиПодключения.
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//     необходимо вызвать после завершения настройки подключения. В случае успешного
//     завершения настройки подключения в результате оповещения будет возвращено Истина.
//  ВариантНастройки - Строка - вариант настройки подключения к Системе быстрых платежей.
//     Допустимые значения:
//      "c2b" - подключение приема оплат от физических лиц;
//      "b2b" - подключение приема оплат от юридических лиц.
//    В случае передачи недопустимого значения будет вызвано исключение.
//  ОтборУчастников - Строка, Неопределено - отбор участников СБП. Для варианта настройки "b2b"
//    параметр игнорируется. Допустимые значения:
//      "ПлатежныеАгрегаторы" - будут отображены только платежные агрегаторы в доступных настройках;
//      "КассовыеСсылки" - будут отображены только участники СБП с поддержкой кассовых ссылок.
//    В случае передачи недопустимого значения будет вызвано исключение.
//  ОтображатьОписаниеСервиса - Булево - Признак отображения страницы описания сервиса при подключении к СБП.
//    Значение по умолчанию Ложь.
//
// Возвращаемое значение:
//  Структура - настройки открытия формы подключения:
//    1) Общие для всех подсистем:
//       * БИК - Строка, Неопределено - идентификатор банка. Используется для автоматического выбора
//           участника в мастере подключения.
//       * ДополнительныеПараметры - Неопределено, Произвольный - любые дополнительные параметры подключения.
//           В зависимости от платежной системы, значение будет передано в методы:
//            ИнтернетЭквайрингПереопределяемый.ПриНастройкеЭлементовФормыПодключения;
//            СистемаБыстрыхПлатежейПереопределяемый.ПриНастройкеЭлементовФормыПодключения;
//            СистемаБыстрыхПлатежейПереопределяемый.ПриЗаполненииФормыНастройкиПодключения;
//            ПлатежныеСистемыПереопределяемый.ПриНастройкеЭлементовФормыПодключения
//            ПлатежныеСистемыПереопределяемый.ПриЗаполненииФормыНастройкиПодключения
//       * ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//           необходимо вызвать после завершения настройки подключения. В случае успешного
//           завершения настройки подключения в результате оповещения будет возвращено Истина.
//    2) Для подсистемы "Система быстрых платежей":
//       * ВариантНастройки - Строка - вариант настройки подключения к Системе быстрых платежей.
//           Возможные значения:
//            c2b - подключение приема оплат от физических лиц;
//            b2b - подключение приема оплат от юридических лиц.
//       * ОтборУчастников - Строка, Неопределено -  отбор участников СБП. Для варианта настройки "b2b"
//          параметр игнорируется. Возможные значения:
//          "ПлатежныеАгрегаторы" - будут отображены только платежные агрегаторы в доступных настройках;
//          "КассовыеСсылки" - будут отображены только участники СБП с поддержкой кассовых ссылок.
//       * ОтображатьОписаниеСервиса - Булево - Признак отображения страницы описания сервиса.
//    3) Для подсистемы "Интернет-эквайринг":
//       * НеПредлагатьПодключениеКОнлайнЗаказам - Булево - если Истина, то в мастере подключения не будет шага с
//           созданием/привязкой к настройке Онлайн-заказов.
//    4) Для Озон Пэй:
//       * ОтображатьОписаниеСервиса - Булево - Признак отображения страницы описания сервиса;
//       * НастройкаДляКопирования - СправочникСсылка.НастройкиПодключенияКПлатежнымСистемам - настройка, будет создана
//                                      копированием существующей.
//                                 - Неопределено - будет создана с нуля.
//       * НеПредлагатьПодключениеКОнлайнЗаказам - Булево - если Истина, то в мастере подключения не будет шага с
//           созданием/привязкой к настройке Онлайн-заказов.
//
//@skip-check method-too-many-params
Функция НовыйПараметрыПодключения(
		БИК = Неопределено,
		ДополнительныеПараметры = Неопределено,
		ОписаниеОповещения = Неопределено,
		ВариантНастройки = "",
		ОтборУчастников = Неопределено,
		ОтображатьОписаниеСервиса = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(ВариантНастройки)
		И ВариантНастройки <> "c2b"
		И ВариантНастройки <> "b2b" Тогда
	
		ВызватьИсключение НСтр("ru = 'Передано недопустимое значение параметра ВариантНастройки'");
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборУчастников)
		И ОтборУчастников <> "ПлатежныеАгрегаторы"
		И ОтборУчастников <> "КассовыеСсылки" Тогда
	
		ВызватьИсключение НСтр("ru = 'Передано недопустимое значение параметра ОтборУчастников'");
	
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Результат.Вставить("БИК", БИК);
	Результат.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	Результат.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	Результат.Вставить("ВариантНастройки", ВариантНастройки);
	Результат.Вставить("ОтборУчастников", ОтборУчастников);
	Результат.Вставить("ОтображатьОписаниеСервиса", ОтображатьОписаниеСервиса);
	Результат.Вставить("НастройкаДляКопирования", Неопределено);
	Результат.Вставить("НеПредлагатьПодключениеКОнлайнЗаказам", Ложь);
	
	Возврат Результат;
	
КонецФункции

// АПК:142-вкл

// Открывает форму пользовательского соглашения.
//
// Параметры:
//  ВладелецФормы - ФормаКлиентскогоПриложения - в качестве владельца может выступить форма или элемент
//    управления другой формы.
//  ОписаниеОповещения - Неопределено, ОписаниеОповещения - описание процедуры, которая будет вызвана
//    при закрытии формы.
//
Процедура ФормаПользовательскогоСоглашения(
		ВладелецФормы = Неопределено,
		ОписаниеОповещения = Неопределено) Экспорт
	
	Если ВладелецФормы = Неопределено Тогда
		РежимОткрытия = РежимОткрытияОкнаФормы.Независимый;
	Иначе
		РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	КонецЕсли;
	
	ОткрытьФорму(
		"ОбщаяФорма.ПользовательскоеСоглашениеПлатежныеСистемы",
		,
		ВладелецФормы,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует параметры, необходимые для обработки выбора строки в списке настроек
// 
// Параметры:
//  ТекущиеДанные - Неопределено, Произвольный - выбранная строка или структура, с ее описанием;
// 
// Возвращаемое значение:
//  Структура - При обработке выбора настройки:
//   * ИмяПодсистемы - Строка - имя подсистемы, к которой относится выбранная строка;
//   * Отказ - Булево - Если Истина, то выбор текущего значения запрещен;
//   * СообщениеОбОшибке - Строка - информация о причине запреты выбора указанного значения. Если заполнена, то
//       пользователю выводится текст ошибки. В противном случае, если Отказ = Истина, действие просто отменяется без
//       информирования пользователя;
//   * ИмяОткрываемойФормы - Строка - полное имя формы, которая должна быть открыта, если нет ошибок;
//   * РежимВыбора - Булево - Истина, если форма открыта в режиме выбора;
//   * ТекущиеДанные - Неопределено, Произвольный - выбранная строка или структура, с ее описанием;
//   * РезультатВыбора - Неопределено, Произвольный - значение, которое будет использоваться в результате выбора;
//   * ТолькоЭлементы - Булево - доступен выбор только элементов;
//   * ТолькоГруппы - Булево - доступен выбор только групп.
//   * ТребуетсяДополнительнаяОбработка - Булево - Истина, если нужен вызов дополнительной обработки.
//   * ПараметрыДополнительнойОбработки - Неопределено, Произвольный - любые дополнительные параметры для
//       последующей обработки.
//   
Функция НовыйПараметрыОбработкиВыбораНастройки(ТекущиеДанные = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяПодсистемы",                    "");
	Результат.Вставить("Отказ",                            Ложь);
	Результат.Вставить("СообщениеОбОшибке",                "");
	Результат.Вставить("ИмяОткрываемойФормы",              "");
	Результат.Вставить("РежимВыбора",                      Ложь);
	Результат.Вставить("ТекущиеДанные",                    ТекущиеДанные);
	Результат.Вставить("РезультатВыбора",                  Неопределено);
	Результат.Вставить("ТолькоЭлементы",                   Ложь);
	Результат.Вставить("ТолькоГруппы",                     Ложь);
	Результат.Вставить("ТребуетсяДополнительнаяОбработка", Ложь);
	Результат.Вставить("ПараметрыДополнительнойОбработки", Неопределено);
	
	Возврат Результат;

КонецФункции

// Формирует параметры, необходимые для обработки выбранной строки в списке настроек при нажатии команды "Изменить".
// 
// Параметры:
//  ТекущиеДанные - Неопределено, Произвольный - выбранная строка или структура, с ее описанием;
// 
// Возвращаемое значение:
//  Структура - При обработке выбора настройки:
//   * ИмяПодсистемы - Строка - имя подсистемы, к которой относится выбранная строка;
//   * Отказ - Булево - если Истина, то выбор текущего значения запрещен;
//   * СообщениеОбОшибке - Строка - информация о причине запреты выбора указанного значения. Если заполнена, то
//       пользователю выводится текст ошибки. В противном случае, если Отказ = Истина, действие просто отменяется без
//       информирования пользователя;
//   * ТекущиеДанные - Неопределено, Произвольный - выбранная строка или структура, с ее описанием;
//   * РезультатВыбора - Неопределено, Произвольный - значение, которое будет использоваться для открытия.
//       Изначально устанавливается равным значению Ссылки в текущей строке, но может быть изменено при обработке;
//   * ИмяФормы - Строка - полное имя открываемой формы, которая должна быть открыта для вывода значения.
// 
Функция НовыйПараметрыОбработкиВыбораНастройкиДляИзменения(
		ТекущиеДанные = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяПодсистемы",             "");
	Результат.Вставить("Отказ",                     Ложь);
	Результат.Вставить("СообщениеОбОшибке",         "");
	Результат.Вставить("ТекущиеДанные",             ТекущиеДанные);
	Результат.Вставить("РезультатВыбора",           Неопределено);
	Результат.Вставить("ИмяФормы",                  "");
	
	Возврат Результат;

КонецФункции

// Открывает форму списка шаблонов сообщений.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - владелец открываемой формы.
//
Процедура ОткрытьШаблоныСообщений(Владелец) Экспорт
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		Возврат;
	КонецЕсли;
	
	ЗависимыеПодсистемы = Новый Массив;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		ЗависимыеПодсистемы.Добавить("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП");
	КонецЕсли;
	
	Если ЗависимыеПодсистемы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроверкиШаблонов = Новый Структура(
		"Подсистемы, Шаблоны",
		Новый Массив,
		Новый Массив);
	
	ЕстьПодсистема = Ложь;
	
	Для Каждого ИмяПодсистемы Из ЗависимыеПодсистемы Цикл
		
		ЕстьПодсистема = Истина;
	
		ПараметрыПодсистемы = Новый Структура(
			"ВсеСозданы, Шаблоны",
			Истина,
			Новый Массив);
		
		Если ИмяПодсистемы = "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП" Тогда
			
			МодульСистемаБыстрыхПлатежейВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль("СистемаБыстрыхПлатежейВызовСервера");
			
			РезультатПроверки = МодульСистемаБыстрыхПлатежейВызовСервера.ШаблоныСозданы();
			ПараметрыПодсистемы.ВсеСозданы = РезультатПроверки.ШаблоныСозданы;
			ПараметрыПодсистемы.Шаблоны    = РезультатПроверки.Шаблоны;
		
		КонецЕсли;
		
		Если Не ПараметрыПодсистемы.ВсеСозданы Тогда
			ПараметрыПроверкиШаблонов.Подсистемы.Добавить(ИмяПодсистемы);
		КонецЕсли;
			
		Если ПараметрыПодсистемы.Шаблоны.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				ПараметрыПроверкиШаблонов.Шаблоны,
				ПараметрыПодсистемы.Шаблоны);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЕстьПодсистема Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Ни одна используемая подсистема платежных сервисов не включена в общие настройки.'"));
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПроверкиШаблонов.Подсистемы.Количество() > 0 Тогда
	
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Шаблоны",    ПараметрыПроверкиШаблонов.Шаблоны);
		ДополнительныеПараметры.Вставить("Владелец",   Владелец);
		ДополнительныеПараметры.Вставить("Подсистемы", ПараметрыПроверкиШаблонов.Подсистемы);
		
		Оповещение = Новый ОписаниеОповещения(
			"ВопросСозданияШаблоновЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Создать шаблоны сообщений для автоматического заполнения на основании данных документов?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		
		ОткрытьФормуШаблонов(ПараметрыПроверкиШаблонов.Шаблоны, Владелец);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму настройки отображения команд.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма которая будет установлена в качестве владельца.
//
Процедура НастройкаОтображенияКоманд(Владелец) Экспорт
	
	ОткрытьФорму(
		"Обработка.НастройкиПлатежныхСистем.Форма.НастройкиОтображенияКоманд",
		,
		Владелец);
	
КонецПроцедуры

// Открывает форму для редактирования шаблонов назначения платежа.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, для которой необходимо
//    выполнить настройку шаблонов.
//  Владелец - Неопределено, ФормаКлиентскогоПриложения - Владелец открываемой формы.
//  ОповещениеОЗакрытии - ОписаниеОповещения - оповещение, которое будет вызвано при закрытии формы настроек.
//
Процедура ОткрытьНастройкуШаблоновНазначения(
		НастройкаПодключения,
		Владелец = Неопределено,
		ОповещениеОЗакрытии = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	Ключ = Неопределено;
	Если ТипЗнч(Владелец) = Тип("ФормаКлиентскогоПриложения") Тогда
		Ключ = Владелец.УникальныйИдентификатор;
	КонецЕсли;
	
	ОткрытьФорму(
		"Обработка.НастройкиПлатежныхСистем.Форма.ФормаНастройкиНазначенияПлатежа",
		ПараметрыФормы,
		Владелец,
		Ключ,
		,
		,
		ОповещениеОЗакрытии);
	
КонецПроцедуры

// Выполняет открытие формы общего списка настроек подключения к платежным системам.
// 
// Параметры:
//  ПараметрыОткрытия - см. НовыйПараметрыОткрытияСпискаНастроек.
//
Процедура НастройкиПодключенияСлужебный(ПараметрыОткрытия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Подсистема", ПараметрыОткрытия.Подсистема);
	ПараметрыФормы.Вставить("ПараметрыПодсистемы", ПараметрыОткрытия.ПараметрыПодсистемы);
	ПараметрыФормы.Вставить("РежимВыбора", ПараметрыОткрытия.РежимВыбора);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", ПараметрыОткрытия.ЗакрыватьПриВыборе);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ПараметрыОткрытия.ВыборГруппИЭлементов);
	ПараметрыФормы.Вставить("Отображение", ПараметрыОткрытия.Отображение);
	ПараметрыФормы.Вставить(
		"НеПредлагатьПодключениеКОнлайнЗаказам",
		ПараметрыОткрытия.НеПредлагатьПодключениеКОнлайнЗаказам);
	
	ОткрытьФорму(
		"Обработка.НастройкиПлатежныхСистем.Форма.Форма",
		ПараметрыФормы,
		ПараметрыОткрытия.Владелец,
		ПараметрыОткрытия.Уникальность,
		,
		,
		ПараметрыОткрытия.ОписаниеОповещения,
		ПараметрыОткрытия.РежимОткрытияОкна);
	
КонецПроцедуры

// Формирует параметры открытия общей формы настроек подключения к платежным системам.
// 
// Возвращаемое значение:
//  Структура - параметры для использования в методе НастройкиПлатежныхСистем:
//   * РежимВыбора - Булево - открывает форму в режиме выбора;
//   * ЗакрыватьПриВыборе - Булево - если РежимВыбора = Истина, то форма будет закрыта после выбора значения;
//   * Владелец - Неопределено, Произвольный - форма или элемент управления другой формы;
//   * Уникальность - Неопределено, Произвольный - ключ уникальности формы;
//   * ОписаниеОповещения - Неопределено, ОписаниеОповещения - описание процедуры, которая будет вызвана при
//     закрытии формы;
//   * РежимОткрытияОкна - РежимОткрытияОкнаФормы - указывает режим открытия окна формы клиентского приложения;
//   * Подсистема - Строка - определяет, какие подсистемы должны выводится в списке. Допустимые значения:
//                  - <Пустая строка>, "Все" - выводит все внедренные подсистемы;
//                  - "СистемаБыстрыхПлатежей" - только Система быстрых платежей;
//                  - "ИнтернетЭквайринг" - только Интернет-эквайринг;
//                  - "ОзонПэй" - только Ozon Pay.
//   * ПараметрыПодсистемы - Неопределено, Произвольный - произвольные параметры, которые могут использоваться
//     при работе с настройками подключения к подсистеме;
//   * ВыборГруппИЭлементов - ИспользованиеГруппИЭлементов - возможность выбора групп и элементов.
//   * Отображение - Строка - вариант отображения списка. Доступные значения: "Список", "Дерево", "ИерархическийСписок".
//       Если не заполнено, то используется значение по умолчанию - "Дерево".
//   * НеПредлагатьПодключениеКОнлайнЗаказам - Булево - если в мастере создания новой настройки подключения к платежной
//       системе есть возможность выполнить ее привязку к настройке Онлайн-заказов, то она не будет использоваться,
//       если данный параметр установлен в Истину.
//
Функция НовыйПараметрыОткрытияСпискаНастроек() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("РежимВыбора", Ложь);
	Результат.Вставить("ЗакрыватьПриВыборе", Ложь);
	Результат.Вставить("Владелец", Неопределено);
	Результат.Вставить("Уникальность", Неопределено);
	Результат.Вставить("ОписаниеОповещения", Неопределено);
	Результат.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Результат.Вставить("Подсистема", "");
	Результат.Вставить("ПараметрыПодсистемы", Неопределено);
	Результат.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	Результат.Вставить("Отображение", "");
	Результат.Вставить("НеПредлагатьПодключениеКОнлайнЗаказам", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Обновляет текст декорации на форме настроек подключения к платежным системам, которая отвечает за настройку шаблонов
//  назначений платежей.
//
// Параметры:
//  КоличествоШаблонов - Произвольный - результат закрытия формы настройки шаблонов назначений;
//  ДекорацияФормы - ДекорацияФормы - декорация, которую необходимо обновить.
// 
Процедура ОбновитьТекстНастройкиШаблонов(
		КоличествоШаблонов,
		ДекорацияФормы) Экспорт
	
	Если КоличествоШаблонов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(КоличествоШаблонов) = Тип("Число") И КоличествоШаблонов > 0 Тогда
		ПредставлениеСсылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Шаблоны назначения (%1)'"),
			КоличествоШаблонов);
	Иначе
		ПредставлениеСсылки = НСтр("ru = 'Шаблоны назначения'");
	КонецЕсли;
	
	ДекорацияФормы.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '<a href = ""open:paymentPurpose"">%1</a>'"),
			ПредставлениеСсылки))
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Открывает форму списка шаблонов сообщений с отбором по ссылкам, относящимся к платежным системам.
//
// Параметры:
//  Шаблоны - Массив Из СправочникСсылка.ШаблоныСообщений - шаблоны сообщений, относящиеся к платежным системам.
//  Владелец - ФормаКлиентскогоПриложения - владелец открываемой формы.
//
Процедура ОткрытьФормуШаблонов(Шаблоны, Владелец)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		ВызватьИсключение НСтр("ru = 'Отсутствует подсистема ""Шаблоны сообщений"".
			|Обратитесь к администратору.'");
	КонецЕсли;
	
	ОтборФормы = Новый Структура();
	ОтборФормы.Вставить("Ссылка", Шаблоны);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Отбор", ОтборФормы);
	
	ИмяФормыСпискаШаблонов = "Справочник.ШаблоныСообщений.ФормаСписка";
	ОткрытьФорму(
		ИмяФормыСпискаШаблонов,
		ПараметрыФормы,
		Владелец,
		Владелец.УникальныйИдентификатор);
	
КонецПроцедуры

// Обрабатывает ответ на вопрос о создании отсутствующих шаблонов сообщений.
//
// Параметры:
//  Результат - КодВозвратаДиалога - результат выбора пользователя.
//  ДополнительныеПараметры - Структура - параметры, необходимые для дальнейшей обработки:
//    * Шаблоны - Массив Из СправочникСсылка.ШаблоныСообщений - шаблоны сообщений, относящиеся к платежным системам.
//    * Владелец - ФормаКлиентскогоПриложения - владелец открываемой формы.
//    * Подсистемы - Массив Из Строка - имена подсистем, по которым созданы не все шаблоны.
//
Процедура ВопросСозданияШаблоновЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда

		Для Каждого ИмяПодсистемы Из ДополнительныеПараметры.Подсистемы Цикл
		
			СозданныеШаблоны = Новый Массив;
			
			Если ИмяПодсистемы = "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП" Тогда
				МодульСистемаБыстрыхПлатежейВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль("СистемаБыстрыхПлатежейВызовСервера");
				СозданныеШаблоны = МодульСистемаБыстрыхПлатежейВызовСервера.СоздатьПредопределенныеШаблоныСообщений(
					Истина);
			КонецЕсли;
			
			Если СозданныеШаблоны.Количество() > 0 Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
					ДополнительныеПараметры.Шаблоны,
					СозданныеШаблоны);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Форма открывается если выбран вариант Да или Нет
	ОткрытьФормуШаблонов(
		ДополнительныеПараметры.Шаблоны,
		ДополнительныеПараметры.Владелец);
	
КонецПроцедуры

// Проверяет доступность выполнения команды формы списка настроек подключения в зависимости от текущей строки дерева.
// Если текущие данные дерева равны Неопределено или выбран корень подсистемы, то метод только возвращает Ложь.
// В других случаях, если выполнение команды недоступно, дополнительно выводится сообщение пользователю.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма списка настроек подключения.
//  Команда - КомандаФормы - выполняемая команда.
//  
// Возвращаемое значение:
//  Булево - признак доступности команды для текущей строки.
//
Функция КомандаДоступна(Форма, Команда) Экспорт

	ТекущиеДанные = Форма.Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Не выбрана настройка подключения'"));
		Возврат Ложь;
	КонецЕсли;
	
	Если ТекущиеДанные.ТипЭлемента = НастройкиПлатежныхСистемКлиентСервер.ИмяКорень() Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Действие недоступно для строки верхнего уровня'"));
		Возврат Ложь;
	КонецЕсли;

	Если Форма.КешПараметров.ОбщиеКоманды.Найти(Команда.Имя) <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	ДоступностьПоПодсистеме = Форма.КешПараметров.ДоступностьКоманд.Получить(ТекущиеДанные.Подсистема);
	
	Если ДоступностьПоПодсистеме <> Неопределено Тогда
		Если ДоступностьПоПодсистеме.Найти(Команда.Имя) <> Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьПредупреждение(
		Неопределено,
		НСтр("ru = 'Действие недоступно для текущей строки'"));
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

