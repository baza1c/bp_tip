///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается перед открытием формы нового письма.
// Открытие формы может быть отменено изменением параметра СтандартнаяОбработка.
//
// Параметры:
//  ПараметрыОтправки    - см. РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма
//  ОбработчикЗавершения - ОписаниеОповещения - описание процедуры, которая будет вызвана после завершения
//                                              отправки письма.
//  СтандартнаяОбработка - Булево - признак продолжения открытия формы нового письма после выхода из этой
//                                  процедуры. Если установить Ложь, форма письма открыта не будет.
//
Процедура ПередОткрытиемФормыОтправкиПисьма(ПараметрыОтправки, ОбработчикЗавершения, СтандартнаяОбработка) Экспорт

	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПисьмаПоУмолчанию = ОтправкаПочтовыхСообщенийВызовСервера.ПараметрыПисьмаПоУмолчанию();
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Тема");
	ПараметрыПисьма.Вставить("Вложения");
	ПараметрыПисьма.Вставить("ШаблоныПисьма");
	ПараметрыПисьма.Вставить("АдресПолучателяСкрытойКопии");
	ПараметрыПисьма.Вставить("УдалятьФайлыПослеОтправки");
	ПараметрыПисьма.Вставить("ВыбиратьПолучателей");
	ПараметрыПисьма.Вставить("АдресИсходногоВложения");
	ПараметрыПисьма.Вставить("ДанныеУчастника");
	ЗаполнитьЗначенияСвойств(ПараметрыПисьма, ПараметрыОтправки);
	
	Если ПараметрыОтправки.Свойство("Отправитель") Тогда
		Если ЗначениеЗаполнено(ПараметрыОтправки.Отправитель) Тогда
			Отправитель = ПараметрыОтправки.Отправитель;
		Иначе
			Отправитель = ПараметрыПисьмаПоУмолчанию.Отправитель;
		КонецЕсли;
		
		ПараметрыПисьма.Вставить("Отправитель", Отправитель);
	КонецЕсли;
	
	Если ПараметрыОтправки.Свойство("Получатель") Тогда
		ПараметрыПисьма.Вставить("Получатель", ПараметрыОтправки.Получатель);
	КонецЕсли;
	
	Если ПараметрыОтправки.Свойство("Текст") Тогда
		Если ЗначениеЗаполнено(ПараметрыОтправки.Текст) Тогда
			Текст = ПараметрыОтправки.Текст;
		Иначе
			Текст = ПараметрыПисьмаПоУмолчанию.Текст;
		КонецЕсли;
		
		ПараметрыПисьма.Вставить("Текст", Текст);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПисьма.АдресПолучателяСкрытойКопии) Тогда
		ПараметрыПисьма.АдресПолучателяСкрытойКопии = ПараметрыПисьмаПоУмолчанию.АдресПолучателяСкрытойКопии;
	КонецЕсли;
	
	ИспользоватьВзаимодействияБП = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтправки,
		"ИспользоватьВзаимодействияБП");
	
	Если ИспользоватьВзаимодействияБП <> Ложь
		И ПараметрыПисьма.ВыбиратьПолучателей <> Истина
		И СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().СозданиеИсходящихПисемДоступно Тогда
		Если ПараметрыОтправки.Свойство("ОбъектыНазначения")
			И ВзаимодействияКлиентБП.РассылкаРегистрируетсяКакВзаимодействие(ПараметрыОтправки.ОбъектыНазначения[0]) Тогда
			ПараметрыПисьма.Вставить("ДокументыОснования", ПараметрыОтправки.ОбъектыНазначения);
		ИначеЕсли ПараметрыОтправки.Свойство("Предмет")
			И ВзаимодействияКлиентБП.РассылкаРегистрируетсяКакВзаимодействие(ПараметрыОтправки.Предмет) Тогда
			ПараметрыПисьма.Вставить("ДокументыОснования", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыОтправки.Предмет));
		КонецЕсли;
		ВзаимодействияКлиентБП.СоздатьПисьмо(ПараметрыПисьма);
	Иначе
		ОткрытьФорму("ОбщаяФорма.ОтправкаСообщенияБП", ПараметрыПисьма, , , , , ОбработчикЗавершения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти