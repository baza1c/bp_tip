////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиДепозитыСлужебный: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ДепозитныеПродукты

// Проверить актуальность и получить данные для запроса предложений.
// 
// Параметры:
//  ВсеБанки - Булево - если Истина, данные для запроса предложений будут получены для всех доступных банков,
//    вне зависимости от актуальности уже полученных предложений.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - см. ПараметрыПолученияПредложенияПоДепозитам.
//
Функция ПроверитьАктуальностьИПолучитьДанныеДляЗапросаПредложений(ВсеБанки) Экспорт
	
	ТаблицаОтборБанков = Новый ТаблицаЗначений;
	
	Если УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей()
		И Не Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Банк КАК Банк
		|ИЗ
		|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанкамиИсходящиеДокументы
		|ГДЕ
		|	НастройкиОбменСБанкамиИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита)
		|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Недействительна
		|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.ПометкаУдаления";
		
		ТаблицаОтборБанков = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	// Если требуется получить предложения по депозитам от всех банков без учета текущей актуальности предложений,
	// удалим уже полученные данные о предложениях по депозитам.
	
	Если ВсеБанки Тогда
		ОчиститьПредложенияБанковПоДепозитам(ТаблицаОтборБанков);
	КонецЕсли;
		
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Банк КАК Банк,
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.АдресСервера КАК АдресСервера,
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.НаименованиеБанка КАК НаименованиеБанка,
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Организация КАК Организация,
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.ИдентификаторОрганизации КАК ИдентификаторОрганизации
	|ПОМЕСТИТЬ ВТ_ДоступенОбменДепозитами
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанкамиИсходящиеДокументы
	|ГДЕ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита)
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Недействительна
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.ПометкаУдаления
	|	И &УсловиеПоБанку
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Банк
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДепозитныеПродуктыОбменСБанками.Банк КАК Банк,
	|	МИНИМУМ(ЕСТЬNULL(ДепозитныеПродуктыОбменСБанками.ДатаАктуальности, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаАктуальности
	|ПОМЕСТИТЬ ВТ_АктуальностьПредложенийБанков
	|ИЗ
	|	ВТ_ДоступенОбменДепозитами КАК ВТ_ДоступенОбменДепозитами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДепозитныеПродуктыОбменСБанками КАК ДепозитныеПродуктыОбменСБанками
	|		ПО (ДепозитныеПродуктыОбменСБанками.Банк = ВТ_ДоступенОбменДепозитами.Банк)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДепозитныеПродуктыОбменСБанками.Банк
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Банк
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДоступенОбменДепозитами.Банк КАК Банк,
	|	ВТ_ДоступенОбменДепозитами.НаименованиеБанка КАК НаименованиеБанка,
	|	ВТ_ДоступенОбменДепозитами.АдресСервера КАК АдресСервера,
	|	ВТ_ДоступенОбменДепозитами.Организация КАК Организация,
	|	ВТ_ДоступенОбменДепозитами.ИдентификаторОрганизации КАК ИдентификаторОрганизации
	|ИЗ
	|	ВТ_ДоступенОбменДепозитами КАК ВТ_ДоступенОбменДепозитами
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АктуальностьПредложенийБанков КАК ВТ_АктуальностьПредложенийБанков
	|		ПО ВТ_ДоступенОбменДепозитами.Банк = ВТ_АктуальностьПредложенийБанков.Банк
	|			И (ВТ_АктуальностьПредложенийБанков.ДатаАктуальности = &ДатаАктуальности)
	|ГДЕ
	|	ВТ_АктуальностьПредложенийБанков.ДатаАктуальности ЕСТЬ NULL
	|ИТОГИ ПО
	|	Банк";
	
	Если ЗначениеЗаполнено(ТаблицаОтборБанков) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоБанку", 
			"НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Банк В (&ОтборБанки)");
		Запрос.УстановитьПараметр("ОтборБанки", ТаблицаОтборБанков.ВыгрузитьКолонку("Банк"));
	Иначе
		Запрос.УстановитьПараметр("УсловиеПоБанку", Истина);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаАктуальности", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаБанки = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = Новый Соответствие();

	Пока ВыборкаБанки.Следующий() Цикл
		
		Если Не БанкПоддерживаетОбменДепозитами(ВыборкаБанки.Банк) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеЗапроса = НовыеПараметрыПолученияПредложенияПоДепозитам();
		ВыборкаДетальныеЗаписи = ВыборкаБанки.Выбрать();
		СоответствиеИдКлиентовБанкаОрганизациям = ДанныеЗапроса.СоответствиеИдКлиентовБанкаОрганизациям;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если Не ЗначениеЗаполнено(ДанныеЗапроса.АдресСервера) Тогда
				ДанныеЗапроса.АдресСервера = ВыборкаДетальныеЗаписи.АдресСервера;
				ДанныеЗапроса.НаименованиеБанка = ВыборкаДетальныеЗаписи.НаименованиеБанка;
			КонецЕсли;
			
			СоответствиеИдКлиентовБанкаОрганизациям.Вставить(ВыборкаДетальныеЗаписи.ИдентификаторОрганизации,
				ВыборкаДетальныеЗаписи.Организация);
			
		КонецЦикла;
		
		Результат.Вставить(ВыборкаБанки.Банк, ДанныеЗапроса);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьДанныеДепозитныхПродуктов(ОбновитьВсеБанки) Экспорт
	
	ДанныеДляЗапросаПредложений = ПроверитьАктуальностьИПолучитьДанныеДляЗапросаПредложений(ОбновитьВсеБанки);
	
	Если ДанныеДляЗапросаПредложений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаБанковСОшибкой = Новый ТаблицаЗначений();
	ТаблицаБанковСОшибкой.Колонки.Добавить("Банк", Метаданные.ОпределяемыеТипы.БанкОбменСБанками.Тип);
	ТаблицаБанковСОшибкой.Колонки.Добавить("Ошибка");
	
	ДатаНачалаОтправкиВБанк = ТекущаяДатаСеанса();
	
	Для Каждого КлючИЗначение Из ДанныеДляЗапросаПредложений Цикл
		
		Попытка
			ОтправитьЗапросИОбновитьПредложенияПоДепозитам(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Исключение
			
			ТекущаяСтрока = ТаблицаБанковСОшибкой.Добавить();
			ТекущаяСтрока.Банк = КлючИЗначение.Ключ;
			ТекущаяСтрока.Ошибка = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ТаблицаБанковСОшибкой.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировкиПродукты = БлокировкаДанных.Добавить("РегистрСведений.ДепозитныеПродуктыОбменСБанками");
	ЭлементБлокировкиПродукты.ИсточникДанных = ТаблицаБанковСОшибкой;
	ЭлементБлокировкиПродукты.ИспользоватьИзИсточникаДанных("Банк", "Банк");
	ЭлементБлокировкиУсловия = БлокировкаДанных.Добавить("РегистрСведений.УсловияДепозитныхПродуктовОбменСБанками");
	ЭлементБлокировкиУсловия.ИсточникДанных = ТаблицаБанковСОшибкой;
	ЭлементБлокировкиУсловия.ИспользоватьИзИсточникаДанных("Банк", "Банк");
	
	НачатьТранзакцию();
	
	Попытка
	
		БлокировкаДанных.Заблокировать();
		
		Для каждого ТекущаяСтрока Из ТаблицаБанковСОшибкой Цикл
			
			НаборЗаписейРегистраПродукты = РегистрыСведений.ДепозитныеПродуктыОбменСБанками.СоздатьНаборЗаписей();
			НаборЗаписейРегистраПродукты.Отбор.Банк.Установить(ТекущаяСтрока.Банк);
			
			НаборЗаписейРегистраУсловия = РегистрыСведений.УсловияДепозитныхПродуктовОбменСБанками.СоздатьНаборЗаписей();
			НаборЗаписейРегистраУсловия.Отбор.Банк.Установить(ТекущаяСтрока.Банк);
			
			ЗаписьОбОшибке = НаборЗаписейРегистраПродукты.Добавить();
			ЗаписьОбОшибке.Банк =  ТекущаяСтрока.Банк;
			ЗаписьОбОшибке.ПодробноеОписание = ТекущаяСтрока.Ошибка;
			ЗаписьОбОшибке.ДатаАктуальности = ТекущаяДатаСеанса();
			ЗаписьОбОшибке.ОшибкаЗагрузки = Истина;
			ЗаписьОбОшибке.ПредложенияОтсутствуют = Ложь;
			
			НаборЗаписейРегистраУсловия.Записать(Истина);
			НаборЗаписейРегистраПродукты.Записать(Истина);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Если Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда
			
			ШаблонСообщения = НСтр("ru = 'Возникла ошибка при обновлении предложений банка по депозитам.
				|%1'");
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки);
			
		Иначе 
			
			ТекстСообщения = НСтр("ru = 'Не удалось обновить предложения банка, так как при записи
				|предложений по депозитам произошла ошибка.'");
			
		КонецЕсли;
		
		ВидОперации = НСтр("ru = 'Обновление предложений банков'");
		ПодробнаяИнформация = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
КонецПроцедуры

// Получает доступные для организации предложения банков по депозитам.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - отбор по организации.
// 
// Возвращаемое значение:
//  Структура:
//   * Продукты - ТаблицаЗначений:
//      ** Банк - ОпределяемыйТип.БанкОбменСБанками - ссылка на банк.
//      ** НаименованиеБанка - Строка - наименование банка.
//      ** КлючПродукта - Строка - уникальный идентификатор депозитного продукта.
//      ** ИдентификаторПродукта - Строка - идентификатор продукта в системе банка.
//      ** Наименование - Строка - наименование депозитного продукта.
//      ** ПодробноеОписание - Строка - описание депозитного продукта.
//      ** Валюта - СправочникСсылка.Валюта - валюта депозитного продукта.
//      ** СуммаМинимум -  - Число - минимальная сумма депозита.
//      ** СуммаМаксимум - Число - максимальная сумма депозита.
//      ** СрокМинимум - Число - минимальный срок депозита в днях.
//      ** СрокМаксимум - Число - максимальный срок депозита в днях.
//      ** СтавкаМаксимум - Число - процентная ставка депозита
//      ** ВариантРасчета - ПеречислениеСсылка.МетодыРасчетаПроцентовДепозитовОбменСБанками - метод расчета процентов по депозитам.
//      ** Капитализация - Булево - если Истина, используется капитализация.
//      ** ВыплатаПроцентов - ПеречислениеСсылка.СпособыВыплатыПроцентовДепозитовОбменСБанками - способ выплаты процентов.
//      ** Срок - Число - срок депозита.
//      ** Сумма - Число - сумма депозита.
//      ** Ставка - Число - процентная ставка депозита.
//      ** Доход - Число - доход по депозиту.
//      ** Продвигаемый - Булево -  приоритетное условие для продукта банка.
//      ** РасчетПриОформлении - Булево - если Истина, расчет доходности выполняется в банке.
//      ** ИдентификаторУсловия - уникальный идентификатор условия.
//   * Условия - Соответствие из КлючИЗначение:
//      ** Ключ - Строка - уникальный идентификатор депозитного продукта.
//      ** Значение - Массив из Структура: условия депозитного продукта см. НовыеПараметрыУсловияДепозитногоПродукта.
//   * ОшибкаПолученияПредложений - Строка - содержит текст ошибки, если не удалось получить предложения ни от одного банка.
//
Функция ДанныеДепозитныхПродуктов(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Результат = Новый Структура;
	Результат.Вставить("Продукты", Новый ТаблицаЗначений);
	Результат.Вставить("Условия", Новый Соответствие);
	Результат.Вставить("ОшибкаПолученияПредложений", "");
	
	ДатаАктуальности = НачалоДня(ТекущаяДатаСеанса());
	
	ОтборОрганизации = Новый Массив;
	ОтборОрганизации.Добавить(Организация);
	
	ИмяСправочникаОрганизация = ОбщегоНазначенияБЭД.ИмяПрикладногоСправочника("Организации");
	ОрганизацияПустаяСсылка = Справочники[ИмяСправочникаОрганизация].ПустаяСсылка();
	ОтборОрганизации.Добавить(ОрганизацияПустаяСсылка);
	
	Запрос.УстановитьПараметр("ОтборОрганизации", ОтборОрганизации);
	Запрос.УстановитьПараметр("ОрганизацияНастройкиОбмена", Организация);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Банк КАК Банк
	|ПОМЕСТИТЬ ВТ_БанкиСчетовОрганизации
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанкамиИсходящиеДокументы
	|ГДЕ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Организация = &ОрганизацияНастройкиОбмена
	|	И НастройкиОбменСБанкамиИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита)
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Недействительна
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДепозитыУсловияПродуктов.Банк КАК Банк,
	|	ДепозитыУсловияПродуктов.Организация КАК Организация,
	|	ДепозитыУсловияПродуктов.КлючПродукта КАК КлючПродукта,
	|	ДепозитыУсловияПродуктов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДепозитыУсловияПродуктов.СуммаМинимум КАК СуммаМинимум,
	|	ВЫБОР
	|		КОГДА ДепозитыУсловияПродуктов.СуммаМаксимум = 0
	|			ТОГДА 9999999999999.99
	|		ИНАЧЕ ДепозитыУсловияПродуктов.СуммаМаксимум
	|	КОНЕЦ КАК СуммаМаксимум,
	|	ДепозитыУсловияПродуктов.СрокМинимум КАК СрокМинимум,
	|	ВЫБОР
	|		КОГДА ДепозитыУсловияПродуктов.СрокМаксимум = 0
	|			ТОГДА 9999
	|		ИНАЧЕ ДепозитыУсловияПродуктов.СрокМаксимум
	|	КОНЕЦ КАК СрокМаксимум,
	|	ДепозитыУсловияПродуктов.Ставка КАК Ставка,
	|	ДепозитыУсловияПродуктов.Продвигаемый КАК Продвигаемый,
	|	ДепозитыПродукты.ИдентификаторПродукта КАК ИдентификаторПродукта,
	|	ДепозитыПродукты.Наименование КАК Наименование,
	|	ДепозитыПродукты.ВариантРасчета КАК ВариантРасчета,
	|	ДепозитыПродукты.Капитализация КАК Капитализация,
	|	ДепозитыПродукты.ПодробноеОписание КАК ПодробноеОписание,
	|	ДепозитыПродукты.НаименованиеБанка КАК НаименованиеБанка,
	|	ДепозитыПродукты.Валюта КАК Валюта,
	|	ДепозитыПродукты.ВыплатаПроцентов КАК ВыплатаПроцентов
	|ПОМЕСТИТЬ ВТ_ПродуктыИусловия
	|ИЗ
	|	ВТ_БанкиСчетовОрганизации КАК ВТ_БанкиСчетовОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДепозитныеПродуктыОбменСБанками КАК ДепозитыПродукты
	|		ПО ВТ_БанкиСчетовОрганизации.Банк = ДепозитыПродукты.Банк
	|			И (НЕ ДепозитыПродукты.ПредложенияОтсутствуют)
	|			И (НЕ ДепозитыПродукты.ОшибкаЗагрузки)
	|			И (ДепозитыПродукты.ДатаАктуальности = &ДатаАктуальности)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДепозитныхПродуктовОбменСБанками КАК ДепозитыУсловияПродуктов
	|		ПО (ДепозитыУсловияПродуктов.Банк = ДепозитыПродукты.Банк)
	|			И (ДепозитыУсловияПродуктов.КлючПродукта = ДепозитыПродукты.КлючПродукта)
	|ГДЕ
	|	ДепозитыУсловияПродуктов.Организация В(&ОтборОрганизации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПродуктыИусловия.Банк КАК Банк,
	|	ВТ_ПродуктыИусловия.НаименованиеБанка КАК НаименованиеБанка,
	|	ВТ_ПродуктыИусловия.КлючПродукта КАК КлючПродукта,
	|	ВТ_ПродуктыИусловия.ИдентификаторПродукта КАК ИдентификаторПродукта,
	|	ВТ_ПродуктыИусловия.Наименование КАК Наименование,
	|	ВТ_ПродуктыИусловия.ПодробноеОписание КАК ПодробноеОписание,
	|	ВТ_ПродуктыИусловия.Валюта КАК Валюта,
	|	МИНИМУМ(ВТ_ПродуктыИусловия.СуммаМинимум) КАК СуммаМинимум,
	|	МАКСИМУМ(ВТ_ПродуктыИусловия.СуммаМаксимум) КАК СуммаМаксимум,
	|	МИНИМУМ(ВТ_ПродуктыИусловия.СрокМинимум) КАК СрокМинимум,
	|	МАКСИМУМ(ВТ_ПродуктыИусловия.СрокМаксимум) КАК СрокМаксимум,
	|	МАКСИМУМ(ВТ_ПродуктыИусловия.Ставка) КАК СтавкаМаксимум,
	|	ВТ_ПродуктыИусловия.ВариантРасчета КАК ВариантРасчета,
	|	ВТ_ПродуктыИусловия.Капитализация КАК Капитализация,
	|	ВТ_ПродуктыИусловия.ВыплатаПроцентов КАК ВыплатаПроцентов
	|ИЗ
	|	ВТ_ПродуктыИусловия КАК ВТ_ПродуктыИусловия
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПродуктыИусловия.Банк,
	|	ВТ_ПродуктыИусловия.КлючПродукта,
	|	ВТ_ПродуктыИусловия.ИдентификаторПродукта,
	|	ВТ_ПродуктыИусловия.Наименование,
	|	ВТ_ПродуктыИусловия.ВариантРасчета,
	|	ВТ_ПродуктыИусловия.Капитализация,
	|	ВТ_ПродуктыИусловия.Валюта,
	|	ВТ_ПродуктыИусловия.ВыплатаПроцентов,
	|	ВТ_ПродуктыИусловия.ПодробноеОписание,
	|	ВТ_ПродуктыИусловия.НаименованиеБанка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПродуктыИусловия.КлючПродукта КАК КлючПродукта,
	|	ВТ_ПродуктыИусловия.ИдентификаторСтроки КАК ИдентификаторУсловия,
	|	ВТ_ПродуктыИусловия.СуммаМинимум КАК СуммаМинимум,
	|	ВТ_ПродуктыИусловия.СуммаМаксимум КАК СуммаМаксимум,
	|	ВТ_ПродуктыИусловия.СрокМинимум КАК СрокМинимум,
	|	ВТ_ПродуктыИусловия.СрокМаксимум КАК СрокМаксимум,
	|	ВТ_ПродуктыИусловия.Ставка КАК Ставка,
	|	ВТ_ПродуктыИусловия.Продвигаемый КАК Продвигаемый
	|ИЗ
	|	ВТ_ПродуктыИусловия КАК ВТ_ПродуктыИусловия
	|
	|УПОРЯДОЧИТЬ ПО
	|	СрокМинимум,
	|	СуммаМинимум
	|ИТОГИ ПО
	|	КлючПродукта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДепозитыПродукты.Банк КАК Банк,
	|	ДепозитыПродукты.ПодробноеОписание КАК Ошибка
	|ИЗ
	|	ВТ_БанкиСчетовОрганизации КАК ВТ_БанкиСчетовОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДепозитныеПродуктыОбменСБанками КАК ДепозитыПродукты
	|		ПО ВТ_БанкиСчетовОрганизации.Банк = ДепозитыПродукты.Банк
	|			И (ДепозитыПродукты.ОшибкаЗагрузки)
	|			И (ДепозитыПродукты.ДатаАктуальности = &ДатаАктуальности)";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РезультатЗапросаПродукты = РезультатЗапроса[2];
	РезультатЗапросаУсловия = РезультатЗапроса[3];
	РезультатЗапросаОшибкиПолучения = РезультатЗапроса[4];
	
	Если РезультатЗапросаПродукты.Пустой() Или РезультатЗапросаУсловия.Пустой() Тогда
		
		ТаблицаОшибок = РезультатЗапросаОшибкиПолучения.Выгрузить();
		ТекстОшибки = ПроверитьОшибкиПолученияПредложенийИСформироватьСтрокуОшибки(ТаблицаОшибок, ДатаАктуальности);
		Результат.ОшибкаПолученияПредложений = ТекстОшибки;
		Возврат Результат;
		
	КонецЕсли;
	
	ТаблицаПродуктов = РезультатЗапросаПродукты.Выгрузить();
	
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита("Срок");
	ТаблицаПродуктов.Колонки.Добавить("Срок", ОписаниеТипаПараметра);
	
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита("Сумма");
	ТаблицаПродуктов.Колонки.Добавить("Сумма", ОписаниеТипаПараметра);
	
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита("Ставка");
	ТаблицаПродуктов.Колонки.Добавить("Ставка", ОписаниеТипаПараметра);
	
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита("Доход");
	ТаблицаПродуктов.Колонки.Добавить("Доход", ОписаниеТипаПараметра);
	
	ТаблицаПродуктов.Колонки.Добавить("РасчетПриОформлении", Новый ОписаниеТипов("Булево"));
	
	ТаблицаПродуктов.Колонки.Добавить("Продвигаемый", Новый ОписаниеТипов("Булево"));
	
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита("ИдентификаторУсловия");
	ТаблицаПродуктов.Колонки.Добавить("ИдентификаторУсловия", ИмяПараметра);
	
	ВыборкаУсловияПродуктов = РезультатЗапросаУсловия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	УсловияПродуктов = Новый Соответствие();
	
	Пока ВыборкаУсловияПродуктов.Следующий() Цикл
	
		Условия = Новый Массив;
		ВыборкаУсловия = ВыборкаУсловияПродуктов.Выбрать();
		
		Пока ВыборкаУсловия.Следующий() Цикл
			
			ПараметрыУсловия = НовыеПараметрыУсловияДепозитногоПродукта();
			ЗаполнитьЗначенияСвойств(ПараметрыУсловия, ВыборкаУсловия);
			Условия.Добавить(ПараметрыУсловия);
			
		КонецЦикла;
		
		УсловияПродуктов.Вставить(ВыборкаУсловияПродуктов.КлючПродукта, Условия);
	
	КонецЦикла;
	
	Результат.Продукты = ТаблицаПродуктов;
	Результат.Условия = УсловияПродуктов;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеПредложенийБанков(АдресХранилищаПредложенийБанков, ПараметрыРасчетаПредложений) Экспорт
	
	ПродуктыБанков = ПолучитьИзВременногоХранилища(АдресХранилищаПредложенийБанков);
	ПредложенияБанков = СформироватьПредложенияБанков(ПродуктыБанков, ПараметрыРасчетаПредложений);
	Возврат ПредложенияБанков;
	
КонецФункции

// Проверяет выполнение условий депозитных продуктов по сроку и сумме
// рассчитывает и заполняет колонки доходности в таблице "Продукты".
//
// Параметры:
//  ПредложенияБанков - Структура - см. НовыеДанныеПредложенийБанка().
//  ДатаРасчета - Дата - дата расчета предложений.
//  Сумма - Число - сумма депозита.
//  Срок - Число - срок депозита в днях.
// 
Процедура ПроверитьПродуктыИРассчитатьДоходность(ПредложенияБанков, ДатаРасчета, Сумма = 0, Срок = 0) Экспорт
	
	Продукты = ПредложенияБанков.Продукты;
	
	Если Продукты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Условия = ПредложенияБанков.Условия;
	ОдинБанк = Истина;
	
	ТаблицаУсловий = Новый ТаблицаЗначений();
	ТаблицаУсловий.Колонки.Добавить("КлючПродукта");
	ТаблицаУсловий.Колонки.Добавить("ИдентификаторУсловия");
	ТаблицаУсловий.Колонки.Добавить("СрокМинимум");
	ТаблицаУсловий.Колонки.Добавить("СуммаМинимум");
	ТаблицаУсловий.Колонки.Добавить("Ставка");
	ТаблицаУсловий.Колонки.Добавить("Доход");
	ТаблицаУсловий.Колонки.Добавить("Продвигаемый");
	
	Банк = Продукты[0].Банк;
	
	РасчетныйСрок = 1;
	
	Если Не Срок = 0 Тогда
		РасчетныйСрок = Срок;
	КонецЕсли;
	
	Для каждого ТекущийПродукт Из Продукты Цикл
		
		РасчетПриОформлении = Ложь;
		
		Если ТекущийПродукт.ВариантРасчета <> Перечисления.МетодыРасчетаПроцентовДепозитовОбменСБанками.ПростыеПроценты
			Или ТекущийПродукт.Капитализация Тогда
			РасчетПриОформлении = Истина;
		КонецЕсли;
		
		ТекущийПродукт.РасчетПриОформлении = РасчетПриОформлении;
		
		Если ОдинБанк И Банк <> ТекущийПродукт.Банк Тогда
			ОдинБанк = Ложь;
		КонецЕсли;
		
		ТекущийПродукт.Сумма = Сумма;
		
		Если Сумма = 0 Тогда
			ТекущийПродукт.Сумма = ТекущийПродукт.СуммаМинимум;
		КонецЕсли;
		
		ТаблицаУсловий.Очистить();
		
		// Сначала проверяем условия продукта по нижнему и верхнему пределу.
		// Если параметры "Срок" и "Сумма" проходят проверку, рассчитаем доходность каждого условия.
		
		Если ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ВыполняютсяУсловияПродукта(ТекущийПродукт, Сумма, Срок) Тогда
			
			УсловияПродукта = Условия[ТекущийПродукт.КлючПродукта];
			
			Для каждого ТекущееУсловие Из УсловияПродукта Цикл
				
				Если ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ВыполняютсяУсловияПродукта(ТекущееУсловие, Сумма, Срок) Тогда
					
					ТекущаяСтрокаТаблицы = ТаблицаУсловий.Добавить();
					ТекущаяСтрокаТаблицы.КлючПродукта = ТекущийПродукт.КлючПродукта;
					ТекущаяСтрокаТаблицы.СрокМинимум = ТекущееУсловие.СрокМинимум;
					ТекущаяСтрокаТаблицы.Ставка = ТекущееУсловие.Ставка;
					Если Не РасчетПриОформлении Тогда
						ТекущаяСтрокаТаблицы.Доход = ОбменСбанкамиДепозитыСлужебныйКлиентСервер.РассчитатьДоход(ДатаРасчета,
							ТекущийПродукт.Сумма, ТекущееУсловие.Ставка, РасчетныйСрок);
					Иначе
						ТекущаяСтрокаТаблицы.Доход = 0;
					КонецЕсли;
					ТекущаяСтрокаТаблицы.Продвигаемый = ТекущееУсловие.Продвигаемый;
					ТекущаяСтрокаТаблицы.СуммаМинимум = ТекущееУсловие.СуммаМинимум;
					ТекущаяСтрокаТаблицы.ИдентификаторУсловия = ТекущееУсловие.ИдентификаторУсловия;
					
				КонецЕсли;
				
			КонецЦикла;
		Иначе
			ТекущийПродукт.Доход = 0;
			ТекущийПродукт.ИдентификаторУсловия = "";
		КонецЕсли;
		
		Если ТаблицаУсловий.Количество() > 0 Тогда
			
			ТаблицаУсловий.Сортировать("Продвигаемый Убыв, Доход Убыв");
			ВыбранноеУсловие = ТаблицаУсловий[0];
			
			// Заполним параметры продукта по выбранному условию
			ТекущийПродукт.Срок = Срок;
			ТекущийПродукт.Ставка = ВыбранноеУсловие.Ставка;
			ТекущийПродукт.Доход = ВыбранноеУсловие.Доход;
			ТекущийПродукт.Продвигаемый = ВыбранноеУсловие.Продвигаемый;
			ТекущийПродукт.ИдентификаторУсловия = ВыбранноеУсловие.ИдентификаторУсловия;
			Если Сумма = 0 Тогда
				ТекущийПродукт.Сумма = ВыбранноеУсловие.СуммаМинимум;
			КонецЕсли;
			Если Срок = 0 Тогда
				ТекущийПродукт.Срок = ВыбранноеУсловие.СрокМинимум;
			КонецЕсли;
			Если Не РасчетПриОформлении И (Сумма = 0 Или Срок = 0) Тогда
				ТекущийПродукт.Доход = ОбменСбанкамиДепозитыСлужебныйКлиентСервер.РассчитатьДоход(ДатаРасчета,
					ТекущийПродукт.Сумма, ТекущийПродукт.Ставка, ТекущийПродукт.Срок);
			КонецЕсли;
		Иначе
			ТекущийПродукт.ИдентификаторУсловия = "";
		КонецЕсли;
		
	КонецЦикла;
		
	Продукты.Сортировать("Продвигаемый Убыв, Доход Убыв");
	
	ПредложенияБанков.ОдинБанк = ОдинБанк;
	
КонецПроцедуры

Функция ПолучитьУсловияПродукта(АдресХранилища, КлючПродукта) Экспорт
	
	ДанныеПредложений = ПолучитьИзВременногоХранилища(АдресХранилища);
	Результат = Неопределено;
	Если Не ДанныеПредложений = Неопределено Тогда
		Результат = ДанныеПредложений.Условия.Получить(КлючПродукта);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Функция ПроверитьПродуктыИСформироватьДанныеПредложенийБанков(Организация, ИдентификаторФормы, ПараметрыРасчетаПредложений) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПредложенияБанков", Неопределено);
	Результат.Вставить("АдресХранилища", "");
	Результат.Вставить("ОшибкаПолученияПредложений", "");
	
	ПродуктыИУсловия = ДанныеДепозитныхПродуктов(Организация);
	
	Продукты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПродуктыИУсловия, "Продукты", Неопределено);
	Условия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПродуктыИУсловия, "Условия", Неопределено);
	Результат.ОшибкаПолученияПредложений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПродуктыИУсловия, "ОшибкаПолученияПредложений", "");
	
	Если Не ЗначениеЗаполнено(Продукты) Или Не ЗначениеЗаполнено(Условия) Тогда
		Возврат Результат;
	КонецЕсли;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ПродуктыИУсловия, ИдентификаторФормы);
	
	ПредложенияБанков = СформироватьПредложенияБанков(ПродуктыИУсловия, ПараметрыРасчетаПредложений);
	
	Результат.ПредложенияБанков = ПредложенияБанков;
	Результат.АдресХранилища = АдресХранилища;
	
	Возврат Результат;
	
КонецФункции

// Функция-конструктор параметров предложений банков
// для функции ПроверитьПродуктыИРассчитатьДоходность
//
// Возвращаемое значение:
//  Структура:
//   * Продукты - ТаблицаЗначений: см. ДанныеДепозитныхПродуктов.
//   * Условия - Соответствие из КлючИЗначение:
//      ** Ключ - Строка - уникальный идентификатор депозитного продукта.
//      ** Значение - Массив из Структура: условия депозитного продукта см. НовыеПараметрыУсловияДепозитногоПродукта.
//   * ОдинБанк - Булево - если Истина, все предложения от одного банка.
//   * ОшибкаПолученияПредложений - Строка - содержит текст ошибки, если не удалось получить предложения ни от одного банка.
// 
Функция НовыеДанныеПредложенийБанка() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Продукты", Новый ТаблицаЗначений);
	Результат.Вставить("Условия", Новый Соответствие);
	Результат.Вставить("ОдинБанк", Истина);
	Результат.Вставить("ОшибкаПолученияПредложений", "");
	
	Возврат Результат;
	
КонецФункции

Процедура ПрочитатьИзвещениеОСостоянииДепозита(
	НастройкаОбмена,
	ПакетОбменСБанками,
	ДвоичныеДанныеФайла,
	Document,
	НовоеСообщениеОбмена,
	ДанныеВозврата) Экспорт
	
	АктуальныеВидыЭД = ОбменСБанкамиСлужебныйПовтИсп.АктуальныеВидыЭД();
	Если АктуальныеВидыЭД.Найти(Перечисления.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита) = Неопределено Тогда
		// Если в конфигурации не поддерживается заявка на депозит, не нужно загружать извещение о состоянии депозита
		Возврат;
	КонецЕсли;
	
	DepositStatusNotice = ОбъектXDTOИзвещениеОСостоянииДепозита(ДвоичныеДанныеФайла, ПакетОбменСБанками);
	
	ОбъектВладелец = Неопределено;
	ИдентификаторЭДВладельца = ЗначениеСвойстваXDTO(DepositStatusNotice, "ExtID");
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОбмена, "Организация, Банк");
	ИдентификаторЗапроса = ЗначениеСвойстваXDTO(DepositStatusNotice, "ExtIDStatusRequest");
	
	ЭтоОтветНаЗапросСостояния = ЗначениеЗаполнено(ИдентификаторЗапроса);
	
	СообщениеЗапрос = Неопределено;
	Если ЭтоОтветНаЗапросСостояния Тогда
		СообщениеЗапрос = ОбменСБанкамиСлужебный.СообщениеОбменаПоИдентификатору(НастройкаОбмена, ИдентификаторЗапроса, 
			Перечисления.ВидыЭДОбменСБанками.ЗапросСостоянияДепозита);
	КонецЕсли;
	
	СообщениеРодитель = Неопределено;
	Если ЗначениеЗаполнено(ИдентификаторЭДВладельца) Тогда
		СообщениеРодитель = ОбменСБанкамиСлужебный.СообщениеОбменаПоИдентификатору(НастройкаОбмена, 
			ИдентификаторЭДВладельца);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СообщениеРодитель) Тогда
		Возврат;
	КонецЕсли;
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла);

	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("СообщениеРодитель", СообщениеРодитель);
	СтруктураРеквизитов.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Исполнен);
	СтруктураРеквизитов.Вставить("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ИзвещениеОСостоянииДепозита);
	СтруктураРеквизитов.Вставить("НастройкаОбмена", НастройкаОбмена);
	СтруктураРеквизитов.Вставить("Направление", Перечисления.НаправленияЭДО.Входящий);
	СтруктураРеквизитов.Вставить("Расширение", "xml");
	СтруктураРеквизитов.Вставить("АдресФайлаВоВременномХранилище", АдресВХранилище);
	ВнешнийИдентификатор = ЗначениеСвойстваXDTO(DepositStatusNotice, "id");
	СтруктураРеквизитов.Вставить("ВнешнийИдентификатор", ВнешнийИдентификатор);
	
	СсылкаНаОбъект = ОбменСБанкамиСлужебный.ОбъектПривязки(СтруктураРеквизитов.СообщениеРодитель);
	
	Если Не СсылкаНаОбъект = Неопределено Тогда
		СтруктураРеквизитов.Вставить("СсылкаНаОбъект", СсылкаНаОбъект);
	КонецЕсли;
	
	СтруктураРеквизитов.Вставить("ФорматФайла", "xml");
	
	ОбменСБанкамиСлужебный.СохранитьСообщениеОбмена(СтруктураРеквизитов, НовоеСообщениеОбмена);
	
	Если Не ЗначениеЗаполнено(НовоеСообщениеОбмена) Тогда
		Возврат;
	КонецЕсли;

	ОбменСБанкамиСлужебный.ДобавитьСообщениеОбменаВПакет(ПакетОбменСБанками, НовоеСообщениеОбмена);

	РеквизитыРодителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СтруктураРеквизитов.СообщениеРодитель, "Статус, ДатаИзмененияВнешнегоСтатуса, ВидЭД");

	Если ЭтоОтветНаЗапросСостояния И ЗначениеЗаполнено(СообщениеЗапрос)
		И Не СообщениеЗапрос = СообщениеРодитель Тогда
		РеквизитыЗапроса = Новый Структура("Статус", Перечисления.СтатусыОбменСБанками.ПолученоИзвещение);
		ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(СообщениеЗапрос, РеквизитыЗапроса);
	КонецЕсли;
	
	ДанныеДепозита = ЗначениеСвойстваXDTO(DepositStatusNotice, "DepositData");
		
	НовыйСтатус = Перечисления.СтатусыОбменСБанками.Исполнен;
	РеквизитыДляИзменения = Новый Структура;
	ДатаИзмененияСтатуса = ЗначениеСвойстваXDTO(DepositStatusNotice, "creationDate");
	Если ЗначениеЗаполнено(НовыйСтатус)
			И РеквизитыРодителя.ДатаИзмененияВнешнегоСтатуса <= ДатаИзмененияСтатуса Тогда
		РеквизитыДляИзменения.Вставить("ДатаИзмененияВнешнегоСтатуса", ДатаИзмененияСтатуса);
		РеквизитыДляИзменения.Вставить("Статус", НовыйСтатус);
		ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(СообщениеРодитель, РеквизитыДляИзменения);
	КонецЕсли;
	
	РеквизитыЗаявкиДляИзменения = НовыеРеквизитыЗаявкиНаДепозитДляИзменения();
	РеквизитыЗаявкиДляИзменения.КомментарийБанка = ЗначениеСвойстваXDTO(
		DepositStatusNotice, "MoreInfo", "");
	РеквизитыЗаявкиДляИзменения.НомерДоговора = ЗначениеСвойстваXDTO(ДанныеДепозита,
		"ContractNo", "");
	РеквизитыЗаявкиДляИзменения.ДатаОткрытия = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "OpeningDate");
	РеквизитыЗаявкиДляИзменения.НаименованиеПродукта = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "ProductName", "");
	РеквизитыЗаявкиДляИзменения.ИдентификаторПродукта = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "ProductID", "");
	РеквизитыЗаявкиДляИзменения.СпособВыплатыПроцентов = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "InterestPayment", "01");
	РеквизитыЗаявкиДляИзменения.КодВалюты = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "CurrencyCode", "643");
	РеквизитыЗаявкиДляИзменения.Капитализация = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "Capitalization", Ложь, "Булево");
	РеквизитыЗаявкиДляИзменения.ОписаниеПродукта = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "Description", "");
	РеквизитыЗаявкиДляИзменения.СуммаДепозита = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "DepositSum");
	РеквизитыЗаявкиДляИзменения.Ставка = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "Rate");
	РеквизитыЗаявкиДляИзменения.СрокДепозита = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "PeriodDay");
	РеквизитыЗаявкиДляИзменения.ДатаЗакрытия = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "ClosingDate");
	РеквизитыЗаявкиДляИзменения.ДатаВозвратаСредств = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "ReturnDate");
	РеквизитыЗаявкиДляИзменения.НомерСчета = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "Account", "");
	РеквизитыЗаявкиДляИзменения.НомерСчетаДепозита = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "DepositAccount", "");
	
	СписокПлатежей = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "InterestPaymentSchedule.Payment", Новый Массив(), , Истина);
		
	ГрафикПлатежей = Новый Массив;
		
	Для каждого ТекущаяСтрокаСписка Из СписокПлатежей Цикл
		
		ТекущийПлатеж = Новый Структура;
		ТекущийПлатеж.Вставить("ДатаВыплаты", ЗначениеСвойстваXDTO(
			ТекущаяСтрокаСписка, "Date"));
		ТекущийПлатеж.Вставить("СуммаДепозита", ЗначениеСвойстваXDTO(
			ТекущаяСтрокаСписка, "DepositSum"));
		ТекущийПлатеж.Вставить("СуммаВыплаты", ЗначениеСвойстваXDTO(
			ТекущаяСтрокаСписка, "InterestSum"));
		ГрафикПлатежей.Добавить(ТекущийПлатеж);
		
	КонецЦикла;
	
	РеквизитыЗаявкиДляИзменения.ГрафикВыплатыПроцентов = ГрафикПлатежей;
	РеквизитыЗаявкиДляИзменения.Подтвержден = Истина;
	
	Вложения = ЗначениеСвойстваXDTO(DepositStatusNotice, "Attachments.Attachment", Новый Массив, , Истина);
	
	Если Вложения.Количество() Тогда
		РеквизитыЗаявкиДляИзменения.ЕстьВложение = Истина;
	КонецЕсли;
	
	ДокументУчета = ОбменСБанкамиСлужебныйВызовСервера.ДокументУчета(СообщениеРодитель);
	
	Если ЗначениеЗаполнено(ДокументУчета) Тогда
		
		СостояниеЭДО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеРодитель, "Состояние");
		ДанныеВозврата.ПараметрОповещения.Вставить(ДокументУчета, СостояниеЭДО);
		ДобавитьПрисоединенныеФайлыЗаявкиНаДепозит(ДокументУчета, Вложения);
		Документы.ЗаявкаНаДепозитОбменСБанками.ЗаполнитьДанныеЗаявкиПоСостоянию(ДокументУчета, 
			РеквизитыЗаявкиДляИзменения);
		ОбменСБанкамиПереопределяемый.ПриПолученииИзвещенияОСостоянииДепозита(ДокументУчета,
			РеквизитыЗаявкиДляИзменения);
			
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьПредложенияБанкаПоДепозитам(Банк) Экспорт
	
	Если Не ЗначениеЗаполнено(Банк) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировкиПродукты = БлокировкаДанных.Добавить("РегистрСведений.ДепозитныеПродуктыОбменСБанками");
	ЭлементБлокировкиПродукты.УстановитьЗначение("Банк", Банк);
	ЭлементБлокировкиУсловия = БлокировкаДанных.Добавить("РегистрСведений.УсловияДепозитныхПродуктовОбменСБанками");
	ЭлементБлокировкиПродукты.УстановитьЗначение("Банк", Банк);
	
	НачатьТранзакцию();
	
	Попытка
	
		БлокировкаДанных.Заблокировать();
		НаборЗаписейРегистраПродукты = РегистрыСведений.ДепозитныеПродуктыОбменСБанками.СоздатьНаборЗаписей();
		НаборЗаписейРегистраУсловия = РегистрыСведений.УсловияДепозитныхПродуктовОбменСБанками.СоздатьНаборЗаписей();
		
		НаборЗаписейРегистраПродукты.Отбор.Банк.Установить(Банк);
		НаборЗаписейРегистраУсловия.Отбор.Банк.Установить(Банк);
		НаборЗаписейРегистраУсловия.Записать(Истина);
		НаборЗаписейРегистраПродукты.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ПодробнаяИнформация = НСтр("ru = 'Ошибка при удалении данных о депозитных продуктах для банка %1
									|Подробное представление ошибки:
									|%2'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ПодробнаяИнформация = СтрШаблон(ПодробнаяИнформация, Банк, ПодробноеПредставлениеОшибки);
		ВидОперации = НСтр("ru = 'Удаление данных о предложениях банка по депозитам'");
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , Банк);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаявкаНаДепозит

// Формирует данные для электронного документа Заявка на депозит
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка.ЗаявкаНаДепозитОбменСБанками - ссылка на документ информационной базы,
//    на основании которого формируется электронный документ;
//  НастройкиОбменаЭД - Структура - настройки обмена электронными документами;
//  СтруктураПараметров - Структура - данные, возвращаемые процедурой.
//
Процедура СформироватьЗаявкуНаДепозит(СсылкаНаОбъект, НастройкиОбменаЭД, СтруктураПараметров) Экспорт
	
	СтруктураЭД = Новый Структура;
	СтруктураЭД.Вставить("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита);
	СтруктураЭД.Вставить("Направление", Перечисления.НаправленияЭДО.Исходящий);
	СтруктураЭД.Вставить("СсылкаНаОбъект", СсылкаНаОбъект);
	
	ПечатныйНомерДокумента = "";
	ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект, ПечатныйНомерДокумента);
	
	РеквизитыЗаявки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "Ссылка, Дата, Идентификатор,
		|СчетОрганизации, Ставка, ИдентификаторПродукта, СрокДепозита, СуммаДепозита, ДатаОткрытия, ДатаЗакрытия");
	
	СтруктураЭД.Вставить("НомерДокументаОтправителя", ПечатныйНомерДокумента);
	СтруктураЭД.Вставить("ДатаДокументаОтправителя", РеквизитыЗаявки.Дата);
	СтруктураЭД.Вставить("ДатаНачала", РеквизитыЗаявки.ДатаОткрытия);
	СтруктураЭД.Вставить("ДатаОкончания", РеквизитыЗаявки.ДатаЗакрытия);
	СтруктураЭД.Вставить("НастройкаОбмена", НастройкиОбменаЭД.НастройкаОбмена);
	СтруктураЭД.Вставить("ПрограммаБанка", НастройкиОбменаЭД.ПрограммаБанка);
	СтруктураЭД.Вставить("Подписывать", НастройкиОбменаЭД.Подписывать);
	СтруктураЭД.Вставить("СуммаДокумента", РеквизитыЗаявки.СуммаДепозита);
	РеквизитыСчета = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыБанковскогоСчетаОрганизации(РеквизитыЗаявки.СчетОрганизации);
	СчетОрганизацииСтрокой = "";
	Если ЗначениеЗаполнено(РеквизитыСчета.НомерСчета) Тогда
		СчетОрганизацииСтрокой = РеквизитыСчета.НомерСчета;
	КонецЕсли;
	СтруктураЭД.Вставить("НомерСчета", СчетОрганизацииСтрокой);
	СтруктураЭД.Вставить("ФорматФайла", "xml");
	
	АдресФайлаВоВременномХранилище = Неопределено;
	
	Идентификатор = РеквизитыЗаявки.Идентификатор;
	
	СформироватьДвоичныеДанныеЗаявкиНаДепозит(РеквизитыЗаявки, НастройкиОбменаЭД.НастройкаОбмена,
		АдресФайлаВоВременномХранилище);
	
	СтруктураЭД.Вставить("Идентификатор", Идентификатор);
	СтруктураЭД.Вставить("Основной", Истина);
	СтруктураЭД.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Сформирован);
	СтруктураЭД.Вставить("АдресФайлаВоВременномХранилище", АдресФайлаВоВременномХранилище);
	СтруктураПараметров.Вставить("СтруктураЭД", СтруктураЭД);
	СтруктураПараметров.Вставить("Организация", НастройкиОбменаЭД.Организация);
	СтруктураПараметров.Вставить("ВидЭД", СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("Номер", СтруктураЭД.НомерДокументаОтправителя);
	
КонецПроцедуры

// Формирует запрос состояния электронного документа или запрос состояния депозита в зависимости
// от переданного флага ЗаявкаИсполнена:
//    - Если флаг ЗаявкаИсполнена = Истина, формируется "Запрос состояния депозита".
//    - Если флаг ЗаявкаИсполнена = Ложь, формируется "Запрос состояния электронного документа".
//
// Параметры:
//  СсылкаЭД - ДокументСсылка.СообщениеОбменСБанками - Ссылка на ЭД заявки на депозит.
//  ЗаявкаИсполнена - Булево - Если Истина, заявка на депозит имеет статус Исполнена.
//
// Возвращаемое значение:
//  ЗапросСостоянияЭД - ДокументСсылка.СообщениеОбменСБанками - ссылка на созданный документ
//
Функция СформироватьЗапросСостоянияЗаявкиНаДепозит(СсылкаЭД, ЗаявкаИсполнена = Ложь) Экспорт
	
	НастройкаОбмена = СсылкаЭД.НастройкаОбмена;
	ТекстОшибки = "";
	ВерсияПрограммы = ОбменСБанкамиСлужебныйПовтИсп.ВерсияПрограммыКлиентаДляБанка();
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СсылкаЭД.НастройкаОбмена, "Организация, ИдентификаторОрганизации, Банк, Недействительна, ВерсияФормата");
	Путь = "StatusRequest";
	ВидЭД = Перечисления.ВидыЭДОбменСБанками.ЗапросОСостоянииЭД;
	
	Если ЗаявкаИсполнена Тогда
		
		Путь = "DepositStatusRequest";
		ВидЭД = Перечисления.ВидыЭДОбменСБанками.ЗапросСостоянияДепозита;
		
	КонецЕсли;
	
	Если РеквизитыНастройкиОбмена.Недействительна Тогда
		ШаблонСообщения = НСтр("ru = 'Настройка обмена с банком %1 недействительна'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, НастройкаОбмена);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыНастройкиОбмена.ВерсияФормата) Тогда
		ВерсияФормата = РеквизитыНастройкиОбмена.ВерсияФормата;
	Иначе
		ВерсияФормата = ОбменСБанкамиКлиентСервер.АктуальнаяВерсияФорматаАсинхронногоОбмена();
	КонецЕсли;
	
	ПространствоИмен = ОбменСБанкамиСлужебный.ПространствоИменАсинхронногоОбмена(ВерсияФормата);

	Фабрика = ОбменСБанкамиСлужебныйПовтИсп.ФабрикаAsyncXDTO(ВерсияФормата);
	
	ОтправительНаименование = ОбщегоНазначенияБЭД.СокращенноеНаименованиеОрганизации(
		РеквизитыНастройкиОбмена.Организация);
	РеквизитыОрганизации = ОбщегоНазначенияБЭД.ДанныеЮрФизЛица(РеквизитыНастройкиОбмена.Организация);
	РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыНастройкиОбмена.Банк, "Код, Наименование");
	
	ИдентификаторЗаявки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаЭД, "Идентификатор");
	Попытка
		
		УникальныйИдЭД = Новый УникальныйИдентификатор;

		ЗапросСостояния = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, Путь, ПространствоИмен);
		ЗаполнитьСвойствоXDTO(ЗапросСостояния, "id", Строка(УникальныйИдЭД), Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(ЗапросСостояния, "ExtID", ИдентификаторЗаявки, Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(ЗапросСостояния, "formatVersion", ВерсияФормата, Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(
			ЗапросСостояния, "creationDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(ЗапросСостояния, "userAgent", ВерсияПрограммы, , ТекстОшибки);
		Отправитель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "CustomerPartyType", ПространствоИмен);
		ЗаполнитьСвойствоXDTO(
			Отправитель, "id", РеквизитыНастройкиОбмена.ИдентификаторОрганизации, Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(Отправитель, "name", ОтправительНаименование, Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(Отправитель, "inn", РеквизитыОрганизации.ИНН, Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(Отправитель, "kpp", РеквизитыОрганизации.КПП, , ТекстОшибки);
		ЗаполнитьСвойствоXDTO(ЗапросСостояния, "Sender", Отправитель, Истина, ТекстОшибки);
		
		Получатель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "BankPartyType", ПространствоИмен);
		ЗаполнитьСвойствоXDTO(Получатель, "bic", РеквизитыБанка.Код, Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(Получатель, "name", РеквизитыБанка.Наименование, Истина, ТекстОшибки);
		ЗаполнитьСвойствоXDTO(ЗапросСостояния, "Recipient", Получатель, Истина, ТекстОшибки);
		ЗапросСостояния.Проверить();
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ТекстСообщения = ТекстОшибки;
			Операция = НСтр("ru = 'Формирование электронного документа'");
			ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
				ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками,
				ТекстОшибки, ТекстСообщения, СсылкаЭД);
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		ДвоичныеДанные = ОбменСБанкамиСлужебный.ДвоичныеДанныеИзXDTO(Фабрика, ЗапросСостояния, Ложь);
		
	Исключение
		ТекстСообщения = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Операция = НСтр("ru = 'Формирование электронного документа'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками,
			ПодробноеПредставлениеОшибки, ТекстСообщения, СсылкаЭД);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Расширение", "xml");
	Реквизиты.Вставить("АдресФайлаВоВременномХранилище", АдресФайла);
	Реквизиты.Вставить("Идентификатор", Строка(УникальныйИдЭД));
	Реквизиты.Вставить("Направление", Перечисления.НаправленияЭДО.Исходящий);
	Реквизиты.Вставить("ВидЭД", ВидЭД);
	Реквизиты.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Сформирован);
	Реквизиты.Вставить("Организация", РеквизитыНастройкиОбмена.Организация);
	Реквизиты.Вставить("Банк", РеквизитыНастройкиОбмена.Банк);
	Реквизиты.Вставить("СообщениеРодитель", СсылкаЭД);
	Реквизиты.Вставить("НастройкаОбмена", НастройкаОбмена);
	Реквизиты.Вставить("ФорматФайла", "xml");
	
	ЗапросСостоянияЭД = Неопределено;
	ОбменСБанкамиСлужебный.СохранитьСообщениеОбмена(Реквизиты, ЗапросСостоянияЭД);
	
	Возврат ЗапросСостоянияЭД.Ссылка;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция СпособыВыплатыПроцентовОбменСБанкамиДепозиты(КодСпособа) Экспорт
	
	Результат = Перечисления.СпособыВыплатыПроцентовДепозитовОбменСБанками.ПустаяСсылка();
	
	Если КодСпособа = "01" Тогда
		Результат = Перечисления.СпособыВыплатыПроцентовДепозитовОбменСБанками.ВКонцеСрока;
	ИначеЕсли КодСпособа = "02" Тогда
		Результат = Перечисления.СпособыВыплатыПроцентовДепозитовОбменСБанками.Ежедневно;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Функция НастройкаПоддерживаетОбменДепозитами(НастройкаОбмена) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанкамиИсходящиеДокументы
	|ГДЕ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка = &НастройкаОбмена
	|	И НастройкиОбменСБанкамиИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита)
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Недействительна
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Определяет, есть ли в программе настройки обмена с поддержкой депозитов
// 
// Возвращаемое значение:
//  Булево - если Истина, то есть поддержка депозитов.
//
Функция ЕстьПоддержкаДепозитов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанкамиИсходящиеДокументы
	|ГДЕ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита)
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.ПометкаУдаления
	|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Недействительна";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Проверяет доступность для банка обмена по депозитам.
//  см. ОбменСБанкамиСлужебныйВызовСервера.ПоставляемыеНастройкиОбмена.
//
// Параметры:
//  Банк - ОпределяемыйТип.БанкОбменаСБанками - ссылка на банк.
// 
// Возвращаемое значение:
//  Булево - если Истина, то есть поддержка депозитов.
//
Функция БанкПоддерживаетОбменДепозитами(Банк) Экспорт
	
	Результат = Ложь;
	
	ПоставляемыеНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПоставляемыеНастройкиОбмена(Банк);
	Если ПоставляемыеНастройкиОбмена = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	МассивПроектов = СтрРазделить(ПоставляемыеНастройкиОбмена.Проект, ",", Ложь);
	ПроектОбменДепозитами = "5";
	
	Если МассивПроектов.Найти(ПроектОбменДепозитами) <> Неопределено Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗапросРасчетаДепозитаВБанке

// Формирует данные для запроса расчета депозита в банке.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - ссылка на организацию.
//  Банк - ОпределяемыйТип.БанкОбменСБанками - ссылка на банк.
//  ПараметрыДепозита - Структура: см. ОбменСБанкамиДепозитыСлужебныйКлиентСервер.НовыеПараметрыРасчетаДепозитаВБанке.
//
// Возвращаемое значение:
//  Структура:
//   * ДанныеСообщенияОбмена - ДвоичныеДанные - данные сформированного запроса расчета депозита.
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком.
//   * РеквизитыНастройкиОбмена - Структура:
//      ** АдресСервера - Строка - адрес сервера банка.
//      ** ИдентификаторОрганизации - идентификатор клиента банка.
//      ** ВерсияФормата - Строка - версия формата обмена.
//
Функция СформироватьДанныеЗапросаРасчетаДепозита(Организация, Банк, ПараметрыДепозита) Экспорт
	
	ТекущаяНастройкаОбмена = ОбменСБанкамиСлужебный.НастройкаОбмена(Организация, Банк, Истина);
	РеквизитыНастройкиОбмена = Новый Структура("АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		ТекущаяНастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ОтправительНаименование = ОбщегоНазначенияБЭД.СокращенноеНаименованиеОрганизации(
		Организация);
	РеквизитыОрганизации = ОбщегоНазначенияБЭД.ДанныеЮрФизЛица(Организация);
	РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Банк, "Код, Наименование");
		
	ВидОперации = НСтр("ru = 'Запрос расчета депозита в банке'");
	ВерсияФормата = РеквизитыНастройкиОбмена.ВерсияФормата;
	ПространствоИмен = ОбменСБанкамиСлужебный.ПространствоИменАсинхронногоОбмена(ВерсияФормата);
	Фабрика = ОбменСБанкамиСлужебныйПовтИсп.ФабрикаAsyncXDTO(ВерсияФормата);
	
	ЗапросРасчетаДепозита = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "DepositCalculateRequest", 
		ПространствоИмен);
	ТекстОшибки = "";
	
	ЗаполнитьСвойствоXDTO(ЗапросРасчетаДепозита, "formatVersion", ВерсияФормата, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗапросРасчетаДепозита, "creationDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
	Отправитель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "CustomerPartyType", ПространствоИмен);
	
	ИдентификаторОрганизации = СокрЛП(РеквизитыНастройкиОбмена.ИдентификаторОрганизации);
	ЗаполнитьСвойствоXDTO(
		Отправитель, "id", ИдентификаторОрганизации, Истина, ТекстОшибки);
		
	ЗаполнитьСвойствоXDTO(Отправитель, "name", ОтправительНаименование, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(Отправитель, "inn", РеквизитыОрганизации.ИНН, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(Отправитель, "kpp", РеквизитыОрганизации.КПП, , ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗапросРасчетаДепозита, "Sender", Отправитель, Истина, ТекстОшибки);
	
	Получатель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "BankPartyType", ПространствоИмен);
	ЗаполнитьСвойствоXDTO(Получатель, "bic", РеквизитыБанка.Код, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(Получатель, "name", РеквизитыБанка.Наименование, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗапросРасчетаДепозита, "Recipient", Получатель, Истина, ТекстОшибки);
	
	ДанныеДепозита = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "DepositCalculateRequest.Data", 
		ПространствоИмен);
	ИдентификаторПродукта = СокрЛП(ПараметрыДепозита.ИдентификаторПродукта);
	
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "ProductID", ИдентификаторПродукта, Истина, ТекстОшибки);
	
	РеквизитыБанковскогоСчета = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыБанковскогоСчетаОрганизации(
		ПараметрыДепозита.СчетОткрытия);
	НомерСчета = РеквизитыБанковскогоСчета.НомерСчета;
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "Account", НомерСчета, Истина, ТекстОшибки);
	
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "DepositSum", ПараметрыДепозита.СуммаДепозита, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "PeriodDay", ПараметрыДепозита.Срок, Истина, ТекстОшибки);
	
	ДатаЗакрытия = НачалоДня(ТекущаяДатаСеанса()) + (ПараметрыДепозита.Срок * 24 * 60 * 60);
	
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "ClosingDate", ДатаЗакрытия, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗапросРасчетаДепозита, "Data", ДанныеДепозита, Истина, ТекстОшибки);
	ЗапросРасчетаДепозита.Проверить();
	
	Данные = ДвоичныеДанныеИзXDTO(Фабрика, ЗапросРасчетаДепозита, Ложь);
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеСообщенияОбмена", Данные);
	Результат.Вставить("НастройкаОбмена", ТекущаяНастройкаОбмена);
	Результат.Вставить("РеквизитыНастройкиОбмена", РеквизитыНастройкиОбмена);
	
	Возврат Результат;
	
КонецФункции

// Выполняет отправку запрос расчета депозита в банк, и читает ответ банка.
// см. СформироватьДанныеЗапросаРасчетаДепозита.
//
// Параметры:
//  ИдентификаторСессии - идентификатор авторизованной сессии обмена с банком.
//  ДополнительныеПараметры - Структура: см. ОбменСБанкамиДепозитыСлужебныйКлиентСервер.НовыеПараметрыРасчетаДепозитаВБанке. 
// 
// Возвращаемое значение:
//   - Неопределено - не удалось выполнить запрос.
//   - Структура:
//       * ПараметрыДепозита - Структура: см. НовыеДанныеРасчетаДепозита.
//       * ОшибкиДепозита - Структура: см. ПрочитатьРасчетыПоДепозиту.
//
Функция ЗапроситьРасчетДепозитаВБанке(ИдентификаторСессии, ДополнительныеПараметры) Экспорт
	
	НастройкаОбмена = ДополнительныеПараметры.НастройкаОбмена;
	РеквизитыНастройкиОбмена = ДополнительныеПараметры.РеквизитыНастройкиОбмена;
	
	Настройки = Новый Структура;
	Настройки.Вставить("Адрес", РеквизитыНастройкиОбмена.АдресСервера);
	Настройки.Вставить("Ресурс", "DepositCalculate");
	Настройки.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	Настройки.Вставить("ИдентификаторОрганизации", РеквизитыНастройкиОбмена.ИдентификаторОрганизации);
	Настройки.Вставить("ВерсияФормата", РеквизитыНастройкиОбмена.ВерсияФормата);
	
	Данные = ДополнительныеПараметры.ДанныеСообщенияОбмена;
	ТекстОшибки = "";
	ОтветБанка = Неопределено;
	
	ОбменСБанкамиСлужебный.ОтправитьВБанк(НастройкаОбмена, Настройки, Данные, ОтветБанка, ТекстОшибки);
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		
		ПодробнаяИнформация = НСтр("ru = 'Ошибка при отправке запроса DepositCalculate
			|Подробное представление ошибки:
			|%1'");
		ПодробнаяИнформация = СтрШаблон(ПодробнаяИнформация, ТекстОшибки);
		ВидОперации = НСтр("ru = 'Отправка DepositCalculate.'");
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , Неопределено);
		
		ВызватьИсключение ПодробнаяИнформация;
		
	КонецЕсли;
	
	Результат = Неопределено;
	
	Попытка
		
		ЧтениеДанных = Новый ЧтениеДанных(ОтветБанка);
		ПакетXML = Новый ЧтениеXML;
		ПакетXML.ОткрытьПоток(ЧтениеДанных.ИсходныйПоток());
		ResultBank = ФабрикаXDTO.ПрочитатьXML(ПакетXML);	
		ПакетXML.Закрыть();

		Если ЗначениеСвойстваXDTO(ResultBank, "Error") <> Неопределено Тогда
			
			ТекстОшибки = ОбменСБанкамиСлужебный.ТекстСообщенияОбОшибкеОтветаБанка(ResultBank.Error);
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		ДанныеРасчета = ЗначениеСвойстваXDTO(ResultBank, "Success.DepositCalculateResponse");
		
		Если ДанныеРасчета <> Неопределено Тогда
		
			Результат = ПрочитатьРасчетыПоДепозиту(ДанныеРасчета);
			
		КонецЕсли;
		
	Исключение
		
		Если Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда  
			
			ШаблонСообщения = НСтр("ru = 'Возникла ошибка при разборе документа %1.
				|%2'");
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(ШаблонСообщения, "DepositCalculateResponse", КраткоеПредставлениеОшибки);
			
		Иначе 
			
			ТекстСообщения = НСтр("ru = 'Файл, полученный из банка, невозможно прочитать, так как он содержит ошибку'");
			
		КонецЕсли;
		
		ПодробнаяИнформация = НСтр("ru = 'Ошибка при распаковке документа %1
			|Подробное представление ошибки:
			|%2'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ПодробнаяИнформация = СтрШаблон(ПодробнаяИнформация, "DepositCalculateResponse", ПодробноеПредставлениеОшибки);
		ВидОперации = НСтр("ru = 'Чтение ЭД.'");
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , Неопределено);
		
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СостоянияЗаявкиНадепозит

Функция СостоянияЭДЗаявкиНаДепозитПоСтроке(СостояниеСтрокой) Экспорт
	
	Результат = Новый Массив;
	
	Если СостояниеСтрокой = "Отклонено" Тогда
		
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.Отклонен);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ОшибкаПередачи);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.Аннулирован);
		
	ИначеЕсли СостояниеСтрокой = "Подготовлено" Тогда
		
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.НеСформирован);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ТребуетсяОтправка);
		
	ИначеЕсли СостояниеСтрокой = "НаПодписи" Тогда
		
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.НаПодписи);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ТребуетсяПодтверждение);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ТребуетсяПодтверждениеВБанке);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ЧастичноПодтвержден);
		
	ИначеЕсли СостояниеСтрокой = "ВОбработке" Тогда
		
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ОжидаетсяВыписка);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ОжидаетсяИзвещениеОПолучении);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ОжидаетсяИсполнение);
		Результат.Добавить(Перечисления.СостоянияОбменСБанками.ПлатежИсполнен);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает состояние заявки на депозит определенное по состоянию ЭД.
// 
// Параметры:
//  СсылкаНаЗаявку - ДокументСсылка.ЗаявкаНаДепозит - ссылка на заявку.
// 
// Возвращаемое значение:
//  Строка - состояние заявки на депозит.
//
Функция СостояниеЗаявкиНаДепозитПоЭД(СсылкаНаЗаявку) Экспорт
	
	Результат = Неопределено;
	ЗаявкаИсполнена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаЗаявку, "Подтвержден");
	
	Если ЗаявкаИсполнена Тогда
		Возврат "Исполнено";
	КонецЕсли;
	
	Состояние = ОбменСБанкамиСлужебный.СостояниеЭД(СсылкаНаЗаявку);
	
	Если Состояние = Перечисления.СостоянияОбменСБанками.Отклонен
		Или Состояние = Перечисления.СостоянияОбменСБанками.ОшибкаПередачи
		Или Состояние = Перечисления.СостоянияОбменСБанками.Аннулирован Тогда
		
		Результат = "Отклонено";
		
	ИначеЕсли Состояние = Перечисления.СостоянияОбменСБанками.НеСформирован
		Или Состояние = Перечисления.СостоянияОбменСБанками.ТребуетсяОтправка Тогда
		
		Результат = "Подготовлено";
		
	ИначеЕсли Состояние = Перечисления.СостоянияОбменСБанками.НаПодписи
		Или Состояние = Перечисления.СостоянияОбменСБанками.ТребуетсяПодтверждение
		Или Состояние = Перечисления.СостоянияОбменСБанками.ТребуетсяПодтверждениеВБанке Тогда
		
		Результат = "НаПодписи";
		
	ИначеЕсли Состояние = Перечисления.СостоянияОбменСБанками.ОжидаетсяВыписка
		Или Состояние = Перечисления.СостоянияОбменСБанками.ОжидаетсяИзвещениеОПолучении
		Или Состояние = Перечисления.СостоянияОбменСБанками.ОжидаетсяИсполнение
		Или Состояние = Перечисления.СостоянияОбменСБанками.ПлатежИсполнен Тогда
		
		Результат = "ВОбработке";
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РаботаСXDTO

// Читает данные заявки на открытие депозита и заполняет строку дерева разбора.
// см. ОбменСБанкамиСлужебный.ПрочитатьФайлПоСхеме
//
// Параметры:
//  ДанныеСообщения - ОбъектXDTO - данные сообщения обмена.
//  НовыйЭД - СтрокаДереваЗначений - строка дерева разбора см. ИнициализироватьДеревоРазбора.
//
Процедура ПрочитатьЗаявкуНаОткрытиеДепозитаAsyncXDTO(ДанныеСообщения, НовыйЭД) Экспорт

	НовыйЭД.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита;

	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Идентификатор", ДанныеСообщения.id);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ВерсияФормата", ДанныеСообщения.formatVersion);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаСоздания",  ДанныеСообщения.creationDate);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Программа",  ДанныеСообщения.userAgent);

	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентификаторКлиента", ДанныеСообщения.Sender.id);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "НаименованиеКлиента", ДанныеСообщения.Sender.name);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИНН",  ДанныеСообщения.Sender.inn);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "КПП",  ДанныеСообщения.Sender.kpp);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "БИК",  ДанныеСообщения.Recipient.bic);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "НаименованиеБанка", ДанныеСообщения.Recipient.name);

	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаЗаявки", ДанныеСообщения.DepositData.DocDate);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "СчетОткрытия", ДанныеСообщения.DepositData.Account);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаПолученияПредложения", ДанныеСообщения.DepositData.DepositOfferDate);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентфикаторПродукта", ДанныеСообщения.DepositData.ProductID);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "СуммаДепозита", ДанныеСообщения.DepositData.DepositSum);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Ставка", ДанныеСообщения.DepositData.Rate);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Срок", ДанныеСообщения.DepositData.PeriodDay);

КонецПроцедуры

// Читает данные запроса состояния депозита и заполняет строку дерева разбора.
// см. ОбменСБанкамиСлужебный.ПрочитатьФайлПоСхеме
//
// Параметры:
//  ДанныеСообщения - ОбъектXDTO - данные сообщения обмена.
//  НовыйЭД - СтрокаДереваЗначений - строка дерева разбора см. ИнициализироватьДеревоРазбора.
//
Процедура ПрочитатьЗапросСостоянияДепозитаAsyncXDTO(ДанныеСообщения, НовыйЭД) Экспорт

	НовыйЭД.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ЗапросСостоянияДепозита;

	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Идентификатор", ДанныеСообщения.id);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ВерсияФормата", ДанныеСообщения.formatVersion);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаСоздания", ДанныеСообщения.creationDate);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Программа", ДанныеСообщения.userAgent);

	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентификаторКлиента", ДанныеСообщения.Sender.id);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "НаименованиеКлиента", ДанныеСообщения.Sender.name);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИНН", ДанныеСообщения.Sender.inn);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "КПП", ДанныеСообщения.Sender.kpp);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "БИК", ДанныеСообщения.Recipient.bic);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "НаименованиеБанка", ДанныеСообщения.Recipient.name);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентификаторИсходногоДокумента", ДанныеСообщения.ExtID);
	
КонецПроцедуры

// Читает данные извещения о состоянии депозита и заполняет строку дерева разбора.
// см. ОбменСБанкамиСлужебный.ПрочитатьФайлПоСхеме
//
// Параметры:
//  ДанныеСообщения - ОбъектXDTO - данные сообщения обмена.
//  НовыйЭД - СтрокаДереваЗначений - строка дерева разбора см. ИнициализироватьДеревоРазбора.
//
Процедура ПрочитатьИзвещениеОСостоянииДепозитаAsyncXDTO(ДанныеСообщения, НовыйЭД) Экспорт

	НовыйЭД.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ИзвещениеОСостоянииДепозита;

	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентификаторДокумента", ЗначениеСвойстваXDTO(ДанныеСообщения, "id"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ВерсияФормата", ЗначениеСвойстваXDTO(ДанныеСообщения, "formatVersion"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаСоздания", ЗначениеСвойстваXDTO(ДанныеСообщения, "creationDate", , "Дата"));
	
	ДанныеПолучателя = ЗначениеСвойстваXDTO(ДанныеСообщения, "Recipient");
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентификаторКлиента", ЗначениеСвойстваXDTO(ДанныеПолучателя, "id", ""));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "НаименованиеКлиента", ЗначениеСвойстваXDTO(ДанныеПолучателя, "name", ""));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИНН", ЗначениеСвойстваXDTO(ДанныеПолучателя, "inn", ""));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "КПП", ЗначениеСвойстваXDTO(ДанныеПолучателя, "kpp", ""));
	
	ДанныеОтправителя = ЗначениеСвойстваXDTO(ДанныеСообщения, "Sender");
		
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "БИК", ЗначениеСвойстваXDTO(ДанныеОтправителя, "bic"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "НаименованиеБанка", ЗначениеСвойстваXDTO(ДанныеОтправителя, "name"));
		
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентификаторИсходногоДокумента", ЗначениеСвойстваXDTO(ДанныеСообщения, "ExtID"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ИдентификаторЗапроса", ЗначениеСвойстваXDTO(ДанныеСообщения, "ExtIDStatusRequest"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "КомментарийБанка", ЗначениеСвойстваXDTO(ДанныеСообщения, "MoreInfo", ""));
	
	ДанныеДепозита = ЗначениеСвойстваXDTO(ДанныеСообщения, "DepositData");
	
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "НомерДоговора", ЗначениеСвойстваXDTO(ДанныеДепозита, "ContractNo"));
	ДатаОткрытия = ЗначениеСвойстваXDTO(ДанныеДепозита, "OpeningDate");
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаОткрытия", Формат(ДатаОткрытия, "ДЛФ=D"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "СчетОткрытия", ЗначениеСвойстваXDTO(ДанныеДепозита, "Account"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "СчетДепозита", ЗначениеСвойстваXDTO(ДанныеДепозита, "DepositAccount"));
	
	Валюта = ОбщегоНазначенияБЭД.НайтиСсылку("Валюты", ЗначениеСвойстваXDTO(ДанныеДепозита, "CurrencyCode"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(НовыйЭД, "Валюта", Валюта);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "СуммаДепозита", ЗначениеСвойстваXDTO(ДанныеДепозита, "DepositSum"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Срок", ЗначениеСвойстваXDTO(ДанныеДепозита, "PeriodDay"));
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Ставка", ЗначениеСвойстваXDTO(ДанныеДепозита, "Rate"));
	СтрокаСпособВыплатыПроцентов = ЗначениеСвойстваXDTO(
		ДанныеДепозита, "InterestPayment");
	СпособВыплатыПроцентов = ОбменСБанкамиДепозитыСлужебный.СпособыВыплатыПроцентовОбменСБанкамиДепозиты(
		СтрокаСпособВыплатыПроцентов);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ВыплатаПроцентов", СпособВыплатыПроцентов);
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "Капитализация", ЗначениеСвойстваXDTO(ДанныеДепозита, "Capitalization"));
	ДатаЗакрытия = ЗначениеСвойстваXDTO(ДанныеДепозита, "ClosingDate");
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаЗакрытия", Формат(ДатаЗакрытия, "ДЛФ=D"));
	ДатаВозвратаСредств = ЗначениеСвойстваXDTO(ДанныеДепозита, "ReturnDate");
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(
		НовыйЭД, "ДатаВозвратаСредств", Формат(ДатаВозвратаСредств, "ДЛФ=D"));

	// Продукт
	ИнформацияОПродукте = Новый Структура;
	ИнформацияОПродукте.Вставить("НаименованиеПродукта", ЗначениеСвойстваXDTO(ДанныеДепозита, "ProductName", ""));
	ИнформацияОПродукте.Вставить("ИдентификаторПродукта", ЗначениеСвойстваXDTO(ДанныеДепозита, "ProductID"));
	ИнформацияОПродукте.Вставить("СпособРасчета", ЗначениеСвойстваXDTO(ДанныеДепозита, "CalculateOption"));
	ИнформацияОПродукте.Вставить("ОписаниеПродукта", ЗначениеСвойстваXDTO(ДанныеДепозита, "Description", ""));
	
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(НовыйЭД, "ИнформацияОПродукте", ИнформацияОПродукте);
	
	// Таблица графика выплат
	ДанныеГрафикаВыплаты = ЗначениеСвойстваXDTO(ДанныеДепозита, "InterestPaymentSchedule.Payment", Новый Массив(), , Истина);
	ГрафикВыплатыПроцентов = Новый Массив;
	
	Для каждого ТекущаяВыплата Из ДанныеГрафикаВыплаты Цикл
		
		СтрокаВыплаты = Новый Структура;
		СтрокаВыплаты.Вставить("СтрокаВыплаты_ДатаВыплаты", Формат(ТекущаяВыплата.Date, "ДЛФ=D"));
		СтрокаВыплаты.Вставить("СтрокаВыплаты_СуммаДепозита", ТекущаяВыплата.DepositSum);
		СтрокаВыплаты.Вставить("СтрокаВыплаты_СуммаВыплаты", ТекущаяВыплата.InterestSum);
		ГрафикВыплатыПроцентов.Добавить(СтрокаВыплаты);
		
	КонецЦикла;
	
	ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(НовыйЭД, "ГрафикВыплатыПроцентов", ГрафикВыплатыПроцентов);
	
	ДанныеШтампа = ЗначениеСвойстваXDTO(ДанныеСообщения, "Stamp");
	
	Если Не ДанныеШтампа = Неопределено Тогда
		
		ПараметрыШтампа = Новый Структура;
		ПараметрыШтампа.Вставить("БИК", ЗначениеСвойстваXDTO(ДанныеШтампа, "BIC"));
		ПараметрыШтампа.Вставить("НаименованиеБанка", ЗначениеСвойстваXDTO(ДанныеШтампа, "Name"));
		ПараметрыШтампа.Вставить("ГородБанка", ЗначениеСвойстваXDTO(ДанныеШтампа, "City"));
		ПараметрыШтампа.Вставить("КоррСчетБанка", ЗначениеСвойстваXDTO(ДанныеШтампа, "CorrespAcc"));
		ПараметрыШтампа.Вставить("Отделение", ЗначениеСвойстваXDTO(ДанныеШтампа, "Branch"));
		
		ДеревоЭлектронногоДокументаБЭД.ДобавитьРеквизитШапкиОбъекта(НовыйЭД, "Штамп", ПараметрыШтампа);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДепозитныеПродукты

// Формирует данные предложений банков для списка выбора предложений.
//
// Параметры:
//  ПредложенияБанков - Структура - данные предложений банков:
//   * Условия - Соответствие из КлючИЗначение:
//      ** Ключ - Строка - уникальный идентификатор депозитного продукта.
//      ** Значение - Структура - условия депозитного продукта см. ДанныеДепозитныхПродуктов.
//   * Продукты - ТаблицаЗначений: см. ПроверитьПродуктыИРассчитатьДоходность.
//   * ОдинБанк - Булево - Если Истина, в таблице Продукты предложения одного банка.
// 
// Возвращаемое значение:
//  Массив из Структра:
//   * Значение - Строка - наименование продукта.
//   * ДополнительныеДанные - Структра: см. НовыеДополнительныеДанныеСпискаПредложений.
//
Функция СформироватьДанныеСпискаВыбораПредложений(ПредложенияБанков)
	
	Условия = ПредложенияБанков.Условия;
	Продукты = ПредложенияБанков.Продукты;
	ОдинБанк = ПредложенияБанков.ОдинБанк;
	МассивРезультат = Новый Массив;
	
	Для каждого ТекущаяСтрокаПродукта Из Продукты Цикл
		
		ТекущиеУсловия = Условия[ТекущаяСтрокаПродукта.КлючПродукта];
		УсловиеПродукта = Неопределено;
		СтавкаПродукта = ТекущаяСтрокаПродукта.СтавкаМаксимум;
		
		ЕстьУсловия = Ложь;
		
		Если ЗначениеЗаполнено(ТекущаяСтрокаПродукта.ИдентификаторУсловия) Тогда
			УсловиеПродукта = НайтиУсловиеПоИдентификатору(ТекущиеУсловия, ТекущаяСтрокаПродукта.ИдентификаторУсловия);
			СтавкаПродукта = УсловиеПродукта.Ставка;
			ЕстьУсловия = Истина;
		КонецЕсли;
		
		ОписаниеЭлементаСпискаПродукты = Новый Структура();
		НаименованиеПродукта = "";
		
		Если ОдинБанк Тогда
			НаименованиеПродукта = СтрШаблон("%1%% %2", СтавкаПродукта, ТекущаяСтрокаПродукта.Наименование);
		Иначе
			НаименованиеПродукта = СтрШаблон("%1: %2%% %3", ТекущаяСтрокаПродукта.НаименованиеБанка, 
				СтавкаПродукта, ТекущаяСтрокаПродукта.Наименование);
		КонецЕсли;
			
		ОписаниеЭлементаСпискаПродукты.Вставить("Значение", НаименованиеПродукта);
		
		ДополнительныеДанные = НовыеДополнительныеДанныеСпискаПредложений();
		ЗаполнитьЗначенияСвойств(ДополнительныеДанные, ТекущаяСтрокаПродукта);
		Если ЕстьУсловия Тогда
			ДополнительныеДанные.СуммаМаксимум = УсловиеПродукта.СуммаМаксимум;
			ДополнительныеДанные.СуммаМинимум = УсловиеПродукта.СуммаМинимум;
			ДополнительныеДанные.СрокМаксимум = УсловиеПродукта.СрокМаксимум;
			ДополнительныеДанные.СрокМинимум = УсловиеПродукта.СрокМинимум;
		Иначе
			ДополнительныеДанные.НеВыполненыУсловия = Истина;
		КонецЕсли;
		
		ДополнительныеДанные.ОписаниеПродукта = ТекущаяСтрокаПродукта.ПодробноеОписание;
		ОписаниеЭлементаСпискаПродукты.Вставить("ДополнительныеДанные", ДополнительныеДанные);
		МассивРезультат.Добавить(ОписаниеЭлементаСпискаПродукты);
		
	КонецЦикла;
	
	Возврат МассивРезультат;
	
КонецФункции

// Формирует данные для параметров предложений и списка выбора депозитных продуктов.
//
// Параметры:
//  ПродуктыБанков - ТаблицаЗначений: см. ДанныеДепозитныхПродуктов.
//  ПараметрыРасчетаПредложений - Структура: см.ОбменСБанкамиДепозитыКлиент.НовыйПараметрыРасчетаПредложений.
//
// Возвращаемое значение:
//  Структура:
//   * МассивПредложений - Массив из Структура: см. СформироватьДанныеСпискаВыбораПредложений.
//   * УсловияТекущегоПродукта - Структура:
//      ** КлючПродукта - Строка - Строка - уникальный идентификатор депозитного продукта.
//      ** Условия - Массив из Структура: см. НовыеПараметрыУсловияДепозитногоПродукта.
//
Функция СформироватьПредложенияБанков(ПродуктыБанков, ПараметрыРасчетаПредложений)
	
	ДатаРасчета = ПараметрыРасчетаПредложений.ДатаРасчета;
	Сумма = ПараметрыРасчетаПредложений.Сумма;
	Срок = ПараметрыРасчетаПредложений.Срок;
	
	ПредложенияБанков = НовыеДанныеПредложенийБанка();
	ПредложенияБанков.Продукты = ПродуктыБанков.Продукты;
	ПредложенияБанков.Условия = ПродуктыБанков.Условия;
	
	ПроверитьПродуктыИРассчитатьДоходность(ПредложенияБанков, ДатаРасчета,
		Сумма, Срок);
	ДанныеСпискаВыбора = СформироватьДанныеСпискаВыбораПредложений(ПредложенияБанков);
	
	ВыбранноеПредложение = ДанныеСпискаВыбора[0];
	ДанныеВыборанногоУсловия = ВыбранноеПредложение.ДополнительныеДанные;
	КлючПродукта = ДанныеВыборанногоУсловия.КлючПродукта;
	
	УсловияТекущегоПродукта = Новый Структура();
	УсловияТекущегоПродукта.Вставить("КлючПродукта", КлючПродукта);
	УсловияТекущегоПродукта.Вставить("Условия", ПредложенияБанков.Условия[КлючПродукта]);
	
	Результат = Новый Структура;
	Результат.Вставить("МассивПредложений", ДанныеСпискаВыбора);
	Результат.Вставить("УсловияТекущегоПродукта", УсловияТекущегоПродукта);
	Результат.вставить("ОдинБанк", ПредложенияБанков.ОдинБанк);
	
	Возврат Результат;
	
КонецФункции

Процедура ОчиститьПредложенияБанковПоДепозитам(ТаблицаОтборБанки)
	
	Если Не ЗначениеЗаполнено(ТаблицаОтборБанки) Тогда
		ТаблицаОтборБанки = ПолучитьДоступныеБанкиДляОбменаПоДепозитам();
	КонецЕсли;
	
	Если ТаблицаОтборБанки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировкиПродукты = БлокировкаДанных.Добавить("РегистрСведений.ДепозитныеПродуктыОбменСБанками");
	ЭлементБлокировкиПродукты.ИсточникДанных = ТаблицаОтборБанки;
	ЭлементБлокировкиПродукты.ИспользоватьИзИсточникаДанных("Банк", "Банк");
	ЭлементБлокировкиУсловия = БлокировкаДанных.Добавить("РегистрСведений.УсловияДепозитныхПродуктовОбменСБанками");
	ЭлементБлокировкиУсловия.ИсточникДанных = ТаблицаОтборБанки;
	ЭлементБлокировкиУсловия.ИспользоватьИзИсточникаДанных("Банк", "Банк");
	
	НачатьТранзакцию();
	
	Попытка
	
		БлокировкаДанных.Заблокировать();
		
		Для каждого ТекущаяСтрока Из ТаблицаОтборБанки Цикл
			
			НаборЗаписейРегистраПродукты = РегистрыСведений.ДепозитныеПродуктыОбменСБанками.СоздатьНаборЗаписей();
			НаборЗаписейРегистраПродукты.Отбор.Банк.Установить(ТекущаяСтрока.Банк);
			
			НаборЗаписейРегистраУсловия = РегистрыСведений.УсловияДепозитныхПродуктовОбменСБанками.СоздатьНаборЗаписей();
			НаборЗаписейРегистраУсловия.Отбор.Банк.Установить(ТекущаяСтрока.Банк);
			
			НаборЗаписейРегистраУсловия.Записать(Истина);
			НаборЗаписейРегистраПродукты.Записать(Истина);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Если Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда
			
			ШаблонСообщения = НСтр("ru = 'Возникла ошибка при обновлении предложений банка по депозитам.
				|%1'");
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки);
			
		Иначе 
			
			ТекстСообщения = НСтр("ru = 'Не удалось обновить предложения банка, так как при записи
				|предложений по депозитам произошла ошибка.'");
			
		КонецЕсли;
		
		ВидОперации = НСтр("ru = 'Обновление предложений банков'");
		ПодробнаяИнформация = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПолучитьДоступныеБанкиДляОбменаПоДепозитам()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Банк КАК Банк
		|ИЗ
		|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанкамиИсходящиеДокументы
		|ГДЕ
		|	НастройкиОбменСБанкамиИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита)
		|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.Недействительна
		|	И НЕ НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка.ПометкаУдаления";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область ЗаявкаНаДепозит

// Функция формирует структуру реквизитов заявки на депозит
// для заполнения данными из извещения о состоянии депозита
// см. ПрочитатьИзвещениеОСостоянииДепозита
// Возвращаемое значение:
//  Структура:
//   * НомерДоговора - Строка - номер депозитного договора.
//   * КомментарийБанка - Строка - комментарий банка.
//   * ДатаОткрытия - Дата - дата открытия депозита.
//   * НаименованиеПродукта - Строка - наименование депозитного продукта банка.
//   * ИдентификаторПродукта - Строка - идентификатор депозитного продукта в системе банка.
//   * СпособВыплатыПроцентов - Строка - код способа выплаты процентов.
//   * КодВалюты - Строка - код валюты.
//   * Капитализация - Булево - если Истина, доступна капитализация.
//   * ОписаниеПродукта - Строка - описание депозитного продукта.
//   * СуммаДепозита - Число - сумма депозита.
//   * Ставка - Число - ставка в % годовых.
//   * СрокДепозита - Число - срок депозита в днях.
//   * ДатаЗакрытия - Дата - дата закрытия депозита.
//   * ДатаВозвратаСредств - дата возврата средств.
//   * НомерСчета - Строка - номер счета списания средств на открытие депозита.
//   * НомерСчетаДепозита - Строка - номер счета депозита.
//   * ГрафикВыплатыПроцентов - Массив из Структура - содержит поля:
//       - ДатаВыплаты - Дата - дата и сумма выплаты процентов
//       - СуммаДепозита - Число - остаток депозита на дату начисления процентов.
//       - СуммаВыплаты - Число - сумма выплаты.
//   * Подтвержден - Булево - депозит открыт и получен документ извещение о состоянии депозита.
//   * ЕстьВложение - Булево - если Истина, есть вложенные файлы.
// 
Функция НовыеРеквизитыЗаявкиНаДепозитДляИзменения()
	
	Результат = Новый Структура;
	Результат.Вставить("НомерДоговора", "");
	Результат.Вставить("КомментарийБанка", "");
	Результат.Вставить("ДатаОткрытия", Дата(1, 1, 1));
	Результат.Вставить("НаименованиеПродукта", "");
	Результат.Вставить("ИдентификаторПродукта", "");
	Результат.Вставить("СпособВыплатыПроцентов", "");
	Результат.Вставить("КодВалюты", "");
	Результат.Вставить("Капитализация", Ложь);
	Результат.Вставить("ОписаниеПродукта", "");
	Результат.Вставить("СуммаДепозита", 0);
	Результат.Вставить("Ставка", 0);
	Результат.Вставить("СрокДепозита", 0);
	Результат.Вставить("ДатаЗакрытия", Дата(1, 1, 1));
	Результат.Вставить("ДатаВозвратаСредств", Дата(1, 1, 1));
	Результат.Вставить("НомерСчета","");
	Результат.Вставить("НомерСчетаДепозита","");
	Результат.Вставить("ГрафикВыплатыПроцентов", Новый Массив);
	Результат.Вставить("Подтвержден", Ложь);
	Результат.Вставить("ЕстьВложение", Ложь);
	
	Возврат Результат;
	
КонецФункции

Процедура СформироватьДвоичныеДанныеЗаявкиНаДепозит(ПараметрыЗаявки, НастройкаОбмена, АдресФайлаВоВременномХранилище)
	
	РеквизитыНастройкиОбмена = Новый Структура;
	РеквизитыНастройкиОбмена.Вставить("Организация", Неопределено);
	РеквизитыНастройкиОбмена.Вставить("ИдентификаторОрганизации", Неопределено);
	РеквизитыНастройкиОбмена.Вставить("Банк", Неопределено);
	РеквизитыНастройкиОбмена.Вставить("ВерсияФормата", Неопределено);
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ПространствоИмен = ОбменСБанкамиСлужебный.ПространствоИменАсинхронногоОбмена(
		РеквизитыНастройкиОбмена.ВерсияФормата);
	ТекстОшибки = "";
	
	Фабрика = ОбменСБанкамиСлужебныйПовтИсп.ФабрикаAsyncXDTO(РеквизитыНастройкиОбмена.ВерсияФормата);

	ЗаявкаНаДепозит = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "DepositApp", ПространствоИмен);
	
	ОтправительНаименование = ОбщегоНазначенияБЭД.СокращенноеНаименованиеОрганизации(
		РеквизитыНастройкиОбмена.Организация);
	РеквизитыОрганизации = ОбщегоНазначенияБЭД.ДанныеЮрФизЛица(РеквизитыНастройкиОбмена.Организация);
	РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыНастройкиОбмена.Банк, "Код, Наименование");
	
	ЗаполнитьСвойствоXDTO(ЗаявкаНаДепозит, "formatVersion", РеквизитыНастройкиОбмена.ВерсияФормата, Истина,
		ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗаявкаНаДепозит, "creationDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(
		ЗаявкаНаДепозит, "id", ПараметрыЗаявки.Идентификатор, Истина, ТекстОшибки);
		
	Отправитель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "CustomerPartyType", ПространствоИмен);
	ЗаполнитьСвойствоXDTO(Отправитель, "id", РеквизитыНастройкиОбмена.ИдентификаторОрганизации, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(Отправитель, "name", ОтправительНаименование, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(Отправитель, "inn", РеквизитыОрганизации.ИНН, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(Отправитель, "kpp", РеквизитыОрганизации.КПП, , ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗаявкаНаДепозит, "Sender", Отправитель, Истина, ТекстОшибки);
	
	Получатель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "BankPartyType", ПространствоИмен);
	ЗаполнитьСвойствоXDTO(Получатель, "bic", РеквизитыБанка.Код, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(Получатель, "name", РеквизитыБанка.Наименование, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗаявкаНаДепозит, "Recipient", Получатель, Истина, ТекстОшибки);
	
	ДанныеДепозита = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "DepositApp.DepositData", 
		ПространствоИмен);
	
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "DocDate", ПараметрыЗаявки.Дата, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "Account", ПараметрыЗаявки.СчетОрганизации.НомерСчета, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "Rate", ПараметрыЗаявки.Ставка, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "ProductID", СокрЛП(ПараметрыЗаявки.ИдентификаторПродукта), Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "PeriodDay", ПараметрыЗаявки.СрокДепозита, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "DepositOfferDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ДанныеДепозита, "DepositSum", ПараметрыЗаявки.СуммаДепозита, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗаявкаНаДепозит, "DepositData", ДанныеДепозита, Истина, ТекстОшибки);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ШаблонСообщения = НСтр("ru = 'При формировании электронного документа произошла ошибка:
			|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекстОшибки);
		ВидОперации = НСтр("ru = 'Формирование электронного документа'");
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ТекстСообщения, , ПараметрыЗаявки.Ссылка);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

	ЗаявкаНаДепозит.Проверить();
	ДвоичныеДанные = ДвоичныеДанныеИзXDTO(Фабрика, ЗаявкаНаДепозит);
	АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
КонецПроцедуры

Функция ОбъектXDTOИзвещениеОСостоянииДепозита(ДвоичныеДанныеФайла, ПакетОбменСБанками)
	
	ОбъектXML = Новый ЧтениеXML;
	Попытка
		ОбъектXML.ОткрытьПоток(ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения());
		DepositStatusNotice = ФабрикаXDTO.ПрочитатьXML(ОбъектXML);
		Фабрика = ОбменСБанкамиСлужебныйПовтИсп.ФабрикаAsyncXDTO(DepositStatusNotice.formatVersion);
		Если Фабрика = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Неизвестная версия формата файла.'");
		КонецЕсли;
		ПространствоИмен = ОбменСБанкамиСлужебный.ПространствоИменАсинхронногоОбмена(DepositStatusNotice.formatVersion);
		ТипИзвещениеОСостоянииДепозита = ОбменСБанкамиСлужебный.ТипЗначенияCML(Фабрика, ПространствоИмен, 
			"DepositStatusNotice");
		ОбъектXML.ОткрытьПоток(ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения());
		DepositStatusNotice = Фабрика.ПрочитатьXML(ОбъектXML, ТипИзвещениеОСостоянииДепозита);
	Исключение
		Если Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда
			ШаблонСообщения = НСтр("ru = 'Возникла ошибка при разборе пакета %1.
				|%2'");
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПакетОбменСБанками, КраткоеПредставлениеОшибки);
		Иначе
			ТекстСообщения = НСтр("ru = 'Файл, полученный из банка невозможно прочитать так как он содержит ошибку'");
		КонецЕсли;
		ПодробнаяИнформация = НСтр("ru = 'Ошибка при распаковке пакета %1
									|Подробное представление ошибки:
									|%2'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ПодробнаяИнформация = СтрШаблон(ПодробнаяИнформация, ПакетОбменСБанками, ПодробноеПредставлениеОшибки);
		ВидОперации = НСтр("ru = 'Чтение ЭД.'");
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения, ПакетОбменСБанками);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	ОбъектXML.Закрыть();
	
	Возврат DepositStatusNotice;
	
КонецФункции

Процедура ДобавитьПрисоединенныеФайлыЗаявкиНаДепозит(ЗаявкаСсылка, Вложения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВладелецФайла", ЗаявкаСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы.Наименование КАК Наименование,
	|	ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы.Расширение КАК Расширение,
	|	ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы.Ссылка КАК Ссылка,
	|	ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы.ВладелецФайла КАК ВладелецФайла
	|ИЗ
	|	Справочник.ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы КАК ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы
	|ГДЕ
	|	ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	|	И ЗаявкаНаДепозитОбменСБанкамиПрисоединенныеФайлы.ПометкаУдаления = ЛОЖЬ";
	ФайлыЗаявки = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Вложение Из Вложения Цикл
		ДобавитьПрисоединенныйФайлВЗаявкуНаДепозит(ЗаявкаСсылка, Вложение, ФайлыЗаявки);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПрисоединенныйФайлВЗаявкуНаДепозит(ЗаявкаСсылка, Вложение, ФайлыЗаявки)
	
	ПараметрыФайла = Новый Структура;
	ПараметрыФайла.Вставить("Автор", Пользователи.АвторизованныйПользователь());
	ПараметрыФайла.Вставить("ВладелецФайлов", ЗаявкаСсылка);
	ПолноеИмяФайла = ЗначениеСвойстваXDTO(Вложение, "BinaryFile.name");

	СтруктураФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПолноеИмяФайла);
	ПараметрыФайла.Вставить("ИмяБезРасширения", СтруктураФайла.ИмяБезРасширения);
	ПараметрыФайла.Вставить("РасширениеБезТочки", ЗначениеСвойстваXDTO(Вложение, "BinaryFile.extension"));
	ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", ЗначениеСвойстваXDTO(Вложение, "BinaryFile.creationDate"));
	
	ДвоичныеДанныеФайла = ЗначениеСвойстваXDTO(Вложение, "BinaryFile.__content");
	АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла);
	СуществующийФайл = Неопределено;
	Если ФайлыЗаявки.Количество() > 0 Тогда
		ОтборФайла = Новый Структура("Наименование, Расширение",
			ПараметрыФайла.ИмяБезРасширения, ПараметрыФайла.РасширениеБезТочки);
		НайденныеФайлы = ФайлыЗаявки.НайтиСтроки(ОтборФайла);
		Если НайденныеФайлы.Количество() > 0 Тогда
			СуществующийФайл = НайденныеФайлы[0].Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СуществующийФайл) Тогда
		ИнформацияОФайле = Новый Структура;
		ИнформацияОФайле.Вставить("АдресФайлаВоВременномХранилище", АдресФайлаВоВременномХранилище);
		ИнформацияОФайле.Вставить("АдресВременногоХранилищаТекста", "");
		РаботаСФайлами.ОбновитьФайл(СуществующийФайл, ИнформацияОФайле);
	Иначе
		РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайлаВоВременномХранилище);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеПредложенийОтБанков

// Параметры получения предложения по депозитам.
//
// Возвращаемое значение:
//  Структура - Содержит адрес сервера банка и соответствие организаций их идентификаторам клиента банка:
//    * НаименованиеБанка - Строка - наименование банка из настройки обмена;
//    * АдресСервера - Строка - адрес сервера банка;
//    * СоответствиеИдКлиентовБанкаОрганизациям - Соответствие из КлючИЗначение:
//        ** Ключ - Строка - идентификатор клиента банка;
//        ** Значение - ОпределяемыйТип.Организация - ссылка на организацию.
//
Функция НовыеПараметрыПолученияПредложенияПоДепозитам()
	
	Результат = Новый Структура();
	Результат.Вставить("НаименованиеБанка", "");
	Результат.Вставить("АдресСервера", "");
	Результат.Вставить("СоответствиеИдКлиентовБанкаОрганизациям", Новый Соответствие());
	
	Возврат Результат;
	
КонецФункции

// Функция-конструктор параметров процедуры ОбменСБанкамиДепозиты.ОбновитьПредложенияБанка.
// см. ОбновитьПредложенияБанка.
//
// Возвращаемое значение:
//  Структура - Параметры обновления предложений:
// * НаименованиеБанка - Строка - Наименованеи банка указанное в настройке обмена
// * СоответствиеИдКлиентовБанкаОрганизациям - Соответствие из КлючИЗначение:
//        ** Ключ - Строка - идентификатор клиента банка;
//        ** Значение - ОпределяемыйТип.Организация - ссылка на организацию.
//
Функция ПараметрыОбновленияПредложений()
	
	Результат = Новый Структура;
	Результат.Вставить("НаименованиеБанка", "");
	Результат.Вставить("СоответствиеИдКлиентовБанкаОрганизациям", Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

// Отправляет запрос DepositOffers для получения предложений банка по депозитам и загружает ответ.
// 
// Параметры:
//  Банк - ОпределяемыйТип.БанкОбменСБанками - ссылка на банк, по которому обновляются предложения;
//  ПараметрыЗапроса - Соответствие из КлючИЗначение - см. ПараметрыПолученияПредложенияПоДепозитам;
//
Процедура ОтправитьЗапросИОбновитьПредложенияПоДепозитам(Банк, ПараметрыЗапроса)
	
	ВидОперации = НСтр("ru = 'Запрос предложений банка по депозитным продуктам'");
	АдресСервера = ПараметрыЗапроса.АдресСервера;
	ВерсияФормата = ОбменСБанкамиДепозитыКлиентСервер.МинимальнаяВерсияФорматаОбменСБанкамиДепозиты();

	ПространствоИмен = ОбменСБанкамиСлужебный.ПространствоИменАсинхронногоОбмена(ВерсияФормата);
	Фабрика = ОбменСБанкамиСлужебныйПовтИсп.ФабрикаAsyncXDTO(ВерсияФормата);
	ЗапросПредложений = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "DepositOffersRequest", 
		ПространствоИмен);
	ТекстОшибки = "";
	СвойствоВерсияФормата = "formatVersion";
	
	ЗаполнитьСвойствоXDTO(ЗапросПредложений, СвойствоВерсияФормата, ВерсияФормата, Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(ЗапросПредложений, "creationDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
	ВерсияПрограммы = ОбменСБанкамиСлужебныйПовтИсп.ВерсияПрограммыКлиентаДляБанка();
	ЗаполнитьСвойствоXDTO(ЗапросПредложений, "userAgent", ВерсияПрограммы, , ТекстОшибки);
	
	Для Каждого КлючИЗначение Из ПараметрыЗапроса.СоответствиеИдКлиентовБанкаОрганизациям Цикл
		ИдентификаторОрганизации = СокрЛП(КлючИЗначение.Ключ);
		ЗапросПредложений.CustomerID.Добавить(ИдентификаторОрганизации);
	КонецЦикла;
	
	ЗапросПредложений.Проверить();
	
	Данные = ДвоичныеДанныеИзXDTO(Фабрика, ЗапросПредложений, Ложь);
	
	Настройки = Новый Структура();
	Настройки.Вставить("Адрес", АдресСервера);
	Настройки.Вставить("Ресурс", "DepositOffers");
	Настройки.Вставить("ВерсияФормата", ВерсияФормата);
	
	Результат = Неопределено;
	
	ОтправитьВБанкБезАвторизации(Настройки, Данные, Результат, Банк, ТекстОшибки);
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда  
		
		ПодробнаяИнформация = НСтр("ru = 'Ошибка при отправке запроса DepositOffers
			|Подробное представление ошибки:
			|%1'");
		ПодробнаяИнформация = СтрШаблон(ПодробнаяИнформация, ТекстОшибки);
		ВидОперации = НСтр("ru = 'Отправка DepositOffers.'");
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , Банк);
		
		ВызватьИсключение ПодробнаяИнформация;
		
	КонецЕсли;
	
	Попытка
		
		ЧтениеДанных = Новый ЧтениеДанных(Результат);
		ПакетXML = Новый ЧтениеXML;
		ПакетXML.ОткрытьПоток(ЧтениеДанных.ИсходныйПоток());
		ResultBank = ФабрикаXDTO.ПрочитатьXML(ПакетXML);
		ПакетXML.Закрыть();
		
		ПараметрыБанка = ПараметрыОбновленияПредложений();
		ПараметрыБанка.НаименованиеБанка = ПараметрыЗапроса.НаименованиеБанка;
		ПараметрыБанка.СоответствиеИдКлиентовБанкаОрганизациям =
			ПараметрыЗапроса.СоответствиеИдКлиентовБанкаОрганизациям;
			
		Если ЗначениеСвойстваXDTO(ResultBank, "Success.DepositOffersResponse") <> Неопределено Тогда
			
			ТекстОшибки = НСтр("ru = 'Не указана версия формата обмена. Минимальная версия обмена для работы с депозитами 2.3.4'");
			Если ЗначениеСвойстваXDTO(ResultBank, СвойствоВерсияФормата) = Неопределено Тогда
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
			
			ВерсияФорматаОтвета = Строка(ЗначениеСвойстваXDTO(ResultBank, СвойствоВерсияФормата));
			РезультатСравнения = ОбменСБанкамиСлужебныйКлиентСервер.СравнитьВерсиюФормата(ВерсияФорматаОтвета,
				ВерсияФормата);
			
			Если Не РезультатСравнения Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Не поддерживаемая версия обмена для работы с депозитами %1. Минимальная версия обмена 2.3.4'"),
					ВерсияФорматаОтвета);
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
			
			DepositOffersResponse = ЗначениеСвойстваXDTO(ResultBank, "Success.DepositOffersResponse");
			
			НаименованиеБанка = ЗначениеСвойстваXDTO(DepositOffersResponse, "BankBrandName");
			
			Если ЗначениеЗаполнено(НаименованиеБанка) Тогда
				
				ПараметрыБанка.НаименованиеБанка = НаименованиеБанка;
				
			КонецЕсли;
			
		ИначеЕсли ЗначениеСвойстваXDTO(ResultBank, "Error") <> Неопределено Тогда
			
			ТекстОшибки = ОбменСБанкамиСлужебный.ТекстСообщенияОбОшибкеОтветаБанка(ResultBank.Error);
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		ПрочитатьОтветИОбновитьПредложенияБанка(Банк, Результат, ПараметрыБанка);
	
	Исключение
		
		Если Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда  
			
			ШаблонСообщения = НСтр("ru = 'Возникла ошибка при разборе документа %1.
				|%2'");
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(ШаблонСообщения, "DepositOffersResponse", КраткоеПредставлениеОшибки);
			
		Иначе 
			
			ТекстСообщения = НСтр("ru = 'Файл, полученный из банка, невозможно прочитать так как он содержит ошибку'");
			
		КонецЕсли;
		
		ПодробнаяИнформация = НСтр("ru = 'Ошибка при чтении ответа DepositOffers %1
			|Подробное представление ошибки:
			|%2'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ПодробнаяИнформация = СтрШаблон(ПодробнаяИнформация, "DepositOffersResponse", ПодробноеПредставлениеОшибки);
		ВидОперации = НСтр("ru = 'Чтение ЭД.'");
		ОбменСБанкамиСлужебный.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , Неопределено);
		
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ПрочитатьОтветИОбновитьПредложенияБанка(Банк, ОтветБанка, ПараметрыБанка)
	
	ЧтениеДанных = Новый ЧтениеДанных(ОтветБанка);
	ПакетXML = Новый ЧтениеXML;
	ПакетXML.ОткрытьПоток(ЧтениеДанных.ИсходныйПоток());
	ResultBank = ФабрикаXDTO.ПрочитатьXML(ПакетXML);
	ПакетXML.Закрыть();
	
	МассивПараметрыПредложений = Новый Массив;
	МассивУсловий = Новый Массив;
	ПредложенияБанкаПоДепозитам = ЗначениеСвойстваXDTO(ResultBank, "Success.DepositOffersResponse");
	ДатаПредложений = ЗначениеСвойстваXDTO(ПредложенияБанкаПоДепозитам, "creationDate", ТекущаяДатаСеанса(), "Дата");
	МассивПредложений = ЗначениеСвойстваXDTO(ResultBank, "Success.DepositOffersResponse.Data.DepositOffer", 
		Новый Массив(), , Истина);
	
	Для каждого ТекущееПредложение Из МассивПредложений Цикл
		
		ПараметрыПредложения = НовыеПараметрыОписанияПродуктаИзОтветаБанка();
		
		// Уникальность идентификаторов депозитных продуктов для разных банков не гарантирована.
		// Поэтому назначаем внутренний ключ продукта.
		//
		КлючПродукта = Новый УникальныйИдентификатор();
		
		ПараметрыПредложения.КлючПродукта = КлючПродукта;
		ПараметрыПредложения.ИдентификаторПродукта = ЗначениеСвойстваXDTO(ТекущееПредложение, "ProductID");
		ПараметрыПредложения.Банк = Банк;
		
		Валюта = ЗначениеСвойстваXDTO(ТекущееПредложение, "CurrencyCode", "643");
		ПараметрыПредложения.Валюта = ОбщегоНазначенияБЭД.НайтиСсылку("Валюты", Валюта);
		Если Не ЗначениеЗаполнено(ПараметрыПредложения.Валюта) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПредложения.Наименование = ЗначениеСвойстваXDTO(ТекущееПредложение, "ProductName");
		
		КодМетодаРасчета = ЗначениеСвойстваXDTO(ТекущееПредложение, "CalculateOption", "01");
		ПараметрыПредложения.ВариантРасчета = МетодыРасчетаПроцентовДепозита(КодМетодаРасчета);
		Если Не ЗначениеЗаполнено(ПараметрыПредложения.ВариантРасчета) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПредложения.Капитализация = ЗначениеСвойстваXDTO(ТекущееПредложение, "Capitalization", Ложь, 
			"Булево");
		ПараметрыПредложения.ПодробноеОписание = ЗначениеСвойстваXDTO(ТекущееПредложение, "Description", "");
		ПараметрыПредложения.НаименованиеБанка = ЗначениеСвойстваXDTO(ТекущееПредложение, "BankBrandName", 
			ПараметрыБанка.НаименованиеБанка);
			
		КодСпособаВыплатыПроцентов =  ЗначениеСвойстваXDTO(ТекущееПредложение, "InterestPayment", "01");
		ПараметрыПредложения.ВыплатаПроцентов =
			СпособыВыплатыПроцентовОбменСБанкамиДепозиты(КодСпособаВыплатыПроцентов);
		Если Не ЗначениеЗаполнено(ПараметрыПредложения.ВыплатаПроцентов) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПредложения.ДатаАктуальности = ДатаПредложений;
		
		СписокУсловий = ЗначениеСвойстваXDTO(ТекущееПредложение, "DepositTermsAndConditions.Order", Новый Массив(), , Истина);
		
		Если Не СписокУсловий.Количество() = 0 Тогда
			МассивПараметрыПредложений.Добавить(ПараметрыПредложения);
		КонецЕсли;
			
		Для каждого ТекущееУсловие Из СписокУсловий Цикл
			
			ПараметрыУсловия = НовыеПараметрыОписанияУсловияПродуктаИзОтветаБанка();
			ПараметрыУсловия.КлючПродукта = КлючПродукта;
			ПараметрыУсловия.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор());
			ПараметрыУсловия.Банк = Банк;
			ПараметрыУсловия.СуммаМинимум = ЗначениеСвойстваXDTO(ТекущееУсловие, "DepositSumMin");
			ПараметрыУсловия.СуммаМаксимум = ЗначениеСвойстваXDTO(ТекущееУсловие, "DepositSumMax");
			ПараметрыУсловия.СрокМинимум = ЗначениеСвойстваXDTO(ТекущееУсловие, "PeriodDayMin");
			ПараметрыУсловия.СрокМаксимум = ЗначениеСвойстваXDTO(ТекущееУсловие, "PeriodDayMax");
			ПараметрыУсловия.Ставка = ЗначениеСвойстваXDTO(ТекущееУсловие, "Rate");
			ПараметрыУсловия.Продвигаемый = ЗначениеСвойстваXDTO(ТекущееУсловие, "Promoted", Ложь, "Булево");
			СписокОрганизаций = ЗначениеСвойстваXDTO(ТекущееУсловие, "PersonalOfferFor.CustomerID", , , Истина);
			
			Если СписокОрганизаций <> Неопределено И СписокОрганизаций.Количество() > 0 Тогда
				
				СоответствиеИдКлиентовБанкаОрганизациям = ПараметрыБанка.СоответствиеИдКлиентовБанкаОрганизациям;
				
				Для каждого ТекущаяОрганизация Из СписокОрганизаций Цикл
					
					ОрганизацияСсылка = СоответствиеИдКлиентовБанкаОрганизациям.Получить(ТекущаяОрганизация);
					
					Если ОрганизацияСсылка <> Неопределено Тогда
						
						ПараметрыУсловияПоОрганизации = НовыеПараметрыОписанияУсловияПродуктаИзОтветаБанка();
						ЗаполнитьЗначенияСвойств(ПараметрыУсловияПоОрганизации, ПараметрыУсловия);
						ПараметрыУсловияПоОрганизации.Организация = ОрганизацияСсылка;
						МассивУсловий.Добавить(ПараметрыУсловияПоОрганизации);
						
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				
				МассивУсловий.Добавить(ПараметрыУсловия);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировкиПродукты = БлокировкаДанных.Добавить("РегистрСведений.ДепозитныеПродуктыОбменСБанками");
	ЭлементБлокировкиПродукты.УстановитьЗначение("Банк", Банк);
	ЭлементБлокировкиУсловия = БлокировкаДанных.Добавить("РегистрСведений.УсловияДепозитныхПродуктовОбменСБанками");
	ЭлементБлокировкиУсловия.УстановитьЗначение("Банк", Банк);
	
	НачатьТранзакцию();
	
	Попытка
	
		БлокировкаДанных.Заблокировать();
		НаборЗаписейРегистра = РегистрыСведений.ДепозитныеПродуктыОбменСБанками.СоздатьНаборЗаписей();
		НаборЗаписейРегистра.Отбор.Банк.Установить(Банк);
		
		Если МассивПараметрыПредложений.Количество() = 0 Тогда
			
			ТекущаяЗаписьНабора = НаборЗаписейРегистра.Добавить();
			ТекущаяЗаписьНабора.Банк = Банк;
			ТекущаяЗаписьНабора.КлючПродукта = ПустойУникальныйИдентификатор();
			ТекущаяЗаписьНабора.ДатаАктуальности = ТекущаяДатаСеанса();
			ТекущаяЗаписьНабора.ПредложенияОтсутствуют = Истина;
			
		Иначе
			
			Для Каждого ТекущееПредложение Из МассивПараметрыПредложений Цикл
				ТекущаяЗаписьНабора = НаборЗаписейРегистра.Добавить();
				ЗаполнитьЗначенияСвойств(ТекущаяЗаписьНабора, ТекущееПредложение);
			КонецЦикла;
			
		КонецЕсли;
		
		НаборЗаписейРегистра.Записать();
		
		НаборЗаписейРегистра = РегистрыСведений.УсловияДепозитныхПродуктовОбменСБанками.СоздатьНаборЗаписей();
		НаборЗаписейРегистра.Отбор.Банк.Установить(Банк);
		
		Для Каждого ТекущееУсловие Из МассивУсловий Цикл
			ТекущаяЗаписьНабора = НаборЗаписейРегистра.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяЗаписьНабора, ТекущееУсловие);
		КонецЦикла;
		
		НаборЗаписейРегистра.Записать();
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение ИнформацияОбОшибке();
		
	КонецПопытки;
		
КонецПроцедуры

// Функция-конструктор параметров условия депозитного продукта, 
// для функции ПолучитьДанныеДепозитныхПродуктов.
//
// Возвращаемое значение:
//  Структура - содержит поля:
//   * ИдентификаторУсловия - Строка - идентификатор условия продукта банка;
//   * СуммаМинимум - Число - минимальная сумма депозита;
//   * СуммаМаксимум - Число - максимальная сумма депозита;
//   * СрокМинимум - Число - минимальный срок депозита в днях;
//   * СрокМаксимум - Число - максимальный срок депозита в днях;
//   * Ставка - Число - процентная ставка депозита;
//   * Продвигаемый - Булево - приоритетное условие для продукта банка.
//
Функция НовыеПараметрыУсловияДепозитногоПродукта()
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторУсловия", "");
	Результат.Вставить("СуммаМинимум", 0);
	Результат.Вставить("СуммаМаксимум", 0);
	Результат.Вставить("СрокМинимум", 0);
	Результат.Вставить("СрокМаксимум", 0);
	Результат.Вставить("Ставка", 0);
	Результат.Вставить("Продвигаемый", Ложь);
	Возврат Результат;
	
КонецФункции

// Функция-конструктор параметров депозитного продукта,
// для чтения из ответа на запрос депозитных продуктов банка.
// см. ПрочитатьОтветИОбновитьПредложенияБанка.
//
// Возвращаемое значение:
//  Структура - содержит поля:
//    ** КлючПродукта - Строка - уникальный идентификатор депозитного продукта;
//    ** ИдентификаторПродукта - Строка - идентификатор депозитного продукта в системе банка;
//    ** Банк - ОпределяемыйТип.БанкОбменСБанками, Неопределено - ссылка на банк;
//    ** Валюта - Строка - код валюты;
//    ** Наименование - Строка - наименование продукта;
//    ** ВариантРасчета - Строка - код метода расчета процентов по депозитам;
//    ** Капитализация - Булево - если Истина, используется капитализация;
//    ** ПодробноеОписание - Строка - подробное описание продукта полученное от банка;
//    ** НаименованиеБанка - Строка - наименование банка;
//    ** ВыплатаПроцентов - Строка - код способа выплаты процентов;
//    ** ДатаАктуальности - Дата - дата получения предложений.
//
Функция НовыеПараметрыОписанияПродуктаИзОтветаБанка()
	
	Результат = Новый Структура;
	Результат.Вставить("КлючПродукта", "");
	Результат.Вставить("ИдентификаторПродукта", "");
	Результат.Вставить("Банк", Неопределено);
	Результат.Вставить("Валюта", "");
	Результат.Вставить("Наименование", "");
	Результат.Вставить("ВариантРасчета", "");
	Результат.Вставить("Капитализация", Ложь);
	Результат.Вставить("ПодробноеОписание", "");
	Результат.Вставить("НаименованиеБанка", "");
	Результат.Вставить("ВыплатаПроцентов", "");
	Результат.вставить("ДатаАктуальности", Дата(1, 1, 1));
	Возврат Результат;
	
КонецФункции

// Функция-конструктор параметров условия депозитного продукта, 
// для чтения из ответа на запрос депозитных продуктов банка.
// см. ПрочитатьОтветИОбновитьПредложенияБанка.
//
// Возвращаемое значение:
//  Структура - содержит поля:
//   * КлючПродукта - Строка - уникальный идентификатор депозитного продукта банка;
//   * ИдентификаторСтроки - Строка - уникальный идентфиикатор условия продукта банка;
//   * Банк - ОпределяемыйТип.БанкОбменСБанками, Неопределено - ссылка на банк;
//   * Организация - ОпределяемыйТип.Организация, Неопределено - ссылка на организацию для которой доступно это условие, 
//        если не заполнено условие доступно всем клиентам банка;
//   * СуммаМинимум - Число - минимальная сумма депозита;
//   * СуммаМаксимум - Число - максимальная сумма депозита;
//   * СрокМинимум - Число - минимальный срок депозита в днях;
//   * СрокМаксимум - Число - максимальный срок депозита в днях;
//   * Ставка - Число - процентная ставка депозита;
//   * Продвигаемый - Булево -  приоритетное условие для продукта банка.
//
Функция НовыеПараметрыОписанияУсловияПродуктаИзОтветаБанка()
	
	Результат = Новый Структура;
	Результат.Вставить("КлючПродукта", "");
	Результат.Вставить("ИдентификаторСтроки", "");
	Результат.Вставить("Банк", Неопределено);
	Результат.Вставить("Организация", Неопределено);
	Результат.Вставить("СуммаМинимум", 0);
	Результат.Вставить("СуммаМаксимум", 0);
	Результат.Вставить("СрокМинимум", 0);
	Результат.Вставить("СрокМаксимум", 0);
	Результат.Вставить("Ставка", 0);
	Результат.Вставить("Продвигаемый", Ложь);
	Возврат Результат;
	
КонецФункции

Функция МетодыРасчетаПроцентовДепозита(КодМетода)
	
	Результат = Перечисления.МетодыРасчетаПроцентовДепозитовОбменСБанками.ПустаяСсылка();
	
	Если КодМетода = "00" Тогда
		Результат = Перечисления.МетодыРасчетаПроцентовДепозитовОбменСБанками.РасчетНаСторонеБанка;
	ИначеЕсли КодМетода = "01" Тогда
		Результат = Перечисления.МетодыРасчетаПроцентовДепозитовОбменСБанками.ПростыеПроценты;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПодходящиеПредложенияБанка

Функция НайтиУсловиеПоИдентификатору(УсловияПродукта, ИдентификаторУсловия)
	
	Результат = Неопределено;
	
	Для Каждого ТекущееУсловие Из УсловияПродукта Цикл
		Если ТекущееУсловие.ИдентификаторУсловия = ИдентификаторУсловия Тогда
			Результат = ТекущееУсловие;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Функция-конструктор свойств депозитного продукта для списка выбора поля предложений банков
// см. СформироватьМассивДляСпискаВыбораПредложений
//
// Возвращаемое значение:
//  Структура:
//   * Банк - ОпределяемыйТип.БанкОбменСБанками, Неопределено - ссылка на банк.
//   * Валюта - СправочникСсылка.Валюты, Неопределено - валюта депозита.
//   * ВариантРасчета - ПеречислениеСсылка.МетодыРасчетаПроцентовДепозитовОбменСБанками - способ расчета процентов.
//   * ВыплатаПроцентов - ПеречислениеСсылка.СпособыВыплатыПроцентовДепозитовОбменСБанками - способ выплаты процентов.
//   * Капитализация - Булево - если Истина, продукт использует капитализацию процентов.
//   * КлючПродукта - Строка - уникальный идентификатор депозитного продукта.
//   * Наименование - Строка - наименование продукта банка.
//   * НаименованиеБанка - Строка - наименование бака.
//   * ИдентификаторУсловия - Строка - идентификатор условия.
//   * НеВыполненыУсловия - Булево - если Истина, условия продукта не выполняется.
//   * ОписаниеПродукта - Строка -  описание депозитного продукта полученное от банка.
//   * Срок - Число - срок депозита в днях.
//   * Сумма - Число - сумма депозита.
//   * СуммаМинимум - Число - минимальная сумма депозита.
//   * СуммаМаксимум - Число - максимальная сумма депозита.
//   * СрокМинимум - Число - минимальный срок депозита в днях.
//   * СрокМаксимум - Число - максимальный срок депозита в днях.
//   * Ставка - Число - процентная ставка депозита.
//   * Доход - Число - доход по депозиту.
//   * РасчетПриОформлении - Булево - если Истина, расчет доходности выполняется в банке.
// 
Функция НовыеДополнительныеДанныеСпискаПредложений()
	
	Результат = Новый Структура;
	Результат.Вставить("Банк", Неопределено);
	Результат.Вставить("Валюта", Неопределено);
	Результат.Вставить("ВариантРасчета", "");
	Результат.Вставить("ВыплатаПроцентов", "");
	Результат.Вставить("Капитализация", Ложь);
	Результат.Вставить("КлючПродукта", "");
	Результат.Вставить("Наименование", "");
	Результат.Вставить("НаименованиеБанка", "");
	Результат.Вставить("ИдентификаторУсловия", "");
	Результат.Вставить("НеВыполненыУсловия", Ложь);
	Результат.Вставить("ОписаниеПродукта", "");
	Результат.Вставить("Срок", 0);
	Результат.Вставить("Сумма", 0);
	Результат.Вставить("СуммаМинимум", 0);
	Результат.Вставить("СуммаМаксимум", 0);
	Результат.Вставить("СрокМинимум", 0);
	Результат.Вставить("СрокМаксимум", 0);
	Результат.Вставить("Ставка", 0);
	Результат.Вставить("Доход", 0);
	Результат.Вставить("РасчетПриОформлении", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗапросРасчетаДепозитаВБанке

// Функция-конструктор свойств данных расчета депозита в банке
// для функции ПрочитатьРасчетыПоДепозиту
// 
// Возвращаемое значение:
//  СуммаДепозита - Число - сумма депозита.
//  Срок - Число - срок депозита в днях.
//  Ставка - Число - процентная ставка депозита.
//  СуммаДоход - Число - сумма дохода.
//  ДатаЗакрытия - Дата - дата закрытия депозита.
//
Функция НовыеДанныеРасчетаДепозита()
	
	Результат = Новый Структура;
	Результат.Вставить("СуммаДепозита", 0);
	Результат.Вставить("Срок", 0);
	Результат.Вставить("Ставка", 0);
	Результат.Вставить("СуммаДоход", 0);
	Результат.Вставить("ДатаЗакрытия", Дата(1, 1, 1));
	
	Возврат Результат;
	
КонецФункции

// Читает ответ банка на запрос расчета депозита
// 
// Параметры:
//  РасчетДепозита - ОбъектXDTO - ответ банка на запрос расчета депозита.
// 
// Возвращаемое значение:
//  Структура:
//   * ПараметрыДепозита - Структура: см. НовыеДанныеРасчетаДепозита.
//   * ОшибкиДепозита - Структура - ошибки расчета депозита, переданные банком. Может содержать поля:
//      ** КодОшибки - Строка - код ошибки расчета депозита по классификатору.
//      ** Описание - Строка - описание ошибки.
//      ** ДополнительнаяИнформация - Строка - дополнительная информация от банка.
//
Функция ПрочитатьРасчетыПоДепозиту(РасчетДепозита)

	ПараметрыДепозита = НовыеДанныеРасчетаДепозита();
	
	ДанныеРасчета = ЗначениеСвойстваXDTO(РасчетДепозита, "Data");
	Если ДанныеРасчета <> Неопределено Тогда
		
		ПараметрыДепозита.СуммаДепозита = ЗначениеСвойстваXDTO(ДанныеРасчета, "DepositSum",,"Число");
		ПараметрыДепозита.Срок = ЗначениеСвойстваXDTO(ДанныеРасчета, "PeriodDay",,"Число");
		ПараметрыДепозита.Ставка = ЗначениеСвойстваXDTO(ДанныеРасчета, "Rate",,"Число");
		ПараметрыДепозита.СуммаДоход = ЗначениеСвойстваXDTO(ДанныеРасчета, "ProfitSum",,"Число");
		ПараметрыДепозита.ДатаЗакрытия = ЗначениеСвойстваXDTO(ДанныеРасчета, "ClosingDate",,"Дата");
		
	КонецЕсли;
	
	ОшибкиПоДепозиту = Новый Структура;
	Ошибка = ЗначениеСвойстваXDTO(РасчетДепозита, "Error");
	Если Ошибка <> Неопределено Тогда
		
		ОшибкиПоДепозиту.Вставить("КодОшибки", ЗначениеСвойстваXDTO(Ошибка, "Code"));
		ОшибкиПоДепозиту.Вставить("Описание", ЗначениеСвойстваXDTO(Ошибка, "Description"));
		ОшибкиПоДепозиту.Вставить("ДополнительнаяИнформация", ЗначениеСвойстваXDTO(Ошибка, "MoreInfo"));
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыДепозита", ПараметрыДепозита);
	Результат.Вставить("ОшибкиДепозита", ОшибкиПоДепозиту);
	
	Возврат Результат;
	
КонецФункции

#конецОбласти

#Область Прочее

// Отправляет в банк информацию переданную в Данных, помещая результат отправки в Результат
//
// Параметры:
//  Настройки - Структура - параметры для отправки POST Запроса:
//   * Адрес - Строка - адрес сервера банка, URI;
//   * Ресурс - Строка - адрес ресурса выполняющего обработку информации, например Logon, SendPack;
//   * ВерсияФормата - Строка - версия формата обмена, например 2.1.3.
//  Данные - ДвоичныеДанные - тело запроса:
//   - Строка - тело запроса строкой;
//   - Неопределено - отправить запрос без тела.
//  Результат - Структура - структура со свойствами:
//   * Статус - Булево - результат получения файла;
//   * Тело - ДвоичныеДанные, Неопределено - данные ответа сервера;
//   * СообщениеОбОшибке - Строка, Неопределено - сообщение об ошибке, если статус Ложь;
//   * КодСостояния - Число, Неопределено - код состояния HTTP-ответа. Наличие кода означает, что был ответ от сервера.
//  ОбъектЛогирования - СправочникСсылка.НастройкиОбменСБанками, ОпределяемыйТип.БанкОбменСБанками - текущая настройка
//       обмена или банк настройки обмена;
//  ТекстОшибки - Строка, Неопределено - сообщение об ошибке.
//
Процедура ОтправитьВБанкБезАвторизации(Настройки, Данные, Результат, ОбъектЛогирования, ТекстОшибки)
	
	Заголовки = Новый Соответствие;

	Заголовки.Вставить("Content-Type", "application/xml; charset=utf-8");

	Если Настройки.Свойство("ВерсияФормата") Тогда
		Заголовки.Вставить("APIVersion", Настройки.ВерсияФормата);
	КонецЕсли;
	
	РезультатВыполнения = ОбменСБанкамиСлужебный.ОтправитьPOSTЗапрос(
		Настройки.Адрес, Настройки.Ресурс, Заголовки, Данные, , , ОбъектЛогирования);

	Если РезультатВыполнения.Статус Тогда
		Результат = РезультатВыполнения.Тело;
	Иначе
		Если ЗначениеЗаполнено(РезультатВыполнения.КодСостояния) Тогда
			Шаблон = НСтр("ru = 'При отправке документа в банк произошла ошибка (%1). %2'");
			ТекстОшибки = СтрШаблон(Шаблон, РезультатВыполнения.КодСостояния, РезультатВыполнения.СообщениеОбОшибке);
			ШаблонПодробныйТекстОшибки = НСтр("ru = 'При отправке в банк произошла ошибка.
				|Код ошибки: %1
				|Сообщение: %2
				|Тело ответа: %3'");
			Операция = НСтр("ru = 'Отправка документа в банк'");
			ПодробныйТекстОшибки = СтрШаблон(ШаблонПодробныйТекстОшибки, РезультатВыполнения.КодСостояния,
				РезультатВыполнения.СообщениеОбОшибке, РезультатВыполнения.Тело);
			ОбменСБанкамиСлужебный.ОбработатьОшибку(Операция, ПодробныйТекстОшибки, , ОбъектЛогирования);
		Иначе
			ТекстОшибки = РезультатВыполнения.СообщениеОбОшибке;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// см. РаботаСФайламиБЭД.ЗначениеСвойстваXDTO
//
Функция ЗначениеСвойстваXDTO(ОбъектXDTO, ИмяСвойства, ЗначениеПоУмолчанию = Неопределено, Тип = Неопределено, 
		ЭтоСписок = Ложь) Экспорт
	Результат = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ОбъектXDTO, ИмяСвойства, ЗначениеПоУмолчанию, Неопределено, 
			ЭтоСписок);
	Если Тип = "Число" Тогда
		Результат = ОбменСБанкамиКлиентСервер.ЧислоИзСтроки(Результат);
	ИначеЕсли Тип = "Дата" Тогда
		Результат = ДатаИзСтроки(Результат);
	КонецЕсли;
	Возврат Результат;

КонецФункции

// Преобразует строковое представление даты в форматах DD.MM.YYYY, YYYY-MM-DD в дату.
//
// Параметры:
//  Строка - Строка - представление даты.
// Возвращаемое значение:
//  Дата - преобразованное значение.
//
Функция ДатаИзСтроки(Строка) Экспорт
	
	Если ТипЗнч(Строка) = Тип("Дата") Тогда
		Возврат Строка;
	КонецЕсли;
	
	ЧастьДаты    = "";
	ЧастьВремени = "000000";
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Сред(Строка, 3, 1)) Тогда
		ЧастьДаты = Сред(Строка, 7, 4) + Сред(Строка, 4, 2) + Сред(Строка, 1, 2);
		// заполним дополнительно время для формата "dd.MM.yyyy HH.mm.ss".
		Если СтрДлина(Строка) = 19 Тогда
			ЧастьВремени = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(".:", Сред(Строка, 12), "");
		КонецЕсли;
	Иначе
		ЧастьДаты = Сред(Строка, 1, 4) + Сред(Строка, 6, 2) + Сред(Строка, 9, 2);
		// заполним дополнительно время для формата 20 символов.
		Если СтрДлина(Строка) = 20 Тогда
			ЧастьВремени = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(".:", Сред(Строка, 12), "");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Дата(ЧастьДаты + ЧастьВремени);
	
КонецФункции

// см. ОбменСБанкамиСлужебный.ДвоичныеДанныеИзXDTO
//
Функция ДвоичныеДанныеИзXDTO(Фабрика, ЭД, УказаниеТипа = Истина, ЛокальноеИмя = Неопределено)
	Возврат ОбменСБанкамиСлужебный.ДвоичныеДанныеИзXDTO(Фабрика, ЭД, УказаниеТипа, ЛокальноеИмя);
КонецФункции

// см. ЗаполнитьСвойствоXDTO
//
Процедура ЗаполнитьСвойствоXDTO(ОбъектXDTO, ИмяСвойства, Значение, Обязательное = Ложь, ТекстОшибки = "",
	УстанавливатьПустыеЗначения = Ложь) Экспорт
	ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ОбъектXDTO, ИмяСвойства, Значение, Обязательное, ТекстОшибки,
		УстанавливатьПустыеЗначения);
КонецПроцедуры

Функция ПустойУникальныйИдентификатор()
	Возврат Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
КонецФункции

// Функция формирует описание ошибки получения предложений банков
// 
// Параметры:
//  ТаблицаОшибок - ТаблицаЗначений:
//   * Банк - ОпределяемыйТип.БанкОбменСБанками - ссылка на банк.
//   * Ошибка - Строка - описание ошибки получения предложений банка по депозитам.
//  ДатаНачала - Дата - дата начала получения предложений банка по депозитам.
// 
// Возвращаемое значение:
//  Строка, Неопределено - сообщение об ошибке.
//
Функция ПроверитьОшибкиПолученияПредложенийИСформироватьСтрокуОшибки(ТаблицаОшибок, ДатаНачала)
	
	ТекстСообщения = "";
	СтрокаБанков = "";
	СообщениеОбОшибке = Неопределено;
	
	КоличествоУникальныхОшибок = 0;
	КоличествоОшибок = ТаблицаОшибок.Количество();
	
	Если КоличествоОшибок = 0 Тогда
		Возврат СообщениеОбОшибке;
	КонецЕсли;
	
	Если КоличествоОшибок > 1 Тогда
		МассивОшибок = ТаблицаОшибок.ВыгрузитьКолонку("Ошибка");
		МассивУникальныхОшибок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивОшибок);
		КоличествоУникальныхОшибок = МассивУникальныхОшибок.Количество();
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru = 'Не удалось получить предложения по депозитам от банка: %1.
		|По причине:
		|%2'");
	
	СтрокаБанков = "";
	
	Если КоличествоОшибок > 1 Тогда
		МассивБанковСОшибкой = ТаблицаОшибок.ВыгрузитьКолонку("Банк");
		СтрокаБанков = СтрСоединить(МассивБанковСОшибкой, ", ");
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаБанков) Тогда
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ТаблицаОшибок[0].Банк, ТаблицаОшибок[0].Ошибка);
	ИначеЕсли КоличествоУникальныхОшибок = 1 Тогда
		
		ШаблонСообщения = НСтр("ru = 'Не удалось получить предложения по депозитам от банков: %1.
			|По причине:
			|%2'");
			
		ТекстСообщения = СтрШаблон(ШаблонСообщения, СтрокаБанков, ТаблицаОшибок[0].Ошибка);
	Иначе 
		
		ШаблонСообщения = НСтр("ru = 'Не удалось получить предложения по депозитам от банков: %1.
			|Произошла ошибка'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, СтрокаБанков);
		
	КонецЕсли;
	
	Если ПравоДоступа("Просмотр", Метаданные.Обработки.ЖурналРегистрации) Тогда
	
		Отбор = Новый Структура("Пользователь, СобытиеЖурналаРегистрации, ДатаНачала,
			|ДатаОкончания, Данные, Сеанс, Уровень");
		Отбор.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
		Отбор.СобытиеЖурналаРегистрации = "Электронное взаимодействие.Обмен с банками";
		Отбор.ДатаНачала = ДатаНачала;
		Отбор.ДатаОкончания = КонецДня(ДатаНачала);
		Отбор.Уровень = "Ошибка";
		СтрокаПредставлениеНавигационнойСсылки = 
			СтрШаблон("DepositOffers %1 %2", ДатаНачала, Отбор.Пользователь);
		
		СообщениеОбОшибке = СтрШаблон(
			НСтр("ru = '%1, см. <a href = ""%2"">журнал регистрации.</a>'"),
			ТекстСообщения, 
			ПолучитьНавигационнуюСсылку(Метаданные.Обработки.ЖурналРегистрации, 
				СтрокаПредставлениеНавигационнойСсылки, Отбор));
			
	Иначе
			
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = '%1. Обратитесь к администратору или техническому специалисту'"),
			ТекстСообщения);
			
	КонецЕсли;
	
	Возврат СообщениеОбОшибке;
	
КонецФункции

// Функция проверяет наличие заявок на депозит, которые находятся в обработке в банке 
// и по которым еще не получено извещение о состоянии депозита.
// 
// Параметры:
//  Банк - ОпределяемыйТип.БанкОбменСБанками - ссылка на банк.
//
// Возвращаемое значение:
//  Булево - если Истина, есть заявки на депозит в состоянии обработки.
//
Функция ЕстьЗаявкиНаДепозитОжидающиеИсполнения(Банк, ДатаНачала) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("Банк", Банк);
	Запрос.УстановитьПараметр("СостоянияВОбработке", СостоянияЭДЗаявкиНаДепозитПоСтроке("ВОбработке"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЗаявкаНаДепозитОбменСБанками.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаДепозитОбменСБанками КАК ЗаявкаНаДепозитОбменСБанками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОбменСБанками КАК СостоянияОбменСБанками
	|		ПО ЗаявкаНаДепозитОбменСБанками.Ссылка = СостоянияОбменСБанками.СсылкаНаОбъект
	|			И (ЗаявкаНаДепозитОбменСБанками.Дата >= &ДатаНачала)
	|			И (ЗаявкаНаДепозитОбменСБанками.Банк = &Банк)
	|			И (ЗаявкаНаДепозитОбменСБанками.Подтвержден = ЛОЖЬ)
	|			И (ЗаявкаНаДепозитОбменСБанками.ПометкаУдаления = ЛОЖЬ)
	|			И (СостоянияОбменСБанками.Состояние В (&СостоянияВОбработке))";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецОбласти