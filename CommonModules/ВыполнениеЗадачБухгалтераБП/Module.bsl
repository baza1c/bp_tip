#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриОпределенииНалоговыхПлатежей(НалоговыеПлатежи, ТаблицаЗадач) Экспорт
	
	НалоговыеПлатежи = Обработки.ПлатежныйКалендарь.НоваяТаблицаПлатежи();
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		
		Платеж = НалоговыеПлатежи.Добавить();
		
		ЗаполнитьЗначенияСвойств(Платеж, Задача);
		
		Платеж.Расшифровка = ВРег(Лев(Платеж.Расшифровка, 1)) + Сред(Платеж.Расшифровка, 2);
		
		Платеж.ПараметрыКоманды = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
		
		Если Задача.Просрочен Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Платеж.ПараметрыКоманды, Задача);
		
		ИмяЗадачи = Задача.ИдентификаторЗадачи;
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Задача.Правило);
		
		Если ИмяЗадачи = "СтраховыеВзносы" Тогда
			
			ДополнитьПлатежУплатаСтраховыхВзносов(Платеж, Задача);
			
		ИначеЕсли ПолноеИмяПравила = "ТранспортныйНалог.2013_УплатаАванс"
			Или ПолноеИмяПравила = "ЗемельныйНалог.2013_Аванс" 
			Или ПолноеИмяПравила = "ЗемельныйНалог.2015_Аванс" 
			Или ПолноеИмяПравила = "НалогНаИмущество.2013_Аванс" Тогда
			
			ДополнитьПлатежУплатаАвансовПоИмущественнымНалогам(Платеж, Задача);
			
		ИначеЕсли ИмяЗадачи = "СтраховыеВзносы_Предприниматель" Тогда
			
			ДополнитьПлатежУплатаСтраховыхВзносовИП(Платеж, Задача);
			
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетаНалогаУСН(Задача.Правило) Тогда
			
			ДополнитьПлатежУплатаНалогаУСН(Платеж, Задача);
			
		ИначеЕсли ПолноеИмяПравила = "ТорговыйСбор.2015_Уплата" Тогда
			
			ДополнитьПлатежУплатаТорговогоСбора(Платеж, Задача);
			
		ИначеЕсли ТипЗнч(Задача.Правило) = Тип("СправочникСсылка.Патенты") Тогда
			
			ДополнитьПлатежОплатаПатента(Платеж, Задача);
			
		ИначеЕсли СтрНачинаетсяС(ПолноеИмяПравила, "НДС.2015_Уплата") Тогда
			
			ДополнитьПлатежУплатаНДС(Платеж, Задача);
			
		ИначеЕсли ИнтерфейсыВзаимодействияБРО.ТребованиеОбрабатывается(ПолноеИмяПравила) Тогда
			
			ДополнитьПлатежПоДаннымДекларации(Платеж, Задача);
			
		КонецЕсли;
		
		Если Платеж.Сумма = 0 И НЕ ЗначениеЗаполнено(Платеж.Примечание) Тогда
			Платеж.Примечание = НСтр("ru = 'Нет данных для определения суммы платежа'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗавершитьПодчиненныеЗадачиЕдиногоНалоговогоСчета(Задача) Экспорт

	Если Задача.ИдентификаторЗадачи <> "ЕдиныйНалоговыйСчет" Тогда
		Возврат;
	КонецЕсли;

	ПараметрыЗадачи = Обработки.ПомощникУплатыНалога.НовыеПараметрыЗадачи();
	ЗаполнитьЗначенияСвойств(ПараметрыЗадачи, Задача);
	ТаблицаПодчиненныхЗадач = Обработки.ПомощникУплатыНалога.ПодчиненныеЗадачиПоЕдиномуНалоговомуСчету(ПараметрыЗадачи);

	Для Каждого ПодчиненнаяЗадача Из ТаблицаПодчиненныхЗадач Цикл
		ЗадачаСоСтатусом = РегистрыСведений.ЗадачиБухгалтера.НовыйЗадачаСоСтатусом();
		ЗаполнитьЗначенияСвойств(ЗадачаСоСтатусом, ПодчиненнаяЗадача);

		ВыполнениеЗадачБухгалтера.ЗаписатьНовоеСостояниеЗадачи(
			ЗадачаСоСтатусом,
			Истина,
			ЗадачаСоСтатусом.Статус);
		КонецЦикла;
		
КонецПроцедуры

Функция ПодготовленноеЗаявлениеНаПолучениеПатентаИзЗадачи(Правило, ПериодСобытия, СозданиеПатента = Ложь) Экспорт
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтераПродлениеПатента.Заявление КАК Заявление
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераПродлениеПатента КАК ЗадачиБухгалтераПродлениеПатента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОСпецрежимахНалогообложения КАК УведомлениеОСпецрежимахНалогообложения
	|		ПО ЗадачиБухгалтераПродлениеПатента.Заявление = УведомлениеОСпецрежимахНалогообложения.Ссылка
	|		И НЕ УведомлениеОСпецрежимахНалогообложения.ПометкаУдаления
	|ГДЕ
	|	ЗадачиБухгалтераПродлениеПатента.Правило = &Правило
	|
	|УПОРЯДОЧИТЬ ПО
	|	УведомлениеОСпецрежимахНалогообложения.ДатаПодписи УБЫВ");
	
	УсловияЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор;
	Если СозданиеПатента Тогда
		ТекстУсловия = "ЗадачиБухгалтераПродлениеПатента.ПериодСобытия < &ПериодСобытия";
	Иначе
		ТекстУсловия = "ЗадачиБухгалтераПродлениеПатента.ПериодСобытия = &ПериодСобытия";
	КонецЕсли;
	УсловияЗапроса.Добавить(ТекстУсловия);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Правило", Правило);
	Запрос.УстановитьПараметр("ПериодСобытия", ПериодСобытия);
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Заявление;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЗаполнитьПоДаннымНастройки(Настройка, ДанныеПлатежногоДокумента, ИмяИсходного, ИмяРеквизита = "") Экспорт
	
	Если ПустаяСтрока(ИмяРеквизита) Тогда
		ИмяРеквизита = ИмяИсходного;
	КонецЕсли;
	
	РеквизитОбъектЗаполненИсходнымиДанными = Ложь;
	Если Настройка.Свойство(ИмяИсходного) И ЗначениеЗаполнено(Настройка[ИмяИсходного]) Тогда
		ДанныеПлатежногоДокумента.Вставить(ИмяРеквизита, Настройка[ИмяИсходного]);
		РеквизитОбъектЗаполненИсходнымиДанными = Истина;
	КонецЕсли;
	
	Возврат РеквизитОбъектЗаполненИсходнымиДанными;
	
КонецФункции

Функция ДополнитьТекстЗапросаПлатежиПоДекларации(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка) КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ОтборПравило
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&Декларация) КАК СвязанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежныйДокумент
	|		ПО СвязанныеДокументы.Ссылка = ПлатежныйДокумент.Ссылка
	|			И (ПлатежныйДокумент.ПоказательПериода В (&ПоказательПериода))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение,
	|	ЗадачиБухгалтераНалоговыеПлатежи.РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежныйДокумент
	|		ПО ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение = ПлатежныйДокумент.Ссылка
	|			И (ПлатежныйДокумент.ПоказательПериода В (&ПоказательПериода, &ПоказательПериодаОсобыйПорядок))
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация = &Организация
	|	И ЗадачиБухгалтераНалоговыеПлатежи.Правило = &Правило
	|	И ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия = &ПериодСобытия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции
 
Функция ДополнитьТекстЗапросаПлатежи(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение КАК Ссылка,
	|	ЗадачиБухгалтераНалоговыеПлатежи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ОтборПравило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация = &Организация
	|	И ЗадачиБухгалтераНалоговыеПлатежи.Правило = &Правило
	|	И ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия = &ПериодСобытия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов, Налог, ВидНалоговогоОбязательства = Неопределено, ПериодСобытия, ЗаполнятьКБК) Экспорт

	Если ВидНалоговогоОбязательства = Неопределено Тогда
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	КонецЕсли;

	СчетУчетаВзносов = Справочники.ВидыНалоговИПлатежейВБюджет.СчетУчета(Налог, ПериодСобытия);
	Если ЗначениеЗаполнено(СчетУчетаВзносов) Тогда
		НоваяСтрока = ТаблицаСчетовСтраховыхВзносов.Добавить();
		НоваяСтрока.Налог                      = Налог;
		НоваяСтрока.ВидНалоговогоОбязательства = ВидНалоговогоОбязательства;
		НоваяСтрока.СчетУчета                  = СчетУчетаВзносов;
		Если ЗаполнятьКБК Тогда
			НоваяСтрока.КБК                        = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог, ВидНалоговогоОбязательства, ПериодСобытия);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ПоказательПериодаПлатежаПоПравилу(ПериодичностьПравила, Срок, ПериодСобытия) Экспорт

	ПоказательПериода = "";

	// Определим период, к которому относится платеж,
	// в соответствии с классификатором, используемым в платежных поручениях
	ПериодичностьПоКлассификатору = ПлатежиВБюджетПереопределяемый.ПериодичностьПоКлассификатору(ПериодичностьПравила);

	Если ПериодичностьПоКлассификатору = ПлатежиВБюджетКлиентСервер.ПлатежПоКонкретнойДате() Тогда
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(Срок, ПериодичностьПоКлассификатору);
	Иначе
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(КонецКвартала(ПериодСобытия), ПериодичностьПоКлассификатору);
	КонецЕсли;

	Возврат ПоказательПериода;

КонецФункции

Функция СвязанныеЗадачиПодготовкиРегулярныхПлатежей(РеквизитыДокумента) Экспорт
	
	Если ТипЗнч(РеквизитыДокумента.Ссылка) <> Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", РеквизитыДокумента.Организация);
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДокумента.Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегулярныеПлатежи.Организация КАК Организация,
	|	РегулярныеПлатежи.Правило КАК Правило,
	|	РегулярныеПлатежи.ПериодСобытия КАК ПериодСобытия,
	|	ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка) КАК РегистрацияВНалоговомОргане,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж) КАК Действие
	|ИЗ
	|	РегистрСведений.РегулярныеПлатежи КАК РегулярныеПлатежи
	|ГДЕ
	|	РегулярныеПлатежи.Организация = &Организация
	|	И РегулярныеПлатежи.ПлатежноеПоручение = &Ссылка
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СвязанныеЗадачиУплатыНалога(РеквизитыДокумента, ПараметрыРасчетаСтатусов) Экспорт
	
	ТаблицаЗадач = ВыполнениеЗадачБухгалтера.НоваяТаблицаЗадачСвязанныхСПлатежами();
	
	ДокументОбрабатывается = ЭтоУплатаНалога(РеквизитыДокумента.ВидОперации)
		И РеквизитыДокумента.ВидНалога <> Перечисления.ВидыНалогов.НДФЛ;
	
	Если Не ДокументОбрабатывается Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	ТекущаяЗадача = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииТекущейЗадачиНалоговогоПлатежа(ТекущаяЗадача, РеквизитыДокумента);
	
	НоваяЗадача = ВыполнениеЗадачБухгалтера.НоваяЗадачаНалоговогоПлатежа(РеквизитыДокумента, ТекущаяЗадача, ПараметрыРасчетаСтатусов);
	
	ПропуститьОбновлениеСвязиДокументЗадача = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыРасчетаСтатусов, "ПропуститьОбновлениеСвязиДокументЗадача", Ложь);
	
	// Зарегистрируем новое состояние платежа в регистре исполнения задач ЗадачиБухгалтераНалоговыеПлатежи.
	
	Если НоваяЗадача <> Неопределено Тогда
		
		// Новое исполнение при записи заместит предыдущее.
		ОплатаЗадолженности = Ложь;
		ВыполнениеЗадачБухгалтераПереопределяемый.ОпределитьЭтоПогашениеНалоговойЗадолженности(ОплатаЗадолженности, РеквизитыДокумента);
		
		РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.ДобавитьЗаписьПравила(
			РеквизитыДокумента.Организация,
			РеквизитыДокумента.Ссылка,
			НоваяЗадача.Правило,
			НоваяЗадача.ПериодСобытия,
			ОплатаЗадолженности);
		
	ИначеЕсли ТекущаяЗадача <> Неопределено
		И ПлатежиПоНалогуВсегдаОбновляютСвязанныеЗадачи(РеквизитыДокумента.ВидНалога)
		И Не ПропуститьОбновлениеСвязиДокументЗадача Тогда
		
		// Для указанного налога в указанный период задачи отсутствуют.
		// Если связанные задачи по налогу обновляются автоматически, старую запись исполнения необходимо удалить.
		
		РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.УдалитьЗапись(
			РеквизитыДокумента.Организация, РеквизитыДокумента.Ссылка);
		
	КонецЕсли;
	
	// Заполним таблицу задач, для которых необходимо обновить статусы
	
	Если ТекущаяЗадача <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ТаблицаЗадач.Добавить(), ТекущаяЗадача);
	КонецЕсли;
	
	Если НоваяЗадача <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ТаблицаЗадач.Добавить(), НоваяЗадача);
	КонецЕсли;
	
	// Особые ситуации
	
	// Задача по оплате патента регистрируется только из помощника оплаты патента.
	// Авторегистрация новой задачи на оплату патента не поддерживается.
	// Поэтому патент может содержаться только в правиле текущей задачи.
	ДополнитьЗадачиОплатыПатента(ТекущаяЗадача, ТаблицаЗадач);
	
	// При уплате фиксированных взносов может измениться статус и задачи на уплату "постоянной" части в пределах года,
	// и статус задачи на уплату взносов с доходов.
	ДополнитьЗадачиУплатыФиксированныхВзносов(ТаблицаЗадач);
	
	// При уплате добровольных взносов по временной нетрудоспособности может измениться статус в пределах года
	ДополнитьЗадачиУплатыДобровольныхВзносовИП(РеквизитыДокумента.Организация, КонецГода(РеквизитыДокумента.НалоговыйПериод), ТаблицаЗадач);
	
	// При оплате 1/3 НДС нужно актуализировать все задачи по оплате 1/3 в этом квартале.
	ДополнитьЗадачиОплатыНДС(ТекущаяЗадача, ТаблицаЗадач);
	
	ТаблицаЗадач.Свернуть(ИменаКолонокСтрокой(ТаблицаЗадач.Колонки));
	
	ЗаполнитьРегистрациюВНалоговомОргане(ТаблицаЗадач, РеквизитыДокумента);
	
	Возврат ТаблицаЗадач;
	
КонецФункции

Функция ЭтоУплатаНалога(ВидОперацииДокумента) Экспорт
	
	Возврат ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога
		Или ВидОперацииДокумента = Перечисления.ВидыОперацийРКО.УплатаНалога;
	
КонецФункции

Функция ЭтоВыплатаЗарплаты(ВидОперацииДокумента) Экспорт
	
	Возврат ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
		ИЛИ ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		ИЛИ ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда
		ИЛИ ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда
		ИЛИ ВидОперацииДокумента = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям
		ИЛИ ВидОперацииДокумента = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику
		ИЛИ ВидОперацииДокумента = Перечисления.ВидыОперацийРКО.ВыплатаСотрудникуПоДоговоруПодряда;
	
КонецФункции

Функция ЭтоРасчетыПоДоходамСотрудников(ВидОперацииДокумента) Экспорт
	
	Возврат ЭтоВыплатаЗарплаты(ВидОперацииДокумента)
		Или ВидОперацииДокумента = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДивидендов
		Или ВидОперацииДокумента = Перечисления.ВидыОперацийРКО.ВыплатаДивидендов;
	
КонецФункции

Функция ПлатежиПоНалогуВсегдаОбновляютСвязанныеЗадачи(ВидНалога) Экспорт
	
	// Сейчас для большинства налогов действуют ограничения:
	//  - регистрируются в исполнении задач только платежи, содержащие бюджетные реквизиты (показатель периода и т.п.);
	//  - при изменении реквизитов платежа исполняемая задача и ее статус не обновляются.
	
	// В данной функции проверяются налоги-исключения, для которых ограничения сняты.
	
	ВидыНалоговСАвтообновлениемЗадач = ВидыНалоговСПоддержкойАвтообновленияСвязанныхЗадач();
	
	Возврат ВидыНалоговСАвтообновлениемЗадач.Найти(ВидНалога) <> Неопределено;
	
КонецФункции

// Возвращает периодичность для периодичности из показателя периода платежного поручения.
//
// Параметры:
//  ПериодичностьИзПоказателяПериода - Строка
//
// Возвращаемое значение:
//  Периодичность - ПеречислениеСсылка.Периодичность
//
Функция ПериодичностьПоПоказателюПериода(ПериодичностьИзПоказателяПериода) Экспорт
	
	Периодичность = Перечисления.Периодичность.ПустаяСсылка();
	
	Если ПериодичностьИзПоказателяПериода = ПлатежиВБюджетКлиентСервер.ПериодичностьКвартал() Тогда
		
		Периодичность = Перечисления.Периодичность.Квартал;
		
	ИначеЕсли ПериодичностьИзПоказателяПериода = ПлатежиВБюджетКлиентСервер.ПериодичностьГод() Тогда
		
		Периодичность = Перечисления.Периодичность.Год;
		
	ИначеЕсли ПериодичностьИзПоказателяПериода = ПлатежиВБюджетКлиентСервер.ПериодичностьМесяц() Тогда
		
		Периодичность = Перечисления.Периодичность.Месяц;
		
	ИначеЕсли ПериодичностьИзПоказателяПериода = ПлатежиВБюджетКлиентСервер.ПериодичностьПолугодие() Тогда
		
		Периодичность = Перечисления.Периодичность.Полугодие;
		
	ИначеЕсли ПериодичностьИзПоказателяПериода = ПлатежиВБюджетКлиентСервер.ПлатежПоКонкретнойДате() Тогда
		
		Периодичность = Перечисления.Периодичность.День;
		
	КонецЕсли;
	
	Возврат Периодичность;
	
КонецФункции

Функция НоваяЗадачаСвязаннаяСПлатежом() Экспорт
	
	ТаблицаЗадач = ВыполнениеЗадачБухгалтера.НоваяТаблицаЗадачСвязанныхСПлатежами();
	СтрокаЗадачи = ТаблицаЗадач.Добавить();
	
	Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаЗадачи);
	
КонецФункции

Функция ЭтоПлатежноеПоручениеНаОснованииСписанияСоСчета(РеквизитыПлатежа) Экспорт
	
	Возврат РеквизитыПлатежа.ВидДокумента = "ПлатежноеПоручение"
		И ЗначениеЗаполнено(РеквизитыПлатежа.ДокументОснование)
		И ТипЗнч(РеквизитыПлатежа.ДокументОснование) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета");
	
КонецФункции

Функция ПериодСобытияПлатежа(РеквизитыДокумента, ПравилоДоИзменения, ПериодСобытияДоИзменения) Экспорт
	
	Перем ДатаСобытия;
	
	Если ПлатежиВБюджетКлиентСервер.РеквизитЗаполнен(РеквизитыДокумента.ПоказательПериода) Тогда
		
		ОписаниеПериода = ПлатежиВБюджетКлиентСервер.РазобратьНалоговыйПериод(РеквизитыДокумента.ПоказательПериода);
		
		ДатаСобытия   = ОписаниеПериода.Дата;
		Периодичность = ПериодичностьПоПоказателюПериода(ОписаниеПериода.Периодичность);
		
	ИначеЕсли ЗначениеЗаполнено(РеквизитыДокумента.НалоговыйПериод) Тогда
		
		ДатаСобытия   = РеквизитыДокумента.НалоговыйПериод;
		Периодичность = ПериодичностьУплатыНалогаВзноса(
			РеквизитыДокумента.ВидНалога,
			РеквизитыДокумента.Организация,
			РеквизитыДокумента.НалоговыйПериод);
		
	ИначеЕсли ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоФиксированныеВзносыВФСС(РеквизитыДокумента.ВидНалога)
		И УчетнаяПолитика.УплачиватьДобровольныеВзносыВФСС(РеквизитыДокумента.Организация, РеквизитыДокумента.Дата) Тогда
		
		// Добровольные взносы ИП в ФСС уплачиваются в течение страхового года.
		ДатаСобытия = РеквизитыДокумента.Дата;
		Периодичность = Перечисления.Периодичность.Год;
		
	ИначеЕсли ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоДобровольныйФиксированныйВзносВПФР(
		РеквизитыДокумента.ВидНалога) Тогда
		
		// Добровольные взносы ИП в ФСС уплачиваются в течение страхового года.
		ДатаСобытия = РеквизитыДокумента.Дата;
		Периодичность = Перечисления.Периодичность.Год;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаСобытия) Тогда
		Возврат '00010101';
	КонецЕсли;
	
	Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоФиксированныеВзносы(РеквизитыДокумента.ВидНалога) Тогда
		
		Если Не ЗначениеЗаполнено(ПериодСобытияДоИзменения) Тогда
			
			// Платеж создан не из помощника.
			// При ежеквартальной уплате в пределах года платеж регистрируется тем кварталом, в котором он создан.
			// Если же платеж оформлен в следующем году - он регистрируется в последнем квартале оплачиваемого страхового года.
			ДатаСобытия = Мин(КонецГода(ДатаСобытия), КонецКвартала(РеквизитыДокумента.Дата));
			
		ИначеЕсли (Год(ДатаСобытия) = Год(ПериодСобытияДоИзменения))
			И (ЗначениеЗаполнено(ПравилоДоИзменения)
				И ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетСтраховыхВзносовИП(ПравилоДоИзменения)) Тогда
			
			// Платежи по фиксированным взносам могут уплачиваться ежеквартально - сведения о квартале хранятся в ЗадачиБухгалтераНалоговыеПлатежи.
			// Но показатель периода или налоговый период в документах содержат информацию только о страховом годе.
			// Поэтому в случае, если налог не менялся и страховой год в реквизитах платежа остался прежним,
			// период события для фиксированных взносов не обновляем.
			Возврат ПериодСобытияДоИзменения;
			
		КонецЕсли;
		
		Периодичность = УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(
			РеквизитыДокумента.Организация, ДатаСобытия);
		
	КонецЕсли;
	
	Возврат РегистрыСведений.ЗадачиБухгалтера.ПериодСобытияПоНалогамИСборам(Периодичность, ДатаСобытия);
	
КонецФункции

Функция РекомендацияПоНачислениюЗарплаты(Организация, ПериодСобытия, ЭтоАванс = Ложь) Экспорт
	
	ИмяДокумента = ЗакрытиеМесяца.ВидДокументаНачисленияЗарплаты(Организация);
	
	Если ИмяДокумента = "НачислениеЗарплаты" Тогда
		// начисляем в БП
		Если ЭтоАванс Тогда
			Рекомендация = НСтр("ru = 'рассчитать аванс'")
		Иначе
			Рекомендация = НСтр("ru = 'начислить зарплату'");
		КонецЕсли;
		
	ИначеЕсли ИмяДокумента = "ОтражениеЗарплатыВУчете" Тогда
		// загружаем из ЗУП 2.5
		
		Рекомендация = НСтр("ru = 'загрузить данные'");
		
	Иначе
		// вводим вручную
		Рекомендация = НСтр("ru = 'ввести данные'");
	КонецЕсли;
	
	ШаблонТекста = НСтр("ru = 'Рекомендуется [Рекомендация] за [Период]
		|Тогда будет возможно определить сумму платежа.'");
	
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("Рекомендация", Рекомендация);
	ПараметрыТекста.Вставить("Период",
		КалендарьБухгалтера.ПредставлениеПериодаСобытия(НачалоМесяца(ПериодСобытия), КонецМесяца(ПериодСобытия)));
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста);
	
КонецФункции

Функция РеквизитыПлатежногоПоручения(ДокументСсылка) Экспорт
	
	Если ТипЗнч(ДокументСсылка) <> Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументУплаты.Ссылка КАК Ссылка,
	|	""ПлатежноеПоручение"" КАК ВидДокумента,
	|	ДокументУплаты.ДокументОснование КАК ДокументОснование,
	|	ДокументУплаты.Дата КАК Дата,
	|	ДокументУплаты.Организация КАК Организация,
	|	ДокументУплаты.ВидОперации КАК ВидОперации,
	|	ДокументУплаты.Налог КАК Налог,
	|	ДокументУплаты.Налог.ВидНалога КАК ВидНалога,
	|	ДокументУплаты.Контрагент КАК Контрагент,
	|	ДокументУплаты.КПППлательщика КАК КПППлательщика,
	|	ДокументУплаты.ПоказательПериода КАК ПоказательПериода,
	|	ДокументУплаты.ПоказательОснования КАК ПоказательОснования,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК НалоговыйПериод,
	|	ДокументУплаты.ПлатежнаяВедомость КАК ПлатежнаяВедомость
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ДокументУплаты
	|ГДЕ
	|	ДокументУплаты.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		РеквизитыПлатежа = ВыполнениеЗадачБухгалтера.НовыйРеквизитыПлатежаВлияющиеНаСтатусЗадачи();
		ЗаполнитьЗначенияСвойств(РеквизитыПлатежа, Выборка);
		
		Возврат РеквизитыПлатежа;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция РеквизитыРасходногоКассовогоОрдера(ДокументСсылка) Экспорт
	
	Если ТипЗнч(ДокументСсылка) <> Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументУплаты.Ссылка КАК Ссылка,
	|	""РасходныйКассовыйОрдер"" КАК ВидДокумента,
	|	ДокументУплаты.ДокументОснование КАК ДокументОснование,
	|	ДокументУплаты.Дата КАК Дата,
	|	ДокументУплаты.Организация КАК Организация,
	|	ДокументУплаты.ВидОперации КАК ВидОперации,
	|	ДокументУплаты.Налог КАК Налог,
	|	ДокументУплаты.Налог.ВидНалога КАК ВидНалога,
	|	ДокументУплаты.Контрагент КАК Контрагент,
	|	"""" КАК КПППлательщика,
	|	ДокументУплаты.ПоказательПериода КАК ПоказательПериода,
	|	ДокументУплаты.ПоказательОснования КАК ПоказательОснования,
	|	ДокументУплаты.НалоговыйПериод КАК НалоговыйПериод,
	|	ДокументУплаты.ПлатежнаяВедомость КАК ПлатежнаяВедомость
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК ДокументУплаты
	|ГДЕ
	|	ДокументУплаты.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		РеквизитыПлатежа = ВыполнениеЗадачБухгалтера.НовыйРеквизитыПлатежаВлияющиеНаСтатусЗадачи();
		ЗаполнитьЗначенияСвойств(РеквизитыПлатежа, Выборка);
		
		Возврат РеквизитыПлатежа;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция РеквизитыСписанияСРасчетногоСчета(ДокументСсылка) Экспорт
	
	Если ТипЗнч(ДокументСсылка) <> Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументУплаты.Ссылка КАК Ссылка,
	|	""СписаниеСРасчетногоСчета"" КАК ВидДокумента,
	|	ДокументУплаты.ДокументОснование КАК ДокументОснование,
	|	ДокументУплаты.Дата КАК Дата,
	|	ДокументУплаты.Организация КАК Организация,
	|	ДокументУплаты.ВидОперации КАК ВидОперации,
	|	ДокументУплаты.Налог КАК Налог,
	|	ДокументУплаты.Налог.ВидНалога КАК ВидНалога,
	|	ДокументУплаты.Контрагент КАК Контрагент,
	|	"""" КАК КПППлательщика,
	|	"""" КАК ПоказательПериода,
	|	"""" КАК ПоказательОснования,
	|	ДокументУплаты.НалоговыйПериод КАК НалоговыйПериод,
	|	ДокументУплаты.ПлатежнаяВедомость КАК ПлатежнаяВедомость
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК ДокументУплаты
	|ГДЕ
	|	ДокументУплаты.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		РеквизитыПлатежа = ВыполнениеЗадачБухгалтера.НовыйРеквизитыПлатежаВлияющиеНаСтатусЗадачи();
		ЗаполнитьЗначенияСвойств(РеквизитыПлатежа, Выборка);
		
		Возврат РеквизитыПлатежа;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура ЗарегистрироватьИсполнениеЗадачиПриЗаписиУведомления(РеквизитыУведомления, ДополнительныеСвойства, ПараметрыРасчетаСтатусов) Экспорт
	
	ПравилоУведомления =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПравилоУведомления");
	ПериодСобытияКалендаря =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПериодСобытияКалендаря");
	РегистрацияВНалоговомОргане =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РегистрацияВНалоговомОргане");
	
	Если ЗначениеЗаполнено(ПравилоУведомления) И ЗначениеЗаполнено(ПериодСобытияКалендаря) Тогда
		
		// Правило и период задачи переданы из помощника, можно сразу регистрировать документ.
		ПараметрыРасчетаСтатусов.Вставить("ПропуститьОбновлениеСвязиДокументЗадача", Истина);
		
		РегистрыСведений.ЗадачиБухгалтераУведомления.ДобавитьЗаписьПравила(
			РеквизитыУведомления.Организация,
			РеквизитыУведомления.Ссылка,
			ПравилоУведомления,
			ПериодСобытияКалендаря,
			РегистрацияВНалоговомОргане);
		
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыУведомленияПоДаннымОбъекта(ДокументОбъект) Экспорт
	
	РеквизитыУведомления = НовыйРеквизитыУведомленияВлияющиеНаСтатусЗадачи();
	
	ЗаполнитьЗначенияСвойств(РеквизитыУведомления, ДокументОбъект);
	ЗаполнитьЗначенияСвойств(РеквизитыУведомления, ДокументОбъект.Налоги[0]);
	
	// Особенные реквизиты, не содержащиеся в объекте непосредственно.
	
	РеквизитыУведомления.ВидДокумента = ДокументОбъект.Метаданные().Имя;
	
	Если ЗначениеЗаполнено(РеквизитыУведомления.Налог) Тогда
		РеквизитыУведомления.ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыУведомления.Налог, "ВидНалога");
	КонецЕсли;
	
	Возврат РеквизитыУведомления;
	
КонецФункции

Функция ПравилаОтчетностиРасчетПоСтраховымВзносам() Экспорт
	
	МассивПравил = Новый Массив;
	МассивПравил.Добавить("СтраховыеВзносы.2017_ОтчетностьСтраховыеВзносы");
	
	Возврат МассивПравил;
	
КонецФункции

Функция ПравилаОтчетностиПерсонифицированныеСведения() Экспорт
	
	МассивПравил = Новый Массив;
	МассивПравил.Добавить("СтраховыеВзносы.2023_ОтчетностьПерсСведенияОФизлицах");
	
	Возврат МассивПравил;
	
КонецФункции

Функция ПравилаОтчетностиЖурналУчетаСчетовФактур() Экспорт
	
	МассивПравил = Новый Массив;
	МассивПравил.Добавить("НДС.2015_ЖурналСчетовФактур");
	
	Возврат МассивПравил;
	
КонецФункции

// Возвращает правило для задачи по уплате налога АУСН
//
// Параметры:
//  Период - Дата - налоговый период, за который производится выплата
//  ВидДействия - ПеречислениеСсылка.ВидыДействийКалендаряБухгалтера - вид действия задачи
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов, Неопределено - правило уплаты налога АУСН,
//                   если правило не найдено, возвращается Неопределено
//
Функция ПравилоДействияКалендаряБухгалтера_АУСН(Период, ВидДействия) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Правила.Ссылка КАК Правило
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Правила
	|ГДЕ
	|	Правила.Действие = &ВидДействия
	|	И Правила.Владелец.Код = &КодЗадачи
	|	И НЕ Правила.ПометкаУдаления
	|	И Правила.НачалоДействия <= &Период
	|	И (Правила.КонецДействия > &Период
	|			ИЛИ Правила.КонецДействия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Правила.РеквизитДопУпорядочивания");
	
	Запрос.УстановитьПараметр("КодЗадачи", ЗадачиБухгалтераКлиентСервер.КодЗадачиАУСН());
	Запрос.УстановитьПараметр("ВидДействия", ВидДействия);
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Правило;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Завершает задачи по проверке банковских операций для организаций, находящихся на АУСН,
// после истечения возможного срока корректировки данных.
// Если выполнено завершение задач проверки банковских операций помещает значение Истина в параметр Результат
// Параметры:
//  Результат - Булево
//
Процедура ЗавершитьПроверкуБанковскихОперацийАУСН(Результат) Экспорт
	
	Результат = Ложь;
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	""Сдано"" Как Статус,
	|	ЗадачиБухгалтера.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ЗадачиБухгалтера.Статус КАК РучнойСтатус
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|Где
	|	ЗадачиБухгалтера.Правило = &Правило
	|	И (ЗадачиБухгалтера.Срок < &Период)");
	
	Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Правило", ПравилоДействияКалендаряБухгалтера_АУСН(
		Период,
		Перечисления.ВидыДействийКалендаряБухгалтера.Отчет));
	
	ЗадачиКЗавершению = Запрос.Выполнить().Выбрать();
	Пока ЗадачиКЗавершению.Следующий() Цикл
		Результат = Истина;
		Если ЗадачиКЗавершению.СтатусУстановленВручную Тогда
			Продолжить;
		КонецЕсли;
		ВыполнениеЗадачБухгалтера.ЗаписатьНовоеСостояниеЗадачи(ЗадачиКЗавершению, Истина, ЗадачиКЗавершению.Статус);
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает для задач бухгалтера новый статус, если он был изменен
//
// Параметры:
//  ПараметрыЗадач - ТаблицаЗначений - см. Обработки.ПомощникУплатыНалогаАУСН.НовыеПараметрыЗадач
//
Процедура УстановитьСтатусыЗадачПоОплатеАУСН(ПараметрыЗадач) Экспорт
	
	ПараметрыЗадач.ЗаполнитьЗначения(Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка(),
		"РегистрацияВНалоговомОргане");
	Обработки.ПомощникУплатыНалогаАУСН.ЗаполнитьСтатусыЗадач(ПараметрыЗадач);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПараметрыЗадач.Организация,
		|	ПараметрыЗадач.ПериодСобытия,
		|	ПараметрыЗадач.Правило,
		|	ПараметрыЗадач.РегистрацияВНалоговомОргане,
		|	ПараметрыЗадач.Действие,
		|	ПараметрыЗадач.Статус
		|ПОМЕСТИТЬ ВТ_ПараметрыЗадач
		|ИЗ
		|	&ПараметрыЗадач КАК ПараметрыЗадач
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПараметрыЗадач.Организация,
		|	ВТ_ПараметрыЗадач.ПериодСобытия,
		|	ВТ_ПараметрыЗадач.Правило,
		|	ВТ_ПараметрыЗадач.РегистрацияВНалоговомОргане,
		|	ВТ_ПараметрыЗадач.Действие,
		|	ВТ_ПараметрыЗадач.Статус
		|ИЗ
		|	ВТ_ПараметрыЗадач КАК ВТ_ПараметрыЗадач
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|		ПО ВТ_ПараметрыЗадач.Организация = ЗадачиБухгалтера.Организация
		|		И ВТ_ПараметрыЗадач.Правило = ЗадачиБухгалтера.Правило
		|		И ВТ_ПараметрыЗадач.ПериодСобытия = ЗадачиБухгалтера.ПериодСобытия
		|		И ВТ_ПараметрыЗадач.РегистрацияВНалоговомОргане = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
		|		И ВТ_ПараметрыЗадач.Действие = ЗадачиБухгалтера.Действие
		|		И ВТ_ПараметрыЗадач.Статус <> ЗадачиБухгалтера.Статус
		|		И НЕ ЗадачиБухгалтера.СтатусУстановленВручную";
	
	Запрос.УстановитьПараметр("ПараметрыЗадач", ПараметрыЗадач);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка, , "Статус");
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Запись.Статус = Выборка.Статус;
			Запись.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Актуализирует статусы задач по уплате фиксированных взносов ИП.
// Статус задачи уплаты может быть установлен после отражения сведений об уплате на ЕНС.
//
// Параметры:
//   Организация - СправочникСсылка.Организации - ИП, для которого нужно актуализировать статусы уплаты взносов за себя
//   Период      - Дата - Конец периода, до которого выбираются задачи для актуализации статусов относительно начала страхового периода (начало года)
//
Процедура АктуализироватьСтатусыЗадачПоФиксированнымВзносамИП(Организация, Период) Экспорт
	
	// После того, как все сведения об уплатах на ЕНС отражены, можно закрыть задачи, которые выполнены по факту списаний
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ЗадачиБухгалтера.НачалоВыполнения КАК НачалоВыполнения,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.НачальныйСрок КАК НачальныйСрок,
	|	ЗадачиБухгалтера.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Правило = ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|			И (ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код = &КодЗадачи)
	|			И (НЕ ПравилаПредставленияОтчетовУплатыНалогов.ВыполняетсяЕдинымПомощником)
	|			И (ЗадачиБухгалтера.Организация = &Организация)
	|			И (ЗадачиБухгалтера.Срок МЕЖДУ &НачалоГода И &КонецПериода)
	|			И (ЗадачиБухгалтера.Действие = &Действие)
	|			И (НЕ ЗадачиБухгалтера.СтатусУстановленВручную)
	|			И (НЕ ЗадачиБухгалтера.ВАрхиве)");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период));
	Запрос.УстановитьПараметр("КодЗадачи", ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносыИП());
	Запрос.УстановитьПараметр("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗадач = РегистрыСведений.ЗадачиБухгалтера.НовыйРасписание();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатЗапроса.Выгрузить(), ТаблицаЗадач);
	
	Если ТаблицаЗадач.Количество() > 0 Тогда
		ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(ТаблицаЗадач);
		Для Каждого ТекущаяЗадача Из ТаблицаЗадач Цикл
			ВыполнениеЗадачБухгалтера.ЗаписатьНовоеСостояниеЗадачи(ТекущаяЗадача, Ложь, ТекущаяЗадача.Статус);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтатусыЗадачАУСН(ПараметрыЗадач) Экспорт
	
	Для Каждого ПараметрыЗадачи Из ПараметрыЗадач Цикл
		
		Если ПараметрыЗадачи.СтатусУстановленВручную Тогда
			ПараметрыЗадачи.Статус = ПараметрыЗадачи.РучнойСтатус;
			Продолжить;
		КонецЕсли;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СписаниеСРасчетногоСчета.Ссылка
		|ИЗ
		|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
		|ГДЕ
		|	НЕ СписаниеСРасчетногоСчета.ПометкаУдаления
		|	И СписаниеСРасчетногоСчета.Проведен
		|	И СписаниеСРасчетногоСчета.Организация = &Организация
		|	И СписаниеСРасчетногоСчета.Налог.ВидНалога В (&ВидыНалогов)
		|	И СписаниеСРасчетногоСчета.НалоговыйПериод = &НалоговыйПериод");
		
		Запрос.УстановитьПараметр("Организация", ПараметрыЗадачи.Организация);
		Запрос.УстановитьПараметр("НалоговыйПериод", НачалоМесяца(ПараметрыЗадачи.ПериодСобытия));
		ВидыНалогов = Новый Массив;
		ВидыНалогов.Добавить(Перечисления.ВидыНалогов.АУСН);
		ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж);
		Запрос.УстановитьПараметр("ВидыНалогов", ВидыНалогов);
		
		РезультатЗапроса  = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ПараметрыЗадачи.Статус = "";
		Иначе
			ПараметрыЗадачи.Статус = "Оплачено";
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьПлатежПоДаннымДекларации(ПлатежПоЗадаче, Задача)
	
	Срок                        = Задача.ДатаПлатежа;
	Правило                     = Задача.Правило;
	Организация                 = Задача.Организация;
	ПериодСобытия               = Задача.ПериодСобытия;
	РегистрацияВНалоговомОргане = Задача.РегистрацияВНалоговомОргане;
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ПериодСобытия);
	
	Показатели                  = ВыполнениеЗадачБухгалтера.ПоказателиБазовогоПериода(НачалоПериода, КонецПериода, Правило);
	КонецБазовогоПериода        = Показатели.КонецБазовогоПериода;
	
	ПолноеИмяПравила  = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	
	ПоказательПериода = ВыполнениеЗадачБухгалтера.ПоказательПериодаПлатежаПоДекларации(Правило, ПериодСобытия, Срок);
	ДанныеДекларации  = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыНалогаПоДекларации(Правило, Организация, РегистрацияВНалоговомОргане, ПериодСобытия, Срок, ПоказательПериода);
	
	Если ДанныеДекларации = Неопределено Тогда
		
		// В примечании надо указать на то, что нет декларации.
		
		ШаблонТекста = НСтр("ru = 'Рекомендуется подготовить отчет ""[Декларация] за [Период]"".
			|Тогда будет возможно определить сумму платежа.'");
		
		ПараметрыТекста = Новый Структура;
		ПараметрыТекста.Вставить("Декларация", ИнтерфейсыВзаимодействияБРО.ПредставлениеФормыРегламентированногоОтчета(ПолноеИмяПравила, КонецБазовогоПериода));
		НачалоНалоговогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.ФинансовыйПериод, КонецБазовогоПериода);
		ПараметрыТекста.Вставить("Период",     КалендарьБухгалтера.ПредставлениеПериодаСобытия(НачалоНалоговогоПериода, КонецБазовогоПериода));
		
		ПлатежПоЗадаче.Примечание = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста);
		
		Возврат;
		
	КонецЕсли;
	
	ПлатежПоЗадаче.ЕстьПлатежныйДокумент = ВыполнениеЗадачБухгалтера.ЕстьПлатежныйДокумент(ДанныеДекларации.Платежи);
	ПлатежПоЗадаче.Сумма = ДанныеДекларации.Платежи.Итог("Сумма");
	
КонецПроцедуры

Процедура ДополнитьПлатежУплатаАвансовПоИмущественнымНалогам(ПлатежПоЗадаче, Задача)
	
	Срок          = Задача.Срок;
	Организация   = Задача.Организация;
	Правило       = Задача.Правило;
	ПериодСобытия = Задача.ПериодСобытия;
	
	Платежи = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыИмущественныхНалогов(Задача);
	ПлатежПоЗадаче.ЕстьПлатежныйДокумент = ВыполнениеЗадачБухгалтера.ЕстьПлатежныйДокумент(Платежи);
	ПлатежПоЗадаче.Сумма = Платежи.Итог("Сумма");
	
КонецПроцедуры

Процедура ДополнитьПлатежУплатаСтраховыхВзносов(ПлатежПоЗадаче, Задача)
	
	Организация   = Задача.Организация;
	ПериодСобытия = Задача.ПериодСобытия;
	Правило       = Задача.Правило;
	
	Если НЕ ВыполнениеЗадачБухгалтера.ОперацияНачисленияВзносовВыполнена(Организация, ПериодСобытия) Тогда
		
		ПлатежПоЗадаче.Примечание = РекомендацияПоНачислениюЗарплаты(Организация, ПериодСобытия);
		
		Возврат; // начислений нет.
	КонецЕсли;
	
	ВариантыВыборкиСтраховыхВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	Травматизм = ВариантыВыборкиСтраховыхВзносов.Травматизм;
	Платежи = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыСтраховыхВзносов(Правило, Организация, ПериодСобытия, , Травматизм);
	
	ПлатежПоЗадаче.ЕстьПлатежныйДокумент = ВыполнениеЗадачБухгалтера.ЕстьПлатежныйДокумент(Платежи);
	ПлатежПоЗадаче.Сумма = Платежи.Итог("Сумма")
	
КонецПроцедуры

Процедура ДополнитьПлатежУплатаСтраховыхВзносовИП(ПлатежПоЗадаче, Задача)
	
	Организация   = Задача.Организация;
	Период        = Задача.ПериодСобытия;
	Периодичность = Задача.Периодичность;
	ТекущаяДата   = ОбщегоНазначения.ТекущаяДатаПользователя();
		
	Если ВыполнениеЗадачБухгалтера.ЭтоОплатаНалогаЗаПрошлыйПериод(ПлатежПоЗадаче.ПараметрыКоманды)
		И УчетнаяПолитика.ПрименяетсяУСН(Организация, ТекущаяДата) Тогда
		
		ПлатежПоЗадаче.Сумма = Обработки.ПомощникУплатыНалоговВзносовПрошлыхЛет.ЗадолженностьПоНалогамВзносамЗаПрошлыйПериод(Задача);
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПлатежноеПоручение.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Платежи
		|ИЗ
		|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		|ГДЕ
		|	ПлатежноеПоручение.Дата >= &ДатаНачалаОбзора
		|	И ПлатежноеПоручение.Проведен
		|	И НЕ ПлатежноеПоручение.ПометкаУдаления
		|	И ПлатежноеПоручение.Организация = &Организация
		|	И ПлатежноеПоручение.ВидОперации = &ВидОперации
		|	И ПлатежноеПоручение.СуммаДокумента > 0
		|	И ПОДСТРОКА(ПлатежноеПоручение.ПоказательПериода, 0, 5) = ""ГД.00""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение КАК Ссылка
		|ИЗ
		|	ВТ_Платежи КАК ПлатежныйДокумент
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
		|		ПО ПлатежныйДокумент.Ссылка = ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение
		|ГДЕ
		|	ЗадачиБухгалтераНалоговыеПлатежи.Организация = &Организация
		|	И ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия = &ПериодСобытия
		|	И ЗадачиБухгалтераНалоговыеПлатежи.Правило = &Правило");
		
		Запрос.УстановитьПараметр("Правило", Задача.Правило);
		Запрос.УстановитьПараметр("ПериодСобытия", Период);
		Запрос.УстановитьПараметр("ВидОперации", Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
		Запрос.УстановитьПараметр("ДатаНачалаОбзора", Дата(Год(ТекущаяДата) - 3, 1, 1));
		
	Иначе
		
		// Фиксированная часть страховых взносов
		Взносы = УчетСтраховыхВзносовИП.ФиксированныеСтраховыеВзносыКУплате(
			Организация, Период, Периодичность);
		
		// Страховые взносы, исчисленные с суммы доходов
		СтруктураДоходов = УчетСтраховыхВзносовИП.СтруктураДоходовПоВидамДеятельности(
			Организация, НачалоГода(Период), КонецКвартала(Период));
			
		СтраховыеВзносы = УчетСтраховыхВзносовИП.СтраховыеВзносыСДоходовКУплате(
			Организация, Период, СтруктураДоходов, Ложь);
		
		ПлатежПоЗадаче.Сумма = Взносы.СуммаВзносаПФР + Взносы.СуммаВзносаФФОМС
			+ Взносы.СуммаВзносаФСС + СтраховыеВзносы.СуммаВзносаПФРсДоходов + Взносы.СуммаВзносаЕдиныйТариф;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПлатежноеПоручение.Ссылка
		|ИЗ
		|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		|ГДЕ
		|	НЕ ПлатежноеПоручение.ПометкаУдаления
		|	И ПлатежноеПоручение.Организация = &Организация
		|	И НАЧАЛОПЕРИОДА(ПлатежноеПоручение.Дата, КВАРТАЛ) = &НачалоВыполнения
		|	И ПлатежноеПоручение.Налог.ВидНалога В(&ВидыНалогов)";
		
		ВидыНалогов = Новый Массив;
		ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть);
		ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ФиксированныеВзносы_ФСС);
		ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС);
		ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф);
		ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела);
		
		Запрос.УстановитьПараметр("ВидыНалогов", ВидыНалогов);
		Запрос.УстановитьПараметр("НачалоВыполнения", Задача.НачалоВыполнения);
		
	КонецЕсли;
		
	Если ПлатежПоЗадаче.Сумма = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Поиск платежного поручения
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПлатежПоЗадаче.ЕстьПлатежныйДокумент = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПлатежУплатаНалогаУСН(ПлатежПоЗадаче, Задача)
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	
	ПараметрыПроверки = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности();
	ПараметрыПроверки.Организация = Задача.Организация;
	ПараметрыПроверки.Период      = КонецКвартала(Задача.ПериодСобытия);
	ПараметрыПроверки.АктуализироватьВесьПериод = Истина;
	
	РезультатПроверки = Обработки.ЗакрытиеМесяца.ПроверитьАктуальность(ПараметрыПроверки);
	
	Если Не РезультатПроверки.ТребуетсяАктуализация Тогда
		
		Показатели = Обработки.ПомощникРасчетаНалогаУСН.АктуальныеПоказатели(Задача.Организация, Задача.ПериодСобытия);
		
		ПлатежПоЗадаче.Сумма = Показатели.НалогКУплате;
		
		ДокументыУплатыНалога = Обработки.ПомощникРасчетаНалогаУСН.ДокументыУплатыНалогов(
			Задача.Организация, Задача.ПериодСобытия, Задача.Правило);
		
		ЕстьПлатежноеПоручение = Ложь;
		Для Каждого Платеж Из ДокументыУплатыНалога Цикл
			Если ТипЗнч(Платеж.Ссылка) = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
				ЕстьПлатежноеПоручение = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ПлатежПоЗадаче.ЕстьПлатежныйДокумент = ЕстьПлатежноеПоручение;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПлатежУплатаТорговогоСбора(ПлатежПоЗадаче, Задача)
	
	ТаблицаТорговыйСбор = ТорговыйСбор.СуммаТорговогоСбора(
	Задача.Организация, НачалоКвартала(Задача.ПериодСобытия), КонецКвартала(Задача.ПериодСобытия));
	
	ПлатежПоЗадаче.Сумма = ТаблицаТорговыйСбор.Итог("Сумма");
	
	// Поиск платежного поручения
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И ПлатежноеПоручение.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ПлатежноеПоручение.Дата, КВАРТАЛ) = &НачалоВыполнения
	|	И ПлатежноеПоручение.Налог.ВидНалога В(&ВидыНалогов)";
	
	ВидыНалогов = Новый Массив;
	ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ТорговыйСбор);
	
	Запрос.УстановитьПараметр("НачалоВыполнения", Задача.НачалоВыполнения);
	Запрос.УстановитьПараметр("Организация",      Задача.Организация);
	Запрос.УстановитьПараметр("ВидыНалогов",      ВидыНалогов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПлатежПоЗадаче.ЕстьПлатежныйДокумент = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПлатежОплатаПатента(ПлатежПоЗадаче, Задача)
	
	ДокументПатент = УчетПСН.ДокументПатентПоДеятельностиИСроку(Задача.Правило, Задача.Срок);
	
	ДанныеПатента = Обработки.ПомощникОплатыПатента.ПоказателиПатента(ДокументПатент, Задача.Срок);
	
	// Поиск платежного поручения
	ДокументыУплатыНалога = Обработки.ПомощникОплатыПатента.ДокументыУплатыПатента(
		Задача.Организация,
		Задача.Правило,
		Задача.ПериодСобытия,
		ДанныеПатента.ВидНалога);
	
	ДанныеПоУменьшениюНалога = РегистрыСведений.УменьшениеНалогаПоПатенту.СведенияУменьшениеНалогаЗаГод(
		ДанныеПатента.Организация, ДанныеПатента.ДатаНачала, ДокументПатент);
	УменьшениеНалога = ДанныеПоУменьшениюНалога[ДокументПатент];
	Если ТипЗнч(УменьшениеНалога) = Тип("Структура") Тогда
		РасходыУменьшающиеНалог = УменьшениеНалога.Сумма;
	Иначе
		РасходыУменьшающиеНалог = 0;
	КонецЕсли;
	СуммаКОплате = ДанныеПатента.ТекущийПлатеж + ДанныеПатента.ПредыдущийПлатеж - РасходыУменьшающиеНалог;
	Оплачено = ДокументыУплатыНалога.Скопировать(Новый Структура("Оплачено", Истина), "Сумма").Итог("Сумма");
	
	ПлатежПоЗадаче.Сумма = Макс(0, СуммаКОплате - Оплачено);
	
	ПлатежПоЗадаче.ЕстьПлатежныйДокумент = (ДокументыУплатыНалога.НайтиСтроки(Новый Структура("Оплачено", Ложь)).Количество() > 0);
	
	Если ПлатежПоЗадаче.Сумма = 0 И СуммаКОплате <> 0 Тогда
		ПлатежПоЗадаче.Примечание = НСтр("ru = 'Оплачено'");
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПлатежУплатаНДС(ПлатежПоЗадаче, Задача)
	
	Срок                        = Задача.ДатаПлатежа;
	Правило                     = Задача.Правило;
	Организация                 = Задача.Организация;
	ПериодСобытия               = Задача.ПериодСобытия;
	РегистрацияВНалоговомОргане = Задача.РегистрацияВНалоговомОргане;
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ПериодСобытия);
	
	Показатели                  = ВыполнениеЗадачБухгалтера.ПоказателиБазовогоПериода(НачалоПериода, КонецПериода, Правило);
	КонецБазовогоПериода        = Показатели.КонецБазовогоПериода;
	
	ПолноеИмяПравила  = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	
	ПоказательПериода = ВыполнениеЗадачБухгалтера.ПоказательПериодаПлатежаПоДекларации(Правило, ПериодСобытия, Срок);
	ДанныеДекларации  = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыНалогаПоДекларации(Правило, Организация, РегистрацияВНалоговомОргане, ПериодСобытия, Срок, ПоказательПериода);
	
	Если ДанныеДекларации = Неопределено Тогда
		
		// В примечании надо указать на то, что нет декларации.
		
		ШаблонТекста = НСтр("ru = 'Рекомендуется подготовить отчет ""[Декларация] за [Период]"".
			|Тогда будет возможно определить сумму платежа.'");
		
		ПараметрыТекста = Новый Структура;
		ПараметрыТекста.Вставить("Декларация", ИнтерфейсыВзаимодействияБРО.ПредставлениеФормыРегламентированногоОтчета(ПолноеИмяПравила, КонецБазовогоПериода));
		НачалоНалоговогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.ФинансовыйПериод, КонецБазовогоПериода);
		ПараметрыТекста.Вставить("Период",     КалендарьБухгалтера.ПредставлениеПериодаСобытия(НачалоНалоговогоПериода, КонецБазовогоПериода));
		
		ПлатежПоЗадаче.Примечание = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста);
		
		Возврат;
		
	КонецЕсли;
	
	ПлатежиЗаВыбранныйПериод = ДанныеДекларации.Платежи.СкопироватьКолонки();
	Для Каждого СтрокаПлатежа Из ДанныеДекларации.Платежи Цикл
		Если СтрокаПлатежа.Период > НачалоПериода И СтрокаПлатежа.Период <= КонецБазовогоПериода Тогда
			ЗаполнитьЗначенияСвойств(ПлатежиЗаВыбранныйПериод.Добавить(), СтрокаПлатежа);
		КонецЕсли;
	КонецЦикла;
	ПлатежПоЗадаче.ЕстьПлатежныйДокумент = ВыполнениеЗадачБухгалтера.ЕстьПлатежныйДокумент(ПлатежиЗаВыбранныйПериод);
	ПлатежПоЗадаче.Сумма = ПлатежиЗаВыбранныйПериод.Итог("Сумма");
	
КонецПроцедуры

Процедура ДополнитьЗадачиОплатыПатента(ЗадачаПлатежа, ТаблицаЗадач)
	
	Если ЗадачаПлатежа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// При изменении платежа по патенту в периоде действия вычета по онлайн-кассам
	// могут измениться статусы всех задач, связанных с данным патентом.
	
	Если ТипЗнч(ЗадачаПлатежа.Правило) = Тип("СправочникСсылка.Патенты")
		И ВычетыНаОнлайнКассыКлиентСервер.ВычетыПрименяются(
			ЗадачаПлатежа.Организация,
			ЗадачаПлатежа.ПериодСобытия,
			Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.Патент) Тогда
			
		ЗадачиУплатыПатента = Новый ТаблицаЗначений;
		ВыполнениеЗадачБухгалтераПереопределяемый.ЗаполнитьЗадачиУплатыПатента(ЗадачиУплатыПатента,
			ЗадачаПлатежа.Организация, ЗадачаПлатежа.Правило);
			
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
			ЗадачиУплатыПатента,
			ТаблицаЗадач);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьЗадачиУплатыФиксированныхВзносов(ТаблицаЗадач)
	
	ПериодыУплатыВзносов = Новый ТаблицаЗначений;
	ПериодыУплатыВзносов.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПериодыУплатыВзносов.Колонки.Добавить("СтраховойГод", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	
	ИдентификаторПравилаВзносыСДоходов = ВыполнениеЗадачБухгалтера.ИдентификаторПравилаСтраховыеВзносыИПСДоходов();
	
	СтраховыеПериоды = Новый Массив;
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		
		Если ТипЗнч(Задача.Правило) = Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов")
			И ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетСтраховыхВзносовИП(Задача.Правило)
			И Не Задача.Правило.Код = ИдентификаторПравилаВзносыСДоходов Тогда
			
			НоваяСтрока = ПериодыУплатыВзносов.Добавить();
			НоваяСтрока.Организация  = Задача.Организация;
			НоваяСтрока.СтраховойГод = КонецГода(Задача.ПериодСобытия); // Взносы с доходов уплачиваются всегда за полный год.
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПериодыУплатыВзносов.Свернуть(ИменаКолонокСтрокой(ПериодыУплатыВзносов.Колонки));
	
	ОтборПравила = Новый Структура;
	ОтборПравила.Вставить("Задача", ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносыИП());
	ОтборПравила.Вставить("Уплата", ИдентификаторПравилаВзносыСДоходов);
	
	Для Каждого ПериодУплаты Из ПериодыУплатыВзносов Цикл
		
		Порядок = РегистрыСведений.ЗадачиБухгалтера.ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(
			ПериодУплаты.Организация,
			ОтборПравила,
			ПериодУплаты.СтраховойГод,
			Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога);
		
		ПорядокУплаты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Порядок, "Уплата", Неопределено);
		
		Если ПорядокУплаты <> Неопределено Тогда
			
			НоваяЗадача = ТаблицаЗадач.Добавить();
			
			НоваяЗадача.Организация   = ПериодУплаты.Организация;
			НоваяЗадача.Правило       = ПорядокУплаты.Правило;
			НоваяЗадача.ПериодСобытия = ПорядокУплаты.ПериодСобытия;
			НоваяЗадача.Действие      = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьЗадачиОплатыНДС(ЗадачаПлатежа, ТаблицаЗадач)
	
	Если ЗадачаПлатежа = Неопределено 
		Или ЗадачаПлатежа.Правило = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ЗадачаПлатежа.Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", ЗадачаПлатежа.РегистрацияВНалоговомОргане);
	Запрос.УстановитьПараметр("ПериодСобытия", ЗадачаПлатежа.ПериодСобытия);
	Запрос.УстановитьПараметр("ИдентификаторыПлатежей", ИдентификаторыПравилУплатыЕжемесячныхПлатежейПоНДС());
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ЗадачиБухгалтера.Правило) = ТИП(Справочник.ПравилаПредставленияОтчетовУплатыНалогов)
	|	И ЗадачиБухгалтера.Правило.Код В(&ИдентификаторыПлатежей)
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)";
	ЗадачиОплатыНДС = Запрос.Выполнить().Выгрузить();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ЗадачиОплатыНДС, ТаблицаЗадач);
	
КонецПроцедуры

Процедура ДополнитьЗадачиУплатыДобровольныхВзносовИП(Организация, НалоговыйПериод, ТаблицаЗадач)
	
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Или Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Порядок = РегистрыСведений.ЗадачиБухгалтера.ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(
		Организация,
		Перечисления.ВидыНалогов.ФиксированныеВзносы_ФСС,
		НалоговыйПериод,
		Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога);
	
	ПорядокУплаты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Порядок, "Уплата", Неопределено);
	
	Если ПорядокУплаты <> Неопределено Тогда
		
		НоваяЗадача = ТаблицаЗадач.Добавить();
		
		НоваяЗадача.Организация   = Организация;
		НоваяЗадача.Правило       = ПорядокУплаты.Правило;
		НоваяЗадача.ПериодСобытия = НалоговыйПериод;
		НоваяЗадача.Действие      = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИменаКолонокСтрокой(КолонкиТаблицы)
	
	ИменаКолонок = Новый Массив;
	
	Для Каждого Колонка Из КолонкиТаблицы Цикл
		ИменаКолонок.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Возврат СтрСоединить(ИменаКолонок, ",");
	
КонецФункции

Процедура ЗаполнитьРегистрациюВНалоговомОргане(ТаблицаЗадач, РеквизитыДокумента)
	
	// Для задач уплаты местных налогов РегистрацияВНалоговомОргане должна быть заполнена.
	// Для других задач РегистрацияВНалоговомОргане должна быть пустой.
	// С такими значениями задачи записываются в регистр ЗадачиБухгалтера.
	// Соответственно, при поиске задач это нужно учесть.
	
	КодыЗадачМестныхНалогов = РегистрыСведений.ПорядокУплатыНалоговНаМестах.КодыНалоговПорядокКоторыхУстанавливаетсяНаМестах();
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		
		ИдентификаторыПравила
			= Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ИдентификаторыПравилаИЗадачи(Задача.Правило);
		
		Если ИдентификаторыПравила <> Неопределено
			И КодыЗадачМестныхНалогов.Найти(ИдентификаторыПравила.ИдентификаторЗадачи) <> Неопределено Тогда
			
			Задача.РегистрацияВНалоговомОргане =
				Документы.ПлатежноеПоручение.РегистрацияВНалоговомОрганеПоДаннымПлатежногоПоручения(
					РеквизитыДокумента.Организация, РеквизитыДокумента.Контрагент, РеквизитыДокумента.КПППлательщика);
			
		Иначе
			
			Задача.РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВидыНалоговСПоддержкойАвтообновленияСвязанныхЗадач()
	
	// Для указанных налогов поддерживается обновление исполняемой задачи и ее статуса
	// при вводе и изменении любого платежа, кроме ручной операции.
	
	ВидыНалогов = Новый Массив;
	
	// УСН
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыНалогов,
		ПлатежиВБюджетКлиентСерверПереопределяемый.ВидыНалоговУСН());
	
	ВидыНалогов.Добавить(ПредопределенноеЗначение("Перечисление.ВидыНалогов.АУСН"));
	
	// Фиксированные страховые взносы ИП
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыНалогов,
		ПлатежиВБюджетКлиентСерверПереопределяемый.ВидыНалоговФиксированныеВзносы());
	
	// НДФЛ ИП
	ВидыНалогов.Добавить(ПредопределенноеЗначение("Перечисление.ВидыНалогов.НДФЛ_ИП"));
	ВидыНалогов.Добавить(ПредопределенноеЗначение("Перечисление.ВидыНалогов.ФиксированныеВзносы_ФСС"));
	
	Возврат ВидыНалогов;
	
КонецФункции

Функция ИдентификаторыПравилУплатыЕжемесячныхПлатежейПоНДС() 
	
	Идентификаторы = Новый Массив;
	Идентификаторы.Добавить("2023_Уплата1");
	Идентификаторы.Добавить("2023_Уплата2");
	Идентификаторы.Добавить("2023_Уплата3");
	Идентификаторы.Добавить("2023_Уплата1_ЕдиныйПомощник");
	Идентификаторы.Добавить("2023_Уплата2_ЕдиныйПомощник");
	Идентификаторы.Добавить("2023_Уплата3_ЕдиныйПомощник");
	
	Идентификаторы.Добавить("2025_Уплата1_ЕдиныйПомощникНДСПриУСН");
	Идентификаторы.Добавить("2025_Уплата2_ЕдиныйПомощникНДСПриУСН");
	Идентификаторы.Добавить("2025_Уплата3_ЕдиныйПомощникНДСПриУСН");
	
	Возврат Идентификаторы;
	
КонецФункции

Функция ПериодичностьУплатыНалогаВзноса(ВидНалога, Организация, Период)
	
	Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНДФЛ(ВидНалога) Тогда
		
		Возврат Перечисления.Периодичность.Месяц;
		
	ИначеЕсли ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНалогУСН(ВидНалога) Тогда
		
		Возврат УчетУСН.ПериодичностьНалоговогоПлатежа(Период);
		
	ИначеЕсли ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоФиксированныеВзносы(ВидНалога) Тогда
		
		Возврат УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период);
		
	ИначеЕсли ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоОбязательныеСтраховыеВзносы(ВидНалога) Тогда
		
		Возврат Перечисления.Периодичность.Месяц;
		
	ИначеЕсли ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоТорговыйСбор(ВидНалога) Тогда
		
		Возврат Перечисления.Периодичность.Квартал;
		
	ИначеЕсли УчетДенежныхСредствКлиентСервер.ЭтоУплатаНалогаНаПрибыль(ВидНалога) Тогда
		
		Возврат НалоговыйУчет.ПериодичностьУплатыНалога(Период, Организация);
		
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.НДС Тогда
		
		Возврат Перечисления.Периодичность.Квартал;
		
	Иначе
		
		Возврат ПериодичностьУплатыНалогаПоУмолчанию();
		
	КонецЕсли;
	
КонецФункции

Функция ПериодичностьУплатыНалогаПоУмолчанию()
	
	Возврат Перечисления.Периодичность.Квартал;
	
КонецФункции

Функция НовыйРеквизитыУведомленияВлияющиеНаСтатусЗадачи()
	
	ОписаниеРеквизитов = Новый Структура;
	
	ОписаниеРеквизитов.Вставить("Ссылка", Неопределено);
	ОписаниеРеквизитов.Вставить("ВидДокумента", "");
	ОписаниеРеквизитов.Вставить("Дата",        '00010101');
	ОписаниеРеквизитов.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ОписаниеРеквизитов.Вставить("Налог",       Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка());
	ОписаниеРеквизитов.Вставить("ВидНалога",   Перечисления.ВидыНалогов.ПустаяСсылка());
	ОписаниеРеквизитов.Вставить("КПП",         "");
	ОписаниеРеквизитов.Вставить("КодБК",       "");
	ОписаниеРеквизитов.Вставить("КодПоОКТМО",  "");
	ОписаниеРеквизитов.Вставить("Уведомление", Документы.УведомлениеОСпецрежимахНалогообложения.ПустаяСсылка());
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

#КонецОбласти
