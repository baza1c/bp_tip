
#Область ПрограммныйИнтерфейс

// Возвращает идентификатор модуля для вызова через внешний программный интерфейс.
//
// Возвращаемое значение:
//   Строка - идентификатор модуля.
//
Функция ПубликуемыйИдентификатор() Экспорт
	
	Возврат "personalized_offers";
	
КонецФункции

// Возвращает данные по идентификатору данных и переданным параметрам.
//
// Параметры:
//  ИдентификаторДанных - Строка - идентификатор данных. Может быть переопределено при обработке.
//                        Указывается в качестве имени файла, возвращаемого в результате.
//  Параметры - ДвоичныеДанные - переданные параметры получения данных.
//  КодВозврата - Строка - код возврата обработчика получения данных (по умолчанию - 10200).
//  Ошибка - Булево - признак завершения выполнения метода с ошибкой.
//  СообщениеОбОшибке - Строка - текст сообщения об ошибке.
// 
// Возвращаемое значение:
//   ДвоичныеДанные - возвращаемые данные.
//
Функция ВозвращаемыеДанные(ИдентификаторДанных, Параметры, КодВозврата, Ошибка, СообщениеОбОшибке) Экспорт
	
	Если ВРег(ИдентификаторДанных) <> ВРег(ПубликуемыйИдентификатор()) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры) Тогда
		
		СообщениеОбОшибке = НСтр("ru = 'Не переданы входящие настройки для отчета.'");
		КодВозврата = АсинхронноеПолучениеДанныхСловарь.КодВозвратаОшибкаДанных();
		Ошибка = Истина;
		Возврат Неопределено;
		
	КонецЕсли;
	
	НастройкиСтрока = ПолучитьСтрокуИзДвоичныхДанных(Параметры);
	НастройкиЗакрытияБаннера = РаботаВМоделиСервисаБТС.СтруктураИзСтрокиJSON(НастройкиСтрока);
	
	Если НастройкиЗакрытияБаннера.Свойство("organization") Тогда
		
		Если НастройкиЗакрытияБаннера.organization.Свойство("inn") Тогда
			
			КПП = Неопределено;
			НастройкиЗакрытияБаннера.organization.Свойство("kpp", КПП);
			Организация = Справочники.Организации.НайтиОрганизацию(НастройкиЗакрытияБаннера.organization.inn, КПП, Ложь);
			Если Организация.Пустая() Тогда
				СообщениеОбОшибке = НСтр("ru = 'Организация с ИНН %1 КПП %2 отсутствует в информационной базе.'");
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеОбОшибке,
					НастройкиЗакрытияБаннера.organization.inn, НастройкиЗакрытияБаннера.organization.kpp);
			КонецЕсли;
			
		Иначе
			
			СообщениеОбОшибке = НСтр("ru = 'В настройках группа organization должна содержать поля inn и kpp.'");
			
		КонецЕсли;
		
	Иначе
		
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		Если Организация.Пустая() Тогда
			СообщениеОбОшибке = НСтр("ru = 'В информационной базе не задана организация по-умолчанию.'");
		КонецЕсли;
		
	КонецЕсли;
	Если Не ПустаяСтрока(СообщениеОбОшибке) Тогда
		
		КодВозврата = АсинхронноеПолучениеДанныхСловарь.КодВозвратаОшибкаДанных();
		Ошибка = Истина;
		Возврат Неопределено;
		
	КонецЕсли;
	
	ИдентификаторБаннера = НастройкиЗакрытияБаннера.id;
	
	ЗакрытыеБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		НовыйЗакрытыеПользователемБаннеры());
		
	БаннерыОрганизации = ЗакрытыеБаннеры.БаннерыПоОрганизации[Организация];
	
	Если ТипЗнч(БаннерыОрганизации) <> Тип("Структура") Тогда
		БаннерыОрганизации = Новый Структура;
	КонецЕсли;
	
	ОбстоятельстваЗакрытия = Новый Структура;
	ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия", ТекущаяДатаСеанса());
	ОбстоятельстваЗакрытия.Вставить("Периодичность", "");
	БаннерыОрганизации.Вставить(ИдентификаторБаннера, ОбстоятельстваЗакрытия);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		ЗакрытыеБаннеры);
	
	Результат = Новый Структура;
	Результат.Вставить("id", ИдентификаторБаннера);
	Результат.Вставить("status", СтатусЗакрытогоБаннера());
	
	ИдентификаторДанных = ПубликуемыйИдентификатор() + ".json";
	
	ИтоговыеДанные = РаботаВМоделиСервисаБТС.СтрокаИзСтруктурыJSON(Результат);
	
	Возврат ПолучитьДвоичныеДанныеИзСтроки(ИтоговыеДанные);
	
КонецФункции

// Заполняет перечень доступных возвращаемых данных.
//
// Параметры:
//  ДоступныеВозвращаемыеДанные - Соответствие - данные, доступные для получения через API
//    * Ключ - Строка - Идентификатор данных
//    * Значение - Структура - см. АсинхронноеПолучениеДанных.НовыйОписаниеВозвращаемыхДанных().
//
Процедура УстановитьДоступныеВозвращаемыеДанные(ДоступныеВозвращаемыеДанные) Экспорт
	
	ОписаниеВозвращаемыхДанных = АсинхронноеПолучениеДанных.НовыйОписаниеВозвращаемыхДанных();
	ОписаниеВозвращаемыхДанных.Наименование = НСтр("ru = 'Закрыть баннер'");
	ОписаниеВозвращаемыхДанных.Описание = НСтр("ru = 'Закрыть баннер'"); 
	ОписаниеВозвращаемыхДанных.МодульМенеджер = ПерсонализированныеПредложенияСервисов;
	ОписаниеВозвращаемыхДанных.ТипыРезультата.Добавить(АсинхронноеПолучениеДанныхСловарь.ТипJSON());
	
	ДоступныеВозвращаемыеДанные.Вставить(ПубликуемыйИдентификатор(), ОписаниеВозвращаемыхДанных);
	
КонецПроцедуры

#Область ПолучениеИЗакрытиеБаннеров

// Получает баннер, в случае если есть что показать. Если баннер не найден, в хранилище помещается Неопределено.
// Параметры:
//	СтруктураПараметров - Струкутура - Структура параметров:
//		Организация - СправочникСсылка.Организации - Организация, по которой ищем баннер.
//		Размещение - Строка - Идентификатор формы по которой нужно показать баннер. см. функции ИмяРазмещения...().
//		ПоказатьПредыдущий - Булево - Признак показа предыдущего в очереди баннера. Если Ложь - показывается следующий баннер.
//	АдресХранилища - Строка -Адрес хранилища, по которому будет помещен баннер.
//
Процедура ПолучитьБаннер(СтруктураПараметров, АдресХранилища) Экспорт
	
	Если ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		Баннер = НовыйБаннерИнтерфейсИнтеграцииСБанкомИнформированиеОПереносеКомандЕНС(СтруктураПараметров.Размещение);
		ПоместитьВоВременноеХранилище(Баннер, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		// Не показываем баннеры, если работа с внешними ресурсами заблокирована.
		// Нет смысла собирать и показывать баннеры для копий информационных баз.
		ПоместитьВоВременноеХранилище(Неопределено, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Организация = СтруктураПараметров.Организация;
	Размещение = СтруктураПараметров.Размещение;
	ПоказатьПредыдущий = СтруктураПараметров.ПоказатьПредыдущий;
	
	// Получаем баннеры в ротации у текущего пользователя в указанном размещении.
	БаннерыВРотации = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПерсонализированныеПредложенияСервисов_НаборБаннеровВРотации",
		Размещение, Новый Массив);
	
	Если ТипЗнч(БаннерыВРотации) <> Тип("Массив") Тогда
		БаннерыВРотации = Новый Массив;
	КонецЕсли;
	
	// Ротируем пользовательский список баннеров.
	Если БаннерыВРотации.Количество() > 1 Тогда
		Если ПоказатьПредыдущий Тогда
			ПоследнийБаннер = БаннерыВРотации[БаннерыВРотации.ВГраница()];
			БаннерыВРотации.Удалить(БаннерыВРотации.ВГраница());
			БаннерыВРотации.Вставить(0, ПоследнийБаннер);
		Иначе
			ПервыйБаннер = БаннерыВРотации[0];
			БаннерыВРотации.Удалить(0);
			БаннерыВРотации.Добавить(ПервыйБаннер);
		КонецЕсли;
	КонецЕсли;
	
	ТекущаяДата = НачалоДня(ОбщегоНазначенияБП.ТекущаяДатаНаСервере());
	
	// Получим список баннеров для пользователя по организации и размещению.
	// Баннеры, привязанные к организации, не показываем, если организация не заполнена.
	// Баннеры, непривязанные к организации, показываем всегда.
	СписокБаннеров = БаннерыПользователя(Организация, Размещение, ТекущаяДата);
	Если СписокБаннеров.Количество() = 0 Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	// Обновим данные по которым будем получать информацию.
	ИнтервалОбновления = 60 * 60 * 4; // обновляем данные с интервалом 4 часа.
	РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(
		Организация,
		ТекущаяДата,
		ИнтервалОбновления,
		РазделыПерсонализированныхДанных(СписокБаннеров));
	
	// Получим данные для баннеров.
	ДанныеБаннеров = ДанныеБаннеров(СписокБаннеров, СтруктураПараметров, ТекущаяДата);
	
	ТекущаяГруппа = Неопределено;
	
	// Собираем идентификаторы всех доступных баннеров.
	ИдентификаторыДоступныхБаннеров = Новый Массив;
	Для Каждого СтрокаСписка Из СписокБаннеров Цикл
		Если ВыполняютсяУсловияБаннера(СтрокаСписка.Баннер, ДанныеБаннеров) Тогда
			Если ТекущаяГруппа = Неопределено Тогда
				ТекущаяГруппа = СтрокаСписка.Баннер.Группа;
			КонецЕсли;
			Если ТекущаяГруппа <> СтрокаСписка.Баннер.Группа Тогда
				Продолжить; // Пропускаем баннеры, которые не входят в текущую группу.
			КонецЕсли;
			
			ИдентификаторыДоступныхБаннеров.Вставить(0, СтрокаСписка.Баннер.Идентификатор);
			// Если исключительный баннер не входит в группу, показывается только он.
			Если СтрокаСписка.Баннер.Исключительный И СтрокаСписка.Баннер.Группа = Неопределено Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ИдентификаторыНовогоНабораБаннеровВРотации = Новый Массив;
	
	// Оставляем в ротации только доступные баннеры.
	Для каждого БаннерВРотации Из БаннерыВРотации Цикл
		Если ИдентификаторыДоступныхБаннеров.Найти(БаннерВРотации.Идентификатор) <> Неопределено Тогда
			ИдентификаторыНовогоНабораБаннеровВРотации.Добавить(БаннерВРотации.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	// Новые доступные баннеры добавляем в начало очереди.
	Для каждого ИдентификаторДоступногоБаннера Из ИдентификаторыДоступныхБаннеров Цикл
		Позиция = ?(ПоказатьПредыдущий И ИдентификаторыНовогоНабораБаннеровВРотации.Количество() > 0, 1, 0);
		Если ИдентификаторыНовогоНабораБаннеровВРотации.Найти(ИдентификаторДоступногоБаннера) = Неопределено Тогда
			ИдентификаторыНовогоНабораБаннеровВРотации.Вставить(Позиция, ИдентификаторДоступногоБаннера);
		КонецЕсли;
	КонецЦикла;
	
	// Заменяем набор баннеров в ротации на новый
	БаннерыВРотации = Новый Массив;
	Для каждого ИдентификаторНовогоБаннераВРотации Из ИдентификаторыНовогоНабораБаннеровВРотации Цикл
		НовыйБаннер = СписокБаннеров.Найти(ИдентификаторНовогоБаннераВРотации, "ИдентификаторБаннера").Баннер;
		ЗаполнитьДанныеБаннера(НовыйБаннер, ДанныеБаннеров);
		БаннерыВРотации.Добавить(НовыйБаннер);
	КонецЦикла;
	
	// Сохраняем баннеры в ротации у текущего пользователя в указанном размещении.
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПерсонализированныеПредложенияСервисов_НаборБаннеровВРотации",
		Размещение, БаннерыВРотации);
	
	Баннер = Неопределено;
	
	Если БаннерыВРотации.Количество() > 0 Тогда
		Баннер = БаннерыВРотации[0];
		Баннер.Вставить("Единственный", БаннерыВРотации.Количество() = 1);
	КонецЕсли;
	
	Если Баннер <> Неопределено Тогда
		
		ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Персонализированные предложения сервисов. Показан баннер'"),
			УровеньЖурналаРегистрации.Информация,
			,
			,
			Баннер.Идентификатор);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Баннер, АдресХранилища);
	
КонецПроцедуры

// Процедура отображает баннер на форме.
// Параметры:
//		Форма - Форма - Форма, на которой отображается баннер.
//		АдресРезультата - Строка - Адрес временного хранилища, в которое помещен баннер.
//
Процедура УстановитьБаннерНаФорме(Форма, АдресРезультата) Экспорт
	
	Элементы = Форма.Элементы;
	
	Баннер = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Баннер = Неопределено Тогда
		Элементы.Баннер.Видимость = Ложь;
	Иначе
		Форма.Баннер = Баннер;
		Элементы.ГруппаБаннерОжидание.Видимость = Ложь;
		Элементы.ТекстБаннера.Видимость         = Истина;
		Элементы.КартинкаБаннера.Видимость      = Истина;
		Элементы.Баннер.Видимость               = Истина;
		Элементы.ТекстБаннера.Шрифт             = Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 2);
		Элементы.БаннерСФоном.ЦветФона          = ЦветаСтиля[Баннер.ЦветФонаБаннер];
		Если ЗначениеЗаполнено(Баннер.ИмяКартинкаЛоготипа) Тогда
			Элементы.КартинкаБаннера.Видимость  = Истина;
			Элементы.КартинкаБаннера.Картинка   = БиблиотекаКартинок[Баннер.ИмяКартинкаЛоготипа];
		Иначе
			Элементы.КартинкаБаннера.Видимость  = Ложь;
		КонецЕсли;
		Форма.ТекстБаннера = Форма.Баннер.ТекстБаннера;
		
		Элементы.КартинкаПредыдущийБаннер.Видимость = НЕ Баннер.Единственный;
		Элементы.КартинкаСледующийБаннер.Видимость = НЕ Баннер.Единственный;
		
		Элементы.КартинкаЗакрытьБаннер.Доступность = Истина;
		Элементы.КартинкаПредыдущийБаннер.Доступность = Истина;
		Элементы.КартинкаСледующийБаннер.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьЗакрытиеБаннера(Баннер, Организация) Экспорт
	
	СохранитьОбстоятельстваЗакрытияБаннера(Баннер, Организация);
	
	СписокБаннеров = НовыйТаблицаСписокБаннеров();
	ДобавитьБаннерВТаблицу(СписокБаннеров, Баннер);
	РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(
		Организация, Дата(1, 1, 1), 0, РазделыПерсонализированныхДанных(СписокБаннеров));
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Персонализированные предложения сервисов. Закрыт баннер'"),
		УровеньЖурналаРегистрации.Информация,
		,
		,
		Баннер.Идентификатор);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет баннер в Общую форму "ПечатьДокументов"
//
// Параметры
//  Форма - ФормаКлиентскогоПриложения - Общая форма "ПечатьДокументов"
//
Процедура ПечатьДокументовПриСозданииНаСервере(Форма) Экспорт
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПечатныхФорм = Форма.НастройкиПечатныхФорм;
	Если НастройкиПечатныхФорм.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиБаннеров = НастройкиБаннеровПечатныхФормДокументов();
	Настройка = НастройкиБаннеров.Найти(НастройкиПечатныхФорм[0].ИмяМакета, "ИмяМакета");
	
	Если Настройка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьРеквизитыИЭлементыФормыДляРазмещенияБаннера(Форма);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Параметры.Вставить("Размещение", ИмяРазмещенияПечатьДокументов());
	Параметры.Вставить("ПоказатьПредыдущий", Ложь);
	
	ПолучитьБаннер(Параметры, АдресХранилища);
	
	Баннер = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если Баннер <> Неопределено Тогда
		Баннер.ТекстБаннера = ТекстБаннераПечатьАртикулов(Настройка);
	КонецЕсли;
	
	УстановитьБаннерНаФорме(Форма, АдресХранилища);
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

Процедура ОчиститьБаннерыВРотацииПриОбновленииИнформационнойБазы() Экспорт
	
	ИменаРазмещения = Новый Массив;
	ИменаРазмещения.Добавить(ИмяРазмещенияОбщее());
	ИменаРазмещения.Добавить(ИмяРазмещенияНачислениеЗарплаты());
	ИменаРазмещения.Добавить(ИмяРазмещенияПростойИнтерфейсРазделСотрудники());
	
	ОчиститьБаннерыВРотации(ИменаРазмещения);
	
КонецПроцедуры

Процедура ОчиститьБаннерыВРотации(ИменаРазмещения)
	
	ИмяНастройки = "ПерсонализированныеПредложенияСервисов_НаборБаннеровВРотации";
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка,
	|	Пользователи.ИдентификаторПользователяИБ
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ИдентификаторПользователяИБ) Тогда
			
			Пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Выборка.ИдентификаторПользователяИБ);
			Если Пользователь <> Неопределено Тогда
				Для каждого ИмяРазмещения Из ИменаРазмещения Цикл
					ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки,
						ИмяРазмещения, Новый Массив,, Пользователь.Имя);
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли НЕ ОбщегоНазначения.РазделениеВключено() Тогда
			
			Для каждого ИмяРазмещения Из ИменаРазмещения Цикл
				ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки,
					ИмяРазмещения, Новый Массив);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Конструкторы
Функция НовыйЗакрытыеПользователемБаннеры() Экспорт
	
	ЗакрытыеПользователемБаннеры = Новый Структура;
	ЗакрытыеПользователемБаннеры.Вставить("БаннерыБезОрганизации", Новый Структура);
	ЗакрытыеПользователемБаннеры.Вставить("БаннерыПоОрганизации",  Новый Соответствие);
	
	Возврат ЗакрытыеПользователемБаннеры;
	
КонецФункции

Функция НовыйБаннерДляСписка() Экспорт
	
	Баннер = Новый Структура;
	Баннер.Вставить("ИмяСервиса",              "");
	Баннер.Вставить("Идентификатор",           "");
	Баннер.Вставить("НавигационнаяСсылка",     "");
	Баннер.Вставить("ТекстБаннера",            Новый ФорматированнаяСтрока(""));
	Баннер.Вставить("ИмяКартинкаЛоготипа",     "");
	Баннер.Вставить("ЦветФонаБаннер",         "ЦветФонаБаннер");
	Баннер.Вставить("ЗависитОтОрганизации",   Истина);
	Баннер.Вставить("Исключительный",         Ложь);
	Баннер.Вставить("Группа",                 Неопределено);
	Баннер.Вставить("ДоступенПоПравам",       Ложь);
	Баннер.Вставить("ДанныеБаннера",          Новый Структура);
	Баннер.Вставить("ОбстоятельстваЗакрытия", Новый Структура);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия", '00010101');
	Баннер.ОбстоятельстваЗакрытия.Вставить("Периодичность", Перечисления.Периодичность.Месяц);
	
	Возврат Баннер;
	
КонецФункции

Функция НовыйЗаголовок(ТекстЗаголовка, НавигационнаяСсылка = "", Шрифт = Неопределено) Экспорт
	
	ШрифтЗаголовка = ?(Шрифт = Неопределено, ШрифтыСтиля.ШрифтЗаголовкаБаннера, Шрифт);
	
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтЗаголовка, , , НавигационнаяСсылка);
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтЗаголовка);
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтыСтиля.ШрифтЗаголовкаБаннера);
	
КонецФункции

Функция НовыйСтрокаБаннера(ТекстСтроки, НавигационнаяСсылка = "", Шрифт = Неопределено) Экспорт
	
	ШрифтСтроки = ?(Шрифт = Неопределено, ШрифтыСтиля.ШрифтТекстаБаннера, Шрифт);
	
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстСтроки, ШрифтСтроки, , , НавигационнаяСсылка);
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстСтроки, ШрифтСтроки);
	КонецЕсли;
	
КонецФункции

Функция НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка) Экспорт
	
	Возврат Новый ФорматированнаяСтрока(
		ТекстЗаголовка,
		Символы.ПС,
		Новый ФорматированнаяСтрока(" ", Новый Шрифт(,5)), // Отступ между заголовком и подзаголовком.
		Символы.ПС,
		ТекстПодзаголовка);
	
КонецФункции
#КонецОбласти

#КонецОбласти

#Область РаботаСБаннерами

Функция ДанныеБаннеров(СписокБаннеров, СтруктураПараметров, ТекущаяДата)
	
	// Для получения данных удобнее использовать структуру вместо таблицы.
	// Перенесем данные из списка в структуру.
	СтруктураБаннеров = Новый Структура;
	Для Каждого СтрокаСписка Из СписокБаннеров Цикл
		СтруктураБаннеров.Вставить(СтрокаСписка.Баннер.Идентификатор, СтрокаСписка.Баннер);
	КонецЦикла;
	
	Организация = СтруктураПараметров.Организация;
	
	НомераТаблиц = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	ТекстЗапросаОтчетнаяКампания(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаЧисленностьСотрудников(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаНДСКВычетуКонтрагент(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
	+ ТекстЗапросаНДСКВычетуСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
	+ ТекстЗапросаКрупнейшийДебитор(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаКоличествоКонтрагентов(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаКрупнейшиеДебиторыСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаОбщееКоличествоДокументов(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаКоличествоПоступлений(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаКоличествоРеализаций(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаПечатьАртикулов(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаИнтеграцияСоSmartway(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаЗаявкаНаОткрытиеСчета(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаЗаявкаНаКредит(Запрос, СтруктураБаннеров, НомераТаблиц, Организация)
	+ ТекстЗапросаЗаявкаНаЛизинг(Запрос, СтруктураБаннеров, НомераТаблиц, Организация)
	+ ТекстЗапросаФинОтчетность(Запрос, СтруктураБаннеров, НомераТаблиц, Организация)
	+ ТекстЗапросаПереходиНаНКО(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаМоиЗадачи(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаАвтоматическийВыпускПродукции(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаАвтоматическийУчетУбытковПослеРеализацииОС(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаУведомлениеОВыкупеВайлдберриз(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаУведомленияЕНС(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаБанкиАУСН(Запрос, СтруктураБаннеров, НомераТаблиц)
	+ ТекстЗапросаКонтрагентПодключенКСервисуСверки(Запрос, СтруктураБаннеров, НомераТаблиц);
	
	Запрос.УстановитьПараметр("Организация",          Организация);

	СписокОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);

	Запрос.УстановитьПараметр("ТекущаяДата",          ТекущаяДата);
	// Ищем отчетность, которая начнется в течении 2х недель
	Запрос.УстановитьПараметр("СрокНачалаОтчетности", ТекущаяДата + 60*60*24*14);
	Запрос.УстановитьПараметр("НачалоКвартала",       НачалоКвартала(ТекущаяДата));
	Запрос.УстановитьПараметр("КонецКвартала",        КонецКвартала(ТекущаяДата));
	
	Запрос.УстановитьПараметр("НачалоГодаГраница", Новый Граница(НачалоГода(ТекущаяДата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецГодаГраница", Новый Граница(КонецГода(ТекущаяДата), ВидГраницы.Включая));
	
	МетаданныеРасшифровки = Метаданные.РегистрыСведений.ПерсонализированныеДанные.Ресурсы.ДанныеРасшифровки;
	Запрос.УстановитьПараметр("ПустыеЗначенияДанныеРасшифровки", ОбщегоНазначенияБП.МассивПустыхЗначенийРеквизита(МетаданныеРасшифровки));
	
	ДанныеБаннеров = Новый Структура;
	ДанныеБаннеров.Вставить("ТекущаяДата", ТекущаяДата);
	ДанныеБаннеров.Вставить("Организация", Организация);
	Если СтруктураБаннеров.Свойство(ИдентификаторБаннераНастройкаЭлектронныхЧеков())
	   И СтруктураПараметров.Свойство("РабочееМесто") Тогда
		// Дополнительно сохраняем рабочее место как часть информации о баннерах.
		ДанныеБаннеров.Вставить("РабочееМесто", СтруктураПараметров.РабочееМесто);
	КонецЕсли;
	ДанныеБаннеров.Вставить("Размещение", СтруктураПараметров.Размещение);
	
	ПерсонализированныеПредложенияСервисовПереопределяемый.ПолучитьДанныеБаннеров(Запрос, СтруктураБаннеров, НомераТаблиц, ДанныеБаннеров, СтруктураПараметров);
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Получаем данные из информационной базы.
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ДанныеБаннеров.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	// Подготавливаем информацию баннеров по полученным данным.
	ПолучитьДанныеБаннеров(СтруктураБаннеров, ДанныеБаннеров);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеБаннеров;
	
КонецФункции

Процедура ПолучитьДанныеБаннеров(СтруктураБаннеров, ДанныеБаннеров)
	
	Для Каждого ПодготавливаемыйБаннер Из СтруктураБаннеров Цикл
		
		// Баннеры, использующие для получения данных дополнительные параметры, возвращенные функцией НовыйБаннер...() .
		Если ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераАУСН() Тогда
			ПолучитьДанныеБаннераАУСН(ДанныеБаннеров, ПодготавливаемыйБаннер.Значение);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ =
			ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН() Тогда
			ПолучитьДанныеБаннераБанкиАУСН(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераЛКАУСН() Тогда
			ПолучитьДанныеБаннераЛКАУСН(ДанныеБаннеров, ПодготавливаемыйБаннер.Значение);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераНадежностьБанков() Тогда
			ПолучитьДанныеБаннераНадежностьБанков(ДанныеБаннеров, ПодготавливаемыйБаннер.Значение);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераНастройкаЭлектронныхЧеков() Тогда
			ПолучитьДанныеБаннераНастройкаЭлектронныхЧеков(ДанныеБаннеров, ПодготавливаемыйБаннер.Значение);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераЗагрузкаИзОФД() Тогда
			ПолучитьДанныеБаннераЗагрузкаИзОФД(ДанныеБаннеров, ПодготавливаемыйБаннер.Значение);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераПлатформаСамозанятые() Тогда
			ПолучитьДанныеБаннераПлатформаСамозанятые(ДанныеБаннеров, ПодготавливаемыйБаннер.Значение);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераПоддержкаСервиса() Тогда
			ПолучитьДанныеБаннераПоддержкаСервиса(ДанныеБаннеров, ПодготавливаемыйБаннер.Значение);
			
		// Баннеры, которые зависят от организации.
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераПереносСроковУплатыНалогов() Тогда
			ПолучитьДанныеБаннераПереносСроковУплатыНалогов(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераПереносСроковУплатыНалогов_МониторНалогов() Тогда
			ПолучитьДанныеБаннераПереносСроковУплатыНалогов_МониторНалогов(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераПереходНаЕНС() Тогда
			ПолучитьДанныеБаннераПереходНаЕНС(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераПереходНаЕНС_МониторНалогов() Тогда
			ПолучитьДанныеБаннераПереходНаЕНС_МониторНалогов(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераСтороннийСертификат() Тогда
			ПолучитьДанныеБаннераСтороннийСертификат(ДанныеБаннеров);
			
		// Баннеры без параметров.
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей() Тогда
			ПолучитьДанныеБаннераАвтоматическаяЗагрузкаПатчей(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераКабинетСотрудника() Тогда
			ПолучитьДанныеБаннераКабинетСотрудника(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераКадровыеДокументыЭТК() Тогда
			ПолучитьДанныеБаннераКадровыеДокументыЭТК(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераНастройкаВидимостиСчетовУчета() Тогда
			ПолучитьДанныеБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераНастройкаФункциональности() Тогда
			ПолучитьДанныеБаннераНастройкаФункциональности(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераПредодобренныйКредитВСбербанке() Тогда
			ПолучитьДанныеБаннераПредодобренныйКредитВСбербанке(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераТестовыйТариф() Тогда
			ПолучитьДанныеБаннераТестовыйТариф(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераУстановитьДатуЗапрета() Тогда
			ПолучитьДанныеБаннераУстановитьДатуЗапрета(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераЭТрН() Тогда
			ПолучитьДанныеБаннераЭТрН(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераНоваяАвторизацияЯндекс() Тогда
			ПолучитьДанныеБаннераНоваяАвторизацияЯндекс(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте() Тогда
			ПолучитьДанныеБаннераУведомленияОЗадачахПоЭлектроннойПочте(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераСервисСверкиРасчетов() Тогда
			ПолучитьДанныеБаннераСервисСверкиРасчетов(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераМобильныйКлиентБизнесСтарт() Тогда
			ПолучитьДанныеБаннераМобильныйКлиентБизнесСтарт(ДанныеБаннеров);
		ИначеЕсли ПодготавливаемыйБаннер.Ключ = ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов() Тогда
			ПолучитьДанныеБаннераАвтозагрузкаОтчетовМаркетплейсов(ДанныеБаннеров);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннера(Баннер, ДанныеБаннеров)
	
	Идентификатор = Баннер.Идентификатор;
	
	Если Не ДанныеБаннеров.Свойство(Идентификатор) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеБаннера = ДанныеБаннеров[Идентификатор];
	
	ЭтоИдентификаторПереопределяемогоБаннера = Ложь;
	ПерсонализированныеПредложенияСервисовПереопределяемый.ИдентификаторБаннера(Идентификатор, ЭтоИдентификаторПереопределяемогоБаннера);
	
	Если ЭтоИдентификаторПереопределяемогоБаннера Тогда
		ВыполняетсяУсловиеПереопределяемогоБаннера = Ложь;
		ПерсонализированныеПредложенияСервисовПереопределяемый.ВыполняетсяУсловиеБаннера(ДанныеБаннера, Баннер, Идентификатор, ВыполняетсяУсловиеПереопределяемогоБаннера);
		Возврат ВыполняетсяУсловиеПереопределяемогоБаннера;
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераСтороннийСертификат() Тогда
		Возврат ВыполняютсяУсловияБаннераСтороннийСертификат(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЧисленностьСотрудников() Тогда
		Возврат ВыполняютсяУсловияБаннераЧисленностьСотрудников(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОтчетнаяКампания() Тогда
		Возврат ВыполняютсяУсловияБаннераОтчетнаяКампания(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуКонтрагент() Тогда
		Возврат ВыполняютсяУсловияБаннераНДСКВычетуКонтрагент(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЭТрН() Тогда
		Возврат ВыполняютсяУсловияБаннераЭТрН(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНоваяАвторизацияЯндекс() Тогда
		Возврат ВыполняютсяУсловияБаннераНоваяАвторизацияЯндекс(ДанныеБаннера, Баннер);
			
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуСПАРК() Тогда
		Возврат ВыполняютсяУсловияБаннераНДСКВычетуСПАРК(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшийДебитор() Тогда
		Возврат ВыполняютсяУсловияБаннераКрупнейшийДебитор(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоКонтрагентов() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоКонтрагентов(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК() Тогда
		Возврат ВыполняютсяУсловияБаннераКрупнейшиеДебиторыСПАРК(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОбщееКоличествоДокументов() Тогда
		Возврат ВыполняютсяУсловияБаннераОбщееКоличествоДокументов(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоПоступлений() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоПоступлений(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЗагрузкаИзОФД() Тогда
		Возврат ВыполняютсяУсловияБаннераЗагрузкаИзОФД(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоРеализаций() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоРеализаций(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНадежностьБанков() Тогда
		Возврат ВыполняютсяУсловияБаннераНадежностьБанков(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераАУСН() Тогда
		Возврат ВыполняютсяУсловияАУСН(ДанныеБаннера, Баннер);
	
	ИначеЕсли Идентификатор = ИдентификаторБаннераЛКАУСН() Тогда
		Возврат ВыполняютсяУсловияЛКАУСН(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН() Тогда
		Возврат ВыполняютсяУсловияБанкиАУСН(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПлатформаСамозанятые() Тогда
		Возврат ВыполняютсяУсловияПлатформаСамозанятые(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПоддержкаСервиса() Тогда
		Возврат ВыполняютсяУсловияБаннераПоддержкаСервиса(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераТестовыйТариф() Тогда
		Возврат ВыполняютсяУсловияБаннераТестовыйТариф(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПредодобренныйКредитВСбербанке() Тогда
		Возврат ВыполняютсяУсловияБаннераПредодобренныйКредитВСбербанке(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаВидимостиСчетовУчета() Тогда
		Возврат ВыполняютсяУсловияБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераИнтеграцияСоSmartway() Тогда
		Возврат ВыполняютсяУсловияБаннераИнтеграцияСоSmartway(ДанныеБаннера, Баннер);
	
	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаОткрытиеСчета() Тогда
		Возврат ВыполняютсяУсловияБаннераЗаявкаНаОткрытиеСчета(ДанныеБаннера, Баннер);

	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаКредит() Тогда
		Возврат ВыполняютсяУсловияБаннераЗаявкаНаКредит(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаЛизинг() Тогда
		Возврат ВыполняютсяУсловияБаннераЗаявкаНаЛизинг(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераФинОтчетность()
		ИЛИ Идентификатор = ИдентификаторБаннераФинОтчетностьКраткий() Тогда
		Возврат ВыполняютсяУсловияБаннераФинОтчетность(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПереходиНаНКО() Тогда
		Возврат ВыполняютсяУсловияБаннераПереходиНаНКО(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаФункциональности() Тогда
		Возврат ВыполняютсяУсловияБаннераНастройкаФункциональности(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераМоиЗадачи() Тогда
		Возврат ВыполняютсяУсловияБаннераМоиЗадачи(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераАвтоматическийВыпускПродукции() Тогда
		Возврат ВыполняютсяУсловияБаннераАвтоматическийВыпускПродукции(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПечатьАртикулов() Тогда
		Возврат ВыполняютсяУсловияБаннераПечатьАртикулов(ДанныеБаннера, Баннер);	
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКадровыеДокументыЭТК() Тогда
		Возврат ВыполняютсяУсловияБаннераКадровыеДокументыЭТК(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКабинетСотрудника() Тогда
		Возврат ВыполняютсяУсловияБаннераКабинетСотрудника(ДанныеБаннера, Баннер);
		
	ИначеЕсли  Идентификатор = ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей() Тогда
		Возврат ВыполняютсяУсловияБаннераАвтоматическаяЗагрузкаПатчей(ДанныеБаннера, Баннер);
		
	ИначеЕсли  Идентификатор = ИдентификаторБаннераУстановитьДатуЗапрета() Тогда
		Возврат ВыполняютсяУсловияБаннераУстановитьДатуЗапрета(ДанныеБаннера, Баннер);
		
	ИначеЕсли  Идентификатор = ИдентификаторБаннераПереходНаЕНС() Тогда
		Возврат ВыполняютсяУсловияБаннераПереходНаЕНС(ДанныеБаннера, Баннер);
		
	ИначеЕсли  Идентификатор = ИдентификаторБаннераПереходНаЕНС_МониторНалогов() Тогда
		Возврат ВыполняютсяУсловияБаннераПереходНаЕНС(ДанныеБаннера, Баннер);
		
	ИначеЕсли  Идентификатор = ИдентификаторБаннераПереносСроковУплатыНалогов() Тогда
		Возврат ВыполняютсяУсловияБаннераПереносСроковУплатыНалогов(ДанныеБаннера, Баннер);
		
	ИначеЕсли  Идентификатор = ИдентификаторБаннераПереносСроковУплатыНалогов_МониторНалогов() Тогда
		Возврат ВыполняютсяУсловияБаннераПереносСроковУплатыНалогов(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаЭлектронныхЧеков() Тогда
		Возврат ВыполняютсяУсловияБаннераНастройкаЭлектронныхЧеков(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз() Тогда
		Возврат ВыполняютсяУсловияБаннераУведомлениеОВыкупеВайлдберриз(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераАвтоматическийУчетУбытковПослеРеализацииОС() Тогда
		Возврат ВыполняютсяУсловияБаннераАвтоматическийУчетУбытковПослеРеализацииОС(ДанныеБаннера, Баннер);
		
	ИначеЕсли  Идентификатор = ИдентификаторБаннераУведомленияЕНС() Тогда
		Возврат ВыполняютсяУсловияБаннераУведомленияЕНС(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте() Тогда
		Возврат ВыполняютсяУсловияБаннераУведомленияОЗадачахПоЭлектроннойПочте(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераСервисСверкиРасчетов() Тогда
		Возврат ВыполняютсяУсловияБаннераСервисСверкиРасчетов(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису() Тогда
		Возврат ВыполняютсяУсловияБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису(ДанныеБаннера, Баннер);
	ИначеЕсли Идентификатор = ИдентификаторБаннераМобильныйКлиентБизнесСтарт() Тогда
		Возврат ВыполняютсяУсловияБаннераМобильныйКлиентБизнесСтарт(ДанныеБаннера, Баннер);
	ИначеЕсли Идентификатор = ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов() Тогда
		Возврат ВыполняютсяУсловияБаннераАвтозагрузкаОтчетовМаркетплейсов(ДанныеБаннера, Баннер);
		
	Иначе
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннера(Баннер, ДанныеБаннеров)
	
	Идентификатор = Баннер.Идентификатор;
	ДанныеБаннера = ДанныеБаннеров[Идентификатор];
	ТекущаяДата   = ДанныеБаннеров.ТекущаяДата;
	
	ЭтоИдентификаторПереопределяемогоБаннера = Ложь;
	ПерсонализированныеПредложенияСервисовПереопределяемый.ИдентификаторБаннера(Идентификатор, ЭтоИдентификаторПереопределяемогоБаннера);
	
	Если ЭтоИдентификаторПереопределяемогоБаннера Тогда
		ПерсонализированныеПредложенияСервисовПереопределяемый.ЗаполнитьДанныеБаннера(ДанныеБаннера, Баннер);
	
	ИначеЕсли Идентификатор = ИдентификаторБаннераСтороннийСертификат() Тогда
		ЗаполнитьДанныеБаннераСтороннийСертификат(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЧисленностьСотрудников() Тогда
		ЗаполнитьДанныеБаннераЧисленностьСотрудников(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОтчетнаяКампания() Тогда
		ЗаполнитьДанныеБаннераОтчетнаяКампания(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуКонтрагент() Тогда
		ЗаполнитьДанныеБаннераНДСКВычетуКонтрагент(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНДСКВычетуСПАРК() Тогда
		ЗаполнитьДанныеБаннераНДСКВычетуСПАРК(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшийДебитор() Тогда
		ЗаполнитьДанныеБаннераКрупнейшийДебитор(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЭТрН() Тогда
		ЗаполнитьДанныеБаннераЭТрН(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНоваяАвторизацияЯндекс() Тогда
		ЗаполнитьДанныеБаннераНоваяАвторизацияЯндекс(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоКонтрагентов() Тогда
		ЗаполнитьДанныеБаннераКоличествоКонтрагентов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК() Тогда
		ЗаполнитьДанныеБаннераКрупнейшиеДебиторыСПАРК(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераОбщееКоличествоДокументов() Тогда
		ЗаполнитьДанныеБаннераОбщееКоличествоДокументов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоПоступлений() Тогда
		ЗаполнитьДанныеБаннераКоличествоПоступлений(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоРеализаций() Тогда
		ЗаполнитьДанныеБаннераКоличествоРеализаций(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНадежностьБанков() Тогда
		ЗаполнитьДанныеБаннераНадежностьБанков(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераАУСН() Тогда
		ЗаполнитьДанныеБаннераАУСН(Баннер, ДанныеБаннера);
	
	ИначеЕсли Идентификатор = ИдентификаторБаннераЛКАУСН() Тогда
		ЗаполнитьДанныеБаннераЛКАУСН(Баннер, ДанныеБаннера);
	
	ИначеЕсли Идентификатор = ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН() Тогда
		ЗаполнитьДанныеБаннераБанкиАУСН(Баннер, ДанныеБаннера);
	
	ИначеЕсли Идентификатор = ИдентификаторБаннераПлатформаСамозанятые() Тогда
		ЗаполнитьДанныеБаннераПлатформаСамозанятые(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПоддержкаСервиса() Тогда
		ЗаполнитьДанныеБаннераПоддержкаСервиса(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераТестовыйТариф() Тогда
		ЗаполнитьДанныеБаннераТестовыйТариф(Баннер, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПредодобренныйКредитВСбербанке() Тогда
		ЗаполнитьДанныеБаннераПредодобренныйКредитВСбербанке(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаВидимостиСчетовУчета() Тогда
		ЗаполнитьДанныеБаннераНастройкаВидимостиСчетовУчета(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераИнтеграцияСоSmartway() Тогда
		ЗаполнитьДанныеБаннераИнтеграцияСоSmartway(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаОткрытиеСчета() Тогда
		ЗаполнитьДанныеБаннераЗаявкаНаОткрытиеСчета(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаКредит() Тогда
		ЗаполнитьДанныеБаннераЗаявкаНаКредит(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЗаявкаНаЛизинг() Тогда
		ЗаполнитьДанныеБаннераЗаявкаНаЛизинг(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераФинОтчетность() Тогда
		ЗаполнитьДанныеБаннераФинОтчетность(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераФинОтчетностьКраткий() Тогда
		ЗаполнитьДанныеБаннераФинОтчетностьКраткий(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПереходиНаНКО() Тогда
		ЗаполнитьДанныеБаннераПереходиНаНКО(Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаФункциональности() Тогда
		ЗаполнитьДанныеБаннераНастройкаФункциональности(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПечатьАртикулов() Тогда
		ЗаполнитьДанныеБаннераПечатьАртикулов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКадровыеДокументыЭТК() Тогда
		ЗаполнитьДанныеБаннераКадровыеДокументыЭТК(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераМоиЗадачи() Тогда
		ЗаполнитьДанныеБаннераМоиЗадачи(Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераАвтоматическийВыпускПродукции() Тогда
		ЗаполнитьДанныеБаннераАвтоматическийВыпускПродукции(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКабинетСотрудника() Тогда
		ЗаполнитьДанныеБаннераКабинетСотрудника(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей() Тогда
		ЗаполнитьДанныеБаннераАвтоматическаяЗагрузкаПатчей(Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераУстановитьДатуЗапрета() Тогда
		ЗаполнитьДанныеБаннераУстановитьДатуЗапрета(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПереходНаЕНС() Тогда
		ЗаполнитьДанныеБаннераПереходНаЕНС(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПереходНаЕНС_МониторНалогов() Тогда
		ЗаполнитьДанныеБаннераПереходНаЕНС(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПереносСроковУплатыНалогов() Тогда
		ЗаполнитьДанныеБаннераПереносСроковУплатыНалогов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераПереносСроковУплатыНалогов_МониторНалогов() Тогда
		ЗаполнитьДанныеБаннераПереносСроковУплатыНалогов(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераНастройкаЭлектронныхЧеков() Тогда
		ЗаполнитьДанныеБаннераНастройкаЭлектронныхЧеков(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераЗагрузкаИзОФД() Тогда
		ЗаполнитьДанныеБаннераЗагрузкаИзОФД(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз() Тогда
		ЗаполнитьДанныеБаннераУведомлениеОВыкупеВайлдберриз(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераАвтоматическийУчетУбытковПослеРеализацииОС() Тогда
		ЗаполнитьДанныеБаннераАвтоматическийУчетУбытковПослеРеализацииОС(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераУведомленияЕНС() Тогда
		ЗаполнитьДанныеБаннераУведомленияЕНС(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте() Тогда
		ЗаполнитьДанныеБаннераУведомленияОЗадачахПоЭлектроннойПочте(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераСервисСверкиРасчетов() Тогда
		ЗаполнитьДанныеБаннераСервисСверкиРасчетов(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису() Тогда
		ЗаполнитьДанныеБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису(Баннер, ДанныеБаннера);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераМобильныйКлиентБизнесСтарт() Тогда
		ЗаполнитьДанныеБаннераМобильныйКлиентБизнесСтарт(Баннер, ДанныеБаннера);
	ИначеЕсли Идентификатор = ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов() Тогда
		ЗаполнитьДанныеБаннераАвтозагрузкаОтчетовМаркетплейсов(Баннер, ДанныеБаннера);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСоСпискомБаннеров

Функция ОбщийСписокБаннеров(Размещение)
	
	СписокБаннеров = НовыйТаблицаСписокБаннеров();
	
	Если Размещение = ИмяРазмещенияОбщее()
		Или Размещение = ИмяРазмещенияСписокЗадач() Тогда
		// Исключительные баннеры. Если выполняются условия показа для одного из них,
		// остальные баннеры не показываются.
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПоддержкаСервиса()); // должен быть первым
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАУСН()); // у НП на Автоматизированной УСН всегда должен быть первым
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЛКАУСН()); // у НП на Автоматизированной УСН всегда должен быть первым
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые()); // у самозанятых всегда должен быть первым
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерСервисСверкиРасчетов()); // В релизе 3.0.158 должен быть первым

		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерУведомленияЕНС());
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПереходНаЕНС()); // в 2023 у всех должен быть первым
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПереносСроковУплатыНалогов());
		
		Если Размещение = ИмяРазмещенияОбщее() Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАвтоматическаяЗагрузкаПатчей());
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерУстановитьДатуЗапрета());
		КонецЕсли;
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаФункциональности());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерТестовыйТариф());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетность());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПереходиНаНКО());
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНадежностьБанков());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерСтороннийСертификат());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЧисленностьСотрудников());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерОтчетнаяКампания());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНДСКВычетуКонтрагент());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНДСКВычетуСПАРК());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКрупнейшийДебитор());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКоличествоКонтрагентов());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКрупнейшийДебиторСПАРК());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерОбщееКоличествоДокументов());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		
		Если Размещение = ИмяРазмещенияОбщее() Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерМоиЗадачи());
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКабинетСотрудника(Размещение));
		КонецЕсли;
		
		Если Размещение = ИмяРазмещенияСписокЗадач() Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерУведомленияОЗадачахПоЭлектроннойПочте());
		КонецЕсли;
		
	ИначеЕсли Размещение = ИмяРазмещенияДокументы() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаФункциональности());
		
	ИначеЕсли Размещение = ИмяРазмещенияПоступление() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКоличествоПоступлений());
		
	ИначеЕсли Размещение = ИмяРазмещенияРеализация() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКоличествоРеализаций());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАвтоматическийВыпускПродукции());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЭТрН());
		
	ИначеЕсли Размещение = ИмяРазмещенияСчетПокупателю() Тогда
		
		Если ОбщегоНазначенияБП.ЭтоБизнесСтарт() Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерМобильныйКлиентБизнесСтарт());
		КонецЕсли;
	
	ИначеЕсли Размещение = ИмяРазмещенияАктСверкиРасчетов() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерСервисСверкиРасчетов());
		
	ИначеЕсли Размещение = ИмяРазмещенияАктСверкиРасчетовДокумент() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерСервисСверкиРасчетовКонтрагентПодключенКСервису());

	ИначеЕсли Размещение = ИмяРазмещенияКонтрагенты() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерСервисСверкиРасчетов());
		
	ИначеЕсли Размещение = ИмяРазмещенияНалогиИОтчеты() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерУведомленияЕНС());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАУСН());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЛКАУСН());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерБанкиСервисаАУСН());
		
	ИначеЕсли Размещение = ИмяРазмещенияБанковскиеВыписки() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАУСН());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЛКАУСН());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		Если ПолучитьФункциональнуюОпцию("ИнтерфейсТакси") Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		КонецЕсли;
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетность());
		
	ИначеЕсли Размещение = ИмяРазмещенияДеньги() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАУСН());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		Если ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой") Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаФункциональности());
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		КонецЕсли;
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетность());
		
	ИначеЕсли Размещение = ИмяРазмещенияКассовыеДокументы() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАУСН());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые());
		
	ИначеЕсли Размещение = ИмяРазмещенияТовары() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАвтоматическийВыпускПродукции());
		
	ИначеЕсли Размещение = ИмяРазмещенияМониторНалогов() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерУведомленияЕНС());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПереносСроковУплатыНалогов_МониторНалогов());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАУСН());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЛКАУСН());
		Если Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНовыйБаннерПереходНаЕНС_МониторНалогов());
		КонецЕсли;
		
	ИначеЕсли Размещение = ИмяРазмещенияТребованиеНакладная() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение));
		
	ИначеЕсли Размещение = ИмяРазмещенияМониторОсновныхПоказателей() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		
	ИначеЕсли Размещение = ИмяРазмещенияПродажиПоКонтрагентам() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		
	ИначеЕсли Размещение = ИмяРазмещенияАвансовыеОтчеты() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерИнтеграцияСоSmartway());
		
	ИначеЕсли Размещение = ИмяРазмещенияВыпускПродукции() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАвтоматическийВыпускПродукции());
		
	ИначеЕсли Размещение = ИмяРазмещенияОсновныеСредства() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияАнализДвиженийДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияОстаткиДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияПоступленияДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияРасходыДенежныхСредств() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияДоходыРасходы() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияОборотныеСредства() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияСравнениеПродаж() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияАнализНеоплаченныхСчетовПоставщиков() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияДинамикаЗадолженностиПоставщикам() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияЗадолженностьПоставщикам() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияЗадолженностьПоставщикамПоСрокамДолга() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПредодобренныйКредитВСбербанке());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаКредит());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаЛизинг());
		
	ИначеЕсли Размещение = ИмяРазмещенияОборотноСальдоваяВедомость() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетностьКраткий());
		
	ИначеЕсли Размещение = ИмяРазмещенияОборотноСальдоваяВедомостьПоСчету() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетностьКраткий());
		
	ИначеЕсли Размещение = ИмяРазмещенияАнализСчета() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетностьКраткий());
		
	ИначеЕсли Размещение = ИмяРазмещенияКарточкаСчета() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетностьКраткий());
		
	ИначеЕсли Размещение = ИмяРазмещенияОборотыСчета() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерФинОтчетностьКраткий());
		
	ИначеЕсли Размещение = ИмяРазмещенияПечатьДокументов() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПечатьАртикулов(Размещение));
		
	ИначеЕсли Размещение = ИмяРазмещенияНастройкиКадровыеДокументыЭТК() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКадровыеДокументыЭТК());
		
	ИначеЕсли Размещение = ИмяРазмещенияНачислениеЗарплаты() Тогда
		
		Если ПолучитьФункциональнуюОпцию("ИнтерфейсТакси") Тогда
			ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКабинетСотрудника(Размещение));
		КонецЕсли;
		
	ИначеЕсли Размещение = ИмяРазмещенияПростойИнтерфейсРазделСотрудники() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерКабинетСотрудника(Размещение));
		
	ИначеЕсли Размещение = ИмяРазмещенияРозничнаяПродажа() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНастройкаЭлектронныхЧеков());
		
	ИначеЕсли Размещение = ИмяРазмещенияОтчетОРозничныхПродажах() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗагрузкаИзОФД());
		
	ИначеЕсли Размещение = ИмяРазмещенияМаркетПлейсы() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерУведомлениеОВыкупеВайлдберриз());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерНоваяАвторизацияЯндекс());
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАвтозагрузкаОтчетовМаркетплейсов());
		
	ИначеЕсли Размещение = ИмяРазмещенияПередачаОС() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерАвтоматическийУчетУбытковПослеРеализацииОС());
		
	ИначеЕсли Размещение = ИмяРазмещенияБанковскиеСчетаОрганизаций() Тогда

		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		
	ИначеЕсли Размещение = ИмяРазмещенияПлатежныеПоручения() Тогда

		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
		
	ИначеЕсли Размещение = ИмяРазмещенияГосударственныеКонтракты() Тогда

		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерЗаявкаНаОткрытиеСчета());
	
	ИначеЕсли Размещение = ИмяРазмещенияСравненияСистемНалогообложения() Тогда
		
		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерБанкиСервисаАУСН());
		
	ИначеЕсли Размещение = ИмяРазмещенияПомощникИнтеграцииСПлатформойСамозанятые() Тогда

		ДобавитьБаннерВТаблицу(СписокБаннеров, НовыйБаннерПлатформаСамозанятые(Размещение));
	
	КонецЕсли;
	
	Возврат СписокБаннеров;
	
КонецФункции

Функция БаннерыПользователя(Организация, Размещение, ТекущаяДата)
	
	СписокБаннеров             = ОбщийСписокБаннеров(Размещение);
	СписокБаннеровПользователя = НовыйТаблицаСписокБаннеров();
	
	ИсключаемыеБаннеры = Новый Массив;
	
	ПерсонализированныеПредложенияСервисовПереопределяемый.ЗаполнитьМассивИсключаемыхБаннеров(ИсключаемыеБаннеры, Организация, ТекущаяДата);
	
	ПерсонализированныеПредложенияСервисовПереопределяемый.ПодключитьБаннеры(СписокБаннеров, Размещение);
	
	ИсключитьБаннеры(СписокБаннеров, ИсключаемыеБаннеры);
	
	// Если организация не указана, исключим из списка баннеры, зависящие от организации.
	ИсключитьБаннерыПоОрганизации(СписокБаннеров, Организация);
	
	// Исключим из списка баннеры, по которым уже подключены сервисы.
	ИсключитьБаннерыПоПодключеннымСервисам(СписокБаннеров, Организация, ТекущаяДата);
	
	// Дополним список баннеров обстоятельствами закрытия баннера.
	ДополнитьБаннерыОбстоятельствамиЗакрытия(СписокБаннеров, Организация);
	
	Для Каждого СтрокаСписка Из СписокБаннеров Цикл
		
		ОбстоятельстваЗакрытия = СтрокаСписка.Баннер.ОбстоятельстваЗакрытия;
		
		// Пропускаем баннеры, которые уже показывали в этом периоде.
		Если ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.День Тогда
			Если НачалоДня(ОбстоятельстваЗакрытия.ДатаЗакрытия) = НачалоДня(ТекущаяДата) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц Тогда
			Если НачалоМесяца(ОбстоятельстваЗакрытия.ДатаЗакрытия) = НачалоМесяца(ТекущаяДата) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал Тогда
			Если НачалоКвартала(ОбстоятельстваЗакрытия.ДатаЗакрытия) = НачалоКвартала(ТекущаяДата) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ОбстоятельстваЗакрытия.Свойство("ИсключитьБаннер") И ОбстоятельстваЗакрытия.ИсключитьБаннер Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаСпискаПользователя = СписокБаннеровПользователя.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСпискаПользователя, СтрокаСписка);
		
	КонецЦикла;
	
	// Отсортируем баннеры по приоритету.
	СписокБаннеровПользователя.Сортировать("Порядок", Новый СравнениеЗначений);
	
	Возврат СписокБаннеровПользователя;
	
КонецФункции

Процедура ИсключитьБаннеры(СписокБаннеров, ИсключаемыеБаннеры)
	
	МассивУдаляемыхСтрокБаннеров = Новый Массив;
	
	Для Каждого Баннер Из СписокБаннеров Цикл
		Если ИсключаемыеБаннеры.Найти(Баннер.ИдентификаторБаннера) <> Неопределено Тогда
			МассивУдаляемыхСтрокБаннеров.Добавить(Баннер);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Баннер Из МассивУдаляемыхСтрокБаннеров Цикл
		СписокБаннеров.Удалить(Баннер);
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсключитьБаннерыПоПодключеннымСервисам(СписокБаннеров, Организация, ТекущаяДата)
	
	// Обновим данные по которым будем получать информацию.
	Разделы = РазделыПерсонализированныхДанныхПодключенныеСервисы(СписокБаннеров);
	ИнтервалОбновления = 60 * 60 * 24 * 7; // обновляем данные с интервалом 1 неделя.
	РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(
		Организация,
		ТекущаяДата,
		ИнтервалОбновления,
		Разделы);
	
	УстановитьПривилегированныйРежим(Истина);
	ПодключенныеСервисы = ПодключенныеСервисы(Организация, Разделы);
	УстановитьПривилегированныйРежим(Ложь);
	
	// Если подключенных сервисов нет, то дальше ничего не делаем.
	Если ПодключенныеСервисы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИсключенныеСтроки = Новый Массив;
	Для Каждого ИмяСервиса Из ПодключенныеСервисы Цикл
		Отбор = Новый Структура("ИмяСервиса", ИмяСервиса);
		СтрокиБаннеров = СписокБаннеров.НайтиСтроки(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключенныеСтроки, СтрокиБаннеров, Истина);
	КонецЦикла;
	
	УдалитьСтрокиИзТаблицы(СписокБаннеров, ИсключенныеСтроки);
	
КонецПроцедуры

Процедура ДополнитьБаннерыОбстоятельствамиЗакрытия(СписокБаннеров, Организация)
	
	ЗакрытыеПользователемБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
	ИмяНастройкиПерсонализированныеПредложенияСервисов(),
	ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
	НовыйЗакрытыеПользователемБаннеры());
	
	Для Каждого КлючИЗначение Из ЗакрытыеПользователемБаннеры.БаннерыБезОрганизации Цикл
		Для Каждого СтрокаСписка Из СписокБаннеров.НайтиСтроки(Новый Структура("ИдентификаторБаннера", КлючИЗначение.Ключ)) Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
			СтрокаСписка.Баннер.ОбстоятельстваЗакрытия,
			КлючИЗначение.Значение,
			Истина);
		КонецЦикла;
	КонецЦикла;
	
	БаннерыПоОрганизации = ЗакрытыеПользователемБаннеры.БаннерыПоОрганизации[Организация];
	Если БаннерыПоОрганизации <> Неопределено Тогда
		Для Каждого КлючИЗначение Из БаннерыПоОрганизации Цикл
			Для Каждого СтрокаСписка Из СписокБаннеров.НайтиСтроки(Новый Структура("ИдентификаторБаннера", КлючИЗначение.Ключ)) Цикл
				ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
				СтрокаСписка.Баннер.ОбстоятельстваЗакрытия,
				КлючИЗначение.Значение,
				Истина);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИсключитьБаннерыПоОрганизации(СписокБаннеров, Организация)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ИсключенныеСтроки = Новый Массив;
	Для каждого СтрокаБаннера Из СписокБаннеров Цикл
		Если СтрокаБаннера.Баннер.ЗависитОтОрганизации Тогда
			ИсключенныеСтроки.Добавить(СтрокаБаннера);
		КонецЕсли;
	КонецЦикла;
	УдалитьСтрокиИзТаблицы(СписокБаннеров, ИсключенныеСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ПерсонализированныеДанные

// Вызывает обновление записей регистра сведений "ПерсонализированныеДанные" фоновым заданием.
// При обновлении учитывается актуальность данных регистра сведений
// Параметры:
//   Параметры - Структура - Структура с параметрами переданная в фоновое задание
//		*Организация - СправочникСсылка.Организации - организация по которой нужно обновить персонализированные данные.
//									Если не заполнено данные будут обновлены по всем доступным организациям
//	ВременноеХранилищеРезультата - Строка - путь к временному хранилищу (не используется в рамках данной процедуры)	
Процедура ОбновитьПерсонализированныеДанныеВФоне(Параметры, ВременноеХранилищеРезультата) Экспорт
	
	ОбновитьПерсонализированныеДанные(Параметры.Организация, Истина, Параметры.РазделыПерсонализированныхДанных);
	
КонецПроцедуры

// Вызывает перезапись данных регистра сведений "ПерсонализированныеДанные" фоновым заданием
// Данные регистра сведений перезаписываются без учета актуальности.
// Параметры:
//   Параметры - Структура - Структура с параметрами переданная в фоновое задание
//		*Организация - СправочникСсылка.Организации - организация по которой нужно обновить персонализированные данные.
//									Если не заполнено данные будут обновлены по всем доступным организациям
//	ВременноеХранилищеРезультата - Строка - путь к временному хранилищу (не используется в рамках данной процедуры).
//
Процедура ПерезаписатьПерсонализированныеДанныеВФоне(Параметры, ВременноеХранилищеРезультата) Экспорт
	
	ОбновитьПерсонализированныеДанные(Параметры.Организация, Ложь, Параметры.РазделыПерсонализированныхДанных);
	
КонецПроцедуры	

// Вызывает первоначальное заполнение регистра сведений "ПерсонализированныеДанные" по всем организациям
// Запускается при обновлении версии конфиурации и при переходе с предыдущей версии.
Процедура ЗаполнитьПерсонализированныеДанныеРуководителя() Экспорт
	
	ОбновитьПерсонализированныеДанныеПоВсемОрганизациям(ТекущаяДатаСеанса());
	
КонецПроцедуры

// Создает пустую таблицу контейнер для персонализированных данных
// Возвращаемое значение:
//	ТаблицаЗначений - Пустая таблица персонализированных данных.
//
Функция ТаблицаДанных() Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ПерсонализированныеДанные;
	
	НоваяТаблицаДанных = Новый ТаблицаЗначений;
	
	НоваяТаблицаДанных.Колонки.Добавить("ЗначениеПоказателя", МетаданныеРегистра.Ресурсы.ЗначениеПоказателя.Тип);
	НоваяТаблицаДанных.Колонки.Добавить("Порядок",            Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(1)));
	НоваяТаблицаДанных.Колонки.Добавить("ДанныеРасшифровки",  МетаданныеРегистра.Ресурсы.ДанныеРасшифровки.Тип);
	
	Возврат НоваяТаблицаДанных;
	
КонецФункции

// Обновляет данные регистра сведений "ПерсонализированныеДанные".
// Параметры:
//   Параметры - Структура - Структура с параметрами
//		*Организация - СправочникСсылка.Организации - организация по которой нужно обновить персонализированные данные.
//									Если не заполнено данные будут обновлены по всем доступным организациям
//	УчитыватьАктуальностьДанных - булево - Истина - перед обновлением проверить что данные неактуальны, Ложь - не проверять.
//
Процедура ОбновитьПерсонализированныеДанные(Организация, УчитыватьАктуальностьДанных, РазделыПерсонализированныхДанных)
	
	Дата = ТекущаяДатаСеанса();
	
	ИнтервалОбновления = 0;
	Если УчитыватьАктуальностьДанных Тогда
		ИнтервалОбновления = 600;
	КонецЕсли;
	
	// Если организация не заполнена то данные нужно обновить по всем
	Если ЗначениеЗаполнено(Организация) Тогда
		СписокДоступныхОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
		
		Если СписокДоступныхОрганизаций.Найти(Организация) <> Неопределено Тогда
			РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(Организация, Дата, ИнтервалОбновления, РазделыПерсонализированныхДанных);
		КонецЕсли;
	Иначе
		ОбновитьПерсонализированныеДанныеПоВсемОрганизациям(Дата, ИнтервалОбновления, РазделыПерсонализированныхДанных);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет данные регистра сведений "ПерсонализированныеДанные" по всем организациям
// Параметры:
//  Дата - Дата - Дата на которую нужно получить данные для записи в регистр
//	ИнтервалОбновления - Число - Период в секундах в течении которого данные считаются актуальными
//
Процедура ОбновитьПерсонализированныеДанныеПоВсемОрганизациям(Дата, ИнтервалОбновления = 0, РазделыПерсонализированныхДанных = Неопределено)
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	
	Для Каждого Организация Из ДоступныеОрганизации Цикл
		
		РегистрыСведений.ПерсонализированныеДанные.ОбновитьДанные(Организация, Дата, ИнтервалОбновления, РазделыПерсонализированныхДанных);
		
	КонецЦикла;
	
КонецПроцедуры

Функция РазделыПерсонализированныхДанных(СписокБаннеров)
	
	МассивРазделов = Новый Массив; 
	
	МассивИдентификаторовБаннеров = СписокБаннеров.ВыгрузитьКолонку("ИдентификаторБаннера");
	
	Для Каждого ИдентификаторБаннера Из МассивИдентификаторовБаннеров Цикл
		
		Если ИдентификаторБаннера = ИдентификаторБаннераНДСКВычетуКонтрагент()
			ИЛИ ИдентификаторБаннера = ИдентификаторБаннераНДСКВычетуСПАРК() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.НДСКВычету);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераОбщееКоличествоДокументов() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.КоличествоПоступлений);
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.КоличествоРеализаций);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераЧисленностьСотрудников() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленность);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераКоличествоКонтрагентов() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.КоличествоКонтрагентов);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераИнтеграцияСоSmartway() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.ИнтеграцияСоSmartway);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераЗаявкаНаКредит()
			ИЛИ ИдентификаторБаннера = ИдентификаторБаннераЗаявкаНаЛизинг() Тогда
			
			// Для баннеров 1С:Кредит и 1С:Лизинг используются общие данные, поэтому получаем их вместе.
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.ЗаявкаНаЛизинг);
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.ЗаявкаНаКредит);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераФинОтчетность()
			ИЛИ ИдентификаторБаннера = ИдентификаторБаннераФинОтчетностьКраткий() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.ФинОтчетность);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераПечатьАртикулов() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.НастройкаПечатиАртикулов);
			
		ИначеЕсли ИдентификаторБаннера = ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз() Тогда
			
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Маркетплейс);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПерсонализированныеПредложенияСервисовПереопределяемый.РазделыПерсонализированныхДанных(МассивРазделов, МассивИдентификаторовБаннеров);
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивРазделов);
	
КонецФункции

Функция РазделыПерсонализированныхДанныхПодключенныеСервисы(СписокБаннеров)
	
	МассивРазделов = Новый Массив;
	
	МассивСервисов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокБаннеров.ВыгрузитьКолонку("ИмяСервиса"));
	Для Каждого ИмяСервиса Из МассивСервисов Цикл
		Если ИмяСервиса = ИмяСервис1СКонтрагент() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СКонтрагентПодключен);
			
		ИначеЕсли ИмяСервиса = ИмяСервис1СОтчетность() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СОтчетностьПодключен);
			
		ИначеЕсли ИмяСервиса = ИмяСервис1СПАРК() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СПАРКПодключен);
			
		ИначеЕсли ИмяСервиса = ИмяСервис1СЭДО() Тогда
			МассивРазделов.Добавить(Перечисления.РазделыПерсонализированныхДанных.Сервис1СЭДОПодключен);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивРазделов;
	
КонецФункции

// Формирует таблицу данных для персонализированных данных по организации на дату
// Параметры
// 	Организация - СправочникСсылка.Организации - Организация по которой нужны данные
// 	ДатаПолученияДанных - Дата - дата на которую нужны остатки
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с данными для Прсонализированных данных.
//
Функция ПолучитьНДСКВычетуДляПерсонализированныхДанных(Организация, ДатаПолученияДанных) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	
	Если НЕ УчетнаяПолитика.ПлательщикНДС(Организация, ДатаПолученияДанных) Тогда
		Возврат ТаблицаДанных;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("НачалоКвартала", НачалоКвартала(ДатаПолученияДанных));
	Запрос.УстановитьПараметр("КонецКвартала",  Новый Граница(КонецДня(ДатаПолученияДанных), ВидГраницы.Включая));
	ВидыЦенностейАвансы = Новый Массив;
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	ВидыЦенностейАвансы.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	Запрос.УстановитьПараметр("ВидыЦенностейАвансы", ВидыЦенностейАвансы);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДС.Контрагент КАК Контрагент,
	|	СУММА(НДС.ЗначениеПоказателя) КАК ЗначениеПоказателя
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДСПокупки.Поставщик КАК Контрагент,
	|		НДСПокупки.НДСОборот КАК ЗначениеПоказателя
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|				&НачалоКвартала,
	|				&КонецКвартала,
	|				,
	|				Организация = &Организация
	|					И НЕ ВидЦенности В (&ВидыЦенностейАвансы)) КАК НДСПокупки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДСПредъявленный.Поставщик,
	|		НДСПредъявленный.НДСОборот
	|	ИЗ
	|		РегистрНакопления.НДСПредъявленный.Обороты(
	|				&НачалоКвартала,
	|				&КонецКвартала,
	|				,
	|				Организация = &Организация
	|					И НЕ ВидЦенности В (&ВидыЦенностейАвансы)) КАК НДСПредъявленный) КАК НДС
	|
	|СГРУППИРОВАТЬ ПО
	|	НДС.Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеПоказателя УБЫВ";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	УстановитьПривилегированныйРежим(Ложь);
	
	Для ИндексСтроки = 0 По Мин(1, Результат.Количество() - 1) Цикл
		
		СтрокаРезультата = Результат[ИндексСтроки];
		Контрагент = СтрокаРезультата.Контрагент;
		
		СтрокаДанных = ТаблицаДанных.Добавить();
		СтрокаДанных.ДанныеРасшифровки  = Контрагент;
		СтрокаДанных.Порядок            = 1;
		СтрокаДанных.ЗначениеПоказателя = СтрокаРезультата.ЗначениеПоказателя;
		
	КонецЦикла;
	
	// Добавляем итог по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = Результат.Итог("ЗначениеПоказателя");
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Формирует таблицу данных для персонализированных данных по организации на дату
// Параметры:
//		ДатаПолученияДанных - Дата - дата на которую нужны остатки
// Возвращаемое значение:
//		ТаблицаЗначений - Таблица с данными для Персонализированных данных
//
Функция ПолучитьКоличествоКонтрагентовДляПерсонализированныхДанных(ДатаПолученияДанных) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоКвартала", НачалоКвартала(ДатаПолученияДанных));
	Запрос.УстановитьПараметр("КонецКвартала",  КонецДня(ДатаПолученияДанных));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Контрагенты.Ссылка) КАК ЗначениеПоказателя
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ДатаСоздания >= &НачалоКвартала
	|	И Контрагенты.ДатаСоздания <= &КонецКвартала
	|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	УстановитьПривилегированныйРежим(Ложь);
	
	// Добавляем информацию по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = Результат.Итог("ЗначениеПоказателя");
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С-Отчетность подключен.
// Параметры:
//		Организация - Справочник.Организация - Организация, для которой проверяется сервис.
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СОтчетностьПодключен(Организация) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СервисПодключен = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(Организация,,Истина);
	СтрокаДанных.ЗначениеПоказателя = ?(СервисПодключен, 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С:ЭДО подключен.
// Параметры:
//		Организация - Справочник.Организация - Организация, для которой проверяется сервис.
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СЭДОПодключен(Организация) Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СервисПодключен = ОбменСКонтрагентами.ОрганизацияПодключена(Организация);
	СтрокаДанных.ЗначениеПоказателя = ?(СервисПодключен, 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С:Контрагент подключен.
//
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СКонтрагентПодключен() Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СтрокаДанных.ЗначениеПоказателя = ?(РаботаСКонтрагентамиБП.Сервис1СКонтрагентПодключен(), 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Возвращает Истина, если сервис 1С:Контрагент подключен.
//
// Возвращаемое значение:
//		Булево
//
Функция Сервис1СПАРКПодключен() Экспорт
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	
	Если РаботаВМоделиСервиса.РазделениеВключено() Тогда
		СервисПодключен = ИнтернетПоддержкаПользователей.УслугаПодключена(
		СПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска());
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		ВидОшибки = СПАРКРискиМониторингСобытий.ОбновитьСобытияМониторинга();
		СервисПодключен = НЕ ЗначениеЗаполнено(ВидОшибки);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	СтрокаДанных.ЗначениеПоказателя = ?(СервисПодключен, 1, 0);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Формирует таблицу данных для персонализированных данных по организации на дату
// 	Организация - СправочникСсылка.Организации - Организация по которой нужны данные
// 	ДатаПолученияДанных - Дата - дата на которую нужны остатки
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с данными для персонализированных данных
//
Функция СреднесписочнаяЧисленностьОрганизацииДляПерсонализированныеДанные(Организация, ДатаПолученияДанных) Экспорт
	
	ТаблицаДанных = ПерсонализированныеПредложенияСервисов.ТаблицаДанных();
	
	СведенияОЧисленностиРаботников = УчетЗарплаты.СреднесписочнаяЧисленность(
		Организация,
		НачалоГода(ДатаПолученияДанных),
		КонецДня(ДатаПолученияДанных));
	ТекущаяЧисленностьРаботников = СведенияОЧисленностиРаботников.ЧисленностьРаботников;
	
	// Добавляем итог по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = ТекущаяЧисленностьРаботников;
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Устанавливает значение параметра сеанса РазрешенныеПользователюРазделыПерсонализированныхДанных,
// который используется в тексте ограничения доступа к персонализированных данным.
//
// Подробнее см. СтандартныеПодсистемыСервер.УстановкаПараметровСеанса()
//
Процедура УстановитьРазрешенныеПользователюРазделыПерсонализированныхДанных(ИмяПараметра, УстановленныеПараметры) Экспорт
	
	Если ИмяПараметра = "РазрешенныеПользователюРазделыПерсонализированныхДанных" Тогда
		
		ПараметрыСеанса.РазрешенныеПользователюРазделыПерсонализированныхДанных = 
		Перечисления.РазделыПерсонализированныхДанных.РазрешенныеПользователюРазделыПерсонализированныхДанных();
		УстановленныеПараметры.Добавить(ИмяПараметра);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПодключенныеСервисы(Организация, Разделы)
	
	ПодключенныеСервисы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Разделы", Разделы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.Раздел,
	|	ПерсонализированныеДанные.ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел В(&Разделы)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЗначениеПоказателя = 1 Тогда
			Если Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СКонтрагентПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СКонтрагент());
			ИначеЕсли Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СОтчетностьПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СОтчетность());
			ИначеЕсли Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СПАРКПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СПАРК());
			ИначеЕсли Выборка.Раздел = Перечисления.РазделыПерсонализированныхДанных.Сервис1СЭДОПодключен Тогда
				ПодключенныеСервисы.Добавить(ИмяСервис1СЭДО());
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключенныеСервисы;
	
КонецФункции

Процедура ЗаполнитьСреднесписочнаяЧисленностьПредыдущая() Экспорт
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	Раздел = Перечисления.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленностьПредыдущая;
	ДатаПолученияДанных = ТекущаяДатаСеанса();
	
	Для Каждого Организация Из ДоступныеОрганизации Цикл
		
		СведенияОЧисленностиРаботников = УчетЗарплаты.СреднесписочнаяЧисленность(
		Организация,
		НачалоГода(ДатаПолученияДанных),
		КонецДня(ДатаПолученияДанных));
		ТекущаяЧисленностьРаботников = СведенияОЧисленностиРаботников.ЧисленностьРаботников;
		
		НаборЗаписей = РегистрыСведений.ПерсонализированныеДанные.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Отбор.Раздел.Установить(Раздел);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация        = Организация;
		Запись.Раздел             = Раздел;
		Запись.ЗначениеПоказателя = ТекущаяЧисленностьРаботников;
		Запись.НомерСтрокиРаздела = 1;
		Запись.ДатаОбновления     = ДатаПолученияДанных;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПолучениеСтороннегоСертификата

// Возвращает признак нужно ли искать сторонний сертификат отчетности.
// Параметры:
//		Организация - СправочникСсылка.Организации - Организация, по которой выполняется поиск.
//
Функция НужноИскатьСертификат(Организация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(Организация,,Истина) Тогда
		// Сервис подключен, ничего искать не нужно
		Возврат Ложь;
	КонецЕсли;
	
	ИнформацияОСтороннихСертификатах = ИнформацияОСтороннихСертификатах(Организация);
	Если ИнформацияОСтороннихСертификатах = Неопределено Тогда
		// Никакой информации нет - нужно искать.
		Возврат Истина;
	КонецЕсли;
	
	Если НачалоМесяца(ТекущаяДатаСеанса()) <= НачалоМесяца(ИнформацияОСтороннихСертификатах.ДатаПоиска) Тогда
		// В этом месяце уже искали сертификаты.
		Возврат Ложь;
	КонецЕсли;
	
	Если ИнформацияОСтороннихСертификатах.СертификатНайден 
		И ИнформацияОСтороннихСертификатах.СрокДействияСертификата <= ТекущаяДатаСеанса() Тогда
		// Сертификат найден, снова искать не нужно.
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Записывает информацию о сторонних сертификах в хранилище общих настроек.
// Параметры:
//		Организация - СправочникСсылка.Организации - Организация, по которой записывается информация о сертификатах.
//		СторонниеСертификаты - Структура - Ключ - имя удостоверяющего центра, Значение - срок действия сертификата.
//
Процедура ЗаписатьИнформациюОСтороннихСертификатах(Организация, СторонниеСертификаты) Экспорт
	
	Информация = НовыйИнформацияОСтороннихСертификатах();
	Информация.ДатаПоиска           = ТекущаяДатаСеанса();
	Информация.СертификатНайден     = (СторонниеСертификаты.Количество() > 0);
	Информация.СторонниеСертификаты = СторонниеСертификаты;
	СрокДействияСертификата         = '00010101';
	Для Каждого СтороннийСертификат Из СторонниеСертификаты Цикл
		СрокДействия = СтороннийСертификат.Значение;
		Если СрокДействияСертификата = '00010101' Тогда
			СрокДействияСертификата = СрокДействия;
		Иначе
			СрокДействияСертификата = Мин(СрокДействия, СрокДействияСертификата);
		КонецЕсли;
	КонецЦикла;
	Информация.СрокДействияСертификата = СрокДействияСертификата;
	
	// Получим ранее сохраненные данные.
	ИнформацияОСертификатахПоОрганизациям = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастроекСторонниеСертификаты(),
		Новый Соответствие);
	
	// Добавим информация по организации.
	ИнформацияОСертификатахПоОрганизациям.Вставить(Организация, Информация);
	
	// Сохраним всю информацию.
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастроекСторонниеСертификаты(),
		ИнформацияОСертификатахПоОрганизациям);
	
КонецПроцедуры

Функция ИнформацияОСтороннихСертификатах(Организация)
	
	ИнформацияОСертификатахПоОрганизациям = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастроекСторонниеСертификаты(),
		Новый Соответствие);
	
	Возврат ИнформацияОСертификатахПоОрганизациям[Организация]
	
КонецФункции

Функция КомандировкиЗаПрошлыйМесяц(Организация, ДатаПолученияДанных) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвансовыйОтчет.Ссылка КАК Ссылка,
	|	АвансовыйОтчет.ВидОперации КАК ВидОперации,
	|	АвансовыйОтчет.НазначениеАванса КАК НазначениеАванса,
	|	АвансовыйОтчет.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ВТАвансовыеОтчеты
	|ИЗ
	|	Документ.АвансовыйОтчет КАК АвансовыйОтчет
	|ГДЕ
	|	АвансовыйОтчет.Проведен
	|	И АвансовыйОтчет.Организация = &Организация
	|	И АвансовыйОтчет.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Счет,
	|	ХозрасчетныйВидыСубконто.НомерСтроки КАК НомерСубконто
	|ПОМЕСТИТЬ ВТСубконтоСтатьиЗатрат
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыйОтчетПрочее.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ВТСубконтоСтатьиЗатрат.НомерСубконто = 1
	|			ТОГДА ВЫРАЗИТЬ(АвансовыйОтчетПрочее.Субконто1 КАК Справочник.СтатьиЗатрат)
	|		КОГДА ВТСубконтоСтатьиЗатрат.НомерСубконто = 2
	|			ТОГДА ВЫРАЗИТЬ(АвансовыйОтчетПрочее.Субконто2 КАК Справочник.СтатьиЗатрат)
	|		КОГДА ВТСубконтоСтатьиЗатрат.НомерСубконто = 3
	|			ТОГДА ВЫРАЗИТЬ(АвансовыйОтчетПрочее.Субконто3 КАК Справочник.СтатьиЗатрат)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяЗатрат,
	|	АвансовыйОтчетПрочее.Содержание КАК Содержание
	|ПОМЕСТИТЬ ВТТаблицаПрочее
	|ИЗ
	|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСубконтоСтатьиЗатрат КАК ВТСубконтоСтатьиЗатрат
	|		ПО АвансовыйОтчетПрочее.СчетЗатрат = ВТСубконтоСтатьиЗатрат.Счет
	|ГДЕ
	|	АвансовыйОтчетПрочее.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТАвансовыеОтчеты.Ссылка КАК Ссылка
	|			ИЗ
	|				ВТАвансовыеОтчеты КАК ВТАвансовыеОтчеты)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиЗатрат.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%суточные%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%суточных%""
	|			ТОГДА ""СУТОЧНЫЕ""
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%проживани%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%гостиниц%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%отель%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%отеле%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%отеля%""
	|			ТОГДА ""ПРОЖИВАНИЕ""
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%билет%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%посадочн%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%перелет%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%перелёт%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%рейс%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%самолет%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%самолёт%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%поезд%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%а/б%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%%маршут_квитанция%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%контрольный талон%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%ж/д%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%жд%""
	|			ТОГДА ""БИЛЕТ""
	|		КОГДА СтатьиЗатрат.Наименование ПОДОБНО ""%командировочн%""
	|				ИЛИ СтатьиЗатрат.Наименование ПОДОБНО ""%командиров%""
	|			ТОГДА ""КОМАНДИРОВКА""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидРасхода
	|ПОМЕСТИТЬ ВТСтатьиЗатрат
	|ИЗ
	|	Справочник.СтатьиЗатрат КАК СтатьиЗатрат
	|ГДЕ
	|	СтатьиЗатрат.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТТаблицаПрочее.СтатьяЗатрат КАК СтатьяЗатрат
	|			ИЗ
	|				ВТТаблицаПрочее КАК ВТТаблицаПрочее)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаПрочее.Ссылка КАК Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""СУТОЧНЫЕ""
	|				ТОГДА 1
	|			КОГДА ВТТаблицаПрочее.Содержание ПОДОБНО ""%суточные%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%суточных%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировочные%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировочных%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировк%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%командировок%""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееСуточные,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""ПРОЖИВАНИЕ""
	|				ТОГДА 1
	|			КОГДА ВТТаблицаПрочее.Содержание ПОДОБНО ""%проживани%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%гостиниц%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%отель%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%отеле%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%отеля%""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееПроживание,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""БИЛЕТ""
	|				ТОГДА 1
	|			КОГДА ВТТаблицаПрочее.Содержание ПОДОБНО ""%билет%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%посадочн%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%перелет%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%перелёт%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%рейс%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%самолет%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%самолёт%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%поезд%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%а/б%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%маршут_квитанция%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%контрольный талон%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%ж/д%""
	|					ИЛИ ВТТаблицаПрочее.Содержание ПОДОБНО ""%жд%""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееБилеты,
	|	СУММА(ВЫБОР
	|			КОГДА ВТСтатьиЗатрат.ВидРасхода = ""КОМАНДИРОВКА""
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрочееКомандировки
	|ПОМЕСТИТЬ ВТКомандировкиТаблицаПрочее
	|ИЗ
	|	ВТТаблицаПрочее КАК ВТТаблицаПрочее
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиЗатрат КАК ВТСтатьиЗатрат
	|		ПО ВТТаблицаПрочее.СтатьяЗатрат = ВТСтатьиЗатрат.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаПрочее.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТАвансовыеОтчеты.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	ВТАвансовыеОтчеты КАК ВТАвансовыеОтчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКомандировкиТаблицаПрочее КАК ВТКомандировкиТаблицаПрочее
	|		ПО ВТАвансовыеОтчеты.Ссылка = ВТКомандировкиТаблицаПрочее.Ссылка
	|ГДЕ
	|	(ВТАвансовыеОтчеты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАвансовыйОтчет.Командировка)
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командировочные%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командировочных%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командировк%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%командирвок%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%суточные%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%суточных%""
	|			ИЛИ ВТАвансовыеОтчеты.НазначениеАванса ПОДОБНО ""%проживани%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%суточные%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%суточных%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировочные%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировочных%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировк%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%командировок%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%проживани%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%гостиниц%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%отель%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%отеле%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%отеля%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%билет%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%а/б%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%маршрут_квитанция%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%контрольный талон%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%жд%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%посадочн%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%перелет%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%перелёт%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%рейс%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%самолет%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%самолёт%""
	|			ИЛИ ВТАвансовыеОтчеты.Комментарий ПОДОБНО ""%поезд%""
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееСуточные > 0
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееПроживание > 0
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееБилеты > 0
	|			ИЛИ ВТКомандировкиТаблицаПрочее.ПрочееКомандировки > 0)";
	Период = НачалоМесяца(НачалоМесяца(ДатаПолученияДанных) - 1); // проверяем авансовые отчеты за прошлый месяц.
	Запрос.УстановитьПараметр("НачалоПериода", Период);
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(Период));
	Запрос.УстановитьПараметр("Организация",   Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок = 0;
	СтрокаДанных.ЗначениеПоказателя = Выборка.КоличествоДокументов;
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#Область ПредодобренныйКредитВСбербанке

Функция ПредодобренныйКредитВСбербанкеПолучитьПредодобренныеИНН() Экспорт
	
	ПредодобренныйКредитВСбербанкеПолучитьДанныеИзСервиса(Ложь);
	
	ПредодобренныеИНН = ПредодобренныйКредитВСбербанкеПолучитьЗначениеИзХранилища(
	"ПредодобренныеИНН",
	Новый Массив
	);
	
	Результат = Новый Массив;
	
	Для каждого ПредодобренныйИНН Из ПредодобренныеИНН Цикл
		Если КонецДня(ПредодобренныйИНН.СрокДействия) < ТекущаяДатаСеанса() Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(ПредодобренныйИНН.ИНН);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПредодобренныйКредитВСбербанкеПолучитьДанныеИзСервиса(ПолучатьВФоне, ДополнительныеИНН = Неопределено) Экспорт
	
	ДатаОбновления = ПредодобренныйКредитВСбербанкеПолучитьЗначениеИзХранилища(
	"ДатаОбновления",
	Дата(1, 1, 1)
	);
	
	ИнтервалОбновления = 86400 * 14; // Раз в две недели.
	
	Если ТекущаяДатаСеанса() < ДатаОбновления + ИнтервалОбновления Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ДополнительныеИНН", ДополнительныеИНН);
	
	Если ПолучатьВФоне Тогда
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Неопределено);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru='Получение данных по предодобренным кредитам Сбербанка'");
		ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		ПараметрыВыполнения.ОжидатьЗавершение = 0;
		Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПредодобренныйКредитВСбербанкеЗагрузитьДанныеИзСервиса",
		ПараметрыПроцедуры,
		ПараметрыВыполнения
		);
	Иначе
		ПредодобренныйКредитВСбербанкеЗагрузитьДанныеИзСервиса(ПараметрыПроцедуры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПредодобренныйКредитВСбербанкеЗагрузитьДанныеИзСервиса(Параметры = Неопределено, АдресРезультата = Неопределено) Экспорт
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.ИНН КАК ИНН,
		|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо) КАК ЭтоФизЛицо
		|ИЗ
		|	Справочник.Организации КАК Организации";
		Выборка = Запрос.Выполнить().Выбрать();
		
		СписокИНН = Новый Массив;
		Пока Выборка.Следующий() Цикл
			Если ПустаяСтрока(Выборка.ИНН) Тогда
				Продолжить;
			КонецЕсли;
			
			РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(
				Выборка.ИНН,
				Выборка.ЭтоФизЛицо);
				
			Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
				Продолжить;
			КонецЕсли;
			
			СписокИНН.Добавить(Выборка.ИНН);
		КонецЦикла;
		
		// В параметрах может быть передан дополнительный список ИНН.
		// Его добавляем без дополнительных проверок: считаем, что они были выполнены заранее.
		Если ТипЗнч(Параметры) = Тип("Структура") И Параметры.Свойство("ДополнительныеИНН")
			И ТипЗнч(Параметры.ДополнительныеИНН) = Тип("Массив")
			И Параметры.ДополнительныеИНН.Количество() > 0 Тогда 
			
			Для каждого ИНН Из Параметры.ДополнительныеИНН Цикл
				СписокИНН.Добавить(ИНН);
			КонецЦикла;
		КонецЕсли;
		
		Если СписокИНН.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("innList", СписокИНН);
		СтруктураДанных.Вставить("programName", Метаданные.Имя);
		СтруктураДанных.Вставить("programVersion", Метаданные.Версия);
		
		JSON = Новый ЗаписьJSON();
		JSON.УстановитьСтроку();
		ЗаписатьJSON(JSON, СтруктураДанных);
		ТелоЗапроса = JSON.Закрыть();
		
		АдресСервиса = ПредодобренныйКредитВСбербанкеИмяСервера() + "/api/advertisement/rest/check";
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресСервиса);
		ЗапросHTTP = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере);
		ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
		ЗапросHTTP.Заголовки.Вставить("Content-Type", "application/json");
		
		ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураURI.Схема);
		ЗащищенноеСоединение = ?(СтруктураURI.Схема = "https", Новый ЗащищенноеСоединениеOpenSSL, Неопределено);
		Соединение = Новый HTTPСоединение(
			СтруктураURI.Хост,
			СтруктураURI.Порт,,,
			ИнтернетПрокси,
			20,
			ЗащищенноеСоединение);
		
		ОтветСервиса = Соединение.ВызватьHTTPМетод("POST", ЗапросHTTP);
		
		Если ОтветСервиса.КодСостояния <> 200 Тогда
			ВызватьИсключение СтрШаблон(
			"%1: %2 %3",
			НСтр("ru='Сервис получения данных по предодобренным кредитам Сбербанка'"),
				ОтветСервиса.КодСостояния,
				ОтветСервиса.ПолучитьТелоКакСтроку());
		КонецЕсли;
		
		JSON = Новый ЧтениеJSON();
		JSON.УстановитьСтроку(ОтветСервиса.ПолучитьТелоКакСтроку());
		ДанныеОтвета = ПрочитатьJSON(JSON, Ложь, "date");
		
		ПредодобренныеИНН = Новый Массив;
		Для каждого СтрокаОтвета Из ДанныеОтвета.innInfoList Цикл
			ПредодобренныйИНН = Новый Структура;
			ПредодобренныйИНН.Вставить("ИНН", СтрокаОтвета.inn);
			ПредодобренныйИНН.Вставить("СрокДействия", СтрокаОтвета.date);
			ПредодобренныеИНН.Добавить(ПредодобренныйИНН);
		КонецЦикла;
		
		ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище("ПредодобренныеИНН", ПредодобренныеИНН);
		ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище("ДатаОбновления", ТекущаяДатаСеанса());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru='Обновление данных по предодобренным кредитам Сбербанка'"),
		УровеньЖурналаРегистрации.Предупреждение,,,
		ОписаниеОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПредодобренныйКредитВСбербанкеИмяСервера() Экспорт
	
	Возврат "https://reportbank.1c.ru";
	
КонецФункции

Функция ПредодобренныйКредитВСбербанкеПолучитьЗначениеИзХранилища(Ключ, ЗначениеПоУмолчанию)
	
	УстановитьПривилегированныйРежим(Истина);
	Значение = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(
		ИмяСервисПредодобренныйКредитВСбербанке(),
		Ключ); 
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Процедура ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище(Ключ, Значение)
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(
		ИмяСервисПредодобренныйКредитВСбербанке(),
		Значение,
		Ключ);
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриЗаписиОрганизации(ОбъектЗаписи, Отказ) Экспорт
	
	Перем ЭтоНовый;
	
	Если ОбъектЗаписи.ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовый) Тогда
		
		// При записи новой организации необходимо досрочно
		// обновить список предодобренных Сбербанком ИНН.
		Если ЭтоНовый И НЕ Отказ Тогда
			
			// Если ИНН организации валидный, передаем его в фоновое задание
			// для обработки на сервере, поскольку в результаты запроса он еще не попадет.
			ДополнительныеИНН = Новый Массив;
			Если НЕ ПустаяСтрока(ОбъектЗаписи.ИНН) Тогда
				ЭтоФизЛицо = ОбъектЗаписи.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
				РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(
					ОбъектЗаписи.ИНН,
					ЭтоФизЛицо);
					
				Если РезультатПроверки.СоответствуетТребованиям Тогда
					ДополнительныеИНН.Добавить(ОбъектЗаписи.ИНН);
				КонецЕсли;
			КонецЕсли;
			
			ПредодобренныйКредитВСбербанкеЗаписатьЗначениеВХранилище("ДатаОбновления", Дата(1, 1, 1));
			ПредодобренныйКредитВСбербанкеПолучитьДанныеИзСервиса(Истина, ДополнительныеИНН);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СохранитьОбстоятельстваЗакрытияБаннера(Баннер, Организация)
	
	ЗакрытыеБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
	ИмяНастройкиПерсонализированныеПредложенияСервисов(),
	ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
	НовыйЗакрытыеПользователемБаннеры());
	
	Если НЕ Баннер.ЗависитОтОрганизации Тогда
		ЗакрытыеБаннеры.БаннерыБезОрганизации.Вставить(Баннер.Идентификатор, Баннер.ОбстоятельстваЗакрытия);
	Иначе
		ЗакрытыеБаннерыПоОрганизации = ЗакрытыеБаннеры.БаннерыПоОрганизации[Организация];
		Если ЗакрытыеБаннерыПоОрганизации = Неопределено Тогда
			ЗакрытыеБаннерыПоОрганизации = Новый Структура;
		КонецЕсли;
		ЗакрытыеБаннерыПоОрганизации.Вставить(Баннер.Идентификатор, Баннер.ОбстоятельстваЗакрытия);
		ЗакрытыеБаннеры.БаннерыПоОрганизации.Вставить(Организация, ЗакрытыеБаннерыПоОрганизации);
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		ЗакрытыеБаннеры);
	
КонецПроцедуры

Функция ЭтоПериодНДС(ТекущаяДата)
	
	ЭтотГод = Год(ТекущаяДата);
	Если ТекущаяДата > Дата(ЭтотГод - 1, 12, 15) И ТекущаяДата < Дата(ЭтотГод , 1, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли ТекущаяДата > Дата(ЭтотГод, 3, 15) И ТекущаяДата < Дата(ЭтотГод, 4, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли ТекущаяДата > Дата(ЭтотГод, 6, 15) И ТекущаяДата < Дата(ЭтотГод, 7, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли ТекущаяДата > Дата(ЭтотГод, 9, 15) И ТекущаяДата < Дата(ЭтотГод, 10, 21) Тогда
		Возврат Истина;
		
	ИначеЕсли  ТекущаяДата > Дата(ЭтотГод, 12, 15) И ТекущаяДата < Дата(ЭтотГод + 1, 1, 21) Тогда
		Возврат Истина;
		
	Иначе
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция ПорогСуммаДля1СКонтрагент()
	
	// Считаем 1000 рублей ключевой суммой после которой можно показывать баннеры.
	// Используется для определения порога НДС к вычету, НДС к возмещению и дебиторской задолженности.
	Возврат 1000;
	
КонецФункции

Функция ПорогСуммаДля1СПАРК()
	
	// Считаем 10000 рублей ключевой суммой после которой можно показывать баннеры.
	// Используется для определения порога НДС к вычету, НДС к возмещению и дебиторской задолженности.
	Возврат 10000;
	
КонецФункции

Функция ОбщееКоличествоДокументовДляРекламыЭДО()
	
	Возврат 100;
	
КонецФункции

Функция КоличествоПоступленийДляРекламыЭДО()
	
	Возврат 50;
	
КонецФункции

Функция КоличествоРеализацийДляРекламыЭДО()
	
	Возврат 50;
	
КонецФункции

Функция КоличествоКонтрагентовДляРекламыКонтрагент()
	
	Возврат 30;
	
КонецФункции

Функция КоличествоСотрудниковДляРекламыОтчетности()
	
	Возврат 25;
	
КонецФункции

Процедура ДобавитьБаннерВТаблицу(СписокБаннеров, Баннер) Экспорт
	
	Если НЕ Баннер.ДоступенПоПравам Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока                      = СписокБаннеров.Добавить();
	НоваяСтрока.Порядок              = СписокБаннеров.Количество();
	НоваяСтрока.Баннер               = Баннер;
	НоваяСтрока.ИдентификаторБаннера = Баннер.Идентификатор;
	НоваяСтрока.ИмяСервиса           = Баннер.ИмяСервиса;
	
КонецПроцедуры

Процедура УдалитьСтрокиИзТаблицы(Таблица, СтрокиКУдалению)
	
	КоличествоЭлементов = СтрокиКУдалению.Количество();
	Для Индекс = 1 По КоличествоЭлементов Цикл
		Таблица.Удалить(СтрокиКУдалению[КоличествоЭлементов - Индекс]);
	КонецЦикла;
	СтрокиКУдалению = Неопределено;
	
КонецПроцедуры

Функция СтатусЗакрытогоБаннера()
	
	Возврат "close";
	
КонецФункции

#Область Конструкторы

Функция НовыйТаблицаСписокБаннеров()
	
	ТаблицаСпискаБаннеров = Новый ТаблицаЗначений;
	// Колонки
	ТаблицаСпискаБаннеров.Колонки.Добавить("Баннер"); // см. НовыйБаннерДляСписка()
	ТаблицаСпискаБаннеров.Колонки.Добавить("ИдентификаторБаннера", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаСпискаБаннеров.Колонки.Добавить("Порядок",              ОбщегоНазначения.ОписаниеТипаЧисло(5));
	ТаблицаСпискаБаннеров.Колонки.Добавить("ИмяСервиса",           ОбщегоНазначения.ОписаниеТипаСтрока(50));
	
	// Индексы
	ТаблицаСпискаБаннеров.Индексы.Добавить("ИдентификаторБаннера");
	
	Возврат ТаблицаСпискаБаннеров;
	
КонецФункции

Функция НовыйИнформацияОСтороннихСертификатах()
	
	Информация = Новый Структура;
	Информация.Вставить("ДатаПоиска",               '00010101');
	Информация.Вставить("СертификатНайден",         Ложь);
	Информация.Вставить("СрокДействияСертификата",  '00010101');
	Информация.Вставить("СторонниеСертификаты",     Новый Структура);
	
	Возврат Информация;
	
КонецФункции 

#КонецОбласти

#Область Имена

// Ключи хранилища общих настроек

Функция ИмяНастройкиПерсонализированныеПредложенияСервисов() Экспорт
	
	Возврат "ПерсонализированныеПредложенияСервисов";
	
КонецФункции

Функция ИмяКлючНастройкиЗакрытыеПользователемБаннеры() Экспорт
	
	Возврат "ЗакрытыеПользователемБаннеры";
	
КонецФункции

Функция ИмяКлючНастроекСторонниеСертификаты()
	
	Возврат "СторонниеСертификаты";
	
КонецФункции 

// Имена сервисов

Функция ИмяПереносСроковУплатыНалогов_МониторНалогов()
	
	Возврат "ПереносСроковУплатыНалогов_МониторНалогов";
	
КонецФункции

Функция ИмяПереносСроковУплатыНалогов()
	
	Возврат "ПереносСроковУплатыНалогов";
	
КонецФункции

Функция ИмяУстановитьДатуЗапрета()
	
	Возврат "УстановитьДатуЗапрета";
	
КонецФункции

Функция ИмяАвтоматическаяЗагрузкаПатчей()
	
	Возврат "АвтоматическаяЗагрузкаПатчей";
	
КонецФункции

Функция ИмяСервис1СОтчетность()
	
	Возврат "Сервис1СОтчетность";
	
КонецФункции

Функция ИмяСервис1СЭДО()
	
	Возврат "Сервис1СЭДО";
	
КонецФункции

Функция ИмяСервис1СКонтрагент()
	
	Возврат "Сервис1СКонтрагент";
	
КонецФункции

Функция ИмяСервис1СПАРК()
	
	Возврат "Сервис1СПАРК";
	
КонецФункции

Функция ИмяСервисНадежностьБанков()
	
	Возврат "СервисНадежностьБанков";
	
КонецФункции

Функция ИмяСервисМойНалог()
	
	Возврат "СервисМойНалог";
	
КонецФункции

Функция ИмяСервисПоддержкаСервиса()
	
	Возврат "ПоддержкаСервиса";
	
КонецФункции

Функция ИмяСервисЗаявкаНаКредит()
	
	Возврат "ЗаявкаНаКредит";
	
КонецФункции

Функция ИмяСервисЗаявкаНаЛизинг()
	
	Возврат "ЗаявкаНаЛизинг";
	
КонецФункции

Функция ИмяСервисФинОтчетность()
	
	Возврат "ФинОтчетность";
	
КонецФункции

Функция ИмяСервисЗаявкаНаОткрытиеСчета()

	Возврат "ЗаявкаНаОткрытиеСчета";

КонецФункции

Функция ИмяСервисПереходиНаНКО()
	
	Возврат "ПереходиНаНКО";
	
КонецФункции

Функция ИмяСервисМоиЗадачи()
	
	Возврат "МоиЗадачи";
	
КонецФункции

Функция ИмяСервисАвтоматическийВыпускПродукции()
	
	Возврат "АвтоматическийВыпускПродукции";
	
КонецФункции

Функция ИмяНастройкаФункциональности()
	
	Возврат "НастройкаФункциональности";
	
КонецФункции

Функция ИмяСервисАУСН()
	
	Возврат ИнтеграцияАУСНКлиентСервер.ИдентификаторСервиса();
	
КонецФункции

Функция ИмяСервисЛКАУСН()
	
	Возврат ИнтеграцияАУСНКлиентСервер.ИдентификаторСервисаЛКАУСН();
	
КонецФункции

Функция ИмяПереходНаЕНС_МониторНалогов()
	
	Возврат "ПереходНаЕНС_МониторНалогов";
	
КонецФункции

Функция ИмяПереходНаЕНС()
	
	Возврат "ПереходНаЕНС";
	
КонецФункции

Функция ИмяУведомленияЕНС()

	Возврат "УведомленияЕНС";

КонецФункции

Функция ИмяСервисСверкиРасчетов()
	
	Возврат "СервисСверкиРасчетов";
	
КонецФункции

Функция ИмяИнтерфейсИнтеграцииСБанкомПереносКомандЕНС()
	
	Возврат "ИнтерфейсИнтеграцииСБанкомПереносКомандЕНС";
	
КонецФункции

// Имена картинок

Функция ИмяКартинкиПереносСроковУплатыНалогов()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ИмяКартинкиЛоготипУстановитьДатуЗапрета()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ИмяКартинкиЛоготипАвтоматическаяЗагрузкаПатчей()
	
	Возврат "КотВТюрбане";
	
КонецФункции

Функция ИмяКартинкиЛоготип1СОтчетность()
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат "ЛоготипОтчетностьСервис";
	Иначе
		Возврат "ЛоготипОтчетность";
	КонецЕсли;
	
КонецФункции

Функция ИмяКартинкиЛоготип1СЭДО()
	
	Возврат "ЛоготипЭДО";
	
КонецФункции

Функция ИмяКартинкиЛоготип1СКонтрагент()
	
	Возврат "ЛоготипКонтрагент";
	
КонецФункции

Функция ИмяКартинкиЛоготип1СПАРК()
	
	Возврат "ЛоготипКонтрагент";
	
КонецФункции

Функция ИмяКартинкиЛоготипЗаявкаНаКредит()
	
	Возврат "ЛоготипСервисПоКредитам";
	
КонецФункции

Функция ИмяКартинкиЛоготипЗаявкаНаОткрытиеСчета()
	
	Возврат "ЛоготипСервис1СОткрытиеСчета";
	
КонецФункции

Функция ИмяКартинкиЛоготипФинОтчетность()
	
	Возврат "ЛоготипФинОтчетность";
	
КонецФункции

Функция ИмяКартинкиПереходиНаНКО()
	
	Возврат "ЛоготипПереходиНаНКО";
	
КонецФункции

Функция ИмяКартинкиПереходНаЕНС()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ИмяКартинкиЛоготипУведомленияЕНС()
	
	Возврат "КотОтправкаФНС";
	
КонецФункции

Функция ИмяКартинкиИнтерфейсИнтеграцииСБанкомПереносКомандЕНС()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

// Имена размещений

// Возвращает имя размещения для формы настройки налогов и отчетов.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияНалогиИОтчеты() Экспорт
	
	Возврат "НалогиИОтчеты"
	
КонецФункции

// Возвращает имя размещения для формы журнала Документы.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияДокументы() Экспорт
	
	Возврат "Документы"
	
КонецФункции

// Возвращает имя размещения для раздела "Товары" простого интерфейса.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияТовары() Экспорт
	
	Возврат "Товары";
	
КонецФункции

// Возвращает имя размещения для формы списка Реализации товаров и услуг.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияРеализация() Экспорт
	
	Возврат "Реализация"
	
КонецФункции

// Возвращает имя размещения для формы списка Реализации товаров и услуг.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияСчетПокупателю() Экспорт
	
	Возврат "СчетПокупателю"
	
КонецФункции

// Возвращает имя размещения для формы списка Выпуск продукции (отчет производства за смену).
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияВыпускПродукции() Экспорт
	
	Возврат "ВыпускПродукции"
	
КонецФункции

// Возвращает имя размещения для формы списка Поступление товаров и услуг.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПоступление() Экспорт
	
	Возврат "Поступление"
	
КонецФункции

// Возвращает имя размещения для общих форм ИнформационнаяПанель.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОбщее() Экспорт
	
	Возврат "Общее"
	
КонецФункции

// Возвращает имя размещения для общих форм СписокЗадач.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияСписокЗадач() Экспорт
	
	Возврат "СписокЗадач"
	
КонецФункции

// Возвращает имя размещения в случае запроса из стороннего внешнего приложения
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияИнтерфейсБанка() Экспорт
	
	Возврат "ИнтерфейсБанка";
	
КонецФункции

// Возвращает имя размещения для формы списка Акта сверки расчетов с контрагентом.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАктСверкиРасчетов() Экспорт
	
	Возврат "АктСверкиРасчетов"
	
КонецФункции

// Возвращает имя размещения для формы документа Акта сверки расчетов с контрагентом.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАктСверкиРасчетовДокумент() Экспорт
	
	Возврат "АктСверкиРасчетовДокумент"
	
КонецФункции

// Возвращает имя размещения для формы списка справочника Контрагенты.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияКонтрагенты() Экспорт
	
	Возврат "Контрагенты";
	
КонецФункции

// Имя отчетной кампании

Функция ИмяОтчетнойКампании(ОтчетнаяКампания)
	
	// Для текста баннера нужно имя отчетной кампании в дательной падеже,
	// например, "Отчетная кампания по страховым взносам"
	
	ТекстНалогНа = НСтр("ru='Налог на'") + " ";
	ИмяОтчетнойКампании = СтрЗаменить(ОтчетнаяКампания, ТекстНалогНа, "");
	
	Если ВРег(ИмяОтчетнойКампании) = ИмяОтчетнойКампании Тогда
		// Если все символы в строке верхнего регистра, то это аббревиатура
		// и в дательном падеже будет то же имя.
		ОтчетнаяКампанияДательныйПадеж = ИмяОтчетнойКампании;
		
	ИначеЕсли Константы.ИспользоватьСервисСклоненияMorpher.Получить() Тогда
		// Если подключен сервис склонения, то склоняем через него.
		ОтчетнаяКампанияДательныйПадеж = НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(ИмяОтчетнойКампании, 3));
		
	Иначе
		// Если сервис не подключен, то используем заданные имена кампаний.
		Если НРег(ИмяОтчетнойКампании) = НСтр("ru='акцизы'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='акцизам'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='игорный бизнес'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='игорному бизнесу'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='таможенный союз'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='таможенному союзу'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='прибыль'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='прибыли'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='водный налог'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='водному налогу'");
			
		ИначеЕсли НРег(ИмяОтчетнойКампании) = НСтр("ru='страховые взносы'") Тогда
			ОтчетнаяКампанияДательныйПадеж = НСтр("ru='страховым взносам'");
			
		Иначе
			ОтчетнаяКампанияДательныйПадеж = НРег(ИмяОтчетнойКампании);
			
		КонецЕсли;
	КонецЕсли;
	
	// Строка вида "по игорному бизнесу" или "по НДС".
	Возврат СтрШаблон(НСтр("ru='по %1'"), ОтчетнаяКампанияДательныйПадеж);
	
КонецФункции

// Возвращает имя размещения для журнала "Банковские выписки"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияБанковскиеВыписки() Экспорт
	
	Возврат "БанковскиеВыписки";
	
КонецФункции

// Возвращает имя размещения для журнала "Деньги"
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияДеньги() Экспорт
	
	Возврат "Деньги";
	
КонецФункции

// Возвращает имя размещения для журнала "Кассовые документы"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияКассовыеДокументы() Экспорт
	
	Возврат "КассовыеДокументы";
	
КонецФункции

// Возвращает имя размещения для отчета "Монитор налогов и отчетности"
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияМониторНалогов() Экспорт
	
	Возврат "МониторНалогов";
	
КонецФункции

Функция ИмяСервисПредодобренныйКредитВСбербанке()
	
	Возврат "ПредодобренныйКредитВСбербанке";
	
КонецФункции

Функция ИмяКартинкиЛоготипСбербанк()
	
	Возврат "ЛоготипСбербанк";
	
КонецФункции

Функция ИмяНастройкаВидимостиСчетовУчета()
	
	Возврат "НастройкаВидимостиСчетовУчета";
	
КонецФункции

// Возвращает имя размещения для списка документов "Требования-накладные"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияТребованиеНакладная() Экспорт
	
	Возврат "ТребованиеНакладная";
	
КонецФункции

Функция ИмяСервисSmartway()
	
	Возврат "Smartway";
	
КонецФункции

// Возвращает имя размещения для документа "Авансовые отчеты"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАвансовыеОтчеты() Экспорт
	
	Возврат "АвансовыеОтчеты";
	
КонецФункции

// Возвращает имя размещения для отчета "Монитор основных показателей"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияМониторОсновныхПоказателей() Экспорт
	
	Возврат "МониторОсновныхПоказателей";
	
КонецФункции

// Возвращает имя размещения для отчета "Продажи по контрагентам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПродажиПоКонтрагентам() Экспорт
	
	Возврат "ПродажиПоКонтрагентам";
	
КонецФункции

// Возвращает имя размещения для отчета "Анализ движений денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАнализДвиженийДенежныхСредств() Экспорт
	
	Возврат "АнализДвиженийДенежныхСредств";
	
КонецФункции

// Возвращает имя размещения для отчета "Остатки денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОстаткиДенежныхСредств() Экспорт
	
	Возврат "ОстаткиДенежныхСредств";
	
КонецФункции

// Возвращает имя размещения для отчета "Поступления денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПоступленияДенежныхСредств() Экспорт
	
	Возврат "ПоступленияДенежныхСредств";
	
КонецФункции

// Возвращает имя размещения для отчета "Расходы денежных средств"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияРасходыДенежныхСредств() Экспорт
	
	Возврат "РасходыДенежныхСредств";
	
КонецФункции

// Возвращает имя размещения для отчета "Доходы и расходы"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияДоходыРасходы() Экспорт
	
	Возврат "ДоходыРасходы";
	
КонецФункции

// Возвращает имя размещения для отчета "Оборотные средства"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОборотныеСредства() Экспорт
	
	Возврат "ОборотныеСредства";
	
КонецФункции

// Возвращает имя размещения для отчета "Продажи по месяцам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияСравнениеПродаж() Экспорт
	
	Возврат "СравнениеПродаж";
	
КонецФункции

// Возвращает имя размещения для отчета "Динамика задолженности поставщикам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияДинамикаЗадолженностиПоставщикам() Экспорт
	
	Возврат "ДинамикаЗадолженностиПоставщикам";
	
КонецФункции

// Возвращает имя размещения для отчета "Счета, не оплаченные поставщикам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАнализНеоплаченныхСчетовПоставщиков() Экспорт
	
	Возврат "АнализНеоплаченныхСчетовПоставщиков";
	
КонецФункции

// Возвращает имя размещения для отчета "Задолженность поставщикам"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияЗадолженностьПоставщикам() Экспорт
	
	Возврат "ЗадолженностьПоставщикам";
	
КонецФункции

// Возвращает имя размещения для отчета "Задолженность поставщикам по срокам долга"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияЗадолженностьПоставщикамПоСрокамДолга() Экспорт
	
	Возврат "ЗадолженностьПоставщикамПоСрокамДолга";
	
КонецФункции

// Возвращает имя размещения для отчета "Оборотно-сальдовая ведомость"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОборотноСальдоваяВедомость() Экспорт
	
	Возврат "ОборотноСальдоваяВедомость";
	
КонецФункции

// Возвращает имя размещения для отчета "Оборотно-сальдовая ведомость по счету"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОборотноСальдоваяВедомостьПоСчету() Экспорт
	
	Возврат "ОборотноСальдоваяВедомостьПоСчету";
	
КонецФункции

// Возвращает имя размещения для отчета "Анализ счета"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияАнализСчета() Экспорт
	
	Возврат "АнализСчета";
	
КонецФункции

// Возвращает имя размещения для отчета "Карточка счета"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияКарточкаСчета() Экспорт
	
	Возврат "КарточкаСчета";
	
КонецФункции

// Возвращает имя размещения для отчета "Обороты счета"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОборотыСчета() Экспорт
	
	Возврат "ОборотыСчета";
	
КонецФункции

// Возвращает имя размещения в случае запроса из стороннего внешнего приложения
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияНастройкиУчетаЗарплаты() Экспорт
	
	Возврат "НастройкиУчетЗарплаты";
	
КонецФункции

// Возвращает имя размещения для формы списка Заявки на кредит.
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияЗаявкиНаКредит() Экспорт
	
	Возврат "ЗаявкиНаКредит";
	
КонецФункции

// Возвращает имя размещения для формы списка Заявки на открытие счета.
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияЗаявкиНаОткрытиеСчета() Экспорт
	
	Возврат "ЗаявкиНаОткрытиеСчета";
	
КонецФункции

// Возвращает имя размещения для журнала Начисление зарплаты.
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияНачислениеЗарплаты() Экспорт
	
	Возврат "НачислениеЗарплаты";
	
КонецФункции

// Возвращает имя размещения для раздела "Сотрудники" (простой интерфейс).
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияПростойИнтерфейсРазделСотрудники() Экспорт
	
	Возврат "ПростойИнтерфейсРазделСотрудники";
	
КонецФункции

Функция ИмяКадровыеДокументыЭТК()
	
	Возврат "КадровыеДокументыЭТК";
	
КонецФункции

// Возвращает имя размещения в случае запроса из стороннего внешнего приложения
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияНастройкиКадровыеДокументыЭТК() Экспорт
	
	Возврат "КадровыеДокументы";
	
КонецФункции

// Возвращает имя размещения для кабинета сотрудника
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяКабинетСотрудника()
	
	Возврат "КабинетСотрудника";
	
КонецФункции

// Возвращает имя размещения для общей формы ПечатьДокументов
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияПечатьДокументов() Экспорт
	
	Возврат "ПечатьДокументов";
	
КонецФункции

// Возвращает имя размещения для формы списка Начисление зарплаты.
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияНачислениеЗарплатыФормаСписка() Экспорт
	
	Возврат "НачислениеЗарплатыФормаСписка";
	
КонецФункции

// Возвращает имя размещения для списка "Основные средства"
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОсновныеСредства() Экспорт
	
	Возврат "ОсновныеСредства";
	
КонецФункции

// Возвращает имя размещения для формы списка Розничная продажа.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияРозничнаяПродажа() Экспорт
	
	Возврат "РозничнаяПродажа";
	
КонецФункции

Функция ИмяЭТрН()
	
	Возврат "ЭТрН";
	
КонецФункции

Функция ИмяКартинкиУведомлениеОВыкупеМаркетплейс()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

// Возвращает имя размещения для формы списка Маркетплейсы журнала Операции.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияМаркетплейсы() Экспорт
	
	Возврат "Маркетплейс";
	
КонецФункции

// Возвращает имя размещения для формы списка Передачи ОС.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПередачаОС() Экспорт
	
	Возврат "ПередачаОС";
	
КонецФункции

Функция ИмяУведомленияОЗадачахПоЭлектроннойПочте()
	
	Возврат "УведомленияОЗадачахПоЭлектроннойПочте";
	
КонецФункции

// Возвращает имя размещения для формы списка справочника "Банковские счета".
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияБанковскиеСчетаОрганизаций() Экспорт

	Возврат "БанковскиеСчетаОрганизаций";

КонецФункции

// Возвращает имя размещения для формы списка документа "Платежное поручение".
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПлатежныеПоручения() Экспорт

	Возврат "ПлатежныеПоручения";

КонецФункции

// Возвращает имя размещения для формы списка справочника "Государственные контракты".
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияГосударственныеКонтракты() Экспорт

	Возврат "ГосударственныеКонтракты";

КонецФункции

// Возвращает имя размещения для формы списка "Отчет о розничных продажах"
// Возвращаемое значение:
//		Строка - Имя размещения
//

Функция ИмяРазмещенияОтчетОРозничныхПродажах() Экспорт

	Возврат "ОтчетОРозничныхПродажах";

КонецФункции 

// Возвращает имя размещения для обработки "СравнениеРежимовНалогообложения"
//
// Возвращаемое значение:
//  Строка - Имя размещения
//
Функция ИмяРазмещенияСравненияСистемНалогообложения() Экспорт
	
	Возврат "СравнениеСНО";
	
КонецФункции

Функция ИмяКартинкиНоваяАвторизацияЯндекс()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ИмяРазмещенияПомощникИнтеграцииСПлатформойСамозанятые() Экспорт
	
	Возврат "ПомощникИнтеграцииСПлатформойСамозанятые";
	
КонецФункции

#КонецОбласти

#Область Кредит

Функция Сайт1СКредит()
	
	Возврат "https://v8.1c.ru/tekhnologii/obmen-dannymi-i-integratsiya/realizovannye-resheniya/1s-kredit/";
	
КонецФункции

#КонецОбласти


#Область ЗаявкаНаКредит

Функция СреднемесячноеПоступлениеНаСчетДляРекламыЗаявкаНаКредит()
	
	Возврат 50000;
	
КонецФункции

Процедура ОбновитьДанныеДляЗаявкиНаКредит(ДатаПолученияДанных) Экспорт
	
	ТаблицаПоступленияНаСчет = УниверсальныйОбменСБанкамиФормы.ПоступлениеНаСчет(ДатаПолученияДанных, 6);
	
	Раздел = Перечисления.РазделыПерсонализированныхДанных.ЗаявкаНаКредит;
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для каждого ДоступнаяОрганизации Из ДоступныеОрганизации Цикл
		
		НаборЗаписей = РегистрыСведений.ПерсонализированныеДанные.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(ДоступнаяОрганизации);
		НаборЗаписей.Отбор.Раздел.Установить(Раздел);
		
		СтрокаТаблицы = ТаблицаПоступленияНаСчет.Найти(ДоступнаяОрганизации, "Организация");
		
		Если СтрокаТаблицы = Неопределено Тогда
			ЗначениеПоказателя = 0;
		Иначе
			ЗначениеПоказателя = СтрокаТаблицы.Среднее;
		КонецЕсли;
		
		Запись = НаборЗаписей.Добавить();
		Запись.Раздел             = Раздел;
		Запись.НомерСтрокиРаздела = 1;
		Запись.Организация        = ДоступнаяОрганизации;
		Запись.ЗначениеПоказателя = ЗначениеПоказателя;
		Запись.ДатаОбновления     = ДатаПолученияДанных;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция КодыОКВЭД2ДляЛизинга()
	
	Возврат "01,02,03,10,11,13,14,15,16,18,20,21,22,25,26,27,28,29,30,31,32,33,38,41,42,43,49,50,52,53,61,62,77,79,81,87,93";
	
КонецФункции

Процедура ОбновитьДанныеДляЗаявкаНаЛизинг(ДатаПолученияДанных) Экспорт
	
	СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ЗаявкиНаЛизинг;
	
	ТаблицаОборотыПоСчетам = УниверсальныйОбменСБанкамиФормы.ОборотыПоСчетамРасчетов(СервисОбменаСБанками, ДатаПолученияДанных, 6);
	
	Раздел = Перечисления.РазделыПерсонализированныхДанных.ЗаявкаНаЛизинг;
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	Организации.КодОКВЭД2 КАК КодОКВЭД2
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&ДоступныеОрганизации)";
	
	Запрос.УстановитьПараметр("ДоступныеОрганизации", ДоступныеОрганизации);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	УстановитьПривилегированныйРежим(Истина);
	КодыОКВЭД2ДляЛизинга = КодыОКВЭД2ДляЛизинга();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ПерсонализированныеДанные.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(ВыборкаДетальныеЗаписи.Организация);
		НаборЗаписей.Отбор.Раздел.Установить(Раздел);
		
		СтрокаТаблицы = ТаблицаОборотыПоСчетам.Найти(ВыборкаДетальныеЗаписи.Организация, "Организация");
		
		Если СтрокаТаблицы = Неопределено Тогда
			ЗначениеПоказателя = 0;
		Иначе
			ЗначениеПоказателя = ?(СтрокаТаблицы.ОборотДт <> 0 ИЛИ СтрокаТаблицы.ОборотКт <> 0, 1, 0);
		КонецЕсли;
		
		Запись = НаборЗаписей.Добавить();
		Запись.Раздел             = Раздел;
		Запись.НомерСтрокиРаздела = 1;
		Запись.Организация        = ВыборкаДетальныеЗаписи.Организация;
		Запись.ЗначениеПоказателя = ЗначениеПоказателя;
		Запись.ДатаОбновления     = ДатаПолученияДанных;
		
		ЗначениеПоказателя = 0;
		Если СтрДлина(ВыборкаДетальныеЗаписи.КодОКВЭД2) >= 2 Тогда
			ВидДеятельности = Лев(ВыборкаДетальныеЗаписи.КодОКВЭД2, 2);
			Если СтрНайти(КодыОКВЭД2ДляЛизинга, ВидДеятельности) <> 0  Тогда
				ЗначениеПоказателя = 1;	
			КонецЕсли;
		КонецЕсли;
		
		Запись = НаборЗаписей.Добавить();
		Запись.Раздел             = Раздел;
		Запись.НомерСтрокиРаздела = 2;
		Запись.Организация        = ВыборкаДетальныеЗаписи.Организация;
		Запись.ЗначениеПоказателя = ЗначениеПоказателя;
		Запись.ДатаОбновления     = ДатаПолученияДанных;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФинОтчетность

Процедура ОбновитьДанныеФинОтчетность(Организация, ДатаПолученияДанных) Экспорт
	
	// Проверяем, есть ли кредиты, займы или лизинговые обязательства перед контрагентами,
	// которые подключены к сервису 1С:ФинОтчетность.
	
	СчетаКредитовЗаймовЛизинга = Новый Массив;
	СчетаКредитовЗаймовЛизинга.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоКраткосрочнымКредитамИЗаймам); // 66
	СчетаКредитовЗаймовЛизинга.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДолгосрочнымКредитамИЗаймам);  // 67
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
	СчетаКредитовЗаймовЛизинга, ПланыСчетов.Хозрасчетный.СчетаРасчетовПоАренде());
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетаКредитовЗаймовЛизинга", СчетаКредитовЗаймовЛизинга);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ ВТ_Счета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетаКредитовЗаймовЛизинга)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	Контрагенты.Ссылка КАК Контрагент,
	|	Контрагенты.ИНН КАК ИНН
	|ПОМЕСТИТЬ ВТ_ХозрасчетныйОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ВТ_Счета.Счет
	|				ИЗ
	|					ВТ_Счета),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты),
	|			Организация = &Организация) КАК ХозрасчетныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ХозрасчетныйОстатки.Субконто1 = Контрагенты.Ссылка
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстатокКт > 0
	|	И Контрагенты.ИНН <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_ХозрасчетныйОстатки.Организация КАК Организация
	|ИЗ
	|	ВТ_ХозрасчетныйОстатки КАК ВТ_ХозрасчетныйОстатки";
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		// Остатков по кредитам, займам или лизингу нет, баннер показывать не будем,
		// обновлять данные сервиса не требуется.
		// Запишем в регистр нулевое значение, которое используется как признак того, что это значение было получено
		// и его не требуется получать повторно в ближайщее время (до истечения стандартного срока хранения кэшированных данных).
		ЗаписатьНулевоеЗначениеПоказателяФинОтчетности(Организация, ДатаПолученияДанных);
		Возврат;
	КонецЕсли;
	
	// Запускам принудительное обновление данных сервиса перед тем, как обращаться к его справочникам,
	// если с момента последнего обновления прошло более 3-х дней.
	ДатаПоследнегоОбновления = УниверсальныйОбменСБанками.ДатаПоследнегоОбновленияСервиса(
	Перечисления.СервисыОбменаСБанками.ФинансоваяОтчетность);
	ДлинаСуток = 24 * 60 *60;
	
	Если НЕ ЗначениеЗаполнено(ДатаПоследнегоОбновления)
		ИЛИ НачалоДня(ДатаПоследнегоОбновления) + 3 * ДлинаСуток  < НачалоДня(ТекущаяДатаСеанса()) Тогда
		ФинОтчетностьВБанки.ОбновитьДанныеСервисаФинОтчетности();
	КонецЕсли;
	
	// Получаем список доступных банков после обновления данных с сервера.
	Запрос.УстановитьПараметр("ДоступныеБанки",  ФинОтчетностьВБанки.ДоступныеБанки());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БанкиУниверсальногоОбмена.Ссылка КАК Банк,
	|	БанкиУниверсальногоОбмена.ИНН КАК ИНН
	|ПОМЕСТИТЬ ВТ_ДоступныеБанки
	|ИЗ
	|	Справочник.БанкиУниверсальногоОбмена КАК БанкиУниверсальногоОбмена
	|ГДЕ
	|	БанкиУниверсальногоОбмена.Ссылка В(&ДоступныеБанки)
	|	И БанкиУниверсальногоОбмена.ИНН <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Банки.Ссылка,
	|	КлассификаторБанков.ИНН
	|ИЗ
	|	Справочник.Банки КАК Банки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК КлассификаторБанков
	|		ПО Банки.Код = КлассификаторБанков.Код
	|ГДЕ
	|	Банки.Ссылка В(&ДоступныеБанки)
	|	И КлассификаторБанков.ИНН <> """"
	|	И НЕ КлассификаторБанков.ПометкаУдаления
	|	И НЕ КлассификаторБанков.ДеятельностьПрекращена
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ХозрасчетныйОстатки.Организация КАК Организация,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ДоступныеБанки.Банк) КАК КоличествоБанков,
	|	МАКСИМУМ(ВТ_ДоступныеБанки.Банк) КАК Банк
	|ИЗ
	|	ВТ_ХозрасчетныйОстатки КАК ВТ_ХозрасчетныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДоступныеБанки КАК ВТ_ДоступныеБанки
	|		ПО ВТ_ХозрасчетныйОстатки.ИНН = ВТ_ДоступныеБанки.ИНН
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ХозрасчетныйОстатки.Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Раздел = Перечисления.РазделыПерсонализированныхДанных.ФинОтчетность;
	
	НаборЗаписей = РегистрыСведений.ПерсонализированныеДанные.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Раздел.Установить(Раздел);
	
	Пока Выборка.Следующий() Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись.Раздел             = Раздел;
		Запись.НомерСтрокиРаздела = 1;
		Запись.Организация        = Выборка.Организация;
		Запись.ЗначениеПоказателя = Выборка.КоличествоБанков;
		Запись.ДанныеРасшифровки  = Выборка.Банк;
		Запись.ДатаОбновления     = ДатаПолученияДанных;
		
	КонецЦикла;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей.Записать();
	Иначе
		ЗаписатьНулевоеЗначениеПоказателяФинОтчетности(Организация, ДатаПолученияДанных);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьНулевоеЗначениеПоказателяФинОтчетности(Организация, ДатаПолученияДанных)
	
	МенеджерЗаписи = РегистрыСведений.ПерсонализированныеДанные.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Организация        = Организация;
	МенеджерЗаписи.Раздел             = Перечисления.РазделыПерсонализированныхДанных.ФинОтчетность;
	МенеджерЗаписи.НомерСтрокиРаздела = 1;
	МенеджерЗаписи.ДатаОбновления     = ДатаПолученияДанных;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ДоступКВнешнимРесурсам

// См. РаботаВБезопасномРежимеПереопределяемый.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	НовыеРазрешения = Новый Массив;
	
	Сервисы = Новый Соответствие;
	Сервисы.Вставить(ПредодобренныйКредитВСбербанкеИмяСервера(), НСтр("ru = 'Информационное взаимодействие с банками'"));
	
	Для каждого Сервис Из Сервисы Цикл
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(Сервис.Ключ);
		Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		ВРег(СтруктураURI.Схема),
		СтруктураURI.Хост,
		СтруктураURI.Порт,
		Сервис.Значение);
		НовыеРазрешения.Добавить(Разрешение);
	КонецЦикла;
	
	ЗапросыРазрешений.Добавить(РаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(НовыеРазрешения));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОписанияБаннеров

// Каждый баннер имеет следующие функции:
// 1. Функция НовыйБаннер...               описывает структуру добавления баннера в общий список.
// 2. Функция ТекстЗапроса...              формирует текст запроса для получения данных баннера.
// 3. Функция ВыполняютсяУсловияБаннера... проверяет данные баннера на условие срабатывания.
// 4. Функция ЗаполнитьДанныеБаннера...    помещает данные из общей коллекции ДанныеБаннеров в данные конкретного баннера, 
//    который помещается в реквизит формы. Эта информация может быть перенесена в ОбстоятельстваЗакрытияБаннера.

// 5. Функция ТекстБаннера...              возвращает форматированную строку с текстом баннера, который будет размещен на форме.
// 6. Функция ИдентификаторБаннера...      ключ баннера, который используется для поиска и идентификации баннера.

#Область ЗаявкаНаОткрытиеСчета

Функция НовыйБаннерЗаявкаНаОткрытиеСчета()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисЗаявкаНаОткрытиеСчета();
	Баннер.Идентификатор        = ИдентификаторБаннераЗаявкаНаОткрытиеСчета();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипЗаявкаНаОткрытиеСчета();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннер";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераЗаявкаНаОткрытиеСчета();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераЗаявкаНаОткрытиеСчета();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	Баннер.Группа               = "Банки";
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаЗаявкаНаОткрытиеСчета(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераЗаявкаНаОткрытиеСчета();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИСТИНА КАК Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаОткрытиеСчета КАК ЗаявкаНаОткрытиеСчета
	|ГДЕ
	|	ЗаявкаНаОткрытиеСчета.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ТекущаяДата, МЕСЯЦ, -12), ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|	И ЗаявкаНаОткрытиеСчета.Организация В(&СписокОрганизаций)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераЗаявкаНаОткрытиеСчета(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЗаявкаНаОткрытиеСчета(Баннер, ДанныеБаннера, ТекущаяДата)
	
	НавигационнаяСсылка = Баннер.НавигационнаяСсылка;
	
	Баннер.ТекстБаннера = ТекстБаннерЗаявкаНаОткрытиеСчета(ДанныеБаннера, НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерЗаявкаНаОткрытиеСчета(ДанныеБаннера, НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Откройте расчетный счет на выгодных условиях и подписывайте платежки прямо из 1С'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Подробнее'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераЗаявкаНаОткрытиеСчета()

	Возврат "ЗаявкаНаОткрытиеСчета";

КонецФункции

Функция ДоступностьБаннераЗаявкаНаОткрытиеСчета()
	
	Возврат НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком")
		И ПравоДоступа("Изменение",	Метаданные.Документы.ЗаявкаНаОткрытиеСчета);
		
КонецФункции

#КонецОбласти

#Область ПереносСроковУплатыНалогов

Функция НовыйБаннерПереносСроковУплатыНалогов()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяПереносСроковУплатыНалогов();
	Баннер.Идентификатор        = ИдентификаторБаннераПереносСроковУплатыНалогов();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераПереносСроковУплатыНалогов();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиПереносСроковУплатыНалогов();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераПереносСроковУплатыНалогов();
	Баннер.Исключительный       = Истина;
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция НовыйБаннерПереносСроковУплатыНалогов_МониторНалогов()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяПереносСроковУплатыНалогов_МониторНалогов();
	Баннер.Идентификатор        = ИдентификаторБаннераПереносСроковУплатыНалогов_МониторНалогов();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераПереносСроковУплатыНалогов_МониторНалогов();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиПереносСроковУплатыНалогов();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераПереносСроковУплатыНалогов();
	Баннер.Исключительный       = Истина;
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераПереносСроковУплатыНалогов(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("Организация",                       ДанныеБаннеров.Организация);
	ДанныеБаннера.Вставить("СрокиПереносаУплатыНалоговИстекли", ДанныеБаннеров.ТекущаяДата >= ДатаОкончанияПереносаСроковУплатыНалогов());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПереносСроковУплатыНалогов(), ДанныеБаннера);
	
КонецПроцедуры

Процедура ПолучитьДанныеБаннераПереносСроковУплатыНалогов_МониторНалогов(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("Организация",                       ДанныеБаннеров.Организация);
	ДанныеБаннера.Вставить("СрокиПереносаУплатыНалоговИстекли", ДанныеБаннеров.ТекущаяДата >= ДатаОкончанияПереносаСроковУплатыНалогов());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПереносСроковУплатыНалогов_МониторНалогов(), ДанныеБаннера);
	
КонецПроцедуры

Функция ИдентификаторБаннераПереносСроковУплатыНалогов()
	
	Возврат "ПереносСроковУплатыНалогов";
	
КонецФункции

Функция ИдентификаторБаннераПереносСроковУплатыНалогов_МониторНалогов()
	
	Возврат "ПереносСроковУплатыНалогов_МониторНалогов";
	
КонецФункции

Функция ВыполняютсяУсловияБаннераПереносСроковУплатыНалогов(ДанныеБаннера, Баннер)
	
	Возврат Не ДанныеБаннера.СрокиПереносаУплатыНалоговИстекли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПереносСроковУплатыНалогов(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераПереносСроковУплатыНалогов(ДанныеБаннера.Организация, ТекущаяДата);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	Баннер.ДанныеБаннера.Вставить("Организация", ДанныеБаннера.Организация);
	
КонецПроцедуры

Функция ТекстБаннераПереносСроковУплатыНалогов(Организация, Период)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Перенос сроков уплаты налогов и страховых взносов в 2022 году'"));
	ШрифтТекста =  Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 10);
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Для ряда отраслей в 2022 году продлены сроки уплаты налогов и страховых взносов.'"), , ШрифтТекста),
		НовыйСтрокаБаннера(Символы.ПС, , ШрифтТекста),
		НовыйСтрокаБаннера("Проверьте", , ШрифтТекста),
		НовыйСтрокаБаннера(" ", , ШрифтТекста),
		НовыйСтрокаБаннера(НСтр("ru='настройки налогов и отчетов'"), ГиперссылкаАнтикризис(), ШрифтТекста));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ДоступностьБаннераПереносСроковУплатыНалогов()
	
	ПравоДоступаКЗадачамБухгалтера = ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ЗадачиБухгалтера);
	
	Возврат ПравоДоступаКЗадачамБухгалтера
		И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

Функция ДатаОкончанияПереносаСроковУплатыНалогов()
	
	Возврат '2023-01-01';
	
КонецФункции

Функция ГиперссылкаАнтикризис()
	
	Возврат "АнтикризисПродлениеСроков";
	
КонецФункции

#КонецОбласти

#Область УстановитьДатуЗапрета

Функция НовыйБаннерУстановитьДатуЗапрета()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяУстановитьДатуЗапрета();
	Баннер.Идентификатор        = ИдентификаторБаннераУстановитьДатуЗапрета();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераУстановитьДатуЗапрета();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипУстановитьДатуЗапрета();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераУстановитьДатуЗапрета();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	
	Баннер.ДанныеБаннера.Вставить("ОткрытьНастройкиПользователейИПрав", Ложь);
	Баннер.ДанныеБаннера.Вставить("ОткрытьДатыЗапретаИзмененияДанных", Ложь);
	Баннер.ДанныеБаннера.Вставить("ОткрытьСинхронизациюДанных", Ложь);
	Баннер.ДанныеБаннера.Вставить("ОткрытьДатыЗапретаЗагрузкиДанных", Ложь);
	
	Возврат Баннер;
	
КонецФункции

Функция ВыполняютсяУсловияБаннераУстановитьДатуЗапрета(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0
		ИЛИ ДанныеБаннера.ДатаНачалаПоказаБаннера > ТекущаяДатаСеанса()
		Тогда
		
		Возврат Ложь;
		
	ИначеЕсли ДанныеБаннера.ПроверятьЗапретЗагрузки Тогда 
		
		Возврат НЕ (ДанныеБаннера.ЕстьЗапретИзменения И ДанныеБаннера.ЕстьЗапретЗагрузки);
		
	Иначе
		
		Возврат НЕ ДанныеБаннера.ЕстьЗапретИзменения;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераУстановитьДатуЗапрета(Баннер, ДанныеБаннера, ТекущаяДата)
	
	// несмотря на наличие роли "ДобавлениеИзменениеДатЗапретаИзменения" у пользователя
	// может не быть права на редактирование константы "ИспользоватьДатыЗапретаИзменения"
	// в таком случае, выводим текст без навигационной ссылки
	
	ЕстьПравоРедактироватьЗапрет = ПравоДоступа("Редактирование", Метаданные.Константы.ИспользоватьДатыЗапретаИзменения);
	ЕстьПравоРедактироватьЗагрузку = ПравоДоступа("Редактирование", Метаданные.Константы.ИспользоватьДатыЗапретаЗагрузки);
	
	Если Не ДанныеБаннера.ИспользоватьДатыЗапретаИзменения 
		И Не ЕстьПравоРедактироватьЗапрет 
		Тогда
		
		Баннер.ТекстБаннера = ТекстБаннераУстановитьДатуЗапретаИзмененияБезСсылки(ДанныеБаннера);
		
	ИначеЕсли ДанныеБаннера.ЕстьЗапретИзменения 
		И ДанныеБаннера.ПроверятьЗапретЗагрузки
		И Не ДанныеБаннера.ЕстьЗапретЗагрузки И Не ЕстьПравоРедактироватьЗагрузку 
		Тогда
		
		Баннер.ТекстБаннера = ТекстБаннераУстановитьДатуЗапретаЗагрузкиБезСсылки(ДанныеБаннера);
		
	Иначе
		
		Если Не ДанныеБаннера.ИспользоватьДатыЗапретаИзменения Тогда
			
			Баннер.ДанныеБаннера.ОткрытьНастройкиПользователейИПрав = Истина;
			
		ИначеЕсли Не ДанныеБаннера.ЕстьЗапретИзменения Тогда
			
			Баннер.ДанныеБаннера.ОткрытьДатыЗапретаИзмененияДанных = Истина;
			
		ИначеЕсли Не ДанныеБаннера.ИспользоватьДатыЗапретаЗагрузки Тогда
			
			Баннер.ДанныеБаннера.ОткрытьСинхронизациюДанных = Истина;
			
		Иначе
			
			Баннер.ДанныеБаннера.ОткрытьДатыЗапретаЗагрузкиДанных = Истина;
			
		КонецЕсли;
		
		Баннер.ТекстБаннера = ТекстБаннераУстановитьДатуЗапрета(ДанныеБаннера, Баннер.НавигационнаяСсылка);
		
	КонецЕсли;
	
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия", ТекущаяДата);
	
КонецПроцедуры

Функция ТекстБаннераУстановитьДатуЗапрета(ДанныеБаннера, НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(
		СтрШаблон(НСтр("ru='Отчетная кампания за %1 год завершена'"), Формат(ДанныеБаннера.КонецОтчетногоГода, "ДФ=yyyy")));
	
	ШрифтТекста =  Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 10);
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Для предотвращения случайного изменения данных, '"), , ШрифтТекста),
		НовыйСтрокаБаннера(НСтр("ru='закройте доступ'"), НавигационнаяСсылка, ШрифтТекста),
		НовыйСтрокаБаннера(НСтр("ru=' к прошлому году'"), , ШрифтТекста));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ТекстБаннераУстановитьДатуЗапретаИзмененияБезСсылки(ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(
		СтрШаблон(НСтр("ru='Отчетная кампания за %1 год завершена'"), Формат(ДанныеБаннера.КонецОтчетногоГода, "ДФ=yyyy")));
	
	ШрифтТекста =  Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 10);
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Обратитесь к администратору для включения запрета доступа к прошлому году'"), , ШрифтТекста));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ТекстБаннераУстановитьДатуЗапретаЗагрузкиБезСсылки(ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(
	СтрШаблон(НСтр("ru='Отчетная кампания за %1 год завершена'"), Формат(ДанныеБаннера.КонецОтчетногоГода, "ДФ=yyyy")));
	
	ШрифтТекста =  Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 10);
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Обратитесь к администратору для включения запрета загрузки данных прошлого года'"), , ШрифтТекста));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераУстановитьДатуЗапрета()
	
	Возврат "УстановитьДатуЗапрета";
	
КонецФункции

Функция ДоступностьБаннераУстановитьДатуЗапрета()
	
	Возврат Пользователи.РолиДоступны("ДобавлениеИзменениеДатЗапретаИзменения", , Ложь);
	
КонецФункции

Процедура ПолучитьДанныеБаннераУстановитьДатуЗапрета(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ДатаНачалаПоказаБаннера",          ДобавитьМесяц(НачалоГода(ДанныеБаннеров.ТекущаяДата), 3));
	ДанныеБаннера.Вставить("КонецОтчетногоГода",               КонецГода(НачалоГода(ДанныеБаннеров.ТекущаяДата) - 1));
	ДанныеБаннера.Вставить("ИспользоватьДатыЗапретаЗагрузки",  Константы.ИспользоватьДатыЗапретаЗагрузки.Получить());
	ДанныеБаннера.Вставить("ИспользоватьДатыЗапретаИзменения", Константы.ИспользоватьДатыЗапретаИзменения.Получить());
	
	// запрет загрузки по дате проверяем только при включенной синхронизации данных
	
	ПроверятьЗапретЗагрузки = Константы.ИспользоватьСинхронизациюДанных.Получить();
	ДанныеБаннера.Вставить("ПроверятьЗапретЗагрузки", ПроверятьЗапретЗагрузки);
	
	ПроверкаЗапрета = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	
	ДанныеДляПроверки			= ПроверкаЗапрета.Добавить();
	ДанныеДляПроверки.Раздел	= "БухгалтерскийУчет";
	ДанныеДляПроверки.Дата		= ДанныеБаннера.КонецОтчетногоГода;
	
	ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		
		ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
		
		ЕстьЗапретИзменения	= Истина;
		ЕстьЗапретЗагрузки	= ПроверятьЗапретЗагрузки;
		
		Для каждого ДоступнаяОрганизация Из ДоступныеОрганизации Цикл
			
			ПервыйГодУчетаПоОрганизации = ЭтоПервыйГодУчетаОрганизации(ДоступнаяОрганизация);
			
			ДанныеДляПроверки.Объект = ДоступнаяОрганизация;
			
			ЕстьЗапретИзмененияПоОрганизации	=	ПервыйГодУчетаПоОрганизации 
				ИЛИ ЕстьЗапретИзменения 
					И ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ПроверкаЗапрета);
				
			ЕстьЗапретЗагрузкиПоОрганизации		=	ПервыйГодУчетаПоОрганизации 
				ИЛИ ЕстьЗапретЗагрузки 
					И ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ПроверкаЗапрета, , ,
						Перечисления.ВидыНазначенияДатЗапрета.ДляВсехИнформационныхБаз);
			
			Если Не ЕстьЗапретИзмененияПоОрганизации Тогда
				ЕстьЗапретИзменения = Ложь;												
			КонецЕсли;
			
			Если Не ЕстьЗапретЗагрузкиПоОрганизации Тогда
				ЕстьЗапретЗагрузки = Ложь;												
			КонецЕсли;
			
			Если ЕстьЗапретЗагрузки = Ложь И ЕстьЗапретИзменения = Ложь Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ПервыйГодУчета = ЭтоПервыйГодУчетаОрганизации(ОрганизацияПоУмолчанию); 
		
		ЕстьЗапретИзменения	=	ПервыйГодУчета
			ИЛИ ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ПроверкаЗапрета);
		ЕстьЗапретЗагрузки	=	ПервыйГодУчета
			ИЛИ ПроверятьЗапретЗагрузки	
			И 	ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ПроверкаЗапрета, , ,
				Перечисления.ВидыНазначенияДатЗапрета.ДляВсехИнформационныхБаз);
	КонецЕсли;
	
	ДанныеБаннера.Вставить("ЕстьЗапретЗагрузки", ЕстьЗапретЗагрузки);
	ДанныеБаннера.Вставить("ЕстьЗапретИзменения", ЕстьЗапретИзменения);
			
	ДанныеБаннеров.Вставить(ИдентификаторБаннераУстановитьДатуЗапрета(), ДанныеБаннера);
	
КонецПроцедуры

Функция ЭтоПервыйГодУчетаОрганизации(Организация)
	
	ДатаПервогоПроведенногоДокумента = ЖурналыДокументов.ЖурналОпераций.ДатаПервогоПроведенногоДокумента(Организация);
	
	Возврат	Не ЗначениеЗаполнено(ДатаПервогоПроведенногоДокумента)
		ИЛИ Год(ТекущаяДатаСеанса()) = Год(ДатаПервогоПроведенногоДокумента)
	
КонецФункции

#КонецОбласти

#Область АвтоматическаяЗагрузкаПатчей

Функция НовыйБаннерАвтоматическаяЗагрузкаПатчей()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяАвтоматическаяЗагрузкаПатчей();
	Баннер.Идентификатор        = ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипАвтоматическаяЗагрузкаПатчей();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераАвтоматическаяЗагрузкаПатчей();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ВыполняютсяУсловияБаннераАвтоматическаяЗагрузкаПатчей(ДанныеБаннера, Баннер)
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераАвтоматическаяЗагрузкаПатчей(Баннер)
	
	Баннер.ТекстБаннера = ТекстБаннераАвтоматическаяЗагрузкаПатчей(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераАвтоматическаяЗагрузкаПатчей(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Автоматическая загрузка обновлений'"));
	
	Если ПолучениеОбновленийПрограммы.ДоступноИспользованиеОбновленияПрограммы(Истина) Тогда
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru='Включите'"), НавигационнаяСсылка),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru='автоматическую загрузку быстрых обновлений'")));
	Иначе
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru='Попросите администратора включить автоматическую загрузку быстрых обновлений'")));
	КонецЕсли;
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей()
	
	Возврат "АвтоматическаяЗагрузкаПатчей";
	
КонецФункции

Функция ДоступностьБаннераАвтоматическаяЗагрузкаПатчей()
	
	// Баннер доступен в ПРОФ и КОРП в режиме коробки.
	Возврат Не ВариантыПриложений.ЭтоБазоваяВерсия()
		И Не ОбщегоНазначения.РазделениеВключено()
		И ПолучениеОбновленийПрограммы.ДоступнаАвтоматическаяЗагрузкаИсправлений()
		И Не ПолучениеОбновленийПрограммы.АвтоматическаяЗагрузкаИсправленийВключена();
	
КонецФункции

Процедура ПолучитьДанныеБаннераАвтоматическаяЗагрузкаПатчей(ДанныеБаннеров)
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей(), Новый Структура);
	
КонецПроцедуры

#КонецОбласти

#Область СтороннийСертификат

Функция НовыйБаннерСтороннийСертификат()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СОтчетность();
	Баннер.Идентификатор       = ИдентификаторБаннераСтороннийСертификат();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераСтороннийСертификат();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераСтороннийСертификат();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаОкончанияНайденногоСертификата", Дата(1,1,1));
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераСтороннийСертификат(ДанныеБаннеров)
	
	ИнформацияОСтороннихСертификатах = ИнформацияОСтороннихСертификатах(ДанныеБаннеров.Организация);
	
	Если ИнформацияОСтороннихСертификатах <> Неопределено И ИнформацияОСтороннихСертификатах.СертификатНайден Тогда
		ДатаСертификата = ИнформацияОСтороннихСертификатах.СрокДействияСертификата;
	Иначе
		ДатаСертификата = Неопределено;
	КонецЕсли;
	ДанныеБаннеров.Вставить(ИдентификаторБаннераСтороннийСертификат(), ДатаСертификата);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераСтороннийСертификат(ДатаОкончанияСертификата, Баннер)
		
	Если НЕ ЗначениеЗаполнено(ДатаОкончанияСертификата) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Баннер.ОбстоятельстваЗакрытия.ДатаОкончанияНайденногоСертификата = ДатаОкончанияСертификата Тогда
		// Баннер показывали, не повторяемся.
		Возврат Ложь;
	КонецЕсли;
	
	// Проверим условие баннера.
	Возврат ((ДатаОкончанияСертификата > ТекущаяДатаСеанса()) И ДатаОкончанияСертификата <= ДобавитьМесяц(ТекущаяДатаСеанса(), 3));
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераСтороннийСертификат(Баннер, ДатаОкончанияНайденногоСертификата)
	
	Баннер.ТекстБаннера = ТекстБаннераСтороннийСертификат(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаОкончанияНайденногоСертификата = ДатаОкончанияНайденногоСертификата;
	
КонецПроцедуры

Функция ТекстБаннераСтороннийСертификат(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Пользуетесь электронной отчетностью?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Делайте это просто -'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='отправляйте отчетность прямо из 1С'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераСтороннийСертификат()
	
	Возврат "СтороннийСертификат";
	
КонецФункции

Функция ДоступностьБаннераСтороннийСертификат()
	
	Возврат НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ЧисленностьСотрудников

Функция НовыйБаннерЧисленностьСотрудников()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СОтчетность();
	Баннер.Идентификатор       = ИдентификаторБаннераЧисленностьСотрудников();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераЧисленностьСотрудников();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераЧисленностьСотрудников();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаЧисленностьСотрудников(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераЧисленностьСотрудников();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаЧисленностьСотрудниковПредыдущая", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаЧисленностьСотрудников", НомераТаблиц.Количество());
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоСотрудниковПредыдущее,
	|	0 КАК КоличествоСотрудников
	|ПОМЕСТИТЬ ВременнаяТаблицаЧисленностьСотрудниковПредыдущая
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленностьПредыдущая)
	|	И ПерсонализированныеДанные.НомерСтрокиРаздела = 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	0,
	|	ПерсонализированныеДанные.ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.СреднесписочнаяЧисленность)
	|	И ПерсонализированныеДанные.НомерСтрокиРаздела = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВременнаяТаблицаЧисленностьСотрудниковПредыдущая.КоличествоСотрудниковПредыдущее) КАК КоличествоСотрудниковПредыдущее,
	|	СУММА(ВременнаяТаблицаЧисленностьСотрудниковПредыдущая.КоличествоСотрудников) КАК КоличествоСотрудников
	|ПОМЕСТИТЬ ВременнаяТаблицаЧисленностьСотрудников
	|ИЗ
	|	ВременнаяТаблицаЧисленностьСотрудниковПредыдущая КАК ВременнаяТаблицаЧисленностьСотрудниковПредыдущая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаЧисленностьСотрудников.КоличествоСотрудников КАК КоличествоСотрудников
	|ИЗ
	|	ВременнаяТаблицаЧисленностьСотрудников КАК ВременнаяТаблицаЧисленностьСотрудников
	|ГДЕ
	|	ВременнаяТаблицаЧисленностьСотрудников.КоличествоСотрудников > ВременнаяТаблицаЧисленностьСотрудников.КоличествоСотрудниковПредыдущее";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераЧисленностьСотрудников(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат (ДанныеБаннера[0].КоличествоСотрудников >= КоличествоСотрудниковДляРекламыОтчетности());
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЧисленностьСотрудников(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераЧисленностьСотрудников(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераЧисленностьСотрудников(НавигационнаяСсылка, ДанныеБаннера)
	
	ЧисленностьСотрудников = ДанныеБаннера[0].КоличествоСотрудников;
	ТекстПроСотрудников = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ЧисленностьСотрудников, "сотрудник, сотрудника, сотрудников");
	
	ТекстЗаголовка = НовыйЗаголовок(
	СтрШаблон(НСтр("ru='Ваша компания выросла! У вас %1.'"), ТекстПроСотрудников));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Теперь отчетность в фонды нужно'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='сдавать по-новому'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераЧисленностьСотрудников()
	
	Возврат "ЧисленностьСотрудников";
	
КонецФункции

Функция ДоступностьБаннераЧисленностьСотрудников()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоПросмотраВсехКадровыхДанных()
	И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ТестовыйТариф

Функция НовыйБаннерТестовыйТариф()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервис1СОтчетность();
	Баннер.Идентификатор        = ИдентификаторБаннераТестовыйТариф();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераТестовыйТариф();
	Баннер.Исключительный       = Истина;
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераТестовыйТариф();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.День;
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераТестовыйТариф(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами",
	ТарификацияБПВызовСервераПовтИсп.РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераТестовыйТариф(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераТестовыйТариф(ДанныеБаннера, Баннер)
	
	Возврат НЕ ДанныеБаннера.РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераТестовыйТариф(Баннер, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннерТестовыйТариф(Баннер.НавигационнаяСсылка);
	
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерТестовыйТариф(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Отправляйте отчетность через Интернет, экономьте Ваше время.'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Подключите платный тариф'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераТестовыйТариф()
	
	Возврат "ТестовыйТариф";
	
КонецФункции

Функция ДоступностьБаннераТестовыйТариф()
	
	Возврат Обработки.ОплатаСервисаБП.ДоступнаОплатаСервиса()
	И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ОтчетнаяКампания

Функция НовыйБаннерОтчетнаяКампания()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СОтчетность();
	Баннер.Идентификатор       = ИдентификаторБаннераОтчетнаяКампания();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СОтчетность();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераОтчетнаяКампания();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераОтчетнаяКампания();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатыПрошедшихОтчетныхКампаний", Новый Массив);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаОтчетнаяКампания(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераОтчетнаяКампания();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.НаименованиеСокращенное КАК ОтчетнаяКампания,
	|	ЗадачиБухгалтера.НачалоВыполнения КАК НачалоОтчетнойКампании
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И ЗадачиБухгалтера.НачалоВыполнения МЕЖДУ &ТекущаяДата И &СрокНачалаОтчетности
	|	И ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|	И ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).Периодичность <> ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			И ВЫРАЗИТЬ(ЗадачиБухгалтера.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов).Периодичность <> ЗНАЧЕНИЕ(Перечисление.Периодичность.День)";
	
	// В закрытых ранее баннерах хранятся даты начала отчетных кампания, которые пользователь закрыл.
	// Исключим их из выборки, чтобы не показывать еще раз.
	ОбстоятельстваЗакрытия = СтруктураБаннеров[Идентификатор].ОбстоятельстваЗакрытия;
	Если ОбстоятельстваЗакрытия.ДатыПрошедшихОтчетныхКампаний.Количество() > 0 Тогда
		ТекстЗапроса = ТекстЗапроса + " И ЗадачиБухгалтера.НачалоВыполнения НЕ В (&ПоказанныеДаты)";
		Запрос.УстановитьПараметр("ПоказанныеДаты", ОбстоятельстваЗакрытия.ДатыПрошедшихОтчетныхКампаний);
	КонецЕсли;
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераОтчетнаяКампания(ОтчетныеКампании, Баннер)
	
	Возврат (ОтчетныеКампании.Количество() > 0);
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераОтчетнаяКампания(Баннер, ОтчетныеКампании, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераОтчетнаяКампания(Баннер.НавигационнаяСсылка, ОтчетныеКампании, ТекущаяДата);
	
	СтрокаОтчетнойКампании = ОтчетныеКампании[0];
	Баннер.ОбстоятельстваЗакрытия.ДатыПрошедшихОтчетныхКампаний.Добавить(СтрокаОтчетнойКампании.НачалоОтчетнойКампании);
	
КонецПроцедуры

Функция ТекстБаннераОтчетнаяКампания(НавигационнаяСсылка, ОтчетныеКампании, ТекущаяДата)
	
	СтрокаОтчетнойКампании = ОтчетныеКампании[0];
	Если ОтчетныеКампании.Количество() = 1 Тогда
		// Если отчетная кампания по одному отчету, то укажем имя отчета.
		ИмяОтчетнойКампании = ИмяОтчетнойКампании(СтрокаОтчетнойКампании.ОтчетнаяКампания);
	Иначе
		// Если в эту отчетную кампанию нужно сдавать несколько отчетов,
		// то не будем уточнять название отчетов.
		ИмяОтчетнойКампании = "";
	КонецЕсли;
	
	СрокиПоДням        = Перечисления.ОтносительныеСроки.СрокиПоДням();
	ОсталосьДней       = (НачалоДня(СтрокаОтчетнойКампании.НачалоОтчетнойКампании) - НачалоДня(ТекущаяДата))/86400;
	ОтносительныйСрок  = СрокиПоДням.Получить(ОсталосьДней);
	ПредставлениеСрока = НРег(БлокиИнформационнойПанели.ПредставлениеСрока(ОтносительныйСрок, ОсталосьДней));
	
	ТекстОтчетнаяКампания = ?(ПустаяСтрока(ИмяОтчетнойКампании),
		СтрШаблон(НСтр("ru='начинается %1'"),
			ПредставлениеСрока),
		СтрШаблон(НСтр("ru='%1 начинается %2'"),
			ИмяОтчетнойКампании,
			ПредставлениеСрока));
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Отчетная кампания %1'"), ТекстОтчетнаяКампания));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как просто'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='отправлять отчетность из программы'"),  НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ИдентификаторБаннераОтчетнаяКампания()
	
	Возврат "ОтчетнаяКампания";
	
КонецФункции

Функция ДоступностьБаннераОтчетнаяКампания()
	
	ПравоДоступаКЗадачамБухгалтера = ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ЗадачиБухгалтера);
	
	Возврат ПравоДоступаКЗадачамБухгалтера
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область НДСКВычетуКонтрагент

Функция НовыйБаннерНДСКВычетуКонтрагент()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СКонтрагент();
	Баннер.Идентификатор       = ИдентификаторБаннераНДСКВычетуКонтрагент();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СКонтрагент();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерКонтрагент";
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераНДСКВычетуКонтрагент();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераНДСКВычетуКонтрагент();
	Баннер.ДанныеБаннера.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаНДСКВычетуКонтрагент(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
	
	Идентификатор = ИдентификаторБаннераНДСКВычетуКонтрагент();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ЭтоПериодНДС(ТекущаяДата) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПерсонализированныеДанные.ДанныеРасшифровки КАК Контрагент,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК НДСКВычету
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.НДСКВычету)
	|	И ПерсонализированныеДанные.НомерСтрокиРаздела = 1";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераНДСКВычетуКонтрагент(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Строка = ДанныеБаннера[0];
	Если Строка.НДСКВычету > ПорогСуммаДля1СКонтрагент() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНДСКВычетуКонтрагент(Баннер, ДанныеБаннера, ТекущаяДата)
	
	СтрокаДанных = ДанныеБаннера[0];
	Баннер.ТекстБаннера = ТекстБаннераНДСКВычетуКонтрагент(Баннер.НавигационнаяСсылка, СтрокаДанных);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, СтрокаДанных);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераНДСКВычетуКонтрагент(НавигационнаяСсылка, СтрокаДанных)
	
	НаименованиеКонтрагента = Строка(СтрокаДанных.Контрагент);
	// Ограничим длину наименования контрагента 20 символами, чтобы поместилось в баннер.
	НаименованиеКонтрагента = ?(СтрДлина(НаименованиеКонтрагента) > 20,
		Лев(НаименованиеКонтрагента, 18) + "...",
		НаименованиеКонтрагента);
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='%1 предъявил НДС на сумму %2 руб'"),
	НаименованиеКонтрагента, СтрокаДанных.НДСКВычету));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(СтрШаблон(НСтр("ru='Узнайте всё про %1 в'"), СтрокаДанных.Контрагент)),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='досье контрагента!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераНДСКВычетуКонтрагент()
	
	Возврат "НДСКВычетуКонтрагент";
	
КонецФункции

Функция ДоступностьБаннераНДСКВычетуКонтрагент()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область НДСКВычетуСПАРК

Функция НовыйБаннерНДСКВычетуСПАРК()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СПАРК();
	Баннер.Идентификатор       = ИдентификаторБаннераНДСКВычетуСПАРК();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СПАРК();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераНДСКВычетуСПАРК();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераНДСКВычетуСПАРК();
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаНДСКВычетуСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц, ТекущаяДата)
	
	Идентификатор = ИдентификаторБаннераНДСКВычетуСПАРК();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ЭтоПериодНДС(ТекущаяДата) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК НДСКВычету
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.НДСКВычету)
	|	И ПерсонализированныеДанные.ДанныеРасшифровки В (&ПустыеЗначенияДанныеРасшифровки)";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераНДСКВычетуСПАРК(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Строка = ДанныеБаннера[0];
	Если Строка.НДСКВычету > ПорогСуммаДля1СПАРК() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНДСКВычетуСПАРК(Баннер, ДанныеБаннера, ТекущаяДата)
	
	СтрокаДанных = ДанныеБаннера[0];
	Баннер.ТекстБаннера = ТекстБаннераНДСКВычетуСПАРК(Баннер.НавигационнаяСсылка, СтрокаДанных, ТекущаяДата);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераНДСКВычетуСПАРК(НавигационнаяСсылка, СтрокаДанных, ТекущаяДата)
	
	Квартал = Формат(ТекущаяДата, "ДФ=q");
	
	Если Квартал = "2" Тогда
		ПредлогКвартал = НСтр("ru='во'");
	Иначе
		ПредлогКвартал = НСтр("ru='в'");
	КонецЕсли;
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='НДС к вычету %1 %2 квартале составляет %3 руб'"),
		ПредлогКвартал, Квартал, СтрокаДанных.НДСКВычету));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, кто из Ваших контрагентов имеет'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='низкие индексы надежности!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераНДСКВычетуСПАРК()

	Возврат "НДСКВычетуСПАРК";

КонецФункции

Функция ДоступностьБаннераНДСКВычетуСПАРК()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КрупнейшийДебиторКонтрагент

Функция НовыйБаннерКрупнейшийДебитор()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СКонтрагент();
	Баннер.Идентификатор       = ИдентификаторБаннераКрупнейшийДебитор();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СКонтрагент();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерКонтрагент";
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКрупнейшийДебитор();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераКрупнейшийДебитор();
	Баннер.ДанныеБаннера.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКрупнейшийДебитор(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКрупнейшийДебитор();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СУММА(ДанныеМонитораРуководителя.Сумма) КАК СуммаЗадолженности,
	|	ДанныеМонитораРуководителя.ДанныеРасшифровки КАК Контрагент
	|ИЗ
	|	РегистрСведений.ДанныеМонитораРуководителя КАК ДанныеМонитораРуководителя
	|ГДЕ
	|	ДанныеМонитораРуководителя.Организация = &Организация
	|	И ДанныеМонитораРуководителя.РазделМонитора = ЗНАЧЕНИЕ(Перечисление.РазделыМонитораРуководителя.ЗадолженностьПокупателей)
	|	И НЕ ДанныеМонитораРуководителя.ДанныеРасшифровки В (&ПустыеЗначенияДанныеРасшифровки)
	|	И ДанныеМонитораРуководителя.ПрошлыйПериод = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеМонитораРуководителя.ДанныеРасшифровки
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаЗадолженности УБЫВ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКрупнейшийДебитор(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Строка = ДанныеБаннера[0];
	Если Строка.СуммаЗадолженности > ПорогСуммаДля1СКонтрагент() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКрупнейшийДебитор(Баннер, ДанныеБаннера, ТекущаяДата)
	
	СтрокаДанных = ДанныеБаннера[0];
	Баннер.ТекстБаннера = ТекстБаннераКрупнейшийДебиторКонтрагент(Баннер.НавигационнаяСсылка, СтрокаДанных);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, СтрокаДанных);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКрупнейшийДебиторКонтрагент(НавигационнаяСсылка, СтрокаДанных)
	
	НаименованиеКонтрагента = Строка(СтрокаДанных.Контрагент);
	// Ограничим длину наименования контрагента 25 символами, чтобы поместилось в баннер.
	НаименованиеКонтрагента = ?(СтрДлина(НаименованиеКонтрагента) > 25,
		Лев(НаименованиеКонтрагента, 23) + "...",
		НаименованиеКонтрагента);
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='%1 должен нам %2 руб'"),
		НаименованиеКонтрагента, СтрокаДанных.СуммаЗадолженности));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(СтрШаблон(НСтр("ru='Узнайте всё про %1 в'"), СтрокаДанных.Контрагент)),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='досье контрагента!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераКрупнейшийДебитор()
	
	Возврат "КрупнейшийДебиторКонтрагент";
	
КонецФункции

Функция ДоступностьБаннераКрупнейшийДебитор()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КрупнейшиеДебиторыСПАРК

Функция НовыйБаннерКрупнейшийДебиторСПАРК()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СПАРК();
	Баннер.Идентификатор       = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СПАРК();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераКрупнейшиеДебиторыСПАРК();
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКрупнейшиеДебиторыСПАРК(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СУММА(ДанныеМонитораРуководителя.Сумма) КАК СуммаЗадолженности
	|ИЗ
	|	РегистрСведений.ДанныеМонитораРуководителя КАК ДанныеМонитораРуководителя
	|ГДЕ
	|	ДанныеМонитораРуководителя.Организация = &Организация
	|	И ДанныеМонитораРуководителя.РазделМонитора = ЗНАЧЕНИЕ(Перечисление.РазделыМонитораРуководителя.ЗадолженностьПокупателей)
	|	И ДанныеМонитораРуководителя.ДанныеРасшифровки В (&ПустыеЗначенияДанныеРасшифровки)
	|	И ДанныеМонитораРуководителя.ПрошлыйПериод = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеМонитораРуководителя.НомерСтрокиРаздела
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеМонитораРуководителя.НомерСтрокиРаздела УБЫВ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКрупнейшиеДебиторыСПАРК(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СуммаЗадолженности = ДанныеБаннера[0].СуммаЗадолженности;
	
	Если СуммаЗадолженности > ПорогСуммаДля1СПАРК() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКрупнейшиеДебиторыСПАРК(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКрупнейшиеДебиторыСПАРК(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКрупнейшиеДебиторыСПАРК(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Покупатели должны нам %1 руб'"), 
		Формат(ДанныеБаннера.СуммаЗадолженности, "ЧДЦ=")));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, кто из контрагентов имеет'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='низкие индексы надежности!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераКрупнейшиеДебиторыСПАРК()
	
	Возврат "КрупнейшиеДебиторыСПАРК";
	
КонецФункции

Функция ДоступностьБаннераКрупнейшиеДебиторыСПАРК()
	
	Возврат УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии()
	И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КоличествоДокументов

Функция НовыйБаннерОбщееКоличествоДокументов()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераОбщееКоличествоДокументов();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераОбщееКоличествоДокументов();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераОбщееКоличествоДокументов();
	Баннер.ДанныеБаннера.Вставить("КоличествоПоступлений", 0);
	Баннер.ДанныеБаннера.Вставить("КоличествоРеализаций", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаОбщееКоличествоДокументов(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераОбщееКоличествоДокументов();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаКоличествоОбщихДокументов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоРеализаций,
	|	0 КАК КоличествоПоступлений
	|ПОМЕСТИТЬ ВременнаяТаблицаКоличествоОбщихДокументов
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоРеализаций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ПерсонализированныеДанные.ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоПоступлений)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВременнаяТаблицаКоличествоОбщихДокументов.КоличествоРеализаций), 0) КАК КоличествоРеализаций,
	|	ЕСТЬNULL(СУММА(ВременнаяТаблицаКоличествоОбщихДокументов.КоличествоПоступлений), 0) КАК КоличествоПоступлений
	|ИЗ
	|	ВременнаяТаблицаКоличествоОбщихДокументов КАК ВременнаяТаблицаКоличествоОбщихДокументов";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераОбщееКоличествоДокументов(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КоличествоДокументов = ДанныеБаннера[0].КоличествоРеализаций + ДанныеБаннера[0].КоличествоПоступлений;
	
	Если КоличествоДокументов > ОбщееКоличествоДокументовДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераОбщееКоличествоДокументов(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераОбщееКоличествоДокументов(Баннер.НавигационнаяСсылка, ДанныеБаннера[0], ТекущаяДата);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераОбщееКоличествоДокументов(НавигационнаяСсылка, ДанныеБаннера, ТекущаяДата)
	
	КоличествоРеализаций = ДанныеБаннера.КоличествоРеализаций;
	КоличествоПоступлений = ДанныеБаннера.КоличествоПоступлений;
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы ввели %1 реализаций и поступлений в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоРеализаций + КоличествоПоступлений, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='сэкономить на документообороте!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераОбщееКоличествоДокументов()
	
	Возврат "ОбщееКоличествоДокументов";
	
КонецФункции

Функция ДоступностьБаннераОбщееКоличествоДокументов()
	
	ПравоДоступаКРеализациям = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.РеализацияТоваровУслуг
		);
	
	ПравоДоступаКПоступлениям = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.ПоступлениеТоваровУслуг
		);
	
	Возврат ПравоДоступаКРеализациям
		И ПравоДоступаКПоступлениям
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область КоличествоПоступлений

Функция НовыйБаннерКоличествоПоступлений()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераКоличествоПоступлений();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКоличествоПоступлений();
	Баннер.ДоступенПоПравам    = ПравоДоступа("Просмотр",
		Метаданные.Документы.ПоступлениеТоваровУслуг);
	Баннер.ДанныеБаннера.Вставить("КоличествоПоступлений", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоПоступлений(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКоличествоПоступлений();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоПоступлений
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоПоступлений)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоПоступлений(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоПоступлений > КоличествоПоступленийДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоПоступлений(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоПоступлений(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоПоступлений(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы ввели %1 поступлений в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ДанныеБаннера.КоличествоПоступлений, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='сэкономить на документообороте!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераКоличествоПоступлений()
	
	Возврат "КоличествоПоступлений";
	
КонецФункции

#КонецОбласти

#Область КоличествоРеализаций

Функция НовыйБаннерКоличествоРеализаций()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераКоличествоРеализаций();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКоличествоРеализаций();
	Баннер.ДоступенПоПравам    = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.РеализацияТоваровУслуг);
	Баннер.ДанныеБаннера.Вставить("КоличествоРеализаций", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоРеализаций(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКоличествоРеализаций();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоРеализаций
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоРеализаций)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоРеализаций(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоРеализаций > КоличествоРеализацийДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоРеализаций(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоРеализаций(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоРеализаций(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы оформили %1 реализаций в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ДанныеБаннера.КоличествоРеализаций, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='сэкономить на документообороте!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераКоличествоРеализаций()
	
	Возврат "КоличествоРеализаций";
	
КонецФункции

#КонецОбласти

#Область КоличествоКонтрагентов

Функция НовыйБаннерКоличествоКонтрагентов()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервис1СКонтрагент();
	Баннер.Идентификатор        = ИдентификаторБаннераКоличествоКонтрагентов();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннерКонтрагент";
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготип1СКонтрагент();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераКоличествоКонтрагентов();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераКоличествоКонтрагентов();
	Баннер.ДанныеБаннера.Вставить("КоличествоКонтрагентов", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоКонтрагентов(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераКоличествоКонтрагентов();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоКонтрагентов
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.КоличествоКонтрагентов)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоКонтрагентов(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоКонтрагентов >= КоличествоКонтрагентовДляРекламыКонтрагент() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоКонтрагентов(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоКонтрагентов(Баннер.НавигационнаяСсылка, ДанныеБаннера[0], ТекущаяДата);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоКонтрагентов(НавигационнаяСсылка, ДанныеБаннера, ТекущаяДата)
	
	МинутНаОдногоКонтрагента = 1;
	ВсегоМинут = ДанныеБаннера.КоличествоКонтрагентов * МинутНаОдногоКонтрагента;
	КоличествоЧасов = Цел(ВсегоМинут / 60);
	КоличествоМинут = ВсегоМинут % 60;
	
	Если КоличествоМинут = 0 Тогда
		ПотраченоВремени = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоЧасов, "час, часа, часов");
	ИначеЕсли КоличествоЧасов = 0 Тогда
		ПотраченоВремени = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ВсегоМинут, "минуту, минуты, минут");
	Иначе
		ПотраченоВремени = СтрШаблон("%1 %2",
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоЧасов, "час, часа, часов"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоМинут, "минуту, минуты, минут"));
	КонецЕсли;
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы потратили %1 на ввод контрагентов в этом квартале'"), 
	ПотраченоВремени));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как делать это быстрее с помощью'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='1С:Контрагент!'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераКоличествоКонтрагентов()
	
	Возврат "КоличествоКонтрагентов";
	
КонецФункции

Функция ДоступностьБаннераКоличествоКонтрагентов()
	
	ПравоДоступаККонтрагентам = ПравоДоступа(
		"Просмотр",
		Метаданные.Справочники.Контрагенты);
	
	Возврат ПравоДоступаККонтрагентам
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область НадежностьБанков

Функция НовыйБаннерНадежностьБанков()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисНадежностьБанков();
	Баннер.Идентификатор       = ИдентификаторБаннераНадежностьБанков();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераНадежностьБанков();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераНадежностьБанков();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераНадежностьБанков(ДанныеБаннеров, ПодготавливаемыйБаннер)
	
	Информация = НадежностьБанков.ИнформацияОСобытияхБанковОрганизаций();
	Если НЕ Информация.Используется Тогда
		Возврат;
	КонецЕсли;
	Если Информация.События.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ИдентификаторыЗакрытыхБаннеров = ПодготавливаемыйБаннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий;
	Если ТипЗнч(ИдентификаторыЗакрытыхБаннеров) <> Тип("Массив") Тогда
		ИдентификаторыЗакрытыхБаннеров = Новый Массив;
	КонецЕсли;
	
	ПодготавливаемыйБаннер.ДанныеБаннера.Вставить("ДанныеСобытия", НадежностьБанков.НовоеСобытие());
	ТаблицаДанныеБаннера = Новый ТаблицаЗначений;
	Для каждого ЭлементСобытия Из ПодготавливаемыйБаннер.ДанныеБаннера.ДанныеСобытия Цикл
		ТаблицаДанныеБаннера.Колонки.Добавить(ЭлементСобытия.Ключ);
	КонецЦикла;
	
	Для каждого ОписаниеСобытия Из Информация.События Цикл
		ДанныеСобытия = ОписаниеСобытия.Значение;
		Если ИдентификаторыЗакрытыхБаннеров.Найти(ДанныеСобытия.Идентификатор) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаДанныеБаннера.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСобытия);
	КонецЦикла;
	ТаблицаДанныеБаннера.Сортировать("Критичное Убыв, Идентификатор Возр");
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераНадежностьБанков(), ТаблицаДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераНадежностьБанков(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНадежностьБанков(Баннер, ДанныеБаннера)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Баннер.ТекстБаннера = ТекстБаннераНадежностьБанков(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера.ДанныеСобытия, ДанныеБаннера[0]);
	Идентификатор = ДанныеБаннера[0].Идентификатор;
	КлючСобытия = "НадежностьБанков" + Формат(Идентификатор, "ЧГ=");
	Баннер.ДанныеБаннера.Вставить("КлючСобытия", КлючСобытия);
	
	Если ДанныеБаннера[0].Критичное Тогда
		Баннер.ЦветФонаБаннер = "КритичноеСобытиеНадежностьБанковЦветФона";
		Баннер.ИмяКартинкаЛоготипа = "КритичноеСобытиеБаннераНадежностьБанков";
	Иначе
		Баннер.ЦветФонаБаннер = "ЦветФонаБлоковИнформационнойПанели";
		Баннер.ИмяКартинкаЛоготипа = "НекритичноеСобытиеБаннераНадежностьБанков";
	КонецЕсли;
	
	Если Баннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий.Найти(Идентификатор) = Неопределено Тогда
		Баннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий.Добавить(Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстБаннераНадежностьБанков(НавигационнаяСсылка, ДанныеБаннера)
	
	ЧастиЗаголовка = Новый Массив;
	ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(ДанныеБаннера.ТекстСобытия));
	Если ЗначениеЗаполнено(ДанныеБаннера.ИсточникИнформации) Тогда
		ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(" ("));
		Если ДанныеБаннера.Событие = Перечисления.СобытияНадежностьБанков.Информация Тогда
			ТекстИсточник = НСтр("ru='подробнее'");
		Иначе
			ТекстИсточник = СтрЗаменить(НСтр("ru='информация ЦБ РФ'"), " ", Символы.НПП);
		КонецЕсли;
		ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(ТекстИсточник, ДанныеБаннера.ИсточникИнформации));
		ЧастиЗаголовка.Добавить(НовыйЗаголовокНадежностьБанков(")"));
	КонецЕсли;
	ТекстЗаголовка = Новый ФорматированнаяСтрока(ЧастиЗаголовка);
	
	ЧастиПодзаголовка = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеБаннера.ТекстПояснения) Тогда
		ЧастиПодзаголовка.Добавить(НовыйСтрокаНадежностьБанков(ДанныеБаннера.ТекстПояснения));
	КонецЕсли;
	Если ДанныеБаннера.Критичное Тогда
		ЧастиПодзаголовка.Добавить(НовыйСтрокаНадежностьБанков(" "));
		ЧастиПодзаголовка.Добавить(НовыйСтрокаНадежностьБанков(НСтр("ru='Что делать'"), НавигационнаяСсылка));
	КонецЕсли;
	
	Если ЧастиПодзаголовка.Количество() > 0 Тогда
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(ЧастиПодзаголовка);
		Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	Иначе
		Возврат ТекстЗаголовка;
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторБаннераНадежностьБанков()
	
	Возврат "НадежностьБанков";
	
КонецФункции

Функция НовыйЗаголовокНадежностьБанков(ТекстЗаголовка, НавигационнаяСсылка = "")
	
	Возврат НовыйЗаголовок(ТекстЗаголовка, НавигационнаяСсылка, ШрифтыСтиля.ШрифтЗаголовкаИнформационнойПанели);
	
КонецФункции

Функция НовыйСтрокаНадежностьБанков(ТекстСтроки, НавигационнаяСсылка = "")
	
	Возврат НовыйСтрокаБаннера(ТекстСтроки, НавигационнаяСсылка, ШрифтыСтиля.ОбычныйШрифтТекста);
	
КонецФункции

Функция ДоступностьБаннераНадежностьБанков()
	
	Возврат НадежностьБанков.СервисИспользуется()
	И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область ПлатформаСамозанятые

Функция НовыйБаннерПлатформаСамозанятые(Размещение = Неопределено)
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисМойНалог();
	Баннер.Идентификатор       = ИдентификаторБаннераПлатформаСамозанятые();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераПлатформаСамозанятые();
	Баннер.Исключительный      = Истина;
	Баннер.ИмяКартинкаЛоготипа = "ПлатформаСамозанятыеИнтеграция";
	Баннер.ДоступенПоПравам    = ДоступностьБаннераПлатформаСамозанятые();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Баннер.ЗависитОтОрганизации = Размещение <> ИмяРазмещенияПомощникИнтеграцииСПлатформойСамозанятые();
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераПлатформаСамозанятые(ДанныеБаннеров, ПодготавливаемыйБаннер);
	
	Если ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые() Тогда
		ДанныеСобытия = РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ИнформацияОСобытияхПлатформыСамозанятые(
			ДанныеБаннеров.Организация);
	ИначеЕсли УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(ДанныеБаннеров.Организация, ДанныеБаннеров.ТекущаяДата) Тогда
		ДанныеСобытия = Новый Структура("Организация, ПереходВОблако", ДанныеБаннеров.Организация, "https://1cfresh.com/register");
	КонецЕсли;
	
	Если ДанныеСобытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодготавливаемыйБаннер.ДанныеБаннера.Вставить("ДанныеСобытия", ДанныеСобытия);
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПлатформаСамозанятые(), ДанныеСобытия);
	
КонецПроцедуры

Функция ВыполняютсяУсловияПлатформаСамозанятые(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПлатформаСамозанятые(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера        = ТекстБаннераПлатформаСамозанятые(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	Баннер.ЦветФонаБаннер      = ЦветФонаБаннерПлатформаСамозанятые(ДанныеБаннера);
	Баннер.ИмяКартинкаЛоготипа = КартинкаЛоготипаБаннерПлатформаСамозанятые(ДанныеБаннера);
	// После закрытия баннер нельзя исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Ложь);
	
КонецПроцедуры

Функция ТекстБаннераПлатформаСамозанятые(НавигационнаяСсылка, ДанныеБаннера)
	
	Если ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые() Тогда
		
		Если ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.ЗапросОтправлен Тогда
			ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Не настроен сервис Мой налог'"));
			ТекстПодзаголовка = НСтр("ru='Выполните настройку в личном кабинете сервиса'");
		Иначе
			ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Отправляйте данные о доходах в ФНС прямо из 1С'"));
			ТекстПодзаголовка = НСтр("ru='Подключитесь к сервису'");
		КонецЕсли;
		
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(ТекстПодзаголовка),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru='Мой налог'"), НавигационнаяСсылка));
		
	Иначе
		
		ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Отправляйте данные о доходах в ФНС прямо из 1С'"));
		ТекстПодзаголовка = НСтр("ru='Для этого перейдите в облако'");
		
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(ТекстПодзаголовка),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru='1cfresh.com'"), НавигационнаяСсылка));
		
	КонецЕсли;
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ЦветФонаБаннерПлатформаСамозанятые(ДанныеБаннера)
	
	Если ДанныеБаннера.Свойство("Состояние")
		И ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.ЗапросОтправлен Тогда
		Возврат "ЦветБаннераПлатформаСамозанятыеКрасный";
	Иначе
		Возврат "ЦветБаннераПлатформаСамозанятыеЗеленый";
	КонецЕсли;
	
КонецФункции

Функция КартинкаЛоготипаБаннерПлатформаСамозанятые(ДанныеБаннера)
	
	Если ДанныеБаннера.Свойство("Состояние")
		И ДанныеБаннера.Состояние = Перечисления.СостоянияИнтеграцииСПлатформойСамозанятые.ЗапросОтправлен Тогда
		Возврат "ПлатформаСамозанятыеОшибка";
	Иначе
		Возврат "ПлатформаСамозанятыеИнтеграция";
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторБаннераПлатформаСамозанятые()
	
	Возврат "ПлатформаСамозанятые";
	
КонецФункции

Функция ДоступностьБаннераПлатформаСамозанятые()
	
	РекламироватьПереходВОблако = Не ОбщегоНазначения.РазделениеВключено()
		И Не ОбщегоНазначения.ЭтоАвтономноеРабочееМесто();
		
	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяНалогНаПрофессиональныйДоход")
		И Не ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком")
		И (ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые()
		Или РекламироватьПереходВОблако);
	
КонецФункции

#КонецОбласти

#Область ПоддержкаСервиса

Функция НовыйБаннерПоддержкаСервиса()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисПоддержкаСервиса();
	Баннер.Идентификатор       = ИдентификаторБаннераПоддержкаСервиса();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераПоддержкаСервиса();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераПоддержкаСервиса();
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераПоддержкаСервиса()
	
	Возврат "ПоддержкаСервиса";
	
КонецФункции

Функция ДоступностьБаннераПоддержкаСервиса()
	
	Возврат ОбщегоНазначения.РазделениеВключено()
	И ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

Функция НовыйЗаголовокПоддержкаСервиса(ТекстЗаголовка, НавигационнаяСсылка = "")
	
	Возврат НовыйЗаголовок(ТекстЗаголовка, НавигационнаяСсылка, ШрифтыСтиля.ШрифтЗаголовкаИнформационнойПанели);
	
КонецФункции

Процедура ПолучитьДанныеБаннераПоддержкаСервиса(ДанныеБаннеров, ПодготавливаемыйБаннер)
	
	ТаблицаСообщенийСервиса = Новый ТаблицаЗначений;
	ТаблицаСообщенийСервиса.Колонки.Добавить(
		"СсылкаНаДанные", Новый ОписаниеТипов("СправочникСсылка.ОбщиеДанныеИнформационногоЦентра"));
	УстановитьПривилегированныйРежим(Истина);
	ИнформационныйЦентрСервер.СформироватьСписокНовостейНаРабочийСтол(ТаблицаСообщенийСервиса, 1);
	
	Если ТаблицаСообщенийСервиса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыЗакрытыхБаннеров = ПодготавливаемыйБаннер.ОбстоятельстваЗакрытия.ИдентификаторыСобытий;
	Если ТипЗнч(ИдентификаторыЗакрытыхБаннеров) <> Тип("Массив") Тогда
		ИдентификаторыЗакрытыхБаннеров = Новый Массив;
	КонецЕсли;
	
	РеквизитыСообщения = "Наименование, Идентификатор, Критичность, ТипИнформации";
	ДанныеСообщений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ТаблицаСообщенийСервиса.ВыгрузитьКолонку("СсылкаНаДанные"), РеквизитыСообщения);
	
	ПодготавливаемыйБаннер.ДанныеБаннера.Вставить("ДанныеСобытия", Новый Структура(РеквизитыСообщения));
	
	ТаблицаДанныеБаннера = Новый ТаблицаЗначений;
	Для каждого ЭлементСобытия Из ПодготавливаемыйБаннер.ДанныеБаннера.ДанныеСобытия Цикл
		ТаблицаДанныеБаннера.Колонки.Добавить(ЭлементСобытия.Ключ);
	КонецЦикла;
	
	Для каждого ОписаниеСобытия Из ДанныеСообщений Цикл
		ДанныеСобытия = ОписаниеСобытия.Значение;
		Если ИдентификаторыЗакрытыхБаннеров.Найти(ДанныеСобытия.Идентификатор) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаДанныеБаннера.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСобытия);
	КонецЦикла;
	ТаблицаДанныеБаннера.Сортировать("Критичность Убыв, Идентификатор Возр");
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПоддержкаСервиса(), ТаблицаДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераПоддержкаСервиса(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат КритичноеСообщениеСервиса(ДанныеБаннера[0]);
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПоддержкаСервиса(Баннер, ДанныеБаннера)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Баннер.ТекстБаннера = ТекстБаннераПоддержкаСервиса(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера.ДанныеСобытия, ДанныеБаннера[0]);
	
	Баннер.ЦветФонаБаннер = ЦветФонаБаннераКритичноеСообщениеСервиса();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиКритичноеСообщениеСервиса();
	
КонецПроцедуры

Функция ТекстБаннераПоддержкаСервиса(НавигационнаяСсылка, ДанныеБаннера)
	
	ЧастиЗаголовка = Новый Массив;
	ЧастиЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Внимание!'"), Новый Шрифт(,, Истина)));
	ЧастиЗаголовка.Добавить(Символы.ПС);
	ЧастиЗаголовка.Добавить(НовыйЗаголовокПоддержкаСервиса(ДанныеБаннера.Наименование));
	ЧастиЗаголовка.Добавить(НовыйСтрокаБаннера(" "));
	ЧастиЗаголовка.Добавить(НовыйЗаголовокПоддержкаСервиса(НСтр("ru = 'Подробнее'"), НавигационнаяСсылка));
	ТекстЗаголовка = НовыйЗаголовок(ЧастиЗаголовка);
	
	Возврат ТекстЗаголовка;
	
КонецФункции

Функция КритичноеСообщениеСервиса(ТекущееСообщение) Экспорт
	
	Возврат ТекущееСообщение.Критичность > 5 Или Строка(ТекущееСообщение.ТипИнформации) = "Недоступность";
	
КонецФункции

Функция ЦветФонаБаннераКритичноеСообщениеСервиса()
	
	Возврат "ЦветФонаПредупреждения";
	
КонецФункции

Функция ИмяКартинкиКритичноеСообщениеСервиса()
	
	Возврат "КотКритичноеСообщениеСервиса";
	
КонецФункции

#КонецОбласти

#Область ПредодобренныйКредитВСбербанке

Функция НовыйБаннерПредодобренныйКредитВСбербанке()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисПредодобренныйКредитВСбербанке();
	Баннер.Идентификатор        = ИдентификаторБаннераПредодобренныйКредитВСбербанке();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипСбербанк();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннерПредодобренныйКредитВСбербанке";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераПредодобренныйКредитВСбербанке();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераПредодобренныйКредитВСбербанке();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	Баннер.Группа               = "Банки";
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераПредодобренныйКредитВСбербанке(ДанныеБаннеров)
	
	МассивИНН = ПредодобренныйКредитВСбербанкеПолучитьПредодобренныеИНН();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивИНН", МассивИНН);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.ИНН КАК ИНН
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ИНН В(&МассивИНН)";
	ДанныеБаннера = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИНН");
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПредодобренныйКредитВСбербанке(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераПредодобренныйКредитВСбербанке(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПредодобренныйКредитВСбербанке(Баннер, ДанныеБаннера, ТекущаяДата)
	
	НавигационнаяСсылка = Баннер.НавигационнаяСсылка + ?(ДанныеБаннера.Количество() = 1, ДанныеБаннера[0], "");
	
	Баннер.ТекстБаннера = ТекстБаннерПредодобренныйКредитВСбербанке(ДанныеБаннера, НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерПредодобренныйКредитВСбербанке(ДанныеБаннера, НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Оформите СМАРТ-кредит в Сбербанке'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнать подробности'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераПредодобренныйКредитВСбербанке()
	
	Возврат "ПредодобренныйКредитВСбербанке";
	
КонецФункции

Функция ДоступностьБаннераПредодобренныйКредитВСбербанке()
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ЭтоГлавныйБухгалтер = РольДоступна("ДобавлениеИзменениеНастроекБухгалтерии");
	
	Возврат (ЭтоПолноправныйПользователь ИЛИ ЭтоГлавныйБухгалтер)
	И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

#КонецОбласти

#Область НастройкаВидимостиСчетовУчета

Функция НовыйБаннерНастройкаВидимостиСчетовУчета(Размещение)
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяНастройкаВидимостиСчетовУчета();
	Баннер.Идентификатор        = ИдентификаторБаннераНастройкаВидимостиСчетовУчета();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераНастройкаВидимостиСчетовУчета();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераНастройкаВидимостиСчетовУчета();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиНастройкаВидимостиСчетовУчета();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ДанныеБаннера.Вставить("Размещение", Размещение);
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ДатаНачалаПоказаБаннера", ДатаНачалаПоказаБаннераНастройкаВидимостиСчетов());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераНастройкаВидимостиСчетовУчета(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераНастройкаВидимостиСчетовУчета(ДанныеБаннера, Баннер)
	
	ПользовательНеИзменялНастройкуСчетовУчета =
	ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ВРег("ПоказыватьСчетаУчетаВДокументах"), Неопределено) = Неопределено;
	
	БаннерПоказываетсяМеньшеМесяца = НачалоДня(ТекущаяДатаСеанса()) < ДобавитьМесяц(ДанныеБаннера.ДатаНачалаПоказаБаннера, 1);
	
	Возврат ПользовательНеИзменялНастройкуСчетовУчета И БаннерПоказываетсяМеньшеМесяца;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНастройкаВидимостиСчетовУчета(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераНастройкаВидимостиСчетовУчета(Баннер.НавигационнаяСсылка);
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераНастройкаВидимостиСчетовУчета(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Счета бухгалтерского учета могут показываться или скрываться'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Управляйте этим в'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='Персональных настройках'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераНастройкаВидимостиСчетовУчета()
	
	Возврат "НастройкаВидимостиСчетовУчета";
	
КонецФункции

Функция ДоступностьБаннераНастройкаВидимостиСчетовУчета()
	
	УстановитьПривилегированныйРежим(Истина);

	Возврат Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком()
		И УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии();
	
КонецФункции

Функция ИмяКартинкиНастройкаВидимостиСчетовУчета()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ДатаНачалаПоказаБаннераНастройкаВидимостиСчетов()
	
	ИмяНастройки = ВРег("ДатаНачалаПоказаБаннераНастройкаВидимостиСчетов");
	ДатаНачалаПоказаБаннера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяНастройки, Неопределено);
	Если ДатаНачалаПоказаБаннера = Неопределено Тогда
		ДатаНачалаПоказаБаннера = НачалоДня(ТекущаяДатаСеанса());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки, Неопределено, ДатаНачалаПоказаБаннера);
	КонецЕсли;
	
	Возврат ДатаНачалаПоказаБаннера;
	
КонецФункции

#КонецОбласти

#Область ЗаявкаНаКредит

Функция НовыйБаннерЗаявкаНаКредит()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисЗаявкаНаКредит();
	Баннер.Идентификатор        = ИдентификаторБаннераЗаявкаНаКредит();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипЗаявкаНаКредит();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннера1СКредит";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераЗаявкаНаКредит();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераЗаявкаНаКредит();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	Баннер.Группа               = "Банки";
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаЗаявкаНаКредит(Запрос, СтруктураБаннеров, НомераТаблиц, Организация)
	
	Идентификатор = ИдентификаторБаннераЗаявкаНаКредит();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПерсонализированныеДанные.Организация КАК Организация,
	|	ЕСТЬNULL(ПерсонализированныеДанные.Организация.ИностраннаяОрганизация, ЛОЖЬ) КАК ИностраннаяОрганизация,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК СреднемесячноеПоступлениеНаСчет,
	|	ЕСТЬNULL(ЗаявкаНаКредит.Ссылка, ЗНАЧЕНИЕ(Документ.ЗаявкаНаКредит.ПустаяСсылка)) КАК ЗаявкаНаКредит
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
	|			ЗаявкаНаКредит.Ссылка КАК Ссылка
	|		ИЗ
	|			Документ.ЗаявкаНаКредит КАК ЗаявкаНаКредит
	|		ГДЕ
	|			ЗаявкаНаКредит.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ТекущаяДата, МЕСЯЦ, -6), ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|			И ЗаявкаНаКредит.СервисОбменаСБанками = ЗНАЧЕНИЕ(Перечисление.СервисыОбменаСБанками.ЗаявкиНаКредит)) КАК ЗаявкаНаКредит
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ПерсонализированныеДанные.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ЗаявкаНаКредит)";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ОператорЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	ОператорВложенногоЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Источники[1].Источник.Запрос.Операторы[0];
	Если ЗначениеЗаполнено(Организация) Тогда
		ОператорЗапроса.Отбор.Добавить("ПерсонализированныеДанные.Организация = &Организация");
		ОператорВложенногоЗапроса.Отбор.Добавить("ЗаявкаНаКредит.Организация = &Организация");
	Иначе
		ОператорЗапроса.Отбор.Добавить("ПерсонализированныеДанные.Организация В(&СписокОрганизаций)");
		ОператорВложенногоЗапроса.Отбор.Добавить("ЗаявкаНаКредит.Организация В(&СписокОрганизаций)");
	КонецЕсли;
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераЗаявкаНаКредит(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого Данные Из ДанныеБаннера Цикл
		
		Если НЕ Данные.ЗаявкаНаКредит.Пустая() Тогда
			// Создана хотя бы одна заявка на кредит.
			Возврат Ложь;
		КонецЕсли;
		
		Если Данные.ИностраннаяОрганизация Тогда
			Продолжить;
		КонецЕсли;
		
		Если Данные.СреднемесячноеПоступлениеНаСчет >= СреднемесячноеПоступлениеНаСчетДляРекламыЗаявкаНаКредит() Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЗаявкаНаКредит(Баннер, ДанныеБаннера, ТекущаяДата)
	
	НавигационнаяСсылка = Баннер.НавигационнаяСсылка;
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ЭтоГлавныйБухгалтер = РольДоступна("ДобавлениеИзменениеНастроекБухгалтерии");
	Если НЕ (ЭтоПолноправныйПользователь ИЛИ ЭтоГлавныйБухгалтер) Тогда
		НавигационнаяСсылка = Сайт1СКредит();
	КонецЕсли;
	
	Баннер.ТекстБаннера = ТекстБаннерЗаявкаНаКредит(ДанныеБаннера, НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерЗаявкаНаКредит(ДанныеБаннера, НавигационнаяСсылка)
	
	ТаблицаВариантовТекста = ТаблицаВариантовТекста(НавигационнаяСсылка);
	
	Генератор = Новый ГенераторСлучайныхЧисел;
	ИндексСтрокиТаблицыВариантов = Генератор.СлучайноеЧисло(0, 5);
	
	СтрокаТаблицыВариантов = ТаблицаВариантовТекста[ИндексСтрокиТаблицыВариантов];
	
	Возврат НовыйТекстБаннера(СтрокаТаблицыВариантов.ТекстЗаголовка, СтрокаТаблицыВариантов.ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераЗаявкаНаКредит()
	
	Возврат "ЗаявкаНаКредит";
	
КонецФункции

Функция ДоступностьБаннераЗаявкаНаКредит()
	
	Возврат НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

Функция ТаблицаВариантовТекста(НавигационнаяСсылка)
	
	ТаблицаВариантовТекста = Новый ТаблицаЗначений;
	ТаблицаВариантовТекста.Колонки.Добавить("ТекстЗаголовка", Новый ОписаниеТипов("ФорматированнаяСтрока"));
	ТаблицаВариантовТекста.Колонки.Добавить("ТекстПодзаголовка", Новый ОписаниеТипов("ФорматированнаяСтрока"));
	
	Заголовок = НСтр("ru='Выбирайте выгодные кредиты и оформляйте заявки прямо в 1С'");
	Подзаголовок = НСтр("ru='Подробнее'");
	ДобавитьСтрокуТаблицыВариантов(ТаблицаВариантовТекста, Заголовок, Подзаголовок, НавигационнаяСсылка);
	Заголовок = НСтр("ru='Подберите выгодный кредит на развитие вашего бизнеса'");
	Подзаголовок = НСтр("ru='в сервисе 1С:Кредит'");
	ДобавитьСтрокуТаблицыВариантов(ТаблицаВариантовТекста, Заголовок, Подзаголовок, НавигационнаяСсылка);
	Заголовок = НСтр("ru='Быстрый сбор документов и подбор кредита в 1С:Кредит'");
	Подзаголовок = НСтр("ru='Оставить заявку'");
	ДобавитьСтрокуТаблицыВариантов(ТаблицаВариантовТекста, Заголовок, Подзаголовок, НавигационнаяСсылка);
	Заголовок = НСтр("ru='1С:Кредит - подготовка документов и заявка на кредит'");
	Подзаголовок = НСтр("ru='Из программы 1С'");
	ДобавитьСтрокуТаблицыВариантов(ТаблицаВариантовТекста, Заголовок, Подзаголовок, НавигационнаяСсылка);
	Заголовок = НСтр("ru='Нужен кредит? 1С:Кредит - поиск выгодных кредитов для бизнеса'");
	Подзаголовок = НСтр("ru='Оставить заявку'");
	ДобавитьСтрокуТаблицыВариантов(ТаблицаВариантовТекста, Заголовок, Подзаголовок, НавигационнаяСсылка);
	Заголовок = НСтр("ru='Нужен кредит для бизнеса? Соберите документы за 5 минут в 1С:Кредит'");
	Подзаголовок = НСтр("ru='Оставить заявку'");
	ДобавитьСтрокуТаблицыВариантов(ТаблицаВариантовТекста, Заголовок, Подзаголовок, НавигационнаяСсылка);

	Возврат ТаблицаВариантовТекста;
	
КонецФункции

Процедура ДобавитьСтрокуТаблицыВариантов(ТаблицаВариантовТекста, Заголовок, Подзаголовок, НавигационнаяСсылка)
	
	СтрокаВариантов = ТаблицаВариантовТекста.Добавить();
	СтрокаВариантов.ТекстЗаголовка = НовыйЗаголовок(Заголовок);
	СтрокаВариантов.ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(Подзаголовок, НавигационнаяСсылка));
		
КонецПроцедуры

#КонецОбласти

#Область ЗаявкаНаЛизинг

Функция НовыйБаннерЗаявкаНаЛизинг()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисЗаявкаНаЛизинг();
	Баннер.Идентификатор        = ИдентификаторБаннераЗаявкаНаЛизинг();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипЗаявкаНаЛизинг();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннера1СКредит";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераЗаявкаНаЛизинг();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераЗаявкаНаЛизинг();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	Баннер.Группа               = "Банки";
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаЗаявкаНаЛизинг(Запрос, СтруктураБаннеров, НомераТаблиц, Организация)
	
	Идентификатор = ИдентификаторБаннераЗаявкаНаЛизинг();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПерсонализированныеДанные.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ПерсонализированныеДанные.ЗначениеПоказателя <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОборотыПоЛизингу,
	|	ЕСТЬNULL(ЗаявкаНаЛизинг.Ссылка, ЗНАЧЕНИЕ(Документ.ЗаявкаНаКредит.ПустаяСсылка)) КАК ЗаявкаНаЛизинг,
	|	ЕСТЬNULL(ПерсонализированныеДанныеКредит.ЗначениеПоказателя,0) КАК СреднемесячноеПоступлениеНаСчет,
	|	ЕСТЬNULL(ПерсонализированныеДанныеЛизинг2.ЗначениеПоказателя, 0) КАК ВидДеятельностиИспользуетЛизинг
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
	|			ЗаявкаНаКредит.Ссылка КАК Ссылка
	|		ИЗ
	|			Документ.ЗаявкаНаКредит КАК ЗаявкаНаКредит
	|		ГДЕ
	|			ЗаявкаНаКредит.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ТекущаяДата, МЕСЯЦ, -6), ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|			И ЗаявкаНаКредит.СервисОбменаСБанками = ЗНАЧЕНИЕ(Перечисление.СервисыОбменаСБанками.ЗаявкиНаЛизинг)) КАК ЗаявкаНаЛизинг
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанныеКредит
	|		ПО ПерсонализированныеДанные.Организация = ПерсонализированныеДанныеКредит.Организация
	|			И (ПерсонализированныеДанныеКредит.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ЗаявкаНаКредит))
	|			И (ПерсонализированныеДанныеКредит.НомерСтрокиРаздела = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанныеЛизинг2
	|		ПО ПерсонализированныеДанные.Организация = ПерсонализированныеДанныеЛизинг2.Организация
	|			И (ПерсонализированныеДанныеЛизинг2.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ЗаявкаНаЛизинг))
	|			И (ПерсонализированныеДанныеЛизинг2.НомерСтрокиРаздела = 2)
	|ГДЕ
	|	ПерсонализированныеДанные.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ЗаявкаНаЛизинг)
	|	И ПерсонализированныеДанные.НомерСтрокиРаздела = 1";
	
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ОператорЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	ОператорВложенногоЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Источники[1].Источник.Запрос.Операторы[0];
	Если ЗначениеЗаполнено(Организация) Тогда
		ОператорЗапроса.Отбор.Добавить("ПерсонализированныеДанные.Организация = &Организация");
		ОператорВложенногоЗапроса.Отбор.Добавить("ЗаявкаНаКредит.Организация = &Организация");
	Иначе
		ОператорЗапроса.Отбор.Добавить("ПерсонализированныеДанные.Организация В(&СписокОрганизаций)");
		ОператорВложенногоЗапроса.Отбор.Добавить("ЗаявкаНаКредит.Организация В(&СписокОрганизаций)");
	КонецЕсли;
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераЗаявкаНаЛизинг(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого Данные Из ДанныеБаннера Цикл
		
		Если НЕ Данные.ЗаявкаНаЛизинг.Пустая() Тогда
			// Создана хотя бы одна заявка на лизинг.
			Возврат Ложь;
		КонецЕсли;
		
		Если Данные.СреднемесячноеПоступлениеНаСчет >= СреднемесячноеПоступлениеНаСчетДляРекламыЗаявкаНаКредит() Тогда
			
			Если Данные.ЕстьОборотыПоЛизингу Тогда
				Возврат Истина;
			КонецЕсли;
			
			Если Данные.ВидДеятельностиИспользуетЛизинг <> 0 Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЗаявкаНаЛизинг(Баннер, ДанныеБаннера, ТекущаяДата)
	
	НавигационнаяСсылка = Баннер.НавигационнаяСсылка;
	
	Баннер.ТекстБаннера = ТекстБаннерЗаявкаНаЛизинг(ДанныеБаннера, НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннерЗаявкаНаЛизинг(ДанныеБаннера, НавигационнаяСсылка)
	
	ЭлементыЗаголовка = Новый Массив;
	ЭлементыЗаголовка.Добавить(НСтр("ru='1С:Лизинг - выгодные условия лизинга с экспертом по лизингу'"));
	
	ТекстЗаголовка = НовыйЗаголовок(ЭлементыЗаголовка);
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Подробнее'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераЗаявкаНаЛизинг()
	
	Возврат "ЗаявкаНаЛизинг";
	
КонецФункции

Функция ДоступностьБаннераЗаявкаНаЛизинг()
	
	Возврат ПравоДоступа("Изменение", Метаданные.Документы.ЗаявкаНаКредит)
	И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

Функция ИмяКартинкиЛоготипЗаявкаНаЛизинг()
	
	Возврат "ЛоготипСервис1СЛизинг";
	
КонецФункции

#КонецОбласти

#Область ФинОтчетность

Функция НовыйБаннерФинОтчетность()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисФинОтчетность();
	Баннер.Идентификатор        = ИдентификаторБаннераФинОтчетность();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипФинОтчетность();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннера1СКредит";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераФинОтчетность();
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераФинОтчетность();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	Баннер.Группа               = "Банки";
	// Параметры для открытия формы списка при переходе по гиперссылке.
	Баннер.ДанныеБаннера.Вставить("Организация");
	Баннер.ДанныеБаннера.Вставить("Банк");
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаФинОтчетность(Запрос, СтруктураБаннеров, НомераТаблиц, Организация)
	
	ПроверяемыеИдентификаторы = Новый Массив;
	ПроверяемыеИдентификаторы.Добавить(ИдентификаторБаннераФинОтчетность());
	ПроверяемыеИдентификаторы.Добавить(ИдентификаторБаннераФинОтчетностьКраткий());
	
	Идентификатор = "";
	
	Для каждого ПроверяемыйИдентификатор Из ПроверяемыеИдентификаторы Цикл
		Если СтруктураБаннеров.Свойство(ПроверяемыйИдентификатор) Тогда
			Идентификатор = ПроверяемыйИдентификатор;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПерсонализированныеДанные.Организация КАК Организация,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК КоличествоБанков,
	|	ПерсонализированныеДанные.ДанныеРасшифровки КАК Банк,
	|	ВЫБОР
	|		КОГДА БанкиУниверсальногоОбмена.Ссылка ЕСТЬ NULL
	|			ТОГДА Банки.Наименование
	|		КОГДА БанкиУниверсальногоОбмена.НаименованиеСокращенное <> """"
	|			ТОГДА БанкиУниверсальногоОбмена.НаименованиеСокращенное
	|		ИНАЧЕ БанкиУниверсальногоОбмена.Наименование
	|	КОНЕЦ КАК НаименованиеБанка
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналСтатусовФинОтчетностиВБанки КАК ЖурналСтатусовФинОтчетностиВБанки
	|		ПО ПерсонализированныеДанные.Организация = ЖурналСтатусовФинОтчетностиВБанки.Организация
	|			И (ЖурналСтатусовФинОтчетностиВБанки.ДатаОтправки МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ТекущаяДата, МЕСЯЦ, -6), ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ))
	|			И ВЫБОР
	|				КОГДА ПерсонализированныеДанные.ЗначениеПоказателя = 1
	|					ТОГДА ПерсонализированныеДанные.ДанныеРасшифровки = ЖурналСтатусовФинОтчетностиВБанки.Банк
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Банки КАК Банки
	|		ПО ПерсонализированныеДанные.ДанныеРасшифровки = Банки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанкиУниверсальногоОбмена КАК БанкиУниверсальногоОбмена
	|		ПО ПерсонализированныеДанные.ДанныеРасшифровки = БанкиУниверсальногоОбмена.Ссылка
	|ГДЕ
	|	ПерсонализированныеДанные.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ФинОтчетность)
	|	И ПерсонализированныеДанные.ЗначениеПоказателя <> 0
	|	И ЖурналСтатусовФинОтчетностиВБанки.Предмет ЕСТЬ NULL";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ОператорЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	Если ЗначениеЗаполнено(Организация) Тогда
		ОператорЗапроса.Отбор.Добавить("ПерсонализированныеДанные.Организация = &Организация");
	Иначе
		ОператорЗапроса.Отбор.Добавить("ПерсонализированныеДанные.Организация В(&СписокОрганизаций)");
	КонецЕсли;
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераФинОтчетность(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() <> 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераФинОтчетность(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДанных = ДанныеБаннера[0];
	
	НавигационнаяСсылка = Баннер.НавигационнаяСсылка;
	Баннер.ТекстБаннера = ТекстБаннерФинОтчетность(СтрокаДанных, НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
	// Сохраним параметры для открытия формы списка.
	Баннер.ДанныеБаннера.Организация = СтрокаДанных.Организация;
	Баннер.ДанныеБаннера.Банк        = ?(СтрокаДанных.КоличествоБанков = 1, СтрокаДанных.Банк, Неопределено);
	
КонецПроцедуры

Функция ТекстБаннерФинОтчетность(СтрокаДанных, НавигационнаяСсылка)
	
	Если СтрокаДанных.КоличествоБанков = 1 Тогда
		ТекстЗаголовка = СтрШаблон(НСтр("ru='Регулярно сдаете отчеты в %1?'"), СтрокаДанных.НаименованиеБанка);
	Иначе
		ТекстЗаголовка = НСтр("ru='Регулярно сдаете отчеты в банки?'");
	КонецЕсли;
	
	ТекстЗаголовка = НовыйЗаголовок(ТекстЗаголовка);
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='1С:ФинОтчетность'"), НавигационнаяСсылка),
		НовыйСтрокаБаннера(НСтр("ru=' поможет подготовить и отправить необходимые документы'")));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераФинОтчетность()
	
	Возврат "ФинОтчетность";
	
КонецФункции

Функция ДоступностьБаннераФинОтчетность()
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ЖурналСтатусовФинОтчетностиВБанки)
	И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

#КонецОбласти

#Область ФинОтчетностьКраткий

Функция НовыйБаннерФинОтчетностьКраткий()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисФинОтчетность();
	Баннер.Идентификатор        = ИдентификаторБаннераФинОтчетностьКраткий();
	Баннер.ИмяКартинкаЛоготипа  = ""; // Без картинки
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннера1СКредит";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераФинОтчетностьКраткий();
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераФинОтчетность();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	Баннер.Группа               = "Банки";
	// Параметры для открытия формы списка при переходе по гиперссылке.
	Баннер.ДанныеБаннера.Вставить("Организация");
	Баннер.ДанныеБаннера.Вставить("Банк");
	
	Возврат Баннер;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераФинОтчетностьКраткий(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДанных = ДанныеБаннера[0];
	
	НавигационнаяСсылка = Баннер.НавигационнаяСсылка;
	Баннер.ТекстБаннера = ТекстБаннерФинОтчетностьКраткий(СтрокаДанных, НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
	// Сохраним параметры для открытия формы списка.
	Баннер.ДанныеБаннера.Организация = СтрокаДанных.Организация;
	Баннер.ДанныеБаннера.Банк        = ?(СтрокаДанных.КоличествоБанков = 1, СтрокаДанных.Банк, Неопределено);
	
КонецПроцедуры

Функция ТекстБаннерФинОтчетностьКраткий(СтрокаДанных, НавигационнаяСсылка)
	
	ТекстЗаголовка = Новый ФорматированнаяСтрока(
	НовыйСтрокаБаннера(НСтр("ru='1С:ФинОтчетность'"), НавигационнаяСсылка, ШрифтыСтиля.ОбычныйШрифтТекста),
	НовыйСтрокаБаннера(НСтр("ru=' - автоматическая подготовка и отправка отчетов в банки'"), , ШрифтыСтиля.ОбычныйШрифтТекста));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, "");
	
КонецФункции

Функция ИдентификаторБаннераФинОтчетностьКраткий()
	
	Возврат "ФинОтчетностьКраткий";
	
КонецФункции

#КонецОбласти

#Область ПереходиНаНКО

Функция НовыйБаннерПереходиНаНКО()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисПереходиНаНКО();
	Баннер.Идентификатор       = ИдентификаторБаннераПереходиНаНКО();
	Баннер.ДоступенПоПравам    = НЕ ОбщегоНазначения.РазделениеВключено() И НЕ ВариантыПриложений.ЭтоВерсияКОРП();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиПереходиНаНКО();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерПереходиНаНКО";
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераПереходиНаНКО();
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаПереходиНаНКО(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераПереходиНаНКО();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Организации.НаименованиеПолное КАК НаименованиеПолное,
	|	Организации.КодОКОПФ КАК КодОКОПФ
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|	И Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераПереходиНаНКО(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СписокКодовОКОПФНКО = Справочники.Организации.СписокКодовОКОПФНКО();
	КодОКОПФ = СокрЛП(ДанныеБаннера[0].КодОКОПФ);
	Если ЗначениеЗаполнено(КодОКОПФ) Тогда
		Возврат (СписокКодовОКОПФНКО.Найти(КодОКОПФ) <> Неопределено);
	КонецЕсли;
	
	// Если ОКОПФ не задан, то ищем по полному наименованию организации
	НаименованиеОрганизации = ДанныеБаннера[0].НаименованиеПолное;
	Если ПустаяСтрока(НаименованиеОрганизации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтрокаРегулярногоВыражения = СтрокаРегулярногоВыраженияДляПоискаНКО();
	Результат = СтрНайтиПоРегулярномуВыражению(НРег(НаименованиеОрганизации), СтрокаРегулярногоВыражения, , , , Истина);
	Если Результат.Длина > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПереходиНаНКО(Баннер)
	
	Баннер.ТекстБаннера = ТекстБаннераПереходиНаНКО(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераПереходиНаНКО(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Ведете учет НКО?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Переходите на'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='1С:Бухгалтерия некоммерческой организации'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераПереходиНаНКО()
	
	Возврат "ПереходиНаНКО";
	
КонецФункции

Функция СтрокаРегулярногоВыраженияДляПоискаНКО()
	
	Список = Новый Массив;
	Для Каждого Элемент Из Справочники.Организации.СписокОПФНКО() Цикл
		// Добавим любые символы слева и справа, чтобы отрабатывал поиск в больших группах
		Список.Добавить(СтрШаблон("(.*%1.*)", НРег(Элемент)));
	КонецЦикла;
	
	Возврат СтрСоединить(Список, "|");
	
КонецФункции

#КонецОбласти

#Область МоиЗадачи

Функция НовыйБаннерМоиЗадачи()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисМоиЗадачи();
	Баннер.Идентификатор        = ИдентификаторБаннераМоиЗадачи();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиМоиЗадачи();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераМоиЗадачи();
	Баннер.ЦветФонаБаннер       = "ЦветФонаБаннерМоиЗадачи";
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераМоиЗадачи();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаМоиЗадачи(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераМоиЗадачи();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИСТИНА КАК НужноВыводитьБаннер
	|ИЗ
	|	Константа.ИспользоватьБизнесПроцессыИЗадачи КАК ИспользоватьБизнесПроцессыИЗадачи
	|ГДЕ
	|	ИспользоватьБизнесПроцессыИЗадачи.Значение = ЛОЖЬ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераМоиЗадачи(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераМоиЗадачи(Баннер)
	
	Баннер.ТекстБаннера = ТекстБаннераМоиЗадачи(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераМоиЗадачи(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Запутались в своих задачах?'"));
	
	Если МоиЗадачи.ЕстьПравоВключатьМоиЗадачи() Тогда
		
		ТекстПодзаголовка = 
		НовыйСтрокаБаннера(НСтр("ru='Включите Мои задачи в настройках и ведите их в программе'"), НавигационнаяСсылка);
		
	Иначе
		
		ТекстПодзаголовка = 
		НовыйСтрокаБаннера(НСтр("ru='Попросите администратора включить Мои задачи в настройках'"));
		
	КонецЕсли;
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИмяКартинкиМоиЗадачи()
	
	Возврат "КотСПланшетом";
	
КонецФункции

Функция ИдентификаторБаннераМоиЗадачи()
	
	Возврат "МоиЗадачи";
	
КонецФункции

Функция ДоступностьБаннераМоиЗадачи()
	
	Возврат НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

#КонецОбласти

#Область АвтоматическийВыпускПродукции

Функция НовыйБаннерАвтоматическийВыпускПродукции()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисАвтоматическийВыпускПродукции();
	Баннер.Идентификатор       = ИдентификаторБаннераАвтоматическийВыпускПродукции();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераАвтоматическийВыпускПродукции();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиАвтоматическийВыпускПродукции();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерВыпускПродукции";
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераАвтоматическийВыпускПродукции();
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаАвтоматическийВыпускПродукции(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераАвтоматическийВыпускПродукции();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УчетнаяПолитика.Организация КАК Организация,
	|	МАКСИМУМ(УчетнаяПолитика.УчетВыпускаПродукцииПоДаннымПродаж) КАК УчетВыпускаПродукцииПоДаннымПродаж
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетнаяПолитика.Организация
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(УчетнаяПолитика.УчетВыпускаПродукцииПоДаннымПродаж) = ЛОЖЬ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераАвтоматическийВыпускПродукции(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераАвтоматическийВыпускПродукции(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераАвтоматическийВыпускПродукции(Баннер.НавигационнаяСсылка);
	Баннер.ДанныеБаннера.Вставить("Организация", ДанныеБаннера[0].Организация);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераАвтоматическийВыпускПродукции(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Упростите учет выпуска продукции!'"));
	ТекстПодзаголовка = НовыйСтрокаБаннера(НСтр("ru='Включите автоматический учет выпущенной продукции при продаже'"), НавигационнаяСсылка);
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераАвтоматическийВыпускПродукции()
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьВыпускПродукции")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.УчетнаяПолитика);
	
КонецФункции

Функция ИмяКартинкиАвтоматическийВыпускПродукции()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ИдентификаторБаннераАвтоматическийВыпускПродукции()
	
	Возврат "АвтоматическийВыпускПродукции";
	
КонецФункции

#КонецОбласти

#Область НастройкаФункциональности

Функция НовыйБаннерНастройкаФункциональности()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяНастройкаФункциональности();
	Баннер.Идентификатор        = ИдентификаторБаннераНастройкаФункциональности();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераНастройкаФункциональности();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераНастройкаФункциональности();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиНастройкаФункциональности();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераНастройкаФункциональности(ДанныеБаннеров)
	
	ПоказыватьБаннер = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПерсонализированныеПредложенияСервисов_НастройкаФункциональности",
		"ПоказыватьБаннер",
		Неопределено);
	
	Если ПоказыватьБаннер = Неопределено Тогда
		ПоказыватьБаннер = Ложь;
		Для каждого ФункциональнаяОпция Из ФункциональныеОпцииБаннераНастройкаФункциональности() Цикл
			Если НЕ ПолучитьФункциональнуюОпцию(ФункциональнаяОпция.Имя) Тогда
				ПоказыватьБаннер = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ПерсонализированныеПредложенияСервисов_НастройкаФункциональности",
			"ПоказыватьБаннер",
			ПоказыватьБаннер);
	КонецЕсли;
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ПоказыватьБаннер", ПоказыватьБаннер);
	Если ПоказыватьБаннер Тогда
		ДанныеБаннера.Вставить("ФункциональнаяОпция", ПолучитьФункциональнуюОпциюДляБаннераНастройкаФункциональности());
	КонецЕсли;
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераНастройкаФункциональности(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераНастройкаФункциональности(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.ПоказыватьБаннер;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНастройкаФункциональности(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераНастройкаФункциональности(Баннер.НавигационнаяСсылка,
		ДанныеБаннера.ФункциональнаяОпция);
	Баннер.ДанныеБаннера = ДанныеБаннера;
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераНастройкаФункциональности(НавигационнаяСсылка, ФункциональнаяОпция)
	
	ПредставлениеПриложения = НСтр("ru='1С:Бухгалтерию'");
	Если ОбщегоНазначенияБП.ЭтоВитринаБизнесСтарта() Или ОбщегоНазначенияБП.ЭтоБизнесСтарт() Тогда
		Если НЕ ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда
			ПредставлениеПриложения = НСтр("ru='1С:Нулевку'");
		Иначе
			ПредставлениеПриложения = НСтр("ru='1С:БизнесСтарт'");
		КонецЕсли;
	ИначеЕсли СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
		ПредставлениеПриложения = ОбщегоНазначенияБП.ПредставлениеПрограммыВинительныйПадеж();
	КонецЕсли;
	
	ПроцентИспользуемойФункциональности = ПолучитьПроцентИспользуемойФункциональностиДляБаннераНастройкаФункиональности();
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru = 'Вы используете %1 на %2%%'"),
		ПредставлениеПриложения, ПроцентИспользуемойФункциональности));
	
	Если ФункциональнаяОпция = Неопределено Тогда
		Если ПроцентИспользуемойФункциональности = 100 Тогда
			ТекстПодзаголовка = Новый ФорматированнаяСтрока(
				НовыйСтрокаБаннера(НСтр("ru='Включена'")),
				НовыйСтрокаБаннера(" "),
				НовыйСтрокаБаннера(НСтр("ru='полная функциональность'"), НавигационнаяСсылка));
		Иначе
			ТекстПодзаголовка = Новый ФорматированнаяСтрока(
				НовыйСтрокаБаннера(НСтр("ru='Настройте'"), НавигационнаяСсылка),
				НовыйСтрокаБаннера(" "),
				НовыйСтрокаБаннера(НСтр("ru='нужную функциональность'")));
		КонецЕсли;
	Иначе
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru='Настройте'"), НавигационнаяСсылка),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(СтрШаблон(НСтр("ru='нужную функциональность, например: %1'"),
			Нрег(ФункциональнаяОпция.Представление))));
	КонецЕсли;
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераНастройкаФункциональности()
	
	Возврат "НастройкаФункциональности";
	
КонецФункции

Функция ДоступностьБаннераНастройкаФункциональности()
	
	Возврат НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком()
		И ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности()
		И Пользователи.ЭтоПолноправныйПользователь(,, Ложь);
	
КонецФункции

Функция ИмяКартинкиНастройкаФункциональности()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ПолучитьПроцентИспользуемойФункциональностиДляБаннераНастройкаФункиональности()
	
	ОписаниеФункциональности = Обработки.ФункциональностьПрограммы.ОписаниеФункциональности();
	СписокВсейФункциональности = Обработки.ФункциональностьПрограммы.СписокВсейФункциональности(ОписаниеФункциональности);
	ВключеннаяФункциональность = Обработки.ФункциональностьПрограммы.ВключеннаяФункциональность(СписокВсейФункциональности);
	
	ОсновнаяФункциональность = 10; // Основная функциональность, которая всегда включена, принимается за 10%.
	НастраиваемаяФункциональность = 90; // Функциональность, которой может управлять пользователь, принимается за 90%.
	ДоляВключеннойФункциональности = ВключеннаяФункциональность.Количество() / СписокВсейФункциональности.Количество();
	Возврат ОсновнаяФункциональность + Окр(НастраиваемаяФункциональность * ДоляВключеннойФункциональности);
	
КонецФункции

Функция ПолучитьФункциональнуюОпциюДляБаннераНастройкаФункциональности()
	
	ВсеФункциональныеОпции = ФункциональныеОпцииБаннераНастройкаФункциональности();
	
	МассивФункциональныхОпций = Новый Массив;
	Для каждого Опция Из ВсеФункциональныеОпции Цикл
		МассивФункциональныхОпций.Добавить(Опция.Имя);
	КонецЦикла;
	
	ВключенныеФункциональныеОпции = Обработки.ФункциональностьПрограммы.ВключеннаяФункциональность(МассивФункциональныхОпций);
	
	ФункциональныеОпции = Новый Массив;
	Для каждого Опция Из ВсеФункциональныеОпции Цикл
		Если ВключенныеФункциональныеОпции.Найти(Опция.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ФункциональныеОпции.Добавить(Опция);
	КонецЦикла;
	
	Если ФункциональныеОпции.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИндексФункциональнойОпции = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПерсонализированныеПредложенияСервисов_НастройкаФункциональности",
		"ИндексФункциональнойОпции", 0);
	
	Если ТипЗнч(ИндексФункциональнойОпции) <> Тип("Число") Тогда
		ИндексФункциональнойОпции = 0;
	КонецЕсли;
	
	МаксимальныйИндекс = ФункциональныеОпции.Количество() - 1;
	
	ИндексФункциональнойОпции = Мин(ИндексФункциональнойОпции, МаксимальныйИндекс);
	
	ИндексСледующейФункциональнойОпции = ИндексФункциональнойОпции + 1;
	Если ИндексСледующейФункциональнойОпции > МаксимальныйИндекс Тогда
		ИндексСледующейФункциональнойОпции = 0;
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПерсонализированныеПредложенияСервисов_НастройкаФункциональности",
		"ИндексФункциональнойОпции",
		ИндексСледующейФункциональнойОпции);
	
	ФункциональнаяОпция = ФункциональныеОпции[ИндексФункциональнойОпции];
	
	Возврат ФункциональнаяОпция;
	
КонецФункции

Функция ФункциональныеОпцииБаннераНастройкаФункциональности()
	
	ФункциональныеОпции = Новый Массив;
	
	Опция = Новый Структура;
	Опция.Вставить("Имя", "ВестиУчетРасчетовСКонтрагентами");
	Опция.Вставить("Представление", НСтр("ru='Расчеты с покупателями и поставщиками'"));
	ФункциональныеОпции.Добавить(Опция);
	
	Опция = Новый Структура;
	Опция.Вставить("Имя", "ВестиУчетПоДоговорам");
	Опция.Вставить("Представление", НСтр("ru='учет по договорам'"));
	ФункциональныеОпции.Добавить(Опция);
	
	Возврат ФункциональныеОпции;
	
КонецФункции

#КонецОбласти

#Область АУСН

Функция НовыйБаннерАУСН()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисАУСН();
	Баннер.Идентификатор       = ИдентификаторБаннераАУСН();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераАУСН();
	Баннер.Исключительный      = Истина;
	Баннер.ИмяКартинкаЛоготипа = КартинкаЛоготипаБаннераАУСН(Неопределено);
	Баннер.ДоступенПоПравам    = ДоступностьБаннераАУСН();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераАУСН(ДанныеБаннеров, ПодготавливаемыйБаннер)
	
	Если Не УчетнаяПолитика.ПрименяетсяАУСН(ДанныеБаннеров.Организация, ДанныеБаннеров.ТекущаяДата) Тогда
		Возврат;
	КонецЕсли; 
	
	Если РегистрыСведений.СостоянияИнтеграцииАУСН.СервисПодключен() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПодключения = ИнтеграцияАУСН.НовыеНастройкиПодключения(ДанныеБаннеров.Организация, ИнтеграцияАУСН.ПодключенныеБанки());

	Если ЗначениеЗаполнено(НастройкиПодключения.Банк) Тогда
		ПодготавливаемыйБаннер.ДанныеБаннера.Вставить("ДанныеСобытия", НастройкиПодключения);
		ДанныеБаннеров.Вставить(ИдентификаторБаннераАУСН(), НастройкиПодключения);
	КонецЕсли;
	
КонецПроцедуры

Функция ВыполняютсяУсловияАУСН(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераАУСН(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера        = ТекстБаннераАУСН(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	Баннер.ЦветФонаБаннер      = ЦветФонаБаннераАУСН(ДанныеБаннера);
	Баннер.ИмяКартинкаЛоготипа = КартинкаЛоготипаБаннераАУСН(ДанныеБаннера);
	// После закрытия баннер нельзя исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Ложь);
	
КонецПроцедуры

Функция ТекстБаннераАУСН(НавигационнаяСсылка, ДанныеБаннера)
	
	Перем Банк;
	
	Если ДанныеБаннера <> Неопределено И ДанныеБаннера.Свойство("Банк", Банк) Тогда
		//@skip-check wrong-string-literal-content
		НаименованиеБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Наименование");
	Иначе
		НаименованиеБанка = НСтр("ru = 'Неизвестный банк'");
	КонецЕсли;
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Уточняйте банковские операции АУСН в программе'"));
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru = 'Подключите'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru = 'Сервис АУСН'"), НавигационнаяСсылка),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(СтрШаблон(НСтр("ru = 'и обменивайтесь с %1 автоматически'"), НаименованиеБанка)));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ЦветФонаБаннераАУСН(ДанныеБаннера)
	
	Перем СостояниеИнтеграции;
	
	Если ДанныеБаннера <> Неопределено
		И ДанныеБаннера.Свойство("СостояниеИнтеграции", СостояниеИнтеграции)
		И СостояниеИнтеграции = Перечисления.СостоянияИнтеграцииАУСН.Ошибка Тогда
		Возврат "ЦветБаннераАУСНКрасный";
	Иначе
		Возврат "ЦветБаннераАУСНЗеленый";
	КонецЕсли;
	
КонецФункции

Функция КартинкаЛоготипаБаннераАУСН(ДанныеБаннера = Неопределено)
	
	Перем СостояниеИнтеграции;
	
	Если ДанныеБаннера <> Неопределено
		И ДанныеБаннера.Свойство("СостояниеИнтеграции", СостояниеИнтеграции)
		И СостояниеИнтеграции = Перечисления.СостоянияИнтеграцииАУСН.Ошибка Тогда
		Возврат "ИнтеграцияАУСНБаннерОшибка";
	Иначе
		Возврат "ИнтеграцияАУСНБаннерПодключение";
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторБаннераАУСН()
	
	Возврат "АУСН";
	
КонецФункции

Функция ДоступностьБаннераАУСН()
	
	Возврат ПравоДоступа("Просмотр", Метаданные.Обработки.ПодключениеАУСН)
		И ПолучитьФункциональнуюОпцию("ИспользуетсяАУСН")
		И Не ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

Функция НовыйБаннерЛКАУСН()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисЛКАУСН();
	Баннер.Идентификатор       = ИдентификаторБаннераЛКАУСН();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераЛКАУСН();
	Баннер.Исключительный      = Истина;
	Баннер.ИмяКартинкаЛоготипа = "КотВажноеСообщение";
	Баннер.ДоступенПоПравам    = ДоступностьБаннераЛКАУСН();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераЛКАУСН(ДанныеБаннеров, ПодготавливаемыйБаннер)
	
	Если Не УчетнаяПолитика.ПрименяетсяАУСН(ДанныеБаннеров.Организация, ДанныеБаннеров.ТекущаяДата) Тогда
		Возврат;
	КонецЕсли; 
	
	Если РегистрыСведений.СостоянияИнтеграцииАУСН.СервисЛКАУСНПодключен() Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеИнтеграции = РегистрыСведений.СостоянияИнтеграцииАУСН.СостояниеИнтеграции(
		ДанныеБаннеров.Организация,
		Справочники.КлассификаторБанков.ПустаяСсылка());
	МассивДанных = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СостояниеИнтеграции);
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераЛКАУСН(), МассивДанных);
	
КонецПроцедуры

Функция ВыполняютсяУсловияЛКАУСН(ДанныеБаннера, Баннер)
	
	Если Не ИнтеграцияАУСНКлиентСервер.ОбменСЛКАУСНДоступен() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЛКАУСН(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера        = ТекстБаннераЛКАУСН(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	Баннер.ЦветФонаБаннер      = ЦветФонаБаннераЛКАУСН(ДанныеБаннера);
	Баннер.ИмяКартинкаЛоготипа = КартинкаЛоготипаБаннераЛКАУСН(ДанныеБаннера);

	// После закрытия баннер нельзя исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Ложь);
	
КонецПроцедуры

Функция ТекстБаннераЛКАУСН(НавигационнаяСсылка, ДанныеБаннера)
	
	ЕстьОшибки =  (ДанныеБаннера <> Неопределено
		И ДанныеБаннера.Количество() > 0 
		И ДанныеБаннера[0] = Перечисления.СостоянияИнтеграцииАУСН.Ошибка);
		
	Если ЕстьОшибки Тогда
		ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Есть ошибки при обмене с ФНС по АУСН'"));
	Иначе
		ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Получайте информацию о расчете налога АУСН в программе'"));
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru = 'Проверьте настройки'")),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru = 'Сервиса АУСН'"), НавигационнаяСсылка));

	Иначе
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru = 'Подключите'")),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru = 'Сервис АУСН'"), НавигационнаяСсылка),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru = 'и обменивайтесь с ФНС автоматически'")));
	КонецЕсли;
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ЦветФонаБаннераЛКАУСН(ДанныеБаннера)
	
	Если ДанныеБаннера <> Неопределено
		И ДанныеБаннера.Количество() > 0 
		И ДанныеБаннера[0] = Перечисления.СостоянияИнтеграцииАУСН.Ошибка Тогда
		Возврат "ЦветБаннераАУСНКрасный";
	Иначе
		Возврат "ЦветБаннераАУСНЗеленый";
	КонецЕсли;
	
КонецФункции

Функция КартинкаЛоготипаБаннераЛКАУСН(ДанныеБаннера = Неопределено)
	
	Если ДанныеБаннера <> Неопределено
		И ДанныеБаннера.Количество() > 0 
		И ДанныеБаннера[0] = Перечисления.СостоянияИнтеграцииАУСН.Ошибка Тогда
		Возврат "ИнтеграцияАУСНБаннерОшибка";
	Иначе
		Возврат "КотВажноеСообщение";
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторБаннераЛКАУСН()
	
	Возврат "ЛКАУСН";
	
КонецФункции

Функция ДоступностьБаннераЛКАУСН()
	
	Возврат ПравоДоступа("Использование", Метаданные.Обработки.ПодключениеАУСН)
		И ПолучитьФункциональнуюОпцию("ИспользуетсяАУСН")
		И Не ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

Функция НовыйБаннерБанкиСервисаАУСН()
	
	Баннер = НовыйБаннерДляСписка();
	Баннер.Идентификатор       = ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН();
	Баннер.НавигационнаяСсылка = ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН();
	Баннер.ИмяКартинкаЛоготипа = "Баннер_БанкиАУСН";
	Баннер.ДоступенПоПравам    = ДоступностьБаннераБанкиАУСН();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ЗависитОтОрганизации                 = Ложь;
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераБанкиАУСН(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ТекущаяДата = ДанныеБаннеров.ТекущаяДата;
	ИдентификаторБаннера = ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН();
	ДанныеБаннера.Вставить("СуммаГодовогоДохода", 0);
	ДанныеБаннера.Вставить("ЧисленностьРаботников", 0);
	ДанныеБаннера.Вставить("СтоимостьОсновныхСредств", 0);
	БаннерНеСвязанСОрганизацией = ДанныеБаннеров.Размещение = ИмяРазмещенияСравненияСистемНалогообложения();
	ДанныеБаннера.Вставить("ПоказыватьНезависимоОтОрганизации", БаннерНеСвязанСОрганизацией);
	
	Если БаннерНеСвязанСОрганизацией Тогда
		ДанныеБаннеров.Вставить(ИдентификаторБаннера, ДанныеБаннера);
		Возврат;
	КонецЕсли;
	
	ДанныеПоДоходу = Неопределено;
	Если ДанныеБаннеров.Свойство(ИдентификаторБаннера, ДанныеПоДоходу) И ЗначениеЗаполнено(ДанныеПоДоходу) Тогда
		ДанныеБаннера.СуммаГодовогоДохода = ДанныеПоДоходу[0].СуммаДоходаЗаГод;
	КонецЕсли;
	
	СуммаГодовогоДоходаСпецрежим = КонтрольПраваПримененияСпецрежима.СуммаГодовогоДохода(ДанныеБаннеров.Организация, ТекущаяДата);
	Если ЗначениеЗаполнено(СуммаГодовогоДоходаСпецрежим) Тогда
		ДанныеБаннера.СуммаГодовогоДохода = СуммаГодовогоДоходаСпецрежим;
	КонецЕсли;
	
	ДанныеПоСотрудникам = УчетЗарплаты.СреднесписочнаяЧисленность(ДанныеБаннеров.Организация, ТекущаяДата, ТекущаяДата);
	ДанныеБаннера.ЧисленностьРаботников = ДанныеПоСотрудникам.Численность;
	СтоимостьОсновныхСредств = КонтрольПраваПримененияСпецрежима.СтоимостьОсновныхСредств(
		ДанныеБаннеров.Организация, ТекущаяДата);
	Если ЗначениеЗаполнено(СтоимостьОсновныхСредств) Тогда
		ДанныеБаннера.СтоимостьОсновныхСредств = СтоимостьОсновныхСредств;
	КонецЕсли;
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннера, ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБанкиАУСН(ДанныеБаннера, Баннер)
	
	ВсеПроверкиПройдены = Истина;
	Если ДанныеБаннера.ПоказыватьНезависимоОтОрганизации Тогда
		Возврат ВсеПроверкиПройдены;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеБаннера.СуммаГодовогоДохода) Тогда
		ВсеПроверкиПройдены = ВсеПроверкиПройдены И ДанныеБаннера.СуммаГодовогоДохода <= УчетАУСН.ГраницаДоходов();
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеБаннера.ЧисленностьРаботников) Тогда
		ВсеПроверкиПройдены = ВсеПроверкиПройдены И ДанныеБаннера.ЧисленностьРаботников <= УчетАУСН.ГраницаЧисленностиРаботников();
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеБаннера.СтоимостьОсновныхСредств) Тогда
		ВсеПроверкиПройдены = ВсеПроверкиПройдены
			И ДанныеБаннера.СтоимостьОсновныхСредств <= УчетАУСН.ГраницаСтоимостиОсновныхСредств();
	КонецЕсли;
	
	Возврат ВсеПроверкиПройдены;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераБанкиАУСН(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераБанкиАУСН(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	
КонецПроцедуры

Функция ТекстБаннераБанкиАУСН(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Переходите на АУСН?'"));
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru = 'Не все банки являются партнером ФНС по проекту АУСН. '")),
		НовыйСтрокаБаннера(НСтр("ru = 'В каком банке открыть счет?'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераБанкиАУСН()
	
	Возврат ПравоДоступа("Редактирование", Метаданные.Документы.ЗаявкаНаОткрытиеСчета)
		И Не Справочники.НастройкиИнтеграцииСБанками.ИнтеграцияВИнформационнойБазеВключена();
	
КонецФункции

Функция ТекстЗапросаБанкиАУСН(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН();
	
	Если Не СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаДоходаЗаГод
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоГодаГраница, &КонецГодаГраница, , Счет В (&СчетаДохода), , Организация = &Организация, , ) КАК ХозрасчетныйОбороты";
	
	СчетаДохода = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаДохода, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Выручка));
	СчетаДохода.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеДоходы));
	Запрос.УстановитьПараметр("СчетаДохода", СчетаДохода);
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#Область ПереходНаЕНС

Функция НовыйБаннерПереходНаЕНС()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяПереходНаЕНС();
	Баннер.Идентификатор        = ИдентификаторБаннераПереходНаЕНС();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераПереходНаЕНС();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиПереходНаЕНС();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераПереходНаЕНС();
	Баннер.Исключительный       = Истина;
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Функция НовыйБаннерНовыйБаннерПереходНаЕНС_МониторНалогов()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяПереходНаЕНС_МониторНалогов();
	Баннер.Идентификатор        = ИдентификаторБаннераПереходНаЕНС_МониторНалогов();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераПереходНаЕНС_МониторНалогов();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиПереходНаЕНС();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераПереходНаЕНС();
	Баннер.Исключительный       = Истина;
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераПереходНаЕНС(ДанныеБаннеров)
	
	Если УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(ДанныеБаннеров.Организация, ДанныеБаннеров.ТекущаяДата) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("Организация",               ДанныеБаннеров.Организация);
	ДанныеБаннера.Вставить("СрокиПереходаНаЕНСИстекли", ДанныеБаннеров.ТекущаяДата >= ДатаОкончанияПереходаНаЕНС());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПереходНаЕНС(), ДанныеБаннера);
	
КонецПроцедуры

Процедура ПолучитьДанныеБаннераПереходНаЕНС_МониторНалогов(ДанныеБаннеров)
	
	Если УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(ДанныеБаннеров.Организация, ДанныеБаннеров.ТекущаяДата) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("Организация",               ДанныеБаннеров.Организация);
	ДанныеБаннера.Вставить("СрокиПереходаНаЕНСИстекли", ДанныеБаннеров.ТекущаяДата >= ДатаОкончанияПереходаНаЕНС());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераПереходНаЕНС_МониторНалогов(), ДанныеБаннера);
	
КонецПроцедуры

Функция ИдентификаторБаннераПереходНаЕНС()
	
	Возврат "ПереходНаЕНС";
	
КонецФункции

Функция ИдентификаторБаннераПереходНаЕНС_МониторНалогов()
	
	Возврат "ПереходНаЕНС_МониторНалогов";
	
КонецФункции

Функция ВыполняютсяУсловияБаннераПереходНаЕНС(ДанныеБаннера, Баннер)
	
	Возврат Не ДанныеБаннера.СрокиПереходаНаЕНСИстекли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПереходНаЕНС(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераПереходНаЕНС(ДанныеБаннера.Организация, ТекущаяДата);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	Баннер.ДанныеБаннера.Вставить("Организация", ДанныеБаннера.Организация);
	
КонецПроцедуры

Функция ТекстБаннераПереходНаЕНС(Организация, Период)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Единый налоговый счет и единый налоговый платеж с 2023 года'"));
	ШрифтТекста =  Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 10);
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера("Введите начальные остатки расчетов по налогам и взносам", , ШрифтТекста),
		НовыйСтрокаБаннера(" ", , ШрифтТекста),
		НовыйСтрокаБаннера(НСтр("ru='на едином налоговом счете'"), ГиперссылкаПомощникПереходаНаЕНС(), ШрифтТекста));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
КонецФункции

Функция ДоступностьБаннераПереходНаЕНС()
	
	ПравоДоступаКЗадачамБухгалтера = ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ЗадачиБухгалтера);
	
	Возврат ПравоДоступаКЗадачамБухгалтера
	И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

Функция ДатаОкончанияПереходаНаЕНС()
	
	Возврат '2023-02-01';
	
КонецФункции

Функция ГиперссылкаПомощникПереходаНаЕНС()
	
	Возврат "ПомощникПереходаНаЕНС";
	
КонецФункции

#КонецОбласти

#Область АвтоматическийУчетУбытковПослеРеализацииОС

Функция НовыйБаннерАвтоматическийУчетУбытковПослеРеализацииОС()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисАвтоматическийУчетУбытковПослеРеализацииОС();
	Баннер.Идентификатор       = ИдентификаторБаннераАвтоматическийУчетУбытковПослеРеализацииОС();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераАвтоматическийУчетУбытковПослеРеализацииОС();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиАвтоматическийУчетУбытковПослеРеализацииОС();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннер";
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераАвтоматическийУчетУбытковПослеРеализацииОС();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	
	Возврат Баннер;
	
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераАвтоматическийУчетУбытковПослеРеализацииОС(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераАвтоматическийУчетУбытковПослеРеализацииОС(Баннер.НавигационнаяСсылка);
	
КонецПроцедуры

Функция ИмяСервисАвтоматическийУчетУбытковПослеРеализацииОС()
	
	Возврат "АвтоматическийУчетУбытковПослеРеализацииОС";
	
КонецФункции

Функция ТекстБаннераАвтоматическийУчетУбытковПослеРеализацииОС(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Упростите учет убытков после реализации амортизируемых ОС!'"));
	ТекстПодзаголовка = НовыйСтрокаБаннера(НСтр("ru='Укажите дату последнего документа с введенной ручной операцией переноса убытка, чтобы последующие документы отражали убыток при проведении'"), НавигационнаяСсылка);
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераАвтоматическийУчетУбытковПослеРеализацииОС()
	
	Возврат "АвтоматическийУчетУбытковПослеРеализацииОС";
	
КонецФункции

Функция ДоступностьБаннераАвтоматическийУчетУбытковПослеРеализацииОС()
	
	Возврат ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств")
	И ПравоДоступа("Изменение", Метаданные.Константы.ДатаНачалаАвтоматическогоУчетаУбытковОтРеализацииОС);
	
КонецФункции

Функция ИмяКартинкиАвтоматическийУчетУбытковПослеРеализацииОС()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ТекстЗапросаАвтоматическийУчетУбытковПослеРеализацииОС(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераАвтоматическийУчетУбытковПослеРеализацииОС();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК НужноВыводитьБаннер
	|ИЗ
	|	Справочник.РасходыБудущихПериодов КАК РасходыБудущихПериодов
	|ГДЕ
	|	РасходыБудущихПериодов.ВидРБП = ЗНАЧЕНИЕ(Перечисление.ВидыРБП.УбыткиОтРеализацииАмортизируемогоИмущества)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераАвтоматическийУчетУбытковПослеРеализацииОС(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 
		ИЛИ ТекущаяДатаСеанса() > Константы.ДатаНачалаАвтоматическогоУчетаУбытковОтРеализацииОС.Получить() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область КадровыеДокументыЭТК

Функция НовыйБаннерКадровыеДокументыЭТК()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяКадровыеДокументыЭТК();
	Баннер.Идентификатор       = ИдентификаторБаннераКадровыеДокументыЭТК();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиКадровыеДокументыЭТК();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКадровыеДокументыЭТК();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераКадровыеДокументыЭТК();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Месяц;
	Баннер.ЗависитОтОрганизации = Ложь;
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераКадровыеДокументыЭТК()
	
	Возврат "КадровыеДокументыЭТК";
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКадровыеДокументыЭТК(ДанныеБаннера, Баннер)
	
	Возврат Истина;
	
КонецФункции

Процедура ПолучитьДанныеБаннераКадровыеДокументыЭТК(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ДатаНачалаПоказаБаннераКадровыеДокументыЭТК", ДатаНачалаПоказаБаннераКадровыеДокументыЭТК());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераКадровыеДокументыЭТК(), ДанныеБаннера);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеБаннераКадровыеДокументыЭТК(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКадровыеДокументыЭТК(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия",   ТекущаяДата);
	
КонецПроцедуры

Функция ТекстБаннераКадровыеДокументыЭТК(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Оформили прием или увольнение сотрудника?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Не забудьте отправить'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='отчет ЕФС-1 подр. 1.1'"), НавигационнаяСсылка),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='не позднее следующего дня после оформления приказа'"), ));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераКадровыеДокументыЭТК()
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ЭтоГлавныйБухгалтер = РольДоступна("ДобавлениеИзменениеНастроекБухгалтерии");
	
	Возврат (ЭтоПолноправныйПользователь ИЛИ ЭтоГлавныйБухгалтер)
	И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком");
	
КонецФункции

Функция ИмяКартинкиКадровыеДокументыЭТК()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ДатаНачалаПоказаБаннераКадровыеДокументыЭТК()
	
	ИмяНастройки = ВРег("ДатаНачалаПоказаБаннераКадровыеДокументыЭТК");
	ДатаНачалаПоказаБаннера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяНастройки, Неопределено);
	Если ДатаНачалаПоказаБаннера = Неопределено Тогда
		ДатаНачалаПоказаБаннера = НачалоДня(ТекущаяДатаСеанса());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки, Неопределено, ДатаНачалаПоказаБаннера);
	КонецЕсли;
	
	Возврат ДатаНачалаПоказаБаннера;
	
КонецФункции

#КонецОбласти

#Область КабинетСотрудника

Функция НовыйБаннерКабинетСотрудника(Размещение)
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяКабинетСотрудника();
	Баннер.Идентификатор       = ИдентификаторБаннераКабинетСотрудника();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиКабинетСотрудника();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКабинетСотрудника();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераКабинетСотрудника();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДанныеБаннера.Вставить("Размещение", Размещение);
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераКабинетСотрудника()
	
	Возврат "КабинетСотрудника";
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКабинетСотрудника(ДанныеБаннера, Баннер)
	
	Возврат Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника");
	
КонецФункции

Процедура ПолучитьДанныеБаннераКабинетСотрудника(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ДатаНачалаПоказаБаннераКабинетСотрудника", ДатаНачалаПоказаБаннераКабинетСотрудника());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераКабинетСотрудника(), ДанныеБаннера);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеБаннераКабинетСотрудника(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКабинетСотрудника(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия",   ТекущаяДата);
	
КонецПроцедуры

Функция ТекстБаннераКабинетСотрудника(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Оформляйте расчетные листки, 2-НДФЛ и другие справки электронно'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Подключите сервис'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='1С:Кабинет сотрудника'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераКабинетСотрудника()
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ЭтоГлавныйРасчетчик = РольДоступна("НастройкаСервисаКабинетСотрудника");
	
	Возврат (ЭтоПолноправныйПользователь ИЛИ ЭтоГлавныйРасчетчик)
	И ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров")
	И ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")
	И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком")
	И НЕ ПолучитьФункциональнуюОпцию("ИнтерфейсЭлектронныхТрудовыхКнижек");
	
КонецФункции

Функция ИмяКартинкиКабинетСотрудника()
	
	Возврат "КотСервисКабинетСотрудника";
	
КонецФункции

Функция ДатаНачалаПоказаБаннераКабинетСотрудника()
	
	ИмяНастройки = ВРег("ДатаНачалаПоказаБаннераКабинетСотрудника");
	ДатаНачалаПоказаБаннера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяНастройки, Неопределено);
	Если ДатаНачалаПоказаБаннера = Неопределено Тогда
		ДатаНачалаПоказаБаннера = НачалоДня(ТекущаяДатаСеанса());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки, Неопределено, ДатаНачалаПоказаБаннера);
	КонецЕсли;
	
	Возврат ДатаНачалаПоказаБаннера;
	
КонецФункции

#КонецОбласти

#Область ПечатьАртикула

Функция НовыйБаннерПечатьАртикулов(Размещение)
	
	Баннер = НовыйБаннерДляСписка();
	Баннер.Идентификатор = ИдентификаторБаннераПечатьАртикулов();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераПечатьАртикулов();
	Баннер.ДоступенПоПравам = ДоступностьБаннераПечатьАртикулов();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиНастройкаФункциональности();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДанныеБаннера.Вставить("Размещение", Размещение);
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераПечатьАртикулов() Экспорт
	
	Возврат "ПечатьАртикулов";
	
КонецФункции

Функция ДоступностьБаннераПечатьАртикулов()
	
	Возврат ПравоДоступа("Редактирование", Метаданные.Константы.ДополнительнаяКолонкаПечатныхФормДокументов);
	
КонецФункции

Функция ПоказательНастройкиПечатиАртикулов() Экспорт
	
	ЗначениеПоказателя = 0;
	НастройкаПечатиАртикулов = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	ВключенаНастройкаПоУмолчанию =
	НастройкаПечатиАртикулов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить;
	Если ВключенаНастройкаПоУмолчанию Тогда
		ЗначениеПоказателя = 1;
	КонецЕсли;
	
	ЗакрытыеБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		НовыйЗакрытыеПользователемБаннеры());
		
	Если ЗакрытыеБаннеры.Свойство("БаннерыБезОрганизации")
		И ТипЗнч(ЗакрытыеБаннеры.БаннерыБезОрганизации) = Тип("Структура") Тогда
		Если ЗакрытыеБаннеры.БаннерыБезОрганизации.Свойство(ИдентификаторБаннераПечатьАртикулов()) Тогда
			ЗначениеПоказателя = 0;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.ЗначениеПоказателя = ЗначениеПоказателя;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ТекстЗапросаПечатьАртикулов(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераПечатьАртикулов();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.НастройкаПечатиАртикулов)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераПечатьАртикулов(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗначениеПоказателя = ДанныеБаннера[0].ЗначениеПоказателя;
	
	Возврат ЗначениеПоказателя > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераПечатьАртикулов(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

#КонецОбласти

#Область ПечатьДокументов

Функция НастройкиБаннеровПечатныхФормДокументов()
	
	НастройкиБаннеров = НовыеНастройкиБаннеровПечатныхФормДокументов();
	
	ИменаМакетов = МакетыСВозможностьюДополнительнойКолонкиПриПечати();
	
	Для Каждого ИмяМакета Из ИменаМакетов Цикл
		Настройка              = НастройкиБаннеров.Добавить();
		Настройка.ИмяМакета    = ИмяМакета;
		Настройка.Заголовок    = ТекстЗаголовкаДополнительнаяКолонкаАртикул();
		Настройка.Подзаголовок = ТекстПодзаголовкаДобавитьДополнительнуюКолонкуПриПечати();
		Настройка.Гиперссылка  = ТекстГиперссылкиПечатиАртикула();
	КонецЦикла;
	
	Макеты = МакетыСПостояннойДополнительнойКолонкойПриПечати();
	
	Для Каждого Макет Из Макеты Цикл
		Настройка = НастройкиБаннеров.Добавить();
		Настройка.ИмяМакета = Макет.Ключ;
		Настройка.Заголовок = ТекстЗаголовкаПечататьАртикул();
		МассивСтрокПодзаголовка = Новый Массив;
		МассивСтрокПодзаголовка.Добавить(СтрШаблон(НСтр("ru='В колонке ""%1"" можно выводить артикул.'"), Макет.Значение));
		МассивСтрокПодзаголовка.Добавить(Символы.ПС);
		МассивСтрокПодзаголовка.Добавить(НСтр("ru='Управляйте этим в настройке'"));
		Настройка.Подзаголовок = СтрСоединить(МассивСтрокПодзаголовка);
		Настройка.Гиперссылка = ТекстГиперссылкиПечатиАртикула();
	КонецЦикла;
	
	Возврат НастройкиБаннеров;
	
КонецФункции

Функция НовыеНастройкиБаннеровПечатныхФормДокументов()
	
	ОписаниеТипаСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(150);
	НастройкиБаннеров = Новый ТаблицаЗначений;
	НастройкиБаннеров.Колонки.Добавить("ИмяМакета", ОписаниеТипаСтрока);
	НастройкиБаннеров.Колонки.Добавить("Заголовок", ОписаниеТипаСтрока);
	НастройкиБаннеров.Колонки.Добавить("Подзаголовок", ОписаниеТипаСтрока);
	НастройкиБаннеров.Колонки.Добавить("Гиперссылка", ОписаниеТипаСтрока);
	
	Возврат НастройкиБаннеров;
	
КонецФункции

Процедура СоздатьРеквизитыИЭлементыФормыДляРазмещенияБаннера(Форма)
	
	РеквизитыФормыБаннер = Новый Массив;
	РеквизитыФормыБаннер.Добавить(Новый РеквизитФормы("Баннер", Новый ОписаниеТипов()));
	РеквизитыФормыБаннер.Добавить(Новый РеквизитФормы("ТекстБаннера", Новый ОписаниеТипов("ФорматированнаяСтрока")));
	Форма.ИзменитьРеквизиты(РеквизитыФормыБаннер);
	
	ГруппаБаннер = Форма.Элементы.Вставить("Баннер", Тип("ГруппаФормы"), , Форма.Элементы.Найти("ГруппаКоманднаяПанель"));
	ГруппаБаннер.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаБаннер.РастягиватьПоГоризонтали = Истина;
	ГруппаБаннер.Отображение = ОтображениеОбычнойГруппы.Нет;
	
	ГруппаБаннерСФоном = Форма.Элементы.Добавить("БаннерСФоном", Тип("ГруппаФормы"), ГруппаБаннер);
	ГруппаБаннерСФоном.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаБаннерСФоном.РастягиватьПоГоризонтали = Истина;
	ГруппаБаннерСФоном.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаБаннерСФоном.ОтображатьЗаголовок = Ложь;
	
	ГруппаОжидание = Форма.Элементы.Добавить("ГруппаБаннерОжидание", Тип("ГруппаФормы"), ГруппаБаннерСФоном);
	ГруппаОжидание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОжидание.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОжидание.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Право;
	ГруппаОжидание.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Центр;
	ГруппаОжидание.РастягиватьПоГоризонтали = Ложь;
	ГруппаОжидание.Ширина = 24;
	ГруппаОжидание.ОтображатьЗаголовок = Ложь;
	
	КартинкаОжидание = Форма.Элементы.Добавить("КартинкаОжидание", Тип("ДекорацияФормы"), ГруппаОжидание);
	КартинкаОжидание.Вид = ВидДекорацииФормы.Картинка;
	
	НадписьОжидание = Форма.Элементы.Добавить("НадписьОжидание", Тип("ДекорацияФормы"), ГруппаОжидание);
	НадписьОжидание.Вид = ВидДекорацииФормы.Надпись;
	
	ТекстБаннера = Форма.Элементы.Добавить("ТекстБаннера", Тип("ПолеФормы"), ГруппаБаннерСФоном);
	ТекстБаннера.Вид = ВидПоляФормы.ПолеНадписи;
	ТекстБаннера.ПутьКДанным = "ТекстБаннера";
	ТекстБаннера.Высота = 3;
	ТекстБаннера.РастягиватьПоГоризонтали = Истина;
	ТекстБаннера.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	ТекстБаннера.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
	ТекстБаннера.АвтоМаксимальнаяШирина = Ложь;
	ТекстБаннера.МаксимальнаяШирина = 0;
	ТекстБаннера.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ТекстБаннера.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_ОбработкаНавигационнойСсылки");
	
	КартинкаБаннера = Форма.Элементы.Добавить("КартинкаБаннера", Тип("ДекорацияФормы"), ГруппаБаннерСФоном);
	КартинкаБаннера.Вид = ВидДекорацииФормы.Картинка;
	КартинкаБаннера.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	КартинкаБаннера.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
	КартинкаБаннера.Ширина = 26;
	КартинкаБаннера.Высота = 3;
	
	ГруппаКнопкиБаннера = Форма.Элементы.Добавить("ГруппаКнопкиБаннера", Тип("ГруппаФормы"), ГруппаБаннерСФоном);
	ГруппаКнопкиБаннера.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаКнопкиБаннера.РастягиватьПоВертикали = Истина;
	ГруппаКнопкиБаннера.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	ГруппаКнопкиБаннера.ОтображатьЗаголовок = Ложь;
	
	КартинкаЗакрытьБаннер = Форма.Элементы.Добавить("КартинкаЗакрытьБаннер", Тип("ДекорацияФормы"), ГруппаКнопкиБаннера);
	КартинкаЗакрытьБаннер.Вид = ВидДекорацииФормы.Картинка;
	КартинкаЗакрытьБаннер.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	КартинкаЗакрытьБаннер.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Верх;
	КартинкаЗакрытьБаннер.Гиперссылка = Истина;
	КартинкаЗакрытьБаннер.Картинка = БиблиотекаКартинок.ПолеВводаОчистить;
	КартинкаЗакрытьБаннер.УстановитьДействие("Нажатие", "Подключаемый_ВыполнитьКоманду");
	
	ГруппаКнопкиПереключенияБаннера = Форма.Элементы.Добавить("ГруппаКнопкиПереключенияБаннера", Тип("ГруппаФормы"), ГруппаКнопкиБаннера);
	ГруппаКнопкиПереключенияБаннера.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаКнопкиПереключенияБаннера.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Низ;
	
	КартинкаПредыдущийБаннер = Форма.Элементы.Добавить("КартинкаПредыдущийБаннер", Тип("ДекорацияФормы"), ГруппаКнопкиПереключенияБаннера);
	КартинкаПредыдущийБаннер.Вид = ВидДекорацииФормы.Картинка;
	КартинкаПредыдущийБаннер.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Низ;
	КартинкаПредыдущийБаннер.Гиперссылка = Истина;
	КартинкаПредыдущийБаннер.Картинка = БиблиотекаКартинок.ПредыдущийБаннер;
	
	КартинкаСледующийБаннер = Форма.Элементы.Добавить("КартинкаСледующийБаннер", Тип("ДекорацияФормы"), ГруппаКнопкиПереключенияБаннера);
	КартинкаСледующийБаннер.Вид = ВидДекорацииФормы.Картинка;
	КартинкаСледующийБаннер.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Низ;
	КартинкаСледующийБаннер.Гиперссылка = Истина;
	КартинкаСледующийБаннер.Картинка = БиблиотекаКартинок.СледующийБаннер;
	
КонецПроцедуры

Функция МакетыСВозможностьюДополнительнойКолонкиПриПечати()
	
	ИменаМакетов = Новый Массив;
	
	ИменаМакетов.Добавить("СчетЗаказ");
	ИменаМакетов.Добавить("СчетПокупателю");
	ИменаМакетов.Добавить("Накладная");
	
	Возврат ИменаМакетов;
	
КонецФункции

Функция МакетыСПостояннойДополнительнойКолонкойПриПечати()
	
	Макеты = Новый Структура;
	
	Макеты.Вставить("УПДСтатус1",            НСтр("ru = 'код товара'"));
	Макеты.Вставить("УПДСтатус1_981",        НСтр("ru = 'код товара'"));
	Макеты.Вставить("УПДСтатус2",            НСтр("ru = 'код товара'"));
	Макеты.Вставить("УПДСтатус2_981",        НСтр("ru = 'код товара'"));
	Макеты.Вставить("УПДСтатус1_534",        НСтр("ru = 'код товара'"));
	Макеты.Вставить("УПДСтатус2_534",        НСтр("ru = 'код товара'"));
	Макеты.Вставить("УПДСтатус1_534Прослеж", НСтр("ru = 'код товара'"));
	Макеты.Вставить("УПДСтатус2_534Прослеж", НСтр("ru = 'код товара'"));
	Макеты.Вставить("ТОРГ12_БезУслуг",       НСтр("ru = 'код'"));
	Макеты.Вставить("ТОРГ12",                НСтр("ru = 'код'"));
	Макеты.Вставить("ТТН",                   НСтр("ru = 'Артикул или номер по прейскуранту'"));
	
	Возврат Макеты;
	
КонецФункции

Функция ТекстБаннераПечатьАртикулов(Настройка)
	
	ТекстЗаголовка = НовыйЗаголовок(Настройка.Заголовок);
	
	КоличествоСтрокПодзаголовка = СтрЧислоСтрок(Настройка.Подзаголовок);
	
	Если КоличествоСтрокПодзаголовка = 1 Тогда
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(Настройка.Подзаголовок),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(Настройка.Гиперссылка,
			УправлениеПечатьюБПКлиентСервер.ГиперссылкаФормыНастройкиПечатиАртикулов()));
		
		Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	Иначе
		ПустаяСтрока = Новый ФорматированнаяСтрока(" ", Новый Шрифт(, 2));
		
		СтрокиПодзаголовка = СтрРазделить(Настройка.Подзаголовок, Символы.ПС);
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(СтрокиПодзаголовка[0]),
			Символы.ПС, ПустаяСтрока, Символы.ПС,
			НовыйСтрокаБаннера(СтрокиПодзаголовка[1]),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(Настройка.Гиперссылка,
			УправлениеПечатьюБПКлиентСервер.ГиперссылкаФормыНастройкиПечатиАртикулов()));
		
		Возврат Новый ФорматированнаяСтрока(
			ТекстЗаголовка, Символы.ПС, ПустаяСтрока, Символы.ПС, ТекстПодзаголовка);
	КонецЕсли;
	
КонецФункции

Функция ТекстЗаголовкаДополнительнаяКолонкаАртикул()
	
	Возврат НСтр("ru = 'Хотите выводить колонку ""Артикул""?'");
	
КонецФункции

Функция ТекстЗаголовкаПечататьАртикул()
	
	Возврат НСтр("ru = 'Хотите печатать артикул?'");
	
КонецФункции

Функция ТекстГиперссылкиПечатиАртикула()
	
	Возврат НСтр("ru='Печати артикулов'");
	
КонецФункции

Функция ТекстПодзаголовкаДобавитьДополнительнуюКолонкуПриПечати()
	
	Возврат НСтр("ru='Можно добавить колонку с артикулом или кодом в настройке'");
	
КонецФункции

#КонецОбласти

#Область НастройкаЭлектронныхЧеков

Функция НовыйБаннерНастройкаЭлектронныхЧеков()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяНастройкаЭлектронныхЧеков();
	Баннер.Идентификатор       = ИдентификаторБаннераНастройкаЭлектронныхЧеков();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиНастройкаЭлектронныхЧеков();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераНастройкаЭлектронныхЧеков();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераНастройкаЭлектронныхЧеков();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	
	Возврат Баннер;
	
КонецФункции

Функция ИмяНастройкаЭлектронныхЧеков()
	
	Возврат "НастройкаЭлектронныхЧеков";
	
КонецФункции

Функция ИдентификаторБаннераНастройкаЭлектронныхЧеков()
	
	Возврат "НастройкаЭлектронныхЧеков";
	
КонецФункции

Процедура ПолучитьДанныеБаннераНастройкаЭлектронныхЧеков(ДанныеБаннеров, ПодготавливаемыйБаннер)
	Перем РабочееМесто;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОборудованиеПоОрганизациям.Оборудование КАК Оборудование
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|ГДЕ
	|	ОборудованиеПоОрганизациям.Оборудование В(&СписокОборудования)
	|	И (ОборудованиеПоОрганизациям.ОтправлятьEMail
	|				ИЛИ ОборудованиеПоОрганизациям.ОтправлятьSMS)";
	
	ТребуетсяНастройкаККТ = Ложь;
	Если ДанныеБаннеров.Свойство("РабочееМесто", РабочееМесто) Тогда
		
		СписокОборудованияОтбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
		СписокОборудованияОтбор.ТипыПО.ККТ   = Истина;
		СписокОборудованияОтбор.РабочееМесто = РабочееМесто;
		
		СписокОборудования = МенеджерОборудованияВызовСервера.СписокОборудования(СписокОборудованияОтбор);
		Запрос.УстановитьПараметр("СписокОборудования", ОбщегоНазначения.ВыгрузитьКолонку(СписокОборудования, "Ссылка"));
		
		ТребуетсяНастройкаККТ = СписокОборудования.Количество() > 0 И Запрос.Выполнить().Пустой();
		
	КонецЕсли; 
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ТребуетсяНастройкаККТ", ТребуетсяНастройкаККТ); 

	ДанныеБаннеров.Вставить(ИдентификаторБаннераНастройкаЭлектронныхЧеков(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераНастройкаЭлектронныхЧеков(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.ТребуетсяНастройкаККТ;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНастройкаЭлектронныхЧеков(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераНастройкаЭлектронныхЧеков(Баннер.НавигационнаяСсылка);
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераНастройкаЭлектронныхЧеков(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Электронный чек вместо бумажного'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru = 'Узнайте'"), НавигационнаяСсылка),
		НовыйСтрокаБаннера(НСтр("ru=' как настроить отправку электронных чеков в SMS или по e-mail'")));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераНастройкаЭлектронныхЧеков()
	
	Возврат Не ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком")
	И ПолучитьФункциональнуюОпцию("ИспользоватьФискальныйРегистратор");
	
КонецФункции

Функция ИмяКартинкиНастройкаЭлектронныхЧеков()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

#КонецОбласти

#Область ЭТрН

Функция НовыйБаннерЭТрН()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяЭТрН();
	Баннер.Идентификатор       = ИдентификаторБаннераЭТрН();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЭТрН();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераЭТрН();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераЭТрН();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераЭТрН()
	
	Возврат "ЭТрН";
	
КонецФункции

Процедура ПолучитьДанныеБаннераЭТрН(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ДатаНачалаПоказаБаннера", ДатаНачалаПоказаБаннераЭТрН());
	ДанныеБаннера.Вставить("ЕстьОснованияЭТрН",       ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") И ЕстьОснованияЭТрН());
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераЭТрН(), ДанныеБаннера);
	
КонецПроцедуры

Функция ЕстьОснованияЭТрН()
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|		ПО РеализацияТоваровУслуг.Организация = УчетныеЗаписиЭДО.Организация
	|ГДЕ
	|	РеализацияТоваровУслуг.Перевозчик <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И НЕ УчетныеЗаписиЭДО.ИдентификаторЭДО ЕСТЬ NULL
	|	И РеализацияТоваровУслуг.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭлектроннаяТранспортнаяНакладная.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭлектроннаяТранспортнаяНакладная КАК ЭлектроннаяТранспортнаяНакладная");
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Возврат НЕ Результаты[0].Пустой() И Результаты[1].Пустой();
КонецФункции

Функция ВыполняютсяУсловияБаннераЭТрН(ДанныеБаннера, Баннер)
	Возврат ДанныеБаннера.ЕстьОснованияЭТрН;
КонецФункции

Процедура ЗаполнитьДанныеБаннераЭТрН(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераЭТрН(Баннер.НавигационнаяСсылка);
	
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераЭТрН(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Электронные транспортные накладные'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru = 'С 01.09.2022 возможно использование электронных транспортных накладных наряду с бумажными.'")),
		Символы.ПС,
		НовыйСтрокаБаннера(НСтр("ru = 'Подробная информация'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераЭТрН()
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД");
	
КонецФункции

Функция ИмяКартинкиЭТрН()
	
	Возврат "КотДальнобойщик";
	
КонецФункции

Функция ДатаНачалаПоказаБаннераЭТрН()
	
	ИмяНастройки = ВРег("ДатаНачалаПоказаБаннераЭТрН");
	ДатаНачалаПоказаБаннера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяНастройки, Неопределено);
	Если ДатаНачалаПоказаБаннера = Неопределено Тогда
		ДатаНачалаПоказаБаннера = НачалоДня(ТекущаяДатаСеанса());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки, Неопределено, ДатаНачалаПоказаБаннера);
	КонецЕсли;
	
	Возврат ДатаНачалаПоказаБаннера;
	
КонецФункции

#КонецОбласти

#Область УведомлениеОВыкупеВайлдберриз

Функция НовыйБаннерУведомлениеОВыкупеВайлдберриз()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = "";
	Баннер.Идентификатор       = ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиУведомлениеОВыкупеМаркетплейс();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз();
	Баннер.ЗависитОтОрганизации = Истина;
	Баннер.ДоступенПоПравам    = ПравоДоступа(
	"Просмотр",
	Метаданные.Документы.РеализацияТоваровУслуг);
	
	Возврат Баннер;
	
КонецФункции

Функция ВыполняютсяУсловияБаннераУведомлениеОВыкупеВайлдберриз(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.Количество() > 0;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераУведомлениеОВыкупеВайлдберриз(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераУведомлениеОВыкупеВайлдберриз(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера);
	
КонецПроцедуры

Функция ТекстБаннераУведомлениеОВыкупеВайлдберриз(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Упростите работу с Wildberries!'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Узнайте, как отразить в учете выкуп товаров маркетплейсом'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз()
	
	Возврат "УведомлениеОВыкупеВайлдберриз";
	
КонецФункции

Функция ПоказательЕстьПродажиЧерезМаркетплейсВайлдберриз(Организация) Экспорт
	
	ЗначениеПоказателя = 0;
	
	Если ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров") Тогда
		
		КонтрагентВайлдберриз = ОбменСМаркетплейс.КонтрагентМаркетплейсаПоУмолчанию(Перечисления.ВидыМаркетплейсов.Wildberries);
		
		Если ЗначениеЗаполнено(КонтрагентВайлдберриз) Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	1 КАК Поле1
			|ИЗ
			|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
			|ГДЕ
			|	РеализацияТоваровУслуг.Организация = &Организация
			|	И РеализацияТоваровУслуг.ВидОперации = &ВидОперации
			|	И РеализацияТоваровУслуг.Контрагент = &Контрагент";
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("Контрагент", КонтрагентВайлдберриз);
			Запрос.УстановитьПараметр("ВидОперации", Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров);
			
			Если Не Запрос.Выполнить().Пустой() Тогда
				ЗначениеПоказателя = 1;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаДанных = ТаблицаДанных();
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.ЗначениеПоказателя = ЗначениеПоказателя;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ТекстЗапросаУведомлениеОВыкупеВайлдберриз(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Организация = &Организация
	|	И ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.Маркетплейс)
	|	И ПерсонализированныеДанные.ЗначениеПоказателя > 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#Область ИнтеграцияСоSmartway

Функция НовыйБаннерИнтеграцияСоSmartway()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервисSmartway();
	Баннер.Идентификатор       = ИдентификаторБаннераИнтеграцияСоSmartway();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераИнтеграцияСоSmartway();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераИнтеграцияСоSmartway();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготипSmartway();
	Баннер.ДанныеБаннера.Вставить("Организация");
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	Баннер.ЗависитОтОрганизации = Истина;
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаИнтеграцияСоSmartway(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераИнтеграцияСоSmartway();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПерсонализированныеДанные.Организация КАК Организация,
	|	ПерсонализированныеДанные.ЗначениеПоказателя КАК ЗначениеПоказателя
	|ИЗ
	|	РегистрСведений.ПерсонализированныеДанные КАК ПерсонализированныеДанные
	|ГДЕ
	|	ПерсонализированныеДанные.Раздел = ЗНАЧЕНИЕ(Перечисление.РазделыПерсонализированныхДанных.ИнтеграцияСоSmartway)
	|	И ПерсонализированныеДанные.Организация = &Организация";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ВыполняютсяУсловияБаннераИнтеграцияСоSmartway(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтрокаДанныеБаннера = ДанныеБаннера[0];
	ЗначениеПоказателя = СтрокаДанныеБаннера.ЗначениеПоказателя;
	Организация        = СтрокаДанныеБаннера.Организация;
	
	Возврат ЗначениеПоказателя >= КоличествоКомандировокДляВыводаБаннераSmartway()
	И Не Обработки.ОтправкаЗаявокВSmartway.ЗаявкаВSmartwayОтправлена(Организация);
	
КонецФункции

Функция КоличествоКомандировокДляВыводаБаннераSmartway()
	
	Возврат 10;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераИнтеграцияСоSmartway(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераИнтеграцияСоSmartway(Баннер.НавигационнаяСсылка);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераИнтеграцияСоSmartway(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Отправляете сотрудников в командировки?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Автоматизируйте заполнение авансовых отчетов по командировкам'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераИнтеграцияСоSmartway()
	
	Возврат "ИнтеграцияСоSmartway";
	
КонецФункции

Функция ДоступностьБаннераИнтеграцияСоSmartway()
	
	Возврат ПравоДоступа("Просмотр", Метаданные.Обработки.ОтправкаЗаявокВSmartway)
		И Не (ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком")
		Или ЗначениеЗаполнено(Справочники.НастройкиЗагрузкиДанныхВнешнихСистем.ИспользуемаяНастройкаSmartway())
		Или ОбщегоНазначенияБПВызовСервераПовтИсп.ДанныеSmartway() = Неопределено);
	
КонецФункции

Функция ИмяКартинкиЛоготипSmartway()
	
	Возврат "ЛоготипSmartway";
	
КонецФункции

#КонецОбласти

#Область УведомленияЕНС

Функция НовыйБаннерУведомленияЕНС()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяУведомленияЕНС();
	Баннер.Идентификатор        = ИдентификаторБаннераУведомленияЕНС();
	Баннер.Исключительный       = Истина;
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераУведомленияЕНС();
	Баннер.ИмяКартинкаЛоготипа  = ИмяКартинкиЛоготипУведомленияЕНС();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераУведомленияЕНС();
	Баннер.ЗависитОтОрганизации = Ложь; 
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.День;
	
	Возврат Баннер;
	
КонецФункции

Функция ВыполняютсяУсловияБаннераУведомленияЕНС(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДатаСеанса = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	
	СрокПредупреждения = 5 * 24 * 60 * 60;
	СрокВыполненияДляПредупреждения = НачалоДня(ДатаСеанса) + СрокПредупреждения;
	
	Строка = ДанныеБаннера[0];
	Возврат Строка.Срок <= СрокВыполненияДляПредупреждения;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераУведомленияЕНС(Баннер, ДанныеБаннера, ТекущаяДата)

	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДанныхБаннера = ДанныеБаннера[0];
	
	Баннер.ТекстБаннера = ТекстБаннераУведомленияЕНС(СтрокаДанныхБаннера, Баннер.НавигационнаяСсылка);
	
	Если НачалоДня(СтрокаДанныхБаннера.Срок) < НачалоДня(ТекущаяДата) Тогда
		Баннер.ЦветФонаБаннер = "ЦветФонаПредупреждения";
	КонецЕсли;
	
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия", ТекущаяДата);
	
КонецПроцедуры

Функция ТекстБаннераУведомленияЕНС(СтрокаДанныхБаннера, НавигационнаяСсылка)
	
	ДатаСеанса = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	
	Если НачалоДня(СтрокаДанныхБаннера.Срок) >= НачалоДня(ДатаСеанса) Тогда
		СекундВСутках = 24 * 60 * 60;
		КоличествоДнейОсталось = (НачалоДня(СтрокаДанныхБаннера.Срок) - НачалоДня(ДатаСеанса)) / СекундВСутках;
		
		Если КоличествоДнейОсталось = 0 Тогда
			ОписаниеКоличестваДней = НСтр("ru='Сегодня последний день'");
		Иначе
			ОписаниеКоличестваДней = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru=';Завтра последний день;;Осталось %1 дня;Осталось %1 дней;'"),
				КоличествоДнейОсталось);
		КонецЕсли;
		
		ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='%1 для отправки уведомлений о налогах в ФНС'"), ОписаниеКоличестваДней));
		
		ШрифтТекста =  Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 10);
		
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru='Уведомление нужно'"), , ШрифтТекста),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(
				СтрШаблон(НСтр("ru='подать до %1'"), Формат(СтрокаДанныхБаннера.Срок, "ДФ='dd MMMM'" )) + " ", НавигационнаяСсылка, ШрифтТекста));
	Иначе
		ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Уведомления о налогах не отправлены в ФНС в срок!'"));
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru='Отправьте уведомления сейчас'"), НавигационнаяСсылка, ШрифтТекста));
	КонецЕсли;
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ИдентификаторБаннераУведомленияЕНС()

	Возврат "УведомленияЕНС";

КонецФункции

Функция ДоступностьБаннераУведомленияЕНС()
	
	Возврат ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ЗадачиБухгалтера);
	
КонецФункции

Функция ТекстЗапросаУведомленияЕНС(Запрос, СтруктураБаннеров, НомераТаблиц) Экспорт
	
	Идентификатор = ИдентификаторБаннераУведомленияЕНС();
	
	Если НЕ СтруктураБаннеров.Свойство(Идентификатор) Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СтатусСдано", ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой());
	Запрос.УстановитьПараметр("ПравилаПоНеобязательнымУведомлениям", ВыполнениеЗадачБухгалтера.ПравилаПоНеобязательнымУведомлениям());
	Запрос.УстановитьПараметр("ДатаНачалаЕНС", НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж());
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	НЕ ЗадачиБухгалтера.ВАрхиве
	|	И ЗадачиБухгалтера.ПериодСобытия >= &ДатаНачалаЕНС
	|	И ЗадачиБухгалтера.Срок МЕЖДУ ДОБАВИТЬКДАТЕ(&ТекущаяДата, ГОД, -1) И ДОБАВИТЬКДАТЕ(&ТекущаяДата, ДЕНЬ, 5)
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление)
	|	И ЗадачиБухгалтера.Статус <> &СтатусСдано
	|	И НЕ ЗадачиБухгалтера.Правило В (&ПравилаПоНеобязательнымУведомлениям)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадачиБухгалтера.Срок";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#Область УведомленияОЗадачахПоЭлектроннойПочте

Функция НовыйБаннерУведомленияОЗадачахПоЭлектроннойПочте()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяУведомленияОЗадачахПоЭлектроннойПочте();
	Баннер.Идентификатор       = ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиУведомленияОЗадачахПоЭлектроннойПочте();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераУведомленияОЗадачахПоЭлектроннойПочте();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = "Истина";
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте()
	
	Возврат "УведомленияОЗадачахПоЭлектроннойПочте";
	
КонецФункции

Процедура ПолучитьДанныеБаннераУведомленияОЗадачахПоЭлектроннойПочте(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ДатаНачалаПоказаБаннера", ДатаНачалаПоказаБаннераУведомленияОЗадачахПоЭлектроннойПочте());
	
	ОтправляютсяУведомленияПоЭлектроннойПочте =
		СписокЗадачУведомленияПоЭлектроннойПочте.ОтправлятьУведомления()
		И СписокЗадачУведомленияПоЭлектроннойПочте.КорректностьНастройки();
	
	ДанныеБаннера.Вставить("ОтправляютсяУведомленияПоЭлектроннойПочте", ОтправляютсяУведомленияПоЭлектроннойПочте);
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераУведомленияОЗадачахПоЭлектроннойПочте(ДанныеБаннера, Баннер)
	
	Возврат Не ДанныеБаннера.ОтправляютсяУведомленияПоЭлектроннойПочте;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераУведомленияОЗадачахПоЭлектроннойПочте(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераУведомленияОЗадачахПоЭлектроннойПочте(Баннер.НавигационнаяСсылка);
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераУведомленияОЗадачахПоЭлектроннойПочте(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Получайте уведомления о задачах по электронной почте'"));
	
	ТекстПодзаголовка = НовыйСтрокаБаннера(НСтр("ru='Настройка уведомлений'"),
		ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте());
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераУведомленияОЗадачахПоЭлектроннойПочте()
	
	Возврат Пользователи.ЭтоПолноправныйПользователь();
	
КонецФункции

Функция ИмяКартинкиУведомленияОЗадачахПоЭлектроннойПочте()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

Функция ДатаНачалаПоказаБаннераУведомленияОЗадачахПоЭлектроннойПочте()

	ИмяНастройки = ВРег("ДатаНачалаПоказаБаннераУведомленияОЗадачахПоЭлектроннойПочте");
	ДатаНачалаПоказаБаннера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяНастройки, Неопределено);
	Если ДатаНачалаПоказаБаннера = Неопределено Тогда
		ДатаНачалаПоказаБаннера = НачалоДня(ТекущаяДатаСеанса());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяНастройки, Неопределено, ДатаНачалаПоказаБаннера);
	КонецЕсли;
	
	Возврат ДатаНачалаПоказаБаннера;

КонецФункции

#КонецОбласти

#Область ЗагрузкаИзОФД

Функция НовыйБаннерЗагрузкаИзОФД()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяЗагрузкаИзОФД();
	Баннер.Идентификатор       = ИдентификаторБаннераЗагрузкаИзОФД();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЗагрузкаИзОФД();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераЗагрузкаИзОФД();
	Баннер.ДоступенПоПравам    = ДоступностьБаннераЗагрузкаИзОФД();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = ""; // Баннер не периодичный.
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	
	Возврат Баннер;
	
КонецФункции

Функция ИмяЗагрузкаИзОФД()
	
	Возврат "ЗагрузкаИзОФД";
	
КонецФункции

Функция ИдентификаторБаннераЗагрузкаИзОФД()
	
	Возврат "ЗагрузкаИзОФД";
	
КонецФункции

Процедура ПолучитьДанныеБаннераЗагрузкаИзОФД(ДанныеБаннеров, ПодготавливаемыйБаннер)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ОФДНастроен", ЗагрузкаИзОФД.ЕстьНастройкиКассОФД());

	ДанныеБаннеров.Вставить(ИдентификаторБаннераЗагрузкаИзОФД(), ДанныеБаннера);
	
КонецПроцедуры

Функция ВыполняютсяУсловияБаннераЗагрузкаИзОФД(ДанныеБаннера, Баннер)
	
	Возврат НЕ ДанныеБаннера.ОФДНастроен;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераЗагрузкаИзОФД(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераЗагрузкаИзОФД(Баннер.НавигационнаяСсылка);
	// После закрытия баннер можно исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераЗагрузкаИзОФД(НавигационнаяСсылка)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Упростите внесение данных о продажах'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru = 'Настройте'"), НавигационнаяСсылка),
		НовыйСтрокаБаннера(НСтр("ru=' загрузку данных о продажах от оператора ""Астрал.ОФД""'")));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ДоступностьБаннераЗагрузкаИзОФД()
	
	Возврат Не ПолучитьФункциональнуюОпцию("ИнтерфейсИнтеграцииСБанком") И ОФДСлужебный.НастройкаПодключенияДоступна();
	
КонецФункции

Функция ИмяКартинкиЗагрузкаИзОФД()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции

#КонецОбласти 

#Область СервисСверкиРасчетов

Функция НовыйБаннерСервисСверкиРасчетов()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисСверкиРасчетов();
	Баннер.Идентификатор        = ИдентификаторБаннераСервисСверкиРасчетов();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераСервисСверкиРасчетов();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	Баннер.ИмяКартинкаЛоготипа  = КартинкаЛоготипаБаннераСервисСверкиРасчетов();
	Баннер.ДоступенПоПравам     = ДоступностьБаннераСервисСверкиРасчетов();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	
	Возврат Баннер;
	
КонецФункции 

Функция ИдентификаторБаннераСервисСверкиРасчетов()
	
	Возврат "СервисСверкиРасчетов";
	
КонецФункции

Функция КартинкаЛоготипаБаннераСервисСверкиРасчетов()
	
	Возврат "КотСервисСверкиРасчетов";
	
КонецФункции

Функция ДоступностьБаннераСервисСверкиРасчетов()
	
	Возврат ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ИнтеграцияССервисомСверкиРасчетов)
		И Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисСверкиРасчетов");
	
КонецФункции
	
Процедура ЗаполнитьДанныеБаннераСервисСверкиРасчетов(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераСервисСверкиРасчетов(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	Баннер.ИмяКартинкаЛоготипа = КартинкаЛоготипаБаннераСервисСверкиРасчетов();
	// После закрытия баннер нельзя исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Ложь);
	
КонецПроцедуры

Функция ТекстБаннераСервисСверкиРасчетов(НавигационнаяСсылка, ДанныеБаннера) Экспорт
	
	КэшДанныхОРелевантныхКонтрагентах = СервисСверкиРасчетов.КэшДанныхОРелевантныхКонтрагентах();
	
	Если ЗначениеЗаполнено(КэшДанныхОРелевантныхКонтрагентах) Тогда
		КоличествоПодключенныхКонтрагентов = КэшДанныхОРелевантныхКонтрагентах.КоличествоПодключенныхКонтрагентов;
		ФорматнаяСтрока = "Л = ru_RU; ЧДЦ=0";
		Предмет = "контрагент,контрагента,контрагентов,м,,,,0";
		ПрописьЧисла = ЧислоПрописью(КоличествоПодключенныхКонтрагентов, ФорматнаяСтрока, Предмет);
		ИндексПредмета = СтрНайти(ПрописьЧисла, "контрагент");
		НадписьКонтрагент = Сред(ПрописьЧисла, ИндексПредмета, СтрДлина(ПрописьЧисла)- ИндексПредмета - 3);
		Если НадписьКонтрагент = "контрагент" Тогда
			ТекстЗаголовка = Новый ФорматированнаяСтрока(
				НовыйЗаголовок(СтрШаблон("%1 ваш", Строка(КоличествоПодключенныхКонтрагентов))),
				НовыйЗаголовок(" "),
				НовыйЗаголовок(НадписьКонтрагент, "СервисСверкиРасчетов"),
				НовыйЗаголовок(" "),
				НовыйЗаголовок("уже использует сервис 1С:Сверка 2.0"));
		Иначе
			ТекстЗаголовка = Новый ФорматированнаяСтрока(
				НовыйЗаголовок(СтрШаблон("%1 ваших", Строка(КоличествоПодключенныхКонтрагентов))),
				НовыйЗаголовок(" "),
				НовыйЗаголовок(НадписьКонтрагент, "СервисСверкиРасчетов"),
				НовыйЗаголовок(" "),
				НовыйЗаголовок("уже используют сервис 1С:Сверка 2.0"));
		КонецЕсли;
		
	ИначеЕсли ДанныеБаннера.ПрименяетсяОСНО Тогда
		ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Какие первичные документы и счета-фактуры введены в программу с ошибками?'"));
	Иначе
		ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Какие первичные документы введены в программу с ошибками?'"));
	КонецЕсли;

	
	Если ЗначениеЗаполнено(КэшДанныхОРелевантныхКонтрагентах) Тогда
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru='Включите сервис и убедитесь, что нет расхождений с контрагентами'")));
	Иначе
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			НовыйСтрокаБаннера(НСтр("ru='Включите сервис'")),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru='1С:Сверка 2.0'"), НавигационнаяСсылка),
			НовыйСтрокаБаннера(" "),
			НовыйСтрокаБаннера(НСтр("ru='и узнавайте о расхождениях заранее'")));
	КонецЕсли;
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Функция ВыполняютсяУсловияБаннераСервисСверкиРасчетов(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.ПоказыватьБаннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераСервисСверкиРасчетов(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("Организация",      Неопределено);
	ДанныеБаннера.Вставить("ПоказыватьБаннер", Истина);
	ДанныеБаннера.Вставить("ПрименяетсяОСНО", 
		ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО") Или ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП"));
	ДанныеБаннеров.Вставить(ИдентификаторБаннераСервисСверкиРасчетов(), ДанныеБаннера);
	
КонецПроцедуры

#КонецОбласти


#Область СервисСверкиРасчетовКонтрагентПодключенКСервису

Функция НовыйБаннерСервисСверкиРасчетовКонтрагентПодключенКСервису()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисСверкиРасчетов();
	Баннер.Идентификатор        = ИдентификаторБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису();
	Баннер.НавигационнаяСсылка  = ИдентификаторБаннераСервисСверкиРасчетов();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	Баннер.ДоступенПоПравам     = ДоступностьБаннераСервисСверкиРасчетов();
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИдентификаторыСобытий", Новый Массив);
	Баннер.ДанныеБаннера.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Баннер.ДанныеБаннера.Вставить("ПодключенКСервису", Ложь);
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису()
	
	Возврат "СервисСверкиРасчетовКонтрагентПодключенКСервису";
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису(Баннер, ДанныеБаннера)
	
	СтрокаДанных = ДанныеБаннера[0];
	Баннер.ТекстБаннера = ТекстБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису(
		Баннер.НавигационнаяСсылка, 
		ДанныеБаннера);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, СтрокаДанных);
	// После закрытия баннер нельзя исключать из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Ложь);
	
КонецПроцедуры

Функция ТекстБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису(НавигационнаяСсылка, ДанныеБаннера)
	
	Возврат Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Доступна автоматическая сверка с контрагентом'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(Строка(ДанныеБаннера[0].Контрагент)),
		НовыйСтрокаБаннера(", "),
		НовыйСтрокаБаннера(НСтр("ru='осталось только включить сервис'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='1С:Сверка 2.0'"), "СервисСверкиРасчетов"));
	
КонецФункции

Функция ТекстЗапросаКонтрагентПодключенКСервисуСверки(Запрос, СтруктураБаннеров, НомераТаблиц)
	
	Идентификатор = ИдентификаторБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису();
	
	НомераТаблиц.Вставить(Идентификатор, НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Организация КАК Контрагент,
	|	ВЫБОР
	|		КОГДА КонтрагентыБизнесСеть.Контрагент ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПодключенКСервису
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть
	|		ПО (Контрагенты.Ссылка = КонтрагентыБизнесСеть.Контрагент)
	|ГДЕ
	|	Контрагенты.Ссылка = &Организация";
	
	Возврат ТекстЗапроса  + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ВыполняютсяУсловияБаннераСервисСверкиРасчетовКонтрагентПодключенКСервису(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Строка = ДанныеБаннера[0];
	Если Строка.ПодключенКСервису = Истина Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область НоваяАвторизацияЯндекс

Функция НовыйБаннерНоваяАвторизацияЯндекс()
	
	Баннер                     	= НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          	= "";
	Баннер.Идентификатор       	= ИдентификаторБаннераНоваяАвторизацияЯндекс();
	Баннер.ИмяКартинкаЛоготипа 	= ИмяКартинкиНоваяАвторизацияЯндекс();
	Баннер.НавигационнаяСсылка 	= ИдентификаторБаннераНоваяАвторизацияЯндекс();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам 	= ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ФормаЗагрузкиИзМаркетплейс);
	
	Возврат Баннер;
	
КонецФункции

Функция ИдентификаторБаннераНоваяАвторизацияЯндекс()
	
	Возврат "НоваяАвторизацияЯндекс";
	
КонецФункции

Функция ВыполняютсяУсловияБаннераНоваяАвторизацияЯндекс(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.ЕстьЯндексМаркет;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераНоваяАвторизацияЯндекс(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераНоваяАвторизацияЯндекс(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера);
	
КонецПроцедуры

Функция ТекстБаннераНоваяАвторизацияЯндекс(НавигационнаяСсылка, ДанныеБаннера)
	
	Если ДанныеБаннера.Свойство("НастройкаИнтеграции") И ЗначениеЗаполнено(ДанныеБаннера.НастройкаИнтеграции) Тогда
		ИдентификаторНастройки			= ДанныеБаннера.НастройкаИнтеграции.УникальныйИдентификатор();
		НавигационнаяСсылкаНастройки 	= ?(ЗначениеЗаполнено(ИдентификаторНастройки),"ОткрытьНастройку_" + ИдентификаторНастройки, Неопределено); 
	Иначе
		НавигационнаяСсылкаНастройки 	= Неопределено;
	КонецЕсли;
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Новая авторизация на Яндекс.Маркете'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
	НовыйСтрокаБаннера(НСтр("ru = 'Получите новый токен и укажите его в настройках '")),
	НовыйСтрокаБаннера(НСтр("ru = 'Подключения Яндекс.Маркет.'"), НавигационнаяСсылкаНастройки),
	НовыйСтрокаБаннера(НСтр("ru = ' Подробнее в статье '")),
	НовыйСтрокаБаннера(НСтр("ru = 'Авторизация'"), НавигационнаяСсылка));
	
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Процедура ПолучитьДанныеБаннераНоваяАвторизацияЯндекс(ДанныеБаннеров)
	
	ЕстьЯндексМаркет = Ложь;
	НастройкаИнтеграции = Справочники.НастройкиИнтеграцииМаркетплейс.ПустаяСсылка();
	
	Если ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПродажиЧерезМаркетплейс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		               |	НастройкиИнтеграцииМаркетплейс.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
		               |ГДЕ
		               |	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			НастройкаИнтеграции = Выборка.Ссылка;
			Если Не ЗначениеЗаполнено(ИнтеграцияЯндексМаркет.ПолучитьТокенАПИ(НастройкаИнтеграции.Владелец)) Тогда
				ЕстьЯндексМаркет = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ЕстьЯндексМаркет", ЕстьЯндексМаркет);
	ДанныеБаннера.Вставить("НастройкаИнтеграции", НастройкаИнтеграции); 
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераНоваяАвторизацияЯндекс(), ДанныеБаннера);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсИнтеграцииСБанкомИнформированиеОПереносеКомандЕНС

Функция НовыйБаннерИнтерфейсИнтеграцииСБанкомИнформированиеОПереносеКомандЕНС(Размещение)
	
	Если Размещение <> ИмяРазмещенияОбщее() 
		Или ТекущаяДатаСеанса() > ДатаОкончанияПоказаБаннераИнтерфейсИнтеграцииСБанкомПереносКомандЕНС() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Баннер = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса = ИмяИнтерфейсИнтеграцииСБанкомПереносКомандЕНС();
	Баннер.Идентификатор = ИдентификаторИнтерфейсИнтеграцииСБанкомПереносКомандЕНС();
	Баннер.НавигационнаяСсылка = НавигационнаяСсылкаИнтерфейсИнтеграцииСБанкомПереносКомандЕНС();
	Баннер.ДоступенПоПравам = ДоступностьБаннераИнтерфейсИнтеграцииСБанкомПереносКомандЕНСВРазделНалоги();
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиИнтерфейсИнтеграцииСБанкомПереносКомандЕНС();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный = Истина;
	Баннер.Вставить("Единственный", Истина);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	Баннер.ТекстБаннера = ТекстБаннераИнтерфейсИнтеграцииСБанкомПереносКомандЕНС(Баннер.НавигационнаяСсылка);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
	Возврат Баннер;
	
КонецФункции

Функция ДатаОкончанияПоказаБаннераИнтерфейсИнтеграцииСБанкомПереносКомандЕНС()
	
	// Конец отчетной кампании по УСН за 2025 год.
	Возврат КонецДня(Дата(2026, 04, 27));
	
КонецФункции

Функция ТекстБаннераИнтерфейсИнтеграцииСБанкомПереносКомандЕНС(НавигационнаяСсылка)
	
	Возврат Новый ФорматированнаяСтрока(НовыйСтрокаБаннера(НСтр("ru='Всё для работы с ЕНС вы найдёте в разделе'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='Налоги'"), НавигационнаяСсылка));
	
КонецФункции

Функция ИдентификаторИнтерфейсИнтеграцииСБанкомПереносКомандЕНС()
	
	Возврат "ИнтерфейсИнтеграцииСБанкомПереносКомандЕНС";
	
КонецФункции

Функция ДоступностьБаннераИнтерфейсИнтеграцииСБанкомПереносКомандЕНСВРазделНалоги()
	
	Возврат ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

Функция НавигационнаяСсылкаИнтерфейсИнтеграцииСБанкомПереносКомандЕНС()
	
	Возврат "e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.НалогиБазовый";
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиентБизнесСтарт

Функция НовыйБаннерМобильныйКлиентБизнесСтарт()
	
	Баннер                      = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса           = ИмяСервисаМобильныйКлиентБизнесСтарт();
	Баннер.Идентификатор        = ИмяСервисаМобильныйКлиентБизнесСтарт();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.Исключительный       = Истина;
	Баннер.ИмяКартинкаЛоготипа  = КартинкаЛоготипаМобильныйКлиентБизнесСтарт();
	Баннер.ДоступенПоПравам     = Истина;
	Баннер.ОбстоятельстваЗакрытия.Периодичность = "";
	//Исключить баннер после закрытия пользователем
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Ложь);
	
	Возврат Баннер;
	
КонецФункции 

Функция ИмяСервисаМобильныйКлиентБизнесСтарт()
	
	Возврат "МобильныйКлиентБизнесСтарт";
	
КонецФункции

Функция ИдентификаторБаннераМобильныйКлиентБизнесСтарт()
	
	Возврат ИмяСервисаМобильныйКлиентБизнесСтарт();
	
КонецФункции

Функция КартинкаЛоготипаМобильныйКлиентБизнесСтарт()
	
	Возврат "СобытиеБаннераМобильноеПриложение";
	
КонецФункции

Функция ВыполняютсяУсловияБаннераМобильныйКлиентБизнесСтарт(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.ПоказыватьБаннер;
	
КонецФункции

Процедура ПолучитьДанныеБаннераМобильныйКлиентБизнесСтарт(ДанныеБаннеров)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("Организация",      Неопределено);
	ДанныеБаннера.Вставить("ПоказыватьБаннер", Истина);
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераМобильныйКлиентБизнесСтарт(), ДанныеБаннера);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеБаннераМобильныйКлиентБизнесСтарт(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера         = ТекстБаннераМобильныйКлиентБизнесСтарт();
	// После закрытия баннер исключим из списка баннеров.
	Баннер.ОбстоятельстваЗакрытия.Вставить("ИсключитьБаннер", Истина);
	
КонецПроцедуры

Функция ТекстБаннераМобильныйКлиентБизнесСтарт() Экспорт
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru='Часто работаете в дороге или на бегу?'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru='Установите на смартфон или планшет'")),
		НовыйСтрокаБаннера(" "),
		НовыйСтрокаБаннера(НСтр("ru='мобильное приложение 1С:БизнесСтарт'"),
			ИмяСервисаМобильныйКлиентБизнесСтарт()));
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

#КонецОбласти

#Область АвтозагрузкаОтчетовМаркетплейсов

Функция НовыйБаннерАвтозагрузкаОтчетовМаркетплейсов()
	
	Баннер                     	= НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          	= "";
	Баннер.Идентификатор       	= ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов();
	Баннер.ИмяКартинкаЛоготипа 	= ИмяКартинкиАвтозагрузкаОтчетовМаркетплейсов();
	Баннер.НавигационнаяСсылка 	= ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов();
	Баннер.ЗависитОтОрганизации = Ложь;
	Баннер.ДоступенПоПравам 	= ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ФормаЗагрузкиИзМаркетплейс);
	
	Возврат Баннер;
	
КонецФункции

Функция ИмяКартинкиАвтозагрузкаОтчетовМаркетплейсов()
	
	Возврат "КотВажноеСообщение";
	
КонецФункции 

Функция ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов()
	
	Возврат "АвтозагрузкаОтчетовМаркетплейсов";
	
КонецФункции

Функция ВыполняютсяУсловияБаннераАвтозагрузкаОтчетовМаркетплейсов(ДанныеБаннера, Баннер)
	
	Возврат ДанныеБаннера.НеобходимостьПодключения;
	
КонецФункции

Функция ТекстБаннераАвтозагрузкаОтчетовМаркетплейсов(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(НСтр("ru = 'Упростите работу с маркетплейсами'"));
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		НовыйСтрокаБаннера(НСтр("ru = 'Подключите автоматическую загрузку отчетов маркетплейсов'"), НавигационнаяСсылка));
	
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераАвтозагрузкаОтчетовМаркетплейсов(Баннер, ДанныеБаннера)
	
	Баннер.ТекстБаннера = ТекстБаннераАвтозагрузкаОтчетовМаркетплейсов(Баннер.НавигационнаяСсылка, ДанныеБаннера);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера);
	
КонецПроцедуры

Процедура ПолучитьДанныеБаннераАвтозагрузкаОтчетовМаркетплейсов(ДанныеБаннеров)
	
	НеобходимостьПодключения = Ложь;
	
	Если ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьПродажиЧерезМаркетплейс") Тогда
		
		НеобходимостьПодключения = Не ОбменСМаркетплейс.АвтозагрузкаОтчетовМаркетплейсовВключена();
		
	КонецЕсли;
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("НеобходимостьПодключения", НеобходимостьПодключения);
	
	ДанныеБаннеров.Вставить(ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов(), ДанныеБаннера);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти