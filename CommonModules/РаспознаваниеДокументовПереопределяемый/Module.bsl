#Область ПрограммныйИнтерфейс

// Параметры:
//  Логин - Строка 
//  Пароль - Строка
//
Процедура ПриВыполненииАвторизации(ПараметрыАвторизации) Экспорт
	
КонецПроцедуры

// Реализует обработку события изменения авторизации информационной базы
// в сервисе распознавания документов.
//
// Параметры:
//  ПараметрыАвторизации - Структура - структура с полями
//    * Состояние - Строка - возможные варианты "Ожидает", "Активирован";
//    * ТипАутентификации - Строка - возможные варианты "НеВыполнена", "ПоТикетуИТС", "ПоЛогинуПаролю";
//
Процедура ПриИзмененииДанныхАвторизации(ПараметрыАвторизации) Экспорт
	
	// БП
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не УчетКассовыхЧековПодотчетныхЛиц.ВключеныКассовыеЧекиПодотчетныхЛиц() Тогда
		УчетКассовыхЧековПодотчетныхЛиц.ВключитьКассовыеЧекиПодотчетныхЛиц();
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// Перед записью распознанного документа.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  РезультатОбратнойСвязи - Неопределено - Результат обратной связи
//
Процедура ПередЗаписьюРаспознанногоДокумента(
		ДокументОбъект,
		РезультатОбратнойСвязи = Неопределено
	) Экспорт
	
	// Общее
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.КассовыйЧек
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УКД Тогда
		
		ПараметрНомер = "";
		ПараметрДата = "";
		ПараметрСумма = "";
		
		ОсновныеРеквизиты = Новый Структура;
		Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
			ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
			ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
			
			Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
				ОсновныеРеквизиты.Вставить("Покупатель", "Контрагент");
				ОсновныеРеквизиты.Вставить("ПродавецОрганизация", "Организация");
			Иначе
				ОсновныеРеквизиты.Вставить("Продавец", "Контрагент");
				ОсновныеРеквизиты.Вставить("ПокупательОрганизация", "Организация");
			КонецЕсли;
		ИначеЕсли ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
			
			Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
				ОсновныеРеквизиты.Вставить("Покупатель", "Контрагент");
				ОсновныеРеквизиты.Вставить("Исполнитель", "Организация");
			Иначе
				ОсновныеРеквизиты.Вставить("Продавец", "Контрагент");
				ОсновныеРеквизиты.Вставить("ПокупательОрганизация", "Организация");
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ДокументОбъект.РеквизитыДокумента Цикл
			Если СтрокаТаблицы.ИмяРеквизита = "НомерДокумента" Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Значение) Тогда
					ПараметрНомер = СтрокаТаблицы.Значение;
				Иначе
					ПараметрНомер = СтрокаТаблицы.РаспознанныйТекст;
				КонецЕсли;
			ИначеЕсли СтрокаТаблицы.ИмяРеквизита = "ДатаДокумента" Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Значение) Тогда
					ПараметрДата = Формат(СтрокаТаблицы.Значение, "ДФ='dd.MM.yyyy'");
				Иначе
					ПараметрДата = СтрокаТаблицы.РаспознанныйТекст;
				КонецЕсли;
			ИначеЕсли СтрокаТаблицы.ИмяРеквизита = "ИтогоВсего" Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Значение) Тогда
					ПараметрСумма = СтрокаТаблицы.Значение;
				Иначе
					ПараметрСумма = СтрокаТаблицы.РаспознанныйТекст;
				КонецЕсли;
			ИначеЕсли ОсновныеРеквизиты.Свойство(СтрокаТаблицы.ИмяРеквизита) Тогда
				ДокументОбъект[ОсновныеРеквизиты[СтрокаТаблицы.ИмяРеквизита]] = СтрокаТаблицы.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Если РезультатОбратнойСвязи <> Неопределено Тогда
			РезультатОбратнойСвязи.НомерРаспознанногоДокумента = ДокументОбъект.Номер;
			РезультатОбратнойСвязи.ЭтоВходящийДокумент = (ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий);
			РезультатОбратнойСвязи.НомерДокумента = ПараметрНомер;
			РезультатОбратнойСвязи.ДатаДокумента = ПараметрДата;
			РезультатОбратнойСвязи.СуммаДокумента = ПараметрСумма;
			РезультатОбратнойСвязи.Контрагент = РаспознаваниеДокументов.УбратьОрганизационнуюФорму(ДокументОбъект.Контрагент);
			РезультатОбратнойСвязи.Организация = РаспознаваниеДокументов.УбратьОрганизационнуюФорму(ДокументОбъект.Организация);
		КонецЕсли;
		
	КонецЕсли;
	
	// Конец Общее
	
КонецПроцедуры

// При сопоставлении типов документов.
// 
// Параметры:
//  СоответствиеТиповДокументов - Соответствие Из Строка, ПеречислениеСсылка.ТипыДокументовРаспознаваниеДокументов
//
Процедура ПриСопоставленииТиповДокументов(СоответствиеТиповДокументов) Экспорт
	
КонецПроцедуры

// При сопоставлении реквизитов документа.
// 
// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовРаспознаваниеДокументов 
//  СопоставленныеРеквизиты - ТаблицаЗначений:
// * Адрес - Строка 
// * ИмяРеквизита - Строка 
// * ОписаниеТипа - ОписаниеТипов
// * НеОтображать - Булево 
// * НетНаПечатнойФорме - Булево
//
Процедура ПриСопоставленииРеквизитовДокумента(ТипДокумента, РеквизитыДокумента) Экспорт
	
	// БП
	
	Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД")
		Или ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.ТОРГ12")
		Или ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура")
		Или ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг") Тогда
		
		// Счета учета
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетУчета", "СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетДоходов", "СчетДоходов", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.Субконто", "Субконто", Новый ОписаниеТипов("Неопределено"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетРасходов", "СчетРасходов", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетУчетаНДСПоРеализации", "СчетУчетаНДСПоРеализации", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.ПереданныеСчетУчета", "ПереданныеСчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетУчетаНДС", "СчетУчетаНДС", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетРасчетов", "СчетРасчетов", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СпособУчетаНДС", "СпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.ОтражениеВУСН", "ОтражениеВУСН", Новый ОписаниеТипов("ПеречислениеСсылка.ОтражениеВУСН"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетЗатрат", "СчетЗатрат", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.ПодразделениеЗатрат", "ПодразделениеЗатрат", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.Субконто1", "Субконто1", Новый ОписаниеТипов("Неопределено"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.Субконто2", "Субконто2", Новый ОписаниеТипов("Неопределено"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.Субконто3", "Субконто3", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СчетЗатратНУ", "СчетЗатратНУ", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СубконтоНУ1", "СубконтоНУ1", Новый ОписаниеТипов("Неопределено"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СубконтоНУ2", "СубконтоНУ2", Новый ОписаниеТипов("Неопределено"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.СубконтоНУ3", "СубконтоНУ3", Новый ОписаниеТипов("Неопределено"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.Контрагент", "Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		РаспознаваниеДокументов.ДобавитьСоответствиеРеквизита(РеквизитыДокумента, "Таблица_1.Строка.ДоговорКонтрагента", "ДоговорКонтрагента", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// При приведении типа реквизита в объекте.
// 
// Параметры:
//  МетаданныеОбъекта - ОбъектМетаданных
//  ПереданноеЗначение - Произвольный
//  ТипЭтогоЗначения - Тип
//  ТипРеквизитаВОбъекте - Тип
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыСтавокНДС - При приведении типа реквизита в объекте
Функция ПриПриведенииТипаРеквизитаВОбъекте(
		МетаданныеОбъекта,
		ПереданноеЗначение,
		ТипЭтогоЗначения,
		ТипРеквизитаВОбъекте
	) Экспорт
	
	// БП
	
	Если ТипРеквизитаВОбъекте = Тип("ПеречислениеСсылка.ВидыСтавокНДС")
		И ТипЭтогоЗначения = Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
		
		Результат = Перечисления.ВидыСтавокНДС.ВидСтавки(ПереданноеЗначение);
	Иначе
		Результат = ПереданноеЗначение;
	КонецЕсли;
	Возврат Результат;
	
	// Конец БП
	
КонецФункции

// При определении юр физ лица по организационной форме.
// 
// Параметры:
//  Результат - Структура Из ПеречислениеСсылка.ЮридическоеФизическоеЛицо
//
Процедура ПриОпределенииЮрФизЛицаПоОрганизационнойФорме(Результат) Экспорт
	
	// Общее
	
	Значение = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	Результат.Вставить("публичное акционерное общество", Значение);
	Результат.Вставить("открытое акционерное общество", Значение);
	Результат.Вставить("закрытое акционерное общество", Значение);
	Результат.Вставить("общество с ограниченной ответственностью", Значение);
	Результат.Вставить("акционерное общество", Значение);
	Результат.Вставить("федеральное государственное унитарное предприятие", Значение);
	Результат.Вставить("пао", Значение);
	Результат.Вставить("оао", Значение);
	Результат.Вставить("зао", Значение);
	Результат.Вставить("ооо", Значение);
	Результат.Вставить("ао", Значение);
	Результат.Вставить("фгуп", Значение);
	
	Значение = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	Результат.Вставить("индивидуальный предприниматель", Значение);
	Результат.Вставить("ип", Значение);
	
	// Конец Общее
	
КонецПроцедуры

// В параметре Параметры.ТекстЗапросаКандидаты требуется выбрать:
// - Ссылка
// - ДополнительнаяСсылка
// - Все поля, добавленные в параметр ВесаРеквизитовПредварительнойОценки
// или оставить параметр пустым, тогда будет составлен запрос по-умолчанию к объекту метаданного.
//
Процедура ПриОпределенииПараметровНечеткогоПоиска(Параметры, МетаданныеОбъекта) Экспорт
	
	// БП
	Если МетаданныеОбъекта = Метаданные.Справочники.Номенклатура Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("НаименованиеПолное", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Артикул", "Артикул");
		
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.ВесаРеквизитов.Вставить("Артикул", 0.1);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("НаименованиеПолное");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Артикул");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("НаименованиеПолное", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Артикул", 1);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.Контрагенты
		Или МетаданныеОбъекта = Метаданные.Справочники.Организации Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("НаименованиеПолное", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("ИНН", "ИНН");
		Параметры.СоответствиеРеквизитов.Вставить("КПП", "КПП");
		
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.ВесаРеквизитов.Вставить("ИНН", 1);
		Параметры.ВесаРеквизитов.Вставить("КПП", 0.5);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("НаименованиеПолное");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.РеквизитыПолногоСоответствия.Добавить("ИНН");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("НаименованиеПолное", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("ИНН", 1);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("КПП", 0);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.Банки Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Код", "Код");
		Параметры.ВесаРеквизитов.Вставить("Код", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("Код");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Код", 1);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.КлассификаторЕдиницИзмерения Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Код", "Код");
		
		Параметры.ВесаРеквизитов.Вставить("Наименование", 0.75);
		Параметры.ВесаРеквизитов.Вставить("Код", 1);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Код");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.75);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Код", 1);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.ДоговорыКонтрагентов Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Номер", "Номер");
		Параметры.СоответствиеРеквизитов.Вставить("Дата", "Дата");
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		
		Параметры.ВесаРеквизитов.Вставить("Номер", 1);
		Параметры.ВесаРеквизитов.Вставить("Дата", 0.75);
		Параметры.ВесаРеквизитов.Вставить("Наименование", 0.75);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("Номер");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Дата");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Номер", 1);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Дата", 0.75);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.75);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.НомераГТД Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Код", "Код");
		Параметры.ВесаРеквизитов.Вставить("Код", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("Код");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Код", 1);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.БанковскиеСчета Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("НомерСчета", "НомерСчета");
		Параметры.ВесаРеквизитов.Вставить("НомерСчета", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("НомерСчета");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("НомерСчета", 1);
		
	Иначе
		
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 1);
		
	КонецЕсли;
	// Конец БП
	
КонецПроцедуры

// При заполнении распознанного документа.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ДокументСсылка - ДокументСсылка.РаспознанныйДокумент
//
Процедура ПриЗаполненииРаспознанногоДокумента(ДокументОбъект, ДокументСсылка) Экспорт
	
	// БП
	
	ПроверяемыеРеквизиты = Новый Структура;
	ПроверяемыеРеквизиты.Вставить("Исполнитель", Неопределено);
	ПроверяемыеРеквизиты.Вставить("ПродавецОрганизация", Неопределено);
	ПроверяемыеРеквизиты.Вставить("ПокупательОрганизация", Неопределено);
	ПроверяемыеРеквизиты.Вставить("СуммаВключаетНДС", Ложь);
	ПроверяемыеРеквизиты.Вставить("ТекстНДС", "");
	ПроверяемыеРеквизиты.Вставить("ИтогоСумма", 0);
	ПроверяемыеРеквизиты.Вставить("ИтогоСуммаНДС", 0);
	ПроверяемыеРеквизиты.Вставить("ИтогоВсего", 0);
	
	Для Каждого РеквизитШапки Из ДокументОбъект.РеквизитыДокумента Цикл
		// Если Грузоотправитель совпадает с полем Продавец или Грузополучатель - с полем Покупатель, 
		// тогда Значение нужно оставить пустым
		Если РеквизитШапки.ИмяРеквизита = "Грузоотправитель" Тогда
			НайденнаяСтрока = ДокументОбъект.РеквизитыДокумента.Найти("Продавец", "ИмяРеквизита");
			Если НайденнаяСтрока <> Неопределено И РеквизитШапки.Значение = НайденнаяСтрока.Значение Тогда
				РеквизитШапки.Значение = Справочники.Контрагенты.ПустаяСсылка();
			КонецЕсли;
		ИначеЕсли РеквизитШапки.ИмяРеквизита = "Грузополучатель" Тогда
			НайденнаяСтрока = ДокументОбъект.РеквизитыДокумента.Найти("Покупатель", "ИмяРеквизита");
			Если НайденнаяСтрока <> Неопределено И РеквизитШапки.Значение = НайденнаяСтрока.Значение Тогда
				РеквизитШапки.Значение = Справочники.Контрагенты.ПустаяСсылка();
			КонецЕсли;
		ИначеЕсли РеквизитШапки.ИмяРеквизита = "СуммаВключаетНДС" Тогда
			СтароеЗначение = РеквизитШапки.Значение;
			
			Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг 
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
				
				Если СтрНачинаетсяС(РеквизитШапки.РаспознанныйТекст, "В том числе НДС") Тогда
					РеквизитШапки.Значение = Истина;
				Иначе
					// "Без налога (НДС)"
					// "Сумма НДС"
					РеквизитШапки.Значение = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
			ПроверяемыеРеквизиты.Вставить("СуммаВключаетНДС", РеквизитШапки.Значение);
			ПроверяемыеРеквизиты.Вставить("ТекстНДС", РеквизитШапки.РаспознанныйТекст);
			ПроверяемыеРеквизиты.Вставить("ИзмениласьСуммаВключаетНДС", СтароеЗначение <> РеквизитШапки.Значение);
		ИначеЕсли ПроверяемыеРеквизиты.Свойство(РеквизитШапки.ИмяРеквизита) Тогда
			ПроверяемыеРеквизиты.Вставить(РеквизитШапки.ИмяРеквизита, РеквизитШапки.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
		
		Если ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПродавецОрганизация) И НЕ ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПокупательОрганизация) Тогда
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий;
		Иначе
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий;
		КонецЕсли;
	ИначеЕсли ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		Если ЗначениеЗаполнено(ПроверяемыеРеквизиты.Исполнитель) И НЕ ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПокупательОрганизация) Тогда
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий;
		Иначе
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий;
		КонецЕсли;
	КонецЕсли;
	
	// На основании направления можно определить Контрагента и Организацию, а по ним заполнить Договор
	// 
	ПараметрыВыбораДоговора = РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора(ДокументОбъект);
	ЕстьНезаполненныйПараметр = Ложь;
	Для Каждого ПараметрВыбора Из ПараметрыВыбораДоговора Цикл
		Если Не ЗначениеЗаполнено(ПараметрВыбора.Значение) Тогда
			// для массива тоже подходит. т.е. при условии ТипЗнч(ПараметрВыбора.Значение) = Тип("Массив")
			ЕстьНезаполненныйПараметр = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНезаполненныйПараметр Тогда
		// Используем заполнение без нечеткого поиска
		РаспознаваниеДокументовСлужебный.ЗаполнитьДоговорКонтрагента(ДокументОбъект);
	Иначе
		
		ОтборМетаданных = Новый Массив;
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"Владелец", "=", ПараметрыВыбораДоговора.Контрагент));
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"Организация", "=", ПараметрыВыбораДоговора.Организация));
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"ВидДоговора", "В", ПараметрыВыбораДоговора.ВидыДоговоров));
		
		ДанныеПоиска = Новый Структура;
		РеквизитыПоиска = Новый Структура; // Ключ - имя реквизита из РаспознанныйДокумент.РеквизитыДокумента, Значение - имя реквизита объекта метаданных
		РеквизитыПоиска.Вставить("НомерДоговора", "Номер");
		РеквизитыПоиска.Вставить("ДатаДоговора", "Дата");
		РеквизитыПоиска.Вставить("Договор", "Наименование");
		
		Для Каждого ИмяРеквизита Из РеквизитыПоиска Цикл
			Отбор = Новый Структура("ИмяРеквизита", ИмяРеквизита.Ключ);
			СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
			Если СтрокиПоиска.Количество() <> 0 Тогда
				ЗначениеПоиска = СтрокиПоиска[0].РаспознанныйТекст;
				//ЗначениеПоиска = СтрокиПоиска[0].Значение;
				//Если Не ЗначениеЗаполнено(ЗначениеПоиска) Тогда
				//	// Пустую дату и т.д. заменяем на пустую строку
				//	ЗначениеПоиска = "";
				//КонецЕсли;
			Иначе
				ЗначениеПоиска = Неопределено;
			КонецЕсли;
			
			ДанныеПоиска.Вставить(ИмяРеквизита.Значение, ЗначениеПоиска);
		КонецЦикла;
		
		Тип = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
		Кандидаты = РаспознаваниеДокументов.ПолучитьКандидатов(
			ДанныеПоиска,
			Тип,
			,
			ОтборМетаданных,
			ДокументОбъект.ИдентификаторРезультата
		);
		РаспознаваниеДокументовСлужебный.ДобавитьКандидатовВСписокВыбора(ДокументОбъект, ДокументСсылка, "Договор", Кандидаты);
		
		Отбор = Новый Структура("ИмяРеквизита", "Договор");
		СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
		Если СтрокиПоиска.Количество() <> 0 Тогда
			Реквизит = СтрокиПоиска[0];
			Если Реквизит.УверенностьНайденногоЗначения >= РаспознаваниеДокументовСлужебныйКлиентСервер.ГраницаПроблемныхЗначений()
				И НЕ РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(Реквизит.Значение))
				И НЕ РаспознаваниеДокументовКлиентСервер.РаспознанныйТекстСодержитПустоеЗначениеПоля(Реквизит.ИмяРеквизита, Реквизит.РаспознанныйТекст) Тогда
				
				Реквизит.Значение = Реквизит.НайденноеЗначение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// На основании Контрагента, Организации, Валюты из Договора, данных о Банке и Счете можно найти банковский счет
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		РаспознаваниеДокументовСлужебный.ЗаполнитьБанковскийСчет(ДокументОбъект);
	КонецЕсли;
	
	Для Каждого ЯчейкаТаблицы Из ДокументОбъект.РеквизитыТабличныхЧастей Цикл
		
		Если ЯчейкаТаблицы.ИмяРеквизита = "СтавкаНДС" Или ЯчейкаТаблицы.ИмяРеквизита = "СтавкаНДСДоКорректировки"Тогда
			ЯчейкаТаблицы.Значение = РаспознаваниеДокументовБП.СтавкаНДСПоРаспознанномуТексту(ЯчейкаТаблицы.РаспознанныйТекст, ЯчейкаТаблицы.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаДокумента = РаспознаваниеДокументовСлужебный.ЗаполненнаяТаблицаДокумента(ДокументОбъект);
	
	Если ПроверяемыеРеквизиты.Свойство("ИзмениласьСуммаВключаетНДС") И ПроверяемыеРеквизиты.ИзмениласьСуммаВключаетНДС Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			РаспознаваниеДокументовСлужебныйКлиентСервер.ПриИзмененииСтавкаНДС(СтрокаТаблицы, ПроверяемыеРеквизиты.СуммаВключаетНДС);
		КонецЦикла;
	КонецЕсли;
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		
		ИтогоСуммаСкидкиРаспознано = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ДокументОбъект, "ИтогоСуммаСкидки", 0);
		ИтогоСуммаСкидкиПоТаблице = ТаблицаДокумента.Итог("СуммаСкидки");
		
		ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НеПредоставлена;
		Если ИтогоСуммаСкидкиПоТаблице <> 0 Тогда
			ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции;
		ИначеЕсли ИтогоСуммаСкидкиПоТаблице = 0 И ИтогоСуммаСкидкиРаспознано <> 0 Тогда
			ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().ПоДокументуВЦелом;
		КонецЕсли;
		
		РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, "ВидСкидки", ВидСкидки);
		
		Если ИтогоСуммаСкидкиРаспознано = 0 И ИтогоСуммаСкидкиПоТаблице <> 0 Тогда
			РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, "ИтогоСуммаСкидки", ИтогоСуммаСкидкиПоТаблице);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДокументОбъект.ТипДокумента <> Перечисления.ТипыДокументовРаспознаваниеДокументов.УКД Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			Если СтрокаТаблицы.Количество = 0
				И СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПроверяемыеРеквизиты.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС) Тогда
				СтрокаТаблицы.Количество = 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		
		// Определение ставки НДС
		Если ПроверяемыеРеквизиты.ТекстНДС = "Без налога (НДС)" Тогда
			СтавкаНДС = Новый Структура("Числом, Ссылкой", 0, Перечисления.СтавкиНДС.БезНДС);
		Иначе 
			СтавкаНДС = РаспознаваниеДокументовБП.ОпределитьСтавкуНДС(ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС);
		КонецЕсли;
		
		Если СтавкаНДС = Неопределено Тогда
			// Ставку НДС не удалось определить по полям итогов. Будем брать из номенклатуры
			ВсяНоменклатура = ТаблицаДокумента.ВыгрузитьКолонку("Номенклатура");
			НДСПоНоменклатуре = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВсяНоменклатура, "ВидСтавкиНДС");
			
			// ДанныеНДС содержит ВидСтавкиНДС, преобразуем ее в ставку НДС по дате документа
			НайденнаяСтрока = ДокументОбъект.РеквизитыДокумента.Найти("ДатаДокумента", "ИмяРеквизита");
			Если НайденнаяСтрока = Неопределено Тогда
				НДСПоНоменклатуре = Неопределено;
			Иначе
				ДатаДокумента = НайденнаяСтрока.Значение;
				Для Каждого ЭлементНДС Из НДСПоНоменклатуре Цикл
					НДСПоНоменклатуре.Вставить(ЭлементНДС.Ключ, Перечисления.СтавкиНДС.СтавкаНДС(ЭлементНДС.Значение, ДатаДокумента));
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		ПоляПроверки = Новый Массив;
		ПоляПроверки.Добавить("Цена");
		ПоляПроверки.Добавить("Количество");
		ПоляПроверки.Добавить("Сумма");
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			// Восстановление нулей и потерянных запятых
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьЕдинственныйНоль(СтрокаТаблицы, ПоляПроверки, ДокументОбъект, ПроверяемыеРеквизиты.СуммаВключаетНДС);
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьПотеряннуюЗапятую(СтрокаТаблицы, ПоляПроверки, ДокументОбъект);
			
			// Пытаемся восстановить ставку НДС
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
				Если СтавкаНДС <> Неопределено Тогда
					СтрокаТаблицы.СтавкаНДС = СтавкаНДС.Ссылкой;
					СтавкаНДСЧислом = СтавкаНДС.Числом;
				ИначеЕсли НДСПоНоменклатуре <> Неопределено Тогда
					СтрокаТаблицы.СтавкаНДС = НДСПоНоменклатуре.Получить(СтрокаТаблицы.Номенклатура);
					СтавкаНДСЧислом = РаспознаваниеДокументовСлужебныйВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
					СтрокаТаблицы.СуммаНДС = РаспознаваниеДокументовСлужебныйКлиентСервер.РассчитатьСуммуНДС(
						СтрокаТаблицы.Сумма, ПроверяемыеРеквизиты.СуммаВключаетНДС, СтавкаНДСЧислом);
					СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПроверяемыеРеквизиты.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
		
		// Этот расчет происходит до того, как пользователь что-то увидит + в УПД, Торг12 и Счетах-фактурах
		// введены 4 колонки, которые можно легко сверить для каждой строки по формулам:
		// 1) Если (Сумма без НДС) + (Сумма НДС) = (Сумма с НДС), то проверить и при необходимости восстановить Ставку НДС.
		// 2) Если (Сумма без НДС) * (100 + Ставка НДС)/100 = (Сумма с НДС), то проверить и при необходимости восстановить Сумму НДС.
		
		// Если СуммаВключаетНДС = Истина, то такая проверка не пройдет и ее нужно исключить. Но для этих трех документов
		// при печате на бумаге Сумма без НДС не должна совпадать с колонкой Всего даже на нетиповых документах.
		Если Не ПроверяемыеРеквизиты.СуммаВключаетНДС Тогда
			Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
				СтавкаНДСЧислом = РаспознаваниеДокументовСлужебныйВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
				СложениемСовпало = (СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.Всего);
				УмножениемСовпало = (Окр(СтрокаТаблицы.Сумма * (100 + СтавкаНДСЧислом)/100, 2) = СтрокаТаблицы.Всего);
				Если СложениемСовпало Или УмножениемСовпало Тогда
					Если Не СложениемСовпало Тогда
						СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.Всего - СтрокаТаблицы.Сумма;
					ИначеЕсли Не УмножениемСовпало Тогда
						СтруктураСтавкиНДС = РаспознаваниеДокументовБП.ОпределитьСтавкуНДС(СтрокаТаблицы.Всего, СтрокаТаблицы.СуммаНДС);
						Если СтруктураСтавкиНДС <> Неопределено Тогда
							СтрокаТаблицы.СтавкаНДС = СтруктураСтавкиНДС.Ссылкой;
						КонецЕсли;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) И СтавкаНДСЧислом = 0 Тогда
						СтрокаТаблицы.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ПоляПроверки = Новый Массив;
		ПоляПроверки.Добавить("Всего");
		ПоляПроверки.Добавить("СуммаНДС");
		ПоляПроверки.Добавить("Сумма");
		
		Счетчик = 0;
		РаспознаваниеДокументовСлужебный.ВосстановлениеПо2Формулам(ПроверяемыеРеквизиты.СуммаВключаетНДС, ТаблицаДокумента, ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС, ПоляПроверки);
		НеобходимоПроверять = Истина;
		Пока НеобходимоПроверять И Счетчик < 5 Цикл
			Счетчик = Счетчик + 1;
			НеобходимоПроверять = РаспознаваниеДокументовСлужебный.ВосстановлениеПо1ФормулеСУверенностью(ДокументОбъект, ПроверяемыеРеквизиты.СуммаВключаетНДС, ТаблицаДокумента, ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС, ПроверяемыеРеквизиты.ИтогоСумма);
			НеобходимоПроверять = НеобходимоПроверять Или РаспознаваниеДокументовСлужебный.ВосстановлениеПо2Формулам(ПроверяемыеРеквизиты.СуммаВключаетНДС, ТаблицаДокумента, ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС, ПоляПроверки);
		КонецЦикла;
		
		ПоляПроверки = Новый Массив;
		ПоляПроверки.Добавить("Цена");
		ПоляПроверки.Добавить("Количество");
		СтрокаСДатой = ДокументОбъект.РеквизитыДокумента.Найти("ДатаДокумента", "ИмяРеквизита");
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			Если СтрокаСДатой <> Неопределено И ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
				ВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Номенклатура, "ВидСтавкиНДС");
				СтрокаТаблицы.СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавкиНДС, СтрокаСДатой.Значение);
			КонецЕсли;
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьЕдинственныйНоль(СтрокаТаблицы, ПоляПроверки, ДокументОбъект, ПроверяемыеРеквизиты.СуммаВключаетНДС);
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьПотеряннуюЗапятую(СтрокаТаблицы, ПоляПроверки, ДокументОбъект);
		КонецЦикла;
	КонецЕсли;
	
	РаспознаваниеДокументовСлужебный.СохранитьТаблицуДокумента(ДокументОбъект, ТаблицаДокумента);
	
	// Конец БП
	
КонецПроцедуры

// При заполнении параметров выбора договора.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыВыбораДоговора - см. РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора
//
Процедура ПриЗаполненииПараметровВыбораДоговора(ДокументОбъект, ПараметрыВыбораДоговора) Экспорт
	
	// БП
	
	ВидыДоговоров = Новый Массив;
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
	Иначе
		ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
		ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	КонецЕсли;
	
	ПараметрыВыбораДоговора.Вставить("ВидыДоговоров", ВидыДоговоров);
	
	// Конец БП
	
КонецПроцедуры

// При заполнении договора контрагента.
// 
// Параметры:
//  ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыДоговора - см. РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора
//
Процедура ПриЗаполненииДоговораКонтрагента(ДоговорКонтрагента, ДокументОбъект, ПараметрыДоговора) Экспорт
	
	// БП
	
	ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(ПараметрыДоговора.Организация) Или Не ЗначениеЗаполнено(ПараметрыДоговора.Контрагент) Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметрыПоиска = Новый Структура;
	Если ЗначениеЗаполнено(ПараметрыДоговора.НомерДоговора) Тогда
		ДополнительныеПараметрыПоиска.Вставить("Номер", Новый Структура("ЗначениеОтбора", ПараметрыДоговора.НомерДоговора));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыДоговора.ДатаДоговора) Тогда
		ДополнительныеПараметрыПоиска.Вставить("Дата", Новый Структура("ЗначениеОтбора", ПараметрыДоговора.ДатаДоговора));
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента,
		ПараметрыДоговора.Контрагент, ПараметрыДоговора.Организация, ПараметрыДоговора.ВидыДоговоров, ДополнительныеПараметрыПоиска);
	
	Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента,
			ПараметрыДоговора.Контрагент, ПараметрыДоговора.Организация, ПараметрыДоговора.ВидыДоговоров);
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// При заполнении банковского счета.
// 
// Параметры:
//  Счет - СправочникСсылка.БанковскиеСчета
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыСчета - Структура:
//	*Банк - СправочникСсылка.Банки
//	*БанкНаименование - Строка
//	*БанкСчет - СправочникСсылка.БанковскиеСчета
//	*Договор - СправочникСсылка.ДоговорыКонтрагентов
//
Процедура ПриЗаполненииБанковскогоСчета(Счет, ДокументОбъект, ПараметрыСчета) Экспорт
	
	// БП
	
	Если ЗначениеЗаполнено(Счет) Тогда
		// Проверим соответствие счета и владельца, т.к. нечеткий поиск этого пока не делает
		Если ПараметрыСчета.Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Счет, "Владелец") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВалютаОплаты = РаспознаваниеДокументовСлужебный.ВалютаОплаты(ПараметрыСчета.Договор);
	УчетДенежныхСредствБП.УстановитьБанковскийСчет(Счет, ПараметрыСчета.Владелец, ВалютаОплаты, Истина);
	
	// Конец БП
	
КонецПроцедуры

// При заполнении валюты оплаты.
// 
// Параметры:
//  ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов
//  ВалютаОплаты - СправочникСсылка.Валюты
//
Процедура ПриЗаполненииВалютыОплаты(ДоговорКонтрагента, ВалютаОплаты) Экспорт
	
	// БП
	
	ВалютаУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДоговорКонтрагента, "ВалютаВзаиморасчетов, РасчетыВУсловныхЕдиницах");
		
		Если РеквизитыДоговора.РасчетыВУсловныхЕдиницах Тогда
			ВалютаОплаты = ВалютаУчета;
		Иначе
			ВалютаОплаты = РеквизитыДоговора.ВалютаВзаиморасчетов;
		КонецЕсли;
	Иначе
		ВалютаОплаты = ВалютаУчета;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ПриЗаполненииПлатежногоПоручения(Источник, ДанныеЗаполнения) Экспорт
	
	НаименованиеДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.ДоговорКонтрагента, "Наименование");
	СтрокаПоиска = СтрШаблон(НСтр("ru = 'Оплата по договору %1'"), СокрЛП(НаименованиеДоговора));
	
	Если СтрНайти(Источник.НазначениеПлатежа, СтрокаПоиска) > 0 Тогда
		
		ТекстНазначение = НСтр("ru = 'Оплата по счету'");
		Если НЕ ПустаяСтрока(ДанныеЗаполнения.НомерВходящегоДокумента) И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаВходящегоДокумента) Тогда
			ТекстНазначение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 № %2 от %3'"),
				ТекстНазначение,
				СокрЛП(ДанныеЗаполнения.НомерВходящегоДокумента),
				Формат(ДанныеЗаполнения.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy")); // формат даты не локальный, чтобы не влияли настройки компьютера / сервера
				
			Источник.НазначениеПлатежа = СтрЗаменить(Источник.НазначениеПлатежа, СтрокаПоиска, ТекстНазначение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// При определении ставки НДС.
// 
// Параметры:
//  СтавкаНДСЧислом - Число
//  СтавкаНДС - ОпределяемыйТип.СтавкаНДСБРД
//  ПрименяютсяСтавки4и2 - Булево
//
Процедура ПриОпределенииСтавкиНДС(СтавкаНДСЧислом, СтавкаНДС, ПрименяютсяСтавки4и2 = Ложь) Экспорт
	
	// БП
	
	СтавкаНДСЧислом = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДС, ПрименяютсяСтавки4и2);
	
	// Конец БП
	
КонецПроцедуры

// При определении проверяемых реквизитов.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПроверяемыеРеквизиты - Произвольный
//
Процедура ПриОпределенииПроверяемыхРеквизитов(ДокументОбъект, ПроверяемыеРеквизиты) Экспорт
	
	// БП
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("Договор");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("Склад");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// При обработке проверки заполнения реквизитов.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ТаблицаПроблем - ТаблицаЗначений 
//
Процедура ПриОбработкеПроверкиЗаполненияРеквизитов(ДокументОбъект, ТаблицаПроблем) Экспорт
	
КонецПроцедуры

Процедура ПриЗаполненииНовогоЭлементаСправочника(СправочникОбъект, ДанныеДляЗаполнения) Экспорт
	
	// БП
	
	СправочникОбъект.Заполнить(ДанныеДляЗаполнения);
	
	// Конец БП
	
КонецПроцедуры

// При создании документа на основании распознанного.
// 
// Параметры:
//  ДокументОбъект - ОпределяемыйТип.ДокументыСоздаваемыеИзРаспознанногоДокументаБРДОбъект
//  РаспознанныйДокумент - ДокументСсылка.РаспознанныйДокумент
//  ПараметрыЗаполнения - см. РаспознаваниеДокументовСлужебныйВызовСервера.ПараметрыЗаполненияТиповойНаОсновеСчетНаОплату
//
Процедура ПриСозданииДокументаНаОснованииРаспознанного(ДокументОбъект, РаспознанныйДокумент, ПараметрыЗаполнения) Экспорт
	
КонецПроцедуры

// При проведении документа на основании распознанного.
// 
// Параметры:
//  ДокументОбъект - ОпределяемыйТип.ДокументыСоздаваемыеИзРаспознанногоДокументаБРДОбъект
//
Процедура ПриПроведенииДокументаНаОснованииРаспознанного(ДокументОбъект) Экспорт

КонецПроцедуры

// После создания документа на основании распознанного.
// 
// Параметры:
//  ДокументОбъект - ОпределяемыйТип.ДокументыСоздаваемыеИзРаспознанногоДокументаБРДОбъект
//
Процедура ПослеСозданияДокументаНаОснованииРаспознанного(ДокументОбъект) Экспорт
	
	// БП
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетНаОплатуПокупателю")
		ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетНаОплатуПоставщика") Тогда
		СрокиОплатыДокументов.ЗаписатьСрокОплатыДокумента(ДокументОбъект.Ссылка, ДокументОбъект.ДополнительныеСвойства.СрокОплаты);
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// При создании счет фактуры.
// 
// Параметры:
//  ПараметрыСоздания - Структура:
//		*ТипДокументаСтрокой - Строка,
//		*Основание - ОпределяемыйТип.ПоступлениеТоваровУслугБРД, ОпределяемыйТип.РеализацияТоваровУслугБРД - Документ-основание
//		*ПараметрыЗаполнения - Структура:
//  		**Контрагент - СправочникСсылка.Контрагенты
//  		**Организация - СправочникСсылка.Организации
//  		**ИмяРеквизитаНомерДокумента - Строка
//  		**ИмяРеквизитаДатаДокумента  - Строка 
//
//  СчетФактураСсылка - ОпределяемыйТип.СчетФактураВыданныйБРД, ОпределяемыйТип.СчетФактураПолученныйБРД - Счет фактура ссылка
//
Процедура ПриСозданииСчетФактуры(ПараметрыСоздания, СчетФактураСсылка = Неопределено) Экспорт
	
	// БП
	
	ТипДокументаСтрокой = ПараметрыСоздания.ТипДокументаСтрокой;
	Основание = ПараметрыСоздания.Основание;
	ПараметрыЗаполнения = ПараметрыСоздания.ПараметрыЗаполнения;
	
	ДанныеСчетаФактуры = Неопределено;
	
	Если ТипДокументаСтрокой = "СчетФактураПолученный" Или ТипДокументаСтрокой = "ПоступлениеТоваровУслуг" Тогда
		ПараметрыСозданияСчетаФактуры = УчетНДСКлиентСервер.НовыеПараметрыСозданияПолученногоСчетаФактуры();
			ПараметрыСозданияСчетаФактуры.Основание = Основание;
			ПараметрыСозданияСчетаФактуры.НомерСчетаФактурыПолученного = ПараметрыЗаполнения.НомерВходящегоДокумента;
			ПараметрыСозданияСчетаФактуры.ДатаСчетаФактурыПолученного  = ПараметрыЗаполнения.ДатаВходящегоДокумента;
			
		ДанныеСчетаФактуры = УчетНДСВызовСервера.СоздатьСчетФактуруПолученныйНаОсновании(
			ПараметрыСозданияСчетаФактуры);
	Иначе
		ПараметрыСозданияСчетаФактуры = УчетНДСКлиентСервер.НовыеПараметрыСозданияВыданногоСчетаФактуры();
		ПараметрыСозданияСчетаФактуры.Основание = Основание;
		ПараметрыСозданияСчетаФактуры.УникальныйИдентификатор = Неопределено;
		ДанныеСчетаФактуры = УчетНДСВызовСервера.СоздатьСчетФактуруВыданныйНаОсновании(ПараметрыСозданияСчетаФактуры);
		Если ДанныеСчетаФактуры <> Неопределено Тогда
			СчетФактураОбъект = ДанныеСчетаФактуры.Ссылка.ПолучитьОбъект();
			СчетФактураОбъект.Номер = ПараметрыЗаполнения.НомерВходящегоДокумента;

			РежимЗаписи = ?(СчетФактураОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
			СчетФактураОбъект.Записать(РежимЗаписи);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеСчетаФактуры <> Неопределено Тогда 
		СчетФактураСсылка = ДанныеСчетаФактуры.Ссылка;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// При определении параметров создания номенклатуры.
// 
// Параметры:
//  Параметры - ТаблицаЗначений
//
Процедура ПриОпределенииПараметровСозданияНоменклатуры(Параметры) Экспорт
	
	// БП
	МетаданныеНоменклатура = Метаданные.Справочники.Номенклатура;
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.СтандартныеРеквизиты.Наименование);
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.Реквизиты.Артикул);
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.СтандартныеРеквизиты.Родитель);
	Параметр.ОписаниеЭлемента = РаспознаваниеДокументов.ОписаниеЭлемента();
	Параметр.ОписаниеЭлемента.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.Реквизиты.ВидНоменклатуры);
	
	// Ставка НДС (хранение - перечисление "ВидыСтавокНДС", выбор - перечисление "СтавкиНДС")
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита();
	Параметр.ОписаниеРеквизита.Имя = "СтавкаНДС";
	Параметр.ОписаниеРеквизита.Синоним = "Ставка НДС";
	Параметр.ОписаниеРеквизита.Тип = Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС");
	Параметр.ОписаниеРеквизита.ПроверкаЗаполнения = ПроверкаЗаполнения.НеПроверять;
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.Реквизиты.ЕдиницаИзмерения);
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.Реквизиты.НоменклатурнаяГруппа);
	
	// Конец БП
	
КонецПроцедуры

// Обработка создания номенклатуры.
// 
// Параметры:
//  РезультатСсылка - СправочникСсылка.Номенклатура
//  ДанныеЗаполнения - см. РаспознаваниеДокументовСлужебный.ПараметрыСозданияНовогоЭлемента
//
Процедура ОбработкаСозданияНоменклатуры(РезультатСсылка, ДанныеЗаполнения) Экспорт
	
	// БП
	
	НоваяНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НоваяНоменклатура, ДанныеЗаполнения);
	НоваяНоменклатура.Заполнить(ДанныеЗаполнения);
	
	НоваяНоменклатура.НаименованиеПолное = НоваяНоменклатура.Наименование;
	НоваяНоменклатура.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.ВидСтавки(ДанныеЗаполнения.СтавкаНДС);
	
	Если НоваяНоменклатура.ПроверитьЗаполнение() Тогда
		НоваяНоменклатура.Записать();
		РезультатСсылка = НоваяНоменклатура.Ссылка;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// При заполнении параметров создания нового элемента.
// 
// Параметры:
//  СвязанныеКолонки - см. РаспознаваниеДокументовСлужебный.ПараметрыСозданияНовогоЭлемента
//  ИмяЭлемента - Строка
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовРаспознаваниеДокументов
//  Направление - ПеречислениеСсылка.НаправленияРаспознанногоДокумента
//
Процедура ПриЗаполненииПараметровСозданияНовогоЭлемента(СвязанныеКолонки, ИмяЭлемента, ТипДокумента, Направление) Экспорт
	
	// БП
	
	Если ИмяЭлемента = "Номенклатура" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Номенклатура", Истина, Истина, "НаименованиеПолное"));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "Артикул", "Артикул", Истина, Ложь));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный, ВыборГруппИЭлементов", "Родитель", "Родитель", "Входит в группу", Ложь, ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"), Ложь, ГруппыИЭлементы.Группы));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЕдиницаИзмерения", "ЕдиницаИзмерения", "Единица измерения", Ложь, ПредопределенноеЗначение("Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ВидСтавкиНДС", "СтавкаНДС", "% НДС", Ложь, ПредопределенноеЗначение("Перечисление.СтавкиНДС.ПустаяСсылка"), Ложь));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ВидНоменклатуры", "Вид номенклатуры", Ложь, ПредопределенноеЗначение("Справочник.ВидыНоменклатуры.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "ЕдиницаИзмерения" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Наименование", "ЕдиницаИзмерения", "Наименование", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Код", "КодЕдиницыИзмерения", "Код", Истина, Истина));
	ИначеЕсли ИмяЭлемента = "СтранаПроисхождения" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Наименование", "СтранаПроисхождения", "Наименование", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Код", "КодСтраныПроисхождения", "Код", Истина, Истина));
	ИначеЕсли ИмяЭлемента = "НомерГТД" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Код", "НомерГТД", "Номер", Истина, Истина));
		
	ИначеЕсли ИмяЭлемента = "БанковскийСчетКонтрагента" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный, ПодходящиеОбязательноеРавенство",
			"Владелец", "Продавец", "Контрагент", Истина, ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"), Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "НомерСчета", "БанковскийСчетКонтрагента", "Номер счета", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "Банк", "Банк", "Банк", Истина, ПредопределенноеЗначение("Справочник.Банки.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ВалютаДенежныхСредств", "Валюта", Ложь, ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета(), Истина));
	ИначеЕсли ИмяЭлемента = "БанковскийСчетОрганизации" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный, ПодходящиеОбязательноеРавенство",
			"Владелец", "ПродавецОрганизация", "Контрагент", Истина, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"), Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "НомерСчета", "БанковскийСчетОрганизации", "Номер счета", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "Банк", "Банк", "Банк", Истина, ПредопределенноеЗначение("Справочник.Банки.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ВалютаДенежныхСредств", "Валюта", Ложь, ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета(), Истина));
		
	ИначеЕсли ИмяЭлемента = "Продавец" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Продавец", "Наименование", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППродавца", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППродавца", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "ПродавецОрганизация" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "ПродавецОрганизация", Истина, Истина, "НаименованиеПолное,НаименованиеСокращенное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППродавца", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППродавца", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Покупатель" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Покупатель", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППокупателя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППокупателя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "ПокупательОрганизация" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "ПокупательОрганизация", Истина, Истина, "НаименованиеПолное,НаименованиеСокращенное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППокупателя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППокупателя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Грузоотправитель" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Грузоотправитель", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКППГрузоотправителя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКППГрузоотправителя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННГрузоотправителя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КППГрузоотправителя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Грузополучатель" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Грузополучатель", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКППГрузополучателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКППГрузополучателя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННГрузополучателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КППГрузополучателя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Договор" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
			"ВидДоговора", "Вид договора", Истина, Перечисления.ВидыДоговоровКонтрагентов.ПустаяСсылка(), Истина));
		
		Если Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Владелец", "Покупатель", "Контрагент", Истина, Справочники.Контрагенты.ПустаяСсылка(), Истина));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Организация", "ПродавецОрганизация", "Организация", Истина, Справочники.Организации.ПустаяСсылка(), Истина));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Владелец", "Продавец", "Контрагент", Истина, Справочники.Контрагенты.ПустаяСсылка(), Истина));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Организация", "ПокупательОрганизация", "Организация", Истина, Справочники.Организации.ПустаяСсылка(), Истина));
		КонецЕсли;
		
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный",
			"Наименование", "Договор", "Наименование", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный",
			"Номер", "НомерДоговора", "Номер договора", Истина, Ложь));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
			"Дата", "ДатаДоговора", "Дата договора", Истина, '00010101', Ложь));
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

// При создании формы распознавания на сервере.
// 
// Параметры:
//  ЭтотОбъект - Форма
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура ПриСозданииФормыРаспознаванияНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка) Экспорт	
	
	
	
КонецПроцедуры

// Перед записью формы распознавания на сервере.
// 
// Параметры:
//  ЭтотОбъект - Форма
//  Отказ - Булево
//  ТекущийОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыЗаписи - Структура
//
Процедура ПередЗаписьюФормыРаспознаванияНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
	
	
КонецПроцедуры

// После записи формы распознавания на сервере.
// 
// Параметры:
//  ЭтотОбъект - Форма
//  ТекущийОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыЗаписи - Структура
//
Процедура ПослеЗаписиФормыРаспознаванияНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
КонецПроцедуры

// Заполнить добавленные колонки таблиц.
// 
// Параметры:
//  Объект - ДанныеФормыСтруктура
//  Строки - ТаблицаЗначений
//  Очистить - Булево
//
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(Объект, Строки, Очистить = Ложь) Экспорт
	
	// БП
	
	РаспознаваниеДокументовБП.ЗаполнитьДобавленныеКолонкиТаблиц(Объект, Строки, Очистить);
	
	// Конец БП
	
КонецПроцедуры

// Создание контрагента в информационной базе по реквизитам.
//
// Параметры:
//   РеквизитыКонтрагента - Структура - реквизиты необходимые для создания контрагента.
//    * ИНН - Строка - ИНН контрагента.
//    * КПП - Строка - КПП контрагента.
//    * Наименование - Строка - наименование контрагента.
//   Контрагент - СправочникСсылка - ссылка на созданного контрагента.
//   Отказ - Булево - признак ошибки.
//
Процедура СоздатьКонтрагентаПоРеквизитам(Знач РеквизитыКонтрагента, Контрагент, Отказ = Ложь) Экспорт
	
	// БП
	
	ОбменСКонтрагентамиБП.СоздатьКонтрагентаПоРеквизитам(РеквизитыКонтрагента, Контрагент, Отказ);
	
	// Конец БП
	
КонецПроцедуры
	
#Область УстаревшиеПроцедурыИФункции

Процедура ПриИзмененииКолонкиНаСервере(Объект, СтрокаТаблицы, ИмяРеквизита, НомерСтрокиТаблицы, ВыбранноеЗначение) Экспорт
	
КонецПроцедуры

Процедура СтавкиНДСПустаяСсылка(Тип) Экспорт
	
КонецПроцедуры

Процедура СоздатьДокументыКомплектаВызовСервера(ПараметрыСоздания, Результат) Экспорт
	
КонецПроцедуры

Процедура НужноПрикрепитьДополнительныйСкан(ТипКомплекта, ТипРаспознанного, ТипСозданного, Результат) Экспорт
	
КонецПроцедуры

Процедура ОтобразитьРезультатПроверкиКонтрагентовВФорме(Форма, РезультатПроверкиСуществования = "") Экспорт
	
КонецПроцедуры

#КонецОбласти

// Получение количества товаров и услуг документа-основания корректировки
//
// Параметры:
//   Документ - ДокументСсылка - ссылка на документ-основание корректировки.
//   ТоварыИУслугиДокумента - Массив - возвращаеммый список товаров и услуг.
//
Процедура СтрокиДокументаОснованияКорректировки(Знач Документ, ТоварыИУслугиДокумента) Экспорт
	
	// БП
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.НомерСтроки КАК НомерСтроки,
		|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ЛОЖЬ КАК ЭтоУслуга
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугУслуги.НомерСтроки,
		|	ПоступлениеТоваровУслугУслуги.Номенклатура,
		|	ИСТИНА КАК ЭтоУслуга
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|ГДЕ
		|	ПоступлениеТоваровУслугУслуги.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = Новый Структура;
		ДанныеСтроки.Вставить("НомерСтроки", Выборка.НомерСтроки);
		ДанныеСтроки.Вставить("Номенклатура", Выборка.Номенклатура);
		ДанныеСтроки.Вставить("ПредставлениеНоменклатуры", Строка(Выборка.Номенклатура));
		ДанныеСтроки.Вставить("ЭтоУслуга", Выборка.ЭтоУслуга);
		ТоварыИУслугиДокумента.Добавить(ДанныеСтроки);
		
	КонецЦикла;
	
	// Конец БП
	
КонецПроцедуры

Процедура СвойстваСтрокиДокументаОснованияКорректировки(Знач Документ, Знач НомерСтроки, ДанныеСтроки) Экспорт
	
	// БП
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.НомерСтроки КАК НомерСтроки,
		|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровУслугТовары.Количество КАК КоличествоПоДокументу,
		|	ПоступлениеТоваровУслугТовары.Цена КАК ЦенаПоДокументу,
		|	ПоступлениеТоваровУслугТовары.Сумма КАК СуммаПоДокументу,
		|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДСПоДокументу,
		|	ПоступлениеТоваровУслугТовары.СуммаНДС КАК СуммаНДСПоДокументу,
		|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхожденияПоДокументу,
		|	ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
		|	И ПоступлениеТоваровУслугТовары.НомерСтроки = &НомерСтроки
		|	И НЕ &ЭтоУслуга
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугУслуги.НомерСтроки,
		|	ПоступлениеТоваровУслугУслуги.Номенклатура,
		|	ПоступлениеТоваровУслугУслуги.Количество,
		|	ПоступлениеТоваровУслугУслуги.Цена,
		|	ПоступлениеТоваровУслугУслуги.Сумма,
		|	ПоступлениеТоваровУслугУслуги.СтавкаНДС,
		|	ПоступлениеТоваровУслугУслуги.СуммаНДС,
		|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|ГДЕ
		|	ПоступлениеТоваровУслугУслуги.Ссылка = &Ссылка
		|	И ПоступлениеТоваровУслугУслуги.НомерСтроки = &НомерСтроки
		|	И &ЭтоУслуга";
	
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Запрос.УстановитьПараметр("НомерСтроки", НомерСтроки);
	Запрос.УстановитьПараметр("ЭтоУслуга", ДанныеСтроки.ЭтоУслуга);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, Выборка);
		ДанныеСтроки.Вставить("Номенклатура", Выборка.Номенклатура);
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДополнительныеРеквизиты(Форма) Экспорт
	
	// БП
	
	Дата = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Форма.Объект, "ДатаДокумента", '20100101');
	Организация = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(
		Форма.Объект, 
		РаспознаваниеДокументовСлужебный.ОсновныеРеквизиты(Форма.Объект)["Организация"], 
		Справочники.Организации.ПустаяСсылка()
	);
	
	Форма.ПлательщикНДФЛ = УчетнаяПолитика.ПлательщикНДФЛ(Организация, Дата);
	Форма.СуммаВключаетНДС = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Форма.Объект, "СуммаВключаетНДС", Ложь);
	
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Организация, Дата);
	ДокументБезНДС = НЕ ПлательщикНДС И Форма.ТаблицаДокумента.Итог("СуммаНДС") = 0;
	
	Форма.НДСВключенВСтоимость = Не ПлательщикНДС;
	Форма.ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Дата);
	Форма.ПрименениеУСН       = УчетнаяПолитика.ПрименяетсяУСН(Организация, Дата);
	Форма.ПрименениеУСНДоходы = УчетнаяПолитика.ПрименяетсяУСНДоходы(Организация, Дата);
	Форма.УчетАгентскогоНДС = Ложь; //  ЗначениеЗаполнено(Объект.ДоговорКонтрагента)
	Форма.РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата);
	
	// Конец БП
	
КонецПроцедуры

Процедура УстановитьВидимостьСчетовУчета(Форма) Экспорт
	
	// БП
	
	ЭлементыСчетов = Новый Массив();
	ЭлементыСчетов.Добавить("АналитикаУчета");
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Форма.Элементы, ЭлементыСчетов);
	
	// Конец БП
	
КонецПроцедуры

Процедура ОпределитьПрименяетсяМалоценка(Форма, ПрименяетсяМалоценка) Экспорт
	
	// БП
	
	Дата = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Форма.Объект, "ДатаДокумента", '20100101');
	ПрименяетсяМалоценка = УчетнаяПолитика.ПрименяетсяФСБУ5_2019(Дата);
	
	// Конец БП
	
КонецПроцедуры

Процедура ОпределитьНеобходимостьРасчетаСуммыВРознице(Форма, РассчитыватьСуммаВРознице) Экспорт
	
	// БП
	
	Если Форма.Объект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
		// Счета учета скрываются только для поступлений
		// код частично из ПоступлениеТоваровУслугФормы.ОпределитьНеобходимостьРасчетаСуммыВРознице
		
		Дата = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Форма.Объект, "ДатаДокумента", '20100101');
		Склад = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Форма.Объект, "Склад", Справочники.Склады.ПустаяСсылка());
		Организация = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(
			Форма.Объект, 
			РаспознаваниеДокументовСлужебный.ОсновныеРеквизиты(Форма.Объект)["Организация"], 
			Справочники.Организации.ПустаяСсылка()
		);
		
		УчетВПродажныхЦенах = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата)
			= Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
		ТипСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ТипСклада");
		Если Справочники.Склады.ЭтоСкладРозничнойТорговли(ТипСклада)
			И УчетВПродажныхЦенах Тогда
			
			РассчитыватьСуммаВРознице = Истина;
		Иначе
			РассчитыватьСуммаВРознице = Ложь;
		КонецЕсли;
	Иначе
		РассчитыватьСуммаВРознице = Ложь;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ОпределитьСкладПоУмолчанию(СкладПоУмолчанию) Экспорт
	
	// БП
	
	СкладПоУмолчанию = Справочники.Склады.ПолучитьСкладПоУмолчанию();
	Если ЗначениеЗаполнено(СкладПоУмолчанию) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	Склады.Ссылка КАК Склад
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	НЕ Склады.ЭтоГруппа
		|	И НЕ Склады.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Если Выборка.Следующий() Тогда
				СкладПоУмолчанию = Выборка.Склад;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СкладПоУмолчанию = Справочники.Склады.ПустаяСсылка();
	
	// Конец БП
	
КонецПроцедуры

Процедура НайтиПодчиненныйСчетФактуруПолученный(ДокументОснование, СчетФактура) Экспорт
	
	// БП
	СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруПолученный(ДокументОснование);
	// Конец БП
	
КонецПроцедуры

Процедура НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ДокументОснование, СчетФактура) Экспорт
	
	// БП
	СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ДокументОснование);
	// Конец БП
	
КонецПроцедуры

Процедура ПолучитьСоответствиеСтавокНДС(Соответствие) Экспорт 
	
	// БП
	
	СоответствиеСтавок = Новый Соответствие();
	Для Каждого ЗначениеПеречисления Из Перечисления.ВидыСтавокНДС Цикл
		СоответствиеСтавок.Вставить(ЗначениеПеречисления,
			Перечисления.СтавкиНДС.СтавкаНДС(ЗначениеПеречисления, ТекущаяДатаСеанса()));
	КонецЦикла;
	
	// Конец БП
	
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюУчетДоговоров(УчетДоговоров) Экспорт
	
	// БП
	
	УчетДоговоров = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	
	// Конец БП
	
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюУчетПоНесколькимСкладам(УчетПоНесколькимСкладам) Экспорт
	
	// БП
	
	УчетПоНесколькимСкладам = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет");
	
	// Конец БП
	
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюИспользоватьХарактеристики(ИспользоватьХарактеристики) Экспорт
	
	// БП
	
	ИспользоватьХарактеристики = Ложь;
	
	// Конец БП
	
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюИспользоватьКорректировочныеДокументы(ИспользоватьКорректировочныеДокументы) Экспорт
	
	// БП
	
	ИспользоватьКорректировочныеДокументы = ПолучитьФункциональнуюОпцию("ИспользоватьКорректировочныеДокументы");
	
	// Конец БП
	
КонецПроцедуры

Процедура ЭтоНоменклатураУслуга(Ссылка, Результат) Экспорт
	
	// БП
	
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Услуга");
	
	// Конец БП
	
КонецПроцедуры

Процедура ЗаполнитьСписокУслугиПоНоменклатурам(СписокНоменклатур, СписокУслугиПоНоменклатурам) Экспорт
	
	// БП
	
	СписокУслугиПоНоменклатурам = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокНоменклатур, "Услуга");
	
	// Конец БП
	
КонецПроцедуры

Процедура ПриИзмененииНоменклатуры(СтрокаТаблицы, НомерСтрокиТаблицы, ВыбранноеЗначение, Объект) Экспорт	
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыПоискаДоговора(РеквизитыПоиска, ДанныеДоговора) Экспорт
	
	// БП
	РеквизитыПоиска.Вставить("Номер", ДанныеДоговора.Номер);
	РеквизитыПоиска.Вставить("Дата", ДанныеДоговора.Дата);
	РеквизитыПоиска.Вставить("Наименование", ДанныеДоговора.Наименование);
	// Конец БП
	
КонецПроцедуры

Процедура ЗаполнитьСкидкиВТаблицеТовары(ДокументОбъект, ТаблицаТовары, ПараметрыЗаполнения, ТипДокументаСтрокой) Экспорт
	
	// БП
	
	ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(
		ДокументОбъект, "ВидСкидки", РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НеПредоставлена);
	Если ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции Тогда
		Если ТипДокументаСтрокой = "СчетНаОплатуПоставщика" Тогда
			
			// В счете от поставщика сумма принимает смысл "Сумма с учетом скидки".
			
			Для Каждого Строка Из ТаблицаТовары Цикл
				Строка.Цена = Строка.Сумма / ?(Строка.Количество = 0, 1, Строка.Количество);
			КонецЦикла;
			
		Иначе
			
			// Сумма используемая в типовом документе имеет смысл "Сумма без скидки".
			
			Для Каждого Строка Из ТаблицаТовары Цикл
				Если Строка.СуммаСкидки < 0 Тогда
					Строка.Цена = Строка.Сумма / ?(Строка.Количество = 0, 1, Строка.Количество);
				Иначе
					Строка.Сумма = Строка.Сумма + Строка.СуммаСкидки;
					Строка.Всего = Строка.Сумма;
					Строка.ПроцентСкидки = ?(Строка.Сумма = 0, 0, Строка.СуммаСкидки / Строка.Сумма * 100);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	ИначеЕсли ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().ПоДокументуВЦелом Тогда
		
		Если ТипДокументаСтрокой = "СчетНаОплатуПоставщика" Тогда
			
			ИтогоСуммаСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ДокументОбъект, "ИтогоСуммаСкидки", 0);
			ДанныеОбъекта = Новый Структура;
			ДанныеОбъекта.Вставить("СуммаСкидки", ИтогоСуммаСкидки);
			ДанныеОбъекта.Вставить("СуммаВключаетНДС", ПараметрыЗаполнения.СуммаВключаетНДС);
			
			ОбработкаТабличныхЧастей.РаспределитьСкидкуПоСтрокамТабЧасти(ТаблицаТовары, ДанныеОбъекта);
			
		Иначе
			
			ПараметрыЗаполнения.Вставить("СуммаСкидки", ПараметрыЗаполнения.ИтогоСуммаСкидки);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ОбработкаАвтоПодбораБанка(Текст, ДанныеВыбора, СтандартнаяОбработка, Параметры) Экспорт 
	
КонецПроцедуры

Процедура ОбработатьВыборБанка(ВыбранноеЗначение) Экспорт 
	
КонецПроцедуры

Процедура ПроверитьНомерСчетаБИК(Банк, БанкРаспознанныйТекст, НомерСчета) Экспорт
	
КонецПроцедуры

Процедура ИмяРеквизитаОбъекта(ИмяРеквизита, ТипОбъекта = Неопределено) Экспорт
	
КонецПроцедуры

Процедура ВидыОперацийПоступлениеТоваровУслуг(Результат) Экспорт
	
	// БП
	Результат.Вставить("ПриемНаКомиссию", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия);
	// Конец БП
	
КонецПроцедуры

Процедура ВидыОперацийРеализацияТоваров(Результат) Экспорт
	
	// БП
	Результат.Вставить("ПередачаНаКомиссию", Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия);
	// Конец БП
	
КонецПроцедуры

Процедура ТекстЗапросаПоПервичнымДокументам(ИмяТипа, ТекстЗапроса, ЕстьДокументОснование = Истина) Экспорт
	
	// БП
	
	ИмяТаблицы = РаспознаваниеДокументовСлужебныйВызовСервера.ИмяДокументаПоТипу(ИмяТипа);
	ИмяТаблицы = "Документ." + ИмяТаблицы;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеПервичныхДокументов.Документ КАК &ИмяТаблицы) КАК Документ
		|ПОМЕСТИТЬ ВсеДокументы
		|ИЗ
		|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|ГДЕ
		|	ДанныеПервичныхДокументов.Документ ССЫЛКА &ИмяТаблицы
		|	И ДанныеПервичныхДокументов.Организация = &Организация
		|	И ДанныеПервичныхДокументов.Номер = &Номер
		|	И ДанныеПервичныхДокументов.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеДокументы.Документ КАК Документ,
		|	&ДокументОснование КАК ДокументОснование
		|ИЗ
		|	ВсеДокументы КАК ВсеДокументы
		|ГДЕ
		|	НЕ ВсеДокументы.Документ.ПометкаУдаления
		|	И ВсеДокументы.Документ.Контрагент = &Контрагент
		|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы);
	Если ЕстьДокументОснование Тогда
		// 1. СчетФактураПолученный
		// 2. СчетФактураВыданный
		// 3. ПоступлениеТоваровУслуг
		// 4. РеализацияТоваровУслуг
		// 5. СчетНаОплатуПоставщика
		// 6. СчетНаОплатуПокупателю
		// 7. КорректировкаПоступления
		// 8. КорректировкаРеализации
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДокументОснование",
			"ВЫБОР
			|	КОГДА ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.СчетФактураПолученный)
			|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.СчетФактураВыданный)
			|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
			|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.РеализацияТоваровУслуг)
			|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.СчетНаОплатуПоставщика)
			|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.СчетНаОплатуПокупателю)
			|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.КорректировкаПоступления)
			|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.КорректировкаРеализации)
			|		ТОГДА ВсеДокументы.Документ.ДокументОснование
			|	ИНАЧЕ NULL
			|КОНЕЦ"
		);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДокументОснование", "NULL");
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ТекстЗапросаПоПлатежнымПоручениям(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Документ
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И ПлатежноеПоручение.Контрагент = &Контрагент
	|	И ПлатежноеПоручение.Организация = &Организация
	|	И ПлатежноеПоручение.СуммаДокумента = &СуммаДокумента";
	
КонецПроцедуры

Процедура ПроверитьСоответствиеТребованиямИНН(ИНН, ЭтоЮрЛицо, РезультатПроверки) Экспорт
	
	// БП
	РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИНН, ЭтоЮрЛицо);
	// Конец БП
	
КонецПроцедуры

Процедура ПроверитьСоответствиеТребованиямКПП(КПП, ЭтоЮрЛицо, РезультатПроверки) Экспорт
	
	// БП
	РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямКПП(КПП, ЭтоЮрЛицо, Ложь);
	// Конец БП
	
КонецПроцедуры

Процедура ТребуетсяВключениеФункциональныхОпцийПоСкидкам(ВидСкидки, Направление, Результат) Экспорт
	
КонецПроцедуры

Процедура ЕстьПравоУстановкиФункциональныхОпцийПоСкидкам(ЕстьПраво, Направление) Экспорт
	
КонецПроцедуры

Процедура УстановитьФункциональныеОпцииПоСкидкам(Направление) Экспорт
	
КонецПроцедуры

Процедура ЗаполнитьСправочникиБезПравНаСоздание(СправочникиБезПравНаСоздание) Экспорт
	
	// БП
	СправочникиБезПравНаСоздание.Добавить("ЕдиницаИзмерения");
	// Конец БП
	
КонецПроцедуры

Процедура СозданиеЭлементаСОтображениемКартинкиПриСозданииНаСервере(Форма) Экспорт
	
КонецПроцедуры

Процедура ГрупповаяОбработкаПриСозданииНаСервере(Форма) Экспорт
	
КонецПроцедуры

Процедура ЗарегистрироватьЧек(ДокументОбъект, ПоДаннымСервиса = Ложь, ИмяКоманды = "", КассовыйЧекСсылка = Неопределено) Экспорт
	
	// БП
	
	РеквизитыШапки = Новый Структура;
	РеквизитыШапки.Вставить("Продавец", "ПоставщикНаименование");
	РеквизитыШапки.Вставить("ИННПродавца", "ПоставщикИНН");
	РеквизитыШапки.Вставить("ИтогоВсего", "СуммаДокумента");
	РеквизитыШапки.Вставить("ДатаДокумента", "Дата");
	РеквизитыШапки.Вставить("НомерДокумента", "Номер");
	РеквизитыШапки.Вставить("QRКод", "QRКод");
	
	СтруктураЗаполнения = Новый Структура;
	Для Каждого КлючИЗначение Из РеквизитыШапки Цикл
		
		Реквизит = РаспознаваниеДокументовСлужебныйКлиентСервер.РеквизитДокумента(ДокументОбъект, КлючИЗначение.Ключ);
		Если ЗначениеЗаполнено(Реквизит.Значение) И Не ПоДаннымСервиса Тогда
			СтруктураЗаполнения.Вставить(КлючИЗначение.Значение, Реквизит.Значение);
		Иначе
			СтруктураЗаполнения.Вставить(КлючИЗначение.Значение, Реквизит.РаспознанныйТекст);
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураЗаполнения.Вставить("Статус", Перечисления.СтатусыКассовыхЧековПодотчетныхЛиц.Получен);
	
	Если ТипЗнч(СтруктураЗаполнения.Дата) <> Тип("Дата") Тогда
		СтруктураЗаполнения.Дата = ПрочитатьДатуJSON(СтруктураЗаполнения.Дата, ФорматДатыJSON.ISO);
	КонецЕсли;
	
	
	Товары = Новый ТаблицаЗначений;
	Товары.Колонки.Добавить("НоменклатураНаименование");
	Товары.Колонки.Добавить("Количество");
	Товары.Колонки.Добавить("Цена");
	Товары.Колонки.Добавить("Сумма");
	Товары.Колонки.Добавить("СтавкаНДС");
	Товары.Колонки.Добавить("СуммаНДС");
	
	ТаблицаДокумента = РаспознаваниеДокументовСлужебный.ЗаполненнаяТаблицаДокумента(ДокументОбъект);
	
	Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
		
		НоваяСтрока = Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) И Не ПоДаннымСервиса Тогда
			НоваяСтрока.НоменклатураНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Номенклатура, "Наименование");
		Иначе
			НоваяСтрока.НоменклатураНаименование = СтрокаТаблицы.Содержание;
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураЗаполнения.Вставить("Товары", Товары);
	
	Чек = Документы.КассовыйЧекПодотчетногоЛица.СоздатьДокумент();
	Чек.Заполнить(СтруктураЗаполнения);
	
	Чек.QRКод = СтруктураЗаполнения.QRКод;
	
	Чек.Записать();
	
	РегистрыСведений.СканированиеКассовыхЧековПодотчетныхЛиц.Включить(Чек.Ссылка);
	
	РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ЗаписатьЗначения(
		Чек.Ссылка,
		ДокументОбъект.Ссылка,
		Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не УчетКассовыхЧековПодотчетныхЛиц.ВключеныКассовыеЧекиПодотчетныхЛиц() Тогда
		УчетКассовыхЧековПодотчетныхЛиц.ВключитьКассовыеЧекиПодотчетныхЛиц();
	КонецЕсли;
	
	Если ИмяКоманды = "Автоматически" Тогда
		ВариантРегистрации = "Автоматически";
	Иначе
		ВариантРегистрации = "Вручную";
	КонецЕсли;
	
	ДанныеСозданногоДокумента = РаспознаваниеДокументов.ОписаниеОбратнойСвязи("Проведен");
	ДанныеСозданногоДокумента.IdСозданногоДокумента = Строка(Чек.Ссылка.УникальныйИдентификатор());
	ДанныеСозданногоДокумента.НомерРаспознанногоДокумента = ДокументОбъект.Номер;
	ДанныеСозданногоДокумента.ЭтоВходящийДокумент = (ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	ДанныеСозданногоДокумента.НомерДокумента = СтруктураЗаполнения.Номер;
	ДанныеСозданногоДокумента.ДатаДокумента = СтруктураЗаполнения.Дата;
	ДанныеСозданногоДокумента.СуммаДокумента = СтруктураЗаполнения.СуммаДокумента;
	ДанныеСозданногоДокумента.Контрагент = РаспознаваниеДокументов.УбратьОрганизационнуюФорму(СтруктураЗаполнения.ПоставщикНаименование);
	ДанныеСозданногоДокумента.ТипРаспознанногоДокумента = Строка(ДокументОбъект.ТипДокумента);
	ДанныеСозданногоДокумента.ИмяКоманды = ИмяКоманды;
	ДанныеСозданногоДокумента.ВариантРегистрации = ВариантРегистрации;
	
	Пакет = Новый Структура;
	Пакет.Вставить("created", ДанныеСозданногоДокумента);
	
	РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(ДокументОбъект.ИдентификаторРезультата, Пакет);
	
	КассовыйЧекСсылка = Чек.Ссылка;
	
	// Конец БП
	
КонецПроцедуры

Процедура КоличествоЧековЗарегистрировано(ЧековЗарегистрировано) Экспорт
	
	// БП
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КассовыйЧекПодотчетногоЛица.Ссылка) КАК Количество
	|ИЗ
	|	РегистрСведений.СканированиеКассовыхЧековПодотчетныхЛиц КАК СканированиеКассовыхЧековПодотчетныхЛиц
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КассовыйЧекПодотчетногоЛица КАК КассовыйЧекПодотчетногоЛица
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыПоКассовымЧекамПодотчетныхЛиц КАК ДокументыПоКассовымЧекамПодотчетныхЛиц
	|			ПО ДокументыПоКассовымЧекамПодотчетныхЛиц.КассовыйЧек = КассовыйЧекПодотчетногоЛица.Ссылка
	|		ПО СканированиеКассовыхЧековПодотчетныхЛиц.КассовыйЧек = КассовыйЧекПодотчетногоЛица.Ссылка
	|ГДЕ
	|	КассовыйЧекПодотчетногоЛица.Статус = &Статус
	|	И ДокументыПоКассовымЧекамПодотчетныхЛиц.Документ ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыКассовыхЧековПодотчетныхЛиц.Получен);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЧековЗарегистрировано = Выборка.Количество;
	КонецЕсли;
	
	// Конец БП
	
КонецПроцедуры

Процедура ОбработкаСозданияКорректировкиПоступленияРеализации(СозданныеДокументы, ОбработкаОбъект) Экспорт
	
	// БП
	
	ВыборкаПоДокументам = РаспознаваниеДокументовБП.ВыборкаДляСозданияКорректировок(ОбработкаОбъект);
	
	Если Не ВыборкаПоДокументам.Количество() Тогда
		ВызватьИсключение НСтр("ru = 'В таблице документа не выбрано ни одного документа-основания'");
	КонецЕсли;
	
	ТолькоОдноОснование = (ВыборкаПоДокументам.Количество() = 1);
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
	
		ОбъектКорректировки = Документы.КорректировкаПоступления.СоздатьДокумент();
		ОбъектКорректировки.Заполнить(ВыборкаПоДокументам.ДокументОснованиеКорректировки);
		
		ОбъектКорректировки.ДатаВходящегоДокумента = ОбработкаОбъект.ДатаДокумента;
		ОбъектКорректировки.НомерВходящегоДокумента = ОбработкаОбъект.НомерДокумента;
		
		Если ТолькоОдноОснование Тогда
			// Это случай когда корректировка одного поступления и это УКД статус 1
			ОбъектКорректировки.ЭтоУниверсальныйДокумент = Истина;
		КонецЕсли;
		
		ВыборкаПоСтрокам = ВыборкаПоДокументам.Выбрать();
		
		Пока ВыборкаПоСтрокам.Следующий() Цикл
			
			ИмяТаблицыДокумента = ?(ВыборкаПоСтрокам.ЭтоУслуга, "Услуги", "Товары");
			
			Если Число(ВыборкаПоСтрокам.НомерИсходнойСтроки) < 1
			 Или Число(ВыборкаПоСтрокам.НомерИсходнойСтроки) > ОбъектКорректировки[ИмяТаблицыДокумента].Количество() Тогда
				
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'В корректируемом документе %1 не найдена строка %2'"),
					ВыборкаПоДокументам.Представление,
					ВыборкаПоСтрокам.НомерИсходнойСтроки);
					
			КонецЕсли;
			
			ИзменяемаяСтрока = ОбъектКорректировки[ИмяТаблицыДокумента][ВыборкаПоСтрокам.НомерИсходнойСтроки - 1];
			
			ЗаполнитьЗначенияСвойств(ИзменяемаяСтрока, ВыборкаПоСтрокам);
			
		КонецЦикла;
		
		Если Не ОбъектКорректировки.ПроверитьЗаполнение() Тогда
			
			Сообщения = ПолучитьСообщенияПользователю(Истина);
			ТекстыСообщений = Новый Массив;
			Для Каждого Сообщение Из Сообщения Цикл
				ТекстыСообщений.Добавить(Сообщение.Текст);
			КонецЦикла;
			ВызватьИсключение СтрСоединить(ТекстыСообщений, Символы.ПС);
			
		КонецЕсли;
		
		ОбъектКорректировки.Записать(РежимЗаписиДокумента.Проведение);
		
		СозданныеДокументы.Добавить(ОбъектКорректировки.Ссылка);
		
	КонецЦикла;
	
	// КСФ +
	
	Если Не ТолькоОдноОснование И СозданныеДокументы.Количество() Тогда
		ОбъектКСФ = РаспознаваниеДокументовБП.СоздатьКорректировочныйСчетФактуру(СозданныеДокументы, ОбработкаОбъект);
		СозданныеДокументы.Добавить(ОбъектКСФ.Ссылка);
	КонецЕсли;
	
	// КСФ -
	
	РеквизитыРаспознанногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ОбработкаОбъект.Ссылка, "Ссылка, ИмяФайла, ИдентификаторРезультата, ИсходноеИзображение");
	АдресКартинки = ПоместитьВоВременноеХранилище(РеквизитыРаспознанногоДокумента.ИсходноеИзображение.Получить());
	
	Для Каждого СозданныйОбъект Из СозданныеДокументы Цикл
		
		РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ЗаписатьЗначения(
			СозданныйОбъект,
			ОбработкаОбъект.Ссылка,
			Ложь);
			
		РаспознаваниеДокументовСлужебный.ДобавитьПрисоединенныйФайл(РеквизитыРаспознанногоДокумента, СозданныйОбъект, АдресКартинки);
	КонецЦикла;
	
	УдалитьИзВременногоХранилища(АдресКартинки);
	
	// Конец БП
	
КонецПроцедуры

Процедура ОбработкаСозданияВозвратаТоваровПоставщику(СозданныеДокументы, ОбработкаОбъект) Экспорт
	
	ТекстыСообщений = Новый Массив;
	
	СтрокиУслуг = ОбработкаОбъект.Товары.НайтиСтроки(Новый Структура("ЭтоУслуга", Истина));
	Если СтрокиУслуг.Количество() Тогда
		ТекстыСообщений.Добавить(НСтр("ru = 'В таблице документа не должно быть строк с услугами'"));
	КонецЕсли;
	
	Для Каждого Строка Из ОбработкаОбъект.Товары Цикл
		
		Если Строка.Количество - Строка.КоличествоДоКорректировки > 0 Тогда
			ТекстыСообщений.Добавить(СтрШаблон(НСтр("ru = 'В строке %1 увеличивается количество товаров'"), Строка.НомерСтроки));
		КонецЕсли;
		
	КонецЦикла;
	
	Выборка = РаспознаваниеДокументовБП.ВыборкаДляСозданияВозвратаПоставщику(ОбработкаОбъект);
	
	Если Не Выборка.Количество() Тогда
		ТекстыСообщений.Добавить(НСтр("ru = 'В таблице документа не выбрано ни одного документа-основания'"));
	КонецЕсли;
	
	Если ТекстыСообщений.Количество() Тогда
		ВызватьИсключение СтрСоединить(ТекстыСообщений, Символы.ПС);
	КонецЕсли;
	
	ТолькоОдноОснование = (ОбработкаОбъект.ДокументыОснованияКорректировки.Количество() = 1);
	
	ОбъектКорректировки = Документы.ВозвратТоваровПоставщику.СоздатьДокумент();
	ОбъектКорректировки.Заполнить(ОбработкаОбъект.ДокументыОснованияКорректировки[0].ДокументОснование);
	
	Если Не ТолькоОдноОснование Тогда
		ОбъектКорректировки.Сделка = Неопределено;
	КонецЕсли;
	
	ОбъектКорректировки.Товары.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ОбъектКорректировки.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Не ТолькоОдноОснование Тогда
			НоваяСтрока.Сделка = Выборка.ДокументОснованиеКорректировки;
		КонецЕсли;
		
	КонецЦикла;
	
	СчетаУчетаВДокументах.ЗаполнитьПередОтображениемПользователю(ОбъектКорректировки);
	
	Если Не ОбъектКорректировки.ПроверитьЗаполнение() Тогда
		
		Сообщения = ПолучитьСообщенияПользователю(Истина);
		ТекстыСообщений = Новый Массив;
		Для Каждого Сообщение Из Сообщения Цикл
			ТекстыСообщений.Добавить(Сообщение.Текст);
		КонецЦикла;
		ВызватьИсключение СтрСоединить(ТекстыСообщений, Символы.ПС);
		
	КонецЕсли;
	
	ОбъектКорректировки.Записать(РежимЗаписиДокумента.Проведение);
	
	СозданныеДокументы.Добавить(ОбъектКорректировки.Ссылка);
	
	ОбъектКСФ = РаспознаваниеДокументовБП.СоздатьКорректировочныйСчетФактуру(СозданныеДокументы, ОбработкаОбъект);
	СозданныеДокументы.Добавить(ОбъектКСФ.Ссылка);
	
	РеквизитыРаспознанногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ОбработкаОбъект.Ссылка, "Ссылка, ИмяФайла, ИдентификаторРезультата, ИсходноеИзображение");
	АдресКартинки = ПоместитьВоВременноеХранилище(РеквизитыРаспознанногоДокумента.ИсходноеИзображение.Получить());
	
	Для Каждого СозданныйОбъект Из СозданныеДокументы Цикл
		
		РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ЗаписатьЗначения(
			СозданныйОбъект,
			ОбработкаОбъект.Ссылка,
			Ложь);
			
		РаспознаваниеДокументовСлужебный.ДобавитьПрисоединенныйФайл(РеквизитыРаспознанногоДокумента, СозданныйОбъект, АдресКартинки);
	КонецЦикла;
	
	УдалитьИзВременногоХранилища(АдресКартинки);
	
КонецПроцедуры

Процедура СтавкиНДСПоДатеРаспознанногоДокумента(ДатаДокумента, ДоступныеСтавки) Экспорт
	
	До2019Года = ДатаДокумента < '20190101' И ДатаДокумента > '00010101';
	
	СтавкиНДС = Новый Массив;
	Если До2019Года Тогда
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС18);
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС18_118);
	Иначе
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС20);
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС20_120);
	КонецЕсли;
	
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС10);
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС10_110);
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС0);
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
	
	Для Каждого СтавкаНДС Из СтавкиНДС Цикл
		
		Если ДоступныеСтавки.НайтиПоЗначению(СтавкаНДС) = Неопределено Тогда
			ДоступныеСтавки.Добавить(СтавкаНДС);
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ РегистрыСведений.НастройкиУчетаНДС.ВедетсяУчетНДСНаУСН() Тогда
		Возврат;
	КонецЕСли;
	
	Для Каждого ЭлементСписка Из ДоступныеСтавки Цикл
		
		Если ЭлементСписка.Значение = Перечисления.СтавкиНДС.НДС20 Тогда 
			ЭлементСписка.Представление = "20% (5%, 7% при УСН)";
		ИначеЕсли ЭлементСписка.Значение = Перечисления.СтавкиНДС.НДС10 Тогда 
			ЭлементСписка.Представление = "10% (5%, 7% при УСН)";
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОбработкеДокументовСоСкидками(ВидСкидки, Направление, Результат) Экспорт
	
	// Результат:
	// ПостОбработкаНеТребуется
	// ПотребоватьВключениеФункциональныхОпцийПоСкидкам
	// ПересчитатьСкидкиВЦенуНаОтдельныеПозиции
	// УстановитьСкидкиПоДокументуВЦелом
	// УстановитьСкидкиНаОтдельныеПозиции
	
	// БП
	Если ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НеПредоставлена Тогда
		Результат = "ПостОбработкаНеТребуется";
		Возврат;
	КонецЕсли;
	
	Если Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
		
		Если ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции Тогда
			Результат = "УстановитьСкидкиНаОтдельныеПозиции";
		ИначеЕсли ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().ПоДокументуВЦелом Тогда
			Результат = "УстановитьСкидкиПоДокументуВЦелом";
		Иначе
			// Невозможная ситуация
		КонецЕсли;
		
	Иначе // Исходящий
		Результат = "ПересчитатьСкидкиВЦенуНаОтдельныеПозиции";
	КонецЕсли;
	// Конец БП
	
КонецПроцедуры

Процедура ЗаполнитьПредупрежденияПоСкидкам(РеквизитыШапки, Форма) Экспорт
	
КонецПроцедуры

Процедура ДоступноРаспознаваниеЧеков(ДоступноРаспознаваниеЧеков) Экспорт
	
КонецПроцедуры

Процедура ДоступноСозданиеПлатежныхПоручений(ДоступноСозданиеПлатежныхПоручений) Экспорт
	
КонецПроцедуры

Процедура ПриПолученииДанныхВыбораТипыДокументов(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

Процедура ЗаполнитьСтрокиТаблицыТоварыКорректировкиИзОснований(Объект) Экспорт
	
	РаспознаваниеДокументовБП.ЗаполнитьСтрокиТаблицыТоварыКорректировкиИзОснований(Объект, Истина);
	РаспознаваниеДокументовБП.ЗаполнитьСтрокиТаблицыТоварыКорректировкиИзОснований(Объект, Ложь);
	РаспознаваниеДокументовБП.ЗаполнитьСопоставленныеВручнуюСтрокиТаблицыТоварыКорректировки(Объект);
	
КонецПроцедуры

Процедура НайтиИЗаполнитьДокументыОснованияКорректировки(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	НомераДатыДокументов.НомерДокумента КАК НомерВходящегоДокумента,
		|	НомераДатыДокументов.ДатаДокумента КАК ДатаВходящегоДокумента,
		|	НомераДатыДокументов.РаспознанныйТекст КАК РаспознанныйТекст
		|ПОМЕСТИТЬ ВТ_ВходящиеНомераДаты
		|ИЗ
		|	&НомераДатыДокументов КАК НомераДатыДокументов
		|ГДЕ
		|	НомераДатыДокументов.ДокументОснование В (&ПустыеСсылки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
		|	ПоступлениеТоваровУслуг.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
		|	ПоступлениеТоваровУслуг.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ
		|	(ПоступлениеТоваровУслуг.НомерВходящегоДокумента, ПоступлениеТоваровУслуг.ДатаВходящегоДокумента) В
		|			(ВЫБРАТЬ
		|				ВТ_ВходящиеНомераДаты.НомерВходящегоДокумента,
		|				ВТ_ВходящиеНомераДаты.ДатаВходящегоДокумента
		|			ИЗ
		|				ВТ_ВходящиеНомераДаты)
		|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
		|	И ПоступлениеТоваровУслуг.Организация = &Организация";
	
	ПустыеСсылки = Новый Массив;
	Для Каждого Тип Из Метаданные.ОпределяемыеТипы.ДокументОснованиеКорректировкиБРД.Тип.Типы() Цикл
		ПустаяСсылка = Новый(Тип);
		ПустыеСсылки.Добавить(ПустаяСсылка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("НомераДатыДокументов", Объект.ДокументыОснованияКорректировки.Выгрузить());
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ПустыеСсылки", ПустыеСсылки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("НомерДокумента", Выборка.НомерВходящегоДокумента);
		ПараметрыОтбора.Вставить("ДатаДокумента", Выборка.ДатаВходящегоДокумента);
		
		ИзменяемыеСтроки = Объект.ДокументыОснованияКорректировки.НайтиСтроки(ПараметрыОтбора);
		Для Каждого Строка Из ИзменяемыеСтроки Цикл
			Строка.ДокументОснование = Выборка.Ссылка;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписиАвансовогоОтчета(Источник) Экспорт
	
	РаспознаваниеДокументовБП.УстановитьСвязиПрикрепитьИзображения(Источник);
	
КонецПроцедуры

Процедура ПриЗаписиРасходовПредпринимателя(Источник) Экспорт
	
	РаспознаваниеДокументовБП.УстановитьСвязиПрикрепитьИзображения(Источник);
	
КонецПроцедуры

Процедура ПриЗаписиПутевогоЛиста(Источник) Экспорт
	
	РаспознаваниеДокументовБП.УстановитьСвязиПрикрепитьИзображения(Источник);
	
КонецПроцедуры

#КонецОбласти
