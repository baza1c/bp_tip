#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Позволяет дополнить предопределенные правила сдачи отчетов и уплаты налогов
// специфичными для конфигурации правилами.
//
// Параметры:
//  Правила - ТаблицаЗначений - содержит предопределенные правила конфигурации, колонки:
//   Идентификатор      - Строка
//   Представление      - Строка
//   НаименованиеПолное - Строка
//   Порядок            - Число
//   Требования         - ТаблицаЗначений - см. Справочники.ЗадачиБухгалтера.НовыйОписаниеТребования()
//
Процедура ПриПолученииПредопределенныхПравилСдачиОтчетовУплатыНалогов(Правила) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.РасписаниеПоДавнимСобытиям
//
Процедура ПриОпределенииРасписанияПоДавнимСобытиям(Организация, Расписание, ТекущаяДата, НачалоОбзора, КонецОбзора) Экспорт
	
	СрокиСдачиФинОтчетности = Справочники.ПравилаФинОтчетности.СрокиСдачиПоОрганизацииЗаПериод(
		Организация,
		НачалоОбзора,
		КонецОбзора);
	
	ЗадачиБухгалтераБП.ЗаполнитьРасписаниеФинОтчетности(
		Организация,
		СрокиСдачиФинОтчетности,
		Расписание,
		НачалоОбзора,
		КонецОбзора);
	
	ПравилаРегулярныеПлатежи = Справочники.ПравилаРегулярныхПлатежей.ПравилаОрганизации(
		Организация, 
		НачалоОбзора, 
		КонецОбзора);
	
	ЗадачиБухгалтераБП.ЗаполнитьРасписаниеРегулярныеПлатежи(
		Организация, 
		ПравилаРегулярныеПлатежи, 
		Расписание,
		НачалоОбзора, 
		КонецОбзора);
	
	Если Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация)
		И УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, ТекущаяДата) Тогда
		
		ПравилаУплатыИПродленияПатентов = Документы.ОперацияСПатентом.ПравилаУплатыИПродленияПатентов(
			Организация,
			ТекущаяДата);
		
		ЗадачиБухгалтераБП.ЗаполнитьРасписаниеУплатыИПродленияПатентов(
			Организация,
			ПравилаУплатыИПродленияПатентов,
			Расписание);
			
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.РасписаниеПоНедавнимСобытиям
//
Процедура ПриОпределенииРасписанияПоНедавнимСобытиям(Организация, Расписание, НачалоОбзора, КонецОбзора) Экспорт
	
	СрокиСдачиФинОтчетности = Справочники.ПравилаФинОтчетности.СрокиСдачиПоОрганизацииЗаПериод(
		Организация,
		НачалоОбзора,
		КонецОбзора);
	
	ЗадачиБухгалтераБП.ЗаполнитьРасписаниеФинОтчетности(
		Организация,
		СрокиСдачиФинОтчетности,
		Расписание,
		НачалоОбзора,
		КонецОбзора);
	
	ПравилаРегулярныеПлатежи = Справочники.ПравилаРегулярныхПлатежей.ПравилаОрганизации(
		Организация, 
		НачалоОбзора, 
		КонецОбзора);
	
	ЗадачиБухгалтераБП.ЗаполнитьРасписаниеРегулярныеПлатежи(
		Организация, 
		ПравилаРегулярныеПлатежи, 
		Расписание,
		НачалоОбзора, 
		КонецОбзора);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ОписаниеДействия
//
Процедура ПриОпределенииОписанияДействия(ОписаниеДействия, Правило, ПериодСобытия, Организация, ОсобыеСрокиСверкиИмущественныхНалогов) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗадачиСнятияСУчетаВНалоговой
//
Процедура ПриПолученииЗадачСнятияСУчетаВНалоговой(Организации, ЗадачиНачалаРаботы) Экспорт
	
	ШаблонНаименования = НСтр("ru = 'Указать дату закрытия индивидуального предпринимателя'");
	Для Каждого Организация Из РегистрыСведений.ЗадачиБухгалтера.МассивОрганизаций(Организации) Цикл
		
		// Задачи прекращения деятельности доступны только для ИП
		Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗадача = ЗадачиНачалаРаботы.Добавить();
		НоваяЗадача.Организация = Организация;
		НоваяЗадача.Задача = Перечисления.ЗадачиНачалаРаботы.УказатьДатуПрекращенияДеятельности;
		
		НоваяЗадача.Наименование = ШаблонНаименования;
		
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.НовыеЗадачиСнятияСУчета
//
Процедура ПриПолученииНовыхЗадачСнятияСУчета(ОтборОрганизации, ЗадачиНачалаРаботы) Экспорт
	
	НовыеЗадачи = РегистрыСведений.ЗадачиБухгалтера.ЗадачиСнятияСУчетаВНалоговой(ОтборОрганизации);
	
	// Составим список задач
	// Задача ПараметрыУчета в списке должна быть только одна
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗадачиНовыхОрганизаций.Задача КАК Задача,
	|	ЗадачиНовыхОрганизаций.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ЗадачиНовыхОрганизаций.Наименование КАК СТРОКА(150)) КАК Наименование
	|ПОМЕСТИТЬ НовыеЗадачи
	|ИЗ
	|	&НовыеЗадачи КАК ЗадачиНовыхОрганизаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НовыеЗадачи.Организация КАК Организация,
	|	НовыеЗадачи.Задача КАК Правило,
	|	НовыеЗадачи.Наименование КАК Наименование,
	|	ЗначенияУпорядочивания.Порядок КАК Порядок
	|ИЗ
	|	НовыеЗадачи КАК НовыеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК СуществующиеЗадачи
	|		ПО НовыеЗадачи.Организация = СуществующиеЗадачи.Организация
	|			И НовыеЗадачи.Задача = СуществующиеЗадачи.Правило
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ЗадачиНачалаРаботы КАК ЗначенияУпорядочивания
	|		ПО НовыеЗадачи.Задача = ЗначенияУпорядочивания.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО НовыеЗадачи.Организация = Организации.Ссылка
	|			И (Организации.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОСпецрежимахНалогообложения КАК УведомлениеОСпецрежимахНалогообложения
	|		ПО НовыеЗадачи.Организация = УведомлениеОСпецрежимахНалогообложения.Организация
	|			И (УведомлениеОСпецрежимахНалогообложения.ВидУведомления = &ВидУведомления)
	|			И (НЕ УведомлениеОСпецрежимахНалогообложения.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначенияУпорядочивания.Порядок";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НовыеЗадачи", НовыеЗадачи);
	Запрос.УстановитьПараметр("ВидУведомления", Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаР26001);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ЗадачиНачалаРаботы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Наименование = СокрЛП(НоваяСтрока.Наименование);
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗавершитьЗадачуУказанияДатыЗакрытияИП
//
Процедура ОпределитьЗавершитьЗадачуУказанияДатыЗакрытияИП(Завершить) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ЗадачиБухгалтера.Организация = Организации.Ссылка
	|			И (Организации.ДатаРегистрации <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|			И (ЗадачиБухгалтера.Правило = &Правило)
	|			И (НЕ ЗадачиБухгалтера.ВАрхиве)
	|			И (Организации.ДатаЗакрытия <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))");
	
	Запрос.УстановитьПараметр("Правило", Перечисления.ЗадачиНачалаРаботы.УказатьДатуПрекращенияДеятельности);
	
	ЗадачиКЗавершению = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока ЗадачиКЗавершению.Следующий() Цикл
		ВыполнениеЗадачБухгалтераВызовСервера.ПеренестиВАрхив(ЗадачиКЗавершению);
		Завершить = Истина;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.КорректныйПериодВводаДокументов
//
Процедура ЗаполнитьКорректныйПериодВводаДокументов(КорректныйПериод) Экспорт
	
	КорректныйПериод = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ИмяЗадачиБухгалтера
//
Процедура ПолучитьИмяЗадачиБухгалтера(ИмяЗадачи, ВидНалога, Организация, Период) Экспорт
	
	ИмяЗадачи = РасчетыСБюджетом.ИмяЗадачиБухгалтера(ВидНалога);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗаполнитьРасписаниеПоНалогамИСборам
//
Процедура ПолучитьОсобыеСрокиСверкиИмущественныхНалогов(ОсобыеСрокиСверки, Организация) Экспорт
	
	ОсобыеСрокиСверки = РегистрыСведений.СообщенияСРасчетомИмущественныхНалогов.ОсобыеСрокиСверкиИмущественныхНалогов(Организация);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.МассивОрганизаций
//
Процедура ДополнитьИзбранныеРегламентированныеОтчеты(Правила, Организация) Экспорт
	
	РегистрыСведений.ИзбранныеРегламентированныеОтчеты.ДополнитьИзбранныеРегламентированныеОтчеты(Правила, Организация);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ОписаниеДействия
//
Процедура СдвинутьСроки(Срок, ПериодСобытия, Правило, День, ОсобыеСрокиСверкиИмущественныхНалогов, ПараметрыПродленияСроков) Экспорт
	
	ЗадачиБухгалтераБП.СдвинутьСроки(
		Срок, 
		ПериодСобытия, 
		Правило, 
		День, 
		ОсобыеСрокиСверкиИмущественныхНалогов, 
		ПараметрыПродленияСроков);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ОписаниеДействия
//
Процедура ПроверитьЭтоПравилоУплатыВзносовСДоходов(ЭтоПравилоУплатыВзносовСДоходов, Правило) Экспорт
	ЭтоПравилоУплатыВзносовСДоходов = УчетСтраховыхВзносовИП.ЭтоПравилоУплатыВзносовСДоходов(Правило.Ссылка);
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.МассивОрганизаций
//
Процедура ЗаполнитьДоступныеОрганизации(ДоступныеОрганизации) Экспорт
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииРазрешенныеПользователю();
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗадачиНовыхОрганизаций
//
Процедура ПриПолученииЗадачНовыхОрганизаций(ЗадачиОрганизаций) Экспорт
	
	Если Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		НоваяЗадача = ЗадачиОрганизаций.Добавить();
		НоваяЗадача.Задача                    = Перечисления.ЗадачиНачалаРаботы.СписокНалоговОтчетов;
		НоваяЗадача.ОбособленноеПодразделение = Ложь;
		
		НоваяЗадача = ЗадачиОрганизаций.Добавить();
		НоваяЗадача.Задача                    = Перечисления.ЗадачиНачалаРаботы.ВводНачальныхОстатков;
		НоваяЗадача.ОбособленноеПодразделение = Ложь;

		Если ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда
			НоваяЗадача = ЗадачиОрганизаций.Добавить();
			НоваяЗадача.Задача                    = Перечисления.ЗадачиНачалаРаботы.Подключение1СЭДО;
			НоваяЗадача.ОбособленноеПодразделение = Ложь;
		КонецЕсли;
		
		НоваяЗадача = ЗадачиОрганизаций.Добавить();
		НоваяЗадача.Задача                    = Перечисления.ЗадачиНачалаРаботы.СписокНалоговОтчетов;
		НоваяЗадача.ОбособленноеПодразделение = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗадачиОплатыЗадолженностиПоНалогамВзносамИП
//
Процедура ПриПолученииЗадачОплатыЗадолженностиПоНалогамВзносамИП(ЗадачиНачалаРаботы, Организации) Экспорт
	
	Если Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонНаименования = НСтр("ru = 'Ввести начальные остатки по налогам и взносам'");
	Для Каждого Организация Из РегистрыСведений.ЗадачиБухгалтера.МассивОрганизаций(Организации) Цикл
		
		НоваяЗадача = ЗадачиНачалаРаботы.Добавить();
		НоваяЗадача.Организация = Организация;
		НоваяЗадача.Задача = Перечисления.ЗадачиНачалаРаботы.ОплатитьЗадолженностьПоНалогамВзносам;
		
		НоваяЗадача.Наименование = ШаблонНаименования;
		
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗадачиПроверкиВидаДеятельности
//
Процедура ПриПолученииЗадачПроверкиВидаДеятельности(ЗадачиНачалаРаботы, Организации) Экспорт
	
	Если ОбщегоНазначенияБП.ТекущаяДатаНаСервере() > РегистрыСведений.ЗадачиБухгалтера.ДатаПроверкиВидаДеятельности() Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонНаименования = НСтр("ru = 'Проверить основной код ОКВЭД'");
	Для Каждого Организация Из РегистрыСведений.ЗадачиБухгалтера.МассивОрганизаций(Организации) Цикл
		
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ДатаРегистрации, ЮридическоеФизическоеЛицо");
		Если РеквизитыОрганизации.ДатаРегистрации >= НачалоГода(РегистрыСведений.ЗадачиБухгалтера.ДатаПроверкиВидаДеятельности()) Тогда
			Продолжить;
		КонецЕсли;
		
		Если РеквизитыОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
			И Не РегламентированнаяОтчетность.ИПИспользуетТрудНаемныхРаботников(Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗадача = ЗадачиНачалаРаботы.Добавить();
		НоваяЗадача.Организация = Организация;
		НоваяЗадача.Задача = Перечисления.ЗадачиНачалаРаботы.ПроверитьОсновнойВидДеятельности;
		
		НоваяЗадача.Наименование = ШаблонНаименования;
		
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗавершитьЗадачуПроверкиВидаДеятельности
//
Процедура ОпределитьЗавершитьЗадачуПроверкиВидаДеятельности(Организация) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Правило = &Правило
	|	И НЕ ЗадачиБухгалтера.ВАрхиве");
	
	Запрос.УстановитьПараметр("Правило",     Перечисления.ЗадачиНачалаРаботы.ПроверитьОсновнойВидДеятельности);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ЗадачиКЗавершению = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока ЗадачиКЗавершению.Следующий() Цикл
		ВыполнениеЗадачБухгалтераВызовСервера.ПеренестиВАрхив(ЗадачиКЗавершению);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ТипыПравил
//
Процедура ПриПолученииТиповПравил(ТипыПравил) Экспорт
	
	ТипыПравил.Добавить(Тип("СправочникСсылка.ПравилаРегулярныхПлатежей"));
	ТипыПравил.Добавить(Тип("СправочникСсылка.ПроверкиКонтролирующимиОрганами"));
	ТипыПравил.Добавить(Тип("СправочникСсылка.Патенты"));
	ТипыПравил.Добавить(Тип("СправочникСсылка.ПравилаФинОтчетности"));
	ТипыПравил.Добавить(Тип("ДокументСсылка.ВходящийЗапросФССДляРасчетаПособия"));
	ТипыПравил.Добавить(Тип("ДокументСсылка.ИзвещениеФСС"));
	ТипыПравил.Добавить(Тип("ДокументСсылка.ОтказВВозмещенииВыплатРодителямДетейИнвалидов"));
	ТипыПравил.Добавить(Тип("ДокументСсылка.УведомлениеОСтатусеВыплатыПособия"));
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.НовыеЗадачиНачалаРаботы
//
// Возвращает список форм статистики для которых может быть выполнена настройка заполнения
// и которые встречаются в настройках списка налогов и отчетов переданной организации.
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация для которой нужно найти формы.
// Возвращаемое значение:
// ТаблицаЗначений:
//	* Форма- СправочникСсылка.ФормыСтатистики - Форма статистики.
//	* Организация - СправочникСсылка.Организации - Организация.
//
Процедура ПолучитьНастраиваемыеФормыСтатистикиПредоставляемыеОрганизацией(НастраиваемыеФормыСтатистики, ОтборОрганизации) Экспорт
	
	НастраиваемыеФормыСтатистики = ЗаполнениеФормСтатистикиПереопределяемый.НастраиваемыеФормыСтатистикиПредоставляемыеОрганизацией(ОтборОрганизации);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.НовыеЗадачиНачалаРаботы
//
Процедура ОпределитьНеТребуетсяЗадачаНачалаРаботыПодключение1СЭДО(НеТребуетсяПодключение1СЭДО, Организация) Экспорт
	
	НеТребуетсяПодключение1СЭДО = ОбменСКонтрагентами.ОрганизацияПодключена(Организация);
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ОписаниеДействия
//
Процедура ОписаниеДействияЗадачаЗакрытияИП(Правило, Организация, ОписаниеДействия) Экспорт
	
	ДатаНепериодическойЗадачи = ОбщегоНазначенияБПВызовСервераПовтИсп.ДатаЗакрытияОрганизации(Организация);
	Если ЗначениеЗаполнено(ДатаНепериодическойЗадачи) Тогда
		
		Если Правило.ИдентификаторЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиНДС() Тогда
			// Для НДС особый срок задач - конец отчетного периода, квартал
			КонецПериода = КонецКвартала(ДатаНепериодическойЗадачи);
		ИначеЕсли ЗначениеЗаполнено(Правило.СрокМесяцев) Тогда
			// Для задач, которые выполняются со сроком в месяцах, следующих за месяцем от даты задачи:
			// Напр., 25 число месяца, следующего за месяцем выполнения условия
			// (в правилах указывается СрокМесяцев = 1, СрокДней = 25).
			// Соответственно, отсчет начинаем с последней секунды прошлого месяца
			КонецПериода = НачалоМесяца(ДатаНепериодическойЗадачи) - 1;
		Иначе
			КонецПериода = ДатаНепериодическойЗадачи;
		КонецЕсли;
		ОписаниеДействия.Вставить("ПериодСобытия", КонецГода(ДатаНепериодическойЗадачи));
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ОписаниеДействия
//
Процедура ОписаниеДействияЗадачаОплатыУставногоКапитала(Правило, Организация, ОписаниеДействия, КонецПериода, Срок) Экспорт
	
	// регистрируем событие как непериодическое:
	// это нужно для того, чтобы в случае изменения условия выполнения правила
	// не задваивались задачи в списке по разным периодам события.
	ОписаниеДействия.Вставить("ПериодСобытия", '00010101');
	// Срок необходимо переопределить - зависит от точки события.
	ДатаНепериодическойЗадачи = ОбщегоНазначенияБПВызовСервераПовтИсп.ДатаРегистрацииОрганизации(Организация);
	Если Не ЗначениеЗаполнено(ДатаНепериодическойЗадачи) Тогда 
		Срок = КонецПериода;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ОписаниеДействия
//
Процедура ОпределитьУчитыватьНерабочиеДни(УчитыватьНерабочиеДни, Правило, ПараметрыПродленияСроков) Экспорт
	
	РаботаетВНерабочиеДни       = ПараметрыПродленияСроков.РаботаетВНерабочиеДни;
	ПравилаОтчетовВФонды        = ПараметрыПродленияСроков.ПравилаОтчетовВФонды;
	ПеренестиСрокиОтчетовВФонды = ПараметрыПродленияСроков.ПеренестиСрокиОтчетовВФонды;
	ПравилаБезПереносаСроков    = ПараметрыПродленияСроков.ПравилаБезПереносаСроков;
	
	УчитыватьНерабочиеДни = Не РаботаетВНерабочиеДни;
	
	Если Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата Тогда
		УчитыватьНерабочиеДни = Ложь;
		
	ИначеЕсли Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет Тогда
		
		ПолныйПутьКПравилу = СтрШаблон("%1.%2", Правило.ИдентификаторЗадачи, Правило.ИдентификаторПравила);
		
		Если ПравилаОтчетовВФонды.Найти(ПолныйПутьКПравилу) <> Неопределено Тогда
			
			УчитыватьНерабочиеДни = ПеренестиСрокиОтчетовВФонды;
			
		ИначеЕсли ПравилаБезПереносаСроков.Найти(ПолныйПутьКПравилу) <> Неопределено
			Или ПравилаБезПереносаСроков.Найти(Правило.ИдентификаторЗадачи) <> Неопределено Тогда
			
			УчитыватьНерабочиеДни = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.УведомленияПоЭлектроннойПочтеКнопка
//
Процедура ПриПолученииПутиКСпискуЗадач(ПутьКСпискуЗадач) Экспорт
	
	ПутьКСпискуЗадач = ?(ОбщегоНазначенияБП.ЭтоПолныйИнтерфейс(),
		"ОбщаяКоманда.СписокЗадач",
		"Обработка.ПанелиПростойИнтерфейс.Команда.СписокЗадач");
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗаписатьИзменения
//
Процедура ПриОпределенииЗапросаИзменений(Запрос, ОтборДействие) Экспорт
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Расписание.Индекс КАК Индекс,
	|	Расписание.Организация КАК Организация,
	|	Расписание.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Расписание.Правило КАК Правило,
	|	Расписание.ПериодСобытия КАК ПериодСобытия,
	|	Расписание.Срок КАК Срок,
	|	Расписание.НачальныйСрок КАК НачальныйСрок,
	|	Расписание.Наименование КАК Наименование,
	|	Расписание.Действие КАК Действие
	|ПОМЕСТИТЬ Расписание
	|ИЗ
	|	&Расписание КАК Расписание
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Правило,
	|	ПериодСобытия,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ПОМЕСТИТЬ МогутБытьУдалены
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегулярныеПлатежи КАК РегулярныеПлатежи
	|		ПО ЗадачиБухгалтера.Организация = РегулярныеПлатежи.Организация
	|			И ЗадачиБухгалтера.Правило = РегулярныеПлатежи.Правило
	|			И ЗадачиБухгалтера.ПериодСобытия = РегулярныеПлатежи.ПериодСобытия
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПроверкиКонтролирующимиОрганами
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Перечисление.ЗадачиНачалаРаботы
	|	И ЗадачиБухгалтера.Комментарий = """"
	|	И НЕ ЗадачиБухгалтера.СтатусУстановленВручную
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Документ.ВходящийЗапросФССДляРасчетаПособия
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Документ.ИзвещениеФСС
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов
	|	И НЕ ЗадачиБухгалтера.Правило ССЫЛКА Документ.УведомлениеОСтатусеВыплатыПособия
	|	И (ЗадачиБухгалтера.Срок >= &ТекущаяДата
	|		ИЛИ ЗадачиБухгалтера.ПериодСобытия = ДАТАВРЕМЯ(1, 1, 1))
	|	И РегулярныеПлатежи.ПлатежноеПоручение ЕСТЬ NULL
	|	И &ОтборДействие
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Правило,
	|	ПериодСобытия,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расписание.Индекс КАК Индекс
	|ИЗ
	|	Расписание КАК Расписание
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО Расписание.Организация = ЗадачиБухгалтера.Организация
	|			И Расписание.Правило = ЗадачиБухгалтера.Правило
	|			И Расписание.ПериодСобытия = ЗадачиБухгалтера.ПериодСобытия
	|			И Расписание.РегистрацияВНалоговомОргане = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
	|			И Расписание.Действие = ЗадачиБухгалтера.Действие
	|ГДЕ
	|	ЗадачиБухгалтера.Организация ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МогутБытьУдалены.Организация КАК Организация,
	|	МогутБытьУдалены.Правило КАК Правило,
	|	МогутБытьУдалены.ПериодСобытия КАК ПериодСобытия,
	|	МогутБытьУдалены.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	МогутБытьУдалены.Действие КАК Действие
	|ИЗ
	|	МогутБытьУдалены КАК МогутБытьУдалены
	|		ЛЕВОЕ СОЕДИНЕНИЕ Расписание КАК Расписание
	|		ПО МогутБытьУдалены.Организация = Расписание.Организация
	|			И МогутБытьУдалены.Правило = Расписание.Правило
	|			И МогутБытьУдалены.ПериодСобытия = Расписание.ПериодСобытия
	|			И МогутБытьУдалены.РегистрацияВНалоговомОргане = Расписание.РегистрацияВНалоговомОргане
	|			И МогутБытьУдалены.Действие = Расписание.Действие
	|ГДЕ
	|	Расписание.Организация ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расписание.Индекс КАК Индекс,
	|	ЗадачиБухгалтера.Статус КАК Статус
	|ИЗ
	|	Расписание КАК Расписание
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО Расписание.Организация = ЗадачиБухгалтера.Организация
	|			И Расписание.Правило = ЗадачиБухгалтера.Правило
	|			И Расписание.ПериодСобытия = ЗадачиБухгалтера.ПериодСобытия
	|			И Расписание.РегистрацияВНалоговомОргане = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
	|			И Расписание.Действие = ЗадачиБухгалтера.Действие
	|ГДЕ
	|	НЕ ЗадачиБухгалтера.ВАрхиве
	|	И НЕ Расписание.Правило ССЫЛКА Справочник.Патенты
	|	И Расписание.Срок <> ЗадачиБухгалтера.Срок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расписание.Индекс КАК Индекс,
	|	ЗадачиБухгалтера.Статус КАК Статус
	|ИЗ
	|	Расписание КАК Расписание
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО Расписание.Организация = ЗадачиБухгалтера.Организация
	|			И Расписание.Правило = ЗадачиБухгалтера.Правило
	|			И Расписание.ПериодСобытия = ЗадачиБухгалтера.ПериодСобытия
	|			И Расписание.РегистрацияВНалоговомОргане = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
	|			И Расписание.Действие = ЗадачиБухгалтера.Действие
	|ГДЕ
	|	НЕ ЗадачиБухгалтера.ВАрхиве
	|	И Расписание.Правило ССЫЛКА Справочник.Патенты
	|	И (Расписание.Срок <> ЗадачиБухгалтера.Срок
	|			ИЛИ Расписание.Наименование <> ЗадачиБухгалтера.Наименование)";
	
	Если ЗначениеЗаполнено(ОтборДействие) Тогда
		Запрос.УстановитьПараметр("Действие", ОтборДействие);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборДействие", "ЗадачиБухгалтера.Действие = &Действие");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборДействие", "Истина");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗаписатьИзменения
//
Процедура ЗаписатьИзменения(Результат, Расписание, ИзмененыЗаписи) Экспорт
	
	ЗаписиИзмененПатент = Результат[5].Выбрать();
	// Сменим срок и наименование патента
	Пока ЗаписиИзмененПатент.Следующий() Цикл
		Запись = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Расписание[ЗаписиИзмененПатент.Индекс]);
		Запись.Прочитать();
		Если Не Запись.Выбран() Тогда
			// Обработаем задачи, созданные до регистрации действий
			ЗаполнитьЗначенияСвойств(Запись, Расписание[ЗаписиИзмененПатент.Индекс]);
			Запись.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ПустаяСсылка();
			Запись.Прочитать();
			ЗаполнитьЗначенияСвойств(Запись, Расписание[ЗаписиИзмененПатент.Индекс]);
		КонецЕсли;
		Запись.Срок = Расписание[ЗаписиИзмененПатент.Индекс].Срок;
		Запись.Статус = ЗаписиИзмененПатент.Статус;
		Запись.Записать();
		ИзмененыЗаписи = Истина;
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ЗаполнитьРасписаниеПоНалогамИСборам
//
Процедура СоздатьПравилоВыплатаЗарплатыУплатаНДФЛ(МенеджерВременныхТаблиц, Организация, ОтборПравил) Экспорт
	
	УчетЗарплаты.СоздатьПравилоВыплатаЗарплатыУплатаНДФЛ(МенеджерВременныхТаблиц, Организация, ОтборПравил); // Отдельное правило для удержанного НДФЛ
	
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.ИсключитьЗадачуИзУплатыНалога
//
Процедура ПриОпределенииИсключитьЗадачуИзУплатыНалога(ИсключитьЗадачу, Правило, ВидНалогаПравила, ПолноеИмяПравила) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрыСведений.ЗадачиБухгалтера.СкорректироватьРасписаниеСУчетомЗакрытияОрганизации
//
Процедура СкорректироватьРасписаниеСУчетомЗакрытияОрганизации(Организация, Расписание, ТекущаяДата) Экспорт
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Организация", Организация);
	ПараметрыВыполнения.Вставить("ДатаНачала", ТекущаяДата);
	ПараметрыВыполнения.Вставить("ДатаОкончания", КонецГода(ТекущаяДата));
	
	Если Не ЗначениеЗаполнено(ОбщегоНазначенияБПВызовСервераПовтИсп.ДатаЗакрытияОрганизации(Организация)) Тогда
		Возврат;
	КонецЕсли;
	
	// При закрытии организации необходимо заплатить налоги и взносы за отчетный период
	// Если есть задачи с предыдущего года, необходимо им скорректировать сроки
	// Если есть задачи текущего периода, то их необходимо удалить, т.к. вместо них будут сгененрированы задачи при закрытии организации
	
	НачалоПериода = НачалоГода(ТекущаяДата);
	КонецПериода = КонецГода(ТекущаяДата);
	ЗадачиКУдалению = Новый Массив;
	
	Для Каждого ТекущаяСтрока Из Расписание Цикл
		
		Если ТипЗнч(ТекущаяСтрока.Правило) <> Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
			Продолжить;
		КонецЕсли;
		
		// Необходимо проверить идентификатор правила на "закрывающие" задачи
		СвойстваЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрока.Правило, "Код, Владелец");
		ЭтоЗадачаСнятияСУчета = ВыполнениеЗадачБухгалтераПовтИспНаВызов.ЭтоЗадачаЗакрытияИП(СвойстваЗадачи.Код);
		
		Если ТекущаяСтрока.ПериодСобытия < НачалоПериода И Не ЭтоЗадачаСнятияСУчета Тогда
			// Корректируем срок задачи предыдущего периода на срок созданной задачи для закрытия организации
			СрокЗадачи = ЗадачиБухгалтераБП.СрокЗакрывающейЗадачиОрганизации(Расписание, ТекущаяСтрока.ПериодСобытия, ТекущаяСтрока.Действие, СвойстваЗадачи.Владелец);
			Если ЗначениеЗаполнено(СрокЗадачи) Тогда
				ТекущаяСтрока.Срок = СрокЗадачи;
			КонецЕсли;
		ИначеЕсли ТекущаяСтрока.ПериодСобытия < КонецПериода И Не ЭтоЗадачаСнятияСУчета Тогда
			// Убираем задачи текущего периода, напр. задачи авансовых платежей со сроком больше срока задачи при закрытии организации
			ЗадачиКУдалению.Добавить(ТекущаяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоСтрок = ЗадачиКУдалению.Количество();
	Для НомерСтроки = 1 По КоличествоСтрок Цикл
		Индекс = КоличествоСтрок - НомерСтроки;
		Расписание.Удалить(ЗадачиКУдалению[Индекс]);
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ЗаполнитьПравилаОрганизации
//
Процедура СоздатьПравилаУплатыНалоговНаМестах(МенеджерВременныхТаблиц, Организация, ИмяЗадачи = "", ТекущийПериод = Неопределено) Экспорт
	
	РегистрыСведений.ПорядокУплатыНалоговНаМестах.СоздатьПравилаУплатыНалоговНаМестах(
		МенеджерВременныхТаблиц,
		Организация,
		ИмяЗадачи,
		ТекущийПериод);
		
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.Форма.ЗадачиВАрхиве.ПриСозданииНаСервере
//
Процедура УстановитьОтборПоОсновнойОрганизации(Форма, ИмяСписка = "Список", ИмяРеквизита = "Организация", ЗначениеОтбора = Неопределено) Экспорт
	ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(Форма, ИмяСписка, ИмяРеквизита, ЗначениеОтбора);
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.Форма.ЗадачиВАрхиве.ПриСозданииНаСервере
//
Процедура ПриСозданииНаСервереЗадачиВАрхиве(Форма) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.Форма.ЗадачиВАрхиве.СписокПередЗагрузкойПользовательскихНастроекНаСервере
//
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Список, Настройки) Экспорт
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.Форма.ЗадачиВАрхиве.УбратьИзВыполненных
//
Процедура УбратьИзВыполненныхЕдиныйНалоговыйСчет(Задача) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ОбработкаЗаполнения
//
Процедура ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения, ИмяРеквизитаОрганизация = "Организация") Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаписатьДанные
//
Процедура СоздатьНаборЗаписейПорядокУплатыНалоговНаМестах(НаборЗаписей, Организация) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаписатьДанные
//
Процедура ДобавитьЗаписьПорядокУплатыНалоговНаМестах(НаборЗаписей, Организация, Строка, ЗначениеПараметра) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаписатьДанные
//
Процедура ОбработатьЗаписиПорядокУплатыНалоговНаМестах(НаборЗаписей, Организация) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаписатьДанные
//
Процедура ОбновитьИЗаполнитьИзбранныеРегламентированныеОтчеты(Организация, Дерево) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаписатьДанные
//
Процедура НалогиОтчетыЗаписатьДанные(Организация, ИзмененыРегулярныеЗадачи) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаписатьВыборочноДополнительныеНастройки
//
Процедура ЗаписатьДополнительныеНастройкиНалогиОтчеты(Организация, Строка, ПараметрыДетальнойНастройки, ЗначенияДетальнойНастройки) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаписатьВыборочноДополнительныеНастройки
//
Процедура ЗаписатьДополнительныеНастройкиНалогиОтчетыПорядокУплатыНалоговНаМестах(Организация, Строка, ЗначенияДетальнойНастройки) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.ЗаполнитьПараметрыДетальнойНастройки
//
Процедура ЗаписатьПараметрыНастройкиНалогиОтчетыПорядокУплатыНалоговНаМестах(ПараметрыДетальнойНастройки, Организация) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.Форма.Настройка.ПриСозданииНаСервере
//
Процедура ПроверитьПравоРедактированияНалогиОтчеты(Право) Экспорт
КонецПроцедуры

// Подробнее
// См. РегистрСведений.НалогиОтчеты.Форма.Настройка.ОбработатьРезультатДетальнойНастройки
//
Процедура УстановитьОписаниеПорядокУплатыНалоговНаМестах(НастройкиУплатыНалога) Экспорт
КонецПроцедуры

// Подробнее
// См. Справочник.ПравилаПредставленияОтчетовУплатыНалогов.СоздатьАктуальныеПравила
//
Процедура ДополнитьЗапросПриПолученииАктуальныхПравил(Запрос, ТекстЗапроса) Экспорт
КонецПроцедуры

// Подробнее
// См. Справочник.ЗадачиБухгалтера.ЗаполнитьТребованиямиНормативныхДокументов
//
// Определяет массив видов действий календаря бухгалтера, имеющих смысл для конфигурации
//
Процедура ПолучитьДоступныеВидыДействийКалендаряБухгалтера(МассивДоступныхДействий) Экспорт
КонецПроцедуры

// Подробнее
// См. Справочник.ЗадачиБухгалтера.ЗаполнитьТребованиямиНормативныхДокументов
//
// Заполняет идентификаторы правил и задач для отбора правил - требований по сдаче отчетов, исполняемых текущей конфигурацией
//
// Параметры:
//  ИдентификаторыТребований: ТаблицаЗначений
//  Колонки:
//   * ИдентификаторПравила - Строка
//   * ИдентификаторЗадачи  - Строка
//
Процедура ЗаполнитьКорректныеИдентификаторыТребованийОтчетов(ИдентификаторыТребований) Экспорт
КонецПроцедуры

// Подробнее
// См. Справочник.ЗадачиБухгалтера.СоздатьАктуальныеПравила
//
// Создает временную таблицу ПравилаИсключения:
// Правила по сдаче отчетов, не исполняемые текущей конфигурацией
//
// Параметры:
// МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
// Возвращаемое значение: МенеджерВременныхТаблиц
// в него помещена таблица ПравилаИсключения: ТаблицаЗначений
//  Колонки:
//   * Ссылка                  - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * ИдентификаторТребования - Строка
//   * Задача                  - СправочникСсылка.ЗадачиБухгалтера
Процедура ПолучитьПравилаИсключения(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Правила.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПравилаИсключения
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Правила
	|ГДЕ
	|	ЛОЖЬ";
	Запрос.Выполнить();
	
КонецПроцедуры

// Подробнее
// См. Справочник.ЗадачиБухгалтера.ЗаполнитьТребованиямиНормативныхДокументов
//
Процедура ПроверитьКорректностьЗагружаемогоТребования(Некорректное, КорректныеИдентификаторы, МассивДоступныхДействий, ИдентификаторЗадачи, ИдентификаторТребования, Действие) Экспорт
КонецПроцедуры

// Подробнее
// См.  Справочник.ЗадачиБухгалтера.ЗаполнитьТребованиямиНормативныхДокументов
//
Процедура ДополнитьПредопределенныеПравила(Правила, ВерсияПравил) Экспорт
КонецПроцедуры

// Подробнее
// См.  Справочник.ЗадачиБухгалтера.ЗаполнитьТребованиямиНормативныхДокументов
//
Процедура УстановитьВерсиюДополнительныхПравилПредставленияОтчетовУплатыНалогов(ВерсияДополнительныхПравил) Экспорт
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ПриСозданииНаСервере
//
Процедура ПриОпределенииТекстаЗапросаСпискаЗадач(СписокЗадач) Экспорт
	
	СписокЗадач.ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Требования.Владелец.Код = ""НДФЛ_Агент_Уплата""
	|			ТОГДА ""Зарплата""
	|		КОГДА ПравилаРегулярныхПлатежей.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ""Регулярный платеж""
	|		КОГДА Патенты.Ссылка ЕСТЬ НЕ NULL 
	|				И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ТОГДА ""Уплата налога""
	|		КОГДА Патенты.Ссылка ЕСТЬ НЕ NULL 
	|				И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|			ТОГДА ""Отчет""
	|		КОГДА Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ТОГДА ""Уплата налога""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ТипЗадачи,
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЕСТЬNULL(Организации.Наименование, """") КАК ОрганизацияНаименование,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	Требования.Код КАК ИдентификаторПравила,
	|	Требования.Владелец КАК Задача,
	|	Требования.Владелец.Код КАК ИдентификаторЗадачи,
	|	ЕСТЬNULL(Требования.Владелец.РеквизитДопУпорядочивания, 0) КАК РегулярнаяЗадачаПорядок,
	|	ВЫБОР
	|		КОГДА ПроверкиКонтролирующимиОрганами.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Проверка)
	|		КОГДА Патенты.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ЕСТЬNULL(ЗадачиБухгалтера.Действие, ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога))
	|		КОГДА ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ТребованиеФНС)
	|		КОГДА ТранспортноеСообщение.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ТребованиеФНС)
	|		КОГДА НЕ ПравилаРегулярныхПлатежей.Ссылка ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ЗадачиБухгалтера.Действие, ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ПустаяСсылка))
	|		КОГДА НЕ ПравилаФинОтчетности.Ссылка ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ЗадачиБухгалтера.Действие, ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ФинОтчетность))
	|		КОГДА ЗадачиБухгалтера.Правило ССЫЛКА Документ.ВходящийЗапросФССДляРасчетаПособия
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО)
	|		КОГДА ЗадачиБухгалтера.Правило ССЫЛКА Документ.ИзвещениеФСС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО)
	|		КОГДА ЗадачиБухгалтера.Правило ССЫЛКА Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО)
	|		КОГДА ЗадачиБухгалтера.Правило ССЫЛКА Документ.УведомлениеОСтатусеВыплатыПособия
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО)
	|		ИНАЧЕ ЕСТЬNULL(Требования.Действие, ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ПустаяСсылка))
	|	КОНЕЦ КАК Действие,
	|	ЕСТЬNULL(Требования.ОтправкаОтчетаОграничена, ЛОЖЬ) КАК ОтправкаОтчетаОграничена,
	|	ВЫБОР
	|		КОГДА &Подключена1СОтчетность
	|				И Требования.ОтправкаОтчетаОграничена = ИСТИНА
	|			ТОГДА 8
	|		КОГДА ПравилаРегулярныхПлатежей.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 3
	|		КОГДА Патенты.Ссылка ЕСТЬ НЕ NULL 
	|				И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ТОГДА 3
	|		КОГДА Патенты.Ссылка ЕСТЬ НЕ NULL 
	|				И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|			ТОГДА 2
	|		КОГДА ПроверкиКонтролирующимиОрганами.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 4
	|		КОГДА ПравилаФинОтчетности.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 2
	|		КОГДА ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 10
	|		КОГДА ТранспортноеСообщение.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 10
	|		КОГДА ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО)
	|			ТОГДА 10
	|		КОГДА Требования.Действие ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|			ТОГДА 2
	|		КОГДА Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|				ИЛИ Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж)
	|			ТОГДА 3
	|		КОГДА Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата)
	|			ТОГДА 7
	|		КОГДА Требования.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление)
	|			ТОГДА 9
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КартинкаЗадачи,
	|	ЕСТЬNULL(Требования.Действие.Порядок, 0) КАК ДействиеПорядок,
	|	ЕСТЬNULL(Требования.РеквизитДопУпорядочивания, 0) КАК ПравилоПорядок,
	|	ЕСТЬNULL(Требования.ЕстьИнформацияНаИТС, ЛОЖЬ) КАК ЕстьИнформацияНаИТС,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ВЫБОР
	|		КОГДА ЗадачиБухгалтера.НачальныйСрок > &ТекущаяДата
	|				И НЕ ЗадачиБухгалтера.СтатусУстановленВручную
	|			ТОГДА ЗадачиБухгалтера.НачальныйСрок
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК НачальныйСрок,
	|	ВЫБОР
	|		КОГДА Требования.Ссылка ЕСТЬ NULL
	|				И ПравилаРегулярныхПлатежей.Ссылка ЕСТЬ NULL
	|				И ПравилаФинОтчетности.Ссылка ЕСТЬ NULL
	|				И ПроверкиКонтролирующимиОрганами.Ссылка ЕСТЬ NULL
	|				И Патенты.Ссылка ЕСТЬ NULL
	|				И ТранспортноеСообщение.Ссылка ЕСТЬ NULL
	|				И ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка ЕСТЬ NULL
	|			ТОГДА 999
	|		ИНАЧЕ РАЗНОСТЬДАТ(&ТекущаяДата, ЗадачиБухгалтера.Срок, ДЕНЬ)
	|	КОНЕЦ КАК ОсталосьДней,
	|	ЗадачиБухгалтера.НачалоВыполнения КАК НачалоВыполнения,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЕСТЬNULL(ЗадачиНачалаРаботы.Порядок, 0) КАК ЗадачиНачалаРаботыПорядок,
	|	ЕСТЬNULL(ПроверкиКонтролирующимиОрганами.ПлановаяПроверка, ЛОЖЬ) КАК ПлановаяПроверка,
	|	0 КАК ЗавершитьЗадачу,
	|	ВЫБОР
	|		КОГДА ЗадачиБухгалтера.СтатусУстановленВручную
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК РучнойСтатусКартинка,
	|	ЕСТЬNULL(ЗадачиБухгалтера.СтатусУстановленВручную, ЛОЖЬ) КАК СтатусУстановленВручную,
	|	ЕСТЬNULL(ЗадачиБухгалтера.Комментарий, """") КАК Комментарий,
	|	ЕСТЬNULL(ЗадачиБухгалтера.ПользовательУстановившийСтатус.Представление, """") КАК ПользовательУстановившийСтатус,
	|	ЗадачиБухгалтера.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Требования
	|		ПО ЗадачиБухгалтера.Правило = Требования.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ЗадачиНачалаРаботы КАК ЗадачиНачалаРаботы
	|		ПО ЗадачиБухгалтера.Правило = ЗадачиНачалаРаботы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРегулярныхПлатежей КАК ПравилаРегулярныхПлатежей
	|		ПО ЗадачиБухгалтера.Правило = ПравилаРегулярныхПлатежей.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаФинОтчетности КАК ПравилаФинОтчетности
	|		ПО ЗадачиБухгалтера.Правило = ПравилаФинОтчетности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Патенты КАК Патенты
	|		ПО ЗадачиБухгалтера.Правило = Патенты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроверкиКонтролирующимиОрганами КАК ПроверкиКонтролирующимиОрганами
	|		ПО ЗадачиБухгалтера.Правило = ПроверкиКонтролирующимиОрганами.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов КАК ДокументыРеализацииПолномочийНалоговыхОрганов
	|		ПО ЗадачиБухгалтера.Правило = ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	|		ПО ЗадачиБухгалтера.Правило = ТранспортноеСообщение.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ЗадачиБухгалтера.Организация = Организации.Ссылка
	|ГДЕ
	|	НЕ ЗадачиБухгалтера.ВАрхиве
	|	И НЕ ЕСТЬNULL(Требования.ВыполняетсяЕдинымПомощником, ЛОЖЬ)
	|{ГДЕ
	|	(ЕСТЬNULL(ЗадачиНачалаРаботы.Ссылка, ЗНАЧЕНИЕ(Перечисление.ЗадачиНачалаРаботы.ПустаяСсылка))) КАК ЗадачаНачалаРаботы}";
		
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадачНастройка.ПриСозданииНаСервере
//
Процедура ПриОпределенииПоказыватьНалогиИОтчеты(ПоказыватьНалогиИОтчеты) Экспорт
	
	ПоказыватьНалогиИОтчеты = ПолучитьФункциональнуюОпцию("ИнтерфейсТакси")
		Или ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой")
		Или ПолучитьФункциональнуюОпцию("ИнтерфейсБухгалтерия77");
	
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадачНастройка.ПриСозданииНаСервере
//
Процедура ПриОпределенииПоказыватьСинхронизациюСКалендаремGoogle(ПоказыватьСинхронизацию) Экспорт
	ПоказыватьСинхронизацию = СинхронизацияСКалендаремGoogle.ДоступнаНастройкаСинхронизации();
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадачНастройка.ПриСозданииНаСервере
//
Процедура ПриСозданииНаСервереСписокЗадачНастройка(Форма) Экспорт
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадачНастройка.ПроверитьСистемуНалогообложения
//
Процедура ПроверитьСистемуНалогообложенияСписокЗадачНастройка(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Если Справочники.Организации.КоличествоОрганизаций() = 1 Тогда
		Элементы.НалогиИОтчеты.Заголовок = ОрганизацииФормыКлиентСервер.ПредставлениеСистемыНалогообложения(
			Справочники.Организации.ОрганизацияПоУмолчанию());
	Иначе
		Элементы.НалогиИОтчеты.Заголовок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадачНастройка.ПроверитьСостояниеСинхронизацииСКалендаремGoogle
//
Процедура ПроверитьСостояниеСинхронизацииСКалендаремGoogle(РезультатПроверки, Пользователь, Форма) Экспорт
	
	РезультатПроверки = Неопределено;
	
	НастройкиСинхронизации = СинхронизацияСКалендаремGoogle.НастройкиСинхронизации(Пользователь);
	
	Если Не НастройкиСинхронизации.Включено Тогда
		Форма.Элементы.НастройкаСинхронизацииСКалендаремGoogle.Заголовок = НСтр("ru = 'Выключено'");
	Иначе
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("ВыполнитьСинхронизациюПослеПроверки", Ложь);
		
		ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(Форма.УникальныйИдентификатор);
		ПараметрыВыполненияВФоне.Вставить("НаименованиеФоновогоЗадания", НСтр("ru = 'Проверка доступа к календарю Google'"));
		ПараметрыВыполненияВФоне.Вставить("ОжидатьЗавершение", 0);
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
			"СинхронизацияСКалендаремGoogle.ПроверитьДоступностьСинхронизацииВФоне",
			ПараметрыПроцедуры,
			ПараметрыВыполненияВФоне);
		
		Если ДлительнаяОперация.Статус = "Выполняется" Тогда
			Форма.Элементы.НастройкаСинхронизацииСКалендаремGoogle.Заголовок = Неопределено;
			РезультатПроверки = ДлительнаяОперация;
		ИначеЕсли ДлительнаяОперация.Статус = "Выполнено" Тогда
			Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
			Форма.Элементы.НастройкаСинхронизацииСКалендаремGoogle.Заголовок = 
				?(Результат.СинхронизацияДоступна,
					НСтр("ru = 'Включено'"),
					НСтр("ru = 'Выключено'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ПриСозданииНаСервере
//
Процедура ПриСозданииНаСервереСписокЗадач(Форма) Экспорт
	
	Форма.Элементы.Организация.Видимость =
		ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийБухгалтерскийУчет");
		
	Форма.Элементы.ОрганизацияПредставление.Видимость =
		ПолучитьФункциональнуюОпцию("НеИспользоватьНесколькоОрганизацийБухгалтерскийУчет");
	
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ПриСозданииНаСервере
//
Процедура ПроверитьЭтоИнтерфейсИнтеграцииСБанком(ИнтерфейсИнтеграцииСБанками) Экспорт
	ИнтерфейсИнтеграцииСБанками = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ПриСозданииНаСервере
//
Процедура ПроверитьНастроенОбменССервисомАУСН(НастроенОбменССервисомАУСН) Экспорт
	НастроенОбменССервисомАУСН = ИнтеграцияАУСН.НастроенОбменССервисомАУСН();
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ПриСозданииНаСервере
//
Процедура ПроверитьНастроенОбменССервисомЛКАУСН(НастроенОбменССервисомЛКАУСН) Экспорт
	НастроенОбменССервисомЛКАУСН = ИнтеграцияАУСН.НастроенОбменССервисомЛКАУСН();
КонецПроцедуры

// Определяет, поддерживает ли текущая конфигурация 1С:ФинОтчетность
//
Процедура ПроверитьПоддерживается1СФинОтчетность(Поддерживается1СФинОтчетность) Экспорт
	// Данная конфигурация поддерживает 1С:ФинОтчетность
	Поддерживается1СФинОтчетность = Истина;
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.СписокЗадачПередЗагрузкойПользовательскихНастроекНаСервере
//
// Заменяет отбор, установленный пользователем в сохраненной настройке списка, на отбор, установленный программно при создании формы списка.
// Вызывается при восстановлении пользовательских настроек динамического списка
// из обработчика списка ПередЗагрузкойПользовательскихНастроекНаСервере.
//
// Параметры:
//  Список      - ДинамическийСписок - Динамический список, для которого устанавливается отбор.
//  Настройки   - ПользовательскиеНастройкиКомпоновкиДанных - Восстанавливаемые настройки списка.
//  ИмяОтбора   - Строка - Имя элемента отбора.
//
Процедура ВосстановитьОтборСписка(Список, Настройки, ИмяОтбора) Экспорт
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.УстановитьУсловноеОформление
//
// Добавляет в коллекцию оформляемых полей компоновки данных новое поле
//
// Параметры:
//	КоллекцияОформляемыхПолей 	- коллекция оформляемых полей КД
//	ИмяПоля                   - Строка - имя поля
//
// Возвращаемое значение:
//	ОформляемоеПолеКомпоновкиДанных - созданное поле
//
// Пример:
// 	Форма.УсловноеОформление.Элементы[0].Поля
//
Процедура ДобавитьОформляемоеПоле(КоллекцияОформляемыхПолей, ИмяПоля) Экспорт
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(КоллекцияОформляемыхПолей, ИмяПоля);
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ПозицияПоследнейПросроченнойЗаписи
//
Процедура ПолучитьСтруктуруИзРезультатаЗапроса(СтруктураПараметров, РезультатЗапроса) Экспорт
	СтруктураПараметров = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(РезультатЗапроса);
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ЗавершитьЗадачу
//
Процедура ДополнитьПараметрыКоманды(ПараметрыКоманды) Экспорт
	
	Если ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей") Тогда
		// Проверим есть ли платеж по данному правилу.
		ШаблонПравила = РегистрыСведений.РегулярныеПлатежи.ШаблонПравила(
			ПараметрыКоманды.Организация, ПараметрыКоманды.Правило, ПараметрыКоманды.ПериодСобытия);
		
		Если ШаблонПравила <> Неопределено
			И ШаблонПравила.ПериодСобытия = ПараметрыКоманды.ПериодСобытия Тогда
			ПараметрыКоманды.Вставить("Оплачено", Истина);
		Иначе
			ПараметрыКоманды.Вставить("Оплачено", Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ПолучитьБаннерНаСервере
//
Процедура ПолучитьБаннерНаСервере(РезультатПолученияБаннера, СторонниеСертификаты, ПоказатьПредыдущий, Форма) Экспорт
	
	Если Форма.НужноИскатьСертификат И СторонниеСертификаты <> Неопределено Тогда
		Форма.НужноИскатьСертификат = Ложь;
		// Сохраним информацию о сторонних сертификатах.
		ПерсонализированныеПредложенияСервисов.ЗаписатьИнформациюОСтороннихСертификатах(
			Форма.Организация, СторонниеСертификаты);
	КонецЕсли;
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(Форма.УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;

	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", Форма.Организация);
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияСписокЗадач());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);

	РезультатПолученияБаннера = ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
		
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.УстановитьБаннерНаФорме
//
Процедура УстановитьБаннерНаФорме(Форма, АдресРезультата) Экспорт
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(Форма, АдресРезультата);
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ДанныеОрганизации
//
Процедура ПолучитьНужноИскатьСертификат(НужноИскатьСертификат, Организация) Экспорт
	НужноИскатьСертификат = ПерсонализированныеПредложенияСервисов.НужноИскатьСертификат(Организация);
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.НастройкиОформленияСпискаЗадач
//
Процедура ДополнитьПоложительныйРезультатЗадачи(ПоложительныйРезультатЗадачи) Экспорт
	ПоложительныйРезультатЗадачи.Добавить(Строка(Перечисления.СостоянияБанковскихДокументов.Оплачено));
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.НастройкиОформленияСпискаЗадач
//
Процедура ДополнитьОтрицательныйРезультатЗадачи(ОтрицательныйРезультатЗадачи) Экспорт
	ОтрицательныйРезультатЗадачи.Добавить(Строка(Перечисления.СостоянияБанковскихДокументов.Отклонено));
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.ЗаполнитьРасписаниеПоНалогамИСборам
//
Процедура ПолучитьПараметрыПродленияСроков(ПараметрыПродленияСроков, Организация) Экспорт
	
	ПараметрыПродленияСроков = РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов.ПараметрыПродленияСроков(Организация);
	
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадачВводКомментария.ПриСозданииНаСервере
//
Процедура ПриСозданииНаСервереСписокЗадачВводКомментария(Форма) Экспорт
	ЗадачиБухгалтераБП.МобильныйКлиентАдаптацияФормыВводКомментария(Форма);
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.ЗаполнитьРасписаниеДокументыСЭДО
//
Процедура ЗаполнитьРасписаниеДокументыСЭДО(Запрос, Расписание) Экспорт
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ВходящийЗапросФССДляРасчетаПособияНаименование КАК Наименование,
	|	&ВходящийЗапросФССДляРасчетаПособияНаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ВходящийЗапросФССДляРасчетаПособия.Организация КАК Организация,
	|	ВходящийЗапросФССДляРасчетаПособия.Ссылка КАК Правило,
	|	ВходящийЗапросФССДляРасчетаПособия.Дата КАК ПериодСобытия,
	|	ВходящийЗапросФССДляРасчетаПособия.Дата КАК НачалоВыполнения,
	|	ВЫБОР
	|		КОГДА ВходящийЗапросФССДляРасчетаПособия.Обработан
	|			ТОГДА ЕСТЬNULL(ЗадачиБухгалтера.Срок, ДАТАВРЕМЯ(1, 1, 1))
	|		ИНАЧЕ ВходящийЗапросФССДляРасчетаПособия.МаксимальнаяДатаОтвета
	|	КОНЕЦ КАК Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО) КАК Действие,
	|	ИСТИНА КАК УчитыватьНерабочиеДни
	|ИЗ
	|	Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФССДляРасчетаПособия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО ВходящийЗапросФССДляРасчетаПособия.Ссылка = ЗадачиБухгалтера.Правило
	|ГДЕ
	|	НЕ ВходящийЗапросФССДляРасчетаПособия.ПометкаУдаления
	|	И ВходящийЗапросФССДляРасчетаПособия.Страхователь = &Организация
	|	И ВЫБОР
	|			КОГДА ВходящийЗапросФССДляРасчетаПособия.Обработан
	|				ТОГДА ЕСТЬNULL(ЗадачиБухгалтера.Срок, ДАТАВРЕМЯ(1, 1, 1))
	|			КОГДА ВходящийЗапросФССДляРасчетаПособия.МаксимальнаяДатаОтвета = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ВходящийЗапросФССДляРасчетаПособия.Дата
	|			ИНАЧЕ ВходящийЗапросФССДляРасчетаПособия.МаксимальнаяДатаОтвета
	|		КОНЕЦ МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ИзвещениеФССНаименование,
	|	&ИзвещениеФССНаименованиеСокращенное,
	|	ИзвещениеФСС.Организация,
	|	ИзвещениеФСС.Ссылка,
	|	ИзвещениеФСС.Дата,
	|	ИзвещениеФСС.Дата,
	|	ВЫБОР
	|		КОГДА ИзвещениеФСС.Обработано
	|			ТОГДА ИзвещениеФСС.МаксимальнаяДатаОтправкиРеестра
	|		КОГДА НЕ ИзвещениеФСС.Загружено
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		КОГДА ИзвещениеФСС.Загружено
	|				И (НЕ ИзвещениеФСС.ПодтверждениеПолученоФСС
	|					ИЛИ ИзвещениеФСС.ДатаОтправкиПодтверждения = ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА ИзвещениеФСС.МаксимальнаяДатаПодтверждения
	|		ИНАЧЕ ИзвещениеФСС.МаксимальнаяДатаОтправкиРеестра
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ИСТИНА
	|ИЗ
	|	Документ.ИзвещениеФСС КАК ИзвещениеФСС
	|ГДЕ
	|	НЕ ИзвещениеФСС.ПометкаУдаления
	|	И ИзвещениеФСС.Организация = &Организация
	|	И ВЫБОР
	|			КОГДА ИзвещениеФСС.Обработано
	|				ТОГДА ИзвещениеФСС.МаксимальнаяДатаОтправкиРеестра
	|			КОГДА НЕ ИзвещениеФСС.Загружено
	|				ТОГДА ИзвещениеФСС.Дата
	|			КОГДА ИзвещениеФСС.Загружено
	|					И (НЕ ИзвещениеФСС.ПодтверждениеПолученоФСС
	|						ИЛИ ИзвещениеФСС.ДатаОтправкиПодтверждения = ДАТАВРЕМЯ(1, 1, 1))
	|				ТОГДА ИзвещениеФСС.МаксимальнаяДатаПодтверждения
	|			ИНАЧЕ ИзвещениеФСС.МаксимальнаяДатаОтправкиРеестра
	|		КОНЕЦ МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&УведомлениеОСтатусеВыплатыПособияНаименование,
	|	&УведомлениеОСтатусеВыплатыПособияНаименованиеСокращенное,
	|	УведомлениеОСтатусеВыплатыПособия.Страхователь,
	|	УведомлениеОСтатусеВыплатыПособия.Ссылка,
	|	УведомлениеОСтатусеВыплатыПособия.Дата,
	|	УведомлениеОСтатусеВыплатыПособия.Дата,
	|	ВЫБОР
	|		КОГДА НЕ УведомлениеОСтатусеВыплатыПособия.Загружен
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ УведомлениеОСтатусеВыплатыПособия.МаксимальнаяДатаПодтверждения
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ИСТИНА
	|ИЗ
	|	Документ.УведомлениеОСтатусеВыплатыПособия КАК УведомлениеОСтатусеВыплатыПособия
	|ГДЕ
	|	НЕ УведомлениеОСтатусеВыплатыПособия.ПометкаУдаления
	|	И УведомлениеОСтатусеВыплатыПособия.Страхователь = &Организация
	|	И ВЫБОР
	|			КОГДА НЕ УведомлениеОСтатусеВыплатыПособия.Загружен
	|				ТОГДА УведомлениеОСтатусеВыплатыПособия.Дата
	|			ИНАЧЕ УведомлениеОСтатусеВыплатыПособия.МаксимальнаяДатаПодтверждения
	|		КОНЕЦ МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ОтказВВозмещенииВыплатРодителямДетейИнвалидовНаименование,
	|	&ОтказВВозмещенииВыплатРодителямДетейИнвалидовНаименованиеСокращенное,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Страхователь,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Ссылка,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Дата,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Дата,
	|	ВЫБОР
	|		КОГДА НЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Загружен
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.МаксимальнаяДатаПодтверждения
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ИСТИНА
	|ИЗ
	|	Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов КАК ОтказВВозмещенииВыплатРодителямДетейИнвалидов
	|ГДЕ
	|	НЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.ПометкаУдаления
	|	И ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Страхователь = &Организация
	|	И ВЫБОР
	|			КОГДА НЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Загружен
	|				ТОГДА ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Дата
	|			ИНАЧЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.МаксимальнаяДатаПодтверждения
	|		КОНЕЦ МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодСобытия";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = Расписание.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		Если Не ЗначениеЗаполнено(Выборка.Срок) Тогда // Заполняем минимальной из всех возможных датой на ответ
			НоваяЗапись.Срок = СЭДОФСС.СледующийРабочийДень(Выборка.НачалоВыполнения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.ЗаполнитьЗаписьРасписанияДокументСЭДО
//
Процедура ЗаполнитьЗаписьРасписанияДокументСЭДО(Запрос, Расписание) Экспорт
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ВходящийЗапросФССДляРасчетаПособияНаименование КАК Наименование,
	|	&ВходящийЗапросФССДляРасчетаПособияНаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ВходящийЗапросФССДляРасчетаПособия.Организация КАК Организация,
	|	ВходящийЗапросФССДляРасчетаПособия.Ссылка КАК Правило,
	|	ВходящийЗапросФССДляРасчетаПособия.Дата КАК ПериодСобытия,
	|	ВходящийЗапросФССДляРасчетаПособия.Дата КАК НачалоВыполнения,
	|	ВЫБОР
	|		КОГДА ВходящийЗапросФССДляРасчетаПособия.Обработан
	|			ТОГДА ЕСТЬNULL(ЗадачиБухгалтера.Срок, ДАТАВРЕМЯ(1, 1, 1))
	|		ИНАЧЕ ВходящийЗапросФССДляРасчетаПособия.МаксимальнаяДатаОтвета
	|	КОНЕЦ КАК Срок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО) КАК Действие,
	|	ИСТИНА КАК УчитыватьНерабочиеДни
	|ИЗ
	|	Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФССДляРасчетаПособия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО ВходящийЗапросФССДляРасчетаПособия.Ссылка = ЗадачиБухгалтера.Правило
	|ГДЕ
	|	НЕ ВходящийЗапросФССДляРасчетаПособия.ПометкаУдаления
	|	И ВходящийЗапросФССДляРасчетаПособия.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ИзвещениеФССНаименование,
	|	&ИзвещениеФССНаименованиеСокращенное,
	|	ИзвещениеФСС.Организация,
	|	ИзвещениеФСС.Ссылка,
	|	ИзвещениеФСС.Дата,
	|	ИзвещениеФСС.Дата,
	|	ВЫБОР
	|		КОГДА ИзвещениеФСС.Обработано
	|			ТОГДА ИзвещениеФСС.МаксимальнаяДатаОтправкиРеестра
	|		КОГДА НЕ ИзвещениеФСС.Загружено
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		КОГДА ИзвещениеФСС.Загружено
	|				И (НЕ ИзвещениеФСС.ПодтверждениеПолученоФСС
	|					ИЛИ ИзвещениеФСС.ДатаОтправкиПодтверждения = ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА ИзвещениеФСС.МаксимальнаяДатаПодтверждения
	|		ИНАЧЕ ИзвещениеФСС.МаксимальнаяДатаОтправкиРеестра
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ИСТИНА
	|ИЗ
	|	Документ.ИзвещениеФСС КАК ИзвещениеФСС
	|ГДЕ
	|	НЕ ИзвещениеФСС.ПометкаУдаления
	|	И ИзвещениеФСС.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&УведомлениеОСтатусеВыплатыПособияНаименование,
	|	&УведомлениеОСтатусеВыплатыПособияНаименованиеСокращенное,
	|	УведомлениеОСтатусеВыплатыПособия.Страхователь,
	|	УведомлениеОСтатусеВыплатыПособия.Ссылка,
	|	УведомлениеОСтатусеВыплатыПособия.Дата,
	|	УведомлениеОСтатусеВыплатыПособия.Дата,
	|	ВЫБОР
	|		КОГДА НЕ УведомлениеОСтатусеВыплатыПособия.Загружен
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ УведомлениеОСтатусеВыплатыПособия.МаксимальнаяДатаПодтверждения
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ИСТИНА
	|ИЗ
	|	Документ.УведомлениеОСтатусеВыплатыПособия КАК УведомлениеОСтатусеВыплатыПособия
	|ГДЕ
	|	НЕ УведомлениеОСтатусеВыплатыПособия.ПометкаУдаления
	|	И УведомлениеОСтатусеВыплатыПособия.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ОтказВВозмещенииВыплатРодителямДетейИнвалидовНаименование,
	|	&ОтказВВозмещенииВыплатРодителямДетейИнвалидовНаименованиеСокращенное,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Страхователь,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Ссылка,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Дата,
	|	ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Дата,
	|	ВЫБОР
	|		КОГДА НЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Загружен
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.МаксимальнаяДатаПодтверждения
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО),
	|	ИСТИНА
	|ИЗ
	|	Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов КАК ОтказВВозмещенииВыплатРодителямДетейИнвалидов
	|ГДЕ
	|	НЕ ОтказВВозмещенииВыплатРодителямДетейИнвалидов.ПометкаУдаления
	|	И ОтказВВозмещенииВыплатРодителямДетейИнвалидов.Ссылка = &ДокументСсылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = Расписание.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		Если Не ЗначениеЗаполнено(Выборка.Срок) Тогда // Заполняем минимальной из всех возможных датой на ответ
			НоваяЗапись.Срок = СЭДОФСС.СледующийРабочийДень(Выборка.НачалоВыполнения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. РегистрСведений.ЗадачиБухгалтера.ЗаполнитьДокументыСЭДО
//
Процедура ПроверитьУчетЗарплатыВЭтойПрограмме(УчетЗарплатыВЭтойПрограмме) Экспорт
	УчетЗарплатыВЭтойПрограмме = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
КонецПроцедуры

// Адаптирует форму под мобильный клиент
//
Процедура МобильныйКлиентАдаптацияФормыСпискаЗадач(Форма) Экспорт
	
	Форма.ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	Если Не Форма.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(Форма);

	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(Форма, Элементы.ГруппаШапка);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(Форма, Элементы.ГруппаЗадачиБухгалтера);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(Форма, Элементы.ГруппаВсеЗадачиВыполнены);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(Форма, Элементы.ГруппаОжидание);

	ЗадачиБухгалтераБП.АдаптироватьМКШапкаФормы(Форма);
	ЗадачиБухгалтераБП.АдаптироватьМККоманднаяПанельФормы(Форма);
	ЗадачиБухгалтераБП.АдаптироватьМКСписокФормы(Форма);
	
КонецПроцедуры

Процедура МобильныйКлиентАдаптацияФормыЗадачиВАрхиве(Форма) Экспорт
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	Элементы.ГруппаКоманднаяПанель.Видимость = Ложь;
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормыСправочника(Форма, "Список");
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(Форма, Элементы.Срок);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(Форма, Элементы.ГруппаЗадача, "1");
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(Форма, Элементы.Статус, "2");
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуСоответствияЗадачБухгалтераДляУведомлений(ВидыКодыНалогов) Экспорт
	
	ВидыКодыНалогов = РасчетыСБюджетом.ТаблицаСоответствияЗадачБухгалтераДляУведомлений();
	
КонецПроцедуры

// Подробнее
// См. Справочник.ПравилаПредставленияОтчетовУплатыНалогов.ЗаполнитьПравилаОрганизации
//
Процедура ЗаполнитьУсловияПримененияПравил(Условия, Организация, НачалоПериода, КонецПериода) Экспорт
	
	Условия.Вставить("КадровыеМероприятия", УчетЗарплаты.КадровыеМероприятияЗаПериод(Организация, НачалоПериода, КонецПериода));
	Условия.Вставить("ИспользуетсяТрудНаемныхРаботников", УчетЗарплаты.ИспользуетсяТрудНаемныхРаботников(Организация));
	Условия.Вставить("ПериодыУдержанияНДФЛ", УчетЗарплаты.ПериодыУдержанияНДФЛ(Организация, НачалоПериода, КонецПериода));
	ЭтоЮрЛицо = Условия.Получить("ЭтоЮридическоеЛицо");
	Условия.Вставить("ПериодыВыплатыЗарплаты", УчетЗарплаты.ПериодыВыплатыЗарплаты(Организация, ЭтоЮрЛицо, НачалоПериода, КонецПериода));
	ПериодыРасчетов = УчетЗарплаты.ПериодыРасчетовПоСоциальномуСтрахованию(Организация, НачалоПериода, КонецПериода);
	Условия.Вставить("ПериодыРасчетовПоСоциальномуСтрахованию", ПериодыРасчетов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли