#Область СлужебныйПрограммныйИнтерфейс

// Возвращает данные, необходимые для запуска фонового задания по получению статуса самозанятого контрагента
//
// Параметры:
//  ИсточникДанных - СправочникСсылка.Контрагенты, СправочникСсылка.ДоговорыКонтрагентов - ссылка на объект из
//                   которого будет получен ИНН самозанятого контрагента
//  ДатаПроверки - Дата - дата в часовом поясе "Europe/Moscow"
//  ИспользуютсяДоговорыССамозанятыми - Булево, Непределено - признак того, что используются договоры с самозанятыми
//
// Возвращаемое значение:
//  Структура - см. НовыйПараметрыПолученияСтатуса
//
Функция ПараметрыПолученияСтатуса(ИсточникДанных, ДатаПроверки, Знач ИспользуютсяДоговорыССамозанятыми = Неопределено) Экспорт
	
	СервисИспользуется = ИнтеграцияСтатусСамозанятого.СервисИспользуется();
	ВестиУчетПоДоговорам = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	Если ИспользуютсяДоговорыССамозанятыми = Неопределено Тогда
		ИспользуютсяДоговорыССамозанятыми = ПолучитьФункциональнуюОпцию("ИспользуютсяДоговорыССамозанятыми");
	КонецЕсли;
	
	Параметры = НовыйПараметрыПолученияСтатуса(СервисИспользуется И ВестиУчетПоДоговорам И ИспользуютсяДоговорыССамозанятыми);
	Если Не Параметры.ВыполнятьПроверкуСтатуса Или Не ЗначениеЗаполнено(ИсточникДанных) Тогда
		//@skip-check constructor-function-return-section
		Возврат Параметры;
	КонецЕсли;
	
	Если ТипЗнч(ИсточникДанных) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		РеквизитыСамозанятого = ИнтеграцияСтатусСамозанятогоБП.РеквизитыСамозанятогоПоДоговору(ИсточникДанных);
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("СправочникСсылка.Контрагенты") Тогда
		РеквизитыСамозанятого = ИнтеграцияСтатусСамозанятогоБП.РеквизитыСамозанятогоПоКонтрагенту(ИсточникДанных);
	Иначе
		ВызватьИсключение НСтр(
			"ru = 'Вызван метод ""ПараметрыПолученияСтатуса"" общего модуля ИнтеграцияСтатусСамозанятогоБПВызовСервера с некорректным параметром'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыСамозанятого.ИНН) Тогда
		Параметры.ИННСамозанятого = РеквизитыСамозанятого.ИНН;
		Параметры.СохраненныйСтатус = РегистрыСведений.СтатусыСамозанятых.ПлательщикНПД(
			РеквизитыСамозанятого.Контрагент, ДатаПроверки);
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйПараметрыПолученияСтатуса(ВыполнятьПроверкуСтатуса)
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ВыполнятьПроверкуСтатуса", ВыполнятьПроверкуСтатуса);
	Параметры.Вставить("ИННСамозанятого", "");
	Параметры.Вставить("СохраненныйСтатус", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти