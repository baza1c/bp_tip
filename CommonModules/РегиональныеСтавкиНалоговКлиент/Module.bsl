#Область ПрограммныйИнтерфейс

// Конструктор структуры параметров метода получения видов деятельности ПСН
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыМетодаВидыДеятельностиПСН() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодРегиона", "");
	Результат.Вставить("Период", '00010101');
	
	Возврат Результат;
	
КонецФункции

// Конструктор структуры параметров метода получения ставок ПСН
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыМетодаСтавкиПСН() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодРегиона", "");
	Результат.Вставить("КодВидаДеятельности", "");
	Результат.Вставить("Период", '00010101');
	
	Возврат Результат;
	
КонецФункции

// Конструктор структуры параметров метода получения краткого описания ставок ПСН
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыМетодаОписаниеСтавокПСН() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодРегиона", "");
	Результат.Вставить("КодВидаДеятельности", "");
	Результат.Вставить("Период", '00010101');
	
	Возврат Результат;
	
КонецФункции

// Конструктор структуры параметров метода получения ставок УСН
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыМетодаСтавкиУСН() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодРегиона", "");
	Результат.Вставить("ЭтоЮрЛицо", Ложь);
	Результат.Вставить("КодВидаДеятельности", "");
	Результат.Вставить("ПрименяетсяУСНДоходы", Ложь);
	Результат.Вставить("ПрименяетсяУСНДоходыРасходы", Ложь);
	Результат.Вставить("Период", '00010101');
	
	Возврат Результат;
	
КонецФункции

// Конструктор структуры параметров метода получения краткого описания ставок УСН
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыМетодаОписаниеСтавокУСН() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодРегиона", "");
	Результат.Вставить("ЭтоЮрЛицо", Ложь);
	Результат.Вставить("КодВидаДеятельности", "");
	Результат.Вставить("ПрименяетсяУСНДоходы", Ложь);
	Результат.Вставить("ПрименяетсяУСНДоходыРасходы", Ложь);
	Результат.Вставить("Период", '00010101');
	
	Возврат Результат;
	
КонецФункции

// Конструктор структуры параметров отчета "Ставки налога в вашем регионе"
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыОтчетаСтавкиНалогаВВашемРегионе() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Налог", "");
	Результат.Вставить("Режим", "");
	Результат.Вставить("КодРегиона", "");
	Результат.Вставить("НаименованиеРегиона", "");
	Результат.Вставить("КодВидаДеятельности", "");
	Результат.Вставить("НаименованиеВидаДеятельности", "");
	Результат.Вставить("ЭтоЮрЛицо", Ложь);
	Результат.Вставить("ПрименяетсяУСНДоходы", Ложь);
	Результат.Вставить("ПрименяетсяУСНДоходыРасходы", Ложь);
	Результат.Вставить("Период", '00010101');
	Результат.Вставить("ПоказыватьПрименить", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет, что в списке присутствуют виды деятельности, которые можно применять при переходе с ЕНВД
// до принятия регионального закона. Если вида деятельности нет, добавляет.
//
// Параметры:
//  КодРегиона - Строка
//  КодыВидовДеятельностиПереходногоПериода - Соответствие
//  СписокВидовДеятельности - Массив, Неопределено
//
Процедура ДополнитьВидамиДеятельностиПереходногоПериода(КодРегиона, КодыВидовДеятельностиПереходногоПериода, СписокВидовДеятельности) Экспорт
	
	Если Не ЗначениеЗаполнено(КодРегиона)
		Или КодыВидовДеятельностиПереходногоПериода.Количество() = 0
		Или СписокВидовДеятельности = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Если региональный закон уже принят, удалим код из списка
	Для Каждого РеквизитыВидаДеятельности Из СписокВидовДеятельности Цикл
		КодВидаДеятельности = Лев(СокрЛ(РеквизитыВидаДеятельности.Код), 2);
		Если КодыВидовДеятельностиПереходногоПериода.Получить(КодВидаДеятельности) <> Неопределено Тогда
			КодыВидовДеятельностиПереходногоПериода.Удалить(КодВидаДеятельности);
		КонецЕсли;
	КонецЦикла;
	
	// Оставшиеся коды добавляем с условным номером 01
	Для Каждого ВидДеятельностиПереходногоПериода Из КодыВидовДеятельностиПереходногоПериода Цикл
		
		ЧастиКода = Новый Массив;
		ЧастиКода.Добавить(ВидДеятельностиПереходногоПериода.Ключ);
		ЧастиКода.Добавить(КодРегиона);
		ЧастиКода.Добавить("01");
		ЧастиКода.Добавить("00");
		
		НовыйВидДеятельности = НовыйВидДеятельностиПСН(СтрСоединить(ЧастиКода), ВидДеятельностиПереходногоПериода.Значение);
		
		СписокВидовДеятельности.Добавить(НовыйВидДеятельности);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьНавигационнуюСсылкуСтавкиВВашемРегионе(Владелец, НавигационнаяСсылка, ПараметрыВыполнения) Экспорт
	
	Если НавигационнаяСсылка = "СтавкиНалогаВВашемРегионе" Тогда
		
		ПараметрыФормы = Новый Структура("ПараметрыОтчета", ПараметрыВыполнения);
		ОткрытьФорму("Отчет.СтавкиНалогаВВашемРегионе.ФормаОбъекта", ПараметрыФормы, Владелец, Истина);
		
	ИначеЕсли НавигационнаяСсылка = "ИнтернетПоддержка" Тогда
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", Владелец);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОповещениеОЗакрытии, Владелец);
		
	КонецЕсли;

КонецПроцедуры

// Возвращает текст подсказки для вида деятельности в случае, если интернет-поддержка не подключена.
//
// Возвращаемое значение:
//   Строка - текст подсказки
//
Функция ТекстПодсказкиИнтернетПоддержкаНеПодключена() Экспорт

	ТекстПодсказки = НСтр("ru='В вашем регионе могут применяться льготные ставки.
	|Для получения информации о них подключите <a href=""ИнтернетПоддержка"">Интернет-поддержку</a>.'");
	
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(ТекстПодсказки);

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОписаниеСтавокНалога

Процедура ПолучитьОписаниеСтавокУСНВФоне(Форма, ДекорацияОписание, ДекорацияДлительнаяОперация, ПараметрыМетода, Обработчик) Экспорт

	ДлительнаяОперация = РегиональныеСтавкиНалоговВызовСервера.ПолучитьОписаниеСтавокУСНВФоне(
		ПараметрыМетода, Форма.УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		ДекорацияДлительнаяОперация.Видимость = Истина;
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	Иначе
		ОбработкаОшибкиПолученияОписанияСтавокНалога(ДекорацияОписание, ДекорацияДлительнаяОперация);
	КонецЕсли;

КонецПроцедуры

Процедура ПолучитьОписаниеСтавокПСНВФоне(Форма, ДекорацияОписание, ДекорацияДлительнаяОперация, ПараметрыМетода, Обработчик) Экспорт

	ДлительнаяОперация = РегиональныеСтавкиНалоговВызовСервера.ПолучитьОписаниеСтавокПСНВФоне(
		ПараметрыМетода, Форма.УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		ДекорацияДлительнаяОперация.Видимость = Истина;
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	Иначе
		ОбработкаОшибкиПолученияОписанияСтавокНалога(ДекорацияОписание, ДекорацияДлительнаяОперация);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПолученияОписанияСтавокНалога(ДлительнаяОперация, ДекорацияОписание, ДекорацияДлительнаяОперация) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" И ЭтоАдресВременногоХранилища(ДлительнаяОперация.АдресРезультата) Тогда
		
		ДекорацияДлительнаяОперация.Видимость = Ложь;
		Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		
		Если Результат.КодСостояния = РегиональныеСтавкиНалоговКлиентСервер.КодыОтветаСервиса().Успех Тогда
			
			ПредставлениеСсылки = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
				СтрШаблон("<a href = СтавкиНалогаВВашемРегионе >%1</a>",  НСтр("ru='Подробнее'")));
			
			ДекорацияОписание.Заголовок = Новый ФорматированнаяСтрока(
				Результат.Ответ,
				Символы.ПС,
				ПредставлениеСсылки);
			
		Иначе
			ОбработкаОшибкиПолученияОписанияСтавокНалога(ДекорацияОписание, ДекорацияДлительнаяОперация,
				Результат.КодСостояния,
				Результат.ОписаниеОшибки);
		КонецЕсли;
	Иначе
		ОбработкаОшибкиПолученияОписанияСтавокНалога(ДекорацияОписание, ДекорацияДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОшибкиПолученияОписанияСтавокНалога(ДекорацияОписание, ДекорацияДлительнаяОперация, КодОшибки = "", ОписаниеОшибки = "")
	
	ДекорацияОписание.Заголовок = РегиональныеСтавкиНалоговКлиентСервер.ТекстОписанияОшибки(КодОшибки, ОписаниеОшибки);
	ДекорацияДлительнаяОперация.Видимость = Ложь;
	
КонецПроцедуры

Функция НовыйВидДеятельностиПСН(Код, Наименование)
	
	НовыйВидДеятельности = Новый Структура;
	НовыйВидДеятельности.Вставить("Код", Код);
	НовыйВидДеятельности.Вставить("Наименование", Наименование);
	Возврат НовыйВидДеятельности;
	
КонецФункции

#КонецОбласти

#КонецОбласти
