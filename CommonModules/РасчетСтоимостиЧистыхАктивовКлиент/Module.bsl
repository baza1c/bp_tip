
#Область СлужебныйПрограммныйИнтерфейс

// Открывает форму расчета чистых активов по данным бухгалтерской отчетности
//
// Параметры:
//  БухгалтерскаяОтчетность  - ДокументСсылка.РегламентированныйОтчет - ссылка на документ, по
//                                                данным которого необходимо произвести расчет
//  Организация  - СправочникСсылка.Организации - организация бухгалтерской отчетности
//  Период  - Дата - конец периода бухгалтерской отчетности
//
// Пример:
//  // Вызов из Отчет.РегламентированныйОтчетБухОтчетность.Форма.ФормаОтчета2025Кв1
//  Отчетность  = СтруктураРеквизитовФормы.мСохраненныйДок;
//  Организация = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
//  Период      = СтруктураРеквизитовФормы.Организация;
//  РасчетСтоимостиЧистыхАктивовКлиент.ОткрытьДляБухгалтерскойОтчетности(Отчетность, Организация, Период);
//
Процедура ОткрытьДляБухгалтерскойОтчетности(БухгалтерскаяОтчетность, Организация, Период) Экспорт
	
	Если Не ЗначениеЗаполнено(БухгалтерскаяОтчетность) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СпособЗаполнения", "ПоБухгалтерскойОтчетности");
	ПараметрыФормы.Вставить("Период", Период);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("БухгалтерскаяОтчетность", БухгалтерскаяОтчетность);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.РасчетСтоимостиЧистыхАктивов.Форма", ПараметрыФормы, , БухгалтерскаяОтчетность);
	
КонецПроцедуры 

// Открывает расчет стоимости чистых активов из формы документа
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - форма документа:
//    * Объект  - ДанныеФормыСтруктура:
//      ** Организация  - СправочникСсылка.Организации
//      ** Ссылка  - ДокументСсылка
//  ИмяПериода  - Строка - имя реквизита реквизита "Объект"
//                   параметра "Форма", определяющего период
//                   расчета. По умолчанию период берется из
//                   стандартного реквизита "Дата"
// Пример:
//  // Вызов из Документ.НачислениеДивидендов.ФормаОбъекта
//  РасчетСтоимостиЧистыхАктивовКлиент.ОткрытьИзФормыДокумента(ЭтотОбъект, "РасчетныйПериод");
//
Процедура ОткрытьИзФормыДокумента(Форма, ИмяПериода = "Дата") Экспорт
	
	Организация = Форма.Объект.Организация;
	Период = КонецМесяца(Форма.Объект[ИмяПериода]);
	Уникальность = Форма.Объект.Ссылка;
	
	ОткрытьПоДаннымУчета(Организация, Период, Уникальность);
	
КонецПроцедуры

// Открывает расчет стоимости чистах активов по данных учета организации на дату
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Период - Дата - дата остатков
//  Уникальность - Произвольный - параметр "Уникальность" открываемой
//                                формы. Подробности см. ОткрытьФорму
//
Процедура ОткрытьПоДаннымУчета(Организация, Период, Уникальность = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СпособЗаполнения", "ПоДаннымУчета");
	ПараметрыФормы.Вставить("Период", Период);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.РасчетСтоимостиЧистыхАктивов.Форма", ПараметрыФормы, , Уникальность);
	
КонецПроцедуры

#КонецОбласти