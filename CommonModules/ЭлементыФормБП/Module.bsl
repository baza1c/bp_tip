#Область СлужебныйПрограммныйИнтерфейс

Процедура ОтобратьСтрокиНаСервере(Форма, ИмяКоманды, ИмяТабличнойЧасти = Неопределено, ЗначениеОтбора = Неопределено, ИмяПоля = Неопределено) Экспорт
	ОтметитьВыбранныйВариантОтбораСтрок(Форма, ИмяКоманды);
	Если ЗначениеЗаполнено(ИмяТабличнойЧасти) Тогда
		УстановитьОтборСтрок(Форма, ИмяТабличнойЧасти, ЗначениеОтбора);
	Иначе
		УстановитьОтборСписка(Форма, ИмяПоля);
		УстановитьУсловноеОформлениеСписка(Форма.Список, ИмяПоля);
	КонецЕсли;
КонецПроцедуры

Процедура НастроитьОтборСпискаПриСозданииНаСервере(Форма, ИмяПоля) Экспорт
	
	НастроитьОтборСтрок(Форма);
	УстановитьОтборСписка(Форма, ИмяПоля);
	УстановитьУсловноеОформлениеСписка(Форма.Список, ИмяПоля);
	
Конецпроцедуры

Процедура НастроитьОтборСтрок(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	ЭлементыИзГруппыОтбора = Элементы.ГруппаОтбор.ПодчиненныеЭлементы;
	Форма.АктивныйОтборСтрок = "";
	Для Каждого ЭлементВариантОтбора Из ЭлементыИзГруппыОтбора Цикл
		Если ТипЗнч(ЭлементВариантОтбора) <> Тип("КнопкаФормы")
			Или ЭлементВариантОтбора.Вид <> ВидКнопкиФормы.КнопкаКоманднойПанели Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Форма.ВариантыОтбора.Добавить(), ЭлементВариантОтбора);
		Если Форма.АктивныйОтборСтрок = "" Тогда
			Форма.АктивныйОтборСтрок = ЭлементВариантОтбора.ИмяКоманды;
		КонецЕсли;
	КонецЦикла;
	
	ОтметитьВыбранныйВариантОтбораСтрок(Форма, Форма.АктивныйОтборСтрок);

КонецПроцедуры

Процедура УстановитьОтборСтрок(Форма, ИмяТабличнойЧасти, ЗначениеОтбора) Экспорт
	
	Элементы = Форма.Элементы;
	Элементы[ИмяТабличнойЧасти].ОтборСтрок = Новый ФиксированнаяСтруктура(ЗначениеОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтметитьВыбранныйВариантОтбораСтрок(Форма, Знач ВыбранныйВариантОтбора)
	
	Элементы = Форма.Элементы;
	Для Каждого ВариантОтбора Из Форма.ВариантыОтбора Цикл
		ИмяКомандыОтбора = ВариантОтбора.ИмяКоманды;
		ИмяКнопкиОтбора = ВариантОтбора.Имя;
		Элементы[ИмяКнопкиОтбора].Пометка = (ИмяКомандыОтбора = ВыбранныйВариантОтбора);
		Если ИмяКомандыОтбора = ВыбранныйВариантОтбора Тогда
			Элементы[ИмяКнопкиОтбора].ЦветТекста = ЦветаСтиля.ЦветАкцента;
		Иначе
			Элементы[ИмяКнопкиОтбора].ЦветТекста = Новый Цвет;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьОтборСписка(Форма, ИмяПоля)
	
	ОтборИспользование = Истина;
	Если Форма.АктивныйОтборСтрок = "ОтобратьВсе" Тогда
		ОтборИспользование = Ложь;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список,
		ИмяПоля,
		Ложь,
		,
		,
		ОтборИспользование);
	
КонецПроцедуры

Процедура УстановитьУсловноеОформлениеСписка(Список, ИмяПоля)
	
	Список.УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = Список.УсловноеОформление.Элементы.Добавить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		ИмяПоля, ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
КонецПроцедуры

#КонецОбласти
