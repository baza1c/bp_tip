////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиДепозиты: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ИнициализацияДепозитногоКалькулятора

// Описание параметров депозитного калькулятора, используемых в обработчике ПриСозданииНаСервере
// 
// Возвращаемое значение:
//  Структура - Дополнительные праметры формы:
// * ИмяГруппыКоманд - Строка - имя группы команд размещения команды отображения депозитного калькулятора;
// * ИмяГруппыРазмещения - Строка - имя группы размещения элементов депозитного калькулятора;
// * ПутьКРеквизитуОрганизация - Строка - путь к реквизиту "организация";
// * ИмяЭлементаРеквизитаОрганизация - Строка - имя элемента реквизита организация;
// * ИспользоватьКомандуДепозиты - Булево - Если, Истина команда депозиты добавляется при инициализации;
// * ВыводитьЗаголовокПанелиРазмещения - Булево - Если, Истина отображается заголовок депозитного калькулятора;
// * ОтложеннаяИнициализация - Булево - если Истина, то создание элементов депозитного калькулятора
//                                      на форме будет отложено;
// * ВыполненаИнициализация - Булево - флаг указывающий что страница "Расчеты" депозитного калькулятора создана;
// * ВыполняетсяАктуализация - Булево - если Истина, запущено фоновое задание обновления депозитных продуктов банков;
// * АдресХранилищаПредложенийБанков - Строка - адрес временного хранилища с данными о предложениях банков;
// * ДатаОбновленияПредложений - Дата - дата обновления предложений банка.
// * ВыбранноеПредложение - Строка - уникальный идентификатор депозитного продукта банка.
// * ПоказыватьПанельРасчетаДепозитов - Булево - Если Истина, панель расчета депозитов откроется при открытии формы размещения.
//
Функция ПараметрыДепозитногоКалькулятора() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяГруппыКоманд", "ГруппаОбменСБанкамиДепозиты_Команда");
	Результат.Вставить("ИмяГруппыРазмещения", "ГруппаОбменСБанкамиДепозиты_Расчеты");
	Результат.Вставить("ПутьКРеквизитуОрганизация", "");
	Результат.Вставить("ИмяЭлементаРеквизитаОрганизация", "");
	Результат.Вставить("ИспользоватьКомандуДепозиты", Истина);
	Результат.Вставить("ВыводитьЗаголовокПанелиРазмещения", Истина);
	Результат.Вставить("ОтложеннаяИнициализация", Истина);
	Результат.Вставить("ВыполненаИнициализация", Ложь);
	Результат.Вставить("ВыполняетсяАктуализация", Ложь);
	Результат.Вставить("АдресХранилищаПредложенийБанков", "");
	Результат.Вставить("ДатаОбновленияПредложений", Дата(1, 1, 1));
	Результат.Вставить("ВыбранноеПредложение", "");
	Результат.Вставить("ПоказыватьПанельРасчетаДепозитов", Истина);
	Возврат Результат;
	
КонецФункции

// Обработчик для события формы ПриСозданииНаСервере.
// Вызывается из модуля формы объекта-владельца депозитного калькулятора при внедрении подсистемы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, предназначенная для отображения
//                                       депозитного калькулятора.
//  ДополнительныеПараметры - Структура - см. ОбменСБанкамиДепозиты.ПараметрыДепозитногоКалькулятора
//
Процедура ПриСозданииНаСервере(Форма, ДополнительныеПараметры) Экспорт
	
	Если Не ДоступенОбменДепозитами() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеПараметры.Свойство("ПутьКРеквизитуОрганизация")
		Или ПустаяСтрока(ДополнительныеПараметры.ПутьКРеквизитуОрганизация) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы = Форма.Элементы;
	
	ИмяГруппыКоманд = "ГруппаОбменСБанкамиДепозиты_Команда";
	Если ДополнительныеПараметры.Свойство("ИмяГруппыКоманд") Тогда
		ИмяГруппыКоманд = ДополнительныеПараметры.ИмяГруппыКоманд;
	КонецЕсли;
	
	ИмяГруппыРазмещения = "ГруппаОбменСБанкамиДепозиты_Расчеты";
	Если ДополнительныеПараметры.Свойство("ИмяГруппыРазмещения") Тогда
		ИмяГруппыРазмещения = ДополнительныеПараметры.ИмяГруппыРазмещения;
	КонецЕсли;
	
	Если ЭлементыФормы.Найти(ИмяГруппыРазмещения) = Неопределено
		Или (ДополнительныеПараметры.ИспользоватьКомандуДепозиты 
			И ЭлементыФормы.Найти(ИмяГруппыКоманд) = Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользоватьКомандуДепозиты Тогда
		ДобавитьИРазместитьКомандуДепозиты(Форма, ИмяГруппыКоманд);
	КонецЕсли;
	
	ДобавитьКомандуОтправитьЗаявку(Форма);
	ДобавитьКомандуПовторитьЗапросПредложенийБанков(Форма);
	
	ДобавляемыеРеквизиты = Новый Массив;
	ПроверитьНаличиеРеквизитовДепозитногоКалькулятора(Форма, ДобавляемыеРеквизиты);
	
	Если ДобавляемыеРеквизиты.Количество() > 0 Тогда
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;
	
	ПодготовитьГруппуРазмещенияДепозитногоКалькулятора(ЭлементыФормы, ДополнительныеПараметры);
	ДополнительныеПараметры.ПоказыватьПанельРасчетаДепозитов =
		ОбменСБанкамиДепозитыВызовСервера.ПоказыватьПанельРасчетаДепозитов();
	
	ИмяРеквизитаДополнительныеПараметры = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра("ДополнительныеПараметры");
	Форма[ИмяРеквизитаДополнительныеПараметры] = ДополнительныеПараметры;
	
	Если Не ДополнительныеПараметры.ОтложеннаяИнициализация Тогда
		СоздатьЭлементыПараметровДепозита(Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Определяет, используется обмен с банком заявками на депозиты.
// 
// Возвращаемое значение:
//  Булево - значение функциональной опции.
//
Функция ИспользуютсяДепозитыОбменСБанками() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьДепозитыОбменСБанками");
	
КонецФункции

// Определяет, есть ли в программе настройки обмена с поддержкой депозитов
// см. ОбменСБанкамиДепозитыСлужебный.ЕстьПоддержкаДепозитов
//
// Возвращаемое значение:
//  Булево - результатПроверки.
//
Функция ДоступенОбменДепозитами() Экспорт
	
	Результат = Ложь;
	
	Если Не ОбменСБанками.ИспользуетсяСервисДиректБанк() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ИспользуютсяДепозитыОбменСБанками() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ОбменСБанкамиСлужебный.ПравоОбработкиЭД() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Возврат ОбменСБанкамиДепозитыСлужебный.ЕстьПоддержкаДепозитов();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет наличие доступных продуктов банка по депозитам и завершает инициализацию депозитного калькулятора.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта-владельца, в которой размещен депозитный калькулятор;
// 
// Возвращаемое значение:
//  Структура:
//   * ЕстьПредложенияБанков - Булево - если Истина, были получены данные предложений банков.
//   * ОшибкаПолученияПредложений - Строка - содержит текст ошибки, если не удалось получить предложения ни от одного банка.
//
Функция ПроверитьПродуктыБанковИЗавершитьИнициализацию(Форма) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьПредложенияБанков", Ложь);
	Результат.Вставить("ОшибкаПолученияПредложений", "");
	
	ДополнительныеПараметры = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДополнительныеПараметрыДепозитов(Форма);
	ТекущаяОрганизация = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, 
		ДополнительныеПараметры.ПутьКРеквизитуОрганизация);
	
	ПродуктыИУсловия = ОбменСБанкамиДепозитыСлужебный.ДанныеДепозитныхПродуктов(ТекущаяОрганизация);
	
	Продукты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПродуктыИУсловия, "Продукты", Неопределено);
	Условия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПродуктыИУсловия, "Условия", Неопределено);
	Результат.ОшибкаПолученияПредложений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПродуктыИУсловия, "ОшибкаПолученияПредложений", "");
	
	Если ЗначениеЗаполнено(Продукты) И ЗначениеЗаполнено(Условия) Тогда
		
		ИдентификаторФормы = Форма.УникальныйИдентификатор;
		ДополнительныеПараметры.АдресХранилищаПредложенийБанков = ПоместитьВоВременноеХранилище(ПродуктыИУсловия, 
			ИдентификаторФормы);
		СоздатьЭлементыПараметровДепозита(Форма);
		ДополнительныеПараметры.ВыполненаИнициализация = Истина;
		Результат.ЕстьПредложенияБанков = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИмяНастройкиРасчетДепозитов()
	
	Возврат "ЭлектронноеВзаимодействиеОбменСБанкамиДепозитыРасчетДепозитов";
	
КонецФункции

Функция ИмяКлючНастройкиЗакрытиеРасчетДепозитов()
	
	Возврат "ЭлектронноеВзаимодействиеОбменСБанкамиДепозитыЗакрытиеРасчетДепозитов";
	
КонецФункции

// Проверяет наличие данных о закрытии пользователем панели расчета депозита.
// 
// Параметры:
//  ИмяПользователя - Строка - см. ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить.
//
// Возвращаемое значение:
//  Булево - с момента закрытия прошло 30 дней, или пользователь не закрывал депозитный калькулятор.
//
Функция ПоказыватьПанельРасчетаДепозитов(ИмяПользователя) Экспорт
	
	Результат = Истина;
	ПараметрыЗакрытия = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиРасчетДепозитов(),
		ИмяКлючНастройкиЗакрытиеРасчетДепозитов(), , , ИмяПользователя);
		
	Если Не ПараметрыЗакрытия = Неопределено Тогда
			
		ДатаЗакрытия = ПараметрыЗакрытия.ДатаЗакрытия;
		ПериодЗакрытия = 86400 * 30;
		
		Если ТекущаяДатаСеанса() - ДатаЗакрытия >= ПериодЗакрытия Тогда
			ОбщегоНазначения.ХранилищеОбщихНастроекУдалить(ИмяНастройкиРасчетДепозитов(), 
				ИмяКлючНастройкиЗакрытиеРасчетДепозитов(), ИмяПользователя);
		Иначе
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет решение пользователя закрыть панель расчета депозита в хранилище
// общих настроек.
// 
// Параметры:
//  ИмяПользователя - Строка - см. ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить.
//
Процедура СохранитьЗакрытиеПанелиРасчетаДепозитов(ИмяПользователя) Экспорт
	
	ПоказыватьРасчетДепозитов = ПоказыватьПанельРасчетаДепозитов(ИмяПользователя);
	
	Если ПоказыватьРасчетДепозитов Тогда
		
		ПараметрыЗакрытия = ПараметрыЗакрытияРасчетаДепозитов();
		ПараметрыЗакрытия.ДатаЗакрытия = ТекущаяДатаСеанса();
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			ИмяНастройкиРасчетДепозитов(),
			ИмяКлючНастройкиЗакрытиеРасчетДепозитов(),
			ПараметрыЗакрытия, , ИмяПользователя);
		
	КонецЕсли;
		
КонецПроцедуры

// Проверяет наличие данных о решении пользователя закрыть панель расчета депозита и удаляет их.
// 
// Параметры:
//  ИмяПользователя - Строка - см. ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить.
//
Процедура ПроверитьИУдалитьДанныеОЗакрытииПанелиРасчетаДепозитов(ИмяПользователя) Экспорт
	
	Если ПоказыватьПанельРасчетаДепозитов(ИмяПользователя) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекУдалить(ИмяНастройкиРасчетДепозитов(), 
		ИмяКлючНастройкиЗакрытиеРасчетДепозитов(), ИмяПользователя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыЗакрытияРасчетаДепозитов()
	
	Результат = Новый Структура();
	Результат.Вставить("ДатаЗакрытия", Дата(1, 1, 1));
	
	Возврат Результат;
	
КонецФункции

#Область ИнициализацияДепозитногоКалькулятора

#Область Реквизиты

Процедура ПроверитьНаличиеРеквизитовДепозитногоКалькулятора(Форма, ДобавляемыеРеквизиты)
	
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	РеквизитыДляСоздания = Новый Соответствие();
	
	КлючПараметра = "Сумма";
	ИмяРеквизита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаРеквизита = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(КлючПараметра);
	РеквизитыДляСоздания.Вставить(ИмяРеквизита, ОписаниеТипаРеквизита);
	
	КлючПараметра = "Срок";
	ИмяРеквизита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаРеквизита = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(КлючПараметра);
	РеквизитыДляСоздания.Вставить(ИмяРеквизита, ОписаниеТипаРеквизита);
	
	КлючПараметра = "ДатаЗакрытия";
	ИмяРеквизита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаРеквизита = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(КлючПараметра);
	РеквизитыДляСоздания.Вставить(ИмяРеквизита, ОписаниеТипаРеквизита);
	
	КлючПараметра = "Предложение";
	ИмяРеквизита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаРеквизита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(
		КлючПараметра);
	РеквизитыДляСоздания.Вставить(ИмяРеквизита, ОписаниеТипаРеквизита);
	
	КлючПараметра = "ДополнительныеПараметры";
	ИмяРеквизита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаРеквизита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(
		КлючПараметра);
	РеквизитыДляСоздания.Вставить(ИмяРеквизита, ОписаниеТипаРеквизита);
	
	Для каждого ТекРеквизит Из РеквизитыФормы Цикл
		
		Если РеквизитыДляСоздания.Получить(ТекРеквизит.Имя) <> Неопределено Тогда
			
			РеквизитыДляСоздания.Удалить(ТекРеквизит.Имя);
			Если РеквизитыДляСоздания.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого КлючИЗначение Из РеквизитыДляСоздания Цикл
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(КлючИЗначение.Ключ, КлючИЗначение.Значение));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Команды

Процедура ДобавитьИРазместитьКомандуДепозиты(Форма, ИмяГруппыКоманд)
	
	ЭлементыФормы = Форма.Элементы;
	ИмяКомандыДепозиты = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПоказатьРасчетДепозитов");
	Если Не ЭлементыФормы.Найти(ИмяКомандыДепозиты) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МестоРазмещения = Форма.Элементы[ИмяГруппыКоманд];
	
	ИмяПроцедурыВыполнитьКоманду = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ВыполнитьКоманду");
	ЗаголовокКоманды = НСтр("ru = 'Депозиты'");
	
	НоваяКоманда = Форма.Команды.Добавить(ИмяКомандыДепозиты);
	НоваяКоманда.Заголовок = ЗаголовокКоманды;
	НоваяКоманда.Действие = ИмяПроцедурыВыполнитьКоманду;
    НоваяКоманда.ФункциональныеОпции = "ИспользоватьДепозитыОбменСБанками";
	
	НовыйЭлемент = ЭлементыФормы.Добавить(ИмяКомандыДепозиты, Тип("КнопкаФормы"), МестоРазмещения);
	
	ВидКнопки = ВидКнопкиФормы.ОбычнаяКнопка;
	Если ТипЗнч(МестоРазмещения) = Тип("КоманднаяПанельФормы") Тогда
		ВидКнопки = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КонецЕсли;
	
	НовыйЭлемент.Вид = ВидКнопки;
	НовыйЭлемент.Заголовок = ЗаголовокКоманды;
	НовыйЭлемент.ИмяКоманды = ИмяКомандыДепозиты;

КонецПроцедуры

Процедура ДобавитьКомандуОтправитьЗаявку(Форма)
	
	ЭлементыФормы = Форма.Элементы;
	ИмяКомандыОтправитьЗаявку = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ОтправитьЗаявку");
	
	Если Не ЭлементыФормы.Найти(ИмяКомандыОтправитьЗаявку) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПроцедурыВыполнитьКоманду = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ВыполнитьКоманду");
	ЗаголовокКоманды = НСтр("ru = 'Оформить заявку'");
	
	НоваяКоманда = Форма.Команды.Добавить(ИмяКомандыОтправитьЗаявку);
	НоваяКоманда.Заголовок = ЗаголовокКоманды;
	НоваяКоманда.Действие = ИмяПроцедурыВыполнитьКоманду;
	
КонецПроцедуры

Процедура ДобавитьКомандуПовторитьЗапросПредложенийБанков(Форма)
	
	ЭлементыФормы = Форма.Элементы;
	ИмяКомандыЗапросПредложений = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПовторитьЗапросПредложений");
	
	Если Не ЭлементыФормы.Найти(ИмяКомандыЗапросПредложений) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПроцедурыВыполнитьКоманду = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ВыполнитьКоманду");
	ЗаголовокКоманды = НСтр("ru = 'Запросить предложения банков'");
	
	НоваяКоманда = Форма.Команды.Добавить(ИмяКомандыЗапросПредложений);
	НоваяКоманда.Заголовок = ЗаголовокКоманды;
	НоваяКоманда.Действие = ИмяПроцедурыВыполнитьКоманду;
	
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы

Процедура ПодготовитьГруппуРазмещенияДепозитногоКалькулятора(ЭлементыФормы, ДополнительныеПараметры)
	
	ГруппаРазмещения = ЭлементыФормы[ДополнительныеПараметры.ИмяГруппыРазмещения];
	
	ГруппаРазмещения.Видимость = Ложь;
	ГруппаРазмещения.Ширина = 32;
	ГруппаРазмещения.РастягиватьПоГоризонтали = Ложь;
	ГруппаРазмещения.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Если ДополнительныеПараметры.ВыводитьЗаголовокПанелиРазмещения Тогда
		ГруппаРазмещения.Заголовок = НСтр("ru = 'Депозитный калькулятор'");
		ГруппаРазмещения.ОтображатьЗаголовок = Истина;
	КонецЕсли;
	
	ТипГруппаФормы = Тип("ГруппаФормы");
	ТипДекорацияФормы = Тип("ДекорацияФормы");
	
	ИмяГруппыСтраницыИнформации = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СтраницаИнформации");
	ЭлементСтраницаИнформации = ЭлементыФормы.Добавить(ИмяГруппыСтраницыИнформации, ТипГруппаФормы, 
		ГруппаРазмещения);
	ЭлементСтраницаИнформации.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементСтраницаИнформации.Отображение = ОтображениеОбычнойГруппы.Нет;
	ЭлементСтраницаИнформации.ОтображатьЗаголовок = Ложь;
	ЭлементСтраницаИнформации.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ИмяГруппыИнформация = СтрШаблон("Группа%1", 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДобавитьПрефиксОбменСБанкамиДепозиты("ИнформацияОписание"));
	ЭлементИнформацияОписание = ЭлементыФормы.Добавить(ИмяГруппыИнформация, ТипГруппаФормы, 
		ЭлементСтраницаИнформации);
	ЭлементИнформацияОписание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементИнформацияОписание.Отображение = ОтображениеОбычнойГруппы.Нет;
	ЭлементИнформацияОписание.ОтображатьЗаголовок = Ложь;
	ЭлементИнформацияОписание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	ИмяЭлементаОжидание = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ОжиданиеКартинка");
	ЭлементОжиданиеКартинка = ЭлементыФормы.Добавить(ИмяЭлементаОжидание, ТипДекорацияФормы, 
		ЭлементИнформацияОписание);
	ЭлементОжиданиеКартинка.Вид = ВидДекорацииФормы.Картинка;
	ЭлементОжиданиеКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация32БЭД;
	
	ИмяЭлементаОшибка = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ОшибкаКартинка");
	ЭлементОшибкаКартинка = ЭлементыФормы.Добавить(ИмяЭлементаОшибка, ТипДекорацияФормы, 
		ЭлементИнформацияОписание);
	ЭлементОшибкаКартинка.Вид = ВидДекорацииФормы.Картинка;
	ЭлементОшибкаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
	ЭлементОшибкаКартинка.Видимость = Ложь;
	
	ИмяЭлементаТекстСообщения = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ТекстСообщения");
	ЭлементТекстСообщения = ЭлементыФормы.Добавить(ИмяЭлементаТекстСообщения, ТипДекорацияФормы, 
		ЭлементИнформацияОписание);
	ЭлементТекстСообщения.Заголовок = ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияОбновлениеПредложений();

	ИмяКомандыЗапросПредложений = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПовторитьЗапросПредложений");
	ЭлементКомандыЗапросПредложений = ЭлементыФормы.Добавить(ИмяКомандыЗапросПредложений,
		Тип("КнопкаФормы"), ЭлементСтраницаИнформации);
	ЭлементКомандыЗапросПредложений.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	ЭлементКомандыЗапросПредложений.Заголовок = НСтр("ru = 'Повторить запрос предложений'");
	ЭлементКомандыЗапросПредложений.ИмяКоманды = ИмяКомандыЗапросПредложений;
	ЭлементКомандыЗапросПредложений.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	ЭлементКомандыЗапросПредложений.Видимость = Ложь;
	
	ИмяГруппыРасчетДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СтраницаРасчетДепозита");
	
	СтраницаРасчетДепозита = ЭлементыФормы.Добавить(ИмяГруппыРасчетДепозита, ТипГруппаФормы, 
		ГруппаРазмещения);
	СтраницаРасчетДепозита.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	СтраницаРасчетДепозита.Видимость = Ложь;
	СтраницаРасчетДепозита.ОтображатьЗаголовок = Ложь;
	СтраницаРасчетДепозита.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
КонецПроцедуры

Процедура СоздатьЭлементыПараметровДепозита(Форма)
	
	ЭлементыФормы = Форма.Элементы;
	
	ИмяГруппыРасчетДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СтраницаРасчетДепозита");
	СтраницаРасчетДепозита = ЭлементыФормы[ИмяГруппыРасчетДепозита];
	
	СоздатьЭлементПараметраСумма(ЭлементыФормы, СтраницаРасчетДепозита);
	СоздатьЭлементыПараметраСрокИДатаЗакрытия(ЭлементыФормы, СтраницаРасчетДепозита);
	СоздатьЭлементПараметраСтавка(ЭлементыФормы, СтраницаРасчетДепозита);
	
	ИмяГруппыПараметров = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ГруппаПараметровДоходности");
	ГруппаПараметровДоходности = ЭлементыФормы.Добавить(ИмяГруппыПараметров, Тип("ГруппаФормы"), СтраницаРасчетДепозита);
	ГруппаПараметровДоходности.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПараметровДоходности.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаПараметровДоходности.ОтображатьЗаголовок = Ложь;
	ГруппаПараметровДоходности.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	СоздатьЭлементПараметраДоход(ЭлементыФормы, ГруппаПараметровДоходности);
	СоздатьГоризонтальныйРазделительЭлементовДепозита(ЭлементыФормы, ГруппаПараметровДоходности);
	СоздатьЭлементПараметраИтог(ЭлементыФормы, ГруппаПараметровДоходности);
	
	ТипДекорацияФормы = Тип("ДекорацияФормы");
	
	ИмяЭлементаПримечание = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДобавитьПрефиксОбменСБанкамиДепозиты(
		"ДепозитыДоходПримечание");
	ДепозитыДоходПримечаниеТекст = НСтр("ru = '* Расчет является предварительным.'");
	ДепозитыДоходПримечание = ЭлементыФормы.Добавить(ИмяЭлементаПримечание, ТипДекорацияФормы, 
		ГруппаПараметровДоходности);
	ДепозитыДоходПримечание.РастягиватьПоГоризонтали = Истина;
	ДепозитыДоходПримечание.Доступность = Ложь;
	ДепозитыДоходПримечание.Заголовок = ДепозитыДоходПримечаниеТекст;
	
	КлючПараметра = "РасчетПриОформлении";
	ИмяЭлементаРасчетПриОформлении = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	ЭлементРасчетПриОформлении =  ЭлементыФормы.Добавить(ИмяЭлементаРасчетПриОформлении, ТипДекорацияФормы, 
		СтраницаРасчетДепозита);
	ЭлементРасчетПриОформлении.Вид = ВидДекорацииФормы.Надпись;
	ЭлементРасчетПриОформлении.АвтоМаксимальнаяШирина = Истина;
	ЭлементРасчетПриОформлении.РастягиватьПоГоризонтали = Истина;
	ЭлементРасчетПриОформлении.МаксимальнаяШирина = 0;
	ЭлементРасчетПриОформлении.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ЭлементРасчетПриОформлении.Заголовок = НСтр("ru = 'Расчет при оформлении заявки'");
	ЭлементРасчетПриОформлении.Подсказка =
		НСтр("ru = 'Расчет доходности депозита будет получен из банка при оформления заявки.'");
	ЭлементРасчетПриОформлении.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	ЭлементРасчетПриОформлении.Видимость = Ложь;
	
	СоздатьЭлементыПредложения(ЭлементыФормы, СтраницаРасчетДепозита);
	
КонецПроцедуры

Процедура СоздатьЭлементПараметраСумма(ЭлементыФормы, ЭлементРазмещения)
	
	КлючПараметра = "Сумма";
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ИмяГруппыПараметра = СтрШаблон("Группа%1", ИмяПараметра);
	ИмяЭлементаПараметраДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	
	ГруппаПараметра = ЭлементыФормы.Добавить(ИмяГруппыПараметра, Тип("ГруппаФормы"), ЭлементРазмещения);
	ЗаполнитьСвойстваГруппыПараметраДепозита(ГруппаПараметра);
	
	ЭлементПараметраДепозита = ЭлементыФормы.Добавить(ИмяЭлементаПараметраДепозита, Тип("ПолеФормы"),
		ГруппаПараметра);
	ЗаполнитьСвойстваПолеВВодаПараметраДепозита(ЭлементПараметраДепозита);
	ЭлементПараметраДепозита.ПутьКДанным = ИмяПараметра;
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(
		КлючПараметра);
	ЭлементПараметраДепозита.ОграничениеТипа = ОписаниеТипаПараметра;
	ЭлементПараметраДепозита.Подсказка = "";
	ЭлементПараметраДепозита.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	ЭлементПараметраДепозита.Заголовок = НСтр("ru = 'Сумма'");
	
	ИмяОбработчика = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ПриИзменении");
	ЭлементПараметраДепозита.УстановитьДействие("ПриИзменении", ИмяОбработчика);
	
КонецПроцедуры

Процедура СоздатьЭлементыПараметраСрокИДатаЗакрытия(ЭлементыФормы, ЭлементРазмещения)
	
	КлючПараметра = "ДатаЗакрытия";
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(
		КлючПараметра);
	ИмяЭлементаПараметраДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	
	ИмяГруппыПараметра = СтрШаблон("Группа%1", ИмяПараметра);
	
	ГруппаПараметра = ЭлементыФормы.Добавить(ИмяГруппыПараметра, Тип("ГруппаФормы"), ЭлементРазмещения);
	ЗаполнитьСвойстваГруппыПараметраДепозита(ГруппаПараметра);
	
	ЭлементПараметраДепозита = ЭлементыФормы.Добавить(ИмяЭлементаПараметраДепозита, Тип("ПолеФормы"),
		ГруппаПараметра);
	ЗаполнитьСвойстваПолеВВодаПараметраДепозита(ЭлементПараметраДепозита);
	ЭлементПараметраДепозита.ПутьКДанным = ИмяПараметра;
	ЭлементПараметраДепозита.КнопкаОчистки = Ложь;
	ЭлементПараметраДепозита.МинимальноеЗначение = НачалоДня(ТекущаяДатаСеанса());
	ЭлементПараметраДепозита.ОграничениеТипа = ОписаниеТипаПараметра;
	ЭлементПараметраДепозита.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	ЭлементПараметраДепозита.Заголовок = НСтр("ru = 'Дата закрытия'");
	ЭлементПараметраДепозита.РастягиватьПоГоризонтали = Ложь;
	ЭлементПараметраДепозита.Ширина = 6;
	ЭлементПараметраДепозита.МаксимальнаяШирина = 6;
	ЭлементПараметраДепозита.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	
	ИмяОбработчика = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ПриИзменении");
	ЭлементПараметраДепозита.УстановитьДействие("ПриИзменении", ИмяОбработчика);
	
	КлючПараметра = "Срок";
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(
		КлючПараметра);
	
	ИмяЭлементаПараметраДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	
	
	ЭлементПараметраДепозита = ЭлементыФормы.Добавить(ИмяЭлементаПараметраДепозита, Тип("ПолеФормы"),
		ГруппаПараметра);
	ЗаполнитьСвойстваПолеВВодаПараметраДепозита(ЭлементПараметраДепозита);
	ЭлементПараметраДепозита.ПутьКДанным = ИмяПараметра;
	ЭлементПараметраДепозита.КнопкаРегулирования = Истина;
	ЭлементПараметраДепозита.КнопкаВыбора = Ложь;
	ЭлементПараметраДепозита.КнопкаОчистки = Ложь;
	ЭлементПараметраДепозита.МинимальноеЗначение = 1;
	ЭлементПараметраДепозита.ОграничениеТипа = ОписаниеТипаПараметра;
	ЭлементПараметраДепозита.ФорматРедактирования = СтрШаблон("ЧН=; ЧФ='Ч, %1'", НСтр("ru = 'дн.'"));
	ЭлементПараметраДепозита.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	ЭлементПараметраДепозита.Заголовок = НСтр("ru = 'На срок'");
	ЭлементПараметраДепозита.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	
	ИмяОбработчика = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ПриИзменении");
	ЭлементПараметраДепозита.УстановитьДействие("ПриИзменении", ИмяОбработчика);
	
КонецПроцедуры

Процедура СоздатьЭлементПараметраСтавка(ЭлементыФормы, ЭлементРазмещения)
	
	ТипДекорацияФормы = Тип("ДекорацияФормы");
	КлючПараметра = "Ставка";
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ИмяГруппыПараметра = СтрШаблон("Группа%1", ИмяПараметра);
	ГруппаПараметра = ЭлементыФормы.Добавить(ИмяГруппыПараметра, Тип("ГруппаФормы"), ЭлементРазмещения);
	ЗаполнитьСвойстваГруппыПараметраДепозита(ГруппаПараметра);
	
	ИмяЭлементаЗаголовокПараметра = СтрШаблон("Заголовок%1", ИмяПараметра);
	ЗаголовокПараметра =  ЭлементыФормы.Добавить(ИмяЭлементаЗаголовокПараметра, ТипДекорацияФормы, 
		ГруппаПараметра);
	ЗаполнитьСвойстваЗаголовокПараметраДепозита(ЗаголовокПараметра);
	ЗаголовокПараметра.Заголовок = НСтр("ru = 'Ставка:'");
	
	ИмяЭлементаПараметраДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	ЭлементПараметра = ЭлементыФормы.Добавить(ИмяЭлементаПараметраДепозита, ТипДекорацияФормы,
		ГруппаПараметра);
	ЭлементПараметра.Вид = ВидДекорацииФормы.Надпись;
	ЭлементПараметра.АвтоМаксимальнаяШирина = Истина;
	ЭлементПараметра.РастягиватьПоГоризонтали = Истина;
	ЭлементПараметра.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	ЭлементПараметра.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	ЭлементПараметра.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	ЭлементПараметра.АвтоМаксимальнаяШирина = Ложь;
	ЭлементПараметра.РастягиватьПоГоризонтали = Истина;
	ЭлементПараметра.Заголовок = 0;
	
КонецПроцедуры

Процедура СоздатьЭлементПараметраДоход(ЭлементыФормы, ЭлементРазмещения)
	
	ТипДекорацияФормы = Тип("ДекорацияФормы");
	КлючПараметра = "Доход";
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ИмяГруппыПараметра = СтрШаблон("Группа%1", ИмяПараметра);
	ГруппаПараметра = ЭлементыФормы.Добавить(ИмяГруппыПараметра, Тип("ГруппаФормы"), ЭлементРазмещения);
	ЗаполнитьСвойстваГруппыПараметраДепозита(ГруппаПараметра);
	
	ИмяЭлементаЗаголовокПараметра = СтрШаблон("Заголовок%1", ИмяПараметра);
	ЗаголовокПараметра =  ЭлементыФормы.Добавить(ИмяЭлементаЗаголовокПараметра, ТипДекорацияФормы, 
		ГруппаПараметра);
	ЗаполнитьСвойстваЗаголовокПараметраДепозита(ЗаголовокПараметра);
	ЗаголовокПараметра.Заголовок = НСтр("ru = 'Доход*:'");
	
	ИмяЭлементаПараметраДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	ЭлементПараметра = ЭлементыФормы.Добавить(ИмяЭлементаПараметраДепозита, ТипДекорацияФормы,
		ГруппаПараметра);
	ЭлементПараметра.Вид = ВидДекорацииФормы.Надпись;
	ЭлементПараметра.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	ЭлементПараметра.АвтоМаксимальнаяШирина = Истина;
	ЭлементПараметра.РастягиватьПоГоризонтали = Истина;
	ЭлементПараметра.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	ЭлементПараметра.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	ЭлементПараметра.АвтоМаксимальнаяШирина = Ложь;
	ЭлементПараметра.РастягиватьПоГоризонтали = Истина;
	ЭлементПараметра.Заголовок = 0;
	ЗаголовокПараметра.Заголовок = НСтр("ru = 'Доход*:'");
	
КонецПроцедуры

Процедура СоздатьЭлементПараметраИтог(ЭлементыФормы, ЭлементРазмещения)
	
	ТипДекорацияФормы = Тип("ДекорацияФормы");
	КлючПараметра = "Итог";
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ИмяГруппыПараметра = СтрШаблон("Группа%1", ИмяПараметра);
	ГруппаПараметра = ЭлементыФормы.Добавить(ИмяГруппыПараметра, Тип("ГруппаФормы"), ЭлементРазмещения);
	ЗаполнитьСвойстваГруппыПараметраДепозита(ГруппаПараметра);
	
	ИмяЭлементаЗаголовокПараметра = СтрШаблон("Заголовок%1", ИмяПараметра);
	ЗаголовокПараметра =  ЭлементыФормы.Добавить(ИмяЭлементаЗаголовокПараметра, ТипДекорацияФормы, 
		ГруппаПараметра);
	ЗаполнитьСвойстваЗаголовокПараметраДепозита(ЗаголовокПараметра);
	ЗаголовокПараметра.Заголовок = НСтр("ru = 'Итого:'");
	
	ИмяЭлементаПараметраДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	ЭлементПараметра = ЭлементыФормы.Добавить(ИмяЭлементаПараметраДепозита, ТипДекорацияФормы,
		ГруппаПараметра);
	ЭлементПараметра.Вид = ВидДекорацииФормы.Надпись;
	ЭлементПараметра.АвтоМаксимальнаяШирина = Истина;
	ЭлементПараметра.РастягиватьПоГоризонтали = Истина;
	ЭлементПараметра.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	ЭлементПараметра.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	ЭлементПараметра.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	ЭлементПараметра.АвтоМаксимальнаяШирина = Ложь;
	ЭлементПараметра.РастягиватьПоГоризонтали = Истина;
	ЭлементПараметра.Заголовок = 0;
	
КонецПроцедуры

Процедура СоздатьЭлементыПредложения(ЭлементыФормы, ЭлементРазмещения)
	
	ТипДекорацияФормы = Тип("ДекорацияФормы");
	КлючПараметра = "Предложение";
	ИмяПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяПараметра(КлючПараметра);
	ОписаниеТипаПараметра = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ОписаниеТипаРеквизитаПараметраДепозита(
		КлючПараметра);
	ИмяГруппыПараметра = СтрШаблон("Группа%1", ИмяПараметра);
	ИмяЭлементаПараметраДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы(КлючПараметра);
	
	ГруппаПараметра = ЭлементыФормы.Добавить(ИмяГруппыПараметра, Тип("ГруппаФормы"), ЭлементРазмещения);
	ЗаполнитьСвойстваГруппыПараметраДепозита(ГруппаПараметра);
	
	ЭлементПараметраДепозита = ЭлементыФормы.Добавить(ИмяЭлементаПараметраДепозита, Тип("ПолеФормы"),
		ГруппаПараметра);
	ЗаполнитьСвойстваПолеВВодаПараметраДепозита(ЭлементПараметраДепозита);
	ЭлементПараметраДепозита.ПутьКДанным = ИмяПараметра;
	ЭлементПараметраДепозита.КнопкаВыпадающегоСписка = Истина;
	ЭлементПараметраДепозита.РедактированиеТекста = Ложь;
	ЭлементПараметраДепозита.РежимВыбораИзСписка = Ложь;
	ЭлементПараметраДепозита.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	ЭлементПараметраДепозита.ШрифтЗаголовка = ШрифтыСтиля.ЖирныйШрифтБЭД;
	ЭлементПараметраДепозита.ОграничениеТипа = ОписаниеТипаПараметра;
	ЭлементПараметраДепозита.Заголовок = НСтр("ru = 'Предложения'");
	
	ИмяОбработчикаПриИзменении = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ПриИзменении");
	ЭлементПараметраДепозита.УстановитьДействие("ПриИзменении", ИмяОбработчикаПриИзменении);
	
	ИмяОбработчикаАвтоПодбор = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("АвтоПодбор");
	ЭлементПараметраДепозита.УстановитьДействие("АвтоПодбор", ИмяОбработчикаАвтоПодбор);
	
	ИмяОбработчикаОбработкаВыбора = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ОбработкаВыбора");
	ЭлементПараметраДепозита.УстановитьДействие("ОбработкаВыбора", ИмяОбработчикаОбработкаВыбора);
	
	ИмяЭлементаДоходность = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ДоходностьОписаниеДепозита");
	ДепозитыДоходность = ЭлементыФормы.Добавить(ИмяЭлементаДоходность, ТипДекорацияФормы, 
		ЭлементРазмещения);
	ДепозитыДоходность.Заголовок = "";
	
	ИмяЭлементаОписание = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("ОписаниеДепозита");
	ДепозитыОписание = ЭлементыФормы.Добавить(ИмяЭлементаОписание, ТипДекорацияФормы, 
		ЭлементРазмещения);
	ДепозитыОписание.Заголовок = "";
	
	ИмяЭлементаСрокиИУсловия = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяЭлементаФормы("СрокиИУсловия");
	ДепозитыСрокиИУсловия = ЭлементыФормы.Добавить(ИмяЭлементаСрокиИУсловия, ТипДекорацияФормы, 
		ЭлементРазмещения);
	ДепозитыСрокиИУсловия.Заголовок = НСтр("ru = 'Сроки и условия'");
	ДепозитыСрокиИУсловия.Гиперссылка = Истина; 
	
	ИмяОбработчика = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ИмяОбработчика("ВыполнитьКоманду");
	ДепозитыСрокиИУсловия.УстановитьДействие("Нажатие", ИмяОбработчика);
	
	ИмяКомандыОтправитьЗаявку = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ОтправитьЗаявку");
	НовыйЭлемент = ЭлементыФормы.Добавить(ИмяКомандыОтправитьЗаявку, Тип("КнопкаФормы"), ЭлементРазмещения);
	НовыйЭлемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	НовыйЭлемент.Заголовок = НСтр("ru = 'Оформить заявку'");
	НовыйЭлемент.ИмяКоманды = ИмяКомандыОтправитьЗаявку;
	НовыйЭлемент.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	
КонецПроцедуры

Процедура СоздатьГоризонтальныйРазделительЭлементовДепозита(ЭлементыФормы, ЭлементРазмещения)
	
	ТипДекорацияФормы = Тип("ДекорацияФормы");
	ИмяРазделителя = СтрЗаменить(Новый УникальныйИдентификатор(), "-", "");
	ИмяРазделителя = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ДобавитьПрефиксОбменСБанкамиДепозиты(ИмяРазделителя);
	
	ИмяЭлементаРазделитель = СтрШаблон("Разделитель%1", ИмяРазделителя);
	
	ЭлементПараметра = ЭлементыФормы.Добавить(ИмяЭлементаРазделитель, ТипДекорацияФормы, ЭлементРазмещения);
	ЭлементПараметра.Вид = ВидДекорацииФормы.Надпись;
	ЭлементПараметра.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Подчеркивание, 1);
	ЭлементПараметра.Шрифт = Новый Шрифт(, 1, , , , , 1);
	ЭлементПараметра.АвтоМаксимальнаяШирина = Ложь;
	ЭлементПараметра.РастягиватьПоГоризонтали = Ложь;
	ЭлементПараметра.Ширина = 27;
	ЭлементПараметра.МаксимальнаяШирина = 27;
	ЭлементПараметра.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	ЭлементПараметра.Заголовок = "";
	
КонецПроцедуры

Процедура ЗаполнитьСвойстваГруппыПараметраДепозита(ГруппаПараметра)
	
	ГруппаПараметра.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПараметра.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаПараметра.ОтображатьЗаголовок = Ложь;
	ГруппаПараметра.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

КонецПроцедуры

Процедура ЗаполнитьСвойстваПолеВВодаПараметраДепозита(ЭлементПараметра)
	
	ЭлементПараметра.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементПараметра.РастягиватьПоГоризонтали = Истина;
	ЭлементПараметра.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	ЭлементПараметра.МаксимальнаяШирина = 15;
	
КонецПроцедуры

Процедура ЗаполнитьСвойстваЗаголовокПараметраДепозита(Элемент)
	
	Элемент.Вид = ВидДекорацииФормы.Надпись;
	Элемент.Ширина = 7;
	Элемент.АвтоМаксимальнаяШирина = Ложь;
	Элемент.МаксимальнаяШирина = 7;
	Элемент.РастягиватьПоГоризонтали = Истина;
	Элемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти