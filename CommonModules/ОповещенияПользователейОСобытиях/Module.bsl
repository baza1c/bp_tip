#Область ПрограммныйИнтерфейс

// Проверяет возможность использования оповещений ответственному по счету.
// 
// Возвращаемое значение:
//  Булево - Если Истина, то использование оповещений доступно.
Функция ИспользуютсяОповещенияОтветственнымПоСчету() Экспорт
	
	Возврат НапоминанияПользователя.ИспользуютсяНапоминанияПользователя()
		И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОтветственных");
	
КонецФункции

// Устанавливает настройку оповещения при изменении статусов для счета покупателю.
// 
// Параметры:
//  Счет - ДокументСсылка.СчетНаОплатуПокупателю - Счет покупателю, для которого устанавливаются настройки оповещения.
//  ОповещатьПриИзмененииСтатусаОплаты - Булево - Устанавливаемая настройка оповещения при изменении статуса опаты.
//  ОповещатьПриИзмененииСтатусаОтгрузки - Булево - Устанавливаемая настройки оповещения при изменении статуса отгрузки.
Процедура УстановитьНастройкиОповещенияПриИзмененииСтатусовСчета(Счет, ОповещатьПриИзмененииСтатусаОплаты, ОповещатьПриИзмененииСтатусаОтгрузки) Экспорт
	
	РегистрыСведений.НастройкиОповещенийПоСчетамПокупателю.УстановитьНастройкиОповещенияПоСчетуПокупателя(
		Счет, ОповещатьПриИзмененииСтатусаОплаты, ОповещатьПриИзмененииСтатусаОтгрузки);
	
КонецПроцедуры

// Устанавливает настройку оповещения при изменении статуса для задачи.
// 
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - Задача, для которой устанавливается настройка оповещения.
//  ОповещатьПриИзмененииСтатуса - Булево - Устанавливаемая настройка оповещения при изменении статуса задачи.
Процедура УстановитьНастройкиОповещенияПриИзмененииСтатусаЗадачи(Задача, ОповещатьПриИзмененииСтатуса) Экспорт
	
	РегистрыСведений.НастройкиОповещенийПоЗадачам.УстановитьНастройкиОповещенияПоЗадаче(
			Задача, ОповещатьПриИзмененииСтатуса);
	
КонецПроцедуры

// Создает оповещение ответственному об изменении статуса оплаты по счету покупателю.
// 
// Параметры:
//  СчетПокупателю - ДокументСсылка.СчетНаОплатуПокупателю - Счет покупателю, по которому необходимо отправить оповещение.
//  ИзмененСтатус - Булево - Признак, что статус оплаты изменен с "Оплачен" на "Не оплачен" или "Отмене".
Процедура ОтправитьОповещениеОбОплатеСчета(СчетПокупателю, ИзмененСтатус = Ложь) Экспорт
	
	ДанныеСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетПокупателю, "Контрагент, Ответственный");
	
	Если ИзмененСтатус Тогда
		ШаблонОповещения = НСтр("ru = 'Изменен статус оплаты счета для покупателя %1'");
	Иначе
		ШаблонОповещения = НСтр("ru = 'Поступила оплата от покупателя %1'");
	КонецЕсли;
	
	ТекстОповещения = СтрШаблон(ШаблонОповещения, ДанныеСчета.Контрагент);
	
	ОписаниеНапоминания = НапоминанияПользователяКлиентСервер.ОписаниеНапоминания(, Истина);
	ОписаниеНапоминания.Пользователь = ДанныеСчета.Ответственный;
	ОписаниеНапоминания.ВремяСобытия = ОбщегоНазначения.ТекущаяДатаПользователя();
	ОписаниеНапоминания.СрокНапоминания = ОписаниеНапоминания.ВремяСобытия;
	ОписаниеНапоминания.Источник = СчетПокупателю;
	ОписаниеНапоминания.Описание = ТекстОповещения;
	ОписаниеНапоминания.Идентификатор =
		ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОбОплатеСчета();
	ОписаниеНапоминания.Вставить("НавигационнаяСсылка", ПолучитьНавигационнуюСсылку(СчетПокупателю));
	
	НапоминанияПользователяСлужебный.ПодключитьНапоминание(ОписаниеНапоминания);
	
КонецПроцедуры

// Создает оповещение ответственному об изменении статуса отгрузки по счету покупателю.
// 
// Параметры:
//  СчетПокупателю - ДокументСсылка.СчетНаОплатуПокупателю - Счет покупателю, по которому необходимо отправить оповещение.
//  ИзмененСтатус - Булево - Признак, что статус отгрузки изменен с "Отгружен" на "Не отгружен".
Процедура ОтправитьОповещениеОбОтгрузкеСчета(СчетПокупателю, ИзмененСтатус = Ложь) Экспорт
	
	ДанныеСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетПокупателю, "Контрагент, Ответственный");
	
	Если ИзмененСтатус Тогда
		ШаблонОповещения = НСтр("ru = 'Изменен статус отгрузки счета для покупателя %1'");
	Иначе
		ШаблонОповещения = НСтр("ru = 'Произведена отгрузка покупателю %1'");
	КонецЕсли;
	
	ТекстОповещения = СтрШаблон(ШаблонОповещения, ДанныеСчета.Контрагент);
	
	ОписаниеНапоминания = НапоминанияПользователяКлиентСервер.ОписаниеНапоминания(, Истина);
	ОписаниеНапоминания.Пользователь = ДанныеСчета.Ответственный;
	ОписаниеНапоминания.ВремяСобытия = ОбщегоНазначения.ТекущаяДатаПользователя();
	ОписаниеНапоминания.СрокНапоминания = ОписаниеНапоминания.ВремяСобытия;
	ОписаниеНапоминания.Источник = СчетПокупателю;
	ОписаниеНапоминания.Описание = ТекстОповещения;
	ОписаниеНапоминания.Идентификатор =
		ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОбОтгрузкеСчета();
	ОписаниеНапоминания.Вставить("НавигационнаяСсылка", ПолучитьНавигационнуюСсылку(СчетПокупателю));
	
	НапоминанияПользователяСлужебный.ПодключитьНапоминание(ОписаниеНапоминания);
	
КонецПроцедуры

// Создает оповещение исполнителю о поступлении новой задачи.
// 
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - Задача, по которой необходимо отправить оповещение.
Процедура ОтправитьОповещениеОНовойЗадаче(Задача) Экспорт
	
	ДанныеЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Задача, "Исполнитель, Наименование");
	
	Исполнитель = ДанныеЗадачи.Исполнитель;
	
	ИмяПользователяИБ = ИмяПользователяИБ(Исполнитель);
	
	Если Не ЗначениеЗаполнено(ИмяПользователяИБ) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ОповещатьОНовыхЗадачах = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
		ОповещенияПользователейОСобытияхКлиентСервер.ИмяНастройкиОповещенияОПоступленииНовыхЗадач(), ИмяПользователяИБ);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ОповещатьОНовыхЗадачах Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОповещения = СтрШаблон(НСтр("ru = 'Направлена новая задача: %1'"), ДанныеЗадачи.Наименование);
	
	ОписаниеНапоминания = НапоминанияПользователяКлиентСервер.ОписаниеНапоминания(, Истина);
	ОписаниеНапоминания.Пользователь = Исполнитель;
	ОписаниеНапоминания.ВремяСобытия = ОбщегоНазначения.ТекущаяДатаПользователя();
	ОписаниеНапоминания.СрокНапоминания = ОписаниеНапоминания.ВремяСобытия;
	ОписаниеНапоминания.Источник = Задача;
	ОписаниеНапоминания.Описание = ТекстОповещения;
	ОписаниеНапоминания.Идентификатор =
		ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОНовойЗадаче();
	ОписаниеНапоминания.Вставить("НавигационнаяСсылка", ПолучитьНавигационнуюСсылку(Задача));
	
	НапоминанияПользователяСлужебный.ПодключитьНапоминание(ОписаниеНапоминания);
	
КонецПроцедуры

// Создает оповещения авторам задач об изменении статуса.
// 
// Параметры:
//  СписокЗадач - Массив из ЗадачаСсылка.ЗадачаИсполнителя - Список задач, по которым необходимо отправить оповещения.
//  НовыйСтатус - ПеречислениеСсылка.СтатусЗадачи - Новый статус для задач.
Процедура ОтправитьОповещенияОбИзмененииСтатусаЗадач(СписокЗадач, НовыйСтатус) Экспорт
	
	ТаблицаЗадач = ПодготовитьТаблицуЗадачДляОповещения(СписокЗадач, НовыйСтатус);
	
	Если Не ЗначениеЗаполнено(ТаблицаЗадач) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаЗадача Из ТаблицаЗадач Цикл
		ОтправитьОповещениеОбИзмененииСтатусаЗадачи(
			СтрокаЗадача.Задача, СтрокаЗадача.Наименование, СтрокаЗадача.Автор, НовыйСтатус);
	КонецЦикла;
	
КонецПроцедуры

// Создает оповещение автору задачи об изменении статуса.
// 
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - Задача, по которой необходимо отправить оповещение.
//  Наименование - Строка - Наименование задачи.
//  Пользователь - СправочникСсылка.Пользователи - Пользователь, которого необходимо оповестить.
//  Статус - ПеречислениеСсылка.СтатусЗадачи - Новый статус задачи.
Процедура ОтправитьОповещениеОбИзмененииСтатусаЗадачи(Задача, Наименование, Пользователь, Статус) Экспорт
	
	Если Статус = Перечисления.СтатусЗадачи.Выполнена Тогда
		ТекстОповещения = СтрШаблон(НСтр("ru = 'Выполнена задача: %1'"), Наименование);
	ИначеЕсли Статус = Перечисления.СтатусЗадачи.Отменена Тогда
		ТекстОповещения = СтрШаблон(НСтр("ru = 'Отменена задача: %1'"), Наименование);
	ИначеЕсли Статус = Перечисления.СтатусЗадачи.НеВыполнена Тогда
		ТекстОповещения = СтрШаблон(НСтр("ru = 'Изменился статус задачи: %1'"), Наименование);
	Иначе
		ТекстОповещения = Наименование;
	КонецЕсли;
	
	ОписаниеНапоминания = НапоминанияПользователяКлиентСервер.ОписаниеНапоминания( , Истина);
	ОписаниеНапоминания.Пользователь = Пользователь;
	ОписаниеНапоминания.ВремяСобытия = ОбщегоНазначения.ТекущаяДатаПользователя();
	ОписаниеНапоминания.СрокНапоминания = ОписаниеНапоминания.ВремяСобытия;
	ОписаниеНапоминания.Источник = Задача;
	ОписаниеНапоминания.Описание = ТекстОповещения;
	ОписаниеНапоминания.Идентификатор =
		ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОбИзмененииСтатусаЗадачи();
	ОписаниеНапоминания.Вставить("НавигационнаяСсылка", ПолучитьНавигационнуюСсылку(Задача));
	
	НапоминанияПользователяСлужебный.ПодключитьНапоминание(ОписаниеНапоминания);
	
КонецПроцедуры

// Подключает оповещение по задаче по переданным параметрам.
// 
// Параметры:
//  ОписаниеНапоминания - см. НапоминанияПользователяКлиентСервер.ОписаниеНапоминания
Процедура ПодключитьОповещениеПоЗадаче(ОписаниеНапоминания) Экспорт
	
	НапоминанияПользователяСлужебный.ПодключитьНапоминание(ОписаниеНапоминания);
	
КонецПроцедуры

// Удаляет подключенные оповещения по задаче.
// 
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - Задача, оповещения по которой необходимо удалить.
//  ИдентификаторОповещения - Строка - Идентификатор оповещения. Если задан, то будут удалены оповещения с данным идентификатором.
Процедура УдалитьПодключенныеОповещенияПоЗадаче(Задача, ИдентификаторОповещения = "") Экспорт
	
	НапоминанияПоПредмету = НапоминанияПользователя.НайтиНапоминания(Задача, ИдентификаторОповещения);
	Для Каждого Напоминание Из НапоминанияПоПредмету Цикл
		НапоминанияПользователяСлужебный.ОтключитьНапоминание(Напоминание);
	КонецЦикла;
	
КонецПроцедуры

// Обработка установки статуса задачи, при необходимости отправляются оповещения пользователям.
// 
// Параметры:
//  СписокЗадач - Массив из ЗадачаСсылка.ЗадачаИсполнителя - Список задач, по которым устанавливается статус.
//  НовыйСтатус - ПеречислениеСсылка.СтатусЗадачи - Новый статус для задач.
Процедура ОбработатьУстановкуСтатусаЗадачи(СписокЗадач, НовыйСтатус) Экспорт
	
	Если Не НапоминанияПользователя.ИспользуютсяНапоминанияПользователя() Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьОповещенияОбИзмененииСтатусаЗадач(СписокЗадач, НовыйСтатус);
	
КонецПроцедуры

// Создает оповещение ответственному о выдаче налоговой патента.
// 
// Параметры:
//  ОписаниеПатента - Структура - Описание патента:
//   * Патент - ДокументСсылка.ОперацияСПатентом - Патент, который был создан в программе.
//   * ДатаЗаявления - Дата - Дата заявления на получение патента.
//   * Ответственный - СправочникСсылка.Пользователи - Ответственный, которому будет отправлено оповещение.
Процедура ОтправитьОповещениеОВыдачеПатента(ОписаниеПатента) Экспорт
	
	ТекстОповещения = СтрШаблон(
		НСтр("ru = 'ИФНС выдала патент по вашему заявлению от %1'"),
		Формат(ОписаниеПатента.ДатаЗаявления, "ДФ=dd.MM.yyyy;"));
	
	ОписаниеНапоминания = НапоминанияПользователяКлиентСервер.ОписаниеНапоминания(, Истина);
	ОписаниеНапоминания.Пользователь = ОписаниеПатента.Ответственный;
	ОписаниеНапоминания.ВремяСобытия = ОбщегоНазначения.ТекущаяДатаПользователя();
	ОписаниеНапоминания.СрокНапоминания = ОписаниеНапоминания.ВремяСобытия;
	ОписаниеНапоминания.Источник = ОписаниеПатента.Патент;
	ОписаниеНапоминания.Описание = ТекстОповещения;
	ОписаниеНапоминания.Идентификатор =
		ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияОВыдачеПатента();
	ОписаниеНапоминания.Вставить("НавигационнаяСсылка", ПолучитьНавигационнуюСсылку(ОписаниеПатента.Патент));
	
	НапоминанияПользователяСлужебный.ПодключитьНапоминание(ОписаниеНапоминания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяПользователяИБ(Пользователь)
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ИдентификаторПользователяИБ"));
	
	Если ПользовательИБ = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ПользовательИБ.Имя;
	
КонецФункции

Функция ПодготовитьТаблицуЗадачДляОповещения(СписокЗадач, НовыйСтатус)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокЗадач", СписокЗадач);
	Запрос.УстановитьПараметр("НовыйСтатус", НовыйСтатус);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачаИсполнителя.Ссылка КАК Задача,
	|	ЗадачаИсполнителя.Наименование КАК Наименование,
	|	ЗадачаИсполнителя.Автор КАК Автор
	|ИЗ
	|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОповещенийПоЗадачам КАК НастройкиОповещенийПоЗадачам
	|		ПО ЗадачаИсполнителя.Ссылка В (&СписокЗадач)
	|		И ЗадачаИсполнителя.СтатусЗадачи <> &НовыйСтатус
	|		И ЗадачаИсполнителя.Ссылка = НастройкиОповещенийПоЗадачам.Задача
	|		И НастройкиОповещенийПоЗадачам.ОповещатьПриИзмененииСтатуса = ИСТИНА";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти