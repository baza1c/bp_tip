
#Область ПрограммныйИнтерфейс

// Возвращает текст, который выводится на баннер опции в том случае,
// если пользователь использовал максимальное количество опций в разных приложениях.
// 
// Возвращаемое значение:
//   - Строка - Тест ограничение тарифа для баннера
//
Функция ТекстОграничениеТарифа() Экспорт
	Возврат НСтр("ru = 'Лимит тарифа'");
КонецФункции

// Возвращает цвет баннера, который выводится для опции в том случае,
// если пользователь использовал максимальное количество опций в разных приложениях.
// 
// Возвращаемое значение:
//   Цвет
//
Функция ЦветБаннераОграниченияТарифа() Экспорт
	
	Возврат ЦветаСтиля.ЦветБаннераЗеленый;
	
КонецФункции

// Возвращает текст, который выводится на баннер опции в том случае,
// если опция не доступна для пользователя по тарифу.
// 
// Возвращаемое значение:
//   - Строка - Тест ограничение тарифа для баннера
//
Функция ТекстНедоступенТариф() Экспорт
	Возврат НСтр("ru = 'Доступно на других тарифах'");
КонецФункции

// Возвращает цвет баннера, который выводится для опции в том случае,
// если пользователь использовал максимальное количество опций в разных приложениях.
// 
// Возвращаемое значение:
//   Цвет
//
Функция ЦветБаннераНедоступенТариф() Экспорт
	
	Возврат ЦветаСтиля.ЦветБаннераБежевый;
	
КонецФункции

// Возвращает признак доступности раздела Лиды
//
// Возвращаемое значение:
//   Булево - Истина, если разрешено
//
Функция РазделЛидыДоступен() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию(ФункциональностьПрограммыВызовСервера.НаименованиеОпцииЛиды())
		И ТарификацияБПВызовСервераПовтИсп.РазрешеноВзаимодействиеСЛидами();
	
КонецФункции

// Выполняет изменение опции "Взаимодействие с лидами и покупателями"
//
// Параметры:
//   ВключитьЛиды - Булево - Значение константы ИспользоватьЛидыПростойИнтерфейс или ИспользоватьЛидыПолныйИнтерфейс
//
Процедура ИзменитьСвязанныеОбъектыВзаимодействияСЛидами(ВключитьЛиды = Ложь) Экспорт
	
	ЭтоПолныйИнтерфейс = ОбщегоНазначенияБП.ЭтоПолныйИнтерфейс();
	
	Если ВключитьЛиды Тогда
		
		// При включении опции работы с лидами обеспечиваем полную функциональность взаимодействия с клиентом:
		// - постановка задач в разрезе клиентов
		// - установка меток по лидам и контрагентам
		// - переписка с клиентом через почтовый клиент
		
		Если ЭтоПолныйИнтерфейс Тогда
			Константы.МоиЗадачиПолныйИнтерфейс.Установить(Истина);
		Иначе
			Константы.МоиЗадачиПростойИнтерфейс.Установить(Истина);
		КонецЕсли;
		
		Константы.ИспользоватьБизнесПроцессыИЗадачи.Установить(Истина);
		Константы.ИспользоватьДополнительныеРеквизитыИСведения.Установить(Истина);
		Константы.ИспользоватьНесколькоОтветственных.Установить(Истина);
		Константы.ЗаполнятьОтветственногоПоПродажамИзКонтрагента.Установить(Истина);
		Константы.ИспользоватьНапоминанияПользователя.Установить(Истина);
		Константы.ИспользоватьПочтовыйКлиент.Установить(Истина);
		Константы.ИспользоватьПрочиеВзаимодействия.Установить(Истина);
		Константы.ОтправлятьПисьмаВФорматеHTML.Установить(Истина);
		Константы.ИспользоватьПризнакРассмотрено.Установить(Истина);
		
		Справочники.СостоянияРаботыСЛидами.ЗаполнитьЗначенияПоУмолчанию();
		
	Иначе
		
		// Зависимые опции отключаем, если объекты не использовались
		
		Если Не МоиЗадачиИспользуются() Тогда
			
			Если ЭтоПолныйИнтерфейс Тогда
				Константы.МоиЗадачиПолныйИнтерфейс.Установить(Ложь);
			Иначе
				Константы.МоиЗадачиПростойИнтерфейс.Установить(Ложь);
			КонецЕсли;
			
			Константы.ИспользоватьБизнесПроцессыИЗадачи.Установить(Ложь);
		КонецЕсли;
		
		Константы.ЗаполнятьОтветственногоПоПродажамИзКонтрагента.Установить(Ложь);
		Если Не ВДокументаИспользуетсяОтветственный() Тогда
			Константы.ИспользоватьНесколькоОтветственных.Установить(Ложь);
		КонецЕсли;
		
		Если Не СведенияИспользуются() Тогда
			Константы.ИспользоватьДополнительныеРеквизитыИСведения.Установить(Ложь);
		КонецЕсли;
		
		// Безусловно отключаем почтовый клиент
		Константы.ИспользоватьПочтовыйКлиент.Установить(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МоиЗадачиИспользуются()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачаИсполнителя.Ссылка
	|ИЗ
	|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	|ГДЕ
	|	Не ЗадачаИсполнителя.ПометкаУдаления");
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СведенияИспользуются()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДополнительныеРеквизитыИСведения.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	НЕ ДополнительныеРеквизитыИСведения.ПометкаУдаления");
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ВДокументаИспользуетсяОтветственный()
	
	Если ПользователиБП.КоличествоАктуальныхПользователей() <= 1 Тогда
		// В базе один пользователь, разделение документов по ответственным не актуально
		Возврат Ложь;
	КонецЕсли;
	
	// Если хотя бы в одном документе заполнен ответственный и это не текущий пользователь,
	// считаем, что ответственные используются в документах 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЖурналОпераций.Ссылка
	|ИЗ
	|	ЖурналДокументов.ЖурналОпераций КАК ЖурналОпераций
	|ГДЕ
	|	НЕ ЖурналОпераций.ПометкаУдаления
	|	И (ЖурналОпераций.Ответственный <> &ТекущийПользователь
	|	И ЖурналОпераций.Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти