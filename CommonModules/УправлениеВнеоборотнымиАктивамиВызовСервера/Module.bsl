#Область ПрограммныйИнтерфейс

#Область СообщениеОНаличииТранспортаИЗемли

// Возвращает структуру со значениями полей страницы транспортного средства сообщения о наличии ТС и зем. участков.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, по которой подается сообщение о наличии ТС.
//  ТранспортноеСредство - СправочникСсылка.ОсноныеСредства - транспортное средство, по которому заполняется страница сообщения.
//  Период - дата - дата окончания отчетного периода, за который подается сообщение.
//
Функция ДанныеСтраницыТранспортногоСредства(Организация, ТранспортноеСредство, Период) Экспорт
	
	// Структура со значениями полей страницы транспортного средства
	ДанныеСтраницы = Новый Структура;
	ДанныеСтраницы.Вставить("ВидТС");        // вид транспортного средства
	ДанныеСтраницы.Вставить("ИдНомТС");      // идентификационный номер (VIN)
	ДанныеСтраницы.Вставить("МаркаТС");      // марка
	ДанныеСтраницы.Вставить("РегЗнакТС");    // регистрационный знак
	ДанныеСтраницы.Вставить("ДатаРегТС");    // дата регистрации
	ДанныеСтраницы.Вставить("ДатаСнУчетТС"); // дата снятия с учета
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетТранспортногоНалога.ВидТранспортногоСредства КАК ВидТранспортногоСредства,
	|	РасчетТранспортногоНалога.ИдентификационныйНомер КАК ИдНомТС,
	|	РасчетТранспортногоНалога.Марка КАК МаркаТС,
	|	РасчетТранспортногоНалога.РегистрационныйЗнак КАК РегЗнакТС,
	|	РасчетТранспортногоНалога.ДатаРегистрации КАК ДатаРегТС,
	|	РасчетТранспортногоНалога.ДатаСнятияСУчета КАК ДатаСнУчетТС
	|ИЗ
	|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
	|ГДЕ
	|	РасчетТранспортногоНалога.Организация В(&Организации)
	|	И РасчетТранспортногоНалога.ПериодРасчета = &ПериодРасчета
	|	И РасчетТранспортногоНалога.ОсновноеСредство = &ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновноеСредство,
	|	ДатаРегТС";
	
	Запрос.УстановитьПараметр("Организации", ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(Организация));
	Запрос.УстановитьПараметр("ПериодРасчета", Период);
	Запрос.УстановитьПараметр("ОсновноеСредство", ТранспортноеСредство);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ДанныеСтраницы;
	КонецЕсли;
	
	ВыборкаТС = РезультатЗапроса.Выбрать();
	ВыборкаТС.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДанныеСтраницы, ВыборкаТС);
	
	СведенияОВидахТранспортныхСредств = РасчетИмущественныхНалоговВызовСервера.СведенияОВидахТранспортныхСредств();
	ДанныеСтраницы.ВидТС = РасчетИмущественныхНалоговВызовСервера.НомерКатегорииПоВидуТранспортногоСредства(
		ВыборкаТС.ВидТранспортногоСредства,
		СведенияОВидахТранспортныхСредств);
	
	Возврат ДанныеСтраницы;
	
КонецФункции

// Возвращает структуру со значениями полей страницы земельного участка сообщения о наличии ТС и зем. участков.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, по которой подается сообщение о наличии земельных участков.
//  ЗемельныйУчасток - СправочникСсылка.ОсноныеСредства - земельный участок, по которому заполняется страница сообщения.
//  Период - дата - дата окончания отчетного периода, за который подается сообщение.
//
Функция ДанныеСтраницыЗемельногоУчастка(Организация, ЗемельныйУчасток, Период) Экспорт
	
	// Структура со значениями полей страницы земельного участка
	ДанныеСтраницы = Новый Структура;
	ДанныеСтраницы.Вставить("НомКадастрЗУ");  // кадастровый номер земельного участка
	ДанныеСтраницы.Вставить("ДатаРегПрав");   // дата регистрации (возникновения) права на участок
	ДанныеСтраницы.Вставить("ДатаПрекрПрав"); // дата прекращения права
	ДанныеСтраницы.Вставить("Комментарий");   // наименование основного средства в программе (служебное поле)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетЗемельногоНалога.КадастровыйНомер КАК НомКадастрЗУ,
	|	РасчетЗемельногоНалога.ОсновноеСредство.Наименование КАК Наименование,
	|	РасчетЗемельногоНалога.ДатаРегистрации КАК ДатаРегПрав,
	|	РасчетЗемельногоНалога.ДатаСнятияСУчета КАК ДатаПрекрПрав
	|ИЗ
	|	РегистрСведений.РасчетЗемельногоНалога КАК РасчетЗемельногоНалога
	|ГДЕ
	|	РасчетЗемельногоНалога.Организация В(&Организации)
	|	И РасчетЗемельногоНалога.ПериодРасчета = &ПериодРасчета
	|	И РасчетЗемельногоНалога.ОсновноеСредство = &ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомКадастрЗУ,
	|	ДатаРегПрав";
	
	Запрос.УстановитьПараметр("Организации", ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(Организация));
	Запрос.УстановитьПараметр("ПериодРасчета", Период);
	Запрос.УстановитьПараметр("ОсновноеСредство", ЗемельныйУчасток);
	
	ВыборкаЗУ = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаЗУ.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеСтраницы, ВыборкаЗУ);
	КонецЕсли;
	
	Возврат ДанныеСтраницы;
	
КонецФункции

// Определяет дату окончания отчетного периода (последнего предшествующего), за который подается уведомление.
// 
// Параметры:
//  ДатаСообщения - Дата - предполагаемая дата подачи сообщения, обычно дата подписи.
// 
// Возвращаемое значение:
//  Дата - окончания последнего отчетного периода, который предшествует дате уведомления 
// и за который это уведомление подается.
//
Функция ПоследнийОтчетныйПериодПоДатеУведомления(Знач ДатаСообщения) Экспорт
	
	ДатаСообщения = ?(ЗначениеЗаполнено(ДатаСообщения), ДатаСообщения, ОбщегоНазначения.ТекущаяДатаПользователя());
	Возврат НачалоДня(НачалоГода(ДатаСообщения)-1); // конец прошлого года
	
КонецФункции

#КонецОбласти

#КонецОбласти
