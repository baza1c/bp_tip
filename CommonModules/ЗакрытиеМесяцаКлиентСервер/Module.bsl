#Область ПрограммныйИнтерфейс

// Определяет, используется ли документ РегламентнаяОперация для переданного вида операции
//
// Параметры:
//  ВидыРегламентныхОпераций - ПеречислениеСсылка.ВидыРегламентныхОпераций
//
// Возвращаемое значение:
//  Булево
Функция ИспользуетсяДокументРегламентнаяОперация(ВидОперации) Экспорт
	
	ТипВидыРегламентныхОпераций = Тип("ПеречислениеСсылка.ВидыРегламентныхОпераций");
	Возврат ТипЗнч(ВидОперации) = ТипВидыРегламентныхОпераций 
		И ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыРегламентныхОпераций.НачислениеНДС")
		И ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыРегламентныхОпераций.НачислениеЗарплаты")
		И ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыРегламентныхОпераций.НачислениеРезервовПоОплатеТруда");
		
КонецФункции

// Определяет, мешает ли операция считать месяц закрытым
//
// Параметры:
//  Операция - Строка таблицы значений, которая содержит колонки Требуется, Устарела и Состояние. 
//             Такую таблицу готовит процедура ЗакрытиеМесяца.СоздатьДетальныйСтатусЗакрытияМесяца()
//             Такая же таблица используется в форме Обработки.ЗакрытиеМесяца
//
// Возвращаемое значение:
//  Булево - Истина - требуется что-то сделать - выполнить или пропустить
//         - Ложь - не мешает считать месяц закрытым
Функция ОперацияВРаботе(Операция) Экспорт
	
	Если Не Операция.Требуется Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Операция.Устарела
		Или Операция.Состояние = ПредопределенноеЗначение("Перечисление.ВидыСостоянийРегламентныхОпераций.ВыполненоСОшибками")
		Или Операция.Состояние = ПредопределенноеЗначение("Перечисление.ВидыСостоянийРегламентныхОпераций.НеВыполнено")
		Или Операция.Состояние = ПредопределенноеЗначение("Перечисление.ВидыСостоянийРегламентныхОпераций.Выполняется");
	
КонецФункции

Функция СобытиеЖурналаРегистрации(ПредставлениеВидаОперации = "") Экспорт
	
	ИмяГруппы = НСтр("ru = 'Закрытие месяца'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()); // Строка записывается в журнал
	
	Если ПустаяСтрока(ПредставлениеВидаОперации) Тогда
		Возврат ИмяГруппы;
	КонецЕсли;
	
	Возврат ИмяГруппы + "." + ПредставлениеВидаОперации;
	
КонецФункции

Функция ИмяФоновогоЗаданияПереносаГраницы() Экспорт
	
	Возврат НСтр("ru = 'Перенос границы последовательности без перепроведения'");
	
КонецФункции

// Возвращает структуру параметров, используемых алгоритмом проверки актуальности.
//
Функция НовыеПараметрыПроверкиАктуальности() Экспорт

	Результат = Новый Структура();
	Результат.Вставить("Организация", 	ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	Результат.Вставить("Период", 		'0001-01-01');
	Результат.Вставить("УИДЗамера");
	Результат.Вставить("УникальныйИдентификаторФормы");
	Результат.Вставить("ПроверятьКонстантуАктуальностиДанныхУчета", Ложь);
	Результат.Вставить("ОтборыРасчетыСКонтрагентами"); 	// Структура - см. БухгалтерскиеОтчетыКлиентСервер.ОпределитьПараметрыОтчетаРасчетыСКонтрагентами()
	Результат.Вставить("ДанныеРасшифровки");			// ДанныеРасшифровкиКомпоновкиДанных или Неопределено
	Результат.Вставить("ТребуетсяПолнаяАктуализация");	// Булево или Неопределено
	Результат.Вставить("АктуализироватьВесьПериод", Ложь);
	Результат.Вставить("АктуализацияДляРасчетаНалога", Ложь);
	
	Возврат Результат;

КонецФункции

// Возвращает структуру параметров, используемых алгоритмом актуализации.
//
Функция НовыеПараметрыАктуализации() Экспорт
	
	ПараметрыАктуализации = Новый Структура;
	
	ПараметрыАктуализации.Вставить("Организация",                  ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	ПараметрыАктуализации.Вставить("Период",                       '00010101'); // месяц, до которого выполняется актуализация
	ПараметрыАктуализации.Вставить("ДатаНачалаАктуализации",       '00010101'); // месяц, начиная с которого выполняется актуализация может быть установлен
	                                                                            // явным образом
	ПараметрыАктуализации.Вставить("ОтборыРасчетыСКонтрагентами"); // см. БухгалтерскиеОтчетыКлиентСервер.ОпределитьПараметрыОтчетаРасчетыСКонтрагентами()
	ПараметрыАктуализации.Вставить("ДанныеРасшифровки");           // см. БухгалтерскиеОтчетыКлиентСервер.ПодготовитьДанныеРасшифровкиДляФоновогоЗадания()
	ПараметрыАктуализации.Вставить("ДополнительныеПараметры");
	
	ПараметрыАктуализации.Вставить("ИдентификаторЗадания");          // идентификатор фонового задания
	ПараметрыАктуализации.Вставить("УникальныйИдентификаторФормы");  // идентификатор формы, откуда вызвано фоновое задание
	ПараметрыАктуализации.Вставить("АдресХранилищаСОшибками",      ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор));
	ПараметрыАктуализации.Вставить("ОчищатьАдресСОшибками",        Истина);
	
	// Требуется ли проверять, что в настройках включен анализ актуальности.
	ПараметрыАктуализации.Вставить("ПроверятьКонстантуАктуальностиДанныхУчета", Ложь);
	
	// Параметры проверки активной работы пользователей с документами последовательности.
	ПараметрыАктуализации.Вставить("ЭтоИнтерактивныйЗапуск", Ложь);
	ПараметрыАктуализации.Вставить("ПроверятьАктивностьПользователей", Ложь);
	ПараметрыАктуализации.Вставить("ИнтервалПроверкиВСекундах", 5);
	
	// Требуется ли выполнять лишь частичную актуализацию.
	ПараметрыАктуализации.Вставить("АктуализацияДляРасчетаНалога", Ложь);
	ПараметрыАктуализации.Вставить("АктуализироватьВесьПериод", Ложь);
	ПараметрыАктуализации.Вставить("ЭтоЗакрытиеМесяца", Ложь);
	
	// Дата начала выполнения регламентных операций при частичной актуализации.
	ПараметрыАктуализации.Вставить("ГраницаАктуализацииНачало", '00010101');
	
	Возврат ПараметрыАктуализации;
	
КонецФункции

// Возвращает структуру параметров, используемых для проверки активности сеансов.
//
Функция НовыеПараметрыПроверкиАктивностиСеансов() Экспорт
	
	ПараметрыПроверки = Новый Структура;
	
	// Когда в последний раз выполнялся запрос к регистру АктивностьСеансовПользователей.
	ПараметрыПроверки.Вставить("ПоследняяПроверкаСеансов", '00010101');
	
	// Когда требуется выполнить следующий запрос к регистру АктивностьСеансовПользователей.
	ПараметрыПроверки.Вставить("СледующаяПроверкаАктивныхСеансов", '00010101');
	
	// Выполнять проверку активной работы пользователей с документами последовательности.
	ПараметрыПроверки.Вставить("ПроверятьАктивностьПользователей", Ложь);
	
	// Минимальная периодичность, с которой можно выполнять запросы к регистру АктивностьСеансовПользователей.
	ПараметрыПроверки.Вставить("ИнтервалПроверкиВСекундах", 5);
	
	// Операция была запущена пользователем из интерактивного сеанса и отслеживать работу с документами не нужно.
	ПараметрыПроверки.Вставить("ЭтоИнтерактивныйЗапуск", Ложь);
	
	Возврат ПараметрыПроверки;
	
КонецФункции

// Возвращает структуру параметров, для записи регламентного задания автоматического закрытия месяца.
//
Функция НовыеПараметрыЗаписиРегламентногоЗаданияЗакрытияМесяца() Экспорт
	
	ПараметрыЗаписи = Новый Структура;
	
	// Организация для которой записывается задание. Так же наименование организации добавляется в название задания.
	ПараметрыЗаписи.Вставить("Организация",          ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	
	// Идентификатор для поиска и изменения существующего задания. Если идентификатор пустой, запишется новое задание.
	ПараметрыЗаписи.Вставить("ИдентификаторЗадания", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	ПараметрыЗаписи.Вставить("ДатаНачала",           '00010101'); // Дата начала запуска задания.
	ПараметрыЗаписи.Вставить("ВремяЗапуска",         '00010101'); // Время запуска задания.
	
	// Отражает факт того, что был перенос расписания задания и необходимо-ли вернуть расписание в исходное состояние.
	// см. описание ЗакрытиеМесяца.АвтоматическоеЗакрытиеМесяцаРегламентноеЗадание() - ВыполнениеОстановлено.
	ПараметрыЗаписи.Вставить("ОтложенныйЗапуск",     Ложь);
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

// Возвращает структуру параметров, используемых алгоритмом проверки периода для автоматического закрытия месяца.
//
Функция НовыеПараметрыПроверкиПериодаАвтозакрытия() Экспорт
	
	Результат = Новый Структура;
	
	// Организация для которой выполняется проверка разрешенного для закрытия периода.
	Результат.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	
	Результат.Вставить("ТолькоПерепроведениеДокументов", Ложь); // Если Ложь, то предполагается полное закрытие месяца.
	Результат.Вставить("ВыполнятьВОтчетномПериоде",      Истина); // Поквартально, или по количеству месяцев.
	Результат.Вставить("РазрешенноеЧислоМесяцевНазад",   0); // Число месяцев назад, разрешенное для автоматического закрытия.
	
	// Перед проверкой, требуется заранее получить ДатуАктуальности (на клиенте лучше сделать это асинхронно),
	// после чего вставить в эту структуру, для дальнейшего использования в процедуре проверки.
	Результат.Вставить("ДатаАктуальности",  '00010101');
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру параметров, используемых для определения момента следующего запуска регламентного задания закрытия месяца.
//
Функция НовыеПараметрыБлижайшейДатыЗапускаЗадания() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Ежедневно",             Истина); // Запуск задания ежедневно, или в отдельные дни.
	Результат.Вставить("ВремяЗапуска",          '00010101'); // Время запуска задания.
	Результат.Вставить("ДеньПервогоЗапуска",    1);  // Используется если Ежедневно = Ложь.
	Результат.Вставить("ДеньПоследнегоЗапуска", 31); // Используется если Ежедневно = Ложь.
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру параметров, используемых для регламентного задания автоматического закрытия месяца.
//
Функция НовыеПараметрыЗаданияЗакрытияМесяца() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаНачала",          '00010101'); // Дата начала запуска задания.
	Результат.Вставить("ВыполнениеВключено",   Ложь); // Выполнение задания включено или нет.
	
	// Идентификатор задания который в дальнейшем сохраняется в регистр с настройками.
	Результат.Вставить("ИдентификаторЗадания", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор()); 
	
	// Дополнение параметрами, от которых напрямую зависит состав параметров этой функции.
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, НовыеПараметрыПроверкиПериодаАвтозакрытия());
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, НовыеПараметрыБлижайшейДатыЗапускаЗадания());
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру параметров актуализации, вызванной из отчета.
//
Функция НовыйПараметрыАктуализацииОтчета() Экспорт
	
	ПараметрыАктуализации = Новый Структура;
	
	ПараметрыАктуализации.Вставить("Организация",                       ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	ПараметрыАктуализации.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	ПараметрыАктуализации.Вставить("ДатаАктуальности",                  '00010101');
	ПараметрыАктуализации.Вставить("ДатаОкончанияАктуализации",         '00010101');
	
	Возврат ПараметрыАктуализации;
	
КонецФункции

// Возвращает структуру - результат запуска фоновой актуализации по умолчанию
//
Функция НовыйРезультатАктуализации() Экспорт
	
	РезультатАктуализации = Новый Структура;
	
	РезультатАктуализации.Вставить("Выполнено",                       Ложь);
	РезультатАктуализации.Вставить("ВывестиИнформациюУведомлений",    Ложь);
	РезультатАктуализации.Вставить("СообщенияПользователю",           Новый Массив);
	РезультатАктуализации.Вставить("АдресХранилища",                  "");
	РезультатАктуализации.Вставить("ИдентификаторЗадания");
	
	// Актуализированы все расчеты, а не только отложенные расчеты с контрагентами.
	РезультатАктуализации.Вставить("ПолнаяАктуализация", Ложь);
	
	// Выполнены только рег.операции, поставляющие данные для расчета налога.
	РезультатАктуализации.Вставить("АктуализацияДляРасчетаНалога", Ложь);
	
	// Требуется при выполнении по организациям с обособленными подразделениями
	РезультатАктуализации.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	
	// Факт остановки выполнения актуализации из-за действий пользователя в интерактивном сеансе. 
	РезультатАктуализации.Вставить("ВыполнениеОстановлено", Ложь);
	
	// В случае остановки по ошибке или передаче приоритета сеансам, содержит в себе дату на котрой завершилось выполнение.
	РезультатАктуализации.Вставить("ПериодСОшибкойВыполнения", '00010101');
	
	// Операция была запущена непосредственно пользователем.
	РезультатАктуализации.Вставить("ЭтоИнтерактивныйЗапуск", Ложь);
	
	Возврат РезультатАктуализации;
	
КонецФункции

Функция ОтчетИспользуетМеханизмАктуализации(Форма) Экспорт
	
	Возврат Форма.Элементы.Найти("Актуализация") <> Неопределено;
	
КонецФункции

// Проверяет использование в отчете нового механизма работы с длительными операциями.
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения - проверяемая форма.
//
// Возвращаемое значение:
//   Булево      - Истина, новый программный интерфейс; Ложь, устаревший программный интерфейс.
//
Функция ИспользуетсяОписаниеЗаданияАктуализации(Форма) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОписаниеЗаданияАктуализации");
	
КонецФункции

// Возвращает строковое имя задания актуализации.
// Используется в качестве общей части ключа задания, до указания параметров.
//
Функция ПрефиксКлючаЗаданияАктуализации() Экспорт
	
	Возврат "АктуализацияДанных";
	
КонецФункции

// Возвращает строковое имя регламентного задания автоматического закрытия месяца.
// Используется в качестве общей части ключа задания, до указания параметров.
//
Функция ПрефиксКлючаРегламентногоЗаданияЗакрытияМесяца() Экспорт
	
	Возврат "АвтоматическоеЗакрытиеМесяца";
	
КонецФункции

// Проверяет, что указанный ключ принадлежит регламентному заданию закрытия месяца.
//
// Параметры:
//  Ключ - Строка - см. КлючЗаданияАктуализации().
// 
// Возвращаемое значение:
//  Булево - это ключ регламентного задания автоматического закрытия месяца.
//
Функция ЭтоКлючРегламентногоЗаданияЗакрытияМесяца(Ключ) Экспорт
	
	КлючЗадания    = СтрРазделить(Ключ, ":");
	ПрефиксЗадания = ?(КлючЗадания.Количество() > 0, КлючЗадания[0], "");
	Результат = ПрефиксЗадания = ПрефиксКлючаРегламентногоЗаданияЗакрытияМесяца();
	
	Возврат Результат;
	
КонецФункции

// Формирует по единому алгоритму ключ задания актуализации.
//
// Параметры:
//  ПараметрыЗадания - Структура - см. ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации()
//  ПользовательЗадания - СправочникСсылка.Пользователи - если задан, то ключ будет содержать идентификатор пользователя.
//
// Возвращаемое значение:
//   Строка      - содержит идентификаторы, разделенные ":".
//
Функция КлючЗаданияАктуализации(ПараметрыЗадания, ПользовательЗадания = Неопределено,
								РегламентноеЗадание = Ложь) Экспорт
	
	ПрефиксКлюча = ?(РегламентноеЗадание, ПрефиксКлючаРегламентногоЗаданияЗакрытияМесяца(),
										  ПрефиксКлючаЗаданияАктуализации());
	
	Если ПользовательЗадания = Неопределено Тогда
		
		КлючЗадания = "%1:%2";
		КлючЗадания = СтрШаблон(КлючЗадания,
			ПрефиксКлюча,
			ПараметрыЗадания.Организация.УникальныйИдентификатор())
		
	Иначе
		
		КлючЗадания = "%1:%2:%3:%4";
		КлючЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(КлючЗадания,
			ПрефиксКлюча,
			ПараметрыЗадания.Организация.УникальныйИдентификатор(),
			ПользовательЗадания.УникальныйИдентификатор(),
			Формат(ТекущаяУниверсальнаяДатаВМиллисекундах(), "ЧГ=0"));
			
	КонецЕсли;
		
	Возврат КлючЗадания;	
		
КонецФункции

// Вызывается в контексте формы.
// Предварительно требует проверки ОтчетИспользуетМеханизмАктуализации()
//
Процедура СкрытьПанельАктуализации(Форма) Экспорт
	
	Форма.Элементы.Актуализация.Видимость = Ложь;
	
КонецПроцедуры

#Область ИндикацияХодаВыполнения

Функция ИмяЭтапаПереносГраницы() Экспорт

	Возврат "ПереносГраницыАктуальности";

КонецФункции

Функция ИмяЭтапаПерепроведениеДокументов() Экспорт

	Возврат "ПерепроведениеДокументов";

КонецФункции

Функция ИмяЭтапаАктуализацияРасчетовСКонтрагентами() Экспорт

	Возврат "АктуализацияРасчетовСКонтрагентами";

КонецФункции

Функция ИмяЭтапаЗакрытиеМесяца() Экспорт

	Возврат "ЗакрытиеМесяца";

КонецФункции

// Возвращает структуру параметров для сообщений прогресса выполнения.
//
Функция НовыеПараметрыСообщенийПрогресса() Экспорт

	Результат = Новый Структура();
	Результат.Вставить("Месяц",            '0001-01-01');
	Результат.Вставить("Организация",      ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	Результат.Вставить("ИмяЭтапа",         ""); // См. ЗакрытиеМесяцаКлиентСервер.ИмяЭтапаХХХ()
	Результат.Вставить("НачальноеЗначение"); 	// Число или Дата - начальная граница диапазона
	Результат.Вставить("КонечноеЗначение");  	// Число или Дата - конечная граница диапазона
	Результат.Вставить("ДостигнутоеЗначение");  // Число или Дата - текущее положение
	
	// Сообщение прогресса связано с контролем наличия ранее запущенных фоновых заданий на файловой базе.
	Результат.Вставить("КонтрольРанееЗапущенных", Ложь); // Булево

	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецОбласти

// Возвращает имя события сбора статистики на основе переданного имени операции.
//
// Параметры:
//  ИмяОперации - Строка
//
// Возвращаемое значение:
//  Строка - Сформированное имя события сбора статистики.
//
Функция ИмяСобытия(ИмяОперации) Экспорт

	ЧастиИмени = Новый Массив;
	ЧастиИмени.Добавить("СтатистикаБП.ЗакрытиеМесяца.");
	ЧастиИмени.Добавить(ИмяОперации);
	
	Возврат СтрСоединить(ЧастиИмени);
	
КонецФункции

