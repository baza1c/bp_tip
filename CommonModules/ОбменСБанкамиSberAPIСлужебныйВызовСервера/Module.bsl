////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиSberAPIСлужебныйВызовСервера: обмен со Сбербанком через сервис SberAPI.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Получает статусы документов и недостающие выписки
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//  НомераСчетов - Массив из Строка - номера счетов, по которым требуется синхронизация.
//
// Возвращаемое значение:
//  Структура - Описание:
//   * Сообщения - ФиксированныйМассив
//   * ПодробноеПредставлениеОшибки - Строка
//   * КраткоеПредставлениеОшибки - Строка
//   * АдресДополнительногоРезультата - Строка
//   * АдресРезультата - Строка
//   * ИдентификаторЗадания - Неопределено
//   * Статус - Строка
//
Функция ЗапускЗаданияПолученияДокументов(Знач НастройкаОбмена, Знач НомераСчетов = Неопределено) Экспорт
	
	ПараметрыСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	Если ПараметрыСессии = Неопределено
		Или Не ПараметрыСессии.Свойство("ДоступныеСчета")
		Или Не ЗначениеЗаполнено(ПараметрыСессии.ДоступныеСчета) Тогда
		Возврат Новый Структура("ТребуетсяАутентификация");
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение статусов документов через сервис SberAPI'");
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыПроцедуры.Вставить("ТекущаяСессия", ПараметрыСессии);
	ПараметрыПроцедуры.Вставить("МассивБанковскихСчетов", НомераСчетов);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиSberAPIСлужебный.ПолучитьСтатусыДокументов", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

// Запускает фоновое задание аутентификации на сервере Сбербанка по логину
//
// Параметры:
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена.
//    Логин - Строка - логин пользователя в системе банка
//    Пароль - Строка - пароль для логина.
//    ФродПараметры - Структура - параметры фрод-мониторинга, полученные на клиенте.
//    Организация - ОпределяемыйТип.Организация, Неопределено - ссылка на организацию
//    Банк - ОпределяемыйТип.БанкОбменСБанками, Неопределено - ссылка на банк.
//
// Возвращаемое значение:
//    Структура - структура возврата описана в ДлительныеОперации.ВыполнитьВФоне.
//
Функция ЗапускЗаданияАутентификацииПоЛогину(Знач НастройкаОбмена,
											Знач Логин,
											Знач Пароль,
											Знач ФродПараметры,
											Знач Организация,
											Знач Банк,
											Знач ИгнорироватьСохраненныеНастройки = Ложь) Экспорт
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОперации.Вставить("Логин", СокрЛП(Логин));
	ПараметрыОперации.Вставить("Пароль", Пароль);
	ПараметрыОперации.Вставить("ФродПараметры", ФродПараметры);
	ПараметрыОперации.Вставить("Организация", Организация);
	ПараметрыОперации.Вставить("Банк", Банк);
	ПараметрыОперации.Вставить("ИгнорироватьСохраненныеНастройки", ИгнорироватьСохраненныеНастройки);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Предварительная аутентификация по логину'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиSberAPIСлужебный.ВыполнитьАутентификациюПоЛогину", ПараметрыОперации, ПараметрыВыполнения);
	
	Возврат Результат
	
КонецФункции

// Отправляет одноразовый пароль в Сбербанк
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком;
//  ИдентификаторСессии - Строка - идентификатор установленной сессии со Сбербанком
//  ОдноразовыйПароль - Строка - пароль из SMS сообщения
//  Организация - ОпределяемыйТип.Организация, Неопределено - ссылка на организацию
//
// Возвращаемое значение:
//  Структура - структура возврата описана в ДлительныеОперации.ВыполнитьВФоне.
//
Функция ЗапускЗаданияОтправитьОдноразовыйПароль(Знач НастройкаОбмена,
												Знач ИдентификаторСессии,
												Знач ОдноразовыйПароль,
												Знач Организация,
												Знач ИгнорироватьСохраненныеНастройки = Ложь) Экспорт
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОперации.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыОперации.Вставить("ОдноразовыйПароль", ОдноразовыйПароль);
	ПараметрыОперации.Вставить("Организация", Организация);
	ПараметрыОперации.Вставить("ИгнорироватьСохраненныеНастройки", ИгнорироватьСохраненныеНастройки);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'SMS аутентификация SberAPI'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиSberAPIСлужебный.ОтправитьОдноразовыйПароль", ПараметрыОперации, ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

Функция ЗапускЗаданияПолучитьТокенДоступа(Знач НастройкаОбмена,
										  Знач ИдентификаторСессии,
										  Знач Организация,
										  Знач ИгнорироватьСохраненныеНастройки,
										  Знач НовыйИдентификаторОрганизации) Экспорт
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОперации.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыОперации.Вставить("Организация", Организация);
	ПараметрыОперации.Вставить("ИгнорироватьСохраненныеНастройки", ИгнорироватьСохраненныеНастройки);
	ПараметрыОперации.Вставить("НовыйИдентификаторОрганизации", НовыйИдентификаторОрганизации);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение токена доступа SberAPI'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиSberAPIСлужебный.ПолучитьТокенДоступа", ПараметрыОперации, ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

Функция ТребуетсяАутентификация(НастройкаОбмена, НомерСчета = Неопределено) Экспорт
	
	ПараметрыСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	Если ПараметрыСессии = Неопределено Или Не ПараметрыСессии.Свойство("ДоступныеСчета")
		Или Не ЗначениеЗаполнено(ПараметрыСессии.ДоступныеСчета)
		Или (ЗначениеЗаполнено(НомерСчета)
			И ПараметрыСессии.ДоступныеСчета.Получить(НомерСчета) = Неопределено) Тогда
			
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Получение выписки Сбербанка через сервис SberAPI
//
// Параметры:
//   ПараметрыПолучения - Структура - содержит поля:
//    * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//    * НомерСчета - Строка - номер банковского счета
//    * ДатаНачала - Дата - дата начала периода запроса выписки
//    * ДатаОкончания - Дата - дата окончания периода запроса выписки
// Возвращаемое значение:
//   Структура - Описание:
//    * Сообщения - ФиксированныйМассив
//    * ПодробноеПредставлениеОшибки - Строка
//    * КраткоеПредставлениеОшибки - Строка
//    * АдресДополнительногоРезультата - Строка
//    * АдресРезультата - Строка
//    * ИдентификаторЗадания - Неопределено
//    * Статус - Строка
//
Функция ЗапускЗаданияПолученияВыписки(Знач ПараметрыПолучения) Экспорт
	
	ПараметрыСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(ПараметрыПолучения.НастройкаОбмена);
	Если ПараметрыСессии = Неопределено Или Не ПараметрыСессии.Свойство("ДоступныеСчета")
		Или Не ЗначениеЗаполнено(ПараметрыСессии.ДоступныеСчета)
		Или (ЗначениеЗаполнено(ПараметрыПолучения.НомерСчета)
			И ПараметрыСессии.ДоступныеСчета.Получить(ПараметрыПолучения.НомерСчета) = Неопределено) Тогда
			
		Возврат Новый Структура("ТребуетсяАутентификация");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПолучения.НомерСчета) Тогда
		МассивБанковскихСчетов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыПолучения.НомерСчета);
	Иначе
		РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПараметрыПолучения.НастройкаОбмена, "Организация, Банк");
		МассивБанковскихСчетов = ОбменСБанкамиСлужебный.НомераБанковскихСчетов(
			РеквизитыНастройкиОбмена.Организация, РеквизитыНастройкиОбмена.Банк);
	КонецЕсли;
	
	ТокенДоступа = Неопределено;
	Для Каждого НомерСчета Из МассивБанковскихСчетов Цикл
		ТокенДоступа = ПараметрыСессии.ДоступныеСчета.Получить(НомерСчета);
		Если ЗначениеЗаполнено(ТокенДоступа) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение выписки через сервис SberAPI'");
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("НастройкаОбмена", ПараметрыПолучения.НастройкаОбмена);
	ПараметрыПроцедуры.Вставить("ТекущаяСессия", ПараметрыСессии);
	ПараметрыПроцедуры.Вставить("МассивБанковскихСчетов", МассивБанковскихСчетов);
	ПараметрыПроцедуры.Вставить("ИдентификаторСессии", ПараметрыСессии.ИдентификаторСессии);
	ПараметрыПроцедуры.Вставить("ТокенДоступа", ТокенДоступа);
	ПараметрыПроцедуры.Вставить("ДатаНачала", ПараметрыПолучения.ДатаНачала);
	ПараметрыПроцедуры.Вставить("ДатаОкончания", ПараметрыПолучения.ДатаОкончания);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиSberAPIСлужебный.ПолучитьВыпискуБанка", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

// Запускает фоновое задание отправки документов по счету в банк
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком
//  ДанныеДляОтправки - Структура - Данные для отправки в банк
// Возвращаемое значение:
//  Структура - Описание:
//   * Сообщения - ФиксированныйМассив -
//   * ПодробноеПредставлениеОшибки - Строка -
//   * КраткоеПредставлениеОшибки - Строка -
//   * АдресДополнительногоРезультата - Строка -
//   * АдресРезультата - Строка -
//   * ИдентификаторЗадания - Неопределено -
//   * Статус - Строка -
//
Функция ЗапускЗаданияОтправкиДокументовПоСчету(Знач НастройкаОбмена, Знач ДанныеДляОтправки) Экспорт
	
	ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка платежных поручений через сервис SberAPI'");
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыПроцедуры.Вставить("ТекущаяСессия", ТекущаяСессия);
	ПараметрыПроцедуры.Вставить("ДанныеДляОтправки", ДанныеДляОтправки);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиSberAPIСлужебный.ОтправитьПлатежныеПорученияПоСчету", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

// Выполняет отзыв ТокенаДоступа на стороне сервера банка
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//
// Возвращаемое значение:
//  Булево - признак успешного отзыва токена доступа
//
Функция ОтозватьТокенДоступа(Знач НастройкаОбмена) Экспорт
	
	ПараметрыСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	Если ПараметрыСессии = Неопределено Тогда
		// Если ПараметрыСессии не найдены, то мы не сможем отозвать ТокенаДоступа, т.к. не знаем его,
		//  потому что он хранится в ПараметрыСессии
		Возврат Ложь;
	КонецЕсли;
	Параметры = Новый Структура;
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("ПараметрыСессии", ПараметрыСессии);
	ТокенаДоступа = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыСессии, "ТокенаДоступа", "");
	Параметры.Вставить("ТокенаДоступа", ТокенаДоступа);
	
	Успешно = ОбменСБанкамиSberAPIСлужебный.ОтозватьТокенДоступа(Параметры);
	
	// Очистим Кеш с параметрами сессии для текущей настройки обмена
	Если Успешно Тогда
		ОбменСБанкамиСлужебныйВызовСервера.УдалитьСессиюНаСервереСбербанк(НастройкаОбмена);
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

// Проверяет что у текущей записи есть доступ к счетам указанной организации
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком
//  Организация - ОпределяемыйТип.Организация, Неопределено - ссылка на организацию
//  Банк - ОпределяемыйТип.БанкОбменСБанками, Неопределено - ссылка на банк.
//  НомерСчета - Строка - номер счета к которому требуется получение доступа
//  ИгнорироватьСохраненныеНастройки - Булево - передается в случае, если метод вызывается в момент создания настройки
// Возвращаемое значение:
//  Структура - Описание:
// * Сообщения - ФиксированныйМассив
// * ПодробноеПредставлениеОшибки - Строка
// * КраткоеПредставлениеОшибки - Строка
// * АдресДополнительногоРезультата - Строка
// * АдресРезультата - Строка
// * ИдентификаторЗадания - Неопределено
// * Статус - Строка
//
Функция ЗапускЗаданияПроверкиНаличияДоступаКДанным(Знач НастройкаОбмена,
												   Знач Организация = Неопределено,
												   Знач Банк = Неопределено,
												   Знач НомерСчета = Неопределено,
												   Знач ИгнорироватьСохраненныеНастройки = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(НастройкаОбмена) И
		(Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(Банк)) Тогда
		
		РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОбмена, "Организация, Банк");
		Организация = РеквизитыНастройкиОбмена.Организация;
		Банк = РеквизитыНастройкиОбмена.Банк;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
		ПараметрыСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	Иначе
		ПараметрыСессии = ОбменСБанкамиSberAPIСлужебный.СлужебныеДанныеАвторизации(Организация);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСессии)
		Или ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыСессии, "ТокенДоступа") = Неопределено Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("ТребуетсяАутентификация", Истина);
		
		Возврат Результат;
		
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка доступа к данным на сервере SberAPI'");
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыПроцедуры.Вставить("ТокенДоступа", ПараметрыСессии.ТокенДоступа);
	ПараметрыПроцедуры.Вставить("Организация", Организация);
	ПараметрыПроцедуры.Вставить("Банк", Банк);
	ПараметрыПроцедуры.Вставить("НомерСчета", НомерСчета);
	ПараметрыПроцедуры.Вставить("ИгнорироватьСохраненныеНастройки", ИгнорироватьСохраненныеНастройки);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиSberAPIСлужебный.ПроверитьНаличиеДоступаКДанным", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

Функция СлужебныеДанныеАвторизации(Организация) Экспорт
	
	Возврат ОбменСБанкамиSberAPIСлужебный.СлужебныеДанныеАвторизации(Организация);
	
КонецФункции

Процедура ЗаполнитьФродПараметрыВСтруктуру(ФродЭлемент, Параметры) Экспорт
	
	ОбменСБанкамиSberAPIСлужебный.ЗаполнитьФродПараметрыВСтруктуру(ФродЭлемент, Параметры);
	
КонецПроцедуры

Процедура СохранитьHTTPЗапросВЛог(Метод, НастройкаОбмена, URL, Сервер, Порт = 0, Заголовки, Тело = Неопределено) Экспорт
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(Метод + " ");
	МассивСтрок.Добавить(URL);
	МассивСтрок.Добавить(" HTTP/1.1
		|Host: ");
	МассивСтрок.Добавить(Сервер);
	МассивСтрок.Добавить(":");
	МассивСтрок.Добавить(Формат(Порт, "ЧГ="));
	МассивСтрок.Добавить("
		|User-Agent: 1C+Enterprise/8.3
		|Accept: */*
		|");
	
	Для Каждого ЭлементКоллекции Из Заголовки Цикл
		МассивСтрок.Добавить(ЭлементКоллекции.Ключ);
		МассивСтрок.Добавить(": ");
		Если ЭлементКоллекции.Ключ = "Authorization" Или ЭлементКоллекции.Ключ = "X-Auth-Token" Тогда
			МассивСтрок.Добавить("***********");
		Иначе
			МассивСтрок.Добавить(ЭлементКоллекции.Значение);
		КонецЕсли;
		МассивСтрок.Добавить(Символы.ПС);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Тело) Тогда
		Тело = ОбработатьТелоЗапроса(Тело);
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Тело);
	КонецЕсли;
	
	Лог = СтрСоединить(МассивСтрок);
	ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурналОбмена(НастройкаОбмена, Лог);
	
КонецПроцедуры

Процедура СохранитьHTTPОтветВЛог(НастройкаОбмена, КодСостояния, ТелоЗапроса, Знач Заголовки = Неопределено, ИмяФайла = Неопределено) Экспорт
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить("HTTP/1.1 ");
	МассивСтрок.Добавить(КодСостояния);
	МассивСтрок.Добавить(?(КодСостояния = 200, " " + "OK", ""));
	МассивСтрок.Добавить(Символы.ПС);
	
	ДвоичныеДанныеВТеле = Ложь;
	Если ЗначениеЗаполнено(Заголовки) И ТипЗнч(Заголовки) = Тип("Соответствие") Тогда
		
		Для Каждого ЭлементКоллекции Из Заголовки Цикл
			МассивСтрок.Добавить(ЭлементКоллекции.Ключ);
			МассивСтрок.Добавить(": ");
			МассивСтрок.Добавить(ЭлементКоллекции.Значение);
			МассивСтрок.Добавить(Символы.ПС);
			Если ЭлементКоллекции.Ключ = "Content-Type"
					И (ЭлементКоллекции.Значение = "application/zip"
					Или ЭлементКоллекции.Значение = "application/gzip") Тогда
				ДвоичныеДанныеВТеле = Истина;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(Заголовки) И ТипЗнч(Заголовки) = Тип("Строка") Тогда
		
		МассивСтрок.Добавить(Заголовки);
		
	КонецЕсли;
	
	Если ИмяФайла <> Неопределено Тогда
		Попытка
			Если ДвоичныеДанныеВТеле Тогда
				ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
				Тело = Base64Строка(ДвоичныеДанные);
			Иначе
				ТекстовыйДокумент = Новый ТекстовыйДокумент;
				ТекстовыйДокумент.Прочитать(ИмяФайла);
				Тело = ТекстовыйДокумент.ПолучитьТекст();
			КонецЕсли;
		Исключение
			ВидОперации = НСтр("ru = 'Чтение ответа сервера из временного файла'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, , НастройкаОбмена);
		КонецПопытки;
	Иначе
		Если ТипЗнч(ТелоЗапроса) = Тип("ДвоичныеДанные") Тогда
			Тело = Base64Строка(Тело);
		Иначе
			Тело = ТелоЗапроса;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Тело) И СтрНайти(НРег(Тело), "<html>") = 0 Тогда
		Тело = ОбработатьТелоЗапроса(Тело);
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Тело);
	Иначе
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Тело);
	КонецЕсли;
	
	Лог = СтрСоединить(МассивСтрок);
	
	ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурналОбмена(НастройкаОбмена, Лог);
	
КонецПроцедуры

Функция ОбработатьТелоЗапроса(ТелоЗапроса) Экспорт
	
	Тело = "";
	Попытка
		
		JSON = ОбщегоНазначения.JSONВЗначение(ТелоЗапроса, , Ложь);
		
		ЗаменитьПараметрВЛоге(JSON, "clientAuthData");
		ЗаменитьПараметрВЛоге(JSON, "authData");
		ЗаменитьПараметрВЛоге(JSON, "accessToken");
		ЗаменитьПараметрВЛоге(JSON, "idToken");
		ЗаменитьПараметрВЛоге(JSON, "clientSecret");
		
		Тело = ОбменСБанкамиСлужебный.ЗначениеВJSON(JSON);
		
		Возврат Тело;
		
	Исключение
		
		Возврат ТелоЗапроса;
		
	КонецПопытки;
	
КонецФункции

Процедура ЗаменитьПараметрВЛоге(JSON, ИмяПараметра)
	
	Если JSON.Свойство(ИмяПараметра) Тогда
		JSON.Вставить(ИмяПараметра, "***");
	КонецЕсли;
	
КонецПроцедуры

// См. ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурналОбмена()
//
Процедура ДобавитьЗаписьВЖурналОбмена(НастройкаОбмена, Лог) Экспорт
	
	ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурналОбмена(НастройкаОбмена, Лог);
	
КонецПроцедуры

Процедура СохранитьСлужебныеДанныеАвторизации(Организация,
											  ПредыдущийКлиентскийСекретИзФайла,
											  ПредыдущийКлиентскийСекретОбновленный,
											  СертификатПодключения,
											  СертификатПодключенияПароль,
											  ИдентификаторОрганизации,
											  ТокенДоступа = "") Экспорт
	
	ОбменСБанкамиSberAPIСлужебный.СохранитьСлужебныеДанныеАвторизации(
		Организация,
		ПредыдущийКлиентскийСекретИзФайла,
		ПредыдущийКлиентскийСекретОбновленный,
		СертификатПодключения,
		СертификатПодключенияПароль,
		ИдентификаторОрганизации,
		ТокенДоступа);
	
КонецПроцедуры

Функция ДанныеПодписанногоСообщения(СообщениеОбмена, НастройкаОбмена, МассивПодписей) Экспорт
	
	ДанныеСообщения = "";
	Если МассивПодписей.Количество() = 0 Тогда
		
		ТекстОшибки = НСтр("ru='Не найдены подписи для сообщения %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Строка(СообщениеОбмена));
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	ЭП = МассивПодписей.Получить(0);
	ПодписьДвоичныеДанные = ЭП.Подпись;
	Подпись = Base64Строка(ПодписьДвоичныеДанные);
	Подпись = СтрЗаменить(СтрЗаменить(Подпись, Символы.ПС, ""), Символы.ВК, "");
	
	ДанныеСертификата = ЭП.Сертификат;
	Если ТипЗнч(ДанныеСертификата) = Тип("ХранилищеЗначения") Тогда
		ДвоичныеДанныеСертификата = ДанныеСертификата.Получить();
	Иначе
		//Значит тут уже ДвоичныеДанные
		ДвоичныеДанныеСертификата = ДанныеСертификата;
	КонецЕсли;
	ДанныеСертификата = ОбменСБанкамиСлужебныйВызовСервера.ИнформацияОСертификатеПодписиСбербанк(ДвоичныеДанныеСертификата);
	Если ДанныеСертификата = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru='Не найден сертификат установленной подписи при отправке платежного поручения
			| через токен SberAPI для %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, СообщениеОбмена);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	СтрокаСерийныйНомер = Строка(ДанныеСертификата.SN);
	СтрокаСерийныйНомер = СтрЗаменить(СтрокаСерийныйНомер, " ", "");
	
	// Получим Идентификатор(Uuid) сертификата подписи
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.Идентификатор КАК Идентификатор
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.СертификатыПодписейОрганизации КАК НастройкиОбменСБанкамиСертификатыПодписейОрганизации
	|ГДЕ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СерийныйНомер = &СерийныйНомер
	|	И НастройкиОбменСБанкамиСертификатыПодписейОрганизации.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("СерийныйНомер", СтрокаСерийныйНомер);
	Запрос.УстановитьПараметр("Ссылка", НастройкаОбмена);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ИдентификаторСертификата = Выборка.Идентификатор;
		
	Иначе
		
		ТекстОшибки = НСтр("ru='Не найден идентификатор установленной подписи при отправке платежного поручения
			| через токен SberAPI для сертификата с SN %1 '");
		ТекстОшибки = СтрШаблон(ТекстОшибки, СтрокаСерийныйНомер);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ВидЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеОбмена, "ВидЭД");
	
	Если ВидЭД = Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение Тогда
		
		ПрисоединенныйФайл = ОбменСБанкамиСлужебный.ПрисоединенныйФайл(СообщениеОбмена);
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл);
		Если Не ЭтоАдресВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла) Тогда
			
			ТекстОшибки = НСтр("ru='Не найдены присоединенные данные для %1 у %2'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Строка(ПрисоединенныйФайл), Строка(СообщениеОбмена));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		ДанныеСтрокой = ОбменСБанкамиСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(ДвоичныеДанныеФайла);
		ДанныеПлатежа = ОбщегоНазначения.JSONВЗначение(ДанныеСтрокой, , Ложь);
		
		ОбменСБанкамиSberAPIСлужебный.ЗаполнитьДанныеДайджестаПлатежногоПоручения(
			ДанныеПлатежа, Подпись, ИдентификаторСертификата);
		
		ДанныеСообщения = ОбменСБанкамиСлужебный.ЗначениеВJSON(ДанныеПлатежа);
		// Необходимо передавать null как значение, а не строковой литерал
		ДанныеСообщения = СтрЗаменить(ДанныеСообщения, """null""", "null");
		
	КонецЕсли;
	
	Возврат ДанныеСообщения;
	
КонецФункции

#Область JSON

// Получает данные из строки JSON
//
// Параметры:
//   Строка - Строка - данные в формате JSON
// Возвращаемое значение:
//   Произвольный - структурированные данные
//
Функция ДанныеИзJSON(Знач Строка, ИменаСвойствСоЗначениямиДата = Неопределено, ПрочитатьВСоответствие = Истина) Экспорт
	
	Возврат ОбщегоНазначения.JSONВЗначение(Строка, , Ложь);
	
КонецФункции

Функция ПрочитатьJSONОтветаLogin_sign(ОтветLogin) Экспорт
	
	JSON = ОбщегоНазначения.JSONВЗначение(ОтветLogin, , Ложь);
	sessionId = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "sessionId", "");
	
	Если Не ЗначениеЗаполнено(sessionId) Тогда
		
		ТекстОшибки = НСтр("ru='Не найдено поле sessionId в ответе сервера на запрос Login_sign'");
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ОтветLogin_sign = Новый Структура;
	ОтветLogin_sign.Вставить("sessionId", sessionId);
	
	Возврат ОтветLogin_sign;
	
КонецФункции

Функция ЗаголовкиJSON(Заголовки) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	Для Каждого КлючИЗначение Из Заголовки Цикл
		ЗаписьJSON.ЗаписатьИмяСвойства(КлючИЗначение.Ключ);
		ЗаписьJSON.ЗаписатьЗначение(Строка(КлючИЗначение.Значение));
	КонецЦикла;
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	ЗаголовкиJSON = ЗаписьJSON.Закрыть();
	Возврат ЗаголовкиJSON;
	
КонецФункции

Функция ЗапросJSONPreLogin_sign(issuer, СерийныйНомерСертификата) Экспорт
	
	Данные = Новый Структура("issuer,serialNumber", issuer, СерийныйНомерСертификата);
	
	ДанныеJSON = ОбменСБанкамиСлужебный.ЗначениеВJSON(Данные);
	
	Возврат ДанныеJSON;
	
КонецФункции

Функция ПрочитатьJSONОтветаPreLogin_sign(ОтветPreLogin) Экспорт
	
	JSON = ОбщегоНазначения.JSONВЗначение(ОтветPreLogin, , Ложь);
	sessionId = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "sessionId", "");
	authData = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "authData", "");
	
	Если Не ЗначениеЗаполнено(sessionId) Тогда
		
		ТекстОшибки = НСтр("ru='Не найдено поле sessionId в ответе сервера на запрос PreLogin_sign'");
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	Если Не ЗначениеЗаполнено(authData) Тогда
		
		ТекстОшибки = НСтр("ru='Не найдено поле authData в ответе сервера на запрос PreLogin_sign'");
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ОтветPreLogin_sign = Новый Структура;
	ОтветPreLogin_sign.Вставить("sessionId", sessionId);
	ОтветPreLogin_sign.Вставить("authData",  authData);
	
	Возврат ОтветPreLogin_sign;
	
КонецФункции

Функция ЗапросJSONLogin_sign(ИдентификаторСессии, ПодписьСоли, ФродСтруктура) Экспорт
	
	Данные = Новый Структура();
	Данные.Вставить("sessionId", ИдентификаторСессии);
	Данные.Вставить("clientAuthData", ПодписьСоли);
	Данные.Вставить("fraudParams", ФродСтруктура);
	
	ДанныеJSON = ОбменСБанкамиСлужебный.ЗначениеВJSON(Данные);
	
	Возврат ДанныеJSON;
	
КонецФункции

#КонецОбласти

#КонецОбласти
