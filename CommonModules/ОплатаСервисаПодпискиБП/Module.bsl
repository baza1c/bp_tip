#Область СлужебныйПрограммныйИнтерфейс

// Создает подписки по счету.
//
// Параметры:
//	- Счет - ДокументСсылка.СчетНаОплатуПокупателю - счет, по которому нужно создать подписки
//
// Возвращаемое значение:
//	Массив структур - Данные подписки
//	- КодПодписки - Число - код созданной подписки в мендежере сервиса
//	- КодОбслуживающейОрганизации - Число - код обслуживающей организации, создающей подписку, в менеджере сервиса
//	- КодАбонента - Число - код абонента, которому создается подписка, в менеджере сервиса
//	- КодТарифа - Строка - код тарифа в менеджере сервиса
//	- НачалоПодписки - Дата - дата начала действия подписки
//	- КонецПодписки - Дата - дата окончания действия подписки
//
Функция СоздатьПодпискиПоСчету(Счет, ДатаПодключения = Неопределено) Экспорт
	
	СвойстваСчета = РегистрыСведений.СчетНаОплатуПокупателюОплатаСервиса.СвойстваСчета(Счет);
	Если НЕ СвойстваСчета.Выбран() Тогда
		ВызватьИсключение ПредставлениеОшибки(НСтр("ru='Не заполнены свойства счета для биллинга'"));
	КонецЕсли;
	
	СвойстваПодпискиПоСчету = Новый Структура();
	СвойстваПодпискиПоСчету.Вставить("Счет", Счет);
	СвойстваПодпискиПоСчету.Вставить("ИдентификаторСчета", Счет.УникальныйИдентификатор());
	СвойстваПодпискиПоСчету.Вставить("КодПродавца", СвойстваСчета.КодПродавца);
	СвойстваПодпискиПоСчету.Вставить("КодПокупателя", СвойстваСчета.КодПокупателя);
	СвойстваПодпискиПоСчету.Вставить("ДатаПодключения",
		?(ЗначениеЗаполнено(ДатаПодключения),
		ДатаПодключения,
		НачалоНовойПодпискиАбонента(СвойстваПодпискиПоСчету.КодПродавца, СвойстваПодпискиПоСчету.КодПокупателя)));
	
	КодыСозданныхПодписок = СоздатьПодписку(СвойстваПодпискиПоСчету);
	
	ДанныеПодписокПоСчету = ДанныеПодпискиПоСчету(СвойстваПодпискиПоСчету, КодыСозданныхПодписок);
	
	Для каждого ДанныеПодписки Из ДанныеПодписокПоСчету Цикл
		ОплатаСервисаЭлектроннаяПочтаБП.ОтправитьПисьмоПродавцуОПодписке(Счет, ДанныеПодписки);
		ОплатаСервисаЭлектроннаяПочтаБП.ОтправитьПисьмоПокупателюОПодписке(Счет, ДанныеПодписки);
	КонецЦикла;
	
	СтатусыСчета = РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокумента(Счет, СвойстваСчета.Организация);
	Если СтатусыСчета.ДополнительныйСтатус <> Перечисления.СтатусыОтгрузки.Отгружен Тогда
		Документы.СчетНаОплатуПокупателю.СделатьОтгруженным(Счет);
	КонецЕсли;
	
	Возврат ДанныеПодписокПоСчету;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеПодпискиПоСчету(СвойстваПодпискиПоСчету, КодыСозданныхПодписок)
	
	Подписки = Новый Массив;
	
	ПодпискиАбонента = ПодпискиАбонента(
		СвойстваПодпискиПоСчету.КодПродавца, СвойстваПодпискиПоСчету.КодПокупателя);
	
	Для Каждого ПодпискаАбонента Из ПодпискиАбонента Цикл
		
		Если КодыСозданныхПодписок.Найти(ПодпискаАбонента.Номер) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Подписка = ПрограммныйИнтерфейсСервиса.НовыйШаблонПодпискиНаОсновнойТариф();
		ЗаполнитьЗначенияСвойств(Подписка, ПодпискаАбонента);
		ПОдписка.Вставить("КодПодписки", ПодпискаАбонента.Номер);
		Подписки.Добавить(Подписка);
		
	КонецЦикла;
	
	Возврат Подписки;
	
КонецФункции

Функция СоздатьПодписку(СвойстваПодпискиПоСчету)
	
	ПараметрыСоздания = ПрограммныйИнтерфейсСервиса.НовыйПараметрыСозданияПодписокНаТарифыНаОснованииСчетаНаОплату();
	ПараметрыСоздания.ИдентификаторСчета = СвойстваПодпискиПоСчету.ИдентификаторСчета;
	ПараметрыСоздания.ДатаПодключения = СвойстваПодпискиПоСчету.ДатаПодключения;
	
	СвойстваОтвета = ПрограммныйИнтерфейсСервиса.НовыйОсновныеСвойстваОтвета();
	Результат = ПрограммныйИнтерфейсСервиса.СоздатьПодпискиНаТарифыНаОснованииСчетаНаОплату(
		ПараметрыСоздания, Истина, СвойстваОтвета);
	
	// Если подписка назначена, то сервис возвращает 10200.
	// Если что-то пошло не так, то нужно записать ошибку в журнал.
	Если СвойстваОтвета.КодОтвета <> 10200 Тогда
		ОписаниеОшибки = СтрШаблон(НСтр("ru='При создании подписки для счета %1 возникла ошибка:
		|КодОтвета: %2
		|КодСостояния: %3
		|Сообщение: %4'"), СвойстваПодпискиПоСчету.Счет, СвойстваОтвета.КодОтвета, СвойстваОтвета.КодСостояния, СвойстваОтвета.Сообщение);
		ОплатаСервисаЖурналРегистрацииБП.ЗаписатьОшибку(ОписаниеОшибки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Возврат Результат;
	Иначе
		ВызватьИсключение ПредставлениеОшибки(СтрШаблон("ru = 'Создание подписки: %1 %2'", СвойстваОтвета.КодОтвета, СвойстваОтвета.Сообщение));
	КонецЕсли;
	
КонецФункции

Функция ПолучитьДействующуюПродлевающуюПодписку(КодОбслуживающейОрганизации, КодАбонента) Экспорт
	
	ТекущаяДата = ТекущаяДата();
	
	Подписки = ПодпискиАбонента(КодОбслуживающейОрганизации, КодАбонента);
	
	Для каждого Подписка Из Подписки Цикл
		Если Подписка.ТипПодписки = Перечисления.ТипыПодписокСервиса.Продлевающая
			И Подписка.ДатаПодключения <= ТекущаяДата И Подписка.ДатаОтключения >= ТекущаяДата Тогда
			Возврат Подписка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция НачалоНовойПодпискиАбонента(КодОбслуживающейОрганизации, КодАбонента) Экспорт
	
	КонецПредыдущейПодписки = ОкончаниеПодпискиАбонента(КодОбслуживающейОрганизации, КодАбонента);
	Возврат Макс(НачалоДня(ТекущаяДатаСеанса()), КонецПредыдущейПодписки + 1);
	
КонецФункции

Функция ОкончаниеПодпискиАбонента(КодОбслуживающейОрганизации, КодАбонента) Экспорт
	
	ОкончаниеПодписки = Дата(1, 1, 1);
	
	ОбслуживаемыеТарифы = АртикулыОбслуживаемыхТарифов();
	
	Подписки = ПодпискиАбонента(КодОбслуживающейОрганизации, КодАбонента);
	
	Для каждого Подписка Из Подписки Цикл
		
		// Учитываются только основные подписки
		Если Подписка.ТипПодписки <> Перечисления.ТипыПодписокСервиса.Основная Тогда
			Продолжить;
		КонецЕсли;
		
		// Учитываются только обслуживаемые организацией тарифы
		ШаблонАртикула = "%1-%2-%3";
		АртикулТарифаПровайдера = СтрШаблон(ШаблонАртикула,
			ОплатаСервисаБП.ПрефиксТарифаПровайдера(),
			СокрЛП(Подписка.КодПериодаДействия),
			СокрЛП(Подписка.КодТарифаПровайдера));
		АкртикулТарифаОбслуживающейОрганизации = СтрШаблон(ШаблонАртикула,
			ОплатаСервисаБП.ПрефиксТарифаОбслуживающейОрганизации(),
			СокрЛП(Подписка.КодПериодаДействия),
			СокрЛП(Подписка.КодТарифаОбслуживающейОрганизации));
		
		Если ОбслуживаемыеТарифы[АртикулТарифаПровайдера] = Неопределено
			И ОбслуживаемыеТарифы[АкртикулТарифаОбслуживающейОрганизации] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Подписка.ДатаОтключения > ОкончаниеПодписки Тогда
			ОкончаниеПодписки = Подписка.ДатаОтключения;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОкончаниеПодписки;
	
КонецФункции

Функция ПодпискиАбонента(КодОбслуживающейОрганизации, КодАбонента)
	
	ОтборПодписок = ПрограммныйИнтерфейсСервиса.НовыйОтборПодписокНаТарифы();
	ОтборПодписок.КодОбслуживаемогоАбонента = КодАбонента;
	ОтборПодписок.ТолькоАктивные = Истина;
	
	Подписки = ПрограммныйИнтерфейсСервиса.ПодпискиНаТарифы(ОтборПодписок, КодОбслуживающейОрганизации);
	
	Возврат Подписки;
	
КонецФункции

Функция АртикулыОбслуживаемыхТарифов()
	
	АртикулыОбслуживаемыхТарифов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Артикул КАК Артикул
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		АртикулыОбслуживаемыхТарифов.Вставить(Выборка.Артикул, Истина);
	КонецЦикла;
	
	Возврат АртикулыОбслуживаемыхТарифов;
	
КонецФункции

Функция ПредставлениеОшибки(ТекстОшибки)
	
	Возврат СтрШаблон("%1: %2", НСтр("ru='Подписки'"), ТекстОшибки);
	
КонецФункции

#КонецОбласти