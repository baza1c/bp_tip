#Область СлужебныйПрограммныйИнтерфейс

// Поиск и создание учетного документа при отражении в учете документа Акт сверки взаиморасчетов (информация отправителя).
// Формат по приказу ЕД-7-26/405@.
//
// Параметры:
//  ДеревоДанных		 - ДеревоЗначений - данные электронного документа в виде заполненного макета 
//                         см. Обработка.ОбменСКонтрагентами.Макет.АктСверкиВзаиморасчетов_ИнформацияОтправителя.
//  ДокументУчета	     - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО - ссылка на документ учета, если он уже
//                         прикреплен к электронному документу.
Процедура ПослеПодбораУчетногоДокументаАктСверкиВзаиморасчетов(ЭлектронныйДокумент, ДокументУчета) Экспорт
	
	ПараметрыЗаполнения = ЭлектронныеДокументыЭДО.ДанныеДокументовДляОтраженияВУчете(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлектронныйДокумент))[0];
		
	Если ПараметрыЗаполнения.ТипДокумента <> Перечисления.ТипыДокументовЭДО.АктСверкиВзаиморасчетов Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыПолучения = ЭлектронныеДокументыЭДО.НовыеПараметрыПолученияДанныхДокумента();
	ПараметрыПолучения.ОсновнойФайл = ПараметрыЗаполнения.ДанныеОсновногоФайла;
	ПараметрыПолучения.ДополнительныйФайл = ПараметрыЗаполнения.ДанныеДополнительногоФайла;
	
	Результат = ЭлектронныеДокументыЭДО.ДанныеДокументаДляЗагрузкиПросмотра(ПараметрыПолучения);
	
	НовыйЭД = Результат.НовыйЭД;
	ДеревоЭД = НовыйЭД.ЗначениеРеквизита;
	
	Если ЗначениеЗаполнено(ДеревоЭД) Тогда
		ОбменСКонтрагентамиБП.НайтиСоздатьАктСверкиВзаиморасчетов(ДеревоЭД, ДокументУчета);
	КонецЕсли;
	
КонецПроцедуры

// Прикрепление к договору файла из электронного документа с типом Прочее.
//
// Параметры:
//  ЭлектронныйДокумент - ДеревоЗначений - данные электронного документа в виде заполненного макета 
//  ДокументУчета       - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО - ссылка на документ учета, если он уже
//                         прикреплен к электронному документу.
Процедура ПослеПодбораУчетногоДокументаДоговорыКонтрагентов(ЭлектронныйДокумент, ДокументУчета) Экспорт
	
	МассивПараметров = ЭлектронныеДокументыЭДО.ДанныеДокументовДляОтраженияВУчете(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлектронныйДокумент));
		
	Если МассивПараметров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = МассивПараметров[0];

	Если ТипЗнч(ДокументУчета) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
		И ЗначениеЗаполнено(ПараметрыЗаполнения.ДанныеОсновногоФайла.ДвоичныеДанные) Тогда
	
		Если ПараметрыЗаполнения.ТипДокумента = Перечисления.ТипыДокументовЭДО.ДополнительноеСоглашение Тогда
			ОбменСКонтрагентамиБП.ПрисоединитьДополнительноеСоглашениеКДоговору(ДокументУчета, ПараметрыЗаполнения);
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Функция СчетаТребующиеРаспределенияСкидокПоСтрокам(Знач СписокДокументов) Экспорт
	
	СчетаСоСкидкойПоДокументу = Документы.СчетНаОплатуПокупателю.СчетаНаОплатуСоСкидкойПоДокументуВЦелом(СписокДокументов);
	Если ИспользуетсяФорматСчетаНаОплатуОтФНС(СписокДокументов) Тогда
		Возврат СчетаСоСкидкойПоДокументу;
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
КонецФункции

Процедура РаспределитьСкидкиСчетовНаОплатуПоСтрокам(СчетаНаОплатуСоСкидкойПоДокументу) Экспорт
	
	Документы.СчетНаОплатуПокупателю.РаспределитьСкидкиПоСтрокам(СчетаНаОплатуСоСкидкойПоДокументу);
	
КонецПроцедуры

Функция ПоказатьНастройкуАвтоматическогоПриемаПриглашенийЭДО() Экспорт
	
	Возврат Пользователи.ЭтоПолноправныйПользователь()
		И Константы.ТребуетсяНастройкаАвтоматическогоПриемаПриглашенийЭДО.Получить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИспользуетсяФорматСчетаНаОплатуОтФНС(СписокСчетовНаОплату)
	
	Для Каждого СчетНаОплату Из СписокСчетовНаОплату Цикл
		ДоступныеФорматы = ОбменСКонтрагентами.ДоступныеФорматыЭлектронныхДокументов(СчетНаОплату);
		НайденныеСтроки = ДоступныеФорматы.НайтиСтроки(Новый Структура("ВыбранПоУмолчанию", Истина));
		Если ЗначениеЗаполнено(НайденныеСтроки) И СтрНайти(НайденныеСтроки[0].ИдентификаторФормата, "ON_CHETOP") > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ВыводитьУведомлениеОНовыхДокументахНаКомандахЭДО() Экспорт
	Возврат ОбменСКонтрагентамиБП.ВыводитьУведомлениеОНовыхДокументахНаКомандахЭДО();
КонецФункции

#КонецОбласти