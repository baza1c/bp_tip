
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает код списка задач, направленных текущему пользователю
//
// Возвращаемое значение:
//   Число
//
Функция КодЗадачиМне() Экспорт
	
	Возврат 0;
	
КонецФункции

// Возвращает код списка задач, поставленных текущим пользователем
//
// Возвращаемое значение:
//   Число
//
Функция КодЗадачиОтМеня() Экспорт
	
	Возврат 1;
	
КонецФункции

// Возвращает код списка всех задач
//
// Возвращаемое значение:
//   Число
//
Функция КодЗадачиВсе() Экспорт
	
	Возврат 2;
	
КонецФункции

// Возвращает роли и группы задач текущего пользователя
//
// Возвращаемое значение:
//   Структура - см. НовыеРолиГруппыИсполнителя()
//
Функция РолиГруппыАвторизованногоПользователя() Экспорт
	
	Результат = НовыеРолиГруппыИсполнителя();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Исполнитель", Пользователи.АвторизованныйПользователь());
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиЗадач.РольИсполнителя КАК РольИсполнителя,
	|	ИсполнителиЗадач.ГруппаИсполнителейЗадач КАК ГруппаИсполнителейЗадач
	|ИЗ
	|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ГДЕ
	|	ИсполнителиЗадач.Исполнитель = &Исполнитель";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	РолиИсполнителя = РезультатЗапроса.ВыгрузитьКолонку("РольИсполнителя");
	ГруппыИсполнителя = РезультатЗапроса.ВыгрузитьКолонку("ГруппаИсполнителейЗадач");
	
	Результат.Роли = ОбщегоНазначенияКлиентСервер.СвернутьМассив(РолиИсполнителя);
	Результат.Группы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ГруппыИсполнителя);
	
	Возврат Результат;
	
КонецФункции

// Настраивает условное оформление списка задач
//
// Параметры:
//    УсловноеОформлениеСпискаЗадач - УсловноеОформление - Оформление списка задач
//
Процедура УстановитьОформлениеЗадач(Знач УсловноеОформлениеСпискаЗадач) Экспорт
	
	УсловноеОформлениеСпискаЗадач.ИдентификаторПользовательскойНастройки = "ОсновноеОформление";
	
	// Установка оформления для просроченных задач.
	ЭлементУсловногоОформления = УсловноеОформлениеСпискаЗадач.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтатусЗадачи");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СтатусЗадачи.НеВыполнена;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветТекста = ЭлементУсловногоОформления.Оформление.Элементы.Найти("ЦветТекста");
	ЭлементЦветТекста.Значение = ЦветПросроченнойЗадачи();
	ЭлементЦветТекста.Использование = Истина;
	
	ЭлементШрифт = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Шрифт");
	ЭлементШрифт.Значение =  ШрифтПросроченнойЗадачи();
	ЭлементШрифт.Использование = Истина;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("СрокИсполнения");
	ОформляемоеПоле.Использование = Истина;
	
	// Установка оформления для отмененных задач.
	ЭлементУсловногоОформления = УсловноеОформлениеСпискаЗадач.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
	ЭлементОтбораДанных.Использование = Истина;
	
	ГруппаУсловийОтмена = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаУсловийОтмена.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ЭлементСтатусЗадачи = ГруппаУсловийОтмена.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементСтатусЗадачи.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтатусЗадачи");
	ЭлементСтатусЗадачи.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементСтатусЗадачи.ПравоеЗначение = Перечисления.СтатусЗадачи.Отменена;
	ЭлементСтатусЗадачи.Использование = Истина;
	
	ЭлементСтатусЗадачи = ГруппаУсловийОтмена.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементСтатусЗадачи.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ЭлементСтатусЗадачи.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементСтатусЗадачи.ПравоеЗначение = Истина;
	ЭлементСтатусЗадачи.Использование = Истина;
	
	ЭлементЦветТекста = ЭлементУсловногоОформления.Оформление.Элементы.Найти("ЦветТекста");
	ЭлементЦветТекста.Значение = ЦветаСтиля.НедоступныеДанныеЦвет;
	ЭлементЦветТекста.Использование = Истина;
	
	// Установка оформления для Важных задач.
	ЭлементУсловногоОформления = УсловноеОформлениеСпискаЗадач.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Важность");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.ВариантыВажностиЗадачи.Высокая;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтатусЗадачи");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СтатусЗадачи.НеВыполнена;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Шрифт");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.НеПринятыеКИсполнениюЗадачи.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	// Установка оформления для незаполненного поля "Срок".
	ЭлементУсловногоОформления = УсловноеОформлениеСпискаЗадач.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("СрокИсполнения");
	ОформляемоеПоле.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ЭлементОтбораДанных.ПравоеЗначение = Истина;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Текст");
	ЭлементЦветаОформления.Значение = НСтр("ru = 'Срок не указан'");
	ЭлементЦветаОформления.Использование = Истина;
	
	// Установка оформления для незаполненного поля "Время".
	ЭлементУсловногоОформления = УсловноеОформлениеСпискаЗадач.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Время");
	ОформляемоеПоле.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПоказыватьВремя");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Текст");
	ЭлементЦветаОформления.Значение = "";
	ЭлементЦветаОформления.Использование = Истина;
	
КонецПроцедуры

// Устанавливает указанный статус задачи из списка
//
// Параметры:
//   СписокЗадач - Массив из ЗадачаСсылка.ЗадачаИсполнителя - Список задач, для которых нужно установить новый статус
//   СтатусЗадачи - ПеречислениеСсылка.СтатусЗадачи - Устанавливаемый статус задач
//
Процедура УстановитьСтатусЗадачи(СписокЗадач, СтатусЗадачи) Экспорт
	
	АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
	
	Для Каждого Задача Из СписокЗадач Цикл
		ЗадачаОбъект = Задача.ПолучитьОбъект();
		ЗадачаОбъект.СтатусЗадачи = СтатусЗадачи;
		ЗадачаОбъект.РезультатВыполнения = ПредставлениеРезультатаИзмененияЗадачи(
			АвторизованныйПользователь,
			СтатусЗадачи,
			ЗадачаОбъект.РезультатВыполнения);
		ЗадачаОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Возвращает представление результата при изменении статуса задачи
//
// Параметры:
//   Автор            - СправочникСсылка.Пользователи - Автор изменения
//   СтатусЗадачи     - ПеречислениеСсылка.СтатусЗадачи - Статус изменения задачи
//   СтрокаДополнения - Строка - Дополнение к представлению результата изменения задачи
//
// Возвращаемое значение:
//   Строка
//
Функция ПредставлениеРезультатаИзмененияЗадачи(Автор, СтатусЗадачи, СтрокаДополнения) Экспорт
	
	ШаблонСтроки = НСтр("ru = 'Пользователь %1 изменил статус задачи на %2 %3
	|
	|%4'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Возврат СтрШаблон(ШаблонСтроки, Автор, СтатусЗадачи, Формат(ТекущаяДатаСеанса(), "ДЛФ=DT"), СтрокаДополнения);
	
КонецФункции

// Возвращает представление результата при создании задачи
//
// Параметры:
//   Автор            - СправочникСсылка.Пользователи - Автор изменения
//   СтрокаДополнения - Строка - Дополнение к представлению результата создания задачи
//
// Возвращаемое значение:
//   Строка
//
Функция ПредставлениеРезультатаСозданияЗадачи(Автор, СтрокаДополнения) Экспорт
	
	ШаблонСтроки = НСтр("ru = 'Пользователь %1 создал задачу %2
	|
	|%3'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Возврат СтрШаблон(ШаблонСтроки, Автор, Формат(ТекущаяДатаСеанса(), "ДЛФ=DT"), СтрокаДополнения);
	
КонецФункции

// Возвращает представление результата назначения задачи на исполнителя
//
// Параметры:
//   Автор            - СправочникСсылка.Пользователи - Автор изменения
//   Исполнитель      - СправочникСсылка.ВнешниеПользователи, СправочникСсылка.Пользователи - Исполнитель задачи
//   СтрокаДополнения - Строка - Дополнение к представлению результата задачи
//
// Возвращаемое значение:
//   Строка
//
Функция ПредставлениеРезультатаНаправленияЗадачи(Автор, Исполнитель, СтрокаДополнения) Экспорт
	
	ШаблонСтроки = НСтр("ru = 'Пользователь %1 направил задачу испольнителю %2 %3
	|
	|%4'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Возврат СтрШаблон(ШаблонСтроки, Автор, Исполнитель, Формат(ТекущаяДатаСеанса(), "ДЛФ=DT"), СтрокаДополнения);
	
КонецФункции

// Возвращает доступные типы связи задач исполнителя
//
// Возвращаемое значение:
//   СписокЗначений из ОписаниеТипов
//
Функция ДоступныеТипыСвязиСЗадачей() Экспорт

	Результат = Новый СписокЗначений;
	
	Если ФункциональностьПрограммы.РазделЛидыДоступен() Тогда
		Результат.Добавить(Новый ОписаниеТипов("СправочникСсылка.Контрагенты"), НСтр("ru = 'Контрагентом'"));
		Результат.Добавить(Новый ОписаниеТипов("СправочникСсылка.КонтактныеЛица"), НСтр("ru = 'Лидом'"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает оформление строк списка задач
//
// Параметры:
//   Строки - СтрокиДинамическогоСписка - Строки списка задач
//
Процедура ЗадачиПриПолученииДанныхНаСервере(Строки) Экспорт
	
	// В списке задачи со сроком на текущий день могут не показываться просроченными, т.к.
	// в оформлении устанавливается отбор на момент открытия списка, поэтому в ситуации, когда с утра открыли Мои задачи,
	// а к вечеру часть уже просрочена нужно обработать дополнительно через оформление строк
	
	ТекущаяДата = ТекущаяДатаСеанса();
	ЦветПросроченнойЗадачи = ЦветПросроченнойЗадачи();
	
	Для Каждого ТекущаяСтрока Из Строки Цикл
			
		ТекущаяЗадача = ТекущаяСтрока.Значение;
		Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущаяЗадача.Данные, "СрокИсполнения") < ТекущаяДата
			И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущаяЗадача.Данные, "СтатусЗадачи") = Перечисления.СтатусЗадачи.НеВыполнена Тогда
			
			ЭлементОформления = ТекущаяЗадача.Оформление["СрокИсполнения"];
			ЭлементОформления.УстановитьЗначениеПараметра("ЦветТекста", ЦветПросроченнойЗадачи);
			ЭлементОформления.УстановитьЗначениеПараметра("Шрифт", ШрифтПросроченнойЗадачи());
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеРолиГруппыИсполнителя()
	
	Результат = Новый Структура;
	Результат.Вставить("Роли", Новый Массив);
	Результат.Вставить("Группы", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

Функция ЦветПросроченнойЗадачи()
	
	Возврат ЦветаСтиля.ЦветШрифтаОшибки;
	
КонецФункции

Функция ШрифтПросроченнойЗадачи()
	
	Возврат Новый Шрифт(, , Истина);
	
КонецФункции

#КонецОбласти