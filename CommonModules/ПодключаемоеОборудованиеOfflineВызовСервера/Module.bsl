
#Область ПрограммныйИнтерфейс

// Функция получает параметры устройства.
//
Функция ПолучитьПараметрыУстройства(Устройство, СообщениеОбОшибке = Неопределено) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОборудованиеПоОрганизациям.УзелОбмена КАК УзелИнформационнойБазы,
	|	ОборудованиеПоОрганизациям.Организация КАК Организация,
	|	ОборудованиеПоОрганизациям.Склад КАК Склад,
	|	ОборудованиеПоОрганизациям.Гостиница КАК Гостиница,
	|	ЕСТЬNULL(СправочникОфлайнОборудование.ТипОфлайнОборудования, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыОфлайнОборудования.ОФД) КАК ЗагрузкаИзОФД,
	|	Склады.ТипЦенРозничнойТорговли КАК ТипЦен
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ОборудованиеПоОрганизациям.Склад = Склады.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОфлайнОборудование КАК СправочникОфлайнОборудование
	|		ПО ОборудованиеПоОрганизациям.Оборудование = СправочникОфлайнОборудование.Ссылка
	|ГДЕ
	|	ОборудованиеПоОрганизациям.Оборудование = &Устройство
	|	И (НЕ ОборудованиеПоОрганизациям.УзелОбмена.Ссылка ЕСТЬ NULL
	|			ИЛИ ЕСТЬNULL(СправочникОфлайнОборудование.ТипОфлайнОборудования, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыОфлайнОборудования.ОФД))";
	
	Запрос.УстановитьПараметр("Устройство", Устройство);
	
	Выборка = Запрос.Выполнить().Выбрать();
		
	Если Не Выборка.Следующий() Тогда
		СообщениеОбОшибке = НСтр("ru = 'Оборудование не настроено. Выполните настройку в разделе ""Администрирование""'");
		Возврат Неопределено;
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("УзелИнформационнойБазы",         Выборка.УзелИнформационнойБазы);
	ВозвращаемоеЗначение.Вставить("Склад",                          Выборка.Склад);
	ВозвращаемоеЗначение.Вставить("Гостиница",                      Выборка.Гостиница);
	ВозвращаемоеЗначение.Вставить("Организация",                    Выборка.Организация);
	ВозвращаемоеЗначение.Вставить("ЗагрузкаИзОФД",                  Выборка.ЗагрузкаИзОФД);
	ВозвращаемоеЗначение.Вставить("ТипЦен",                         Выборка.ТипЦен);
	
	Возврат ВозвращаемоеЗначение;
КонецФункции

// Процедура вызывается при очистке товаров в устройстве.
// Выполняет запись информации в узел плана обмена.
//
// Параметры:
//  Устройство       - <СправочникСсылка.ПодключаемоеОборудование>
//  ВыполненоУспешно - <Булево> Признак успешного выполнения операции.
//
// Возвращаемое значение:
//  Нет
//
Процедура ПриОчисткеТоваровВУстройстве(Устройство, ВыполненоУспешно = Истина) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыУстройства = ПолучитьПараметрыУстройства(Устройство);
	Если ПараметрыУстройства = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УзелОбъект = ПараметрыУстройства.УзелИнформационнойБазы.ПолучитьОбъект();
	
	УзелОбъект.ДатаВыгрузки      = ТекущаяДатаСеанса();
	УзелОбъект.ВыгрузкаВыполнена = ВыполненоУспешно;
	УзелОбъект.Записать();
	
	ПараметрыОбъекта = Новый Структура("УзелОбмена, Склад", УзелОбъект.Ссылка, ПараметрыУстройства.Склад);
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
	
	ДлительныеОперации.ВыполнитьВФоне("ПланыОбмена.ОбменСПодключаемымОборудованиемOffline.ОбновитьРегистрКодовНоменклатуры",
			ПараметрыОбъекта, ПараметрыВыполнения);
КонецПроцедуры

// Процедура вызывается при выгрузке товаров в устройство.
// Выполняет запись информации в узел плана обмена.
//
// Параметры:
//  Устройство       - <СправочникСсылка.ПодключаемоеОборудование>
//  ВыполненоУспешно - <Булево> Признак успешного выполнения операции.
//
// Возвращаемое значение:
//  Нет
//
Процедура ПриВыгрузкеТоваровВУстройство(Устройство) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыУстройства = ПолучитьПараметрыУстройства(Устройство);
	Если ПараметрыУстройства = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ПланыОбмена.УдалитьРегистрациюИзменений(ПараметрыУстройства.УзелИнформационнойБазы);
	
	УзелОбъект = ПараметрыУстройства.УзелИнформационнойБазы.ПолучитьОбъект();
	УзелОбъект.ДатаВыгрузки      = ТекущаяДата();
	УзелОбъект.ВыгрузкаВыполнена = Истина;
	УзелОбъект.Записать();
	
КонецПроцедуры

Функция СведенияОСпискеНоменклатуры(СписокКодов)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокКодов", СписокКодов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СправочникНоменклатура.Ссылка КАК Ссылка,
	|	СправочникНоменклатура.Услуга КАК Услуга,
	|	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
	|	СправочникНоменклатура.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	КодыТоваровПодключаемогоОборудованияOffline.Код КАК Код
	|ИЗ
	|	РегистрСведений.КодыТоваровПодключаемогоОборудованияOffline КАК КодыТоваровПодключаемогоОборудованияOffline
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО КодыТоваровПодключаемогоОборудованияOffline.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	КодыТоваровПодключаемогоОборудованияOffline.Код В(&СписокКодов)
	|	И НЕ СправочникНоменклатура.ЭтоГруппа
	|	И НЕ СправочникНоменклатура.ВАрхиве";
	Результат = Новый Соответствие;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		Результат.Вставить(СтрокаРезультата.Код, ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаРезультата));
	КонецЦикла; 
	
	Возврат Результат;
КонецФункции 

Функция СсылкаПоUID(УникальныйИдентификаторНоменклатуры)
	НоменклатураСсылка = Неопределено;
	Если ЗначениеЗаполнено(УникальныйИдентификаторНоменклатуры) Тогда
		НоменклатураСсылка = Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификаторНоменклатуры));
	КонецЕсли;
	
	Возврат НоменклатураСсылка;
КонецФункции 

// Процедура вызывается при загрузке отчета о розничных продажах с устройства.
// Выполняет запись информации в узел плана обмена. Создает отчет о розничных продажах.
//
// Параметры:
//  Устройство       - <СправочникСсылка.ПодключаемоеОборудование>
//  ВыполненоУспешно - <Булево> Признак успешного выполнения операции.
//
// Возвращаемое значение:
//  Нет
//
Функция ПриЗагрузкеОтчетаОРозничныхПродажах(Устройство, ДанныеОПродажах, РезультатЗагрузки, Отказ, СообщениеОбОшибке) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СоответствиеСтавкиНДС = Новый Соответствие;
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаБезНДС(), Перечисления.СтавкиНДС.БезНДС);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС0(), Перечисления.СтавкиНДС.НДС0);
	
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС5(), Перечисления.СтавкиНДС.НДС5);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС7(), Перечисления.СтавкиНДС.НДС7);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС10(), Перечисления.СтавкиНДС.НДС10);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС18(), Перечисления.СтавкиНДС.НДС18);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС20(), Перечисления.СтавкиНДС.НДС20);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС22(), Перечисления.СтавкиНДС.НДС22);
	
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС105(), Перечисления.СтавкиНДС.НДС5);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС107(), Перечисления.СтавкиНДС.НДС7);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС110(), Перечисления.СтавкиНДС.НДС10);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС118(), Перечисления.СтавкиНДС.НДС18);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС120(), Перечисления.СтавкиНДС.НДС20);
	СоответствиеСтавкиНДС.Вставить(МенеджерОфлайнОборудования.СтавкаНДС122(), Перечисления.СтавкиНДС.НДС22);
	
	МассивСозданныхДокументов = Новый Массив;
	
	ПараметрыУстройства = ПолучитьПараметрыУстройства(Устройство, СообщениеОбОшибке);
	Если ПараметрыУстройства = Неопределено Тогда
		Отказ = Истина;
		
		Возврат Неопределено;
	КонецЕсли;
	
	ИдентификаторыДокументовПоСменам = Новый Соответствие;
	
	ТаблицаТоваров           = НовыйТаблицаТоваров();
	ТаблицаОплат             = НовыйТаблицаОплат();
	
	ПродажаСертификатов      = НовыйТаблицаСертификаты();
	
	ТаблицаПредоплаты        = НовыйТаблицаПредоплаты();
	ТаблицаЗачетПредоплаты   = НовыйТаблицаЗачетПредоплаты();
	
	ОтражатьАвансыКакСертификаты = ПолучитьФункциональнуюОпцию("ИспользуютсяПодарочныеСертификаты") 
		И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПредоплатыВРознице");
		
	ВидОплатыПодарочныйСертификатСобственный = Неопределено;
	Если ОтражатьАвансыКакСертификаты Тогда
		ВидОплатыПодарочныйСертификатСобственный = 
			Справочники.ВидыОплатОрганизаций.СобственныйПодарочныйСертификатПоУмолчанию(ПараметрыУстройства.Организация);
	КонецЕсли;
		
	ВидыОплатЭлектроннаяОплатаПоУмолчанию = 
		Справочники.ВидыОплатОрганизаций.ОплатаКартойПоУмолчанию(ПараметрыУстройства.Организация);
	
	ТаблицаВидовОплат = ПолучитьТаблицуВидовОплат(Устройство);
	
	РеквизитыРегистра = Метаданные.РегистрыСведений.СопоставлениеНоменклатурыОФД.Измерения;
	
	НоменклатураЧеков = Новый ТаблицаЗначений;
	НоменклатураЧеков.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
	НоменклатураЧеков.Колонки.Добавить("Наименование",  РеквизитыРегистра.НоменклатураНаименование.Тип);
	НоменклатураЧеков.Колонки.Добавить("Штрихкод",      РеквизитыРегистра.Штрихкод.Тип);
	
	НоменклатураЧеков.Индексы.Добавить("Наименование,Штрихкод");
	
	ЗначениеПоиска = Новый Структура;
	
	РезультатЗагрузки = Новый Соответствие;
	
	Для Каждого ОтчетОПродажах Из ДанныеОПродажах Цикл
		
		// Для формирования ОРП используем данные только закрытых кассовых смен
		Если ЗначениеЗаполнено(ОтчетОПродажах.СтатусСмены) И ОтчетОПродажах.СтатусСмены <> Перечисления.СтатусыКассовойСмены.Закрыта Тогда
			Продолжить;
		КонецЕсли; 
		
		ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(ПараметрыУстройства.Организация, ОтчетОПродажах.ДатаЗакрытияСмены)
			Или УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(ПараметрыУстройства.Организация, ОтчетОПродажах.ДатаЗакрытияСмены);
			
			
		ТаблицаТоваров.Очистить();
		ТаблицаОплат.Очистить();
		НоменклатураЧеков.Очистить();
		ПродажаСертификатов.Очистить();
		ТаблицаПредоплаты.Очистить();
		ТаблицаЗачетПредоплаты.Очистить();
		
		ДанныеОПродажахЗаСмену = ОФД.НовыйОписаниеТаблицыФискальныхДанных();
		Для НомерЧекаЗаСмену = 0 По ОтчетОПродажах.Чеки.Количество()-1 Цикл
			ДанныеЧека = ОтчетОПродажах.Чеки[НомерЧекаЗаСмену];
			Если ДанныеЧека.ТипРасчета <> Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств 
				И ДанныеЧека.ТипРасчета <> Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств Тогда
				Продолжить;
			КонецЕсли;
			
			ЭтоПатент = (ДанныеЧека.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент);
			
			ДанныеОПродажахПоЧеку = ДанныеОПродажахЗаСмену.Добавить();
			ДанныеОПродажахПоЧеку.НомерСмены       = ОтчетОПродажах.НомерСмены;
			ДанныеОПродажахПоЧеку.ФискальныйНомер  = ДанныеЧека.НомерЧека;
			
			// Перебираем товары из каждого чека
			Для Каждого СтрокаТЧ Из ДанныеЧека.Товары Цикл
				Если СтрокаТЧ.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс  Тогда
					Если ЗначениеЗаполнено(ВидОплатыПодарочныйСертификатСобственный) 
						И ДанныеЧека.ТипРасчета =  Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда 
						
						// Возвраты сертификатов не поддерживаем
						НоваяСтрока = ПродажаСертификатов.Добавить();
						НоваяСтрока.ВидОплаты              = ВидОплатыПодарочныйСертификатСобственный;
						НоваяСтрока.Сумма                  = СтрокаТЧ.Сумма;
					ИначеЕсли НЕ ОтражатьАвансыКакСертификаты Тогда
						ИмяРеквизитаСумма = ?(
							ДанныеЧека.ТипРасчета =  Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств, 
							"Сумма", "СуммаВозврат");
						
						НоваяСтрока = ТаблицаПредоплаты.Добавить();
						НоваяСтрока[ИмяРеквизитаСумма] = СтрокаТЧ.Сумма;
						НоваяСтрока.СтавкаНДС = СоответствиеСтавкиНДС[СтрокаТЧ.СтавкаНДС];
					КонецЕсли;
				ИначеЕсли (СтрокаТЧ.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная 
					ИЛИ СтрокаТЧ.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаЧастичная) И НЕ ОтражатьАвансыКакСертификаты Тогда
					
					// Предоплаты как сертификаты не отражаем, так как ставка НДС для предоплат может различаться
					ИмяРеквизитаСумма = ?(
						ДанныеЧека.ТипРасчета =  Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств, 
						"Сумма", "СуммаВозврат");
					
					НоваяСтрока = ТаблицаПредоплаты.Добавить();
					НоваяСтрока[ИмяРеквизитаСумма] = СтрокаТЧ.Сумма;
					НоваяСтрока.СтавкаНДС = СоответствиеСтавкиНДС[СтрокаТЧ.СтавкаНДС];
				КонецЕсли;
				
				// В ТЧ "товары" должны попадать только позиции из чеков на передачу товаров с полной оплатой
				Если СтрокаТЧ.ПризнакСпособаРасчета <> Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой Тогда
					Продолжить;
				КонецЕсли;
				
				НоменклатураСсылка = СсылкаПоUID(СтрокаТЧ.УникальныйИдентификаторНоменклатуры);
				Если НЕ ЗначениеЗаполнено(НоменклатураСсылка) 
					И ЗначениеЗаполнено(СтрокаТЧ.Наименование) Тогда
					
					ЗначениеПоиска.Вставить("Наименование", СокрЛП(СтрокаТЧ.Наименование));
					ЗначениеПоиска.Вставить("Штрихкод", СокрЛП(СтрокаТЧ.КодПредметаРасчета.Значение));
					
					НайденнаяНоменклатураЧеков = НоменклатураЧеков.НайтиСтроки(ЗначениеПоиска);
					Если НайденнаяНоменклатураЧеков.Количество() = 0  Тогда
						СтрокаНоменклатураЧеков = НоменклатураЧеков.Добавить();
						
						СтрокаНоменклатураЧеков.Идентификатор = Новый УникальныйИдентификатор;
						ЗаполнитьЗначенияСвойств(СтрокаНоменклатураЧеков, ЗначениеПоиска);
					Иначе
						СтрокаНоменклатураЧеков = НайденнаяНоменклатураЧеков[0];
					КонецЕсли;
					
					НоменклатураСсылка = СтрокаНоменклатураЧеков.Идентификатор;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаТоваров.Добавить();
				НоваяСтрока.ИдентификаторДокумента           = ДанныеЧека.УникальныйИдентификатор;
				НоваяСтрока.ИдентификаторСвязанногоДокумента = ДанныеЧека.УникальныйИдентификаторСвязанногоДокументаККМ;
				НоваяСтрока.ЭтоПатент                        = ЭтоПатент;
				НоваяСтрока.ЭтоВозврат                       = (ДанныеЧека.ТипРасчета =  Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств);
				НоваяСтрока.НомерФискальногоЧека             = Формат(ДанныеЧека.НомерЧека, "ЧГ=0");
				
				НоваяСтрока.Код                              = СтрокаТЧ.Код;
				НоваяСтрока.Номенклатура                     = НоменклатураСсылка;
				НоваяСтрока.СтавкаНДС                        = СоответствиеСтавкиНДС[СтрокаТЧ.СтавкаНДС];
				НоваяСтрока.Количество                       = СтрокаТЧ.Количество;
				НоваяСтрока.Сумма                            = СтрокаТЧ.Сумма;
			КонецЦикла;
			
			Для каждого СтрокаОплаты Из ДанныеЧека.Оплаты Цикл
				Если СтрокаОплаты.СуммаЭлектроннойОплаты > 0 Тогда
					ВидОплатыСсылка = ?(
						ЗначениеЗаполнено(СтрокаОплаты.УникальныйИдентификаторВидаЭлектроннойОплаты), 
						Справочники.ВидыОплатОрганизаций.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаОплаты.УникальныйИдентификаторВидаЭлектроннойОплаты)),
						Справочники.ВидыОплатОрганизаций.ПустаяСсылка());
						
					Если ВидОплатыСсылка.Пустая() Тогда
						СтрокаТаблицыОплаты = Неопределено;
						Если ЗначениеЗаполнено(СтрокаОплаты.КодВидаЭлектроннойОплаты) Тогда
							СтрокаТаблицыОплаты = ТаблицаВидовОплат.Найти(Число(СтрокаОплаты.КодВидаЭлектроннойОплаты), "Код");
						ИначеЕсли ТаблицаВидовОплат.Количество() = 0 Тогда
							ВидОплатыСсылка = ВидыОплатЭлектроннаяОплатаПоУмолчанию;
						Иначе
							СтрокиОплатаКартой = ТаблицаВидовОплат.НайтиСтроки(Новый Структура("ТипОплаты", Перечисления.ТипыОплат.ПлатежнаяКарта));
							СтрокаТаблицыОплаты = ?(СтрокиОплатаКартой.Количество() = 1, СтрокиОплатаКартой[0], Неопределено);
						КонецЕсли;
						
						ВидОплатыСсылка = ?(
							СтрокаТаблицыОплаты <> Неопределено, 
							СтрокаТаблицыОплаты.Ссылка, 
							ВидОплатыСсылка);
					КонецЕсли; 
					
					НоваяСтрока = ТаблицаОплат.Добавить();
					НоваяСтрока.ЭтоПатент              = ЭтоПатент;
					НоваяСтрока.ИдентификаторДокумента = ДанныеЧека.УникальныйИдентификатор;
					НоваяСтрока.ЭтоВозврат             = (ДанныеЧека.ТипРасчета =  Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств);
					
					НоваяСтрока.ВидОплаты              = ВидОплатыСсылка;
					НоваяСтрока.Сумма                  = СтрокаОплаты.СуммаЭлектроннойОплаты;
				ИначеЕсли СтрокаОплаты.СуммаПредоплатой > 0 И ЗначениеЗаполнено(ВидОплатыПодарочныйСертификатСобственный) Тогда 
					НоваяСтрока = ТаблицаОплат.Добавить();
					НоваяСтрока.ЭтоПатент              = ЭтоПатент;
					НоваяСтрока.ИдентификаторДокумента = ДанныеЧека.УникальныйИдентификатор;
					НоваяСтрока.ЭтоВозврат             = ДанныеЧека.ТипРасчета =  Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств;
					
					НоваяСтрока.ВидОплаты              = ВидОплатыПодарочныйСертификатСобственный;
					НоваяСтрока.Сумма                  = СтрокаОплаты.СуммаПредоплатой;
				ИначеЕсли СтрокаОплаты.СуммаПредоплатой > 0 И НЕ ОтражатьАвансыКакСертификаты 
					И ДанныеЧека.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда // поддерживаем зачет аванса только при продаже
					
					НоваяСтрока = ТаблицаЗачетПредоплаты.Добавить();
					НоваяСтрока.ЭтоПатент              = ЭтоПатент;
					НоваяСтрока.СуммаЗачета = СтрокаОплаты.СуммаПредоплатой;
				КонецЕсли; 
			КонецЦикла; 
		КонецЦикла;
		
		СопоставлениеНоменклатуры = РегистрыСведений.СопоставлениеНоменклатурыОФД.СопоставлениеНоменклатуры(НоменклатураЧеков);
		СопоставлениеНоменклатуры.Индексы.Добавить("Идентификатор");
		
		// Удаляем неподдерживаемый функционал - возвраты услуг.
		СведенияОСпискеНоменклатурыПоКоду   = СведенияОСпискеНоменклатуры(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Код", Истина));
		СведенияОСпискеНоменклатурыПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
			ОбщегоНазначения.ВыгрузитьКолонку(СопоставлениеНоменклатуры, "Номенклатура"), 
			"Ссылка, Услуга, ВидСтавкиНДС, ЭтоГруппа");
		
		ИдентификаторыДокументов    = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "ИдентификаторДокумента", Истина);
		ИтогиПродажПоДокументам     = ТаблицаТоваров.Скопировать(,"ИдентификаторДокумента, Сумма");
		ИтогиОплатПоДокументам      = ТаблицаОплат.Скопировать(,"ИдентификаторДокумента, Сумма");
		
		КоличествоСтрок = ТаблицаТоваров.Количество();
		Для Позиция = 1 По КоличествоСтрок Цикл
			СтрокаТовары = ТаблицаТоваров[КоличествоСтрок - Позиция];
			
			СведенияОНоменклатуре = Неопределено;
			Если ТипЗнч(СтрокаТовары.Номенклатура) = Тип("УникальныйИдентификатор") Тогда
				СтрокаСопоставлениеНоменклатуры = СопоставлениеНоменклатуры.Найти(СтрокаТовары.Номенклатура, "Идентификатор");
				Если СтрокаСопоставлениеНоменклатуры = Неопределено Тогда
					СведенияОНоменклатуре = Новый Структура;
					СведенияОНоменклатуре.Вставить("Ссылка",       СтрокаТовары.Номенклатура);
					СведенияОНоменклатуре.Вставить("Услуга",       Ложь);
					СведенияОНоменклатуре.Вставить("ВидСтавкиНДС", Перечисления.ВидыСтавокНДС.Общая);
					СведенияОНоменклатуре.Вставить("ЭтоГруппа",    Ложь);
				Иначе
					СведенияОНоменклатуре = СведенияОСпискеНоменклатурыПоСсылке[СтрокаСопоставлениеНоменклатуры.Номенклатура];
				КонецЕсли;
			ИначеЕсли СведенияОСпискеНоменклатурыПоКоду[СтрокаТовары.Код] <> Неопределено Тогда
				СведенияОНоменклатуре = СведенияОСпискеНоменклатурыПоКоду[СтрокаТовары.Код];
				СтрокаТовары.Номенклатура = СведенияОНоменклатуре.Ссылка;
			КонецЕсли; 
			
			Если СведенияОНоменклатуре = Неопределено 
				ИЛИ СведенияОНоменклатуре.ЭтоГруппа Тогда
				
				// Если ссылка битая или пришла группа - очистим значение номенклатуры чтобы не поломать документ
				СтрокаТовары.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
				
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаТовары.СтавкаНДС) Тогда
				СтрокаТовары.СтавкаНДС = ?(ПлательщикНДС, 
					Перечисления.СтавкиНДС.СтавкаНДС(СведенияОНоменклатуре.ВидСтавкиНДС, ОтчетОПродажах.ДатаЗакрытияСмены),
					Перечисления.СтавкиНДС.БезНДС);
			КонецЕсли; 
			
			// Дальнейшая логика касается только возвратов 
			Если НЕ СтрокаТовары.ЭтоВозврат Тогда
				Продолжить;
			КонецЕсли;
			
			// При загрузке из ОФД ИдентификаторСвязанногоДокумента не заполняется и определить к какой смене относится возврат - возможности нет
			// поэтому считаем что все возвраты услуг относятся к текущей смене, так как в противном случае они все будут удалены из загрузки
			ЭтоВозвратТекущейСмены = (ИдентификаторыДокументов.Найти(СтрокаТовары.ИдентификаторСвязанногоДокумента) <> Неопределено) 
				ИЛИ (СтрокаТовары.ИдентификаторСвязанногоДокумента = Неопределено И СведенияОНоменклатуре.Услуга);
				
			Если ЭтоВозвратТекущейСмены Тогда
				СтрокаТовары.ДатаРеализации = ОтчетОПродажах.ДатаЗакрытияСмены;
			Иначе
				// Поищем связанный документ в уже загруженных кассовых сменах
				Для каждого ИдентификаторыСмены Из ИдентификаторыДокументовПоСменам Цикл
					Если ИдентификаторыСмены.Значение.Найти(СтрокаТовары.ИдентификаторСвязанногоДокумента) <> Неопределено Тогда
						СтрокаТовары.ДатаРеализации = ИдентификаторыСмены.Ключ;
						Прервать;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
			
			Если НЕ СведенияОНоменклатуре.Услуга Тогда
				// Возвраты товаров отражаем в полном объеме
				Продолжить;
				
			ИначеЕсли ЭтоВозвратТекущейСмены Тогда
				// Возврат услуг текущей смены переносим в ТЧ Товары
				СтрокаТовары.ЭтоВозврат = Ложь;
				
				СтрокаТовары.Количество = СтрокаТовары.Количество;
				СтрокаТовары.Сумма      = СтрокаТовары.Сумма;
			Иначе
				// Возврат услуг из предыдущих смен не поддерживаем - очистим номенклатуру чтобы не поломать логику документа
				СтрокаТовары.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
			КонецЕсли;
		КонецЦикла;
		
		ИдентификаторыДокументовПоСменам.Вставить(ОтчетОПродажах.ДатаЗакрытияСмены, ИдентификаторыДокументов);
		
		СписокСистемНалогообложения = ОБщегоНазначения.ВыгрузитьКолонку(ОтчетОПродажах.Чеки, "СистемаНалогообложения", Истина);
		Для каждого СистемаНалогообложения Из СписокСистемНалогообложения Цикл
			ЭтоПатент = (СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент);
			ПродажиТоваров = ТаблицаТоваров.Скопировать(Новый Структура("ЭтоВозврат, ЭтоПатент", Ложь, ЭтоПатент), "Номенклатура, СтавкаНДС, Количество, Сумма");
			ПродажиТоваров.Свернуть("Номенклатура, СтавкаНДС", "Количество, Сумма");
			
			ВозвратТоваров = ТаблицаТоваров.Скопировать(Новый Структура("ЭтоВозврат, ЭтоПатент", Истина, ЭтоПатент), "Номенклатура, СтавкаНДС, НомерФискальногоЧека, ДатаРеализации, Количество, Сумма");
			ВозвратТоваров.Свернуть("Номенклатура, СтавкаНДС, НомерФискальногоЧека, ДатаРеализации", "Количество, Сумма");
			
			ОплатаТоваров = ТаблицаОплат.Скопировать(Новый Структура("ЭтоВозврат, ЭтоПатент", Ложь, ЭтоПатент), "ВидОплаты, Сумма");
			ОплатаТоваров.Свернуть("ВидОплаты", "Сумма");
			
			ВозвратОплаты = ТаблицаОплат.Скопировать(Новый Структура("ЭтоВозврат, ЭтоПатент", Истина, ЭтоПатент), "ВидОплаты, Сумма");
			ВозвратОплаты.Свернуть("ВидОплаты", "Сумма"); 
			
			ПродажаСертификатов.Свернуть("ЭтоПатент, ВидОплаты", "Сумма");
			
			Комментарий = СформироватьКомментарий(Устройство);
			
			структураОтбор = Новый Структура("ЭтоПатент", ЭтоПатент);
			
			ТаблицыДанных = Новый Структура;
			ТаблицыДанных.Вставить("УникальныйИдентификатор",   ОтчетОПродажах.УникальныйИдентификатор);
			ТаблицыДанных.Вставить("ДатаЗакрытияСмены",         ОтчетОПродажах.ДатаЗакрытияСмены);
			ТаблицыДанных.Вставить("Товары",                    ПродажиТоваров);
			ТаблицыДанных.Вставить("ПодарочныеСертификаты",     ПродажаСертификатов.Скопировать(структураОтбор));
			ТаблицыДанных.Вставить("Предоплаты",                ТаблицаПредоплаты.Скопировать(структураОтбор));
			ТаблицыДанных.Вставить("ЗачетПредоплаты",           ТаблицаЗачетПредоплаты.Скопировать(структураОтбор));
			ТаблицыДанных.Вставить("Оплаты",                    ОплатаТоваров);
			ТаблицыДанных.Вставить("ВозвратыТоваров",           ВозвратТоваров);
			ТаблицыДанных.Вставить("ВозвратыОплат",             ВозвратОплаты);
			ТаблицыДанных.Вставить("НоменклатураЧеков",         НоменклатураЧеков);
			ТаблицыДанных.Вставить("СопоставлениеНоменклатуры", СопоставлениеНоменклатуры);
			
			СозданныйДокумент = СоздатьИЗаполнитьОтчетОПродажах(ПараметрыУстройства, ТаблицыДанных, Комментарий, ЭтоПатент);
			Если СозданныйДокумент = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			МассивСозданныхДокументов.Добавить(СозданныйДокумент);
			РезультатЗагрузки.Вставить(СозданныйДокумент, ДанныеОПродажахЗаСмену);
		
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат МассивСозданныхДокументов;
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейсВыгрузкаТоваров

// Функция возвращает таблицу товаров с данными к выгрузке в устройство.
//
// Параметры:
//  Устройство - <СправочникСсылка.ПодключаемоеОборудование> - Устройство для которого необходимо получить данные.
//  Параметры - <Структура> - Параметры формирования данныъ.
//  ПолнаяВыгрузка - <Булево> - выгружать все товары.
//
// Возвращаемое значение:
//  <Массив> результаты запроса для заполнения .
//
Функция ТаблицыДанныхДляВыгрузки(Устройство, Параметры, ПолнаяВыгрузка)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Ссылка,
	|	КодыТоваровПодключаемогоОборудованияOffline.Код КАК Код,
	|	СправочникНоменклатура.Наименование КАК Наименование,
	|	СправочникНоменклатура.НаименованиеПолное КАК НаименованиеПолное,
	|	СправочникНоменклатура.Артикул КАК Артикул,
	|	ЕСТЬNULL(СправочникНоменклатура.КодНоменклатурнойКлассификацииККТ.Код, НЕОПРЕДЕЛЕНО) КАК КодВидаНоменклатурнойКлассификации,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыМаркировкиККТ.ИзделияИзМеха)
	|		КОГДА СправочникНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыМаркировкиККТ.ТабачнаяПродукция)
	|		КОГДА СправочникНоменклатура.ВидМаркировки = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Обувь)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыМаркировкиККТ.ОбувныеТовары)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ТипМаркированнойПродукции,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Ссылка, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмеренияСсылка,
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	КодыТоваровПодключаемогоОборудованияOffline.Используется КАК Используется,
	|	СправочникНоменклатура.Услуга КАК Услуга,
	|	СправочникНоменклатура.Родитель КАК Родитель,
	|	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
	|	НЕ СведенияОбАлкогольнойПродукции.Номенклатура ЕСТЬ NULL КАК ЭтоАлкогольнаяПродукция,
	|	ЕСТЬNULL(СведенияОбАлкогольнойПродукции.КоэффПересчетаДал, 0) * 10 КАК ЕмкостьТары,
	|	ВЫБОР ЕСТЬNULL(СведенияОбАлкогольнойПродукции.ВидЛицензии, НЕОПРЕДЕЛЕНО)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийНаПродажуАлкоголя.АлкогольнаяПродукция)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоМаркируемаяАлкогольнаяПродукция,
	|	ЕСТЬNULL(СправочникНоменклатура.Производитель.ИНН, """") КАК ПроизводительИНН,
	|	ЕСТЬNULL(СправочникНоменклатура.Производитель.КПП, """") КАК ПроизводительКПП,
	|	ЕСТЬNULL(СведенияОбАлкогольнойПродукции.КодВида169, """") КАК КодВидаАлкогольнойПродукции
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	РегистрСведений.КодыТоваровПодключаемогоОборудованияOffline КАК КодыТоваровПодключаемогоОборудованияOffline
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО КодыТоваровПодключаемогоОборудованияOffline.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровПодключаемогоОборудованияOffline.Изменения КАК КодыТоваровПодключаемогоОборудованияOfflineИзменения
	|		ПО КодыТоваровПодключаемогоОборудованияOffline.Код = КодыТоваровПодключаемогоОборудованияOfflineИзменения.Код
	|			И (КодыТоваровПодключаемогоОборудованияOfflineИзменения.Узел = &УзелИнформационнойБазы)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ПериодЦен, ТипЦен = &ТипЦен) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО КодыТоваровПодключаемогоОборудованияOffline.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
	|		ПО КодыТоваровПодключаемогоОборудованияOffline.Номенклатура = СведенияОбАлкогольнойПродукции.Номенклатура
	|ГДЕ
	|	НЕ СправочникНоменклатура.Ссылка ЕСТЬ NULL
	|	И &СписокУсловий
	|	И СправочникНоменклатура.ВАрхиве = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Ссылка КАК Ссылка,
	|	ВТ_Товары.Код КАК Код,
	|	ВТ_Товары.Наименование КАК Наименование,
	|	ВТ_Товары.НаименованиеПолное КАК НаименованиеПолное,
	|	ВТ_Товары.Артикул КАК Артикул,
	|	ВТ_Товары.Родитель КАК Группа,
	|	ВТ_Товары.КодВидаНоменклатурнойКлассификации КАК КодВидаНоменклатурнойКлассификации,
	|	ВТ_Товары.ТипМаркированнойПродукции КАК ТипМаркированнойПродукции,
	|	ВТ_Товары.Цена КАК Цена,
	|	ВТ_Товары.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмеренияНаименование,
	|	ВТ_Товары.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод,
	|	ВТ_Товары.ЕдиницаИзмеренияСсылка КАК ЕдиницаИзмеренияСсылка,
	|	ВТ_Товары.Услуга КАК Услуга,
	|	ВТ_Товары.ЭтоАлкогольнаяПродукция КАК ЭтоАлкогольнаяПродукция,
	|	ВТ_Товары.ЭтоМаркируемаяАлкогольнаяПродукция КАК ЭтоМаркируемаяАлкогольнаяПродукция,
	|	ВТ_Товары.ЕмкостьТары КАК ЕмкостьТары,
	|	ВТ_Товары.КодВидаАлкогольнойПродукции КАК КодВидаАлкогольнойПродукции,
	|	ВТ_Товары.ПроизводительИНН КАК ПроизводительИНН,
	|	ВТ_Товары.ПроизводительКПП КАК ПроизводительКПП
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|ГДЕ
	|	НЕ ВТ_Товары.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_Товары.Ссылка = ШтрихкодыНоменклатуры.Номенклатура
	|ГДЕ
	|	НЕ ШтрихкодыНоменклатуры.Номенклатура ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Товары.ЕдиницаИзмеренияСсылка КАК Ссылка,
	|	ВТ_Товары.ЕдиницаИзмеренияКод КАК Код,
	|	ВТ_Товары.ЕдиницаИзмеренияНаименование КАК Наименование
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГруппаНоменклатуры.Ссылка КАК Номенклатура,
	|	ГруппаНоменклатуры.Наименование КАК Наименование,
	|	ЕСТЬNULL(КодыТоваровПодключаемогоОборудованияOffline.Код, НЕОПРЕДЕЛЕНО) КАК Код,
	|	ЛОЖЬ КАК ЭтоНовый,
	|	ИСТИНА КАК Используется
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровПодключаемогоОборудованияOffline КАК КодыТоваровПодключаемогоОборудованияOffline
	|		ПО ВТ_Товары.Родитель = КодыТоваровПодключаемогоОборудованияOffline.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ГруппаНоменклатуры
	|		ПО ВТ_Товары.Родитель = ГруппаНоменклатуры.Ссылка
	|ГДЕ
	|	НЕ ГруппаНоменклатуры.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(КодыТоваровПодключаемогоОборудованияOffline.Код) КАК Код
	|ИЗ
	|	РегистрСведений.КодыТоваровПодключаемогоОборудованияOffline КАК КодыТоваровПодключаемогоОборудованияOffline"; 
	
	СписокУсловий = Новый Массив;
	СписокУсловий.Добавить("НЕ СправочникНоменклатура.ЭтоГруппа");
	
	Если НЕ ПолнаяВыгрузка Тогда
		СписокУсловий.Добавить("КодыТоваровПодключаемогоОборудованияOfflineИзменения.Узел = &УзелИнформационнойБазы");
	Иначе
		СписокУсловий.Добавить("ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) <> 0");
	КонецЕсли; 
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокУсловий", СтрСоединить(СписокУсловий, " И "));
	
	Запрос.УстановитьПараметр("УзелИнформационнойБазы", Параметры.УзелИнформационнойБазы);
	Запрос.УстановитьПараметр("ТипЦен",                 Параметры.ТипЦен);
	Запрос.УстановитьПараметр("ПериодЦен",              КонецДня(ТекущаяДата()));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результаты = Новый Структура();
	Результаты.Вставить("ТаблицаТовары",             РезультатЗапроса[1].Выгрузить());
	Результаты.Вставить("ТаблицаШтрихкоды",          РезультатЗапроса[2].Выгрузить());
	Результаты.Вставить("ТаблицаЕдиницыИзмерения",   РезультатЗапроса[3].Выгрузить());
	Результаты.Вставить("ТаблицаГруппыНоменклатуры", РезультатЗапроса[4].Выгрузить());
	
	ВыборкаРеквизиты = РезультатЗапроса[5].Выбрать();
	Если ВыборкаРеквизиты.Следующий() 
		И ЗначениеЗаполнено(ВыборкаРеквизиты.Код) Тогда
		
		Результаты.Вставить("МаксимальныйКод", ВыборкаРеквизиты.Код);
	Иначе
		Результаты.Вставить("МаксимальныйКод", 0);
	КонецЕсли; 
	
	Возврат Результаты;
КонецФункции

Функция СтавкаНДСОфлайнОборудование(Знач СтавкаНДС)
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(ТекущаяДата());
	КонецЕсли;
	
	Если СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда 
		Возврат МенеджерОфлайнОборудования.СтавкаНДС0();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС5 Тогда 
		Возврат МенеджерОфлайнОборудования.СтавкаНДС5();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС5_105 Тогда 
		Возврат МенеджерОфлайнОборудования.СтавкаНДС105();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС7 Тогда 
		Возврат МенеджерОфлайнОборудования.СтавкаНДС7();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС7_107 Тогда 
		Возврат МенеджерОфлайнОборудования.СтавкаНДС107();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда 
		Возврат МенеджерОфлайнОборудования.СтавкаНДС10();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда 
		Возврат МенеджерОфлайнОборудования.СтавкаНДС110();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
		Возврат МенеджерОфлайнОборудования.СтавкаНДС18();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
		Возврат МенеджерОфлайнОборудования.СтавкаНДС118();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
		Возврат МенеджерОфлайнОборудования.СтавкаНДС20();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
		Возврат МенеджерОфлайнОборудования.СтавкаНДС120();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС22 Тогда
		Возврат МенеджерОфлайнОборудования.СтавкаНДС22();
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС22_122 Тогда
		Возврат МенеджерОфлайнОборудования.СтавкаНДС122();
	Иначе
		Возврат МенеджерОфлайнОборудования.СтавкаБезНДС();
	КонецЕсли;
	
КонецФункции

// Функция возвращает структуру с данными в формате, необходимом для выгрузки списка товаров в ККМ Offline.
//
// Параметры:
//  Устройство - <СправочникСсылка.ПодключаемоеОборудование> - Устройство для которого необходимо получить данные.
//  ТолькоИзмененные - <Булево> - Флаг получения только измененных данных.
//
// Возвращаемое значение:
//  <Структура> с массивом структур для выгрузки и количеством не выгруженных строк.
//
Функция ПолучитьДанныеДляКассы(Устройство, ПрайсЛист, ПолнаяВыгрузка) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(Устройство);
	Если Параметры = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДатаЗагрузки = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	ДанныеДляКассы = ТаблицыДанныхДляВыгрузки(Устройство, Параметры, ПолнаяВыгрузка);
	ДанныеДляКассы.ТаблицаШтрихкоды.Индексы.Добавить("Номенклатура");
	
	СведенияОСпискеНоменклатуры = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДляКассы.ТаблицаТовары, "Ссылка", Истина), 
		Новый Структура("Дата, Организация, Реализация", ДатаЗагрузки, Параметры.Организация, Истина), 
		Ложь, Ложь); 
		
	ТаблицаГруппыНоменклатуры = ДанныеДляКассы.ТаблицаГруппыНоменклатуры;
	
	КодНовогоЭлементаТаблицыТовары = Число(ДанныеДляКассы.МаксимальныйКод)+1;
	
	Для каждого СтрокаТовара Из ДанныеДляКассы.ТаблицаТовары Цикл
		ЗаписьТовара = МенеджерОфлайнОборудования.ЗаписьТовараПрайсЛиста();
		
		СведенияОНоменклатуре = СведенияОСпискеНоменклатуры[СтрокаТовара.Ссылка];
		
		ЗаписьТовара.Код                     = СтрокаТовара.Код;
		ЗаписьТовара.Цена                    = СтрокаТовара.Цена;
		ЗаписьТовара.Остаток                 = 0;
		ЗаписьТовара.УникальныйИдентификатор = СтрокаТовара.Ссылка.УникальныйИдентификатор();
		ЗаписьТовара.Наименование            = СтрокаТовара.Наименование;
		ЗаписьТовара.СтавкаНДС               = СтавкаНДСОфлайнОборудование(СведенияОНоменклатуре.СтавкаНДС);
		
		Если СведенияОНоменклатуре.МаркируемаяПродукция Тогда
			ЗаписьТовара.ЭтоМаркированнаяПродукция = Истина;
			ЗаписьТовара.ТипМаркированнойПродукции = СтрокаТовара.ТипМаркированнойПродукции;
			ЗаписьТовара.ВидМаркированнойПродукции = ИнтеграцияИСМПБП.ВидМаркированнойПродукцииБПО(СведенияОНоменклатуре.ВидПродукцииИС);
		КонецЕсли;
		
		ЗаписьТовара.КодВидаНоменклатурнойКлассификации = СтрокаТовара.КодВидаНоменклатурнойКлассификации;
		
		Если НЕ СтрокаТовара.Группа.Пустая() Тогда
			СтрокаГруппыНоменклатуры = ТаблицаГруппыНоменклатуры.Найти(СтрокаТовара.Группа, "Номенклатура");
			Если СтрокаГруппыНоменклатуры <> Неопределено Тогда
				Если СтрокаГруппыНоменклатуры.Код = Неопределено Тогда
					СтрокаГруппыНоменклатуры.Код      = КодНовогоЭлементаТаблицыТовары;
					СтрокаГруппыНоменклатуры.ЭтоНовый = Истина;
					
					КодНовогоЭлементаТаблицыТовары = КодНовогоЭлементаТаблицыТовары + 1;
				КонецЕсли;
				
				ЗаписьТовара.КодГруппы                     = СтрокаГруппыНоменклатуры.Код;
				ЗаписьТовара.УникальныйИдентификаторГруппы = СтрокаТовара.Группа.УникальныйИдентификатор();
			КонецЕсли; 
		КонецЕсли; 
		
		ЗаписьТовара.ПризнакПредметаРасчета = ПечатьФискальныхДокументов.РасчитатьПризнакПредметаРасчета(СведенияОНоменклатуре);
		
		ЗаписьТовара.Артикул                 = СтрокаТовара.Артикул;
		ЗаписьТовара.Описание                = СтрокаТовара.НаименованиеПолное;
		ЗаписьТовара.ЭтоВесовойТовар         = Ложь;
		ЗаписьТовара.НомерСекции             = 1;
		
		ЗаписьТовара.ЭтоАлкоголь = СтрокаТовара.ЭтоАлкогольнаяПродукция;
		Если СтрокаТовара.ЭтоАлкогольнаяПродукция Тогда
			ЗаписьТовара.АлкогольныеРеквизиты.Маркируемый = СтрокаТовара.ЭтоМаркируемаяАлкогольнаяПродукция;
			ЗаписьТовара.АлкогольныеРеквизиты.КодВидаАлкогольнойПродукции = СтрокаТовара.КодВидаАлкогольнойПродукции;
			ЗаписьТовара.АлкогольныеРеквизиты.ЕмкостьТары = СтрокаТовара.ЕмкостьТары;
			ЗаписьТовара.АлкогольныеРеквизиты.ИННПроизводителя = СтрокаТовара.ПроизводительИНН;
			ЗаписьТовара.АлкогольныеРеквизиты.КПППроизводителя = СтрокаТовара.ПроизводительКПП;
		КонецЕсли; 
		
		ЗаписьТовара.КодЕдиницыИзмерения                     = СтрокаТовара.ЕдиницаИзмеренияКод; 
		ЗаписьТовара.УникальныйИдентификаторЕдиницыИзмерения = СтрокаТовара.ЕдиницаИзмеренияСсылка.УникальныйИдентификатор();
		
		ТаблицаШтрихкоды = ДанныеДляКассы.ТаблицаШтрихкоды.НайтиСтроки(Новый Структура("Номенклатура", СтрокаТовара.Ссылка));
		Для каждого СтрокаШтрихкода Из ТаблицаШтрихкоды Цикл
			// Штрихкод
			ЗаписьШтрихкода = МенеджерОфлайнОборудования.ЗаписьШтрихкодаПрайсЛиста();
			ЗаписьШтрихкода.Штрихкод = СтрокаШтрихкода.Штрихкод;
			ЗаписьТовара.Штрихкоды.Добавить(ЗаписьШтрихкода);
		КонецЦикла;
		
		ПрайсЛист.Товары.Добавить(ЗаписьТовара);
	КонецЦикла;
	
	Для каждого СтрокаЕдиницаИзмерения Из ДанныеДляКассы.ТаблицаЕдиницыИзмерения Цикл
		ЗаписьЕИ = МенеджерОфлайнОборудования.ЗаписьЕдиницыИзмеренияПрайсЛиста();
		
		ЗаписьЕИ.Код                     = СтрокаЕдиницаИзмерения.Код;
		ЗаписьЕИ.Наименование            = СтрокаЕдиницаИзмерения.Наименование;
		ЗаписьЕИ.УникальныйИдентификатор = СтрокаЕдиницаИзмерения.Ссылка.УникальныйИдентификатор();
		ЗаписьЕИ.КодОКЕИ                 = СтрокаЕдиницаИзмерения.Код;
		
		ПрайсЛист.ЕдиницыИзмерения.Добавить(ЗаписьЕИ);
	КонецЦикла;
	
	ТаблицаНовыхЗаписейГруппыНоменклатура = ТаблицаГруппыНоменклатуры.Скопировать(
			Новый Структура("ЭтоНовый", Истина), 
			"Номенклатура, Код, Используется");
	
	НаборЗаписейКодыНоменклатуры = РегистрыСведений.КодыТоваровПодключаемогоОборудованияOffline.СоздатьНаборЗаписей();
	НаборЗаписейКодыНоменклатуры.Загрузить(ТаблицаНовыхЗаписейГруппыНоменклатура);
	НаборЗаписейКодыНоменклатуры.Записать(Ложь);
	
	Для каждого СтрокаГруппыТоваров Из ТаблицаГруппыНоменклатуры Цикл
		ЗаписьГруппаТоваров = МенеджерОфлайнОборудования.ЗаписьГруппыТоваровПрайсЛиста();
		
		ЗаписьГруппаТоваров.Код                     = СтрокаГруппыТоваров.Код;
		ЗаписьГруппаТоваров.Наименование            = СтрокаГруппыТоваров.Наименование;
		ЗаписьГруппаТоваров.УникальныйИдентификатор = СтрокаГруппыТоваров.Номенклатура.УникальныйИдентификатор();
		
		ПрайсЛист.ГруппыТоваров.Добавить(ЗаписьГруппаТоваров);
	КонецЦикла; 
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейсВыгрузкаНастроек

// Функция возвращает настройки для экземпляра оборудования.
//
Функция ПолучитьНастройкиДляККМОффлайн(Устройство, СтруктураНастроек) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(Устройство);
	Если Параметры = Неопределено Тогда
		Возврат СтруктураНастроек;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат СтруктураНастроек;
	КонецЕсли;
	
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, ТекущаяДатаСеанса());
	
	СтруктураНастроек.НаименованиеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	СтруктураНастроек.ИНН                     = СведенияОбОрганизации.ИНН;
	СтруктураНастроек.КПП                     = СведенияОбОрганизации.КПП;
	СтруктураНастроек.АдресТочкиПродажи       = СведенияОбОрганизации.ЮридическийАдрес;
	
	Дата = ТекущаяДатаСеанса();
	
	Если УчетнаяПолитика.ПрименяетсяУСНДоходы(Организация, Дата)
		Или УчетнаяПолитика.ПрименяетсяАУСНДоходы(Организация, Дата) Тогда
		СтруктураНастроек.СистемыНалогообложения.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход);
	ИначеЕсли УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата)
		Или УчетнаяПолитика.ПрименяетсяАУСНДоходыМинусРасходы(Организация, Дата) Тогда
		СтруктураНастроек.СистемыНалогообложения.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход);
	ИначеЕсли УчетнаяПолитика.СистемаНалогообложения(Организация, Дата) = Перечисления.СистемыНалогообложения.Общая Тогда 
		СтруктураНастроек.СистемыНалогообложения.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.ОСН);
	КонецЕсли;
	
	Если УчетнаяПолитика.ПлательщикЕНВД(Организация, Дата) Тогда
		СтруктураНастроек.СистемыНалогообложения.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД);
	КонецЕсли; 
	
	Если УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, Дата) Тогда
		СтруктураНастроек.СистемыНалогообложения.Добавить(Перечисления.ТипыСистемНалогообложенияККТ.Патент);
	КонецЕсли; 
	
	ТаблицаВидовОплат = ПолучитьТаблицуВидовОплат(Устройство);
	ВидыОплаты = Новый Массив;
	
	Для Каждого СтрокаВидаОплаты Из ТаблицаВидовОплат Цикл
		
		ВидЭлектроннойОплаты = МенеджерОфлайнОборудования.ЗаписьВидЭлектроннойОплаты();
		ВидЭлектроннойОплаты.Код = СтрокаВидаОплаты.Код;
		ВидЭлектроннойОплаты.Наименование = СтрокаВидаОплаты.Наименование;
		Если СтрокаВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.ПлатежнаяКарта Тогда
			ВидЭлектроннойОплаты.ТипЭлектроннойОплаты = МенеджерОфлайнОборудования.ТипЭлектроннойОплатыПлатежнаяКарта();
		ИначеЕсли СтрокаВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.БанковскийКредит Тогда 
			ВидЭлектроннойОплаты.ТипЭлектроннойОплаты = МенеджерОфлайнОборудования.ТипЭлектроннойОплатыБанковскийКредит();
		ИначеЕсли СтрокаВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.ПодарочныйСертификатСобственный 
			ИЛИ СтрокаВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.ПодарочныйСертификатСторонний Тогда 
			ВидЭлектроннойОплаты.ТипЭлектроннойОплаты = МенеджерОфлайнОборудования.ТипЭлектроннойОплатыПодарочныйСертификат();
		КонецЕсли;
		
		ВидЭлектроннойОплаты.УникальныйИдентификатор = СтрокаВидаОплаты.Ссылка.УникальныйИдентификатор();
		
		СтруктураНастроек.ВидыЭлектроннойОплаты.Добавить(ВидЭлектроннойОплаты);
	КонецЦикла;
	
	Возврат СтруктураНастроек;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоздатьИЗаполнитьОтчетОПродажах(ПараметрыУстройства, ТаблицыДанных, Комментарий = "", ЭтоПатент)
	
	// пустой документ не сохраняем
	Если ТаблицыДанных.Товары.Количество() = 0 
		И ТаблицыДанных.Оплаты.Количество() = 0
		И ТаблицыДанных.ВозвратыТоваров.Количество() = 0
		И ТаблицыДанных.ПодарочныеСертификаты.Количество() = 0
		И ТаблицыДанных.Предоплаты.Количество() = 0
		И ТаблицыДанных.ЗачетПредоплаты.Количество() = 0
		И ТаблицыДанных.ВозвратыОплат.Количество() = 0 Тогда
	
		Возврат Неопределено;
	КонецЕсли;
	
	ЕстьПредоплаты = ТаблицыДанных.Предоплаты.Количество() > 0 
		ИЛИ ТаблицыДанных.ВозвратыОплат.Количество() > 0;
		
	ОбновитьПовторноИспользуемыеЗначения = Ложь;
		
	РозничныйПокупательСсылка          = Неопределено;
	ОсновнойДоговорРозничныйПокупатель = Неопределено;
	
	Если ЕстьПредоплаты Тогда
		РозничныйПокупательСсылка = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.Контрагенты.РозничныйПокупатель");
		ОсновнойДоговорРозничныйПокупатель = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		
		РозничныйПокупательСуществует = ЗначениеЗаполнено(РозничныйПокупательСсылка);
		Если РозничныйПокупательСуществует Тогда
			РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
				ОсновнойДоговорРозничныйПокупатель, 
				РозничныйПокупательСсылка, 
				ПараметрыУстройства.Организация, 
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем));
		Иначе
			СправочникОбъект = Справочники.Контрагенты.СоздатьЭлемент();
			СправочникОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
			СправочникОбъект.Наименование = НСтр("ru = 'Розничный покупатель'");
			СправочникОбъект.НаименованиеПолное = НСтр("ru = 'Розничный покупатель'");
			СправочникОбъект.ИмяПредопределенныхДанных = "РозничныйПокупатель";
			СправочникОбъект.Записать();
			
			РозничныйПокупательСсылка = СправочникОбъект.Ссылка;
			
			ОбновитьПовторноИспользуемыеЗначения = Истина;
			
			// Если учет по договорам не ведется, то договор будет создан при записи "Отчета о розничных продажах"
			Если ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
				СправочникОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
				СправочникОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем;
				СправочникОбъект.Организация = ПараметрыУстройства.Организация;
				СправочникОбъект.Владелец = РозничныйПокупательСсылка;
				СправочникОбъект.Наименование = НСтр("ru='Без договора'", ОбщегоНазначения.КодОсновногоЯзыка());
				СправочникОбъект.Записать();
				
				ОсновнойДоговорРозничныйПокупатель = СправочникОбъект.Ссылка;
				
				РаботаСДоговорамиКонтрагентовБП.УстановитьОсновнойДоговорКонтрагента(ОсновнойДоговорРозничныйПокупатель);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьПредоплаты 
		И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПредоплатыВРознице") Тогда
	
		Константы.ИспользоватьПредоплатыВРознице.Установить(Истина);
		ОбновитьПовторноИспользуемыеЗначения = Истина;
	КонецЕсли;
	
	СтруктураПараметровДокумента = Новый Структура();
	СтруктураПараметровДокумента.Вставить("Организация", 	ПараметрыУстройства.Организация);
	СтруктураПараметровДокумента.Вставить("Склад", 			ПараметрыУстройства.Склад);
	СтруктураПараметровДокумента.Вставить("ВидОперации",	Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетККМОПродажах);
	СтруктураПараметровДокумента.Вставить("Ответственный", 	Пользователи.ТекущийПользователь());
	СтруктураПараметровДокумента.Вставить("ЗагрузкаИзОФД",  ПараметрыУстройства.ЗагрузкаИзОФД);
	СтруктураПараметровДокумента.Вставить("СуммаВключаетНДС", Истина); // Чеки загруженные из торгового оборудования и ОФД всегда с НДС в сумме
	
	СсылкаДокумента = ?(
		ЗначениеЗаполнено(ТаблицыДанных.УникальныйИдентификатор), 
		Документы.ОтчетОРозничныхПродажах.ПолучитьСсылку(ТаблицыДанных.УникальныйИдентификатор), 
		Документы.ОтчетОРозничныхПродажах.ПолучитьСсылку());
	
	ОтчетОРозничныхПродажахОбъект =  СсылкаДокумента.ПолучитьОбъект();
	
	// Такого документа еще нет
	Если ОтчетОРозничныхПродажахОбъект = Неопределено Тогда
		ОтчетОРозничныхПродажахОбъект = Документы.ОтчетОРозничныхПродажах.СоздатьДокумент();
		ОтчетОРозничныхПродажахОбъект.УстановитьСсылкуНового(СсылкаДокумента);
	Иначе
		ОтчетОРозничныхПродажахОбъект.Товары.Очистить();
		ОтчетОРозничныхПродажахОбъект.Возвраты.Очистить();
		ОтчетОРозничныхПродажахОбъект.Оплата.Очистить();
		ОтчетОРозничныхПродажахОбъект.ВозвратОплаты.Очистить();
	КонецЕсли; 
	
	ОтчетОРозничныхПродажахОбъект.Дата = ТаблицыДанных.ДатаЗакрытияСмены;
	ОтчетОРозничныхПродажахОбъект.ДеятельностьНаПатенте = ЭтоПатент;
	
	Если ОтчетОРозничныхПродажахОбъект.ДеятельностьНаПатенте Тогда
		Если НЕ ЗначениеЗаполнено(ОтчетОРозничныхПродажахОбъект.Патент) Тогда
			ОтчетОРозничныхПродажахОбъект.Патент = 
				УчетПСН.ВидДеятельностиПоПатентуПоУмолчанию(ОтчетОРозничныхПродажахОбъект.Организация, ОтчетОРозничныхПродажахОбъект.Дата);
		КонецЕсли;
		ОтчетОРозничныхПродажахОбъект.ДеятельностьНаТорговомСборе = Ложь;
	Иначе
		ОтчетОРозничныхПродажахОбъект.Патент = Справочники.Патенты.ПустаяСсылка();
		Документы.ОтчетОРозничныхПродажах.УстановитьДеятельностьНаТорговомСборе(ОтчетОРозничныхПродажахОбъект);
	КонецЕсли;
	
	ОтчетОРозничныхПродажахОбъект.Заполнить(СтруктураПараметровДокумента);
	ОтчетОРозничныхПродажахОбъект.Комментарий = Комментарий; 
	
	НоменклатураЧековДокументов = РегистрыСведений.НоменклатураДокументовОФД.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ЕстьНесопоставленнаяНоменклатура = Ложь;
	
	Для каждого СтрокаТовары Из ТаблицыДанных.Товары Цикл
		Если СтрокаТовары.Сумма = 0 
			И СтрокаТовары.Количество = 0 Тогда
			
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары, "Номенклатура, СтавкаНДС, Количество, Сумма");
		Если ТипЗнч(СтрокаТовары.Номенклатура) = Тип("УникальныйИдентификатор") Тогда
			СвойстваНоменклатуры = ТаблицыДанных.НоменклатураЧеков.Найти(СтрокаТовары.Номенклатура, "Идентификатор");
			
			НоваяСтрока.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		
			СтрокаНесопоставленнаяНоменклатура = НоменклатураЧековДокументов.Добавить();
			
			СтрокаНесопоставленнаяНОменклатура.ИдентификаторСтроки      = НоваяСтрока.ИдентификаторСтроки;
			СтрокаНесопоставленнаяНОменклатура.ТабличнаяЧасть           = "Товары";
			СтрокаНесопоставленнаяНОменклатура.НоменклатураНаименование = СвойстваНоменклатуры.Наименование;
			СтрокаНесопоставленнаяНОменклатура.Штрихкод                 = СвойстваНоменклатуры.Штрихкод;
			НайденнаяНоменклатура = ТаблицыДанных.СопоставлениеНоменклатуры.Найти(СтрокаТовары.Номенклатура, "Идентификатор");
			Если НайденнаяНоменклатура <> Неопределено Тогда
				НоваяСтрока.Номенклатура = НайденнаяНоменклатура.Номенклатура;
			ИначеЕсли НЕ ЕстьНесопоставленнаяНоменклатура Тогда
				ЕстьНесопоставленнаяНоменклатура = Истина;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Цена = ?(НоваяСтрока.Количество = 0, НоваяСтрока.Сумма, НоваяСтрока.Сумма / НоваяСтрока.Количество);
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(НоваяСтрока, ОтчетОРозничныхПродажахОбъект.СуммаВключаетНДС);
	КонецЦикла; 
	
	// по туристическому налогу заполним гостиницу и вид льготы
	Если УчетнаяПолитика.ПлательщикТуристическогоНалога(ОтчетОРозничныхПродажахОбъект.Организация, ОтчетОРозничныхПродажахОбъект.Дата) Тогда
		СписокТовары = ОбщегоНазначения.ВыгрузитьКолонку(ОтчетОРозничныхПродажахОбъект.Товары, "Номенклатура", Истина);
		УслугаРазмещения = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокТовары, "УслугаРазмещения");
		
		ГостиницаСсылка = ПараметрыУстройства.Гостиница;
		ТуристическийНалогСервер.УстановитьГостиницу(ГостиницаСсылка, ОтчетОРозничныхПродажахОбъект.Организация);
		Для каждого СтрокаТовары Из ОтчетОРозничныхПродажахОбъект.Товары Цикл
			Если НЕ ЗначениеЗаполнено(УслугаРазмещения[СтрокаТовары.Номенклатура]) Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТовары.Гостиница = ГостиницаСсылка;
			СтрокаТовары.ВидЛьготыПоТуристическомуНалогу = Перечисления.ВидыЛьготПоТуристическомуНалогу.НеПрименяется;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаВозвраты Из ТаблицыДанных.ВозвратыТоваров Цикл
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.Возвраты.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВозвраты, "Номенклатура, СтавкаНДС, Количество, Сумма, НомерФискальногоЧека, ДатаРеализации");
		
		Если ТипЗнч(СтрокаВозвраты.Номенклатура) = Тип("УникальныйИдентификатор") Тогда
			СвойстваНоменклатуры = ТаблицыДанных.НоменклатураЧеков.Найти(СтрокаВозвраты.Номенклатура, "Идентификатор");
			
			НоваяСтрока.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		
			СтрокаНесопоставленнаяНоменклатура = НоменклатураЧековДокументов.Добавить();
			
			СтрокаНесопоставленнаяНОменклатура.ИдентификаторСтроки      = НоваяСтрока.ИдентификаторСтроки;
			СтрокаНесопоставленнаяНОменклатура.ТабличнаяЧасть           = "Возвраты";
			СтрокаНесопоставленнаяНОменклатура.НоменклатураНаименование = СвойстваНоменклатуры.Наименование;
			СтрокаНесопоставленнаяНОменклатура.Штрихкод                 = СвойстваНоменклатуры.Штрихкод;
			НайденнаяНоменклатура = ТаблицыДанных.СопоставлениеНоменклатуры.Найти(СтрокаВозвраты.Номенклатура, "Идентификатор");
			Если НайденнаяНоменклатура <> Неопределено Тогда
				НоваяСтрока.Номенклатура = НайденнаяНоменклатура.Номенклатура;
			ИначеЕсли НЕ ЕстьНесопоставленнаяНоменклатура Тогда
				ЕстьНесопоставленнаяНоменклатура = Истина;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Цена = ?(НоваяСтрока.Количество = 0, НоваяСтрока.Сумма, НоваяСтрока.Сумма / НоваяСтрока.Количество);
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(НоваяСтрока, ОтчетОРозничныхПродажахОбъект.СуммаВключаетНДС);
	КонецЦикла; 
	
	Для Каждого СтрокаОплата ИЗ ТаблицыДанных.Оплаты Цикл
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.Оплата.Добавить();
		
		НоваяСтрока.СуммаОплаты = СтрокаОплата.Сумма;
		НоваяСтрока.ВидОплаты   = СтрокаОплата.ВидОплаты;
	КонецЦикла;
	
	Для Каждого СтрокаВозвратОплаты ИЗ ТаблицыДанных.ВозвратыОплат Цикл
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.ВозвратОплаты.Добавить();
		
		НоваяСтрока.СуммаОплаты = СтрокаВозвратОплаты.Сумма;
		НоваяСтрока.ВидОплаты   = СтрокаВозвратОплаты.ВидОплаты;
	КонецЦикла; 
	
	Для каждого СтрокаПродажаСертификата Из ТаблицыДанных.ПодарочныеСертификаты Цикл
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.ПодарочныеСертификаты.Добавить();
		
		НоваяСтрока.Сумма = СтрокаПродажаСертификата.Сумма;
		НоваяСтрока.ВидОплаты   = СтрокаПродажаСертификата.ВидОплаты;
	КонецЦикла;
	
	Для каждого СтрокаПредоплаты Из ТаблицыДанных.Предоплаты Цикл
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.Предоплаты.Добавить();
		
		НоваяСтрока.Контрагент         = РозничныйПокупательСсылка;
		НоваяСтрока.ДоговорКонтрагента = ОсновнойДоговорРозничныйПокупатель;
		НоваяСтрока.Сумма              = СтрокаПредоплаты.Сумма;
		НоваяСтрока.СуммаВозврат       = СтрокаПредоплаты.СуммаВозврат;
		НоваяСтрока.СтавкаНДС          = СтрокаПредоплаты.СтавкаНДС;
	КонецЦикла;
	
	Для каждого СтрокаЗачетПредоплаты Из ТаблицыДанных.ЗачетПредоплаты Цикл
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.ЗачетАвансов.Добавить();
		
		НоваяСтрока.Контрагент         = РозничныйПокупательСсылка;
		НоваяСтрока.ДоговорКонтрагента = ОсновнойДоговорРозничныйПокупатель;
		НоваяСтрока.СуммаЗачета        = СтрокаЗачетПредоплаты.СуммаЗачета;
	КонецЦикла;
	
	ОтчетОРозничныхПродажахОбъект.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
	
	Если ОтчетОРозничныхПродажахОбъект.Товары.Итог("СуммаНДС") <> 0 
		ИЛИ ОтчетОРозничныхПродажахОбъект.Возвраты.Итог("СуммаНДС") <> 0 Тогда
		
		ОтчетОРозничныхПродажахОбъект.ДокументБезНДС = Ложь;
	КонецЕсли; 
	
	ОтчетОРозничныхПродажахОбъект.ДополнительныеСвойства.Вставить("НеВыводитьСообщенияПроверкиЗаполнения");
	
	Если ЕстьНесопоставленнаяНоменклатура 
		ИЛИ Не ОтчетОРозничныхПродажахОбъект.ПроверитьЗаполнение() Тогда
		
		ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Запись);
	Иначе
		Попытка
			// Проводим в попытке, чтобы при ошибке в процелуре проведения (например нет остатка товара) не останавливалась вся загрузка.
			ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецПопытки;
	КонецЕсли;
	
	Если ПараметрыУстройства.ЗагрузкаИзОФД 
		И НЕ ОтчетОРозничныхПродажахОбъект.Ссылка.Пустая() 
		И НоменклатураЧековДокументов.Количество() > 0 Тогда
		
		НоменклатураЧековДокументов.ЗаполнитьЗначения(ОтчетОРозничныхПродажахОбъект.Организация, "Организация");
		НоменклатураЧековДокументов.ЗаполнитьЗначения(ОтчетОРозничныхПродажахОбъект.Ссылка, "ДокументЗагрузки");
		
		НаборЗаписей = РегистрыСведений.НоменклатураДокументовОФД.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Организация.Установить(ОтчетОРозничныхПродажахОбъект.Организация);
		НаборЗаписей.Отбор.ДокументЗагрузки.Установить(ОтчетОРозничныхПродажахОбъект.Ссылка);
		
		НаборЗаписей.Загрузить(НоменклатураЧековДокументов);
		НаборЗаписей.Записать(); 
	КонецЕсли;
	
	// Для загрузки из ОФД план обмена не используется
	Если НЕ ПараметрыУстройства.ЗагрузкаИзОФД Тогда
		УзелОбъект = ПараметрыУстройства.УзелИнформационнойБазы.ПолучитьОбъект();
		УзелОбъект.ДатаЗагрузки = ТекущаяДатаСеанса();
		УзелОбъект.Записать();
	КонецЕсли;
	
	Если ОбновитьПовторноИспользуемыеЗначения Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	Возврат ОтчетОРозничныхПродажахОбъект.Ссылка;
КонецФункции

Функция СформироватьКомментарий(Устройство)
	
	СвойстваОборудования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Устройство, "ТипОфлайнОборудования, Наименование");
	
	Комментарий = СтрШаблон(НСтр("ru = 'Загружено из %1:%2'"), СвойстваОборудования.ТипОфлайнОборудования, СвойстваОборудования.Наименование);
	
	Возврат Комментарий;
	
КонецФункции

Функция НовыйТаблицаТоваров()
	ТаблицаТоваров = Новый ТаблицаЗначений;
	
	СписокТиповНоменклатура = Новый Массив;
	СписокТиповНоменклатура.Добавить(Тип("СправочникСсылка.Номенклатура"));
	СписокТиповНоменклатура.Добавить(Тип("УникальныйИдентификатор"));
	
	ТаблицаТоваров.Колонки.Добавить("ИдентификаторДокумента",           Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ИдентификаторСвязанногоДокумента", Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ИдентификаторСтроки",              Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ЭтоВозврат",                       Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("ЭтоПатент",                       Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("ДатаРеализации",                   Новый ОписаниеТипов("Дата"));
	ТаблицаТоваров.Колонки.Добавить("Код",                              Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Номенклатура",                     Новый ОписаниеТипов(СписокТиповНоменклатура));
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС",                        Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТаблицаТоваров.Колонки.Добавить("Количество",                       Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Сумма",                            Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("НомерФискальногоЧека",             Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаТоваров;
КонецФункции

Функция НовыйТаблицаОплат()
	ТаблицаОплат = Новый ТаблицаЗначений;
	
	ТаблицаОплат.Колонки.Добавить("ИдентификаторДокумента", Новый ОписаниеТипов("Строка"));
	ТаблицаОплат.Колонки.Добавить("ЭтоВозврат",             Новый ОписаниеТипов("Булево"));
	ТаблицаОплат.Колонки.Добавить("ЭтоПатент",              Новый ОписаниеТипов("Булево"));
	ТаблицаОплат.Колонки.Добавить("ВидОплаты",              Новый ОписаниеТипов("СправочникСсылка.ВидыОплатОрганизаций"));
	ТаблицаОплат.Колонки.Добавить("Сумма",                  Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаОплат;
КонецФункции

Функция НовыйТаблицаСертификаты()
	ТаблицаОплат = Новый ТаблицаЗначений;
	
	ТаблицаОплат.Колонки.Добавить("ЭтоПатент",              Новый ОписаниеТипов("Булево"));
	ТаблицаОплат.Колонки.Добавить("ВидОплаты",              Новый ОписаниеТипов("СправочникСсылка.ВидыОплатОрганизаций"));
	ТаблицаОплат.Колонки.Добавить("Сумма",                  Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаОплат;
КонецФункции

Функция НовыйТаблицаПредоплаты()
	ТаблицаОплат = Новый ТаблицаЗначений;
	
	ТаблицаОплат.Колонки.Добавить("ЭтоПатент",           Новый ОписаниеТипов("Булево"));
	ТаблицаОплат.Колонки.Добавить("Контрагент",          Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента",  Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ТаблицаОплат.Колонки.Добавить("Сумма",               Новый ОписаниеТипов("Число"));
	ТаблицаОплат.Колонки.Добавить("СуммаВозврат",        Новый ОписаниеТипов("Число"));
	ТаблицаОплат.Колонки.Добавить("СтавкаНДС",           Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	
	Возврат ТаблицаОплат;
КонецФункции

Функция НовыйТаблицаЗачетПредоплаты()
	ТаблицаОплат = Новый ТаблицаЗначений;
	
	ТаблицаОплат.Колонки.Добавить("ЭтоПатент",           Новый ОписаниеТипов("Булево"));
	ТаблицаОплат.Колонки.Добавить("Контрагент",          Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента",  Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ТаблицаОплат.Колонки.Добавить("СуммаЗачета",               Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаОплат;
КонецФункции

Функция ПолучитьТаблицуВидовОплат(Устройство)
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыВидовОплатыКММ.ВидОплаты КАК Ссылка,
	|	КодыВидовОплатыКММ.ВидОплаты.Наименование КАК Наименование,
	|	КодыВидовОплатыКММ.Код КАК Код,
	|	КодыВидовОплатыКММ.ВидОплаты.ТипОплаты КАК ТипОплаты
	|ИЗ
	|	РегистрСведений.КодыВидовОплатыКММ КАК КодыВидовОплатыКММ
	|ГДЕ
	|	КодыВидовОплатыКММ.ОфлайнОборудование = &Оборудование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код";
	
	Запрос.УстановитьПараметр("Оборудование", Устройство);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

#КонецОбласти
