#Область ПрограммныйИнтерфейс

// Возвращает параметры формы для формы взаимодействия с видом Письмо
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - данные формы документа Взаимодействие.
//   ЭлектроннаяПочтаКонтактногоЛица - Строка - адрес электронной почты, на который необходимо отправить письмо
//
// Возвращаемое значение:
//   Структура:
//       * ДанныеУчастника    - Структура - содержит 
//       * Тема               - Строка - тема взаимодействия.
//       * Текст              - Строка - описание результата взаимодействия.
//       * Получатель         - Строка - адрес электронной почты получателя.
//       * Контрагент         - СправочникСсылка.Контрагенты - контрагент, с которым регистрируется взаимодействие.
//       * КонтактноеЛицо     - СправочникСсылка.КонтактныеЛица - контактное лицо, участник взаимодействия.
//
Функция ПараметрыФормыНовогоПисьмаНаОсновании(Форма, ЭлектроннаяПочтаКонтактногоЛица) Экспорт
	
	Объект = Форма.Объект;
	Контакт = Объект.Предмет;
	
	ДанныеУчастника = Новый Структура;
	
	ДанныеУчастника.Вставить("Контакт", Контакт);
	ДанныеУчастника.Вставить("КакСвязаться", ЭлектроннаяПочтаКонтактногоЛица);
	ДанныеУчастника.Вставить("Представление", Строка(Контакт));
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ДанныеУчастника", ДанныеУчастника);
	ДокументыОснования = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка);
	ПараметрыСоздания.Вставить("ДокументыОснования", ДокументыОснования);
	ПараметрыСоздания.Вставить("Текст", Объект.Описание);
	
	ЗначенияЗаполнения = Новый Структура;
	ТемаСобытия = СтрШаблон(НСтр("ru = 'Результаты встречи от %1'"), Формат(Объект.Дата, "ДФ=dd.MM.yyyy"));
	ЗначенияЗаполнения.Вставить("Тема", ТемаСобытия);
	
	ИмяФормыВзаимодействия = "ФормаДокументаБП";

	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		ПредставлениеПолучателя = Строка(Объект.КонтактноеЛицо);
	Иначе
		ПредставлениеПолучателя = Строка(Объект.Контрагент);
	КонецЕсли;
	
	Получатель =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"%1 <%2>", ПредставлениеПолучателя, ЭлектроннаяПочтаКонтактногоЛица);
	ЗначенияЗаполнения.Вставить("Получатель", Получатель); 
	ЗначенияЗаполнения.Вставить("Контрагент", Объект.Контрагент);
	ЗначенияЗаполнения.Вставить("КонтактноеЛицо", Объект.КонтактноеЛицо);
	
	ЗначенияЗаполнения.Вставить("ИмяФормыВзаимодействия", ИмяФормыВзаимодействия);
	
	ПараметрыСоздания.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат ПараметрыСоздания;
	
КонецФункции

// Подготавливает параметры для создания взаимодействия Телефонный звонок,
// если создание звонка происходит из формы контрагента или контактного лица.
//
// Параметры:
//  КонтактнаяИнформация - Структура:
//       * Вид - СправочникСсылка.ВидыКонтактнойИнформации - Вид контактной информации
//       * Значение - Строка - параметры контактной информации в формате JSON
//       * Представление - Строка - представление контактной информации
//       * Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации Телефон
//  ДополнительныеПараметры - Структура:
//       * АсинхронныйВызов - Булево - тип вызова
//       * ВладелецКонтактнойИнформации - ОпределяемыйТип.ВладелецКонтактнойИнформации.
//       * ИмяРеквизита - Строка - имя реквизита на форме
//       * Форма - ФормаКлиентскогоПриложения - форма из которой была вызвана команда
// 
Процедура ПодготовитьПараметрыИПозвонить(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	Если ПустаяСтрока(КонтактнаяИнформация.Представление) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Для совершения звонка введите номер телефона.'"), , ДополнительныеПараметры.ИмяРеквизита);
	Иначе
		Если ТипЗнч(ДополнительныеПараметры.ВладелецКонтактнойИнформации) = Тип("СправочникСсылка.Контрагенты") Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Контрагент", ДополнительныеПараметры.ВладелецКонтактнойИнформации);
			ПараметрыФормы.Вставить("Телефон", КонтактнаяИнформация.Представление);
			СоздатьЗвонок(ПараметрыФормы);
		ИначеЕсли ТипЗнч(ДополнительныеПараметры.ВладелецКонтактнойИнформации) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("КонтактноеЛицо", ДополнительныеПараметры.ВладелецКонтактнойИнформации);
			ПараметрыФормы.Вставить("Телефон", КонтактнаяИнформация.Представление);
			СоздатьЗвонок(ПараметрыФормы);
		Иначе
			Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
				МобильныйКлиентБПКлиент.ПозвонитьПоТелефону(КонтактнаяИнформация.Представление);
			Иначе
				УправлениеКонтактнойИнформациейКлиент.ПозвонитьПоТелефону(КонтактнаяИнформация.Представление);
			КонецЕСли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает параметры для создания взаимодействия Электронное письмо исходящее,
// если создание письма происходит из формы контрагента или лида.
//
// Параметры:
//  КонтактнаяИнформация - Структура:
//       * Вид - СправочникСсылка.ВидыКонтактнойИнформации - Вид контактной информации
//       * Значение - Строка - параметры контактной информации в формате JSON
//       * Представление - Строка - представление контактной информации
//       * Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации Адрес электронной почты
//  ДополнительныеПараметры - Структура:
//       * АсинхронныйВызов - Булево - тип вызова
//       * ВладелецКонтактнойИнформации - ОпределяемыйТип.ВладелецКонтактнойИнформации.
//       * ИмяРеквизита - Строка - имя реквизита на форме
//       * Форма - ФормаКлиентскогоПриложения - форма из которой была вызвана команда
// 
Процедура ПодготовитьПараметрыИНаписатьПисьмо(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт

	Получатель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"%1 <%2>", Строка(ДополнительныеПараметры.ВладелецКонтактнойИнформации), КонтактнаяИнформация.Представление);
	
	Если ТипЗнч(ДополнительныеПараметры.ВладелецКонтактнойИнформации) = Тип("СправочникСсылка.Контрагенты") Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Контрагент", ДополнительныеПараметры.ВладелецКонтактнойИнформации);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("Получатель", Получатель);
		
		СоздатьПисьмо(ПараметрыФормы);
		
	ИначеЕсли ТипЗнч(ДополнительныеПараметры.ВладелецКонтактнойИнформации) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("КонтактноеЛицо", ДополнительныеПараметры.ВладелецКонтактнойИнформации);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("Получатель", Получатель);
		
		СоздатьПисьмо(ПараметрыФормы);
		
	Иначе
		ПараметрыПисьма = УправлениеКонтактнойИнформациейКлиент.ПараметрыПисьмаSMS();
		ПараметрыПисьма.Представление = КонтактнаяИнформация.Представление;
		ПараметрыПисьма.ОжидаемыйВид = КонтактнаяИнформация.Тип;
		ПараметрыПисьма.ИсточникКонтактнойИнформации = ДополнительныеПараметры.ВладелецКонтактнойИнформации;
		ПараметрыПисьма.ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита;
		УправлениеКонтактнойИнформациейКлиент.СоздатьЭлектронноеПисьмо("", ПараметрыПисьма);
	КонецЕсли;

КонецПроцедуры

// Создает взаимодействие Телефонный звонок.
//
// Параметры:
//  ПараметрыФормы - Структура - Содержит параметры заполнения данных
//
Процедура СоздатьЗвонок(ПараметрыФормы) Экспорт

	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие("ТелефонныйЗвонок", ПараметрыСозданияВзаимодействия(ПараметрыФормы));
	
КонецПроцедуры

// Создает взаимодействие Электронное письмо исходящее.
//
// Параметры:
//  ПараметрыФормы - Структура - Содержит параметры заполнения данных
//
Процедура СоздатьПисьмо(ПараметрыФормы) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьПисьмоНастройкаУчетнойЗаписиПредложена",
		ЭтотОбъект, ПараметрыСозданияВзаимодействия(ПараметрыФормы));
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

// Создает взаимодействие Встреча.
//
// Параметры:
//   ПараметрыФормы - Структура - Содержит параметры заполнения данных
//
Процедура СоздатьВстречу(ПараметрыФормы) Экспорт
	
	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие("Встреча", ПараметрыСозданияВзаимодействия(ПараметрыФормы));
	
КонецПроцедуры

// Создает Прочее взаимодействие.
//
// Параметры:
//   ПараметрыФормы - Структура - Содержит параметры заполнения данных
//
Процедура СоздатьПрочееВзаимодействие(ПараметрыФормы) Экспорт
	
	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие("ПрочееВзаимодействие", ПараметрыФормы);
	
КонецПроцедуры

// Проверяет регистрируется ли рассылка как взаимодействие
//
// Параметры:
//  Документ - ДокументСсылка.* - Ссылка на документ
//
// Возвращаемое значение:
// Булево
Функция РассылкаРегистрируетсяКакВзаимодействие(Документ) Экспорт
	
	ТипыДокументов = ТипыРассылаемыхДокументов();
	
	Возврат ТипыДокументов.Найти(ТипЗнч(Документ)) <> Неопределено;
	
КонецФункции

// Создает новое взаимодействие
// 
// Параметры:
//  ТипОбъекта        - Строка - тип создаваемого объекта.
//  ИмяФормы          - Строка - имя открываемой формы
//  ПараметрыСоздания - Структура - параметры создаваемого документа.
//  Форма             - ФормаКлиентскогоПриложения.
//
Процедура СоздатьНовоеВзаимодействие(ТипОбъекта, ИмяФормы = "ФормаДокумента", ПараметрыСоздания = Неопределено, Форма = Неопределено) Экспорт

	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие(ТипОбъекта, ПараметрыСоздания, Форма);

КонецПроцедуры

// Открывает форму выбора типа предмета
//
Процедура ПредметНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ЖурналДокументов.Взаимодействия.Форма.ВыборТипаПредметаБП", , Форма);
	
КонецПроцедуры

// См. ОтправкаSMSКлиентПереопределяемый.ПриОтправкеSMS
Процедура ПриОтправкеSMS(НомераПолучателей, Текст, ДополнительныеПараметры, СтандартнаяОбработка) Экспорт
	
	// Не используем документ СообщениеSMS
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОтправки = ПараметрыОтправкиSMS();
	ПараметрыОтправки.НомераПолучателей = НомераПолучателей;
	ПараметрыОтправки.Текст             = Текст;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОтправки, ДополнительныеПараметры);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьНовоеSMSПроверкаНастроекВыполнена", ЭтотОбъект, ПараметрыОтправки);
	ПроверитьНаличиеНастроекДляОтправкиSMS(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьПисьмоНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("Получатель")
		И ТипЗнч(ДополнительныеПараметры.Получатель) = Тип("Массив")
		И ДополнительныеПараметры.Получатель.Количество() > 0 
		И Не ДополнительныеПараметры.Свойство("ДанныеУчастника") Тогда
			Получатель = ДополнительныеПараметры.Получатель[0];
			ДанныеУчастника = Новый Структура;
			ДанныеУчастника.Вставить("Контакт", Получатель.ИсточникКонтактнойИнформации);
			ДанныеУчастника.Вставить("КакСвязаться", Получатель.Адрес);
			ДанныеУчастника.Вставить("Представление", Строка(Получатель.ИсточникКонтактнойИнформации));
			ДополнительныеПараметры.Вставить("ДанныеУчастника", ДанныеУчастника);
	КонецЕсли;
	
	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие("ЭлектронноеПисьмоИсходящее", ДополнительныеПараметры);
	
КонецПроцедуры

Функция ТипыРассылаемыхДокументов()
	
	ТипыДокументов = Новый Массив;
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КоммерческоеПредложение"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.СчетНаОплатуПокупателю"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	
	Возврат ТипыДокументов;
	
КонецФункции

Функция ПараметрыСозданияВзаимодействия(ПараметрыФормы)

	ДанныеУчастника = Новый Структура;
	
	Контакт = Неопределено;
	
	Если ПараметрыФормы.Свойство("Контрагент") Тогда
		Контакт = ПараметрыФормы.Контрагент;
	ИначеЕсли ПараметрыФормы.Свойство("КонтактноеЛицо") Тогда
		Контакт = ПараметрыФормы.КонтактноеЛицо;
	ИначеЕсли ПараметрыФормы.Свойство("ЗначенияЗаполнения") Тогда
		Если ПараметрыФормы.ЗначенияЗаполнения.Свойство("Контрагент") Тогда
			Контакт = ПараметрыФормы.ЗначенияЗаполнения.Контрагент;
		ИначеЕсли ПараметрыФормы.ЗначенияЗаполнения.Свойство("КонтактноеЛицо") Тогда
			Контакт = ПараметрыФормы.ЗначенияЗаполнения.КонтактноеЛицо;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеУчастника.Вставить("Контакт", Контакт);
	ДанныеУчастника.Вставить("Представление", ?(ЗначениеЗаполнено(Контакт), Строка(Контакт), ""));
	
	Если ПараметрыФормы.Свойство("ЭлектроннаяПочта") Тогда
		ДанныеУчастника.Вставить("КакСвязаться", ПараметрыФормы.ЭлектроннаяПочта);
	ИначеЕсли ПараметрыФормы.Свойство("Телефон") Тогда
		ДанныеУчастника.Вставить("КакСвязаться", ПараметрыФормы.Телефон);
	Иначе
		ДанныеУчастника.Вставить("КакСвязаться", "");
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ДанныеУчастника", ДанныеУчастника);
	
	Возврат ПараметрыФормы;
	
КонецФункции

Процедура ФормаОбработкаВыбора(Форма, ВыбранноеЗначение, ИсточникВыбора, КонтекстВыбора) Экспорт
	
	 Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ЖурналДокументов.Взаимодействия.Форма.ВыборТипаПредметаБП") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		
		Если Форма.ИмяФормы = "Документ.ЭлектронноеПисьмоИсходящее.Форма.ДокументыОснованияПисьма" Тогда
			КонтекстВыбора = "ВыборОснования";
		Иначе
			КонтекстВыбора = "ВыборПредмета";
		КонецЕсли;
		
		ОткрытьФорму(ВыбранноеЗначение + ".ФормаВыбора", ПараметрыФормы, Форма);
		
	ИначеЕсли КонтекстВыбора = "ВыборПредмета" Тогда
		
		Если ВзаимодействияКлиентСервер.ЯвляетсяПредметом(ВыбранноеЗначение)
			Или ВзаимодействияКлиентСервер.ЯвляетсяВзаимодействием(ВыбранноеЗначение) Тогда
		
			Форма.Предмет = ВыбранноеЗначение;
			Форма.Модифицированность = Истина;
		
		КонецЕсли;
		
		КонтекстВыбора = Неопределено;
	
	ИначеЕсли КонтекстВыбора = "ВыборОснования" Тогда
		
		Если ВзаимодействияКлиентСервер.ЯвляетсяПредметом(ВыбранноеЗначение)
			Или ВзаимодействияКлиентСервер.ЯвляетсяВзаимодействием(ВыбранноеЗначение) Тогда
			Индекс = Форма.Элементы.ДокументыОснования.ТекущаяСтрока;
			Строка = Форма.ДокументыОснования[Индекс];
			Строка.ДокументОснование = ВыбранноеЗначение;
		КонецЕсли;
		
		КонтекстВыбора = Неопределено;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеНастроекДляОтправкиSMS(ОбработчикРезультата)
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента();
	Если ПараметрыРаботыКлиента.ДоступнаОтправкаSMS Тогда
		ВыполнитьОбработкуОповещения(ОбработчикРезультата, Истина);
	Иначе
		Если ПользователиКлиент.ЭтоПолноправныйПользователь() Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеНастройкиSMS", ЭтотОбъект, ОбработчикРезультата);
			ОтправкаSMSКлиент.ОткрытьФормуНастроек(ОписаниеОповещения);
		Иначе
			ТекстСообщения = НСтр("ru = 'Для отправки SMS требуется произвести настройку параметров подключения
				|Обратитесь к администратору.'");
			ПоказатьПредупреждение(, ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеНастройкиSMS(Результат, ОбработчикРезультата) Экспорт
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента();
	Если ПараметрыРаботыКлиента.ДоступнаОтправкаSMS Тогда
		ВыполнитьОбработкуОповещения(ОбработчикРезультата, Истина);
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьНовоеSMSПроверкаНастроекВыполнена(ОтправкаSMSНастроена, ПараметрыОтправки) Экспорт
	
	Если Не ОтправкаSMSНастроена Тогда
		Возврат;
	КонецЕсли;
		
	ОткрытьФорму("ОбщаяФорма.ОтправкаSMS", ПараметрыОтправки);
	
КонецПроцедуры

Функция ПараметрыОтправкиSMS()
	
	Результат = Новый Структура;
	Результат.Вставить("НомераПолучателей", "");
	Результат.Вставить("Текст", "");
	Результат.Вставить("ИмяОтправителя", "");
	Результат.Вставить("ПеревестиВТранслит", Ложь);
	Результат.Вставить("Предмет", Неопределено);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти