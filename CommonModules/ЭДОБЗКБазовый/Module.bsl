#Область СлужебныеПроцедурыИФункции

Функция ТипыОбъектовСоСтороннимиПечатнымиФормами() Экспорт
	
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.Увольнение"));
	
	Возврат ТипыОбъектов;
	
КонецФункции

Процедура ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек) Экспорт
	
	ЭДОБЗК.ДобавитьНастройкуПечатнойФормы(
		ОписанияНастроек,
		Документы.СогласиеНаПрисоединениеКЭДОБЗК.ИдентификаторКомандыПечатиЗаявления(),
		НСтр("ru = 'Заявление на согласие'"),
		Перечисления.СодержимоеДокументов.НеСодержитЗарплаты,
		,
		,
		Перечисления.ВариантыПодписанияДокументовЭДОБЗК.НеобходимаСобственноручнаяПодпись);
	
	ЭлектронныеТрудовыеКнижки.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	УчетНДФЛДокументы.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	КадровыйУчет.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	УчетПособийСоциальногоСтрахования.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций") Тогда
		МодульЗарплатаКадрыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыДляНебольшихОрганизаций");
		МодульЗарплатаКадрыДляНебольшихОрганизаций.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	КонецЕсли;
	
	БизнесПроцессыЗаявокСотрудников.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	ЗаявкиСотрудников.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	
КонецПроцедуры

Процедура ДокументЭДОБЗКФормаСпискаПриСозданииНаСервере(УправляемаяФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	ТекстЗапроса = УправляемаяФорма.Список.ТекстЗапроса;
	ПозицияСкобки = СтрНайти(ТекстЗапроса, "}", НаправлениеПоиска.СКонца);
	Если ПозицияСкобки > 0 Тогда
		ТекстЗапроса = Лев(ТекстЗапроса, ПозицияСкобки - 1);
		ТекстЗапроса = ТекстЗапроса + ",
			|	(ИСТИНА В
			|		(ВЫБРАТЬ ПЕРВЫЕ 1
			|			ИСТИНА
			|		ИЗ
			|			РегистрСведений.СоставДокументовЗарплатаКадры КАК СоставДокументовЗарплатаКадры
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудниковИнтервальный КАК КадроваяИсторияСотрудниковИнтервальный
			|				ПО
			|					СоставДокументовЗарплатаКадры.ФизическоеЛицо = КадроваяИсторияСотрудниковИнтервальный.ФизическоеЛицо
			|		ГДЕ
			|			КадроваяИсторияСотрудниковИнтервальный.Подразделение В ИЕРАРХИИ (&Подразделение)
			|			И ДокументДокументЭДОБЗК.Дата МЕЖДУ КадроваяИсторияСотрудниковИнтервальный.ДатаНачала И КадроваяИсторияСотрудниковИнтервальный.ДатаОкончания
			|			И ДокументДокументЭДОБЗК.Ссылка = СоставДокументовЗарплатаКадры.ДокументФизическогоЛица))}";
		УправляемаяФорма.Список.ТекстЗапроса = ТекстЗапроса;
	КонецЕсли;
	
КонецПроцедуры

#Область УправлениеПечатью

// См. УправлениеПечатьюМультиязычностьПереопределяемый.ПриОпределенииДоступныхДляПереводаМакетов
Процедура ПриОпределенииДоступныхДляПереводаМакетов(Макеты) Экспорт
	
	Макеты.Добавить(Метаданные.Документы.ДокументЭДОБЗК.Макеты.ПФ_MXL_ПустойБланкДокументаЭДОБЗК);
	Макеты.Добавить(Метаданные.Документы.СогласиеНаПрисоединениеКЭДОБЗК.Макеты.ПФ_MXL_СогласиеНаПрисоединениеКЭДОБЗК_БумажныйДокумент);
	Макеты.Добавить(Метаданные.Документы.СогласиеНаПрисоединениеКЭДОБЗК.Макеты.ПФ_MXL_СогласиеНаПрисоединениеКЭДОБЗК_УНЭП);
	Макеты.Добавить(Метаданные.Отчеты.ДокументыЭДОБЗКСотрудников.Макеты.ПФ_MXL_КарточкаЭлектронногоДокумента);
	Макеты.Добавить(Метаданные.Отчеты.ШаблоныДокументовКабинетСотрудника.Макеты.ПФ_MXL_ШаблонДокументаКабинетСотрудника);
	
КонецПроцедуры

#КонецОбласти

Функция ВидДокументаПоОснованиюИРеквизитам(ОснованиеСсылка, РеквизитыОбъекта) Экспорт
	
	ВидДокумента = Неопределено;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РеквизитыОбъекта, "ОтменяемаяЗаявка")
		И ЗначениеЗаполнено(РеквизитыОбъекта.ОтменяемаяЗаявка) Тогда
		
		ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаОтмену");
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РеквизитыОбъекта, "ШаблонДокумента")
		И ЗначениеЗаполнено(РеквизитыОбъекта.ШаблонДокумента) Тогда
		
		ШаблонДокумента = РеквизитыОбъекта.ШаблонДокумента;
	ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаСправка2НДФЛКабинетСотрудника") Тогда
		ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗапросСправки2НДФЛ");
	ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаНалоговыеВычетыКабинетСотрудника") Тогда
		ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаНалоговыеВычеты");
	ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаОтпускКабинетСотрудника") Тогда
		ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаОтпуск");
	ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаИзменениеЛичныхДанныхКабинетСотрудника") Тогда
		ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеОбИзмененииЛичныхДанных");
	ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаОтсутствиеКабинетСотрудника") Тогда
		ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.Отсутствие");
	ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаОтпускБезОплатыКабинетСотрудника") Тогда
		ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.Отсутствие");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонДокумента) Тогда
		Если ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаОтсутствиеКабинетСотрудника") Тогда
			ВидДокумента = Справочники.ВидыДокументовЭДОБЗК.ВидДокументаПоПричинеОтсутствия(РеквизитыОбъекта.ПричинаОтсутствия);
		ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаОтпускБезОплатыКабинетСотрудника") Тогда
			ВидДокумента = Справочники.ВидыДокументовЭДОБЗК.ВидДокументаПоПричинеОтсутствия(
				Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет);
		Иначе
			ВидДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ШаблонДокумента, "ВидДокумента");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВидДокумента;
	
КонецФункции

Процедура УстановитьЗапросДинамическогоСпискаФормыВыбораПубликуемыхСотрудников(УправляемаяФорма) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	МИНИМУМ(ДокументЭДОБЗК.Ссылка) КАК Ссылка,
			|	ДокументЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо
			|ПОМЕСТИТЬ ВТПоследниеДокументы
			|ИЗ
			|	Документ.ДокументЭДОБЗК.ВнешниеПодписанты КАК ДокументЭДОБЗК
			|ГДЕ
			|	ДокументЭДОБЗК.Ссылка.ВидДокумента = &ВидДокумента
			|	И ДокументЭДОБЗК.Ссылка.Дата >= &ДатаПоследнегоДокумента
			|	И НЕ ДокументЭДОБЗК.Ссылка.ПометкаУдаления
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументЭДОБЗК.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КадроваяИстория.Сотрудник КАК Сотрудник,
			|	КадроваяИстория.ФизическоеЛицо КАК ФизическоеЛицо,
			|	КадроваяИстория.Подразделение КАК Подразделение,
			|	КадроваяИстория.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	КадроваяИстория.Организация КАК Организация,
			|	КадроваяИстория.Сотрудник.Наименование КАК Наименование,
			|	КадроваяИстория.Должность КАК Должность,
			|	ВЫБОР
			|		КОГДА КадроваяИстория.Сотрудник В (&РанееВыбранные)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК РанееВыбран,
			|	0 КАК ИндексКартинки
			|ИЗ
			|	РегистрСведений.КадроваяИсторияСотрудниковИнтервальный КАК КадроваяИстория
			|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъекты1СПерсонал КАК ВыгружаемыеОбъекты1СПерсонал
			|		ПО КадроваяИстория.ФизическоеЛицо = ВыгружаемыеОбъекты1СПерсонал.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъектыКабинетСотрудника
			|		ПО КадроваяИстория.ФизическоеЛицо = ВыгружаемыеОбъектыКабинетСотрудника.Ссылка}
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоследниеДокументы КАК ПоследниеДокументы
			|		ПО КадроваяИстория.ФизическоеЛицо = ПоследниеДокументы.ФизическоеЛицо
			|ГДЕ
			|	&ДатаДокумента МЕЖДУ КадроваяИстория.ДатаНачала И КадроваяИстория.ДатаОкончания
			|	И ВЫБОР
			|			КОГДА ВыгружаемыеОбъекты1СПерсонал.Ссылка ЕСТЬ NULL
			|					И ВыгружаемыеОбъектыКабинетСотрудника.Ссылка ЕСТЬ NULL
			|				ТОГДА ЛОЖЬ
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ
			|	И КадроваяИстория.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.ПустаяСсылка)
			|	И КадроваяИстория.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
			|	И ПоследниеДокументы.Ссылка ЕСТЬ NULL";
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	МИНИМУМ(ДокументЭДОБЗК.Ссылка) КАК Ссылка,
			|	ДокументЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо
			|ПОМЕСТИТЬ ВТПоследниеДокументы
			|ИЗ
			|	Документ.ДокументЭДОБЗК.ВнешниеПодписанты КАК ДокументЭДОБЗК
			|ГДЕ
			|	ДокументЭДОБЗК.Ссылка.ВидДокумента = &ВидДокумента
			|	И ДокументЭДОБЗК.Ссылка.Дата >= &ДатаПоследнегоДокумента
			|	И НЕ ДокументЭДОБЗК.Ссылка.ПометкаУдаления
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументЭДОБЗК.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДанныеДляПодбораСотрудников.Сотрудник КАК Сотрудник,
			|	ДанныеДляПодбораСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ДанныеДляПодбораСотрудников.ТекущееПодразделение КАК Подразделение,
			|	ДанныеДляПодбораСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ДанныеДляПодбораСотрудников.ТекущаяОрганизация КАК Организация,
			|	ДанныеДляПодбораСотрудников.Сотрудник.Наименование КАК Наименование,
			|	ДанныеДляПодбораСотрудников.ТекущаяДолжность КАК Должность,
			|	ВЫБОР
			|		КОГДА ДанныеДляПодбораСотрудников.Сотрудник В (&РанееВыбранные)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК РанееВыбран,
			|	0 КАК ИндексКартинки
			|ИЗ
			|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ДанныеДляПодбораСотрудников
			|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъекты1СПерсонал КАК ВыгружаемыеОбъекты1СПерсонал
			|		ПО ДанныеДляПодбораСотрудников.ФизическоеЛицо = ВыгружаемыеОбъекты1СПерсонал.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъектыКабинетСотрудника
			|		ПО ДанныеДляПодбораСотрудников.ФизическоеЛицо = ВыгружаемыеОбъектыКабинетСотрудника.Ссылка}
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоследниеДокументы КАК ПоследниеДокументы
			|		ПО ДанныеДляПодбораСотрудников.ФизическоеЛицо = ПоследниеДокументы.ФизическоеЛицо
			|ГДЕ
			|	&ДатаДокумента МЕЖДУ ДанныеДляПодбораСотрудников.ДатаПриема И ВЫБОР
			|			КОГДА ДанныеДляПодбораСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
			|				ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
			|			ИНАЧЕ ДанныеДляПодбораСотрудников.ДатаУвольнения
			|		КОНЕЦ
			|	И ВЫБОР
			|			КОГДА ВыгружаемыеОбъекты1СПерсонал.Ссылка ЕСТЬ NULL
			|					И ВыгружаемыеОбъектыКабинетСотрудника.Ссылка ЕСТЬ NULL
			|				ТОГДА ЛОЖЬ
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ
			|	И ПоследниеДокументы.Ссылка ЕСТЬ NULL";
	КонецЕсли;
	
	УправляемаяФорма.Сотрудники.ТекстЗапроса = ТекстЗапроса;
	
КонецПроцедуры

#КонецОбласти
