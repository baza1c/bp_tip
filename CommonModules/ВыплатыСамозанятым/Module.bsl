#Область ПрограммныйИнтерфейс

// Процедура заполняет табличную часть документа из файла в фоне
//
// Параметры:
//    Параметры - Структура - Структура параметров с обязательными ключами:
//                * ЗагруженныеДанные - ТаблицаЗначений - Таблица загруженных данных из файла
//                * РеестрВыплат - ДокументСсылка.ВыплатыСамозанятым - Документ, в который загружаем информацию
//    АдресХранилища - Строка
//
Процедура ЗаполнитьТабличнуюЧасть(Параметры, АдресХранилища) Экспорт
	
	Если Параметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект = Параметры.РеестрВыплат.ПолучитьОбъект();
	
	ЗаполнитьЧекиСамозанятыхИзЗагруженныхДанных(ДокументОбъект, Параметры.ЗагруженныеДанные);
	
	Попытка
		ДокументОбъект.Записать();
	Исключение
		ТекстИсключенияЗаписи = НСтр("ru = 'Не удалось загрузить чеки самозанятых из файла. 
			|Возможно, документ открыт или редактируется другим пользователем'");
		ВызватьИсключение ТекстИсключенияЗаписи;
	КонецПопытки;
	
КонецПроцедуры

// Выполняет сопоставление контрагентов по данным из файла и контрагентов из табличной части документа "Выплаты самозанятым".
//
// Параметры:
//   ДанныеИзФайла - ТаблицаЗначений - Таблица загруженных данных из файла.
//   ИсходныйМассивКонтрагентов - Массив - Контрагенты из табличной части документа "Выплаты самозанятым".
//
Процедура ОпределитьКонтрагентовПоДаннымФайла(ДанныеИзФайла, ИсходныйМассивКонтрагентов) Экспорт
	
	// Определим контрагентов по данным из файла
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсходныйМассивКонтрагентов", ИсходныйМассивКонтрагентов);
	Запрос.УстановитьПараметр("ВТ_ДанныеИзФайла", ДанныеИзФайла);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Ссылка,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
	|	Контрагенты.Наименование КАК Наименование
	|ПОМЕСТИТЬ ИсходныеКонтрагенты
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка В(&ИсходныйМассивКонтрагентов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеИзФайла.ИНН КАК ИНН,
	|	ДанныеИзФайла.ПолноеНаименование КАК ПолноеНаименование
	|ПОМЕСТИТЬ ТаблицаДанныеИзФайла
	|ИЗ
	|	&ВТ_ДанныеИзФайла КАК ДанныеИзФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанныеИзФайла.ИНН КАК ИНН,
	|	ТаблицаДанныеИзФайла.ПолноеНаименование КАК ПолноеНаименование,
	|	ИсходныеКонтрагентыПоИНН.Ссылка КАК КонтрагентПоИНН,
	|	ИсходныеКонтрагентыПоПолномуНаименованию.Ссылка КАК КонтрагентПоПолномуНаименованию,
	|	ИсходныеКонтрагентыПоНаименованию.Ссылка КАК КонтрагентПоНаименованию
	|ИЗ
	|	ТаблицаДанныеИзФайла КАК ТаблицаДанныеИзФайла
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсходныеКонтрагенты КАК ИсходныеКонтрагентыПоИНН
	|		ПО ТаблицаДанныеИзФайла.ИНН = ИсходныеКонтрагентыПоИНН.ИНН
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсходныеКонтрагенты КАК ИсходныеКонтрагентыПоПолномуНаименованию
	|		ПО ТаблицаДанныеИзФайла.ПолноеНаименование = ИсходныеКонтрагентыПоПолномуНаименованию.НаименованиеПолное
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсходныеКонтрагенты КАК ИсходныеКонтрагентыПоНаименованию
	|		ПО ТаблицаДанныеИзФайла.ПолноеНаименование = ИсходныеКонтрагентыПоНаименованию.Наименование";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не (ЗначениеЗаполнено(Выборка.КонтрагентПоИНН) 
			Или ЗначениеЗаполнено(Выборка.КонтрагентПоПолномуНаименованию)
			Или ЗначениеЗаполнено(Выборка.КонтрагентПоНаименованию)) Тогда
			// Ничего не нашли
			Продолжить;
		КонецЕсли;
		
		СтрокиДанных = ДанныеИзФайла.НайтиСтроки(Новый Структура("ИНН, ПолноеНаименование", Выборка.ИНН, Выборка.ПолноеНаименование));
		// Строк может быть несколько
		Для Каждого СтрокаДанных Из СтрокиДанных Цикл
			Если ЗначениеЗаполнено(Выборка.КонтрагентПоИНН) Тогда
				СтрокаДанных.Контрагент = Выборка.КонтрагентПоИНН;
			ИначеЕсли ЗначениеЗаполнено(Выборка.КонтрагентПоПолномуНаименованию) Тогда
				СтрокаДанных.Контрагент = Выборка.КонтрагентПоПолномуНаименованию;
			Иначе
				СтрокаДанных.Контрагент = Выборка.КонтрагентПоНаименованию;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Отложенный обработчик обновления, меняет в документах СписаниеСРасчетногоСчета и ПлатежноеПоручение
// неиспользуемый вид операции "ВыплатаСамозанятому" на "ВыплатыСамозанятым"
//
// Параметры:
//  ПараметрыОбработчика - Структура:
//    * ОбработкаЗавершена - Булево - для того чтобы обработчик был вызван повторно для обработки
//    следующей порции данных, следует записать в него значение Ложь
//    * Курсор - МоментВремени - момент времени документа, на котором завершилась прошлая итерация обработчика
//    В структуру можно добавить произвольное количество свойств произвольных типов,
//    значения которых будут автоматически запоминаться между вызовами процедуры - обработчика отложенного обновления.
//    
Процедура ЗаменитьВидОперацииВыплатаСамозанятому(ПараметрыОбработчика) Экспорт
	
	Курсор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОбработчика, "Курсор", Неопределено);
	
	ШаблонТекстаЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ВложенныйЗапрос.Ссылка КАК Ссылка,
		|	ВложенныйЗапрос.МоментВремени КАК МоментВремени
		|ИЗ
		|	(ВЫБРАТЬ
		|		СписаниеСРасчетногоСчета.Ссылка,
		|		СписаниеСРасчетногоСчета.МоментВремени
		|	ИЗ
		|		Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
		|	ГДЕ
		|		СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.УдалитьВыплатаСамозанятому)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ПлатежноеПоручение.Ссылка,
		|		ПлатежноеПоручение.МоментВремени
		|	ИЗ
		|		Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		|	ГДЕ
		|		ПлатежноеПоручение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.УдалитьВыплатаСамозанятому)) КАК
		|		ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка УБЫВ
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Если Не ЗначениеЗаполнено(Курсор) Тогда
		ТекстЗапроса = ШаблонТекстаЗапроса;
	Иначе
		СхемаЗапроса = СхемыЗапросов.Создать(ШаблонТекстаЗапроса);
		ОсновнойОператор = СхемыЗапросов.НайтиТаблицуПоПсевдониму(СхемаЗапроса, "ВложенныйЗапрос").Оператор;
		ОсновнойОператор.Отбор.Добавить("ВложенныйЗапрос.МоментВремени < &Курсор");
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Курсор", Курсор);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	ИмяПроцедурыОбновления = НСтр("ru = 'ВыплатыСамозанятым.ЗаменитьВидОперацииВыплатаСамозанятому'");
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ПараметрыОбработчика.Вставить("Курсор", Выборка.МоментВремени);
		ОбъектовОбработано = ОбъектовОбработано + 1;
		БлокировкаДанных = Новый БлокировкаДанных;
		МетаданныеДокумента = Выборка.Ссылка.Метаданные();
		БлокируемаяТаблица = СтрШаблон("Документ.%1", МетаданныеДокумента.Имя);
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(БлокируемаяТаблица);
		ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		НачатьТранзакцию();
		Попытка
			БлокировкаДанных.Заблокировать();
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();

		Исключение
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ при выполнении процедуры %1 по причине:
					|%2'"),
					ИмяПроцедурыОбновления,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеДокумента,
				,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре %1 не удалось обработать документы:
				|в %2 из %3 возникли ошибки'"),
			ИмяПроцедурыОбновления,
			ПроблемныхОбъектов,
			ОбъектовОбработано);
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедура %1 обработала очередную порцию данных. Количество обработанных документов: %2'"),
			ИмяПроцедурыОбновления,
			ОбъектовОбработано);
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			,
			,
			ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак того, что оплачивается весь реестр выплат самозанятым.
// Используется для обмена с другими прикладными решениями.
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
//
// Возвращаемое значение:
// Булево - Истина, если оплачивается весь реестр,
//          Ложь, если выплата производится отдельному самозанятому
//
Функция ОплачиваетсяВесьРеестр(Контрагент) Экспорт
	
	ВидКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ЮридическоеФизическоеЛицо");
	Если ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроведениеДокумента

Функция ПодготовитьПараметрыВыплатыСамозанятым(ТаблицаВыплатыСамозанятым, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"                        // <Дата> - период движений - дата документа
	+ "Регистратор,"                   // <ДокументСсылка>
	+ "Содержание,"                    // <Строка>
	+ "Подразделение,"                 // <Ссылка на справочник подразделений>  - подразделение, задолженность по которому погашается
	+ "СтатьяДвиженияДенежныхСредств," // <СправочникСсылка.СтатьиДвиженияДенежныхСредств>
	+ "БанковскийСчет,"                // <СправочникСсылка.БанковскиеСчета>
	+ "СчетБанк,"                      // <ПланСчетовСсылка.Хозрасчетный> - счет учета денежных средств
	+ "РеестрВыплатСамозанятым,"       // <ДокументСсылка.ВыплатыСамозанятым> - реестр, по которому производится выплата
	+ "Организация";                   // <СправочникСсылка.Организации>
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.ВыплатыСамозанятым
	
	СписокОбязательныхКолонок = ""
	+ "Подразделение,"                 // <Ссылка на справочник подразделений>  - подразделение, задолженность по которому погашается
	+ "Самозанятый,"                   // <СправочникСсылка.Контрагенты> - контрагент, которому производится выплата
	+ "Сумма";                         // <Число,15,2> - сумма выплаты в валюте документа
	
	Параметры.Вставить("ВыплатыСамозанятым", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВыплатыСамозанятым, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Процедура СформироватьДвиженияВыплатыСамозанятым(ТаблицаВыплатыСамозанятым, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаВыплатыСамозанятым) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыВыплатыСамозанятым(ТаблицаВыплатыСамозанятым, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Для Каждого СтрокаВыплаты Из Параметры.ВыплатыСамозанятым Цикл
		Проводка = Движения.Хозрасчетный.Добавить();
		
		Проводка.Период       = Реквизиты.Период;
		Проводка.Организация  = Реквизиты.Организация;
		Проводка.Содержание   = Реквизиты.Содержание;
		Проводка.Сумма = СтрокаВыплаты.Сумма;
		
		Проводка.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыССамозанятыми;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка["СубконтоДт"],
			"Контрагенты", СтрокаВыплаты.Самозанятый);
		СвойстваСчетаРасчетов = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка["СчетДт"]);
		Если СвойстваСчетаРасчетов.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаВыплаты.Подразделение;
		КонецЕсли;
		
		Проводка.СчетКт = Реквизиты.СчетБанк;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"БанковскиеСчета", Реквизиты.БанковскийСчет);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СтатьиДвиженияДенежныхСредств", Реквизиты.СтатьяДвиженияДенежныхСредств);
		СвойстваКорСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваКорСчета.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = Реквизиты.Подразделение;
		КонецЕсли;
		
		НалоговыйУчет.ЗаполнитьНалоговыеСуммыПроводки(СтрокаВыплаты.Сумма, СтрокаВыплаты.Сумма, , , , , Проводка);
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьУстановитьСостояниеДокументаПриОплате(ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Регистратор,"               // <ДокументСсылка>
		+ "Организация,"               // <СправочникСсылка.Организации>
		+ "РеестрВыплатСамозанятым,"   // <ДокументСсылка.ВыплатыСамозанятым> - реестр, по которому производятся выплаты
		+ "Сумма";                     // <Сумма> - Сумма оплаты
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Процедура УстановитьСостояниеДокументаПриОплате(ТаблицаРеквизитыВыплаты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизитыВыплаты) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьУстановитьСостояниеДокументаПриОплате(ТаблицаРеквизитыВыплаты);
	Реквизиты = Параметры.Реквизиты[0];
	
	МассивДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Реквизиты.РеестрВыплатСамозанятым);
	КоллекцияСтатусов = РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокументов(МассивДокументов);
	ТекущийСтатус = ?(КоллекцияСтатусов[Реквизиты.РеестрВыплатСамозанятым] <> Неопределено, 
		КоллекцияСтатусов[Реквизиты.РеестрВыплатСамозанятым].Статус,
		Неопределено);
	Если ТекущийСтатус <> Перечисления.СтатусыДокументовВыплатыСамозанятым.ЧекиЗагружены 
		И ТекущийСтатус <> Перечисления.СтатусыДокументовВыплатыСамозанятым.ЧекиЧастичноЗагружены
		И ТекущийСтатус <> Перечисления.СтатусыДокументовВыплатыСамозанятым.Оплачено Тогда
		Если РеестрПолностьюОплачен(
			Реквизиты.РеестрВыплатСамозанятым, Реквизиты.Организация, Реквизиты.Сумма, Реквизиты.Регистратор) Тогда
				НовыйСтатус = Перечисления.СтатусыДокументовВыплатыСамозанятым.Оплачено;
			Иначе
				НовыйСтатус = Перечисления.СтатусыДокументовВыплатыСамозанятым.ЧастичноОплачено;
			КонецЕсли;
			НовыеСтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
			НовыеСтатусыДокумента.Статус = НовыйСтатус;
			РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(
				Реквизиты.РеестрВыплатСамозанятым,
				НовыеСтатусыДокумента);
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистру ЗадолженностьСамозанятыхПоРеестру
//
// Параметры:
//   ТаблицаВыплаты - ТаблицаЗначений - описание см. в ПодготовитьПараметрыЗадолженностьСамозанятыхПоРеестру()
//   ТаблицаРеквизитов - ТаблицаЗначений - описание см. в ПодготовитьПараметрыЗадолженностьСамозанятыхПоРеестру()
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - флаг отказа от записи движений
//
Процедура СформироватьДвиженияЗадолженностиСамозанятыхПоРеестру(ТаблицаВыплаты, ТаблицаРеквизитов, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизитов)
	 Или Не ЗначениеЗаполнено(ТаблицаВыплаты) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыЗадолженностьСамозанятыхПоРеестру(ТаблицаВыплаты, ТаблицаРеквизитов);
	Реквизиты = Параметры.Реквизиты[0];
	
	Движения.ЗадолженностьСамозанятыхПоРеестру.Записывать = Истина;
	
	Для Каждого СтрокаВыплаты Из Параметры.Выплаты Цикл
		Если СтрокаВыплаты.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		НовоеДвижение = Движения.ЗадолженностьСамозанятыхПоРеестру.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, СтрокаВыплаты);
		ЗаполнитьЗначенияСвойств(НовоеДвижение, Реквизиты);
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьПараметрыЗадолженностьСамозанятыхПоРеестру(ТаблицаВыплаты, ТаблицаРеквизитов)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Выплаты
	
	СписокОбязательныхКолонок = ""
	    + "ВидДвижения,"              // <ВидДвиженияНакопления> - Приход или Расход
		+ "РеестрВыплатыСамозанятым," // <ДокументСсылка.ВыплатыСамозанятым> - Реестр выплаты самозанятым
		+ "Самозанятый,"              // <СправочникСсылка.Контрагенты> - самозанятой, задолженность которому начисляется
		+ "СчетСамозанятого,"         // <СправочникСсылка.БанковскийСчет> - Банковский счет, по которому начисляем задолженность
		+ "Сумма";                    // <Число, 15, 2> - сумма оплаты в валюте счета включая НДС
		
	Параметры.Вставить("Выплаты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВыплаты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Период,"      // <Дата> - период движений - дата документа
		+ "Регистратор," // <ДокументСсылка.*> - документ-регистратор движений
		+ "Организация"; // <СправочникСсылка.Организации> - организация
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

Процедура ЗаполнитьЧекиСамозанятыхИзЗагруженныхДанных(Документ, ЗагруженныеДанные) Экспорт
	
	Для Каждого СтрокаДанных Из ЗагруженныеДанные Цикл
		
		Если СтрокаДанных.НомерСтрокиТабличнойЧасти = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВыплаты = Документ.Выплаты[СтрокаДанных.НомерСтрокиТабличнойЧасти - 1];
			
		СтрокаВыплаты.НомерЧека = ИнтеграцияСПлатформойСамозанятыеБП.НомерЧекаИзСсылки(СтрокаДанных.СсылкаНаЧек);
		СтрокаВыплаты.СсылкаНаЧек = СтрокаДанных.СсылкаНаЧек;
			
		СтрокаВыплаты.ЧекСамозанятого = ИнтеграцияСПлатформойСамозанятыеБП.ЧекСамозанятогоИзИнтернета(
			СтрокаДанных.СсылкаНаЧек, СтрокаВыплаты.ЧекСамозанятого, Документ.Ссылка);
		
		Если ЗагруженныеДанные.Колонки.Найти("Результат") <> Неопределено Тогда 
			
			Если НРег(СокрЛП(СтрокаДанных.Результат)) = НРег("НеЗачисленоНеКорректныеРеквизитыСчета") Тогда
				СтрокаВыплаты.ПричинаОтказа = Перечисления.ПричиныОтказаВЧекахВыплатСамозанятым.НеКорректныеРеквизитыСчета;
			ИначеЕсли НРег(СокрЛП(СтрокаДанных.Результат)) = НРег("НеЗачисленоПолучательНеЯвляетсяСамозанятым") Тогда
				СтрокаВыплаты.ПричинаОтказа = Перечисления.ПричиныОтказаВЧекахВыплатСамозанятым.ПолучательНеЯвляетсяСамозанятым;
			ИначеЕсли (НРег(СокрЛП(СтрокаДанных.Результат)) <> НРег("Зачислено"))
				И (НРег(СокрЛП(СтрокаДанных.Результат)) <> НРег("ЗачисленоСсылкаНеСформирована")) Тогда
				СтрокаВыплаты.ПричинаОтказа = Перечисления.ПричиныОтказаВЧекахВыплатСамозанятым.ПрочиеПричины;
			КонецЕсли;
			
		КонецЕсли;
		Если ЗагруженныеДанные.Колонки.Найти("РасшифровкаРезультата") <> Неопределено Тогда 
			
			СтрокаВыплаты.РасшифровкаРезультата = СтрокаДанных.РасшифровкаРезультата;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РеестрПолностьюОплачен(Реестр, Организация, Сумма, Регистратор)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Реестр);
	Запрос.УстановитьПараметр("Период", Новый Граница(Регистратор.МоментВремени(), ВидГраницы.Исключая));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадолженностьСамозанятыхПоРееструОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ЗадолженностьСамозанятыхПоРеестру.Остатки(
	|			&Период,
	|			Организация = &Организация
	|				И РеестрВыплатыСамозанятым = &Ссылка) КАК ЗадолженностьСамозанятыхПоРееструОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	РеестрПолностьюОплачен = Ложь;
	Если Выборка.Следующий() Тогда
		// При проведении еще не учтена сумма этого документа,
		// поэтому вычтем сумму документа из остатка
		Если Выборка.СуммаОстаток - Сумма <= 0 Тогда
			РеестрПолностьюОплачен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РеестрПолностьюОплачен;
	
КонецФункции 

Функция ФИОСамозанятых() Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.ФИО КАК ФИО
	|ИЗ
	|	(ВЫБРАТЬ
	|		Контрагенты.Наименование КАК ФИО
	|	ИЗ
	|		Справочник.Контрагенты КАК Контрагенты
	|	ГДЕ
	|		Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|		И НЕ Контрагенты.ПометкаУдаления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Контрагенты.НаименованиеПолное
	|	ИЗ
	|		Справочник.Контрагенты КАК Контрагенты
	|	ГДЕ
	|		Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|		И НЕ Контрагенты.ПометкаУдаления) КАК ВложенныйЗапрос";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ФИО) Тогда
			Результат.Добавить(СокрЛП(Выборка.ФИО));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти