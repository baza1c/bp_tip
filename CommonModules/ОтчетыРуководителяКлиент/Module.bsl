#Область СлужебныйПрограммныйИнтерфейс

Процедура УправлениеВидимостьюПоляОтветственный(Форма, ПолеОтветственныйДоступно) Экспорт
	
	Отчет = Форма.Отчет;
	Элементы = Форма.Элементы;
	
	ПолеОтветственный = Новый ПолеКомпоновкиДанных("Ответственный");
	
 	ДобавитьОтборОтветственный = ПолеОтветственныйДоступно;
	
	Для Каждого ЭлементОтбора Из Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = ПолеОтветственный Тогда
			Если ПолеОтветственныйДоступно Тогда
				Если ЭлементОтбора.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ Тогда
					ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
				КонецЕсли;
				ДобавитьОтборОтветственный = Ложь;
			Иначе
				// При изменении режима отображения элемента отбора, сбрасывается состояние табличного
				// поля "Результат" в значение "Неактуальность".
				// Запомним значение состояния и восстановим его после установки режима отображения.
				ДополнительныйРежимОтображенияЗначение = Строка(Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения);
				ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
				ЭлементОтбора.Использование = Ложь;
				ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, 
					СтрЗаменить(ДополнительныйРежимОтображенияЗначение, " ", ""));
			КонецЕсли;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДобавитьОтборОтветственный Тогда
		ЭлементОтбора = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = ПолеОтветственный;
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		ЭлементОтбора.Использование = Ложь;
	КонецЕсли;
	
	ОтборОтветственный = Элементы.Найти("Ответственный");
	Если ОтборОтветственный <> Неопределено Тогда
		ОтборОтветственный.Видимость = ПолеОтветственныйДоступно;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти