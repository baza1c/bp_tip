#Область ПрограммныйИнтерфейс

// Описывает состав прикладных данных таблицы отчета.
// Под таблицей понимается совокупность строк с одинаковым составом колонок.
// Обычно каждая таблица в отчете имеет отдельное название, номер и шапку с описанием колонок.
//
// Параметры:
//  Описание - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйОписаниеТаблицыОтчета - заполняемая коллекция
//  ИмяТаблицыОтчета - Строка - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета
//
Процедура ОписатьТаблицуОтчета(Описание, ИмяТаблицыОтчета) Экспорт
	
	Если ИмяТаблицыОтчета = "НематериальныеАктивыНаличиеДвижение" Тогда
		ОписатьТаблицуОтчетаНематериальныеАктивыНаличиеДвижение(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение" Тогда
		ОписатьТаблицуОтчетаНематериальныеАктивыСозданныеОрганизациейНаличиеДвижение(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "НематериальныеАктивыАмортизируемыеНеамортизируемые" Тогда
		ОписатьТаблицуОтчетаНематериальныеАктивыАмортизируемыеНеамортизируемые(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "КапитальныеВложенияНематериальныеАктивы" Тогда
		ОписатьТаблицуОтчетаКапитальныеВложенияНематериальныеАктивы(Описание);
	КонецЕсли;
	
КонецПроцедуры

// Определяет состав заполняемых пояснений конкретного отчета (за конкретный период, по конкретной организации)
//
// Параметры:
//  ТаблицыОтчета - Структура - Ключ - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета; Значение - Булево.
//                  Для заполняемых пояснений следует установить значение Истина, состав коллекции изменять не следует.
//  КонтекстОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
Процедура ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчета, КонтекстОтчета) Экспорт
	
	ТаблицыОтчета.НематериальныеАктивыНаличиеДвижение = Истина;
	ТаблицыОтчета.НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение = Истина;
	ТаблицыОтчета.НематериальныеАктивыАмортизируемыеНеамортизируемые = Истина;
	ТаблицыОтчета.КапитальныеВложенияНематериальныеАктивы     = Истина;
	
КонецПроцедуры

// Заполняет таблицы отчета за один период.
//
// Параметры:
//  ПолучениеДанныхТаблицОтчета - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйПолучениеДанныхТаблицОтчета -
//     процессор, который содержит исходные параметры для получения данных,
//     и в который нужно разместить полученные данные.
//  Классификаторы - Структура - Ключ: имя таблицы отчета; Значение: Структура - Ключ и Значение: имя колонки, содержащей значения по классификатору
//  КонтекстЗаполненияОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
//
Процедура ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета) Экспорт
	
	ЗаполнитьПояснениеНематериальныеАктивы(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	ЗаполнитьПояснениеКапитальныеВложения(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Заполнение

Процедура ЗаполнитьПояснениеНематериальныеАктивы(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета)
	
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ЗаполняемыеДанные, "НематериальныеАктивыНаличиеДвижение") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьНаличиеДвижениеНМА(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организации", КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("УсловиеОбъекты", Истина);
	
	//@skip-check ql-temp-table-index
	//@skip-check ql-join-to-sub-query
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СправочникОрганизации.Ссылка КАК Организация,
	|	ПоясненияПоОбъектам.Группа КАК Группа,
	|	ПорядокВсехРасшифровок.Расшифровка КАК Расшифровка,
	|	ПорядокВсехРасшифровок.Порядок КАК ПорядокРасшифровки,
	|	ВсеПризнакИсключительное.ВидПрава КАК ВидПрава,
	|	СправочникОрганизации.Ссылка = СправочникОрганизации.ГоловнаяОрганизация КАК ЭтоГоловнаяОрганизация,
	|	СправочникОрганизации.Представление КАК НаименованиеОрганизации
	|ПОМЕСТИТЬ ВТ_ВсеРасшифровки
	|ИЗ
	|	ПорядокВсехРасшифровок КАК ПорядокВсехРасшифровок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			""Исключительное"" КАК ВидПрава
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			""Неисключительное"") КАК ВсеПризнакИсключительное
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоясненияПоОбъектам КАК ПоясненияПоОбъектам
	|		ПО (&УсловиеОбъекты)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка В (&Организации))
	|
	|СГРУППИРОВАТЬ ПО
	|	СправочникОрганизации.Ссылка,
	|	ПорядокВсехРасшифровок.Расшифровка,
	|	ПорядокВсехРасшифровок.Порядок,
	|	ВсеПризнакИсключительное.ВидПрава,
	|	ПоясненияПоОбъектам.Группа,
	|	СправочникОрганизации.Ссылка = СправочникОрганизации.ГоловнаяОрганизация,
	|	СправочникОрганизации.Представление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеРасшифровки.Организация КАК Организация,
	|	ВсеРасшифровки.ЭтоГоловнаяОрганизация КАК ЭтоГоловнаяОрганизация,
	|	ВсеРасшифровки.НаименованиеОрганизации КАК НаименованиеОрганизации,
	|	ВсеРасшифровки.Группа КАК Группа,
	|	ЕСТЬNULL(ВидыНематериальныхАктивов.Порядок, 100) КАК ПорядокГруппы,
	|	ВсеРасшифровки.ВидПрава КАК ВидПрава,
	|	ВсеРасшифровки.Расшифровка КАК Расшифровка,
	|	ВсеРасшифровки.ПорядокРасшифровки КАК ПорядокРасшифровки,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.НачалоПервоначальная, 0)) КАК НачалоПервоначальная,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.НачалоАмортизация, 0)) КАК НачалоАмортизация,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.Поступило, 0)) КАК Поступило,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.СписаноПервоначальная, 0)) КАК СписаноПервоначальная,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.СписаноАмортизация, 0)) КАК СписаноАмортизация,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.Амортизация, 0)) КАК Амортизация,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.Обесценение, 0)) КАК Обесценение,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.ПереоцененоПервоначальная, 0)) КАК ПереоцененоПервоначальная,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.ПереоцененоАмортизация, 0)) КАК ПереоцененоАмортизация,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.ПереклассифицированоПервоначальная, 0)) КАК ПереклассифицированоПервоначальная,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.ПереклассифицированоАмортизация, 0)) КАК ПереклассифицированоАмортизация,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.КонецПервоначальная, 0)) КАК КонецПервоначальная,
	|	СУММА(ЕСТЬNULL(ПоясненияПоОбъектам.КонецАмортизация, 0)) КАК КонецАмортизация
	|ИЗ
	|	ВТ_ВсеРасшифровки КАК ВсеРасшифровки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоясненияПоОбъектам КАК ПоясненияПоОбъектам
	|		ПО ВсеРасшифровки.Группа = ПоясненияПоОбъектам.Группа
	|			И ВсеРасшифровки.Расшифровка = ПоясненияПоОбъектам.Расшифровка
	|			И ВсеРасшифровки.ВидПрава = ПоясненияПоОбъектам.ВидПрава
	|			И ВсеРасшифровки.Организация = ПоясненияПоОбъектам.Организация
	|			И (&УсловиеОбъекты)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ВидыНематериальныхАктивов КАК ВидыНематериальныхАктивов
	|		ПО ВсеРасшифровки.Группа = ВидыНематериальныхАктивов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеРасшифровки.ВидПрава,
	|	ВсеРасшифровки.Группа,
	|	ВсеРасшифровки.Расшифровка,
	|	ВсеРасшифровки.ПорядокРасшифровки,
	|	ВсеРасшифровки.Организация,
	|	ВсеРасшифровки.ЭтоГоловнаяОрганизация,
	|	ВсеРасшифровки.НаименованиеОрганизации,
	|	ЕСТЬNULL(ВидыНематериальныхАктивов.Порядок, 100)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокГруппы,
	|	ПорядокРасшифровки,
	|	ЭтоГоловнаяОрганизация УБЫВ,
	|	ВсеРасшифровки.Организация.Ссылка
	|ИТОГИ
	|	МАКСИМУМ(ЭтоГоловнаяОрганизация),
	|	МАКСИМУМ(НаименованиеОрганизации),
	|	МАКСИМУМ(ПорядокГруппы),
	|	МАКСИМУМ(Расшифровка),
	|	СУММА(НачалоПервоначальная),
	|	СУММА(НачалоАмортизация),
	|	СУММА(Поступило),
	|	СУММА(СписаноПервоначальная),
	|	СУММА(СписаноАмортизация),
	|	СУММА(Амортизация),
	|	СУММА(Обесценение),
	|	СУММА(ПереоцененоПервоначальная),
	|	СУММА(ПереоцененоАмортизация),
	|	СУММА(ПереклассифицированоПервоначальная),
	|	СУММА(ПереклассифицированоАмортизация),
	|	СУММА(КонецПервоначальная),
	|	СУММА(КонецАмортизация)
	|ПО
	|	Группа,
	|	ВидПрава,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВсеРасшифровки";
	
	ПараметрыТекстовРасшифровки = ПараметрыТекстовРасшифровкиНематериальныеАктивы(КонтекстЗаполненияОтчета);
	
	ИмяТаблицы = "НематериальныеАктивыНаличиеДвижение";
	
	ЗаполнитьПояснениеНематериальныеАктивыНаличиеДвижение(
		ЗаполняемыеДанные,
		ИмяТаблицы,
		ПараметрыТекстовРасшифровки,
		Запрос);
		
	ДобавитьОтборНМАСозданныеОрганизацией(Запрос);
	
	ИмяТаблицы = "НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение";
	
	ЗаполнитьПояснениеНематериальныеАктивыСозданныеОрганизациейНаличиеДвижение(
		ЗаполняемыеДанные,
		ИмяТаблицы,
		ПараметрыТекстовРасшифровки,
		Запрос);

	ИмяТаблицы = "НематериальныеАктивыАмортизируемыеНеамортизируемые";
	КлассификаторыТаблицы = Классификаторы[ИмяТаблицы];
	
	РасшифровкиБалансоваяСтоимость = "СтоимостьНМА,СтоимостьДАП,СтоимостьАмортизацияОбесценение";
	Запрос.УстановитьПараметр("СписокРасшифровок", СтрРазделить(РасшифровкиБалансоваяСтоимость, ","));

	Запрос.УстановитьПараметр("Амортизируемые", КлассификаторыТаблицы.Вид.Амортизируемые);
	Запрос.УстановитьПараметр("Неамортизируемые", КлассификаторыТаблицы.Вид.Неамортизируемые);
	Запрос.УстановитьПараметр("Создание", КлассификаторыТаблицы.СпособПоступления.Создание);
	Запрос.УстановитьПараметр("Приобретение", КлассификаторыТаблицы.СпособПоступления.Приобретение);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СправочникОрганизации.Ссылка КАК Организация,
	|	ПоясненияПоОбъектам.Группа КАК Группа,
	|	ПорядокВсехРасшифровок.Расшифровка КАК Расшифровка,
	|	ПорядокВсехРасшифровок.Порядок КАК ПорядокРасшифровки,
	|	ВсеВиды.Амортизируемые КАК Амортизируемые,
	|	ВсеВиды.ПорядокВид КАК ПорядокВид,
	|	ВсеСпособыПоступления.ПорядокСпособПоступления КАК ПорядокСпособПоступления,
	|	ВсеСпособыПоступления.СозданныйОрганизацией КАК СозданныйОрганизацией,
	|	СправочникОрганизации.Ссылка = СправочникОрганизации.ГоловнаяОрганизация КАК ЭтоГоловнаяОрганизация,
	|	СправочникОрганизации.Представление КАК НаименованиеОрганизации
	|ПОМЕСТИТЬ ВТ_ВсеРасшифровки
	|ИЗ
	|	ПорядокВсехРасшифровок КАК ПорядокВсехРасшифровок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			1 КАК ПорядокВид,
	|			ЛОЖЬ КАК Амортизируемые
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			2,
	|			ИСТИНА) КАК ВсеВиды
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			1 КАК ПорядокСпособПоступления,
	|			ИСТИНА КАК СозданныйОрганизацией
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			2,
	|			ЛОЖЬ) КАК ВсеСпособыПоступления
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоясненияПоОбъектам КАК ПоясненияПоОбъектам
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка В (&Организации))
	|ГДЕ
	|	ПорядокВсехРасшифровок.Расшифровка В(&СписокРасшифровок)
	|
	|СГРУППИРОВАТЬ ПО
	|	СправочникОрганизации.Ссылка,
	|	ПоясненияПоОбъектам.Группа,
	|	ПорядокВсехРасшифровок.Расшифровка,
	|	ПорядокВсехРасшифровок.Порядок,
	|	ВсеВиды.Амортизируемые,
	|	ВсеВиды.ПорядокВид,
	|	ВсеСпособыПоступления.ПорядокСпособПоступления,
	|	СправочникОрганизации.Ссылка = СправочникОрганизации.ГоловнаяОрганизация,
	|	СправочникОрганизации.Представление,
	|	ВсеСпособыПоступления.СозданныйОрганизацией
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеРасшифровки.Организация КАК Организация,
	|	ВсеРасшифровки.НаименованиеОрганизации КАК НаименованиеОрганизации,
	|	ПоясненияПоОбъектам.НМА КАК НМА,
	|	НематериальныеАктивы.Наименование КАК НаименованиеНМА,
	|	ВсеРасшифровки.Группа КАК Группа,
	|	ВЫБОР
	|		КОГДА ВсеРасшифровки.СозданныйОрганизацией
	|			ТОГДА &Создание
	|		ИНАЧЕ &Приобретение
	|	КОНЕЦ КАК СпособПоступления,
	|	ВЫБОР
	|		КОГДА ВсеРасшифровки.Амортизируемые
	|			ТОГДА &Амортизируемые
	|		ИНАЧЕ &Неамортизируемые
	|	КОНЕЦ КАК Вид,
	|	ВсеРасшифровки.Расшифровка КАК Расшифровка,
	|	ВсеРасшифровки.ПорядокВид КАК ПорядокВид,
	|	ВсеРасшифровки.ПорядокСпособПоступления КАК ПорядокСпособПоступления,
	|	ВсеРасшифровки.ЭтоГоловнаяОрганизация КАК ЭтоГоловнаяОрганизация,
	|	ЕСТЬNULL(ВидыНематериальныхАктивов.Порядок, 100) КАК ПорядокГруппы,
	|	ВсеРасшифровки.ПорядокРасшифровки КАК ПорядокРасшифровки,
	|	ЕСТЬNULL(ПоясненияПоОбъектам.НачалоБалансоваяСтоимость, 0) КАК НачалоБалансоваяСтоимость,
	|	ЕСТЬNULL(ПоясненияПоОбъектам.КонецБалансоваяСтоимость, 0) КАК КонецБалансоваяСтоимость
	|ИЗ
	|	ВТ_ВсеРасшифровки КАК ВсеРасшифровки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоясненияПоОбъектамАмортизируемыеНеамортизируемые КАК ПоясненияПоОбъектам
	|		ПО ВсеРасшифровки.Организация = ПоясненияПоОбъектам.Организация
	|			И ВсеРасшифровки.Группа = ПоясненияПоОбъектам.Группа
	|			И ВсеРасшифровки.Расшифровка = ПоясненияПоОбъектам.Расшифровка
	|			И ВсеРасшифровки.СозданныйОрганизацией = ПоясненияПоОбъектам.СозданныйОрганизацией
	|			И ВсеРасшифровки.Амортизируемые = ПоясненияПоОбъектам.Амортизируемые
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО (ПоясненияПоОбъектам.НМА = НематериальныеАктивы.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ВидыНематериальныхАктивов КАК ВидыНематериальныхАктивов
	|		ПО ВсеРасшифровки.Группа = ВидыНематериальныхАктивов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоГоловнаяОрганизация УБЫВ,
	|	НаименованиеОрганизации,
	|	Организация,
	|	ПорядокВид,
	|	ПорядокГруппы,
	|	ПорядокСпособПоступления,
	|	ПорядокРасшифровки,
	|	НаименованиеНМА
	|ИТОГИ
	|	МАКСИМУМ(НаименованиеОрганизации),
	|	МАКСИМУМ(НаименованиеНМА),
	|	МАКСИМУМ(ПорядокВид),
	|	МАКСИМУМ(ПорядокСпособПоступления),
	|	МАКСИМУМ(ЭтоГоловнаяОрганизация),
	|	МАКСИМУМ(ПорядокГруппы),
	|	МАКСИМУМ(ПорядокРасшифровки),
	|	СУММА(НачалоБалансоваяСтоимость),
	|	СУММА(КонецБалансоваяСтоимость)
	|ПО
	|	Вид,
	|	Группа,
	|	СпособПоступления,
	|	Организация,
	|	Расшифровка";
	
	ЗаполнитьПояснениеНематериальныеАктивыАмортизируемыеНеамортизируемые(ЗаполняемыеДанные, ИмяТаблицы, ПараметрыТекстовРасшифровки, Запрос, КлассификаторыТаблицы);
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеНематериальныеАктивыНаличиеДвижение(ЗаполняемыеДанные, ИмяТаблицы, ПараметрыТекстовРасшифровки, Запрос)
	
	ШаблоныРасшифровки = ШаблоныРасшифровкиНематериальныеАктивыНаличиеДвижение(ПараметрыТекстовРасшифровки);
	
	НематериальныеАктивыНаличиеДвижение =
		ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(ЗаполняемыеДанные, ИмяТаблицы);
		
	Если НематериальныеАктивыНаличиеДвижение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаГруппа = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаГруппа.Следующий() Цикл
		
		ВыборкаВидПрава = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаВидПрава.Следующий() Цикл
			
			УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстовРасшифровки, ИмяТаблицы, ВыборкаВидПрава);
			
			НачатьДобавлениеСтрокиНематериальныеАктивыНаличиеДвижение(
				НематериальныеАктивыНаличиеДвижение,
				ВыборкаВидПрава,
				ШаблоныРасшифровки,
				ПараметрыТекстовРасшифровки);
			ВыборкаОрганизация = ВыборкаВидПрава.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				ВывестиРасшифровкуОрганизация(НематериальныеАктивыНаличиеДвижение, ШаблоныРасшифровки, ВыборкаОрганизация);

				ВыборкаДетальная = ВыборкаОрганизация.Выбрать();
				
				Пока ВыборкаДетальная.Следующий() Цикл
					
					ДобавитьСлагаемое(
						НематериальныеАктивыНаличиеДвижение,
						ШаблоныРасшифровки,
						ПараметрыТекстовРасшифровки.СчетаРасшифровки,
						ВыборкаДетальная);
					
				КонецЦикла;
				
			КонецЦикла;
			
			ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(
				НематериальныеАктивыНаличиеДвижение,
				ШаблоныРасшифровки);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеНематериальныеАктивыСозданныеОрганизациейНаличиеДвижение(ЗаполняемыеДанные, ИмяТаблицы, ПараметрыТекстовРасшифровки, Запрос)
	
	ШаблоныРасшифровки = ШаблоныРасшифровкиНематериальныеАктивыСозданныеОрганизациейНаличиеДвижение(ПараметрыТекстовРасшифровки);
	
	НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение =
		ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(ЗаполняемыеДанные, ИмяТаблицы);
		
	Если НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаГруппа = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаГруппа.Следующий() Цикл
		
		ВыборкаВидПрава = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаВидПрава.Следующий() Цикл
			
			УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстовРасшифровки, ИмяТаблицы, ВыборкаВидПрава);
			
			НачатьДобавлениеСтрокиНематериальныеАктивыНаличиеДвижение(
				НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение,
				ВыборкаВидПрава,
				ШаблоныРасшифровки,
				ПараметрыТекстовРасшифровки);
				
			ВыборкаОрганизация = ВыборкаВидПрава.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				ВывестиРасшифровкуОрганизация(
					НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение,
					ШаблоныРасшифровки,
					ВыборкаОрганизация);

				ВыборкаРасшифровка = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаРасшифровка.Следующий() Цикл
					
					ВывестиРасшифровкуИерархияСлагаемых(
						НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение,
						ШаблоныРасшифровки,
						ВыборкаРасшифровка);
						
					ВыборкаОбъектов = ВыборкаРасшифровка.Выбрать();
					
					Пока ВыборкаОбъектов.Следующий() Цикл
						
						ДобавитьСлагаемое(
							НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение,
							ШаблоныРасшифровки,
							ПараметрыТекстовРасшифровки.СчетаРасшифровки, 
							ВыборкаОбъектов);
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
			ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(
				НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение,
				ШаблоныРасшифровки);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеНематериальныеАктивыАмортизируемыеНеамортизируемые(ЗаполняемыеДанные, ИмяТаблицы, ПараметрыТекстовРасшифровки, Запрос, Классификаторы)
	
	НематериальныеАктивыАмортизируемыеНеамортизируемые =
		ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(ЗаполняемыеДанные, ИмяТаблицы);
		
	ВыборкаВид = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ШаблоныРасшифровки = ШаблоныРасшифровкиНематериальныеАктивыАмортизируемыеНеамортизируемые(ПараметрыТекстовРасшифровки);
	
	// Для приобретенных амортизируемых объектов детальная информация в расшифровке не выводится,
	// поскольку таких объектов может быть очень много.
	// В отчете-расшифровке для таких ячеек устанавливается условие "НеВСписке", правым значением которого являются
	// все НМА группы, которые или не амортизируются, или созданы организацией, или и то и другое одновременно.
	// Списки таких объектов в разрезе организаций и групп собираются в переменной ИсключаемыеОбъекты.
	// Обход запроса организован таким образом, что сначала выводятся все неамортизируемые объекты, затем амортизируемые.
	// Среди амортизируемых в первую очередь выводятся созданные организацией, а во вторую - приобретенные.
	// Таким образом, к моменту вывода приобретенных амортизируемых объектов по организации будет известен список
	// исключаемых объектов. Поэтому в запросе важно, чтобы сохранялось упорядочивание: в первую очередь
	// должны выбираться неамортизируемые и созданные организацией НМА.
	ИсключаемыеОбъекты = Новый Соответствие;  // Ключ - Строка - хеш из организации и группы, Значение - Массив - НМА, исключаемые из отчета
	Пока ВыборкаВид.Следующий() Цикл
		ВыборкаГруппа = ВыборкаВид.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаГруппа.Следующий() Цикл

			ВыборкаСпособПоступления = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСпособПоступления.Следующий() Цикл
				
				УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстовРасшифровки, ИмяТаблицы, ВыборкаСпособПоступления);
				
				НачатьДобавлениеСтрокиНематериальныеАктивыАмортизируемыеНеамортизируемые(
					НематериальныеАктивыАмортизируемыеНеамортизируемые,
					ВыборкаСпособПоступления,
					ШаблоныРасшифровки,
					ПараметрыТекстовРасшифровки);
					
					ВыборкаОрганизация = ВыборкаСпособПоступления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					// приобретенных объектов может оказаться много, поэтому детализацию в расшифровке для них не делаем
					ВыводитьРасшифровкуПоОбъектам = 
						(ВыборкаСпособПоступления.СпособПоступления = Классификаторы.СпособПоступления.Создание);
						
					Пока ВыборкаОрганизация.Следующий() Цикл
							
						ВывестиРасшифровкуОрганизация(
							НематериальныеАктивыАмортизируемыеНеамортизируемые,
							ШаблоныРасшифровки,
							ВыборкаОрганизация);
							
						ВыборкаРасшифровка = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						
						Пока ВыборкаРасшифровка.Следующий() Цикл
							
							Если ВыводитьРасшифровкуПоОбъектам Тогда
								
								ВывестиРасшифровкуИерархияСлагаемых(
									НематериальныеАктивыАмортизируемыеНеамортизируемые,
									ШаблоныРасшифровки,
									ВыборкаРасшифровка);
									
								ВыборкаОбъектов = ВыборкаРасшифровка.Выбрать();
								
								Пока ВыборкаОбъектов.Следующий() Цикл
									
									ДобавитьСлагаемое(
										НематериальныеАктивыАмортизируемыеНеамортизируемые,
										ШаблоныРасшифровки,
										ПараметрыТекстовРасшифровки.СчетаРасшифровки,
										ВыборкаОбъектов);
										
									Если ЗначениеЗаполнено(ВыборкаОбъектов.НМА) Тогда
										КлючОтбора = КлючОтбора(ВыборкаОбъектов.Организация, ВыборкаОбъектов.Группа);
										ОбщегоНазначенияБПКлиентСервер.ДополнитьРазделенныйМассив(ИсключаемыеОбъекты, КлючОтбора, ВыборкаОбъектов.НМА);
									КонецЕсли;
									
								КонецЦикла;
								
							Иначе
								
								ДобавитьСлагаемоеАмортизируемыеПриобретенные(
									НематериальныеАктивыАмортизируемыеНеамортизируемые,
									ШаблоныРасшифровки,
									ПараметрыТекстовРасшифровки,
									ВыборкаРасшифровка,
									ИсключаемыеОбъекты);
									
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(
					НематериальныеАктивыАмортизируемыеНеамортизируемые,
					ШаблоныРасшифровки);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьПояснениеКапитальныеВложения(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета)
	
	ИмяТаблицы = "КапитальныеВложенияНематериальныеАктивы";
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ПолучениеДанныхТаблицОтчета, ИмяТаблицы) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнениеБухгалтерскойОтчетностиКапитальныеВложения.ЗаполнитьПояснениеКапитальныеВложения(ПолучениеДанныхТаблицОтчета, ИмяТаблицы, Классификаторы, КонтекстЗаполненияОтчета);
	
КонецПроцедуры

Процедура ДобавитьОтборНМАСозданныеОрганизацией(Запрос)
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОбъекты", "ПоясненияПоОбъектам.СозданныйОрганизацией");
	
	ТочкаЗапроса = СхемыЗапросов.НайтиЗапросРезультата(Запрос.Текст);
	Источники = ТочкаЗапроса.Запрос.Операторы[ТочкаЗапроса.Запрос.Операторы.Количество() - 1].Источники;
	ИсточникПояснения = Источники.НайтиПоПсевдониму("ПоясненияПоОбъектам").Источник;
	ИсточникПояснения.ДоступныеПоля.Добавить("НМА");
	
	СхемыЗапросов.УстановитьВыражение(
		ТочкаЗапроса,
		"НМА",
		"ЕСТЬNULL(ПоясненияПоОбъектам.НМА, ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка)) ",
		Истина);
		
	СхемыЗапросов.УстановитьВыражение(
		ТочкаЗапроса,
		"НаименованиеНМА",
		"ВЫРАЗИТЬ(ПоясненияПоОбъектам.НМА КАК Справочник.НематериальныеАктивы).Наименование ",
		Истина);
		
	ИтогиЗапроса = ТочкаЗапроса.Запрос.КонтрольныеТочкиИтогов;
	ИтогиЗапроса.Добавить(Новый ВыражениеСхемыЗапроса("ВсеРасшифровки.Расшифровка"));
	
	Запрос.Текст = ТочкаЗапроса.Схема.ПолучитьТекстЗапроса();
	
КонецПроцедуры

Процедура НачатьДобавлениеСтрокиНематериальныеАктивыНаличиеДвижение(ВыводТаблицы, Выборка, ШаблоныРасшифровки, ПараметрыТекстовРасшифровки)
	
	НоваяСтрока = НачатьДобавлениеСтрокиТаблицыОтчета(
		ВыводТаблицы,
		ШаблоныРасшифровки,
		ПараметрыТекстовРасшифровки);
		
	НоваяСтрока.Вывод = (Выборка.Группа <> Null);
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "Группа, ВидПрава");
	
КонецПроцедуры

Процедура ДобавитьСлагаемое(ВыводТаблицы, ШаблоныРасшифровки, СчетаРасшифровки, Выборка)
	
	СтрокаТаблицы = ЗаполнениеБухгалтерскойОтчетностиТабличное.КрайняяСтрокаТаблицы(ВыводТаблицы);
	
	Для Каждого ШаблонРасшифровки Из ШаблоныРасшифровки Цикл
		
		ИмяКолонки = ШаблонРасшифровки.Ключ;
		Значение = Выборка[ИмяКолонки];
		
		СтрокаТаблицы[ИмяКолонки] = СтрокаТаблицы[ИмяКолонки] + Значение;
		
		ВывестиРасшифровкуНаличиеДвижениеНМА(ВыводТаблицы, ИмяКолонки, ШаблоныРасшифровки, СчетаРасшифровки, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

Функция НачатьДобавлениеСтрокиТаблицыОтчета(ВыводТаблицы, ШаблоныРасшифровки, ПараметрыТекстовРасшифровки)
	
	ШаблоныКолонок = Новый Соответствие;
	
	Для Каждого ШаблонКолонки Из ШаблоныРасшифровки Цикл
		
		ШаблонТекста = ШаблонКолонки.Значение["Заголовок"];
		ШаблонПорядкаРасчета = ШаблонКолонки.Значение["ПорядокРасчета"];
		Если ШаблонТекста = Неопределено Или ПустаяСтрока(ШаблонТекста) Тогда
			Продолжить;
		КонецЕсли;
		
		ШаблоныКолонок.Вставить(ШаблонКолонки.Ключ, Новый Структура("ЗаголовокГруппы, ПорядокРасчета", ШаблонТекста, ШаблонПорядкаРасчета));
		
	КонецЦикла;
	
	Возврат ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		ВыводТаблицы,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
	
КонецФункции

Процедура НачатьДобавлениеСтрокиНематериальныеАктивыАмортизируемыеНеамортизируемые(ВыводТаблицы, Выборка, ШаблоныРасшифровки, ПараметрыТекстовРасшифровки)
	
	НоваяСтрока = НачатьДобавлениеСтрокиТаблицыОтчета(
		ВыводТаблицы,
		ШаблоныРасшифровки,
		ПараметрыТекстовРасшифровки);
		
	НоваяСтрока.Вывод = (Выборка.Группа <> Null);
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "Группа, Вид, СпособПоступления");
	
КонецПроцедуры

Процедура ДобавитьСлагаемоеАмортизируемыеПриобретенные(ВыводТаблицы,  ШаблоныРасшифровки, ПараметрыТекстовРасшифровки, Выборка, ИсключаемыеОбъекты)
	
	СтрокаТаблицы = ЗаполнениеБухгалтерскойОтчетностиТабличное.КрайняяСтрокаТаблицы(ВыводТаблицы);
	
	ЗначенияНастроекОтчета = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.ЗначенияНастроекОтчетаНематериальныеАктивы("", ПараметрыТекстовРасшифровки.СчетаРасшифровки, Выборка);
	
	ОтборОбъектов = ИсключаемыеОбъекты[КлючОтбора(Выборка.Организация, Выборка.Группа)];
	
	Если ТипЗнч(ОтборОбъектов) = Тип("Массив") И ОтборОбъектов.Количество() > 0 Тогда
		ЗначениеОтбора = Новый СписокЗначений;
		ЗначениеОтбора.ЗагрузитьЗначения(ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОтборОбъектов));
		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
			ЗначенияНастроекОтчета, 
			"Субконто%1",
			ЗначенияНастроекОтчета.Счет,
			ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы,
			ЗначениеОтбора,
			ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;
	
	ШаблонОтбора = "";
	Если Выборка.Группа <> Null Тогда
		ШаблонОтбора = "Субконто%1.ВидНематериальногоАктива";
	КонецЕсли;
	
	ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
		ЗначенияНастроекОтчета, 
		ШаблонОтбора,
		ЗначенияНастроекОтчета.Счет,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы,
		Выборка.Группа);
			
	Для Каждого ШаблонРасшифровки Из ШаблоныРасшифровки Цикл
		
		ИмяКолонки = ШаблонРасшифровки.Ключ;
		Значение = Выборка[ИмяКолонки];
		
		СтрокаТаблицы[ИмяКолонки] = СтрокаТаблицы[ИмяКолонки] + Значение;
		Описание = ШаблоныРасшифровки[ИмяКолонки][Выборка.Расшифровка];

		ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьЗаписьРасшифровкиСтроки(
			ВыводТаблицы,
			ИмяКолонки,
			"Слагаемое",
			Описание,
			Значение,
			Выборка.НМА,
			ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчета, ВыводТаблицы.КонтекстЗаполненияОтчета.Период));
		
	КонецЦикла;
	
КонецПроцедуры

Функция КлючОтбора(Организация, Группа)

	ПоляКлюча = Новый Массив;
	ПоляКлюча.Добавить(XMLСтрока(Организация));
	ПоляКлюча.Добавить(XMLСтрока(Группа));
	Возврат СтрСоединить(ПоляКлюча, "_");
КонецФункции

#КонецОбласти

#Область Описания

Процедура ОписатьТаблицуОтчетаНематериальныеАктивыНаличиеДвижение(Описание)
	
	Описание.Структура.Добавить("Группа");
	Описание.Структура.Добавить("ВидПрава");
	
	Описание.Классификаторы.Вставить("ВидПрава", Новый СписокЗначений);
	Описание.Классификаторы.ВидПрава.Добавить("Исключительное", НСтр("ru = 'Исключительные права'"));
	Описание.Классификаторы.ВидПрава.Добавить("Неисключительное", НСтр("ru = 'Неисключительные права'"));
	
	Описание.ЧисловыеЗначения = ЧисловыеЗначенияПоясненияНематериальныеАктивыНаличиеДвижение();
	
КонецПроцедуры

Функция ЧисловыеЗначенияПоясненияНематериальныеАктивыНаличиеДвижение()
	// Важно сохранять названия колонок: при описании расшифровки показателей отчетами
	// есть привязка к названию колонки: в зависимости от наличия в названии колонки идентификатора "Амортизация"
	// используется расшифровка по счету стоимости НМА или по счету амортизации.
	ЧисловыеЗначения = Новый Массив;
	ЧисловыеЗначения.Добавить("НачалоПервоначальная");
	ЧисловыеЗначения.Добавить("НачалоАмортизация");
	ЧисловыеЗначения.Добавить("Поступило");
	ЧисловыеЗначения.Добавить("СписаноПервоначальная");
	ЧисловыеЗначения.Добавить("СписаноАмортизация");
	ЧисловыеЗначения.Добавить("Амортизация");
	ЧисловыеЗначения.Добавить("Обесценение");
	ЧисловыеЗначения.Добавить("ПереоцененоПервоначальная");
	ЧисловыеЗначения.Добавить("ПереоцененоАмортизация");
	ЧисловыеЗначения.Добавить("ПереклассифицированоПервоначальная");
	ЧисловыеЗначения.Добавить("ПереклассифицированоАмортизация");
	ЧисловыеЗначения.Добавить("КонецПервоначальная");
	ЧисловыеЗначения.Добавить("КонецАмортизация");
	
	Возврат ЧисловыеЗначения;
	
КонецФункции

Процедура ОписатьТаблицуОтчетаНематериальныеАктивыСозданныеОрганизациейНаличиеДвижение(Описание)
	
	Описание.Структура.Добавить("Группа");
	Описание.Структура.Добавить("ВидПрава");
	
	Описание.Классификаторы.Вставить("ВидПрава", Новый СписокЗначений);
	Описание.Классификаторы.ВидПрава.Добавить("Исключительное", НСтр("ru = 'Исключительные права'"));
	Описание.Классификаторы.ВидПрава.Добавить("Неисключительное", НСтр("ru = 'Неисключительные права'"));
	
	Описание.ЧисловыеЗначения = ЧисловыеЗначенияПоясненияНематериальныеАктивыСозданныеОрганизацией();
	
КонецПроцедуры

Функция ЧисловыеЗначенияПоясненияНематериальныеАктивыСозданныеОрганизацией()
	
	Возврат ЧисловыеЗначенияПоясненияНематериальныеАктивыНаличиеДвижение();
	
КонецФункции

Процедура ОписатьТаблицуОтчетаНематериальныеАктивыАмортизируемыеНеамортизируемые(Описание)
	
	Описание.Представление = НСтр("ru = 'Амортизируемые и неамортизируемые нематериальные активы'");
	
	Описание.Структура.Добавить("Вид");
	Описание.Структура.Добавить("Группа");
	Описание.Структура.Добавить("СпособПоступления");
	
	Описание.Классификаторы.Вставить("Вид", Новый СписокЗначений);
	Описание.Классификаторы.Вид.Добавить("Амортизируемые",   НСтр("ru = 'Амортизируемые нематериальные активы'"));
	Описание.Классификаторы.Вид.Добавить("Неамортизируемые", НСтр("ru = 'Неамортизируемые нематериальные активы'"));
	
	Описание.Классификаторы.Вставить("СпособПоступления", Новый СписокЗначений);
	Описание.Классификаторы.СпособПоступления.Добавить("Приобретение",   НСтр("ru = 'Приобретенные'"));
	Описание.Классификаторы.СпособПоступления.Добавить("Создание", НСтр("ru = 'Созданные организацией'"));
	
	Описание.ВариантПериода = "НачалоКонец";
	
	Описание.ЧисловыеЗначения.Добавить("БалансоваяСтоимость"); // НачалоБалансоваяСтоимость, КонецБалансоваяСтоимость

КонецПроцедуры

Процедура ОписатьТаблицуОтчетаКапитальныеВложенияНематериальныеАктивы(Описание)
	
	Описание.Представление = НСтр("ru = 'Капитальные вложения в нематериальные активы'");
	
	Описание.Структура.Добавить("Стадия");
	Описание.Структура.Добавить("Группа");
	
	Описание.Классификаторы.Вставить("Стадия", Новый СписокЗначений);
	Описание.Классификаторы.Стадия.Добавить("Приобретение",        НСтр("ru = 'Приобретение нематериальных активов'"));
	Описание.Классификаторы.Стадия.Добавить("Создание",            НСтр("ru = 'Создание нематериальных активов'"));
	Описание.Классификаторы.Стадия.Добавить("ПоследующиеВложения", НСтр("ru = 'Улучшение нематериальных активов'"));
	
	ЗаполнениеБухгалтерскойОтчетностиКапитальныеВложения.ДобавитьОписаниеЧисловыеЗначенияКапитальныеВложения(Описание.ЧисловыеЗначения);
	
КонецПроцедуры

Функция ПараметрыТекстовРасшифровкиНематериальныеАктивы(КонтекстЗаполненияОтчета)

	ПараметрыТекстов = Новый Структура;
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета);
	
	ПараметрыТекстов.Вставить("НаименованиеГруппы", "");
	
	ПараметрыТекстов.Вставить("НаименованиеГруппыПорядокРасчета", "");
	
	ПараметрыТекстов.Вставить("СчетаРасшифровки", СчетаРасшифровки());
	
	ПараметрыТекстов.Вставить("ВидСтатьиПереоценка", Строка(Перечисления.ВидыПрочихДоходовИРасходов.ПереоценкаВнеоборотныхАктивов));
	ПараметрыТекстов.Вставить("СпособПоступленияСтроительство", Строка(Перечисления.СпособыПоступленияАктивов.Строительство));
	ПараметрыТекстов.Вставить("ВидОбъектовНИОКР", Строка(Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР));
	
	ПараметрыТекстов.Вставить("ИмяРеквизитаИсключительноеПраво",
		Метаданные.Справочники.НематериальныеАктивы.Реквизиты.ЭтоИсключительноеПраво.Представление());
	
	Возврат ПараметрыТекстов;
	
КонецФункции

// Возвращает информацию о счетах, используемых в расшифровке
// 
// Возвращаемое значение:
//  Структура из КлючИЗначение: Ключ - Строка, Значение - см. НовыйСчетРасшифровки
//
Функция СчетаРасшифровки()
	
	ПараметрыСчетов = Новый Структура;
	ПараметрыСчетов.Вставить("СчетСтоимость", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("04"));
	ПараметрыСчетов.Вставить("СчетАмортизацияОбесценение", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("05"));
	ПараметрыСчетов.Вставить("СчетОбесценение", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("05.02"));
	ПараметрыСчетов.Вставить("СчетДолгосрочныеАктивыКПродаже", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("04.09"));
	ПараметрыСчетов.Вставить("СчетПереоценка", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("83.01"));
	ПараметрыСчетов.Вставить("СчетПрочиеДоходы", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("91.01"));
	ПараметрыСчетов.Вставить("СчетПрочиеРасходы", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("91.02"));
	ПараметрыСчетов.Вставить("СчетКапитальныеВложения", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("08"));
	ПараметрыСчетов.Вставить("СчетПередачи", ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет("79"));
		
	СчетаРасшифровки = Новый Структура;
	ВсеСчетаРасшифровки = Новый Массив;
	Для Каждого ПараметрСчета Из ПараметрыСчетов Цикл
		СчетаРасшифровки.Вставить(ПараметрСчета.Ключ, НовыйСчетРасшифровки());
		ВсеСчетаРасшифровки.Добавить(ПараметрСчета.Значение);
	КонецЦикла;
	
	ОписаниеСчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВсеСчетаРасшифровки, "Ссылка, Код");
	
	Для Каждого СчетРасшифровки Из СчетаРасшифровки Цикл
		СчетУчета = ПараметрыСчетов[СчетРасшифровки.Ключ];
		ЗаполнитьЗначенияСвойств(СчетРасшифровки.Значение, ОписаниеСчетов[СчетУчета]);
	КонецЦикла;
	
	Возврат СчетаРасшифровки;
КонецФункции

Функция НовыйСчетРасшифровки()
	СчетРасшифровки = Новый Структура;
	СчетРасшифровки.Вставить("Ссылка", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	СчетРасшифровки.Вставить("Код", "");
	Возврат СчетРасшифровки;
КонецФункции

#КонецОбласти

#Область Расшифровка

Функция ШаблоныРасшифровкиНематериальныеАктивыНаличиеДвижение(ПараметрыТекстовРасшифровки)
	
	СчетаРасшифровки = ПараметрыТекстовРасшифровки.СчетаРасшифровки;
	
	Шаблоны = Новый Соответствие; // Ключ - имя колонки; Значение - Соответствие
	
	// Заголовки
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"НачалоПервоначальная",
		НСтр("ru = '[НаименованиеГруппы]: первоначальная (переоцененная) стоимость на [НачалоПериода]'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по счету %1 (кроме %2) [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетСтоимость.Код,
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"НачалоАмортизация",
		НСтр("ru = '[НаименованиеГруппы]: накопленные амортизация и обесценение на [НачалоПериода]'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по кредиту счета %1 [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"Поступило",
		НСтр("ru = '[НаименованиеГруппы]: поступило за [Период]'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по дебету счета %1 в корреспонденции со счетом %2 [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетКапитальныеВложения.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"СписаноПервоначальная",
		НСтр("ru = '[НаименованиеГруппы]: списано за [Период] - первоначальная (переоцененная) стоимость'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1 (кроме %2) [НаименованиеГруппыПорядокРасчета].
			|Исключаются обороты по передаче между филиалами, переоценке, переклассификации в долгосрочные активы к продаже'"),
			СчетаРасшифровки.СчетСтоимость.Код,
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"СписаноАмортизация",
		НСтр("ru = '[НаименованиеГруппы]: списано за [Период] - накопленные амортизация и обесценение'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по дебету счета %1 в корреспонденции со счетом %2 [НаименованиеГруппыПорядокРасчета].
			|Исключаются обороты по переклассификации в долгосрочные активы к продаже'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетСтоимость.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"Амортизация",
		НСтр("ru = '[НаименованиеГруппы]: амортизация за [Период]'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1 (кроме %2) [НаименованиеГруппыПорядокРасчета].
			|Исключаются обороты по передаче между филиалами, переоценке, переклассификации в долгосрочные активы к продаже'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетОбесценение.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"Обесценение",
		НСтр("ru = '[НаименованиеГруппы]: обесценение за [Период]'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1 [НаименованиеГруппыПорядокРасчета].
			|Исключаются обороты по передаче между филиалами'"),
			СчетаРасшифровки.СчетОбесценение.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"ПереоцененоПервоначальная",
		НСтр("ru = '[НаименованиеГруппы]: переоценка за [Период] - первоначальная (переоцененная) стоимость'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2, а также со счетами %3, %4 по статьям вида %5 [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетПереоценка.Код, // 83.01
			СчетаРасшифровки.СчетПрочиеДоходы.Код, // 91.01
			СчетаРасшифровки.СчетПрочиеРасходы.Код, // 91.02
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"ПереоцененоАмортизация",
		НСтр("ru = '[НаименованиеГруппы]: переоценка за [Период] - накопленная амортизация'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2, а также со счетами %3, %4 по статьям вида %5 [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетПереоценка.Код, // 83.01
			СчетаРасшифровки.СчетПрочиеДоходы.Код, // 91.01
			СчетаРасшифровки.СчетПрочиеРасходы.Код, // 91.02
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"ПереклассифицированоПервоначальная",
		НСтр("ru = '[НаименованиеГруппы]: переклассифицировано за [Период] - первоначальная (переоцененная) стоимость'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по дебету счета %1 в корреспонденции со счетом %2 по объектам, имеющим на отчетную дату сальдо по счету %1, [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код, // 04.09
			СчетаРасшифровки.СчетСтоимость.Код)); // 04
			
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"ПереклассифицированоАмортизация",
		НСтр("ru = '[НаименованиеГруппы]: переклассифицировано за [Период] - накопленные амортизация и обесценение'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1 в корреспонденции со счетом %2 по объектам, имеющим на отчетную дату сальдо по счету %1, [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код, // 04.09
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код)); // 05
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"КонецПервоначальная",
		НСтр("ru = '[НаименованиеГруппы]: первоначальная (переоцененная) стоимость на [КонецПериода]'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по счету %1 (кроме %2) [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетСтоимость.Код,
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код));
		
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"КонецАмортизация",
		НСтр("ru = '[НаименованиеГруппы]: накопленные амортизация и обесценение на [КонецПериода]'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по кредиту счета %1 [НаименованиеГруппыПорядокРасчета]'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код));
		
	// Содержание расшифровок
	// Ключ соответствия - поле Расшифровка в запросе из СоздатьНаличиеДвижениеНМА
	
	// НачалоПервоначальная
	НачалоПервоначальная = Шаблоны["НачалоПервоначальная"];
	
	НачалоПервоначальная.Вставить(
		"СтоимостьНМА",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по счету %1'"),
			СчетаРасшифровки.СчетСтоимость.Код)); // 04
		
	НачалоПервоначальная.Вставить(
		"СтоимостьДАП",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Исключая сальдо по счету %1'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код)); // 04.09

	// НачалоАмортизация
	НачалоАмортизация = Шаблоны["НачалоАмортизация"];
	
	НачалоАмортизация.Вставить(
		"СтоимостьАмортизацияОбесценение",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по кредиту счета %1'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код)); // 05

	// Поступило
	Поступило = Шаблоны["Поступило"];
	
	Поступило.Вставить(
		"КапитальныеВложения",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по дебету счета %1 в корреспонденции со счетом %2'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетКапитальныеВложения.Код)); // 08
		
	// СписаноПервоначальная
	СписаноПервоначальная = Шаблоны["СписаноПервоначальная"];
		
	СписаноПервоначальная.Вставить(
		"СтоимостьНМА",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1'"),
			СчетаРасшифровки.СчетСтоимость.Код)); // 04
		
	СписаноПервоначальная.Вставить(
		"СтоимостьДАП",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код)); // 04.09
		
	СписаноПервоначальная.Вставить(
		"Переклассификация",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по дебету счета %1 в корреспонденции со счетом %2 по объектам, имеющим на отчетную дату сальдо по счету %1'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код, //04.09
			СчетаРасшифровки.СчетСтоимость.Код)); // 04
	
	СписаноПервоначальная.Вставить(
		"Переоценка",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1 в дебет счета %2'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетПереоценка.Код)); // 83.01
		
	СписаноПервоначальная.Вставить(
		"КорректировкаПереоценкиРасходы",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1 в корреспонденции со счетом %2 по статьям вида %3'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетПрочиеРасходы.Код, // 91.02
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	СписаноПервоначальная.Вставить(
		"МежфилиальныйОборот",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1 в корреспонденции со счетом %2'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетПередачи.Код)); // 79
		
	// СписаноАмортизация
	СписаноАмортизация = Шаблоны["СписаноАмортизация"];
	
	СписаноАмортизация.Вставить(
		"ВыбытиеАмортизации",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по дебету счета %1 в корреспонденции со счетом %2'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетСтоимость.Код)); // 04
		
	СписаноАмортизация.Вставить(
		"Переклассификация",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по дебету счета %1 в корреспонденции со счетом %2 по объектам, имеющим на отчетную дату сальдо по счету %2'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код)); // 04.09
		
	// Амортизация
	Амортизация = Шаблоны["Амортизация"];
	
	// В таблицу запроса с идентификатором "СтоимостьАмортизацияОбесценение" выбираются данные по счету 05 в целом,
	// а сумма в поля Амортизация и Обесценение попадает в зависимости от конкретного значения субсчета:
	// в поле Обесценение - сумма по субсчету 05.02, а в поле Амортизация - если счет принадлежит иерархии счета 05,
	// но не принадлежит иерархии счета 05.02.
	Амортизация.Вставить(
		"СтоимостьАмортизацияОбесценение",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1 (исключая оборот по кредиту счета %2)'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетОбесценение.Код)); // 05.02
		
	Амортизация.Вставить(
		"Переоценка",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1 в корреспонденции счетом %2'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетПереоценка.Код)); // 83.01
		
	Амортизация.Вставить(
		"КорректировкаПереоценкиРасходы",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1 в корреспонденции со счетом %2 по статьям вида %3'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетПрочиеРасходы.Код, // 91.02
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	Амортизация.Вставить(
		"МежфилиальныйОборот",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1 (исключая оборот по кредиту счета %2) в корреспонденции со счетом %3'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетОбесценение.Код, // 05.02
			СчетаРасшифровки.СчетПередачи.Код)); // 79
		
	// Обесценение
	Обесценение = Шаблоны["Обесценение"];
	
	Обесценение.Вставить(
		"СтоимостьАмортизацияОбесценение",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1'"),
			СчетаРасшифровки.СчетОбесценение.Код)); // 05.02
		
	Обесценение.Вставить(
		"МежфилиальныйОборот",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'За вычетом оборота по кредиту счета %1 в корреспонденции со счетом %2'"),
			СчетаРасшифровки.СчетОбесценение.Код, // 05.02
			СчетаРасшифровки.СчетПередачи.Код)); // 79
		
	// ПереоцененоПервоначальная
	ПереоцененоПервоначальная = Шаблоны["ПереоцененоПервоначальная"];
		
	ПереоцененоПервоначальная.Вставить(
		"Переоценка",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетПереоценка.Код)); // 83.01
		
	ПереоцененоПервоначальная.Вставить(
		"КорректировкаПереоценкиДоходы",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2 по статьям вида %3'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетПрочиеДоходы.Код, // 91.01
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	ПереоцененоПервоначальная.Вставить(
		"КорректировкаПереоценкиРасходы",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2 по статьям вида %3'"),
			СчетаРасшифровки.СчетСтоимость.Код, // 04
			СчетаРасшифровки.СчетПрочиеРасходы.Код, // 91.02
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	// ПереоцененоАмортизация
	ПереоцененоАмортизация = Шаблоны["ПереоцененоАмортизация"];
		
	ПереоцененоАмортизация.Вставить(
		"Переоценка",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетПереоценка.Код)); // 83.01
		
	ПереоцененоАмортизация.Вставить(
		"КорректировкаПереоценкиДоходы",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2 по статьям вида %3'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетПрочиеДоходы.Код, // 91.01
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	ПереоцененоАмортизация.Вставить(
		"КорректировкаПереоценкиРасходы",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по счету %1 в корреспонденции со счетом %2 по статьям вида %3'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код, // 05
			СчетаРасшифровки.СчетПрочиеРасходы.Код, // 91.02
			ПараметрыТекстовРасшифровки.ВидСтатьиПереоценка));
		
	// ПереклассифицированоПервоначальная
	ПереклассифицированоПервоначальная = Шаблоны["ПереклассифицированоПервоначальная"];
	
	ПереклассифицированоПервоначальная.Вставить(
		"Переклассификация",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по дебету счета %1 в корреспонденции со счетом %2 по объектам, имеющим на отчетную дату сальдо по счету %1'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код, // 04.09
			СчетаРасшифровки.СчетСтоимость.Код)); // 04
	
	// ПереклассифицированоАмортизация
	ПереклассифицированоАмортизация = Шаблоны["ПереклассифицированоАмортизация"];
	
	ПереклассифицированоАмортизация.Вставить(
		"Переклассификация",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оборот по кредиту счета %1 в корреспонденции со счетом %2 по объектам, имеющим на отчетную дату сальдо по счету %1'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код, // 04.09
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код)); // 05

	// КонецПервоначальная	
	КонецПервоначальная = Шаблоны["КонецПервоначальная"];
	
	КонецПервоначальная.Вставить(
		"СтоимостьНМА",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по счету %1'"),
			СчетаРасшифровки.СчетСтоимость.Код)); // 04
		
	КонецПервоначальная.Вставить(
		"СтоимостьДАП",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Исключая сальдо по счету %1'"),
			СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код)); // 04.09
		
	// КонецАмортизация
	КонецАмортизация = Шаблоны["КонецАмортизация"];
		
	КонецАмортизация.Вставить(
		"СтоимостьАмортизацияОбесценение",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сальдо по кредиту счета %1'"),
			СчетаРасшифровки.СчетАмортизацияОбесценение.Код)); // 05
		
	Возврат Шаблоны;
	
КонецФункции

Функция ШаблоныРасшифровкиНематериальныеАктивыСозданныеОрганизациейНаличиеДвижение(ПараметрыРасшифровки)
	
	Возврат ШаблоныРасшифровкиНематериальныеАктивыНаличиеДвижение(ПараметрыРасшифровки); // В текущей версии совпадают
	
КонецФункции

Функция ШаблоныРасшифровкиНематериальныеАктивыАмортизируемыеНеамортизируемые(ПараметрыТекстовРасшифровки)
	
	СчетаРасшифровки = ПараметрыТекстовРасшифровки.СчетаРасшифровки;
	
	Шаблоны = Новый Соответствие; // Ключ - имя колонки; Значение - Соответствие
	
	ТекстПорядокРасчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сальдо по счетам %1, %2 (кроме %3) [НаименованиеГруппыПорядокРасчета]'"),
		СчетаРасшифровки.СчетСтоимость.Код,
		СчетаРасшифровки.СчетАмортизацияОбесценение.Код,
		СчетаРасшифровки.СчетДолгосрочныеАктивыКПродаже.Код);
	
	// Заголовки
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"НачалоБалансоваяСтоимость",
		НСтр("ru = '[НаименованиеГруппы]: балансовая стоимость на [КонецПредшествующегоПериода]'"),
		ТекстПорядокРасчета);
	
	ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны,
		"КонецБалансоваяСтоимость",
		НСтр("ru = '[НаименованиеГруппы]: балансовая стоимость на [КонецПериода]'"),
		ТекстПорядокРасчета);
		
	// Тексты расшифровки совпадают с таблицей "НематериальныеАктивыНаличиеДвижение" в значениях ключей
	// "НачалоПервоначальная" и "НачалоАмортизация".
	// ШаблоныНаличиеДвижение[ИмяПоля] - это соответствие, одним из ключей которого является строка "Заголовок" и "ПорядокРасчета"
	// (они здесь не нужны, поскольку заголовок и порядок расчета для таблицы "НематериальныеАктивыАмортизируемыеНеамортизируемые"
	// не совпадают с таблицей "НематериальныеАктивыНаличиеДвижение", определен выше), а остальные свойства - интересующие нас расшифровки
	// "СтоимостьНМА", "СтоимостьДАП" (ключ "НачалоПервоначальная") и "СтоимостьАмортизацияОбесценение" (ключ "НачалоАмортизация").
	// Значением соответствия является текст расшифровки, например, "Сальдо по счету 04".
	// В результате у полей "НачалоБалансоваяСтоимость" и "КонецБалансоваяСтоимость" в шаблоне будут по четыре значения ключей:
	// "Заголовок", "СтоимостьНМА", "СтоимостьДАП" и "СтоимостьАмортизацияОбесценение".
	ШаблоныНаличиеДвижение = ШаблоныРасшифровкиНематериальныеАктивыНаличиеДвижение(ПараметрыТекстовРасшифровки);
	
	НачалоБалансоваяСтоимость = Шаблоны["НачалоБалансоваяСтоимость"];
	КонецБалансоваяСтоимость = Шаблоны["КонецБалансоваяСтоимость"];
		
	ИменаПолей = "НачалоПервоначальная,НачалоАмортизация";
	
	Для Каждого ИмяПоля Из СтрРазделить(ИменаПолей, ",") Цикл
		Для Каждого ШаблонНаличиеДвижение Из ШаблоныНаличиеДвижение[ИмяПоля] Цикл
			Если ШаблонНаличиеДвижение.Ключ = "Заголовок" Или ШаблонНаличиеДвижение.Ключ = "ПорядокРасчета" Тогда
				Продолжить;
			КонецЕсли;
			НачалоБалансоваяСтоимость.Вставить(ШаблонНаличиеДвижение.Ключ, ШаблонНаличиеДвижение.Значение);
			КонецБалансоваяСтоимость.Вставить(ШаблонНаличиеДвижение.Ключ, ШаблонНаличиеДвижение.Значение);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Шаблоны;
	
КонецФункции

Процедура ДобавитьЗаголовокШаблонаРасшифровки(Шаблоны, ИмяКолонки, ТекстШаблона, ТекстПорядкаРасчета = "")
	
	ШаблоныКолонки = Новый Соответствие;
	
	Шаблоны.Вставить(ИмяКолонки, ШаблоныКолонки);
	
	ШаблоныКолонки.Вставить("Заголовок", ТекстШаблона);
	ШаблоныКолонки.Вставить("ПорядокРасчета", ТекстПорядкаРасчета);
	
КонецПроцедуры

Процедура ВывестиРасшифровкуОрганизация(ВыводТаблицы, ШаблоныРасшифровки, Выборка)
	
	Если Не ВыводТаблицы.ВыводитьФилиалы Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ШаблонРасшифровки Из ШаблоныРасшифровки Цикл
		
		ИмяКолонки = ШаблонРасшифровки.Ключ;
		Значение = Выборка[ИмяКолонки];
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьЗаписьРасшифровкиСтрокиОрганизация(
			ВыводТаблицы,
			ИмяКолонки,
			Выборка.Организация,
			Выборка.НаименованиеОрганизации,
			Значение);
			
	КонецЦикла
	
КонецПроцедуры

Процедура ВывестиРасшифровкуИерархияСлагаемых(ВыводТаблицы, ШаблоныРасшифровки, ВыборкаРасшифровка)

	Для Каждого ШаблонРасшифровки Из ШаблоныРасшифровки Цикл
		
		ИмяКолонки = ШаблонРасшифровки.Ключ;
		ТекстРасшифровки = ШаблонРасшифровки.Значение[ВыборкаРасшифровка.Расшифровка];
		
		Если Не ЗначениеЗаполнено(ТекстРасшифровки) Или ПустаяСтрока(ТекстРасшифровки) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьЗаписьРасшифровкиСтроки(
			ВыводТаблицы,
			ИмяКолонки,
			"ИерархияСлагаемых",
			ТекстРасшифровки,
			ВыборкаРасшифровка[ИмяКолонки]);
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиРасшифровкуНаличиеДвижениеНМА(ВыводТаблицы, ИмяКолонки, ШаблоныРасшифровки, СчетаРасшифровки, Выборка)

	Значение = Выборка[ИмяКолонки];
	ПредметЗаписи = Неопределено;
	
	Если ШаблоныРасшифровки[ИмяКолонки] = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Расшифровка = ШаблоныРасшифровки[ИмяКолонки][Выборка.Расшифровка];
	
	Если Расшифровка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Уровень = 0;
	
	ЗначенияНастроекОтчета = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.ЗначенияНастроекОтчетаНематериальныеАктивы(ИмяКолонки, СчетаРасшифровки, Выборка);
	
	ШаблонОтбора = "";
	Если ВыводТаблицы.ИмяТаблицы = "НематериальныеАктивыНаличиеДвижение" Тогда
		
		Описание = Расшифровка;
		
		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
			ЗначенияНастроекОтчета,
			"Субконто%1.ЭтоИсключительноеПраво",
			ЗначенияНастроекОтчета.Счет,
			ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы,
			Выборка.ВидПрава = "Исключительное");
			
		Если Выборка.Группа <> Null Тогда
			ШаблонОтбора = "Субконто%1.ВидНематериальногоАктива";
			ЗначениеОтбора = Выборка.Группа;
		КонецЕсли;
				
	ИначеЕсли ВыводТаблицы.ИмяТаблицы = "НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение"
		Или ВыводТаблицы.ИмяТаблицы = "НематериальныеАктивыАмортизируемыеНеамортизируемые" Тогда
		
		Если Не ЗначениеЗаполнено(Значение) Тогда
			Возврат;
		КонецЕсли;
		
		Описание = Выборка.НаименованиеНМА;
		ПредметЗаписи = Выборка.НМА;
		Уровень = Уровень + 1;
		
		// Таблица 3.2 "Нематериальные активы, созданные организацией" расшифровываются пообъектно, отбор по группе и виду права не нужен
		ШаблонОтбора = "Субконто%1";
		ЗначениеОтбора = Выборка.НМА;
				
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
		ЗначенияНастроекОтчета,
		ШаблонОтбора,
		ЗначенияНастроекОтчета.Счет,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы,
		ЗначениеОтбора);
		
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьЗаписьРасшифровкиСтроки(
		ВыводТаблицы,
		ИмяКолонки,
		"Слагаемое",
		Описание,
		Значение,
		ПредметЗаписи,
		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчета, ВыводТаблицы.КонтекстЗаполненияОтчета.Период), // Отчет
		Уровень);
		
КонецПроцедуры

Процедура УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстовРасшифровки, ИмяТаблицы, Выборка)
	
	НаименованиеГруппы = Строка(Выборка.Группа);
	НаименованиеГруппыПорядокРасчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '- по объектам с видом %1'"),
		НаименованиеГруппы);
		
	Если ПустаяСтрока(НаименованиеГруппы) Тогда
		НаименованиеГруппы = НСтр("ru = 'Нематериальные активы'");
		НаименованиеГруппыПорядокРасчета = НСтр("ru = '- по объектам'");
	КонецЕсли;
	
	ШаблонНаименования = "";
	Если ИмяТаблицы = "НематериальныеАктивыАмортизируемыеНеамортизируемые" Тогда
		ШаблонНаименования = НСтр("ru = '%1, %2 (%3)'");
		ШаблонНаименованияПорядокРасчета = НСтр("ru = '%1, в документе принятия к учету которых %2установлен признак ""Начислять амортизацию"" %3'");
		
		ТекстАмортизируемые = НСтр("ru = 'амортизируемые'");
		ТекстАмортизируемыеПорядокРасчета = "";
		
		Если Выборка.Вид = "Неамортизируемые" Тогда
			ТекстАмортизируемые = НСтр("ru = 'неамортизируемые'");
			ТекстАмортизируемыеПорядокРасчета = НСтр("ru = 'не '")
		КонецЕсли;
		
		ТекстСпособПоступления = НСтр("ru = 'приобретенные'");
		ШаблонСпособПоступленияПорядокРасчета = НСтр("ru = 'и указан способ поступления ""%1"" или вид объекта учета ""%2""'");
		Если Выборка.СпособПоступления = "Создание" Тогда
			ТекстСпособПоступления = НСтр("ru = 'созданные организацией'");
			ШаблонСпособПоступленияПорядокРасчета = НСтр("ru = '(кроме объектов, в документе принятия к учету которых указан способ поступления ""%1"" или вид объекта учета ""%2"")'");
		КонецЕсли;
		
		ТекстСпособПоступленияПорядокРасчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСпособПоступленияПорядокРасчета,
				ПараметрыТекстовРасшифровки.СпособПоступленияСтроительство,
				ПараметрыТекстовРасшифровки.ВидОбъектовНИОКР);
				
		ПараметрыТекстовРасшифровки.НаименованиеГруппы =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНаименования, НаименованиеГруппы, ТекстАмортизируемые, ТекстСпособПоступления);
			
		ПараметрыТекстовРасшифровки.НаименованиеГруппыПорядокРасчета =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонНаименованияПорядокРасчета,
				НаименованиеГруппыПорядокРасчета,
				ТекстАмортизируемыеПорядокРасчета,
				ТекстСпособПоступленияПорядокРасчета);
	Иначе
		
		Исключительное = (Выборка.ВидПрава = "Исключительное");
		
		ТекстСозданныеОрганизацией = "";
		ТекстСозданныеОрганизациейПорядокРасчета = "";
		
		Если ИмяТаблицы = "НематериальныеАктивыСозданныеОрганизациейНаличиеДвижение" Тогда
			ТекстСозданныеОрганизацией = НСтр("ru = ', созданные организацией'");
			ТекстСозданныеОрганизациейПорядокРасчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = ' и в документе принятия к учету которых указан способ поступления ""%1"" или вид объекта учета ""%2""'"),
				ПараметрыТекстовРасшифровки.СпособПоступленияСтроительство,
				ПараметрыТекстовРасшифровки.ВидОбъектовНИОКР);
		КонецЕсли;
		
		ШаблонНаименования = НСтр("ru = '%1%2 (неисключительные права)'");
		ШаблонНаименованияПорядокРасчета = НСтр("ru = '%1, у которых не отмечен реквизит ""%2""%3'");
		
		Если Исключительное Тогда
			ШаблонНаименования = НСтр("ru = '%1%2 (исключительные права)'");
			ШаблонНаименованияПорядокРасчета = НСтр("ru = '%1, у которых отмечен реквизит ""%2""%3'");
		КонецЕсли;
		
		ПараметрыТекстовРасшифровки.НаименованиеГруппы =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНаименования, НаименованиеГруппы, ТекстСозданныеОрганизацией);
			
		ПараметрыТекстовРасшифровки.НаименованиеГруппыПорядокРасчета =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонНаименованияПорядокРасчета,
				НаименованиеГруппыПорядокРасчета,
				ПараметрыТекстовРасшифровки.ИмяРеквизитаИсключительноеПраво,
				ТекстСозданныеОрганизациейПорядокРасчета);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ДанныеУчета

Функция ИерархияСчета(КодСчета)
	
	Возврат БухгалтерскийУчетПовтИсп.СчетаВИерархии(ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет(КодСчета));
	
КонецФункции

Процедура СоздатьНаличиеДвижениеНМА(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачПериода",              НачалоГода(КонтекстЗаполненияОтчета.Период.Начало));
	Запрос.УстановитьПараметр("КонПериода",              КонтекстЗаполненияОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",             КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("КонецДата",               КонтекстЗаполненияОтчета.Период.КонецДата);
	Запрос.УстановитьПараметр("КонецПредыдущегоПериода", КонтекстЗаполненияОтчета.Период.Начало - 1);
	
	Запрос.УстановитьПараметр("Счет_05",      ИерархияСчета("05"));
	Запрос.УстановитьПараметр("Счет_05_02",   ИерархияСчета("05.02"));
	Запрос.УстановитьПараметр("Счет_04",      ИерархияСчета("04"));
	Запрос.УстановитьПараметр("Счет_04_09",   ИерархияСчета("04.09"));
	Запрос.УстановитьПараметр("Счет_08",      ИерархияСчета("08"));
	Запрос.УстановитьПараметр("Счет_91_01",   ИерархияСчета("91.01"));
	Запрос.УстановитьПараметр("Счет_91_02",   ИерархияСчета("91.02"));
	
	Запрос.УстановитьПараметр("Счет_83_01",   ИерархияСчета("83.01"));
	Запрос.УстановитьПараметр("Счет_79",      ИерархияСчета("79"));
	
	Запрос.УстановитьПараметр("СубконтоНМА", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыИРасходы", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	Запрос.УстановитьПараметр("ПереоценкаВнеоборотныхАктивов", Перечисления.ВидыПрочихДоходовИРасходов.ПереоценкаВнеоборотныхАктивов);
	
	//@skip-check ql-temp-table-index
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	""СтоимостьНМА"" КАК Расшифровка
	|ПОМЕСТИТЬ ПорядокВсехРасшифровок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	""СтоимостьДАП""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	""СтоимостьАмортизацияОбесценение""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	""ВыбытиеАмортизации""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	""КапитальныеВложения""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	""Переклассификация""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	""Переоценка""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	""КорректировкаПереоценкиРасходы""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9,
	|	""КорректировкаПереоценкиДоходы""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	10,
	|	""МежфилиальныйОборот""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Организация КАК Организация,
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК НМА,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт) КАК СНД,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт) КАК КО,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт) КАК СКД
	|ПОМЕСТИТЬ Счет_04
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачПериода, &КонПериода, , , Счет В (&Счет_04), &СубконтоНМА, Организация В (&Организации)) КАК ХозрасчетныйОстаткиИОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиИОбороты.Субконто1,
	|	ХозрасчетныйОстаткиИОбороты.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Организация КАК Организация,
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК НМА,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт) КАК СНД,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт) КАК КО,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт) КАК СКД
	|ПОМЕСТИТЬ Счет_04_09
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачПериода, &КонПериода, , , Счет В (&Счет_04_09), &СубконтоНМА, Организация В (&Организации)) КАК ХозрасчетныйОстаткиИОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиИОбороты.Субконто1,
	|	ХозрасчетныйОстаткиИОбороты.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Организация КАК Организация,
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК НМА,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт) КАК СНК,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет В (&Счет_05_02)
	|				ТОГДА 0
	|			ИНАЧЕ ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
	|		КОНЕЦ) КАК КО_05_01,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстаткиИОбороты.Счет В (&Счет_05_02)
	|				ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КО_05_02,
	|	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт) КАК СКК
	|ПОМЕСТИТЬ Счет_05
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачПериода, &КонПериода, , , Счет В (&Счет_05), &СубконтоНМА, Организация В (&Организации)) КАК ХозрасчетныйОстаткиИОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиИОбороты.Субконто1,
	|	ХозрасчетныйОстаткиИОбороты.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Организация КАК Организация,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НМА,
	|	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот) КАК ОБ_05_04
	|ПОМЕСТИТЬ Счет_05_04
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачПериода, &КонПериода, , СчетДт В (&Счет_05), &СубконтоНМА, СчетКт В (&Счет_04), &СубконтоНМА, Организация В (&Организации)) КАК ХозрасчетныйОборотыДтКт
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|	ХозрасчетныйОборотыДтКт.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	ХозрасчетныйОбороты.Субконто1 КАК НМА,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Счет В (&Счет_04)
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДО_04__83_01,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Счет В (&Счет_04)
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КО_04__83_01,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Счет В (&Счет_05)
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КО_05__83_01,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Счет В (&Счет_05)
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДО_05__83_01
	|ПОМЕСТИТЬ Счета_0405__83_01
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачПериода, &КонПериода, , Счет В (&Счет_04, &Счет_05), &СубконтоНМА, Организация В (&Организации), КорСчет В (&Счет_83_01), ) КАК ХозрасчетныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Организация КАК Организация,
	|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК НМА,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОборотыДтКт.СчетКт В (&Счет_04)
	|				ТОГДА ХозрасчетныйОборотыДтКт.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОБ_91_02__04,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОборотыДтКт.СчетКт В (&Счет_05)
	|				ТОГДА ХозрасчетныйОборотыДтКт.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОБ_91_02__05
	|ПОМЕСТИТЬ Счет_91_02__0405
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&НачПериода,
	|			&КонПериода,
	|			,
	|			СчетДт В (&Счет_91_02),
	|			&СубконтоПрочиеДоходыИРасходы,
	|			СчетКт В (&Счет_04, &Счет_05),
	|			&СубконтоНМА,
	|			Организация В (&Организации)
	|				И СубконтоДт1.ВидПрочихДоходовИРасходов = &ПереоценкаВнеоборотныхАктивов) КАК ХозрасчетныйОборотыДтКт
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|	ХозрасчетныйОборотыДтКт.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Организация КАК Организация,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НМА,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОборотыДтКт.СчетДт В (&Счет_04)
	|				ТОГДА ХозрасчетныйОборотыДтКт.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОБ_04__91_01,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОборотыДтКт.СчетДт В (&Счет_05)
	|				ТОГДА ХозрасчетныйОборотыДтКт.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОБ_05__91_01
	|ПОМЕСТИТЬ Счета_0405_91_01
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&НачПериода,
	|			&КонПериода,
	|			,
	|			СчетДт В (&Счет_04, &Счет_05),
	|			&СубконтоНМА,
	|			СчетКт В (&Счет_91_01),
	|			&СубконтоПрочиеДоходыИРасходы,
	|			Организация В (&Организации)
	|				И СубконтоКт1.ВидПрочихДоходовИРасходов = &ПереоценкаВнеоборотныхАктивов) КАК ХозрасчетныйОборотыДтКт
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|	ХозрасчетныйОборотыДтКт.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Организация КАК Организация,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НМА,
	|	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот) КАК ОБ
	|ПОМЕСТИТЬ Счет_08
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачПериода, &КонПериода, , СчетДт В (&Счет_04), &СубконтоНМА, СчетКт В (&Счет_08), , Организация В (&Организации)) КАК ХозрасчетныйОборотыДтКт
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|	ХозрасчетныйОборотыДтКт.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МежфилиальныйОборот.Организация КАК Организация,
	|	МежфилиальныйОборот.Субконто1 КАК НМА,
	|	ВЫБОР
	|		КОГДА МежфилиальныйОборот.Счет В (&Счет_04)
	|			ТОГДА МежфилиальныйОборот.СуммаОборотКт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОБ_79_04,
	|	ВЫБОР
	|		КОГДА МежфилиальныйОборот.Счет В (&Счет_05)
	|				И НЕ МежфилиальныйОборот.Счет В (&Счет_05_02)
	|			ТОГДА МежфилиальныйОборот.СуммаОборотКт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОБ_79__05_01,
	|	ВЫБОР
	|		КОГДА МежфилиальныйОборот.Счет В (&Счет_05_02)
	|			ТОГДА МежфилиальныйОборот.СуммаОборотКт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОБ_79__05_02
	|ПОМЕСТИТЬ Счет_0405_79
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачПериода,
	|			&КонПериода,
	|			,
	|			Счет В (&Счет_04, &Счет_05),
	|			&СубконтоНМА,
	|			Организация В (&Организации)
	|				И КорСубконто1 В (&Организации),
	|			КорСчет В (&Счет_79),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбособленныеПодразделения)) КАК МежфилиальныйОборот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Счет_04_09.НМА КАК ДАП
	|ПОМЕСТИТЬ ДолгосрочныеАктивыКПродаже
	|ИЗ
	|	Счет_04_09 КАК Счет_04_09
	|ГДЕ
	|	Счет_04_09.СКД > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДАП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	ХозрасчетныйОбороты.Субконто1 КАК НМА,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Счет В (&Счет_04)
	|					И НЕ ХозрасчетныйОбороты.Счет В (&Счет_04_09)
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОБ_04_09__04,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Счет В (&Счет_05)
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОБ_05__04_09
	|ПОМЕСТИТЬ Счет_0405__04_09
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачПериода,
	|			&КонПериода,
	|			,
	|			Счет В (&Счет_04, &Счет_05),
	|			&СубконтоНМА,
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ДолгосрочныеАктивыКПродаже.ДАП КАК ДАП
	|					ИЗ
	|						ДолгосрочныеАктивыКПродаже КАК ДолгосрочныеАктивыКПродаже),
	|			КорСчет В (&Счет_04_09),
	|			&СубконтоНМА) КАК ХозрасчетныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоясненияПоРасшифровкам.Организация КАК Организация,
	|	ПоясненияПоРасшифровкам.НМА КАК НМА,
	|	ПоясненияПоРасшифровкам.Расшифровка КАК Расшифровка,
	|	СУММА(ПоясненияПоРасшифровкам.НачалоПервоначальная) КАК НачалоПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.НачалоАмортизация) КАК НачалоАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.Поступило) КАК Поступило,
	|	СУММА(ПоясненияПоРасшифровкам.СписаноПервоначальная) КАК СписаноПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.СписаноАмортизация) КАК СписаноАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.Амортизация) КАК Амортизация,
	|	СУММА(ПоясненияПоРасшифровкам.Обесценение) КАК Обесценение,
	|	СУММА(ПоясненияПоРасшифровкам.ПереоцененоПервоначальная) КАК ПереоцененоПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.ПереоцененоАмортизация) КАК ПереоцененоАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.ПереклассифицированоПервоначальная) КАК ПереклассифицированоПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.ПереклассифицированоАмортизация) КАК ПереклассифицированоАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.КонецПервоначальная) КАК КонецПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.КонецАмортизация) КАК КонецАмортизация
	|ПОМЕСТИТЬ ПоясненияПоРасшифровкам
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьНМА.Организация КАК Организация,
	|		""СтоимостьНМА"" КАК Расшифровка,
	|		СтоимостьНМА.НМА КАК НМА,
	|		СтоимостьНМА.СНД КАК НачалоПервоначальная,
	|		0 КАК НачалоАмортизация,
	|		0 КАК Поступило,
	|		-СтоимостьНМА.КО КАК СписаноПервоначальная,
	|		0 КАК СписаноАмортизация,
	|		0 КАК Амортизация,
	|		0 КАК Обесценение,
	|		0 КАК ПереоцененоПервоначальная,
	|		0 КАК ПереоцененоАмортизация,
	|		0 КАК ПереклассифицированоПервоначальная,
	|		0 КАК ПереклассифицированоАмортизация,
	|		СтоимостьНМА.СКД КАК КонецПервоначальная,
	|		0 КАК КонецАмортизация
	|	ИЗ
	|		Счет_04 КАК СтоимостьНМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтоимостьДАП.Организация,
	|		""СтоимостьДАП"",
	|		СтоимостьДАП.НМА,
	|		-СтоимостьДАП.СНД,
	|		0,
	|		0,
	|		СтоимостьДАП.КО,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		-СтоимостьДАП.СКД,
	|		0
	|	ИЗ
	|		Счет_04_09 КАК СтоимостьДАП
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Переклассификация.Организация,
	|		""Переклассификация"",
	|		Переклассификация.НМА,
	|		0,
	|		0,
	|		0,
	|		Переклассификация.ОБ_04_09__04,
	|		-Переклассификация.ОБ_05__04_09,
	|		0,
	|		0,
	|		0,
	|		0,
	|		-Переклассификация.ОБ_04_09__04,
	|		Переклассификация.ОБ_05__04_09,
	|		0,
	|		0
	|	ИЗ
	|		Счет_0405__04_09 КАК Переклассификация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтоимостьАмортизацияОбесценение.Организация,
	|		""СтоимостьАмортизацияОбесценение"",
	|		СтоимостьАмортизацияОбесценение.НМА,
	|		0,
	|		-СтоимостьАмортизацияОбесценение.СНК,
	|		0,
	|		0,
	|		0,
	|		-СтоимостьАмортизацияОбесценение.КО_05_01,
	|		-СтоимостьАмортизацияОбесценение.КО_05_02,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		-СтоимостьАмортизацияОбесценение.СКК
	|	ИЗ
	|		Счет_05 КАК СтоимостьАмортизацияОбесценение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВыбытиеАмортизации.Организация,
	|		""ВыбытиеАмортизации"",
	|		ВыбытиеАмортизации.НМА,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВыбытиеАмортизации.ОБ_05_04,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		Счет_05_04 КАК ВыбытиеАмортизации
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КапитальныеВложения.Организация,
	|		""КапитальныеВложения"",
	|		КапитальныеВложения.НМА,
	|		0,
	|		0,
	|		КапитальныеВложения.ОБ,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		Счет_08 КАК КапитальныеВложения
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Переоценка.Организация,
	|		""Переоценка"",
	|		Переоценка.НМА,
	|		0,
	|		0,
	|		0,
	|		Переоценка.КО_04__83_01,
	|		0,
	|		Переоценка.КО_05__83_01,
	|		0,
	|		Переоценка.ДО_04__83_01 - Переоценка.КО_04__83_01,
	|		Переоценка.ДО_05__83_01 - Переоценка.КО_05__83_01,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		Счета_0405__83_01 КАК Переоценка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КорректировкаПереоценкиРасходы.Организация,
	|		""КорректировкаПереоценкиРасходы"",
	|		КорректировкаПереоценкиРасходы.НМА,
	|		0,
	|		0,
	|		0,
	|		КорректировкаПереоценкиРасходы.ОБ_91_02__04,
	|		0,
	|		КорректировкаПереоценкиРасходы.ОБ_91_02__05,
	|		0,
	|		-КорректировкаПереоценкиРасходы.ОБ_91_02__04,
	|		-КорректировкаПереоценкиРасходы.ОБ_91_02__05,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		Счет_91_02__0405 КАК КорректировкаПереоценкиРасходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КорректировкаПереоценкиДоходы.Организация,
	|		""КорректировкаПереоценкиДоходы"",
	|		КорректировкаПереоценкиДоходы.НМА,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		КорректировкаПереоценкиДоходы.ОБ_04__91_01,
	|		КорректировкаПереоценкиДоходы.ОБ_05__91_01,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		Счета_0405_91_01 КАК КорректировкаПереоценкиДоходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МежфилиальныйОборот.Организация,
	|		""МежфилиальныйОборот"",
	|		МежфилиальныйОборот.НМА,
	|		0,
	|		0,
	|		0,
	|		МежфилиальныйОборот.ОБ_79_04,
	|		0,
	|		МежфилиальныйОборот.ОБ_79__05_01,
	|		МежфилиальныйОборот.ОБ_79__05_02,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		Счет_0405_79 КАК МежфилиальныйОборот) КАК ПоясненияПоРасшифровкам
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоясненияПоРасшифровкам.НМА,
	|	ПоясненияПоРасшифровкам.Расшифровка,
	|	ПоясненияПоРасшифровкам.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив КАК НМА,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ПервоначальныеСведенияНМА
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПервых(
	|			,
	|			Организация В (&Организации)
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						ПоясненияПоРасшифровкам.НМА
	|					ИЗ
	|						ПоясненияПоРасшифровкам)) КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПервоначальныеСведенияНМА.НМА КАК НМА
	|ПОМЕСТИТЬ СобственныеНМА
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПервоначальныеСведенияНМА.НМА КАК НМА
	|	ИЗ
	|		ВТ_ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|	ГДЕ
	|		ПервоначальныеСведенияНМА.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.Строительство)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПервоначальныеСведенияНМА.НМА
	|	ИЗ
	|		ВТ_ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|			ПО ПервоначальныеСведенияНМА.Регистратор = ПринятиеКУчетуНМА.Ссылка
	|	ГДЕ
	|		ПринятиеКУчетуНМА.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)) КАК ПервоначальныеСведенияНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоясненияПоРасшифровкам.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.ВидНематериальногоАктива ЕСТЬ NULL
	|				ИЛИ НематериальныеАктивы.ВидНематериальногоАктива = ЗНАЧЕНИЕ(Перечисление.ВидыНематериальныхАктивов.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыНематериальныхАктивов.ДругиеНМА)
	|		ИНАЧЕ НематериальныеАктивы.ВидНематериальногоАктива
	|	КОНЕЦ КАК Группа,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НематериальныеАктивы.ЭтоИсключительноеПраво, ЛОЖЬ)
	|			ТОГДА ""Исключительное""
	|		ИНАЧЕ ""Неисключительное""
	|	КОНЕЦ КАК ВидПрава,
	|	ПоясненияПоРасшифровкам.НМА КАК НМА,
	|	МАКСИМУМ(НЕ СобственныеНМА.НМА ЕСТЬ NULL) КАК СозданныйОрганизацией,
	|	ПоясненияПоРасшифровкам.Расшифровка КАК Расшифровка,
	|	СУММА(ПоясненияПоРасшифровкам.НачалоПервоначальная) КАК НачалоПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.НачалоАмортизация) КАК НачалоАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.Поступило) КАК Поступило,
	|	СУММА(ПоясненияПоРасшифровкам.СписаноПервоначальная) КАК СписаноПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.СписаноАмортизация) КАК СписаноАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.Амортизация) КАК Амортизация,
	|	СУММА(ПоясненияПоРасшифровкам.Обесценение) КАК Обесценение,
	|	СУММА(ПоясненияПоРасшифровкам.ПереоцененоПервоначальная) КАК ПереоцененоПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.ПереоцененоАмортизация) КАК ПереоцененоАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.ПереклассифицированоПервоначальная) КАК ПереклассифицированоПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.ПереклассифицированоАмортизация) КАК ПереклассифицированоАмортизация,
	|	СУММА(ПоясненияПоРасшифровкам.КонецПервоначальная) КАК КонецПервоначальная,
	|	СУММА(ПоясненияПоРасшифровкам.КонецАмортизация) КАК КонецАмортизация
	|ПОМЕСТИТЬ ПоясненияПоОбъектам
	|ИЗ
	|	ПоясненияПоРасшифровкам КАК ПоясненияПоРасшифровкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО ПоясненияПоРасшифровкам.НМА = НематериальныеАктивы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СобственныеНМА КАК СобственныеНМА
	|		ПО ПоясненияПоРасшифровкам.НМА = СобственныеНМА.НМА
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.ВидНематериальногоАктива ЕСТЬ NULL
	|				ИЛИ НематериальныеАктивы.ВидНематериальногоАктива = ЗНАЧЕНИЕ(Перечисление.ВидыНематериальныхАктивов.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыНематериальныхАктивов.ДругиеНМА)
	|		ИНАЧЕ НематериальныеАктивы.ВидНематериальногоАктива
	|	КОНЕЦ,
	|	ПоясненияПоРасшифровкам.Расшифровка,
	|	ПоясненияПоРасшифровкам.Организация,
	|	ПоясненияПоРасшифровкам.НМА,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НематериальныеАктивы.ЭтоИсключительноеПраво, ЛОЖЬ)
	|			ТОГДА ""Исключительное""
	|		ИНАЧЕ ""Неисключительное""
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Группа,
	|	Расшифровка,
	|	ВидПрава,
	|	Организация,
	|	СозданныйОрганизацией
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПервоначальныеСведенияНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведения.НематериальныйАктив КАК НМА,
	|	ПервоначальныеСведения.Период КАК ПериодНачало,
	|	ПервоначальныеСведения.НачислятьАмортизацию КАК НачислятьАмортизациюНачало,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодКонец,
	|	ЛОЖЬ КАК НачислятьАмортизациюКонец
	|ПОМЕСТИТЬ ВТ_ПервоначальныеСведенияНМАБухгалтерскийУчет
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|			&КонецПредыдущегоПериода,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					ПоясненияПоОбъектам.Организация,
	|					ПоясненияПоОбъектам.НМА
	|				ИЗ
	|					ПоясненияПоОбъектам КАК ПоясненияПоОбъектам)) КАК ПервоначальныеСведения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПервоначальныеСведения.НематериальныйАктив,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ЛОЖЬ,
	|	ПервоначальныеСведения.Период,
	|	ПервоначальныеСведения.НачислятьАмортизацию
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|			&КонецДата,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					ПоясненияПоОбъектам.Организация,
	|					ПоясненияПоОбъектам.НМА
	|				ИЗ
	|					ПоясненияПоОбъектам КАК ПоясненияПоОбъектам)) КАК ПервоначальныеСведения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭлементыАмортизации.Организация КАК Организация,
	|	ЭлементыАмортизации.НематериальныйАктив КАК НМА,
	|	ЭлементыАмортизации.Период КАК ПериодНачало,
	|	ЭлементыАмортизации.НачислятьАмортизацию КАК НачислятьАмортизациюНачало,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодКонец,
	|	ЛОЖЬ КАК НачислятьАмортизациюКонец
	|ПОМЕСТИТЬ ВТ_ЭлементыАмортизацииНМАБухгалтерскийУчет
	|ИЗ
	|	РегистрСведений.ЭлементыАмортизацииНМАБухгалтерскийУчет.СрезПоследних(
	|			&КонецПредыдущегоПериода,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					ПоясненияПоОбъектам.Организация,
	|					ПоясненияПоОбъектам.НМА
	|				ИЗ
	|					ПоясненияПоОбъектам КАК ПоясненияПоОбъектам)) КАК ЭлементыАмортизации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЭлементыАмортизации.Организация,
	|	ЭлементыАмортизации.НематериальныйАктив,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ЛОЖЬ,
	|	ЭлементыАмортизации.Период,
	|	ЭлементыАмортизации.НачислятьАмортизацию
	|ИЗ
	|	РегистрСведений.ЭлементыАмортизацииНМАБухгалтерскийУчет.СрезПоследних(
	|			&КонецДата,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					ПоясненияПоОбъектам.Организация,
	|					ПоясненияПоОбъектам.НМА
	|				ИЗ
	|					ПоясненияПоОбъектам КАК ПоясненияПоОбъектам)) КАК ЭлементыАмортизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоясненияПоОбъектам.Организация КАК Организация,
	|	ПоясненияПоОбъектам.НМА КАК НМА,
	|	МАКСИМУМ(ЕСТЬNULL(ВЫБОР
	|				КОГДА ЕСТЬNULL(ПервоначальныеСведения.ПериодНачало, ДАТАВРЕМЯ(1, 1, 1)) <= ЭлементыАмортизации.ПериодНачало
	|					ТОГДА ЭлементыАмортизации.НачислятьАмортизациюНачало
	|				ИНАЧЕ ПервоначальныеСведения.НачислятьАмортизациюНачало
	|			КОНЕЦ, ЛОЖЬ)) КАК АмортизируемыеНачало,
	|	МАКСИМУМ(ЕСТЬNULL(ВЫБОР
	|				КОГДА ЕСТЬNULL(ПервоначальныеСведения.ПериодКонец, ДАТАВРЕМЯ(1, 1, 1)) <= ЭлементыАмортизации.ПериодКонец
	|					ТОГДА ЭлементыАмортизации.НачислятьАмортизациюКонец
	|				ИНАЧЕ ПервоначальныеСведения.НачислятьАмортизациюКонец
	|			КОНЕЦ, ЛОЖЬ)) КАК АмортизируемыеКонец
	|ПОМЕСТИТЬ ВТ_СведенияОбАмортизации
	|ИЗ
	|	ПоясненияПоОбъектам КАК ПоясненияПоОбъектам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияНМАБухгалтерскийУчет КАК ПервоначальныеСведения
	|		ПО ПоясненияПоОбъектам.НМА = ПервоначальныеСведения.НМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЭлементыАмортизацииНМАБухгалтерскийУчет КАК ЭлементыАмортизации
	|		ПО ПоясненияПоОбъектам.Организация = ЭлементыАмортизации.Организация
	|			И ПоясненияПоОбъектам.НМА = ЭлементыАмортизации.НМА
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоясненияПоОбъектам.Организация,
	|	ПоясненияПоОбъектам.НМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Организация КАК Организация,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.НМА КАК НМА,
	|	ЕСТЬNULL(ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Группа, ЗНАЧЕНИЕ(Перечисление.ВидыНематериальныхАктивов.ПустаяСсылка)) КАК Группа,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.СозданныйОрганизацией КАК СозданныйОрганизацией,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Расшифровка КАК Расшифровка,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Амортизируемые КАК Амортизируемые,
	|	СУММА(ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.НачалоБалансоваяСтоимость) КАК НачалоБалансоваяСтоимость,
	|	СУММА(ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.КонецБалансоваяСтоимость) КАК КонецБалансоваяСтоимость
	|ПОМЕСТИТЬ ПоясненияПоОбъектамАмортизируемыеНеамортизируемые
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоясненияПоОбъектам.Организация КАК Организация,
	|		ПоясненияПоОбъектам.НМА КАК НМА,
	|		ПоясненияПоОбъектам.Группа КАК Группа,
	|		ПоясненияПоОбъектам.СозданныйОрганизацией КАК СозданныйОрганизацией,
	|		ПоясненияПоОбъектам.Расшифровка КАК Расшифровка,
	|		СведенияОбАмортизации.АмортизируемыеНачало КАК Амортизируемые,
	|		ПоясненияПоОбъектам.НачалоПервоначальная + ПоясненияПоОбъектам.НачалоАмортизация КАК НачалоБалансоваяСтоимость,
	|		0 КАК КонецБалансоваяСтоимость
	|	ИЗ
	|		ПоясненияПоОбъектам КАК ПоясненияПоОбъектам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбАмортизации КАК СведенияОбАмортизации
	|			ПО ПоясненияПоОбъектам.Организация = СведенияОбАмортизации.Организация
	|				И ПоясненияПоОбъектам.НМА = СведенияОбАмортизации.НМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПоясненияПоОбъектам.Организация,
	|		ПоясненияПоОбъектам.НМА,
	|		ПоясненияПоОбъектам.Группа,
	|		ПоясненияПоОбъектам.СозданныйОрганизацией,
	|		ПоясненияПоОбъектам.Расшифровка,
	|		СведенияОбАмортизации.АмортизируемыеКонец,
	|		0,
	|		ПоясненияПоОбъектам.КонецПервоначальная + ПоясненияПоОбъектам.КонецАмортизация
	|	ИЗ
	|		ПоясненияПоОбъектам КАК ПоясненияПоОбъектам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбАмортизации КАК СведенияОбАмортизации
	|			ПО ПоясненияПоОбъектам.Организация = СведенияОбАмортизации.Организация
	|				И ПоясненияПоОбъектам.НМА = СведенияОбАмортизации.НМА) КАК ПоясненияПоОбъектамАмортизируемыеНеамортизируемые
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Организация,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.НМА,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Группа,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.СозданныйОрганизацией,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Расшифровка,
	|	ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Амортизируемые,
	|	ЕСТЬNULL(ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.Группа, ЗНАЧЕНИЕ(Перечисление.ВидыНематериальныхАктивов.ПустаяСсылка))
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.НачалоБалансоваяСтоимость) <> 0
	|		ИЛИ СУММА(ПоясненияПоОбъектамАмортизируемыеНеамортизируемые.КонецБалансоваяСтоимость) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Группа,
	|	Расшифровка,
	|	СозданныйОрганизацией,
	|	Амортизируемые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПервоначальныеСведенияНМАБухгалтерскийУчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ЭлементыАмортизацииНМАБухгалтерскийУчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СведенияОбАмортизации";
		
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти