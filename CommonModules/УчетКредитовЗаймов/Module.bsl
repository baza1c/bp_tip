
#Область СлужебныйПрограммныйИнтерфейс

Функция СчетаУчетаЗаймовИПроцентов() Экспорт
	
	Результат = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, СчетаУчетаЗаймовПолученных()); // 66.03, 67.03, 66.23, 67.23
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, СчетаУчетаПроцентовПоЗаймамПолученным()); // 66.04, 67.04, 66.24, 67.24
	
	Возврат Результат;
	
КонецФункции

Функция СчетаУчетаЗаймовПолученных() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(ПланыСчетов.Хозрасчетный.КраткосрочныеЗаймы); // 66.03 Краткосрочные рубли
	Результат.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеЗаймы); // 67.03 Долгосрочные рубли
	Результат.Добавить(ПланыСчетов.Хозрасчетный.КраткосрочныеЗаймыВал); // 66.23 Краткосрочные валюта
	Результат.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеЗаймыВал); // 67.23 Долгосрочные валюта
	
	Возврат Результат;
	
КонецФункции

Функция СчетаУчетаПроцентовПоЗаймамПолученным() Экспорт

	Результат = Новый Массив;
	Результат.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймам); // 66.04 Краткосрочные проценты рубли
	Результат.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймам); // 67.04 Долгосрочные проценты рубли
	Результат.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймамВал); // 66.24 Краткосрочные проценты валюта
	Результат.Добавить(ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймамВал); // 67.24 Долгосрочные проценты валюта
	
	Возврат Результат;
	
КонецФункции

Процедура СформироватьДвиженияНачислениеПроцентовПоКредитамИЗаймам(Движения, ТаблицаПроценты, ТаблицаРеквизиты) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаПроценты) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыНачислениеПроцентовПоКредитамИЗаймам(ТаблицаПроценты, ТаблицаРеквизиты);
	
	Реквизиты = Параметры.Реквизиты[0];
	
	СчетРасходов = ПланыСчетов.Хозрасчетный.ПрочиеРасходы; // 91.02
	СтатьяРасходов = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ПроцентыКУплате");
	СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетРасходов);
	УчетПоПодразделениямДт = СвойстваСчетаДт.УчетПоПодразделениям;
	ПриниматьКНалоговомуУчету = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "ПринятиеКНалоговомуУчету");
	
	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаПроценты Цикл
		
		ПодробнаяИнформация = СтрШаблон(НСтр("ru = 'с %1 по %2 (%3 дн.) по ставке %4%% на сумму долга %5'"),
								Формат(СтрокаТаблицы.НачисленоСДаты, "ДЛФ=Д"), Формат(СтрокаТаблицы.НачисленоПоДату, "ДЛФ=Д"),
								СтрокаТаблицы.КоличествоДней, Формат(СтрокаТаблицы.ПроцентнаяСтавка, "ЧДЦ=2"),
								СтрокаТаблицы.СуммаДолгаВал);
		
		Проводка = Движения.Хозрасчетный.Добавить();
		
		Проводка.Период = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Содержание = СтрШаблон(Реквизиты.Содержание, ПодробнаяИнформация);
		
		Проводка.СчетДт = СчетРасходов;
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт, Проводка.СубконтоДт, "ПрочиеДоходыИРасходы", СтатьяРасходов);
		
		Если УчетПоПодразделениямДт Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаПроцентов;
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтрокаТаблицы.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетКт, Проводка.СубконтоКт, "Договоры", СтрокаТаблицы.ДоговорКонтрагента);
		
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Если СвойстваСчетаКт.Валютный Тогда
			Проводка.ВалютаКт = СтрокаТаблицы.Валюта;
			Проводка.ВалютнаяСуммаКт = СтрокаТаблицы.СуммаПроцентовВал;
		КонецЕсли;
		
		Если ПриниматьКНалоговомуУчету Тогда
			Проводка.СуммаНУДт = СтрокаТаблицы.СуммаПроцентов;
		Иначе
			Проводка.СуммаПРДт = СтрокаТаблицы.СуммаПроцентов;
		КонецЕсли;
		Проводка.СуммаНУКт = СтрокаТаблицы.СуммаПроцентов;
		Проводка.Сумма = СтрокаТаблицы.СуммаПроцентов;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область РегламентнаяОперация

// Заполняем таблицы данных для движений регламентной операции "Начисление процентов по кредитам и займам".
//
// Параметры:
//  СтруктураТаблиц  - Структура - на входе пустая структура, процедура добавляет в нее:
//                     * ТаблицаПроценты - ТаблицаЗначений - данные для формирования проводок
//                     * ТаблицаРасчет - ТаблицаЗначений - данные для регистра РасчетНачисленияПроцентовПоКредитамЗаймам
//  ТаблицаРеквизиты - ТаблицаЗначений - реквизиты документа РегламентнаяОперация
//  Отказ            - Булево - возвращается Истина при ошибках
//  Ошибки           - ДеревоЗначений - см. ВыводСообщенийОбОшибках.НовыйДетальнаяИнформацияОбОшибках
//
Процедура ЗаполнитьТаблицыНачислениеПроцентовПоКредитамИЗаймам(СтруктураТаблиц, ТаблицаРеквизиты, Отказ, Ошибки) Экспорт
	
	Реквизиты = ТаблицаРеквизиты[0];
	ГоловнаяОрганизация = БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(Реквизиты.Организация);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Реквизиты.Период));
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	
	ВидыДоговоровЗайма = Новый Массив;
	ВидыДоговоровЗайма.Добавить(Перечисления.ВидыДоговоровКонтрагентов.ЗаемПолученный);
	Запрос.УстановитьПараметр("ВидыДоговоровЗайма", ВидыДоговоровЗайма);
	
	// В запросе счета сравниваются не в иерархии, из-за точного соответствия счет долга - счет начисления процентов.
	Запрос.УстановитьПараметр("СчетаУчетаПроцентовПоЗаймамПолученным", СчетаУчетаПроцентовПоЗаймамПолученным());
	Запрос.УстановитьПараметр("СчетаУчетаЗаймовПолученных", СчетаУчетаЗаймовПолученных());
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	
	// Выберем основные данные из регистра БУ и подготовим таблицу для отбора по договорам.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК ДоговорКонтрагента,
	|	ДоговорыКонтрагентов.Дата КАК ДатаДоговора,
	|	ДоговорыКонтрагентов.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ДоговорыКонтрагентов.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ДоговорыКонтрагентов.Валютный КАК РасчетыВВалюте,
	|	ДоговорыКонтрагентов.ИспользоватьОтсрочкуПоСтавкеЦБ КАК ИспользоватьОтсрочкуПоСтавкеЦБ
	|ПОМЕСТИТЬ ДоговорыКонтрагентов
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Организация = &ГоловнаяОрганизация
	|	И ДоговорыКонтрагентов.ВидДоговора В(&ВидыДоговоровЗайма)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОборотыБУ.Период КАК Период,
	|	ОстаткиИОборотыБУ.Счет КАК Счет,
	|	ВЫБОР
	|		КОГДА ОстаткиИОборотыБУ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КраткосрочныеЗаймы)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймам)
	|		КОГДА ОстаткиИОборотыБУ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДолгосрочныеЗаймы)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймам)
	|		КОГДА ОстаткиИОборотыБУ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КраткосрочныеЗаймыВал)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймамВал)
	|		КОГДА ОстаткиИОборотыБУ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДолгосрочныеЗаймыВал)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймамВал)
	|		ИНАЧЕ ОстаткиИОборотыБУ.Счет
	|	КОНЕЦ КАК СчетУчетаПроцентов,
	|	ОстаткиИОборотыБУ.Организация КАК Организация,
	|	ЕСТЬNULL(ОстаткиИОборотыБУ.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ОстаткиИОборотыБУ.Валюта, &ВалютаРеглУчета) КАК Валюта,
	|	ВЫРАЗИТЬ(ОстаткиИОборотыБУ.Субконто1 КАК Справочник.Контрагенты) КАК Контрагент,
	|	ВЫРАЗИТЬ(ОстаткиИОборотыБУ.Субконто2 КАК Справочник.ДоговорыКонтрагентов) КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ОстаткиИОборотыБУ.Счет.Валютный
	|			ТОГДА ОстаткиИОборотыБУ.ВалютнаяСуммаНачальныйОстатокКт
	|		ИНАЧЕ ОстаткиИОборотыБУ.СуммаНачальныйОстатокКт
	|	КОНЕЦ КАК НачальныйОстаток,
	|	ВЫБОР
	|		КОГДА ОстаткиИОборотыБУ.Счет.Валютный
	|			ТОГДА ОстаткиИОборотыБУ.ВалютнаяСуммаОборотКт
	|		ИНАЧЕ ОстаткиИОборотыБУ.СуммаОборотКт
	|	КОНЕЦ КАК Приход,
	|	ВЫБОР
	|		КОГДА ОстаткиИОборотыБУ.Счет.Валютный
	|			ТОГДА ОстаткиИОборотыБУ.ВалютнаяСуммаОборотДт
	|		ИНАЧЕ ОстаткиИОборотыБУ.СуммаОборотДт
	|	КОНЕЦ КАК Расход,
	|	ВЫБОР
	|		КОГДА ОстаткиИОборотыБУ.Счет.Валютный
	|			ТОГДА ОстаткиИОборотыБУ.ВалютнаяСуммаКонечныйОстатокКт
	|		ИНАЧЕ ОстаткиИОборотыБУ.СуммаКонечныйОстатокКт
	|	КОНЕЦ КАК КонечныйОстаток
	|ПОМЕСТИТЬ ОстаткиИОборотыБУ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|			&НачалоМесяца,
	|			&КонецМесяца,
	|			День,
	|			,
	|			Счет В (&СчетаУчетаЗаймовПолученных, &СчетаУчетаПроцентовПоЗаймамПолученным),
	|			&ВидыСубконто,
	|			Организация = &Организация
	|				И Субконто2 В
	|					(ВЫБРАТЬ
	|						ДоговорыКонтрагентов.ДоговорКонтрагента
	|					ИЗ
	|						ДоговорыКонтрагентов)) КАК ОстаткиИОборотыБУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Счет,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	Приход
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Период КАК Период,
	|	ХозрасчетныйОбороты.Счет КАК Счет,
	|	ХозрасчетныйОбороты.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОбороты.Субконто2 КАК Договор,
	|	ХозрасчетныйОбороты.КорСубконто1 КАК КорКонтрагент,
	|	ХозрасчетныйОбороты.КорСубконто2 КАК КорДоговор,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Счет.Валютный
	|			ТОГДА ХозрасчетныйОбороты.ВалютнаяСуммаОборотКт
	|		ИНАЧЕ ХозрасчетныйОбороты.СуммаОборотКт
	|	КОНЕЦ КАК Приход,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Счет.Валютный
	|			ТОГДА ХозрасчетныйОбороты.ВалютнаяСуммаОборотДт
	|		ИНАЧЕ ХозрасчетныйОбороты.СуммаОборотДт
	|	КОНЕЦ КАК Расход,
	|	ИСТИНА КАК ЭтоРеклассификация
	|ПОМЕСТИТЬ РеклассификацияЗадолженности
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоМесяца,
	|			&КонецМесяца,
	|			День,
	|			Счет В (&СчетаУчетаЗаймовПолученных, &СчетаУчетаПроцентовПоЗаймамПолученным),
	|			&ВидыСубконто,
	|			Организация = &Организация
	|				И Субконто2 В
	|					(ВЫБРАТЬ
	|						ДоговорыКонтрагентов.ДоговорКонтрагента
	|					ИЗ
	|						ДоговорыКонтрагентов),
	|			КорСчет В (&СчетаУчетаЗаймовПолученных, &СчетаУчетаПроцентовПоЗаймамПолученным),
	|			&ВидыСубконто) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.Счет <> ХозрасчетныйОбороты.КорСчет
	|	И ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто1
	|	И ХозрасчетныйОбороты.Субконто2 = ХозрасчетныйОбороты.КорСубконто2
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Счет,
	|	Контрагент,
	|	Договор,
	|	Приход
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОборотыБУ.Период КАК Период,
	|	ОстаткиИОборотыБУ.Счет КАК Счет,
	|	ОстаткиИОборотыБУ.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|	ОстаткиИОборотыБУ.Организация КАК Организация,
	|	ОстаткиИОборотыБУ.Подразделение КАК Подразделение,
	|	ОстаткиИОборотыБУ.Валюта КАК Валюта,
	|	ОстаткиИОборотыБУ.Контрагент КАК Контрагент,
	|	ОстаткиИОборотыБУ.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеклассификацияЗадолженности.ЭтоРеклассификация, ЛОЖЬ)
	|				И ОстаткиИОборотыБУ.Приход > 0
	|			ТОГДА ОстаткиИОборотыБУ.НачальныйОстаток + ОстаткиИОборотыБУ.Приход
	|		ИНАЧЕ ОстаткиИОборотыБУ.НачальныйОстаток
	|	КОНЕЦ КАК НачальныйОстаток,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеклассификацияЗадолженности.ЭтоРеклассификация, ЛОЖЬ)
	|			ТОГДА 0
	|		ИНАЧЕ ОстаткиИОборотыБУ.Приход
	|	КОНЕЦ КАК Приход,
	|	ОстаткиИОборотыБУ.Расход КАК Расход,
	|	ОстаткиИОборотыБУ.КонечныйОстаток КАК КонечныйОстаток,
	|	ЕСТЬNULL(РеклассификацияЗадолженности.ЭтоРеклассификация, ЛОЖЬ) КАК ЭтоРеклассификация
	|ПОМЕСТИТЬ ОстаткиИОборотыБУСРеклассификацией
	|ИЗ
	|	ОстаткиИОборотыБУ КАК ОстаткиИОборотыБУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РеклассификацияЗадолженности КАК РеклассификацияЗадолженности
	|		ПО ОстаткиИОборотыБУ.Период = РеклассификацияЗадолженности.Период
	|			И ОстаткиИОборотыБУ.Счет = РеклассификацияЗадолженности.Счет
	|			И ОстаткиИОборотыБУ.Контрагент = РеклассификацияЗадолженности.Контрагент
	|			И ОстаткиИОборотыБУ.ДоговорКонтрагента = РеклассификацияЗадолженности.Договор
	|			И ОстаткиИОборотыБУ.Приход = РеклассификацияЗадолженности.Приход
	|ГДЕ
	|	ВЫБОР
	|			КОГДА РеклассификацияЗадолженности.ЭтоРеклассификация
	|					И ОстаткиИОборотыБУ.КонечныйОстаток = 0
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОборотыБУСРеклассификацией.Период КАК Период,
	|	ОстаткиИОборотыБУСРеклассификацией.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|	ОстаткиИОборотыБУСРеклассификацией.Организация КАК Организация,
	|	ОстаткиИОборотыБУСРеклассификацией.Подразделение КАК Подразделение,
	|	ОстаткиИОборотыБУСРеклассификацией.Контрагент КАК Контрагент,
	|	ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОстаткиИОборотыБУСРеклассификацией.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА 0
	|			ИНАЧЕ ОстаткиИОборотыБУСРеклассификацией.НачальныйОстаток
	|		КОНЕЦ) КАК НачальныйОстаток,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА 0
	|			ИНАЧЕ ОстаткиИОборотыБУСРеклассификацией.Приход
	|		КОНЕЦ) КАК Выдано,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА 0
	|			ИНАЧЕ ОстаткиИОборотыБУСРеклассификацией.Расход
	|		КОНЕЦ) КАК Погашено,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА 0
	|			ИНАЧЕ ОстаткиИОборотыБУСРеклассификацией.КонечныйОстаток
	|		КОНЕЦ) КАК КонечныйОстаток,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА ОстаткиИОборотыБУСРеклассификацией.НачальныйОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПроцентыНачальныйОстаток,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА ОстаткиИОборотыБУСРеклассификацией.Приход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Начислено,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА ОстаткиИОборотыБУСРеклассификацией.Расход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Оплачено,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиИОборотыБУСРеклассификацией.Счет В (&СчетаУчетаПроцентовПоЗаймамПолученным)
	|				ТОГДА ОстаткиИОборотыБУСРеклассификацией.КонечныйОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПроцентыКонечныйОстаток
	|ПОМЕСТИТЬ ОстаткиИОборотыПоДаннымБУ
	|ИЗ
	|	ОстаткиИОборотыБУСРеклассификацией КАК ОстаткиИОборотыБУСРеклассификацией
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОборотыБУСРеклассификацией.Период,
	|	ОстаткиИОборотыБУСРеклассификацией.СчетУчетаПроцентов,
	|	ОстаткиИОборотыБУСРеклассификацией.Организация,
	|	ОстаткиИОборотыБУСРеклассификацией.Подразделение,
	|	ОстаткиИОборотыБУСРеклассификацией.Валюта,
	|	ОстаткиИОборотыБУСРеклассификацией.Контрагент,
	|	ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ОстаткиИОборотыПоДаннымБУ.Период) КАК МаксимальныйПериод,
	|	ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ПОМЕСТИТЬ ПериодыПодразделений
	|ИЗ
	|	ОстаткиИОборотыПоДаннымБУ КАК ОстаткиИОборотыПоДаннымБУ
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	МаксимальныйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	МАКСИМУМ(ОстаткиИОборотыПоДаннымБУ.Подразделение) КАК Подразделение
	|ПОМЕСТИТЬ ПодразделенияДляНачислений
	|ИЗ
	|	ОстаткиИОборотыПоДаннымБУ КАК ОстаткиИОборотыПоДаннымБУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыПодразделений КАК ПериодыПодразделений
	|		ПО ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента = ПериодыПодразделений.ДоговорКонтрагента
	|			И ОстаткиИОборотыПоДаннымБУ.Период = ПериодыПодразделений.МаксимальныйПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОборотыПоДаннымБУ.Период КАК Период,
	|	ДоговорыКонтрагентов.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ДоговорыКонтрагентов.ИспользоватьОтсрочкуПоСтавкеЦБ КАК ИспользоватьОтсрочкуПоСтавкеЦБ,
	|	ОстаткиИОборотыПоДаннымБУ.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|	ОстаткиИОборотыПоДаннымБУ.Организация КАК Организация,
	|	ЕСТЬNULL(ПодразделенияДляНачислений.Подразделение, ОстаткиИОборотыПоДаннымБУ.Подразделение) КАК Подразделение,
	|	ОстаткиИОборотыПоДаннымБУ.Контрагент КАК Контрагент,
	|	ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОстаткиИОборотыПоДаннымБУ.Валюта КАК Валюта,
	|	ОстаткиИОборотыПоДаннымБУ.НачальныйОстаток КАК НачальныйОстаток,
	|	ОстаткиИОборотыПоДаннымБУ.Выдано КАК Выдано,
	|	ОстаткиИОборотыПоДаннымБУ.Погашено КАК Погашено,
	|	ОстаткиИОборотыПоДаннымБУ.КонечныйОстаток КАК КонечныйОстаток,
	|	ОстаткиИОборотыПоДаннымБУ.ПроцентыНачальныйОстаток КАК ПроцентыНачальныйОстаток,
	|	ОстаткиИОборотыПоДаннымБУ.Начислено КАК Начислено,
	|	ОстаткиИОборотыПоДаннымБУ.Оплачено КАК Оплачено,
	|	ОстаткиИОборотыПоДаннымБУ.ПроцентыКонечныйОстаток КАК ПроцентыКонечныйОстаток
	|ПОМЕСТИТЬ ОстаткиИОборотыПоОсновномуДолгуИПроцентам
	|ИЗ
	|	ОстаткиИОборотыПоДаннымБУ КАК ОстаткиИОборотыПоДаннымБУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента = ДоговорыКонтрагентов.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодразделенияДляНачислений КАК ПодразделенияДляНачислений
	|		ПО ОстаткиИОборотыПоДаннымБУ.ДоговорКонтрагента = ПодразделенияДляНачислений.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДоговорыКонтрагентов.ДатаДоговора КАК ДатаДоговора,
	|	ДоговорыКонтрагентов.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ДоговорыКонтрагентов.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ДоговорыКонтрагентов.РасчетыВВалюте КАК РасчетыВВалюте,
	|	ДоговорыКонтрагентов.ИспользоватьОтсрочкуПоСтавкеЦБ КАК ИспользоватьОтсрочкуПоСтавкеЦБ
	|ПОМЕСТИТЬ ДоговорыДляОтбора
	|ИЗ
	|	ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ДоговорКонтрагента В
	|			(ВЫБРАТЬ
	|				ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ДоговорКонтрагента
	|			ИЗ
	|				ОстаткиИОборотыПоОсновномуДолгуИПроцентам)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОстаткиИОборотыПоДаннымБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДоговорыКонтрагентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОборотыБУСРеклассификацией.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(ОстаткиИОборотыБУСРеклассификацией.Контрагент) КАК КонтрагентПредставление,
	|	ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента КАК ДоговорСсылка,
	|	ПРЕДСТАВЛЕНИЕ(ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента) КАК ДоговорПредставление,
	|	ОстаткиИОборотыБУСРеклассификацией.Подразделение КАК Подразделение,
	|	ОстаткиИОборотыБУСРеклассификацией.Валюта КАК Валюта,
	|	ПРЕДСТАВЛЕНИЕ(МИНИМУМ(ОстаткиИОборотыБУСРеклассификацией.Счет)) КАК Счет1,
	|	ПРЕДСТАВЛЕНИЕ(МАКСИМУМ(ОстаткиИОборотыБУСРеклассификацией.Счет)) КАК Счет2
	|ИЗ
	|	ОстаткиИОборотыБУСРеклассификацией КАК ОстаткиИОборотыБУСРеклассификацией
	|ГДЕ
	|	ОстаткиИОборотыБУСРеклассификацией.Счет В(&СчетаУчетаЗаймовПолученных)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОборотыБУСРеклассификацией.Валюта,
	|	ОстаткиИОборотыБУСРеклассификацией.Подразделение,
	|	ОстаткиИОборотыБУСРеклассификацией.Организация,
	|	ОстаткиИОборотыБУСРеклассификацией.Контрагент,
	|	ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОстаткиИОборотыБУСРеклассификацией.Счет) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиИОборотыБУСРеклассификацией.Организация,
	|	ПРЕДСТАВЛЕНИЕ(ОстаткиИОборотыБУСРеклассификацией.Контрагент),
	|	ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента,
	|	ПРЕДСТАВЛЕНИЕ(ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента),
	|	ОстаткиИОборотыБУСРеклассификацией.Подразделение,
	|	ОстаткиИОборотыБУСРеклассификацией.Валюта,
	|	ПРЕДСТАВЛЕНИЕ(МИНИМУМ(ОстаткиИОборотыБУСРеклассификацией.Счет)),
	|	ПРЕДСТАВЛЕНИЕ(МАКСИМУМ(ОстаткиИОборотыБУСРеклассификацией.Счет))
	|ИЗ
	|	ОстаткиИОборотыБУСРеклассификацией КАК ОстаткиИОборотыБУСРеклассификацией
	|ГДЕ
	|	ОстаткиИОборотыБУСРеклассификацией.Счет В(&СчетаУчетаПроцентовПоЗаймамПолученным)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОборотыБУСРеклассификацией.Валюта,
	|	ОстаткиИОборотыБУСРеклассификацией.Подразделение,
	|	ОстаткиИОборотыБУСРеклассификацией.Организация,
	|	ОстаткиИОборотыБУСРеклассификацией.Контрагент,
	|	ОстаткиИОборотыБУСРеклассификацией.ДоговорКонтрагента
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОстаткиИОборотыБУСРеклассификацией.Счет) > 1
	|ИТОГИ ПО
	|	ДоговорСсылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		// Проверка на корректность заполнения счетов учета при заполнении поступления / списания.
		ПроверкаСчетовДоговор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ПроверкаСчетовДоговор.Следующий() Цикл
			ПроверкаСчетов = ПроверкаСчетовДоговор.Выбрать();
			Пока ПроверкаСчетов.Следующий() Цикл
				ШаблонСообщения = НСтр("ru = 'В документах поступления и списания различаются счета учета (%1, %2)
					|Контрагент: %3, договор: %4.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, 
					ПроверкаСчетов.Счет1,
					ПроверкаСчетов.Счет2,
					ПроверкаСчетов.КонтрагентПредставление, 
					ПроверкаСчетов.ДоговорПредставление);
				
				Ошибка = ВыводСообщенийОбОшибках.ДобавитьПростоеОписаниеОшибки(Ошибки);
				Ошибка.Рекомендация = НСтр("ru = 'Необходимо проверить корректность счетов учета в документах списания / поступления по договору займа.'");
				Ошибка.Ссылка = ПроверкаСчетов.ДоговорСсылка;
				Ошибка.Описание = ТекстСообщения;
				
				Отказ = Истина;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Сформируем таблицу по процентным ставками договоров со всеми срезами из истории и регистра ставок ЦБ.
	Запрос.Текст =
	"УНИЧТОЖИТЬ ОстаткиИОборотыБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.Ссылка КАК ДоговорКонтрагента,
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.Период КАК Период,
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ДоговорыДляОтбора.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ДоговорыДляОтбора.ИспользоватьОтсрочкуПоСтавкеЦБ КАК ИспользоватьОтсрочкуПоСтавкеЦБ
	|ПОМЕСТИТЬ ИсторияПроцентныхСтавок
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов.ИсторияПроцентныхСтавок КАК ДоговорыКонтрагентовИсторияПроцентныхСтавок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговорыДляОтбора КАК ДоговорыДляОтбора
	|		ПО ДоговорыКонтрагентовИсторияПроцентныхСтавок.Ссылка = ДоговорыДляОтбора.ДоговорКонтрагента
	|ГДЕ
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.Период <= &КонецМесяца
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыДляОтбора.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
	|	ДоговорыДляОтбора.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ДоговорыДляОтбора.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки
	|ПОМЕСТИТЬ ИсторияПроцентныхСтавокДоговоров
	|ИЗ
	|	ДоговорыДляОтбора КАК ДоговорыДляОтбора
	|ГДЕ
	|	НЕ ДоговорыДляОтбора.ДоговорКонтрагента В
	|				(ВЫБРАТЬ
	|					ИсторияПроцентныхСтавок.ДоговорКонтрагента
	|				ИЗ
	|					ИсторияПроцентныхСтавок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсторияПроцентныхСтавок.ДоговорКонтрагента,
	|	ИсторияПроцентныхСтавок.Период,
	|	ИсторияПроцентныхСтавок.ПроцентнаяСтавка,
	|	ИсторияПроцентныхСтавок.ТипПроцентнойСтавки
	|ИЗ
	|	ИсторияПроцентныхСтавок КАК ИсторияПроцентныхСтавок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(ОбъединениеДатСтавок.Период, ДЕНЬ, -1) КАК Период,
	|	ОбъединениеДатСтавок.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОбъединениеДатСтавок.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ОбъединениеДатСтавок.ИспользоватьОтсрочкуПоСтавкеЦБ КАК ИспользоватьОтсрочкуПоСтавкеЦБ
	|ПОМЕСТИТЬ ВсеДатыИзмененияСтавок
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсторияПроцентныхСтавок.Период КАК Период,
	|		ИсторияПроцентныхСтавок.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		ИсторияПроцентныхСтавок.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|		ИсторияПроцентныхСтавок.ИспользоватьОтсрочкуПоСтавкеЦБ КАК ИспользоватьОтсрочкуПоСтавкеЦБ
	|	ИЗ
	|		ИсторияПроцентныхСтавок КАК ИсторияПроцентныхСтавок
	|	ГДЕ
	|		ИсторияПроцентныхСтавок.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ДоговорыДляОтбора.ИспользоватьОтсрочкуПоСтавкеЦБ
	|				ТОГДА ДОБАВИТЬКДАТЕ(СтавкаРефинансированияЦБ.Период, ДЕНЬ, 1)
	|			ИНАЧЕ СтавкаРефинансированияЦБ.Период
	|		КОНЕЦ,
	|		ДоговорыДляОтбора.ДоговорКонтрагента,
	|		ДоговорыДляОтбора.ТипПроцентнойСтавки,
	|		ДоговорыДляОтбора.ИспользоватьОтсрочкуПоСтавкеЦБ
	|	ИЗ
	|		ДоговорыДляОтбора КАК ДоговорыДляОтбора
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкаРефинансированияЦБ КАК СтавкаРефинансированияЦБ
	|			ПО (ДоговорыДляОтбора.ТипПроцентнойСтавки = ЗНАЧЕНИЕ(Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ))
	|	ГДЕ
	|		СтавкаРефинансированияЦБ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца) КАК ОбъединениеДатСтавок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Период КАК Период,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ИспользоватьОтсрочкуПоСтавкеЦБ КАК ИспользоватьОтсрочкуПоСтавкеЦБ
	|ПОМЕСТИТЬ ВсеДатыДляСрезаСтавок
	|ИЗ
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам КАК ОстаткиИОборотыПоОсновномуДолгуИПроцентам
	|ГДЕ
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ТипПроцентнойСтавки <> ЗНАЧЕНИЕ(Перечисление.ТипыПроцентныхСтавок.БезНачисленияПроцентов)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВсеДатыИзмененияСтавок.Период,
	|	ВсеДатыИзмененияСтавок.ДоговорКонтрагента,
	|	ВсеДатыИзмененияСтавок.ТипПроцентнойСтавки,
	|	ВсеДатыИзмененияСтавок.ИспользоватьОтсрочкуПоСтавкеЦБ
	|ИЗ
	|	ВсеДатыИзмененияСтавок КАК ВсеДатыИзмененияСтавок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ТипПроцентнойСтавки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(РегистрСведений.Период) КАК Период,
	|	ИзмеренияДаты.Период КАК ЗаданныйПериод,
	|	ИзмеренияДаты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ИзмеренияДаты.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки
	|ПОМЕСТИТЬ МаксимальныеПериодыСрезаЦБ
	|ИЗ
	|	ВсеДатыДляСрезаСтавок КАК ИзмеренияДаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкаРефинансированияЦБ КАК РегистрСведений
	|		ПО (ВЫБОР
	|				КОГДА ИзмеренияДаты.ИспользоватьОтсрочкуПоСтавкеЦБ
	|					ТОГДА ДОБАВИТЬКДАТЕ(РегистрСведений.Период, ДЕНЬ, 1)
	|				ИНАЧЕ РегистрСведений.Период
	|			КОНЕЦ <= ИзмеренияДаты.Период)
	|			И (ИзмеренияДаты.ТипПроцентнойСтавки = ЗНАЧЕНИЕ(Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ))
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмеренияДаты.Период,
	|	ИзмеренияДаты.ДоговорКонтрагента,
	|	ИзмеренияДаты.ТипПроцентнойСтавки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ТипПроцентнойСтавки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыСреза.Период КАК Период,
	|	ДатыСреза.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	МАКСИМУМ(ДатыСреза.ПроцентнаяСтавка) КАК ПроцентнаяСтавка
	|ПОМЕСТИТЬ СрезСтавокЦБСДатамиДоговоров
	|ИЗ
	|	(ВЫБРАТЬ
	|		МаксимальныеПериоды.ЗаданныйПериод КАК Период,
	|		МаксимальныеПериоды.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		РегистрСведений.Размер КАК ПроцентнаяСтавка
	|	ИЗ
	|		МаксимальныеПериодыСрезаЦБ КАК МаксимальныеПериоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкаРефинансированияЦБ КАК РегистрСведений
	|			ПО (РегистрСведений.Период = МаксимальныеПериоды.Период)
	|				И (МаксимальныеПериоды.ТипПроцентнойСтавки = ЗНАЧЕНИЕ(Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВсеДатыДляСрезаСтавок.Период,
	|		ВсеДатыДляСрезаСтавок.ДоговорКонтрагента,
	|		0
	|	ИЗ
	|		ВсеДатыДляСрезаСтавок КАК ВсеДатыДляСрезаСтавок) КАК ДатыСреза
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыСреза.Период,
	|	ДатыСреза.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияПроцентныхСтавокДоговоров.Период) КАК Период,
	|	ИзмеренияДаты.Период КАК ЗаданныйПериод,
	|	ИзмеренияДаты.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ПОМЕСТИТЬ МаксимальныеПериодыСрезаСтавокДоговора
	|ИЗ
	|	СрезСтавокЦБСДатамиДоговоров КАК ИзмеренияДаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИсторияПроцентныхСтавокДоговоров КАК ИсторияПроцентныхСтавокДоговоров
	|		ПО (ИсторияПроцентныхСтавокДоговоров.Период <= ИзмеренияДаты.Период)
	|			И ИзмеренияДаты.ДоговорКонтрагента = ИсторияПроцентныхСтавокДоговоров.ДоговорКонтрагента
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмеренияДаты.Период,
	|	ИзмеренияДаты.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.ЗаданныйПериод КАК Период,
	|	ИсторияПроцентныхСтавокДоговоров.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	МаксимальныеПериоды.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ПОМЕСТИТЬ СтавкиДоговоровСрезПоследних
	|ИЗ
	|	МаксимальныеПериодыСрезаСтавокДоговора КАК МаксимальныеПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсторияПроцентныхСтавокДоговоров КАК ИсторияПроцентныхСтавокДоговоров
	|		ПО МаксимальныеПериоды.ДоговорКонтрагента = ИсторияПроцентныхСтавокДоговоров.ДоговорКонтрагента
	|			И МаксимальныеПериоды.Период = ИсторияПроцентныхСтавокДоговоров.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиДоговоровСрезПоследних.Период КАК Период,
	|	СтавкиДоговоровСрезПоследних.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ДоговорыДляОтбора.ТипПроцентнойСтавки = ЗНАЧЕНИЕ(Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ)
	|			ТОГДА СтавкиДоговоровСрезПоследних.ПроцентнаяСтавка + СрезСтавокЦБСДатамиДоговоров.ПроцентнаяСтавка
	|		ИНАЧЕ СтавкиДоговоровСрезПоследних.ПроцентнаяСтавка
	|	КОНЕЦ КАК ПроцентнаяСтавка,
	|	ВЫБОР
	|		КОГДА ДоговорыДляОтбора.ТипПроцентнойСтавки = ЗНАЧЕНИЕ(Перечисление.ТипыПроцентныхСтавок.СтавкаЦБ)
	|			ТОГДА СрезСтавокЦБСДатамиДоговоров.ПроцентнаяСтавка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаЦБ
	|ПОМЕСТИТЬ ПроцентныеСтавкиПоДоговорам
	|ИЗ
	|	СрезСтавокЦБСДатамиДоговоров КАК СрезСтавокЦБСДатамиДоговоров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтавкиДоговоровСрезПоследних КАК СтавкиДоговоровСрезПоследних
	|		ПО СрезСтавокЦБСДатамиДоговоров.Период = СтавкиДоговоровСрезПоследних.Период
	|			И СрезСтавокЦБСДатамиДоговоров.ДоговорКонтрагента = СтавкиДоговоровСрезПоследних.ДоговорКонтрагента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговорыДляОтбора КАК ДоговорыДляОтбора
	|		ПО СрезСтавокЦБСДатамиДоговоров.ДоговорКонтрагента = ДоговорыДляОтбора.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Период) КАК Период,
	|	ИзмеренияДаты.Период КАК ЗаданныйПериод,
	|	ИзмеренияДаты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ИзмеренияДаты.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ИзмеренияДаты.СтавкаЦБ КАК СтавкаЦБ
	|ПОМЕСТИТЬ МаксимальныеПериодыСрезаОстатковБУ
	|ИЗ
	|	ПроцентныеСтавкиПоДоговорам КАК ИзмеренияДаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиИОборотыПоОсновномуДолгуИПроцентам КАК ОстаткиИОборотыПоОсновномуДолгуИПроцентам
	|		ПО (ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Период <= ИзмеренияДаты.Период)
	|			И ИзмеренияДаты.ДоговорКонтрагента = ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ДоговорКонтрагента
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмеренияДаты.Период,
	|	ИзмеренияДаты.ДоговорКонтрагента,
	|	ИзмеренияДаты.ПроцентнаяСтавка,
	|	ИзмеренияДаты.СтавкаЦБ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.ЗаданныйПериод КАК Период,
	|	МаксимальныеПериоды.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Валюта КАК Валюта,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|	МаксимальныеПериоды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	МаксимальныеПериоды.СтавкаЦБ КАК СтавкаЦБ,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Организация КАК Организация,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Подразделение КАК Подразделение,
	|	ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ СтавкиПоДоговорамСДаннымиБУ
	|ИЗ
	|	МаксимальныеПериодыСрезаОстатковБУ КАК МаксимальныеПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиИОборотыПоОсновномуДолгуИПроцентам КАК ОстаткиИОборотыПоОсновномуДолгуИПроцентам
	|		ПО МаксимальныеПериоды.ДоговорКонтрагента = ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ДоговорКонтрагента
	|			И МаксимальныеПериоды.Период = ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИсторияПроцентныхСтавок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИсторияПроцентныхСтавокДоговоров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеДатыДляСрезаСтавок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МаксимальныеПериодыСрезаЦБ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МаксимальныеПериодыСрезаСтавокДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СрезСтавокЦБСДатамиДоговоров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтавкиДоговоровСрезПоследних";
	
	Запрос.Выполнить();
	
	// Итоговая выборка для начисления процентов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДляОбъединения.ПериодРасчета КАК ПериодРасчета,
	|	ВЫБОР
	|		КОГДА СУММА(ДанныеДляОбъединения.НачальныйОстаток) = 0
	|				И СУММА(ДанныеДляОбъединения.Выдано) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоДеньВыдачиЗайма,
	|	ДанныеДляОбъединения.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|	ДанныеДляОбъединения.Организация КАК Организация,
	|	ДанныеДляОбъединения.Подразделение КАК Подразделение,
	|	ДанныеДляОбъединения.Контрагент КАК Контрагент,
	|	ДанныеДляОбъединения.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДанныеДляОбъединения.Валюта КАК Валюта,
	|	МАКСИМУМ(ДанныеДляОбъединения.ПроцентнаяСтавка) КАК ПроцентнаяСтавка,
	|	МАКСИМУМ(ДанныеДляОбъединения.СтавкаЦБ) КАК СтавкаЦБ,
	|	СУММА(ДанныеДляОбъединения.НачальныйОстаток) КАК НачальныйОстаток,
	|	СУММА(ДанныеДляОбъединения.Выдано) КАК Выдано,
	|	СУММА(ДанныеДляОбъединения.Погашено) КАК Погашено,
	|	СУММА(ДанныеДляОбъединения.КонечныйОстаток) КАК КонечныйОстаток,
	|	СУММА(ДанныеДляОбъединения.ПроцентыНачальныйОстаток) КАК ПроцентыНачальныйОстаток,
	|	СУММА(ДанныеДляОбъединения.Начислено) КАК Начислено,
	|	СУММА(ДанныеДляОбъединения.Оплачено) КАК Оплачено,
	|	СУММА(ДанныеДляОбъединения.ПроцентыКонечныйОстаток) КАК ПроцентыКонечныйОстаток
	|ПОМЕСТИТЬ ДанныеДляРасчетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Период КАК ПериодРасчета,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Организация КАК Организация,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Подразделение КАК Подразделение,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Контрагент КАК Контрагент,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Валюта КАК Валюта,
	|		0 КАК ПроцентнаяСтавка,
	|		0 КАК СтавкаЦБ,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.НачальныйОстаток КАК НачальныйОстаток,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Выдано КАК Выдано,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Погашено КАК Погашено,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.КонечныйОстаток КАК КонечныйОстаток,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ПроцентыНачальныйОстаток КАК ПроцентыНачальныйОстаток,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Начислено КАК Начислено,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.Оплачено КАК Оплачено,
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам.ПроцентыКонечныйОстаток КАК ПроцентыКонечныйОстаток
	|	ИЗ
	|		ОстаткиИОборотыПоОсновномуДолгуИПроцентам КАК ОстаткиИОборотыПоОсновномуДолгуИПроцентам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтавкиПоДоговорамСДаннымиБУ.Период,
	|		СтавкиПоДоговорамСДаннымиБУ.СчетУчетаПроцентов,
	|		СтавкиПоДоговорамСДаннымиБУ.Организация,
	|		СтавкиПоДоговорамСДаннымиБУ.Подразделение,
	|		СтавкиПоДоговорамСДаннымиБУ.Контрагент,
	|		СтавкиПоДоговорамСДаннымиБУ.ДоговорКонтрагента,
	|		СтавкиПоДоговорамСДаннымиБУ.Валюта,
	|		СтавкиПоДоговорамСДаннымиБУ.ПроцентнаяСтавка,
	|		СтавкиПоДоговорамСДаннымиБУ.СтавкаЦБ,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		СтавкиПоДоговорамСДаннымиБУ КАК СтавкиПоДоговорамСДаннымиБУ) КАК ДанныеДляОбъединения
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОбъединения.ПериодРасчета,
	|	ДанныеДляОбъединения.СчетУчетаПроцентов,
	|	ДанныеДляОбъединения.Организация,
	|	ДанныеДляОбъединения.Подразделение,
	|	ДанныеДляОбъединения.Контрагент,
	|	ДанныеДляОбъединения.ДоговорКонтрагента,
	|	ДанныеДляОбъединения.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	ПериодРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляРасчетов.ПериодРасчета КАК ПериодРасчета,
	|	ДанныеДляРасчетов.ЭтоДеньВыдачиЗайма КАК ЭтоДеньВыдачиЗайма,
	|	ВЫБОР
	|		КОГДА ВсеДатыИзмененияСтавок.Период ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоИзменениеСтавки,
	|	ДанныеДляРасчетов.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|	ДанныеДляРасчетов.Организация КАК Организация,
	|	ДанныеДляРасчетов.Подразделение КАК Подразделение,
	|	ДанныеДляРасчетов.Контрагент КАК Контрагент,
	|	ДанныеДляРасчетов.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДоговорыДляОтбора.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ВЫБОР
	|		КОГДА ДоговорыДляОтбора.РасчетыВВалюте
	|			ТОГДА ДанныеДляРасчетов.Валюта
	|		ИНАЧЕ &ВалютаРеглУчета
	|	КОНЕЦ КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.Курс, 0) КАК Курс,
	|	ЕСТЬNULL(КурсыВалют.Кратность, 0) КАК Кратность,
	|	ДанныеДляРасчетов.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ДанныеДляРасчетов.СтавкаЦБ КАК СтавкаЦБ,
	|	ДанныеДляРасчетов.ПроцентнаяСтавка КАК РасчетнаяПроцентнаяСтавка,
	|	ДанныеДляРасчетов.СтавкаЦБ КАК РасчетнаяСтавкаЦБ,
	|	0 КАК ПериодРасчетаДней,
	|	0 КАК НачисленоВал,
	|	ДанныеДляРасчетов.НачальныйОстаток КАК НачальныйОстаток,
	|	ДанныеДляРасчетов.Выдано КАК Выдано,
	|	ДанныеДляРасчетов.Погашено КАК Погашено,
	|	ДанныеДляРасчетов.КонечныйОстаток КАК КонечныйОстаток,
	|	ДанныеДляРасчетов.ПроцентыНачальныйОстаток КАК ПроцентыНачальныйОстаток,
	|	ДанныеДляРасчетов.Начислено КАК Начислено,
	|	ДанныеДляРасчетов.Оплачено КАК Оплачено,
	|	ДанныеДляРасчетов.ПроцентыКонечныйОстаток КАК ПроцентыКонечныйОстаток,
	|	ВЫБОР
	|		КОГДА ДанныеДляРасчетов.Выдано <> 0
	|				ИЛИ ДанныеДляРасчетов.Погашено <> 0
	|				ИЛИ ДанныеДляРасчетов.Оплачено <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОбороты,
	|	ВЫБОР
	|		КОГДА ДанныеДляРасчетов.Выдано = 0
	|				И ДанныеДляРасчетов.Погашено = 0
	|				И ДанныеДляРасчетов.Оплачено <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоОплатаПроцентов
	|ИЗ
	|	ДанныеДляРасчетов КАК ДанныеДляРасчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговорыДляОтбора КАК ДоговорыДляОтбора
	|		ПО ДанныеДляРасчетов.ДоговорКонтрагента = ДоговорыДляОтбора.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеДатыИзмененияСтавок КАК ВсеДатыИзмененияСтавок
	|		ПО ДанныеДляРасчетов.ПериодРасчета = ВсеДатыИзмененияСтавок.Период
	|			И ДанныеДляРасчетов.ДоговорКонтрагента = ВсеДатыИзмененияСтавок.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ДанныеДляРасчетов.ПериодРасчета = КурсыВалют.Период
	|			И ДанныеДляРасчетов.Валюта = КурсыВалют.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорКонтрагента,
	|	ПериодРасчета";
	
	ТаблицаРасчет = Запрос.Выполнить().Выгрузить();
	
	Сутки = 60 * 60 * 24;
	КурсРубля = Новый Структура("Валюта, Курс, Кратность", ВалютаРеглУчета, 1, 1);
	ПредыдущаяСтрока = Неопределено;
	ПредыдущийПериодРасчета = Неопределено;
	ДобавитьДень = Ложь;
	ТаблицаРасчетСтрокиДляУдаления = Новый Массив;
	КоличествоДнейВГоду = ДеньГода(КонецГода(Реквизиты.Период));
	
	Для Каждого ТекущаяСтрока Из ТаблицаРасчет Цикл
		
		Если Не ЗначениеЗаполнено(ПредыдущаяСтрока) Или ПредыдущаяСтрока.ДоговорКонтрагента <> ТекущаяСтрока.ДоговорКонтрагента Тогда
			
			// Это первая запись по договору. В ней содержится начало месяца, а также день выдачи займа.
			ПредыдущаяСтрока = ТекущаяСтрока;
			ПредыдущийПериодРасчета = ТекущаяСтрока.ПериодРасчета;
			ДобавитьДень = Ложь;
			
			Если ТекущаяСтрока.ЕстьОбороты Или ТекущаяСтрока.ЭтоДеньВыдачиЗайма Или ТекущаяСтрока.ЭтоИзменениеСтавки Тогда
				ТекущаяСтрока.ПериодРасчетаДней = 1;
				РассчитатьДанныеПоСтрокеВыданныхЗаймов(ТекущаяСтрока, КурсРубля, КоличествоДнейВГоду);
			Иначе
				// Это начало месяца без каких-либо движений. Текущую строку отображать в справке-расчете нет необходимости.
				ТаблицаРасчетСтрокиДляУдаления.Добавить(ТекущаяСтрока);
				ДобавитьДень = Истина;
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
		// Заполним необходимые для расчета данные.
		ТекущаяСтрока.НачальныйОстаток = ПредыдущаяСтрока.КонечныйОстаток;
		ТекущаяСтрока.ПроцентыНачальныйОстаток = ПредыдущаяСтрока.ПроцентыКонечныйОстаток;
		
		ЭтоКонецМесяца = КонецДня(ТекущаяСтрока.ПериодРасчета) = КонецМесяца(ТекущаяСтрока.ПериодРасчета);
		
		Если ТекущаяСтрока.ЭтоИзменениеСтавки Или ЭтоКонецМесяца Или Не ТекущаяСтрока.ТолькоОплатаПроцентов Тогда
			
			ТекущаяСтрока.ПериодРасчетаДней = День(ТекущаяСтрока.ПериодРасчета) - День(ПредыдущийПериодРасчета);
			Если ДобавитьДень Тогда
				ТекущаяСтрока.ПериодРасчетаДней = ТекущаяСтрока.ПериодРасчетаДней + 1;
				ДобавитьДень = Ложь;
			КонецЕсли;
			
			ПредыдущийПериодРасчета = ТекущаяСтрока.ПериодРасчета;
			
		КонецЕсли;
		
		РассчитатьДанныеПоСтрокеВыданныхЗаймов(ТекущаяСтрока, КурсРубля, КоличествоДнейВГоду);
		
		ПредыдущаяСтрока = ТекущаяСтрока;
		
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаРасчетСтрокиДляУдаления Цикл
		// Удалим из справки-расчета строки, которые не требуется отображать.
		ТаблицаРасчет.Удалить(Строка);
	КонецЦикла;
	
	// Сформируем таблицу проводок БУ.
	ПредыдущаяСтрока = Неопределено;
	ТаблицаПроцентыСтрокиДляУдаления = Новый Массив;
	
	ТаблицаПроценты = ТаблицаРасчет.Скопировать(, "СчетУчетаПроцентов, Подразделение, Контрагент, ДоговорКонтрагента, Валюта, ПериодРасчета, ПериодРасчетаДней, Начислено, НачисленоВал, ПроцентнаяСтавка, НачальныйОстаток");
	ТаблицаПроценты.Колонки.Найти("НачальныйОстаток").Имя  = "СуммаДолгаВал";
	ТаблицаПроценты.Колонки.Найти("ПериодРасчета").Имя = "НачисленоПоДату";
	ТаблицаПроценты.Колонки.Найти("ПериодРасчетаДней").Имя = "КоличествоДней";
	ТаблицаПроценты.Колонки.Найти("НачисленоВал").Имя = "СуммаПроцентовВал";
	ТаблицаПроценты.Колонки.Найти("Начислено").Имя = "СуммаПроцентов";
	ТаблицаПроценты.Колонки.Добавить("НачисленоСДаты", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	
	Для Каждого ТекущаяСтрока Из ТаблицаПроценты Цикл
		
		Если ТекущаяСтрока.КоличествоДней = 0 Тогда
			ТаблицаПроцентыСтрокиДляУдаления.Добавить(ТекущаяСтрока);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПредыдущаяСтрока) Или ПредыдущаяСтрока.ДоговорКонтрагента <> ТекущаяСтрока.ДоговорКонтрагента Тогда
			// Перешли к новому договору.
			Если ТекущаяСтрока.НачисленоПоДату = НачалоМесяца(ТекущаяСтрока.НачисленоПоДату) Тогда
				// Был выполнен расчет за первое число месяца, отметим эту информации в описании проводки.
				ТекущаяСтрока.НачисленоСДаты = ТекущаяСтрока.НачисленоПоДату;
			Иначе
				// Строка с первым днём месяца отсутствует, укажем в описании проводки, что расчет выполнен с 01 числа.
				ТекущаяСтрока.НачисленоСДаты = НачалоМесяца(ТекущаяСтрока.НачисленоПоДату);
			КонецЕсли;
			
			ПредыдущаяСтрока = ТекущаяСтрока;
			Продолжить;
		КонецЕсли;
		
		ТекущаяСтрока.НачисленоСДаты = ПредыдущаяСтрока.НачисленоПоДату + Сутки;
		ПредыдущаяСтрока = ТекущаяСтрока;
		
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаПроцентыСтрокиДляУдаления Цикл
		// Удалим из таблицы проводок строки, в которых не было фактического начисления процентов.
		ТаблицаПроценты.Удалить(Строка);
	КонецЦикла;
    
	СтруктураТаблиц.Вставить("ТаблицаПроценты", ТаблицаПроценты);
	СтруктураТаблиц.Вставить("ТаблицаРасчет", ТаблицаРасчет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьПараметрыНачислениеПроцентовПоКредитамИЗаймам(ТаблицаПроценты, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.ТаблицаПроценты
	
	СписокОбязательныхКолонок = ""
	+ "Подразделение,"			// <СправочникСсылка.ПодразделенияОрганизаций> - подразделение учета остатка основного долга
	+ "Контрагент,"				// <СправочникСсылка.Контрагенты> - заимодавец
	+ "ДоговорКонтрагента,"		// <СправочникСсылка.ДоговорыКонтрагентов> - договор кредита/займа
	+ "Валюта,"       			// <СправочникСсылка.Валюты> - валюта взаиморасчетов договора кредита/займа
	+ "СчетУчетаПроцентов,"		// <ПланСчетовСсылка.Хозрасчетный> - счет учета остатка основного долга
	+ "СуммаДолгаВал,"			// <Число,15,2> - сумма долга, на которую начислена сумма процентов
	+ "НачисленоСДаты,"			// <Дата> - Дата начала периода начисления процентов
	+ "НачисленоПоДату,"		// <Дата> - Дата окончания периода начисления процентов
	+ "КоличествоДней,"			// <Число,5,0> - количество дней
	+ "ПроцентнаяСтавка,"		// <Число,5,2> - процентная ставка
	+ "СуммаПроцентовВал,"		// <Число,15,2> - сумма начисленных процентов в валюте договора
	+ "СуммаПроцентов";			// <Число,15,2> - сумма начисленных процентов в рублях
	
	Параметры.Вставить("ТаблицаПроценты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаПроценты, СписокОбязательныхКолонок));
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"			// <Дата> - период движений - дата документа
	+ "Организация,"	// <СправочникСсылка.Организация>
	+ "Содержание";		// <Строка> - содержание проводок
	
	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Процедура РассчитатьДанныеПоСтрокеВыданныхЗаймов(ТекущаяСтрока, КурсРубля, КоличествоДнейВГоду)
	
	СуммаПроцентовВал = Окр(ТекущаяСтрока.НачальныйОстаток * (ТекущаяСтрока.ПроцентнаяСтавка / 100)
					  * ТекущаяСтрока.ПериодРасчетаДней / КоличествоДнейВГоду, 2);
	
	СуммаПроцентовРуб = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(СуммаПроцентовВал, ТекущаяСтрока, КурсРубля);
	
	ТекущаяСтрока.НачисленоВал = СуммаПроцентовВал;
	ТекущаяСтрока.Начислено = СуммаПроцентовРуб;
	
	Если ТекущаяСтрока.НачисленоВал = 0 Тогда
		ТекущаяСтрока.ПериодРасчетаДней = 0;
	КонецЕсли;
	
	// Рассчитаем конечные остатки.
	ТекущаяСтрока.КонечныйОстаток = ТекущаяСтрока.НачальныйОстаток + ТекущаяСтрока.Выдано - ТекущаяСтрока.Погашено;
	ТекущаяСтрока.ПроцентыКонечныйОстаток = ТекущаяСтрока.ПроцентыНачальныйОстаток + СуммаПроцентовВал - ТекущаяСтрока.Оплачено;
	
КонецПроцедуры

#КонецОбласти
