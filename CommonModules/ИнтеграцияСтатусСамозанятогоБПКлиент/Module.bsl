#Область СлужебныйПрограммныйИнтерфейс

// Возвращает дату, на которую будет получен статус самозанятого, приведенную к часовому поясу "Europe/Moscow"
//
// Параметры:
//  ОсновнаяДата - Дата
//  ДатаВходящегоДокумента - Дата - если в документе заполнена дата входящего документа, то
//                           проверка будет выполнена на эту дату
//
// Возвращаемое значение:
//  Дата - не пустая дата, не превышающая текущей даты сеанса. Даты приведены к часовому поясу "Europe/Moscow"
//
Функция ДатаПолученияСтатуса(ОсновнаяДата, ДатаВходящегоДокумента = Неопределено) Экспорт
	
	ДатаПроверки = ?(ЗначениеЗаполнено(ДатаВходящегоДокумента), ДатаВходящегоДокумента, ОсновнаяДата);
	Если ЗначениеЗаполнено(ДатаПроверки) Тогда
		ЧасовойПояс = ОбщегоНазначенияБПВызовСервера.ЧасовойПоясИнформационнойБазы();
	Иначе
		ДатаПроверки = ОбщегоНазначенияКлиент.ДатаУниверсальная();
		ЧасовойПояс = "UTC";
	КонецЕсли;
	Результат = ИнтеграцияСтатусСамозанятогоВызовСервера.ДатаНеПозжеТекущейПоЧасовомуПоясуСервиса(
		ДатаПроверки, ЧасовойПояс);
	Возврат НачалоДня(Результат);
	
КонецФункции

// Возвращает статус самозанятого контрагента, получив его из записанных данных. При необходимости инициирует
// обращение к сервису, если в базе нет записанных данных о статусе или данные требуют обновления.
//
// Параметры:
//  ФормаВладелец - ФормаКлиентскогоПриложения - форма, из которой была вызвана эта функция
//  ИсточникДанных - СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.Контрагенты - источник данных
//                   о самозанятом контрагенте
//  ДатаПолученияСтатуса - Дата - дата в часовом поясе "Europe/Moscow",
//                         на которую получается информация о статусе самозанятого
//
// Возвращаемое значение:
//  Структура, Неопределено - см. РегистрыСведений.СтатусыСамозанятых.ПлательщикНПД
//
Функция СтатусСамозанятого(ФормаВладелец, ИсточникДанных, ДатаПолученияСтатуса) Экспорт
	
	ПараметрыСтатуса = ИнтеграцияСтатусСамозанятогоБПВызовСервера.ПараметрыПолученияСтатуса(
		ИсточникДанных, ДатаПолученияСтатуса);
	
	СохраненныйСтатус = СохраненныйСтатус(ФормаВладелец, ИсточникДанных, ДатаПолученияСтатуса, ПараметрыСтатуса);
	
	Возврат СохраненныйСтатус;
	
КонецФункции

// Возвращает статус самозанятого контрагента, получив его из записанных данных. При необходимости инициирует
// обращение к сервису, если в базе нет записанных данных о статусе или данные требуют обновления.
//
// Параметры:
//  ФормаВладелец - ФормаКлиентскогоПриложения - форма, из которой была вызвана эта функция
//  ИсточникДанных - СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.Контрагенты - источник данных
//                   о самозанятом контрагенте
//  ДатаПолученияСтатуса - Дата - дата в часовом поясе "Europe/Moscow",
//                         на которую получается информация о статусе самозанятого
//
// Возвращаемое значение:
//  Структура, Неопределено - см. РегистрыСведений.СтатусыСамозанятых.ПлательщикНПД
//
Функция СтатусСамозанятогоДляДоговора(ФормаВладелец, ИсточникДанных, ДатаПолученияСтатуса) Экспорт
	
	ПараметрыСтатуса = ИнтеграцияСтатусСамозанятогоБПВызовСервера.ПараметрыПолученияСтатуса(
		ИсточникДанных, ДатаПолученияСтатуса, Истина);
	
	СохраненныйСтатус = СохраненныйСтатус(ФормаВладелец, ИсточникДанных, ДатаПолученияСтатуса, ПараметрыСтатуса);
	
	Возврат СохраненныйСтатус;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СохраненныйСтатус(ФормаВладелец, ИсточникДанных, ДатаПолученияСтатуса, ПараметрыСтатуса)
	
	Если Не ПараметрыСтатуса.ВыполнятьПроверкуСтатуса Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СохраненныйСтатус = ПараметрыСтатуса.СохраненныйСтатус;
	Если СохраненныйСтатус = Неопределено Или Не СохраненныйСтатус.ЭтоОкончательноеЗначение Тогда
		Если ТипЗнч(ИсточникДанных) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			Источник = ИсточникДанных;
		Иначе
			Источник = ФормаВладелец;
		КонецЕсли;
		ИнтеграцияСтатусСамозанятогоКлиент.НачатьПолучениеСтатусаСамозанятого(
			Источник, ФормаВладелец, ПараметрыСтатуса.ИННСамозанятого, ДатаПолученияСтатуса);
	КонецЕсли;
	
	Возврат СохраненныйСтатус;
	
КонецФункции

#КонецОбласти