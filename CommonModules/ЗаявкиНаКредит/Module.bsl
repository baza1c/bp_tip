////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции для обработки заявок на кредиты:
// - формирование электронных представлений заявок и отчетов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбращениеКСервису

// Структура параметров для алгоритма, передающего информацию о ходе фонового задания в форму с индикатором.
//
// Возвращаемое значение:
//	Структура - см. переменную Состояние.
//
Функция СостояниеПрогрессаПодписанияИОтправки() Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяШага", ""); // 1 = "подготовка"; 2 = "подписание"; 3 = "транзакции"; 4 = "отправка"
	
	Состояние = Новый Структура;
	Состояние.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	Состояние.Вставить("ВыполненоДействий", 0);  // часть выполненных действий шага
	Состояние.Вставить("КоличествоДействий", 0); // сколько условных действий составляет шаг
	
	Возврат Состояние;
	
КонецФункции

// Если получение транзакции связано с событием по заявке на кредит, то обновляем состояние соответствующей заявки.
// В этом случае по типу транзакции можно определить событие по заявке.
// 
// Параметры:
//  РеквизитыТранзакции - Структура - см. УниверсальныйОбменСБанками.РеквизитыТранзакции().
//  Результат - Структура - Содержит ключи:
//		* Выполнено - Булево - признак успешности выполения операции.
//		* ОписаниеОшибки  - Строка - Текст ошибки, если возникла, или пустая строка, если нет ошибок.
//		* ИдентификаторТранзакции  - Строка - идентификатор транзакции.
//		* Документооборот  - СправочникСсылка.ДокументооборотыОбменаСБанками - документооборот, в рамках которого была получена транзакция.
//
Процедура ПриПолученииТранзакции(РеквизитыТранзакции, Результат) Экспорт
	
	Если ТипЗнч(РеквизитыТранзакции.ТипТранзакции) <> Тип("ПеречислениеСсылка.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит")
	   И ТипЗнч(РеквизитыТранзакции.ТипТранзакции) <> Тип("ПеречислениеСсылка.ТипыТранзакцийОбменаСБанкамиЗаявкиНаЛизинг") Тогда
		Возврат;
	КонецЕсли;
	
	Предмет = УниверсальныйОбменСБанками.ПредметТранзакции(РеквизитыТранзакции.Транзакция);
	Если ТипЗнч(Предмет) <> Тип("ДокументСсылка.ЗаявкаНаКредит")
	 Или Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;
		
	Если Результат.Выполнено Тогда
		РегистрыСведений.СостояниеЗаявокНаКредит.ОбновитьСостояниеЗаявкиПриПолученииСообщения(
			Предмет,
			РеквизитыТранзакции);
	Иначе
		РегистрыСведений.СостояниеЗаявокНаКредит.ЗаписатьОшибку(
			Предмет,
			РеквизитыТранзакции.Организация,
			РеквизитыТранзакции.Банк,
			Результат.ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Если расшифровка транзакции связана с событием по заявке на кредит, то обновляем состояние соответствующей заявки.
// В этом случае по типу транзакции можно определить событие по заявке.
// 
// Параметры:
//  РеквизитыТранзакции - Структура - см. УниверсальныйОбменСБанками.РеквизитыТранзакции().
//  Результат - Структура - Содержит ключи:
//		* Выполнено - Булево - признак успешности выполения операции.
//		* ОписаниеОшибки  - Строка - Текст ошибки, если возникла, или пустая строка, если нет ошибок.
//		* Предупреждения  - Строка - Предупреждения, возникшие при расшифровке и проверке подписи.
//		* ОтмененоПользователем  - Булево - признак отмены пользователем.
//		* ПодписьНеДействительна  - Булево - Истина, если подпись под одним из документов неверна.
//		* НеизвестныйСертификатПодписи  - Булево - Истина, если подпись выполнена неизвестным сертификатом.
//
Процедура ПриРасшифровкеТранзакции(РеквизитыТранзакции, Результат) Экспорт
	
	Если ТипЗнч(РеквизитыТранзакции.ТипТранзакции) <> Тип("ПеречислениеСсылка.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит")
	   И ТипЗнч(РеквизитыТранзакции.ТипТранзакции) <> Тип("ПеречислениеСсылка.ТипыТранзакцийОбменаСБанкамиЗаявкиНаЛизинг") Тогда
		Возврат;
	КонецЕсли;
	
	Предмет = УниверсальныйОбменСБанками.ПредметТранзакции(РеквизитыТранзакции.Транзакция);
	Если ТипЗнч(Предмет) <> Тип("ДокументСсылка.ЗаявкаНаКредит")
	 Или Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;
		
	Если Результат.Выполнено Тогда
		РегистрыСведений.СостояниеЗаявокНаКредит.ОбновитьСостояниеЗаявкиПриРасшифровкеСообщения(
			Предмет,
			РеквизитыТранзакции);
	Иначе
		РегистрыСведений.СостояниеЗаявокНаКредит.ЗаписатьОшибку(
			Предмет,
			РеквизитыТранзакции.Организация,
			РеквизитыТранзакции.Банк,
			Результат.ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Если отправка транзакции связана с событием по заявке на кредит, то обновляем состояние соответствующей заявки.
// В этом случае по типу транзакции можно определить событие по заявке.
// 
// Параметры:
//  РеквизитыТранзакции - Структура - см. УниверсальныйОбменСБанками.РеквизитыТранзакции().
//  Результат - Структура - Содержит ключи:
//		* Выполнено - Булево - признак успешности выполения операции.
//		* ОписаниеОшибки  - Строка - Текст ошибки, если возникла, или пустая строка, если нет ошибок.
//
Процедура ПриОтправкеТранзакции(РеквизитыТранзакции, Результат) Экспорт
	
	Если ТипЗнч(РеквизитыТранзакции.ТипТранзакции) <> Тип("ПеречислениеСсылка.ТипыТранзакцийОбменаСБанкамиЗаявкиНаКредит")
	   И ТипЗнч(РеквизитыТранзакции.ТипТранзакции) <> Тип("ПеречислениеСсылка.ТипыТранзакцийОбменаСБанкамиЗаявкиНаЛизинг") Тогда
		Возврат;
	КонецЕсли;
	
	Предмет = УниверсальныйОбменСБанками.ПредметТранзакции(РеквизитыТранзакции.Транзакция);
	Если ТипЗнч(Предмет) <> Тип("ДокументСсылка.ЗаявкаНаКредит")
	 Или Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;
		
	Если Результат.Выполнено Тогда
		РегистрыСведений.СостояниеЗаявокНаКредит.ОбновитьСостояниеЗаявкиПриОтправкеСообщения(
			Предмет,
			РеквизитыТранзакции);
	Иначе
		РегистрыСведений.СостояниеЗаявокНаКредит.ЗаписатьОшибкуПоАктуальномуСостоянию(
			Предмет,
			РеквизитыТранзакции,
			Результат.ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает объект XDTO из корневого элемента XML по схеме пакетаXDTO ЗаявкиНаКредит.
//
// Параметры:
//	СервисОбменаСБанками - ПеречислениеСсылка.СервисыОбменаСБанками - Сервис.
//	ДанныеXML     - Строка, ХранилищеЗначения - Данные XML из характеристик сервиса заявок на кредит.
//
// Возвращаемое значение:
//	ОбъектXDTO, Неопределено - Объект XDTO, прочитанный из XML, или Неопределено в случае ошибок или отсутствия данных.
//
Функция ОбъектXDTOХарактеристики(СервисОбменаСБанками, ДанныеXML) Экспорт
	
	УсловияОбслуживания = ФабрикаXDTO.Тип("http://www.v8.1c.ru/banks/CreditApplication", "УсловияКредитования");
	
	Возврат УниверсальныйОбменСБанкамиОбщегоНазначения.ОбъектXDTOХарактеристики(СервисОбменаСБанками, ДанныеXML, УсловияОбслуживания);

КонецФункции

#КонецОбласти

#Область ОбновлениеДанныхСервиса

// Процедура - обработчик начала получения данных о сервисе заявок на кредит.
// Предназначена для вызова из модуля форм, в которых требуются актуальные данные о сервисе.
//
// Параметры:
//  СервисОбменаСБанками - Перечисление.СервисыОбменаСБанками - Сервис обмена.
//  Форма - ФормаКлиентскогоПриложения - должна содержать реквизиты СведенияОСервисе И СведенияОДлительнойОперации
//
Процедура НачатьОбновлениеДанныхСервиса(СервисОбменаСБанками, Форма) Экспорт 

	СведенияОСервисе = Форма.СведенияОСервисе;
	СведенияОДлительнойОперации = Форма.СведенияОДлительнойОперации;
	
	Если Форма.ТолькоПросмотр 
		ИЛИ СведенияОСервисе.ДанныеАктуальны
		ИЛИ СведенияОСервисе.ТребуетсяПодключениеИнтернетПоддержки Тогда
		// Обновление не требуется или невозможно.
		Возврат;
	КонецЕсли;
	
	Если СведенияОДлительнойОперации.ДлительнаяОперация <> Неопределено Тогда
		// Не завершено предыдущее фоновое задание, новое не запускаем.
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Форма.УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение           = 0; // Не ждем завершения.
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(НСтр("ru = 'Обновление данных сервиса %1'"), СервисОбменаСБанками);
	ПараметрыВыполнения.КлючФоновогоЗадания         = "ОбновитьДанныеСервиса";
	
	СведенияОДлительнойОперации.Имя = ПараметрыВыполнения.КлючФоновогоЗадания;
	СведенияОДлительнойОперации.ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "Документы.ЗаявкаНаКредит.ОбновитьДанныеСервиса", СервисОбменаСБанками);

КонецПроцедуры

#КонецОбласти

#Область СостояниеЗаявокНаКредит

// Возвращает признак того, что заявка была одобрена.
//
// Параметры:
//  Состояние - ПеречислениеСсылка.СостоянияЗаявокНаКредит - текущее состояние заявки.
// 
// Возвращаемое значение:
//   Булево - Истина, если заявка ранее уже была одобрена банком.
//
Функция ЗаявкаОдобрена(Состояние) Экспорт 

	Возврат(Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.Одобрено")
		ИЛИ Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.КредитЗапрошен") 
		ИЛИ Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.Готово"));
	
КонецФункции	

// Возвращает цвет текста или фона для элемента, отображающего состояние заявки на кредит.
//
// Параметры:
//  Состояние - ПеречислениеСсылка.СостоянияЗаявокНаКредит - состояние заявки на кредит.
// 
// Возвращаемое значение:
// 	Цвет - Цвет состояния.
//
Функция ЦветСостояния(Состояние) Экспорт 

	ЦветСостояния = Новый Цвет; // Автоцвет

	Если ЗаявкаОдобрена(Состояние) Тогда
		ЦветСостояния = ЦветаСтиля.ЦветШрифтаСостояниеЗаявкиНаКредитОдобрено;
	КонецЕсли;	
	
	Возврат ЦветСостояния;
	
КонецФункции	

// Возвращает документооборот по заявке на кредит.
//
// Параметры:
//	ЗаявкаНаКредит - ДокументСсылка.ЗаявкаНаКредит - Ссылка на заявку.
//
// Возвращаемое значение:
//	СправочникСсылка.ДокументооборотыОбменаСБанками - ссылка на документооборот по заявке.
//
Функция ДокументооборотПоЗаявке(ЗаявкаНаКредит) Экспорт

	Результат = Справочники.ДокументооборотыОбменаСБанками.ПустаяСсылка();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаявкаНаКредит", ЗаявкаНаКредит);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СостояниеЗаявокНаКредит.Транзакция.Документооборот КАК Документооборот
	|ИЗ
	|	РегистрСведений.СостояниеЗаявокНаКредит КАК СостояниеЗаявокНаКредит
	|ГДЕ
	|	СостояниеЗаявокНаКредит.ЗаявкаНаКредит = &ЗаявкаНаКредит";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.Документооборот) Тогда
			Результат = Выборка.Документооборот;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Заполняет реквизиты, отражающие состояние документооборота по заявке на кредит.
//
// Параметры:
//  Документооборот - СправочникСсылка.ДокументооборотыОбменаСБанками - набор взаимодействий с банком по определенному
//                                                                      предмету обмена (например, заявке на кредит).
//  ОписаниеСтатуса - Структура - см. УниверсальныйОбменСБанкамиПереопределяемый.СтатусДокументооборота()
//
Процедура СтатусДокументооборота(Документооборот, ОписаниеСтатуса) Экспорт
	
	// По-умолчанию состояние как будто заявка во все банки просрочена.
	ОписаниеСтатуса.Завершен         = Истина;
	ОписаниеСтатуса.ОжидаетсяОтвет   = Ложь;
	
	ДатаСегодня = НачалоДня(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документооборот", Документооборот);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостояниеЗаявокНаКредит.Состояние КАК Состояние,
	|	СостояниеЗаявокНаКредит.ЕстьОшибки КАК ЕстьОшибки,
	|	СостояниеЗаявокНаКредит.СрокАктуальности КАК СрокАктуальности,
	|	НАЧАЛОПЕРИОДА(СостояниеЗаявокНаКредит.ДатаИзменения, ДЕНЬ) КАК ДатаИзменения
	|ИЗ
	|	РегистрСведений.СостояниеЗаявокНаКредит КАК СостояниеЗаявокНаКредит
	|ГДЕ
	|	СостояниеЗаявокНаКредит.Транзакция.Документооборот = &Документооборот
	|
	|УПОРЯДОЧИТЬ ПО
	|	СостояниеЗаявокНаКредит.ДатаИзменения УБЫВ,
	|	СостояниеЗаявокНаКредит.Состояние.Порядок УБЫВ";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);

	Если Выборка.Следующий() Тогда
		
		ИстекСрокАктуальности = ЗначениеЗаполнено(Выборка.СрокАктуальности)
			И Выборка.СрокАктуальности < ДатаСегодня;

		// Может быть ситуация, когда банк сначала прислал одно решение, а потом передумал
		// и через несколько дней прислал другое.
		// Поэтому в течение 3-х месяцев с момента последнего изменения считаем, что можем получить еще какой-то ответ.
		ИзмененияМеньшеТрехМесяцевНазад = ЗначениеЗаполнено(Выборка.ДатаИзменения)
			И ДобавитьМесяц(Выборка.ДатаИзменения, 3) > ДатаСегодня;

		Если Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.Черновик
			ИЛИ Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.НеОтправлено Тогда
			// В обычной ситуации сюда попасть не должны, т.к. не отправляли еще и документооборот не создавали.
			ОписаниеСтатуса.Завершен       = Ложь;
			ОписаниеСтатуса.ОжидаетсяОтвет = Ложь;

		ИначеЕсли Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.Отправлено
			ИЛИ Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.ОжидаетРешения
			ИЛИ Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.КредитЗапрошен Тогда
			// Ждем ответ от банка на заявку или акцепт.
			ОписаниеСтатуса.Завершен       = Ложь;
			ОписаниеСтатуса.ОжидаетсяОтвет = НЕ ИстекСрокАктуальности ИЛИ ИзмененияМеньшеТрехМесяцевНазад;

		ИначеЕсли Выборка.Состояние = Перечисления.СостоянияЗаявокНаКредит.Одобрено Тогда
			// Получили одобрение, если разобрали файл ответа без ошибок,
			// то теперь пользователь может выбрать или не выбрать данный банк (ждем акцепт пользователя).
			// Если возникла ошибка, то банк может попытаться прислать исправленный вариант ответа,
			// поэтому продолжаем проверять входящие сообщения.
			ОписаниеСтатуса.Завершен       = Ложь;
			ОписаниеСтатуса.ОжидаетсяОтвет = Выборка.ЕстьОшибки
				ИЛИ НЕ ИстекСрокАктуальности
				ИЛИ ИзмененияМеньшеТрехМесяцевНазад;

		Иначе
			// Во всех остальных случаях от банка ничего не ждем, считаем что процесс завершился,
			// если с момента последнего изменения состояния прошло больше месяца.
			ОписаниеСтатуса.ОжидаетсяОтвет = НЕ ИстекСрокАктуальности ИЛИ ИзмененияМеньшеТрехМесяцевНазад;
			ОписаниеСтатуса.Завершен       = НЕ ОписаниеСтатуса.ОжидаетсяОтвет;

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу этапов отправки заявки.
//
// Параметры:
//	РеквизитыДокументооборота - Структура - см. УниверсальныйОбменСБанками.РеквизитыДокументооборота().
//	ДополнительныеПараметры - Произвольный - Дополнительные параметры формы.
//	ТаблицаЭтаповОтправки - ТаблицаЗначений - см. УниверсальныйОбменСБанками.ЗаготовкаТаблицыЭтаповОтправки().
//
Процедура ЗаполнитьТаблицуЭтаповОтправки(РеквизитыДокументооборота, ДополнительныеПараметры, ТаблицаЭтаповОтправки) Экспорт

    ДатаОтправкиЗаявки  = '0001-01-01';
    МожноДобавлятьЭтапы = Истина;
    
    ТипыТранзакций = ЗаявкиНаКредитКлиентСервер.ТипыТранзакцийСервисаОбменаСБанками(РеквизитыДокументооборота.Сервис);
    
	Транзакции = УниверсальныйОбменСБанками.ТранзакцииПоДокументообороту(РеквизитыДокументооборота.Документооборот);
	ТаблицаДанныхТранзакций = УниверсальныйОбменСБанками.РеквизитыТранзакций(Транзакции);
	
	ЗначенияПеречисления = Метаданные.Перечисления.СостоянияЗаявокНаКредит.ЗначенияПеречисления;

	// 1. Сама заявка.
	СведенияОТранзакции = УниверсальныйОбменСБанками.НайтиТранзакциюВТаблице(
		ТаблицаДанныхТранзакций,
		ТипыТранзакций.Заявка);

	Если СведенияОТранзакции <> Неопределено Тогда
		ДатаОтправкиЗаявки = СведенияОТранзакции.ДатаТранспорта;

		СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
		СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Отправлено.Синоним;
		СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
		СтрокаЭтапа.КомментарийКСостоянию		= "";
		СтрокаЭтапа.ЭтапПройден					= Истина;
		
		// Покажем в комментарии, когда получена банком.
		СведенияОТранзакции = УниверсальныйОбменСБанками.НайтиТранзакциюВТаблице(
			ТаблицаДанныхТранзакций,
			ТипыТранзакций.ИзвещениеОПолученииБанкомЗаявки);
		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа.КомментарийКСостоянию	= СтрШаблон(НСтр("ru = 'Получено %1 %2'"),
				РеквизитыДокументооборота.Банк,
				Формат(СведенияОТранзакции.ДатаТранспорта, "ДЛФ=DT"));
		КонецЕсли;
		
	Иначе
		// Заявка вообще не была отправлена.
		СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
		СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.НеОтправлено.Синоним;
		СтрокаЭтапа.Дата						= '0001-01-01';
		СтрокаЭтапа.КомментарийКСостоянию		= "";
		СтрокаЭтапа.ЭтапПройден					= Ложь;
		
		// Другие этапы не показываем.
		МожноДобавлятьЭтапы = Ложь;
	КонецЕсли;
	

	// 2. Решение банка.
	Если МожноДобавлятьЭтапы Тогда

		СведенияОТранзакции = УниверсальныйОбменСБанками.НайтиТранзакциюВТаблице(
			ТаблицаДанныхТранзакций,
			ТипыТранзакций.ПоложительноеКредитноеРешение,
			ДатаОтправкиЗаявки);
		
		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Одобрено.Синоним;
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		Иначе
			СведенияОТранзакции = УниверсальныйОбменСБанками.НайтиТранзакциюВТаблице(
				ТаблицаДанныхТранзакций,
				ТипыТранзакций.ОтрицательноеКредитноеРешение,
				ДатаОтправкиЗаявки);
			Если СведенияОТранзакции <> Неопределено Тогда
				СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
				СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Отказано.Синоним;
				СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
				СтрокаЭтапа.КомментарийКСостоянию		= "";
				СтрокаЭтапа.ЭтапПройден					= Истина;
			
				// Другие этапы не показываем.
				МожноДобавлятьЭтапы = Ложь;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	

	// 3. Акцепт заемщика.
	Если МожноДобавлятьЭтапы Тогда
		СведенияОТранзакции = УниверсальныйОбменСБанками.НайтиТранзакциюВТаблице(
			ТаблицаДанныхТранзакций,
			ТипыТранзакций.Акцепт,
			ДатаОтправкиЗаявки);

		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.КредитЗапрошен.Синоним;
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		Иначе
			// Другие этапы не показываем.
			МожноДобавлятьЭтапы = Ложь;
		КонецЕсли;
	КонецЕсли;

	
	// 4. Получение банком акцепта.
	Если МожноДобавлятьЭтапы Тогда
		СведенияОТранзакции = УниверсальныйОбменСБанками.НайтиТранзакциюВТаблице(
			ТаблицаДанныхТранзакций,
			ТипыТранзакций.ИзвещениеОПолученииБанкомАкцепта,
			ДатаОтправкиЗаявки);
			
		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= ЗначенияПеречисления.Готово.Синоним;
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		Иначе
			// Другие этапы не показываем.
			МожноДобавлятьЭтапы = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Если банк в процессе какого-либо шага прислал тип транзакции "Ошибка", то обмен считается завершенным.
	// Покажем статус ошибки самым последним шагом.
	Если ТаблицаЭтаповОтправки.Количество() < 4 Тогда // Отображение больше 4-х этапов форма не поддерживает.
		СведенияОТранзакции = УниверсальныйОбменСБанками.НайтиТранзакциюВТаблице(
			ТаблицаДанныхТранзакций,
			ТипыТранзакций.Ошибка,
			ДатаОтправкиЗаявки);

		Если СведенияОТранзакции <> Неопределено Тогда
			СтрокаЭтапа = ТаблицаЭтаповОтправки.Добавить();
			СтрокаЭтапа.ТекстНадписи				= НСтр("ru = 'Ошибка'");
			СтрокаЭтапа.Дата						= СведенияОТранзакции.ДатаТранспорта;
			СтрокаЭтапа.КомментарийКСостоянию		= "";
			СтрокаЭтапа.ЭтапПройден					= Истина;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область БизнесСтатистика

// Записывает данные бизнес-статистики в Пульт и Бизнес-статистику 1С-Фреш.
//
// Параметры:
//   ИдентификаторБанка - Строка - Идентификатор банка в сервисе.
//   ИмяОперации - Строка - имя ключевой операции бизнес-статистики.
//
Процедура ЗаписатьОперациюБизнесСтатистики(ИдентификаторБанка, ИмяОперации) Экспорт
	
	ИмяКлючевойОперации = СтрШаблон("СтатистикаБП.ЗаявкиНаКредит.%1.%2", ИдентификаторБанка, ИмяОперации);
	
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(ИмяКлючевойОперации, 1);
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяКлючевойОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Конструктор параметров формирования имени файла комплекта заявки в банк.
//
// Параметры:
//
// Возвращаемое значение:
//	Структура - Параметры для открытия формы на клиенте.
//
Функция НовыеПараметрыИмениФайлаЗаявки() Экспорт
	
	ПараметрыФайлаЗаявки = Новый Структура;
	ПараметрыФайлаЗаявки.Вставить("ТипДокумента", 			Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.ПустаяСсылка());
	ПараметрыФайлаЗаявки.Вставить("ТипСодержимого", 		Перечисления.ТипыСодержимогоФайловОбменаСБанками.ПустаяСсылка());
	ПараметрыФайлаЗаявки.Вставить("СведенияОЗаявке", 		Неопределено);
	ПараметрыФайлаЗаявки.Вставить("ВидОтчета", 	            "");
	ПараметрыФайлаЗаявки.Вставить("УточнениеНаименования", 	"");
	ПараметрыФайлаЗаявки.Вставить("СоставнойКлючУникальности", 	Неопределено);
	ПараметрыФайлаЗаявки.Вставить("ДатаНачала", 	'00010101');
	ПараметрыФайлаЗаявки.Вставить("ДатаОкончания", 	'00010101');
	ПараметрыФайлаЗаявки.Вставить("ПрефиксФайла", 			"");
	ПараметрыФайлаЗаявки.Вставить("ФорматИменованияФайлов", "");
	ПараметрыФайлаЗаявки.Вставить("УникальныеИменаФайловВПакете", Неопределено);
	ПараметрыФайлаЗаявки.Вставить("СданВНалоговую", Ложь);
	
	Возврат ПараметрыФайлаЗаявки;
КонецФункции

// Возвращает имя файла отчета, сформированное по шаблону.
//
// Параметры:
//	ПараметрыФайлаЗаявки - Структура - параметры для формирования имени файла отчета - см. НовыеПараметрыИмениФайлаЗаявки()
//
// Возвращаемое значение:
//	Строка - имя файла.
//
Функция ПолучитьИмяФайлаЗаявкиВБанк(ПараметрыФайлаЗаявки) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрыФайлаЗаявки.ФорматИменованияФайлов) 
		Или ПараметрыФайлаЗаявки.ФорматИменованияФайлов = УниверсальныйОбменСБанкамиОбщегоНазначения.СтандартФНС() 
		Тогда
		
		Если ПараметрыФайлаЗаявки.Свойство("СоставнойКлючУникальности") Тогда
			КлючУникальности = ПараметрыФайлаЗаявки.СоставнойКлючУникальности;
		Иначе
			КлючУникальности = ПараметрыФайлаЗаявки.УточнениеНаименования;	
		КонецЕсли;
		
		ИмяФайлаЗаявкиВБанк = ИмяФайлаДляКонтейнера(
			ПараметрыФайлаЗаявки.ТипДокумента,
			ПараметрыФайлаЗаявки.ТипСодержимого,
			ПараметрыФайлаЗаявки.СведенияОЗаявке,
			КлючУникальности);
		
	ИначеЕсли ПараметрыФайлаЗаявки.ФорматИменованияФайлов = УниверсальныйОбменСБанкамиОбщегоНазначения.СокращенныйПериодБезУИД()
		Тогда
		
		СведенияОЗаявке = ПараметрыФайлаЗаявки.СведенияОЗаявке;
		
		ПараметрыИмениФайлаОтчета = УниверсальныйОбменСБанкамиОбщегоНазначения.НовыеПараметрыИмениФайлаОтчета();
		ЗаполнитьЗначенияСвойств(ПараметрыИмениФайлаОтчета, ПараметрыФайлаЗаявки); 
		
		ПараметрыИмениФайлаОтчета.ШаблонИмениФайла = ПолучитьШаблонИмениФайлаПоТипу(
			ПараметрыФайлаЗаявки.ТипДокумента, 
			СведенияОЗаявке.ДанныеЗаявки.СервисОбменаСБанками,
			ПараметрыФайлаЗаявки.ВидОтчета);
		
		ПараметрыИмениФайлаОтчета.ИНН =	СведенияОЗаявке.ДанныеЗаемщика.ИНН;
		ПараметрыИмениФайлаОтчета.КПП =	СведенияОЗаявке.ДанныеЗаемщика.КПП;
		
		ИмяФайлаЗаявкиВБанк = УниверсальныйОбменСБанкамиОбщегоНазначения.ПолучитьИмяФайлаОтчета(ПараметрыИмениФайлаОтчета)
			+ "." + ОбщегоНазначения.ИмяЗначенияПеречисления(ПараметрыФайлаЗаявки.ТипСодержимого);
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неподдерживаемый формат именования файлов:%1'"), ПараметрыФайлаЗаявки.ФорматИменованияФайлов);
	КонецЕсли;
	
	ИмяФайлаЗаявкиВБанк = УниверсальныйОбменСБанкамиОбщегоНазначения.УнифицироватьИмяФайлаКВыгрузке(
		ИмяФайлаЗаявкиВБанк,
		ПараметрыФайлаЗаявки.ФорматИменованияФайлов,
		ПараметрыФайлаЗаявки.ПрефиксФайла,
		ПараметрыФайлаЗаявки.УникальныеИменаФайловВПакете);
		
	Возврат ИмяФайлаЗаявкиВБанк;

КонецФункции

// Универсальным образом формирует имя файла, добавляемого в отправляемый от заемщика контейнер.
//
// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит - назначение отправляемой информации.
//  ТипСодержимого - ПеречислениеСсылка.ТипыСодержимогоФайловОбменаСБанками - формат (расширение файла) информации.
//  СведенияОЗаявке - Структура - см. СведенияОЗаявке() или СведенияОбАкцепте() .
//  КлючУникальности - Строка, Массив - для вставки произвольного текста в середину названия файла.
//
// Возвращаемое значение:
//   Строка      - имя файла для переданного типа содержимого.
//
Функция ИмяФайлаДляКонтейнера(ТипДокумента, ТипСодержимого, СведенияОЗаявке, КлючУникальности = "") Экспорт
	
	СоставнойКлюч = (ТипЗнч(КлючУникальности) = Тип("Массив"));
	
	ЧастиИмениФайла = Новый Массив;
 	ЧастиИмениФайла.Добавить("BANK");
	
	Если ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АнкетаЗаемщика Тогда
		
		Если СведенияОЗаявке.ДанныеЗаявки.СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит Тогда
			ЧастиИмениФайла.Добавить("LOANAPP");
		Иначе
			ЧастиИмениФайла.Добавить("LEASAPP");
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.СогласиеНаОбработкуДанных Тогда
		
		ЧастиИмениФайла.Добавить("CONSENT");
		Расширение = ".xml";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.ПланСчетов Тогда
		
		ЧастиИмениФайла.Добавить("COA");
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.ОтчетЗаемщика Тогда
		
		Если СоставнойКлюч Тогда
			// При выгрузке расшифровки бухгалтерской отчетности ключ всегда будет составным.
			Если КлючУникальности[0] = "ОборотноСальдоваяВедомость" Тогда
			    ЧастиИмениФайла.Добавить("OSV");
			ИначеЕсли КлючУникальности[0] = "АнализСчета" Тогда
				ЧастиИмениФайла.Добавить("ANS");
			ИначеЕсли СтрНачинаетсяС(КлючУникальности[0], "КарточкаСчета") Тогда
				ЧастиИмениФайла.Добавить("TRN");
			КонецЕсли;
			КлючУникальности.Удалить(0);
			
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АкцептЗаемщика Тогда
		
		ЧастиИмениФайла.Добавить("ACCEPT");
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Невозможно получить имя файла для контейнера: неизвестный тип документа обмена.'");
	КонецЕсли;
	
	Если СоставнойКлюч Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЧастиИмениФайла, КлючУникальности);
	ИначеЕсли Не ПустаяСтрока(КлючУникальности) Тогда
		ЧастиИмениФайла.Добавить(КлючУникальности);
	КонецЕсли;
	
	ЧастиИмениФайла.Добавить(СведенияОЗаявке.ДанныеЗаемщика.ИдентификаторЗаемщика);
	ЧастиИмениФайла.Добавить(Формат(ТекущаяДатаСеанса(), "ДФ=ггггММдд")); // дата выгрузки
	Если ТипДокумента <> Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АкцептЗаемщика Тогда
		ЧастиИмениФайла.Добавить(Строка(Новый УникальныйИдентификатор())); // для гарантированной уникальности файла
	КонецЕсли;	
	
	ИмяФайла = СтрСоединить(ЧастиИмениФайла, "_") + "." + ОбщегоНазначения.ИмяЗначенияПеречисления(ТипСодержимого);
	
	Возврат ИмяФайла;
	
КонецФункции

// Вызывается после загрузки данных услуги банка, полученных с сервера.
// Параметры:
//		УслугаБанка - СправочникСсылка.УслугиБанков - ссылка на элемент справочника, данные которого были обновлены.
Процедура ПослеЗагрузкиДанныхУслугиБанка(УслугаБанка) Экспорт
	
	Если Не ЗначениеЗаполнено(УслугаБанка) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Характеристика Из УслугаБанка.Характеристики Цикл
		ОбработатьХарактеристику(Характеристика, УслугаБанка);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает XDTO объект из пакета ЗаявкиНаКредит с информацией о кредитном продукте банка.
//
// Параметры:
//	СервисОбменаСБанками - ПеречислениеСсылка.СервисыОбменаСБанками - Сервис.
//	УсловияКредитованияXDTO - ОбъектXDTO - Корневой объект из пакета XDTO ЗаявкиНаКредит, в котором производится поиск.
//	ИдентфикаторКредитногоПродукта - Строка - Идентификатор искомого кредитного продукта.
//	Дата - Дата - Дата, по состоянию на которую возвращает информация.
//
// Возвращаемое значение:
//	ОбъектXDTO или Неопределено
//
Функция НайтиКредитныйПродукт(СервисОбменаСБанками, УсловияКредитованияXDTO, Знач ИдентфикаторКредитногоПродукта, Знач Дата) Экспорт

	Если УсловияКредитованияXDTO = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	ТекущаяВерсияСервиса = УниверсальныйОбменСБанками.ВерсияСервиса(СервисОбменаСБанками);
	Дата                 = НачалоДня(Дата);
	ИдентфикаторКредитногоПродукта = ВРег(ИдентфикаторКредитногоПродукта);

	КредитныйПродуктПоУмолчанию = Неопределено;
	
	Для каждого КредитныйПродукт Из УсловияКредитованияXDTO.КредитныйПродукт Цикл

		Если КредитныйПродукт.ДатаНачала > Дата
			ИЛИ КредитныйПродукт.ДатаОкончания < Дата Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверим версии сервиса, для которых применяются условия.
		Если ЗначениеЗаполнено(КредитныйПродукт.МинВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, КредитныйПродукт.МинВерсияСервиса) < 0 Тогда
			// Кредитный продукт может быть корректно отображен только в следующих версиях программы, 
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КредитныйПродукт.МаксВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, КредитныйПродукт.МаксВерсияСервиса) > 0 Тогда
			// Кредитный продукт предназначен для отображения на предыдущих версиях программы,
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;
	
		Если КредитныйПродукт.Свойства().Получить("Идентификатор") = Неопределено
			ИЛИ НЕ ЗначениеЗаполнено(КредитныйПродукт.Идентификатор) Тогда
			// В старых настройках могут быть продукты, у которых еще не было идентификатора.
			// Если не найдем никакого продукта с искомым идентификатором, то вернем без идентификатора.
			КредитныйПродуктПоУмолчанию = КредитныйПродукт;
			Продолжить;
		КонецЕсли;
		
		Если ВРег(КредитныйПродукт.Идентификатор) = ИдентфикаторКредитногоПродукта Тогда
			Возврат КредитныйПродукт;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат КредитныйПродуктПоУмолчанию;

КонецФункции

// Возвращает список типов предметов лизинга, которые заложены в настройках сервиса.
//
// Параметры:
//	ВключаяАрхивные - Булево - Признак того, необходимо ли включать в список архивные элементы.
//
// Возвращаемое значение:
//	СписокЗначений - Значение - идентификатор, Представление - наименование.
//
Функция ТипыПредметовЛизинга(ВключаяАрхивные = Ложь) Экспорт

	Результат = Новый СписокЗначений;
	
	ЗначенияХарактеристик = УниверсальныйОбменСБанками.ХарактеристикиСервиса(
		Перечисления.СервисыОбменаСБанками.ЗаявкиНаЛизинг);
	
	УсловияКредитованияXDTO = ОбъектXDTOХарактеристики(Перечисления.СервисыОбменаСБанками.ЗаявкиНаЛизинг, ЗначенияХарактеристик);
	Если УсловияКредитованияXDTO = Неопределено
		ИЛИ УсловияКредитованияXDTO.ТипыПредметовЛизинга = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТипыПредметовЛизингаXDTO = УсловияКредитованияXDTO.ТипыПредметовЛизинга;
	
	ТекущаяВерсияСервиса = УниверсальныйОбменСБанками.ВерсияСервиса(
		Перечисления.СервисыОбменаСБанками.ЗаявкиНаЛизинг);
	
	Для каждого ТипПредметаЛизингаXDTO Из ТипыПредметовЛизингаXDTO.ТипПредметаЛизинга Цикл

		// Проверим версии сервиса, для которых применяются условия.
		Если ЗначениеЗаполнено(ТипПредметаЛизингаXDTO.МинВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, ТипПредметаЛизингаXDTO.МинВерсияСервиса) < 0 Тогда
			// Значение может быть корректно обработано только в следующих версиях программы, 
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;

		Если ЗначениеЗаполнено(ТипПредметаЛизингаXDTO.МаксВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, ТипПредметаЛизингаXDTO.МаксВерсияСервиса) > 0 Тогда
			// Значение предназначено для предыдущих версий программы,
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;
		
		Если НЕ ВключаяАрхивные И ТипПредметаЛизингаXDTO.Архивный Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(ТипПредметаЛизингаXDTO.Идентификатор, ТипПредметаЛизингаXDTO.Наименование);

	КонецЦикла;

	Возврат Результат;

КонецФункции

#Область ОбработчикиОбновления

// Процедура устанавливает значение реквизита "Версия" справочника "Услуги банков" равным 0.
// Может быть использована для принудительного обновления данных в этом справочнике при
// следующей синхронизации с сервером обмена.
Процедура СброситьВерсиюУслугБанкаЗаявкиНаКредит() Экспорт
	
	УниверсальныйОбменСБанками.СброситьВерсиюУслугБанка(Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьХарактеристику(Характеристика, УслугаБанка)
	
	Если Характеристика.Характеристика <> Перечисления.ХарактеристикиСервисаЗаявкиНаКредит.ГруппыКонтрагентов Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеХарактеристики = Характеристика.Значение.Получить();
	Если Не ЗначениеЗаполнено(ЗначениеХарактеристики) Тогда
		Возврат;
	КонецЕсли;
	
	СервисХарактеристики = Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит;
	
	ОбъектXDTOХарактеристики = ОбъектXDTOХарактеристики(СервисХарактеристики, ЗначениеХарактеристики);
	
	Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УслугаБанка, "Банк");

	РегистрыСведений.ГруппыКонтрагентовЗаявкиВБанки.ЗаписатьГруппыКонтрагентов(СервисХарактеристики, 
		ОбъектXDTOХарактеристики, Банк); 
	
КонецПроцедуры

#Область ФормированиеЗаявкиНаКредит

Процедура ВывестиУзел_ЗаявкаНаКредит_Документ(Компоновщик, ПараметрыПредставления)
	
	Это1СКредит = (ПараметрыПредставления.СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит);
	Это1СЛизинг = (ПараметрыПредставления.СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ЗаявкиНаЛизинг);

	Компоновщик.ЗаписатьНачалоЭлемента("Документ");
	
	ВывестиАтрибут(Компоновщик, "ПредметОбмена",                   ПараметрыПредставления.ИдентификаторЗаявки, "Т,255");
	ВывестиАтрибут(Компоновщик, "ДатаПредметаОбмена",              ПараметрыПредставления.ДатаЗаявки, "Д");
	ВывестиАтрибут(Компоновщик, "ИдентификаторКредитногоПродукта", ПараметрыПредставления.ИдентификаторКредитногоПродукта);
	ВывестиАтрибут(Компоновщик, "СуммаКредита",                    ПараметрыПредставления.СуммаКредита, "Ч,15");
	ВывестиАтрибут(Компоновщик, "Срок",                            ПараметрыПредставления.СрокКредита, "Ч,4");
	
	// Заполняем атрибуты в зависимости от вида заявки.
	Если Это1СКредит Тогда
		ВывестиАтрибут(Компоновщик, "ЦельКредита",        ПараметрыПредставления.ЦельКредита, "Т,2");
	КонецЕсли;
	
	Если Это1СЛизинг Тогда
		ВывестиАтрибут(Компоновщик, "ТипПредметаЛизинга", ПараметрыПредставления.ТипПредметаЛизинга);
		ВывестиАтрибут(Компоновщик, "СуммаАванса",        ПараметрыПредставления.СуммаАванса, "Ч,15");
	КонецЕсли;
	
	ВывестиУзел_ЗаявкаНаКредит_Банк(Компоновщик, ПараметрыПредставления);
	
	ВывестиУзел_ЗаявкаНаКредит_Организация(Компоновщик, ПараметрыПредставления);
	
	ВывестиУзел_ЗаявкаНаКредит_КонтактноеЛицо(Компоновщик, ПараметрыПредставления);
	
	ВывестиУзел_ЗаявкаНаКредит_ПоказателиБизнеса(Компоновщик, ПараметрыПредставления);
	
	ВывестиУзел_ЗаявкаНаКредит_ПараметрыЗаявки(Компоновщик, ПараметрыПредставления);
	
	УниверсальныйОбменСБанкамиФормы.ВывестиУзел_ДополнительныеПоказатели(Компоновщик, ПараметрыПредставления);
	
	ПараметрыУпорядочения = Новый Структура;
	ПараметрыУпорядочения.Вставить("ГруппаАнкета", Документы.ЗаявкаНаКредит.ГруппаФайловПоПризнаку(
		Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АнкетаЗаемщика));
	ПараметрыУпорядочения.Вставить("ГруппаПоУмолчанию", Документы.ЗаявкаНаКредит.ГруппаФайловПоПризнаку());
	ПараметрыУпорядочения.Вставить("ПорядокВывода", Документы.ЗаявкаНаКредит.ПорядокВыводаФайлов());
	УниверсальныйОбменСБанкамиФормы.ВывестиУзел_ДополнительныеФайлы(Компоновщик, ПараметрыПредставления, ПараметрыУпорядочения);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Документ
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_Банк(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Банк");
	
	ВывестиАтрибут(Компоновщик, "ИНН",  ПараметрыПредставления.Банк.ИНН);
	ВывестиАтрибут(Компоновщик, "Наим", ПараметрыПредставления.Банк.Наименование);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Банк
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_Организация(Компоновщик, ПараметрыПредставления)
	
	ОтправительЮЛ = ПараметрыПредставления.ОтправительЮЛ;
	ОтправительИП = ПараметрыПредставления.ОтправительИП;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Организация");
	
	ВывестиАтрибут(Компоновщик, "ОКВЭД", 	ПараметрыПредставления.ОКВЭД, "Т,8");
	ВывестиАтрибут(Компоновщик, "ОКОПФ", 	ПараметрыПредставления.ОКОПФ, "Т,5");
	ВывестиАтрибут(Компоновщик, "ДатаРег", 	ПараметрыПредставления.ДатаРегистрации, "Д");
	
	Если ПараметрыПредставления.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		Компоновщик.ЗаписатьНачалоЭлемента("ЮЛ");
		
		ВывестиАтрибут(Компоновщик, "ПолнНаимОрг", ОтправительЮЛ.Наименование,     "Т,1000");
		ВывестиАтрибут(Компоновщик, "СокрНаимОрг", ОтправительЮЛ.СокрНаименование, "Т,255");
		ВывестиАтрибут(Компоновщик, "ИННЮЛ",       ОтправительЮЛ.ИНН,              "Т,10");
		ВывестиАтрибут(Компоновщик, "КПП",         ОтправительЮЛ.КПП,              "Т,9");
		ВывестиАтрибут(Компоновщик, "ОГРН",        ОтправительЮЛ.ОГРН,             "Т,13");
		ВывестиАтрибут(Компоновщик, "Нерезидент",  ОтправительЮЛ.Нерезидент,       "Ч,1");
		
		ВывестиУниверсальныйУзел_СведенияОФЛТип(Компоновщик, ПараметрыПредставления.Руководитель, "Руководитель");
		
		ВывестиУзел_ЗаявкаНаКредит_Учредители(Компоновщик, ПараметрыПредставления);
		
		ВывестиУниверсальныйУзел_АдресТип(Компоновщик, ОтправительЮЛ.СведенияОбАдресе);
		
		Если ЗначениеЗаполнено(ОтправительЮЛ.СокращенноеНаименованиеДоРеорганизации) 
			ИЛИ ЗначениеЗаполнено(ОтправительЮЛ.ПолноеНаименованиеДоРеорганизации) Тогда
			Компоновщик.ЗаписатьНачалоЭлемента("СвРеорг");
			ВывестиАтрибут(Компоновщик, "ПолнНаимРеорг", ОтправительЮЛ.ПолноеНаименованиеДоРеорганизации, "Т,1000");
			ВывестиАтрибут(Компоновщик, "СокрНаимРеорг", ОтправительЮЛ.СокращенноеНаименованиеДоРеорганизации, "Т,255");
			Компоновщик.ЗаписатьКонецЭлемента(); // СвРеорг
		КонецЕсли;
		
		Компоновщик.ЗаписатьКонецЭлемента(); // ЮЛ
		
	Иначе
		Компоновщик.ЗаписатьНачалоЭлемента("ИП");
		
		ВывестиАтрибут(Компоновщик, "ОГРНИП",  ОтправительИП.ОГРН,            "Т,15");
		
		ВывестиУниверсальныйУзел_СведенияОФЛТип(Компоновщик, ПараметрыПредставления.Руководитель, "Предприниматель");
		
		Компоновщик.ЗаписатьКонецЭлемента(); // ИП
		
	КонецЕсли;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Организация
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_КонтактноеЛицо(Компоновщик, ПараметрыПредставления)
	
	КонтактноеЛицо = ПараметрыПредставления.КонтактноеЛицо;
	
	Если Не УниверсальныйОбменСБанкамиОбщегоНазначения.СведенияУказаны(КонтактноеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	Компоновщик.ЗаписатьНачалоЭлемента("КонтактноеЛицо");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "Должность", КонтактноеЛицо.Должность, "Т,255");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Телефон", КонтактноеЛицо.Телефон, "Т,255");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ЭлПочта", КонтактноеЛицо.ЭлПочта, "Т,255");
	
	ВывестиУниверсальныйУзел_ФИОТип(Компоновщик, КонтактноеЛицо.ФИО);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // КонтактноеЛицо
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_ПоказателиБизнеса(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("ПоказателиБизнеса");
	
	ВывестиАтрибут(Компоновщик, "ДатаФактНачалаДеят", ПараметрыПредставления.ДатаФактНачалаДеят, "Д");
	ВывестиАтрибут(Компоновщик, "ДатаНачалаУчета", ПараметрыПредставления.ДатаНачалаУчета, "Д");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ДатаОтчетности", ПараметрыПредставления.ДатаОтчетности, "Д");
	
	ВывестиУзел_ЗаявкаНаКредит_СистемыНО(Компоновщик, ПараметрыПредставления);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ПоказателиБизнеса
	
КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_СистемыНО(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("СистемыНО");
	
	Для Каждого СистемаНО Из ПараметрыПредставления.СистемыНО Цикл
		Компоновщик.ЗаписатьНачалоЭлемента("СистемаНО");
		СистемаНО = Лев(СистемаНО, 20);
		Компоновщик.ЗаписатьТекст(СистемаНО);
		Компоновщик.ЗаписатьКонецЭлемента(); // СистемаНО
	КонецЦикла;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // СистемыНО
	
КонецПроцедуры

Функция ПодобратьТекстыМакетов(КредитныеПродукты, ПараметрыСогласия)
	
	СписокБанков = ОбщегоНазначения.ВыгрузитьКолонку(КредитныеПродукты, "Банк", Истина);
	
	ИмяХарактеристики = Перечисления.ХарактеристикиСервисаЗаявкиНаКредит.СогласиеНаОбработкуДанных;
		
	// Для выгрузки текстов согласий по ранее отправленным заявкам, которые сейчас уже в режиме только просмотр,
	// и банки которых могут быть сейчас уже неактивны, указываем,
	// что необходимо возвращать характеристики вне зависимости от активности банков и их услуг.
	ХарактеристикиСервиса = УниверсальныйОбменСБанками.ХарактеристикиУслугБанков(
		ПараметрыСогласия.СервисОбменаСБанками,
		ИмяХарактеристики,
		СписокБанков,
		Истина);
		
	Для Каждого КредитныйПродукт Из КредитныеПродукты Цикл
		
		ХарактеристикиБанка = ХарактеристикиСервиса.НайтиСтроки(Новый Структура("Банк", КредитныйПродукт.Банк));
		
		Для Каждого ХарактеристикаБанка Из ХарактеристикиБанка Цикл
			
			УсловияКредитования = ОбъектXDTOХарактеристики(ПараметрыСогласия.СервисОбменаСБанками, ХарактеристикаБанка.Значение);
			Если УсловияКредитования = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		
			Для Каждого Согласие Из УсловияКредитования.СогласиеНаОбработкуДанных Цикл
				
				Если Согласие.ДатаНачала > ПараметрыСогласия.ДатаСогласия
				 Или Согласие.ДатаОкончания < ПараметрыСогласия.ДатаСогласия Тогда
					Продолжить;
				КонецЕсли;
				
				Если Согласие.ВидСогласия <> Неопределено 
					И Согласие.ВидСогласия <> ОбщегоНазначения.ИмяЗначенияПеречисления(КредитныйПродукт.ВидСогласия) Тогда
					Продолжить;
				КонецЕсли;
								
				Если ПараметрыСогласия.ЭтоЮридическоеЛицо И Согласие.ЮридическоеФизическоеЛицо = "ФизическоеЛицо"
					Или Не ПараметрыСогласия.ЭтоЮридическоеЛицо И Согласие.ЮридическоеФизическоеЛицо = "ЮридическоеЛицо" Тогда
					Продолжить;
				КонецЕсли;
				
				Если СтрНайти(Согласие.ТекстМакета, "<HTML") = 0 И СтрНайти(Согласие.ТекстМакета, "<html") = 0 Тогда
					// Если возникает ошибка, то далее возьмем макет по-умолчанию.
					ИмяСобытия = УниверсальныйОбменСБанкамиОбщегоНазначения.СобытиеЖурналаРегистрации(
						ПараметрыСогласия.СервисОбменаСБанками,
						НСтр("ru = 'Подобрать тексты макетов'", ОбщегоНазначения.КодОсновногоЯзыка()));
					ЗаписьЖурналаРегистрации(
						ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , КредитныйПродукт.Банк,
						НСтр("ru = 'Текст макета не является корректным html-текстом: не содержит тега html'"));
					Продолжить;
					
				КонецЕсли;
				
				Если Согласие.Свойства().Получить("ИдентификаторКредитногоПродукта") = Неопределено
					ИЛИ НЕ ЗначениеЗаполнено(Согласие.ИдентификаторКредитногоПродукта) Тогда
					Если НЕ ПустаяСтрока(КредитныйПродукт.ТекстМакета) И Согласие.ВидСогласия = Неопределено Тогда
						Продолжить; // Пропускаем общее согласие, если уже было найдено специальное.
					КонецЕсли;
				ИначеЕсли Согласие.ИдентификаторКредитногоПродукта <> КредитныйПродукт.ИдентификаторПродукта Тогда
					Продолжить; // Пропускаем согласие, если оно не относится к текущему кредитному продукту.
				КонецЕсли;
				
				КредитныйПродукт.ТекстМакета = Согласие.ТекстМакета;
				КредитныйПродукт.ПараметрыМакета = УниверсальныйОбменСБанкамиФормы.РазметкаТекста(КредитныйПродукт.ТекстМакета);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
		
	// остальные заполняем макетом по-умолчанию
	ТекстМакетаБКИ = "";
	ТекстМакетаПДН = "";

	ПараметрыМакета = Неопределено;
	Для Каждого КредитныйПродукт Из КредитныеПродукты Цикл
		Если Не ПустаяСтрока(КредитныйПродукт.ТекстМакета) Тогда
			Продолжить;
		КонецЕсли;
			
		Если ПустаяСтрока(ТекстМакетаБКИ) И КредитныйПродукт.ВидСогласия = Перечисления.ВидыСогласий.БКИ Тогда 
			ИмяХарактеристики = ?(ПараметрыСогласия.ЭтоЮридическоеЛицо, "СогласиеНаОбработкуДанныхБКИ_ЮЛ", "СогласиеНаОбработкуДанныхБКИ_ФЛ");
			ТекстМакетаБКИ = Документы.ЗаявкаНаКредит.ПолучитьМакет(ИмяХарактеристики).ПолучитьТекст();
			ПараметрыМакета = УниверсальныйОбменСБанкамиФормы.РазметкаТекста(ТекстМакетаБКИ);
		ИначеЕсли ПустаяСтрока(ТекстМакетаПДН) И КредитныйПродукт.ВидСогласия = Перечисления.ВидыСогласий.ПДН Тогда 
			ИмяХарактеристики = ?(ПараметрыСогласия.ЭтоЮридическоеЛицо, "СогласиеНаОбработкуДанныхПДН_ЮЛ", "СогласиеНаОбработкуДанныхПДН_ФЛ");
			ТекстМакетаПДН = Документы.ЗаявкаНаКредит.ПолучитьМакет(ИмяХарактеристики).ПолучитьТекст();
			ПараметрыМакета = УниверсальныйОбменСБанкамиФормы.РазметкаТекста(ТекстМакетаПДН);
		КонецЕсли;
		
		КредитныйПродукт.ТекстМакета = ?(КредитныйПродукт.ВидСогласия = Перечисления.ВидыСогласий.БКИ, ТекстМакетаБКИ, ТекстМакетаПДН);
		КредитныйПродукт.ПараметрыМакета = ПараметрыМакета;

	КонецЦикла;
	
	Возврат КредитныеПродукты;
	
КонецФункции

Процедура ВывестиУзел_ЗаявкаНаКредит_Учредители(Компоновщик, ПараметрыПредставления)

	ОтправительЮЛ = ПараметрыПредставления.ОтправительЮЛ;
	РуководительЕдинственныйУчредитель = 
		(ОтправительЮЛ.СоставУчредителей = Перечисления.СоставУчредителейЮридическогоЛица.РуководительЕдинственныйУчредитель);
	
	Если ОтправительЮЛ.Учредители.Количество() = 0 И НЕ РуководительЕдинственныйУчредитель Тогда
		// В состав учредителей могут входит другие юридические лица.
		// Сейчас не поддерживается передача данных о них.
		Возврат;
	КонецЕсли;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Учредители");
	ВывестиАтрибут(Компоновщик, "РуководительЕдинственныйУчредитель", ?(РуководительЕдинственныйУчредитель, "1", "0"));
	
	Для каждого СтрокаУчредителя Из ОтправительЮЛ.Учредители Цикл

		СведенияФЛ = НовыеСведенияФЛ();
		// Заполняем структуру сведений о физлице для выгрузки в xml.
		ЗаполнитьЗначенияСвойств(СведенияФЛ.ФИО, СтрокаУчредителя);
		СведенияФЛ.ДатаРождения = СтрокаУчредителя.ДатаРождения;
		СведенияФЛ.ИНН          = СтрокаУчредителя.ИНН;
		// Документ, удостоверяющий личность.
		СведенияФЛ.УдостоверениеЛичности.КодВидаДокумента = СтрокаУчредителя.КодВидаДокумента;
		СведенияФЛ.УдостоверениеЛичности.СерияДокумента   = СтрокаУчредителя.Серия;
		СведенияФЛ.УдостоверениеЛичности.НомерДокумента   = СтрокаУчредителя.Номер;
		СведенияФЛ.УдостоверениеЛичности.КемВыдан         = СтрокаУчредителя.КемВыдан;
		СведенияФЛ.УдостоверениеЛичности.ДатаВыдачи       = СтрокаУчредителя.ДатаВыдачи;
		СведенияФЛ.УдостоверениеЛичности.КодПодразделения = СтрокаУчредителя.КодПодразделения;
		// Адрес
		СведенияФЛ.СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(СтрокаУчредителя.ЗначениеJSONАдрес);
		// Место рождения
		СведенияФЛ.МестоРождения = СтрокаУчредителя.МестоРождения;
		
		Телефоны = Новый Массив;
		Если ЗначениеЗаполнено(СтрокаУчредителя.ТелефонРабочий) Тогда
			Телефоны.Добавить(СтрокаУчредителя.ТелефонРабочий);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаУчредителя.ТелефонМобильный) Тогда
			Телефоны.Добавить(СтрокаУчредителя.ТелефонМобильный);
		КонецЕсли;
		
		// Телефон
		Если Телефоны.Количество() > 0 Тогда
			СведенияФЛ.Телефон = Телефоны[0];
		КонецЕсли;
		
		// Доп. телефон
		Если Телефоны.Количество() > 1 Тогда
			СведенияФЛ.ДопТелефон = Телефоны[1];
		КонецЕсли;
		
		// Выгружаем в xml.
		Компоновщик.ЗаписатьНачалоЭлемента("Учредитель");
		
		// Доля в УК
		ВывестиАтрибут(Компоновщик, "ДоляУК", СтрокаУчредителя.ДоляУК, "Ч,5,2");
		
		ВывестиУниверсальныйУзел_СведенияОФЛТип(Компоновщик, СведенияФЛ, "ФЛ");

		Компоновщик.ЗаписатьКонецЭлемента(); // Учредитель
		
	КонецЦикла;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Учредители

КонецПроцедуры

Процедура ВывестиУзел_ЗаявкаНаКредит_ПараметрыЗаявки(Компоновщик, ПараметрыПредставления)
	
	// Расширенные показатели есть не у всех видов заявок.
	Если ПараметрыПредставления.СервисОбменаСБанками <> Перечисления.СервисыОбменаСБанками.ЗаявкиНаЛизинг Тогда
		Возврат;
	КонецЕсли;

	Компоновщик.ЗаписатьНачалоЭлемента("ПараметрыЗаявки");
	
	ВывестиНеобязательныйЭлемент(Компоновщик, "ОписаниеПредметаЛизинга", ПараметрыПредставления.ОписаниеПредметаЛизинга);
	ВывестиНеобязательныйЭлемент(Компоновщик, "ОписаниеПоставщика",      ПараметрыПредставления.ОписаниеПоставщика);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ПараметрыЗаявки
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеАкцепта

Процедура ВывестиУзел_Акцепт_Документ(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Документ");
	
	ВывестиАтрибут(Компоновщик, "ПредметОбмена",     	ПараметрыПредставления.ИдентификаторЗаявки, "Т,255");
	ВывестиАтрибут(Компоновщик, "ДатаПредметаОбмена",   ПараметрыПредставления.ДатаЗаявки, "Д");
	ВывестиНеобязательныйАтрибут(Компоновщик, "РегистрационныйНомер", ПараметрыПредставления.РегистрационныйНомер, "Т,30");
	ВывестиАтрибут(Компоновщик, "ДатаДок",              ТекущаяДатаСеанса(), "Д");
	
	ВывестиУзел_Акцепт_Организация(Компоновщик, ПараметрыПредставления);
	ВывестиУзел_Акцепт_Банк(Компоновщик, ПараметрыПредставления);
	ВывестиУзел_Акцепт_Продукт(Компоновщик, ПараметрыПредставления);
		
	Компоновщик.ЗаписатьКонецЭлемента(); // Документ
	
КонецПроцедуры

Процедура ВывестиУзел_Акцепт_Организация(Компоновщик, ПараметрыПредставления)
		
	Организация = ПараметрыПредставления.Организация;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Организация");
		
	ВывестиАтрибут(Компоновщик, "Наим", Организация.Наименование, "Т,1000");
	ВывестиАтрибут(Компоновщик, "ИНН",  Организация.ИНН, "Т,12");
	ВывестиНеобязательныйАтрибут(Компоновщик, "КПП",  Организация.КПП, "Т,9");
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Организация
	
КонецПроцедуры

Процедура ВывестиУзел_Акцепт_Банк(Компоновщик, ПараметрыПредставления)
		
	Банк = ПараметрыПредставления.Банк;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Банк");
	
	ВывестиАтрибут(Компоновщик, "ИНН",  Банк.ИНН, "Т,10");
	ВывестиАтрибут(Компоновщик, "Наим", Банк.Наименование, "Т,250");
		
	Компоновщик.ЗаписатьКонецЭлемента(); // Банк
	
КонецПроцедуры

Процедура ВывестиУзел_Акцепт_Продукт(Компоновщик, ПараметрыПредставления)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Продукт");
		
	ВывестиАтрибут(Компоновщик, "СуммаКредита", ПараметрыПредставления.СуммаКредита, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "Срок",  ПараметрыПредставления.СрокКредита, "Ч,4");
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Продукт
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеСтруктурыПредставлений

Процедура ВывестиУниверсальныйУзел_ФИОТип(Компоновщик, ФИО, ИмяУзла = "ФИО")
	
	Компоновщик.ЗаписатьНачалоЭлемента(ИмяУзла);
	
	ВывестиАтрибут(Компоновщик, "Фамилия", ФИО.Фамилия);
	ВывестиАтрибут(Компоновщик, "Имя", ФИО.Имя);
	ВывестиНеобязательныйАтрибут(Компоновщик, "Отчество", ФИО.Отчество);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ИмяУзла
	
КонецПроцедуры

Процедура ВывестиУниверсальныйУзел_СведенияОФЛТип(Компоновщик, СведенияФЛ, ИмяУзла)
	
	Компоновщик.ЗаписатьНачалоЭлемента(ИмяУзла);
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "ИННФЛ",        СведенияФЛ.ИНН,          "Т,12");
	ВывестиНеобязательныйАтрибут(Компоновщик, "СНИЛС",        СведенияФЛ.СНИЛС,        "Т,14");

	ВывестиНеобязательныйАтрибут(Компоновщик, "Пол",          СведенияФЛ.Пол,          "Т,1");
	ВывестиАтрибут(Компоновщик, "ДатаРождения", СведенияФЛ.ДатаРождения, "Д");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "Гражданство",   СведенияФЛ.Гражданство,   "Т,3");
	ВывестиНеобязательныйАтрибут(Компоновщик, "МестоРождения", СведенияФЛ.МестоРождения, "Т,255");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "СемПолож", СведенияФЛ.СемейноеПоложение, "Т,1");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "Телефон", СведенияФЛ.Телефон,       "Т,20");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ДопТелефон", СведенияФЛ.ДопТелефон, "Т,20");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ЭлПочта", СведенияФЛ.ЭлПочта,       "Т,255");
	
	ВывестиУниверсальныйУзел_ФИОТип(Компоновщик, СведенияФЛ.ФИО);
	
	Компоновщик.ЗаписатьНачалоЭлемента("УдЛичнФЛ");
	
	УдостоверениеЛичности = СведенияФЛ.УдостоверениеЛичности;
	
	ВывестиАтрибут(Компоновщик, "КодВидДок", УдостоверениеЛичности.КодВидаДокумента,    "Т,2");
	ВывестиНеобязательныйАтрибут(Компоновщик, "СерДок",    УдостоверениеЛичности.СерияДокумента,      "Т,14");
	ВывестиАтрибут(Компоновщик, "НомДок",    УдостоверениеЛичности.НомерДокумента,      "Т,14");
	ВывестиАтрибут(Компоновщик, "ВыдДок",    УдостоверениеЛичности.КемВыдан,            "Т,255");
	ВывестиАтрибут(Компоновщик, "ДатаДок",   УдостоверениеЛичности.ДатаВыдачи,          "Д");
	
	ВывестиНеобязательныйАтрибут(Компоновщик, "КодПодр", УдостоверениеЛичности.КодПодразделения, "Т,255");
	
	Компоновщик.ЗаписатьКонецЭлемента(); // УдЛичнФЛ
	
	ВывестиУниверсальныйУзел_АдресТип(Компоновщик, СведенияФЛ.СведенияОбАдресе); 
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ИмяУзла
	
КонецПроцедуры

Процедура ВывестиУниверсальныйУзел_АдресТип(Компоновщик, СведенияОбАдресе, ИмяУзла = "СведенияОбАдресе")
	
	Компоновщик.ЗаписатьНачалоЭлемента(ИмяУзла);
	
	ВывестиАтрибут(Компоновщик, "Представление", СведенияОбАдресе.Представление, "Т,1000");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Индекс", СведенияОбАдресе.Индекс, "Т,6");
	ВывестиАтрибут(Компоновщик, "КодРегион", ?(СведенияОбАдресе.Свойство("КодРегиона"), СведенияОбАдресе.КодРегиона, ""), "Т,2");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Район", СведенияОбАдресе.Район, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Город", СведенияОбАдресе.Город, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "НаселПункт", СведенияОбАдресе.НаселенныйПункт, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Улица", СведенияОбАдресе.Улица, "Т,50");
	ВывестиНеобязательныйАтрибут(Компоновщик, "Дом", СведенияОбАдресе.Здание.Номер, "Т,20");
	
	Если СведенияОбАдресе.Корпуса.Количество() > 0 Тогда
		ВывестиНеобязательныйАтрибут(Компоновщик, "Корпус", СведенияОбАдресе.Корпуса[0].Номер, "Т,20");
	КонецЕсли;
	
	Если СведенияОбАдресе.Помещения.Количество() > 0 Тогда
		ВывестиНеобязательныйАтрибут(Компоновщик, "Кварт", СведенияОбАдресе.Помещения[0].Номер, "Т,20");
	КонецЕсли;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ИмяУзла
	
КонецПроцедуры

// Возвращает пустую структуру сведений о физическом лице, которая используется при выгрузке в XML.
//
Функция НовыеСведенияФЛ()

	Результат = Новый Структура;
	
	Результат.Вставить("ИНН",               "");
	Результат.Вставить("СНИЛС",             "");
	Результат.Вставить("Пол",               ""); // "1" - мужской, "2" - женский.
	Результат.Вставить("ДатаРождения",      '0001-01-01');
	Результат.Вставить("СведенияОбАдресе",   Новый Структура); // Результат функции РаботаСАдресами.СведенияОбАдресе().
	Результат.Вставить("Гражданство",       ""); // Цифровой 3-значный код страны по классификатору ОКСМ.
	Результат.Вставить("МестоРождения",     "");
	Результат.Вставить("СемейноеПоложение", ""); // "1" - Холост/Не замужем, "2" - В разводе, "3" - Женат/Замужем, "4" - Вдовец/Вдова, "5" - Гражданский брак.

	Результат.Вставить("Телефон", "");
	Результат.Вставить("ДопТелефон", "");
	Результат.Вставить("ЭлПочта", "");
	
	Результат.Вставить("ФИО", Новый Структура("Фамилия, Имя, Отчество", "", "", ""));
	
	УдостоверениеЛичности = Новый Структура;
	УдостоверениеЛичности.Вставить("КодВидаДокумента",    "");
	УдостоверениеЛичности.Вставить("СерияДокумента",      "");
	УдостоверениеЛичности.Вставить("НомерДокумента",      "");
	УдостоверениеЛичности.Вставить("КемВыдан",            "");
	УдостоверениеЛичности.Вставить("ДатаВыдачи",          '0001-01-01');
	УдостоверениеЛичности.Вставить("КодПодразделения",    "");
	
	Результат.Вставить("УдостоверениеЛичности", УдостоверениеЛичности);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область КомпоновщикXML
 
Процедура ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	УниверсальныйОбменСБанкамиОбщегоНазначения.ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута);
	
КонецПроцедуры

Процедура ВывестиНеобязательныйАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	УниверсальныйОбменСБанкамиОбщегоНазначения.ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута, Ложь);
	
КонецПроцедуры

Процедура ВывестиНеобязательныйЭлемент(КомпоновщикXML, ИмяЭлемента, ЗначениеЭлемента)

	УниверсальныйОбменСБанкамиОбщегоНазначения.ВывестиНеобязательныйЭлемент(КомпоновщикXML, ИмяЭлемента, ЗначениеЭлемента);

КонецПроцедуры

#КонецОбласти

#Область СостояниеЗаявок

Процедура ЗапуститьУниверсальныйОбменСБанкамиПоСервисуВФоне(Параметры, АдресРезультата) Экспорт
	
	// Скачивание данных с сервера обмена с банками в рег.задании выполняется под полными правами,
	// поэтому если вызываем его интерактивно, то самостоятельно включаем привилегированный режим.
	УстановитьПривилегированныйРежим(Истина);

	УниверсальныйОбменСБанками.ЗапуститьУниверсальныйОбменСБанкамиПоСервису(Параметры.СервисОбменаСБанками);
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ВыгрузкаXML

Функция ПолучитьШаблонИмениФайлаПоТипу(ТипДокумента, СервисОбменаСБанками = Неопределено, ВидОтчета = Неопределено)
	
	Если ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.АнкетаЗаемщика Тогда
		
		Если СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит Тогда
			ШаблонИмениФайла = "LOANAPP";
		Иначе
			ШаблонИмениФайла = "LEASAPP";
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.СогласиеНаОбработкуДанных Тогда
		
		ШаблонИмениФайла = "CONSENT_%1";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.ПланСчетов Тогда
		
		ШаблонИмениФайла = "COA";
	
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовОбменаСБанкамиЗаявкиНаКредит.ОтчетЗаемщика Тогда
		Если ВидОтчета = "ОборотноСальдоваяВедомость" Тогда
			
			ШаблонИмениФайла = "OSV_%2_%3";
			
		ИначеЕсли ВидОтчета = "АнализСчета" Тогда
			
			ШаблонИмениФайла = "ANS_%1_%2_%3";
			
		ИначеЕсли ВидОтчета = "КарточкаСчета" Тогда
			
			ШаблонИмениФайла = "TRN_%1_%2_%3";
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Невозможно получить шаблон имени файла для контейнера: %1 - неизвестный тип вид отчета.'"), ВидОтчета);
		КонецЕсли;
		
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Невозможно получить шаблон имени файла для контейнера: %1 - неизвестный тип документа обмена.'"), 
			ТипДокумента);
	КонецЕсли;

	Возврат ШаблонИмениФайла;
	
КонецФункции

// Возвращает пустую таблицу для подготовки данных учредителей.
//
// Возвращаемое значение:
//	ТаблицаЗначений - см. переменную Результат.
//
Функция НовыеДанныеУчредителей() Экспорт

	ОписаниеТиповСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(0);
	ОписаниеТиповДата   = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата);

	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("НомерСтроки",  ОбщегоНазначения.ОписаниеТипаЧисло(5));
	Результат.Колонки.Добавить("Учредитель",   Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	
	Результат.Колонки.Добавить("Фамилия",       ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("Имя",           ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("Отчество",      ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("ДатаРождения",  ОписаниеТиповДата);
	Результат.Колонки.Добавить("МестоРождения", ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("ИНН",           ОписаниеТиповСтрока);
	
	// Документ, удостоверяющий личность, показывается на форме в виде представления.
	// Отдельные поля нужны для проверки заполнения.
	Результат.Колонки.Добавить("ПредставлениеДокумента", ОписаниеТиповСтрока); 
	Результат.Колонки.Добавить("ВидДокумента",     Новый ОписаниеТипов("СправочникСсылка.ВидыДокументовФизическихЛиц"));
	Результат.Колонки.Добавить("КодВидаДокумента", ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("Серия",            ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("Номер",            ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("ДатаВыдачи",       ОписаниеТиповДата);
	Результат.Колонки.Добавить("КемВыдан",         ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("КодПодразделения", ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("Страна",           ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("ПаспортУчредителя",Метаданные.ОпределяемыеТипы.ПрисоединенныйФайл.Тип);

	// Адрес по прописке.
	Результат.Колонки.Добавить("Адрес",             ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("ЗначениеJSONАдрес", ОписаниеТиповСтрока);
	
	// Контактная информация
	Результат.Колонки.Добавить("ТелефонРабочий",   ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("ТелефонМобильный", ОписаниеТиповСтрока);
	Результат.Колонки.Добавить("EMail",            ОписаниеТиповСтрока);
	
	// Доля в уставном капитале
	Результат.Колонки.Добавить("ДоляУК", ОбщегоНазначения.ОписаниеТипаЧисло(5, 2));
	
	// Комбинированное представление личных данных
	Результат.Колонки.Добавить("ПредставлениеЛичныхДанных", ОписаниеТиповСтрока);
	
	Возврат Результат;

КонецФункции

// Возвращает структуру с пустыми значениями для параметра ПараметрыПредставления функции ЭлектронноеПредставлениеСообщения().
//
// Возвращаемое значение:
//	Структура - Содержит ключи, которые используются при выгрузке заявки на кредит в xml-файл.
//
Функция НовыеСведенияЗаявкиНаКредит() Экспорт
	
	Сведения = Новый Структура;
	
	Сведения.Вставить("Кодировка", "windows-1251");
	
	Сведения.Вставить("ИдентификаторФайла", "");
	Сведения.Вставить("ВерсияПрограммы",    РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	
	Сведения.Вставить("СервисОбменаСБанками",            Перечисления.СервисыОбменаСБанками.ПустаяСсылка());
	Сведения.Вставить("ИдентификаторЗаявки",             "");
	Сведения.Вставить("ДатаЗаявки",                      '0001-01-01');
	Сведения.Вставить("СуммаКредита",                    0);
	Сведения.Вставить("СрокКредита",                     0);
	Сведения.Вставить("ЦельКредита",                     ""); // 1 - Пополнение оборотных средств, 2 - Инвестиции в ОС
	Сведения.Вставить("ИдентификаторКредитногоПродукта", "");
	
	// Специфические поля для лизинга.
	Сведения.Вставить("ТипПредметаЛизинга",              "");
	Сведения.Вставить("ОписаниеПредметаЛизинга",         "");
	Сведения.Вставить("ОписаниеПоставщика",              "");
	Сведения.Вставить("СуммаАванса",                     0);
	
	
	Сведения.Вставить("ЮридическоеФизическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ПустаяСсылка());
	Сведения.Вставить("ОКВЭД", "");
	Сведения.Вставить("ОКОПФ", "");
	Сведения.Вставить("ДатаРегистрации",    '0001-01-01');
	Сведения.Вставить("ДатаФактНачалаДеят", '0001-01-01');
	Сведения.Вставить("ДатаНачалаУчета",    '0001-01-01');
	Сведения.Вставить("ДатаОтчетности",     '0001-01-01');
	
	// Сведения об банке - получателе заявки.
	Банк = Новый Структура;
	Банк.Вставить("ИНН",          "");
	Банк.Вставить("Наименование", "");
	Сведения.Вставить("Банк", Банк);
	
	// Сведения об отправителе - юридическом лице.
	ОтправительЮЛ = Новый Структура;
	
	ОтправительЮЛ.Вставить("Наименование",    "");
	ОтправительЮЛ.Вставить("СокрНаименование","");
	ОтправительЮЛ.Вставить("ИНН",             "");
	ОтправительЮЛ.Вставить("КПП",             "");
	ОтправительЮЛ.Вставить("СведенияОбАдресе", Новый Структура); // Результат функции РаботаСАдресами.СведенияОбАдресе().
	ОтправительЮЛ.Вставить("ОГРН",            "");
	ОтправительЮЛ.Вставить("Нерезидент",      "");  // "0" - резидент, "1" - нерезидент.
	
	ОтправительЮЛ.Вставить("ПолноеНаименованиеДоРеорганизации", "");
	ОтправительЮЛ.Вставить("СокращенноеНаименованиеДоРеорганизации", "");
	
	// Физические лица - учредители юридического лица
	ОтправительЮЛ.Вставить("СоставУчредителей", Перечисления.СоставУчредителейЮридическогоЛица.ПустаяСсылка());
	ОтправительЮЛ.Вставить("Учредители",        НовыеДанныеУчредителей());
	
	Сведения.Вставить("ОтправительЮЛ", ОтправительЮЛ);
	
	// Сведения об отправителе - индивидуальном предпринимателе.
	ОтправительИП = Новый Структура;
	
	ОтправительИП.Вставить("ОГРН",            "");
	
	Сведения.Вставить("ОтправительИП", ОтправительИП);
	
	// Сведения о руководителе юридического лица / индивидуальном предпринимателе.
	Сведения.Вставить("Руководитель",  НовыеСведенияФЛ());
	
	// Сведения о контактном лице.
	КонтактноеЛицо = Новый Структура;
	
	КонтактноеЛицо.Вставить("Должность", "");
	КонтактноеЛицо.Вставить("Телефон",   "");
	КонтактноеЛицо.Вставить("ЭлПочта",   "");
	КонтактноеЛицо.Вставить("ФИО", Новый Структура("Фамилия, Имя, Отчество", "", "", ""));
	
	Сведения.Вставить("КонтактноеЛицо", КонтактноеЛицо);
	
	Сведения.Вставить("СистемыНО", Новый Массив); // "ОСНО", "УСНД", "УСНДМР", "ЕНВД", "ПСНО", "НПД"
	
	Сведения.Вставить("ДополнительныеПоказатели", Новый Массив); // Строка таблицы значений с колонками:
	                                                             // Банк, Идентификатор, ДанныеОтсутствуют, Значение
	                                                             // См. Документ.ЗаявкаНаКредит.ДополнительныеПоказателиБанков()
																 
	Сведения.Вставить("ДополнительныеФайлы", Новый Структура); // Таблицы значений в структуре по видам прикладываемых файлов.
															   // См. Документ.ЗаявкаНаКредит.ДополнительныеФайлыПоВидам()
																 
	Возврат Сведения;
	
КонецФункции

// Формирует xml-файл сообщения для банка по заявке на кредит
//
// Параметры:
//  ВидСообщения - Строка - определяет, какой файл нужно сформировать, возможные значения:
//		* "Заявка"
//		* "Акцепт"
//  ПараметрыПредставления - Структура - контекст выгружаемых данных, состав свойств зависит от типа транзакции.
//		см. НовыеСведенияЗаявкиНаКредит().
// 
// Возвращаемое значение:
//   - ДвоичныеДанные - двоичные данные XML-файла, содержащего сообщение для банка
//
Функция ЭлектронноеПредставлениеСообщения(ВидСообщения, ПараметрыПредставления) Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	Компоновщик = УниверсальныйОбменСБанкамиОбщегоНазначения.НовыйКомпоновщикXML(ИмяВременногоФайла, ПараметрыПредставления.Кодировка);
	
	Компоновщик.ЗаписатьНачалоЭлемента("Файл");
	
	ВывестиАтрибут(Компоновщик, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ВывестиАтрибут(Компоновщик, "ИдФайл",    ПараметрыПредставления.ИдентификаторФайла);
	ВывестиАтрибут(Компоновщик, "ВерсПрог",  ПараметрыПредставления.ВерсияПрограммы);
	
	Если ВидСообщения = "Заявка" Тогда
		ВывестиАтрибут(Компоновщик, "ВерсФорм",  "1.01.07");
		ВывестиУзел_ЗаявкаНаКредит_Документ(Компоновщик, ПараметрыПредставления);
	ИначеЕсли ВидСообщения = "Акцепт" Тогда
		ВывестиАтрибут(Компоновщик, "ВерсФорм",  "1.01.02");
		ВывестиУзел_Акцепт_Документ(Компоновщик, ПараметрыПредставления);
	КонецЕсли;	
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Файл
	
	Возврат УниверсальныйОбменСБанкамиОбщегоНазначения.ДвоичныеДанныеКомпоновщикаXML(Компоновщик, ИмяВременногоФайла);
	
КонецФункции

#КонецОбласти

#Область СогласиеНаОбработкуДанных

// Подготавливает внутри фонового задания текст согласия на обработку персональных данных.
//
// Параметры:
//	ПараметрыЗаполнения - Структура - Содержит ключи:
//		* ПараметрыСогласия - Структура - см. ПараметрыСогласия() или ПараметрыСогласияУчредителей().
//		* Банки - Массив - Содержит элементы типа СправочникСсылка.БанкиУниверсальногоОбмена.
//	АдресХранилища - Строка - Адрес временного хранилища, в которое поместить результат выполнения функции.
//
Процедура ЗаполнитьТекстСогласияВФоне(ПараметрыЗаполнения, АдресХранилища) Экспорт
	
	Если ТипЗнч(ПараметрыЗаполнения.ОбъектыСогласия[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		РеквизитыОбъектов = Новый ТаблицаЗначений;
		РеквизитыОбъектов.Колонки.Добавить("Учредитель");
		РеквизитыОбъектов.Колонки.Добавить("ТекстМакета");
		РеквизитыОбъектов.Колонки.Добавить("ПараметрыМакета");
		
		ИмяМакета = ?(ПараметрыЗаполнения.ПараметрыСогласия.БолееОдногоБанка,
						"СогласиеУчредителяБанкам", "СогласиеУчредителяБанку");
		ТекстМакета = Документы.ЗаявкаНаКредит.ПолучитьМакет(ИмяМакета).ПолучитьТекст();
		
		Объект = РеквизитыОбъектов.Добавить();
		Объект.Учредитель      = ПараметрыЗаполнения.ПараметрыСогласия.Учредитель;
		Объект.ТекстМакета     = ТекстМакета;
		Объект.ПараметрыМакета = УниверсальныйОбменСБанкамиФормы.РазметкаТекста(ТекстМакета);
		
		ИмяРеквизитаПредставленияБанка = "ПредставлениеФЛ";
		
	Иначе
		
		РеквизитыОбъектов = ПодготовитьИнформациюОБанкахДляСогласийПоКредитнымПродуктам(
			ПараметрыЗаполнения.ОбъектыСогласия, ПараметрыЗаполнения.ПараметрыСогласия);
			
		ИмяРеквизитаПредставленияБанка = "ПолноеНаименованиеБанка";
		
	КонецЕсли;
	
	ПараметрыФорматирования = УниверсальныйОбменСБанкамиФормы.ПараметрыФорматированияСогласия();
	ПараметрыФорматирования.ИмяРеквизитаПредставленияБанка = ИмяРеквизитаПредставленияБанка;
	
	Результат = УниверсальныйОбменСБанкамиФормы.ТекстСогласия(ПараметрыЗаполнения.ПараметрыСогласия, РеквизитыОбъектов, ПараметрыФорматирования);
	
	ПоместитьВоВременноеХранилище(Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9)), АдресХранилища);
	
КонецПроцедуры

// Согласие на обработку персональных данных, которое дает заемщик банку.
// Все возможные параметры заемщика, доступные для использования в макете.
//
// Параметры:
//  ЭтоЮридическоеЛицо    - Булево - если Ложь, то нужны параметры индивидуального предпринимателя; иначе юридического лица.
//
// Возвращаемое значение:
//   Структура   - набор доступных параметров макета.
//
Функция ПараметрыСогласия(ЭтоЮридическоеЛицо) Экспорт
	
	Результат = Новый Структура;
	// Определяют заполнение остальных параметров.
	Результат.Вставить("СервисОбменаСБанками", Перечисления.СервисыОбменаСБанками.ПустаяСсылка());
	Результат.Вставить("Организация",          Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("ДатаСогласия",         '00010101');
	Результат.Вставить("ЭтоЮридическоеЛицо",   ЭтоЮридическоеЛицо);
		
	// Общие для всех вариантов.
	Результат.Вставить("ИНН",              "");
	Результат.Вставить("ЮридическийАдрес", "");
	Результат.Вставить("Город",            "");
	Результат.Вставить("ВидСогласия",      Перечисления.ВидыСогласий.ПустаяСсылка());

	// Для индивидуального предпринимателя - данные самого ИП, для юридического лица - данные руководителя.
	Результат.Вставить("ПредставлениеФЛ",          ""); // Полное ФИО
	Результат.Вставить("ПолФЛ",                    Перечисления.ПолФизическогоЛица.ПустаяСсылка());
	Результат.Вставить("ДатаРожденияФЛ",           '00010101');
	Результат.Вставить("СтраховойНомерПФРФЛ",      ""); // СНИЛС
	Результат.Вставить("ПредставлениеДокументаФЛ", ""); // Документ, серия, номер, когда и где выдан
	Результат.Вставить("МестоРожденияФЛ",          ""); // Место рождения в том виде, как оно должно быть выведено в тексте согласия.
	Результат.Вставить("СемейноеПоложениеФЛ", "");
	Результат.Вставить("ОГРН",                "");
	
	Если ЭтоЮридическоеЛицо Тогда
		
		// Информация о самой организации.
		Результат.Вставить("ПолноеНаименование",                     "");
		Результат.Вставить("СокращенноеНаименование",                "");
		Результат.Вставить("ПолноеНаименованиеДоРеорганизации",      "");
		Результат.Вставить("СокращенноеНаименованиеДоРеорганизации", "");
		Результат.Вставить("ДолжностьРуководителяПредставление",     "");

		// Информация о руководителе организации.
		Результат.Вставить("ПредставлениеФЛРодительныйПадеж",  ""); // Заполняется вызывающей процедурой
		Результат.Вставить("ИННФЛ",                            "");
		Результат.Вставить("АдресПоПропискеФизическиеЛицаФЛ",  ""); // Регистрация по месту жительства
		Результат.Вставить("EMailФизическиеЛицаФЛ",            "");
		Результат.Вставить("ТелефонРабочийФизическиеЛицаФЛ",   ""); // Основной телефон
		Результат.Вставить("ТелефонМобильныйФизическиеЛицаФЛ", ""); // Дополнительный телефон
	Иначе
		
		// Информация о самом ИП.
		Результат.Вставить("Email",                            "");
		Результат.Вставить("Телефоны",                         ""); // Основной телефон
		Результат.Вставить("ТелефонМобильныйФизическиеЛицаФЛ", ""); // Дополнительный телефон
		
		Результат.Вставить("СтранаФЛ",            "");
		
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Согласие на обработку персональных данных, которое дают учредители заемщика своему юр.лицу.
// Все возможные параметры учредителей заемщика, доступные для использования в макете.
//
// Возвращаемое значение:
//   Структура   - набор доступных параметров макета.
//
Функция ПараметрыСогласияУчредителей() Экспорт
	
	Результат = Новый Структура;
	// Определяют заполнение остальных параметров.
	Результат.Вставить("СервисОбменаСБанками",   Перечисления.СервисыОбменаСБанками.ПустаяСсылка());
	Результат.Вставить("Организация",            Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("ДатаСогласия",           '00010101');
	Результат.Вставить("ЭтоЮридическоеЛицо",     Ложь); // Для совместимости с ПараметрыСогласия().
	Результат.Вставить("Город",                  "");
	
	// Кто предоставляет согласие.
	Результат.Вставить("Учредитель",             Справочники.ФизическиеЛица.ПустаяСсылка());
	Результат.Вставить("ПредставлениеФЛ",        ""); // Полное ФИО
	Результат.Вставить("Страна",                 "");
	Результат.Вставить("ДатаРожденияФЛ",         '00010101');
	Результат.Вставить("МестоРождения",          ""); // Место рождения в том виде, как оно должно быть выведено в тексте согласия
	Результат.Вставить("АдресПоПрописке",        ""); // Регистрация по месту жительства
	Результат.Вставить("ПредставлениеДокумента", ""); // Документ, серия, номер, когда и где выдан
	Результат.Вставить("ИНН",                    "");
	Результат.Вставить("ТелефонРабочий",         ""); // Основной телефон
	Результат.Вставить("ТелефонМобильный",       ""); // Дополнительный телефон
	Результат.Вставить("EMail",                  "");
	
	// Кому предназначено согласие.
	Результат.Вставить("ПолноеНаименование", "");
	Результат.Вставить("ОГРН",               "");
	Результат.Вставить("ЮридическийАдрес",   "");
	Результат.Вставить("БанкиРеквизиты",     "");
	Результат.Вставить("БолееОдногоБанка",   Ложь); // признак, что банков-получателей согласия несколько

	Возврат Результат;
	
КонецФункции

// Получает из специального сервиса информацию о банках-получателях согласия на обработку персональных данных.
//
// Параметры:
//  КредитныеПродукты         - Строка таблицы значений, см. Документ.ЗаявкаНаКредит.СведенияОБанках(), Массив - о чем собирается информация.
//  ПараметрыСогласияЗаемщика - Структура - см. ПараметрыСогласия() или ПараметрыСогласияУчредителей().
//
// Возвращаемое значение:
//   ТаблицаЗначений - информация о кредиторах.
//
Функция ПодготовитьИнформациюОБанкахДляСогласийПоКредитнымПродуктам(КредитныеПродукты, ПараметрыСогласияЗаемщика) Экспорт
	
	Банки = ОбщегоНазначения.ВыгрузитьКолонку(КредитныеПродукты, "Банк", Истина);
	
	РеквизитыБанков = УниверсальныйОбменСБанками.РеквизитыБанков(Банки, 
		"Наименование, НаименованиеСокращенное, НаименованиеПолное, ИНН, КПП, ОГРН, Адрес, Логотип");
	Для каждого РеквизитыБанка Из РеквизитыБанков Цикл
		РеквизитыБанка.Адрес = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(РеквизитыБанка.Адрес);
	КонецЦикла;
	
	ИмяРеквизитаИдентификатора = ?(ТипЗнч(КредитныеПродукты) = Тип("ТаблицаЗначений"),
		"ИдентификаторКредитногоПродукта", "ИдентификаторПродукта");
	
	СогласияПоПродуктам = Новый ТаблицаЗначений;
	СогласияПоПродуктам.Колонки.Добавить("Банк", Новый ОписаниеТипов("СправочникСсылка.БанкиУниверсальногоОбмена"));
	СогласияПоПродуктам.Колонки.Добавить("ИдентификаторПродукта", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СогласияПоПродуктам.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СогласияПоПродуктам.Колонки.Добавить("ПолноеНаименованиеБанка", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СогласияПоПродуктам.Колонки.Добавить("ИННБанка", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СогласияПоПродуктам.Колонки.Добавить("КППБанка", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СогласияПоПродуктам.Колонки.Добавить("ОГРНБанка", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СогласияПоПродуктам.Колонки.Добавить("ЮридическийАдресБанка", ОбщегоНазначения.ОписаниеТипаСтрока(0)); 
	СогласияПоПродуктам.Колонки.Добавить("ВидСогласия", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыСогласий"));
	СогласияПоПродуктам.Колонки.Добавить("ТекстМакета", Новый ОписаниеТипов("Строка"));
	СогласияПоПродуктам.Колонки.Добавить("ПараметрыМакета", Новый ОписаниеТипов("ТаблицаЗначений"));
	СогласияПоПродуктам.Колонки.Добавить("Логотип", Новый ОписаниеТипов("ДвоичныеДанные"));
	
	Если ЗначениеЗаполнено(ПараметрыСогласияЗаемщика.ВидСогласия) Тогда
		МассивВидовСогласий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыСогласияЗаемщика.ВидСогласия);
	Иначе
		МассивВидовСогласий = Новый Массив;
		МассивВидовСогласий.Добавить(Перечисления.ВидыСогласий.БКИ);
		МассивВидовСогласий.Добавить(Перечисления.ВидыСогласий.ПДН);
	КонецЕсли;
	
	Для Каждого ВидСогласия Из МассивВидовСогласий Цикл
		
		Для Каждого КредитныйПродукт Из КредитныеПродукты Цикл
			
			НоваяСтрока = СогласияПоПродуктам.Добавить();
			НоваяСтрока.Банк = КредитныйПродукт.Банк;
			НоваяСтрока.ИдентификаторПродукта = КредитныйПродукт[ИмяРеквизитаИдентификатора];
			НоваяСтрока.ВидСогласия = ВидСогласия;

			РеквизитыБанка = РеквизитыБанков.Найти(НоваяСтрока.Банк, "Банк");
			Если РеквизитыБанка <> Неопределено Тогда
				НаименованиеБанкаСокращенное = ?(ЗначениеЗаполнено(РеквизитыБанка.НаименованиеСокращенное),
					РеквизитыБанка.НаименованиеСокращенное, РеквизитыБанка.Наименование);
					
				НоваяСтрока.Наименование            = НаименованиеБанкаСокращенное;
				НоваяСтрока.ПолноеНаименованиеБанка = РеквизитыБанка.НаименованиеПолное;
				НоваяСтрока.ИННБанка                = РеквизитыБанка.ИНН;
				НоваяСтрока.КППБанка                = РеквизитыБанка.КПП;
				НоваяСтрока.ОГРНБанка               = РеквизитыБанка.ОГРН;
				НоваяСтрока.ЮридическийАдресБанка   = РеквизитыБанка.Адрес;
				НоваяСтрока.Логотип                 = РеквизитыБанка.Логотип;
			КонецЕсли;
			
		КонецЦикла;
				
	КонецЦикла;
	
	Если ПараметрыСогласияЗаемщика <> Неопределено Тогда
		ПодобратьТекстыМакетов(СогласияПоПродуктам, ПараметрыСогласияЗаемщика);
	КонецЕсли;

	Возврат СогласияПоПродуктам;
	
КонецФункции

#КонецОбласти
