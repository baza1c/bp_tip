////////////////////////////////////////////////////////////////////////////////
// УчетУСНКлиентСервер: процедуры и функции для отражения документов в учете УСН,
// которые могут вызываться как на клиенте в формах документов, так и на сервере
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает сведения о применении на указанную дату коэффициента-дефлятора
// для индексации базового лимита доходов, ограничивающих право применения УСН.
//
// Параметры:
//  Период - Дата - Дата, на которую получаем сведения об использовании дефлятора.
//
// Возвращаемое значение:
//  Булево - если Истина, то коэффициент-дефлятор применяется.
//
Функция ИспользуетсяКоэффициентДефлятор(Период) Экспорт
	
	Возврат Год(Период) < 2017 Или КоэффициентДефлятор(Период) <> 1;
	
КонецФункции

// Возвращает размер коэффициента ежегодной индексации величины дохода УСН.
//
// Параметры:
//  Период - Дата - Дата, на которую необходимо получить коэффициент.
//
// Возвращаемое значение:
//  Число
//
Функция КоэффициентДефлятор(Период) Экспорт
	
	КоэффициентДефлятор = 1;
	
	Если Год(Период) >= 2026 Тогда
		// Приказ Минэкономразвития России от 6 ноября 2025 г. N 734
		КоэффициентДефлятор = 1.090;
	ИначеЕсли Год(Период) = 2025 Тогда
		// Федеральный закон от 12.07.2024 № 176-ФЗ
		КоэффициентДефлятор = 1;
	ИначеЕсли Год(Период) = 2024 Тогда
		КоэффициентДефлятор = 1.329; // Приказ Минэкономразвития России от 23 октября 2023 г. N 730
	ИначеЕсли Год(Период) = 2023 Тогда
		КоэффициентДефлятор = 1.257; // Приказ Минэкономразвития России от 19 октября 2022 г. N 573
	ИначеЕсли Год(Период) = 2022 Тогда
		КоэффициентДефлятор = 1.096; // Приказ Минэкономразвития России от 28 октября 2021 г. N 654
	ИначеЕсли Год(Период) = 2021 Тогда
		// Приказ Минэкономразвития России от 30.10.2020 № 720.
		// В 2021 году в связи с появлением новых лимитов, коэффициент-дефлятор не используется
		// и начинает влиять на лимиты УСН только с 2022 года.
		// см. Письмо Минфина от 27 января 2021 г. N 03-11-06/2/4855
		КоэффициентДефлятор = 1;
	ИначеЕсли Год(Период) >= 2017 Тогда
		КоэффициентДефлятор = 1;     // Федеральный закон от 03.07.2016 N 243-ФЗ
	ИначеЕсли Год(Период) = 2016 Тогда
		КоэффициентДефлятор = 1.329; // Приказ Минэкономразвития России от 20.10.2015 № 772
	ИначеЕсли Год(Период) = 2015 Тогда
		КоэффициентДефлятор = 1.147; // приказ Минэкономразвития России от 29.10.2014 № 685
	ИначеЕсли Год(Период) = 2014 Тогда
		КоэффициентДефлятор = 1.067; // приказ Минэкономразвития России от 07.11.2013 № 652
	КонецЕсли;
	
	Возврат КоэффициентДефлятор;
	
КонецФункции

// Возвращает точность округления показателей УСН при расчете налога.
//
// Параметры:
//  ДатаРасчета - Дата - дата, на которую необходимо определить точность округления.
//
// Возвращаемое значение:
//  Число - точность округления
//
Функция РазрядностьОкругленияПоказателей(ДатаРасчета) Экспорт

	// С 1 квартала 2015 года расчет налога при выполнении "Закрытия месяца" производится в соответствии с
	// порядком заполнения деларации по УСН, опубликованным 17 декабря 2014 г. (приказ ФНС РФ № ММВ-7-3/352@):
	// промежуточные показатели, влияющие на налог и на авансовые платежи, перед расчетом округляются до целых рублей.
	//
	// Алгоритм расчета, применявшийся в регламентной операции до конца 2014 г., не изменяется.
	
	Возврат ?(ДатаРасчета >= '20150101', 0, 2);

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Заполнение данных для УСН в формах документов

// Возвращает структуру параметров для настройки отражения документа в УСН
// значениями из реквизитов переданной формы и объекта
//
Функция ПараметрыФормыДокументаДляУСН(Форма) Экспорт

	Параметры = НовыеПараметрыДокументаДляОтраженияВУСН();
	
	ЗаполнитьЗначенияСвойств(Параметры, Форма); // Параметры из ФО формы
	
	Если Не Параметры.ПрименениеУСН И Не Параметры.ПрименяетсяАУСН Тогда
		Возврат Параметры;
	КонецЕсли; 
	
	Объект = Форма.Объект;
	
	ЗаполнитьЗначенияСвойств(Параметры, Форма.Объект); // Параметры из реквизитов объекта
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПКО.РозничнаяВыручка") Тогда
		Параметры.НТТПоПродажнымЦенам = Объект.ВыручкаСНТТ И Форма.УчетВПродажныхЦенах;
		Параметры.НТТНаЕНВД           = Форма.НТТНаЕНВД;
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка") Тогда
		Параметры.НТТПоПродажнымЦенам = Форма.УчетВПродажныхЦенах;
		Параметры.НТТНаЕНВД           = Форма.НТТНаЕНВД;
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Конструктор структуры входных параметров для методов заполнения отражения документов в УСН.
//
// Возвращаемое значение:
//   Структура:
//    * ПрименениеУСН - Булево
//    * ПрименениеУСНДоходы - Булево
//    * ПрименяетсяАУСН - Булево
//    * ПрименяетсяАУСНДоходы - Булево
//    * ПрименяетсяОсобыйНалоговыйРежим - Булево - признак применения ЕНВД или патентной системы.
//    * Дата - Дата - дата документа
//    * ВидОперации - Произвольный - вид операции документа, тип определяется документом
//    * Налог - СправочникСсылка.ВидыНалоговИПлатежейВБюджет, Неопределено - налог, заполняется при уплате налога
//    * НалоговыйПериод - Дата - период, за который уплачивается налог
//    * Контрагент - СправочникСсылка.Контрагенты, СправочникСсылка.Склады, СправочникСсылка.ФизическиеЛица Неопределено -
//                   контрагент, физическое лицо или склад (зависит от вида операции документа)
//    * ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов, Неопределено - договор с контрагентом
//    * СчетУчетаРасчетовСКонтрагентом - ПланСчетовСсылка.Хозрасчетный, Неопределено
//    * Выдано - Строка - содержание поля "Выдано" расходного кассового ордера
//    * ВалютаДокумента - СправочникСсылка.Валюты, Неопределено
//    * СтатьяДвиженияДенежныхСредств - СправочникСсылка.СтатьиДвиженияДенежныхСредств, Неопределено
//    * СубконтоДт1 - Произвольный
//    * СубконтоКт1 - Произвольный
//    * ВалютаРегламентированногоУчета - СправочникСсылка.Валюты, Неопределено
//    * КурсДокумента - Число
//    * КратностьДокумента - Число
//    * НТТПоПродажнымЦенам - Булево
//    * НТТНаЕНВД - Булево
//
Функция НовыеПараметрыДокументаДляОтраженияВУСН() Экспорт
	
	Параметры = Новый Структура;
	
	// Обязательные - всегда заполнены.
	Параметры.Вставить("ПрименениеУСН", Ложь);
	Параметры.Вставить("ПрименениеУСНДоходы", Ложь);
	Параметры.Вставить("ПрименяетсяАУСН", Ложь);
	Параметры.Вставить("ПрименяетсяАУСНДоходы", Ложь);
	
	// Общие - заполняются из существующих свойств объекта и реквизитов формы.
	Параметры.Вставить("ПрименяетсяОсобыйНалоговыйРежим", Ложь); // Есть патент или ЕНВД
	Параметры.Вставить("Дата");
	Параметры.Вставить("ВидОперации");
	Параметры.Вставить("Налог");
	Параметры.Вставить("НалоговыйПериод");
	Параметры.Вставить("Контрагент");
	Параметры.Вставить("ДоговорКонтрагента");
	Параметры.Вставить("СчетКонтрагента");
	Параметры.Вставить("СчетУчетаРасчетовСКонтрагентом");
	Параметры.Вставить("ПринятоОт", "");
	Параметры.Вставить("Выдано", "");
	Параметры.Вставить("ВалютаДокумента");
	Параметры.Вставить("СтатьяДвиженияДенежныхСредств");
	Параметры.Вставить("СубконтоДт1");
	Параметры.Вставить("СубконтоКт1");
	Параметры.Вставить("ВалютаРегламентированногоУчета");
	Параметры.Вставить("КурсДокумента", 1);
	Параметры.Вставить("КратностьДокумента", 1);
	
	// Специфичные - заполняются явным образом по месту использования.
	Параметры.Вставить("НТТПоПродажнымЦенам", Ложь);
	Параметры.Вставить("НТТНаЕНВД",           Ложь);
	
	Возврат Параметры;
	
КонецФункции

// Конструктор структуры входных параметров для методов заполнения отражения строк платежей в УСН.
//
// Возвращаемое значение:
//   Структура:
//    * СуммаПлатежа - Число - сумма в строке платежа
//    * ОтражениеВУСН - ПеречислениеСсылка.ОтражениеВУСН - отражение строки в УСН
//    * РасходыУСН - Число - сумма принимаемых расходов УСН
//    * НДСУСН - Число - сумма НДС, включенного в сумму принимаемых расходов УСН
//    * СчетУчетаРасчетовСКонтрагентом - ПланСчетовСсылка.Хозрасчетный
//    * ВидПлатежаПоКредитамЗаймам - ПеречислениеСсылка.ВидыПлатежейПоКредитамЗаймам
//
Функция НовыеДанныеСтрокиПлатежаДляОтраженияВУСН() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("СуммаПлатежа", 0);
	Параметры.Вставить("ОтражениеВУСН", ПредопределенноеЗначение("Перечисление.ОтражениеВУСН.ПустаяСсылка"));
	Параметры.Вставить("РасходыУСН", 0);
	Параметры.Вставить("НДСУСН", 0);
	Параметры.Вставить("СчетУчетаРасчетовСКонтрагентом",
		ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка"));
	Параметры.Вставить("ВидПлатежаПоКредитамЗаймам",
		ПредопределенноеЗначение("Перечисление.ВидыПлатежейПоКредитамЗаймам.ПустаяСсылка"));
	
	Возврат Параметры;
	
КонецФункции

// Возвращает ставки налога УСН, указанные в НК РФ.
// Ставка УСН доходы - п. 1 ст. 346.20 НК РФ.
// Ставка УСН доходы минус расходы - п. 2 ст. 346.20 НК РФ.
// Ставка минимального налога УСН доходы минус расходы - п. 6 ст. 346.18 НК РФ.
// Повышенная ставка УСН доходы - п. 1.1 ст. 346.20 НК РФ.
// Повышенная ставка УСН доходы минус расходы - п. 2.1 ст. 346.20 НК РФ.
//
// Возвращаемое значение:
//  Структура - ставки налогов.
//    * СтавкаУСНДоходы - Число.
//    * СтавкаУСНДоходыМинусРасходы - Число.
//    * СтавкаМинимальногоНалогаУСНДоходыМинусРасходы - Число.
//    * СтавкаУСНДоходыПовышенная - Число
//    * СтавкаУСНДоходыМинусРасходыПовышенная - Число
//
Функция НалоговыеСтавкиПоУмолчанию(Период = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СтавкаУСНДоходы", 6);
	Результат.Вставить("СтавкаУСНДоходыМинусРасходы", 15);
	Результат.Вставить("СтавкаМинимальногоНалогаУСНДоходыМинусРасходы", 1);
	Если Период = Неопределено
		Или (ДатаНачалаПрогрессивнойШкалы() <= Период И Период < ДатаОтменыПрогрессивнойШкалы()) Тогда
		Результат.Вставить("СтавкаУСНДоходыПовышенная", 8);
		Результат.Вставить("СтавкаУСНДоходыМинусРасходыПовышенная", 20);
	Иначе
		Результат.Вставить("СтавкаУСНДоходыПовышенная", Неопределено);
		Результат.Вставить("СтавкаУСНДоходыМинусРасходыПовышенная", Неопределено);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Формирование Книги учета доходов и расходов

Функция ДатаНачалаФормирования4РазделаКУДиР() Экспорт
	
	Возврат Дата(2013, 1, 1);
	
КонецФункции

// Дата, с которой требуется формировать раздел V "Уплаченный торговый сбор" Книги учета доходов и расходов.
//
Функция ДатаНачалаФормирования5РазделаКУДиР() Экспорт
	
	// Приказ Минфина РФ от 07.12.2016 N 227н.
	Возврат Дата(2018, 1, 1);
	
КонецФункции

// Дата, с которой отменяется формирование раздела IV "Уменьшение налога" Книги учета доходов и расходов.
// Приказ ФНС России от 07.11.2023 № ЕА-7-3/816@
// "Об утверждении форм книги учета доходов и расходов организаций
// и индивидуальных предпринимателей, применяющих упрощенную систему налогообложения,
// книги учета доходов индивидуальных предпринимателей, применяющих патентную систему
// налогообложения, и порядков их заполнения"
// 
// Возвращаемое значение:
//  Дата - 01 января 2024 года
//
Функция ДатаОтменыФормирования4РазделаКУДиР() Экспорт
	
	// Приказ ФНС России от 07.11.2023 № ЕА-7-3/816@
	Возврат Дата(2024, 1, 1);
	
КонецФункции

// Возвращает дату начала действия прогрессивной шкалы УСН 
// 
// Возвращаемое значение:
//  Дата 
//
Функция ДатаНачалаПрогрессивнойШкалы() Экспорт
	
	// Федеральный закон от 31.07.2020 № 266-ФЗ
	Возврат Дата(2021, 1, 1);
	
КонецФункции

// Возвращает дату отмены действия прогрессивной шкалы УСН 
// 
// Возвращаемое значение:
//  Дата 
//
Функция ДатаОтменыПрогрессивнойШкалы() Экспорт
	
	// Федеральный закон от 12.07.2024 № 176-ФЗ
	Возврат Дата(2025, 1, 1);
	
КонецФункции

// Возвращает дату, с которой применяется НДС на УСН
//
// Возвращаемое значение:
//  Дата
//
Функция ДатаНачалаПримененияНДС() Экспорт
	
	// Федеральный закон от 12.07.2024 № 176-ФЗ
	Возврат Дата(2025, 1, 1);
	
КонецФункции

// Возвращает дату последнего изменения границы дохода, при превышении которой требуется применять НДС на УСН
//
// Возвращаемое значение:
//  Дата
//
Функция ПоследнийПериодИзмененияГраницыНДС() Экспорт
	
	// Федеральный закон от 28.11.2025 № 425-ФЗ
	Возврат Дата(2028, 1, 1);
	
КонецФункции

// Возвращает границу дохода, при превышении которой требуется применять НДС на УСН
//
// Параметры:
//  Дата - Дата - дата, на которую необходимо вернуть границу
//
// Возвращаемое значение:
//  Число
//
Функция ГраницаПримененияНДС(Дата) Экспорт
	
	Если Год(Дата) >= 2028 Тогда
		// Федеральный закон от 28.11.2025 № 425-ФЗ
		ГраницаДоходов = 10000000;
	ИначеЕсли Год(Дата) >= 2027 Тогда
		// Федеральный закон от 28.11.2025 № 425-ФЗ
		ГраницаДоходов = 15000000;
	ИначеЕсли Год(Дата) >= 2026 Тогда
		// Федеральный закон от 28.11.2025 № 425-ФЗ
		ГраницаДоходов = 20000000;
	Иначе
		// Федеральный закон от 12.07.2024 № 176-ФЗ
		ГраницаДоходов = 60000000;
	КонецЕсли;
	
	Возврат ГраницаДоходов;
	
КонецФункции

// Возвращает границу дохода (без индексации), при превышении которой требуется применять ставку НДС 7%
//
// Возвращаемое значение:
//  Число
//
Функция БазоваяГраницаПримененияПониженнойСтавкиНДС() Экспорт
	
	// Федеральный закон от 12.07.2024 № 176-ФЗ
	Возврат 250000000;
	
КонецФункции

// Возвращает границу дохода (с индексацией), при превышении которой требуется применять ставку НДС 7%
//
// Возвращаемое значение:
//  Число
//
Функция ГраницаПримененияПониженнойСтавкиНДС(Дата) Экспорт
	
	БазовыйЛимитДоходов = БазоваяГраницаПримененияПониженнойСтавкиНДС();
	
	// Коэффициент ежегодной индексации
	КоэффициентДефлятор = КоэффициентДефлятор(Дата);
	
	// Федеральный закон от 12.07.2024 № 176-ФЗ
	Возврат БазовыйЛимитДоходов * КоэффициентДефлятор;
	
КонецФункции

#Область ЛьготныеСтавки

// Проверяет, является ли переданная налоговая ставка льготной (сниженной).
//
// Параметры:
//  РазмерСтавки - Число - размер налоговой ставки в процентах.
//  ПрименяетсяУСНДоходы - Булево - если Истина, проверяется ставка для объекта налогообложения "доходы",
//                                  в противном случае для объекта "доходы минус расходы".
//
// Возвращаемое значение:
//   Булево - если Истина, налоговая ставка льготная.
//
Функция ЭтоЛьготнаяСтавка(РазмерСтавки, ПрименяетсяУСНДоходы) Экспорт
	
	СтавкиПоУмолчанию = НалоговыеСтавкиПоУмолчанию();
	
	Если ПрименяетсяУСНДоходы Тогда
		СтавкаПоУмолчанию = СтавкиПоУмолчанию.СтавкаУСНДоходы;
	Иначе
		СтавкаПоУмолчанию = СтавкиПоУмолчанию.СтавкаУСНДоходыМинусРасходы;
	КонецЕсли;
	
	Возврат РазмерСтавки < СтавкаПоУмолчанию;
	
КонецФункции

// Проверяет, требуется ли заполнять основание применения льготной налоговой ставки
// при подготовке декларации за переданный налоговый период.
//
// Параметры:
//  Период - Дата - дата в налоговом периоде декларации.
//
// Возвращаемое значение:
//   Булево - если Истина, основание льготной ставки требуется.
//
Функция ТребуетсяОснованиеЛьготнойСтавкиНалога(Период) Экспорт
	
	Возврат Период >= Дата(2021, 1, 1); // Основание льготы требуется начиная с декларации за 2021 год.
	
КонецФункции

// Возвращает строковый код основания применения льготной налоговой ставки в формате декларации
// на основании номеров статьи, пункта и подпункта нормативного акта.
//
// Параметры:
//  НомерСтатьи - Строка - номер статьи нормативного акта.
//  Пункт       - Строка - номер пункта статьи нормативного акта.
//  Подпункт    - Строка - номер подпункта статьи нормативного акта.
//
// Возвращаемое значение:
//  Строка - код основания применения льготной ставки налога (длина - 12).
//
Функция ПолныйКодОснованияЛьготы(Знач НомерСтатьи, Знач Пункт, Знач Подпункт) Экспорт
	
	НомерСтатьи = ЗначащиеДанныеНормативногоПункта(НомерСтатьи);
	Пункт       = ЗначащиеДанныеНормативногоПункта(Пункт);
	Подпункт    = ЗначащиеДанныеНормативногоПункта(Подпункт);
	
	Если ПустаяСтрока(НомерСтатьи) И ПустаяСтрока(Пункт) И ПустаяСтрока(Подпункт) Тогда
		Возврат "";
	КонецЕсли;
	
	НомерСтатьиПоФормату = ПолныйКодНормативногоПункта(НомерСтатьи);
	ПунктПоФормату       = ПолныйКодНормативногоПункта(Пункт);
	ПодпунктПоФормату    = ПолныйКодНормативногоПункта(Подпункт);
	
	Возврат (НомерСтатьиПоФормату + ПунктПоФормату + ПодпунктПоФормату);
	
КонецФункции

// Формирует по переданному коду основания льготы развернутые сведения о норме регионального закона -
// номере статьи, пункте и подпункте, на основании которых применяется льготная ставка налога.
//
// Параметры:
//  КодОснованияЛьготы  - Строка - код основания применения льготной ставки налога в формате декларации (длина - 12).
//                                 См. также УчетУСНКлиентСервер.ПолныйКодОснованияЛьготы.
//
// Возвращаемое значение:
//   Структура - разобранный код основания льготы:
//     * НомерСтатьи - Строка
//     * Пункт       - Строка
//     * Подпункт    - Строка
//
Функция НормативныеДанныеОснованияЛьготы(Знач КодОснованияЛьготы) Экспорт
	
	Результат = Новый Структура("НомерСтатьи, Пункт, Подпункт", "", "", "");
	
	Если ПустаяСтрока(КодОснованияЛьготы) Тогда
		Возврат Результат;
	КонецЕсли;
	
	КодОснованияЛьготы = СокрЛП(КодОснованияЛьготы);
	
	Если СтрДлина(КодОснованияЛьготы) <> ДлинаОснованияЛьготы() Тогда
		ВызватьИсключение НСтр("ru = 'Неверный формат кода основания льготной ставки УСН'");
	КонецЕсли;
	
	ДлинаРазряда = ДлинаРазрядаОснованияЛьготы();
	
	НомерСтатьиПоФормату = Сред(КодОснованияЛьготы, 1, ДлинаРазряда);
	ПунктПоФормату       = Сред(КодОснованияЛьготы, 1 + ДлинаРазряда, ДлинаРазряда);
	ПодпунктПоФормату    = Сред(КодОснованияЛьготы, 1 + 2 * ДлинаРазряда, ДлинаРазряда);
	
	Результат.НомерСтатьи = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(НомерСтатьиПоФормату, "0");
	Результат.Пункт       = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ПунктПоФормату, "0");
	Результат.Подпункт    = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ПодпунктПоФормату, "0");
	
	Возврат Результат;
	
КонецФункции

// Возвращает ссылку на раздел статьи ИТС с информацией о льготных налоговых ставках для даннного региона.
//
// Параметры:
//  КодРегиона - Строка - код региона, для которого требуется информация.
//  Период     - Дата - дата, на которую требуется информация.
//
// Возвращаемое значение:
//   Строка - текст гиперссылки на соответствующий региону якорь статьи ИТС.
//
Функция СсылкаНаРазделИТСРегиональныеСтавкиУСН(КодРегиона, Период) Экспорт
	
	ОсновнаяСсылка = ОсновнаяСсылкаНаРазделИТСРегиональныеСтавкиУСН(Период);
	
	Если Не ЗначениеЗаполнено(КодРегиона) Тогда
		Возврат ОсновнаяСсылка;
	КонецЕсли;
	
	Ссылка = СтрШаблон("%1:reg%2", ОсновнаяСсылка, КодРегиона);
	
	Возврат Ссылка;
	
КонецФункции

#КонецОбласти

// Возвращает границу дохода (без индексации), при превышении которой требуется применять ставку НДС 20%
//
// Возвращаемое значение:
//  Число
//
Функция БазоваяГраницаПримененияПовышеннойСтавкиНДС() Экспорт
	
	// Федеральный закон от 12.07.2024 № 176-ФЗ
	Возврат 450000000;
	
КонецФункции

// Возвращает границу дохода (с индексацией), при превышении которой требуется применять ставку НДС 20%
//
// Возвращаемое значение:
//  Число
//
Функция ГраницаПримененияПовышеннойСтавкиНДС(Дата) Экспорт
	
	БазовыйЛимитДоходов = БазоваяГраницаПримененияПовышеннойСтавкиНДС();
	
	// Коэффициент ежегодной индексации
	КоэффициентДефлятор = КоэффициентДефлятор(Дата);
	
	// Федеральный закон от 12.07.2024 № 176-ФЗ
	Возврат БазовыйЛимитДоходов * КоэффициентДефлятор;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЛьготныеСтавкиСлужебныеМетоды

Функция ПолныйКодНормативногоПункта(НормативныйПункт)
	
	ДлинаРазряда = ДлинаРазрядаОснованияЛьготы();
	
	Возврат СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НормативныйПункт, ДлинаРазряда);
	
КонецФункции

Функция ЗначащиеДанныеНормативногоПункта(Знач НормативныйПункт)
	
	НекорректныеСимволы = Новый Массив;
	НекорректныеСимволы.Добавить("0");
	НекорректныеСимволы.Добавить("-");
	
	НормативныйПункт = СокрЛП(НормативныйПункт);
	
	Для Каждого НекорректныйСимвол Из НекорректныеСимволы Цикл
		НормативныйПункт = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(НормативныйПункт, НекорректныйСимвол);
	КонецЦикла;
	
	Возврат Лев(НормативныйПункт, ДлинаРазрядаОснованияЛьготы());
	
КонецФункции

Функция ДлинаРазрядаОснованияЛьготы()
	
	Возврат 4;
	
КонецФункции

Функция ДлинаОснованияЛьготы()
	
	Возврат 12;
	
КонецФункции

Функция ОсновнаяСсылкаНаРазделИТСРегиональныеСтавкиУСН(Период = '00010101')
	
	Если Не ЗначениеЗаполнено(Период) Или Период >= '20210101' Тогда
		Возврат "https://its.1c.ru/db/taxusn#content:486:hdoc";
	Иначе
		Возврат "https://its.1c.ru/db/taxusn#content:472:hdoc";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти