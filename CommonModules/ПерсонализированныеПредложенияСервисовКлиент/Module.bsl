#Область ПрограммныйИнтерфейс

// Вызывает форму, которую нужно открыть при нажатии на гиперссылку в баннере.
// Параметры:
// 		НавигационнаяСсылка - Строка
// 		СтандартнаяОбработка - Булево
// 		Баннер - Произвольный - см. ПерсонализированныеПредложенияСервисов.НовыйБаннерДляСписка()
// 		Форма - Форма, в которой произошло нажатие на навигационную ссылку
//
Процедура ПерейтиПоСсылкеБаннера(НавигационнаяСсылка, СтандартнаяОбработка, Баннер, Форма) Экспорт
	
	ЭтоИдентификаторПереопределяемогоБаннера = Ложь;
	ПерсонализированныеПредложенияСервисовПереопределяемыйКлиент.ИдентификаторБаннера(НавигационнаяСсылка, ЭтоИдентификаторПереопределяемогоБаннера);
	
	Если ЭтоИдентификаторПереопределяемогоБаннера Тогда
		ПерсонализированныеПредложенияСервисовПереопределяемыйКлиент.ПерейтиПоСсылкеБаннера(НавигационнаяСсылка, СтандартнаяОбработка, Баннер, Форма);
	
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераСтороннийСертификат()
		ИЛИ НавигационнаяСсылка = ИдентификаторБаннераЧисленностьСотрудников()
		ИЛИ НавигационнаяСсылка = ИдентификаторБаннераОтчетнаяКампания() Тогда
		
		СтандартнаяОбработка = Ложь;
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ПоказатьФормуПредложениеОформитьЗаявлениеНаПодключение();
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераНДСКВычетуКонтрагент()
		ИЛИ НавигационнаяСсылка = ИдентификаторБаннераКрупнейшийДебитор() Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Контрагент", Баннер.ДанныеБаннера.Контрагент);
		ОткрытьФорму("Отчет.ДосьеКонтрагента.Форма", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераКоличествоКонтрагентов() Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Баннер", Баннер);
		ОткрытьФорму("ОбщаяФорма.Реклама1СКонтрагент", ПараметрыФормы);
	
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераКрупнейшиеДебиторыСПАРК() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПодключитьТестовыйПериодЗавершение", 
			ПерсонализированныеПредложенияСервисовКлиент,
			Новый Структура("ИмяОтчета", "НадежностьДебиторовСПАРКРискиБП"));
			
		ПодключениеСервисовСопровожденияКлиент.ПодключитьТестовыйПериод(
			СПАРКРискиКлиентСервер.ИдентификаторСервиса(),
			Неопределено,
			ОписаниеОповещения);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераНДСКВычетуСПАРК() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПодключитьТестовыйПериодЗавершение", 
			ПерсонализированныеПредложенияСервисовКлиент,
			Новый Структура("ИмяОтчета", "ВходящийНДССПАРКРиски"));
			
		ПодключениеСервисовСопровожденияКлиент.ПодключитьТестовыйПериод(
			СПАРКРискиКлиентСервер.ИдентификаторСервиса(),
			Неопределено,
			ОписаниеОповещения);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераОбщееКоличествоДокументов()
		ИЛИ НавигационнаяСсылка = ИдентификаторБаннераКоличествоПоступлений()
		ИЛИ НавигационнаяСсылка = ИдентификаторБаннераКоличествоРеализаций() Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Баннер", Баннер);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьПомощникПодключенияЭДОПриЗакрытииРекламы", ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.Реклама1СЭДО", ПараметрыФормы,,,,, ОписаниеОповещения);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераНадежностьБанков() Тогда
		
		СтандартнаяОбработка = Ложь;
		НадежностьБанковКлиент.ОткрытьИнформациюОСобытии(Баннер.ДанныеБаннера);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераПлатформаСамозанятые() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеБаннера = Баннер.ДанныеБаннера;
		СсылкаНаОблако = Неопределено;
		Если ДанныеБаннера.Свойство("ДанныеСобытия") И ДанныеБаннера.ДанныеСобытия.Свойство("ПереходВОблако", СсылкаНаОблако) Тогда
			ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку(СсылкаНаОблако);
		Иначе
			ПараметрыФормы = Новый Структура("ДанныеБаннера", Баннер.ДанныеБаннера);
			ОткрытьФорму("Обработка.ПомощникИнтеграцииСПлатформойСамозанятые.Форма", ПараметрыФормы);
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераПоддержкаСервиса() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекущееСообщение = Баннер.ДанныеБаннера.ДанныеСобытия;
		ТипИнформации    = Строка(ТекущееСообщение.ТипИнформации);
		Если ТипИнформации = "Недоступность" Или ТипИнформации = "Новость" Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ОткрытьНовость", Истина);
			ПараметрыФормы.Вставить("Идентификатор",  ТекущееСообщение.Идентификатор);
			ОткрытьФорму("Обработка.ИнформационныйЦентр.Форма.ОтображениеСообщений", ПараметрыФормы);
		ИначеЕсли ТипИнформации = "УведомлениеОПожелании" Тогда
			ИдентификаторИдеи = Строка(ТекущееСообщение.Идентификатор);
			ИнформационныйЦентрКлиент.ПоказатьИдею(ИдентификаторИдеи);
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераТестовыйТариф() Тогда
	
		СтандартнаяОбработка = Ложь;
		ОплатаСервисаКлиент.ОткрытьФормуОплатыСервиса();
		
	ИначеЕсли СтрНачинаетсяС(НавигационнаяСсылка, ИдентификаторБаннераПредодобренныйКредитВСбербанке()) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если НавигационнаяСсылка = ИдентификаторБаннераПредодобренныйКредитВСбербанке() Тогда
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ИмяФормы", Форма.ИмяФормы);
			ОбработкаВыбораОрганизации = Новый ОписаниеОповещения("ПредодобренныйКредитВСбербанкеОбработкаВыбораОрганизации",
				ЭтотОбъект, ДополнительныеПараметры);
			СписокОрганизаций = ПерсонализированныеПредложенияСервисовВызовСервера.ПредодобренныйКредитВСбербанкеПолучитьСписокОрганизаций();
			Форма.ПоказатьВыборИзМеню(ОбработкаВыбораОрганизации, СписокОрганизаций);
		Иначе
			ИНН = СтрЗаменить(НавигационнаяСсылка, ИдентификаторБаннераПредодобренныйКредитВСбербанке(), "");
			Гиперссылка = ПерсонализированныеПредложенияСервисовВызовСервера.ПредодобренныйКредитВСбербанкеГиперссылкаПоИНН(ИНН, Форма.ИмяФормы);
			ПерейтиПоНавигационнойСсылке(Гиперссылка);
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераНастройкаВидимостиСчетовУчета() Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыОткрытия = Новый Структура("АктивныйЭлемент", "ПоказыватьСчетаУчетаВДокументах");
		ОткрытьФорму("ОбщаяФорма.ПерсональныеНастройки", 
			Новый Структура("ПараметрыОткрытия", ПараметрыОткрытия));
			
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераИнтеграцияСоSmartway() Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеБаннера = Баннер.ДанныеБаннера;
		ОбщегоНазначенияБПКлиент.ОтправитьЗаявкуВSmartway(ДанныеБаннера.Организация);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераЗаявкаНаКредит()
		ИЛИ НавигационнаяСсылка = ИдентификаторБаннераЗаявкаНаЛизинг() Тогда
		
		СтандартнаяОбработка = Ложь;

		Это1СЛизинг = (НавигационнаяСсылка = ИдентификаторБаннераЗаявкаНаЛизинг());
		
		Если Это1СЛизинг Тогда
			СервисОбменаСБанками = ПредопределенноеЗначение("Перечисление.СервисыОбменаСБанками.ЗаявкиНаЛизинг");
			КлючеваяОперация = "СозданиеФормыЗаявкаНаЛизинг";
		Иначе
			СервисОбменаСБанками = ПредопределенноеЗначение("Перечисление.СервисыОбменаСБанками.ЗаявкиНаКредит");
			КлючеваяОперация = "СозданиеФормыЗаявкаНаКредит";
		КонецЕсли;
		
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
		
		ЗначенияЗаполнения = Новый Структура();
		ЗначенияЗаполнения.Вставить("СервисОбменаСБанками", СервисОбменаСБанками);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("Документ.ЗаявкаНаКредит.ФормаОбъекта", ПараметрыФормы);

	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераЗаявкаНаОткрытиеСчета() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		КлючеваяОперация = "СозданиеФормыЗаявкаНаОткрытиеСчета";
		
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
		
		ОткрытьФорму("Документ.ЗаявкаНаОткрытиеСчета.ФормаОбъекта");
			
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераФинОтчетность()
		ИЛИ НавигационнаяСсылка = ИдентификаторБаннераФинОтчетностьКраткий() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Баннер.ДанныеБаннера.Организация);
		ПараметрыФормы.Вставить("Банк",        Баннер.ДанныеБаннера.Банк);
		
		ОткрытьФорму("РегистрСведений.ЖурналСтатусовФинОтчетностиВБанки.ФормаСписка", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераПереходиНаНКО() Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.Реклама1СНКО");
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераНастройкаФункциональности() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		
		Если ЗначениеЗаполнено(Баннер.ДанныеБаннера.ФункциональнаяОпция) Тогда
			ФункциональнаяОпция = Баннер.ДанныеБаннера.ФункциональнаяОпция.Имя;
			Если ФункциональнаяОпция = "ВестиУчетРасчетовСКонтрагентами" Тогда
				ПараметрыФормы.Вставить("ТекущаяСтраница", "ГруппаРасчеты");
			ИначеЕсли ФункциональнаяОпция = "ВестиУчетПоДоговорам" Тогда
				ПараметрыФормы.Вставить("ТекущаяСтраница", "ГруппаУчетПоДоговорам");
			КонецЕсли;
		КонецЕсли;
		
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераМоиЗадачи() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ОткрытьМоиЗадачи", Истина);
		
		ОткрытьФорму("Обработка.ПанельАдминистрированияБСП.Форма.Органайзер", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераАвтоматическийВыпускПродукции() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Организация = Неопределено;
		Если ЗначениеЗаполнено(Баннер.ДанныеБаннера)
			И Баннер.ДанныеБаннера.Свойство("Организация") Тогда
			Организация = Баннер.ДанныеБаннера.Организация;
		КонецЕсли;
		
		ОткрытьФорму("РегистрСведений.УчетнаяПолитика.ФормаЗаписи", 
			ПерсонализированныеПредложенияСервисовВызовСервера.ПараметрыФормыУчетнойПолитики(Организация));
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераКадровыеДокументыЭТК() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("ОбщаяФорма.РегламентированнаяОтчетность",
			,
			,
			"1С-Отчетность");
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераЭТРН() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПерейтиПоНавигационнойСсылке("https://buh.ru/articles/documents/153137");


	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераКабинетСотрудника() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("Обработка.ИнтеграцияКабинетСотрудника.Форма.Интеграция",
			,
			,
			"1С:Кабинет сотрудника");
			
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		// Переходим в панель администрирования БИП.
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ФокусНаФлагеАвтозагрузкаПатчей", Истина);
		ОткрытьФорму("Обработка.ПанельАдминистрированияБСП.Форма.ИнтернетПоддержкаИСервисы", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераНастройкаЭлектронныхЧеков() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПерейтиПоНавигационнойСсылке("https://buh.ru/articles/documents/147371/");
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераЗагрузкаИзОФД() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("Обработка.ЗагрузкаИзОФД.Форма.НастройкаПодключения");
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераУстановитьДатуЗапрета() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если Баннер.ДанныеБаннера.ОткрытьНастройкиПользователейИПрав Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ФокусНаФлагеИспользоватьДатыЗапретаИзменения", Истина);
			ОткрытьФорму("Обработка.ПанельАдминистрированияБСП.Форма.НастройкиПользователейИПрав", ПараметрыФормы);
			
		ИначеЕсли Баннер.ДанныеБаннера.ОткрытьДатыЗапретаИзмененияДанных Тогда
			
			ДатыЗапретаИзмененияСлужебныйКлиент.ОткрытьДатыЗапретаИзмененияДанных(Неопределено);
			
		ИначеЕсли Баннер.ДанныеБаннера.ОткрытьСинхронизациюДанных Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ФокусНаФлагеИспользоватьДатыЗапрета", Истина);
			ОткрытьФорму("Обработка.ПанельАдминистрированияБСП.Форма.СинхронизацияДанных", ПараметрыФормы);
			
		Иначе
			
			ДатыЗапретаИзмененияСлужебныйКлиент.ОткрытьДатыЗапретаЗагрузкиДанных(Неопределено);
			
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = ГиперссылкаАнтикризис() Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Баннер.ДанныеБаннера.Организация);
		ПараметрыФормы.Вставить("ОткрытиеИзБаннераАнтикризис", Истина);
		ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ГиперссылкаНастройкиОрганизации() Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Ключ", Баннер.ДанныеБаннера.Организация);
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ГиперссылкаПомощникПереходаНаЕНС() Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Баннер.ДанныеБаннера.Организация);
		ОткрытьФорму("Обработка.ПомощникПереходаНаЕНС.Форма.Форма", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз() Тогда
		
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://buh.ru/articles/documents/154419");
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераНоваяАвторизацияЯндекс() Тогда
		
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://yandex.ru/dev/market/partner-api/doc/ru/concepts/authorization");
			
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераАУСН() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеСобытия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Баннер.ДанныеБаннера, "ДанныеСобытия", Новый Структура);
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСобытия, "Организация");
		Банк = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСобытия, "Банк");
		
		ИнтеграцияАУСНКлиент.ОткрытьПомощникПодключенияСервиса(Организация, Банк);
	
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераЛКАУСН() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеСобытия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Баннер.ДанныеБаннера, "ДанныеСобытия", Новый Структура);
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСобытия, "Организация");
		
		ИнтеграцияАУСНКлиент.ОткрытьПомощникПодключенияСервиса(Организация, Неопределено);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераУведомленияЕНС() Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.СписокЗадачУведомленияЕНС");
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("ОбщаяФорма.СписокЗадачНастройкаУведомленийПоЭлектроннойПочте");
		
	ИначеЕсли НавигационнаяСсылка =
		ПерсонализированныеПредложенияСервисовКлиентСервер.ИдентификаторБаннераБанкиАУСН() Тогда
		СтандартнаяОбработка = Ложь;
		СведенияОСервисе = ЗаявкиНаОткрытиеСчетаВызовСервера.СведенияОСервисе(
			ПредопределенноеЗначение("Перечисление.СервисыОбменаСБанками.ЗаявкиНаОткрытиеСчета"));
		Если СведенияОСервисе.ТребуетсяПодключениеИнтернетПоддержки Тогда
			БанкиИнтегрированныеССервисомАУСНКлиент.ПодключитьИнтернетПоддержкуПользователей();
		Иначе
			ОткрытьФорму("ОбщаяФорма.ПредложениеБанкиАУСН");
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераСервисСверкиРасчетов() Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.ИнтеграцияССервисомСверкиРасчетов");
		
	ИначеЕсли Лев(НавигационнаяСсылка,17) = "ОткрытьНастройку_" Тогда
		
		СтандартнаяОбработка = Ложь; 
		ОбменСМаркетплейсКлиент.ЛокальнаяСсылкаПерейти(НавигационнаяСсылка);
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераМобильныйКлиентБизнесСтарт() Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.УстановкаМобильногоКлиентаБизнесСтарт");
		
	ИначеЕсли НавигационнаяСсылка = ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов() Тогда
		
		СтандартнаяОбработка = Ложь;
		ОбменСМаркетплейсКлиент.ОткрытьНастройкиАвтозагрузкиОтчетовМаркетплейсов(Форма);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодключитьТестовыйПериодЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяФормы = СтрШаблон("Отчет.%1.ФормаОбъекта", ДополнительныеПараметры.ИмяОтчета);
	ОткрытьФорму(ИмяФормы);
	
КонецПроцедуры

// Определяет наличие действующих сертификатов удостоверяющих центров.
//
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - оповещение, которое будет выполнено по окончанию проверки.
//    *Результат - СторонниеСертификаты - Структура, в которой указан УЦ и дата окончания.
//  ИНН                   - Строка             - ИНН организации, для которой выполняется проверка.
//
Процедура ОпределитьНаличиеСертификатовЭлектроннойОтчетности(ОповещениеОЗавершении, ИНН) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИНН) Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Новый Структура);
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЭтоWindowsКлиент() И НЕ ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
		МенеджерКриптографии = Новый МенеджерКриптографии;
		Контекст = Новый Структура;
		Контекст.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
		Контекст.Вставить("ИНН", ИНН);
		Контекст.Вставить("МенеджерКриптографии", МенеджерКриптографии);
		Оповещение = Новый ОписаниеОповещения(
			"ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеИнициализации", ЭтотОбъект, Контекст);
		МенеджерКриптографии.НачатьИнициализацию(Оповещение, "Microsoft Enhanced Cryptographic Provider v1.0", "", 1);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Новый Структура);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьРежимОжиданияНаБаннере(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Элементы.ТекстБаннера.Видимость = Ложь;
	Элементы.КартинкаБаннера.Видимость = Ложь;
	Элементы.ГруппаБаннерОжидание.Видимость = Истина;
	
	Элементы.КартинкаЗакрытьБаннер.Доступность = Ложь;
	Элементы.КартинкаПредыдущийБаннер.Доступность = Ложь;
	Элементы.КартинкаСледующийБаннер.Доступность = Ложь;
	
КонецПроцедуры

Функция ИнтервалПереключенияБаннеров() Экспорт
	
	Возврат 60 * 60; // 1 час
	
КонецФункции

Процедура ПредодобренныйКредитВСбербанкеОбработкаВыбораОрганизации(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ВыбранныйЭлемент) <> Тип("ЭлементСпискаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	ИНН = ВыбранныйЭлемент.Значение;
	Гиперссылка = ПерсонализированныеПредложенияСервисовВызовСервера.ПредодобренныйКредитВСбербанкеГиперссылкаПоИНН(ИНН,
		ДополнительныеПараметры.ИмяФормы);
	ПерейтиПоНавигационнойСсылке(Гиперссылка);
	
КонецПроцедуры

// Процедура закрывает баннер на форме.
// Параметры:
//		Форма - ФормаКлиентскогоПриложения - Форма на которой нужно закрыть баннер.
//		Организация - СправочникСсылка.Организации - Организация, по которой ищем баннер.
//
Процедура ЗакрытьБаннер(Форма, Организация) Экспорт
	
	// Закроем баннер на форме.
	Баннер = Форма.Баннер;
	Форма.Баннер = Неопределено;
	Форма.Элементы.Баннер.Видимость = Ложь;
	
	ПерсонализированныеПредложенияСервисовВызовСервера.ОбработатьЗакрытиеБаннера(Баннер, Организация);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыгрузкаДвоичныхДанныхСертификатов

Процедура ВыгрузитьМассивСертификатов(ОповещениеОЗавершении, Сертификаты)
	
	Если НЕ ЗначениеЗаполнено(Сертификаты) Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Сертификаты);
		Возврат;
	КонецЕсли;
	
	Выгружаемые = Новый Соответствие;
	
	Выгружаемые.Вставить("Осталось", Сертификаты.Количество());
	Выгружаемые.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	
	Для Счетчик = 0 по Сертификаты.Количество()-1 Цикл
		Сертификат = Сертификаты[Счетчик];
		ОписаниеСертификата = Новый Структура("Выгружен, Ошибка", Ложь, Ложь);
		ОписаниеСертификата.Вставить("Данные", Неопределено);
		ОписаниеСертификата.Вставить("Отпечаток", Сертификат.Отпечаток);
		Выгружаемые.Вставить(Счетчик, ОписаниеСертификата);
	КонецЦикла;
	
	Для Счетчик = 0 по Сертификаты.Количество()-1 Цикл
		Сертификат = Сертификаты[Счетчик];
		ПараметрыОповещения = Новый Структура("Выгружаемые, Текущий", Выгружаемые, Счетчик);
		
		Оповещение = Новый ОписаниеОповещения(
			"ВыгрузитьМассивСертификатовПослеВыгрузкиОдиночного", ЭтотОбъект, ПараметрыОповещения,
			"ВыгрузитьМассивСертификатовОшибкаВыгрузкиОдиночного", ЭтотОбъект);
		
		Сертификат.НачатьВыгрузку(Оповещение);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыгрузитьМассивСертификатовОшибкаВыгрузкиОдиночного(ИнформацияОбОшибке, СтандартнаяОбработка, ВходящийКонтекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ВходящийКонтекст.Выгружаемые[ВходящийКонтекст.Текущий].Ошибка = Истина;	
	ВходящийКонтекст.Выгружаемые["Осталось"] = ВходящийКонтекст.Выгружаемые["Осталось"] - 1;
	
	Если ВходящийКонтекст.Выгружаемые["Осталось"] = 0 Тогда 
		ОповещениеОЗавершении = ВходящийКонтекст.Выгружаемые["ОповещениеОЗавершении"];
		ВходящийКонтекст.Выгружаемые.Удалить("Осталось");
		ВходящийКонтекст.Выгружаемые.Удалить("ОповещениеОЗавершении");
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, ВходящийКонтекст.Выгружаемые);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузитьМассивСертификатовПослеВыгрузкиОдиночного(ВыгруженныеДанные, ВходящийКонтекст) Экспорт
	
	Если ЗначениеЗаполнено(ВыгруженныеДанные) Тогда 
		ВходящийКонтекст.Выгружаемые[ВходящийКонтекст.Текущий].Выгружен = Истина;
		ВходящийКонтекст.Выгружаемые[ВходящийКонтекст.Текущий].Данные = ВыгруженныеДанные;		
	КонецЕсли;
	
	ВходящийКонтекст.Выгружаемые["Осталось"] = ВходящийКонтекст.Выгружаемые["Осталось"] - 1;
	
	Если ВходящийКонтекст.Выгружаемые["Осталось"] = 0 Тогда 
		ОповещениеОЗавершении = ВходящийКонтекст.Выгружаемые["ОповещениеОЗавершении"];
		ВходящийКонтекст.Выгружаемые.Удалить("Осталось");
		ВходящийКонтекст.Выгружаемые.Удалить("ОповещениеОЗавершении");
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, ВходящийКонтекст.Выгружаемые);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РазборДвоичныхДанныхASN1

Функция ПрочитатьСвойстваБлока(ЧтениеДанных, ВернутьУказательВНачало = Ложь)
	
	Свойства = Новый Структура;
	
	Буфер = ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных(1);
	ПроскроллитьНазад = 1;
	
	МаскаКлассБлока = 192;   // 1100 0000
	МаскаТег = 31;           // 0001 1111
	МаскаЗначащиеБиты = 127; // 0111 1111
	МаскаСтаршийБит = 128;   // 1000 0000
	
	Свойства.Вставить("Класс", ПобитовоеИ(Буфер[0], МаскаКлассБлока));
	Свойства.Вставить("Тег", ПобитовоеИ(Буфер[0], МаскаТег));
		
	// Прочитать размер блока
	Буфер = ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных(1);
	ПроскроллитьНазад = ПроскроллитьНазад + 1;
	
	// Используется потоковый способ кодирования, т.е. размер блока не указан (окончание блока 00 00)
	Если Буфер[0] = 128 Тогда
		РазмерБлока = -1;
	Иначе
		ДлинныйРазмер = Булево(ПобитовоеИ(Буфер[0], МаскаСтаршийБит));
		Если ДлинныйРазмер Тогда
			КоличествоБайтовПодРазмер = ПобитовоеИ(Буфер[0], МаскаЗначащиеБиты);
			Буфер = ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных(КоличествоБайтовПодРазмер);
			ПроскроллитьНазад = ПроскроллитьНазад + КоличествоБайтовПодРазмер;
			РазмерБлока = 0;
			Для Каждого Байт Из Буфер Цикл
				РазмерБлока = РазмерБлока * 256 + Байт;
			КонецЦикла;
		Иначе
			РазмерБлока = ПобитовоеИ(Буфер[0], МаскаЗначащиеБиты);	
		КонецЕсли;
	КонецЕсли;
	
	Свойства.Вставить("РазмерБлока", РазмерБлока);
	Свойства.Вставить("РазмерБлокаПолный", ПроскроллитьНазад + РазмерБлока);
	
	Если ВернутьУказательВНачало Тогда 
		ПотокДанных = ЧтениеДанных.ИсходныйПоток();
		ЧтениеДанных.Закрыть();
		ПотокДанных.Перейти(-ПроскроллитьНазад, ПозицияВПотоке.Текущая);
		ЧтениеДанных = Новый ЧтениеДанных(ПотокДанных);		
	КонецЕсли;
	
	Возврат Свойства;
	
КонецФункции

Процедура ПропуститьБлок(ЧтениеДанных, СколькоБайт)
	
	Если СколькоБайт > 0 Тогда 
		ЧтениеДанных.Пропустить(СколькоБайт);
	КонецЕсли;
	
КонецПроцедуры

Функция СертификатИмеетПоляКоторыеНевозможноРаспарсить(ДанныеСертификата)
	
	ЧтениеДанных = Новый ЧтениеДанных(ДанныеСертификата);
	ПрочитатьСвойстваБлока(ЧтениеДанных); // SEQUENCE
	ПрочитатьСвойстваБлока(ЧтениеДанных); // SEQUENCE
	Свойства = ПрочитатьСвойстваБлока(ЧтениеДанных, Истина);
	
	ВсегоИтераций = 0;
	// Перебираем все SEQUENCE третьего уровня
	// в которых есть вложенные даты типа UTCTime или GeneralizedTime
	Пока Не ЧтениеДанных.ЧтениеЗавершено Цикл
		
		ВсегоИтераций = ВсегоИтераций + 1;
		Если ВсегоИтераций > 4 Тогда //Странная структура, возможно поврежденный файл
			Прервать;
		КонецЕсли;
		
		Пока Свойства.Тег <> 16 Цикл 			
			ПропуститьБлок(ЧтениеДанных, Свойства.РазмерБлокаПолный);
			Свойства = ПрочитатьСвойстваБлока(ЧтениеДанных, Истина);
		КонецЦикла;	
		
		//Ищем SEQUENCE с вложенным тегом определенного типа
		Свойства = ПрочитатьСвойстваБлока(ЧтениеДанных);
		СвойстваВложенного = ПрочитатьСвойстваБлока(ЧтениеДанных, Истина);	
		Если СвойстваВложенного.Тег = 23 Тогда // UTCTime
			ЧтениеДанных.Закрыть();	
			Возврат Ложь;
		ИначеЕсли СвойстваВложенного.Тег = 24 Тогда // GeneralizedTime
			ЧтениеДанных.Закрыть();	
			Возврат Истина;
		КонецЕсли;	
		
		ПропуститьБлок(ЧтениеДанных, Свойства.РазмерБлока); //SEQUENCE	
		Свойства = ПрочитатьСвойстваБлока(ЧтениеДанных, Истина);
 	КонецЦикла;
	
	ЧтениеДанных.Закрыть();	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ОпределениеСертификатовЭлектроннойОтчетности

Процедура ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеИнициализации(Менеджер, ВходящийКонтекст) Экспорт
	
	Оповещение = Новый ОписаниеОповещения(
		"ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияХранилищаСертификатов", 
		ЭтотОбъект, ВходящийКонтекст);
		
	ВходящийКонтекст.МенеджерКриптографии.НачатьПолучениеХранилищаСертификатов(
		Оповещение, ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты,
		РасположениеХранилищаСертификатовКриптографии.ДанныеПользователяОС);
		
КонецПроцедуры

Процедура ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияХранилищаСертификатов(Хранилище, ВходящийКонтекст) Экспорт
	
	Оповещение = Новый ОписаниеОповещения(	
		"ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияСертификатовПередВыгрузкой", 
		ЭтотОбъект, ВходящийКонтекст);
		
	Хранилище.НачатьПолучениеВсех(Оповещение);
	
КонецПроцедуры

Процедура ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияСертификатовПередВыгрузкой(Сертификаты, ВходящийКонтекст) Экспорт
	
	ВходящийКонтекст.Вставить("Сертификаты", Сертификаты);
	
	Оповещение = Новый ОписаниеОповещения(
		"ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияСертификатовПослеВыгрузки", 
		ЭтотОбъект, ВходящийКонтекст);
		
	ВыгрузитьМассивСертификатов(Оповещение, Сертификаты);
	
КонецПроцедуры

Процедура ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияСертификатовПослеВыгрузки(Сертификаты, ВходящийКонтекст) Экспорт
	
	ВходящийКонтекст.Вставить("СертификатыСНеподдерживаемойДатой", Новый Массив);
	
	Для Каждого Сертификат Из Сертификаты Цикл 
		Если Сертификат.Значение.Выгружен Тогда 
			Неподдерживаемый = СертификатИмеетПоляКоторыеНевозможноРаспарсить(Сертификат.Значение.Данные);
			Если Неподдерживаемый Тогда 
				КопияОтпечатка = Сертификат.Значение.Отпечаток;
				ОтпечатокHex = ПолучитьHexСтрокуИзДвоичныхДанных(КопияОтпечатка);
				ВходящийКонтекст.СертификатыСНеподдерживаемойДатой.Добавить(ОтпечатокHex);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияСертификатов(ВходящийКонтекст.Сертификаты, ВходящийКонтекст);
	
КонецПроцедуры

Процедура ОпределитьНаличиеСертификатовЭлектроннойОтчетностиПослеПолученияСертификатов(Сертификаты, ВходящийКонтекст) Экспорт
	Перем СертификатыСНеподдерживаемойДатой;
	
	СторонниеСертификаты = Новый Структура;
	
	ИНН = ВходящийКонтекст.ИНН;
	ТекущаяДата = ТекущаяДата();
	ГраницаСрокаДействия = ДобавитьМесяц(ТекущаяДата, 4); // Истекает через 4 месяца
	
	// OID ИНН физического лица, до 01.09.2021 использовался также и для юридического лица.
	ПолеИНН_ИП = "OID1_2_643_3_131_1_1";
	// OID ИНН юридического лица c 01.09.2021.
	ПолеИНН_ЮЛ = "OID1_2_643_100_4";
	
	ИспользоватьПредпроверку = ВходящийКонтекст.Свойство("СертификатыСНеподдерживаемойДатой", СертификатыСНеподдерживаемойДатой);
	
	Для Счетчик = 0 по Сертификаты.Количество()-1 Цикл
		Сертификат = Сертификаты[Счетчик];
		Если ИспользоватьПредпроверку Тогда 
			КопияОтпечатка = Сертификат.Отпечаток;
			ОтпечатокHex = ПолучитьHexСтрокуИзДвоичныхДанных(КопияОтпечатка);
			Если СертификатыСНеподдерживаемойДатой.Найти(ОтпечатокHex) <> Неопределено Тогда 
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если НЕ (Сертификат.ДатаОкончания > ТекущаяДата И Сертификат.ДатаОкончания < ГраницаСрокаДействия) Тогда
			Продолжить;
		КонецЕсли;
		
		Субъект = Сертификат.Субъект;
		
		ЕстьПолеИНН_ИП = Субъект.Свойство(ПолеИНН_ИП);
		ЕстьПолеИНН_ЮЛ = Субъект.Свойство(ПолеИНН_ЮЛ);
		
		ИНН_ИП_Совпадает = ЕстьПолеИНН_ИП И СтрНайти(Субъект[ПолеИНН_ИП], ИНН);
		ИНН_ЮЛ_Совпадает = ЕстьПолеИНН_ЮЛ И СтрНайти(Субъект[ПолеИНН_ЮЛ], ИНН);
		
		Если НЕ ИНН_ИП_Совпадает И НЕ ИНН_ЮЛ_Совпадает Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ Сертификат.Издатель.Свойство("O") Тогда
			Продолжить;
		КонецЕсли;
		Слова = РазложитьНазваниеНаОтдельныеСлова(Сертификат.Издатель["O"]);
		
		Для Каждого Индекс Из ПолучитьИндексПоискаДляПроверкиУЦ() Цикл
			Если Слова.Найти(Индекс) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СторонниеСертификаты.Вставить(Индекс, Сертификат.ДатаОкончания);
		КонецЦикла;
	КонецЦикла;
	
	ВыполнитьОбработкуОповещения(ВходящийКонтекст.ОповещениеОЗавершении, СторонниеСертификаты);
	
КонецПроцедуры

Функция РазложитьНазваниеНаОтдельныеСлова(Знач Название)
	
	ЗаменяемыеСимволы = "'""()[].,`~!@#$%^&*-+=|\/:; ";
	Для Индекс = 1 По СтрДлина(ЗаменяемыеСимволы) Цикл
		Название = СтрЗаменить(Название, Сред(ЗаменяемыеСимволы, Индекс, 1), "_");
	КонецЦикла;
	
	Возврат СтрРазделить(НРег(Название), "_", Ложь);
	
КонецФункции

Функция ПолучитьИндексПоискаДляПроверкиУЦ()
	
	Индекс = Новый Массив;
	Индекс.Добавить("контур");
	Индекс.Добавить("тензор");
	
	Возврат Индекс;

КонецФункции

#КонецОбласти

#Область ИдентификаторыБаннеров

Функция ИдентификаторБаннераТестовыйТариф()
	
	Возврат "ТестовыйТариф";
	
КонецФункции

Функция ИдентификаторБаннераСтороннийСертификат()
	
	Возврат "СтороннийСертификат";
	
КонецФункции

Функция ИдентификаторБаннераЧисленностьСотрудников()
	
	Возврат "ЧисленностьСотрудников";
	
КонецФункции

Функция ИдентификаторБаннераОтчетнаяКампания()
	
	Возврат "ОтчетнаяКампания";
	
КонецФункции

Функция ИдентификаторБаннераНДСКВычетуКонтрагент()
	
	Возврат "НДСКВычетуКонтрагент";
	
КонецФункции

Функция ИдентификаторБаннераНДСКВычетуСПАРК()
	
	Возврат "НДСКВычетуСПАРК";
	
КонецФункции

Функция ИдентификаторБаннераКрупнейшийДебитор()
	
	Возврат "КрупнейшийДебиторКонтрагент";
	
КонецФункции

Функция ИдентификаторБаннераКрупнейшиеДебиторыСПАРК()
	
	Возврат "КрупнейшиеДебиторыСПАРК";
	
КонецФункции

Функция ИдентификаторБаннераОбщееКоличествоДокументов()
	
	Возврат "ОбщееКоличествоДокументов";
	
КонецФункции

Функция ИдентификаторБаннераКоличествоПоступлений()
	
	Возврат "КоличествоПоступлений";
	
КонецФункции

Функция ИдентификаторБаннераКоличествоРеализаций()
	
	Возврат "КоличествоРеализаций";
	
КонецФункции

Функция ИдентификаторБаннераКоличествоКонтрагентов()
	
	Возврат "КоличествоКонтрагентов";
	
КонецФункции

Функция ИдентификаторБаннераНадежностьБанков()
	
	Возврат "НадежностьБанков";
	
КонецФункции

Функция ИдентификаторБаннераПлатформаСамозанятые()

	Возврат "ПлатформаСамозанятые";

КонецФункции

Функция ИдентификаторБаннераПоддержкаСервиса()
	
	Возврат "ПоддержкаСервиса";
	
КонецФункции

Функция ИдентификаторБаннераПредодобренныйКредитВСбербанке()

	Возврат "ПредодобренныйКредитВСбербанке";

КонецФункции

Функция ИдентификаторБаннераНастройкаВидимостиСчетовУчета()

	Возврат "НастройкаВидимостиСчетовУчета";

КонецФункции

Функция ИдентификаторБаннераИнтеграцияСоSmartway()

	Возврат "ИнтеграцияСоSmartway";

КонецФункции

Функция ИдентификаторБаннераПереходиНаНКО()

	Возврат "ПереходиНаНКО";

КонецФункции

Функция ИдентификаторБаннераЗаявкаНаОткрытиеСчета()

	Возврат "ЗаявкаНаОткрытиеСчета";

КонецФункции

Функция ИдентификаторБаннераЗаявкаНаКредит()

	Возврат "ЗаявкаНаКредит";

КонецФункции

Функция ИдентификаторБаннераЗаявкаНаЛизинг()

	Возврат "ЗаявкаНаЛизинг";

КонецФункции

Функция ИдентификаторБаннераФинОтчетность()

	Возврат "ФинОтчетность";

КонецФункции

Функция ИдентификаторБаннераФинОтчетностьКраткий()

	Возврат "ФинОтчетностьКраткий";

КонецФункции

Функция ИдентификаторБаннераНастройкаФункциональности()

	Возврат "НастройкаФункциональности";

КонецФункции

Функция ИдентификаторБаннераМоиЗадачи()

	Возврат "МоиЗадачи";

КонецФункции

Функция ИдентификаторБаннераАвтоматическийВыпускПродукции()

	Возврат "АвтоматическийВыпускПродукции";

КонецФункции

Функция ИдентификаторБаннераКадровыеДокументыЭТК()

	Возврат "КадровыеДокументыЭТК";

КонецФункции

Функция ИдентификаторБаннераКабинетСотрудника()

	Возврат "КабинетСотрудника";

КонецФункции

Функция ИдентификаторБаннераАвтоматическаяЗагрузкаПатчей()

	Возврат "АвтоматическаяЗагрузкаПатчей";

КонецФункции

Функция ИдентификаторБаннераУстановитьДатуЗапрета()

	Возврат "УстановитьДатуЗапрета";

КонецФункции

Функция ИдентификаторБаннераНастройкаЭлектронныхЧеков()
	
	Возврат "НастройкаЭлектронныхЧеков";
	
КонецФункции

Функция ИдентификаторБаннераЭТрН()
	
	Возврат "ЭТрН";
	
КонецФункции

Функция ИдентификаторБаннераУведомлениеОВыкупеВайлдберриз()
	
	Возврат "УведомлениеОВыкупеВайлдберриз";
	
КонецФункции

Функция ИдентификаторБаннераАУСН()

	Возврат ИнтеграцияАУСНКлиентСервер.ИдентификаторСервиса();

КонецФункции

Функция ИдентификаторБаннераЛКАУСН()

	Возврат ИнтеграцияАУСНКлиентСервер.ИдентификаторСервисаЛКАУСН();

КонецФункции

Функция ИдентификаторБаннераУведомленияОЗадачахПоЭлектроннойПочте()
	
	Возврат "УведомленияОЗадачахПоЭлектроннойПочте";
	
КонецФункции

Функция ИдентификаторБаннераУведомленияЕНС()

	Возврат "УведомленияЕНС";

КонецФункции

Функция ИдентификаторБаннераЗагрузкаИзОФД()
	
	Возврат "ЗагрузкаИзОФД";
	
КонецФункции

Функция ИдентификаторБаннераСервисСверкиРасчетов()
	
	Возврат "СервисСверкиРасчетов";
	
КонецФункции

Функция ИдентификаторБаннераНоваяАвторизацияЯндекс()
	
	Возврат "НоваяАвторизацияЯндекс";
	
КонецФункции

Функция ИдентификаторБаннераМобильныйКлиентБизнесСтарт()
	
	Возврат "МобильныйКлиентБизнесСтарт";
	
КонецФункции

Функция ИдентификаторБаннераАвтозагрузкаОтчетовМаркетплейсов()
	
	Возврат "АвтозагрузкаОтчетовМаркетплейсов";
	
КонецФункции

#КонецОбласти

#Область ПодключениеКЭДО

Процедура ОткрытьПомощникПодключенияЭДОПриЗакрытииРекламы(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("ОткрытьФормуПомощникаПодключенияКЭДО")
		И Результат.ОткрытьФормуПомощникаПодключенияКЭДО = Истина Тогда
		
		ОбменСКонтрагентамиКлиент.ПомощникПодключенияКСервису1СЭДО();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Функция ГиперссылкаАнтикризис()
	
	Возврат "АнтикризисПродлениеСроков";
	
КонецФункции

Функция ГиперссылкаНастройкиОрганизации()
	
	Возврат "АнтикризисНастройкиОрганизации";
	
КонецФункции

Функция ГиперссылкаПомощникПереходаНаЕНС()
	
	Возврат "ПомощникПереходаНаЕНС";
	
КонецФункции

#КонецОбласти