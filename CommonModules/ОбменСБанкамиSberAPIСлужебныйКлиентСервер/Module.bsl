
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработкаОшибок

// Возвращает текст расшифровки кода состояния HTTP
//
// Параметры:
//  КодСостояния - Число - код состояния HTTP
// 
// Возвращаемое значение:
//  Строка - подробное описание кода.
//
Функция РасшифровкаКодаСостоянияHTTP(КодСостояния) Экспорт
	
	Если КодСостояния = 200 Тогда // OK
		Расшифровка = НСтр("ru = 'Успешно.'");
	ИначеЕсли КодСостояния = 201 Тогда // Created
		Расшифровка = НСтр("ru = 'Создано.'");
	ИначеЕсли КодСостояния = 202 Тогда // Accepted
		Расшифровка = НСтр("ru = 'Принято.'");
	ИначеЕсли КодСостояния = 304 Тогда // Not Modified
		Расшифровка = НСтр("ru = 'Нет необходимости повторно передавать запрошенные ресурсы.'");
	ИначеЕсли КодСостояния = 400 Тогда // Bad Request
		Расшифровка = НСтр("ru = 'Запрос не может быть исполнен.'");
	ИначеЕсли КодСостояния = 401 Тогда // Unauthorized
		Расшифровка = НСтр("ru = 'Попытка авторизации на сервере была отклонена.'");
	ИначеЕсли КодСостояния = 402 Тогда // Payment Required
		Расшифровка = НСтр("ru = 'Требуется оплата.'");
	ИначеЕсли КодСостояния = 403 Тогда // Forbidden
		Расшифровка = НСтр("ru = 'К запрашиваемому ресурсу нет доступа.'");
	ИначеЕсли КодСостояния = 404 Тогда // Not Found
		Расшифровка = НСтр("ru = 'Запрашиваемый ресурс не найден на сервере.'");
	ИначеЕсли КодСостояния = 405 Тогда // Method Not Allowed
		Расшифровка = НСтр("ru = 'Метод запроса не поддерживается сервером.'");
	ИначеЕсли КодСостояния = 406 Тогда // Not Acceptable
		Расшифровка = НСтр("ru = 'Запрошенный формат данных не поддерживается сервером.'");
	ИначеЕсли КодСостояния = 407 Тогда // Proxy Authentication Required
		Расшифровка = НСтр("ru = 'Ошибка аутентификации на прокси-сервере'");
	ИначеЕсли КодСостояния = 408 Тогда // Request Timeout
		Расшифровка = НСтр("ru = 'Время ожидания сервером передачи от клиента истекло.'");
	ИначеЕсли КодСостояния = 409 Тогда // Conflict
		Расшифровка = НСтр("ru = 'Запрос не может быть выполнен из-за конфликтного обращения к ресурсу.'");
	ИначеЕсли КодСостояния = 410 Тогда // Gone
		Расшифровка = НСтр("ru = 'Ресурс на сервере был перемешен.'");
	ИначеЕсли КодСостояния = 411 Тогда // Length Required
		Расшифровка = НСтр("ru = 'Сервер требует указание ""Content-length."" в заголовке запроса.'");
	ИначеЕсли КодСостояния = 412 Тогда // Precondition Failed
		Расшифровка = НСтр("ru = 'Запрос не применим к ресурсу'");
	ИначеЕсли КодСостояния = 413 Тогда // Request Entity Too Large
		Расшифровка = НСтр("ru = 'Сервер отказывается обработать, слишком большой объем передаваемых данных.'");
	ИначеЕсли КодСостояния = 414 Тогда // Request-URL Too Long
		Расшифровка = НСтр("ru = 'Сервер отказывается обработать, слишком длинный URL.'");
	ИначеЕсли КодСостояния = 415 Тогда // Unsupported Media-Type
		Расшифровка = НСтр("ru = 'Сервер заметил, что часть запроса была сделана в неподдерживаемом формат'");
	ИначеЕсли КодСостояния = 416 Тогда // Requested Range Not Satisfiable
		Расшифровка = НСтр("ru = 'Часть запрашиваемого ресурса не может быть предоставлена'");
	ИначеЕсли КодСостояния = 417 Тогда // Expectation Failed
		Расшифровка = НСтр("ru = 'Сервер не может предоставить ответ на указанный запрос.'");
	ИначеЕсли КодСостояния = 429 Тогда // Too Many Requests
		Расшифровка = НСтр("ru = 'Слишком много запросов за короткое время.'");
	ИначеЕсли КодСостояния = 500 Тогда // Internal Server Error
		Расшифровка = НСтр("ru = 'Внутренняя ошибка сервера.'");
	ИначеЕсли КодСостояния = 501 Тогда // Not Implemented
		Расшифровка = НСтр("ru = 'Сервер не поддерживает метод запроса.'");
	ИначеЕсли КодСостояния = 502 Тогда // Bad Gateway
		Расшифровка = НСтр("ru = 'Сервер, выступая в роли шлюза или прокси-сервера,
			|получил недействительное ответное сообщение от вышестоящего сервера.'");
	ИначеЕсли КодСостояния = 503 Тогда // Server Unavailable
		Расшифровка = НСтр("ru = 'Сервер временно не доступен.'");
	ИначеЕсли КодСостояния = 504 Тогда // Gateway Timeout
		Расшифровка = НСтр("ru = 'Сервер в роли шлюза или прокси-сервера
			|не дождался ответа от вышестоящего сервера для завершения текущего запроса.'");
	ИначеЕсли КодСостояния = 505 Тогда // HTTP Version Not Supported
		Расшифровка = НСтр("ru = 'Сервер не поддерживает указанную в запросе версию протокола HTTP'");
	ИначеЕсли КодСостояния = 506 Тогда // Variant Also Negotiates
		Расшифровка = НСтр("ru = 'Сервер настроен некорректно, и не способен обработать запрос.'");
	ИначеЕсли КодСостояния = 507 Тогда // Insufficient Storage
		Расшифровка = НСтр("ru = 'На сервере недостаточно места для выполнения запроса.'");
	ИначеЕсли КодСостояния = 509 Тогда // Bandwidth Limit Exceeded
		Расшифровка = НСтр("ru = 'Сервер превысил отведенное ограничение на потребление трафика.'");
	ИначеЕсли КодСостояния = 510 Тогда // Not Extended
		Расшифровка = НСтр("ru = 'Сервер требует больше информации о совершаемом запросе.'");
	ИначеЕсли КодСостояния = 511 Тогда // Network Authentication Required
		Расшифровка = НСтр("ru = 'Требуется авторизация на сервере.'");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '[%1] %2'"),
		КодСостояния,
		Расшифровка);
	
КонецФункции

Функция КодНекорректногоТокена() Экспорт
	
	Возврат 401;
	
КонецФункции

Функция КодЗапрашиваемыйРесурсНеНайденНаСервере() Экспорт
	
	Возврат 404;
	
КонецФункции

Функция КодВнутренняяОшибкаСервера() Экспорт
	
	Возврат 500;
	
КонецФункции

// Возвращает информацию об ошибке по коду.
//
// Параметры:
//  КодСостояния - Число - код состояния, полученный от сервера банка.
//  Причина - Строка - причина ошибки, полученный от сервера банка.
//
// Возвращаемое значение:
//  Структура - информация об ошибке, содержит поля:
//   * Код - Строка - цифровой код ошибки УПШ
//   * Описание - Строка - подробное описание ошибки.
//
Функция ТекстОшибки(КодСостояния, Причина = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Причина) Тогда
		ОшибкаHTTP = РасшифровкаКодаСостоянияHTTP(КодСостояния);
		Если ЗначениеЗаполнено(ОшибкаHTTP) Тогда
			Возврат Новый Структура("Код, Описание", "", ОшибкаHTTP);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	СписокКодовSberAPI = Новый Соответствие;
	
	СписокКодовSberAPI.Вставить("400_INCORRECT_SMS_SETTINGS", "Fg==");
	
	СписокКодовSberAPI.Вставить("401_NEED_TOKEN_TO_ACCESS", "GA==");
	СписокКодовSberAPI.Вставить("401_BAD_SMS_AUTHENTICATION", "FA==");
	СписокКодовSberAPI.Вставить("401_CERTIFICATE_EXPIRED", "Aw==");
	
	СписокКодовSberAPI.Вставить("401_BAD_CREDENTIALS", "AQ==");
	СписокКодовSberAPI.Вставить("403_BAD_CREDENTIALS", "AQ==");
	
	СписокКодовSberAPI.Вставить("401_MUST_CHANGE_PASSWORD", "Ag==");
	СписокКодовSberAPI.Вставить("403_MUST_CHANGE_PASSWORD", "Ag==");
	
	СписокКодовSberAPI.Вставить("403_ACCOUNT_DISABLED", "Cw==");
	СписокКодовSberAPI.Вставить("403_IP_CHANGED", "Bg==");
	СписокКодовSberAPI.Вставить("403_ACCESS_POINT_NOT_AVAILABLE", "DA==");
	СписокКодовSberAPI.Вставить("403_FRAUDMON_DENY", "BQ==");
	СписокКодовSberAPI.Вставить("403_ORG_LOCKED", "BA==");
	
	СписокКодовSberAPI.Вставить("404_SESSION_NOT_FOUND", "Fw==");
	СписокКодовSberAPI.Вставить("404_CREDENTIALS_NOT_FOUND_BY_CERTIFICATE", "Eg==");
	
	СписокКодовSberAPI.Вставить("429_TOO_FREQUENT_LOGIN_FAILS", "Cg==");
	
	СписокКодовSberAPI.Вставить("500_UNKNOWN_EXCEPTION", "CQ==");
	
	КлючОшибки = СтрШаблон("%1_%2", Формат(КодСостояния, "ЧГ="), ВРег(Причина));
	СписокКодовСбербанк = ОбменСБанкамиСлужебныйКлиентСервер.СписокКодовСбербанк();
	КодОшибки = СписокКодовSberAPI.Получить(КлючОшибки);
	
	Если КодОшибки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекущаяОшибка = СписокКодовСбербанк.Получить(КодОшибки);
	
	Возврат ТекущаяОшибка;
	
КонецФункции

Функция КороткоеПредставлениеОшибки(Знач ТекстОшибки, Знач ТекстОшибкиПоУмолчанию = "") Экспорт
	
	ТекстСообщения = "";
	
	// Детальное описание ошибки получаем только для определенных кодов сервера.
	ОбрабатываемыКоды = ОбрабатываемыКоды();
	
	Если СтрНайти(НРег(ТекстОшибки), "<html>") > 0 Тогда
		Если ЗначениеЗаполнено(ТекстОшибкиПоУмолчанию) Тогда
			ТекстСообщения = ТекстОшибкиПоУмолчанию;
		Иначе
			ТекстСообщения = ТекстОшибки;
		КонецЕсли;
	Иначе
		ДанныеОшибки = ДанныеОшибки(ТекстОшибки);
		ТекстСообщения = ДанныеОшибки.ТекстСообщения;
	КонецЕсли;
	
	ИскомыйТокен = НРег("Access Token");
	ПозицияТокена = СтрНайти(НРег(ТекстСообщения), ИскомыйТокен);
	Если ПозицияТокена > 0 Тогда
		// Обрезаем значение токена перед выводом на экран.
		ТекстСообщения = Лев(ТекстСообщения, ПозицияТокена - 1);
	КонецЕсли;
	
	Возврат ТекстСообщения;
	
КонецФункции

Функция ДетальноеСообщениеИзТекстаОшибки(Знач ТекстОшибки, КодСостояния) Экспорт
	
	ТекстСообщения = "";
	
	// Детальное описание ошибки получаем только для определенных кодов сервера.
	ОбрабатываемыКоды = ОбрабатываемыКоды();
	
	Если Не ЗначениеЗаполнено(ОбрабатываемыКоды[КодСостояния]) Тогда
		Возврат ТекстОшибки;
	КонецЕсли;
	
	ТекстОшибки = СокрЛП(ТекстОшибки);
	
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Возврат ТекстСообщения;
	ИначеЕсли СтрНайти(НРег(ТекстОшибки), "<html>") > 0 Тогда
		ТекстСообщения = ТекстОшибки;
	Иначе
		ДанныеОшибки = ДанныеОшибки(ТекстОшибки, КодСостояния);
		
		СодержимоеОшибки = Новый Массив;
		Если ЗначениеЗаполнено(ДанныеОшибки.ТекстСообщения) Тогда
			ТекстКодаСостояния = СтрШаблон(НСтр("ru = 'Код: %1'"), КодСостояния);
			СодержимоеОшибки.Добавить(ТекстКодаСостояния);
			
			Если ЗначениеЗаполнено(ДанныеОшибки.ТэгОшибки) Тогда
				ТэгОшибки = СтрШаблон(НСтр("ru = 'Причина: %1'"), ДанныеОшибки.ТэгОшибки);
				СодержимоеОшибки.Добавить(ТэгОшибки);
			КонецЕсли;
			
			// Переопределим сообщение типа "Счёт с номером 40702810299996274800 не найден", но более понятное пользователю
			// .т.к. смысл данного сообщения в том, что у данного счета просто нет прав на эту операцию
			// При поиске не используем HСтр(), т.к. ищем текст в сообщениях, полученных из банка
			ПерваяФраза = "Счёт с номером";
			НачалоПервойФразы = СтрНайти(ДанныеОшибки.ТекстСообщения, ПерваяФраза);
			ВтораяФраза = "не найден";
			НачалоВторойФразы = СтрНайти(ДанныеОшибки.ТекстСообщения, ВтораяФраза);
			Если НачалоПервойФразы <> 0
				И НачалоВторойФразы <> 0 Тогда
				
				// Вырежем номер счета
				ТекстНомерСчета = СокрЛП(Сред(
					ДанныеОшибки.ТекстСообщения,
					НачалоПервойФразы + СтрДлина(ПерваяФраза) + 1,
					НачалоВторойФразы - НачалоПервойФразы - СтрДлина(ПерваяФраза) - 1));
				// Составим новую фразу
				Шаблон = НСтр("ru = 'Для счета %1 данная операция запрещена. Обратитесь в банк'");
				ДанныеОшибки.ТекстСообщения = СтрШаблон(Шаблон, ТекстНомерСчета);
				
			КонецЕсли;
			
			СодержимоеОшибки.Добавить(ДанныеОшибки.ТекстСообщения);
			
			Если ЗначениеЗаполнено(ДанныеОшибки.КодОшибки) Тогда
				ТекстКодаОшибки = СтрШаблон(НСтр("ru = '(internalErrorCode: %1)'"), ДанныеОшибки.КодОшибки);
				СодержимоеОшибки.Добавить(ТекстКодаОшибки);
			КонецЕсли;
		КонецЕсли;
		
		ТекстСообщения = СтрСоединить(СодержимоеОшибки, ". ");
	КонецЕсли;
	
	Возврат ТекстСообщения;
	
КонецФункции

Функция СформироватьДетальныйТекстСообщения(ТекстСообщения, Знач СообщениеОбОшибке) Экспорт
	
	Результат = СообщениеОбОшибке;
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		Результат = Результат + ОбменСБанкамиСлужебныйКлиентСервер.ТекстСлужебногоМаркераВЖР();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НовыйЭлементКоллекцииОшибка() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТипОшибки", "");
	Результат.Вставить("КраткоеПредставлениеОшибки", "");
	Результат.Вставить("НастройкаОбмена", ПредопределенноеЗначение("Справочник.НастройкиОбменСБанками.ПустаяСсылка"));
	Результат.Вставить("Банк");
	Результат.Вставить("ПрограммаБанка", ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ПустаяСсылка"));
	Результат.Вставить("АдресСервера", "");
	
	Возврат Результат;
	
КонецФункции

Функция ДополнитьЭлементКоллекцииОшибка(ЭлементКоллекцииОшибка, Источник, НастройкаОбмена) Экспорт
	
	Если Не ЗначениеЗаполнено(НастройкаОбмена) И ТипЗнч(Источник) = Тип("Структура") Тогда
		
		ЭлементКоллекцииОшибка.Банк = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Источник, "Банк", ЭлементКоллекцииОшибка.Банк);
		
		ЭлементКоллекцииОшибка.ПрограммаБанка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Источник, "ПрограммаБанка", ЭлементКоллекцииОшибка.ПрограммаБанка);
		
		ЭлементКоллекцииОшибка.АдресСервера = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Источник, "АдресСервера", ЭлементКоллекцииОшибка.АдресСервера);
		
	КонецЕсли;
	
КонецФункции

// Обрабатывает исключительные ситуации. Записывает текст ошибки в журнал регистрации и выводит сообщение пользователю,
// если заполнен параметр ТекстОшибкиДляПользователя.
//
// Параметры:
//  ВидОперации          - Строка - вид операции при которой возникло исключение.
//  ПодробныйТекстОшибки - Строка
//  ТекстСообщения       - Строка
//  СсылкаНаОбъект       - ДокументСсылка, СправочникСсылка - объект с которым связано данное событие.
//
Процедура ОбработатьОшибку(ВидОперации,
						   Знач ПодробныйТекстОшибки,
						   Знач ТекстСообщения = "",
						   СсылкаНаОбъект = Неопределено) Экспорт
	
	ПодробныйТекстОшибки = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ПодробныйТекстОшибки);
	ТекстСообщения = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ТекстСообщения);
	
	Если ДеталиОшибкиВыведены(ТекстСообщения) Тогда
		ТекстСообщения = "";
	КонецЕсли;
	
	ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации,
		ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробныйТекстОшибки, ТекстСообщения, СсылкаНаОбъект);
	
	Если ЗначениеЗаполнено(ТекстСообщения)
		И ПодробныйТекстОшибки <> ТекстСообщения
		И СтрНайти(ТекстСообщения, ОбменСБанкамиСлужебныйКлиентСервер.ТекстСлужебногоМаркераВЖР()) = 0 Тогда
		ТекстСообщения = СформироватьДетальныйТекстСообщения(ПодробныйТекстОшибки, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеОшибки(ТекстОшибки, КодСостояния = 0) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодСостояния", КодСостояния);
	Результат.Вставить("ТекстСообщения", "");
	Результат.Вставить("ТэгОшибки", "");
	Результат.Вставить("КодОшибки", "");
	
	ТекстОшибки = СокрЛП(ТекстОшибки);
	
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Результат.ТекстСообщения = "";
	ИначеЕсли СтрНайти(НРег(ТекстОшибки), "<html>") > 0 Тогда
		Результат.ТекстСообщения = ТекстОшибки;
	Иначе
		ОтветБанка = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ДанныеИзJSON(ТекстОшибки, , Ложь);
		ДанныеСообщения = ВыделитьДанныеТекстаИзСообщения(ОтветБанка);
		Результат.ТекстСообщения = СтрСоединить(ДанныеСообщения, ". ");
		
		Если ОтветБанка.Свойство("internalErrorCode") И ЗначениеЗаполнено(ОтветБанка.internalErrorCode) Тогда
			Результат.КодОшибки = ОтветБанка.internalErrorCode;
		КонецЕсли;
		
		Если ОтветБанка.Свойство("cause") И ЗначениеЗаполнено(ОтветБанка.cause) Тогда
			Результат.ТэгОшибки = ВРег(ОтветБанка.cause);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Функция проверяет наличие признаков успешного выполнения в переменной Результат
//
// Параметры:
//  Результат - Булево - признак успешного выполнения
//            - Структура - обычно содержит несколько полей, среди них должно быть
//              * Успех - Булево - признак успешного выполнения
//  ВидПроверки - Строка - определяет по какому правилу проивести проверку
//
// Возвращаемое значение:
//  Булево - Истина - если найден признак успешного выполнения, иначе - Ложь.
//
Функция ВыполненоУспешно(Результат) Экспорт
	
	Успех = Ложь;
	Статус = Ложь;
	Если ТипЗнч(Результат) = Тип("Булево") И Результат = Истина Тогда
		Возврат Истина;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("Успех", Успех) И Успех = Истина Тогда
		Возврат Истина;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("Статус", Статус)
		И (Статус = Истина Или Статус = "Выполнено") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция АдресТокена() Экспорт
	
	Возврат "http://localhost:28016";
	
КонецФункции

// В этой функции можно переопределить каким методом нужно выполнить HTTP запрос с Клиента.
//
// Параметры:
//  ТекущийКлиент - Строка - вид текущего клиента, возможны два варианта "ВебКлиент" и "ТонкийКлиент"
//  ВидЗапроса - Строка - вид запроса, который планируется выполнить; параметр нужен если в переопределении нужно
//   разделить МетодВыполненияЗапросов в зависимости от ВидЗапроса
//
// Возвращаемое значение:
//  Строка, Неопределено - допускается три варианта возвращаемого значения: 
//   "Платформа1С" или "HttpBridge" или Неопределено
Функция СпособВыполненияHTTPЗапросовНаКлиенте(ТекущийКлиент, ВидЗапроса) Экспорт
	
	Если ТекущийКлиент = "ВебКлиент" Тогда
		МетодВыполненияЗапросов = "HttpBridge";
	КонецЕсли;
	
	Если ТекущийКлиент = "ТонкийКлиент" Тогда
		МетодВыполненияЗапросов = "Платформа1С";
	КонецЕсли;
	
	Возврат МетодВыполненияЗапросов;
	
КонецФункции

// Создает структуру описание параметров для отправки HTTPЗапроса
//
// Возвращаемое значение:
//  Структура - Описание:
// * ТипЗапроса - Строка - необходимый тип запроса, опреджеляет какой тип запроса надо выполнить,
//    клиентский или серверный, возможные варианты: "Клиент", "Сервер"
// * ВидЗапроса - Строка - необходимый вид запроса, возможные варианты: "POST", "GET"
// * АдресСервера - Строка - адрес сервера, которому необходимо передать запрос,
//    например: "https://iftfintech.testsbi.sberbank.ru:9443"
// * АдресРесурса - Строка - путь до необходимого метода на сервере, которому необходимо передать запрос,
//    например: "fintech/api/auth/v1/login"
// * Заголовки - Соответствие - заголовки, которые необходимо передать в запросе
// * Данные - ДвоичныеДанные,Строка - данные, которые необходимо передать в запросе
// * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками, Неопределено - 
// * СертификатПодключения - СертификатКлиентаФайл, Неопределено - транспортный TLS сертификат
//
Функция ПараметрыОтправкиHTTPЗапроса() Экспорт
	
	ПараметрыHTTPЗапроса = Новый Структура;
	ПараметрыHTTPЗапроса.Вставить("ТипЗапроса", "");
	ПараметрыHTTPЗапроса.Вставить("ВидЗапроса", "");
	ПараметрыHTTPЗапроса.Вставить("АдресРесурса", "");
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("User-Agent", "1C Enterprise");
	ПараметрыHTTPЗапроса.Вставить("Заголовки", Заголовки);
	ПараметрыHTTPЗапроса.Вставить("Данные", Неопределено);
	ПараметрыHTTPЗапроса.Вставить("НастройкаОбмена", Неопределено);
	ПараметрыHTTPЗапроса.Вставить("СертификатПодключения", Неопределено);
	ПараметрыHTTPЗапроса.Вставить("ТокенДоступа", Неопределено);
	ПараметрыHTTPЗапроса.Вставить("ФайлСертификата", Неопределено);
	ПараметрыHTTPЗапроса.Вставить("СертификатПодключенияПароль", Неопределено);
	
	Возврат ПараметрыHTTPЗапроса;
	
КонецФункции

Функция ДвоичныеДанныеВТелеHTTPОтвета(Знач Заголовки) Экспорт
	
	ДвоичныеДанныеВТеле = Ложь;
	Для Каждого ЭлементКоллекции Из Заголовки Цикл
		Если ЭлементКоллекции.Ключ = "Content-Type"
				И (ЭлементКоллекции.Значение = "application/zip"
				Или ЭлементКоллекции.Значение = "application/gzip") Тогда
			ДвоичныеДанныеВТеле = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДвоичныеДанныеВТеле;
	
КонецФункции

Функция ТипОтправкиHTTPЗапросаКлиент() Экспорт
	
	Возврат "Клиент";
	
КонецФункции

Функция ТипОтправкиHTTPЗапросаСервер() Экспорт
	
	Возврат "Сервер";
	
КонецФункции

Процедура ОпределитьПараметрыСайтаКлиентскийМетод(Знач АдресСайта, Адрес, Протокол, Порт = 80, ИсключитьПорт = Ложь) Экспорт
	
	Порт = 80;
	АдресСайта = СокрЛП(АдресСайта);
	
	АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
	АдресСайта = СтрЗаменить(АдресСайта, " ", "");
	
	Если НРег(Лев(АдресСайта, 7)) = "http://" Тогда
		Протокол = "http";
		Адрес = Сред(АдресСайта,8);
	ИначеЕсли НРег(Лев(АдресСайта, 8)) = "https://" Тогда
		Протокол =  "https";
		Адрес = Сред(АдресСайта,9);
	КонецЕсли;
	ПозицияДвоеточие = СтрНайти(Адрес, ":");
	Если ПозицияДвоеточие <> 0 Тогда
		Порт = Число(Сред(Адрес, ПозицияДвоеточие+1));
		Если ИсключитьПорт Тогда
			Адрес = Сред(Адрес, 1, ПозицияДвоеточие-1);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаОшибок

Функция ОбрабатываемыКоды()
	
	Результат = Новый Соответствие;
	
	Код = 400;
	Результат.Вставить(Код, РасшифровкаКодаСостоянияHTTP(Код));
	
	// Обработка некорректного (не просроченного!) токена.
	Код = КодНекорректногоТокена(); // 401
	Результат.Вставить(Код, НСтр("ru = 'Указан некорректный access_token.'"));
	
	Код = 403;
	Результат.Вставить(Код, РасшифровкаКодаСостоянияHTTP(Код));
	
	Код = КодЗапрашиваемыйРесурсНеНайденНаСервере(); // 404
	Результат.Вставить(Код, РасшифровкаКодаСостоянияHTTP(Код));
	
	Код = 429;
	Результат.Вставить(Код, РасшифровкаКодаСостоянияHTTP(Код));
	
	Код = КодВнутренняяОшибкаСервера(); // 500
	Результат.Вставить(Код, РасшифровкаКодаСостоянияHTTP(Код));
	
	Возврат Результат;
	
КонецФункции

Функция ДеталиОшибкиВыведены(ПодробноеПредставлениеОшибки)
	
	Результат = СтрЗаканчиваетсяНа(ПодробноеПредставлениеОшибки,
		ОбменСБанкамиСлужебныйКлиентСервер.ТекстСлужебногоМаркераВЖР());
	
	Возврат Результат;
	
КонецФункции

Процедура ПоложитьДетальноеСообщениеВТекстОтвета(ДанныеСообщения, Данные)
	
	ТекстПоля = "";
	Если Данные.Свойство("fields") И ТипЗнч(Данные.fields) = Тип("Массив") Тогда
		Поля = Новый Массив;
		Для Каждого Поле Из Данные.fields Цикл
			Поля.Добавить(СокрЛП(Поле));
		КонецЦикла;
		
		Если Поля.Количество() > 0 Тогда
			ТекстПоля = СтрСоединить(Поля, " ,") + ":";
		КонецЕсли;
	КонецЕсли;
	
	Если Данные.Свойство("message") И ЗначениеЗаполнено(Данные.message) Тогда
		ДанныеСообщения.Добавить(ТекстПоля + СокрЛП(Данные.message));
	КонецЕсли;
	
КонецПроцедуры

Функция ВыделитьДанныеТекстаИзСообщения(ОтветБанка)
	
	ДанныеСообщения = Новый Массив;
	
	ПоложитьДетальноеСообщениеВТекстОтвета(ДанныеСообщения, ОтветБанка);
	
	Если ОтветБанка.Свойство("checks") И ТипЗнч(ОтветБанка.checks) = Тип("Массив") Тогда
		Для Каждого Детали Из ОтветБанка.checks Цикл
			ПоложитьДетальноеСообщениеВТекстОтвета(ДанныеСообщения, Детали);
		КонецЦикла;
	КонецЕсли;
	
	Если ОтветБанка.Свойство("details") И ТипЗнч(ОтветБанка.details) = Тип("Массив") Тогда // старый вариант тэга
		Для Каждого Детали Из ОтветБанка.details Цикл
			ПоложитьДетальноеСообщениеВТекстОтвета(ДанныеСообщения, Детали);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеСообщения;
	
КонецФункции

#КонецОбласти

#Область ПреобразованиеВДвоичныеДанныеИзBase64

// Функции исключают типовые ошибки, могущие возникнут в результате подобных преобразований
//
Функция ДвоичныеДанныеИзBase64Строки(СтрокаBase64) Экспорт
	
	ДвоичныеДанныеИзBase64 = ДвоичныеДанныеИзBase64СтрокиПерваяИтерация(СтрокаBase64);
	
	Если ДвоичныеДанныеИзBase64 = Неопределено Тогда
		
		ДвоичныеДанныеИзBase64 = ДвоичныеДанныеИзBase64СтрокиВтораяИтерация(СтрокаBase64);
		
	КонецЕсли;
	
	Если ДвоичныеДанныеИзBase64 = Неопределено Тогда
		
		ДвоичныеДанныеИзBase64 = ДвоичныеДанныеИзBase64СтрокиТретьяИтерация(СтрокаBase64);
		
	КонецЕсли;
	
	Возврат ДвоичныеДанныеИзBase64;
	
	
КонецФункции

Функция ДвоичныеДанныеИзBase64СтрокиПерваяИтерация(СтрокаBase64)
	
	Попытка
		
		ДвоичныеДанныеИзBase64 = Base64Значение(СтрокаBase64);
		
		Возврат ДвоичныеДанныеИзBase64;
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

Функция ДвоичныеДанныеИзBase64СтрокиВтораяИтерация(СтрокаBase64)
	
	Попытка
		
		СтрокаBase64 = СтрЗаменить(СтрокаBase64, "_", "/");
		СтрокаBase64 = СтрЗаменить(СтрокаBase64, "-", "+");
		
		ДвоичныеДанныеИзBase64 = Base64Значение(СтрокаBase64);
		
		Возврат ДвоичныеДанныеИзBase64;
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

Функция ДвоичныеДанныеИзBase64СтрокиТретьяИтерация(СтрокаBase64)
	
	Попытка
		
		Если СтрНайти(СтрокаBase64, "==") = 0 Тогда
			
			СтрокаBase64 = СтрокаBase64 + "==";
			
		КонецЕсли;
		
		ДвоичныеДанныеИзBase64 = Base64Значение(СтрокаBase64);
		
		Возврат ДвоичныеДанныеИзBase64;
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
