
////////////////////////////////////////////////////////////////////////////////
// ВзаимодействияФормыБП: серверные процедуры и функции, вызываемые из форм
// документов взаимодействия.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ЗаполнитьСпискиВыбораКонтактнойИнформации(Форма);

	ПолучитьПоследниеТемыСобытий(Форма);
	
КонецПроцедуры

Процедура УстановитьКонтактноеЛицо(Форма, ДанныеУчастника) Экспорт
	
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(ДанныеУчастника) И ЗначениеЗаполнено(ДанныеУчастника.Контакт) Тогда
		Если ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.Контрагенты") Тогда
			Объект.Контрагент = ДанныеУчастника.Контакт;
		ИначеЕсли ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
			Объект.КонтактноеЛицо = ДанныеУчастника.Контакт;
		КонецЕсли;
	КонецЕсли;
	
	КонтрагентКонтакта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.КонтактноеЛицо, "ОбъектВладелец");
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Если (Не ЗначениеЗаполнено(Объект.КонтактноеЛицо) Или КонтрагентКонтакта <> Объект.Контрагент) Тогда
			Объект.КонтактноеЛицо = Справочники.КонтактныеЛица.КонтактноеЛицоПоУмолчанию(Объект.Контрагент);
		КонецЕсли;
	Иначе
		Если Не Справочники.КонтактныеЛица.ЭтоЛид(Объект.КонтактноеЛицо) Тогда
			Объект.КонтактноеЛицо = Справочники.КонтактныеЛица.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	УстановитьКонтактнуюИнформацию(Форма);
	
КонецПроцедуры

Процедура УстановитьКонтактнуюИнформацию(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Если Не ЗначениеЗаполнено(Объект.КонтактноеЛицо) И Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	ЗаполнитьСпискиВыбораКонтактнойИнформации(Форма);
		
	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		Контакт = Объект.КонтактноеЛицо;
	ИначеЕсли ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Контакт = Объект.Контрагент;
	КонецЕсли;
	
	ТипВзаимодействия = ТипЗнч(Объект.Ссылка);
	Если ТипВзаимодействия = Тип("ДокументСсылка.ТелефонныйЗвонок") Тогда
			
		Если Не ЗначениеЗаполнено(Объект.АбонентКакСвязаться) Тогда
			СписокТелефонов = Элементы.ТелефонКонтактногоЛица.СписокВыбора;
			Если СписокТелефонов.Количество() > 0 Тогда
				Объект.АбонентКакСвязаться = СписокТелефонов[0].Значение;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипВзаимодействия = Тип("ДокументСсылка.Встреча") Тогда
		Если Объект.Участники.Количество() > 0 Тогда
			Участник = Объект.Участники[0];
		Иначе
			Участник = Объект.Участники.Добавить();
		КонецЕсли;
		
		СписокАдресовЭП = Элементы.ЭлектроннаяПочтаКонтактногоЛица.СписокВыбора;

		Если СписокАдресовЭП.Количество() > 0 Тогда
			Участник.КакСвязаться = СписокАдресовЭП[0].Значение;
			Элементы.ЭлектроннаяПочтаКонтактногоЛица.КнопкаВыпадающегоСписка = СписокАдресовЭП.Количество() > 1;
		КонецЕсли;
		
		Участник.Контакт = Контакт;
		Участник.ПредставлениеКонтакта = Строка(Контакт);
		
	ИначеЕсли ТипВзаимодействия = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее")
		И Объект.СтатусПисьма <> Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Отправлено Тогда
		
		Если Объект.ПолучателиПисьма.Количество() > 0 Тогда
			СтрокаПолучатели = Объект.ПолучателиПисьма[0];
		Иначе
			СтрокаПолучатели = Объект.ПолучателиПисьма.Добавить();
		КонецЕсли;
		 
		СписокПолучателейПредставление = Элементы.СписокПолучателейПредставление;
		СписокАдресовЭП = СписокПолучателейПредставление.СписокВыбора;
		
		ДанныеКонтакта = Неопределено;
		Если СписокАдресовЭП.Количество() > 0 Тогда
			ДанныеКонтакта = СписокАдресовЭП[0].Значение;
		КонецЕсли;
		
		Если ДанныеКонтакта <> Неопределено Тогда
			СтрокаПолучатели.Контакт = ДанныеКонтакта.Контакт;
			СтрокаПолучатели.Адрес = ДанныеКонтакта.Адрес;
			СтрокаПолучатели.Представление = ДанныеКонтакта.Представление;
		КонецЕсли;
		
		Если Форма.СписокПолучателей.Количество() = 0 Тогда
			НоваяСтрока = Форма.СписокПолучателей.Добавить();
		Иначе
			НоваяСтрока = Форма.СписокПолучателей[0];
		КонецЕсли;

		НоваяСтрока.ВариантОтправки = ВзаимодействияКлиентСерверБП.Кому();
		
		Если ДанныеКонтакта <> Неопределено Тогда
			НоваяСтрока.Представление   = ДанныеКонтакта.Представление;
			НоваяСтрока.Адрес           = ДанныеКонтакта.Адрес;
			НоваяСтрока.Контакт         = ДанныеКонтакта.Контакт;
		Иначе
			НоваяСтрока.Представление   = "";
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтборКонтактногоЛица(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ПараметрыВыбораКонтакта = Новый Массив;
	
	ПараметрВыбора = Новый ПараметрВыбора("Отбор.ВидКонтактногоЛица", Перечисления.ВидыКонтактныхЛиц.КонтактноеЛицоКонтрагента);
	ПараметрыВыбораКонтакта.Добавить(ПараметрВыбора);
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.ОбъектВладелец", Объект.Контрагент);
		ПараметрыВыбораКонтакта.Добавить(ПараметрВыбора);
	КонецЕсли;
	
	Элементы.КонтактноеЛицо.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораКонтакта);
	
КонецПроцедуры

Процедура КонтактноеЛицоПриИзмененииНаСервере(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	КонтрагентКонтакта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.КонтактноеЛицо, "ОбъектВладелец");
	Если Объект.Контрагент <> КонтрагентКонтакта Тогда
		Объект.Контрагент = КонтрагентКонтакта;
	КонецЕсли;
	
КонецПроцедуры

#Область НастройкаФормы

Процедура ПолучитьПоследниеТемыСобытий(Форма) Экспорт

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;

	ТипСсылкиДокумента = ТипЗнч(Объект.Ссылка);
	
	ФормаДоступнаДляРедактирования = Не Форма.ТолькоПросмотр;
	
	Если ТипСсылкиДокумента = Тип("ДокументСсылка.ТелефонныйЗвонок") Тогда
		ТипДокумента = "ТелефонныйЗвонок";
	ИначеЕсли ТипСсылкиДокумента = Тип("ДокументСсылка.Встреча") Тогда
		ТипДокумента = "Встреча";
	ИначеЕсли ТипСсылкиДокумента = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда
		ФормаДоступнаДляРедактирования = ФормаДоступнаДляРедактирования
			И Объект.СтатусПисьма <> Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Отправлено;
		ТипДокумента = "ЭлектронноеПисьмоИсходящее";
	ИначеЕсли ТипСсылкиДокумента = Тип("ДокументСсылка.ПрочееВзаимодействие") Тогда
		ТипДокумента = "ПрочееВзаимодействие";
	КонецЕсли;

	СписокТем = Новый Массив;
	
	Если ФормаДоступнаДляРедактирования Тогда
		Запрос = Новый Запрос;
		
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		
		СхемаЗапроса = Новый СхемаЗапроса;
		
		ПакетЗапроса = СхемаЗапроса.ПакетЗапросов[0];
		ПакетЗапроса.ВыбиратьРазрешенные = Истина;
		ПакетЗапроса.Операторы[0].Источники.Добавить(СтрШаблон("Документ.%1", ТипДокумента));
		ПакетЗапроса.Операторы[0].КоличествоПолучаемыхЗаписей = 100;
		ПакетЗапроса.Операторы[0].ВыбираемыеПоля.Добавить("Тема");
		ПакетЗапроса.Операторы[0].ВыбираемыеПоля.Добавить("Дата");
		ПакетЗапроса.Операторы[0].Отбор.Добавить("Ответственный = &ТекущийПользователь");
		ПорядокЗапроса = ПакетЗапроса.Порядок.Добавить(Новый ВыражениеСхемыЗапроса("Дата"));
		ПорядокЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоУбыванию;
		
		Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
		
		ПакетЗапроса.Операторы[0].Отбор.Добавить("НЕ Тема ПОДОБНО """"");
		
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		РезультатЗапроса = Запрос.Выполнить();

		МаксимальноеКоличество = 10;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если СписокТем.Количество() >= МаксимальноеКоличество Тогда
				Прервать;
			КонецЕсли;
			Если СписокТем.Найти(Выборка.Тема) = Неопределено Тогда
				СписокТем.Добавить(Выборка.Тема);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

	Если СписокТем.Количество() > 0 Тогда
		Элементы.Тема.СписокВыбора.ЗагрузитьЗначения(СписокТем);
	Иначе
		Элементы.Тема.КнопкаВыпадающегоСписка = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СписокКонтактнойИнформации(Объект)
	
	СписокТелефонов = Новый СписокЗначений;
	СписокАдресовЭП = Новый СписокЗначений;
	СписокАдресовЭППредставление = Новый СписокЗначений;
	ЗаполнитьКонтактнуюИнформацию(Объект, СписокАдресовЭП, СписокАдресовЭППредставление, СписокТелефонов);

	Возврат Новый Структура("СписокТелефонов, СписокАдресовЭП, СписокАдресовЭППредставление", 
		СписокТелефонов, СписокАдресовЭП, СписокАдресовЭППредставление);
	
КонецФункции

Процедура ЗаполнитьКонтактнуюИнформацию(Объект, СписокАдресовЭП, СписокАдресовЭППредставление, СписокТелефонов)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "";
	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда

		ТекстЗапроса =
		"ВЫБРАТЬ
		|	КонтактныеЛицаКонтактнаяИнформация.Представление КАК Представление,
		|	КонтактныеЛицаКонтактнаяИнформация.Тип КАК Тип,
		|	КонтактныеЛицаКонтактнаяИнформация.Вид КАК Вид,
		|	КонтактныеЛицаКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей,
		|	""КонтактныеДанные"" КАК ИмяЭлементаДляРазмещения,
		|	КонтактныеЛицаКонтактнаяИнформация.Значение КАК Значение,
		|	КонтактныеЛица.Фамилия + "" "" + КонтактныеЛица.Имя + "" "" + КонтактныеЛица.Отчество КАК НаименованиеКонтакта,
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка КАК Контакт
		|ИЗ
		|	Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ПО КонтактныеЛицаКонтактнаяИнформация.Ссылка = КонтактныеЛица.Ссылка
		|ГДЕ
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка = &КонтактноеЛицо
		|	И КонтактныеЛицаКонтактнаяИнформация.Тип = &ТипКонтактнойИнформации";

		Запрос.УстановитьПараметр("КонтактноеЛицо", Объект.КонтактноеЛицо);

	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		
		Если Не ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
										  |ОБЪЕДИНИТЬ ВСЕ
										  |";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	КонтрагентыКонтактнаяИнформация.Представление КАК Представление,
		|	КонтрагентыКонтактнаяИнформация.Тип КАК Тип,
		|	КонтрагентыКонтактнаяИнформация.Вид КАК Вид,
		|	КонтрагентыКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей,
		|	""КонтактныеДанные"" КАК ИмяЭлементаДляРазмещения,
		|	КонтрагентыКонтактнаяИнформация.Значение КАК Значение,
		|	ПРЕДСТАВЛЕНИЕ(Ссылка) КАК НаименованиеКонтакта,
		|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Контакт
		|ИЗ
		|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
		|ГДЕ
		|	КонтрагентыКонтактнаяИнформация.Ссылка = &Контрагент
		|	И КонтрагентыКонтактнаяИнформация.Тип = &ТипКонтактнойИнформации";
		
		Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
		
	КонецЕсли;
	
	Если ТекстЗапроса = "" Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ТелефонныйЗвонок") Тогда
		Запрос.УстановитьПараметр("ТипКонтактнойИнформации", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Иначе 
		Запрос.УстановитьПараметр("ТипКонтактнойИнформации", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			СписокТелефонов.Добавить(Выборка.Представление);
		Иначе
			ДанныеКонтакта = Новый Структура;
			ПредставлениеАдресаЭлектроннойПочты = СтрШаблон(НСтр("ru = '%1 <%2>'"), Выборка.НаименованиеКонтакта, Выборка.Представление);
			ДанныеКонтакта.Вставить("Контакт", Выборка.Контакт);
			ДанныеКонтакта.Вставить("Адрес", Выборка.Представление);
			ДанныеКонтакта.Вставить("Представление", ПредставлениеАдресаЭлектроннойПочты);
			СписокАдресовЭППредставление.Добавить(ДанныеКонтакта, ПредставлениеАдресаЭлектроннойПочты);
			СписокАдресовЭП.Добавить(Выборка.Представление);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСпискиВыбораКонтактнойИнформации(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	СписокКонтактнойИнформации = СписокКонтактнойИнформации(Объект);

	Если ЭлементФормыСуществует(Форма, "ТелефонКонтактногоЛица") Тогда
		
		СписокТелефонов = СписокКонтактнойИнформации.СписокТелефонов;
		
		Элементы.ТелефонКонтактногоЛица.СписокВыбора.Очистить();
		Элементы.ТелефонКонтактногоЛица.КнопкаВыпадающегоСписка = СписокТелефонов.Количество() > 0;
		
		Если СписокТелефонов.Количество() > 0 Тогда
			Элементы.ТелефонКонтактногоЛица.СписокВыбора.ЗагрузитьЗначения(
			СписокТелефонов.ВыгрузитьЗначения());
			Если ПустаяСтрока(Объект.АбонентКакСвязаться) Тогда
				Объект.АбонентКакСвязаться = СписокТелефонов[0];
			КонецЕсли;
		КонецЕсли; 
		
	КонецЕсли;
	
	Если ЭлементФормыСуществует(Форма, "ЭлектроннаяПочтаКонтактногоЛица") Тогда		
		СписокАдресовЭП = СписокКонтактнойИнформации.СписокАдресовЭП;
		Элементы.ЭлектроннаяПочтаКонтактногоЛица.КнопкаВыпадающегоСписка = СписокАдресовЭП.Количество() > 0;;
		Если СписокАдресовЭП.Количество() > 0 Тогда
			Элементы.ЭлектроннаяПочтаКонтактногоЛица.СписокВыбора.ЗагрузитьЗначения(
				СписокАдресовЭП.ВыгрузитьЗначения());
		Иначе
			Элементы.ЭлектроннаяПочтаКонтактногоЛица.СписокВыбора.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлементФормыСуществует(Форма, "СписокПолучателей") Тогда
		СписокПолучателейПредставление = Элементы.СписокПолучателейПредставление;
		СписокАдресовЭППредставление = СписокКонтактнойИнформации.СписокАдресовЭППредставление;
		СписокПолучателейПредставление.СписокВыбора.Очистить();
		СписокПолучателейПредставление.КнопкаВыпадающегоСписка = СписокАдресовЭППредставление.Количество() > 0;
		Если СписокАдресовЭППредставление.Количество() > 0 Тогда
			Для Каждого СтрокаАдреса Из СписокАдресовЭППредставление Цикл
				СписокПолучателейПредставление.СписокВыбора.Добавить(СтрокаАдреса.Значение, СтрокаАдреса.Представление);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Функция ЭлементФормыСуществует(Форма, ИмяРеквизита)
		
	Возврат Форма.Элементы.Найти(ИмяРеквизита) <> Неопределено;

КонецФункции

#КонецОбласти
