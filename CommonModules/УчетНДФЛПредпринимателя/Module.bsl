////////////////////////////////////////////////////////////////////////////////
// Подсистема "Индивидуальный предприниматель"
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает дату начала применения схемы расчета авансовых платежей предпринимателем
// (п. 7 ст. 1 Федерального закона от 15.04.2019 N 63-ФЗ)
//
// Возвращаемое значение:
//   Дата
//
Функция ДатаНачалаРасчетаАвансовыхПлатежейНДФЛ() Экспорт
	
	Возврат Дата('20200101');
	
КонецФункции

// Возвращает признак ввода начальных остатков по счету 68.21 - НДФЛ индивидуального предпринимателя
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация, по которой требуется проверить остатки по счету.
//
// Возвращаемое значение:
//   Булево
//
Функция НачальныеОстаткиПоНалогуВведены(Организация) Экспорт
	
	Если Обработки.ПомощникУплатыНалоговВзносовПрошлыхЛет.ПройденОпросПоНалогу(
		Организация, ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель()) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВводНачальныхОстатковРасчетыПоНалогамИСборам.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.ВводНачальныхОстатков.РасчетыПоНалогамИСборам КАК ВводНачальныхОстатковРасчетыПоНалогамИСборам
	|ГДЕ
	|	ВводНачальныхОстатковРасчетыПоНалогамИСборам.Ссылка.Проведен
	|	И ВводНачальныхОстатковРасчетыПоНалогамИСборам.Ссылка.Организация = &Организация
	|	И ВводНачальныхОстатковРасчетыПоНалогамИСборам.Ссылка.ОтражатьВБухгалтерскомУчете
	|	И ВводНачальныхОстатковРасчетыПоНалогамИСборам.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.РасчетыПоНалогамИСборам)
	|	И ВводНачальныхОстатковРасчетыПоНалогамИСборам.СчетУчета В(&СчетНачисленияНалога)");
	
	Запрос.УстановитьПараметр("СчетНачисленияНалога",
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП)); // 68.21
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает период, за который уплачивается налог.
//
// Параметры:
//  ПоказательПериода - Строка - Показатель периода платежа в бюджет.
//  ДатаПлатежа       - Дата - дата платежного документа.
//
// Возвращаемое значение:
//   Дата - начало квартала, за который уплачивается налог;
//          для налога за год - начало последнего квартала в году.
//
Функция НалоговыйПериодПоПлатежнымДанным(ПоказательПериода, ДатаПлатежа) Экспорт
	
	ОписаниеПериода = ПлатежиВБюджетКлиентСервер.РазобратьНалоговыйПериод(ПоказательПериода);
	
	Если ОписаниеПериода.Периодичность = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение() Тогда
		
		НалоговыйПериод = НачалоМесяца(ДобавитьМесяц(ДатаПлатежа, -1));
		
	ИначеЕсли ОписаниеПериода.Периодичность = ПлатежиВБюджетКлиентСервер.ПериодичностьГод() Тогда
		
		НалоговыйПериод = НачалоКвартала(КонецГода(ОписаниеПериода.Дата));
		
	ИначеЕсли ОписаниеПериода.Периодичность = ПлатежиВБюджетКлиентСервер.ПериодичностьПолугодие() Тогда
		
		НалоговыйПериод = НачалоКвартала(ДобавитьМесяц(ОписаниеПериода.Дата, 3));
		
	ИначеЕсли ОписаниеПериода.Периодичность = ПлатежиВБюджетКлиентСервер.ПериодичностьКвартал()
		Или ОписаниеПериода.Периодичность = ПлатежиВБюджетКлиентСервер.ПериодичностьМесяц() Тогда
		
		НалоговыйПериод = НачалоКвартала(ОписаниеПериода.Дата);
		
	Иначе
		
		НалоговыйПериод = ДатаПлатежа;
		
	КонецЕсли;
	
	Возврат НалоговыйПериод;
	
КонецФункции

// Определяет периодичность налогового платежа в зависимости от оплачиваемого периода.
//
// Параметры:
//  НалоговыйПериод - Дата - оплачиваемый период
//
// Возвращаемое значение:
//   ПеречислениеСсылка.Периодичность - возможные значения:
//     Год, если платеж за последний квартал (налог за год)
//     Квартал, если платеж за все остальные кварталы (авансовый платеж).
//
Функция ПериодичностьНалоговогоПлатежа(НалоговыйПериод) Экспорт
	
	Если КонецКвартала(НалоговыйПериод) = КонецГода(НалоговыйПериод) Тогда
		// Платеж за последний квартал года - это налог за год.
		Возврат Перечисления.Периодичность.Год;
	Иначе
		// За периоды кроме последнего квартала года уплачивается ежеквартальный авансовый платеж.
		Возврат Перечисления.Периодичность.Квартал;
	КонецЕсли;
	
КонецФункции

// Возвращает наиболее вероятный период (квартал), за который уплачивается НДФЛ
//
// Параметры:
//   ДатаПлатежа - Дата - Дата платежа фактическая
//
// Возвращаемое значение:
//   Дата - начало квартала, за который выполняется платеж
//
Функция РелевантныйПериодНалоговогоПлатежа(ДатаПлатежа) Экспорт
	
	// Обработаем наиболее вероятные сценарии:
	// - до 15 июля - оплата налога за год
	// - в срок с начала апреля до 25 апреля - уплата авансового платежа
	// - после 15 июля - уплата авансового платежа
	
	ДеньУплатыАвансаПоУмолчанию = ДеньУплатыАвансовогоПлатежаПоУмолчанию();
	СрокУплатыНалогаЗаГод = СрокУплатыНалогаПоУмолчанию(ДобавитьМесяц(ДатаПлатежа, -12));
	
	Если Месяц(ДатаПлатежа) = 4 И День(ДатаПлатежа) <= ДеньУплатыАвансаПоУмолчанию Тогда
		
		НалоговыйПериод = НачалоКвартала(ДобавитьМесяц(ДатаПлатежа, -3));
		
	ИначеЕсли ДатаПлатежа < СрокУплатыНалогаЗаГод Тогда
		
		НалоговыйПериод = НачалоКвартала(НачалоГода(ДатаПлатежа) - 1);
		
	Иначе
		
		НалоговыйПериод = НачалоКвартала(ДобавитьМесяц(ДатаПлатежа, -3));
		
	КонецЕсли;
	
	Возврат НалоговыйПериод;
	
КонецФункции

// Возвращает календарный срок уплаты налога на доходы физических лиц
//
// Параметры:
//   Период - Дата - Дата в пределах года, за который определяется срок уплаты налога
//
// Возвращаемое значение:
//   Дата
//
Функция СрокУплатыНалогаПоУмолчанию(Период) Экспорт
	
	Возврат Дата(Год(Период) + 1, 07, 15);
	
КонецФункции

// Возвращает признак того, применяется ли освобождение от уплаты налога по НДФЛ
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация, для которой выполняется проверка освобождения от налога
//   Период      - Дата - Дата, для которой определяется освобождение от налога
//
// Возвращаемое значение:
//   Булево
//
Функция ПрименяетсяОсвобождениеОтНалога(Организация, Период) Экспорт
	
	Если НалоговыйУчет.ВозможноОсвобождениеОтНалога(ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель(), Период)
		И НалоговыйУчет.ДеятельностьОтнесенаКПострадавшимОтКоронавируса(Организация) Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает ставки налога НДФЛ, указанные в НК РФ
//
// Параметры:
//   Период        - Дата - Дата, для которой определяются ставки налога
//   ГруппаДоходов - Строка - Идентификатор группы доходов налоговой базы НДФЛ (см. Приложение 4 к Порядку заполнения 3-НДФЛ)
//
// Возвращаемое значение:
//   ТаблицаЗначений - ставки налогов
//
Функция НалоговыеСтавки(Период, ГруппаДоходов = "") Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("СтавкаНалога", ОбщегоНазначения.ОписаниеТипаЧисло(5, 2));
	Результат.Колонки.Добавить("МинимальнаяСуммаДохода", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	Результат.Колонки.Добавить("МаксимальнаяСуммаДохода", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	Результат.Колонки.Добавить("БазовыйНалог", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	Результат.Колонки.Добавить("КодВидаДоходов", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	
	Если УчетНДФЛПредпринимателяКлиентСервер.ПрименяетсяРасширеннаяПрогрессивнаяйШкала(Период)
		И Не ГруппаДоходовОсобыеУсловия(ГруппаДоходов) Тогда
		
		// Базовая ставка налога
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.СтавкаНалога = 13;
		НоваяСтрока.МаксимальнаяСуммаДохода = 2400000;
		НоваяСтрока.КодВидаДоходов = Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛПредпринимателя13();
		// 15%
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.СтавкаНалога = 15;
		НоваяСтрока.БазовыйНалог = 312000;
		НоваяСтрока.МинимальнаяСуммаДохода = 2400000;
		НоваяСтрока.КодВидаДоходов = Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛПредпринимателя15(Период);
		
		Если Не ГруппаДоходовПоИмуществу(ГруппаДоходов) Тогда
			НоваяСтрока.МаксимальнаяСуммаДохода = 5000000;
			// 18%
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.СтавкаНалога = 18;
			НоваяСтрока.МинимальнаяСуммаДохода = 5000000;
			НоваяСтрока.МаксимальнаяСуммаДохода = 20000000;
			НоваяСтрока.БазовыйНалог = 702000;
			НоваяСтрока.КодВидаДоходов = Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛПредпринимателя18();
			// 20%
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.СтавкаНалога = 20;
			НоваяСтрока.МинимальнаяСуммаДохода = 20000000;
			НоваяСтрока.МаксимальнаяСуммаДохода = 50000000;
			НоваяСтрока.БазовыйНалог = 3402000;
			НоваяСтрока.КодВидаДоходов = Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛПредпринимателя20();
			// 22%
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.СтавкаНалога = 22;
			НоваяСтрока.МинимальнаяСуммаДохода = 50000000;
			НоваяСтрока.БазовыйНалог = 9402000;
			НоваяСтрока.КодВидаДоходов = Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛПредпринимателя22();
		КонецЕсли;
		
	ИначеЕсли Период >= УчетНДФЛПредпринимателяКлиентСервер.ДатаНачалаПрогрессивнойШкалы()
		Или ГруппаДоходовОсобыеУсловия(ГруппаДоходов) Тогда
		// Базовая ставка налога
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.СтавкаНалога = 13;
		НоваяСтрока.МаксимальнаяСуммаДохода = 5000000;
		НоваяСтрока.КодВидаДоходов = Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛПредпринимателя13();
		// Повышенная ставка
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.СтавкаНалога = 15;
		НоваяСтрока.МинимальнаяСуммаДохода = 5000000;
		НоваяСтрока.БазовыйНалог = 650000;
		НоваяСтрока.КодВидаДоходов = Справочники.ВидыНалоговИПлатежейВБюджет.КодВидаДоходаНДФЛПредпринимателя15(Период);
	Иначе
		// Базовая ставка налога
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.СтавкаНалога = 13;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает сумму налога на доходы физических лиц
//
// Параметры:
//   НалоговаяБаза - Число - сумма налоговой базы
//   Период        - Дата - Дата, для которой рассчитывается сумма налога
//   ГруппаДоходов - Строка - Идентификатор группы доходов налоговой базы НДФЛ (см. Приложение 4 к Порядку заполнения 3-НДФЛ)
//
// Возвращаемое значение:
//   Число
//
Функция СуммаНалога(НалоговаяБаза, Период, ГруппаДоходов = "") Экспорт
	
	НалоговыеСтавки = НалоговыеСтавки(Период, ГруппаДоходов);
	Результат = 0;
	
	ПараметрыРасчета = ПараметрыРасчетаНалоговойБазы(НалоговыеСтавки, НалоговаяБаза);
	
	Результат = ПараметрыРасчета.БазовыйНалог
		+ (НалоговаяБаза - ПараметрыРасчета.МинимальнаяСуммаДохода) * ПараметрыРасчета.СтавкаНалога / 100;
	
	Возврат Окр(Результат, 0);
	
КонецФункции

// Возвращает базовую ставку НДФЛ при минимальной налоговой базе
//
// Параметры:
//   Период          - Дата - Дата, для которой нужно получить базовую ставку
//   НалоговыеСтавки - ТаблицаЗначений - см. НалоговыеСтавки()
//                   - Неопределено - Расчет согласно текущий ставкам за период
//
// Возвращаемое значение:
//   Число
// 
Функция МинимальнаяСтавка(Период, НалоговыеСтавки = Неопределено) Экспорт
	
	Если НалоговыеСтавки = Неопределено Тогда
		ТаблицаСтавок = НалоговыеСтавки(Период);
	Иначе
		ТаблицаСтавок = НалоговыеСтавки;
	КонецЕсли;
	
	НайденноеЗначение = ТаблицаСтавок.Найти(0, "МинимальнаяСуммаДохода");
	Если Не ЗначениеЗаполнено(НайденноеЗначение) Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат НайденноеЗначение.СтавкаНалога;
	
Конецфункции

// Возвращает максимальную ставку НДФЛ
//
// Параметры:
//   Период - Дата - Дата, для которой нужно получить максимальную ставку
//
// Возвращаемое значение:
//   Число
// 
Функция МаксимальнаяСтавка(Период) Экспорт
	
	Возврат НалоговыеСтавки(Период).Найти(0, "МаксимальнаяСуммаДохода").СтавкаНалога;
	
Конецфункции

// Возвращает параметры расчета для налоговой базы
//
// Параметры:
//   НалоговыеСтавки - ТаблицаЗначений - см. НалоговыеСтавки()
//   НалоговаяБаза - Число - Налоговая база, для которой нужно получить параметры расчета налога
//
// Возвращаемое значение:
//   Структура - См. НовыеПараметрыРасчетаНалоговойБазы()
// 
Функция ПараметрыРасчетаНалоговойБазы(Знач НалоговыеСтавки, НалоговаяБаза = 0) Экспорт
	
	Результат = НовыеПараметрыРасчетаНалоговойБазы();
	
	Если НалоговаяБаза = 0 Тогда
		// Вычисляет параметры для базовой ставки
		НалоговыеСтавки.Сортировать("МинимальнаяСуммаДохода Возр");
		ЗаполнитьЗначенияСвойств(Результат, ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(НалоговыеСтавки[0]));
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтавка Из НалоговыеСтавки Цикл
		Если НалоговаяБаза > ТекущаяСтавка.МинимальнаяСуммаДохода
			И (НалоговаяБаза <= ТекущаяСтавка.МаксимальнаяСуммаДохода Или ТекущаяСтавка.МаксимальнаяСуммаДохода = 0) Тогда
			
			ЗаполнитьЗначенияСвойств(Результат, ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТекущаяСтавка));
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
Конецфункции

// Возвращает максимальную сумму дохода, для которой применяется базовая ставка НДФЛ
//
// Параметры:
//   Период - Дата - Налоговый период, для которого нужно вычислить сумму. Если Неопределено - возращается сумма по текущему периоду.
//   ГруппаДоходов - Строка - Идентификатор группы доходов налоговой базы НДФЛ (см. Приложение 4 к Порядку заполнения 3-НДФЛ)
//
// Возвращаемое значение:
//    Число
//
Функция ПредельнаяСуммаДоходаПоБазовойСтавке(Период, ГруппаДоходов = "") Экспорт
	
	НалоговыеСтавки = НалоговыеСтавки(Период, ГруппаДоходов);
	Возврат ПараметрыРасчетаНалоговойБазы(НалоговыеСтавки, 0).МаксимальнаяСуммаДохода;
	
КонецФункции

// Возвращает имя формы декларации для указанного налогового периода
// 
// Параметры:
//   Период - Дата - Налоговый период
//
// Возвращаемое значение:
//   Булево
//
Функция ФормаДекларацииПоУмолчанию(Период) Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса();
	НачалоДействияУпрощеннойФормы = ПомощникЗаполнения3НДФЛКлиентСервер.НачалоДействияУпрощеннойФормыДекларации();
	
	Если ТекущаяДата >= НачалоДействияУпрощеннойФормы И Год(Период) >= Год(НачалоДействияУпрощеннойФормы) - 1 Тогда
		Результат = "ФормаОтчета2025Кв4";
	Иначе
		Результат = Отчеты.РегламентированныйОтчет3НДФЛ.ФормаПоУмолчанию(Период);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает таблицу начисленных сумм НДФЛ ИП по видам налогов
//
// Параметры:
//   Показатели - Структура - см. НовыеПараметрыРасчетаСуммыПоВидамНалога()
//   НалоговыеСтавки - ТаблицаЗначений - См. НалоговыеСтавки()
//
// Возвращаемое значение:
//   ТаблицаЗначений - см. НовыйТаблицаПлатежейНДФЛ()
//
Функция НачисленныеСуммыПоВидамНалога(Показатели, НалоговыеСтавки = Неопределено) Экспорт
	
	Результат = НовыйТаблицаПлатежейНДФЛ();
	ВидНалогаПоУмолчанию = Перечисления.ВидыНалогов.НДФЛ_ИП;
	
	Если Показатели.Период < УчетНДФЛПредпринимателяКлиентСервер.ДатаНачалаПрогрессивнойШкалы() Тогда
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.ВидНалога = ВидНалогаПоУмолчанию;
		НоваяСтрока.Сумма = Показатели.НалогКУплате;
		
		Возврат Результат;
		
	КонецЕсли;
	
	// Получаем таблицу действующий налоговых ставок и вычисляем по налоговым базам, что нужно заплатить
	Если НалоговыеСтавки = Неопределено Тогда
		НалоговыеСтавки = НалоговыеСтавки(Показатели.Период);
	КонецЕсли;
	
	НалоговыеСтавки.Сортировать("БазовыйНалог Возр");
	
	СуммаКРаспределению = Показатели.НалогКУплате;
	Для Каждого ТекущаяСтавка Из НалоговыеСтавки Цикл
		
		Если СуммаКРаспределению <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяСтавка.МаксимальнаяСуммаДохода = 0 Тогда
			МаксимальныйНалог = Показатели.НалогИсчисленныйВсего;
		Иначе
			МаксимальныйНалог = Мин(СуммаНалога(ТекущаяСтавка.МаксимальнаяСуммаДохода, Показатели.Период),
				Показатели.НалогИсчисленныйВсего);
		КонецЕсли;
		
		Если Показатели.АвансовыеПлатежи >= МаксимальныйНалог Тогда
			Продолжить;
		КонецЕсли;
		
		МинимальныйНалог = Макс(ТекущаяСтавка.БазовыйНалог, Показатели.АвансовыеПлатежи);
		
		Если МаксимальныйНалог < МинимальныйНалог Тогда
			НалогКУплате = СуммаКРаспределению;
		Иначе
			НалогКУплате = МаксимальныйНалог - МинимальныйНалог;
		КонецЕсли;
		
		СуммаКРаспределению = СуммаКРаспределению - НалогКУплате;
		
		Если НалогКУплате > 0 Тогда
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.ВидНалога = ВидНалогаПоУмолчанию;
			НоваяСтрока.КодВидаДоходов = ТекущаяСтавка.КодВидаДоходов;
			НоваяСтрока.НалоговаяБаза = Мин(Показатели.НалоговаяБаза, ТекущаяСтавка.МаксимальнаяСуммаДохода);
			НоваяСтрока.Сумма = НалогКУплате;
			НоваяСтрока.СтавкаНалога = ТекущаяСтавка.СтавкаНалога;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область РасчетыПоНДФЛ

// Определяет сумму отраженных в бухгалтерском учете (уплаченных) платежей по налогу НДФЛ за указанный период.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - Налогоплательщик.
//  НачалоПериода - Дата - Дата и время начала анализируемого периода.
//  КонецПериода  - Дата - Дата и время окончания анализируемого периода.
//
// Возвращаемое значение:
//  Число - сумма уплаченного налога (авансовых платежей).
//
Функция УплаченныйНалогЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если КонецПериода < НачалоПериода
		Или Не УчетнаяПолитика.ПлательщикНДФЛЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если КонецПериода < ЕдиныйНалоговыйСчет.НачалоПростогоУчета() Тогда
		Возврат УплаченныйНалогЗаПериодДоПростогоУчетаЕНС(Организация, НачалоПериода, КонецПериода);
	Иначе
		Возврат УплаченныйНалогЗаПериодВПростомУчетеЕНС(Организация, НачалоПериода, КонецПериода);
	КонецЕсли;
	
КонецФункции

// Возвращает остаток расчетов по НДФЛ на конец периода.
//
// Параметры:
//   Организация - СправочникСсылка.Организации - налогоплательщик.
//   Период      - Дата - Дата и время, на которую будет рассчитан остаток.
//
// Возвращаемое значение:
//   Число - остаток расчетов по налогу:
//           если отрицательный - имеется непогашенная задолженность;
//           если положительный - имеется переплата по налогу.
//
Функция ОстатокРасчетовПоНДФЛ(Организация, Период) Экспорт
	
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // 68.21
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГраницаОстатков",       Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("СчетаНачисленияНалога", СчетаНачисленияНалога);
	Запрос.УстановитьПараметр("ВидыНалоговыхПлатежей", Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ХозрасчетныйОстатки.СуммаОстаток), 0) КАК Остаток
	|ПОМЕСТИТЬ вт_Долг
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаОстатков,
	|			Счет В (&СчетаНачисленияНалога),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|			Организация = &Организация
	|				И Субконто1 В (&ВидыНалоговыхПлатежей)) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	-ЕСТЬNULL(РасчетыПоНалогамНаЕдиномНалоговомСчетеОстатки.СуммаОстаток, 0)
	|ИЗ
	|	РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете.Остатки(
	|			&ГраницаОстатков,
	|			СчетУчета В (&СчетаНачисленияНалога)
	|				И Организация = &Организация) КАК РасчетыПоНалогамНаЕдиномНалоговомСчетеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(вт_Долг.Остаток) КАК Остаток
	|ИЗ
	|	вт_Долг КАК вт_Долг";
	
	Возврат Запрос.Выполнить().Выгрузить()[0].Остаток;
	
КонецФункции

// Определяет остаток неоплаченного налога за периоды ранее переданного.
// Остаток определяется исходя изо всех оплат за прошлые налоговые периоды независимо от даты фактических платежей.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - организация, для которой требуются сведения.
//  ТекущийПериод - Дата - дата в пределах налогового периода (года),
//                         на начало которого требуется остаток расчетов за прошлые годы.
//
// Возвращаемое значение:
//   Число - остаток расчетов по налогу:
//           если отрицательный - имеется непогашенная задолженность за прошлые годы;
//           если положительный - имеется переплата по налогу за прошлые годы.
//
Функция ОстатокРасчетовПоНалогуЗаПредыдущиеПериоды(Организация, ТекущийПериод) Экспорт
	
	ДатаНачалаУчета = Справочники.Организации.ДатаНачалаВеденияУчета(Организация);
	
	// Если учет еще не ведется, или передан год до первого года ведения учета,
	// расчеты за предыдущие годы заведомо нулевые.
	Если ДатаНачалаУчета = Неопределено Или Год(ДатаНачалаУчета) > Год(ТекущийПериод) Тогда
		Возврат 0;
	КонецЕсли;
	
	НачалоГода = НачалоГода(ТекущийПериод);
	СчетНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // 68.21
	
	СчетаОплатыНалога = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаОплатыНалога,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Касса));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаОплатыНалога,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаОплатыНалога,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЦифровойРубль));
		
	ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(
		ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель(),
		Организация);
	СрокУплаты = ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(Организация, НачалоГода, ВидНалога).Срок;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоГода",             НачалоГода);
	Запрос.УстановитьПараметр("НачалоПредыдущегоГода",  НачалоГода(НачалоГода - 1));
	Запрос.УстановитьПараметр("КонецПредыдущегоГода",   Новый Граница(НачалоГода - 1, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("СчетНачисленияНалога",   СчетНачисленияНалога);
	Запрос.УстановитьПараметр("СчетаОплатыНалога",      СчетаОплатыНалога);
	Запрос.УстановитьПараметр("ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.УстановитьПараметр("СрокУплаты", СрокУплаты);
	
	// Данные о расчетах по налогу за предыдущие налоговые периоды определяются следующим образом:
	// 1. Получаем платежи за предыдущие налоговые периоды, совершенные в текущем налоговом периоде.
	// 2. Суммируем с суммой задолженности по НДФЛ на начало текущего налогового периода
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ДанныеЗаполнения.СуммаПлатежа) КАК СуммаПлатежа
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот), 0) КАК СуммаПлатежа
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачалоГода,
	|				,
	|				Регистратор,
	|				СчетДт В (&СчетНачисленияНалога),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|				СчетКт В (&СчетаОплатыНалога),
	|				,
	|				Организация = &Организация
	|					И СубконтоДт1 В (&ВидыПлатежейВГосБюджет)) КАК ХозрасчетныйОборотыДтКт
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.СписаниеСРасчетногоСчета)
	|					ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.СписаниеСРасчетногоСчета).НалоговыйПериод
	|				КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
	|					ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РасходныйКассовыйОрдер).НалоговыйПериод
	|				ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|			КОНЕЦ < &НачалоГода
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(СУММА(ХозрасчетныйОстатки.СуммаОстаток), 0)
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&КонецПредыдущегоГода,
	|				Счет В (&СчетНачисленияНалога),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|				Организация = &Организация
	|					И Субконто1 В (&ВидыПлатежейВГосБюджет)) КАК ХозрасчетныйОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(СУММА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СуммаРасход), 0)
	|	ИЗ
	|		РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете.Обороты(
	|				&НачалоПредыдущегоГода,
	|				,
	|				Период,
	|				Организация = &Организация
	|					И СчетУчета В (&СчетНачисленияНалога)
	|					И СрокУплаты = &СрокУплаты) КАК РасчетыПоНалогамНаЕдиномНалоговомСчете) КАК ДанныеЗаполнения";
	
	Возврат Запрос.Выполнить().Выгрузить()[0].СуммаПлатежа;
	
КонецФункции

// Получает развернутые по годам расчеты по налогу за налоговые периоды ранее переданного.
// Оплаченный налог за каждый год определяется исходя изо всех оплат, в которых указан период в пределах года,
// независимо от даты фактических платежей.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - организация, для которой требуются сведения.
//  ТекущийПериод - Дата - дата в пределах налогового периода (года),
//                         на начало которого требуются данные расчетов за прошлые годы.
//
// Возвращаемое значение:
//   ТаблицаЗначений - развернутые по годам суммы расчетов по налогу, состав см. в НоваяТаблицаРасчетовПоНалогу().
//
Функция РасчетыПоНалогуЗаПредыдущиеПериоды(Организация, ТекущийПериод) Экспорт
	
	ТаблицаРасчетов = НоваяТаблицаРасчетовПоНалогу();
	
	ДатаНачалаУчетаРасчетов = Справочники.Организации.ДатаНачалаВеденияУчета(Организация);
	
	// Если учет еще не ведется, суммы налога за предыдущие годы заведомо нулевые.
	Если Не ЗначениеЗаполнено(ДатаНачалаУчетаРасчетов) Или Год(ДатаНачалаУчетаРасчетов) > Год(ТекущийПериод) Тогда
		Возврат ТаблицаРасчетов;
	КонецЕсли;
	
	НачалоПериода = НачалоГода(ДобавитьМесяц(ДатаНачалаУчетаРасчетов, - 12)); // Начало года ввода первых документов, включая ввод остатков.
	КонецПериода  = НачалоГода(ТекущийПериод) - 1;                            // Конец года, предшествующего переданному.
	
	Периоды = КалендарьБухгалтера.Периоды(НачалоПериода, КонецПериода, Перечисления.Периодичность.Год);
	
	Если НЕ ЗначениеЗаполнено(Периоды) Тогда
		Возврат ТаблицаРасчетов;
	КонецЕсли;
	
	// Для получения расчетов за каждый налоговый период определяется начисленный налог за период (год),
	// затем из начислений вычитаются платежи за данный налоговый период и переплата за прошлые годы при ее наличии.
	
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // Кт 68.21
	
	СчетаОплатыНалога = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаОплатыНалога, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Касса));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаОплатыНалога, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаОплатыНалога, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЦифровойРубль));
	
	ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(
		ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель(),
		Организация);
	
	// При переходе на ЕНС налоговый период определяем по сроку уплаты из регистра РасчетыПоНалогамНаЕдиномНалоговомСчете.
	// Для года, в котором ввели уплату на ЕНС, учитываем, что взносы с доходов предшествующего года также уплачивались на ЕНС
	УплатаНаЕНСГод = Год(НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж()) - 1;
	СрокиУплаты = Новый Массив; // Сроки уплаты налога за год, по которым оплаты осуществляются на ЕНС
	
	Для Каждого ТекущийНалоговыйПериод Из Периоды Цикл
		Если Год(ТекущийНалоговыйПериод) < УплатаНаЕНСГод Тогда
			// До перехода на ЕНС не учитываем сроки уплаты
			Продолжить;
		КонецЕсли;
		// По операциям на ЕНС нужно вычислить налоговый период по конректному сроку уплаты
		КрайнийСрокУплаты = НачалоДня(ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(Организация,
			КонецГода(ТекущийНалоговыйПериод),
			ВидНалога).Срок);
		СрокиУплаты.Добавить(КрайнийСрокУплаты);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",          НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",           КонецПериода);
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("СчетаНачисленияНалога",  СчетаНачисленияНалога);
	Запрос.УстановитьПараметр("СчетаОплатыНалога",      СчетаОплатыНалога);
	Запрос.УстановитьПараметр("ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.УстановитьПараметр("КрайнийСрокУплаты",      КрайнийСрокУплаты);
	Запрос.УстановитьПараметр("СрокиУплаты",            СрокиУплаты);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, ГОД) КАК НалоговыйПериод,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт - ХозрасчетныйОбороты.СуммаОборотДт) КАК Начислено
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Счет В (&СчетаНачисленияНалога),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|			Организация = &Организация
	|				И Субконто1 В (&ВидыПлатежейВГосБюджет),
	|			НЕ КорСчет В (&СчетаОплатыНалога),
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ОперацияПоЕдиномуНалоговомуСчету
	|	И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.УведомлениеОбИсчисленныхСуммахНалогов
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, ГОД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Регистратор КАК ДокументУплаты,
	|	ХозрасчетныйОбороты.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОбороты.Регистратор) = ТИП(Документ.СписаниеСРасчетногоСчета)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.СписаниеСРасчетногоСчета).НалоговыйПериод
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОбороты.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РасходныйКассовыйОрдер).НалоговыйПериод
	|		КОГДА ХозрасчетныйОбороты.СуммаОборот < 0
	|			ТОГДА НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, КВАРТАЛ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК НалоговыйПериод,
	|	ХозрасчетныйОбороты.СуммаОборот КАК СуммаПлатежа
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			,
	|			Регистратор,
	|			Счет В (&СчетаНачисленияНалога),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|			Организация = &Организация
	|				И Субконто1 В (&ВидыПлатежейВГосБюджет),
	|			КорСчет В (&СчетаОплатыНалога),
	|			) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.ПлатежныйДокумент,
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.Период,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ДЕНЬ) В (&СрокиУплаты)
	|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ГОД, -1), ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, КВАРТАЛ), КВАРТАЛ, -1)
	|	КОНЕЦ,
	|	СУММА(РасчетыПоНалогамНаЕдиномНалоговомСчете.Сумма)
	|ИЗ
	|	РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете КАК РасчетыПоНалогамНаЕдиномНалоговомСчете
	|ГДЕ
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.Период >= &НачалоПериода
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.Организация = &Организация
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.СчетУчета В(&СчетаНачисленияНалога)
	|	И (РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты МЕЖДУ &НачалоПериода И &КонецПериода
	|			ИЛИ НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ДЕНЬ) = &КрайнийСрокУплаты)
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.ПлатежныйДокумент,
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.Период,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ДЕНЬ) В (&СрокиУплаты)
	|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ГОД, -1), ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, КВАРТАЛ), КВАРТАЛ, -1)
	|	КОНЕЦ";
	
	Результат = Запрос.ВыполнитьПакет();
	
	Начисления = Результат[Результат.ВГраница() - 1].Выгрузить();
	Платежи    = Результат[Результат.ВГраница()].Выгрузить();
	
	Для Каждого НалоговыйПериод Из Периоды Цикл
		
		Начислено = 0;
		Уплачено  = 0;
		
		НачалоНалоговогоПериода = НачалоГода(НалоговыйПериод);
		КонецНалоговогоПериода  = КонецГода(НалоговыйПериод);
		
		СтрокаРасчетов = Начисления.Найти(НалоговыйПериод, "НалоговыйПериод");
		Если СтрокаРасчетов <> Неопределено Тогда
			Начислено = СтрокаРасчетов.Начислено;
		КонецЕсли;
		
		Для Каждого Платеж Из Платежи Цикл
			// Определим налоговый период для операций, в которых он явно не указан.
			Если НЕ ЗначениеЗаполнено(Платеж.НалоговыйПериод) Тогда
				Платеж.НалоговыйПериод = РелевантныйПериодНалоговогоПлатежа(Платеж.ДатаПлатежа);
			КонецЕсли;
			
			Если Платеж.НалоговыйПериод <= КонецНалоговогоПериода И Платеж.НалоговыйПериод >= НачалоНалоговогоПериода Тогда
				Уплачено = Уплачено + Платеж.СуммаПлатежа;
			КонецЕсли;
		КонецЦикла;
		
		НоваяСтрока = ТаблицаРасчетов.Добавить();
		
		НоваяСтрока.НалоговыйПериод = НалоговыйПериод;
		НоваяСтрока.Начислено = Начислено;
		НоваяСтрока.Уплачено  = Уплачено;
		
		ОстатокРасчетов = Уплачено - Начислено;
		
		Если ОстатокРасчетов > 0 Тогда
			НоваяСтрока.Переплата = ОстатокРасчетов;
		Иначе
			НоваяСтрока.Задолженность = - ОстатокРасчетов;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаРасчетов;
	
КонецФункции

#КонецОбласти

#Область НачислениеНДФЛ

// Описывает контекст, в котором выполняется расчет налога НФДЛ - организацию, период и т.п.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - налогоплательщик.
//  ПериодРасчета - Дата - дата из последнего квартала отчетного периода, за который рассчитывается налог.
//
// Возвращаемое значение:
//  Структура, Неопределено - см. НовыеПараметрыРасчетаНалога().
//  Неопределено - расчет не имеет смысла: в указанный период организация еще не зарегистрирована, или не применяет НДФЛ,
//                 или же налог в данном периоде не исчисляется в силу п. 2 статьи 55 НК РФ.
//
Функция НовыйКонтекстРасчетаНалога(Организация, ПериодРасчета) Экспорт
	
	Перем ДатаИзменения;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НачалоОтчетногоПериода = НачалоКвартала(ПериодРасчета);
	КонецОтчетногоПериода  = КонецКвартала(ПериодРасчета);
	
	ДатаРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ДатаРегистрации");
	
	НалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(Организация,
		КонецОтчетногоПериода,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВДекабре, ,
		ДатаРегистрации);
	
	НалоговыйПериодПропущен = УчетДоходовИРасходовПредпринимателя.НалоговыйПериодПропущен(
		Организация, КонецОтчетногоПериода, ДатаРегистрации);
	
	Если КонецОтчетногоПериода < НалоговыйПериод.Начало Или НалоговыйПериодПропущен Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПлательщикНДФЛ = УчетнаяПолитика.ПлательщикНДФЛ(Организация, КонецОтчетногоПериода, ДатаИзменения);
	
	Если Не ПлательщикНДФЛ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФормыОтчета = ФормаДекларацииПоУмолчанию(КонецОтчетногоПериода);
	
	КонтекстРасчета = НовыеПараметрыРасчетаНалога();
	
	КонтекстРасчета.Организация = Организация;
	КонтекстРасчета.Период = ПериодРасчета;
	КонтекстРасчета.ИмяФормыДекларации3НДФЛ = ИмяФормыОтчета;
	КонтекстРасчета.НачалоНалоговогоПериода = НалоговыйПериод.Начало;
	КонтекстРасчета.КонецОтчетногоПериода = КонецОтчетногоПериода;
	КонтекстРасчета.НачалоГода = НалоговыйПериод.Период;
	КонтекстРасчета.ПлательщикНДФЛ = ПлательщикНДФЛ;
	
	// Если организация зарегистрирована в текущем году, не имеет смысла рассчитывать налог с начала года.
	НачалоПериодаРасчета = Макс(НалоговыйПериод.Период, НалоговыйПериод.Начало);
	
	ЭтоПервыйОтчетныйПериод      = (НачалоОтчетногоПериода = НачалоКвартала(НачалоПериодаРасчета));
	ЭтоЗавершающийОтчетныйПериод = (КонецОтчетногоПериода = КонецКвартала(НалоговыйПериод.Конец));
	
	КонтекстРасчета.ЭтоПервыйОтчетныйПериод = ЭтоПервыйОтчетныйПериод;
	КонтекстРасчета.ЭтоЗавершающийОтчетныйПериод = ЭтоЗавершающийОтчетныйПериод;
	
	Возврат КонтекстРасчета;
	
КонецФункции

// Определяет сумму отраженных в бухгалтерском учете начислений по НДФЛ за указанный период.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - Налогоплательщик.
//  НачалоПериода - Дата - Дата и время начала анализируемого периода.
//  КонецПериода  - Дата - Дата и время окончания анализируемого периода.
//
// Возвращаемое значение:
//  Число - сумма уплаченного налога (авансовых платежей).
//
Функция НачисленныйНалогЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если КонецПериода < НачалоПериода
		Или Не УчетнаяПолитика.ПлательщикНДФЛЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
		Возврат 0;
	КонецЕсли;
	
	СчетОтраженияНалога = ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД; // 99.01.1
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // 68.21
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("НачалоПериода",         Новый Граница(НачалоПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецПериода",          Новый Граница(КонецПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СчетОтраженияНалога",   СчетОтраженияНалога);
	Запрос.УстановитьПараметр("СчетаНачисленияНалога", СчетаНачисленияНалога);
	Запрос.УстановитьПараметр("ВидыПлатежей",          Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот), 0) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			СчетДт = &СчетОтраженияНалога,
	|			,
	|			СчетКт В (&СчетаНачисленияНалога),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|			Организация = &Организация
	|				И СубконтоКт1 В (&ВидыПлатежей)) КАК ХозрасчетныйОборотыДтКт";
	
	Возврат Запрос.Выполнить().Выгрузить()[0].Сумма;
	
КонецФункции

// Возвращает таблицу отраженных в бухгалтерском учете (уплаченных) платежей по налогу НДФЛ за указанный период.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - Налогоплательщик.
//  НачалоПериода - Дата - Дата и время начала анализируемого периода.
//  КонецПериода  - Дата - Дата и время окончания анализируемого периода.
//
// Возвращаемое значение:
//  ТаблицаЗначений, Неопределено - документы авансовых платежей.
//  Неопределено - при неверных входящих параметрах.
//
Функция УплаченныйНалогЗаПериодПоДокументам(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если КонецПериода < НачалоПериода
		Или Не УчетнаяПолитика.ПлательщикНДФЛЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если КонецПериода < ЕдиныйНалоговыйСчет.НачалоПростогоУчета() Тогда
		Возврат УплаченныйНалогЗаПериодПоДокументамОплаты(Организация, НачалоПериода, КонецПериода);
	Иначе
		Возврат УплаченныйНалогЗаПериодПоДокументамВПростомУчетеЕНС(Организация, НачалоПериода, КонецПериода);
	КонецЕсли;
	
КонецФункции

// Рассчитывает налог НДФЛ по данным информационной базы и возвращает данные
// для отражения начисленного налога в проводках и во вспомогательных регистрах для справки-расчета.
//
// Параметры:
//  КонтекстРасчета - Структура - см. НовыйКонтекстРасчета().
//
// Возвращаемое значение:
//  Структура - содержит таблицы для отражения начисленного налога в бухучете и для формирования справки-расчета:
//     * ТаблицаПроводок       - ТаблицаЗначений - см. НалоговыйУчет.НоваяТаблицаНачисленияНалога()
//     * ТаблицаСправкиРасчета - ТаблицаЗначений - см. НоваяТаблицаСправкиРасчетаНалога()
//
Функция ПодготовитьТаблицыРасчетаНДФЛ(КонтекстРасчета) Экспорт
	
	ТаблицаПроводок       = НалоговыйУчет.НоваяТаблицаНачисленияНалога();
	ТаблицаСправкиРасчета = НоваяТаблицаСправкиРасчетаНалога();
	
	РассчитатьНДФЛ(КонтекстРасчета, ТаблицаПроводок, ТаблицаСправкиРасчета);
	
	РезультатРасчета = Новый Структура();
	РезультатРасчета.Вставить("ТаблицаПроводок",       ТаблицаПроводок);
	РезультатРасчета.Вставить("ТаблицаСправкиРасчета", ТаблицаСправкиРасчета);
	
	Возврат РезультатРасчета;
	
КонецФункции

// Выполняет запись движений в базу
//
// Параметры:
//   Проводки      - ТаблицаЗначений - Данные, необходимые для записи с РегистрБухгалтерии.Хозрасчетный
//   СправкаРасчет - ТаблицаЗначений - Данные, необходимые для записи в РегистрСведений.ИПРасчетНДФЛ
//   Реквизиты     - ТаблицаЗначений - Реквизиты регламентной операции
//   Движения      - КоллекцияДвижений - Движения регламентной операции
//
Процедура СформироватьДвиженияНачислениеНДФЛ(Проводки, СправкаРасчет, Реквизиты, Движения) Экспорт

	Если Не ЗначениеЗаполнено(Реквизиты) Тогда
		Возврат;
	КонецЕсли;

	Параметры = ПодготовитьПараметрыНачисленияНалога(Проводки, СправкаРасчет, Реквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	// Проводка по начислению налога.
	Для Каждого СтрокаПроводки Из Параметры.Проводки Цикл
	
		Проводка = Движения.Хозрасчетный.Добавить();
		
		ЗаполнитьЗначенияСвойств(Проводка, Реквизиты);
		ЗаполнитьЗначенияСвойств(Проводка, СтрокаПроводки);
		
		НалоговыйУчет.УстановитьСубконтоСчетаПоНачислениюНалога(СтрокаПроводки, Проводка.СчетДт, Проводка.СубконтоДт);
		НалоговыйУчет.УстановитьСубконтоСчетаПоНачислениюНалога(СтрокаПроводки, Проводка.СчетКт, Проводка.СубконтоКт);
		
		Движения.Хозрасчетный.Записывать = Истина;
		
	КонецЦикла;
	
	// Данные для справки-расчета.
	ЗакрытиеМесяца.ЗаписьВоВспомогательныеРегистрыСведений(Движения,
		Параметры.СправкаРасчет,
		Параметры.Реквизиты,
		"ИПРасчетНДФЛ");
	
КонецПроцедуры

#КонецОбласти

#Область СохранениеДанныхВычета

// Выполняет запись данных в регистр стандартных вычетов
//
// Параметры:
//   ПараметрыВычета - ТаблицаЗначений - Данные для записи в регистр.
//   РеквизитыЗаписи - Структура - см. УчетНДФЛПредпринимателяКлиентСервер.НовыеРеквизитыЗаписиВычета()
//
Процедура СохранитьСтандартныйВычетЗаПериод(ПараметрыВычета, РеквизитыЗаписи) Экспорт
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(РеквизитыЗаписи.ИмяОбъектаМетаданных);
	
	ВычетыЗаПериод = МенеджерОбъекта.ДанныеВычетаЗаПериод(
		РеквизитыЗаписи.Организация, РеквизитыЗаписи.Период, РеквизитыЗаписи.КонецПериода);
	
	ОтчетныйГод = Год(РеквизитыЗаписи.КонецПериода);
	
	// Удаляем строки за выбранный период
	Для Каждого ТекущийВычет Из ВычетыЗаПериод Цикл
		
		Если Год(ТекущийВычет.Период) < ОтчетныйГод Тогда
			Продолжить;
		КонецЕсли;
			
		МенеджерЗаписи = МенеджерОбъекта.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ТекущийВычет);
		МенеджерЗаписи.Удалить();
		
	КонецЦикла;
	
	ЕстьНормыГТО = РеквизитыЗаписи.Свойство("ПериодСдачиГТО");
	Если ЕстьНормыГТО Тогда
		ИсключаяСвойства = "СданыНормыГТО";
	Иначе
		ИсключаяСвойства = "";
	КонецЕсли;
	
	// Вычет не применяется
	Если Не ЗначениеЗаполнено(ПараметрыВычета) Тогда
		
		НоваяЗапись = МенеджерОбъекта.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, РеквизитыЗаписи, , ИсключаяСвойства);
		НоваяЗапись.Записать();
		
	Иначе
		
		// Запись переданных данных
		Для Каждого ТекущийВычет Из ПараметрыВычета Цикл
			
			НоваяЗапись = МенеджерОбъекта.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущийВычет);
			ЗаполнитьЗначенияСвойств(НоваяЗапись, РеквизитыЗаписи, , ИсключаяСвойства);
			НоваяЗапись.Период = Дата(ОтчетныйГод, ТекущийВычет.НомерМесяца, 1);
			
			НоваяЗапись.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЕстьНормыГТО Тогда
		Отбор = Новый Структура("Организация", РеквизитыЗаписи.Организация);
		ДействующийВычет = МенеджерОбъекта.СрезПоследних(РеквизитыЗаписи.ПериодСдачиГТО, Отбор);
		Если ДействующийВычет.Количество() > 0
			И ДействующийВычет[0].СданыНормыГТО <> РеквизитыЗаписи.СданыНормыГТО Тогда
			// Вычет ГТО применяется с того квартала, в котором был получен знак отличия
			// пп. 2.1 п. 1 ст. 218 НК РФ
			НоваяЗапись = МенеджерОбъекта.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, РеквизитыЗаписи);
			НоваяЗапись.Период = РеквизитыЗаписи.ПериодСдачиГТО;
			НоваяЗапись.ВидВычета = ДействующийВычет[0].ВидВычета;
			НоваяЗапись.Записать();
		КонецЕсли;
	КонецЕсли;
	
	// Данные являются первичными для расчета начислений по НДФЛ,
	// поэтому необходимо сбить регламентную операцию "Расчет НДФЛ ИП" за последний месяц
	СдвинутьГраницуАктуальностиРасчетНДФЛДляИП(РеквизитыЗаписи.Организация, РеквизитыЗаписи.КонецПериода);
	
КонецПроцедуры

#КонецОбласти

#Область РасчетСуммыВычета

// Конструктор параметров расчета суммы стандартного вычета.
//
// Возвращаемое значение:
//   Структура:
//     * Организация - СправочникСсылка.Организации - ИП, для которого нужно рассчитать вычет
//     * НачалоПериода - Дата - Дата начала периода расчета вычета
//     * КонецПериода - Дата - Дата окончания периода расчета вычета
//     * КоличествоМесяцев - Число - Количество месяцев применения вычета
//     * ВыбраннаяФорма - Строка - Имя формы регламентированного отчета для Декларации 3-НДФЛ
//     * ВычетЗаВесьПериод - Булево - Признак, определяющий применяется ли вычет весь период
//     * ИмяОбъектаМетаданных - Строка - Полное имя объекта метаданного (напр., РегистрСведений.ИПСтандартныеВычетыНаДетей)
//
Функция НовыйПараметрыВычета() Экспорт
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	СтруктураРезультат.Вставить("НачалоПериода", '00010101');
	СтруктураРезультат.Вставить("КонецПериода", '00010101');
	СтруктураРезультат.Вставить("КоличествоМесяцев", 0);
	СтруктураРезультат.Вставить("ВыбраннаяФорма", "");
	СтруктураРезультат.Вставить("ВычетЗаВесьПериод", Ложь);
	СтруктураРезультат.Вставить("ИмяОбъектаМетаданных", "");
	
	Возврат СтруктураРезультат;
	
КонецФункции

// Конструктор параметров расчета суммы стандартного вычета.
//
// Параметры:
//   Организация    - СправочникСсылка.Организации - Организация, для которой применяется вычет
//   НачалоПериода  - Дата - Дата начала периода расчета вычета
//   КонецПериода   - Дата - Конец периода расчета вычета
//   ВыбраннаяФорма - Строка - наименование формы отчета для Декларации 3-НДФЛ
//
// Возвращаемое значение:
//   Структура:
//     * Организация - СправочникСсылка.Организации - ИП, для которого нужно рассчитать доходы и расходы
//     * НачалоПериода - Дата - Дата начала периода расчета доходов и расходов ИП
//     * КонецПериода - Дата - Дата окончания периода расчета доходов и расходов ИП
//     * ВыбраннаяФорма - Строка - Форма отчета Декларация 3-НДФЛ
//     * УникальныйИдентификаторФормы - Строка - уникальный идентификатор формы
//
Функция НовыеПараметрыРасчетаДоходыИРасходыПредпринимателя(Организация, НачалоПериода, КонецПериода, ВыбраннаяФорма) Экспорт
	
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("Организация", Организация);
	ПараметрыРасчета.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыРасчета.Вставить("КонецПериода", КонецПериода);
	ПараметрыРасчета.Вставить("ВыбраннаяФорма", ВыбраннаяФорма);
	ПараметрыРасчета.Вставить("УникальныйИдентификаторФормы", "");
	
	Возврат ПараметрыРасчета;
	
КонецФункции

// Возвращает рассчитанные значения согласно КУДиР ИП за период:
//  - сумма доходов
//  - сумма расходов
//  - сумма предела для применения стандартного вычета на детей
//  - количество месяцев, для которых применяется стандартный вычет на детей
//
// Параметры:
//   ПараметрыВыполнения - Структура - см. НовыеПараметрыРасчетаДоходыИРасходыПредпринимателя()
//
// Возвращаемое значение:
//   Структура - см. НовыйРезультатРасчетаДоходыРасходыПредпринимателя()
//
Функция СведенияОДоходахИВычетах(ПараметрыВыполнения) Экспорт
	
	ДанныеРезультат = НовыйРезультатРасчетаДоходыРасходыПредпринимателя();
	
	Если ПараметрыВыполнения.НачалоПериода > ПараметрыВыполнения.КонецПериода Тогда 
		Возврат ДанныеРезультат;
	КонецЕсли;
	
	ТаблицаДоходовРасходов =
		УчетДоходовИРасходовПредпринимателя.ДоходыРасходыОтПредпринимательскойДеятельностиЗаПериод3НДФЛ(ПараметрыВыполнения);
	
	// Дополним таблицу месяцами, в которых у предпринимателя не было доходов и расходов
	МесяцНачало = 1;
	МесяцКонец  = Месяц(ПараметрыВыполнения.КонецПериода);
	ОтчетныйГод = Год(ПараметрыВыполнения.НачалоПериода);
	
	Для НомерМесяца = МесяцНачало По МесяцКонец Цикл
		
		Период = Дата(ОтчетныйГод, НомерМесяца, 1);
		Если ТаблицаДоходовРасходов.Найти(Период, "Период") = Неопределено Тогда
			НоваяСтрока = ТаблицаДоходовРасходов.Добавить();
			НоваяСтрока.Период = Период;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаДоходовРасходов.Сортировать("Период");
	
	// Рассчитаем количество месяцев вычета с учетом пределов
	ПределыВычетов = ПомощникЗаполнения3НДФЛ.ПределыВычетов(ПараметрыВыполнения.ВыбраннаяФорма,
		ПараметрыВыполнения.НачалоПериода);
	ДанныеРезультат.ПределВычетаНаДетейДоходы = ПределыВычетов.ВычетНаДетейДоходы;
	
	ОбщаяСуммаДоходов = 0;
	Для Каждого СтрокаДоходыРасходы Из ТаблицаДоходовРасходов Цикл
		
		ОбщаяСуммаДоходов = ОбщаяСуммаДоходов + СтрокаДоходыРасходы.ИтогоДоходов;
		ДанныеРезультат.Доходы  = ДанныеРезультат.Доходы + СтрокаДоходыРасходы.ИтогоДоходов;
		ДанныеРезультат.Расходы = ДанныеРезультат.Расходы + СтрокаДоходыРасходы.ИтогоРасходов;
		ДанныеРезультат.ПрочиеДоходы = ДанныеРезультат.ПрочиеДоходы + СтрокаДоходыРасходы.ПрочиеДоходы;
		
		Если ОбщаяСуммаДоходов < ПределыВычетов.ВычетНаДетейДоходы Тогда
			ДанныеРезультат.КоличествоМесяцевВычетНаДетей = ДанныеРезультат.КоличествоМесяцевВычетНаДетей + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеРезультат;
	
КонецФункции

// Возвращает таблицу с данными о вычетах помесячно за выбранный период.
//
// Параметры:
//   ПараметрыВыполнения - Структура - см. НовыйПараметрыВычета()
//
// Возвращаемое значение:
//   ТаблицаЗначений
//
Функция ДанныеВычетаПоМесяцамЗаПериод(ПараметрыВыполнения) Экспорт
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПараметрыВыполнения.ИмяОбъектаМетаданных);
	ДанныеВычетов = МенеджерОбъекта.ДанныеВычетаЗаПериод(ПараметрыВыполнения.Организация,
		ПараметрыВыполнения.НачалоПериода, ПараметрыВыполнения.КонецПериода);
	
	ДанныеВычетов.Колонки.Добавить("СуммаВычета", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ДанныеВычетов.Колонки.Добавить("Месяц", ОбщегоНазначения.ОписаниеТипаСтрока(8));  // Представление месяца
	
	Если ЗначениеЗаполнено(ПараметрыВыполнения.ВыбраннаяФорма) Тогда
		ВыбраннаяФорма = ПараметрыВыполнения.ВыбраннаяФорма;
	Иначе
		ВыбраннаяФорма = ФормаДекларацииПоУмолчанию(ПараметрыВыполнения.НачалоПериода);
	КонецЕсли;
	
	РазмерыВычетов = ПомощникЗаполнения3НДФЛ.РазмерыВычетов(ВыбраннаяФорма, ПараметрыВыполнения.НачалоПериода);
	
	МесяцНачало = 1;
	МесяцКонец  = Месяц(ПараметрыВыполнения.КонецПериода);
	
	// Добавим недостающие периоды в таблицу по действующим данным
	ДобавитьНедостающиеПериодыВТаблицу(ДанныеВычетов, МесяцКонец, Год(ПараметрыВыполнения.КонецПериода));
	
	// Удалим строку с данными предыдущего отчетного периода
	СтрокаПредыдущийПериод = ДанныеВычетов.Найти(0, "НомерМесяца");
	Если ЗначениеЗаполнено(ДанныеВычетов) И СтрокаПредыдущийПериод <> Неопределено Тогда
		ДанныеВычетов.Удалить(СтрокаПредыдущийПериод);
	КонецЕсли;
	
	// Отсортируем данные по порядку
	ДанныеВычетов.Сортировать("НомерМесяца");
	
	Если ПараметрыВыполнения.ВычетЗаВесьПериод Тогда
		КоличествоМесяцев = МесяцКонец - МесяцНачало + 1;
	Иначе
		КоличествоМесяцев = ПараметрыВыполнения.КоличествоМесяцев;
	КонецЕсли;
	
	// Расчет суммы вычета с учетом количества месяцев к применению
	Для Каждого ТекущаяСтрока Из ДанныеВычетов Цикл
		
		Если ТекущаяСтрока.НомерМесяца <= КоличествоМесяцев Тогда
			ТекущаяСтрока.СуммаВычета = МенеджерОбъекта.РазмерТекущегоВычета(ТекущаяСтрока, РазмерыВычетов);
		КонецЕсли;
		
		ТекущаяСтрока.Месяц = ОбщегоНазначенияБПКлиентСервер.ПредставлениеМесяца(ТекущаяСтрока.НомерМесяца);
		
	КонецЦикла;
	
	Возврат ДанныеВычетов;
	
КонецФункции

// Возвращает сумму вычета за выбранный период.
//
// Параметры:
//   ПараметрыВыполнения - Структура - см. НовыйПараметрыВычета()
//
// Возвращаемое значение:
//   Число
//
Функция СуммаСтандартногоВычета(ПараметрыВыполнения) Экспорт
	
	СуммаВычета   = 0;
	ТаблицаДанных = ДанныеВычетаПоМесяцамЗаПериод(ПараметрыВыполнения);
	
	Если ЗначениеЗаполнено(ТаблицаДанных) Тогда
		
		СданыНормыГТО = Ложь;
		УчитываетсяВычетЗаСдачуНормГТО = УчетНДФЛПредпринимателяКлиентСервер.УчитываетсяВычетЗаСдачуНормГТО(
			ПараметрыВыполнения.ИмяОбъектаМетаданных);
		
		Для Каждого ТекущийМесяц Из ТаблицаДанных Цикл
			СуммаВычета = СуммаВычета + ТекущийМесяц.СуммаВычета;
			Если УчитываетсяВычетЗаСдачуНормГТО И Не СданыНормыГТО Тогда
				СданыНормыГТО = ТекущийМесяц.СданыНормыГТО;
			КонецЕсли;
		КонецЦикла;
		
		Если УчитываетсяВычетЗаСдачуНормГТО И СданыНормыГТО Тогда
			РазмерыВычетов = ПомощникЗаполнения3НДФЛ.РазмерыВычетов(ПараметрыВыполнения.ВыбраннаяФорма);
			СуммаВычета = СуммаВычета + ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				РазмерыВычетов, "СтандартныйВычетЗаСдачуНормГТО", 0);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СуммаВычета;
	
КонецФункции

// Рассчитывает сумму профессионального вычета, на который имеет право предприниматель
//
// Параметры:
//   СуммаРасходов                - Число - Сумма подтвержденных расходов по данным КУДиР ИП
//   СуммаДоходов                 - Число - Сумма подтвержденных доходов по данным КУДиР ИП
//   ВыбраннаяФорма               - Строка - Имя действующей формы декларации 3-НДФЛ на период расчета налога
//   ЭтоЗавершающийОтчетныйПериод - Булево - Признак, указывающий, является ли период расчета налога завершающим в налоговом периоде
//
// Возвращаемое значение:
//   Число
//
Функция СуммаПрофессиональногоВычета(СуммаРасходов, СуммаДоходов, ВыбраннаяФорма, ЭтоЗавершающийОтчетныйПериод) Экспорт
	
	Если ЭтоЗавершающийОтчетныйПериод И СуммаРасходов = 0 Тогда
		// Абз. 4 п. 1 ст. 221 НК РФ
		// Если ИП не в состоянии документально подтвердить свои расходы,
		// профессиональный налоговый вычет производится в размере 20% общей суммы доходов
		КоэффициентВычета = ПомощникЗаполнения3НДФЛ.ПроцентПрофессиональногоВычета(ВыбраннаяФорма) / 100;
		Результат = СуммаДоходов * КоэффициентВычета;
	Иначе
		Результат = СуммаРасходов;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НовыеПараметрыРасчетаСуммыПоВидамНалога() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Период", '00010101');
	Результат.Вставить("НалогИсчисленныйВсего", 0);
	Результат.Вставить("АвансовыеПлатежи", 0);
	Результат.Вставить("НалогКУплате", 0);
	Результат.Вставить("НалоговаяБаза", 0);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйТаблицаПлатежейНДФЛ() Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ВидНалога", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНалогов"));
	Результат.Колонки.Добавить("КодВидаДоходов", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	Результат.Колонки.Добавить("НалоговаяБаза", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	Результат.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	Результат.Колонки.Добавить("СтавкаНалога", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДеньУплатыАвансовогоПлатежаПоУмолчанию()
	
	Возврат 25;
	
КонецФункции

Процедура ДобавитьНедостающиеПериодыВТаблицу(ДанныеВычетов, КоличествоМесяцев, ОтчетныйГод)
	
	// Если в таблице вычетов не добавлены все строки по месяцам,
	// тогда следует добавить значения, исходя из данных действующих периодов
	Если КоличествоМесяцев >= ДанныеВычетов.Количество() Тогда
		
		Для Каждого ТекущаяСтрока Из ДанныеВычетов Цикл
			
			НомерМесяцаСледующий = ТекущаяСтрока.НомерМесяца + 1;
			// Проверим, есть ли в таблице месяц, следующий за текущим:
			// если нет, тогда добавим строку в таблицу вычетов, исходя их данных текущей строки
			Если НомерМесяцаСледующий <= КоличествоМесяцев
				И ДанныеВычетов.Найти(НомерМесяцаСледующий, "НомерМесяца") = Неопределено Тогда
				
				НоваяСтрока = ДанныеВычетов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
				НоваяСтрока.Период = Дата(ОтчетныйГод, НомерМесяцаСледующий, 01);
				НоваяСтрока.НомерМесяца = НомерМесяцаСледующий;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция НоваяТаблицаРасчетовПоНалогу()
	
	ТаблицаРасчетов = Новый ТаблицаЗначений;
	
	ТаблицаРасчетов.Колонки.Добавить("НалоговыйПериод", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаРасчетов.Колонки.Добавить("Начислено", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРасчетов.Колонки.Добавить("Уплачено", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРасчетов.Колонки.Добавить("Переплата", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРасчетов.Колонки.Добавить("Задолженность", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Возврат ТаблицаРасчетов;
	
КонецФункции

Функция НоваяТаблицаСправкиРасчетаНалога()
	
	ТипСтандартнаяСумма                = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	ТипСтандартнаяСуммаНеотрицательная = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2, ДопустимыйЗнак.Неотрицательный);
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("ПериодРасчета", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Результат.Колонки.Добавить("СтавкаНалога",  ОбщегоНазначения.ОписаниеТипаЧисло(5, 2, ДопустимыйЗнак.Неотрицательный));
	Результат.Колонки.Добавить("Доходы",                ТипСтандартнаяСумма);
	Результат.Колонки.Добавить("ВидВычета",             Новый ОписаниеТипов("ПеречислениеСсылка.ВычетыФизическихЛиц"));
	Результат.Колонки.Добавить("СуммаВычета",           ТипСтандартнаяСумма);
	Результат.Колонки.Добавить("НалоговаяБаза",         ТипСтандартнаяСуммаНеотрицательная);
	Результат.Колонки.Добавить("НалогИсчисленныйВсего", ТипСтандартнаяСумма);
	Результат.Колонки.Добавить("АвансовыеПлатежи",      ТипСтандартнаяСуммаНеотрицательная);
	Результат.Колонки.Добавить("НалогКУплате",          ТипСтандартнаяСумма);
	Результат.Колонки.Добавить("Переплата",             ТипСтандартнаяСумма);
	
	Возврат Результат;
	
КонецФункции

// Возвращает параметры расчета для налоговой базы
//
// Возвращаемое значение:
//   Структура:
//        * Доходы - Число - Доходы ИП
//        * Расходы - Число - Расходы ИП
//        * КоличествоМесяцевВычетНаДетей - Число - Количество месяцев применения вычета на детей
//        * ПределВычетаНаДетейДоходы - Число - Предельная сумма дохода для применения вычета на детей
//        * ПрочиеДоходы - Число - Сумма прочих расходов ИП
//
Функция НовыйРезультатРасчетаДоходыРасходыПредпринимателя()
	
	ДанныеРезультат = Новый Структура;
	ДанныеРезультат.Вставить("Доходы", 0);
	ДанныеРезультат.Вставить("Расходы", 0);
	ДанныеРезультат.Вставить("КоличествоМесяцевВычетНаДетей", 0);
	ДанныеРезультат.Вставить("ПределВычетаНаДетейДоходы", 0);
	ДанныеРезультат.Вставить("ПрочиеДоходы", 0);
	
	Возврат ДанныеРезультат;
	
КонецФункции

Функция ПодготовитьПараметрыНачисленияНалога(ТаблицаПроводок, ТаблицаСправкиРасчета, ТаблицаРеквизитов)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	Разделитель = ",";
	
	СписокОбязательныхКолонок = Новый Массив;
	СписокОбязательныхКолонок.Добавить("Период");      // <Дата> - период движений - дата документа
	СписокОбязательныхКолонок.Добавить("Регистратор"); // <ДокументСсылка.РегламентнаяОперация> - документ-регистратор движений
	СписокОбязательныхКолонок.Добавить("Организация"); // <СправочникСсылка.Организации> - организация
		
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов, СтрСоединить(СписокОбязательныхКолонок, Разделитель)));
		
	// Подготовка таблицы Параметры.Проводки
	
	СписокОбязательныхКолонок = Новый Массив;
	СписокОбязательныхКолонок.Добавить("СчетДт");                 // <ПланСчетовСсылка.Хозрасчетный> - счет дебета
	СписокОбязательныхКолонок.Добавить("СчетКт");                 // <ПланСчетовСсылка.Хозрасчетный> - счет кредита
	СписокОбязательныхКолонок.Добавить("ПрибылиИУбытки");         // <ПеречислениеСсылка.ПрибылиИУбытки> - субконто счета дебета
	СписокОбязательныхКолонок.Добавить("ВидыПлатежейВГосБюджет"); // <ПеречислениеСсылка.ВидыПлатежейВГосБюджет> - субконто кредита
	СписокОбязательныхКолонок.Добавить("Сумма");                  // <Число, 15, 2> - сумма проводки
	СписокОбязательныхКолонок.Добавить("Содержание");             // <Строка, 150> - содержание проводки
		
	Параметры.Вставить("Проводки", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПроводок, СтрСоединить(СписокОбязательныхКолонок, Разделитель)));
		
	// Подготовка таблицы Параметры.СправкаРасчет
	
	СписокОбязательныхКолонок = Новый Массив;
	СписокОбязательныхКолонок.Добавить("ПериодРасчета");         // <Дата> - период расчета (конец квартала)
	СписокОбязательныхКолонок.Добавить("Доходы");                // <Число, 15, 2> - сумма доходов за период (с начала года)
	СписокОбязательныхКолонок.Добавить("ВидВычета");             // <ПеречислениеСсылка.ВычетыФизическихЛиц>
	СписокОбязательныхКолонок.Добавить("СуммаВычета");           // <Число, 15, 2> - сумма расходов за период (с начала года)
	СписокОбязательныхКолонок.Добавить("НалоговаяБаза");         // <Число, 15, 2> - налоговая база с начала года
	СписокОбязательныхКолонок.Добавить("СтавкаНалога");          // <Число,  5, 2> - ставка налога НДФЛ
	СписокОбязательныхКолонок.Добавить("НалогИсчисленныйВсего"); // <Число, 15, 2> - сумма фактически исчисленного налога с начала года по текущий квартал включительно
	СписокОбязательныхКолонок.Добавить("АвансовыеПлатежи");      // <Число, 15, 2> - сумма исчисленных в предыдущих кварталах (с начала года) авансовых платежей по налогу
	СписокОбязательныхКолонок.Добавить("НалогКУплате");          // <Число, 15, 2> - сумма налога (авансового платежа), начисленного к уплате в текущем периоде
	СписокОбязательныхКолонок.Добавить("Переплата");             // <Число, 15, 2> - сумма налога (авансового платежа), начисленного к уплате в текущем периоде
	
	Параметры.Вставить("СправкаРасчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСправкиРасчета, СтрСоединить(СписокОбязательныхКолонок, Разделитель)));
	
	Возврат Параметры;

КонецФункции

Процедура РассчитатьНДФЛ(КонтекстРасчета, ТаблицаПроводок, ТаблицаСправкиРасчета)
	
	Если КонтекстРасчета.Период < ДатаНачалаРасчетаАвансовыхПлатежейНДФЛ() Тогда
		Возврат;
	КонецЕсли;
	
	// Расчет доходов и расходов
	
	ПараметрыРасчета = НовыеПараметрыРасчетаДоходыИРасходыПредпринимателя(
		КонтекстРасчета.Организация,
		КонтекстРасчета.НачалоНалоговогоПериода,
		КонтекстРасчета.КонецОтчетногоПериода,
		КонтекстРасчета.ИмяФормыДекларации3НДФЛ);
	
	ДоходыРасходы = СведенияОДоходахИВычетах(ПараметрыРасчета);
	Доходы = ДоходыРасходы.Доходы;
	
	// Стандартные вычеты
	
	ПараметрыВычета = НовыйПараметрыВычета();
	ПараметрыВычета.НачалоПериода     = КонтекстРасчета.НачалоНалоговогоПериода;
	ПараметрыВычета.КонецПериода      = КонтекстРасчета.КонецОтчетногоПериода;
	ПараметрыВычета.Организация       = КонтекстРасчета.Организация;
	ПараметрыВычета.ВыбраннаяФорма    = КонтекстРасчета.ИмяФормыДекларации3НДФЛ;
	ПараметрыВычета.ВычетЗаВесьПериод = Истина;
	ПараметрыВычета.ИмяОбъектаМетаданных = "РегистрСведений.ИПСтандартныеВычетыНаНалогоплательщика";
	
	СтандартныйВычетНаНалогоплательщика = СуммаСтандартногоВычета(ПараметрыВычета);
	
	ПараметрыВычета.ИмяОбъектаМетаданных = "РегистрСведений.ИПСтандартныеВычетыНаДетей";
	ПараметрыВычета.КоличествоМесяцев = ДоходыРасходы.КоличествоМесяцевВычетНаДетей;
	ПараметрыВычета.ВычетЗаВесьПериод = Ложь;
	СтандартныйВычетНаДетей = СуммаСтандартногоВычета(ПараметрыВычета);
	
	СтандартныйВычет = СтандартныйВычетНаДетей + СтандартныйВычетНаНалогоплательщика;
	
	// Расчет налога
	
	Если Не КонтекстРасчета.ЭтоПервыйОтчетныйПериод Тогда
		
		КонецПредыдущегоКвартала = НачалоКвартала(КонтекстРасчета.КонецОтчетногоПериода) - 1;
		ИсчисленоЗаПредыдущиеПериоды = НачисленныйНалогЗаПериод(КонтекстРасчета.Организация,
			КонтекстРасчета.НачалоНалоговогоПериода, КонецПредыдущегоКвартала);
		
	Иначе
		
		КонецПредыдущегоКвартала = '00010101';
		ИсчисленоЗаПредыдущиеПериоды = 0;
		
	КонецЕсли;
	
	ПрофессиональныйВычет = СуммаПрофессиональногоВычета(ДоходыРасходы.Расходы,
		ДоходыРасходы.Доходы,
		КонтекстРасчета.ИмяФормыДекларации3НДФЛ,
		КонтекстРасчета.ЭтоЗавершающийОтчетныйПериод);
	
	НалоговаяБаза = Макс(Доходы - ПрофессиональныйВычет - СтандартныйВычет, 0);
	НалогСНачалаГодаВсего = СуммаНалога(НалоговаяБаза, КонтекстРасчета.Период);
	
	ПараметрыНачисления = НовыйПараметрыОтраженияНачисленийПоНДФЛ();
	ЗаполнитьЗначенияСвойств(ПараметрыНачисления, КонтекстРасчета);
	ПараметрыНачисления.НалогИсчисленныйВсего = НалогСНачалаГодаВсего;
	ПараметрыНачисления.НалоговаяБаза = НалоговаяБаза;
	ПараметрыНачисления.АвансовыеПлатежи = ИсчисленоЗаПредыдущиеПериоды;
	ПараметрыНачисления.Доходы = Доходы;
	ПараметрыНачисления.КонецПредыдущегоКвартала = КонецПредыдущегоКвартала;
	
	Если УчетНДФЛПредпринимателяКлиентСервер.ПрименяетсяРасширеннаяПрогрессивнаяйШкала(КонтекстРасчета.Период) Тогда
		НалогКУплатеВсего = РассчитатьНДФЛпоРасширеннойШкале(ПараметрыНачисления,
			ТаблицаПроводок,
			ТаблицаСправкиРасчета);
	Иначе
		НалогКУплатеВсего = РассчитатьНДФЛпоПлоскойШкале(ПараметрыНачисления, ТаблицаПроводок, ТаблицаСправкиРасчета);
	КонецЕсли;
	
	// Запись справки-расчета по вычетам
	
	СтрокаСправки = ТаблицаСправкиРасчета.Добавить();
	СтрокаСправки.ПериодРасчета = КонтекстРасчета.Период;
	СтрокаСправки.ВидВычета     = Перечисления.ВычетыФизическихЛиц.Профессиональный;
	СтрокаСправки.СуммаВычета   = ПрофессиональныйВычет;
	
	СтрокаСправки = ТаблицаСправкиРасчета.Добавить();
	СтрокаСправки.ПериодРасчета = КонтекстРасчета.Период;
	СтрокаСправки.ВидВычета     = Перечисления.ВычетыФизическихЛиц.СтандартныйНаДетей;
	СтрокаСправки.СуммаВычета   = СтандартныйВычетНаДетей;
	
	СтрокаСправки = ТаблицаСправкиРасчета.Добавить();
	СтрокаСправки.ПериодРасчета = КонтекстРасчета.Период;
	СтрокаСправки.ВидВычета     = Перечисления.ВычетыФизическихЛиц.СтандартныйНаНалогоплательщика;
	СтрокаСправки.СуммаВычета   = СтандартныйВычетНаНалогоплательщика;
	
	// Если организация попадает под условия освобождения уплаты налога, тогда делаем дополнительную проводку
	Если НалогКУплатеВсего > 0 И ПрименяетсяОсвобождениеОтНалога(КонтекстРасчета.Организация, КонтекстРасчета.Период) Тогда
		
		НалоговыйУчет.ДополнитьПроводкамиПоОсвобождениюОтНалога(
			ТаблицаПроводок,
			КонтекстРасчета.Период,
			ПланыСчетов.Хозрасчетный.НДФЛ_ИП,
			НалогКУплатеВсего);
		
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйПараметрыОтраженияНачисленийПоНДФЛ()
	
	Результат = Новый Структура;
	Результат.Вставить("Период", '00010101');
	Результат.Вставить("НачалоНалоговогоПериода", '00010101');
	Результат.Вставить("КонецОтчетногоПериода", '00010101');
	Результат.Вставить("НачалоГода", '00010101');
	Результат.Вставить("Счет");
	Результат.Вставить("ЭтоЗавершающийОтчетныйПериод", Ложь);
	Результат.Вставить("СтавкаНалога", 0);
	Результат.Вставить("НалогИсчисленныйВсего", 0);
	Результат.Вставить("АвансовыеПлатежи", 0);
	Результат.Вставить("Доходы", 0);
	Результат.Вставить("НалоговаяБаза", 0);
	Результат.Вставить("Сумма", 0);
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("КонецПредыдущегоКвартала", '00010101');
	Результат.Вставить("ЭтоПовышеннаяСтавка", Ложь);
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьПроводкиПоНачислениюНалога(ТаблицаПроводок, ПараметрыНачисления, ТаблицаСправкиРасчета = Неопределено)
	
	СчетОтраженияНалога = ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД; // Дт 99.01.1
	
	СтрокаПроводки = ТаблицаПроводок.Добавить();
	
	СтрокаПроводки.СчетДт                 = СчетОтраженияНалога;
	СтрокаПроводки.СчетКт                 = ПараметрыНачисления.Счет;
	СтрокаПроводки.ПрибылиИУбытки         = Перечисления.ПрибылиИУбытки.НалогНаПрибыль;
	СтрокаПроводки.ВидыПлатежейВГосБюджет = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	СтрокаПроводки.Сумма                  = ПараметрыНачисления.Сумма;
	СтрокаПроводки.Содержание             = СодержаниеПроводкиНачислениеНалога(ПараметрыНачисления);
	
	Если ТаблицаСправкиРасчета <> Неопределено Тогда
		СтрокаСправки = ТаблицаСправкиРасчета.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСправки, ПараметрыНачисления);
		
		СтрокаСправки.ПериодРасчета = ПараметрыНачисления.Период;
		
		Если ПараметрыНачисления.Сумма > 0 Тогда
			СтрокаСправки.НалогКУплате = ПараметрыНачисления.Сумма;
		Иначе
			СтрокаСправки.Переплата = -ПараметрыНачисления.Сумма;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Сдвигает границу актуальности операции "Расчет НДФЛ для ИП"
//
// Параметры:
//   Организация - СправочникСсылка.Организации - Организация, для которой необходимо сдвинуть границу актуальности
//   Период      - Дата - Период операции
//
Процедура СдвинутьГраницуАктуальностиРасчетНДФЛДляИП(Организация, Период)
	
	УстаревшаяРегламентнаяОперация = Перечисления.ВидыРегламентныхОпераций.РасчетНДФЛДляИП;
	НомерГруппы = Перечисления.ВидыРегламентныхОпераций.НомерГруппы(УстаревшаяРегламентнаяОперация);
	
	РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
		Организация, Период, НомерГруппы, УстаревшаяРегламентнаяОперация, Истина);
	
КонецПроцедуры

Функция СодержаниеПроводкиНачислениеНалога(Реквизиты)
	
	Содержание = "";
	
	ПредставлениеПериода = ПредставлениеПериода(Реквизиты.НачалоГода, Реквизиты.КонецОтчетногоПериода, "ФП=Истина");
	
	Если Реквизиты.ЭтоЗавершающийОтчетныйПериод Тогда
		Содержание = СтрШаблон(НСтр("ru = 'Начислен налог на доходы физических лиц за %1'"), ПредставлениеПериода);
	ИначеЕсли Реквизиты.ЭтоПовышеннаяСтавка Тогда
		Содержание = СтрШаблон(НСтр("ru = 'Начислен авансовый платеж по налогу на доходы физических лиц за %1, исчисленный с налоговой базы свыше %2'"),
			ПредставлениеПериода,
			ПредельнаяСуммаДоходаПоБазовойСтавке(Реквизиты.НачалоГода));
	Иначе
		Содержание = СтрШаблон(НСтр("ru = 'Начислен авансовый платеж по налогу на доходы физических лиц за %1'"), 
			ПредставлениеПериода);
	КонецЕсли;
	
	Возврат Содержание;
	
КонецФункции

// Возвращает параметры расчета для налоговой базы
//
// Возвращаемое значение:
//  Структура:
//        * СтавкаНалога - Число - Ставка налога для налоговой базы.
//        * МинимальнаяСуммаДохода - Число - Минимальная граница налоговой базы, для которой действует ставка.
//        * МаксимальнаяСуммаДохода - Число - Максимальная граница налоговой базы, для которой действует ставка.
//        * БазовыйНалог - Число - Базовый налог по минимальной границе налоговой базы.
//
Функция НовыеПараметрыРасчетаНалоговойБазы()
	
	Результат = Новый Структура;
	Результат.Вставить("СтавкаНалога", 0); // 13, 15, 18, 20, 22
	Результат.Вставить("МинимальнаяСуммаДохода", 0); // Например, 2400000 для ставки 15%
	Результат.Вставить("МаксимальнаяСуммаДохода", 0); // Например, 5000000 для ставки 15%
	Результат.Вставить("БазовыйНалог", 0); // Например, 320000 для ставки 15%
	
	Возврат Результат;
	
КонецФункции

// Возвращает параметры расчета налога
//
// Возвращаемое значение:
//  Структура:
//        * Организация - СправочникСсылка.Организации - Ссылка на Ип, для которого нужно выполнить расчет налога.
//        * Период - Дата - Период, за который нужно рассчитать налог.
//        * ИмяФормыДекларации3НДФЛ - Строка - Имя регламентированной формы декларации.
//        * НачалоНалоговогоПериода - Дата - Начало налогового периода.
//        * КонецОтчетногоПериода - Дата - Конец налогового периода.
//        * НачалоГода - Дата - Начало года периода или начало ведения деятельности в текущем периоде.
//        * ПлательщикНДФЛ - Булево - Признак, что Ип является плательщиком НДФЛ в данном периоде.
//        * ЭтоПервыйОтчетныйПериод - Булево - Признак, что период является первым расчетным кварталом в налоговом периоде.
//        * ЭтоЗавершающийОтчетныйПериод - Булево - 4 квартал или последний квартал, в котором ИП работал на ОСНО.
//
Функция НовыеПараметрыРасчетаНалога()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("Период", '00010101');
	Результат.Вставить("ИмяФормыДекларации3НДФЛ", "");
	Результат.Вставить("НачалоНалоговогоПериода", '00010101');
	Результат.Вставить("КонецОтчетногоПериода",  '00010101');
	Результат.Вставить("НачалоГода", '00010101');
	Результат.Вставить("ПлательщикНДФЛ", Ложь);
	Результат.Вставить("ЭтоПервыйОтчетныйПериод", Ложь);
	Результат.Вставить("ЭтоЗавершающийОтчетныйПериод", Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция РассчитатьНДФЛпоПлоскойШкале(КонтекстРасчета, ТаблицаПроводок, ТаблицаСправкиРасчета)
	
	СчетНачисленияНалога = ПланыСчетов.Хозрасчетный.НДФЛ_ИП;
	НалогСНачалаГодаВсего = СуммаНалога(КонтекстРасчета.НалоговаяБаза, КонтекстРасчета.Период);
	
	НалогКУплате = 0;
	НалогКУплатеСвышеПредела = 0;
	
	НалоговыеСтавки = НалоговыеСтавки(КонтекстРасчета.Период);
	ПараметрыРасчета = ПараметрыРасчетаНалоговойБазы(НалоговыеСтавки, КонтекстРасчета.НалоговаяБаза);
	БазоваяСтавка = МинимальнаяСтавка(КонтекстРасчета.Период, НалоговыеСтавки);
	
	ПараметрыРасчетаБазоваяСтавка = ПараметрыРасчетаНалоговойБазы(НалоговыеСтавки, 0);
	МаксимальнаяСуммаДоходаПоБазовойСтавке = ПараметрыРасчетаБазоваяСтавка.МаксимальнаяСуммаДохода;
	
	ПрименяетсяПовышеннаяСтавка = ПараметрыРасчета.СтавкаНалога <> БазоваяСтавка;
	
	ПараметрыНачисления = НовыйПараметрыОтраженияНачисленийПоНДФЛ();
	ЗаполнитьЗначенияСвойств(ПараметрыНачисления, КонтекстРасчета);
	ПараметрыНачисления.НалогИсчисленныйВсего = НалогСНачалаГодаВсего;
	
	Если ПрименяетсяПовышеннаяСтавка Тогда
		
		БазовыйНалог = МаксимальнаяСуммаДоходаПоБазовойСтавке * ПараметрыРасчетаБазоваяСтавка.СтавкаНалога/100;
		НалогКУплате = Макс(БазовыйНалог - КонтекстРасчета.АвансовыеПлатежи, 0);
		НалогКУплатеСвышеПредела = Макс(НалогСНачалаГодаВсего - КонтекстРасчета.АвансовыеПлатежи - НалогКУплате, 0);
		
	Иначе
		
		НалогКУплате = Окр(НалогСНачалаГодаВсего - КонтекстРасчета.АвансовыеПлатежи, 0);
		
	КонецЕсли;
	
	// Данные для проводки по начислению налога по базовой ставке
	Если ЗначениеЗаполнено(НалогКУплате) Тогда
		ПараметрыНачисления.Сумма = НалогКУплате;
		ПараметрыНачисления.Счет = СчетНачисленияНалога;
		ПараметрыНачисления.СтавкаНалога = БазоваяСтавка;
		ПараметрыНачисления.ЭтоПовышеннаяСтавка = Ложь;
		Если КонтекстРасчета.НалоговаяБаза > ПараметрыРасчетаБазоваяСтавка.МаксимальнаяСуммаДохода
			И ПараметрыРасчетаБазоваяСтавка.МаксимальнаяСуммаДохода > 0 Тогда
			
			ПараметрыНачисления.НалоговаяБаза = ПараметрыРасчетаБазоваяСтавка.МаксимальнаяСуммаДохода;
		Иначе
			ПараметрыНачисления.НалоговаяБаза = КонтекстРасчета.НалоговаяБаза;
		КонецЕсли;
		ДобавитьПроводкиПоНачислениюНалога(ТаблицаПроводок, ПараметрыНачисления, ТаблицаСправкиРасчета);
	КонецЕсли;
	
	// Данные для проводки по начислению налога по повышенным ставкам
	Если ЗначениеЗаполнено(НалогКУплатеСвышеПредела) Тогда
		ПараметрыНачисления.Сумма = НалогКУплатеСвышеПредела;
		ПараметрыНачисления.Счет = СчетНачисленияНалога;
		ПараметрыНачисления.СтавкаНалога = ПараметрыРасчета.СтавкаНалога;
		ПараметрыНачисления.НалоговаяБаза = КонтекстРасчета.НалоговаяБаза - ПараметрыРасчета.МинимальнаяСуммаДохода;
		ПараметрыНачисления.ЭтоПовышеннаяСтавка = Истина;
		ДобавитьПроводкиПоНачислениюНалога(ТаблицаПроводок, ПараметрыНачисления, ТаблицаСправкиРасчета);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТаблицаСправкиРасчета) Тогда
		
		Если НалогСНачалаГодаВсего < КонтекстРасчета.АвансовыеПлатежи Тогда
			// Запишем сумму переплаты, которую уплатили в прошлом квартале
			ПараметрыНачисления.Сумма = НалогКУплатеСвышеПредела;
			ПараметрыНачисления.Счет = СчетНачисленияНалога;
			ПараметрыНачисления.СтавкаНалога = ПараметрыРасчета.СтавкаНалога;
			ПараметрыНачисления.НалоговаяБаза = КонтекстРасчета.НалоговаяБаза - ПараметрыРасчета.МинимальнаяСуммаДохода;
			ПараметрыНачисления.Сумма = НалогСНачалаГодаВсего - КонтекстРасчета.АвансовыеПлатежи;
			ПараметрыНачисления.ЭтоПовышеннаяСтавка = ПараметрыРасчета.БазовыйНалог > 0;
			ДобавитьПроводкиПоНачислениюНалога(ТаблицаПроводок, ПараметрыНачисления, ТаблицаСправкиРасчета);
		Иначе
			СтрокаСправки = ТаблицаСправкиРасчета.Добавить();
			СтрокаСправки.ПериодРасчета = КонтекстРасчета.Период;
			ЗаполнитьЗначенияСвойств(СтрокаСправки, ПараметрыНачисления);
			СтрокаСправки.СтавкаНалога = ПараметрыРасчета.СтавкаНалога;
			СтрокаСправки.НалоговаяБаза = КонтекстРасчета.НалоговаяБаза;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НалогКУплате + НалогКУплатеСвышеПредела;
	
КонецФункции

Функция РассчитатьНДФЛпоРасширеннойШкале(КонтекстРасчета, ТаблицаПроводок, ТаблицаСправкиРасчета)
	
	НалогСНачалаГодаВсего = СуммаНалога(КонтекстРасчета.НалоговаяБаза, КонтекстРасчета.Период);
	НалоговыеСтавки = НалоговыеСтавки(КонтекстРасчета.Период);
	ПараметрыРасчета = ПараметрыРасчетаНалоговойБазы(НалоговыеСтавки, КонтекстРасчета.НалоговаяБаза);
	
	ПараметрыНачисления = НовыйПараметрыОтраженияНачисленийПоНДФЛ();
	ЗаполнитьЗначенияСвойств(ПараметрыНачисления, КонтекстРасчета);
	ПараметрыНачисления.НалогИсчисленныйВсего = НалогСНачалаГодаВсего;
	
	ПараметрыНачисления.Сумма = НалогСНачалаГодаВсего - КонтекстРасчета.АвансовыеПлатежи;
	ПараметрыНачисления.Счет = ПланыСчетов.Хозрасчетный.НДФЛ_ИП;
	ПараметрыНачисления.СтавкаНалога = ПараметрыРасчета.СтавкаНалога;
	ПараметрыНачисления.НалоговаяБаза = КонтекстРасчета.НалоговаяБаза - ПараметрыРасчета.МинимальнаяСуммаДохода;
	ДобавитьПроводкиПоНачислениюНалога(ТаблицаПроводок, ПараметрыНачисления);
	
	ПараметрыРасчетаДляСправки = НовыеПараметрыРасчетаСуммыПоВидамНалога();
	ЗаполнитьЗначенияСвойств(ПараметрыРасчетаДляСправки, КонтекстРасчета);
	ПараметрыРасчетаДляСправки.НалогИсчисленныйВсего = НалогСНачалаГодаВсего;
	ПараметрыРасчетаДляСправки.НалогКУплате = НалогСНачалаГодаВсего - КонтекстРасчета.АвансовыеПлатежи;
	
	РасчетПоВидамДохода = НачисленныеСуммыПоВидамНалога(ПараметрыРасчетаДляСправки, НалоговыеСтавки);
	
	Если РасчетПоВидамДохода.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из РасчетПоВидамДохода Цикл
			СтрокаСправки = ТаблицаСправкиРасчета.Добавить();
			СтрокаСправки.ПериодРасчета = КонтекстРасчета.Период;
			ЗаполнитьЗначенияСвойств(СтрокаСправки, ПараметрыНачисления);
			СтрокаСправки.СтавкаНалога = ТекущаяСтрока.СтавкаНалога;
			СтрокаСправки.НалоговаяБаза = ТекущаяСтрока.НалоговаяБаза;
			СтрокаСправки.НалогКУплате = ТекущаяСтрока.Сумма;
		КонецЦикла;
	Иначе
		СтрокаСправки = ТаблицаСправкиРасчета.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСправки, ПараметрыНачисления);
		СтрокаСправки.ПериодРасчета = ПараметрыНачисления.Период;
		Если ПараметрыНачисления.Сумма > 0 Тогда
			СтрокаСправки.НалогКУплате = ПараметрыНачисления.Сумма;
		Иначе
			СтрокаСправки.Переплата = -ПараметрыНачисления.Сумма;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыНачисления.Сумма;
	
КонецФункции

Функция УплаченныйНалогЗаПериодДоПростогоУчетаЕНС(Организация, НачалоПериода, КонецПериода)
	
	// Период поиска с учетом авансовых платежей, совершенных в прошлом году
	НачалоГода = НачалоГода(НачалоГода(НачалоПериода) - 1);

	СчетаДенежныхСредств = Новый Массив;
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.КассаОрганизации)); // 50.01
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета));   // 51
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЦифровойРубль));   // 53
	
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // 68.21
	
	ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(
		ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель(),
		Организация);
	
	СрокУплаты = ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(Организация, КонецПериода, ВидНалога).Срок;
	// Исключаем движения по ЕНС по сроку уплаты за предыдущий год
	СрокУплатыЗаПредыдущийГод = ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(
		Организация,
		НачалоГода(КонецПериода) - 1,
		ВидНалога).Срок;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("НачалоПериода",         Новый Граница(НачалоГода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаНачала",            НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания",         КонецПериода);
	Запрос.УстановитьПараметр("ПериодРасчета",         НачалоДня(КонецКвартала(КонецПериода)));
	Запрос.УстановитьПараметр("СчетаНачисленияНалога", СчетаНачисленияНалога);
	Запрос.УстановитьПараметр("СчетаДенежныхСредств",  СчетаДенежныхСредств);
	Запрос.УстановитьПараметр("ВидыПлатежей",          Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.УстановитьПараметр("СрокУплаты",            Мин(СрокУплаты, КонецГода(КонецПериода)));
	Запрос.УстановитьПараметр("СрокУплатыЗаПредыдущийГод", СрокУплатыЗаПредыдущийГод);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(СуммыАвансовыхПлатежей.Сумма) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот), 0) КАК Сумма
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачалоПериода,
	|				,
	|				Регистратор,
	|				СчетДт В (&СчетаНачисленияНалога),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|				СчетКт В (&СчетаДенежныхСредств),
	|				,
	|				Организация = &Организация
	|					И СубконтоДт1 В (&ВидыПлатежей)) КАК ХозрасчетныйОборотыДтКт
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.СписаниеСРасчетногоСчета)
	|					ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.СписаниеСРасчетногоСчета).НалоговыйПериод
	|				КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
	|					ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РасходныйКассовыйОрдер).НалоговыйПериод
	|				ИНАЧЕ ХозрасчетныйОборотыДтКт.Период
	|			КОНЕЦ МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(СУММА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СуммаРасход), 0)
	|	ИЗ
	|		РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете.Обороты(
	|				&ДатаНачала,
	|				,
	|				Период,
	|				Организация = &Организация
	|					И СчетУчета В (&СчетаНачисленияНалога)
	|					И СрокУплаты <= &СрокУплаты
	|					И СрокУплаты <> &СрокУплатыЗаПредыдущийГод) КАК РасчетыПоНалогамНаЕдиномНалоговомСчете) КАК СуммыАвансовыхПлатежей";
	
	Возврат Запрос.Выполнить().Выгрузить()[0].Сумма;
	
КонецФункции

Функция УплаченныйНалогЗаПериодВПростомУчетеЕНС(Организация, НачалоПериода, КонецПериода)
	
	ДатаЗакрытия = Справочники.Организации.ДатаЗакрытияОрганизации(Организация);
	Если ЗначениеЗаполнено(ДатаЗакрытия) Тогда
		КонецПериодаРасчета = КонецДня(Мин(КонецПериода, ДатаЗакрытия));
	Иначе
		КонецПериодаРасчета = КонецДня(КонецПериода);
	КонецЕсли;
	
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // 68.21
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериодаРасчета);
	Запрос.УстановитьПараметр("КонецНалоговогоПериода", КонецГода(НачалоПериода));
	Запрос.УстановитьПараметр("СчетаНачисленияНалога", СчетаНачисленияНалога);
	
	НачалоСледущегоПериода = КонецКвартала(КонецПериода) + 1;
	Запрос.УстановитьПараметр("НачалоСледущегоПериода", НачалоСледущегоПериода);
	Запрос.УстановитьПараметр("КонецСледущегоПериода", КонецКвартала(НачалоСледущегоПериода));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(РасчетНачисленияНалоговНаЕНССрезПоследних.Начислено, 0) КАК Сумма
	|ПОМЕСТИТЬ ВТ_Сумма
	|ИЗ
	|	РегистрСведений.РасчетНачисленияНалоговНаЕНС.СрезПоследних(
	|			&КонецПериода,
	|			ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
	|				И Организация = &Организация
	|				И СчетУчета В (&СчетаНачисленияНалога)
	|				И (КонецПериода МЕЖДУ &НачалоПериода И &КонецПериода)
	|				И (СрокУплаты МЕЖДУ &НачалоПериода И &КонецНалоговогоПериода)) КАК РасчетНачисленияНалоговНаЕНССрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(КорректировкаЕНСОперации.Сумма, 0)
	|ИЗ
	|	Документ.КорректировкаЕНС.Операции КАК КорректировкаЕНСОперации
	|ГДЕ
	|	КорректировкаЕНСОперации.СчетУчета В(&СчетаНачисленияНалога)
	|	И КорректировкаЕНСОперации.СрокУплаты МЕЖДУ &НачалоСледущегоПериода И &КонецСледущегоПериода
	|	И КорректировкаЕНСОперации.Ссылка.Проведен
	|	И КорректировкаЕНСОперации.Ссылка.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВТ_Сумма.Сумма), 0) КАК Сумма
	|ИЗ
	|	ВТ_Сумма КАК ВТ_Сумма";
	
	Возврат Запрос.Выполнить().Выгрузить()[0].Сумма;
	
КонецФункции

Функция УплаченныйНалогЗаПериодПоДокументамОплаты(Организация, НачалоПериода, КонецПериода)
	
	// Уплаченный налог за период с учетом авансовых платежей, совершенных в прошлом году
	НачалоГода = НачалоГода(НачалоГода(НачалоПериода) - 1);
	КонецГода  = КонецГода(КонецПериода);
	
	СчетаДенежныхСредств = Новый Массив;
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.КассаОрганизации)); // 50.01
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета));   // 51
	СчетаДенежныхСредств.Добавить(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЦифровойРубль));   // 53
	
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // 68.21
	
	ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(
		ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель(),
		Организация);
	
	// При переходе на ЕНС налоговый период определяем по сроку уплаты из регистра РасчетыПоНалогамНаЕдиномНалоговомСчете.
	// Для года, в котором ввели уплату на ЕНС, учитываем, что взносы с доходов предшествующего года также уплачивались на ЕНС
	УплатаНаЕНСГод = Год(НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж()) - 1;
	СрокиУплаты = Новый Массив; // Сроки уплаты налога за год, по которым оплаты осуществляются на ЕНС
	
	// По операциям на ЕНС нужно вычислить налоговый период по конректному сроку уплаты
	КрайнийСрокУплаты = НачалоДня(ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(Организация,
		КонецГода,
		ВидНалога).Срок);
	СрокиУплаты.Добавить(КрайнийСрокУплаты);
	СрокиУплаты.Добавить(НачалоДня(ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(Организация, НачалоПериода, ВидНалога).Срок));
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("НачалоПериода",         Новый Граница(НачалоГода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаНачала",            НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания",         КонецПериода);
	Запрос.УстановитьПараметр("СчетаНачисленияНалога", СчетаНачисленияНалога);
	Запрос.УстановитьПараметр("СчетаДенежныхСредств",  СчетаДенежныхСредств);
	Запрос.УстановитьПараметр("ВидыПлатежей",          Перечисления.ВидыПлатежейВГосБюджет.ВидыНалоговыхПлатежей());
	Запрос.УстановитьПараметр("КрайнийСрокУплаты",     КрайнийСрокУплаты);
	Запрос.УстановитьПараметр("СрокиУплаты",           СрокиУплаты);
	Запрос.УстановитьПараметр("ПериодРасчета",         НачалоДня(КонецКвартала(КонецПериода)));
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК ДокументОплаты,
	|	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот) КАК СуммаОплаты,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК ВидПлатежа,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.СписаниеСРасчетногоСчета)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.СписаниеСРасчетногоСчета).НалоговыйПериод
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РасходныйКассовыйОрдер).НалоговыйПериод
	|		ИНАЧЕ ХозрасчетныйОборотыДтКт.Период
	|	КОНЕЦ КАК НалоговыйПериод,
	|	ХозрасчетныйОборотыДтКт.СчетДт КАК СчетУчета
	|ПОМЕСТИТЬ ВТ_Выписки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&НачалоПериода,
	|			,
	|			Регистратор,
	|			СчетДт В (&СчетаНачисленияНалога),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет),
	|			СчетКт В (&СчетаДенежныхСредств),
	|			,
	|			Организация = &Организация
	|				И СубконтоДт1 В (&ВидыПлатежей)) КАК ХозрасчетныйОборотыДтКт
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.Регистратор,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.СписаниеСРасчетногоСчета)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.СписаниеСРасчетногоСчета).НалоговыйПериод
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОборотыДтКт.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РасходныйКассовыйОрдер).НалоговыйПериод
	|		ИНАЧЕ ХозрасчетныйОборотыДтКт.Период
	|	КОНЕЦ,
	|	ХозрасчетныйОборотыДтКт.СчетДт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.ПлатежныйДокумент,
	|	СУММА(РасчетыПоНалогамНаЕдиномНалоговомСчете.Сумма),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог),
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ДЕНЬ) В (&СрокиУплаты)
	|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ГОД, -1), ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, КВАРТАЛ), КВАРТАЛ, -1)
	|	КОНЕЦ,
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.СчетУчета
	|ИЗ
	|	РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете КАК РасчетыПоНалогамНаЕдиномНалоговомСчете
	|ГДЕ
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.Период >= &ДатаНачала
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.Организация = &Организация
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.СчетУчета В(&СчетаНачисленияНалога)
	|	И (РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИЛИ НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ДЕНЬ) = &КрайнийСрокУплаты)
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыПоНалогамНаЕдиномНалоговомСчете.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.ПлатежныйДокумент,
	|	РасчетыПоНалогамНаЕдиномНалоговомСчете.СчетУчета,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ДЕНЬ) В (&СрокиУплаты)
	|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, ГОД, -1), ГОД)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(РасчетыПоНалогамНаЕдиномНалоговомСчете.СрокУплаты, КВАРТАЛ), КВАРТАЛ, -1)
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Выписки.ДокументОплаты КАК ДокументОплаты,
	|	ВТ_Выписки.СуммаОплаты КАК СуммаОплаты,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ВТ_Выписки.НалоговыйПериод КАК НалоговыйПериод,
	|	ВТ_Выписки.ВидПлатежа КАК ВидПлатежа,
	|	ВТ_Выписки.СчетУчета КАК СчетУчета
	|ИЗ
	|	ВТ_Выписки КАК ВТ_Выписки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация = &Организация)
	|			И ВТ_Выписки.ДокументОплаты = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ВТ_Выписки.НалоговыйПериод МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция УплаченныйНалогЗаПериодПоДокументамВПростомУчетеЕНС(Организация, НачалоПериода, КонецПериода)
	
	ДатаЗакрытия = Справочники.Организации.ДатаЗакрытияОрганизации(Организация);
	Если ЗначениеЗаполнено(ДатаЗакрытия) Тогда
		КонецПериодаРасчета = КонецДня(Мин(КонецПериода, ДатаЗакрытия));
	Иначе
		КонецПериодаРасчета = КонецДня(КонецПериода);
	КонецЕсли;
	
	СчетаНачисленияНалога = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП); // 68.21
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериодаРасчета);
	Запрос.УстановитьПараметр("КонецНалоговогоПериода", КонецГода(НачалоПериода));
	Запрос.УстановитьПараметр("СчетаНачисленияНалога", СчетаНачисленияНалога);
	
	НачалоСледущегоПериода = КонецКвартала(КонецПериода) + 1;
	Запрос.УстановитьПараметр("НачалоСледущегоПериода", НачалоСледущегоПериода);
	Запрос.УстановитьПараметр("КонецСледущегоПериода", КонецКвартала(НачалоСледущегоПериода));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(КорректировкаЕНСОперации.Сумма) КАК Сумма,
	|	КорректировкаЕНСОперации.Ссылка КАК Ссылка,
	|	КорректировкаЕНСОперации.СчетУчета КАК СчетУчета
	|ПОМЕСТИТЬ ВТ_ОперацииЕНС
	|ИЗ
	|	Документ.КорректировкаЕНС.Операции КАК КорректировкаЕНСОперации
	|ГДЕ
	|	КорректировкаЕНСОперации.СчетУчета В (&СчетаНачисленияНалога)
	|	И КорректировкаЕНСОперации.СрокУплаты МЕЖДУ &НачалоСледущегоПериода И &КонецСледущегоПериода
	|	И КорректировкаЕНСОперации.Ссылка.Проведен
	|	И КорректировкаЕНСОперации.Ссылка.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаЕНСОперации.Ссылка,
	|	КорректировкаЕНСОперации.СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНачисленияНалоговНаЕНССрезПоследних.Регистратор КАК ДокументОплаты,
	|	РасчетНачисленияНалоговНаЕНССрезПоследних.Начислено КАК СуммаОплаты,
	|	РасчетНачисленияНалоговНаЕНССрезПоследних.СрокУплаты КАК ДатаДокумента,
	|	"""" КАК НомерДокумента,
	|	РасчетНачисленияНалоговНаЕНССрезПоследних.ПериодРасчета КАК НалоговыйПериод,
	|	РасчетНачисленияНалоговНаЕНССрезПоследних.Субконто1 КАК ВидПлатежа,
	|	РасчетНачисленияНалоговНаЕНССрезПоследних.СчетУчета КАК СчетУчета
	|ИЗ
	|	РегистрСведений.РасчетНачисленияНалоговНаЕНС.СрезПоследних(
	|			&КонецПериода,
	|			ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
	|				И Организация = &Организация
	|				И СчетУчета В (&СчетаНачисленияНалога)
	|				И (КонецПериода МЕЖДУ &НачалоПериода И &КонецПериода)
	|				И (СрокУплаты МЕЖДУ &НачалоПериода И &КонецНалоговогоПериода)) КАК РасчетНачисленияНалоговНаЕНССрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОперацииЕНС.Ссылка,
	|	ВТ_ОперацииЕНС.Сумма,
	|	ВТ_ОперацииЕНС.Ссылка.Дата,
	|	ВТ_ОперацииЕНС.Ссылка.Номер,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог),
	|	ВТ_ОперацииЕНС.СчетУчета
	|ИЗ
	|	ВТ_ОперацииЕНС КАК ВТ_ОперацииЕНС";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ГруппаДоходовПоИмуществу(ГруппаДоходов)
	
	Возврат ГруппаДоходов = ПомощникЗаполнения3НДФЛКлиентСервер.ГруппыДоходов().Имущество;
	
КонецФункции

Функция ГруппаДоходовОсобыеУсловия(ГруппаДоходов)
	
	Возврат ГруппаДоходов = ПомощникЗаполнения3НДФЛКлиентСервер.ГруппыДоходов().ОсобыеУсловия;
	
КонецФункции

#КонецОбласти