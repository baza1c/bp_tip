
#Область КомандыВводаНаОсновании

Функция ПараметрыИсправленияСчетаФактурыПолученного(ПараметрКоманды, ИсправлениеСобственнойОшибки = Ложь) Экспорт

	ПараметрыОткрытия = Новый Структура;
	
	РеквизитыДляОбработки = УчетНДСВызовСервера.СтруктураРеквизитовСчетаФактурыПолученного(ПараметрКоманды);
	ПорядокИсправления    = РеквизитыДляОбработки.ПорядокИсправления;
	ОснованиеИсправления  = РеквизитыДляОбработки.ОснованиеДляКорректировки;
	
	Если НЕ ПорядокИсправления[РеквизитыДляОбработки.ВидСчетаФактуры] 
		ИЛИ ТипЗнч(РеквизитыДляОбработки.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
			// Счета-фактуры создаются без документа "Корректировка поступления"
			Если ТипЗнч(РеквизитыДляОбработки.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
				
				ПараметрыЗаполнения = Новый Структура(
					"ДокументОснование,
					|ВидСчетаФактуры,
					|Исправление,
					|ИсправляемыйСчетФактура,
					|ИсправлениеСобственнойОшибки,
					|ЗаполнятьПоСчетуФактуре",
					РеквизитыДляОбработки.ДокументОснование,
					РеквизитыДляОбработки.ВидСчетаФактуры,
					?(ИсправлениеСобственнойОшибки, РеквизитыДляОбработки.Исправление, Истина),
					РеквизитыДляОбработки.СчетФактура,
					ИсправлениеСобственнойОшибки,
					Истина);
				
				ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
			ИначеЕсли ИсправлениеСобственнойОшибки Тогда
				ПараметрыЗаполнения = Новый Структура(
					"ДокументОснование,
					|ВидСчетаФактуры,
					|Исправление,
					|ИсправляемыйСчетФактура,
					|ИсправлениеСобственнойОшибки,
					|ЗаполнятьПоСчетуФактуре",
					РеквизитыДляОбработки.СчетФактура,
					РеквизитыДляОбработки.ВидСчетаФактуры,
					РеквизитыДляОбработки.Исправление,
					РеквизитыДляОбработки.СчетФактура,
					ИсправлениеСобственнойОшибки,
					Истина);
				
				ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
				
			Иначе
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("Основание", РеквизитыДляОбработки.ПоследнееИсправление);
			КонецЕсли;
		
		ПараметрыОткрытия.Вставить("ИмяДокумента", "СчетФактураПолученный"); 
		ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
		
	Иначе
		// Счета-фактуры создаются через документ "Корректировка поступления"
		ВидОперации = ?(ИсправлениеСобственнойОшибки, 
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеСобственнойОшибки"),
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки"));
		
		ПараметрыЗаполнения = Новый Структура("ДокументОснование, ВидОперации", ОснованиеИсправления, ВидОперации);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
		
		ПараметрыОткрытия.Вставить("ИмяДокумента", "КорректировкаПоступления"); 
		ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
		
	КонецЕсли;
	
	Возврат ПараметрыОткрытия;

КонецФункции

Функция ПараметрыКорректировкиСчетаФактурыПолученного(ПараметрКоманды) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	
	РеквизитыДляОбработки  = УчетНДСВызовСервера.СтруктураРеквизитовСчетаФактурыПолученного(ПараметрКоманды);
	ПорядокИсправления     = РеквизитыДляОбработки.ПорядокИсправления;
	ОснованиеКорректировки = РеквизитыДляОбработки.ОснованиеДляКорректировки;
	
	Если НЕ ПорядокИсправления[РеквизитыДляОбработки.ВидСчетаФактуры] Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для счета-фактуры с видом ""%1"" возможность корректировки не предусмотрена'"), РеквизитыДляОбработки.ВидСчетаФактуры);
		ВызватьИсключение ТекстСообщения;
		
	Иначе
		
		Если ТипЗнч(РеквизитыДляОбработки.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда 
			ПараметрыЗаполнения = Новый Структура(
				"ДокументОснование,
				|ВидСчетаФактуры,
				|ЗаполнятьПоСчетуФактуре,
				|ИсправляемыйСчетФактура,
				|ИсправлениеСобственнойОшибки,
				|Исправление",
				РеквизитыДляОбработки.ДокументОснование,
				ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыПолученного.Корректировочный"),
				Истина,
				РеквизитыДляОбработки.СчетФактура,
				Ложь,
				Ложь);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
			ПараметрыОткрытия.Вставить("ИмяДокумента", "СчетФактураПолученный");
		Иначе
			ПараметрыЗаполнения = Новый Структура("ДокументОснование, Видоперации",
				ОснованиеКорректировки, ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение"));
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
			ПараметрыОткрытия.Вставить("ИмяДокумента", "КорректировкаПоступления");
		КонецЕсли;
		
		ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
		
	КонецЕсли;
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

Функция ПараметрыИсправленияСчетаФактурыВыданного(ПараметрКоманды, ИсправлениеСобственнойОшибки = Ложь) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	
	РеквизитыДляОбработки = УчетНДСВызовСервера.СтруктураРеквизитовСчетаФактурыВыданного(ПараметрКоманды);
	ПорядокИсправления    = РеквизитыДляОбработки.ПорядокИсправления;
	ВозможноИсправить     = РеквизитыДляОбработки.ВозможноИсправить;
	ИсправлятьСчетФактуру = РеквизитыДляОбработки.ИсправлятьСчетФактуру;
	ОснованиеИсправления  = ?(ИсправлятьСчетФактуру, РеквизитыДляОбработки.ПоследнееИсправление, РеквизитыДляОбработки.ДокументОснование);
	
	Если НЕ ВозможноИсправить Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для счета-фактуры с видом ""%1"" возможность исправления не предусмотрена'"), РеквизитыДляОбработки.ВидСчетаФактуры);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Если НЕ ПорядокИсправления[РеквизитыДляОбработки.ВидСчетаФактуры] Тогда
		
		ПараметрыФормы = Новый Структура("Основание", РеквизитыДляОбработки.ПоследнееИсправление);
		
		ПараметрыОткрытия.Вставить("ИмяДокумента",   "СчетФактураВыданный");
		ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы);
		
	Иначе
		
		ВидОперации = ?(ИсправлениеСобственнойОшибки,
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеСобственнойОшибки"),
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки"));
		
		ПараметрыЗаполнения = Новый Структура("ДокументОснование, Видоперации", ОснованиеИсправления, ВидОперации);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
		
		ПараметрыОткрытия.Вставить("ИмяДокумента",   "КорректировкаРеализации"); 
		ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
		
	КонецЕсли;
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

Функция ПараметрыКорректировкиСчетаФактурыВыданного(ПараметрКоманды) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	
	РеквизитыДляОбработки     = УчетНДСВызовСервера.СтруктураРеквизитовСчетаФактурыВыданного(ПараметрКоманды);
	ВозможноКорректировать    = РеквизитыДляОбработки.ВозможноКорректировать;
	КорректироватьСчетФактуру = РеквизитыДляОбработки.КорректироватьСчетФактуру;
	ОснованиеКорректировки    = ?(КорректироватьСчетФактуру, РеквизитыДляОбработки.ПоследнееИсправление, РеквизитыДляОбработки.ДокументОснование);
	
	Если НЕ ВозможноКорректировать Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для счета-фактуры с видом ""%1"" возможность корректировки не предусмотрена'"), РеквизитыДляОбработки.ВидСчетаФактуры);
		ВызватьИсключение ТекстСообщения;
			
	Иначе
		
		ПараметрыЗаполнения = Новый Структура("ДокументОснование, Видоперации", 
			ОснованиеКорректировки, ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение"));
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
			
		ПараметрыОткрытия.Вставить("ИмяДокумента",   "КорректировкаРеализации"); 
		ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
		
	КонецЕсли;
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

Функция ПараметрыКорректировочнойСправки(ПараметрКоманды) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	
	РеквизитыДляОбработки           = УчетНДСВызовСервера.СтруктураРеквизитовСчетаФактурыВыданного(ПараметрКоманды);
	ДоступнаКорректировочнаяСправка = РеквизитыДляОбработки.ДоступнаКорректировочнаяСправка;
	ВидСчетаФактуры                 = РеквизитыДляОбработки.ВидСчетаФактуры;
	
	Если НЕ ДоступнаКорректировочнаяСправка Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для счета-фактуры с видом ""%1"" создание корректировочной справки не предусмотрено'"), ВидСчетаФактуры);
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;

	ПараметрыЗаполнения = Новый Структура("ИсправляемыйСчетФактура, ВидСчетаФактуры", 
		ПараметрКоманды,
		ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.КорректировочнаяСправка"));
	ПараметрыФормы      = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
		
	ПараметрыОткрытия.Вставить("ИмяДокумента", "СчетФактураВыданный"); 
	ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

Функция ПараметрыСчетФактураВыданныйНаАвансКомитента(ПараметрКоманды) Экспорт 
	
	ПараметрыОткрытия = Новый Структура;

	ПоследнееИсправление = УчетНДСВызовСервера.ПолучитьПоследнееИсправлениеСчетаФактурыПолученного(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура("Основание", ПоследнееИсправление);
	
	ПараметрыОткрытия.Вставить("ИмяДокумента",  "СчетФактураВыданный"); 
	ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
	
	Возврат ПараметрыОткрытия;

КонецФункции 

Функция ПараметрыСчетаФактурыНалоговогоАгентаКомитенту(ПараметрКоманды) Экспорт

	ПараметрыОткрытия = Новый Структура;
	
	РеквизитыДляОбработки     = УчетНДСВызовСервера.СтруктураРеквизитовСчетаФактурыВыданного(ПараметрКоманды);
	Если РеквизитыДляОбработки.ВидСчетаФактуры <> ПредопределенноеЗначение("Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент") Тогда 
		ТекстСообщения = НСтр("ru='Счет-фактура налогового агента перевыставляется комитенту на основании счета-фактуры с видом ""Налоговый агент""'");
		ВызватьИсключение ТекстСообщения;
	ИначеЕсли ТипЗнч(РеквизитыДляОбработки.ДокументОснование) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда 
		ТекстСообщения = НСтр("ru='Счет-фактура налогового агента перевыставляется комитенту на основании исходного счета-фактуры'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура("Основание, ПеревыставлениеСФНалоговогоАгента", ПараметрКоманды, Истина);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
	
	ПараметрыОткрытия.Вставить("ПараметрыФормы", ПараметрыФормы); 
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

#КонецОбласти

#Область Печать

Функция ВыполнитьКомандуПечатиСчетовФактур(ОписаниеКоманды) Экспорт
	
	НастройкиПечати = УчетНДСВызовСервера.ПолучитьНастройкиПечатиСчетовФактур(ОписаниеКоманды.ОбъектыПечати);
	
	СчетаФактуры = НастройкиПечати.СчетаФактуры;
	ДокументыБезСчетовФактур = НастройкиПечати.ДокументыБезСчетовФактур;
	СписокМакетов = НастройкиПечати.СписокМакетов;
	
	ПараметрыПечати = ОбщегоНазначенияБПКлиент.ПолучитьЗаголовокПечатнойФормы(СчетаФактуры);
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") 
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
	
	Если СписокМакетов <> "" Тогда 
		
		УправлениеПечатьюБПКлиент.НачатьЗамерВремени(ОписаниеКоманды.Форма, 
			ОписаниеКоманды.Идентификатор, НастройкиПечати.СчетаФактуры);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Документ.СчетФактураВыданный",
			СписокМакетов,
			СчетаФактуры,
			ОписаниеКоманды.Форма,
			ПараметрыПечати);
			
	КонецЕсли;
	
	Для каждого ДокументБезСчетаФактуры Из ДокументыБезСчетовФактур Цикл
		
		ТекстСообщения = НСтр("ru='По документу %1 счет-фактура не выписан.'");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументБезСчетаФактуры);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЦикла;
	
	КоличествоНеВыставленныхСчетовФактурНаАванс = НастройкиПечати.НеВыставленныеСчетаФактурыНаАванс.Количество();
	Если КоличествоНеВыставленныхСчетовФактурНаАванс > 0 Тогда
		
		Если КоличествоНеВыставленныхСчетовФактурНаАванс = 1 Тогда
			ТекстВопроса = НСтр("ru = 'Установить в счете-фактуре признак ""Выставлен (передан контрагенту)""?
								|Переданные контрагенту документы не актуализируются при расчете налога.'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Установить в счетах-фактурах на аванс признак ""Выставлен (передан контрагенту)""?
								|Переданные контрагенту документы не актуализируются при расчете налога.'");
		КонецЕсли;
			
		Оповещение = Новый ОписаниеОповещения("ВопросНеВыставленныеСчетаФактурыНаАвансЗавершение",
			УчетНДСКлиент, НастройкиПечати.НеВыставленныеСчетаФактурыНаАванс);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецФункции

Функция ВыполнитьКомандуПечатиСчетовФактурПолученных(ОписаниеКоманды) Экспорт
	
	НастройкиПечати = УчетНДСВызовСервера.ПолучитьНастройкиПечатиСчетовФактурПолученных(ОписаниеКоманды.ОбъектыПечати);
	
	СчетаФактуры = НастройкиПечати.СчетаФактуры;
	ДокументыБезСчетовФактур = НастройкиПечати.ДокументыБезСчетовФактур;
	СписокМакетов = НастройкиПечати.СписокМакетов;
	
	ПараметрыПечати = ОбщегоНазначенияБПКлиент.ПолучитьЗаголовокПечатнойФормы(СчетаФактуры);
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") 
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
	
	Если СписокМакетов <> "" Тогда 
		
		УправлениеПечатьюБПКлиент.НачатьЗамерВремени(ОписаниеКоманды.Форма, 
			ОписаниеКоманды.Идентификатор, НастройкиПечати.СчетаФактуры);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Документ.СчетФактураПолученный",
			СписокМакетов,
			СчетаФактуры,
			ОписаниеКоманды.Форма,
			ПараметрыПечати);
			
	КонецЕсли;
	
	Для каждого ДокументБезСчетаФактуры Из ДокументыБезСчетовФактур Цикл
		
		ТекстСообщения = НСтр("ru='По документу %1 счет-фактура не выписан.'");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументБезСчетаФактуры);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЦикла;
	
КонецФункции

Функция ВыполнитьКомандуПечатиУниверсальныхПередаточныхДокументов(ОписаниеКоманды) Экспорт
	
	НастройкиПечати = УчетНДСВызовСервера.ПолучитьНастройкиПечатиУниверсальныхПередаточныхДокументов(
		ОписаниеКоманды.ОбъектыПечати);
	
	ПараметрыПечати = Новый Структура("ЗаголовокФормы", НастройкиПечати.ЗаголовокФормы);
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры")
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
	
	Если НастройкиПечати.УниверсальныеПередаточныеДокументы.Количество() > 0 Тогда
		
		УправлениеПечатьюБПКлиент.НачатьЗамерВремени(ОписаниеКоманды.Форма, 
			ОписаниеКоманды.Идентификатор, НастройкиПечати.УниверсальныеПередаточныеДокументы);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Обработка.ПечатьУПД",
			НастройкиПечати.СписокМакетов, 
			НастройкиПечати.УниверсальныеПередаточныеДокументы, 
			ОписаниеКоманды.Форма,
			ПараметрыПечати);
			
	КонецЕсли;
	
	Для каждого ДокументБезСчетаФактуры Из НастройкиПечати.ДокументыБезСчетовФактур Цикл
		
		Если НастройкиПечати.ДокументыСчетФактураНеТребуются.Найти(ДокументБезСчетаФактуры) = Неопределено Тогда
			
			Если ТипЗнч(ДокументБезСчетаФактуры) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда
				ТекстСообщения = НСтр("ru = 'По документу %1 счета-фактуры не выписаны'");
			Иначе
				ТекстСообщения = НСтр("ru = 'По документу %1 счет-фактура не выписан'");
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументБезСчетаФактуры);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ВыполнитьКомандуПечатиУниверсальныхКорректировочныхДокументов(ОписаниеКоманды) Экспорт
	
	НастройкиПечати = УчетНДСВызовСервера.ПолучитьНастройкиПечатиУниверсальныхКорректировочныхДокументов(ОписаниеКоманды.ОбъектыПечати);
	
	ПараметрыПечати = Новый Структура("ЗаголовокФормы", НастройкиПечати.ЗаголовокФормы);
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры")
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
		
	Если НастройкиПечати.УниверсальныеКорректировочныеДокументы.Количество() > 0 Тогда
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Обработка.ПечатьУКД",
			 НастройкиПечати.СписокМакетов, 
			 НастройкиПечати.УниверсальныеКорректировочныеДокументы, 
			 ОписаниеКоманды.Форма,
			 ПараметрыПечати);
	КонецЕсли;
	
	Для каждого ДокументБезСчетаФактуры Из НастройкиПечати.ДокументыБезСчетовФактур Цикл
		
		Если НастройкиПечати.ДокументыСчетФактураНеТребуются.Найти(ДокументБезСчетаФактуры) = Неопределено Тогда
			
			ТекстСообщения = НСтр("ru = 'По документу %1 счет-фактура не выписан'");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументБезСчетаФактуры);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ВыполнитьКомандуПечатиЗаявленияОВвозеТоваров(ОписаниеКоманды) Экспорт

	ПараметрыПечати = ОбщегоНазначенияБПКлиент.ПолучитьЗаголовокПечатнойФормы(ОписаниеКоманды.ОбъектыПечати);
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") 
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли;

	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.ЗаявлениеОВвозеТоваров",
		"ЗаявлениеОВвозеТоваров",
		ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма,
		ПараметрыПечати);
	
КонецФункции

Функция ВыполнитьКомандуПечатиСтатистическойФормыУчетаПеремещенияТоваров(ОписаниеКоманды) Экспорт

	ПараметрыПечати = ОбщегоНазначенияБПКлиент.ПолучитьЗаголовокПечатнойФормы(ОписаниеКоманды.ОбъектыПечати);
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") 
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли;

	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.ЗаявлениеОВвозеТоваров",
		"СтатистическаяФормаУчетаПеремещенияТоваров",
		ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма,
		ПараметрыПечати);

КонецФункции

Функция ВыполнитьКомандуПечатиСводныхСправок(ОписаниеКоманды) Экспорт
	
	НастройкиПечати = УчетНДСВызовСервера.ПолучитьНастройкиПечатиСводныхСправок(ОписаниеКоманды.ОбъектыПечати);
	
	СводныеСправки = НастройкиПечати.СводныеСправки;
	СписокМакетов  = НастройкиПечати.СписокМакетов;
	
	Если ТипЗнч(СводныеСправки) = Тип("Массив") И СводныеСправки.Количество() = 1 Тогда 
		ПараметрыПечати = Новый Структура("ЗаголовокФормы", 
							СтрЗаменить(СтрШаблон(НСтр("ru='%1'"), СводныеСправки[0]),
							НСтр("ru='Счет-фактура выданный'"),
							НСтр("ru='Сводная справка по розничным продажам'")));
	Иначе
		ПараметрыПечати = Неопределено;
	КонецЕсли;
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") 
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
	
	Если СписокМакетов <> "" Тогда 
		
		УправлениеПечатьюБПКлиент.НачатьЗамерВремени(ОписаниеКоманды.Форма, 
			ОписаниеКоманды.Идентификатор, НастройкиПечати.СводныеСправки);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Документ.СчетФактураВыданный",
			СписокМакетов,
			СводныеСправки,
			ОписаниеКоманды.Форма,
			ПараметрыПечати);
			
	КонецЕсли;
	
КонецФункции

Функция ВыполнитьКомандуПечатиКорректировочныхСправок(ОписаниеКоманды) Экспорт
	
	НастройкиПечати = УчетНДСВызовСервера.ПолучитьНастройкиПечатиКорректировочныхСправок(ОписаниеКоманды.ОбъектыПечати);
	
	КорректировочныеСправки = НастройкиПечати.КорректировочныеСправки;
	СписокМакетов  = НастройкиПечати.СписокМакетов;
	
	Если ТипЗнч(КорректировочныеСправки) = Тип("Массив") И КорректировочныеСправки.Количество() = 1 Тогда 
		ПараметрыПечати = Новый Структура("ЗаголовокФормы", 
							СтрЗаменить(СтрШаблон(НСтр("ru='%1'"), КорректировочныеСправки[0]),
							НСтр("ru='Счет-фактура выданный'"),
							НСтр("ru='Корректировочная справка по розничным продажам'")));
	Иначе
		ПараметрыПечати = Неопределено;
	КонецЕсли;
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") 
		И ПараметрыПечати <> Неопределено Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
	
	Если СписокМакетов <> "" Тогда 
		
		УправлениеПечатьюБПКлиент.НачатьЗамерВремени(ОписаниеКоманды.Форма, 
			ОписаниеКоманды.Идентификатор, НастройкиПечати.КорректировочныеСправки);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Документ.СчетФактураВыданный",
			СписокМакетов,
			КорректировочныеСправки,
			ОписаниеКоманды.Форма,
			ПараметрыПечати);
			
	КонецЕсли;
	
КонецФункции

Процедура ВопросНеВыставленныеСчетаФактурыНаАвансЗавершение(Результат, СчетаФактуры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УчетНДСВызовСервера.УстановитьПризнакВыставленСчетовФактурНаАванс(СчетаФактуры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Выгрузка

Процедура ВыгрузкаЗаявленияОВвозеТоваров(Ссылка) Экспорт

	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров") Тогда
		
		УникальныйИдентификаторЗаявлениеОВвозе = Новый УникальныйИдентификатор;
		УникальныйИдентификаторУчетПеремещенияТоваров = Новый УникальныйИдентификатор;
		
		ВыгружаемыеДанные = УчетНДСВызовСервера.ПолучитьВыгружаемыеДанныеЗаявлениеОВвозеТоваров(
			Ссылка, 
			УникальныйИдентификаторЗаявлениеОВвозе,
			УникальныйИдентификаторУчетПеремещенияТоваров);
			
		Если ВыгружаемыеДанные.ЗаявлениеОВвозеТоваров <> Неопределено Тогда
			
			Для Каждого ФайлВыгрузки Из ВыгружаемыеДанные.ЗаявлениеОВвозеТоваров Цикл
				
				Попытка
					
					ПолучитьФайл(ФайлВыгрузки.АдресФайлаВыгрузки, ФайлВыгрузки.ИмяФайлаВыгрузки);
					
				Исключение
					
					Сообщение = Новый СообщениеПользователю;
					
					ТекстСообщения = "Не удалось записать файл """ + ФайлВыгрузки.ИмяФайлаВыгрузки 
					+ """! Возможно, недостаточно места на диске или диск защищен от записи.";
					
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"), ТекстСообщения);
					
					Сообщение.Сообщить();
					
				КонецПопытки;
				
			КонецЦикла;
			
		КонецЕсли;
			
		Если ВыгружаемыеДанные.УчетПеремещенияТоваров <> Неопределено Тогда
			
			Для Каждого ФайлВыгрузки Из ВыгружаемыеДанные.УчетПеремещенияТоваров Цикл
				
				Попытка
					
					ПолучитьФайл(ФайлВыгрузки.АдресФайлаВыгрузки, ФайлВыгрузки.ИмяФайлаВыгрузки);
					
				Исключение
					
					Сообщение = Новый СообщениеПользователю;
					
					ТекстСообщения = "Не удалось записать файл """ + ФайлВыгрузки.ИмяФайлаВыгрузки 
					+ """! Возможно, недостаточно места на диске или диск защищен от записи.";
					
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"), ТекстСообщения);
					
					Сообщение.Сообщить();
					
				КонецПопытки;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	

КонецПроцедуры

Процедура ВыгрузкаЖурналУчетаСчетовФактур(Ссылка) Экспорт

	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЖурналУчетаСчетовФактурДляПередачиВЭлектронномВиде") Тогда
		
		УникальныйИдентификаторЖурнала = Новый УникальныйИдентификатор;
		
		ВыгружаемыеДанные = УчетНДСВызовСервера.ПолучитьВыгружаемыеДанныеЖурналаУчетаСчетовФактур(
			Ссылка, 
			УникальныйИдентификаторЖурнала);
			
		СохранитьФайлВыгрузкиНаКлиенте(ВыгружаемыеДанные);
		
	КонецЕсли;

КонецПроцедуры

Процедура СохранитьФайлВыгрузкиНаКлиенте(ВыгружаемыеДанные) Экспорт
	
	Если ВыгружаемыеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;

	#Если НЕ ВебКлиент Тогда
		ПутьВыгрузки = РегламентированнаяОтчетностьКлиент.ПолучитьПутьВыгрузки();
		Если ПутьВыгрузки = Ложь Тогда
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	Для Каждого ФайлВыгрузки Из ВыгружаемыеДанные Цикл
		
		#Если ВебКлиент Тогда
			Попытка
				ПолучитьФайл(ФайлВыгрузки.АдресФайлаВыгрузки, ФайлВыгрузки.ИмяФайлаВыгрузки);
			Исключение
				ТекстСообщения = НСтр("ru='Не удалось записать файл ""%1"". Возможно, недостаточно места на диске, диск защищен от записи или не подключено расширение для работы с файлами.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ФайлВыгрузки.ИмяФайлаВыгрузки);
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ТекстСообщения;
				Сообщение.Сообщить();
			КонецПопытки;
		#Иначе
			ДвоичныйФайл = ПолучитьИзВременногоХранилища(ФайлВыгрузки.АдресФайлаВыгрузки);
			Попытка
				ДвоичныйФайл.Записать(ПутьВыгрузки + ФайлВыгрузки.ИмяФайлаВыгрузки);
				
				ТекстСообщения = НСтр("ru='Файл выгрузки регламентированного отчета ""%1"" сохранен в каталог ""%2"".'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ФайлВыгрузки.ИмяФайлаВыгрузки, ПутьВыгрузки);
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ТекстСообщения;
				Сообщение.Сообщить();
			Исключение
				ТекстСообщения = НСтр("ru='Не удалось записать файл ""%1"". Возможно, недостаточно места на диске или диск защищен от записи.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ФайлВыгрузки.ИмяФайлаВыгрузки);
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ТекстСообщения;
				Сообщение.Сообщить();
			КонецПопытки;
		#КонецЕсли
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ПереносСтрокТабличныхЧастей

// Функция проверяет для переданной формы не превышает ли количество строк в табличной части (табличных частях)
// максимально возможное для записи в базу данных.
// Если лимит возможного количества строк превышен, то пользователю предлагается перенести лишние строки
// в новый документ (новые документы).
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма документа
//   ПараметрыПереносаСтрокТЧ - Структура - структура входных параметров с ключами:
//     * ТабличныеЧасти          - Соответствие - соотствие между проверяемой табличной частью и подчиненной.
//               * Ключ             - Строка - имя проверяемой табличной части
//               * Значение         - Структура - структура с ключами:
//....................* КоличествоСтрокДляПереноса - Число - обязательный ключ в него записывается вычисляемое в функции
//..........................................................количество строк для переноса проверяемой табличной части.
//                    * ПодчиненныеТабличныеЧасти - Массив - необязательный ключ массив подчинённых табличных частей,
//                                                            строки из которых нужно перенести в новый документ. 
//                    * ПодчиненнаяТЧМаксимальноеКоличествоСтрок - Строка - имя подчиненной табличной части с
//                                                                          максимальным количеством строк.
//                    * МаксимальноеКоличествоСтрокВДокументе    - Число - максимальное количество строк основной
//                                                                         табличной части в новом документе.
//     * РеквизитыДокумента      - Строка - список реквизитов для копирования из документа.
//     * РежимЗаписи             - РежимЗаписиДокумента - режим записи документа.
// Возвращаемое значение:
//  - Булево - Истина, если количество строк табличной части документа превышает
//             максимальное количество строк, хранимое платформой в табличной части.
//
//  Если количество строк подчинной табличной части превышает количество строк основной табличной части и максимально
//   возможное количество для записи в базу данных, то в структуру, связанную с основной табличной частью 
//   добавляется ключи:
//  * ПодчиненнаяТЧМаксимальноеКоличествоСтрок - Строка - имя подчиненной табличной части с максимальным
//                                                        количеством строк.
//  * МаксимальноеКоличествоСтрокВДокументе    - Число - максимальное количество строк основной табличной
//                                                       части в новом документе.
Функция ПроверкаЗаписиСтрокТабличныхЧастей(Форма, ПараметрыПереносаСтрокТЧ) Экспорт
	
	Объект = Форма.Объект;
	МаксимальноеКоличествоСтрок = 99999;
	КоличествоНовыхДокументов = 0;
	
	Отказ = Ложь; 
	
	ПодчиненнаяТЧМаксимальноеКоличествоСтрок = "";
	
	Для Каждого ТабличнаяЧасть Из ПараметрыПереносаСтрокТЧ.ТабличныеЧасти Цикл 
		
		ОсновнаяТЧ = Объект[ТабличнаяЧасть.Ключ];
		КоличествоСтрокОсновнойТЧ = ОсновнаяТЧ.Количество();
		МасимальноеКоличествоСтрокТЧ = КоличествоСтрокОсновнойТЧ; 
		ПодчиненныеТабличныеЧасти = Новый Массив;
		
		Если ТабличнаяЧасть.Значение.Свойство("ПодчиненныеТабличныеЧасти") Тогда
			ПодчиненныеТабличныеЧасти = ТабличнаяЧасть.Значение.ПодчиненныеТабличныеЧасти; 
		КонецЕсли;
		
		Для каждого ПодчиненнаяТабличнаяЧасть ИЗ ПодчиненныеТабличныеЧасти Цикл
			КоличествоСтрокПодчиненнойТЧ = Объект[ПодчиненнаяТабличнаяЧасть].Количество();
			Если КоличествоСтрокПодчиненнойТЧ > МасимальноеКоличествоСтрокТЧ Тогда
				ПодчиненнаяТЧМаксимальноеКоличествоСтрок = ПодчиненнаяТабличнаяЧасть;
				МасимальноеКоличествоСтрокТЧ = КоличествоСтрокПодчиненнойТЧ;
			КонецЕсли;
			
		КонецЦикла;
		
		Если МасимальноеКоличествоСтрокТЧ > МаксимальноеКоличествоСтрок Тогда
			
			Отказ = Истина;
			КоличествоСтрокДляПереноса = МасимальноеКоличествоСтрокТЧ - МаксимальноеКоличествоСтрок;
			КоличествоНовыхДокументов = КоличествоНовыхДокументов + 
										Цел(КоличествоСтрокДляПереноса / МаксимальноеКоличествоСтрок) +
										?(КоличествоСтрокДляПереноса % МаксимальноеКоличествоСтрок <> 0, 1, 0);
			ТабличнаяЧасть.Значение.Вставить("КоличествоСтрокДляПереноса", КоличествоСтрокДляПереноса);
			
			Если МасимальноеКоличествоСтрокТЧ > КоличествоСтрокОсновнойТЧ Тогда
				
				ТабличнаяЧасть.Значение.Вставить("ПодчиненнаяТЧМаксимальноеКоличествоСтрок", ПодчиненнаяТЧМаксимальноеКоличествоСтрок); 
				
				// Вычисляем сколько нужно перенести строк с конца основной табличной части
				
				КоличествоСтрокДляПереносаИзОсновнойТЧ = 0;
				КоличествоСтрокДляПереносаИзПодчиненнойТЧ = 0;
				ОстатокСтрокДляПереносаИзПодчиненнойТЧ = 0;
				ИндексПоследнийСтрокиОсновнойТЧ = КоличествоСтрокОсновнойТЧ - 1;
				ИндкесНачалаНовогоДокумента = ИндексПоследнийСтрокиОсновнойТЧ;
				КоличествоСтрокОсновнойТЧНовогоДокумента = 0;
				
				Пока КоличествоСтрокДляПереноса > КоличествоСтрокДляПереносаИзПодчиненнойТЧ
					И ИндексПоследнийСтрокиОсновнойТЧ >= 0 Цикл
					
					СтрокаТЧ = ОсновнаяТЧ[ИндексПоследнийСтрокиОсновнойТЧ];
					КлючПоиска = Новый Структура("КлючСтроки", СтрокаТЧ.КлючСтроки);
					МассивСтрок = Объект[ПодчиненнаяТЧМаксимальноеКоличествоСтрок].НайтиСтроки(КлючПоиска);
					КоличествоСтрокДляПереносаИзПодчиненнойТЧ = КоличествоСтрокДляПереносаИзПодчиненнойТЧ + МассивСтрок.Количество();
					
					Если КоличествоСтрокДляПереносаИзПодчиненнойТЧ - ОстатокСтрокДляПереносаИзПодчиненнойТЧ >
						МаксимальноеКоличествоСтрок Тогда
						
						Если КоличествоСтрокОсновнойТЧНовогоДокумента = 0 Тогда
							КоличествоСтрокОсновнойТЧНовогоДокумента = ИндкесНачалаНовогоДокумента - ИндексПоследнийСтрокиОсновнойТЧ;
						Иначе
							КоличествоСтрокОсновнойТЧНовогоДокумента = Мин(КоличествоСтрокОсновнойТЧНовогоДокумента,
													ИндкесНачалаНовогоДокумента - ИндексПоследнийСтрокиОсновнойТЧ);
						КонецЕсли;
							
						ОстатокСтрокДляПереносаИзПодчиненнойТЧ = КоличествоСтрокДляПереносаИзПодчиненнойТЧ - МассивСтрок.Количество();
						ИндкесНачалаНовогоДокумента = ИндексПоследнийСтрокиОсновнойТЧ;
						
					КонецЕсли;
						
					КоличествоСтрокДляПереносаИзОсновнойТЧ = КоличествоСтрокДляПереносаИзОсновнойТЧ + 1;
					ИндексПоследнийСтрокиОсновнойТЧ = ИндексПоследнийСтрокиОсновнойТЧ - 1;
					
				КонецЦикла;
				
				ТабличнаяЧасть.Значение.Вставить("КоличествоСтрокДляПереноса", КоличествоСтрокДляПереносаИзОсновнойТЧ);
				ТабличнаяЧасть.Значение.Вставить("МаксимальноеКоличествоСтрокВДокументе", КоличествоСтрокОсновнойТЧНовогоДокумента);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ Тогда
		ПараметрыПереносаСтрокТЧ.Вставить("МаксимальноеКоличествоСтрок", МаксимальноеКоличествоСтрок);
		ПараметрыПереносаСтрокТЧ.Вставить("Объект", Объект);
		
		Оповещение = Новый ОписаниеОповещения("ВопросПеренестиСтрокиТабличныхЧастейВДругойДокументЗавершение", Форма, ПараметрыПереносаСтрокТЧ);
		
		ТекстВопроса = СтрШаблон(НСтр("ru='В табличные части можно записать не более %1 строк.
			|Перенести превышающие лимит записи строки в %2 ?'"),
			МаксимальноеКоличествоСтрок,
			?(КоличествоНовыхДокументов = 1,НСтр("ru='новый документ'"),НСтр("ru='новые документы'")));
			
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

// Процедура оповещает об успехе, либо неудачи переноса в новый документ (новые документы)
// строк, которые превыщают лимит количества записи строк табличной части (табличных частей) в базу данных.
// Внутри себя вызывает функцию разбиения табличной части (табличных частей) на несколько документов.
//
// Параметры:
//   ПараметрыПереносаСтрокТЧ - Структура - структура входных параметров с ключами:
//     * ТабличныеЧасти          - Соответвие - соотствие между проверяемой табличной частью и подчиненной.
//               * Ключ             - Строка - имя проверяемой табличной части
//               * Значение         - Структура - структура с ключами:
//....................* КоличествоСтрокДляПереноса - Число - обязательный ключ в него записывается вычисляемое в функции
//..........................................................количество строк для переноса проверяемой табличной части.
//                    * ПодчиненныеТабличныеЧасти - Массив - необязательный ключ является массивом подчинённых
//                                                           табличных частей для переноса строк в новый документ.
//     * РеквизитыДокумента          - Строка - список реквизитов для копирования из документа.
//     * РежимЗаписи                 - РежимЗаписиДокумента - режим записи документа.
//     * МаксимальноеКоличествоСтрок - Число - максимальное количество строк, хранимое платформой в табличной части.
//     * Объект                      - ДанныеФормыСтруктура - объект клиентской формы документа, для которого нужно
//                                                            перености строки табличных частей в новый документ.
//
Процедура ПеренестиСтрокиТабличныхЧастейВДругойДокумент(ПараметрыПереносаСтрокТЧ) Экспорт
	
	Если УчетНДСВызовСервера.ПеренестиСтрокиТабличныхЧастейВДругойДокументНаСервере(ПараметрыПереносаСтрокТЧ) Тогда
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Создание:'"),,
			НСтр("ru = 'Перенос строк произошел успешно.'"),
			БиблиотекаКартинок.ВажнаяИнформация32,
			СтатусОповещенияПользователя.Информация);
	Иначе
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = ' Не удалось создать новый документ для переноса строк табличной части. '")+
						ПараметрыПереносаСтрокТЧ.ИнформацияОбОшибке;
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаРеквизитовДокументов
// Процедура определеяет дату отметки о регистрации заявления о ввозе товаров из номера отметки о регистрации
// заявления о ввозе товаров в ФНС РФ.
// Регистрационный номер таможенной декларации может быть получен из "полного" номера таможенной декларации
// Номер отметки о регистрации заявления о ввозе товаров в ФНС РФ представляет собой шестнадцатизначный цифровой код,
// состоящий из последовательности цифр слева направо (письмо ФНС России от 21.03.2016 № ЕД-4-15/4611@):
// 4 цифры - код налогового органа, который присвоил этот регистрационный номер (НННН);
// 8 цифр - дата регистрации заявления (ДДММГГГГ);
// 4 цифры - порядковый номер о регистрации в течение дня (ХХХХ).
//
// Параметры
//    ДатаОтметкиОРегистрацииЗаявленияОВвозеТоваров - Дата - Определяемое процедурой значение из номера отметки о регистрации заявления о ввозе тововаров в ФНС РФ.
//    НомерОтметкиОРегистрацииЗаявленияОВвозеТоваров - Строка - Номера отметки о регистрации заявления о ввозе тововаров в ФНС РФ
//
Процедура ДатаОтметкиОРегистрацииЗаявленияОВвозеТоваров(ДатаОтметкиОРегистрацииЗаявленияОВвозеТоваров, Знач НомерОтметкиОРегистрацииЗаявленияОВвозеТоваров) Экспорт
	
	ДатаОтметкиОРегистрацииЗаявленияОВвозеТоваров = '00010101';
	ЕстьОшибкаВНомереОтметкиОРегистрацииЗаявленияОВвозеТоваров = Ложь;
	
	Если СтрДлина(НомерОтметкиОРегистрацииЗаявленияОВвозеТоваров) <> 16 Тогда
		ЕстьОшибкаВНомереОтметкиОРегистрацииЗаявленияОВвозеТоваров = Истина;
	Иначе
		
		Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерОтметкиОРегистрацииЗаявленияОВвозеТоваров) Тогда
			ЕстьОшибкаВНомереОтметкиОРегистрацииЗаявленияОВвозеТоваров = Истина;
		КонецЕсли;
		
		День = Сред(НомерОтметкиОРегистрацииЗаявленияОВвозеТоваров,5,2);
		Месяц = Сред(НомерОтметкиОРегистрацииЗаявленияОВвозеТоваров,7,2);
		Год = Сред(НомерОтметкиОРегистрацииЗаявленияОВвозеТоваров,9,4);
		
		ДатаОтметкиОРегистрацииЗаявленияОВвозеТоваров = СтроковыеФункцииКлиентСервер.СтрокаВДату(День + "." + Месяц + "." + Год);
		
		Если НЕ ЗначениеЗаполнено(ДатаОтметкиОРегистрацииЗаявленияОВвозеТоваров) Тогда
			ЕстьОшибкаВНомереОтметкиОРегистрацииЗаявленияОВвозеТоваров = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьОшибкаВНомереОтметкиОРегистрацииЗаявленияОВвозеТоваров Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru='№ отметки о регистрации заявления'"),,,
			НСтр("ru='Номер отметки должен состоять из 16 цифр:
			|первые 4 цифры - код налоговой инспекции,
			|следующие 8 цифр - дата отметки в формате ДДММГГГГ,
			|последние 4 цифры - порядковый номер регистрации в течение дня.'"));
			
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "НомерСчетаФактурыПолученного");

	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

Процедура ОткрытьФормуРеквизитовУПД(Форма, СтандартнаяОбработка, АдресХранилищаПродавцы = Неопределено) Экспорт
	
	Объект = Форма.Объект;
	
	Если НЕ Форма.ТолькоПросмотр Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",         Форма.ТолькоПросмотр);
	ПараметрыФормы.Вставить("СчетФактура",            Форма.СчетФактура);
	ПараметрыФормы.Вставить("АдресХранилищаПродавцы", АдресХранилищаПродавцы);
	ПараметрыФормы.Вставить("КодВидаОперации",        Форма.КодВидаОперации);
	ПараметрыФормы.Вставить("НДСПредъявленКВычету",   Форма.НДСПредъявленКВычету);
	ПараметрыФормы.Вставить("ЭтоКомиссияНаЗакупку",   Форма.ЭтоКомиссияНаЗакупку);
	ПараметрыФормы.Вставить("Дата",                   Объект.Дата);
	ПараметрыФормы.Вставить("Организация",            Объект.Организация);
	ПараметрыФормы.Вставить("ЭтоВыданныйДокумент",    Ложь);
	ПараметрыФормы.Вставить("РаздельныйУчетНДС",      Форма.РаздельныйУчетНДС);
	
	ОткрытьФорму("ОбщаяФорма.УниверсальныйПередаточныйДокумент", ПараметрыФормы, Форма);

КонецПроцедуры

Процедура ОткрытьФормуРеквизитовУПДВыданный(Форма, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	Если НЕ Форма.ТолькоПросмотр Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",      Форма.ТолькоПросмотр);
	ПараметрыФормы.Вставить("СчетФактура",         Форма.СчетФактура);
	ПараметрыФормы.Вставить("КодВидаОперации",     Форма.КодВидаОперации);
	ПараметрыФормы.Вставить("Дата",                Объект.Дата);
	ПараметрыФормы.Вставить("ЭтоВыданныйДокумент", Истина);
	
	ОткрытьФорму("ОбщаяФорма.УниверсальныйПередаточныйДокумент", ПараметрыФормы, Форма);

КонецПроцедуры

// Открывает форму списка документов НДС в режиме выбора, в качестве описания оповещения при открытии формы используется 
// ОписаниеОповещения, передаваемое в параметрах процедуры.
//
// Параметры:
//  ОписаниеОповещения- ОписаниеОповещения.
//  ПараметрыОтбора - Структура - задает начальные значения отборов. Может содержать ключи:
//                    * Организация- СправочникСсылка.Организации.
//
Процедура ВыбратьДокументНДСДляПередачиФНС(ОписаниеОповещения, ПараметрыОтбора) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	Если ПараметрыОтбора <> Неопределено Тогда
		Если ПараметрыОтбора.Свойство("Организация") Тогда
			ПараметрыФормы.Вставить("Организация", ПараметрыОтбора.Организация);
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("ЖурналДокументов.ДокументыПоУчетуНДСДляПередачиВЭлектронномВиде.ФормаСписка", ПараметрыФормы,,,,,ОписаниеОповещения);
		
КонецПроцедуры

Процедура ОткрытьСчетФактуру(Форма, СчетФактура, ВидСчетаФактуры) Экспорт

	СтандартнаяОбработка = Ложь;

	Если НЕ ЗначениеЗаполнено(Форма.Параметры.Ключ) ИЛИ Форма.Модифицированность Тогда
		ОбъектЗаписан = Форма.Записать();
		
		Если НЕ ОбъектЗаписан Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;		
	
	Если ЗначениеЗаполнено(СчетФактура) Тогда
		Параметры = Новый Структура("Ключ", СчетФактура);
	Иначе
		Если Форма.Объект.ПометкаУдаления Тогда
			ПоказатьПредупреждение( , НСтр("ru = 'Счет-фактуру нельзя вводить на основании документа, помеченного на удаление.'"));
			Возврат;
		КонецЕсли;

		Параметры = Новый Структура("Основание", Форма.Параметры.Ключ);
	КонецЕсли;
	
	ФормаСФ = ОткрытьФорму("Документ." + ВидСчетаФактуры + ".ФормаОбъекта", Параметры, Форма);

КонецПроцедуры

Функция СоздатьСчетФактуруВыданный(Форма, СтруктураОтбора = Неопределено) Экспорт

	Если Форма.Объект.ПометкаУдаления Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Счет-фактуру нельзя выписать на основании документа, помеченного на удаление.'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Отказ = Ложь;
	
	Если Форма.Объект.Свойство("ИспользоватьДокументРасчетовКакСчетФактуру") И Форма.Объект.ИспользоватьДокументРасчетовКакСчетФактуру Тогда
		
		Если НЕ ЗначениеЗаполнено(Форма.Объект.РасчетныйДокумент) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Счет-фактура не выписан!'"));
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Документ расчетов'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "РасчетныйДокумент", "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыписыватьСчетаФактурыСпецРежимы = ПолучитьФункциональнуюОпциюИнтерфейса("ВыписыватьСчетаФактурыСпецРежимы");

	Если НЕ ЗначениеЗаполнено(Форма.Параметры.Ключ) ИЛИ Форма.Модифицированность Тогда
		
		ПараметрыЗаписи = Новый Структура("РежимЗаписи, ВыписатьСчетФактуру", РежимЗаписиДокумента.Запись, Истина);
		Форма.Записать(ПараметрыЗаписи);
		
		Если ВыписыватьСчетаФактурыСпецРежимы <> ПолучитьФункциональнуюОпциюИнтерфейса("ВыписыватьСчетаФактурыСпецРежимы") Тогда
			ОбновитьИнтерфейс();
		КонецЕсли;
	
		Возврат Неопределено;
				
	КонецЕсли;		
	
	Основание = Форма.Объект.Ссылка;
	
	Если Форма.Объект.Свойство("ИспользоватьДокументРасчетовКакСчетФактуру") И Форма.Объект.ИспользоватьДокументРасчетовКакСчетФактуру Тогда
		Основание = Форма.Объект.РасчетныйДокумент;
	КонецЕсли;
	
	ПараметрыСоздания = УчетНДСКлиентСервер.НовыеПараметрыСозданияВыданногоСчетаФактуры();
	ПараметрыСоздания.Основание = Основание;
	ПараметрыСоздания.УникальныйИдентификатор = Форма.УникальныйИдентификатор;
	ПараметрыСоздания.СтруктураОтбора = СтруктураОтбора;
	
	Результат = УчетНДСВызовСервера.СоздатьСчетФактуруВыданныйНаОсновании(ПараметрыСоздания);
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Основание", Основание);
	ДополнительныеПараметры.Вставить("ВыписыватьСчетаФактурыСпецРежимы", ВыписыватьСчетаФактурыСпецРежимы);
		
	Если Результат.Свойство("ФоновоеВыполнение") Тогда // обработать выполнение в фоновом задании
		
		ДополнительныеПараметры.Вставить("АдресХранилищаСОшибками", Результат.АдресХранилищаСОшибками);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ОбработатьВыпискуСчетаФактуры", Форма, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Актуализация расчетов с контрагентами'");
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат.ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ОбработатьВыпискуСчетаФактуры(Форма, Результат, ДополнительныеПараметры);
	
	Возврат Результат;	

КонецФункции

Функция ОбработатьВыпискуСчетаФактуры(Форма, Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено                         // фоновое задание прервано
		Или Результат.Свойство("ФоновоеВыполнение") Тогда // фоновое задание завершено
		
		ПараметрыСоздания = УчетНДСКлиентСервер.НовыеПараметрыСозданияВыданногоСчетаФактурыПослеАктуализации();
		ПараметрыСоздания.Основание = ДополнительныеПараметры.Основание;
		ПараметрыСоздания.АдресХранилищаСОшибками = ДополнительныеПараметры.АдресХранилищаСОшибками;
		
		// сначала получим данные из временного хранилища
		УчетНДСВызовСервера.СоздатьСчетФактуруВыданныйНаОснованииПослеАктуализации(ПараметрыСоздания, Результат);
		
		Если ПараметрыСоздания.ОткрытьФормуОшибки Тогда
			ОбщегоНазначенияБПКлиент.ОткрытьФормуОшибокПерепроведения(Форма, ПараметрыСоздания.АдресХранилищаСОшибками);
		КонецЕсли;
		
		СчетФактура                      = ПараметрыСоздания.Ссылка;
		РеквизитыСФ                      = ПараметрыСоздания.РеквизитыСФ;
		ВыписыватьСчетаФактурыСпецРежимы = ПараметрыСоздания.ВыписыватьСчетаФактурыСпецРежимы;
		
	Иначе
		
		СчетФактура                      = Результат.Ссылка;
		РеквизитыСФ                      = Результат;
		ВыписыватьСчетаФактурыСпецРежимы = Результат.ВыписыватьСчетаФактурыСпецРежимы;
	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СчетФактура) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОповеститьОбИзменении(СчетФактура);
	
	Если ДополнительныеПараметры.ВыписыватьСчетаФактурыСпецРежимы <> ВыписыватьСчетаФактурыСпецРежимы Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	Возврат РеквизитыСФ;
	
КонецФункции

Процедура ОжидатьОкончаниеАктуализации(Форма, ПараметрыЗаписи) Экспорт
	
	Если Не ПараметрыЗаписи.Свойство("ВыписатьСчетФактуру")
		Или Не ПараметрыЗаписи.ВыписатьСчетФактуру
		Или Не ПараметрыЗаписи.Свойство("ДлительнаяОперация") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Основание", Форма.Объект.Ссылка);
	ДополнительныеПараметры.Вставить("АдресХранилищаСОшибками", ПараметрыЗаписи.ДлительнаяОперация.АдресХранилищаСОшибками);
	ДополнительныеПараметры.Вставить("ВыписыватьСчетаФактурыСпецРежимы",
		ПолучитьФункциональнуюОпциюИнтерфейса("ВыписыватьСчетаФактурыСпецРежимы"));
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ОбработатьВыпискуСчетаФактуры", Форма, ДополнительныеПараметры);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Актуализация расчетов с контрагентами'");
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ПараметрыЗаписи.ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

Функция СоздатьСчетФактуруПолученный(Форма, ЭтоКомиссияНаЗакупку = Ложь, ЭтоКорректировкаПоступления = Ложь, СтруктураОтбора = Неопределено) Экспорт
	
	// Признак, что вместо счета-фактуры регистрируется заявление о ввозе товаров, полученное от комиссионера
	Заявление = Новый Структура("ЗаявлениеОВвозеТоваровОтКомиссионера", Ложь);
	ЗаполнитьЗначенияСвойств(Заявление, Форма);
	
	Если Форма.Объект.ПометкаУдаления Тогда
		Если Заявление.ЗаявлениеОВвозеТоваровОтКомиссионера = Истина Тогда
			ПоказатьПредупреждение(,
			НСтр("ru = 'Заявление о ввозе товаров, полученное от комиссионера нельзя зарегистрировать на основании документа, помеченного на удаление.'"));
			Возврат Неопределено;
		КонецЕсли;
		ПоказатьПредупреждение( , НСтр("ru = 'Счет-фактуру нельзя зарегистрировать на основании документа, помеченного на удаление.'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Отказ = Ложь;
	Основание = Форма.Объект.Ссылка;
	
	Если Форма.Объект.Свойство("ИспользоватьДокументРасчетовКакСчетФактуру") 
		И Форма.Объект.ИспользоватьДокументРасчетовКакСчетФактуру Тогда
	
		Основание = Форма.Объект.РасчетныйДокумент;
		
		Если НЕ ЗначениеЗаполнено(Форма.Объект.РасчетныйДокумент) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Счет-фактура не зарегистрирован!'"));
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Документ расчетов'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "РасчетныйДокумент", "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоКорректировкаПоступления
		И Форма.Объект.Свойство("ВидОперации") Тогда
		
		Если Форма.Объект.ВидОперации = 
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки") Тогда
			ТекстНеЗаполненНомер = НСтр("ru = 'Номер исправления'");
			ИмяПоляНомера        = "НомерИсправления";
			ТекстНеЗаполненаДата = НСтр("ru = 'Дата исправления'");
			НомерСчетаФактуры    = Форма.НомерИсправления;
		Иначе
			ТекстНеЗаполненНомер = НСтр("ru = 'Номер корректировочного счета-фактуры'"); 
			ИмяПоляНомера        = "НомерСчетаФактурыПолученного";
			ТекстНеЗаполненаДата = НСтр("ru = 'Дата корректировочного счета-фактуры'"); 
			НомерСчетаФактуры    = Форма.НомерСчетаФактурыПолученного;
		КонецЕсли;
	Иначе
		ТекстНеЗаполненНомер = НСтр("ru = 'Номер счета-фактуры'");
		ИмяПоляНомера        = "НомерСчетаФактурыПолученного";
		ТекстНеЗаполненаДата = НСтр("ru = 'Дата счета-фактуры'");
		НомерСчетаФактуры    = Форма.НомерСчетаФактурыПолученного;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НомерСчетаФактуры) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", ТекстНеЗаполненНомер);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ИмяПоляНомера,, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Форма.ДатаСчетаФактурыПолученного) Тогда
		Если Заявление.ЗаявлениеОВвозеТоваровОтКомиссионера = Истина Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", НСтр("ru='№ отметки о регистрации заявления'"),,,
				НСтр("ru='Номер отметки должен состоять из 16 цифр:
				|первые 4 цифры - код налоговой инспекции,
				|следующие 8 цифр - дата отметки в формате ДДММГГГГ,
				|последние 4 цифры - порядковый номер регистрации в течение дня.'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "НомерСчетаФактурыПолученного",, Отказ);
		Иначе
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", ТекстНеЗаполненаДата);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ДатаСчетаФактурыПолученного",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоКомиссияНаЗакупку И НЕ ЗначениеЗаполнено(Форма.Продавец) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Составлен от имени'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Продавец",, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;

	РегистрироватьСчетаФактурыСпецРежимы = ПолучитьФункциональнуюОпциюИнтерфейса("РегистрироватьСчетаФактурыСпецРежимы");
	
	Если НЕ ЗначениеЗаполнено(Форма.Параметры.Ключ) ИЛИ Форма.Модифицированность Тогда
		
		ПараметрыЗаписи = Новый Структура("РежимЗаписи, ВыписатьСчетФактуру", РежимЗаписиДокумента.Запись, Истина);
		Форма.Записать(ПараметрыЗаписи);
		
		Если РегистрироватьСчетаФактурыСпецРежимы <> ПолучитьФункциональнуюОпциюИнтерфейса("РегистрироватьСчетаФактурыСпецРежимы") Тогда
			ОбновитьИнтерфейс();
		КонецЕсли;
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ПараметрыСозданияСчетаФактуры = УчетНДСКлиентСервер.НовыеПараметрыСозданияПолученногоСчетаФактуры();
	ПараметрыСозданияСчетаФактуры.Основание = Основание;
	ПараметрыСозданияСчетаФактуры.НомерСчетаФактурыПолученного = НомерСчетаФактуры;
	ПараметрыСозданияСчетаФактуры.ДатаСчетаФактурыПолученного  = Форма.ДатаСчетаФактурыПолученного;
	ПараметрыСозданияСчетаФактуры.Продавец = ?(ЭтоКомиссияНаЗакупку, Форма.Продавец, Неопределено);
	Если Заявление.ЗаявлениеОВвозеТоваровОтКомиссионера Тогда
		ПараметрыСозданияСчетаФактуры.КодВидаОперации = "46";
		ПараметрыСозданияСчетаФактуры.ЗаявлениеОВвозеТоваровОтКомиссионера = Истина;
	КонецЕсли;
	
	Результат = УчетНДСВызовСервера.СоздатьСчетФактуруПолученныйНаОсновании(
		ПараметрыСозданияСчетаФактуры, СтруктураОтбора);
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СчетФактура = Результат.Ссылка;
	
	ОповеститьОбИзменении(СчетФактура);
	
	Если РегистрироватьСчетаФактурыСпецРежимы <> ПолучитьФункциональнуюОпциюИнтерфейса("РегистрироватьСчетаФактурыСпецРежимы") Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Процедура ПриИзмененииНомераСчетаФактуры(Форма, НомерСчетаФактуры) Экспорт

	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(НомерСчетаФактуры) Тогда
		
		Если СокрЛП(НомерСчетаФактуры) = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Объект.Номер, Истина, Ложь) Тогда
			// Номер фактически не отредактирован
			Объект.ОтредактированНомерСчетаФактуры = Ложь;
			Объект.НомерВходящегоДокумента = "";
		Иначе
			// Номер отредактирован, необходимо присвоить другой системный номер
			Объект.ОтредактированНомерСчетаФактуры = Истина;
			Объект.НомерВходящегоДокумента = СокрЛП(НомерСчетаФактуры);
			Объект.Номер = "";
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Объект.НомерВходящегоДокумента)
		Или Объект.ОтредактированНомерСчетаФактуры Тогда
		// Ранее номер редактировался, необходимо присвоить стандартный номер
		Объект.ОтредактированНомерСчетаФактуры = Ложь;
		Объект.НомерВходящегоДокумента = "";
		Объект.Номер = "";
		
	КонецЕсли
	
	
КонецПроцедуры

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьЗаявлениеОВвозеТоваровПолученное(Форма, ЗаявлениеОВвозеТоваровПолученное) Экспорт

	СтандартнаяОбработка = Ложь;

	Если НЕ ЗначениеЗаполнено(Форма.Параметры.Ключ) ИЛИ Форма.Модифицированность Тогда
		ОбъектЗаписан = Форма.Записать();
		
		Если НЕ ОбъектЗаписан Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаявлениеОВвозеТоваровПолученное) Тогда
		Параметры = Новый Структура("Ключ", ЗаявлениеОВвозеТоваровПолученное);
	Иначе
		Если Форма.Объект.ПометкаУдаления Тогда
			ПоказатьПредупреждение( , НСтр("ru = 'Заявление о ввозе товаров(полученное) невозможно вводить на основании документа, помеченного на удаление.'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ФормаЗаявления = ОткрытьФорму("Документ.ЗаявлениеОВвозеТоваровПолученное.ФормаОбъекта", Параметры, Форма);

КонецПроцедуры

// Открывает на основании реквизитов документа форму для заполнения отчета статистическая форма учета перемещения товаров
// Параметры
// НаправлениеПеремещения        - Строка - "ЭК" - экспорт товаров в страны ЕАЭС, "ИМ" - импорт товаров из стран ЕАЭС
// Реквизиты                     - Структура - содержит реквизиты документа, на основании которого
//                                 открывается форма статистическая форма учета перемещения товаров.
// поля структуры:
//    Ссылка                     - РеализацияТоваровУслуг.Ссылка при экспорте товаров в страны ЕАЭС,
//                                 либо ЗаявлениеОВвозеТоваров.Ссылка при импорте товаров из стран ЕАЭС.
//    Дата                       - Дата - дата документа основания.
//    Организация                - СправочникСсылка.Организации - организация документа основания.
// СсылкаНаСуществующуюСтатформу - Документ.РегламентированныйОтчет или Неопределено.
//
Процедура ОткрытьСтатформу(Знач НаправлениеПеремещения, Знач Реквизиты, Знач СсылкаНаСуществующуюСтатформу = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаСуществующуюСтатформу) Тогда
		
		Попытка
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", НачалоМесяца(Реквизиты.Дата));
			ПараметрыФормы.Вставить("мСохраненныйДок",          Неопределено);
			ПараметрыФормы.Вставить("мСкопированаФорма",        Неопределено);
			ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета",  КонецМесяца(Реквизиты.Дата));
			ПараметрыФормы.Вставить("мПериодичность",           ПредопределенноеЗначение("Перечисление.Периодичность.Месяц"));
			ПараметрыФормы.Вставить("Организация",              Реквизиты.Организация);
			ПараметрыФормы.Вставить("мВыбраннаяФорма",          "ФормаАналогЛК");
			ПараметрыФормы.Вставить("Основание",                Реквизиты.Ссылка);
			ПараметрыФормы.Вставить("НаправлениеПеремещения",   НаправлениеПеремещения);
			ПараметрыФормы.Вставить("НужноОповещатьОСоздании");
			
			Если ТипЗнч(Реквизиты.Ссылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
				
				ПараметрыФормы.Вставить("Дата",                            Реквизиты.Дата);
				ПараметрыФормы.Вставить("СистемныйНомер",                  Реквизиты.СистемныйНомер);
				ПараметрыФормы.Вставить("КлючСтроки",                      Реквизиты.КлючСтроки);
				ПараметрыФормы.Вставить("Организация",                     Реквизиты.Организация);
				ПараметрыФормы.Вставить("Покупатель",                      Реквизиты.Покупатель);
				ПараметрыФормы.Вставить("СтранаРеализации",                Реквизиты.СтранаРеализации);
				ПараметрыФормы.Вставить("ВалютаДокумента",                 Реквизиты.ВалютаДокумента);
				ПараметрыФормы.Вставить("НомерДоговора",                   Реквизиты.НомерДоговора);
				ПараметрыФормы.Вставить("ДатаДоговора",                    Реквизиты.ДатаДоговора);
				ПараметрыФормы.Вставить("НомерСчетаФактурыОтКомиссионера", Реквизиты.НомерСчетаФактурыОтКомиссионера);
				ПараметрыФормы.Вставить("ДатаСчетаФактурыОтКомиссионера",  Реквизиты.ДатаСчетаФактурыОтКомиссионера);
				ПараметрыФормы.Вставить("НомерСчетаФактурыВыданного",      Реквизиты.НомерСчетаФактурыВыданного);
				ПараметрыФормы.Вставить("ДатаСчетаФактурыВыданного",       Реквизиты.ДатаСчетаФактурыВыданного);
				
			КонецЕсли;
			
			ПолучитьФорму("Отчет.РегламентированныйОтчетСтатистикаФормаУчетаПеремещенияТоваровТС.Форма.ФормаАналогЛК",
				ПараметрыФормы).Открыть();
			
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось открыть форму отчета ""Статистическая форма учета перемещения товаров"".'"));
			
		КонецПопытки;
		
	Иначе
		
		ПоказатьЗначение(,СсылкаНаСуществующуюСтатформу);
		
	КонецЕсли;

КонецПроцедуры

Функция СоздатьЗаявлениеОВвозеТоваровПолученное(Форма) Экспорт
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Форма.НомерЗаявленияОВвозеТоваровПолученного) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru = 'Номер отметки о регистрации заявления о ввозе товаров покупателя из ЕАЭС в налоговом органе своей страны'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "НомерЗаявленияОВвозеТоваровПолученного",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Форма.ДатаЗаявленияОВвозеТоваровПолученного) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru = 'Дата отметки о регистрации заявления о ввозе товаров покупателя из ЕАЭС в налоговом органе своей страны'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ДатаЗаявленияОВвозеТоваровПолученного",, Отказ);
	КонецЕсли;
	
	Если Форма.Объект.ПометкаУдаления Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Заявление о ввозе товаров (полученное) нельзя выписать на основании документа, помеченного на удаление.'"));
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Форма.Параметры.Ключ)
		ИЛИ Форма.Модифицированность Тогда
		
		ПараметрыЗаписи = Новый Структура("РежимЗаписи, ЗаписьБезЗаявленияОВвозеТоваровПолученного", РежимЗаписиДокумента.Запись, Истина);
		Форма.Записать(ПараметрыЗаписи);
		
	КонецЕсли;
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("Организация", Форма.Объект.Организация);
	ПараметрыСоздания.Вставить("Контрагент", Форма.Объект.Контрагент);
	ПараметрыСоздания.Вставить("НомерВходящегоДокумента", Форма.НомерЗаявленияОВвозеТоваровПолученного);
	ПараметрыСоздания.Вставить("Дата", Форма.ДатаЗаявленияОВвозеТоваровПолученного);
	ПараметрыСоздания.Вставить("ДокументОтгрузки", Форма.Объект.Ссылка);
	ПараметрыСоздания.Вставить("Ответственный", Форма.Объект.Ответственный);
	
	Если Форма.Объект.Проведен Тогда
		ПараметрыСоздания.Вставить("РежимЗаписиДокумента", РежимЗаписиДокумента.Проведение);
	Иначе
		ПараметрыСоздания.Вставить("РежимЗаписиДокумента", РежимЗаписиДокумента.Запись);
	КонецЕсли;

	Результат = УчетНДСВызовСервера.СоздатьЗаявлениеОВвозеТоваровПолученноеНаОсновании(ПараметрыСоздания);
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти
