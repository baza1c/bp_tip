// Содержимое модуля отличается в версиях ПРОФ и КОРП.
// В версии ПРОФ отсутствует:
// - тело экспортных процедур (остаются только сигнатуры методов)
// - неэкспортные процедуры и все функции.
// Поэтому не допускается реализация в модуле экспортных функций: вместо них следует использовать процедуры с возвращаемым параметром.

#Область СлужебныйПрограммныйИнтерфейс

// Получает графики платежей на дату и дисконтированное обязательство через 12 месяцев
//
// Параметры:
//  ИсходныеДанные - см. РасчетПоказателейБалансаПоАренднымОбязательствам.НовыйИсходныеДанныеПоАренде
//  КонтекстРасчета - см. РасчетПоказателейБалансаПоАренднымОбязательствам.НовыйКонтекстРасчета
//
Процедура ДобавитьТаблицуГрафикиПлатежейПоАренде(ИсходныеДанные, КонтекстРасчета) Экспорт
	
	ИсходныеДанные.ГрафикиПлатежей = НовыйГрафикиПлатежейПоАренде();
	
	// Находим последние записи на дату по графику процентных расходов
	// Если у предыдущего графика срок окончания больше, чем у текущего
	// и на момент получения графика текущий закончился, то последняя запись будет из предыдущего графика
	// Поэтому отбираем записи с учетом последнего введенного графика
	Запрос = Новый Запрос;
	
	ДатаНачисления =   НачалоДня(КонтекстРасчета.КонецПериодаДата);
	// Добавляем к периоду месяцы, после находим конец месяца для случая: 28.02.2023 --> 29.02.2024
	ДатаНачисления12 = НачалоДня(КонецМесяца(ДобавитьМесяц(КонтекстРасчета.КонецПериодаДата, 12)));
	
	ДатыНачисления = Новый Массив;
	ДатыНачисления.Добавить(ДатаНачисления);
	ДатыНачисления.Добавить(ДатаНачисления12);
	
	Запрос.УстановитьПараметр("Организации",     КонтекстРасчета.Организации);
	Запрос.УстановитьПараметр("Период",          КонтекстРасчета.КонецПериодаДата);
	Запрос.УстановитьПараметр("ДатаНачисления",  ДатаНачисления);
	Запрос.УстановитьПараметр("ДатыНачисления",  ДатыНачисления);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА КурсыВалютСрезПоследних.Курс = 0
	|			ТОГДА 1
	|		ИНАЧЕ КурсыВалютСрезПоследних.Курс
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА КурсыВалютСрезПоследних.Кратность = 0
	|			ТОГДА 1
	|		ИНАЧЕ КурсыВалютСрезПоследних.Кратность
	|	КОНЕЦ КАК Кратность,
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикиСрезПоследних.Период КАК Период,
	|	ГрафикиСрезПоследних.Регистратор КАК Регистратор,
	|	ГрафикиСрезПоследних.Организация КАК Организация,
	|	ГрафикиСрезПоследних.Контрагент КАК Контрагент,
	|	ГрафикиСрезПоследних.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ГрафикиСрезПоследних.ПредметНачисления КАК ПредметНачисления,
	|	СУММА(ВЫБОР
	|			КОГДА ГрафикиСрезПоследних.ДатаНачисления = &ДатаНачисления
	|				ТОГДА 0
	|			ИНАЧЕ ГрафикиСрезПоследних.Обязательство - ГрафикиСрезПоследних.СуммаПлатежа + ГрафикиСрезПоследних.СуммаПроцентов
	|		КОНЕЦ) КАК ДолгосрочноеОбязательство
	|ПОМЕСТИТЬ ОбязательстваПоГрафикам
	|ИЗ
	|	РегистрСведений.ГрафикиПроцентныхРасходов.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И ДатаНачисления В (&ДатыНачисления)) КАК ГрафикиСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикиСрезПоследних.Период,
	|	ГрафикиСрезПоследних.Регистратор,
	|	ГрафикиСрезПоследних.Организация,
	|	ГрафикиСрезПоследних.Контрагент,
	|	ГрафикиСрезПоследних.ДоговорКонтрагента,
	|	ГрафикиСрезПоследних.ПредметНачисления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	ПредметНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикиПроцентныхРасходов.Организация КАК Организация,
	|	ГрафикиПроцентныхРасходов.Контрагент КАК Контрагент,
	|	ГрафикиПроцентныхРасходов.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ГрафикиПроцентныхРасходов.ПредметНачисления КАК ПредметНачисления,
	|	МАКСИМУМ(ГрафикиПроцентныхРасходов.Период) КАК Период
	|ПОМЕСТИТЬ ПоследниеПериоды
	|ИЗ
	|	ОбязательстваПоГрафикам КАК ОбязательстваПоГрафикам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиПроцентныхРасходов КАК ГрафикиПроцентныхРасходов
	|		ПО ОбязательстваПоГрафикам.Организация = ГрафикиПроцентныхРасходов.Организация
	|			И ОбязательстваПоГрафикам.Контрагент = ГрафикиПроцентныхРасходов.Контрагент
	|			И ОбязательстваПоГрафикам.ДоговорКонтрагента = ГрафикиПроцентныхРасходов.ДоговорКонтрагента
	|			И ОбязательстваПоГрафикам.ПредметНачисления = ГрафикиПроцентныхРасходов.ПредметНачисления
	|			И (ГрафикиПроцентныхРасходов.Период <= &Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикиПроцентныхРасходов.Организация,
	|	ГрафикиПроцентныхРасходов.Контрагент,
	|	ГрафикиПроцентныхРасходов.ДоговорКонтрагента,
	|	ГрафикиПроцентныхРасходов.ПредметНачисления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	ПредметНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбязательстваПоГрафикам.Регистратор КАК ДокументАрендыГрафик,
	|	ОбязательстваПоГрафикам.Контрагент КАК Контрагент,
	|	ОбязательстваПоГрафикам.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СУММА(ОбязательстваПоГрафикам.ДолгосрочноеОбязательство) КАК ОбязательствоГрафик12,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоговорыКонтрагентов.Валютный, ЛОЖЬ)
	|			ТОГДА СУММА(ОбязательстваПоГрафикам.ДолгосрочноеОбязательство * ЕСТЬNULL(КурсыВалют.Курс, 1) / ЕСТЬNULL(КурсыВалют.Кратность, 1))
	|		ИНАЧЕ СУММА(ОбязательстваПоГрафикам.ДолгосрочноеОбязательство)
	|	КОНЕЦ КАК ОбязательствоГрафикРуб12,
	|	ЕСТЬNULL(КурсыВалют.Валюта, &ВалютаРеглУчета) КАК ВалютаГрафик,
	|	СРЕДНЕЕ(ЕСТЬNULL(КурсыВалют.Курс, 1)) КАК КурсГрафик,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.Валютный, ЛОЖЬ) КАК РасчетыВВалюте,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокументАрендыГрафик,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.НомерРегистратора, """") КАК НомерДокументАрендыГрафик
	|ИЗ
	|	ОбязательстваПоГрафикам КАК ОбязательстваПоГрафикам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследниеПериоды КАК ПоследниеПериоды
	|		ПО ОбязательстваПоГрафикам.Период = ПоследниеПериоды.Период
	|			И ОбязательстваПоГрафикам.Организация = ПоследниеПериоды.Организация
	|			И ОбязательстваПоГрафикам.Контрагент = ПоследниеПериоды.Контрагент
	|			И ОбязательстваПоГрафикам.ДоговорКонтрагента = ПоследниеПериоды.ДоговорКонтрагента
	|			И ОбязательстваПоГрафикам.ПредметНачисления = ПоследниеПериоды.ПредметНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|			ПО ДоговорыКонтрагентов.ВалютаВзаиморасчетов = КурсыВалют.Валюта
	|		ПО ОбязательстваПоГрафикам.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ОбязательстваПоГрафикам.Регистратор = ДанныеПервичныхДокументов.Документ
	|			И ОбязательстваПоГрафикам.Организация = ДанныеПервичныхДокументов.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбязательстваПоГрафикам.Регистратор,
	|	ОбязательстваПоГрафикам.Контрагент,
	|	ОбязательстваПоГрафикам.ДоговорКонтрагента,
	|	ЕСТЬNULL(КурсыВалют.Валюта, &ВалютаРеглУчета),
	|	ДоговорыКонтрагентов.Валютный,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.НомерРегистратора, """")";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ИсходныеДанные.ГрафикиПлатежей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		
		// если обязательство через 12 месяцев получилось меньше нуля, то считаем равным нулю
		СтрокаТаблицы.ОбязательствоГрафик12    = Макс(СтрокаТаблицы.ОбязательствоГрафик12, 0);
		СтрокаТаблицы.ОбязательствоГрафикРуб12 = Макс(СтрокаТаблицы.ОбязательствоГрафикРуб12, 0);
		
		СтрокаТаблицы.ДокументАрендыГрафикПредставление = ПредставлениеДокументаАренды(СтрокаТаблицы);
	КонецЦикла;
	ИсходныеДанные.ГрафикиПлатежей.Индексы.Добавить("Контрагент, ДоговорКонтрагента");
	
КонецПроцедуры

// Заполняет строку таблицы "АрендныеОбязательства" данными графика
//
// Параметры:
//  СтрокаТаблицы - СтрокаТаблицыЗначений из см. РасчетПоказателейБалансаПоАренднымОбязательствам.НовыйАрендныеОбязательства()
//  ГрафикиПлатежей - см. РасчетПоказателейБалансаПоАренднымОбязательствамГрафики.НовыйГрафикиПлатежейПоАренде
//                  - Неопределено - если Не ЕстьДисконтирование
//
Процедура ДополнитьСтрокуДаннымиИзГрафикаПоАренде(СтрокаТаблицы, ГрафикиПлатежей) Экспорт
	
	Если Не ЗначениеЗаполнено(ГрафикиПлатежей) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Контрагент, ДоговорКонтрагента");
	ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
	
	СтрокиГрафиков = ГрафикиПлатежей.НайтиСтроки(Отбор);
	Если Не ЗначениеЗаполнено(СтрокиГрафиков) Тогда
		Возврат;
	КонецЕсли;
	
	// устанавливаем флаг, если у договора имеется график платежей
	СтрокаТаблицы.ЕстьГрафикПлатежей = Истина;
	
	КоличествоДокументовАренды = 0;
	Обязательство12 = 0;
	ОбязательствоРуб12 = 0;
	Для Каждого СтрокаГрафика Из СтрокиГрафиков Цикл
		КоличествоДокументовАренды = КоличествоДокументовАренды + 1;
		Обязательство12 = Обязательство12 + СтрокаГрафика.ОбязательствоГрафик12;
		ОбязательствоРуб12 = ОбязательствоРуб12 + СтрокаГрафика.ОбязательствоГрафикРуб12;
	КонецЦикла;
	
	СтрокаТаблицы.Обязательство12 = Обязательство12;
	СтрокаТаблицы.ОбязательствоРуб12 = ОбязательствоРуб12;
	
	Если КоличествоДокументовАренды > 1 Тогда
		СтрокаТаблицы.НесколькоДокументовАренды = Истина;
	Иначе
		// валюта и курс при отборе "Контрагент", "ДоговорКонтрагента" должны быть одинаковыми, поэтому берем из первой строки
		СтрокаГрафика = СтрокиГрафиков[0];
		Если СтрокаТаблицы.РасчетыВВалюте Тогда
			СтрокаТаблицы.Валюта = СтрокаГрафика.ВалютаГрафик;
			СтрокаТаблицы.Курс = СтрокаГрафика.КурсГрафик;
		КонецЕсли;
		СтрокаТаблицы.ДокументАренды = СтрокаГрафика.ДокументАрендыГрафик;
		СтрокаТаблицы.ДокументАрендыПредставление = СтрокаГрафика.ДокументАрендыГрафикПредставление;
		СтрокаТаблицы.ДатаДокументАренды = СтрокаГрафика.ДатаДокументАрендыГрафик;
		СтрокаТаблицы.НомерДокументАренды = СтрокаГрафика.НомерДокументАрендыГрафик;
	КонецЕсли;
	
КонецПроцедуры

// Дополняет массив типами регистраторов из РегистрыСведений.ГрафикиПроцентныхРасходов
// Параметры:
//  Типы - Массив
//
Процедура ДобавитьТипыДокументовАренды(Типы) Экспорт
	ТипыРегистраторов = Метаданные.РегистрыСведений.ГрафикиПроцентныхРасходов.СтандартныеРеквизиты.Регистратор.Тип.Типы();
	
	Для Каждого Тип Из ТипыРегистраторов Цикл
		Типы.Добавить(Тип);
	КонецЦикла;
		
КонецПроцедуры

// Заполняет шаблон расшифровки на начало периода для аренды (лизинга) в пояснении баланса "Обязательства"
// Параметры:
//  Шаблон - Строка
//
Процедура ЗаполнитьШаблонРасшифровкиПоАрендеПояснениеБалансаОбязательстваНачалоПериода(Шаблон) Экспорт
	
	Шаблон = НСтр("ru = 'Расчетное обязательство через 12 месяцев по графику процентных расходов на дату, следующую за [КонецПериода], в размере, не превышающем фактическое обязательство.
				|По договорам без графика платежей - отрицательное сальдо по [СчетуАренды] за вычетом относящегося к нему НДС (сальдо по кредиту [СчетаНДСАвансовВыданных]). Включаются суммы со сроком действия договора не ранее [СрокПогашенияНачало]
				|Расчет выполняется отдельно по каждому договору аренды (лизинга)[СуммарноПоФилиалам].'");
		
КонецПроцедуры

// Заполняет шаблон расшифровки на конец периода для аренды (лизинга) в пояснении баланса "Обязательства"
// Параметры:
//  Шаблон - Строка
//
Процедура ЗаполнитьШаблонРасшифровкиПоАрендеПояснениеБалансаОбязательстваКонецПериода(Шаблон) Экспорт
	
	Шаблон = НСтр("ru = 'Расчетное обязательство через 12 месяцев по графику процентных расходов на дату, следующую за [КонецСледующегоПериода], в размере, не превышающем фактическое обязательство.
				|По договорам без графика платежей - отрицательное сальдо по [СчетуАренды] за вычетом относящегося к нему НДС (сальдо по кредиту [СчетаНДСАвансовВыданных]). Включаются суммы со сроком действия договора не ранее [СрокПогашенияКонец]
				|Расчет выполняется отдельно по каждому договору аренды (лизинга)[СуммарноПоФилиалам].'");
				
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйГрафикиПлатежейПоАренде()
	ТипСумма = БухгалтерскийУчетКлиентСервер.ТипСумма();
	
	Таблица = Новый ТаблицаЗначений; 
	Таблица.Колонки.Добавить("Контрагент",                        Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	Таблица.Колонки.Добавить("ДоговорКонтрагента",                Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	Таблица.Колонки.Добавить("РасчетыВВалюте",                    Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ДокументАрендыГрафик",              Новый ОписаниеТипов(РасчетПоказателейБалансаПоАренднымОбязательствам.ТипыДокументовАренды()));
	Таблица.Колонки.Добавить("ДокументАрендыГрафикПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	Таблица.Колонки.Добавить("ДатаДокументАрендыГрафик",          Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("НомерДокументАрендыГрафик",         ОбщегоНазначения.ОписаниеТипаСтрока(30));
	Таблица.Колонки.Добавить("ВалютаГрафик",                      Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	Таблица.Колонки.Добавить("КурсГрафик",                        Метаданные.ОпределяемыеТипы.КурсВалюты.Тип);
	Таблица.Колонки.Добавить("ОбязательствоГрафик12",             ТипСумма);
	Таблица.Колонки.Добавить("ОбязательствоГрафикРуб12",          ТипСумма);
	
	Возврат Таблица;
	
КонецФункции

Функция ПредставлениеДокументаАренды(СтрокаТаблицы)
	
	МетаданныеДокумента = СтрокаТаблицы.ДокументАрендыГрафик.Метаданные();
	Представление = СтрШаблон(НСтр("ru = '%1 %2 от %3'"),
					МетаданныеДокумента.Синоним,
					СтрокаТаблицы.НомерДокументАрендыГрафик,
					Формат(СтрокаТаблицы.ДатаДокументАрендыГрафик, "ДФ=dd.MM.yyyy"));
		
	Возврат Представление;
	
КонецФункции

#КонецОбласти