///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "СПАРК".
// ОбщийМодуль.СПАРКФакторыРискаКлиент.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ФакторыРискаКонтрагента

// Возвращает информацию о факторах риска контрагента.
// В случае, если информации нет в кэше, то инициируется фоновое задание.
//
// Параметры:
//  Контрагент - ОпределяемыйТип.КонтрагентБИП, Строка - Контрагент или ИНН контрагента;
//  Параметры - см. СПАРКФакторыРискаКлиентСервер.ПараметрыПолученияФакторов.
//
// Возвращаемое значение:
//  см. СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска.
//
Функция ФакторыРискаКонтрагента(Контрагент, Параметры) Экспорт
	
	Возврат СПАРКФакторыРискаВызовСервера.ФакторыРискаКонтрагента(
		Контрагент,
		Параметры);
	
КонецФункции

// Выводит информацию о факторах риска контрагента в элемент управления.
// В случае, если информации нет в кэше, то инициируется фоновое задание.
//
// Параметры:
//  РезультатФакторыРиска - Структура, Неопределено - результат выполнения функции ФакторыРискаКонтрагента,
//                          или Неопределено, если необходимо вызвать эту функцию;
//  КонтрагентОбъект - ОпределяемыйТип.КонтрагентБИПОбъект, Неопределено - заполняется в том случае, если форма
//    это форма элемента справочника, а не форма документа.
//  Контрагент - ОпределяемыйТип.КонтрагентБИП, Строка - Контрагент или ИНН контрагента;
//  Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо вывести информацию о факторах риска СПАРК.
//  ПараметрыОтображения - Структура - прочие параметры. См СПАРКРискиКлиентСервер.ПараметрыОтображенияДанныхСПАРК.
//
Процедура ОтобразитьФакторыРиска(
			РезультатФакторыРиска,
			КонтрагентОбъект,
			Контрагент,
			Форма,
			ПараметрыОтображения = Неопределено) Экспорт

	ТипСтруктура = Тип("Структура");
	ТипСтрока    = Тип("Строка");

	ИспользованиеРазрешено = СПАРКРискиКлиент.ИспользованиеРазрешено();
	Если Не ИспользованиеРазрешено Тогда

		Если ТипЗнч(РезультатФакторыРиска) <> ТипСтруктура Тогда
			РезультатФакторыРиска = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
			// После вызова функции необходимо самостоятельно дозаполнить ключ ДатаАктуальности:
			//  ТекущаяДатаСеанса() - для сервера или ОбщегоНазначенияКлиент.ДатаСеанса() - для клиента.
			РезультатФакторыРиска.Вставить("ДатаАктуальности", ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецЕсли;
		РезультатФакторыРиска.Вставить(
			"ВидОшибки",
			ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыОшибокСПАРКРиски.ИспользованиеЗапрещено"));

	Иначе

		// УслугаПодключена - только для модели сервиса.
		УслугаПодключена = ИнтернетПоддержкаПользователейВызовСервера.УслугаПодключена(
			СПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска(),
			Неопределено); // Предполагаем, что клиент всегда запущен в какой-то области данных, поэтому явно можно не передавать номер области.
		Если Не УслугаПодключена Тогда
			РезультатФакторыРиска = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
			// После вызова функции необходимо самостоятельно дозаполнить ключ ДатаАктуальности:
			//  ТекущаяДатаСеанса() - для сервера или ОбщегоНазначенияКлиент.ДатаСеанса() - для клиента.
			РезультатФакторыРиска.Вставить("ДатаАктуальности", ОбщегоНазначенияКлиент.ДатаСеанса());
			РезультатФакторыРиска.Вставить(
				"ВидОшибки",
				ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыОшибокСПАРКРиски.ТребуетсяОплатаИлиПревышенЛимит"));
			РезультатФакторыРиска.Вставить("РаботаВМоделиСервиса", Истина); // Проверка услуги используется только при работе в модели сервиса.
			РезультатФакторыРиска.Вставить(
				"ТекстОшибки",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось загрузить факторы риска 1СПАРК.
						|Услуга с идентификатором %1 не подключена.'"),
					СПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска()));
		КонецЕсли;

		Если ТипЗнч(Контрагент) = ТипСтрока Тогда
			ЛокальныйКонтрагент = СокрЛП(Контрагент);
		Иначе
			ЛокальныйКонтрагент = Контрагент;
		КонецЕсли;

		Если ТипЗнч(РезультатФакторыРиска) <> ТипСтруктура
			Или РезультатФакторыРиска.ОжиданиеОтвета Тогда
			
			ПараметрыВыполнения = СПАРКФакторыРискаКлиентСервер.ПараметрыПолученияФакторов();
			ПараметрыВыполнения.ИдентификаторФормы = Форма.УникальныйИдентификатор;
			РезультатФакторыРиска = ФакторыРискаКонтрагента(
				ЛокальныйКонтрагент,
				ПараметрыВыполнения);
			
			Форма.ФакторыРискаСПАРК = РезультатФакторыРиска;
			Если Форма.ФакторыРискаСПАРК.ДлительнаяОперация <> Неопределено Тогда
				ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
				ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
				ПараметрыОповещения = Новый Структура(
					"ДлительнаяОперация, ИдентификаторФормы", 
					Форма.ФакторыРискаСПАРК.ДлительнаяОперация,
					Форма.УникальныйИдентификатор);
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ПриОкончанииЗагрузкиФакторовРиска",
					СПАРКФакторыРискаКлиент,
					ПараметрыОповещения);
				ДлительныеОперацииКлиент.ОжидатьЗавершение(
					Форма.ФакторыРискаСПАРК.ДлительнаяОперация,
					ОписаниеОповещения,
					ПараметрыОжидания);
			ИначеЕсли РезультатФакторыРиска.ОжиданиеОтвета Тогда
				Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеФакторовРискаСПАРК", 0.5, Истина);
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
	СПАРКФакторыРискаКлиентСервер.ОтобразитьФакторыРиска(
		РезультатФакторыРиска,
		КонтрагентОбъект,
		ЛокальныйКонтрагент,
		Форма,
		ИспользованиеРазрешено,
		ПараметрыОтображения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФакторыРиска

// Вызывается из форм, в которые встроен показ факторов риска.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой инициировано событие;
//  КонтрагентОбъект - Объект, Неопределено - заполняется в том случае,
//                     если форма - это форма элемента справочника, а не форма документа.
//
Процедура ПриОткрытии(Форма, КонтрагентОбъект = Неопределено) Экспорт
	
	Если Не Форма.НастройкиСПАРКРиски.ИспользованиеРазрешено Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.ФакторыРискаСПАРК.ДлительнаяОперация <> Неопределено Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОповещения = Новый Структура(
			"ДлительнаяОперация, ИдентификаторФормы", 
			Форма.ФакторыРискаСПАРК.ДлительнаяОперация,
			Форма.УникальныйИдентификатор);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПриОкончанииЗагрузкиФакторовРиска",
			СПАРКФакторыРискаКлиент,
			ПараметрыОповещения);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			Форма.ФакторыРискаСПАРК.ДлительнаяОперация,
			ОписаниеОповещения,
			ПараметрыОжидания);
	ИначеЕсли Форма.ФакторыРискаСПАРК.ОжиданиеОтвета Тогда
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеФакторовРискаСПАРК", 0.5, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработка загрузки факторов риска.
//
// Параметры:
//  Форма    - ФормаКлиентскогоПриложения - форма, в которой инициировано событие;
//  Параметр - Произвольный - параметр оповещения;
//  Источник - Произвольный - источник оповещения.
//
Процедура ОбработатьЗагрузкуФакторовРиска(Форма, Параметр, Источник) Экспорт
	
	Если Источник <> Форма.УникальныйИдентификатор Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметр.Статус = "Выполнено" Тогда
		Форма.ФакторыРискаСПАРК = СПАРКФакторыРискаВызовСервера.ЗагруженныеФакторыРиска(Параметр.АдресРезультата);
		Если Форма.ФакторыРискаСПАРК.ОжиданиеОтвета Тогда
			Интервал = 0.5;
		Иначе
			Интервал = 0.1;
		КонецЕсли;
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеФакторовРискаСПАРК", Интервал, Истина);
	ИначеЕсли Параметр.Статус = "Отменено" Тогда
		Если Форма.ФакторыРискаСПАРК <> Тип("Структура") Тогда
			Форма.ФакторыРискаСПАРК = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
			// После вызова функции необходимо самостоятельно дозаполнить ключ ДатаАктуальности:
			//  ТекущаяДатаСеанса() - для сервера или ОбщегоНазначенияКлиент.ДатаСеанса() - для клиента.
			Форма.ФакторыРискаСПАРК.Вставить("ДатаАктуальности", ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецЕсли;
		Форма.ФакторыРискаСПАРК.Вставить("ВидОшибки", ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыОшибокСПАРКРиски.НеизвестнаяОшибка"));
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеФакторовРискаСПАРК", 0.1, Истина);
	ИначеЕсли Параметр.Статус = "Ошибка" Тогда
		Если Форма.ФакторыРискаСПАРК <> Тип("Структура") Тогда
			Форма.ФакторыРискаСПАРК = СПАРКФакторыРискаКлиентСервер.НовыйРезультатФакторыРиска();
			// После вызова функции необходимо самостоятельно дозаполнить ключ ДатаАктуальности:
			//  ТекущаяДатаСеанса() - для сервера или ОбщегоНазначенияКлиент.ДатаСеанса() - для клиента.
			Форма.ФакторыРискаСПАРК.Вставить("ДатаАктуальности", ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецЕсли;
		Форма.ФакторыРискаСПАРК.Вставить("ТекстОшибки", Параметр.ПодробноеПредставлениеОшибки);
		Форма.ФакторыРискаСПАРК.Вставить("ВидОшибки", ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыОшибокСПАРКРиски.НеизвестнаяОшибка"));
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеФакторовРискаСПАРК", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при окончании загрузки факторов риска.
//
// Параметры:
//  Результат - Структура - результат длительной операции загрузки факторов риска;
//  ДополнительныеПараметры - Структура- параметры оповещения.
//
Процедура ПриОкончанииЗагрузкиФакторовРиска(Результат, ДополнительныеПараметры) Экспорт
	
	Оповестить(
		"СПАРКРиски:ЗагруженыФакторыРиска", Результат, ДополнительныеПараметры.ИдентификаторФормы);
	
КонецПроцедуры

#КонецОбласти


