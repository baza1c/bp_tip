
#Область ПрограммныйИнтерфейс

// Функция возвращает количество дней в периоде.
// Может включать все дни, включая дни начала и окончания периода, 
// либо все дни, кроме первого. Последний вариант подходит для случаев,
// когда день начала включается в расчет по условиям, определяемых вовне
// (например, расчет суточных по нескольким сегментам командировки)
//
// Параметры:
//  ДатаНачала			 - Дата - дата начала периода (без времени)
//  ДатаОкончания		 - Дата - дата окончания периода (без времени)
//  ВключаяДатуНачала	 - Булево - Истина - учитывать обе границы периода
//									Ложь - дата начала не учитывается
//						
// 
// Возвращаемое значение:
//   - Число - количество дней в периоде
//
Функция ПродолжительностьПериодаВДнях(ДатаНачала, ДатаОкончания, ВключаяДатуНачала = Ложь) Экспорт 
	
	Если ДатаНачала > ДатаОкончания Тогда
		Возврат 0;
	КонецЕсли;	
	
	ДлинаСуток = 86400;
	
	Возврат (ДатаОкончания - ДатаНачала) / ДлинаСуток + ?(ВключаяДатуНачала, 1, 0);
			
КонецФункции	

// Процедура рассчитывает суточные для одного сегмента командировки (строки расчета)
// на основании принятых норм суточных в организации и с учетом особенностей однодневных командировок
// (см. Постановление Правительства РФ от 13.10.2008 N 749 "Об особенностях направления работников в служебные командировки" 
// (вместе с "Положением об особенностях направления работников в служебные командировки"))
//
// Параметры:
//  СтрокаРасчета	 - ДанныеФормыЭлементКоллекции - строка таблицы расчета суточных формы
//		обязательно содержит свойства:
//		- ЗаРубежом - Булево
//		- Норма - Число
//		- КоличествоДней - Число
//		- Сумма - Число
//		- СуммаСверхНормы - Число
//  Однодневная		 - Булево - Истина - сегмент командировки начинается и заканчивается в один день
//  НормыСуточных	 - Структура - см. комментарий к УчетКомандировок.НормыСуточных()
//
Процедура РассчитатьСуммуСуточныхВСтроке(СтрокаРасчета, Однодневная, НормыСуточных) Экспорт 
	
	Если СтрокаРасчета.КоличествоДней = 0 Тогда
		СтрокаРасчета.Сумма = 0;
		СтрокаРасчета.СуммаСверхНормы = 0;
		Возврат;
	КонецЕсли;	
	
	// Определяем дневные нормы суточных.
	// Для однодневных командировок (отправление и возвращение в один день) особый расчет норм:
	// - по России – суточные не выплачиваются (п. 11 Положения № 749)
	// - за рубежом – суточные выплачиваются в размере 50% от нормы для командировок за границу (п. 20 Положения № 749)
	Норма = 0;
	НормаНеоблагаемая = 0;
	Если Однодневная Тогда
		Если СтрокаРасчета.ЗаРубежом Тогда 
			// однодневная за рубежом
			Норма = СтрокаРасчета.Норма * 0.5;
			НормаНеоблагаемая = НормыСуточных.НормаЗаРубежомНеоблагаемая * 0.5;
		Иначе
			// однодневная по России
			Норма = 0;
			НормаНеоблагаемая = 0;
		КонецЕсли;
	Иначе
		// многодневная
		Норма = СтрокаРасчета.Норма;
		НормаНеоблагаемая = ?(СтрокаРасчета.ЗаРубежом, 
			НормыСуточных.НормаЗаРубежомНеоблагаемая,
			НормыСуточных.НормаПоРФНеоблагаемая);
	КонецЕсли;	
	
	СтрокаРасчета.Сумма = СтрокаРасчета.КоличествоДней * Норма;
	
	Если Норма > НормаНеоблагаемая Тогда
		СтрокаРасчета.СуммаСверхНормы = СтрокаРасчета.КоличествоДней * (Норма - НормаНеоблагаемая);
	Иначе
		СтрокаРасчета.СуммаСверхНормы = 0;
	КонецЕсли;	
	
КонецПроцедуры	

// Функция возвращает предельную норму суточных по РФ, не облагаемую НДФЛ и взносами
// (п. 3 ст. 217 НК РФ, п. 2 ст. 422 НК РФ)
//
// Возвращаемое значение:
//   - Число
//
Функция НалоговаяНормаСуточныхПоРФ() Экспорт 
	
	Возврат 700;
	
КонецФункции	

// Функция возвращает предельную норму суточных за рубежом, не облагаемую НДФЛ и взносами
// (п. 3 ст. 217 НК РФ, п. 2 ст. 422 НК РФ)
//
// Возвращаемое значение:
//   - Число
//
Функция НалоговаяНормаСуточныхЗаРубежом() Экспорт 
	
	Возврат 2500;
	
КонецФункции	

// Заполняет список выбора ставки НДС для операций с билетами
//
// Параметры:
//  ДанныеВыбора   - СписокЗначений - список выбора для заполнения
//  Период         - Дата - дата, на которую нужно получить действующие ставки НДС
//  ДобавитьПустую - Булево - если Истина, то будет добавлено пустое значение в виде "н/д"
//
Процедура ЗаполнитьДанныеВыбораСтавкиНДС(ДанныеВыбора, Период, ДобавитьПустую = Ложь) Экспорт
	
	ДанныеВыбора.Очистить();
	
	До2019Года = Период < '20190101';
	До2026Года = Период < '20260101';
	Применяется176ФЗ = УчетНДСКлиентСервер.Применяется176ФЗ(Период);
	
	Если ДобавитьПустую Тогда
		ДанныеВыбора.Добавить(
			ПредопределенноеЗначение("Перечисление.СтавкиНДС.ПустаяСсылка"),
			НСтр("ru='н/д'"));
	КонецЕсли;
	
	Если До2019Года Тогда
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118"));
	ИначеЕсли До2026Года Тогда
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120"));
	Иначе
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС22"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС22_122"));
	КонецЕсли;
	
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10"));
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10_110"));
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0"));
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"));
	
	Если Применяется176ФЗ Тогда
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС5"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС5_105"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС7"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС7_107"));
	КонецЕсли;
	
	Если До2019Года Тогда
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120"));
	ИначеЕсли До2026Года Тогда
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118"));
	Иначе
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти