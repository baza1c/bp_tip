
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиентские методы формы записи регистра сведений НастройкиУчетаНДС
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ПериодПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиУчетаНДС;
	
	Если Запись.Период < '20120101' Тогда
		Запись.РаздельныйУчетНДСНаСчете19 = Ложь;
	КонецЕсли;
	
	Если '20120101' >= Запись.Период Тогда
		Запись.УпрощенныйУчетНДС = Ложь;
	КонецЕсли;
	
	Если (Запись.Период < '20110101') Или ('20140701' <= Запись.Период) Тогда
		Запись.НачислятьНДСПриПередачеНедвижимости = Ложь;
	КонецЕсли;
	
	Если Форма.ПлательщикНДС Тогда
		// Очистим ставку УСН если это плательщик НДС
		Запись.СтавкаНалогообложенияПриУСН = Неопределено;
	КонецЕсли;
	
	НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура СложныйУчетНДСПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиУчетаНДС;
	
	Если Запись.СложныйУчетНДС Тогда
		Запись.УпрощенныйУчетНДС = Ложь;
	КонецЕсли;

	Если Форма.ПлательщикНДС Тогда
		// Очистим ставку УСН если это плательщик НДС
		Запись.СтавкаНалогообложенияПриУСН = Неопределено;
	КонецЕсли;
	
	Если (Запись.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ОбщаяУСН")
		ИЛИ Запись.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ПониженнаяУСН")) 
		И Запись.СложныйУчетНДС Тогда
		Запись.РаздельныйУчетНДСБезВычетов = Истина;
		Запись.РаздельныйУчетНДСНаСчете19  = Ложь;
	Иначе
		Запись.РаздельныйУчетНДСНаСчете19  = Запись.СложныйУчетНДС;
		Запись.РаздельныйУчетНДСБезВычетов = Ложь;
	КонецЕсли;
	
	Запись.РаздельныйУчетНДСДо2014Года = Ложь;
	
	НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура УпрощенныйУчетНДСПриИзменении(Форма, Элемент) Экспорт
	
	НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура ОбновитьИнтерфейсПослеЗаписиНастройкиУчетаНДС(ПараметрыЗаписи) Экспорт

	Перем ИспользуетсяРаздельныйУчетНДСПередЗаписью, ИспользуетсяРаздельныйУчетНДСПослеЗаписи;
	
	Если ПараметрыЗаписи.Свойство("ИспользуетсяРаздельныйУчетНДСПередЗаписью", ИспользуетсяРаздельныйУчетНДСПередЗаписью)
		И ПараметрыЗаписи.Свойство("ИспользуетсяРаздельныйУчетНДСПослеЗаписи", ИспользуетсяРаздельныйУчетНДСПослеЗаписи) Тогда

		Если ИспользуетсяРаздельныйУчетНДСПередЗаписью <> ИспользуетсяРаздельныйУчетНДСПослеЗаписи Тогда
		
			ОбновитьИнтерфейс();
			
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры 

Процедура КонтролироватьДолюВычетаПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиУчетаНДС;
	БезопаснаяДоляВычетаПоУмолчанию = УчетНДСКлиентСервер.БезопаснаяДоляВычетаПоУмолчанию();
	
	Если Запись.КонтролироватьДолюВычета Тогда 
		Запись.БезопаснаяДоляВычета = БезопаснаяДоляВычетаПоУмолчанию;
	Иначе
		Запись.БезопаснаяДоляВычета = 0;
	КонецЕсли;
	
	НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура СтавкаНДСПриУСНПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиУчетаНДС;
	СтавкаНДСПриУСН = Форма.СтавкаНДСПриУСН;
	
	Если Запись.Период < УчетУСНКлиентСервер.ДатаНачалаПримененияНДС() Тогда
		Запись.Период = УчетУСНКлиентСервер.ДатаНачалаПримененияНДС();
		Форма.ДатаИзменения = УчетУСНКлиентСервер.ДатаНачалаПримененияНДС();
		Форма.ДатаИзмененияСтрокой = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			Форма.ДатаИзменения, КонецМесяца(Форма.ДатаИзменения), Истина);
	КонецЕсли;
	
	Если СтавкаНДСПриУСН = 0 Тогда
		Запись.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ПониженнаяУСН");
	ИначеЕсли СтавкаНДСПриУСН = 1 Тогда
		Запись.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ОбщаяУСН");
	ИначеЕсли СтавкаНДСПриУСН = 2 Тогда
		Запись.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.Общая");
	КонецЕсли;
	
	Если (Запись.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ОбщаяУСН")
		ИЛИ Запись.СтавкаНалогообложенияПриУСН = ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.ПониженнаяУСН")) 
		И Запись.СложныйУчетНДС Тогда
		Запись.РаздельныйУчетНДСБезВычетов	= Истина;
		Запись.РаздельныйУчетНДСНаСчете19	= Ложь;
		Запись.НачислятьНДСПоОтгрузке		= Ложь;
	Иначе
		Запись.РаздельныйУчетНДСНаСчете19	= Запись.СложныйУчетНДС;
		Запись.РаздельныйУчетНДСБезВычетов	= Ложь;
	КонецЕсли;
	
	НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

#КонецОбласти