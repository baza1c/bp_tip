#Область ПрограммныйИнтерфейс

// Конструктор структуры параметров для формирования чека.
// См. РегистрыСведений.ЧекиНПД.СформироватьЧекВФоне()
// и РегистрыСведений.ЧекиНПД.СформироватьЧек().
// 
// Возвращаемое значение:
//   - Структура.
//
Функция НовыйПараметрыФормированияЧека() Экспорт
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("Ссылка", Неопределено);
	ПараметрыФормирования.Вставить("Организация", Неопределено);
	ПараметрыФормирования.Вставить("Контрагент", Неопределено);
	ПараметрыФормирования.Вставить("Дата", '00010101');
	ПараметрыФормирования.Вставить("СуммаДокумента", 0);
	ПараметрыФормирования.Вставить("Услуги", НовыйТаблицаУслуг());
	
	Возврат ПараметрыФормирования;
	
КонецФункции

// Возвращает параметры формирования чека.
//
// Параметры:
//   ДокументОснование - ДокументСсылка - ссылка на основание чека.
//   Организация - СправочникСсылка.Организации - ссылка на организацию.
//
// Возвращаемое значение:
//   Структура - см. НовыйПараметрыФормированияЧека()
//
Функция ПараметрыФормированияЧека(ДокументОснование, Организация) Экспорт
	
	МенеджерОснования = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументОснование);
	Возврат МенеджерОснования.ПараметрыФормированияЧекаНПД(ДокументОснование, Организация);
	
КонецФункции

// Возвращает цену услуги НПД для организации.
//
// Параметры:
//  УслугаНПД	 - СправочникСсылка.Номенклатура - Услуга, по которой нужно определить цену.
//  Организация	 - СправочникСсылка.Организация - Организация на НПД, для которой нужно вернуть цену услуги.
//  Дата		 - Дата - Дата, на которую нужно вернуть цену.
// 
// Возвращаемое значение:
//   - Число - цена услуги НПД.
//
Функция ЦенаУслугиНПД(УслугаНПД, Организация, Дата) Экспорт
	
	ДанныеОбъекта = Новый Структура();
	ДанныеОбъекта.Вставить("Дата", Дата);
	ДанныеОбъекта.Вставить("Организация", Организация);
	ДанныеОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам);
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", СуммаВключаетНДСДляНПД());
	ДанныеОбъекта.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС);
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		УслугаНПД, ДанныеОбъекта, Ложь);
	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат СведенияОНоменклатуре.Цена;
	
КонецФункции

// Сохраняет цену услуги НПД при записи документа.
//
// Параметры:
//  Объект	 - ДокументСсылка - Документ, в котором указывается цена НПД.
//             В модуле менеджера данного документа должна быть реализована функция ТекстЗапросаДанныеДляОбновленияЦенДокументов().
// 
Процедура СохранитьЦенуУслугиНПД(Объект) Экспорт
	
	Ценообразование.ОбновитьЦеныНоменклатуры(Объект.Ссылка,
		Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам,
		Объект.ВалютаДокумента,
		СуммаВключаетНДСДляНПД());
	
КонецПроцедуры

// Возвращает контактную информацию для отправки чека.
//
// Параметры:
//  Контрагент	 - СправочникСсылка.Контрагенты - Контрагент, для которого возвращается контактная информация.
// 
// Возвращаемое значение:
//   - Структура - контактная информация для отправки чека.
//
Функция ПараметрыОтправкиЧека(Контрагент) Экспорт
	
	ПараметрыОтправки = Новый Структура;
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат ПараметрыОтправки;
	КонецЕсли;
	
	Если ТипЗнч(Контрагент) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат ПараметрыОтправки;
	КонецЕсли;
	
	АдресЭлектроннойПочты = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияНаДату(
		Контрагент, Справочники.ВидыКонтактнойИнформации.EmailКонтрагенты);
	
	Если ЗначениеЗаполнено(АдресЭлектроннойПочты.Представление) Тогда
		ПараметрыОтправки.Вставить("АдресЭлектроннойПочты", АдресЭлектроннойПочты.Представление);
	КонецЕсли;
	
	Телефон = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияНаДату(
		Контрагент, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
	
	Если ЗначениеЗаполнено(Телефон.Представление) Тогда
		ПараметрыОтправки.Вставить("НомерТелефона", Телефон.Представление);
	КонецЕсли;
	
	Возврат ПараметрыОтправки;
	
КонецФункции

// Процедура "УстановитьВДинамическомСпискеПредставленияАннулированныхЧеков"
//
// Параметры:
//  Строки - СтрокиДинамическогоСписка - в которых необходимо изменить представление поля "НомерЧека"
//
Процедура УстановитьВДинамическомСпискеПредставленияАннулированныхЧеков(Строки) Экспорт
	
	Если ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые() Тогда
		СостояниеАннулирован = Перечисления.СостоянияЧековНПД.Аннулирован;
		Для Каждого Строка Из Строки Цикл
			ДанныеСтроки = Строка.Значение.Данные;
			Если ДанныеСтроки.Состояние = СостояниеАннулирован
				Или ДанныеСтроки.ПроизведенВозвратПоЧеку Тогда
				ДанныеСтроки.НомерЧека = РегистрыСведений.ЧекиНПД.ПредставлениеАннулированногоЧека(
					ДанныеСтроки.НомерЧека, ДанныеСтроки.ДатаАннулированияЧека);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиОбновления

// Удаляет дату обновления чеков НПД из хранилища за ненадобностью
//
Процедура ОчиститьДатуОбновленияЧековНПД() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначенияБП.УдалитьДанныеИзХранилища(Выборка.Ссылка, "ДатаОбновленияЧековНПД");
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НачатьОбновлениеСтатусовОфлайнЧековНПД(Организация) Экспорт
	
	ОфлайнЧеки = РегистрыСведений.ЧекиНПД.ЧекиОжидающиеОтправкуВФНС(Организация);
	
	Если Не ЗначениеЗаполнено(ОфлайнЧеки) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыМетода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия("СтатусыЧеков");
	ПараметрыМетода.Организация = Организация;
	ПараметрыМетода.Чеки = ОфлайнЧеки;
	
	Запрос = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	
	РезультатЗапроса = Неопределено;
	ТекстОшибки = "";
	
	Если Не Запрос.Ошибка Тогда
		ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполненияФункцииВФоне(Запрос, РезультатЗапроса, ТекстОшибки);
	Иначе
		ТекстОшибки = Запрос.Сообщение;
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'При обновлении статусов офлайн чеков возникла ошибка: %1'"),
			ТекстОшибки);
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если РезультатЗапроса <> Неопределено
		И ТипЗнч(РезультатЗапроса) = Тип("Структура") Тогда
		
		Результат = ИзменитьСтатусыОфлайнЧеков(Организация, РезультатЗапроса);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбновитьСведенияОСформированныхЧекахНПДВФоне(Организация, НачалоПериода, КонецПериода) Экспорт
	
	ПараметрыМетода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия("СписокАннулированныхЧеков");
	ПараметрыМетода.Организация = Организация;
	ПараметрыМетода.НачалоПериода = НачалоПериода;
	ПараметрыМетода.КонецПериода = КонецПериода;
	
	Запрос = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	
	РезультатЗапроса = Неопределено;
	ТекстОшибки = "";
	
	Если Не Запрос.Ошибка Тогда
		ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполненияФункцииВФоне(Запрос, РезультатЗапроса, ТекстОшибки);
	Иначе
		ТекстОшибки = Запрос.Сообщение;
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'При обновлении сведений о чеках возникла ошибка: %1'"),
			ТекстОшибки);
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПоследняяДатаАннулирования = Неопределено;
	
	Если РезультатЗапроса <> Неопределено
		И ТипЗнч(РезультатЗапроса) = Тип("Структура")
		И ЗначениеЗаполнено(РезультатЗапроса.СписокАннулированныхЧеков) Тогда
		
		Результат = РегистрыСведений.ЧекиНПД.ОбновитьЧекиПоСпискуАннулированныхВФоне(
			РезультатЗапроса.СписокАннулированныхЧеков, Организация);
			
		РезультатЗапроса.СписокАннулированныхЧеков.Сортировать("ДатаАннулирования Убыв");
		ПоследняяДатаАннулирования = РезультатЗапроса.СписокАннулированныхЧеков[0].ДатаАннулирования;
	КонецЕсли;
	
	Если ПоследняяДатаАннулирования <> Неопределено Тогда
		РегистрыСведений.ПараметрыАннулированныхЧековМойНалог.ОбновитьДатуОкончания(Организация, ПоследняяДатаАннулирования);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбновитьСведенияОЧекеНПДВФоне(СведенияОЧеке) Экспорт
	
	ПараметрыМетода = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия("СписокДоходов");
	ПараметрыМетода.Организация = СведенияОЧеке.Организация;
	ПараметрыМетода.НачалоПериода = НачалоДня(СведенияОЧеке.ДатаЧека);
	ПараметрыМетода.КонецПериода = КонецДня(СведенияОЧеке.ДатаЧека);
	
	Запрос = ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	
	РезультатЗапроса = Неопределено;
	ТекстОшибки = "";
	
	Если Не Запрос.Ошибка Тогда
		ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполненияФункцииВФоне(Запрос, РезультатЗапроса, ТекстОшибки);
	Иначе
		ТекстОшибки = Запрос.Сообщение;
		КомментарийЖурналаРегистрации = СтрШаблон(
			НСтр("ru = 'При обновлении сведений о чеках возникла ошибка: %1'"),
			ТекстОшибки);
		ЗаписьЖурналаРегистрации(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			КомментарийЖурналаРегистрации);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", ТекстОшибки);
	Результат.Вставить("НовыеСведенияОЧеке", Неопределено);
	
	Если Не ПустаяСтрока(Результат.ТекстОшибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если РезультатЗапроса <> Неопределено
		И ТипЗнч(РезультатЗапроса) = Тип("Структура") Тогда
		
		Для Каждого КлючИЗначение Из РезультатЗапроса Цикл 
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("ТаблицаЗначений") Тогда
				ТаблицаДоходов = КлючИЗначение.Значение;
				ОтборЧека = Новый Структура("ИдентификаторЧека", СведенияОЧеке.НомерЧека);
				Доход = ТаблицаДоходов.НайтиСтроки(ОтборЧека);
				Если Доход.Количество() > 0 И Доход[0].ЧекОтменен Тогда
					НовыеСведенияОЧеке = РегистрыСведений.ЧекиНПД.НовыйСведенияОбАннулированномЧеке();
					НовыеСведенияОЧеке.НомерЧека = Доход[0].ИдентификаторЧека;
					НовыеСведенияОЧеке.ДатаАннулированияЧека = Доход[0].ДатаОтмены;
					НовыеСведенияОЧеке.АдресЧекаНаСайте = Доход[0].Ссылка;
					ЧекОбновлен = РегистрыСведений.ЧекиНПД.ОбновитьЧекВФоне(СведенияОЧеке, НовыеСведенияОЧеке);
					Если ЧекОбновлен Тогда
						Результат.НовыеСведенияОЧеке = СведенияОЧеке;
						Возврат Результат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ДатаОбновленияСформированныхЧековНПД(Организация) Экспорт
	
	Возврат РегистрыСведений.ПараметрыАннулированныхЧековМойНалог.ДатаОкончания(Организация);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СуммаВключаетНДСДляНПД()
	
	Возврат Истина;
	
КонецФункции

Функция НовыйТаблицаУслуг()
	
	ТаблицаУслуг = Новый ТаблицаЗначений;
	ТаблицаУслуг.Колонки.Добавить("Услуга", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаУслуг.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТаблицаУслуг.Колонки.Добавить("Цена", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Возврат ТаблицаУслуг;
	
КонецФункции

Функция ИзменитьСтатусыОфлайнЧеков(Организация, СведенияОЧеках)
	
	ОбновленныеОфлайнЧеки = Новый Массив;
	
	Если Не ЗначениеЗаполнено(СведенияОЧеках) Тогда
		Возврат ОбновленныеОфлайнЧеки;
	КонецЕсли;
	
	СтатусыЧеков = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЧеков();
	
	Для Каждого СведенияОЧеке Из СведенияОЧеках.СтатусыЧеков Цикл
		
		Если Не СведенияОЧеке.ЧекOffLine Тогда
			Продолжить; // Обрабатываем только офлайн чеки.
		КонецЕсли;
		
		Если ВРег(СведенияОЧеке.Статус) = ВРег(СтатусыЧеков.ОжидаетОтправкиВФНС) Тогда
			Продолжить; // Статус не изменился. Чек по-прежнему не передан в ФНС.
		КонецЕсли;
		
		РегистрыСведений.ЧекиНПД.ОбновитьОфлайнЧекВФоне(Организация, СведенияОЧеке);
		
		ОбновленныеОфлайнЧеки.Добавить(СведенияОЧеке.ИдентификаторЧека);
		
	КонецЦикла;
	
	Возврат ОбновленныеОфлайнЧеки;
	
КонецФункции

#КонецОбласти
