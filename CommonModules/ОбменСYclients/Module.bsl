
#Область ПрограммныйИнтерфейс

Функция ЗагрузитьПродажи(Настройка) Экспорт
	
	МассивДокументов = Новый Массив;
	
	ДанныеЗагрузки = Новый Структура;
	ДанныеЗагрузки.Вставить("Результат",             Ложь);
	ДанныеЗагрузки.Вставить("СообщениеОбОшибке",     "");
	ДанныеЗагрузки.Вставить("ДанныеДокументов",      МассивДокументов);
	ДанныеЗагрузки.Вставить("company_id",            Настройка.company_id);
	ДанныеЗагрузки.Вставить("User_Login",            Настройка.User_Login);
	ДанныеЗагрузки.Вставить("ОшибкаАвторизации",     Ложь);
	ДанныеЗагрузки.Вставить("ДатаПоследнейЗагрузки", Настройка.ДатаКонцаЗагрузки);
	
	Токены = РегистрыСведений.НастройкиYclients.UserToken(Настройка.User_Login);
	Если Токены = Неопределено Тогда
		ДанныеЗагрузки.ОшибкаАвторизации = Истина;
		ДанныеЗагрузки.СообщениеОбОшибке = СтрШаблон(Нстр("ru='Не удалось получить данные аутентификации пользователя %1 в системе Yclients'"), Настройка.User_Login);
		Возврат ДанныеЗагрузки;
	КонецЕсли;
	
	ДанныеПродаж  = ПолучитьДанныеПродаж(Настройка, Токены, ДанныеЗагрузки.СообщениеОбОшибке, ДанныеЗагрузки.ОшибкаАвторизации);
	ТаблицаДокументов = ДанныеПродаж.Продажи.Скопировать();
	ТаблицаДокументов.Свернуть("Организация, Склад, Дата");
	
	//Добавим продажи без товаров и услуг (только сертификаты, абонементы и предоплаты)
	Для Каждого СтрокаОплат Из ДанныеПродаж.Оплаты Цикл
		
		Если (СтрокаОплат.ЗачетПредоплаты = 0 ИЛИ Не ЗначениеЗаполнено(Настройка.Покупатель))
			И (СтрокаОплат.Предоплата = 0 ИЛИ Не ЗначениеЗаполнено(Настройка.Покупатель))
			И (СтрокаОплат.ОплатаКартой = 0 ИЛИ Не ЗначениеЗаполнено(Настройка.ВидОплатыКартой)) Тогда
			//Есть безналичные оплаты, но их отражение отключено
			Продолжить;
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура("Организация, Склад, Дата", СтрокаОплат.Организация, СтрокаОплат.Склад, СтрокаОплат.Дата);
		Если ТаблицаДокументов.НайтиСтроки(СтруктураОтбора).Количество() = 0 Тогда
			СтрокаДокумента = ТаблицаДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаОплат);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаДокументов.Сортировать("Дата, Организация, Склад");
		
	МассивИдентификаторов = ДанныеПродаж.Продажи.ВыгрузитьКолонку("Идентификатор");
	МассивИдентификаторов = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивИдентификаторов);
	ИдентификаторыНоменклатуры = НоменклатураПоИдентификаторам(МассивИдентификаторов);
	
	НоменклатурыДляСоспоставления = НовыйТаблицаНоменклатуры();
		
	//Создание документов
	Для Каждого Документ Из ТаблицаДокументов Цикл
		Отбор = Новый Структура("Организация, Склад, Дата", Документ.Организация, Документ.Склад, Документ.Дата);
		СтрокиДокумента = ДанныеПродаж.Продажи.НайтиСтроки(Отбор);
		
		НоменклатурыДляСоспоставления.Очистить();
		ДокументСсылка = НайтиСуществующийДокумент(Отбор, Настройка);
		
		Если ДокументСсылка = Неопределено Тогда
			ДокументОбъект =  Документы.ОтчетОРозничныхПродажах.СоздатьДокумент();
			ТипСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отбор.Склад, "ТипСклада");
			ДокументОбъект.ВидОперации      = ?(ТипСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка, 
					Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах,
					Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетККМОПродажах);
			ДокументОбъект.СчетКасса        = ПланыСчетов.Хозрасчетный.КассаОрганизации;
			ДокументОбъект.СуммаВключаетНДС = Истина;
			ДокументОбъект.ВалютаДокумента  = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
			ДокументОбъект.Комментарий      = СтрШаблон(НСтр("ru='Продажи из Yclients по компании ""%1""'"), Настройка.company_title);
			ДокументОбъект.Организация      = Отбор.Организация;
			ДокументОбъект.Склад            = Отбор.Склад;
			ДокументОбъект.Дата             = КонецДня(Отбор.Дата);
			ДокументОбъект.ЗагрузкаИзYclients = Истина;
		Иначе
			ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			Исключение
				ДанныеЗагрузки.СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Возврат ДанныеЗагрузки;
			КонецПопытки;
		
			ДокументОбъект.Товары.Очистить();
			ДокументОбъект.Оплата.Очистить();
			ДокументОбъект.Предоплаты.Очистить();
			ДокументОбъект.ЗачетАвансов.Очистить();
		КонецЕсли;
		
		СтрокиДляЗаполненияСчетовТовары          = Новый Массив;
		СтрокиДляЗаполненияСчетовПредоплата      = Новый Массив;
		СтрокиДляЗаполненияСчетовЗачетПредоплаты = Новый Массив;
		
		Для Каждого СтрокаДокумента Из СтрокиДокумента Цикл
			НоваяСтрока = ДокументОбъект.Товары.Добавить();
			НоваяСтрока.Номенклатура = ИдентификаторыНоменклатуры.Получить(СтрокаДокумента.Идентификатор);
			Если Не ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
				НесопоставленнаяНоменклатура = НоменклатурыДляСоспоставления.Добавить();
				НесопоставленнаяНоменклатура.НомерСтрокиЗаказа = НоваяСтрока.НомерСтроки;
				ЗаполнитьЗначенияСвойств(НесопоставленнаяНоменклатура, СтрокаДокумента, "Идентификатор, Наименование, Услуга");
			Иначе
				СтрокиДляЗаполненияСчетовТовары.Добавить(НоваяСтрока);
			КонецЕсли;
			НоваяСтрока.Количество = СтрокаДокумента.Количество;
			НоваяСтрока.Сумма 	   = СтрокаДокумента.Сумма;
		КонецЦикла;
		
		ДанныеОбъекта = Новый Структура("Дата, Организация, Реализация, ДокументБезНДС, ТипЦен, СуммаВключаетНДС, ВедетсяУчетПрослеживаемыхТоваров, ДеятельностьНаПатенте");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ДокументОбъект);
		ДанныеОбъекта.Реализация = Истина;
		ДанныеОбъекта.ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
			И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(ДокументОбъект.Дата);
		
		Если ДокументОбъект.Товары.Количество() > 0 Тогда
			Документы.ОтчетОРозничныхПродажах.ЗаполнитьСтрокиТЧЗагрузкаИзФайла(ДокументОбъект.Товары, "Товары", ДанныеОбъекта);
		КонецЕсли;
		
		ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента, Настройка.Покупатель, Документ.Организация, Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		
		СтавкаНДСПриРеализации = УчетНДСКлиентСервер.СтавкаНДСПриРеализации(ДокументОбъект.Дата, ДокументОбъект.Организация);
		
		Отбор = Новый Структура("Организация, Склад, Дата", Документ.Организация, Документ.Склад, Документ.Дата);
		ОплатыДокумента = ДанныеПродаж.Оплаты.НайтиСтроки(Отбор);
		Для Каждого СтрокаОплаты Из ОплатыДокумента Цикл
			Если СтрокаОплаты.Предоплата > 0 И ЗначениеЗаполнено(Настройка.Покупатель) Тогда
				НоваяСтрока = ДокументОбъект.Предоплаты.Добавить();
				НоваяСтрока.Контрагент = Настройка.Покупатель;
				НоваяСтрока.ДоговорКонтрагента = ДоговорКонтрагента;
				НоваяСтрока.Сумма      = СтрокаОплаты.Предоплата;
				НоваяСтрока.СтавкаНДС = СтавкаНДСПриРеализации;
				СтрокиДляЗаполненияСчетовПредоплата.Добавить(НоваяСтрока);
			КонецЕсли;
			Если СтрокаОплаты.ЗачетПредоплаты > 0 И ЗначениеЗаполнено(Настройка.Покупатель) Тогда
				НоваяСтрока = ДокументОбъект.ЗачетАвансов.Добавить();
				НоваяСтрока.Контрагент  = Настройка.Покупатель;
				НоваяСтрока.ДоговорКонтрагента = ДоговорКонтрагента;
				НоваяСтрока.СуммаЗачета = СтрокаОплаты.ЗачетПредоплаты;
				СтрокиДляЗаполненияСчетовЗачетПредоплаты.Добавить(НоваяСтрока);
			КонецЕсли;
			Если СтрокаОплаты.ОплатаКартой > 0 И ЗначениеЗаполнено(Настройка.ВидОплатыКартой) Тогда
				НоваяСтрока = ДокументОбъект.Оплата.Добавить();
				НоваяСтрока.ВидОплаты   = Настройка.ВидОплатыКартой;
				НоваяСтрока.СуммаОплаты = СтрокаОплаты.ОплатаКартой;
			КонецЕсли;
		КонецЦикла;
			
		ЗаполнениеДокументов.Заполнить(ДокументОбъект);
		
		// Заполнение счетов учета
		СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляЗаполненияСчетовТовары, "Товары", ДокументОбъект, Документы.ОтчетОРозничныхПродажах);
		СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляЗаполненияСчетовПредоплата, "Предоплаты", ДокументОбъект, Документы.ОтчетОРозничныхПродажах);
		СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляЗаполненияСчетовЗачетПредоплаты, "ЗачетАвансов", ДокументОбъект, Документы.ОтчетОРозничныхПродажах);
	
		Попытка
			ДокументОбъект.Записать();
		Исключение
			ДанныеЗагрузки.СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Возврат ДанныеЗагрузки;
		КонецПопытки;
		
		Если НоменклатурыДляСоспоставления.Количество() > 0 Тогда
			НовыеОбъекты = РегистрыСведений.НесопоставленныеОбъектыИнтернетМагазина.СоздатьМенеджерЗаписи();
			НовыеОбъекты.Документ = ДокументОбъект.Ссылка;
			НовыеОбъекты.ДанныеОбъекта = Новый ХранилищеЗначения(НоменклатурыДляСоспоставления, Новый СжатиеДанных(9));
			НовыеОбъекты.Записать();
		КонецЕсли;
		
		МассивДокументов.Добавить(ДокументОбъект.Ссылка);
		
	КонецЦикла;
	
	ДанныеЗагрузки.Результат = Истина;
	
	Возврат ДанныеЗагрузки;
	
КонецФункции

Функция НовыйТаблицаПродаж()
	
	ТаблицаПродаж = Новый ТаблицаЗначений;
	ТаблицаПродаж.Колонки.Добавить("Организация",    Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаПродаж.Колонки.Добавить("Склад",          Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаПродаж.Колонки.Добавить("Дата",           ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаПродаж.Колонки.Добавить("Идентификатор",  ОбщегоНазначения.ОписаниеТипаЧисло(15));
	ТаблицаПродаж.Колонки.Добавить("Наименование",   ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаПродаж.Колонки.Добавить("Услуга",         Новый ОписаниеТипов("Булево"));
	ТаблицаПродаж.Колонки.Добавить("Количество",     ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТаблицаПродаж.Колонки.Добавить("Сумма",          ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Возврат ТаблицаПродаж;
	
КонецФункции

Функция НовыйТаблицаОплат()
	
	ТаблицаОплат = Новый ТаблицаЗначений;
	ТаблицаОплат.Колонки.Добавить("Организация",     Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаОплат.Колонки.Добавить("Склад",           Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаОплат.Колонки.Добавить("Дата",            ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаОплат.Колонки.Добавить("ОплатаКартой",    ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаОплат.Колонки.Добавить("Предоплата",      ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаОплат.Колонки.Добавить("ЗачетПредоплаты", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Возврат ТаблицаОплат;
	
КонецФункции

Функция НовыйТаблицаНоменклатуры() Экспорт
	
	ТаблицаНоменклатуры = Новый ТаблицаЗначений;
	ТаблицаНоменклатуры.Колонки.Добавить("Номенклатура",      Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаНоменклатуры.Колонки.Добавить("НомерСтрокиЗаказа", ОбщегоНазначения.ОписаниеТипаЧисло(15));
	ТаблицаНоменклатуры.Колонки.Добавить("Идентификатор",     ОбщегоНазначения.ОписаниеТипаЧисло(15));
	ТаблицаНоменклатуры.Колонки.Добавить("Наименование",      ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаНоменклатуры.Колонки.Добавить("Артикул",           ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаНоменклатуры.Колонки.Добавить("Услуга",            Новый ОписаниеТипов("Булево")); 
	ТаблицаНоменклатуры.Колонки.Добавить("Единица",           ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаНоменклатуры.Колонки.Добавить("Сертификат",        Новый ОписаниеТипов("Булево"));
	ТаблицаНоменклатуры.Колонки.Добавить("Абонимент",         Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаНоменклатуры;
	
КонецФункции

Функция ПолучитьДанныеПродаж(Настройка, Токены, СообщениеОбОшибке, ОшибкаАвторизации)

	ТаблицаПродаж = НовыйТаблицаПродаж();
	ТаблицаОплат  = НовыйТаблицаОплат();
	
	ИдДокументов = Новый Соответствие();
	
	//Финансовые транзакции
	ДанныеОтвета = ПолучитьФинансовыеТранзакции(Настройка, Токены, СообщениеОбОшибке, ОшибкаАвторизации);
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Новый Структура("Продажи, Оплаты", ТаблицаПродаж, ТаблицаОплат);
	КонецЕсли;
	
	Если ДанныеОтвета.success Тогда
		Для Каждого СтрокаОтчета Из ДанныеОтвета.Data Цикл
			ИдДокументов.Вставить(СтрокаОтчета.document_id, НачалоДня(СтрокаОтчета.Date));
		КонецЦикла;
	КонецЕсли;
	
	//Товарные транзакции
	ДанныеОтвета = ПолучитьТоварныеТранзакции(Настройка, Токены, СообщениеОбОшибке, ОшибкаАвторизации);
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Новый Структура("Продажи, Оплаты", ТаблицаПродаж, ТаблицаОплат);
	КонецЕсли;
	
	Если ДанныеОтвета.success Тогда
		Для Каждого СтрокаОтчета Из ДанныеОтвета.Data Цикл
			Если НЕ СтрокаОтчета.type_id = 1 Тогда //Продажа товара
				Продолжить;
			КонецЕсли;
			ИдДокументов.Вставить(СтрокаОтчета.document_id, НачалоДня(СтрокаОтчета.create_date));
		КонецЦикла;
	КонецЕсли;
	
	//Записи
	ДанныеОтвета = ПолучитьЗаписи(Настройка, Токены, СообщениеОбОшибке, ОшибкаАвторизации);
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Новый Структура("Продажи, Оплаты", ТаблицаПродаж, ТаблицаОплат);
	КонецЕсли;
	
	Если ДанныеОтвета.Получить("success") Тогда
		Для Каждого СтрокаОтчета Из ДанныеОтвета.Получить("data") Цикл
			Для Каждого СтрокаДокумент Из СтрокаОтчета.Получить("documents") Цикл
				Если НЕ СтрокаДокумент.Получить("type_id") = 7 Тогда //Визит
					Продолжить;
				КонецЕсли;
				Если НЕ СтрокаОтчета.Получить("visit_attendance") = 1 Тогда //Клиент пришел, услуга оказана
					Продолжить;
				КонецЕсли;
				ИдДокументов.Вставить(СтрокаДокумент.Получить("id"), НачалоДня(СтрокаОтчета.Получить("datetime")));
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Продажа Из ИдДокументов Цикл
		
		ИменаПолейДата = "";
		МетодЗапроса   = СтрШаблон("api/v1/company/%1/sale/%2", Формат(Настройка.company_id, "ЧГ=0"), Формат(Продажа.Ключ, "ЧГ=0"));
		Таймаут        = 240;
		СтруктураURI   = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api.yclients.com");
		
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
		ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
		HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , , ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
		
		ЗаголовкиHTTPЗапроса = Новый Соответствие();
		ЗаголовкиHTTPЗапроса.Вставить("Accept",       "application/vnd.api.v2+json");
		ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json");
		ЗаголовкиHTTPЗапроса.Вставить("Authorization", СтрШаблон("%1,%2", Токены.Bearer, Токены.User));
		
		HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
		Попытка 
			HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос); // GET
		Исключение
			Продолжить; //не найден документ продажи (или это не продажа)
		КонецПопытки;
		
		КодОтвета = HTTPОтвет.КодСостояния;
		Если КодОтвета <> 200 Тогда
			Продолжить; //не найден документ продажи (или это не продажа)
		КонецЕсли;
		
		Попытка
			ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(HTTPОтвет.ПолучитьТелоКакСтроку(), ИменаПолейДата, Ложь);
		Исключение
			Продолжить; //не найден документ продажи (или это не продажа)
		КонецПопытки;
		
		ПродажаСертификатов = Новый Соответствие;
		ОплатаБонусами      = Новый Соответствие;
		
		Если ДанныеОтвета.success Тогда
			Для Каждого СтрокаОплаты Из ДанныеОтвета.Data.state.loyalty_transactions Цикл
				Если СтрокаОплаты.type_id = 9            //оплата абонементом
					ИЛИ СтрокаОплаты.type_id = 8         //оплата сертификатом
					ИЛИ СтрокаОплаты.type_id = 11 Тогда  //списание с депозита
					
					НоваяСтрока = ТаблицаОплат.Добавить();
					НоваяСтрока.Организация     = Настройка.Организация;
					НоваяСтрока.Склад           = Настройка.Склад;
					НоваяСтрока.Дата            = Продажа.Значение; 
					НоваяСтрока.ЗачетПредоплаты = СтрокаОплаты.amount;
				Иначе //скидка (баллы, бонусы)
					Если ОплатаБонусами.Получить(СтрокаОплаты.sale_item_id) = Неопределено Тогда
						ОплатаБонусами.Вставить(СтрокаОплаты.sale_item_id, СтрокаОплаты.amount);
					Иначе
						ОплатаБонусами[СтрокаОплаты.sale_item_id] = ОплатаБонусами[СтрокаОплаты.sale_item_id] + СтрокаОплаты.amount;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого СтрокаОплаты Из ДанныеОтвета.Data.state.payment_transactions Цикл
				Если СтрокаОплаты.expense_id = 10            //Пополнение депозита
					ИЛИ СтрокаОплаты.expense_id = 6          //Продажа абонемента
					ИЛИ СтрокаОплаты.expense_id = 12 Тогда   //Продажа сертификата
					
					НоваяСтрока = ТаблицаОплат.Добавить();
					НоваяСтрока.Организация     = Настройка.Организация;
					НоваяСтрока.Склад           = Настройка.Склад;
					НоваяСтрока.Дата            = Продажа.Значение; 
					НоваяСтрока.Предоплата      = СтрокаОплаты.amount;
					
					//Проданные сертификаты и абонементы надо исключить из товаров
					Если ПродажаСертификатов.Получить(СтрокаОплаты.sale_item_id) = Неопределено Тогда
						ПродажаСертификатов.Вставить(СтрокаОплаты.sale_item_id, СтрокаОплаты.amount);
					Иначе
						ПродажаСертификатов[СтрокаОплаты.sale_item_id] = ПродажаСертификатов[СтрокаОплаты.sale_item_id] + СтрокаОплаты.amount;
					КонецЕсли;
				КонецЕсли;
				Если НЕ СтрокаОплаты.account.is_cash Тогда
					НоваяСтрока = ТаблицаОплат.Добавить();
					НоваяСтрока.Организация     = Настройка.Организация;
					НоваяСтрока.Склад           = Настройка.Склад;
					НоваяСтрока.Дата            = Продажа.Значение; 
					НоваяСтрока.ОплатаКартой    = СтрокаОплаты.amount;
				КонецЕсли;
			КонецЦикла;
			
			Если ДанныеОтвета.Data.state.loyalty_transactions.Количество() = 0
				И ДанныеОтвета.Data.state.payment_transactions.Количество() = 0 Тогда
				Продолжить; //Не оплаченный или отменный визит
			КонецЕсли;
			
			Для Каждого СтрокаПродажи Из ДанныеОтвета.Data.state.items Цикл
				
				Если НЕ ПродажаСертификатов.Получить(СтрокаПродажи.id) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				СуммаБонусов = ОплатаБонусами.Получить(СтрокаПродажи.id);
				СуммаБонусов = ?(СуммаБонусов = Неопределено, 0, Мин(СуммаБонусов, СтрокаПродажи.cost_to_pay_total));
				
				НоваяСтрока = ТаблицаПродаж.Добавить();
				НоваяСтрока.Организация    = Настройка.Организация;
				НоваяСтрока.Склад          = Настройка.Склад;
				НоваяСтрока.Дата           = Продажа.Значение;
				НоваяСтрока.Идентификатор  = СтрокаПродажи.item_id;
				НоваяСтрока.Наименование   = СтрокаПродажи.title;
				НоваяСтрока.Услуга         = СтрокаПродажи.type = "service";
				НоваяСтрока.Количество     = СтрокаПродажи.amount;
				НоваяСтрока.Сумма          = СтрокаПродажи.cost_to_pay_total - СуммаБонусов;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаОплат.Свернуть("Организация, Склад, Дата", "ОплатаКартой, Предоплата, ЗачетПредоплаты");
	ТаблицаПродаж.Свернуть("Организация, Склад, Дата, Идентификатор, Наименование, Услуга", "Количество, Сумма");
	
	Возврат Новый Структура("Продажи, Оплаты", ТаблицаПродаж, ТаблицаОплат);

КонецФункции

Функция ПолучитьФинансовыеТранзакции(Настройка, Токены, СообщениеОбОшибке, ОшибкаАвторизации)

	МетодЗапроса   = "api/v1/transactions";
	ИменаПолейДата = "date, last_change_date";
	МетодЗапроса   = СтрШаблон("%1/%2", МетодЗапроса, Формат(Настройка.company_id, "ЧГ=0"));
	Таймаут        = 240;
	СтруктураURI   = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api.yclients.com");
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , , ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
	
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Accept",       "application/vnd.api.v2+json");
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", СтрШаблон("%1,%2", Токены.Bearer, Токены.User));
	
	СписокПараметровМетода = Новый Структура;
	СписокПараметровМетода.Вставить("start_date", Формат(КонецДня(Настройка.ДатаПоследнейЗагрузки) + 1, "ДФ=yyyy-MM-dd"));
	СписокПараметровМетода.Вставить("end_date",   Формат(Настройка.ДатаКонцаЗагрузки, "ДФ=yyyy-MM-dd"));
	ПараметрыСтрокой = Новый Массив;
	Для Каждого Параметр Из СписокПараметровМетода Цикл
		ПараметрыСтрокой.Добавить(Параметр.Ключ+"="+Параметр.Значение);
	КонецЦикла;
	МетодЗапроса = МетодЗапроса+ "?" + СтрСоединить(ПараметрыСтрокой,"&");
	
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	Попытка 
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос); // GET
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки;
	
	КодОтвета = HTTPОтвет.КодСостояния;
	Если КодОтвета <> 200 Тогда
		Если КодОтвета = 401 Тогда
			ОшибкаАвторизации = Истина;
		КонецЕсли;
		СообщениеОбОшибке = HTTPОтвет.ПолучитьТелоКакСтроку();
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(HTTPОтвет.ПолучитьТелоКакСтроку(), ИменаПолейДата, Ложь);
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных. Причина: %1'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ПолучитьТоварныеТранзакции(Настройка, Токены, СообщениеОбОшибке, ОшибкаАвторизации)

	МетодЗапроса   = "api/v1/storages/transactions";
	ИменаПолейДата = "create_date, last_change_date";
	МетодЗапроса   = СтрШаблон("%1/%2", МетодЗапроса, Формат(Настройка.company_id, "ЧГ=0"));
	Таймаут        = 240;
	СтруктураURI   = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api.yclients.com");
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , , ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
	
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Accept",       "application/vnd.api.v2+json");
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", СтрШаблон("%1,%2", Токены.Bearer, Токены.User));
	
	СписокПараметровМетода = Новый Структура;
	СписокПараметровМетода.Вставить("start_date", Формат(КонецДня(Настройка.ДатаПоследнейЗагрузки) + 1, "ДФ=yyyy-MM-dd"));
	СписокПараметровМетода.Вставить("end_date",   Формат(Настройка.ДатаКонцаЗагрузки, "ДФ=yyyy-MM-dd"));
	ПараметрыСтрокой = Новый Массив;
	Для Каждого Параметр Из СписокПараметровМетода Цикл
		ПараметрыСтрокой.Добавить(Параметр.Ключ+"="+Параметр.Значение);
	КонецЦикла;
	МетодЗапроса = МетодЗапроса+ "?" + СтрСоединить(ПараметрыСтрокой,"&");
	
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	Попытка 
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос); // GET
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки;
	
	КодОтвета = HTTPОтвет.КодСостояния;
	Если КодОтвета <> 200 Тогда
		Если КодОтвета = 401 Тогда
			ОшибкаАвторизации = Истина;
		КонецЕсли;
		СообщениеОбОшибке = HTTPОтвет.ПолучитьТелоКакСтроку();
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(HTTPОтвет.ПолучитьТелоКакСтроку(), ИменаПолейДата, Ложь);
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных. Причина: %1'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ПолучитьЗаписи(Настройка, Токены, СообщениеОбОшибке, ОшибкаАвторизации)

	МетодЗапроса   = "api/v1/records";
	ИменаПолейДата = "datetime, last_change_date";
	МетодЗапроса   = СтрШаблон("%1/%2", МетодЗапроса, Формат(Настройка.company_id, "ЧГ=0"));
	Таймаут        = 240;
	СтруктураURI   = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api.yclients.com");
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , , ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
	
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Accept",       "application/vnd.api.v2+json");
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTPЗапроса.Вставить("Authorization", СтрШаблон("%1,%2", Токены.Bearer, Токены.User));
	
	СписокПараметровМетода = Новый Структура;
	СписокПараметровМетода.Вставить("start_date", Формат(КонецДня(Настройка.ДатаПоследнейЗагрузки) + 1, "ДФ=yyyy-MM-dd"));
	СписокПараметровМетода.Вставить("end_date",   Формат(Настройка.ДатаКонцаЗагрузки, "ДФ=yyyy-MM-dd"));
	ПараметрыСтрокой = Новый Массив;
	Для Каждого Параметр Из СписокПараметровМетода Цикл
		ПараметрыСтрокой.Добавить(Параметр.Ключ+"="+Параметр.Значение);
	КонецЦикла;
	МетодЗапроса = МетодЗапроса+ "?" + СтрСоединить(ПараметрыСтрокой,"&");
	
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	Попытка 
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос); // GET
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки;
	
	КодОтвета = HTTPОтвет.КодСостояния;
	Если КодОтвета <> 200 Тогда
		Если КодОтвета = 401 Тогда
			ОшибкаАвторизации = Истина;
		КонецЕсли;
		СообщениеОбОшибке = HTTPОтвет.ПолучитьТелоКакСтроку();
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(HTTPОтвет.ПолучитьТелоКакСтроку(), ИменаПолейДата, Истина);
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных. Причина: %1'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция НайтиСуществующийДокумент(Отбор, Настройка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Отбор.Организация);
	Запрос.УстановитьПараметр("Склад",       Отбор.Склад);
	Запрос.УстановитьПараметр("Дата",        КонецДня(Отбор.Дата));
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Организация = &Организация
	|	И ОтчетОРозничныхПродажах.Склад = &Склад
	|	И ОтчетОРозничныхПродажах.Дата = &Дата
	|	И ОтчетОРозничныхПродажах.ЗагрузкаИзYclients
	|	И НЕ ОтчетОРозничныхПродажах.ПометкаУдаления";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
		
КонецФункции

Функция НоменклатураПоИдентификаторам(МассивИдентификаторов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураYclients.Номенклатура КАК Номенклатура,
	|	НоменклатураYclients.ИдентификаторНоменклатуры КАК Идентификатор
	|ИЗ
	|	РегистрСведений.НоменклатураYclients КАК НоменклатураYclients
	|ГДЕ
	|	НоменклатураYclients.ИдентификаторНоменклатуры В(&ИдентификаторыНоменклатуры)";
	
	Запрос.УстановитьПараметр("ИдентификаторыНоменклатуры", МассивИдентификаторов);
	Результат = Запрос.Выполнить().Выбрать();
	ИдентификаторыНоменклатуры = Новый Соответствие;
	
	Пока Результат.Следующий() Цикл
		ИдентификаторыНоменклатуры.Вставить(Результат.Идентификатор, Результат.Номенклатура);
	КонецЦикла;
	
	Возврат ИдентификаторыНоменклатуры;
	
КонецФункции

Функция НайтиНоменклатуруПоНаименованию(ТаблицаНоменклатуры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Наименование КАК Наименование,
	|	ТаблицаНоменклатуры.Артикул КАК Артикул,
	|	ТаблицаНоменклатуры.Идентификатор КАК Идентификатор,
	|	ТаблицаНоменклатуры.Единица КАК Единица,
	|	ТаблицаНоменклатуры.Услуга КАК Услуга
	|ПОМЕСТИТЬ ТабНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	ТабНоменклатуры.Идентификатор КАК Идентификатор
	|ИЗ
	|	ТабНоменклатуры КАК ТабНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО (ТабНоменклатуры.Наименование = Номенклатура.Наименование
	|				ИЛИ ТабНоменклатуры.Наименование = Номенклатура.НаименованиеПолное)
	|			И ТабНоменклатуры.Артикул = Номенклатура.Артикул
	|			И ТабНоменклатуры.Услуга = Номенклатура.Услуга
	|			И (НЕ Номенклатура.ЭтоГруппа)
	|			И (НЕ Номенклатура.ВАрхиве)
	|			И (НЕ Номенклатура.ПометкаУдаления)
	|ГДЕ
	|	Номенклатура.Ссылка <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить().Выбрать();
	ИдентификаторыНоменклатуры = Новый Соответствие;
	
	Пока Результат.Следующий() Цикл
		ИдентификаторыНоменклатуры.Вставить(Результат.Идентификатор, Результат.Ссылка);
	КонецЦикла;
	
	Возврат ИдентификаторыНоменклатуры;
	
КонецФункции

Процедура УдалитьДанныеДляСопоставления(ДокументСсылка) Экспорт
	НесопоставленныеОбъекты =РегистрыСведений.НесопоставленныеОбъектыИнтернетМагазина.СоздатьНаборЗаписей();
	НесопоставленныеОбъекты.Отбор.Документ.Установить(ДокументСсылка);
	НесопоставленныеОбъекты.Записать();
КонецПроцедуры

#КонецОбласти