#Область ПрограммныйИнтерфейс

// Конструктор строки списка Содержание задания
//
// Параметры:
//  ИНН - Строка - ИНН физического лица
//  НаДату - Дата - дата в часовом поясе "Europe/Moscow",
//           на которую необходимо получить статус самозанятого
// 
// Возвращаемое значение:
//  Структура - новая строка содержания задания:
//    * ИНН - Строка - ИНН физ. лица
//    * НаДату - Дата, Неопределено - дата, на которую выполняется проверка
//
Функция НоваяСтрокаСодержанияЗадания(ИНН, Знач НаДату) Экспорт
	
	СтрокаСодержания = Новый Структура();
	СтрокаСодержания.Вставить("ИНН", ИНН);
	СтрокаСодержания.Вставить("НаДату", НачалоДня(НаДату));
	
	Возврат СтрокаСодержания;
	
КонецФункции

// Конструктор строки списка результата выполнения задания
// 
// Возвращаемое значение:
//  Структура - новая строка результата выполнения задания:
//    * ИНН - Строка - ИНН физического лица
//    * НаДату - Дата - дата, на которую выполнялась проверка по часовому поясу
//               сервиса получения статусов самозанятых
//    * ПлательщикНПД - Булево - Истина, если физ.лицо применяет НПД
//    * ДатаРегистрации - Дата, Неопределено - дата изменения статуса по часовому поясу
//                        сервиса получения статусов самозанятых
//    * ЭтоОкончательноеЗначение - Булево - признак того, что повторно запрашивать значение из сервиса не требуется
//
Функция НоваяСтрокаРезультатаВыполненияЗадания() Экспорт
	
	СтрокаРезультатаВыполнения = Новый Структура();
	СтрокаРезультатаВыполнения.Вставить("ИНН", "");
	СтрокаРезультатаВыполнения.Вставить("НаДату", Дата(1, 1, 1));
	СтрокаРезультатаВыполнения.Вставить("ПлательщикНПД", Ложь);
	СтрокаРезультатаВыполнения.Вставить("ДатаРегистрации", Неопределено);
	СтрокаРезультатаВыполнения.Вставить("ЭтоОкончательноеЗначение", Ложь);
	
	Возврат СтрокаРезультатаВыполнения;
	
КонецФункции

// Конструктор статуса задания
// 
// Возвращаемое значение:
//  Структура - результат выполнения метода:
//    * Идентификатор - Строка - идентификтор задания
//    * Статус - Строка - статус задания
//
Функция НовыйСтатусЗадания() Экспорт
	
	СтатусЗадания = Новый Структура();
	СтатусЗадания.Вставить("Идентификатор", "");
	СтатусЗадания.Вставить("Статус", "");
	
	Возврат СтатусЗадания;
	
КонецФункции

// Конструктор настроек лимитов
// 
// Возвращаемое значение:
//  Структура - результат выполнения метода:
//    * МаксимальноеЧислоСтрокВЗадании - Число - максимальное число строк в одном задании
//    * ВремяЖизниЗадания - Число - время жизни задания в секундах
//    * ВремяКэшированияТекущегоСтатуса - Число - время кэширования текущего статуса самозанятого в секундах
//
Функция НовыйНастройкиЛимитов() Экспорт
	
	НастройкиЛимитов = Новый Структура();
	НастройкиЛимитов.Вставить("МаксимальноеЧислоСтрокВЗадании", 0);
	НастройкиЛимитов.Вставить("ВремяЖизниЗадания", 0);
	НастройкиЛимитов.Вставить("ВремяКэшированияТекущегоСтатуса", 0);
	
	Возврат НастройкиЛимитов;
	
КонецФункции

// Формирует представление статуса физического лица.
// 
// Параметры:
//  ПлательщикНПД - Булево - Булево - Истина, если физ.лицо применяет НПД
//  ДатаРегистрации - Дата - дата изменения статуса
// 
// Возвращаемое значение:
//  Строка - Описание статуса
//
Функция ОписаниеСтатуса(ПлательщикНПД, ДатаРегистрации) Экспорт
	
	Если ЗначениеЗаполнено(ДатаРегистрации) Тогда
		ПредставлениеДатыРегистрации = Формат(ДатаРегистрации, "ДЛФ=D;");
	КонецЕсли;
	
	Если ПлательщикНПД Тогда
		Если ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Описание = СтрШаблон(
				НСтр("ru = 'Контрагент %1 зарегистрирован в качестве плательщика налога на профессиональный доход (по московскому времени)'"),
				ПредставлениеДатыРегистрации);
		Иначе
			Описание = НСтр("ru = 'Контрагент является плательщиком налога на профессиональный доход'");
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Описание = СтрШаблон(
				НСтр("ru = 'Контрагент %1 утратил статус плательщика налога на профессиональный доход (по московскому времени)'"),
				ПредставлениеДатыРегистрации);
		Иначе
			Описание = НСтр("ru = 'Контрагент не является плательщиком налога на профессиональный доход'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает имя подсистемы
// 
// Возвращаемое значение:
//  Строка - Имя подсистемы
//
Функция ИмяПодсистемы() Экспорт
	
	Возврат "СтатусСамозанятого";
	
КонецФункции

// Возвращает синоним подсистемы
// 
// Возвращаемое значение:
//  Строка - Синоним подсистемы
//
Функция СинонимПодсистемы() Экспорт
	
	Возврат НСтр("ru = 'Статус самозанятого'", КодОсновногоЯзыка());
	
КонецФункции

// Имя события в журнале регистрации
//
// Параметры:
//  Детализация - Строка - уточнение события
// 
// Возвращаемое значение:
//   Строка - полное имя события
// 
Функция ИмяСобытияЖурналаРегистрации(Детализация = "") Экспорт
	
	Если ПустаяСтрока(Детализация) Тогда
		Возврат ИмяПодсистемы();
	Иначе
		Возврат СтрШаблон("%1.%2", СинонимПодсистемы(), Детализация);
	КонецЕсли;
	
КонецФункции

// Возвращает список параметров для вызова метода взаимодействия по его имени.
//
// Параметры:
//   МетодВзаимодействия - Строка - имя метода из списка методов ИнтеграцияСтатусСамозанятого.МетодыВзаимодействия()
//
// Возвращаемое значение:
//  Структура - ключом является имя параметра, значение - пустым значением нужного типа
//
Функция НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия) Экспорт
	
	ИменаМетодов = ИменаМетодовВзаимодействия();
	
	// API для работы с документами
	
	Если МетодВзаимодействия = ИменаМетодов.СоздатьТокенАвторизации Тогда
		
		//@skip-check constructor-function-return-section
		Возврат НовыеПараметры_СоздатьТокенАвторизации(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = ИменаМетодов.СоздатьЗадание Тогда
		
		//@skip-check constructor-function-return-section
		Возврат НовыеПараметры_СоздатьЗадание(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = ИменаМетодов.РезультатВыполненияЗадания Тогда
		
		//@skip-check constructor-function-return-section
		Возврат НовыеПараметры_РезультатВыполненияЗадания(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = ИменаМетодов.СтатусЗадания Тогда
		
		//@skip-check constructor-function-return-section
		Возврат НовыеПараметры_СтатусЗадания(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = ИменаМетодов.НастройкиЛимитов Тогда
		
		//@skip-check constructor-function-return-section
		Возврат НовыеПараметры(МетодВзаимодействия);
		
	Иначе
		ВызватьИсключение СтрШаблон("ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия():
			|Метод взаимодействия %1 не определен", МетодВзаимодействия);
	КонецЕсли;
	
КонецФункции

// Возвращает список имен методов взаимодействия.
// 
// Возвращаемое значение:
//  Структура - имена методов взаимодействия
//
Функция ИменаМетодовВзаимодействия() Экспорт
	
	ИменаМетодов = Новый Структура;
	ИменаМетодов.Вставить("СоздатьТокенАвторизации", "СоздатьТокенАвторизации");
	ИменаМетодов.Вставить("СоздатьЗадание", "СоздатьЗадание");
	ИменаМетодов.Вставить("РезультатВыполненияЗадания", "РезультатВыполненияЗадания");
	ИменаМетодов.Вставить("СтатусЗадания", "СтатусЗадания");
	ИменаМетодов.Вставить("НастройкиЛимитов", "НастройкиЛимитов");
	
	//@skip-check constructor-function-return-section
	Возврат ИменаМетодов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеПараметры(МетодВзаимодействия)
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("МетодВзаимодействия", МетодВзаимодействия);
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СоздатьТокенАвторизации(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("ИмяКонфигурации", "");
	ПараметрыМетода.Вставить("Тикет", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СоздатьЗадание(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("СодержаниеЗадания", Новый Массив());
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_РезультатВыполненияЗадания(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Идентификатор", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СтатусЗадания(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Идентификатор", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция КодОсновногоЯзыка()
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	#Иначе
		КодЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
	#КонецЕсли
	
	Возврат КодЯзыка;
	
КонецФункции

#КонецОбласти
