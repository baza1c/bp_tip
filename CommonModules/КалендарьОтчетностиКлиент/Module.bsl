
#Область ПрограммныйИнтерфейс

// Показывает форму помощника подготовки регламентированных отчетов, уведомлений ЕНС
// либо форму требования ФНС. Если требование нерасшифрованное, то запускается его расшифровка.
//
// Параметры:
//   Параметры - Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыПомощника,
//                           КалендарьОтчетностиКлиентСервер.НовыйПараметрыТребованияФНС
//
// Возвращаемое значение:
//   Строка - текст предупреждения, по какой причине не удалось открыть форму помощника;
//            если помощник был успешно открыт, возвращается пустая строка.
//
Функция ПоказатьПомощник(Параметры) Экспорт
	
	Событие = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
	ЗаполнитьЗначенияСвойств(Событие, Параметры);
	ОписаниеДействия = ВыполнениеЗадачБухгалтераВызовСервера.ОписаниеДействия(Событие);
	
	Если ОписаниеДействия.Свойство("Предупреждение") Тогда
		Возврат ОписаниеДействия.Предупреждение;
	КонецЕсли;
	ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	
	Возврат "";
КонецФункции

// Пробует выполнить переход по навигационной ссылке и показать помощник либо требование ФНС.
//
// Параметры:
//   ПараметрыСсылки -  Структура - структура переданных параметров.
//                      Чтобы переход по ссылке был выполнен, структура параметров должна соответствовать
//                      конструктору КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиПодготовкаОтчета,
//......................либо конструктору КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиТребованиеФНС.
//
// Возвращаемое значение:
//   Булево - Истина, если переход по ссылке был выполнен.
//
Функция ВыполненПереходПоНавигационнойСсылке(ПараметрыСсылки) Экспорт
	
	ПереходВыполнен = Ложь;
	КалендарьОтчетностиКлиентПереопределяемый.ПриВыполненииПереходаПоНавигационнойСсылке(ПараметрыСсылки, ПереходВыполнен);
	Возврат ПереходВыполнен;
	
КонецФункции

// Создает новое письмо по заданному шаблону
//
Процедура ОтправитьОтзывПоЭлектроннойПочте() Экспорт
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru='Отзыв о Календаре отчетности'");
	ПараметрыПисьма.Текст = НСтр("ru='Поделитесь вашим мнением о Календаре отчетности?
		|
		|
		|'");
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

// Выполняет оповещение Календаря отчетности при изменении статуса и комментария задачи.
//
// Параметры:
//   Задача - Структура - см. РегистрСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой()
//   Статус - Строка - новый статус задачи
//   Комментарий - Строка - новый комментарий к задаче
//   СтатусУстановленВручную - Булево - установлен ли новый статус задачи вручную пользователем
//
Процедура ОповеститьКалендарьОтчетности(Задача, Статус = "", Комментарий = "", СтатусУстановленВручную = Ложь) Экспорт
	
	// Календарь отчетности поддерживает не все виды действий.
	Если Не КалендарьОтчетностиКлиентСервер.ДействиеПоддерживается(Задача.Действие) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = НовыйПараметрыОповещенияКалендаряОтчетности();
	ПараметрыОповещения.ПараметрыПоискаОтчета = КалендарьОтчетностиКлиентСервер.НовыйПараметрыПоискаОтчета();
	ЗаполнитьЗначенияСвойств(ПараметрыОповещения.ПараметрыПоискаОтчета, Задача);
	ПараметрыОповещения.ПараметрыПоискаОтчета.РегистрацияФНС = Задача.РегистрацияВНалоговомОргане;
	ПараметрыОповещения.Статус = Статус;
	ПараметрыОповещения.СтатусУстановленВручную = СтатусУстановленВручную;
	ПараметрыОповещения.Комментарий = Комментарий;
	
	Оповестить(КалендарьОтчетностиКлиентСервер.ИмяСобытияОповещенияСпискаЗадач(), ПараметрыОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйПараметрыОповещенияКалендаряОтчетности()
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ПараметрыПоискаОтчета", Неопределено);
	ПараметрыОповещения.Вставить("Статус", "");
	ПараметрыОповещения.Вставить("СтатусУстановленВручную", Ложь);
	ПараметрыОповещения.Вставить("Комментарий", "");
	
	Возврат ПараметрыОповещения;
КонецФункции

#КонецОбласти