////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиИнтеграцияКлиентСобытия: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ПослеНачалаРаботыСистемы() Экспорт 
	
	Если Не ОбщегоНазначенияКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеОбменаСбанками = ОбменСБанкамиСлужебныйВызовСервера.ТекущееСостояниеИДанныеУведомленийОбменаСБанками();
	
	Если Не СостояниеОбменаСбанками.ЕстьПравоЧтенияДанных Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СостояниеОбменаСбанками.ИспользуетсяОбменСБанками Тогда
		Возврат;
	КонецЕсли;
	
	СостоянияПисем = СостояниеОбменаСбанками.ТекущееСостояниеПисем;
	
	Если СостоянияПисем.ЕстьНастройка Тогда
		
		ДанныеОНепрочитанныхПисьмах = СостоянияПисем.ДанныеОНепрочитанныхПисьмах;
		УведомленияОНепрочитанныхПисьмах(ДанныеОНепрочитанныхПисьмах);
		
		КоличествоНеотправленныхПисем = СостоянияПисем.КоличествоНеотправленныхПисем;
		УведомленияОбИсходящихНеотправленныхПисьмах(КоличествоНеотправленныхПисем);
		
	КонецЕсли;
	
	ТекущееСостояниеПоддержкиСессии = СостояниеОбменаСбанками.ТекущееСостояниеПоддержкиСессии;
	
	Если ТекущееСостояниеПоддержкиСессии.ЕстьНастройка Тогда
		
		ДанныеУведомленийДляПользователя = ТекущееСостояниеПоддержкиСессии.ДанныеДляУведомлений;
		ОбменСБанкамиКлиентПереопределяемый.УведомленияОбОтклоненныхДокументах(ДанныеУведомленийДляПользователя);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УведомленияОНовыхПисьмах(ПолученныеПисьма)
	
	ОтправителиПоБанкам = ОбменСБанкамиСлужебныйВызовСервера.РассчитатьПолученныеПисьмаПоБанкам(ПолученныеПисьма);
	Для Каждого СведенияОПисьмах Из ОтправителиПоБанкам Цикл
		ОповещениеДляОткрытияФормы = Новый ОписаниеОповещения("ДействиеИнформацияОПисьмах", ЭтотОбъект, "Входящие");
		СтруктураСообщения = ТекстОНовыхСообщениях(СведенияОПисьмах.КоличествоПисем, СведенияОПисьмах.НазваниеБанка);
		ПоказатьОповещениеПользователя(СтруктураСообщения.Текст,
			ОповещениеДляОткрытияФормы,
			СтруктураСообщения.Пояснение,
			БиблиотекаКартинок.ОповещениеОНовыхПисьмахОбменСБанками, 
			СтатусОповещенияПользователя.Важное,
			"УведомленияОНовыхПисьмахОбменСБанками");
	КонецЦикла;
	
КонецПроцедуры

Процедура УведомленияОНепрочитанныхПисьмах(СтруктураСостоянияПисем)
	
	ОповещениеДляОткрытияФормы = Новый ОписаниеОповещения("ДействиеИнформацияОПисьмах", ЭтотОбъект,"Входящие");
	СведенияОНепрочитанных = СтруктураСостоянияПисем.СведенияОНепрочитанных;
	
	Для Каждого СтрСведения Из СведенияОНепрочитанных Цикл
		ВсегоНепрочитанных = СтрСведения.Количество;
		Если ВсегоНепрочитанных = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтруктураСообщения = ТекстОНепрочитанныхСообщениях(ВсегоНепрочитанных, СтрСведения.Банк); 
		ПоказатьОповещениеПользователя(СтруктураСообщения.Текст,
			ОповещениеДляОткрытияФормы,
			СтруктураСообщения.Пояснение,
			БиблиотекаКартинок.ОповещениеОНовыхПисьмахОбменСБанками, 
			СтатусОповещенияПользователя.Важное,
			"УведомленияОНепрочитанныхПисьмахОбменСБанками");
	КонецЦикла;
	
КонецПроцедуры 

Процедура УведомленияОбИсходящихНеотправленныхПисьмах(ВсегоИсходящих)
	
	ОповещениеДляОткрытияФормы = Новый ОписаниеОповещения("ДействиеИнформацияОПисьмах", ЭтотОбъект,"Исходящие");
	
	ТекстОповещения = НСтр("ru = 'Проверьте папку ""Исходящие""'");
	Если ВсегоИсходящих > 0 Тогда
		ПоказатьОповещениеПользователя(
			ТекстОНеотправленныхИсходящих(ВсегоИсходящих),
			ОповещениеДляОткрытияФормы,
			ТекстОповещения,
			БиблиотекаКартинок.Предупреждение32, 
			СтатусОповещенияПользователя.Важное,
			"УведомленияОбИсходящихНеотправленныхПисьмахОбменСБанками");
	КонецЕсли;
	
КонецПроцедуры

// Уведомления об отклоненных документах.
// 
// Параметры:
//  ПользователиИДанныеУведомлений - Соответствие из КлючИЗначение: 
//    см. ОбменСБанкамиСлужебный.ДанныеДляУведомленийОбОтклоненныхДокументах.
//
Процедура УведомленияОбОтклоненныхДокументах(ПользователиИДанныеУведомлений) Экспорт
	
	ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
	ДанныеУведомлений = ПользователиИДанныеУведомлений.Получить(ТекущийПользователь);
	
	Если Не ЗначениеЗаполнено(ДанныеУведомлений) Тогда
		Возврат;
	КонецЕсли;
	
	СсылкиНаОбъектыУведомлений = Новый Массив;
	
	Для каждого ТекЭлемент Из ДанныеУведомлений Цикл
		ТекстУведомления = ТекстОбОтклоненныхДокументах(ТекЭлемент);
		ПоказатьОповещениеПользователя(
			ТекстУведомления.Текст,
			ПолучитьНавигационнуюСсылку(ТекЭлемент.СсылкаНаОбъект),
			ТекстУведомления.Пояснение,
			БиблиотекаКартинок.Предупреждение32, 
			СтатусОповещенияПользователя.Важное,
			ТекЭлемент.КлючУникальности);
		СсылкиНаОбъектыУведомлений.Добавить(ТекЭлемент.СсылкаНаОбъект);
	КонецЦикла;
	
	ОбменСБанкамиСлужебныйВызовСервера.ОтметитьОтправленныеУведомленияОбОтклоненныхДокументах(
		СсылкиНаОбъектыУведомлений);
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
КонецПроцедуры

Функция ТекстОНовыхСообщениях(ВсегоКоличество, НазваниеБанка)
	
	СклоняемыйТекст = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru=';1С:ДиректБанк - %1 новое письмо;;1С:ДиректБанк - %1 новых письма;1С:ДиректБанк - %1 новых писем;'"), 
		ВсегоКоличество);
	
	СтруктураСообщения = Новый Структура;
	СтруктураСообщения.Вставить("Текст", СклоняемыйТекст);
	СтруктураСообщения.Вставить("Пояснение", СтрШаблон(НСтр("ru='от %1'"), НазваниеБанка));
	
	Возврат СтруктураСообщения;
	
КонецФункции

Функция ТекстОНепрочитанныхСообщениях(ВсегоКоличество, НазваниеБанка)
	
	СклоняемыйТекст = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru=';1С:ДиректБанк - %1 непрочтенное письмо;;1С:ДиректБанк - %1 непрочитанных письма;1С:ДиректБанк - %1 непрочитанных писем;'"), 
		ВсегоКоличество);
	
	СтруктураСообщения = Новый Структура;
	СтруктураСообщения.Вставить("Текст",СклоняемыйТекст);
	СтруктураСообщения.Вставить("Пояснение", СтрШаблон("от %1",НазваниеБанка));
	
	Возврат СтруктураСообщения;
	
КонецФункции

Функция ТекстОНеотправленныхИсходящих(ВсегоКоличество)
	
	СклоняемыйТекст = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru=';1С:ДиректБанк - %1 неотправленное письмо;;1С:ДиректБанк - %1 неотправленных письма;1С:ДиректБанк - %1 неотправленных писем;'"), 
		ВсегоКоличество);
	
	Возврат СклоняемыйТекст;
	
КонецФункции

Функция ТекстОбОтклоненныхДокументах(ДанныеСобытия)
	
	Результат = Новый Структура;
	Текст = "";
	ПредставлениеДокумента = НСтр("ru='Документ №%1 от %2'");
	
	Если ДанныеСобытия.ВидЭД = ПредопределенноеЗначение(
		"Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение") Тогда
		
		ПредставлениеДокумента = НСтр("ru='Платеж №%1 от %2'");
		
	ИначеЕсли ДанныеСобытия.ВидЭД = ПредопределенноеЗначение(
		"Перечисление.ВидыЭДОбменСБанками.СписокНаЗачислениеДенежныхСредствНаСчетаСотрудников") Тогда
		
		ПредставлениеДокумента = НСтр("ru='Ведомость в банк №%1 от %2'");
		
	ИначеЕсли ДанныеСобытия.ВидЭД = ПредопределенноеЗначение(
		"Перечисление.ВидыЭДОбменСБанками.РеестрВыплатСамозанятым") Тогда
		
		ПредставлениеДокумента = НСтр("ru='Выплата самозанятым №%1 от %2'");
		
	ИначеЕсли ДанныеСобытия.ВидЭД = ПредопределенноеЗначение(
		"Перечисление.ВидыЭДОбменСБанками.ЗаявкаНаОткрытиеДепозита") Тогда
		
		ПредставлениеДокумента = НСтр("ru='Заявка на депозит №%1 от %2'");
		
	КонецЕсли;
	
	Текст = СтрШаблон(ПредставлениеДокумента,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеСобытия.Номер, Истина, Истина), 
		Формат(ДанныеСобытия.Дата, "ДФ=dd.MM.yy"));
	
	Результат.Вставить("Текст", Текст);
	
	Пояснение = Нстр("ru='Операция отклонена банком.'");
	
	ДеталиОперации = Новый Массив;
	
	Если ЗначениеЗаполнено(ДанныеСобытия.СуммаДокумента) Тогда
		ДеталиОперации.Добавить(СтрШаблон(НСтр("ru='%1 руб.'"), Формат(ДанныеСобытия.СуммаДокумента, "ЧДЦ=2; ЧГ=")));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСобытия.НаименованиеКонтрагента) Тогда
		ДеталиОперации.Добавить(СтрШаблон(НСтр("ru='%1'"), ДанныеСобытия.НаименованиеКонтрагента));
	КонецЕсли;
	
	Если ДеталиОперации.Количество() > 0 Тогда
		Пояснение = Пояснение + Символы.ПС + СтрСоединить(ДеталиОперации, " ");
	КонецЕсли;
	
	Результат.Вставить("Пояснение", Пояснение);
	
	Возврат Результат;
	
КонецФункции

Процедура ДействиеИнформацияОПисьмах(НазваниеРаздела) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Раздел", НазваниеРаздела);
	
	ОткрытьФорму("Документ.ПисьмоОбменСБанками.ФормаСписка", ПараметрыОткрытия);
	
КонецПроцедуры

// СтандартныеПодсистемы.БазоваяФункциональность

// См. СтандартныеПодсистемыКлиент.ПриПолученииСерверногоОповещения.
//
Процедура ПриПолученииСерверногоОповещения(ИмяОповещения, Результат) Экспорт
	
	Если ИмяОповещения = ОбменСБанкамиСлужебныйКлиентСервер.ИмяОповещенияОНовыхПисьмах() Тогда
		УведомленияОНовыхПисьмах(Результат);
	ИначеЕсли ИмяОповещения = ОбменСБанкамиСлужебныйКлиентСервер.ИмяОповещенияОНепрочитанныхПисьмах() Тогда
		УведомленияОНепрочитанныхПисьмах(Результат);
	ИначеЕсли ИмяОповещения = ОбменСБанкамиСлужебныйКлиентСервер.ИмяОповещенияОНеотправленныхПисьмах() Тогда
		УведомленияОбИсходящихНеотправленныхПисьмах(Результат);
	ИначеЕсли ИмяОповещения = ОбменСБанкамиСлужебныйКлиентСервер.ИмяОповещенияОбОтклоненныхДокументах() Тогда
		ОбменСБанкамиКлиентПереопределяемый.УведомленияОбОтклоненныхДокументах(Результат);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.БазоваяФункциональность

#КонецОбласти





