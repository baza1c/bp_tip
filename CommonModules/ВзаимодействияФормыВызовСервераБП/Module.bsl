#Область СлужебныйПрограммныйИнтерфейс

// Ищет элемент справочника "КонтактныеЛица" по реквизитам ОбъектВладелец и Наименование.
//
// Параметры:
//   Контрагент - СправочникСсылка.Контрагенты - ссылка на владельца контактного лица
//   Наименование - Строка - наименование контактного лица.
//
// Возвращаемое значение:
//   СправочникСсылка.КонтактныеЛица - ссылка на найденный элемент справочника или Неопределено, если элемент не найден.
//
Функция НайтиКонтактноеЛицо(Контрагент, Наименование) Экспорт
	Возврат Справочники.КонтактныеЛица.НайтиКонтактноеЛицо(Контрагент, Наименование);
КонецФункции

Процедура ОбработкаПолученияФормыВзаимодействийОбработкаПолученияФормы(Источник, ВидФормы, Параметры, ВыбраннаяФорма,
	ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт

	Если ТипЗнч(Источник) <> Тип("ДокументМенеджер.ПрочееВзаимодействие") Тогда
		Если ВидФормы = "ФормаВыбора" Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаВыбораБП";
		ИначеЕсли ВидФормы = "ФормаСписка" Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаСпискаБП";
		КонецЕсли;
	КонецЕсли;
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Источник) <> Тип("ДокументМенеджер.ПрочееВзаимодействие") Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "ФормаДокументаБП";
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументМенеджер.ЭлектронноеПисьмоИсходящее")
		Или ТипЗнч(Источник) = Тип("ДокументМенеджер.ЭлектронноеПисьмоВходящее") Тогда
			Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс()
		И Не ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ключ")) Тогда
		ДанныеУчастника = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ДанныеУчастника", Неопределено);
		Если ДанныеУчастника <> Неопределено
			И (ДанныеУчастника.Свойство("Контакт")
			И ЗначениеЗаполнено(ДанныеУчастника.Контакт)) Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаНовогоДокумента";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
