#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьОстаткиТоваров(НастройкиИнтеграцииМаркетплейс, Отбор, Отказ) Экспорт
	ТаблицаРезультата = ОбменСМаркетплейс.НовыйТаблицаОстатковТоваров();
	Если НЕ ЗначениеЗаполнено(Отбор) Тогда
		Возврат ТаблицаРезультата;
	КонецЕсли;
	
	ФильтрПоТоварам = НовыйФильтрЗапросаПоТоварам(ОбщегоНазначения.ВыгрузитьКолонку(Отбор, "ИдентификаторТовара"));
	
	ФильтрыВидимости                   = ЗначениеВМассив(ФильтрПоТоварам.ФильтрВидимости);
	ИдентификаторыОбъектовМаркетплейса = ОбщегоНазначения.СкопироватьРекурсивно(ФильтрПоТоварам.ИдентификаторыОбъектовМаркетплейса);

	ПараметрыЗапроса                 = НовыеПараметрыЗапросаПолученияСпискаТоваров(ФильтрПоТоварам);
	ПараметрыЗапроса.ФильтрВидимости = ФильтрПоТоварам.ФильтрВидимости;
	
	МаксимальныйРазмерПорции = Мин(ПараметрыЗапроса.МаксимальноеКоличествоВЗапросе, ПараметрыЗапроса.МаксимальноеКоличествоВОтвете);
	
	Порции = ОбменСМаркетплейс.ПодготовитьПорцииДляОбмена(ИдентификаторыОбъектовМаркетплейса, МаксимальныйРазмерПорции);
	Для каждого Порция Из Порции Цикл
		ПолучитьПорциюОстаткиТоваров(Порция, ТаблицаРезультата, ПараметрыЗапроса, НастройкиИнтеграцииМаркетплейс, Отказ);
		Если Отказ Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;

	Возврат ТаблицаРезультата;
КонецФункции

Процедура ПередатьОстаткиТоваров(НастройкаИнтеграции, ТаблицаТовары, СообщениеОбОшибке, Отказ, КодОтветаСервера = 0) Экспорт
	Порции = ОбменСМаркетплейс.ПодготовитьПорцииДляОбмена(ТаблицаТовары, 100);
	Для каждого Порция Из Порции Цикл
		ВыгруженныеТовары = ОбновитьОстаткиТоваровВСервисе(НастройкаИнтеграции, Порция, СообщениеОбОшибке, Отказ);
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		ОбменСМаркетплейс.ОбновитьДанныеОбОстатках(НастройкаИнтеграции, ВыгруженныеТовары);
	КонецЦикла;
КонецПроцедуры

Функция СписокСкладов(НастройкаИнтеграции, Отказ) Экспорт
	Перем КодОтвета, СообщенияОбОшибках;
	
	АтрибутыОтвета = Новый Структура;
	АтрибутыОтвета.Вставить("Результат", "result");
	
	ТекстЗапроса = "/v1/warehouse/list";
	ДанныеЗапроса = Новый Структура();
	РезультатЗапроса = ПолучитьРезультатЗапроса(ТекстЗапроса, ДанныеЗапроса, НастройкаИнтеграции, СообщенияОбОшибках, АтрибутыОтвета, КодОтвета);
	
	Если КодОтвета <> 200 Тогда
		Отказ = Истина;
		ЗарегистрироватьОшибку(КодОтвета, 
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СообщенияОбОшибках, "message", ""));
			
		Возврат Неопределено;
	КонецЕсли;
	
	СписокСкладов = Новый СписокЗначений;
	Для каждого ЭлементРезультата Из РезультатЗапроса["Результат"] Цикл
		СписокСкладов.Добавить(XMLСтрока(ЭлементРезультата["warehouse_id"]), ЭлементРезультата["name"]);
	КонецЦикла;
	
	Возврат СписокСкладов;
КонецФункции

Функция ПолучитьДанныеДокумента(ПараметрыОтчета, КодОтветаСервера, СообщениеОбОшибке) Экспорт
	
	РезультатЧтения = ПрочитатьОтчетOZON(ПараметрыОтчета, КодОтветаСервера, СообщениеОбОшибке);
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатЧтения) Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось разобрать ответ от маркетплейс. Попробуйте загрузить отчет позже'");
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.OZON);
		Возврат Неопределено;
	КонецЕсли;
	
	РезультатЧтения = ПреобразоватьДанныеПрочитанногоОтчета(ПараметрыОтчета, РезультатЧтения.result);
	
	СообщениеОбОшибкеФинОтчеты = "";
	ДанныеФайловРеестра        = ПрочитатьФинансовыйОтчет(ПараметрыОтчета, "document-b2b-sales", СообщениеОбОшибкеФинОтчеты);
	СообщениеОбОшибкеФинОтчеты = СтрЗаменить(СообщениеОбОшибкеФинОтчеты, "&ТекстФайлОтчета", "реестра продаж юридическим лицам");
	
	Если ЗначениеЗаполнено(СообщениеОбОшибкеФинОтчеты) Тогда
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибкеФинОтчеты, Перечисления.ВидыМаркетплейсов.OZON);
	КонецЕсли;
		
	Если ДанныеФайловРеестра <> Неопределено Тогда
		Если ДанныеФайловРеестра.Количество() > 0
			И ДанныеФайловРеестра[0].Свойство("ТекстОшибки")
			И ЗначениеЗаполнено(ДанныеФайловРеестра[0].ТекстОшибки) Тогда
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ДанныеФайловРеестра[0].ТекстОшибки, Перечисления.ВидыМаркетплейсов.OZON);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатЧтения, ДанныеФайловРеестра);
			ПреобразоватьДанныеФайлов(РезультатЧтения);
		КонецЕсли;
	КонецЕсли;
	
	//Отчет о выкупе
	СообщениеОбОшибкеВыкуп = "";
	ДанныеФайловВыкупа = ПрочитатьОтчетОВыкупеOZON(ПараметрыОтчета, СообщениеОбОшибкеВыкуп);
	
	Если ЗначениеЗаполнено(СообщениеОбОшибкеВыкуп) Тогда
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибкеВыкуп, Перечисления.ВидыМаркетплейсов.OZON);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФайловВыкупа) Тогда
		ДанныеФайловВыкупа = ПреобразоватьДанныеОтчетаОВыкупе(ПараметрыОтчета, ДанныеФайловВыкупа);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатЧтения, ДанныеФайловВыкупа);
	КонецЕсли;
		
	//Отчет о компенсациях
	СообщениеОбОшибкеФинОтчеты = "";
	ДанныеФайловРеестра        = ПрочитатьФинансовыйОтчет(ПараметрыОтчета, "compensation" ,СообщениеОбОшибкеФинОтчеты);
	СообщениеОбОшибкеФинОтчеты = СтрЗаменить(СообщениеОбОшибкеФинОтчеты, "&ТекстФайлОтчета", "отчета о компенсациях");
	
	Если ЗначениеЗаполнено(СообщениеОбОшибкеФинОтчеты) Тогда
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибкеФинОтчеты, Перечисления.ВидыМаркетплейсов.OZON);
	КонецЕсли;
	
	Если ДанныеФайловРеестра <> Неопределено Тогда
		Если ДанныеФайловРеестра.Количество() > 0
			И ДанныеФайловРеестра[0].Свойство("ТекстОшибки")
			И ЗначениеЗаполнено(ДанныеФайловРеестра[0].ТекстОшибки) Тогда
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ДанныеФайловРеестра[0].ТекстОшибки, Перечисления.ВидыМаркетплейсов.OZON);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатЧтения, ДанныеФайловРеестра);
		КонецЕсли;
	КонецЕсли;
	
	//Отчет о декомпенсациях
	СообщениеОбОшибкеФинОтчеты = "";
	ДанныеФайловРеестра        = ПрочитатьФинансовыйОтчет(ПараметрыОтчета, "decompensation" ,СообщениеОбОшибкеФинОтчеты);
	СообщениеОбОшибкеФинОтчеты = СтрЗаменить(СообщениеОбОшибкеФинОтчеты, "&ТекстФайлОтчета", "отчета о декомпенсациях");
	
	Если ЗначениеЗаполнено(СообщениеОбОшибкеФинОтчеты) Тогда
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибкеФинОтчеты, Перечисления.ВидыМаркетплейсов.OZON);
	КонецЕсли;
	
	Если ДанныеФайловРеестра <> Неопределено Тогда
		Если ДанныеФайловРеестра.Количество() > 0
			И ДанныеФайловРеестра[0].Свойство("ТекстОшибки")
			И ЗначениеЗаполнено(ДанныеФайловРеестра[0].ТекстОшибки) Тогда
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ДанныеФайловРеестра[0].ТекстОшибки, Перечисления.ВидыМаркетплейсов.OZON);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатЧтения, ДанныеФайловРеестра);
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатЧтения;
	
КонецФункции

Функция ПодготовитьФайлОстатков(НастройкаИнтеграции, ТаблицаОстатки) Экспорт
	СтруктураФайла = Новый Структура;
	СтруктураФайла.Вставить("ИмяФайла", "stock-update.xlsx");
	СтруктураФайла.Вставить("Страницы", Новый Соответствие);
	
	Макет = РегистрыСведений.ОстаткиТоваровМаркетплейсов.ПолучитьМакет("ФормаОстатковОЗОН");
	
	СтруктураФайла.Страницы.Вставить("Инструкция", ПоместитьВоВременноеХранилище(Макет.ПолучитьОбласть("Инструкция")));
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Вывести(Макет.ПолучитьОбласть("ОстаткиНаСкладе_Шапка"));
	
	ОбластьОстатки = Макет.ПолучитьОбласть("ОстаткиНаСкладе_Строка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОстатки", ТаблицаОстатки);
	Запрос.УстановитьПараметр("НаименованиеСклада", СтрШаблон("%1 (%2)", НастройкаИнтеграции.НаименованиеСклада, НастройкаИнтеграции.ИдентификаторСклада));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОстатки.Остаток КАК Остаток,
	|	ТаблицаОстатки.ИдентификаторТовара КАК ИдентификаторТовара
	|ПОМЕСТИТЬ ВТ_ОстаткиТоваров
	|ИЗ
	|	&ТаблицаОстатки КАК ТаблицаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НаименованиеСклада КАК НаименованиеСклада,
	|	НоменклатураМаркетплейсов.Номенклатура.Артикул КАК НоменклатураАртикул,
	|	ВТ_ОстаткиТоваров.Остаток КАК Остаток
	|ИЗ
	|	ВТ_ОстаткиТоваров КАК ВТ_ОстаткиТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ПО ВТ_ОстаткиТоваров.ИдентификаторТовара = НоменклатураМаркетплейсов.ИдентификаторТовара";
	
	ВыборкаРезультатов = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаРезультатов.Следующий() Цикл
		ОбластьОстатки.Параметры.Заполнить(ВыборкаРезультатов);
		
		ТабличныйДокумент.Вывести(ОбластьОстатки);
	КонецЦикла;
	
	СтруктураФайла.Страницы.Вставить("Остатки на складе", ПоместитьВоВременноеХранилище(ТабличныйДокумент));
	
	Возврат СтруктураФайла;
КонецФункции

#Область РеестрПродажЮридическимЛицам

Процедура ПреобразоватьДанныеФайлов(ДанныеФайлов) Экспорт
	
	ПродажиНаОзон = Новый Массив;
	Для Каждого ДанныеФайла Из ДанныеФайлов Цикл
		Если ДанныеФайла.Свойство("ВидДокумента")
			И ДанныеФайла.Свойство("СкорректированНаПродажиЮрлицам") Тогда
			Если ДанныеФайла.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzon()
				И Не ДанныеФайла.СкорректированНаПродажиЮрлицам Тогда
				ПродажиНаОзон.Добавить(ДанныеФайла);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ДанныеФайла Из ДанныеФайлов Цикл
		Если ДанныеФайла.Свойство("ВидДокумента")
			И ДанныеФайла.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon()
			И ПродажиНаОзон.Количество() > 0 Тогда
			Для Каждого Продажа Из ПродажиНаОзон Цикл
				Если Продажа.Организация = ДанныеФайла.Организация
					И Продажа.Дата = ДанныеФайла.Дата Тогда
					УбратьПродажиЮрлицамИзОтчетаОРеализации(Продажа, ДанныеФайла);
					ПродажиНаОзон.Удалить(ПродажиНаОзон.Найти(Продажа));
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция РазбитьРеестрПродажЮридическимЛицамПоДокументам(ТаблицаТоваров) Экспорт
	
	Результат = Новый Массив;
	
	Реквизиты = ТаблицаТоваров.Скопировать(, "НомерДокумента, ДатаДокумента");
	Реквизиты.Свернуть("НомерДокумента, ДатаДокумента");
	
	Для Каждого Документ Из Реквизиты Цикл
		
		ОтборПоСтрокам = Новый Структура("НомерДокумента, ДатаДокумента", Документ.НомерДокумента, Документ.ДатаДокумента);
		ТаблицаТоваровПоДокументу = ТаблицаТоваров.Скопировать(ОтборПоСтрокам);
		Если ТаблицаТоваровПоДокументу.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Шапка = Новый Структура("ТипДокумента, НомерДокумента, ДатаДокумента, НомерСФ, ДатаСФ, НомерКоррСФ, ДатаКоррСФ,
			|ИННПокупателя, КПППокупателя, НаименованиеПокупателя, КраткоеНаименованиеПокупателя, Контрагент, ДоговорКонтрагента, Документ");
		ЗаполнитьЗначенияСвойств(Шапка, ТаблицаТоваровПоДокументу[0]);
		Шапка.КраткоеНаименованиеПокупателя = Справочники.Контрагенты.ПолучитьКраткоеНаименованиеКонтрагента(
			Шапка.НаименованиеПокупателя);
		
		Результат.Добавить(Новый Структура("Шапка, Товары", Шапка, ТаблицаТоваровПоДокументу));
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьКонтрагентаИДоговорПоРееструПродажЮридическимЛицам(ДанныеСчетаФактуры, Организация, ГруппаДляНовыхКонтрагентов, ОписаниеОшибки) Экспорт
	
	Шапка = ДанныеСчетаФактуры.Шапка;
	
	РеквизитыПокупателя = Новый Структура("Наименование, НаименованиеПолное, ИНН, КПП, ЮридическоеФизическоеЛицо",
		Шапка.КраткоеНаименованиеПокупателя,
		Шапка.НаименованиеПокупателя,
		Шапка.ИННПокупателя,
		Шапка.КПППокупателя,
		?(СтрДлина(Шапка.ИННПокупателя) = 12 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Шапка.ИННПокупателя),
			Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо,
			Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо));
	Контрагент = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(РеквизитыПокупателя);
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		РеквизитыПокупателя.Вставить("Родитель", ГруппаДляНовыхКонтрагентов);
		Попытка
			Контрагент = Справочники.Контрагенты.СоздатьКонтрагента(РеквизитыПокупателя);
		Исключение
			ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать контрагента: %1 (ИНН: %2),
				|по причине %2'"),
				Шапка.НаименованиеПокупателя,
				Шапка.ИННПокупателя,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
	КонецЕсли;
	Шапка.Контрагент = Контрагент;
	
	// Для возвратов договор берем из документа основания
	Если Не ЭтоДокументНаВозврат(Шапка.ТипДокумента) Тогда
		
		НаименованиеДоговора = СтрШаблон(НСтр("ru = '%1 от %2'"),
			СокрЛП(Шапка.НомерДокумента),
			Формат(Шапка.ДатаДокумента, "ДФ=dd.MM.yyyy"));
		
		РеквизитыДоговора = Новый Структура;
		РеквизитыДоговора.Вставить("Владелец", Контрагент);
		РеквизитыДоговора.Вставить("Организация", Организация);
		РеквизитыДоговора.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		РеквизитыДоговора.Вставить("Наименование", НаименованиеДоговора);
		РеквизитыДоговора.Вставить("Номер", СокрЛП(Шапка.НомерДокумента));
		РеквизитыДоговора.Вставить("Дата", Шапка.ДатаДокумента);
		
		ДополнительныеПараметры = Новый Структура("Наименование, Номер, Дата",
			Новый Структура("ЗначениеОтбора", НаименованиеДоговора),
			Новый Структура("ЗначениеОтбора", СокрЛП(Шапка.НомерДокумента)),
			Новый Структура("ЗначениеОтбора", Шапка.ДатаДокумента));
		
		ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ЕстьДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
			ДоговорКонтрагента,
			РеквизитыДоговора.Владелец,
			РеквизитыДоговора.Организация,
			РеквизитыДоговора.ВидДоговора,
			ДополнительныеПараметры);
		Если Не ЕстьДоговорКонтрагента Тогда
			ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", РеквизитыДоговора);
			ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
			Если ДоговорКонтрагента = Неопределено Тогда
				ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать договор с покупателем контрагента: %1 (ИНН: %2).'"),
					Шапка.НаименованиеПокупателя,
					Шапка.ИННПокупателя);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Шапка.ДоговорКонтрагента = ДоговорКонтрагента;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область АвтозагрузкаОтчетовМаркетплейсов

Функция ПериодыАвтозагрузки(Интеграция, ПериодыАвтозагрузки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Интеграция.Организация);
	Запрос.УстановитьПараметр("Контрагент", Интеграция.Контрагент);
	Запрос.УстановитьПараметр("ПериодыАвтозагрузки", ПериодыАвтозагрузки);
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, МЕСЯЦ)), ДАТАВРЕМЯ(1, 1, 1)) КАК МаксимальныйПериод
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах)
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, МЕСЯЦ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера)
	|	И РеализацияТоваровУслуг.Комиссионер = &Контрагент
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, МЕСЯЦ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|	И ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ТоварыНаСкладКомиссионера)
	|	И ВозвратТоваровОтПокупателя.Комиссионер = &Контрагент
	|	И ВозвратТоваровОтПокупателя.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, МЕСЯЦ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, МЕСЯЦ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОСписании)
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, МЕСЯЦ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, МЕСЯЦ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	НЕ ОтчетКомиссионераОПродажах.ПометкаУдаления
	|	И ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОбОприходовании)
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ОтчетКомиссионераОПродажах.Дата, МЕСЯЦ) В (&ПериодыАвтозагрузки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ)), ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером)
	|	И РеализацияТоваровУслуг.Комиссионер = &Контрагент
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ) В (&ПериодыАвтозагрузки)";
	
	Результат = Новый Массив;
	МаксимальныйПериод = Дата(1, 1, 1);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МаксимальныйПериод = Макс(МаксимальныйПериод, Выборка.МаксимальныйПериод);
	КонецЦикла;
	
	Для Каждого ПериодАвтозагрузки Из ПериодыАвтозагрузки Цикл
		Если ПериодАвтозагрузки > МаксимальныйПериод Тогда
			Результат.Добавить(Новый Структура("НачалоПериода, ОкончаниеПериода",
				НачалоМесяца(ПериодАвтозагрузки), КонецМесяца(ПериодАвтозагрузки)));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция АвтозагрузкаДокументов(Интеграция, ДанныеМаркеплейса) Экспорт
	
	СозданныеДокументы = Новый Массив;
	
	Ошибки = Новый Массив;
	СписокЗагрузки = Новый Массив;
	Для Каждого ДанныеФайла Из ДанныеМаркеплейса Цикл
		ПодготовитьДокументыКСозданию(СписокЗагрузки, ДанныеФайла, Интеграция, Ошибки);
		Если ЗначениеЗаполнено(Ошибки) Тогда
			Для Каждого ОписаниеОшибки Из Ошибки Цикл
				ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ОписаниеОшибки, Интеграция.Маркетплейс);
			КонецЦикла;
			Возврат Новый Массив;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоздатьДокументы(СписокЗагрузки);
	
КонецФункции

#КонецОбласти

#Область СозданиеДокументов

Функция СоздатьДокументы(ДанныеДокументов) Экспорт
	
	СозданныеДокументы = Новый Массив;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	ЭлектронноеВзаимодействиеБП.СоздатьДокументыИзСтруктуры(ДанныеДокументов, АдресХранилища);
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	Для Каждого СтрокаРезультата Из Результат Цикл
		Если СтрокаРезультата.Свойство("СозданДокумент") Тогда
			СозданныеДокументы.Добавить(СтрокаРезультата.СозданДокумент);
		ИначеЕсли СтрокаРезультата.Свойство("ТекстОшибки") Тогда
			ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СтрокаРезультата.ТекстОшибки, Перечисления.ВидыМаркетплейсов.OZON);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СозданныеДокументы;
	
КонецФункции

Процедура ПодготовитьДокументыКСозданию(СписокЗагрузки, ДанныеМаркеплейса, ПараметрыИнтеграции, Ошибки) Экспорт
	
	Если ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonВыкуп() Тогда
		СворачиваемыеКолонки = "Наименование,Артикул,ИД,НатуральныйИД,СтавкаНДС,ШтрихКод,КодМагазина";
		СуммируемыеКолонки = "Количество,Сумма,СуммаНДС";
	ИначеЕсли ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon() Тогда
		СворачиваемыеКолонки = "Артикул,ДатаДокумента,ДатаКоррСФ,ДатаСФ,ИД,ИННПокупателя,КПППокупателя,КодМагазина,
			|Наименование,НаименованиеПокупателя,НатуральныйИД,НомерДокумента,НомерКоррСФ,НомерСФ,СтавкаНДС,
			|ТипДокумента,Цена,Штрихкод";
		СуммируемыеКолонки = "Количество,Сумма,СуммаБезНДС,СуммаНДС";
	Иначе
		СворачиваемыеКолонки = "Наименование,Артикул,ИД,НатуральныйИД,ВидДвижения,ТипДокумента,КодСтраны,НазваниеСтраны,
			|ШтрихКод,КодМагазина,ДатаПродажи";
		СуммируемыеКолонки = "Количество,Сумма,КоличествоВозврат,СуммаВозврат,Доплата,ДоплатаВозврат,ДоплатаПартнеров,ДоплатаПартнеровВозврат";
	КонецЕсли;
	
	Если ДанныеМаркеплейса.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon() Тогда
		ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(ПараметрыИнтеграции.Организация, НачалоМесяца(ПараметрыИнтеграции.НачалоПериода))
			Или УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(ПараметрыИнтеграции.Организация, НачалоМесяца(ПараметрыИнтеграции.НачалоПериода));
		Для Каждого ПодчиненныйДокумент Из ДанныеМаркеплейса.ДанныеДокумента.ПодчиненныеДокументы Цикл
			Шапка = ПодчиненныйДокумент.Шапка;
			
			// Для неплательщиков НДС счет-фактура не оформляется
			Если Не ПлательщикНДС Тогда
				Шапка.НомерСФ     = "";
				Шапка.ДатаСФ      = Дата(1,1,1);
				Шапка.НомерКоррСФ = "";
				Шапка.ДатаКоррСФ  = Дата(1,1,1);
			КонецЕсли;
			
			ОписаниеОшибки = "";
			ИнтеграцияOzon.СоздатьКонтрагентаИДоговорПоРееструПродажЮридическимЛицам(
				ПодчиненныйДокумент, ПараметрыИнтеграции.Организация, ПараметрыИнтеграции.ГруппаДляНовыхКонтрагентов, ОписаниеОшибки);
			Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				Ошибки.Добавить(ОписаниеОшибки);
			КонецЕсли;
			
			Если ПараметрыИнтеграции.СпособСверткиОтчетов = "Сворачивать" Тогда
				ПодчиненныйДокумент.Товары.Свернуть(СворачиваемыеКолонки, СуммируемыеКолонки);
			КонецЕсли;
			
			ОписаниеРодителя = ДанныеМаркеплейса.Описание;
			Если ЗначениеЗаполнено(ОписаниеРодителя) Тогда
				МассивПодстрок = СтрРазделить(ОписаниеРодителя, Символы.ПС, Ложь);
				Если МассивПодстрок.Количество() > 0 Тогда
					ОписаниеРодителя = МассивПодстрок[0];
				КонецЕсли;
			КонецЕсли; 
			
			ДанныеДляЗагрузки = Новый Структура;
			ЭтоДокументНаВозврат = ЭтоДокументНаВозврат(Шапка.ТипДокумента);
			Если ЭтоДокументНаВозврат Тогда
				ШаблонОписание = НСтр("ru = 'Возврат на склад комиссионера %1 от %2'");
			Иначе
				ШаблонОписание = НСтр("ru = 'Продажа со склада комиссионера %1 от %2'");
			КонецЕсли;
			ДанныеДляЗагрузки.Вставить("Описание", СтрШаблон(ШаблонОписание, Шапка.НомерДокумента, Формат(Шапка.ДатаДокумента, "ДФ=dd.MM.yyyy")));
			ДанныеДляЗагрузки.Вставить("ОписаниеРодителя", ОписаниеРодителя);
			ДанныеДляЗагрузки.Вставить("ВидДокумента", ДанныеМаркеплейса.ВидДокумента);
			ДанныеДляЗагрузки.Вставить("Документ", ПодчиненныйДокумент);
			ДанныеДляЗагрузки.Вставить("Организация", ПараметрыИнтеграции.Организация);
			ДанныеДляЗагрузки.Вставить("КонтрагентМаркетплейс", ДанныеМаркеплейса.Контрагент);
			ДанныеДляЗагрузки.Вставить("Маркетплейс", Перечисления.ВидыМаркетплейсов.OZON);
			ДанныеДляЗагрузки.Вставить("ЭтоВозврат", ЭтоДокументНаВозврат);
			ДанныеДляЗагрузки.Вставить("ПервичныеДанныеМаркетплейса", ОбменСМаркетплейс.ОтобратьПервичныеДанныеМаркетплейса(
				ДанныеМаркеплейса.ДанныеДокумента.ПервичныеДанныеМаркетплейса, "номерсчетафактуры", Шапка.НомерДокумента));
			ДанныеДляЗагрузки.Вставить("СпособЗагрузки", ПараметрыИнтеграции.СпособЗагрузки);
			СписокЗагрузки.Добавить(ДанныеДляЗагрузки);
		КонецЦикла;
	Иначе
		Если ПараметрыИнтеграции.СпособСверткиОтчетов = "Сворачивать" Тогда
			ДанныеМаркеплейса.ДанныеДокумента.ТаблицаТоваров.Свернуть(СворачиваемыеКолонки, СуммируемыеКолонки);
		КонецЕсли;
		
		ДанныеМаркеплейса.Вставить("СпособЗагрузки", ПараметрыИнтеграции.СпособЗагрузки);
		
		ДобавитьПолучателяВСтруктуру(ДанныеМаркеплейса.ДанныеДокумента, ПараметрыИнтеграции.Организация);
		
		СписокЗагрузки.Добавить(ДанныеМаркеплейса);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПолучателяВСтруктуру(ДанныеДокумента, Организация)
	
	Если ДанныеДокумента.Свойство("Получатель") 
		И ТипЗнч(ДанныеДокумента.Получатель) = Тип("Структура") Тогда
		ДанныеДокумента.Получатель.Вставить("Ссылка", Организация);
	Иначе
		Получатель = Новый Структура;
		Получатель.Вставить("Ссылка", Организация);
		ДанныеДокумента.Вставить("Получатель", Получатель);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОтложенноеСопоставлениеНоменклатуры

Функция НастройкиСопоставленияНоменклатуры(ВидДокумента) Экспорт
	
	ЕстьСтавкаНДС = (ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonВыкуп())
		Или (ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаРеестрПродажЮрлицамOzon());
	
	ВидимостьКолонокСопоставления = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ВидимостьКолонокСопоставления.Вставить("ЕдиницаИзмерения", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Упаковка", Ложь);
	ВидимостьКолонокСопоставления.Вставить("СтавкаНДС", ЕстьСтавкаНДС);
	
	ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
	ДополнительныйРеквизит.Имя = "КодМагазина";
	ДополнительныйРеквизит.Представление = "Код маркетплейса";
	НовыеКолонкиСопоставления = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДополнительныйРеквизит);
	
	ДополнительныеПараметрыПоиска = Новый Структура;
	ДополнительныеПараметрыПоиска.Вставить("НатуральныеКлючи", "Артикул");
	ДополнительныеПараметрыПоиска.Вставить("Маркетплейс", Перечисления.ВидыМаркетплейсов.OZON);
	
	НастройкиСопоставления = Новый Структура;
	НастройкиСопоставления.Вставить("ВидимостьКолонокСопоставления", ВидимостьКолонокСопоставления);
	НастройкиСопоставления.Вставить("ДополнительныеРеквизитыСопоставления", НовыеКолонкиСопоставления);
	НастройкиСопоставления.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметрыПоиска);
	НастройкиСопоставления.Вставить("ОграничениеТипаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НастройкиСопоставления.Вставить("ОтключитьПоискПоСловарю", Истина);
	НастройкиСопоставления.Вставить("ТочностьПоискаПоУмолчанию", 100);
	
	Возврат НастройкиСопоставления;
	
КонецФункции

Функция ПодготовитьОтложенноеСопоставлениеНоменклатуры(Контрагент, НезаполненнаяНоменклатура) Экспорт
	
	НесопоставленнаяНоменклатура = Новый Массив;
	
	Для Каждого СтрокаКСопоставлению Из НезаполненнаяНоменклатура Цикл
		СтрокаТовара = СтрокаКСопоставлению.СтрокаТовара;
		
		НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(Контрагент);
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, СтрокаТовара);
		НоменклатураКонтрагента.Идентификатор             = СтрокаТовара.ИД;
		НоменклатураКонтрагента.ИдентификаторНоменклатуры = СтрокаТовара.ИД;
		НоменклатураКонтрагента.ШтрихкодКомбинации        = СтрокаТовара.Штрихкод;
		НоменклатураКонтрагента.ШтрихкодыНоменклатуры     = СтрокаТовара.Штрихкод;
		Если ЗначениеЗаполнено(СтрокаТовара.НатуральныйИД) Тогда
			НоменклатураКонтрагента.ИсторияИдентификаторов.Добавить(СтрокаТовара.НатуральныйИД);
		КонецЕсли;
		НоменклатураКонтрагента.Вставить("ИмяТабличнойЧасти", СтрокаКСопоставлению.ИмяТабличнойЧасти);
		НоменклатураКонтрагента.Вставить("ИндексТабличнойЧасти", СтрокаКСопоставлению.ИндексТабличнойЧасти);
		НесопоставленнаяНоменклатура.Добавить(НоменклатураКонтрагента);
		
	КонецЦикла;
	
	Возврат НесопоставленнаяНоменклатура;
	
КонецФункции

#КонецОбласти

#Область РаботаСНастройкамиАвторизации

Функция ПрочитатьНастройкиАвторизации(НастройкаИнтеграции)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НастройкиАвторизации = Новый Структура();
	НастройкиАвторизации.Вставить("Токен");
	НастройкиАвторизации.Вставить("КлиентИд");
	
	НастройкиАвторизации.КлиентИд = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(НастройкаИнтеграции, "КлиентИд");
	НастройкиАвторизации.Токен = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(НастройкаИнтеграции, "Токен");
	
	Возврат НастройкиАвторизации;
	
КонецФункции

Процедура СохранитьНастройкиАвторизации(НастройкаИнтеграции, Токен, КлиентИд) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкаИнтеграции, Токен, "Токен");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкаИнтеграции, КлиентИд, "КлиентИд");
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПрочитатьКлиентИд(НастройкаИнтеграции) Экспорт
	Возврат ПрочитатьНастройкиАвторизации(НастройкаИнтеграции).КлиентИд;
КонецФункции

Функция ИнтеграцияНастроена(НастройкаИнтеграции) Экспорт
	Возврат ЗначениеЗаполнено(ПрочитатьНастройкиАвторизации(НастройкаИнтеграции).КлиентИд);
КонецФункции 


#КонецОбласти

#Область УправлениеОстаткамиТоваров

// Конструктор списка параметров для фильтра по товарам.
//
// Параметры:
//   ВключатьТоварыИзАрхива             - Булево - Истина, если в выборку включаются товары из архива.
//   ИдентификаторыПубликации           - Массив Из Строка - список внутренних идентификаторов товаров в системе продавца;
//                                      - Неопределено - выборка по идентификаторам публикации не используется.
//   ИдентификаторыОбъектовМаркетплейса - Массив Из Строка - список идентификаторов товаров на маркетплейсе;
//                                      - Неопределено - выборка по идентификаторам товаров на маркетплейсе не используется.
//
// Возвращаемое значение:
//   Структура - фильтры для получения списка товаров (filter):
//     * ФильтрВидимости                    - Массив Из Строка - для указания нескольких фильтров по видимости;
//                                          - Строка - фильтр по видимости товара (visibility): 
//                                              "ALL"                      - все товары, кроме архивных;
//                                              "VISIBLE"                  - товары, которые видны покупателям;.
//                                              "INVISIBLE"                - товары, которые не видны покупателям;
//                                              "EMPTY_STOCK"              - товары, у которых не указано наличие;
//                                              "NOT_MODERATED"            - товары, которые не прошли модерацию;
//                                              "MODERATED"                - товары, которые прошли модерацию;
//                                              "DISABLED"                 - товары, которые видны покупателям, но недоступны к покупке;
//                                              "STATE_FAILED"             - товары, создание которых завершилось ошибкой;
//                                              "READY_TO_SUPPLY"          - товары, готовые к поставке;
//                                              "VALIDATION_STATE_PENDING" - товары, которые проходят проверку валидатором на предварительной модерации;
//                                              "VALIDATION_STATE_FAIL"    - товары, которые не прошли проверку валидатором на предварительной модерации;
//                                              "VALIDATION_STATE_SUCCESS" - товары, которые прошли проверку валидатором на предварительной модерации;
//                                              "TO_SUPPLY"                - товары, готовые к продаже;
//                                              "IN_SALE"                  - товары в продаже;
//                                              "REMOVED_FROM_SALE"        - товары, скрытые от покупателей;
//                                              "BANNED"                   - заблокированные товары;
//                                              "OVERPRICED"               - товары с завышенной ценой;
//                                              "CRITICALLY_OVERPRICED"    - товары со слишком завышенной ценой;
//                                              "EMPTY_BARCODE"            - товары без штрихкода;
//                                              "BARCODE_EXISTS"           - товары со штрихкодом;
//                                              "QUARANTINE"               - товары на карантине после изменения цены более чем на 50%;
//                                              "ARCHIVED"                 - товары в архиве;
//                                              "OVERPRICED_WITH_STOCK"    - товары в продаже со стоимостью выше, чем у конкурентов;
//                                              "PARTIAL_APPROVED"         - товары в продаже с пустым или неполным описанием;
//                                              "IMAGE_ABSENT"             - товары без изображений;
//                                              "MODERATION_BLOCK"                  - товары, для которых заблокирована модерация.
//     * ИдентификаторыПубликации           - Массив Из Строка - список внутренних идентификаторов товаров в системе продавца (offer_id);
//     * ИдентификаторыОбъектовМаркетплейса - Массив Из Строка - список идентификаторов товаров на маркетплейсе (product_id).
//
Функция НовыйФильтрЗапросаПоТоварам(ИдентификаторыОбъектовМаркетплейса = Неопределено, 
	ИдентификаторыПубликации = Неопределено, ИдентификаторыSKU = Неопределено) Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("ФильтрВидимости", "ALL");
	Результат.Вставить("ИдентификаторыПубликации",           ИдентификаторыПубликации);
	Результат.Вставить("ИдентификаторыОбъектовМаркетплейса", ИдентификаторыОбъектовМаркетплейса);
	Результат.Вставить("ИдентификаторыSKU",                  ИдентификаторыSKU);

	Возврат Результат;

КонецФункции

// Конструктор списка параметров для запроса получения идентификаторов товаров.
// 
// Параметры:
//   ФильтрПоТоварам - Структура - фильтр по товарам, см. НовыйФильтрЗапросаПоТоварам;
//                   - Неопределено - использовать фильтр по умолчанию.
//
// Возвращаемое значение:
//   Структура - список параметров:
//     * ФильтрВидимости                    - Строка - фильтр по видимости товара (filter.visibility): 
//     * ИдентификаторыПубликации           - Массив Из Строка - список внутренних идентификаторов товаров в системе продавца (filter.offer_id);
//     * ИдентификаторыОбъектовМаркетплейса - Массив Из Строка - список идентификаторов товаров на маркетплейсе (filter.product_id);
//     * ИдентификаторыSKU                  - Массив Из Строка - список идентификаторов SKU (filter.sku);
//     * ИдентификаторПоследнегоЗначения    - Строка - идентификатор последнего значения на странице (last_id);
//     * МаксимальноеКоличествоВОтвете      - Число - количество значений на странице (limit).
//
Функция НовыеПараметрыЗапросаПолученияСпискаТоваров(ФильтрПоТоварам = Неопределено)
	Если ФильтрПоТоварам = Неопределено Тогда
		ФильтрПоТоварам = НовыйФильтрЗапросаПоТоварам();
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторПоследнегоЗначения", "");
	Результат.Вставить("МаксимальноеКоличествоВОтвете",   1000);
	Результат.Вставить("МаксимальноеКоличествоВЗапросе",  1000);
	
	Для Каждого КлючЗначение Из ФильтрПоТоварам Цикл
		Результат.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;

	Возврат Результат;
КонецФункции


Функция СписокТоваровМаркетплейс(НастройкаИнтеграции, СообщениеОбОшибке, Отказ) Экспорт
	ПараметрыЗапросаСписокТоваров = НовыеПараметрыЗапросаПолученияСпискаТоваров();
	
	СписокТоваров = Новый Массив;
	Пока ПолучитьПорциюСписокТоваров(СписокТоваров, ПараметрыЗапросаСписокТоваров, НастройкаИнтеграции, СообщениеОбОшибке, Отказ) Цикл
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат СписокТоваров;
КонецФункции

Функция ПолучитьПорциюСписокТоваров(СписокТоваров, ПараметрыЗапроса, НастройкиИнтеграцииМаркетплейс, СообщениеОбОшибке, Отказ)
	
	ОтветСервиса = ПолучитьПорциюСпискаТоваровИзСервиса(НастройкиИнтеграцииМаркетплейс, ПараметрыЗапроса, СообщениеОбОшибке);
	Если ОтветСервиса = Неопределено Тогда
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли; 
	
	Контрагент = Справочники.НастройкиИнтеграцииМаркетплейс.КонтрагентДоговорНаДату(НастройкиИнтеграцииМаркетплейс, ТекущаяДатаСеанса()).Контрагент;
	Владелец = СопоставлениеНоменклатурыКонтрагентовСлужебный.ВладелецНоменклатурыКонтрагента(Контрагент);
	
	ИнформацияОТоварах = ПолучитьИнформациюОТоварахИзСервиса(НастройкиИнтеграцииМаркетплейс, 
		ОбщегоНазначения.ВыгрузитьКолонку(ОтветСервиса.Результат, "product_id", Истина));
	Если ИнформацияОТоварах = Неопределено Тогда
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого ИнформацияОТоваре Из ИнформацияОТоварах Цикл
		НоменклатураМаркетплейса = ОбменСМаркетплейс.НовыйНоменклатураМаркетплейса(Перечисления.ВидыМаркетплейсов.OZON, Владелец);
		НоменклатураМаркетплейса.КодМагазина  = Формат(ИнформацияОТоваре.Получить("id"), "ЧГ=0");
		НоменклатураМаркетплейса.Наименование = ИнформацияОТоваре.Получить("name");
		НоменклатураМаркетплейса.Артикул      = ИнформацияОТоваре.Получить("offer_id");
		НоменклатураМаркетплейса.Штрихкоды    = ИнформацияОТоваре.Получить("barcodes");
		
		ОбменСМаркетплейс.ДобавитьНоменклатуруВСписок(СписокТоваров, НоменклатураМаркетплейса);
	КонецЦикла;
		
	ПараметрыЗапроса.ИдентификаторПоследнегоЗначения = ОтветСервиса.ИдентификаторПоследнегоЗначения;
	Возврат ОтветСервиса.КоличествоТоваров = ПараметрыЗапроса.МаксимальноеКоличествоВОтвете 
		И ЗначениеЗаполнено(ОтветСервиса.ИдентификаторПоследнегоЗначения); 
		
КонецФункции

Функция ПолучитьПорциюСпискаТоваровИзСервиса(НастройкаИнтеграции, Параметры, СообщениеОбОшибке)
	Перем СообщенияОбОшибках, КодОтвета;
	
	ТекстЗапроса = "/v3/product/list";

	ФильтрПоТоварам = Новый Структура;
	ФильтрПоТоварам.Вставить("visibility", "ALL");

	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("filter",  ФильтрПоТоварам);
	ПараметрыЗапроса.Вставить("last_id", Параметры.ИдентификаторПоследнегоЗначения);
	ПараметрыЗапроса.Вставить("limit",   Параметры.МаксимальноеКоличествоВОтвете);

	ПустойРезультат = Новый Структура;
	ПустойРезультат.Вставить("items",   Новый Массив);
	ПустойРезультат.Вставить("last_id", "");
	ПустойРезультат.Вставить("total",   0);
	
	СлужебныеПараметры 								= СлужебныеПараметрыHTTPЗапроса();
	СлужебныеПараметры.ПодстрокаПоискаВТекстеОтвета = "item not found";
	СлужебныеПараметры.ПустойРезультат              = Новый Структура("result", ПустойРезультат);
	ПараметрыЗапроса.Вставить("СлужебныеПараметры", СлужебныеПараметры);

	АтрибутыОтвета = Новый Структура;
	АтрибутыОтвета.Вставить("Результат",                       "result.items");
	АтрибутыОтвета.Вставить("ИдентификаторПоследнегоЗначения", "result.last_id");
	АтрибутыОтвета.Вставить("КоличествоТоваров",               "result.total");

	// POST-запрос
	РезультатЗапроса = ПолучитьРезультатЗапроса(ТекстЗапроса, ПараметрыЗапроса, НастройкаИнтеграции, СообщенияОбОшибках, АтрибутыОтвета, КодОтвета);
	
	СообщениеОбОшибке = СообщениеОбОшибкеДляПользователя(КодОтвета);
	
	Если КодОтвета <> 200 Тогда
		ЗарегистрироватьОшибку(КодОтвета, 
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СообщенияОбОшибках, "message", ""));
			
		Возврат Неопределено;
	КонецЕсли;

	Возврат РезультатЗапроса;
КонецФункции 

Функция ПолучитьИнформациюОТоварахИзСервиса(НастройкаИнтеграции, СписокИдентификатор)
	Перем СообщенияОбОшибках, КодОтвета;
	ТекстЗапроса = "/v3/product/info/list";

	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("product_id",   СписокИдентификатор);

	МассивПолейТипаДата = Новый Массив;
	МассивПолейТипаДата.Добавить("created_at");
	МассивПолейТипаДата.Добавить("state_updated_at");

	СлужебныеПараметры 				= СлужебныеПараметрыHTTPЗапроса();
	СлужебныеПараметры.ПоляТипаДата = МассивПолейТипаДата;
	ПараметрыЗапроса.Вставить("СлужебныеПараметры", СлужебныеПараметры);

	АтрибутыОтвета = Новый Структура;
	АтрибутыОтвета.Вставить("Результат", "items");
	
	//В разделе errors есть свойства, содержащие пробелы, которые не могут служить ключами структуры
	ПрочитатьВСоответствие = Истина;
	// POST-запрос
	РезультатЗапроса = ПолучитьРезультатЗапроса(ТекстЗапроса,
		ПараметрыЗапроса,
		НастройкаИнтеграции,
		СообщенияОбОшибках,
		АтрибутыОтвета,
		КодОтвета,
		ПрочитатьВСоответствие);
	Если КодОтвета <> 200 Тогда
		ЗарегистрироватьОшибку(КодОтвета, 
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СообщенияОбОшибках, "message", ""));
			
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Результат;
КонецФункции


// Получает остатки товаров из сервиса Ozon.
// Описание метода см. https://docs.ozon.ru/api/seller/#operation/ProductAPI_GetProductInfoStocksV3
//
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Параметры                 - Структура - список параметров запроса, 
//                                 см. ИнтеграцияСМаркетплейсомOzonСервер.НовыеПараметрыЗапросаПолученияСпискаТоваров.
// 
// Возвращаемое значение:
//   Структура    - результат выполнения запроса:
//     * Результат                       - Массив Из Соответствие - информация о товарах (productv3GetProductInfoStocksV3ResponseItem):
//       ** offer_id                       - Строка - идентификатор товара в системе продавца (артикул);
//       ** product_id                     - Число - идентификатор товара в маркетплейсе;
//       ** stocks	                       - Массив Из Соответствие - информация об остатках (GetProductInfoStocksV3ResponseStock):
//         *** present	                     - Число - сейчас на складе;
//         *** reserved	                     - Число - зарезервировано;
//         *** type	                         - Строка - тип склада.
//     * ИдентификаторПоследнегоЗначения - Строка - идентификатор последнего значения на странице (last_id);
//     * КоличествоТоваров               - Число - количество товаров в списке (total).
//   Неопределено - при выполнении запроса к внешнему ресурсу возникли ошибки.
//
Функция ПолучитьОстаткиТоваровИзСервиса(УчетнаяЗаписьМаркетплейса, Параметры)
	Перем СообщенияОбОшибках, КодОтвета;
	ТекстЗапроса = "/v4/product/info/stocks";

	ФильтрПоТоварам = Новый Структура;
	ФильтрПоТоварам.Вставить("visibility", "ALL");


	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("filter",  ФильтрПоТоварам);
	ПараметрыЗапроса.Вставить("cursor", Параметры.ИдентификаторПоследнегоЗначения);
	ПараметрыЗапроса.Вставить("limit",   Параметры.МаксимальноеКоличествоВОтвете);
	
	ПустойРезультат = Новый Структура;
	ПустойРезультат.Вставить("items",   Новый Массив);
	ПустойРезультат.Вставить("cursor", "");
	ПустойРезультат.Вставить("total",   0);
	
	СлужебныеПараметры 								= СлужебныеПараметрыHTTPЗапроса();
	СлужебныеПараметры.ПодстрокаПоискаВТекстеОтвета = "item not found";
	СлужебныеПараметры.ПустойРезультат              = Новый Структура("result", ПустойРезультат);
	ПараметрыЗапроса.Вставить("СлужебныеПараметры", СлужебныеПараметры);

	АтрибутыОтвета = Новый Структура;
	АтрибутыОтвета.Вставить("Результат",                       "items");
	АтрибутыОтвета.Вставить("ИдентификаторПоследнегоЗначения", "cursor");
	АтрибутыОтвета.Вставить("КоличествоТоваров",               "total");

	// POST-запрос
	РезультатЗапроса = ПолучитьРезультатЗапроса(ТекстЗапроса, ПараметрыЗапроса, УчетнаяЗаписьМаркетплейса, СообщенияОбОшибках, АтрибутыОтвета, КодОтвета); 
	Если КодОтвета <> 200 Тогда
		ЗарегистрироватьОшибку(КодОтвета, 
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СообщенияОбОшибках, "message", ""));
			
		Возврат Неопределено;
	КонецЕсли;

	Возврат РезультатЗапроса;
КонецФункции 

Процедура ПолучитьПорциюОстаткиТоваров(Порция, ТаблицаОстатковТоваров, ПараметрыЗапроса, НастройкиИнтеграцииМаркетплейс, Отказ)
	ПараметрыЗапроса.ИдентификаторПоследнегоЗначения    = "";
	ПараметрыЗапроса.ИдентификаторыПубликации           = Неопределено;
	ПараметрыЗапроса.ИдентификаторыОбъектовМаркетплейса = Порция;
	
	ОтветСервиса = ПолучитьОстаткиТоваровИзСервиса(НастройкиИнтеграцииМаркетплейс, ПараметрыЗапроса);
	Порция.Очистить();
	
	Если ОтветСервиса = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса.ИдентификаторПоследнегоЗначения = ОтветСервиса.ИдентификаторПоследнегоЗначения;
	КоличествоТоваров                                = ОтветСервиса.КоличествоТоваров;

	ДобавитьОтветСервисаВТаблицуОстатковТоваров(
		ТаблицаОстатковТоваров, 
		ОтветСервиса, 
		НастройкиИнтеграцииМаркетплейс);
КонецПроцедуры

// Заполняет таблицу остатков товаров значениями из сервиса.
//
// Параметры:
//   ТаблицаОстатковТоваров    - ТаблицаЗначений - остатки товаров, 
//                                 см. ИнтеграцияСМаркетплейсомOzonСервер.НоваяТаблицаОстатковТоваров.
//   ОтветСервиса              - Массив Из Соответствие - информацию о списке товаров из сервиса Ozon,
//                                 см. ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьОстаткиТоваровИзСервиса.
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
Процедура ДобавитьОтветСервисаВТаблицуОстатковТоваров(ТаблицаОстатковТоваров, ОтветСервиса, УчетнаяЗаписьМаркетплейса)

	Для каждого ЭлементОтветаСервиса Из ОтветСервиса.Результат Цикл
		Для каждого ЭлементКоллекции Из ЭлементОтветаСервиса["stocks"] Цикл 
			Если НЕ ВРег(ЭлементКоллекции["type"]) = "FBS" Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока                     = ТаблицаОстатковТоваров.Добавить();
			НоваяСтрока.ИдентификаторТовара = ЧислоВСтроку(ЭлементОтветаСервиса["product_id"]);
			НоваяСтрока.Остаток              = ЭлементКоллекции["present"];
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры 


Функция ОбновитьОстаткиТоваровВСервисе(НастройкиИнтеграции, ТаблицаТовары, СообщениеОбОшибке, Отказ)
	Перем СообщенияОбОшибках, КодОтвета;
	
	ТекстЗапроса = "/v2/products/stocks";
	
	ТаблицаОстатковТоваровКИзменению = ОбменСМаркетплейс.НовыйТаблицаОстатковТоваров();
	
	ОстаткиТоваров = Новый Массив;
	Для каждого СтрокаТовары Из ТаблицаТовары Цикл
		
		ОстаткиТоваров.Добавить(
			Новый Структура("product_id, stock, warehouse_id", 
				СтрокаВЧисло(СтрокаТовары.ИдентификаторТовара), СтрокаТовары.Остаток, СтрокаВЧисло(НастройкиИнтеграции.ИдентификаторСклада)));
				
		НоваяСтрока = ТаблицаОстатковТоваровКИзменению.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
	КонецЦикла;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("stocks", ОстаткиТоваров);

	АтрибутыОтвета = Новый Структура;
	АтрибутыОтвета.Вставить("Результат", "result");

	// POST-запрос
	РезультатЗапроса = ПолучитьРезультатЗапроса(ТекстЗапроса, ПараметрыЗапроса, НастройкиИнтеграции, СообщенияОбОшибках, АтрибутыОтвета, КодОтвета);
	
	Отказ = КодОтвета <> 200;
	
	СообщениеОбОшибке = СообщениеОбОшибкеДляПользователя(КодОтвета);
	
	Если Отказ Тогда
		ЗарегистрироватьОшибку(КодОтвета, 
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СообщенияОбОшибках, "message", ""));
			
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаРезультат = ОбменСМаркетплейс.НовыйТаблицаОстатковТоваров();
	Для каждого СтрокаРезультата Из РезультатЗапроса["Результат"] Цикл
		Если НЕ СтрокаРезультата.updated Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("ИдентификаторТовара", ЧислоВСтроку(СтрокаРезультата.product_id));
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОстатковТоваровКИзменению.НайтиСтроки(Отбор), ТаблицаРезультат);
	КонецЦикла;
	
	Возврат ТаблицаРезультат;
КонецФункции 

#КонецОбласти

#Область РаботаСОтчетами

Функция ПрочитатьОтчетOZON(ПараметрыОтчета, КодОтвета, СообщениеОбОшибке)
	Перем СообщенияОбОшибках;
	
	ПоляЗапроса = Новый Структура();
	ПоляЗапроса.Вставить("month", Месяц(ПараметрыОтчета.НачалоПериода));
	ПоляЗапроса.Вставить("year", Год(ПараметрыОтчета.НачалоПериода));
	
	МетодЗапроса = "/v2/finance/realization";
	РезультатЧтения = ПолучитьРезультатЗапроса(МетодЗапроса, ПоляЗапроса, ПараметрыОтчета.НастройкаИнтеграции, СообщенияОбОшибках, ,КодОтвета);
	
	Если ТипЗнч(СообщенияОбОшибках) = Тип("Структура") Тогда
		Если СокрЛП(СообщенияОбОшибках.message) = "Request validation error: out of date" Тогда
			ШаблонСообщения = НСтр("ru = 'Отчет за период %1 недоступен'");
			СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, Формат(ПараметрыОтчета.НачалоПериода));
		иначе
			СообщениеОбОшибке = СообщенияОбОшибках.message;
		КонецЕсли;
		
		СообщениеОбОшибке = ОписаниеОшибкиПоКоду(КодОтвета, СообщениеОбОшибке);
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.OZON);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РезультатЧтения = Неопределено Тогда 
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'Отсутствует данные отчета за %1. Попробуйте загрузить отчет позже'"),
		Формат(ПараметрыОтчета.НачалоПериода,"ДЛФ=D"));
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.OZON);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЧтения;
КонецФункции

Функция ПреобразоватьДанныеПрочитанногоОтчета(ПараметрыОтчета, ДанныеОтчета)
	
	ТаблицаТоваров = ЭлектронноеВзаимодействиеБП.ПодготовитьТаблицуТоваровМаркетплейс();
	
	Для Каждого СтрокаОтчета Из ДанныеОтчета.rows Цикл
		
		Товар = СтрокаОтчета.item;
		
		Если Товар = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаТовара = ТаблицаТоваров.Добавить();
		НоваяСтрокаТовара.НомТов = СтрокаОтчета.rowNumber;
		
		НоваяСтрокаТовара.ИД = Формат(Товар.sku, "ЧГ=0");
		НоваяСтрокаТовара.КодМагазина = Формат(Товар.sku, "ЧГ=0");
		
		НоваяСтрокаТовара.НаимТов = Товар.name;
		НоваяСтрокаТовара.Артикул = Товар.offer_id;
		НоваяСтрокаТовара.ШтрихКод = Товар.barcode;
		
		Продажи = СтрокаОтчета.delivery_commission;
		Возвраты = СтрокаОтчета.return_commission;
		
		Если ТипЗнч(Продажи) = Тип("Структура") Тогда
			НоваяСтрокаТовара.Количество = Продажи.quantity;
			НоваяСтрокаТовара.Сумма = Продажи.amount;
			НоваяСтрокаТовара.ДоплатаПартнеров = Продажи.stars
				+ Продажи.bank_coinvestment
				+ Продажи.pick_up_point_coinvestment;
		КонецЕсли;
		
		Если ТипЗнч(Возвраты) = Тип("Структура") Тогда
			НоваяСтрокаТовара.КоличествоВозврат = Возвраты.quantity;
			НоваяСтрокаТовара.СуммаВозврат = Возвраты.amount;
			НоваяСтрокаТовара.ДоплатаПартнеровВозврат = Возвраты.stars
				+ Возвраты.bank_coinvestment
				+ Возвраты.pick_up_point_coinvestment;;
		КонецЕсли;
		
	КонецЦикла;
	
	КолонкиТаблицыТоваров = ТаблицаТоваров.Колонки;
	Если КолонкиТаблицыТоваров.Найти("НатуральныйИД") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("НатуральныйИД", ОбщегоНазначения.ОписаниеТипаСтрока(300));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("КоличествоВозврат") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("КоличествоВозврат", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СуммаВозврат") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СуммаВозврат", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СтавкаНДС") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СуммаНДС") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СуммаНДС", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	КонецЕсли;
	
	КолонкаНаимТов = КолонкиТаблицыТоваров.Найти("НаимТов");
	КолонкаНаименование = КолонкиТаблицыТоваров.Найти("Наименование");
	Если КолонкаНаимТов <> Неопределено
		И КолонкаНаименование = Неопределено Тогда
		КолонкаНаимТов.Имя = "Наименование";
	КонецЕсли;
	
	НастройкиСопоставления = Новый Структура;
	НастройкиСопоставления.Вставить("ОграничениеТипаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НастройкиСопоставления.Вставить("ОтключитьПоискПоСловарю",     Истина);
	НастройкиСопоставления.Вставить("ТочностьПоискаПоУмолчанию",   100);

	ДополнительныеПараметрыПоиска = Новый Структура;
	ДополнительныеПараметрыПоиска.Вставить("НатуральныеКлючи", "Артикул");
	ДополнительныеПараметрыПоиска.Вставить("Маркетплейс",      Перечисления.ВидыМаркетплейсов.OZON);
	НастройкиСопоставления.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметрыПоиска);
	
	НовыеКолонкиСопоставления = Новый Массив;
	ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
	ДополнительныйРеквизит.Имя = "КодМагазина";
	ДополнительныйРеквизит.Представление = "Код маркетплейса";
	НовыеКолонкиСопоставления.Добавить(ДополнительныйРеквизит);
	НастройкиСопоставления.Вставить("ДополнительныеРеквизитыСопоставления", НовыеКолонкиСопоставления);
	
	ВидимостьКолонокСопоставления = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ВидимостьКолонокСопоставления.Вставить("ЕдиницаИзмерения", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Упаковка", Ложь);
	ВидимостьКолонокСопоставления.Вставить("СтавкаНДС", Ложь);
	НастройкиСопоставления.Вставить("ВидимостьКолонокСопоставления", ВидимостьКолонокСопоставления);
	
	НастройкаИнтеграции = ПараметрыОтчета.НастройкаИнтеграции;
	ШапкаДокумента = ДанныеОтчета.header;
	
	ДатаВходящегоДокумента  = Дата(СтрЗаменить(ШапкаДокумента.doc_date,"-",""));
	НомерВходящегоДокумента = Формат(ШапкаДокумента.number, "ЧГ=0");
	Организация             = НастройкаИнтеграции.Владелец;         
	
	КонтрагентДоговор       = Справочники.НастройкиИнтеграцииМаркетплейс.КонтрагентДоговорНаДату(НастройкаИнтеграции, ДатаВходящегоДокумента);
	Контрагент              = КонтрагентДоговор.Контрагент;
	ДоговорКонтрагента      = КонтрагентДоговор.ДоговорКонтрагента;
	Дата                    = ПараметрыОтчета.ОкончаниеПериода;
	СуммаПродаж             = ТаблицаТоваров.Итог("Сумма") + ТаблицаТоваров.Итог("ДоплатаПартнеров");
	СуммаВозвратов          = ТаблицаТоваров.Итог("СуммаВозврат") + ТаблицаТоваров.Итог("ДоплатаПартнеровВозврат");
	
	Описание = СтрШаблон(НСтр("ru='Отчет о продажах Ozon от %1
		|Продажи: %2 Возвраты: %3'"), Формат(Дата, "ДФ=dd.MM.yyyy"), Формат(СуммаПродаж, "ЧДЦ=2; ЧН="), Формат(СуммаВозвратов, "ЧДЦ=2; ЧН="));
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("Плательщик",     Новый Структура("Ссылка", Контрагент));
	ДанныеДокумента.Вставить("Получатель",     Новый Структура("Ссылка", Организация));
	ДанныеДокумента.Вставить("Договор",        ДоговорКонтрагента);
	ДанныеДокумента.Вставить("ДатаНач",        ПараметрыОтчета.НачалоПериода);
	ДанныеДокумента.Вставить("Дата",           ДатаВходящегоДокумента);
	ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаТоваров);
	ДанныеДокумента.Вставить("ВидДокумента"  , ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzon());
	ДанныеДокумента.Вставить("Номер"         , НомерВходящегоДокумента);
	ОбменСМаркетплейс.ПервичныеДанныеМаркетплейсаИзJSON(ДанныеДокумента, ДанныеОтчета.rows);
	
	Файл = Новый Структура;
	Файл.Вставить("ПолноеИмяФайла",          НомерВходящегоДокумента);
	Файл.Вставить("ДанныеДокумента",         ДанныеДокумента);
	Файл.Вставить("ВидДокумента",            ДанныеДокумента.ВидДокумента);
	Файл.Вставить("Маркетплейс",             Перечисления.ВидыМаркетплейсов.OZON);
	Файл.Вставить("Организация",             Организация);
	Файл.Вставить("Контрагент",              Контрагент);
	Файл.Вставить("ДоговорКонтрагента",      ДоговорКонтрагента);
	Файл.Вставить("Дата",                    ДатаВходящегоДокумента);
	Файл.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
	Файл.Вставить("СуммаПродаж",             СуммаПродаж);
	Файл.Вставить("СуммаВозвратов",          СуммаВозвратов);
	Файл.Вставить("Загружать",               СуммаПродаж > 0 Или СуммаВозвратов > 0);
	Файл.Вставить("Описание",                Описание);
	Файл.Вставить("ТаблицаНоменклатура",     ТаблицаТоваров);
	Файл.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставления);
	Файл.Вставить("СкорректированНаПродажиЮрлицам", Ложь);
	
	ДанныеФайлов = Новый Массив();
	ДанныеФайлов.Добавить(Файл);
	
	Возврат ДанныеФайлов;
	
КонецФункции

Функция ПрочитатьФинансовыйОтчет(ПараметрыОтчета, Метод, СообщениеОбОшибке)
	
	Перем КодОтвета, СообщенияОбОшибках;
	
	ПоляЗапроса = Новый Структура;
	ПоляЗапроса.Вставить("date", Формат(ПараметрыОтчета.НачалоПериода, "ДФ=yyyy-MM"));
	ПоляЗапроса.Вставить("language", "DEFAULT");
	
	МетодЗапроса = СтрШаблон("/v1/finance/%1", Метод);
	РезультатЧтения = ПолучитьРезультатЗапроса(МетодЗапроса, ПоляЗапроса, ПараметрыОтчета.НастройкаИнтеграции, СообщенияОбОшибках, ,КодОтвета);
	
	Если ТипЗнч(СообщенияОбОшибках) = Тип("Структура") Тогда
		СообщениеОбОшибке = "";
		СообщенияОбОшибках.Свойство("message", СообщениеОбОшибке);
		
		СообщениеОбОшибке = ОписаниеОшибкиПоКоду(КодОтвета, СообщениеОбОшибке);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РезультатЧтения = Неопределено
		Или Не РезультатЧтения.Свойство("result")
		Или Не РезультатЧтения.result.Свойство("code") Тогда
		СообщениеОбОшибке = НСтр("ru = 'Данный формат ответа не поддерживается'");
		Возврат Неопределено;
	КонецЕсли;
	
	ПоляЗапроса = Новый Структура;
	ПоляЗапроса.Вставить("code", РезультатЧтения.result.code);
	
	МетодЗапроса = "/v1/report/info";
	
	ФайлСформирован = Ложь;
	ОсталосьКоличествоПопыток = 3;
	Пока Не ФайлСформирован И ОсталосьКоличествоПопыток > 0 Цикл
		РезультатЧтения = ПолучитьРезультатЗапроса(МетодЗапроса, ПоляЗапроса, ПараметрыОтчета.НастройкаИнтеграции, СообщенияОбОшибках, ,КодОтвета);
		
		Если ТипЗнч(СообщенияОбОшибках) = Тип("Структура") Тогда
			СообщениеОбОшибке = "";
			СообщенияОбОшибках.Свойство("message", СообщениеОбОшибке);
			
			СообщениеОбОшибке = ОписаниеОшибкиПоКоду(КодОтвета, СообщениеОбОшибке);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если РезультатЧтения = Неопределено
			Или Не РезультатЧтения.Свойство("result")
			Или Не РезультатЧтения.result.Свойство("status") Тогда
			СообщениеОбОшибке = НСтр("ru = 'Данный формат ответа не поддерживается'");
			Возврат Неопределено;
		КонецЕсли;
		
		ОсталосьКоличествоПопыток = ОсталосьКоличествоПопыток - 1;
		Если РезультатЧтения.result.status = "success" Тогда
			ФайлСформирован = Истина;
		Иначе
			ИнтернетПоддержкаПользователей.Пауза(3);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ФайлСформирован Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось получить файл &ТекстФайлОтчета.'");
		Возврат Неопределено;
	КонецЕсли;
	
	Если РезультатЧтения = Неопределено
		Или Не РезультатЧтения.Свойство("result")
		Или Не РезультатЧтения.result.Свойство("file") Тогда
		СообщениеОбОшибке = НСтр("ru = 'Данный формат ответа не поддерживается'");
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(РезультатЧтения.result.file);
	Соединение = Новый HTTPСоединение(СтруктураURI.Хост);
	ЗапросHTTP = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере);
	
	ИмяФайлаРеестра = ПолучитьИмяВременногоФайла("xlsx");
	
	Попытка
		ОтветHTTP = Соединение.Получить(ЗапросHTTP, ИмяФайлаРеестра);
	Исключение
		СообщениеОбОшибке = НСтр(СтрШаблон("ru = 'Ошибка при получении файла &ТекстФайлОтчета: %1'",
			ОписаниеОшибки()));
		Возврат Неопределено;
	КонецПопытки;
	
	Если ОтветHTTP.КодСостояния <> 200 Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалось получить файл &ТекстФайлОтчета.'");
		Возврат Неопределено;
	КонецЕсли;
	
	ФайлРеестра = Новый Файл(ИмяФайлаРеестра);
	
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("ИмяФайла", ФайлРеестра.Имя);
	ОписаниеФайла.Вставить("РасширениеФайла", ФайлРеестра.Расширение);
	ОписаниеФайла.Вставить("ПолноеИмяФайла", ФайлРеестра.ПолноеИмя);
	ОписаниеФайла.Вставить("ВременныйФайл", ИмяФайлаРеестра);
	ОтправляемыеФайлы = ОбщегоНазначенияКлиентСервер.МассивЗначений(ОписаниеФайла);
	
	Возврат ЭлектронноеВзаимодействиеБП.ПолучитьДанныеФайлов(ОтправляемыеФайлы);
	
КонецФункции

Процедура УбратьПродажиЮрлицамИзОтчетаОРеализации(ДанныеФайлаОтчетОРеализации, ДанныеФайлаРеестрПродажЮрлицам)
	
	ТаблицаТоваровОтчетОРеализации    = ДанныеФайлаОтчетОРеализации.ДанныеДокумента.ТаблицаТоваров;
	ТаблицаТоваровРеестрПродажЮрлицам = ДанныеФайлаРеестрПродажЮрлицам.ДанныеДокумента.ТаблицаТоваров;
	
	ОшибкиПреобразования = Новый Массив;
	
	ПредставлениеОтчетаОРеализации = СтрШаблон(НСтр("ru = 'В отчете о продажах Ozon от %1'"),
		Формат(ДанныеФайлаОтчетОРеализации.Дата, "ДФ=dd.MM.yyyy"));
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваровРеестрПродажЮрлицам Цикл
		
		ЭтоДокументНаВозврат = ЭтоДокументНаВозврат(СтрокаТаблицы.ТипДокумента);
		РеквизитКоличество = ?(ЭтоДокументНаВозврат, "КоличествоВозврат", "Количество");
		РеквизитСумма = ?(ЭтоДокументНаВозврат, "СуммаВозврат", "Сумма");
		
		// Артикул + КодМагазина + Цена
		ПоискСтрок = Новый Структура("Артикул, КодМагазина",
			СтрокаТаблицы.Артикул, СтрокаТаблицы.КодМагазина);
		
		НайденныеСтроки = ТаблицаТоваровОтчетОРеализации.НайтиСтроки(ПоискСтрок);
		Если НайденныеСтроки.Количество() = 0 Тогда
			ПоискСтрок = Новый Структура("КодМагазина", СтрокаТаблицы.КодМагазина);
			НайденныеСтроки = ТаблицаТоваровОтчетОРеализации.НайтиСтроки(ПоискСтрок);
			Если НайденныеСтроки.Количество() = 0 Тогда
				Ошибка = СтрШаблон("%1 не найден товар с параметрами Код товара продавца: %2, Код товара Ozon: %3",
					ПредставлениеОтчетаОРеализации,
					СтрокаТаблицы.Артикул,
					СтрокаТаблицы.КодМагазина);
				ОшибкиПреобразования.Добавить(Ошибка);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Цена = ?(СтрокаТаблицы.Количество = 0, 0, Окр(СтрокаТаблицы.Сумма/СтрокаТаблицы.Количество, 2, 0));
		Если Цена = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаНайдена = Ложь;
		ОсталосьСписатьКоличество = СтрокаТаблицы.Количество;
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			Если ОсталосьСписатьКоличество = 0 Тогда
				Прервать;
			КонецЕсли;
			Если НайденнаяСтрока[РеквизитКоличество] = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НайденнаяСтрокаЦена = ?(НайденнаяСтрока[РеквизитКоличество] = 0, 0, НайденнаяСтрока[РеквизитСумма] / НайденнаяСтрока[РеквизитКоличество]);
			
			Если Цена = НайденнаяСтрокаЦена Тогда
				КоличествоСписать = Мин(ОсталосьСписатьКоличество, НайденнаяСтрока[РеквизитКоличество]);
				
				Если НайденнаяСтрока[РеквизитКоличество] = КоличествоСписать Тогда // списываем строку полностью
					Если ЭтоДокументНаВозврат Тогда
						НайденнаяСтрока.СуммаВозврат = 0;
						НайденнаяСтрока.ДоплатаПартнеровВозврат = 0;
					Иначе
						НайденнаяСтрока.Реализовано  = 0;
						НайденнаяСтрока.Сумма        = 0;
						НайденнаяСтрока.ДоплатаПартнеров = 0;
					КонецЕсли;
				Иначе
					К = ?(НайденнаяСтрока[РеквизитКоличество] = 0, 0, КоличествоСписать / НайденнаяСтрока[РеквизитКоличество]);
					Если ЭтоДокументНаВозврат Тогда
						НайденнаяСтрока.СуммаВозврат = НайденнаяСтрока.СуммаВозврат - Окр(К * НайденнаяСтрока.СуммаВозврат, 2, 0);
						НайденнаяСтрока.ДоплатаПартнеровВозврат = НайденнаяСтрока.ДоплатаПартнеровВозврат - Окр(К * НайденнаяСтрока.ДоплатаПартнеровВозврат, 2, 0);
					Иначе
						НайденнаяСтрока.Реализовано  = НайденнаяСтрока.Реализовано - Окр(К * НайденнаяСтрока.Реализовано, 2, 0);
						НайденнаяСтрока.Сумма        = НайденнаяСтрока.Сумма - Окр(К * НайденнаяСтрока.Сумма, 2, 0);
						НайденнаяСтрока.ДоплатаПартнеров = НайденнаяСтрока.ДоплатаПартнеров - Окр(К * НайденнаяСтрока.ДоплатаПартнеров, 2, 0);
					КонецЕсли;
				КонецЕсли;
				
				СтрокаНайдена = Истина;
				ОсталосьСписатьКоличество = ОсталосьСписатьКоличество - КоличествоСписать;
				НайденнаяСтрока[РеквизитКоличество] = НайденнаяСтрока[РеквизитКоличество] - КоличествоСписать;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не СтрокаНайдена Тогда
			Ошибка = СтрШаблон("%1 не найден товар с параметрами Код товара продавца: %2, Код товара Ozon: %3, Цена: %4",
				ПредставлениеОтчетаОРеализации,
				СтрокаТаблицы.Артикул,
				СтрокаТаблицы.КодМагазина,
				Цена);
			ОшибкиПреобразования.Добавить(Ошибка);
		КонецЕсли;
		
		Если ОсталосьСписатьКоличество > 0 Тогда
			Ошибка = СтрШаблон("%1 для товара с параметрами Код товара продавца: %2, Код товара Ozon: %3, Цена: %4,
				|не удалось удалить строки с товаром в количестве %5 из %6 единиц.",
				ПредставлениеОтчетаОРеализации,
				СтрокаТаблицы.Артикул,
				СтрокаТаблицы.КодМагазина,
				Цена,
				ОсталосьСписатьКоличество,
				СтрокаТаблицы.Количество);
			ОшибкиПреобразования.Добавить(Ошибка);
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДокумента = ДанныеФайлаОтчетОРеализации.ДанныеДокумента;
	
	СуммаПродаж    = ТаблицаТоваровОтчетОРеализации.Итог("Сумма")
		+ ТаблицаТоваровОтчетОРеализации.Итог("ДоплатаПартнеров");
	СуммаВозвратов = ТаблицаТоваровОтчетОРеализации.Итог("СуммаВозврат")
		+ ТаблицаТоваровОтчетОРеализации.Итог("ДоплатаПартнеровВозврат");
	
	Если ЭлектронноеВзаимодействиеБП.УчитыватьДоплатуЗаСчетOzon(ДанныеДокумента.Дата) Тогда
		СуммаПродаж    = СуммаПродаж + ТаблицаТоваровОтчетОРеализации.Итог("Доплата");
		СуммаВозвратов = СуммаВозвратов + ТаблицаТоваровОтчетОРеализации.Итог("ДоплатаВозврат");
	КонецЕсли;
	
	Описание = СтрШаблон(НСтр("ru='Отчет о продажах Ozon от %1
		|Продажи: %2 Возвраты: %3
		|Скорректирован на сумму продаж юрлицам'"),
		Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy"),
		Формат(СуммаПродаж, "ЧДЦ=2; ЧН="),
		Формат(СуммаВозвратов, "ЧДЦ=2; ЧН="));
	
	ДанныеФайлаОтчетОРеализации.Вставить("СуммаПродаж", СуммаПродаж);
	ДанныеФайлаОтчетОРеализации.Вставить("СуммаВозвратов", СуммаВозвратов);
	ДанныеФайлаОтчетОРеализации.Вставить("Описание", Описание);
	ДанныеФайлаОтчетОРеализации.Вставить("СкорректированНаПродажиЮрлицам", Истина);
	Если ОшибкиПреобразования.Количество() > 0 Тогда
		ДанныеФайлаОтчетОРеализации.Вставить("ОшибкиПреобразования", СтрСоединить(ОшибкиПреобразования, Символы.ПС));
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоДокументНаВозврат(ТипДокумента)
	
	Возврат Врег(Лев(СокрЛП(ТипДокумента), 3)) = "УКД"
	
КонецФункции

Функция ПрочитатьОтчетОВыкупеOZON(ПараметрыОтчета, СообщениеОбОшибке)
	Перем КодОтвета, СообщенияОбОшибках;
	
	ПоляЗапроса = Новый Структура();
	ПоляЗапроса.Вставить("date_from", Формат(НачалоМесяца(ПараметрыОтчета.НачалоПериода), "ДФ=yyyy-MM-dd"));
	ПоляЗапроса.Вставить("date_to", Формат(КонецМесяца(ПараметрыОтчета.НачалоПериода), "ДФ=yyyy-MM-dd"));
	
	МетодЗапроса = "/v1/finance/products/buyout";
	РезультатЧтения = ПолучитьРезультатЗапроса(МетодЗапроса, ПоляЗапроса, ПараметрыОтчета.НастройкаИнтеграции, СообщенияОбОшибках, ,КодОтвета);
	
	Если ТипЗнч(СообщенияОбОшибках) = Тип("Структура") Тогда
		Если СокрЛП(СообщенияОбОшибках.message) = "Request validation error: out of date" Тогда
			ШаблонСообщения = НСтр("ru = 'Отчет за период %1 недоступен'");
			СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, Формат(ПараметрыОтчета.НачалоПериода));
		иначе
			СообщениеОбОшибке = СообщенияОбОшибках.message;
		КонецЕсли;
		
		СообщениеОбОшибке = ОписаниеОшибкиПоКоду(КодОтвета, СообщениеОбОшибке);
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.OZON);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РезультатЧтения = Неопределено Тогда 
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'Отсутствует данные отчета за %1. Попробуйте загрузить отчет позже'"),
		Формат(ПараметрыОтчета.НачалоПериода,"ДЛФ=D"));
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.OZON);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЧтения;
КонецФункции

Функция ПреобразоватьДанныеОтчетаОВыкупе(ПараметрыОтчета, ДанныеОтчета)
	
	ТаблицаТоваров = ЭлектронноеВзаимодействиеБП.ПодготовитьТаблицуТоваровМаркетплейс();
	
	КолонкиТаблицыТоваров = ТаблицаТоваров.Колонки;
	Если КолонкиТаблицыТоваров.Найти("СтавкаНДС") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СтавкаНДС", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	КонецЕсли;
	
	Для Каждого СтрокаОтчета Из ДанныеОтчета.products Цикл
		
		НоваяСтрокаТовара = ТаблицаТоваров.Добавить();
		
		НоваяСтрокаТовара.ИД          = Формат(СтрокаОтчета.sku, "ЧГ=0");
		НоваяСтрокаТовара.КодМагазина = Формат(СтрокаОтчета.sku, "ЧГ=0");
		
		НоваяСтрокаТовара.НаимТов     = СтрокаОтчета.name;
		НоваяСтрокаТовара.Артикул     = СтрокаОтчета.offer_id;
		
		НоваяСтрокаТовара.Количество  = СтрокаОтчета.quantity;
		НоваяСтрокаТовара.Сумма       = СтрокаОтчета.amount;
		
		НоваяСтрокаТовара.СтавкаНДС   = СтрокаОтчета.vat_percent;
		
	КонецЦикла;
	
	Если КолонкиТаблицыТоваров.Найти("НатуральныйИД") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("НатуральныйИД", ОбщегоНазначения.ОписаниеТипаСтрока(300));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("КоличествоВозврат") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("КоличествоВозврат", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СуммаВозврат") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СуммаВозврат", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("СуммаНДС") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("СуммаНДС", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	КонецЕсли;
	
	КолонкаНаимТов = КолонкиТаблицыТоваров.Найти("НаимТов");
	КолонкаНаименование = КолонкиТаблицыТоваров.Найти("Наименование");
	Если КолонкаНаимТов <> Неопределено
		И КолонкаНаименование = Неопределено Тогда
		КолонкаНаимТов.Имя = "Наименование";
	КонецЕсли;
	
	НастройкиСопоставления = Новый Структура;
	НастройкиСопоставления.Вставить("ОграничениеТипаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НастройкиСопоставления.Вставить("ОтключитьПоискПоСловарю",     Истина);
	НастройкиСопоставления.Вставить("ТочностьПоискаПоУмолчанию",   100);

	ДополнительныеПараметрыПоиска = Новый Структура;
	ДополнительныеПараметрыПоиска.Вставить("НатуральныеКлючи", "Артикул");
	ДополнительныеПараметрыПоиска.Вставить("Маркетплейс",      Перечисления.ВидыМаркетплейсов.OZON);
	НастройкиСопоставления.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметрыПоиска);
	
	НовыеКолонкиСопоставления = Новый Массив;
	ДополнительныйРеквизит = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыйДополнительныйРеквизитСопоставления();
	ДополнительныйРеквизит.Имя = "КодМагазина";
	ДополнительныйРеквизит.Представление = "Код маркетплейса";
	НовыеКолонкиСопоставления.Добавить(ДополнительныйРеквизит);
	НастройкиСопоставления.Вставить("ДополнительныеРеквизитыСопоставления", НовыеКолонкиСопоставления);
	
	ВидимостьКолонокСопоставления = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеНастройкиВидимостиКолонокСопоставления();
	ВидимостьКолонокСопоставления.Вставить("ЕдиницаИзмерения", Ложь);
	ВидимостьКолонокСопоставления.Вставить("Упаковка", Ложь);
	ВидимостьКолонокСопоставления.Вставить("СтавкаНДС", Истина);
	НастройкиСопоставления.Вставить("ВидимостьКолонокСопоставления", ВидимостьКолонокСопоставления);
	
	НастройкаИнтеграции = ПараметрыОтчета.НастройкаИнтеграции;
	
	ДатаВходящегоДокумента  = ПараметрыОтчета.ОкончаниеПериода;
	Организация             = НастройкаИнтеграции.Владелец;
	
	КонтрагентДоговор       = Справочники.НастройкиИнтеграцииМаркетплейс.КонтрагентДоговорНаДату(НастройкаИнтеграции, ДатаВходящегоДокумента);
	Контрагент              = КонтрагентДоговор.Контрагент;
	ДоговорКонтрагента      = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента, Контрагент, Организация, 
		ЗначениеВМассив(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем), , Истина);
	Дата                    = ПараметрыОтчета.ОкончаниеПериода;
	СуммаВыкупа             = ТаблицаТоваров.Итог("Сумма");
	
	Описание = СтрШаблон(НСтр("ru='Отчет о выкупе Ozon от %1
		|Продажи: %2'"), Формат(Дата, "ДФ=dd.MM.yyyy"), Формат(СуммаВыкупа, "ЧДЦ=2; ЧН="));
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("Плательщик",     Новый Структура("Ссылка", Контрагент));
	ДанныеДокумента.Вставить("Получатель",     Новый Структура("Ссылка", Организация));
	ДанныеДокумента.Вставить("Договор",        ДоговорКонтрагента);
	ДанныеДокумента.Вставить("ДатаНач",        ПараметрыОтчета.НачалоПериода);
	ДанныеДокумента.Вставить("Дата",           ДатаВходящегоДокумента);
	ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаТоваров);
	ДанныеДокумента.Вставить("ВидДокумента"  , ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonВыкуп());
	ДанныеДокумента.Вставить("Номер"         , "");
	ОбменСМаркетплейс.ПервичныеДанныеМаркетплейсаИзJSON(ДанныеДокумента, ДанныеОтчета.products);
	
	Файл = Новый Структура;
	Файл.Вставить("ПолноеИмяФайла",          "");
	Файл.Вставить("ДанныеДокумента",         ДанныеДокумента);
	Файл.Вставить("ВидДокумента",            ДанныеДокумента.ВидДокумента);
	Файл.Вставить("Маркетплейс",             Перечисления.ВидыМаркетплейсов.OZON);
	Файл.Вставить("Организация",             Организация);
	Файл.Вставить("Контрагент",              Контрагент);
	Файл.Вставить("ДоговорКонтрагента",      ДоговорКонтрагента);
	Файл.Вставить("Дата",                    ДатаВходящегоДокумента);
	Файл.Вставить("НомерВходящегоДокумента", "");
	Файл.Вставить("СуммаВыкупа",             СуммаВыкупа);
	Файл.Вставить("Загружать",               СуммаВыкупа > 0);
	Файл.Вставить("Описание",                Описание);
	Файл.Вставить("ТаблицаНоменклатура",     ТаблицаТоваров);
	Файл.Вставить("НастройкиСопоставленияНоменклатуры", НастройкиСопоставления);
	Файл.Вставить("СкорректированНаПродажиЮрлицам", Ложь);
	
	ДанныеФайлов = Новый Массив();
	ДанныеФайлов.Добавить(Файл);
	
	Возврат ДанныеФайлов;
	
КонецФункции

#КонецОбласти

#Область РаботаСЗапросами
	
// Возвращает результат запроса к сервису.
//
// Параметры:
//   ТекстЗапроса         - Строка - метод API.
//   ПараметрыЗапроса     - Структура - параметры запроса согласно описания запрашиваемого метода API.
//   НастройкаИнтеграции  - СправочникСсылка.НастройкиИнтеграцииМаркетплейс - настройка интеграции.
//   АтрибутыРезультата   - Структура - каждому ключу передается в качестве значения имя атрибута из ответа метода API, 
//                          в котором содержится необходимая информация. По ключам этого параметра заполняется возвращаемое значение. 
// 
// Возвращаемое значение:
//   Структура    - содержимое атрибутов результата на основании ключей параметра АтрибутыРезультата.
//   Соответствие - ответ сервиса.
//   Неопределено - при выполнении запроса к внешнему ресурсу возникли ошибки.
//
Функция ПолучитьРезультатЗапроса(ТекстЗапроса, ПараметрыЗапроса, НастройкаИнтеграции, СообщенияОбОшибках, 
		Знач АтрибутыРезультата = Неопределено, КодОтвета = Неопределено, ПрочитатьВСоответствие = Ложь)

	Результат        = Новый Структура;
	РезультатЗапроса = ВыполнитьЗапросКСервису(ТекстЗапроса, ПараметрыЗапроса, НастройкаИнтеграции); 
	
	КодОтвета = РезультатЗапроса.КодСостояния;
	Если КодОтвета <> 200 Тогда
		ПолноеОписаниеОшибки = РезультатЗапроса.ПолучитьТелоКакСтроку();
		СообщенияОбОшибках = ОбщегоНазначенияБП.СтруктураИзСтрокиJSON(ПолноеОписаниеОшибки);
	КонецЕсли;

	Если ЗначениеЗаполнено(СообщенияОбОшибках) тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ОбменСМаркетплейс.ПолучитьДанныеОтвета(РезультатЗапроса, ПрочитатьВСоответствие);
	Исключение
		СообщенияОбОшибках = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка разбора данных, полученных от маркетплейса. Причина: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(
			СообщенияОбОшибках, Перечисления.ВидыМаркетплейсов.OZON);
			
		Возврат Неопределено;
		
	КонецПопытки;

	Если ЗначениеЗаполнено(АтрибутыРезультата) 
		И ЗначениеЗаполнено(ДанныеОтвета) Тогда
		
		Результат = ОбменСМаркетплейс.РазобратьДанныеОтвета(ДанныеОтвета, АтрибутыРезультата);
	Иначе
		Результат = ДанныеОтвета;
	КонецЕсли;

	Возврат Результат;
КонецФункции

// Выполняет POST запрос к сервису
Функция ВыполнитьЗапросКСервису(МетодЗапроса, ПараметрыЗапроса, НастройкаИнтеграции)

	Таймаут      = 240;
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI("https://api-seller.ozon.ru");
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , ,
		ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
		
	СлужебныеПараметры = СлужебныеПараметрыHTTPЗапроса();
	Если ПараметрыЗапроса.Свойство("СлужебныеПараметры") Тогда
		ЗаполнитьЗначенияСвойств(СлужебныеПараметры, ПараметрыЗапроса.СлужебныеПараметры);
		ПараметрыЗапроса.Удалить("СлужебныеПараметры");
	КонецЕсли;
	
	ПрочитатьВСоответствие = Истина;
	Если Не СлужебныеПараметры.Свойство("ПрочитатьВСоответствие", ПрочитатьВСоответствие) Тогда
		ПрочитатьВСоответствие = Истина;
	КонецЕсли;

	ДаныеАвторизации = ПрочитатьНастройкиАвторизации(НастройкаИнтеграции);
	
	КлиентИд = ДаныеАвторизации.КлиентИд;
	Токен    = ДаныеАвторизации.Токен;
	
	ЗаголовкиHTTPЗапроса = Новый Соответствие();
	ЗаголовкиHTTPЗапроса.Вставить("Client-Id",     КлиентИд);
	ЗаголовкиHTTPЗапроса.Вставить("Api-Key",       Токен);
	ЗаголовкиHTTPЗапроса.Вставить("Content-Type",  СлужебныеПараметры.ContentType);
	ЗаголовкиHTTPЗапроса.Вставить("app-name",     "vendor_1c_bp3");
	
	Если СлужебныеПараметры.ЭтоДвоичныеДанные Тогда
		ЗаголовкиHTTPЗапроса.Вставить("Content-Lenght", СлужебныеПараметры.ContentLenght);
	КонецЕсли;
	
	HTTPЗапрос = Новый HTTPЗапрос(МетодЗапроса, ЗаголовкиHTTPЗапроса);
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса) Тогда
		Если СлужебныеПараметры.ЭтоДвоичныеДанные Тогда
			HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(ПараметрыЗапроса.ДвоичныеДанные);
		Иначе
			ТелоЗапроса = ОбменСМаркетплейс.ВJSON(ПараметрыЗапроса);
			HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF8", ИспользованиеByteOrderMark.НеИспользовать);
		КонецЕсли;
	КонецЕсли;
	
	Попытка 
		HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос); // POST
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(СообщениеОбОшибке, Перечисления.ВидыМаркетплейсов.OZON);
		ВызватьИсключение;
	КонецПопытки;
	
	//Логирование
	Если РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЛогированиеВключено() Тогда
		Логирование = РегистрыСведений.ЖурналОбменаСМаркетплейсами.ПараметрыЛогирования(НастройкаИнтеграции);
		Если HTTPСоединение = Неопределено Тогда
			Логирование.АдресСервера = СтруктураURI.Хост;
		Иначе
			Логирование.АдресСервера 	= HTTPСоединение.Сервер +":"+HTTPСоединение.Порт;
			Логирование.Соединение 		= Истина;
			Логирование.Метод 			= "POST";
			Логирование.Запрос 			= HTTPЗапрос;
			Логирование.Ответ 			= HTTPОтвет;
		КонецЕсли;
		РегистрыСведений.ЖурналОбменаСМаркетплейсами.ЗаписатьВЖурнал(Логирование);
	КонецЕсли;
	//Конец Логирование
	
	Возврат HTTPОтвет;
	
КонецФункции

// Конструктор служебных параметров HTTP-запроса.
// 
// Возвращаемое значение:
//   Структура - служебные параметры, содержащие реквизиты:
//     * ПрочитатьВСоответствие       - Булево - если установлено Истина, результат выполнения запроса будет представлен как Соответствие. 
//                                        Если установлено Ложь, результат представляется как Структура. Значение по умолчанию: Истина.
//     * ЭтоДвоичныеДанные            - Булево - признак передачи в тело запроса двоичных данных. Значение по умолчанию: Ложь.
//                                        Если установлена Истина, необходимо заполнить параметр структуры ДвоичныеДанные.
//     * ВернутьКодСостояния          - Булево - если установлено Истина, результат выполнения запроса будет содержать код состояния (ответа)
//                                        HTTP-сервера, см. http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html.
//     * ВернутьСтрокуОтвета          - Булево - если установлена Истина, результат выполнения запроса возвращается без анализа кода состояния.
//     * ПодстрокаПоискаВТекстеОтвета - Строка - если при выполнении запроса получен ответ, в котором содержится заданная подстрока поиска,
//                                        этот результат не считается ошибкой; будет возвращен ПустойРезультат.
//     * ПустойРезультат              - Произвольный - результат выполнения для заданной подстроки поиска в ответе сервиса.
//     * ПоляТипаДата                 - Массив, Строка, ФиксированныйМассив - массив, элементы которого содержат имена свойств JSON, для 
//                                        которых нужно вызывать восстановление даты из строки.
//     * ContentType                  - Строка - элемент заголовка для отправки на сервер.
//     * ContentLenght                - Строка - элемент заголовка для отправки на сервер.
//
Функция СлужебныеПараметрыHTTPЗапроса()

	СлужебныеПараметры = Новый Структура;
	СлужебныеПараметры.Вставить("ПрочитатьВСоответствие",       Истина);
	СлужебныеПараметры.Вставить("ЭтоДвоичныеДанные",            Ложь);
	СлужебныеПараметры.Вставить("ВернутьКодСостояния",          Ложь);
	СлужебныеПараметры.Вставить("ВернутьСтрокуОтвета",          Ложь);
	СлужебныеПараметры.Вставить("ПодстрокаПоискаВТекстеОтвета", "");
	СлужебныеПараметры.Вставить("ПустойРезультат",              Неопределено);
	СлужебныеПараметры.Вставить("ПоляТипаДата",                 "");
	СлужебныеПараметры.Вставить("ContentType",                  "application/json");
	СлужебныеПараметры.Вставить("ContentLenght",                "");

	Возврат СлужебныеПараметры;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Преобразует исходное число в строку в фиксированном формате обмена с сервисом Ozon.
//
// Параметры:
//   Значение                  - Число - форматируемое значение.
//   ВыводитьПредставлениеНуля - Булево - признак включения в результат нулевых значений.
//
// Возвращаемое значение:
//   Строка - строка в фиксированном формате.
//
Функция ЧислоВСтроку(Знач Значение, ВыводитьПредставлениеНуля = Ложь)

	Если Значение = Неопределено Тогда
		Значение = 0;
	КонецЕсли;

	ФорматнаяСтрока = ?(ВыводитьПредставлениеНуля, "ЧН=; ЧРД=.; ЧГ=", "ЧРД=.; ЧГ=");
	Возврат Формат(Значение, ФорматнаяСтрока);  

КонецФункции 

// Преобразует исходную строку в число без вызова исключений.
// Аналог функции СтроковыеФункцииКлиентСервер.СтрокаВЧисло() с дополнительной обработкой строки без нулей.
//
// Параметры:
//   Значение - Строка - строка, которую необходимо привести к числу.
//
// Возвращаемое значение:
//   - Число        - полученное число.
//   - Неопределено - если строка не является числом.
//
Функция СтрокаВЧисло(Знач Значение) Экспорт
	
	Если Значение = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Возврат Значение;
	КонецЕсли;
	
	Значение = СтрЗаменить(Значение, " ", "");
	Если СтрНачинаетсяС(Значение, "(") Тогда
		Значение = СтрЗаменить(Значение, "(", "-");
		Значение = СтрЗаменить(Значение, ")", "");
	КонецЕсли;
	
	СтрокаБезНулей = СтрЗаменить(Значение, "0", "");
	Если ПустаяСтрока(СтрокаБезНулей) ИЛИ СтрокаБезНулей = "-" ИЛИ СтрокаБезНулей = "." ИЛИ СтрокаБезНулей = "," Тогда
		Возврат 0;
	КонецЕсли;
	
	ТипЧисло  = Новый ОписаниеТипов("Число");
	Результат = ТипЧисло.ПривестиЗначение(Значение);
	
	Возврат ?(Результат <> 0 И Не ПустаяСтрока(СтрокаБезНулей), Результат, Неопределено);
	
КонецФункции 

Функция СообщениеОбОшибкеДляПользователя(КодОтвета)
	Перем СообщениеОбОшибке;
	
	// Озон использует 403 во всех случаях нарушения доступа, а 401 не использует вовсе
	Если КодОтвета = 403 Тогда
		СообщениеОбОшибке = НСтр("ru = 'Ошибка авторизации в API OZON.
			|Возможно неверные Client-ID или API-key в настройках подключения.'");
	КонецЕсли;
	
	Возврат СообщениеОбОшибке;
КонецФункции

Функция ОписаниеОшибкиПоКоду(КодОтвета, ПолноеОписаниеОшибки)
	ПоддерживаемыеКодыОшибок = Новый Соответствие;
	ПоддерживаемыеКодыОшибок.Вставить(400, "Неверный параметр");
	ПоддерживаемыеКодыОшибок.Вставить(401, "Укажите в настройках Client-Id или Api-Key");
	ПоддерживаемыеКодыОшибок.Вставить(403, "Доступ запрещен");
	ПоддерживаемыеКодыОшибок.Вставить(404, "Ответ не найден");
	ПоддерживаемыеКодыОшибок.Вставить(409, "Конфликт запроса");
	ПоддерживаемыеКодыОшибок.Вставить(500, "Внутренняя ошибка сервера OZON");
	
	ШаблонОписания = "%1: %2";
	
	КраткоеОписаниеОшибки = ?(ПоддерживаемыеКодыОшибок[КодОтвета] <> Неопределено,
		ПоддерживаемыеКодыОшибок[КодОтвета],
		КодОтвета);
	
	ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписания,
		КраткоеОписаниеОшибки,
		ПолноеОписаниеОшибки);
	
	ТекстОшибкиОбмена = СтрШаблон(НСтр("ru = 'Ошибка получения данных из маркетплейса. Ответ сервера:
		|%1'"), ОписаниеОшибки);
	
	Возврат ТекстОшибкиОбмена;
	
КонецФункции

Процедура ЗарегистрироватьОшибку(КодОтвета, ПолноеОписаниеОшибки)
	ТекстОшибкиОбмена = ОписаниеОшибкиПоКоду(КодОтвета, ПолноеОписаниеОшибки);
	ОбменСМаркетплейс.ЗарегистрироватьОшибкуОбмена(ТекстОшибкиОбмена, Перечисления.ВидыМаркетплейсов.OZON);
КонецПроцедуры

// Преобразует исходное значение в массив значений.
//
// Параметры:
//   Значение               - Произвольный - исходное значение.
//   ПреобразоватьВСтроку   - Булево - Истина, если необходимо преобразовать значение из числа в строку.
//   ПреобразоватьВЧисло    - Булево - Истина, если необходимо преобразовать значение из строки в число.
//   ИсключатьНеЗаполненные - Булево - Истина, если необходимо исключить незаполненные значения.
//
// Возвращаемое значение:
//   Массив Из Произвольный - массив значений.
//
Функция ЗначениеВМассив(Знач Значение, ПреобразоватьВСтроку = Ложь, ПреобразоватьВЧисло = Ложь, ИсключатьНеЗаполненные = Истина)
	
	Результат = Новый Массив;

	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		Для каждого ЭлементКоллекции Из Значение Цикл
			Если НЕ ИсключатьНеЗаполненные
				 ИЛИ ЗначениеЗаполнено(ЭлементКоллекции) Тогда
				Если ПреобразоватьВСтроку
					 И ТипЗнч(ЭлементКоллекции) = Тип("Число") Тогда
					Результат.Добавить(ЧислоВСтроку(ЭлементКоллекции));
				ИначеЕсли ПреобразоватьВЧисло
					 И ТипЗнч(ЭлементКоллекции) = Тип("Строка") Тогда
					Результат.Добавить(СтрокаВЧисло(ЭлементКоллекции));
				Иначе
					Результат.Добавить(ЭлементКоллекции);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Если НЕ ИсключатьНеЗаполненные
			 ИЛИ ЗначениеЗаполнено(Значение) Тогда
			Если ПреобразоватьВСтроку
				 И ТипЗнч(Значение) = Тип("Число") Тогда
				Результат.Добавить(ЧислоВСтроку(Значение));
			ИначеЕсли ПреобразоватьВЧисло
				 И ТипЗнч(Значение) = Тип("Строка") Тогда
				Результат.Добавить(СтрокаВЧисло(Значение));
			Иначе
				Результат.Добавить(Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


