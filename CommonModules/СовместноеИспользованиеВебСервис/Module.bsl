
#Область СлужебныйПрограммныйИнтерфейс

Функция ПроверитьКорректностьДанныхДляОрганизации(ДанныеЗапроса) Экспорт
	
	РезультатПроверки = НовыйРезультатПроверкиJSON();
	
	Если НЕ ДанныеЗапроса.Свойство("entity")
		Или НЕ ЗначениеЗаполнено(ДанныеЗапроса.entity) Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре JSON отсутствует или не заполнена обязательная структура entity'");
		Возврат РезультатПроверки;
	КонецЕсли;
	
	ДанныеОрганизации = ДанныеЗапроса.entity;
	
	ПроверитьНаличиеОбязательныхСтруктур(ДанныеОрганизации, РезультатПроверки);
	
	Если РезультатПроверки.Ошибка Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	Если ДанныеОрганизации.Свойство("organization") Тогда
		ПроверитьКорректностьОбязательныхРеквизитовОрганизации(ДанныеОрганизации.organization, РезультатПроверки);
	Иначе
		ПроверитьКорректностьДанныхИндивидуальногоПредпринимателя(ДанныеОрганизации.entrepreneur, РезультатПроверки);
	КонецЕсли;
	
	ПроверитьКорректностьНаименования(ДанныеОрганизации, РезультатПроверки);
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция СоздатьОрганизацию(ДанныеОрганизации) Экспорт
	
	РезультатСоздания = НовыйРезультатСозданияОрганизации();
	
	ПараметрыСозданияОрганизации = ПараметрыСозданияОрганизацииПоДаннымОрганизации(ДанныеОрганизации);
	
	СуществующаяОрганизация = Справочники.Организации.НайтиОрганизацию(ПараметрыСозданияОрганизации.ИНН, ПараметрыСозданияОрганизации.КПП);
	Если ЗначениеЗаполнено(СуществующаяОрганизация) Тогда
		РезультатСоздания.Организация = СуществующаяОрганизация;
		РезультатСоздания.СозданаНовая = Ложь;
		Возврат РезультатСоздания;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.УникальныйИдентификатор) Тогда
		СуществующаяОрганизация = Справочники.Организации.ПолучитьСсылку(ПараметрыСозданияОрганизации.УникальныйИдентификатор);
		Если ОбщегоНазначения.СсылкаСуществует(СуществующаяОрганизация) Тогда
			РезультатСоздания.Организация = СуществующаяОрганизация;
			РезультатСоздания.СозданаНовая = Ложь;
			Возврат РезультатСоздания;
		КонецЕсли;
	КонецЕсли;
	
	// Создаем новую организаци, но нужно сначала проверить,
	// что такая фукнциональность поддерживается.
	
	Если Не Обработки.ФункциональностьПрограммы.ДоступнаФункциональностьУчетПоНесколькимОрганизациям()
		И Не Обработки.ФункциональностьПрограммы.УсловиеВыполненоВестиУчетПоОднойОрганизации(Истина) Тогда
		РезультатСоздания.Ошибка = Истина;
		РезультатСоздания.СообщениеОбОшибке = НСтр("ru = 'В информационной базе можно создать только одну организацию.
		|Поэтому создание еще одной недоступно, т.к. организация уже создана'");
		Возврат РезультатСоздания;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоОрганизациям") Тогда
		ФункциональностьДляВключения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("ВестиУчетПоОрганизациям");
		Обработки.ФункциональностьПрограммы.ДополнитьРодительскойФункциональностью(ФункциональностьДляВключения);
		РезультатИзменения = Обработки.ФункциональностьПрограммы.ИзменитьСписокФункциональности(ФункциональностьДляВключения, Истина);
		Если РезультатИзменения.Отказ = Истина Тогда
			РезультатСоздания.Ошибка = Истина;
			РезультатСоздания.СообщениеОбОшибке = НСтр("ru = 'В информационной базе отключена функциональность учета нескольких организаций.
			|При включении фукнциональности произошла ошибка.'");
			Возврат РезультатСоздания;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыСозданияОрганизации = РеквизитыСозданияОрганизации(ПараметрыСозданияОрганизации);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ВидОрганизации", ПараметрыСозданияОрганизации.ВидОрганизации);
	ПараметрыЗадания.Вставить("СистемаНалогообложения", ПараметрыСозданияОрганизации.СистемаНалогообложения);
	ПараметрыЗадания.Вставить("Реквизиты", РеквизитыСозданияОрганизации);
	ПараметрыЗадания.Вставить("ПлательщикЕНВД", Ложь);
	ПараметрыЗадания.Вставить("ПлательщикНДСПрименяющийУСН", ПараметрыСозданияОрганизации.ПлательщикНДСПрименяющийУСН);
	ПараметрыЗадания.Вставить("ПрименяетсяУСНПатент", ПараметрыСозданияОрганизации.ПрименяетсяУСНПатент);
	ПараметрыЗадания.Вставить("ПлательщикТорговогоСбора", ПараметрыСозданияОрганизации.ПлательщикТорговогоСбора);
	ПараметрыЗадания.Вставить("ИНН", ПараметрыСозданияОрганизации.ИНН);
	ПараметрыЗадания.Вставить("ИспользуетсяТрудНаемныхРаботников", ПараметрыСозданияОрганизации.ИспользуетсяТрудНаемныхРаботников);
	ПараметрыЗадания.Вставить("ВидБизнеса", ПараметрыСозданияОрганизации.ВидБизнеса);
	ПараметрыЗадания.Вставить("ПрименяетсяЕНВДдо2021", Ложь);
	ПараметрыЗадания.Вставить("ДатаАктуальности", ПараметрыСозданияОрганизации.ДатаАктуальности);
	ПараметрыЗадания.Вставить("НеИзменятьНачалоРаботы", Истина);
	ПараметрыЗадания.Вставить("УникальныйИдентификаторОрганизации", ПараметрыСозданияОрганизации.УникальныйИдентификатор);
	
	СсылкаНаСозданнуюОрганизацию = Справочники.Организации.СоздатьОрганизацию(ПараметрыЗадания);
	
	// В методе Справочники.Организации.СоздатьОрганизацию() для ИП всегда
	// наименование, полное и краткое наименование формируется из ФИО. Но нам явно передали нужное наименование,
	// поэтому их нужно исправить.
	Если ЗначениеЗаполнено(СсылкаНаСозданнуюОрганизацию)
		И ПараметрыЗадания.ВидОрганизации <> "ЮридическоеЛицо" Тогда
		
		ОрганизацияОбъект = СсылкаНаСозданнуюОрганизацию.ПолучитьОбъект();
		ОрганизацияОбъект.ДополнительныеСвойства.Вставить("НеИзменятьНачалоРаботы", Истина);
		
		Если РеквизитыСозданияОрганизации.Свойство("Наименование")
			И ЗначениеЗаполнено(РеквизитыСозданияОрганизации.Наименование) Тогда
			ОрганизацияОбъект.Наименование = РеквизитыСозданияОрганизации.Наименование;
		КонецЕсли;
		
		Если РеквизитыСозданияОрганизации.Свойство("НаименованиеПолное")
			И ЗначениеЗаполнено(РеквизитыСозданияОрганизации.НаименованиеПолное) Тогда
			ОрганизацияОбъект.НаименованиеПолное = РеквизитыСозданияОрганизации.НаименованиеПолное;
		КонецЕсли;
		
		Если РеквизитыСозданияОрганизации.Свойство("НаименованиеСокращенное")
			И ЗначениеЗаполнено(РеквизитыСозданияОрганизации.НаименованиеСокращенное) Тогда
			ОрганизацияОбъект.НаименованиеСокращенное = РеквизитыСозданияОрганизации.НаименованиеСокращенное;
		КонецЕсли;
		
		ОрганизацияОбъект.Записать();
		
	КонецЕсли;
	
	ЭтоНачалоРаботы = Константы.НачалоРаботы.Получить() Или Справочники.Организации.КоличествоОрганизаций() = 1;
	
	Если ЭтоНачалоРаботы Тогда
		Константы.НачалоРаботы.Установить(Ложь);
		
		НастройкиИнтерфейса = Обработки.НачалоРаботы.ПодходящиеНастройкиИнтерфейса();
		ОбщегоНазначенияБП.УстановитьРежимКомандногоИнтерфейса(НастройкиИнтерфейса.ВариантИнтерфейса);
		
		ВключитьНеобходимыеФункциональныеОпции();
		
		// Привилегированный режим устанавливается, так как нужно изменить настройки интерфейса
		// для всех пользователей, а не только текущего пользователя.
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначенияБПВызовСервера.УстановитьСтандартныйИнтерфейсДляВсехпользователей();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СсылкаНаСозданнуюОрганизацию) Тогда
		РезультатСоздания.Ошибка = Истина;
		РезультатСоздания.СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'При создании организацит с ИНН ""%1"" и КПП ""%2"" возникла ошибка. Подробности в журнале регистрации'"),
			ПараметрыСозданияОрганизации.ИНН, ПараметрыСозданияОрганизации.КПП);
	Иначе
		РезультатСоздания.Организация = СсылкаНаСозданнуюОрганизацию;
		РезультатСоздания.СозданаНовая = Истина;
	КонецЕсли;
		
	Возврат РезультатСоздания;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйРезультатПроверкиJSON()
	
	РезультатПроверки = Новый Структура();
	
	РезультатПроверки.Вставить("Ошибка", Ложь);
	РезультатПроверки.Вставить("СообщениеОбОшибке", "");
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция НовыйРезультатСозданияОрганизации()
	
	РезультатСоздания = Новый Структура();
	
	РезультатСоздания.Вставить("Ошибка", Ложь);
	РезультатСоздания.Вставить("СообщениеОбОшибке", "");
	РезультатСоздания.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	РезультатСоздания.Вставить("СозданаНовая", Ложь);
	
	Возврат РезультатСоздания;
	
КонецФункции

Функция НовыйПараметрыСозданияОрганизации()
	
	Параметры = Новый Структура;
	Параметры.Вставить("УникальныйИдентификатор", "");
	Параметры.Вставить("ИНН", "");
	Параметры.Вставить("КПП", "");
	Параметры.Вставить("ВидОрганизации", "");
	Параметры.Вставить("ЮридическоеФизическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ПустаяСсылка());
	Параметры.Вставить("ФамилияИП", "");
	Параметры.Вставить("ИмяИП", "");
	Параметры.Вставить("ОтчествоИП", "");
	Параметры.Вставить("ИспользуетсяТрудНаемныхРаботников", Ложь);
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("НаименованиеСокращенное", "");
	Параметры.Вставить("НаименованиеПолное", "");
	Параметры.Вставить("Префикс", "");
	Параметры.Вставить("ЮридическийАдрес", "");
	Параметры.Вставить("ФактическийАдрес", "");
	Параметры.Вставить("ПочтовыйАдрес", "");
	Параметры.Вставить("Телефон", "");
	Параметры.Вставить("ЭлектроннаяПочта", "");
	Параметры.Вставить("ВидБизнеса", Перечисления.ВидыБизнеса.ПустаяСсылка());
	Параметры.Вставить("ДатаАктуальности", '00010101');
	Параметры.Вставить("СистемаНалогообложения", "");
	Параметры.Вставить("ПрименяетсяУСНПатент", Ложь);
	Параметры.Вставить("ПлательщикТорговогоСбора", Ложь);
	Параметры.Вставить("ПлательщикНДСПрименяющийУСН", Ложь);
	
	Возврат Параметры;
	
КонецФункции

Процедура ПроверитьНаличиеОбязательныхСтруктур(ДанныеОрганизации, РезультатПроверки)
	
	Если НЕ ДанныеОрганизации.Свойство("organization")
		И НЕ ДанныеОрганизации.Свойство("entrepreneur") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре JSON организации отсутствует обязательная структура organization или entrepreneur'");
		Возврат;
	КонецЕсли;
	
	Если ДанныеОрганизации.Свойство("organization")
		И ДанныеОрганизации.Свойство("entrepreneur") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре JSON организации присутствуют взаимоисключающие структуры organization и entrepreneur'");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьКорректностьОбязательныхРеквизитовОрганизации(СвойстваОрганизации, РезультатПроверки)
	
	Если НЕ СвойстваОрганизации.Свойство("inn") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре organization отсутствует обязательное поле inn'");
		Возврат;
	КонецЕсли;
	
	Если СтрДлина(СвойстваОрганизации.inn) <> 10 Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'Длина поля organization.inn должна равняться 10 символам'");
		Возврат;
	КонецЕсли;
	
	Если НЕ СвойстваОрганизации.Свойство("kpp") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре organization для юридического лица отсутствует обязательное поле kpp'");
		Возврат;
	КонецЕсли;
	
	Если СтрДлина(СвойстваОрганизации.kpp) <> 9 Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'Длина поля organization.kpp должна равняться 9 символам'");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьКорректностьДанныхИндивидуальногоПредпринимателя(СвойстваОрганизации, РезультатПроверки)
	
	ПроверитьКорректностьИННДляИП(СвойстваОрганизации, РезультатПроверки);
	
	Если РезультатПроверки.Ошибка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СвойстваОрганизации.Свойство("name")
		ИЛИ ТипЗнч(СвойстваОрганизации.name) <> Тип("Структура") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре entrepreneur отсутствует обязательная структура name'");
		Возврат;
	КонецЕсли;
	
	Если НЕ СвойстваОрганизации.name.Свойство("second_name") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре entrepreneur.name отсутствует обязательная структура second_name'");
		Возврат;
	КонецЕсли;
	
	Если НЕ СвойстваОрганизации.name.Свойство("first_name") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре entrepreneur.name отсутствует обязательная структура first_name'");
		Возврат;
	КонецЕсли;
	
	Если НЕ СвойстваОрганизации.name.Свойство("patronymic") Тогда
		Ошибка = Истина;
		СообщениеОбОшибке = НСтр("ru = 'В структуре entrepreneur.name отсутствует обязательная структура patronymic'");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьКорректностьИННДляИП(СвойстваОрганизации, РезультатПроверки)
	
	Если НЕ СвойстваОрганизации.Свойство("inn") Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре entrepreneur отсутствует обязательное поле inn'");
		Возврат;
	КонецЕсли;
	
	Если СтрДлина(СвойстваОрганизации.inn) <> 12 Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'Длина поля entrepreneur.inn должна равняться 12 символам'");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьКорректностьНаименования(СвойстваОрганизации, РезультатПроверки)
	
	Если НЕ СвойстваОрганизации.Свойство("short_name")
		Или Не ЗначениеЗаполнено(СвойстваОрганизации.short_name) Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре JSON организации отсутствует или не заполнено обязательное поле short_name'");
		Возврат;
	КонецЕсли;
	
	Если НЕ СвойстваОрганизации.Свойство("full_name")
		Или Не ЗначениеЗаполнено(СвойстваОрганизации.full_name) Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре JSON организации отсутствует или не заполнено обязательное поле full_name'");
		Возврат;
	КонецЕсли;
	
	Если НЕ СвойстваОрганизации.Свойство("nickname")
		Или Не ЗначениеЗаполнено(СвойстваОрганизации.nickname) Тогда
		РезультатПроверки.Ошибка = Истина;
		РезультатПроверки.СообщениеОбОшибке = НСтр("ru = 'В структуре JSON организации отсутствует или не заполнено обязательное поле nickname'");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыСозданияОрганизацииПоДаннымОрганизации(ДанныеОрганизации)
	
	ПараметрыСозданияОрганизации = НовыйПараметрыСозданияОрганизации();
	
	Если ДанныеОрганизации.Свойство("organization") Тогда
		ПараметрыСозданияОрганизации.ИНН = СокрЛП(ДанныеОрганизации.organization.inn);
		ПараметрыСозданияОрганизации.КПП = СокрЛП(ДанныеОрганизации.organization.kpp);
		ПараметрыСозданияОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		ПараметрыСозданияОрганизации.ВидОрганизации = "ЮридическоеЛицо";
	Иначе
		ПараметрыСозданияОрганизации.ИНН = СокрЛП(ДанныеОрганизации.entrepreneur.inn);
		ПараметрыСозданияОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		ПараметрыСозданияОрганизации.ВидОрганизации = "ИндивидуальныйПредприниматель";
		ПараметрыСозданияОрганизации.ФамилияИП = ДанныеОрганизации.entrepreneur.name.second_name;
		ПараметрыСозданияОрганизации.ИмяИП = ДанныеОрганизации.entrepreneur.name.first_name;
		ПараметрыСозданияОрганизации.ОтчествоИП = ДанныеОрганизации.entrepreneur.name.patronymic;
		ПараметрыСозданияОрганизации.ИспользуетсяТрудНаемныхРаботников = 
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОрганизации.entrepreneur, "have_staff") = Истина;
	КонецЕсли;
	
	ПараметрыСозданияОрганизации.Наименование = СокрЛП(ДанныеОрганизации.nickname);
	ПараметрыСозданияОрганизации.НаименованиеСокращенное = СокрЛП(ДанныеОрганизации.short_name);
	ПараметрыСозданияОрганизации.НаименованиеПолное = СокрЛП(ДанныеОрганизации.full_name);
	
	Если ДанныеОрганизации.Свойство("number_prefix") Тогда
		ПараметрыСозданияОрганизации.Префикс = СокрЛП(ДанныеОрганизации.number_prefix);
	КонецЕсли;
	
	Если ДанныеОрганизации.Свойство("guid") Тогда
		ПараметрыСозданияОрганизации.УникальныйИдентификатор = Новый УникальныйИдентификатор(СокрЛП(ДанныеОрганизации.guid));
	КонецЕсли;
	
	Если ДанныеОрганизации.Свойство("contact_info")
		И ЗначениеЗаполнено(ДанныеОрганизации.contact_info) Тогда
		
		Если ДанныеОрганизации.contact_info.Свойство("legal_address")
			И ЗначениеЗаполнено(ДанныеОрганизации.contact_info.legal_address) Тогда
			ПараметрыСозданияОрганизации.ЮридическийАдрес = ДанныеОрганизации.contact_info.legal_address;
		КонецЕсли;
		
		Если ДанныеОрганизации.contact_info.Свойство("actual_address")
			И ЗначениеЗаполнено(ДанныеОрганизации.contact_info.actual_address) Тогда
			ПараметрыСозданияОрганизации.ФактическийАдрес = ДанныеОрганизации.contact_info.actual_address;
		КонецЕсли;
		
		Если ДанныеОрганизации.contact_info.Свойство("post_address")
			И ЗначениеЗаполнено(ДанныеОрганизации.contact_info.post_address) Тогда
			ПараметрыСозданияОрганизации.ПочтовыйАдрес = ДанныеОрганизации.contact_info.post_address;
		КонецЕсли;
		
		Если ДанныеОрганизации.contact_info.Свойство("phone")
			И ЗначениеЗаполнено(ДанныеОрганизации.contact_info.phone) Тогда
			ПараметрыСозданияОрганизации.Телефон = ДанныеОрганизации.contact_info.phone;
		КонецЕсли;
		
		Если ДанныеОрганизации.contact_info.Свойство("email")
			И ЗначениеЗаполнено(ДанныеОрганизации.contact_info.email) Тогда
			ПараметрыСозданияОрганизации.ЭлектроннаяПочта = ДанныеОрганизации.contact_info.email;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеОрганизации.Свойство("business_type")
		И ЗначениеЗаполнено(ДанныеОрганизации.business_type) Тогда
		
		ИдентификаторыВидовБизнеса = Перечисления.ВидыБизнеса.ИдентификаторыВидовБизнеса();
		ВидБизнеса = ИдентификаторыВидовБизнеса.Получить(ДанныеОрганизации.business_type);
		Если ЗначениеЗаполнено(ВидБизнеса) Тогда
			ПараметрыСозданияОрганизации.ВидБизнеса = ВидБизнеса;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеОрганизации.Свойство("accounting_policy")
		И ЗначениеЗаполнено(ДанныеОрганизации.accounting_policy) Тогда
		
		Если ДанныеОрганизации.accounting_policy.Свойство("period") Тогда
			ДатаАктуальности = ДанныеОрганизации.accounting_policy.period;
			ПараметрыСозданияОрганизации.ДатаАктуальности = НачалоМесяца(ДатаАктуальности);
		КонецЕсли;
		
		Если ДанныеОрганизации.accounting_policy.Свойство("tax_system") Тогда
			ПараметрыСозданияОрганизации.СистемаНалогообложения = СокрЛП(ДанныеОрганизации.accounting_policy.tax_system);
		КонецЕсли;
		
		Если ДанныеОрганизации.accounting_policy.Свойство("patent")
			И ЗначениеЗаполнено(ДанныеОрганизации.accounting_policy.patent)
			И ПараметрыСозданияОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			
			ПараметрыСозданияОрганизации.ПрименяетсяУСНПатент = ДанныеОрганизации.accounting_policy.patent = Истина;
			Если Не ЗначениеЗаполнено(ПараметрыСозданияОрганизации.СистемаНалогообложения) Тогда
				ПараметрыСозданияОрганизации.СистемаНалогообложения = "Патентная";
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеОрганизации.accounting_policy.Свойство("sales_tax")
			И ЗначениеЗаполнено(ДанныеОрганизации.accounting_policy.sales_tax)
			И ПараметрыСозданияОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			
			ПараметрыСозданияОрганизации.ПлательщикТорговогоСбора = ДанныеОрганизации.accounting_policy.sales_tax = Истина;
			
		КонецЕсли;
		
		Если ДанныеОрганизации.accounting_policy.Свойство("vat")
			И ЗначениеЗаполнено(ДанныеОрганизации.accounting_policy.vat) Тогда
			
			Если ВРЕГ(ПараметрыСозданияОрганизации.СистемаНалогообложения) = ВРЕГ("УпрощеннаяДоходы")
				Или ВРЕГ(ПараметрыСозданияОрганизации.СистемаНалогообложения) = ВРЕГ("УпрощеннаяДоходыМинусРасходы") Тогда
				
				ПараметрыСозданияОрганизации.ПлательщикНДСПрименяющийУСН = ДанныеОрганизации.accounting_policy.vat = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПараметрыСозданияОрганизации;
	
КонецФункции

Функция РеквизитыСозданияОрганизации(ПараметрыСозданияОрганизации)
	
	Реквизиты = Обработки.НачалоРаботы.РеквизитыСозданияОрганизацииПоДаннымСервиса(
		ПараметрыСозданияОрганизации, ПараметрыСозданияОрганизации.ВидОрганизации);
	
	// Обработка НачалоРаботы заполняет часть реквизитов по данным из сервиса.
	// Но нам явно их передали в сервис заполнения, и перезатирать их не нужно.
	ЗаполнитьРеквизитыПоДаннымПараметровСозданияОрганизациии(Реквизиты, ПараметрыСозданияОрганизации);
	
	Возврат Реквизиты;
	
КонецФункции

Процедура ЗаполнитьРеквизитыПоДаннымПараметровСозданияОрганизациии(Реквизиты, ПараметрыСозданияОрганизации)
	
	Реквизиты.Вставить("Префикс", ПараметрыСозданияОрганизации.Префикс);
	Если ПараметрыСозданияОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ФамилияИП) Тогда
			Реквизиты.Вставить("Фамилия", ПараметрыСозданияОрганизации.ФамилияИП);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ИмяИП) Тогда
			Реквизиты.Вставить("Имя", ПараметрыСозданияОрганизации.ИмяИП);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ОтчествоИП) Тогда
			Реквизиты.Вставить("Отчество", ПараметрыСозданияОрганизации.ОтчествоИП);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.Наименование) Тогда
		Реквизиты.Вставить("Наименование", ПараметрыСозданияОрганизации.Наименование);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.НаименованиеСокращенное) Тогда
		Реквизиты.Вставить("НаименованиеСокращенное", ПараметрыСозданияОрганизации.НаименованиеСокращенное);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.НаименованиеПолное) Тогда
		Реквизиты.Вставить("НаименованиеПолное", ПараметрыСозданияОрганизации.НаименованиеПолное);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ЮридическийАдрес) Тогда
		
		ДобавитьКонтактнуюИнформациюВРеквизиты(Реквизиты, "ЮридическийАдрес", ПараметрыСозданияОрганизации.ЮридическийАдрес);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ФактическийАдрес) Тогда
		
		ДобавитьКонтактнуюИнформациюВРеквизиты(Реквизиты, "ФактическийАдрес", ПараметрыСозданияОрганизации.ФактическийАдрес);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ПочтовыйАдрес) Тогда
		
		ДобавитьКонтактнуюИнформациюВРеквизиты(Реквизиты, "ПочтовыйАдрес", ПараметрыСозданияОрганизации.ПочтовыйАдрес);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.Телефон) Тогда
		
		ДобавитьКонтактнуюИнформациюВРеквизиты(Реквизиты, "Телефон", ПараметрыСозданияОрганизации.Телефон);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ЭлектроннаяПочта) Тогда
		
		ДобавитьКонтактнуюИнформациюВРеквизиты(Реквизиты, "ЭлектроннаяПочта", ПараметрыСозданияОрганизации.ЭлектроннаяПочта);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКонтактнуюИнформациюВРеквизиты(Реквизиты, ИмяРеквизитаКонтактнойИнформации, ЗначениеКонтактнойИнформацииJSON)
	
	Если Не ЗначениеЗаполнено(ЗначениеКонтактнойИнформацииJSON) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКонтактнойИнформации = Новый Структура;
	СтруктураКонтактнойИнформации.Вставить("КонтактнаяИнформация", ЗначениеКонтактнойИнформацииJSON);
	СтруктураКонтактнойИнформации.Вставить("Представление", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ЗначениеКонтактнойИнформацииJSON));
	СтруктураКонтактнойИнформации.Вставить("Комментарий", УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ЗначениеКонтактнойИнформацииJSON));
	
	Реквизиты.Вставить(ИмяРеквизитаКонтактнойИнформации, СтруктураКонтактнойИнформации);
	
КонецПроцедуры

Процедура ВключитьНеобходимыеФункциональныеОпции()
	
	ФункциональностьДляВключения = ФункциональныеОпцииДляВключениия();
	Обработки.ФункциональностьПрограммы.ДополнитьРодительскойФункциональностью(ФункциональностьДляВключения);
	РезультатИзменения = Обработки.ФункциональностьПрограммы.ИзменитьСписокФункциональности(ФункциональностьДляВключения, Истина);
	
КонецПроцедуры

Функция ФункциональныеОпцииДляВключениия()
	
	Опции = Новый Массив;
	
	Опции.Добавить("ВестиУчетПоОрганизациям");
	Опции.Добавить("ВестиУчетРасчетовСКонтрагентами");
	Опции.Добавить("ИспользоватьОплатуПоПлатежнымКартам");
	Опции.Добавить("ИспользоватьВыплатыСамозанятым");
	Опции.Добавить("ВедетсяРозничнаяТорговля");
	Опции.Добавить("ИспользоватьПредоплатыВРознице");
	Опции.Добавить("ИспользуютсяПодарочныеСертификаты");
	Опции.Добавить("УправлениеЗачетомАвансовПогашениемЗадолженности");
	Опции.Добавить("ОсуществляетсяРеализацияТоваровУслугКомитентов");
	Опции.Добавить("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров");
	Опции.Добавить("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов");
	Опции.Добавить("ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров");
	Опции.Добавить("ИспользоватьВыпускПродукции");
	Опции.Добавить("ВедетсяПроизводственнаяДеятельность");
	Опции.Добавить("ВедетсяУчетИмпортныхТоваров");
	Опции.Добавить("ВестиУчетПрослеживаемыхТоваров");
	Опции.Добавить("ИспользоватьКомплектациюНоменклатуры");
	Опции.Добавить("ВедетсяУчетСпецодеждыСпецоснастки");
	
	Возврат Опции;
	
КонецФункции

#КонецОбласти