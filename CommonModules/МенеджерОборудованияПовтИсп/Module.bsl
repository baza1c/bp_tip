#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция СодержимоеОбщегоМакета(Знач ИмяМакета) Экспорт

	СодержимоеМакета = ПолучитьОбщийМакет(ИмяМакета).ПолучитьТекст();     
	Возврат СодержимоеМакета; 
	
КонецФункции

Функция СовместимостьВнешнейКомпонентыИзФайла(Знач ИмяМакета) Экспорт
	
	ВремИмяМакета = СтрРазделить(ИмяМакета,".", Ложь);
	ВремИмяМакета = ВремИмяМакета[ВремИмяМакета.ВГраница()];
	ДвоичныеДанные = ПолучитьОбщийМакет(ВремИмяМакета);
		
	Результат = ВнешниеКомпонентыБПО.ИнформацияОКомпонентеИзФайла(ДвоичныеДанные, Ложь);
	Если Результат.Разобрано Тогда
		Возврат Результат.Реквизиты;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПоставляемыеДрайверы() Экспорт
	
	МодулиОбновления = Новый Массив();
	Если ОбщегоНазначенияБПО.ИспользуетсяУстройстваВвода() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеУстройстваВвода"));       
	КонецЕсли;
	Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройства"));       
	КонецЕсли;
	Если ОбщегоНазначенияБПО.ИспользуетсяПлатежныеСистемы() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеПлатежныеСистемы"));       
	КонецЕсли;          
	Если ОбщегоНазначенияБПО.ИспользуетсяДисплеиПокупателя() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеДисплеиПокупателя"));       
	КонецЕсли;
	Если ОбщегоНазначенияБПО.ИспользуетсяВесовоеОборудование() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеВесовоеОборудование"));       
	КонецЕсли;                          
// ++ НеМобильноеПриложение
	Если ОбщегоНазначенияБПО.ИспользуетсяТерминалыСбораДанных() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеТерминалыСбораДанных"));       
	КонецЕсли;
	Если ОбщегоНазначенияБПО.ИспользуетсяПринтерыЭтикеток() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеПринтерыЭтикеток"));       
	КонецЕсли;
	Если ОбщегоНазначенияБПО.ИспользуетсяСчитывательRFID() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеСчитывательRFID"));       
	КонецЕсли;
	Если ОбщегоНазначенияБПО.ИспользуетсяУстройствоРаспознавания() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеУстройствоРаспознавания"));       
	КонецЕсли;
	Если ОбщегоНазначенияБПО.ИспользуютсяОблачныеККТ() Тогда
		МодулиОбновления.Добавить(ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеОблачныеККТ"));       
	КонецЕсли;
// -- НеМобильноеПриложение
	ДрайверыОборудования = МенеджерОборудования.НоваяТаблицаПоставляемыхДрайверовОборудования();
	Для Каждого МодульОбновления Из МодулиОбновления Цикл
		МодульОбновления.ОбновитьПоставляемыеДрайвера(ДрайверыОборудования);
	КонецЦикла;
	
	Возврат ДрайверыОборудования;
	
КонецФункции

Функция ИдентификаторыДрайверов() Экспорт
	
	ДрайверыОборудования = ПоставляемыеДрайверы();
	
	ДрайверыОборудования.Свернуть("ИдентификаторОбъекта, СнятСПоддержки");
	Результат = Новый Массив();
	Для каждого Элемент Из ДрайверыОборудования Цикл
		Если Не Элемент.СнятСПоддержки Тогда
			Результат.Добавить(Элемент.ИдентификаторОбъекта);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыТипыОборудования() Экспорт
	
	Результат = Новый Структура();
	ДоступныеТипы = ВсеТипыОборудования(Истина);
	Для каждого Значение Из ДоступныеТипы Цикл
		
		ИмяЗначения = ОбщегоНазначенияБПО.ИмяЗначенияПеречисления(Значение);
		Результат.Вставить(ИмяЗначения, Ложь);
		
	КонецЦикла;
	
	// Дополнение типов оборудования
	Результат.Вставить("ФискальныйРегистратор", Ложь);
	
	Возврат Результат;

КонецФункции

// Получить соответствие типов оборудования
//
// Возвращаемое значение:
//   Соответствие -
//
Функция СоответствиеТиповОборудования() Экспорт
	
	Результат = Новый Соответствие();
	
	ДоступныеТипы = ВсеТипыОборудования(Истина);
	Для каждого Значение Из ДоступныеТипы Цикл
		
		ИмяЗначения = ОбщегоНазначенияБПО.ИмяЗначенияПеречисления(Значение);
		Результат.Вставить(ИмяЗначения, Значение);
		
	КонецЦикла;
	
	// Russian alphabet. Не локализовывать
	Результат.Вставить("УстройствоВвода"       , Перечисления.ТипыПодключаемогоОборудования.СканерШтрихкода); //@NON-NLS-1 @NON-NLS-2   
	
	// English alphabet. 
	Результат.Вставить("BarcodeScanner"        , Перечисления.ТипыПодключаемогоОборудования.СканерШтрихкода); //@NON-NLS-1 @NON-NLS-2      
	Результат.Вставить("CardReader"            , Перечисления.ТипыПодключаемогоОборудования.СчитывательМагнитныхКарт); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("KKT"                   , Перечисления.ТипыПодключаемогоОборудования.ККТ); //@NON-NLS-1 @NON-NLS-2    
	Результат.Вставить("FiscalRegistrar"       , Перечисления.ТипыПодключаемогоОборудования.ФискальныйРегистратор); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("ReceiptPrinter"        , Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("CustomerDisplay"       , Перечисления.ТипыПодключаемогоОборудования.ДисплейПокупателя); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("DataCollectionTerminal", Перечисления.ТипыПодключаемогоОборудования.ТерминалСбораДанных); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("POSTerminal"           , Перечисления.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("WeighingScales"        , Перечисления.ТипыПодключаемогоОборудования.ЭлектронныеВесы); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("LabelPrintingScale"    , Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("LabelPrinter"          , Перечисления.ТипыПодключаемогоОборудования.ПринтерЭтикеток); //@NON-NLS-1 @NON-NLS-2   
	Результат.Вставить("RFIDReader"            , Перечисления.ТипыПодключаемогоОборудования.СчитывательRFID); //@NON-NLS-1 @NON-NLS-2    
	Результат.Вставить("Recognizer"            , Перечисления.ТипыПодключаемогоОборудования.УстройствоРаспознавания); //@NON-NLS-1 @NON-NLS-2       
	
	Возврат Результат;
	
КонецФункции

// Возвращает список доступных типов оборудования.
//
// Параметры:
//  ВключаяНеиспользуемые - Булево - по умолчанию Ложь, добавить неиспользуемые типы, для совместимости
// 
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.ТипыПодключаемогоОборудования - Массив доступных типов подключаемого оборудования в конфигурации.
//
Функция ДоступныеТипыОборудования(ВключаяНеиспользуемые = Ложь) Экспорт
	
	СписокТиповОборудования = Новый Массив;
	СтандартнаяОбработка = Истина;
	
	МенеджерОборудованияВызовСервераПереопределяемый.ДоступныеТипыОборудования(СписокТиповОборудования, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		СписокТиповОборудования.Очистить();
		Если ОбщегоНазначенияБПО.ИспользуетсяУстройстваВвода() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.СканерШтрихкода);
		// ++ НеМобильноеПриложение
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.СчитывательМагнитныхКарт);
		// -- НеМобильноеПриложение     
		КонецЕсли;
		Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ККТ);
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков);
			Если ВключаяНеиспользуемые Тогда
				СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ФискальныйРегистратор);
			КонецЕсли;
		КонецЕсли;
		Если ОбщегоНазначенияБПО.ИспользуетсяДисплеиПокупателя() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ДисплейПокупателя);
		КонецЕсли;
		// ++ НеМобильноеПриложение
		Если ОбщегоНазначенияБПО.ИспользуетсяТерминалыСбораДанных() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ТерминалСбораДанных);
		КонецЕсли;
		// -- НеМобильноеПриложение     
		Если ОбщегоНазначенияБПО.ИспользуетсяПлатежныеСистемы() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал);
		КонецЕсли;
		Если ОбщегоНазначенияБПО.ИспользуетсяВесовоеОборудование() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ЭлектронныеВесы);  
		// ++ НеМобильноеПриложение
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток);
		// -- НеМобильноеПриложение     
		КонецЕсли;
		// ++ НеМобильноеПриложение
		Если ОбщегоНазначенияБПО.ИспользуетсяПринтерыЭтикеток() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ПринтерЭтикеток);
		КонецЕсли;
		Если ОбщегоНазначенияБПО.ИспользуетсяСчитывательRFID() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.СчитывательRFID);
		КонецЕсли;
		Если ОбщегоНазначенияБПО.ИспользуетсяУстройствоРаспознавания() Тогда
			СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.УстройствоРаспознавания);
		КонецЕсли;
		
		Если ОбщегоНазначенияБПО.ИспользуетсяНесертифицированноеОборудование() Тогда
			МодульОборудованиеНесертифицированное = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеНесертифицированное");
			Если ВключаяНеиспользуемые Или МодульОборудованиеНесертифицированное.ДоступноНесертифицированноеОборудование() Тогда
				ТипыОборудования = МенеджерОборудования.ДоступныеТипыНесертифицированногоОборудования();
				Для Каждого Элемент Из ТипыОборудования Цикл
					СписокТиповОборудования.Добавить(Элемент);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		// -- НеМобильноеПриложение     
	КонецЕсли;
	
	// ++ НеМобильноеПриложение
	УдалитьККМОфлайн = СписокТиповОборудования.Найти(Перечисления.ТипыПодключаемогоОборудования.УдалитьККМОфлайн);
	Если УдалитьККМОфлайн <> Неопределено Тогда
		СписокТиповОборудования.Удалить(УдалитьККМОфлайн);
	КонецЕсли;
	
	// -- НеМобильноеПриложение     
	Возврат СписокТиповОборудования;
	
КонецФункции

// Возвращает список всех типов оборудования.
//
// Параметры:
//  ВключаяНеиспользуемые - Булево - по умолчанию Ложь, добавить неиспользуемые типы, для совместимости
// 
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.ТипыПодключаемогоОборудования - Массив доступных типов подключаемого оборудования в конфигурации.
//
Функция ВсеТипыОборудования(ВключаяНеиспользуемые = Ложь) Экспорт
	
	СписокТиповОборудования = Новый Массив;
	СтандартнаяОбработка = Истина;
	
	СписокТиповОборудования.Очистить();
	Для Каждого ТипОборудования Из Перечисления.ТипыПодключаемогоОборудования Цикл
		
		Если ВключаяНеиспользуемые И 
			(ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ФискальныйРегистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		СписокТиповОборудования.Добавить(ТипОборудования);
		
	КонецЦикла;
	
	// ++ НеМобильноеПриложение
	Если ОбщегоНазначенияБПО.ИспользуетсяНесертифицированноеОборудование() Тогда
		МодульОборудованиеНесертифицированное = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеНесертифицированное");
		Если ВключаяНеиспользуемые Или МодульОборудованиеНесертифицированное.ДоступноНесертифицированноеОборудование() Тогда
			ТипыОборудования = МенеджерОборудования.ДоступныеТипыНесертифицированногоОборудования();
			Для Каждого Элемент Из ТипыОборудования Цикл
				СписокТиповОборудования.Добавить(Элемент);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	// -- НеМобильноеПриложение     
	
	// ++ НеМобильноеПриложение
	УдалитьККМОфлайн = СписокТиповОборудования.Найти(Перечисления.ТипыПодключаемогоОборудования.УдалитьККМОфлайн);
	Если УдалитьККМОфлайн <> Неопределено Тогда
		СписокТиповОборудования.Удалить(УдалитьККМОфлайн);
	КонецЕсли;
	
	// -- НеМобильноеПриложение     
	Возврат СписокТиповОборудования;
	
КонецФункции

#Область КлассификацияОшибок

Функция НовыйКлассОшибки(Класс, КраткоеОписание, ПодробноеОписание, Получатель, ЛокальнаяОбработкаОшибки = Ложь) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Группа", Класс);
	Результат.Вставить("МаскиПоиска", Новый Массив);
	Результат.Вставить("КраткоеОписание", КраткоеОписание);
	Результат.Вставить("ПодробноеОписание", ПодробноеОписание);
	Результат.Вставить("Получатель", Получатель);
	Результат.Вставить("ЛокальнаяОбработкаОшибки", ЛокальнаяОбработкаОшибки);
	Возврат Результат;
	
КонецФункции

Функция КлассификаторОшибок() Экспорт
	
	Результат = Новый Массив;
	ТаблицаГрупп = Новый Соответствие;
	
	ЛокальнаяОбработкаОшибки = Истина;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаМаркировкиБлокировка",
		НСтр("ru = 'Ошибка маркировки'"),
		НСтр("ru = 'Касса заблокировала продажу маркированных товаров, так как в течение длительного времени не удаётся передать уведомления о продаже или получить квитанции от системы маркировки. Обычно блокировка возникает при отсутствии обмена с ИСМ в течение около месяца. Необходимо восстановить передачу уведомлений и выполнить синхронизацию.'"),
		"Пользователь",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаМаркировкиБлокировка", НСтр("ru = 'маркиров.*заблокирована'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Ложь;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаМаркировки",
		НСтр("ru = 'Ошибка маркировки'"),
		НСтр("ru = 'Код маркировки не может быть обработан или проверен ККТ. Проверьте корректность КМ, а также отсутствие ограничений или блокировок по работе с маркировкой. После исправления данных повторите операцию. Если эти действия не помогли, обратитесь в техническую поддержку программы.'"),
		"ТехподдержкаПрограммы",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаМаркировки", НСтр("ru = 'отсутствует.*КМ|маркировк|контрольн.*марка|код.*товара.*не распозн|маркир|работа.*маркированн|буфер чека'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаМаркировки", НСтр("ru = 'КМ.*ранее не проверялся'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Истина;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаСвязи",
		НСтр("ru = 'Ошибка связи'"),
		НСтр("ru = 'Связь с ККТ или ФН отсутствует либо потеряна. Проверьте питание, кабели, порты и сетевое соединение. После восстановления связи повторите операцию. Если проблема сохраняется, обратитесь в техподдержку компании, обслуживающей оборудование.'"),
		"Пользователь",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаСвязи", НСтр("ru = 'соединение не установлено|ошибка.*обмена|устройство.*(не отвечает|занято)'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаСвязи", НСтр("ru = 'сетевого обмена|соединение.*потеряно|не отвечает или недоступна|порт не *.|доступ.*интернет'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаСвязи", НСтр("ru = 'нет\s+связи|timeout|timed\s*out|ком\s*порт|port|устройство\s+не\s+найдено|превышен\s+интервал|failed to connect|connection'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Истина;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаСтатусаККТ",
		НСтр("ru = 'Ошибка статуса'"),
		НСтр("ru = 'Операция невозможна в текущем статусе ККТ. Приведите кассу в рабочее состояние, завершив оформление документов или закрыв смену. После этого повторите действие. Если проблема сохраняется, обратитесь в техническую поддержку компании, обслуживающей оборудование.'"),
		"Пользователь",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаСтатусаККТ", НСтр("ru = '73h|24\s*час|Некорректное состояние ККТ|ККТ не зарегистрирована с указанной системой'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаСтатусаККТ", НСтр("ru = 'смена.*(закрыта|открыта|не открыта|истекла)|отчет.*прерван|документ.*(закрыт|открыт|не закрыт)|функция.*не выполнима|invalid\s*mode'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Истина;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаПользовательскихРеквизитов",
		НСтр("ru = 'Ошибка реквизитов'"),
		НСтр("ru = 'Некоторые обязательные поля документа отсутствуют либо заполнены некорректно. Проверьте реквизиты и исправьте ошибочные данные. После внесения исправлений повторите операцию. Если эти действия не помогли, обратитесь в техническую поддержку программы.'"),
		"Пользователь",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПользовательскихРеквизитов", НСтр("ru = '(кассир|инн|адрес|организация).*не заполнен'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПользовательскихРеквизитов", НСтр("ru = 'ошибка передачи CustomerEmail'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПользовательскихРеквизитов", НСтр("ru = 'не указан.*(ИНН|дата)'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПользовательскихРеквизитов", НСтр("ru = 'не соответствует установкам при регистрации'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПользовательскихРеквизитов", НСтр("ru = 'нет денег для выплаты|не.*(достаточно|хватает) наличности в кассе'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Истина;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаРазмерЧека",
		НСтр("ru = 'Ошибка размера чека'"),
		НСтр("ru = 'Размер кассового чека превышает допустимый предел из‑за избыточного количества позиций. Для устранения проблемы сократите число позиций в чеке: разделите одну крупную покупку на несколько чеков меньшего объёма. Если это не помогло, обратитесь в техническую поддержку программы.'"),
		"Пользователь",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаРазмерЧека", НСтр("ru = 'превышение размеров TLV'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаРазмерЧека", НСтр("ru = 'позиций.*превысило.*лимит'"));
	
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Ложь;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаПрограмныхРеквизитов",
		НСтр("ru = 'Ошибка реквизитов'"),
		НСтр("ru = 'Некоторые обязательные поля документа отсутствуют либо заполнены некорректно. Проверьте реквизиты и исправьте ошибочные данные. После внесения исправлений повторите операцию. Если проблема сохраняется, обратитесь в техническую поддержку программы.'"),
		"ТехподдержкаПрограммы",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПрограмныхРеквизитов", НСтр("ru = '(неверн|корректн).*(сумм|количеств)'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПрограмныхРеквизитов", НСтр("ru = '(неверный|нулевой) итог чека'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПрограмныхРеквизитов", НСтр("ru = 'сумма.*(больше|меньше|не равна)'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаПрограмныхРеквизитов", НСтр("ru = 'невозможно записать реквизит агента|оплата по данному чеку не требуется|необходимо указать оплату'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Ложь;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаФН",
		НСтр("ru = 'Ошибка ФН'"),
		НСтр("ru = 'Фискальный накопитель не смог выполнить операцию из‑за ошибки ресурса или сбоя внутреннего интерфейса. Проверьте состояние ФН. При необходимости произведите замену ФН. Если проблема сохраняется, обратитесь в техническую поддержку компании, обслуживающей оборудование.'"),
		"ТехподдержкаККТ",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаФН", НСтр("ru = 'фн закрыт|нет ответа от фн'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаФН", НСтр("ru = 'исчерпан.*ресурс.*|ресурс.*исчерпан.*|ресурс.*(фн|фд)'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Ложь;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаВремениФН",
		НСтр("ru = 'Ошибка времени ФН'"),
		НСтр("ru = 'Произошло расхождение системного времени ККТ и фискального накопителя из‑за сбоя часов реального времени или некорректных настроек. Для устранения проблемы необходимо установить корректную дату и время в настройках ККТ. Также следует проверить синхронизацию с сервером времени и сверить текущую дату и время на оборудовании. Если проблема сохраняется, обратитесь в техническую поддержку компании, обслуживающей оборудование.'"),
		"ТехподдержкаККТ",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаВремениФН", НСтр("ru = 'сбой часов|неверн.*(дата|время|фн|фд)|(разница|расхожден).*времен|проверьте (дату|время)'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Ложь;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаБиблиотеки",
		НСтр("ru = 'Ошибка БПО'"),
		НСтр("ru = 'Ошибка возникла во внутренних механизмах программы при обработке данных устройства. Встроенная библиотека не смогла корректно обработать параметры или идентификатор устройства. Для устранения проблемы обратитесь в техническую поддержку программы.'"),
		"ТехподдержкаБПО",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаБиблиотеки", НСтр("ru = 'неверн.*идент|не найден.*устройств|идустройств|ошибка.*разбор.*xml|xml.*ошибка'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Истина;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаАппаратная",
		НСтр("ru = 'Ошибка аппаратная'"),
		НСтр("ru = 'Печатающий механизм ККТ не готов к работе. Проверьте наличие бумаги, закрытие крышки и отсутствие механических блокировок. После устранения проблемы повторите операцию.'"),
		"Пользователь",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаАппаратная", НСтр("ru = 'бумаги|принтер|крышк|механическ|отрезчик|питани'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Истина;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаУстаревшийДрайвер",
		НСтр("ru = 'Ошибка утаревший драйвера'"),
		НСтр("ru = 'Обновите основную поставку драйвера, выполните обновление прошивки ККТ. Если проблема повторяется, требуется обращение в техническую поддержку разработчика драйвера.'"),
		"ТехподдержкаККТ",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаУстаревшийДрайвер", НСтр("ru = 'значение.*tax.*'"));
	Результат.Добавить(КлассОшибки);
	
	ЛокальнаяОбработкаОшибки = Ложь;
	КлассОшибки = НовыйКлассОшибки(
		"ОшибкаДрайвера",
		НСтр("ru = 'Ошибка драйвера'"),
		НСтр("ru = 'Во время выполнения операции драйвер ККТ или программный модуль вернул ошибку. Перезапустите драйвер или кассу и убедитесь в корректности установленных обновлений. Выполните обновление прошивки ККТ. Если проблема повторяется, требуется обращение в техническую поддержку разработчика драйвера.'"),
		"ТехподдержкаККТ",
		ЛокальнаяОбработкаОшибки);
	ТаблицаГрупп.Вставить(КлассОшибки.Группа, КлассОшибки);
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'некорректн(ая|ые|ый) (формат|параметр|операция)'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'неверн(ая|ые|ый) (формат|параметр|операция)'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'неверн(ый|ое) (формат|значение) (команды|параметра)'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'ошибка программирования реквизита|ошибка записи заголовка пакета|поле.*отсутствует|некоррект.*поля|нет больше данных'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = '(модель|ккт|команда).*не поддерживается|не поддерживает данную команду|команда не разрешена|ошибка.*драйвер|прошивка ККТ не поддерживает|FFFFFFF8h'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'ошибка лицензии|обновите прошивку'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'ошибка печати товарной позициии|превышение длины реквизита|короткий пакет'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'ошибка передачи тега|ошибка чтения данных'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'фатальная ошибка ККТ|неизвестная ошибка|ошибка ККТ.*неверн|ошибка.*движка ккт'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'ожидание команды продолжения печати|идет печать предыдущей команды'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'нет устройства|сбой таблицы настроек|не поддерживается в данном режиме|не поддерживается в данной версии'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'Invalid password|Inout error|Command is invalid'"));
	ДобавитьМаску(ТаблицаГрупп, "ОшибкаДрайвера", НСтр("ru = 'Недопустимый номер ставки налога'"));
	Результат.Добавить(КлассОшибки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КлассификацияОшибок

Процедура ДобавитьМаску(Классификатор, Группа, Маска)
	Классификатор[Группа].МаскиПоиска.Добавить(НРег(Маска));
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

