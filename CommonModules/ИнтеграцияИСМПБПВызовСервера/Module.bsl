#Область ПрограммныйИнтерфейс

Функция КодТНВЭД(Номенклатура) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "КодТНВЭД").Код;
	
КонецФункции

Функция КонтрагентНадпись(Контрагент, ВидМаркируемойПродукции) Экспорт
	КонтрагентНадпись = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Новый ФорматированнаяСтрока(КонтрагентНадпись);
	КонецЕсли; 
	
	КонтрагентПредставление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Представление");
	Если СтрДлина(КонтрагентПредставление) > 25 Тогда
		МассивСлов = СтрРазделить(КонтрагентПредставление, " ", Ложь);
		КонтрагентПредставление = "";
		Для каждого Слово Из МассивСлов Цикл
			КонтрагентПредставление = КонтрагентПредставление+Слово+" ";
			Если СтрДлина(КонтрагентПредставление) > 20 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		КонтрагентПредставление = СокрП(КонтрагентПредставление);
		Если СтрДлина(КонтрагентПредставление) > 25 Тогда
			КонтрагентПредставление  = СокрП(Лев(КонтрагентПредставление,22))+"...";
		КонецЕсли;
	КонецЕсли;

	КонтрагентНадпись.Добавить(Новый ФорматированнаяСтрока(КонтрагентПредставление, Новый Шрифт(,,Истина)));
	КонтрагентНадпись.Добавить("  ");
	
	УчастникСистемыМаркировки = РегистрыСведений.СтатусыКонтрагентовИСМП.УчастникСистемыМаркировки(
		Контрагент, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидМаркируемойПродукции));
		
	Если УчастникСистемыМаркировки = Неопределено Тогда
		СтатусСтрокой = НСтр("ru = 'выбрать статус контрагента'");
	ИначеЕсли УчастникСистемыМаркировки = ИСТИНА Тогда
		СтатусСтрокой = НСтр("ru = 'зарегистрирован в ИС МП'");
	ИначеЕсли УчастникСистемыМаркировки = ЛОЖЬ Тогда
		СтатусСтрокой = НСтр("ru = 'не зарегистрирован в ИС МП'");
	КонецЕсли; 
	
	КлючЗаписи = РегистрыСведений.СтатусыКонтрагентовИСМП.СоздатьКлючЗаписи(Новый Структура("Контрагент, ВидПродукции", Контрагент, ВидМаркируемойПродукции));
	Если ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СтатусыКонтрагентовИСМП) Тогда
		КонтрагентНадпись.Добавить(Новый ФорматированнаяСтрока(СтатусСтрокой,,,,ПолучитьНавигационнуюСсылку(КлючЗаписи)));
	Иначе
		КонтрагентНадпись.Добавить(Новый ФорматированнаяСтрока(СтатусСтрокой));
	КонецЕсли; 
	
	Возврат Новый ФорматированнаяСтрока(КонтрагентНадпись);
КонецФункции

Функция ШтрихкодыМаркируемойПродукции(СписокОбъектов, АдресХранилища) Экспорт

	ШтрихкодыМаркируемойПродукции = Обработки.ПечатьЭтикеток.НовыйШтрихкодыМаркируемойПродукции();
	
	ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	Для каждого ОписаниеЭтикетки Из СписокОбъектов Цикл
		ОписаниеЭтикетки.Вставить("ИдентификаторСтроки", Новый УникальныйИдентификатор);
		
		НоваяСтрока = ШтрихкодыМаркируемойПродукции.Добавить();
		
		НоваяСтрока.ИдентификаторСтроки = ОписаниеЭтикетки.ИдентификаторСтроки;
		НоваяСтрока.Организация         = ОрганизацияПоУмолчанию;
		НоваяСтрока.Номенклатура        = ОписаниеЭтикетки.Номенклатура;
		НоваяСтрока.Количество          = 1;
		НоваяСтрока.ТипШтрихкода        = ОписаниеЭтикетки.ТипШтрихкода;
		
		
		СтруктураШтрихкода  = Новый Структура;
		СтруктураШтрихкода.Вставить("Штрихкод",        ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(ОписаниеЭтикетки.Штрихкод));
		СтруктураШтрихкода.Вставить("ШтрихкодВBase64", Истина);
		
		НоваяСтрока.Штрихкод        = ДанныеВСтрокуJSON(СтруктураШтрихкода);
	КонецЦикла; 
	
	Возврат ПоместитьВоВременноеХранилище(ШтрихкодыМаркируемойПродукции, АдресХранилища);
КонецФункции

Функция ДанныеВСтрокуJSON(Данные)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

Функция ПараметрыОписанияМаркируемогоТовара(Ссылка, ГосударственныйКонтроль) Экспорт
	
	ПараметрыОписанияМаркируемогоТовара 												= ИнтеграцияИСКлиентСервер.ПараметрыФормированияДанныхПредставленияНастройкиНоменклатуры();
	ПараметрыОписанияМаркируемогоТовара.СсылкаНаОбъект 									= Ссылка;
	ПараметрыОписанияМаркируемогоТовара.ВидПродукции 									= ГосударственныйКонтроль; 
	ПараметрыОписанияМаркируемогоТовара.ДопустимаНастройкаЛогистическойЕдиницы 			= Ложь;
	ПараметрыОписанияМаркируемогоТовара.ПредставлениеВНесколькоСтрок 					= Ложь;
	ПараметрыОписанияМаркируемогоТовара.ОтступПриВыводеНастройкиВидаУпаковки 			= Ложь;
	ПараметрыОписанияМаркируемогоТовара.ВыводитьГиперссылкуНастройкиВидовУпаковокПоGTIN = Ложь;

	Возврат ПараметрыОписанияМаркируемогоТовара;
	
КонецФункции

Функция ПолучитьЧастичноеСодержимоеИКодыОСУ(ТаблицаУпаковок, СсылкаНаОбъект, ИмяТабличнойЧасти, ИмяРеквизитаКоличество = "Количество") Экспорт
	
	ТаблицаТоваров = СсылкаНаОбъект[ИмяТабличнойЧасти].Выгрузить(,"Номенклатура," + ИмяРеквизитаКоличество);
	ТаблицаТоваров.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЗнч(СсылкаНаОбъект))));
	ТаблицаТоваров.Колонки.Добавить("Характеристика", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	Если ИмяРеквизитаКоличество <> "Количество" Тогда
		ТаблицаТоваров.Колонки[ИмяРеквизитаКоличество].Имя = "Количество";
	КонецЕсли;
	ТаблицаТоваров.ЗаполнитьЗначения(СсылкаНаОбъект, "Ссылка");
	
	ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(СсылкаНаОбъект);
	Если Не ПараметрыСканирования.Свойство("ДатаДокумента") Тогда
		ПараметрыСканирования.Вставить("ДатаДокумента", Дата(1,1,1));
	КонецЕсли;

	Возврат ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(ТаблицаУпаковок, ТаблицаТоваров, ПараметрыСканирования);
	
КонецФункции

//Возвращает массив выбранных пользователем маркированных товаров
//Параметры: ДатаНачалаМаркировки Тип("Дата")
//
//Возвращаемое значение: ИспользуемаяМаркировка Тип("Массив")
//
Функция ПолучитьИспользованиеМаркировки(ДатаНачалаМаркировки) Экспорт

	ИспользуемаяМаркировка = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НастройкиУчетаМаркируемойПродукцииИСМП.ВидПродукции.Ссылка КАК ВидПродукции
	               |ИЗ
	               |	РегистрСведений.НастройкиУчетаМаркируемойПродукцииИСМП КАК НастройкиУчетаМаркируемойПродукцииИСМП
	               |ГДЕ
	               |	НастройкиУчетаМаркируемойПродукцииИСМП.ВестиУчетПродукции = ИСТИНА
	               |	И НастройкиУчетаМаркируемойПродукцииИСМП.ДатаКонтрольПроизводства <= &ДатаНачалаМаркировки";
	
	Запрос.УстановитьПараметр("ДатаНачалаМаркировки", ДатаНачалаМаркировки);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИспользуемаяМаркировка.Добавить(Выборка.ВидПродукции);	
	КонецЦикла;
	
	Возврат ИспользуемаяМаркировка;	
	
КонецФункции

//Для возможности использовать на клиенте в переопределениях
Функция ПолучитьЗначенияРеквизитовДокументаОснования(Ссылка, СписокРеквизитов) Экспорт
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, СписокРеквизитов);
		
КонецФункции

Функция УчастникОборотаСистемыМаркировки(Контрагент) Экспорт
	
	Возврат РегистрыСведений.СтатусыКонтрагентовИСМП.УчастникСистемыМаркировки(Контрагент, ПолучитьИспользованиеМаркировки(ТекущаяДата()));
	
КонецФункции

//Проверяет что номенклатура это алкогольгаяпродукция( не пиво)
//Параметры: -Номенклатура Тип("СправочникСсылка.Номенлатура")
//
//Возвращаемое значение: ЭтоАлкоголь Тип("Булево")
//
Функция ЭтоАлкогольнаяПродукция(Номенклатура) Экспорт
		
	Возврат Номенклатура.ВидМаркировки = Перечисления.ВидыПродукцииИС.Алкогольная;
	
КонецФункции

Функция ЭтоПростойИнтерфейс() Экспорт
	
	Возврат ОбщегоНазначенияБП.ЭтоПростойИнтерфейс();
	
КонецФункции

//Если единица мерная(вес,объем) тогда Неопределно если Штука тогда Ложь
//Параметры: ЕдиницаИзмерения Тип("СправочникСсылка.КлассификаторЕдиницИзмерения")
//
//Возвращаемое значение: ШтучнаяЕдиница тип("Булево")
//
Функция ЭтоНеШтучнаяЕдиница(ЕдиницаИзмерения) Экспорт
	
	ШтучнаяЕдиница = Неопределено;
	
	СтрокаКодовШтучныхЕдиниц = "778,780,781,796,797,798,799,800";
	ШтучныеЕдиницы = СтрРазделить(СтрокаКодовШтучныхЕдиниц, ",");
	Если Не ШтучныеЕдиницы.Найти(ЕдиницаИзмерения.Код) = Неопределено Тогда
		ШтучнаяЕдиница = Ложь;
	КонецЕсли;
	
	Возврат ШтучнаяЕдиница;
	
КонецФункции

Функция ЭтоВерсияКорп() Экспорт
	
	Возврат ВариантыПриложений.ЭтоВерсияКОРП();
	
КонецФункции

#КонецОбласти