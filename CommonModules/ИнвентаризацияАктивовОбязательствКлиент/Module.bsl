#Область СлужебныйПрограммныйИнтерфейс

#Область ИнвентаризационныеКомиссии

Процедура ПодборФизическихЛиц(Форма) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);

	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаСписка",
		ПараметрыФормы,
		Форма.Элементы.ИнвентаризационнаяКомиссия,
		,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

// Обработчик выбора в поле ФизЛицо списка ИнвентаризационнаяКомиссия документов, обеспечивающий уникальность физических лиц в списке.
//
// Параметры:
//  ТабличнаяЧасть - ДанныеФормыКоллекция - представление табличной части ИнвентаризационнаяКомиссия
//  ПолеСписка - ПолеФормы - поле формы для отображения табличной части ИнвентаризационнаяКомиссия
//  ВыбранноеЗначение    - СправочникСсылка.ФизическиеЛица - выбранное пользователем физлицо
//  СтандартнаяОбработка - Булево - изменяемый параметр обработчика выбора
//
Процедура ИнвентаризационнаяКомиссияИсключитьДублиФизическихЛиц(ТабличнаяЧасть, ПолеСписка, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	ТекущаяСтрокаТЧ = ПолеСписка.ТекущиеДанные;
	
	Если ТекущаяСтрокаТЧ.ФизЛицо = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиТабличнойЧасти = ТабличнаяЧасть.НайтиСтроки(Новый Структура("ФизЛицо", ВыбранноеЗначение));
	
	Если СтрокиТабличнойЧасти.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = ШаблонТекстаФизЛицоВключеноВКомиссию();
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ВыбранноеЗначение);
	
	Если ЗначениеЗаполнено(ТекущаяСтрокаТЧ.ФизЛицо) Тогда
		ПоказатьПредупреждение(, ТекстСообщения, 60);
	Иначе
		
		ОписаниеСтрок = Новый Структура;
		ОписаниеСтрок.Вставить("Найденные",       СтрокиТабличнойЧасти);
		ОписаниеСтрок.Вставить("Лишняя",          ПолеСписка.ТекущаяСтрока);
		ОписаниеСтрок.Вставить("ТабличнаяЧасть",  ТабличнаяЧасть);
		ОписаниеСтрок.Вставить("ПолеСписка",      ПолеСписка);
		
		ОповещениеУдалитьДублирующуюСтроку = Новый ОписаниеОповещения(
			"Подключаемый_ИнвентаризационнаяКомиссияУдалитьДублирующуюСтроку",
			ИнвентаризацияАктивовОбязательствКлиент,
			ОписаниеСтрок);
		ПоказатьПредупреждение(ОповещениеУдалитьДублирующуюСтроку, ТекстСообщения, 60);
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ИнвентаризационнаяКомиссияПередУдалением(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
	Если ТекущаяСтрокаТЧ.Председатель Тогда
		ИндексУдаляемойСтроки = Объект.ИнвентаризационнаяКомиссия.Индекс(ТекущаяСтрокаТЧ);
		КоличествоСтрок = Объект.ИнвентаризационнаяКомиссия.Количество() - 1;

		Если КоличествоСтрок > 0 Тогда
			Если ИндексУдаляемойСтроки <= КоличествоСтрок - 1 Тогда
				ИндексНовогоПредседателя = ИндексУдаляемойСтроки + 1;
			Иначе
				ИндексНовогоПредседателя = КоличествоСтрок - 1;
			КонецЕсли;
			Объект.ИнвентаризационнаяКомиссия[ИндексНовогоПредседателя].Председатель = Истина;
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры

Процедура ИнвентаризационнаяКомиссияПриНачалеРедактирования(Форма, Копирование) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если Копирование Тогда
		ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
		ТекущаяСтрокаТЧ.ФизЛицо = Неопределено;
		ТекущаяСтрокаТЧ.Председатель = Ложь;
	Иначе // Создание заново
		Если Объект.ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
			Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнвентаризационнаяКомиссияОбработкаВыбора(Форма, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;

	Строки = Объект.ИнвентаризационнаяКомиссия.НайтиСтроки(Новый Структура("ФизЛицо", ВыбранноеЗначение));

	Если Строки.Количество() > 0 Тогда
		ШаблонСообщения = ШаблонТекстаФизЛицоВключеноВКомиссию();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ВыбранноеЗначение);
		ПоказатьПредупреждение( , ТекстСообщения, 60);
	Иначе
		НоваяСтрока = Объект.ИнвентаризационнаяКомиссия.Добавить();
		НоваяСтрока.ФизЛицо = ВыбранноеЗначение;
		Если Объект.ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
			НоваяСтрока.Председатель = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнвентаризационнаяКомиссияПредседательПриИзменении(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;

	Если Не ТекущаяСтрокаТЧ.Председатель Тогда
		// Снимать флажок нельзя
		ТекущаяСтрокаТЧ.Председатель = Истина;
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаТЧ Из Объект.ИнвентаризационнаяКомиссия Цикл
		Если СтрокаТЧ.НомерСтроки <> ТекущаяСтрокаТЧ.НомерСтроки Тогда
			СтрокаТЧ.Председатель = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ИнвентаризационнаяКомиссияФизЛицоПриИзменении(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Если Объект.ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
		Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнвентаризационныеКомиссии

Процедура Подключаемый_ИнвентаризационнаяКомиссияУдалитьДублирующуюСтроку(ОписаниеСтрок) Экспорт
	
	ИндексСтроки = ОписаниеСтрок.ТабличнаяЧасть.НайтиПоИдентификатору(ОписаниеСтрок.Лишняя);
	Если ИндексСтроки <> Неопределено Тогда
		ОписаниеСтрок.ТабличнаяЧасть.Удалить(ИндексСтроки);
	КонецЕсли;
	
КонецПроцедуры

Функция ШаблонТекстаФизЛицоВключеноВКомиссию()
	Возврат НСтр("ru = 'Физическое лицо %1 уже включено в состав комиссии'");
КонецФункции

#КонецОбласти

#КонецОбласти
