#Область ПрограммныйИнтерфейс

// Обработчик автоподбора значения для поля Метки
//
// Параметры:
//   Элемент - ПолеФормы - элемент формы, для которого выполняется обработчик
//   Текст - Строка -  текст, введенный в поле Метки
//   ДанныеВыбора - СписокЗначений - содержит подобранные значения
//   ПараметрыПолученияДанных - Структура, Неопределено - параметры поиска
//   Ожидание - Число - интервал после ввода текста
//   СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события
//
Процедура МеткиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если Не ПустаяСтрока(Текст)
		И (Не ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		Или Элемент.ОграничениеТипа.СодержитТип(Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"))) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСМеткамиВызовСервера.ДанныеВыбораМеток(ПараметрыПолученияДанных, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик окончания ввода текста для поля Метки
//
// Параметры:
//   Элемент - ПолеФормы - элемент формы, для которого выполняется обработчик
//   Текст - Строка -  текст, введенный в поле Метки
//   ДанныеВыбора - СписокЗначений - содержит подобранные значения
//   ПараметрыПолученияДанных - Структура, Неопределено - параметры поиска
//   СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события
//
Процедура МеткиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка) Экспорт
	
	Если Не ПустаяСтрока(Текст)
		И (Не ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		Или Элемент.ОграничениеТипа.СодержитТип(Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"))) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСМеткамиВызовСервера.ДанныеВыбораМеток(ПараметрыПолученияДанных, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события выбора для поля Метки
//
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма с метками
// Элемент - Поле формы, для которого выполняется обработчик
// ВыбранноеЗначение - Значение, выбранное в поле "Метки" формы
// СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события
//
Процедура МеткиОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка")
		И (Не ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		Или Элемент.ОграничениеТипа.СодержитТип(Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"))) Тогда
		
		СоздатьМетку(Форма, Элемент);
		
	КонецЕсли;

КонецПроцедуры

// Обработчик события начала выбора для поля Метки и "Отбор меток"
//
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма с метками
// Элемент - Поле формы, для которого выполняется обработчик
// ДанныеВыбора - СписокЗначений - содержит подобранные значения
// ВыборДобавлением - Булево - определяет, как поле формы обработает начало выбора значения
// СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события
//
Процедура МеткиНачалоВыбора(Форма, Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка) Экспорт
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("ИмяВладельца", ПараметрыФормы.ИмяВладельца);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ФормаВыбораМеток",
		ПараметрыВыбора, Элемент);
	
КонецПроцедуры

// Обработчик создания значения для поля Метки
//
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма с метками
// Элемент - Поле формы, для которого выполняется обработчик
// СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события
//
Процедура МеткиСоздание(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	СоздатьМетку(Форма, Элемент);
	
КонецПроцедуры

// Обработчик открытия значения для поля Метки и "Отбор меток"
//
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма с метками
// Элемент - Поле формы, для которого выполняется обработчик
// Идентификатор - Число - идентификатор значения
// СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события
//
Процедура МеткиОткрытиеМножественногоЗначения(Форма, Элемент, Идентификатор, СтандартнаяОбработка) Экспорт
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	ТекущиеДанные = Форма[ПараметрыФормы.ИмяЭлементаМетки].НайтиПоИдентификатору(Идентификатор);
	Если ТекущиеДанные <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.Свойство);
		ПараметрыФормы.Вставить("ТекущийНаборСвойств", ТекущиеДанные.Набор);
	
		ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ФормаМетки",
			ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик изменения для поля "Использовать отбор меток"
//
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма с отбором списка по меткам
// Элемент - Поле формы, для которого выполняется обработчик
//
Процедура ИспользоватьОтборМеткиПриИзменении(Форма, Элемент) Экспорт
	
	РаботаСМеткамиКлиентСервер.УстановитьОтборПоМеткам(Форма);
	
КонецПроцедуры

// Обработчик изменения для поля "Отбор меток"
//
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма с отбором списка по меткам
// Элемент - Поле формы, для которого выполняется обработчик
//
Процедура ОтборМеткиПриИзменении(Форма, Элемент) Экспорт
	
	Форма.ИспользоватьОтборМетки = Форма.ОтборМетки.Количество() > 0;
	
	РаботаСМеткамиКлиентСервер.УстановитьОтборПоМеткам(Форма);
	
КонецПроцедуры

// Определяет необходимость обработки события, связанного с изменением меток
//
// Параметры:
//  Форма      - ФормаКлиентскогоПриложения - форма с метками
//  ИмяСобытия - Строка       - имя обрабатываемого события
//  Параметр   - Произвольный - параметры, переданные в событии
//
// Возвращаемое значение:
//  Булево - если Истина, тогда это оповещение об изменении набора свойств и
//           его нужно обработать в форме.
//
Функция ОбрабатыватьОповещения(Форма, ИмяСобытия, Параметр) Экспорт
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	Если Не ПараметрыФормы.ИспользоватьМетки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДополнительныеРеквизитыИСведения" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьМетку(Форма, Элемент)
	
	ПараметрыФормы = Форма.ПараметрыФормыСМетками;
	
	НаборСвойств = РаботаСМеткамиВызовСервера.НаборСвойствВладельца(ПараметрыФормы.ИмяВладельца);
	
	ПараметрыФормыМетки = Новый Структура;
	ПараметрыФормыМетки.Вставить("РежимВыбора", Истина);
	ПараметрыФормыМетки.Вставить("ТекстЗаполнения", Элемент.ТекстРедактирования);
	ПараметрыФормыМетки.Вставить("ТекущийНаборСвойств", НаборСвойств);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ФормаМетки",
		ПараметрыФормыМетки, Элемент);
	
КонецПроцедуры

#КонецОбласти
