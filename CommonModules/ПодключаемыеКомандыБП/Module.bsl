
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПечатьДокументовПриСозданииНаСервере(Форма, ДокументыДляПечати) Экспорт
	
	Если ДокументыДляПечати.Количество() = 1
		И ТипЗнч(ДокументыДляПечати[0]) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		Элементы = Форма.Элементы;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КнопкаОтправить", "Видимость", Ложь);
		Родитель = Элементы.Найти("ГруппаСохранитьОтправить");
		Если Родитель = Неопределено Тогда
			Родитель = Элементы.Найти("ГруппаКоманднаяПанель");
		КонецЕсли;
		
		ДобавитьКомандыОтправкиДляФормыПечати(Форма, Родитель);
		
	КонецЕсли;
	
	МобильныйКлиентАдаптацияФормы(Форма);
	
КонецПроцедуры

Процедура ПриОпределенииКомандПодключенныхКОбъекту(НастройкиФормы, Источники, ПодключенныеОтчетыИОбработки, Команды) Экспорт
	
	// Удаляем команды ДиректБанка из журнала "Документы" и из ЖурналОпераций
	// Необходимо дополнительно отключить видимость "ПодменюДиректБанк" в форме журнала
	Если НастройкиФормы.ИмяФормы = "ЖурналДокументов.Документы.Форма.ФормаСписка"
		Или НастройкиФормы.ИмяФормы = "ЖурналДокументов.ЖурналОпераций.Форма.ФормаСпискаМаркетплейсы" Тогда
		ПараметрыОтбора = Новый Структура("Вид", "ДиректБанк");
		УдаляемыеСтроки = Команды.НайтиСтроки(ПараметрыОтбора);
		Для каждого ЭлементКоллекции Из УдаляемыеСтроки Цикл
			Команды.Удалить(ЭлементКоллекции);
		КонецЦикла;
	КонецЕсли;
	
	Если НастройкиФормы.ИмяФормы = "Документ.ОтчетОРозничныхПродажах.Форма.ФормаДокументаОбщая" Тогда
		ПараметрыОтбора = Новый Структура("Вид", "ОФД");
		КомандыЧекиОФД = Команды.НайтиСтроки(ПараметрыОтбора);
		Для каждого КомандаЧекиОФД Из КомандыЧекиОФД Цикл
			КомандаЧекиОФД.ЕстьУсловияВидимости = Истина;
			КомандаЧекиОФД.ТолькоВоВсехДействиях = Истина;
			ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаЧекиОФД, "ЗагрузкаИзОФД", Истина);
		КонецЦикла;
	КонецЕсли;
	
	Если НастройкиФормы.ИмяФормы = "Документ.СчетНаОплатуПокупателю.Форма.ФормаСписка"
			Или НастройкиФормы.ИмяФормы = "Документ.СчетНаОплатуПокупателю.Форма.ФормаДокумента" Тогда
		Команда = Команды.Добавить();
		Команда.Вид = "Отправить";
		Команда.Идентификатор = "ОтправитьДокументПоЭлектроннойПочте";
		Команда.Представление = НСтр("ru = 'По электронной почте'");
		Команда.Важность = "Важное";
		Команда.ОтображениеКнопки = ОтображениеКнопки.Текст;
		Команда.ТипПараметра = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПокупателю");
		Команда.ИзменяетВыбранныеОбъекты = Ложь;
		Команда.РежимЗаписи = "Записывать";
		Команда.Обработчик = "ПодключаемыеКомандыБПКлиент.ОтправитьДокументПоЭлектроннойПочте";
		Команда.МножественныйВыбор = Истина;
		Команда.Порядок = 10;
		
		Если ИнтеграцияShare.ЕстьПравоИспользованияСервиса() Тогда
			Команда = Команды.Добавить();
			Команда.Вид = "Отправить";
			Команда.Идентификатор = "ОтправитьСсылкуНаДокумент";
			Команда.Представление = НСтр("ru = 'Ссылку на документ'");
			Команда.Важность = "Важное";
			Команда.ОтображениеКнопки = ОтображениеКнопки.Текст;
			Команда.ТипПараметра = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПокупателю");
			Команда.ИзменяетВыбранныеОбъекты = Ложь;
			Команда.РежимЗаписи = "Проводить";
			Команда.Обработчик = "ПодключаемыеКомандыБПКлиент.ОтправитьСсылкуНаДокумент";
			Команда.МножественныйВыбор = Ложь;
			Команда.Порядок = 20;
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") И ОбменСКонтрагентами.ЕстьПравоВыполненияОбмена() Тогда
			Команда = Команды.Добавить();
			Команда.Вид = "Отправить";
			Команда.Идентификатор = "ОтправитьЭлектронныйДокументПоЭДО";
			Команда.Представление = НСтр("ru = 'Электронный документ по ЭДО'");
			Команда.Важность = "Обычное";
			Команда.ОтображениеКнопки = ОтображениеКнопки.Текст;
			Команда.ТипПараметра = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПокупателю");
			Команда.ИзменяетВыбранныеОбъекты = Ложь;
			Команда.РежимЗаписи = "Проводить";
			Команда.Обработчик = "ПодключаемыеКомандыБПКлиент.ОтправитьЭлектронныйДокументПоЭДО";
			Команда.МножественныйВыбор = Ложь;
			Команда.Порядок = 30;
			
			Команда = Команды.Добавить();
			Команда.Вид = "Отправить";
			Команда.Идентификатор = "ОтправитьПроизвольныйДокументПоЭДО";
			Команда.Представление = НСтр("ru = 'Произвольный документ по ЭДО'");
			Команда.Важность = "Обычное";
			Команда.ОтображениеКнопки = ОтображениеКнопки.Текст;
			Команда.ТипПараметра = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПокупателю");
			Команда.ИзменяетВыбранныеОбъекты = Ложь;
			Команда.РежимЗаписи = "Проводить";
			Команда.Обработчик = "ПодключаемыеКомандыБПКлиент.ОтправитьПроизвольныйДокументПоЭДО";
			Команда.МножественныйВыбор = Ложь;
			Команда.Порядок = 40;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НастройкиФормы.ИмяФормы = "Документ.АктСверкиВзаиморасчетов.Форма.ФормаСписка"
			Или НастройкиФормы.ИмяФормы = "Документ.АктСверкиВзаиморасчетов.Форма.ФормаДокумента" Тогда
			
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") И ОбменСКонтрагентами.ЕстьПравоВыполненияОбмена() Тогда
			Команда = Команды.Добавить();
			Команда.Вид = "Отправить";
			Команда.Идентификатор = "ОтправитьЭлектронныйДокументПоЭДО";
			Команда.Представление = НСтр("ru = 'По ЭДО'");
			Команда.Важность = "Обычное";
			Команда.ОтображениеКнопки = ОтображениеКнопки.Текст;
			Команда.ТипПараметра = Новый ОписаниеТипов("ДокументСсылка.АктСверкиВзаиморасчетов");
			Команда.ИзменяетВыбранныеОбъекты = Ложь;
			Команда.РежимЗаписи = "Проводить";
			Команда.Обработчик = "ПодключаемыеКомандыБПКлиент.ОтправитьЭлектронныйДокументПоЭДО";
			Команда.МножественныйВыбор = Ложь;
			Команда.Порядок = 10;
		КонецЕсли;
			
		Если ИнтеграцияShare.ЕстьПравоИспользованияСервиса() Тогда
			Команда = Команды.Добавить();
			Команда.Вид = "Отправить";
			Команда.Идентификатор = "ОтправитьСсылкуНаДокумент";
			Команда.Представление = НСтр("ru = 'Ссылку на документ'");
			Команда.Важность = "Важное";
			Команда.ОтображениеКнопки = ОтображениеКнопки.Текст;
			Команда.ТипПараметра = Новый ОписаниеТипов("ДокументСсылка.АктСверкиВзаиморасчетов");
			Команда.ИзменяетВыбранныеОбъекты = Ложь;
			Команда.РежимЗаписи = "Проводить";
			Команда.Обработчик = "ПодключаемыеКомандыБПКлиент.ОтправитьСсылкуНаДокумент";
			Команда.МножественныйВыбор = Ложь;
			Команда.Порядок = 20;
		КонецЕсли;
		
		Команда = Команды.Добавить();
		Команда.Вид = "Отправить";
		Команда.Идентификатор = "ОтправитьДокументПоЭлектроннойПочте";
		Команда.Представление = НСтр("ru = 'По электронной почте'");
		Команда.Важность = "Важное";
		Команда.ОтображениеКнопки = ОтображениеКнопки.Текст;
		Команда.ТипПараметра = Новый ОписаниеТипов("ДокументСсылка.АктСверкиВзаиморасчетов");
		Команда.ИзменяетВыбранныеОбъекты = Ложь;
		Команда.РежимЗаписи = "Записывать";
		Команда.Обработчик = "ПодключаемыеКомандыБПКлиент.ОтправитьДокументПоЭлектроннойПочте";
		Команда.МножественныйВыбор = Истина;
		Команда.Порядок = 30;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКомандыОтправкиДляФормыПечати(Форма, Родитель)
	
	Элементы = Форма.Элементы;
	
	ПодменюОтправить = Элементы.ПодменюОтправить;
	
	ГруппаКнопокОтправить = Элементы.Добавить("ГруппаКнопокОтправить", Тип("ГруппаФормы"), ПодменюОтправить);
	ГруппаКнопокОтправить.Вид = ВидГруппыФормы.ГруппаКнопок;
	
	ДобавитьКнопкуФормы(Форма, "ОтправитьДокументПоЭлектроннойПочте", НСтр("ru = 'По электронной почте'"), ГруппаКнопокОтправить);
	Если ИнтеграцияShare.ЕстьПравоИспользованияСервиса() Тогда
		ДобавитьКнопкуФормы(Форма, "ОтправитьСсылкуНаДокумент", НСтр("ru = 'Ссылку на документ'"), ГруппаКнопокОтправить);
	КонецЕсли;
	
	ГруппаКнопокЭДО = Элементы.Добавить("ГруппаКнопокЭДО", Тип("ГруппаФормы"), ПодменюОтправить);
	ГруппаКнопокЭДО.Вид = ВидГруппыФормы.ГруппаКнопок;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") И ОбменСКонтрагентами.ЕстьПравоВыполненияОбмена() Тогда
		ДобавитьКнопкуФормы(Форма, "ОтправитьЭлектронныйДокументПоЭДО", НСтр("ru = 'Электронный документ по ЭДО'"), ГруппаКнопокЭДО);
	КонецЕсли;
	
	// Переместим команду отправки печатной формы Счета на оплату через ЭДО в подменю Отправить.
	КомандаОтправкиПечатнойформы = Элементы.Найти("СформироватьПроизвольныйДокументКнопкаФормы");
	Если КомандаОтправкиПечатнойформы <> Неопределено Тогда
		Элементы.Переместить(КомандаОтправкиПечатнойформы, ГруппаКнопокЭДО);
		КомандаОтправкиПечатнойформы.Заголовок = НСтр("ru = 'Печатную форму по ЭДО'");
		КомандаОтправкиПечатнойформы.Картинка = Новый Картинка; // очистим картинку кнопки
		КомандаОтправкиПечатнойформы.Отображение = ОтображениеКнопки.Текст;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКнопкуФормы(Форма, Имя, Заголовок, Родитель)
	
	Элементы = Форма.Элементы;
	
	КомандаФормы = Форма.Команды.Добавить(Имя);
	КомандаФормы.Заголовок = Заголовок;
	КомандаФормы.Действие = "Подключаемый_ВыполнитьКоманду";
	
	КнопкаФормы = Элементы.Добавить(КомандаФормы.Имя, Тип("КнопкаФормы"), Родитель);
	КнопкаФормы.ИмяКоманды = КомандаФормы.Имя;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

Процедура МобильныйКлиентАдаптацияФормы(Форма)
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ДоступныУчетныеЗаписиДляОтправкиМК", Новый ОписаниеТипов("Булево")));
		
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
		Форма.ДоступныУчетныеЗаписиДляОтправкиМК = Взаимодействия.ИспользуетсяПочтовыйКлиент()
			Или РаботаСПочтовымиСообщениямиВызовСервера.ЕстьДоступныеУчетныеЗаписиДляОтправки();
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(Форма);
	
	АдаптироватьМККоманднаяПанельФормы(Форма);
	АдаптироватьМКШапкаФормы(Форма);
	
КонецПроцедуры

Процедура АдаптироватьМККоманднаяПанельФормы(Форма)
	
	Элементы = Форма.Элементы;
	КоманднаяПанельЕще = Элементы.Найти("КоманднаяПанельЕще");
	Родитель = Элементы.Найти("ГруппаСохранитьОтправитьВсеДействия");
	Если Родитель = Неопределено Тогда
		Родитель = КоманднаяПанельЕще;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"КнопкаОтправитьВсеДействия",
		"Заголовок",
		НСтр("ru = 'Отправить по почте'"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"КнопкаОтправитьВсеДействия",
		"КнопкаПоУмолчанию",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"КнопкаСохранитьВсеДействия",
		"Заголовок",
		НСтр("ru = 'Отправить в ...'"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"КнопкаСохранитьВсеДействия",
		"Картинка",
		БиблиотекаКартинок.ПередачаДанныхНаУстройствеМК);
		
	КомандыМК = "КнопкаОтправитьВсеДействия,КнопкаСохранитьВсеДействия";
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанельФормаОтчета(Форма, КомандыМК);
	
	Если КоманднаяПанельЕще <> Неопределено Тогда
		КнопкаПечатьКоманднаяПанель = КоманднаяПанельЕще.ПодчиненныеЭлементы.Найти("КнопкаПечатьВсеДействия");
		Если КнопкаПечатьКоманднаяПанель <>  Неопределено Тогда
			КнопкаПечатьКоманднаяПанель.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура АдаптироватьМКШапкаФормы(Форма)
	
	Элементы = Форма.Элементы;
	
	НастройкиПечатныхФорм = Элементы.Найти("НастройкиПечатныхФорм");
	Если НастройкиПечатныхФорм <> Неопределено Тогда
		НастройкиПечатныхФорм.ПоведениеПриСжатииПоГоризонтали = ПоведениеТаблицыПриСжатииПоГоризонтали.ПереноситьЭлементыПоВажности;
		НастройкиПечатныхФорм.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
		НастройкиПечатныхФорм.РежимВыбора = Истина;
	КонецЕсли;

	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.НастройкаКоличестваКопий);
	ГруппаПодписьИПечать = УправлениеПечатьюБП.ПолучитьГруппуПодписьИПечать(Форма);
	Если ГруппаПодписьИПечать <> Неопределено Тогда
		МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, ГруппаПодписьИПечать);
		ГруппаПодписьИПечать.Ширина                   = 0;
		ГруппаПодписьИПечать.РастягиватьПоГоризонтали = Истина;
	КонецЕсли;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПечатныеФормы);
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(Форма);
	
КонецПроцедуры

#КонецОбласти
