#Область ПрограммныйИнтерфейс

// Возвращает признак доступности интеграции с платформой Самозанятые
//
// Возвращаемое значение:
//   Булево
//
Функция ДоступнаИнтеграцияСПлатформойСамозанятые() Экспорт
	
	Возврат ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые();
	
КонецФункции

// Возвращает признак подключенной интеграции с платформой Самозанятые.
//
// Возвращаемое значение:
//   Булево
//
Функция ВключенаИнтеграцияСПлатформойСамозанятые() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ВключенаИнтеграцияСПлатформойСамозанятые");
	
КонецФункции

// Запускает длительную операцию по проверке аннулирования чека в сервисе Мой налог.
//
// Параметры:
//	СсылкаНаДокумент - ДокументСсылка - ссылка на документ, в котором запускается проверка.
//	ПараметрыОжиданияРезультата - Структура - параметры результата ожидания.
//	Идентификатор - УникальныйИдентификатор - уникальный идентификатор документа.
//
// Возвращаемое значение:
//   ДлительнаяОперация - Структура.
//
Функция ЗапуститьПроверкуРезультатаАннулированияЧекаВФоне(СсылкаНаДокумент, ПараметрыОжиданияРезультата, Идентификатор) Экспорт
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Ссылка", СсылкаНаДокумент);
	ПараметрыПроцедуры.Вставить("ПараметрыОжиданияРезультата", ПараметрыОжиданияРезультата);
	
	ДлительнаяОперация = 
		РегистрыСведений.ЧекиНПД.ЗапуститьПроверкуРезультатаАннулированияЧекаВФоне(
			ПараметрыПроцедуры, Идентификатор);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию по аннулированию чека в сервисе Мой налог.
//
// Параметры:
//	ПараметрыАннулированияЧека - Структура - параметры аннулирования чека.
//	Идентификатор - УникальныйИдентификатор - уникальный идентификатор документа.
//
// Возвращаемое значение:
//   ДлительнаяОперация - Структура.
//
Функция ЗапуститьАннулированиеЧекаВФоне(ПараметрыАннулированияЧека, Идентификатор) Экспорт

	ДлительнаяОперация = 
		РегистрыСведений.ЧекиНПД.ЗапуститьАннулированиеЧекаВФоне(ПараметрыАннулированияЧека,
			Идентификатор);
			
	Возврат ДлительнаяОперация;
	
КонецФункции

// Возвращает подходящие данные выбора по номеру чека.
//
// Параметры:
//   СтрокаПоиска - Строка - часть номера чека, введенного пользователем.
//   Организация - СправочникСсылка.Организации - ссылка на организацию.
//   ИмяДокументаОснования - Строка - ограничение поиска чека по имени документа-основания.
//
// Возвращаемое значение:
//   СписокЗначений из Строка - список из номеров чеков и их представлений.
//
Функция ДанныеВыбораЧековДляАннулирования(СтрокаПоиска, Организация, ИмяДокументаОснования) Экспорт
	
	Возврат РегистрыСведений.ЧекиНПД.ДанныеВыбораЧековДляАннулирования(СтрокаПоиска, Организация, ИмяДокументаОснования);
	
КонецФункции

// Проверяет, подключена ли организация к сервису Мой налог.
//
// Параметры:
//   Организация - СправочникСсылка.Организации
//
// Возвращаемое значение:
//  Булево - Истина, если организация подключена к сервису Мой налог; Ложь - в противном случае.
//
Функция ПодключенСервисМойНалог(Организация) Экспорт

	Возврат ИнтеграцияСПлатформойСамозанятыеВызовСервера.ПриложениеПодключено(Организация);

КонецФункции

// Определяет право изменения записей в регистре сведений "Чеки НПД".
//
// Возвращаемое значение:
//  Булево
Функция ЕстьПравоОбновленияЧековНПД() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ЧекиНПД);
	
КонецФункции

// Возвращает цвет текст ошибки операции по чеку
//
// Возвращаемое значение:
//  Цвет
//
Функция ЦветТекстаОшибки() Экспорт
	
	Возврат ЦветаСтиля.ЦветТекстаОшибкиЧекиНПД;
	
КонецФункции

// Запускает фоновую проверку непредоставленных чеков НПД
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - контрагент, по которому осуществляется проверка
//  Идентификатор - УникальныйИдентификатор - ключ формы, которая инициировала выполнение фонового задания
//
// Возвращаемое значение:
//  Структура - Результат выполнения длительной операции, см. ДлительныеОперации.ВыполнитьФункцию
//
Функция ПроверитьНепредоставленныеЧекиНПДВФонеНаСервере(Контрагент, Идентификатор) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Или Не ПолучитьФункциональнуюОпцию("ИспользуютсяДоговорыССамозанятыми") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияФункции(Идентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка чеков НПД'");
	
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	
	Результат = ДлительныеОперации.ВыполнитьФункцию(
		НастройкиЗапуска,
		"РегистрыСведений.СтатусыДокументов.ИнформацияПоНепредоставленнымЧекамНПД",
		Контрагент, Истина);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НачатьОбновлениеСтатусовОфлайнЧековНПД(Знач Организация) Экспорт
	
	Если Не ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые()
		Или Не УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(Организация, ДатаСведенийНПД()) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление статусов офлайн чеков НПД.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ЧекиНПД.НачатьОбновлениеСтатусовОфлайнЧековНПД",
		Организация);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

Функция ЗапуститьОбновлениеСведенийОЧекеНПД(СведенияОЧеке, Идентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Идентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление сведений о чеках самозанятых в фоне.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ЧекиНПД.ОбновитьСведенияОЧекеНПДВФоне",
		СведенияОЧеке);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

Функция ЗапуститьОбновлениеСведенийОСформированныхЧекахНПД(Организация, НачалоПериода, КонецПериода) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление сведений о чеках самозанятых в фоне.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ЧекиНПД.ОбновитьСведенияОСформированныхЧекахНПДВФоне",
		Организация,
		НачалоПериода,
		КонецПериода);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

Функция ПолучитьНавигационнуюСсылкуПоНомеруЧека(НомерЧека, Организация) Экспорт
	
	ДопустимыеСостояния = Новый Массив;
	ДопустимыеСостояния.Добавить(Перечисления.СостоянияЧековНПД.Зарегистрирован);
	ДопустимыеСостояния.Добавить(Перечисления.СостоянияЧековНПД.Аннулирован);
	ДопустимыеСостояния.Добавить(Перечисления.СостоянияЧековНПД.ОшибкаПриАннулировании);
	
	ДокументОснование = РегистрыСведений.ЧекиНПД.НайтиДокументПоНомеруЧека(НомерЧека, Организация, ДопустимыеСостояния);
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат ПолучитьНавигационнуюСсылку(ДокументОснование);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ДатаОбновленияСформированныхЧековНПД(Организация) Экспорт
	
	Возврат ЧекиНПД.ДатаОбновленияСформированныхЧековНПД(Организация);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДатаСведенийНПД() Экспорт
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

#КонецОбласти
