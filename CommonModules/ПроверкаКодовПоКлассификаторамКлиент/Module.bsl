
#Область ПрограммныйИнтерфейс

// Запускает проверку кодов по классификаторам в фоновом задании.
//
// Параметры:
//  Форма - УправляемаяФорма - настраиваемая форма, в которой нужно выполнить проверку кодов, должна соответствовать описанию
//  ОписаниеФормы - Структура - см. ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйОписаниеФормы
//  ПараметрыПроверки - Структура - см. ПроверкаКодовПоКлассификаторамКлиентСервер.НовыйПараметрыПроверки
//
Процедура Проверить(Форма, ОписаниеФормы, ПараметрыПроверки) Экспорт
	
	Результат = ПроверкаКодовПоКлассификаторамВызовСервера.НачатьПроверку(
		ПараметрыПроверки,
		Форма.УникальныйИдентификатор);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ПараметрыЗавершения = Новый Структура;
	ПараметрыЗавершения.Вставить("Форма", Форма);
	ПараметрыЗавершения.Вставить("ОписаниеФормы", ОписаниеФормы);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ЗавершитьПроверку",
		ПроверкаКодовПоКлассификаторамКлиент,
		ПараметрыЗавершения);
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗавершитьПроверку(Результат, ПараметрыЗавершения) Экспорт
	
	ЭлементыФормы = ПараметрыЗавершения.Форма.Элементы;
	ОписаниеФормы = ПараметрыЗавершения.ОписаниеФормы;
	
	ЭлементыФормы[ОписаниеФормы.ДекорацияРезультатПроверкиКодаПоОКТМО].Заголовок = "";
	ЭлементыФормы[ОписаниеФормы.ДекорацияРезультатПроверкиКодаПоОКТМО].Видимость = Ложь;
	Если Результат = Неопределено Или Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = ПроверкаКодовПоКлассификаторамВызовСервера.ПрочитатьРезультатПроверки(Результат.АдресРезультата);
	
	Если РезультатПроверки.КодПоОКТМО.Успешно = Ложь Тогда
		ЦветВыделения = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветВыделенияКонтрагентаСОшибкой");
		Заголовок = Новый ФорматированнаяСтрока(РезультатПроверки.КодПоОКТМО.ТекстОшибки, , ЦветВыделения);
		
		ЭлементыФормы[ОписаниеФормы.ДекорацияРезультатПроверкиКодаПоОКТМО].Заголовок = Заголовок;
		ЭлементыФормы[ОписаниеФормы.ДекорацияРезультатПроверкиКодаПоОКТМО].Видимость = Истина;
		ГруппаКонтактнаяИнформация = ЭлементыФормы[ОписаниеФормы.ГруппаКонтактнаяИнформация];
		Если ГруппаКонтактнаяИнформация.Скрыта() Тогда
			ГруппаКонтактнаяИнформация.Показать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти