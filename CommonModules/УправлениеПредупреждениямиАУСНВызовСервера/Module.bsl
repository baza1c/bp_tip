#Область СлужебныйПрограммныйИнтерфейс

// Добавляет на форму объекта баннер с пояснением
//
// Параметры:
//   Форма        - ФормаКлиентскогоПриложения - Форма объекта, для которой необходимо добавить баннер
//   ЖелтыйБаннер - Булево - В случае Истина добавляет на форму баннер желтого цвета с возможностью закрыть его,
//                           В случае Ложь добавляет на форму баннер красного цвета без возможности скрыть его.
//
Процедура ДобавитьБаннерСПредупреждениемАУСН(Форма, ЖелтыйБаннер = Ложь) Экспорт
	
	Элементы = Форма.Элементы;
	КлючСохранения = УправлениеПредупреждениямиАУСНКлиентСервер.КлючСохраненияОбъекта(Форма.Объект);
	
	ГруппаПредупреждение = Элементы.Вставить("ГруппаПредупреждениеАУСН", Тип("ГруппаФормы"), , Элементы.Шапка);
	ГруппаПредупреждение.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПредупреждение.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаПредупреждение.ОтображатьЗаголовок = Ложь; 
	ГруппаПредупреждение.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ДекорацияПредупреждение = Элементы.Добавить("ПредупреждениеАУСН", Тип("ДекорацияФормы"), ГруппаПредупреждение);
	ДекорацияПредупреждение.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияПредупреждение.Заголовок = ТекстПредупреждения(КлючСохранения);
	ДекорацияПредупреждение.АвтоМаксимальнаяШирина = Ложь;
	ДекорацияПредупреждение.РастягиватьПоГоризонтали = Истина;
	
	Если ЖелтыйБаннер Тогда
		
		ГруппаПредупреждение.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
		
		КомандаЗакрыть = Форма.Команды.Добавить("ЗакрытьПредупреждениеАУСН");
		КомандаЗакрыть.Действие    = "Подключаемый_ЗакрытьПредупреждениеАУСН";
		КомандаЗакрыть.Картинка    = БиблиотекаКартинок.ОчиститьЗначение;
		КомандаЗакрыть.Отображение = ОтображениеКнопки.Картинка;
		КомандаЗакрыть.Подсказка   = НСтр("ru='Нажмите для закрытия предупреждения'");
		
		КнопкаЗакрыть = Форма.Элементы.Добавить("ЗакрытьПредупреждениеАУСН", Тип("КнопкаФормы"), ГруппаПредупреждение);
		КнопкаЗакрыть.Заголовок         = НСтр("ru='Закрыть предупреждение'");
		КнопкаЗакрыть.Вид               = ВидКнопкиФормы.ОбычнаяКнопка;
		КнопкаЗакрыть.ИмяКоманды        = КомандаЗакрыть.Имя;
		КнопкаЗакрыть.ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
		КнопкаЗакрыть.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	
	Иначе
		ГруппаПредупреждение.ЦветФона = ЦветаСтиля.ЦветФонаНекорректногоЗначенияБИП;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытьПредупреждениеАУСН(КлючСохранения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(ВладелецХранилищаДанных(), Ложь, КлючСохранения);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПоказыватьПредупреждениеАУСН(Объект) Экспорт
	
	КлючСохранения = УправлениеПредупреждениямиАУСНКлиентСервер.КлючСохраненияОбъекта(Объект);
	
	Если Не ЗначениеЗаполнено(КлючСохранения) Тогда
		Возврат ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта("ПрименяетсяАУСН", Объект);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеИзХранилища = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(ВладелецХранилищаДанных(), КлючСохранения);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеИзХранилища = Неопределено Тогда
		Возврат ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта("ПрименяетсяАУСН", Объект);
	КонецЕсли;
	
	Возврат (ДанныеИзХранилища = Истина);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстПредупреждения(КлючСохранения)
	
	Если СтрНайти(КлючСохранения, "СЗВ_СТАЖ") > 0 Тогда
		Возврат ТекстПредупрежденияСведенияОЗастрахованныхЛицах_СЗВ_СТАЖ();
	ИначеЕсли СтрНайти(КлючСохранения, "СЗВ_М") > 0 Тогда
		Возврат ТекстПредупрежденияСведенияОЗастрахованныхЛицах_СЗВ_М();
	ИначеЕсли СтрНайти(КлючСохранения, "Вычеты") > 0 Тогда
		Возврат ТекстПредупрежденияНалоговыеВычеты();
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ТекстПредупрежденияВыплатаЗПНаличными() Экспорт

	Возврат НСтр("ru = 'При применении АУСН зарплата выплачивается только через банк'");

КонецФункции

Функция ТекстПредупрежденияНерезидент() Экспорт

	Возврат НСтр("ru = 'При применении АУСН для выполнения работ не привлекаются нерезиденты РФ'");

КонецФункции

Функция ТекстПредупрежденияНалоговыеВычеты()
	
	Возврат НСтр("ru = 'При применении АУСН работодатель не предоставляет социальные и имущественные вычеты'");
	
КонецФункции

Функция ТекстПредупрежденияСведенияОЗастрахованныхЛицах_СЗВ_СТАЖ()
	
	Возврат НСтр("ru = 'При применении АУСН требуется предоставлять сведения по сотрудникам, работающим по договорам ГПХ, находящимся в отпуске без содержания или по уходу за ребенком, работающим в районах Крайнего Севера и приравненным к ним местностям и другим льготным категориям сотрудников.'");
	
КонецФункции

Функция ТекстПредупрежденияСведенияОЗастрахованныхЛицах_СЗВ_М()
	
	Возврат НСтр("ru = 'При применении АУСН требуется предоставлять сведения по сотрудникам, работающим не по трудовому договору.'");
	
КонецФункции

Функция ВладелецХранилищаДанных()
	
	Возврат "БаннерПредупреждениеАУСН";
	
КонецФункции

#КонецОбласти