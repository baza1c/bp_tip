#Область ПрограммныйИнтерфейс

Функция ДанныеПубличныхИдентификаторов(Ссылка) Экспорт
	
	ДанныеИдентификаторов = Новый ТаблицаЗначений;
	ДанныеИдентификаторов.Колонки.Добавить("НавигационнаяСсылка", Новый ОписаниеТипов("Строка"));
	ДанныеИдентификаторов.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеПубличныхИдентификаторовСовместногоИспользования.НавигационнаяСсылка КАК НавигационнаяСсылка,
	|	ДанныеПубличныхИдентификаторовСовместногоИспользования.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов КАК ПубличныеИдентификаторыСинхронизируемыхОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПубличныхИдентификаторовСовместногоИспользования КАК ДанныеПубличныхИдентификаторовСовместногоИспользования
	|		ПО (ДанныеПубличныхИдентификаторовСовместногоИспользования.Идентификатор = ПубличныеИдентификаторыСинхронизируемыхОбъектов.Идентификатор)
	|ГДЕ
	|	ПубличныеИдентификаторыСинхронизируемыхОбъектов.Ссылка = &Ссылка
	|	И ПубличныеИдентификаторыСинхронизируемыхОбъектов.УзелИнформационнойБазы = &УзелОбмена");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("УзелОбмена", СовместноеИспользованиеПовтИсп.ТекущийУзелОбмена());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаДанные = ДанныеИдентификаторов.Добавить();
		СтрокаДанные.НавигационнаяСсылка = ПрефиксВнешнейСсылкиСовместногоИспользования() + Выборка.НавигационнаяСсылка;
		СтрокаДанные.Представление = Выборка.Представление;
	КонецЦикла;
	
	Возврат ДанныеИдентификаторов;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НоваяТаблицаДанныхСинхронизируемыхОбъектов() Экспорт
	
	ТаблицаДанные = Новый ТаблицаЗначений;
	
	ТаблицаДанные.Колонки.Добавить("ПубличныйИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Ссылка", Метаданные.ОпределяемыеТипы.СовместноеИспользованиеСинхронизируемыйОбъектСсылка.Тип);
	ТаблицаДанные.Колонки.Добавить("ПометкаУдаления", Новый ОписаниеТипов("Булево"));
	ТаблицаДанные.Колонки.Добавить("Проведен", Новый ОписаниеТипов("Булево"));
	ТаблицаДанные.Колонки.Добавить("Номер", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("ВидОперации", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("СуммаДокумента", Новый ОписаниеТипов("Число"));
	ТаблицаДанные.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("РазделУчета", Новый ОписаниеТипов("Строка"));
	ТаблицаДанные.Колонки.Добавить("Организация", СовместноеИспользованиеПереопределяемый.ОписаниеТиповОрганизация());
	
	Возврат ТаблицаДанные;
	
КонецФункции

Функция ТекущаяНастройка() Экспорт
	
	Возврат Справочники.НастройкиСовместногоИспользованияПриложений.ТекущаяНастройка();
	
КонецФункции

Процедура ПриЗаписиСинхронизируемогоОбъекта(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СовместноеИспользованиеПовтИсп.ОтражениеДокументовВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	// Эта подписка вызывается при записи документа или справочника.
	// При этом у пользователя может не быть прав на специфические объекты Совместного использования.
	// Поэтому весь код ниже нужно выполнять в привилегированном режиме.
	УстановитьПривилегированныйРежим(Истина);
	
	УзелОбмена = СовместноеИспользованиеПовтИсп.ТекущийУзелОбмена();
	
	СсылкиДляРегистрации = Новый Массив;
	
	Если ПланыОбмена.ИзменениеЗарегистрировано(УзелОбмена, Источник.Ссылка)
		Или Источник.ОбменДанными.Получатели.Содержит(УзелОбмена) Тогда
		// 1. Записывается объект, который участвует в регулярном обмене.
		// В РС ПИСО по нему еще может не быть записи (если получение в корреспонденте не было подтверждено).
		// В этом случае нужно регистрировать изменения синхронизируемых объектов, т.к. данные по объекту нужно отправить в корреспондент.
		СсылкиДляРегистрации.Добавить(Источник.Ссылка);
	Иначе
		// 2. Записывается объект, который был получен из корреспондента, но не выгружается обратно.
		// Например, из-за настроек на узле, или ограничений в правилах регистрации.
		// Такие объекты также необходимо регистрировать для отслеживания.
		Отбор = Новый Структура;
		Отбор.Вставить("УзелИнформационнойБазы", УзелОбмена);
		Отбор.Вставить("Ссылка", Источник.Ссылка);
		
		Если РегистрыСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов.ЗаписьЕстьВРегистре(Отбор) Тогда
			СсылкиДляРегистрации.Добавить(Источник.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	СовместноеИспользованиеПереопределяемый.ПриЗаписиСинхронизируемогоОбъекта(Источник, СсылкиДляРегистрации, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		ДопустимыеТипы = Метаданные.ОпределяемыеТипы.СовместноеИспользованиеСинхронизируемыйОбъектСсылка.Тип;
		
		Для Каждого Ссылка Из СсылкиДляРегистрации Цикл
			Если Не ДопустимыеТипы.СодержитТип(ТипЗнч(Ссылка)) Тогда
				Продолжить;
			КонецЕсли;
			
			РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.Зарегистрировать(Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПередЗаписьюСлужебныхСведенийОбменаДанными(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СовместноеИспользованиеПовтИсп.ОтражениеДокументовВключено() Тогда
		Возврат;
	КонецЕсли;
	
	// Эта подписка вызывается при записи служебных регистров БСД.
	// При этом у пользователя может не быть прав на специфические объекты Совместного использования.
	// Поэтому весь код ниже нужно выполнять в привилегированном режиме.
	УстановитьПривилегированныйРежим(Истина);
	
	УзелОбмена = СовместноеИспользованиеПовтИсп.ТекущийУзелОбмена();
	ДопустимыеТипы = Метаданные.ОпределяемыеТипы.СовместноеИспользованиеСинхронизируемыйОбъектСсылка.Тип;
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ПубличныеИдентификаторыСинхронизируемыхОбъектов") Тогда
		ЗарегистрированныеСсылки = Новый Массив;
		
		Для Каждого Запись Из Источник Цикл
			Если Запись.УзелИнформационнойБазы <> УзелОбмена Тогда
				Продолжить;
			КонецЕсли;
			Если Не ДопустимыеТипы.СодержитТип(ТипЗнч(Запись.Ссылка)) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗарегистрированныеСсылки.Найти(Запись.Ссылка) = Неопределено Тогда
				РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.Зарегистрировать(Запись.Ссылка);
				
				ЗарегистрированныеСсылки.Добавить(Запись.Ссылка);
			КонецЕсли;
		КонецЦикла;
		
		Если Замещение = Истина Или ОбщегоНазначения.ЭтоЗамещениеНабораЗаписей(Замещение) Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаДанных.Ссылка КАК Ссылка
			|ИЗ
			|	РегистрСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов КАК ТаблицаДанных
			|ГДЕ
			|	ТаблицаДанных.УзелИнформационнойБазы = &УзелОбмена
			|	И ТИПЗНАЧЕНИЯ(ТаблицаДанных.Ссылка) В (&ДопустимыеТипы)
			|	И НЕ ТаблицаДанных.Ссылка В (&ЗарегистрированныеСсылки)");
			Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
			Запрос.УстановитьПараметр("ДопустимыеТипы", ДопустимыеТипы.Типы());
			Запрос.УстановитьПараметр("ЗарегистрированныеСсылки", ЗарегистрированныеСсылки);
			
			Для Каждого ЭлементОтбора Из Источник.Отбор Цикл
				Если ЭлементОтбора.Использование Тогда
					Запрос.Текст = Запрос.Текст + "
					|	И ТаблицаДанных." + ЭлементОтбора.Имя + " = &ЗначениеОтбора" + ЭлементОтбора.Имя;
					Запрос.УстановитьПараметр("ЗначениеОтбора" + ЭлементОтбора.Имя, ЭлементОтбора.Значение);
				КонецЕсли;
			КонецЦикла;
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.Зарегистрировать(Выборка.Ссылка);
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.РезультатыОбменаДанными") Тогда
		ЗарегистрированныеСсылки = Новый Массив;
		
		Для Каждого Запись Из Источник Цикл
			Если Запись.УзелИнформационнойБазы <> УзелОбмена Тогда
				Продолжить;
			КонецЕсли;
			Если Не ДопустимыеТипы.СодержитТип(ТипЗнч(Запись.ПроблемныйОбъект)) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗарегистрированныеСсылки.Найти(Запись.ПроблемныйОбъект) = Неопределено Тогда
				РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.Зарегистрировать(Запись.ПроблемныйОбъект);
				
				ЗарегистрированныеСсылки.Добавить(Запись.ПроблемныйОбъект);
			КонецЕсли;
		КонецЦикла;
		
		Если Замещение = Истина Или ОбщегоНазначения.ЭтоЗамещениеНабораЗаписей(Замещение) Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаДанных.ПроблемныйОбъект КАК Ссылка
			|ИЗ
			|	РегистрСведений.РезультатыОбменаДанными КАК ТаблицаДанных
			|ГДЕ
			|	ТаблицаДанных.УзелИнформационнойБазы = &УзелОбмена
			|	И ТИПЗНАЧЕНИЯ(ТаблицаДанных.ПроблемныйОбъект) В (&ДопустимыеТипы)
			|	И НЕ ТаблицаДанных.ПроблемныйОбъект В (&ЗарегистрированныеСсылки)");
			Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
			Запрос.УстановитьПараметр("ДопустимыеТипы", ДопустимыеТипы.Типы());
			Запрос.УстановитьПараметр("ЗарегистрированныеСсылки", ЗарегистрированныеСсылки);
			
			Для Каждого ЭлементОтбора Из Источник.Отбор Цикл
				Если ЭлементОтбора.Использование Тогда
					Запрос.Текст = Запрос.Текст + "
					|	И ТаблицаДанных." + ЭлементОтбора.Имя + " = &ЗначениеОтбора" + ЭлементОтбора.Имя;
					Запрос.УстановитьПараметр("ЗначениеОтбора" + ЭлементОтбора.Имя, ЭлементОтбора.Значение);
				КонецЕсли;
			КонецЦикла;
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				РегистрыСведений.ИзмененияСинхронизируемыхОбъектовСовместногоИспользования.Зарегистрировать(Выборка.Ссылка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ДанныеВСтрокуJSON(Данные, БезПереносовСтрок = Ложь) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	Если БезПереносовСтрок Тогда
		Параметры = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет);
		ЗаписьJSON.УстановитьСтроку(Параметры);
	Иначе
		ЗаписьJSON.УстановитьСтроку();
	КонецЕсли;
	
	ЗаписатьJSON(ЗаписьJSON, Данные, , "ПреобразованиеЗначенияВСтрокуJSON", СовместноеИспользование);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

Функция ПреобразованиеЗначенияВСтрокуJSON(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Возврат XMLСтрока(Значение);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрефиксВнешнейСсылкиСовместногоИспользования()
	
	Возврат "cooperation/sbm/";
	
КонецФункции

#КонецОбласти

