#Область ПрограммныйИнтерфейс

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ОткрытьРасшифровкуОтчета
Процедура ОткрытьРасшифровкуОтчета(ИДОтчета, ИДРедакцииОтчета, ИДИменПоказателей, ПараметрыОтчета) Экспорт
	
	Если ИДОтчета = "РегламентированныйОтчетБухОтчетность" Тогда
		ОткрытьРасшифровкуБухОтчетность(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетБухОтчетностьМП" Тогда
		ОткрытьРасшифровкуБухОтчетностьМП(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетБухОтчетностьСОНКО" Тогда
		ОткрытьРасшифровкуБухОтчетностьСОНКО(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетПрибыль" Тогда
		
		ОткрытьРасшифровкуОтчетПрибыль(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетНДС" Тогда
		
		ОткрытьРасшифровкуОтчетНДС(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетПереченьЗаявленийОВвозеТоваров" Тогда
		
		ПараметрыОтчета.Вставить("ИдентификаторМакета", "");
		
		Если ИДИменПоказателей.Количество() > 0 Тогда
			
			ИдПоказателя = ИДИменПоказателей[0];
			
			Если СтрНачинаетсяС(ИдПоказателя, "П000010001001")
				ИЛИ СтрНачинаетсяС(ИдПоказателя, "П000010001004") Тогда
				
				ПараметрыОтчета.Вставить("ИдентификаторМакета", "РасшифровкаПереченьЗаявленийОВвозеТоваров");
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Номер отметки о регистрации налоговым органом заявления о ввозе товаров, составленного покупателем из страны ЕАЭС
		ПараметрыОтчета.Вставить("НомерВходящегоДокумента", ПараметрыОтчета.Графа1);
		ПараметрыОтчета.Вставить("ИдентификационныйНомерПокупателя", ПараметрыОтчета.Графа4);
		
		ОткрытьРасшифровкуОтчетНДС(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетИмущество" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2008Кв1"
			 Или ИДРедакцииОтчета = "ФормаОтчета2011Кв3" Тогда
			Возврат;
		КонецЕсли;
		
		ОткрытьРасшифровкуОтчетИмущество(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетАвансыИмущество" Тогда
		
		Если  ИДРедакцииОтчета = "ФормаОтчета2013Кв4"
			ИЛИ ИДРедакцииОтчета = "ФормаОтчета2017Кв1"
			ИЛИ ИДРедакцииОтчета = "ФормаОтчета2019Кв1" Тогда
			
			ОткрытьРасшифровкуОтчетАвансыИмуществоФормаОтчета2013Кв4(ИДИменПоказателей, ПараметрыОтчета);
			
		КонецЕсли;

	ИначеЕсли ЗаполнениеФормСтатистикиКлиентСервер.РеализованоАвтозаполнениеФормы(ИДОтчета) Тогда
		
		ЗаполнениеФормСтатистикиКлиент.ОткрытьРасшифровкуСтатистики(ИДОтчета, ИДРедакцииОтчета, ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "БухгалтерскаяОтчетностьВБанк" Тогда
		Если ИДРедакцииОтчета = "ФормаОтчета2011Кв4"
		 ИЛИ ИДРедакцииОтчета = "ФормаОтчета2017Кв3" Тогда
			ОткрытьРасшифровкуБухОтчетность(ИДИменПоказателей, ПараметрыОтчета);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетДвижениеСредствПоСчетуВБанкеЗаПределамиРФ" Тогда
		
		ОткрытьРасшифровкуОтчетДвижениеСредствЗаПределамиРФ(ИДИменПоказателей, ПараметрыОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетДоходыИностраннымОрганизациям" Тогда
		
		ОткрытьРасшифровкуОтчетДоходыИностраннымОрганизациям(ИДИменПоказателей, ПараметрыОтчета); 
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетТуристическийНалог" Тогда
		
		ОткрытьРасшифровкуОтчетТуристическийНалог(ИДИменПоказателей, ПараметрыОтчета);
	
	КонецЕсли;
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ПроверитьНастройкиАвтозаполненияОтчета
Процедура ПроверитьНастройкиАвтозаполненияОтчета(ПараметрыОтчета, ВыполняемоеОповещение, СтандартнаяОбработка) Экспорт
	
	ИмяОтчета   = ПараметрыОтчета.ИДОтчета;
	ИмяФормы    = ПараметрыОтчета.ИДРедакцииОтчета;
	Организация = ПараметрыОтчета.Организация;
	
	РезультатПроверки = ЗаполнениеФормСтатистикиВызовСервера.ПроверитьНастройкиЗаполненияОтчета(ИмяОтчета, ИмяФормы, Организация);
		
	Если РезультатПроверки = Неопределено Тогда
		// У пользователя нет прав на редактирование настроек.
		// Поэтому предложить ему ничего не можем.
		// Либо настройка формы не предусмотрена.
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Истина);
		// Обработка оповещения более не требуется
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ФормаСтатистики 			= РезультатПроверки.ФормаСтатистики;
	ВыполнялисьНастройкиФормы 	= РезультатПроверки.ВыполнялисьНастройкиФормы;
	
	Если Не ВыполнялисьНастройкиФормы Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Кнопки = Новый СписокЗначений();
		
		Кнопки.Добавить(КодВозвратаДиалога.Да, 		"Открыть настройки");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, 	"Заполнить");
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, 	"Отмена");
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
		ДополнительныеПараметры.Вставить("Организация", Организация);
		ДополнительныеПараметры.Вставить("ФормаСтатистики", ФормаСтатистики);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьНастройкиЗаполненияОтчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ТекстВопроса = "Перед заполнением этой формы рекомендуем проверить настройки заполнения
			|";
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки);
		
	КонецЕсли;
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ПередЗаполнениемОтчета
Процедура ПередЗаполнениемОтчета(ИДОтчета, ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения, СтандартнаяОбработка = Неопределено) Экспорт
	
	Если СтрНайти(ИДОтчета, "РегламентированноеУведомлениеЕНВД") = 1 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		УчетЕНВДКлиент.ПередЗаполнениемЗаявления(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения);
	ИначеЕсли СтрНайти(ИДОтчета, "РегламентированноеУведомлениеУтратаПраваПатент") = 1 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		УчетПСНКлиент.ПередЗаполнениемЗаявленияУтратаПраваНаПатент(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения);
	ИначеЕсли СтрНайти(ИДОтчета, "РегламентированноеУведомлениеПрекращениеПатент") = 1 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		УчетПСНКлиент.ПередЗаполнениемЗаявленияПрекращениеПатента(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения);
	
	ИначеЕсли СтрНайти(ИДОтчета, "РегламентированноеУведомлениеСообщениеТранспортЗемля") = 1 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		УправлениеВнеоборотнымиАктивамиКлиент.ПередЗаполнениемСообщенияОНаличииТранспортаИЗемли(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения);
	
	КонецЕсли;
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ВыполнитьНазначаемуюКомандуНаКлиенте
Процедура ВыполнитьНазначаемуюКомандуНаКлиенте(Форма) Экспорт
	
	Организации = Форма.Организация;
	
	Если Организации.Количество() = 1 Тогда
		ОрганизацияОтчета = Организации.Получить(0).Значение;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", ОрганизацияОтчета);
	ПараметрыФормы.Вставить("НачалоПериода", Форма.ДатаНачалаПериодаОтчета);
	ПараметрыФормы.Вставить("КонецПериода", Форма.ДатаКонцаПериодаОтчета);
	
	ОткрытьФорму("Отчет.ЭкспрессПроверкаВеденияУчета.Форма", Новый Структура("ПараметрыФормы", ПараметрыФормы));
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ОткрытьФормуНастройкиАвтозаполнения
Процедура ОткрытьФормуНастройкиАвтозаполнения(ПараметрыФормы) Экспорт
	
	Перем ИмяОтчета, ИмяФормы, ИмяПоля, Организация, ДатаНачала, ДатаОкончания;
	
	ПараметрыФормы.Свойство("ИДОтчета",             ИмяОтчета);
	ПараметрыФормы.Свойство("ИДРедакцииОтчета",     ИмяФормы);
	ПараметрыФормы.Свойство("ИДТекущегоПоказателя", ИмяПоля); // Может не передаваться или передаваться Неопределено, если пользователь хочет выполнить настройку для всей формы
	ПараметрыФормы.Свойство("Организация",          Организация);
	ПараметрыФормы.Свойство("ДатаНачала",           ДатаНачала);
	ПараметрыФормы.Свойство("ДатаОкончания",        ДатаОкончания);
	
	ОписаниеФормы = ЗаполнениеФормСтатистикиВызовСервера.ОписаниеФормыНастройкиПоля(
		ИмяОтчета, ИмяФормы, ИмяПоля, Организация, ДатаНачала, ДатаОкончания);
	
	Если ТипЗнч(ОписаниеФормы) = Тип("Строка") Тогда
		
		ПоказатьПредупреждение(,ОписаниеФормы);
		
	Иначе
		
		ОткрытьФорму(ОписаниеФормы.ИмяФормы, ОписаниеФормы.ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ОбработчикСозданияУведомления
Процедура ОбработчикСозданияУведомления(Форма, Параметр) Экспорт
	
	Если ТипЗнч(Параметр) = Тип("Структура") Тогда 
		ВидУведомления = Неопределено;
		Параметр.Свойство("ВидУведомления", ВидУведомления);
		
		Если ВидУведомления =
			ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ФормаТС1") Тогда 
			ТорговыйСборКлиент.ПриВыбореВидаУведомления(Форма, Параметр);
		ИначеЕсли ВидУведомления =
			ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтУплатыНДС") Тогда
			УведомлениеОбОсвобожденииОтУплатыНДСКлиент.ПриВыбореВидаУведомления(Форма, Параметр);
		ИначеЕсли ВидУведомления =
			ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.УменьшениеНалогаНаСтраховыеВзносы") Тогда
			УчетПСНКлиент.ОбработчикСозданияУведомления(Форма, Параметр);
		ИначеЕсли ВидУведомления =
			ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ИсчисленныеСуммыНалогов")
			Или ВидУведомления =
			ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеОЗачетеНалога") Тогда
			ЕдиныйНалоговыйСчетКлиент.ПриВыбореВидаУведомления(Форма, Параметр);
		КонецЕсли;
		
		РегистрацияОрганизацииКлиент.ОбработчикСозданияУведомления(Форма, Параметр);
	КонецЕсли;
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ОткрытьФормуВыбораКодаПоОКВЭД
Процедура ОткрытьФормуВыбораКодаПоОКВЭДЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда
		ВозвращаемыйРезультат = Новый Структура();
		ВозвращаемыйРезультат.Вставить("КодОКВЭД", РезультатВыбора);
		
		Классификатор = ОбщегоНазначенияБПВызовСервера.ПолучитьКлассификатор(ДополнительныеПараметры.ИмяКлассификатора);
		НаименованиеОКВЭД = Классификатор.Получить(РезультатВыбора);
		
		ВозвращаемыйРезультат.Вставить("НаименованиеОКВЭД", НаименованиеОКВЭД);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ВыполняемоеОповещение, ВозвращаемыйРезультат);
	КонецЕсли;
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ОткрытьФормуДляЗаполненияРегистрирующегоОргана
Процедура ОткрытьФормуДляЗаполненияРегистрирующегоОргана(Организация, ВыполняемоеОповещение) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Организация);
	ОткрытьФорму("Справочник.Организации.Форма.ФормаОрганизации", ПараметрыФормы,,,,, ВыполняемоеОповещение);
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ОткрытьФормуНастроекПередАвтозаполнениемРеглОтчета
Процедура ОткрытьФормуНастроекПередАвтозаполнениемРеглОтчета(ВыполняемоеОповещение) Экспорт
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
	ОповещениеОВыборе = Новый ОписаниеОповещения("ОткрытьФормуВыбораСчетаЗавершение", ЭтотОбъект, ПараметрыОповещения);

	ДополнительныеПараметры = ВыполняемоеОповещение.ДополнительныеПараметры;
	Организация = ДополнительныеПараметры.Организация;
	
	НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;
	ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));

	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Банк.Страна");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбора.Использование  = Истина;
	ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Владелец", Организация);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ОткрытьФорму("Справочник.БанковскиеСчета.ФормаВыбора", ПараметрыФормы,,,,, ОповещениеОВыборе);
	
КонецПроцедуры

// Заполнение параметров отчета по реквизитам из номенклатуры
Процедура ОткрытьФормуПодбораНоменклатуры(СтандартнаяОбработка, ОповещениеОЗакрытии, Владелец, ЗапрашиваемыеПоказатели) Экспорт 
	
	Если Владелец.ИмяФормы = "Отчет.РегламентированныйОтчетСтатистикаФормаУчетаПеремещенияТоваровТС.Форма.ФормаВводаТовараЛК" Тогда
		СтандартнаяОбработка = Ложь;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОповещениеОЗакрытии", ОповещениеОЗакрытии);
		ДополнительныеПараметры.Вставить("ЗапрашиваемыеПоказатели", ЗапрашиваемыеПоказатели);
		
		Оповещение = Новый ОписаниеОповещения("ОткрытьФормуПодбораНоменклатурыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",, Владелец,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьФормуВыбораСчетаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда
		
		НастройкиАвтозаполнения = Новый Структура("БанковскийСчет", РезультатВыбора);
		
		ВозвращаемыйРезультат = Новый Структура();
		ВозвращаемыйРезультат.Вставить("НастройкиАвтозаполнения", НастройкиАвтозаполнения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ВыполняемоеОповещение, ВозвращаемыйРезультат);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуБухОтчетность(ИДИменПоказателей, ПараметрыОтчета)
	
	ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтчета);
	ПараметрыФормы.Вставить("ИДИменПоказателей", ИДИменПоказателей);
	ПараметрыФормы.Вставить("ФорматЯчеекФинансоваяОтчетность", Истина);
	
	ЗаголовокРасшифровкиДляВсегоРаздела = НаименованиеРасшифровкиБухОтчетности(
		"Бухгалтерская отчетность", ПараметрыОтчета.мДатаНачалаПериодаОтчета, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	
	Если НЕ ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ЕстьРасшифровкаПоПоказателям(ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ИДИменПоказателей) Тогда
		ТекстВопроса = НСтр("ru = 'Для выбранной ячейки расшифровка не существует." + Символы.ПС
			+ "Показать расшифровку для всех показателей формы?'");
		ДополнительныеПараметры = Новый Структура("ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы", ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьРасшифровкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПоказатьРасшифровкуВсехПоказателейФормы(ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуОтчетИмущество(ИДИменПоказателей, ПараметрыОтчета)
	
	Если ИДИменПоказателей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ПоказательДляРасшифровки = ИДИменПоказателей[0] + ?(ПараметрыОтчета.НомерТекущейстраницы = 0, "", "_" + ПараметрыОтчета.НомерТекущейстраницы);
	
	// Если показатель расшифровывается таблицей детальных записей, то используем стандартную форму расшифровки...
	Если ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ЕстьДетальныеЗаписиРасшифровки(
		 ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, 
		 ПоказательДляРасшифровки) Тогда
		 
		ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтчета);
		ПараметрыФормы.Вставить("ИДИменПоказателей", ИДИменПоказателей);
		ОткрытьФорму("ОбщаяФорма.РасшифровкаПоказателейРегламентированныхОтчетов", ПараметрыФормы,, Истина);
		Возврат;
		
	КонецЕсли;
	
	// ... в ином случае расшифровка выполняет справкой-расчетом налога на имущество
	ДополнительныеПараметрыРасшифровки = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ПолучитьДополнительныеПараметрыРасшифровкиПоказателя(ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ПоказательДляРасшифровки);
	
	Если ДополнительныеПараметрыРасшифровки = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Для выбранной ячейки расшифровка не существует.'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметрыРасшифровки.РегламентныеОперацииВыполнены Тогда
		
		ПараметрыРасшифровки = ДополнительныеПараметрыРасшифровки.ПараметрыРасшифровки;
		
		ЗаполняемыеНастройки = Новый Структура("Показатели, Группировка, Отбор, ВыводимыеДанные", Ложь, Истина, Истина, Ложь);
		ДополнительныеСвойства = ПараметрыРасшифровки.ДополнительныеСвойства;
		ДополнительныеСвойства.Вставить("РежимРасшифровки",  Истина);
		ДополнительныеСвойства.Вставить("Организация",       ПараметрыОтчета.Организация);
		ДополнительныеСвойства.Вставить("НачалоПериода", 	 ПараметрыОтчета.ДатаНачалаПериодаОтчета);
		Если ДополнительныеПараметрыРасшифровки.Свойство("КонецПериода") Тогда
			ДополнительныеСвойства.Вставить("КонецПериода",  ДополнительныеПараметрыРасшифровки.КонецПериода);
		Иначе
			ДополнительныеСвойства.Вставить("КонецПериода",  ПараметрыОтчета.ДатаКонцаПериодаОтчета);
		КонецЕсли;
		ДополнительныеСвойства.Вставить("КлючВарианта",      "РасчетНалогаНаимущество");
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидРасшифровки", 2);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", 		Истина);
		ПараметрыФормы.Вставить("ЗаполняемыеНастройки", 		ЗаполняемыеНастройки);
		ПараметрыФормы.Вставить("ПользовательскиеНастройки", 	ПараметрыРасшифровки);
		
		ОткрытьФорму("Отчет.СправкаРасчетНалогаНаИмущество.Форма.ФормаОтчета", ПараметрыФормы,, Новый УникальныйИдентификатор);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Для расшифровки показателя необходимо выполнить регламентную операцию ""Расчет налога на имущество"".'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуОтчетАвансыИмуществоФормаОтчета2013Кв4(ИДИменПоказателей, ПараметрыОтчета)
	
	Если ИДИменПоказателей.Количество() > 0 Тогда
		
		ПоказательДляРасшифровки = ИДИменПоказателей[0] + "_" + ПараметрыОтчета.НомерТекущейстраницы;
		
		ДополнительныеПараметрыРасшифровки = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ПолучитьДополнительныеПараметрыРасшифровкиПоказателя(ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ПоказательДляРасшифровки);
		
		Если ДополнительныеПараметрыРасшифровки <> Неопределено 
			И ДополнительныеПараметрыРасшифровки.РегламентныеОперацииВыполнены Тогда
			
			ПараметрыРасшифровки = ДополнительныеПараметрыРасшифровки.ПараметрыРасшифровки;
			
			ЗаполняемыеНастройки = Новый Структура("Показатели, Группировка, Отбор, ВыводимыеДанные", Ложь, Истина, Истина, Ложь);
			ДополнительныеСвойства = ПараметрыРасшифровки.ДополнительныеСвойства;
			ДополнительныеСвойства.Вставить("РежимРасшифровки",  Истина);
			ДополнительныеСвойства.Вставить("Организация",       ПараметрыОтчета.Организация);
			ДополнительныеСвойства.Вставить("НачалоПериода",     ПараметрыОтчета.ДатаНачалаПериодаОтчета);
			ДополнительныеСвойства.Вставить("КонецПериода",      ПараметрыОтчета.ДатаКонцаПериодаОтчета);
			ДополнительныеСвойства.Вставить("КлючВарианта",      "РасчетНалогаНаимущество");
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ВидРасшифровки", 2);
			ПараметрыФормы.Вставить("СформироватьПриОткрытии", 		Истина);
			ПараметрыФормы.Вставить("ЗаполняемыеНастройки", 		ЗаполняемыеНастройки);
			ПараметрыФормы.Вставить("ПользовательскиеНастройки", 	ПараметрыРасшифровки);
			
			ОткрытьФорму("Отчет.СправкаРасчетНалогаНаИмущество.Форма.ФормаОтчета", ПараметрыФормы,, Новый УникальныйИдентификатор);
		Иначе
			Если ДополнительныеПараметрыРасшифровки = Неопределено Тогда
				ТекстПредупреждения = НСтр("ru = 'Для выбранной ячейки расшифровка не существует.'");
			Иначе
				ТекстПредупреждения = НСтр("ru = 'Для расшифровки показателя необходимо выполнить регламентную операцию ""Расчет налога на имущество"".'");
			КонецЕсли;
			ПоказатьПредупреждение(,ТекстПредупреждения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуБухОтчетностьМП(ИДИменПоказателей, ПараметрыОтчета)
	
	ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтчета);
	ПараметрыФормы.Вставить("ИДИменПоказателей", ИДИменПоказателей);
	ПараметрыФормы.Вставить("ФорматЯчеекФинансоваяОтчетность", Истина);
	
	ЗаголовокРасшифровкиДляВсегоРаздела = НаименованиеРасшифровкиБухОтчетности(
		"Упрощенная бухгалтерская отчетность", ПараметрыОтчета.ДатаНачалаПериодаОтчета, ПараметрыОтчета.ДатаКонцаПериодаОтчета);
	
	Если НЕ ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ЕстьРасшифровкаПоПоказателям(ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ИДИменПоказателей) Тогда
		ТекстВопроса = НСтр("ru = 'Для выбранной ячейки расшифровка не существует." + Символы.ПС
			+ "Показать расшифровку для всех показателей формы?'");
		ДополнительныеПараметры = Новый Структура("ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы", ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьРасшифровкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПоказатьРасшифровкуВсехПоказателейФормы(ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуБухОтчетностьСОНКО(ИДИменПоказателей, ПараметрыОтчета)
	
	ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтчета);
	ПараметрыФормы.Вставить("ИДИменПоказателей", ИДИменПоказателей);
	
	ЗаголовокРасшифровкиДляВсегоРаздела = НаименованиеРасшифровкиБухОтчетности(
		"Бухгалтерская отчетность СО НКО", ПараметрыОтчета.ДатаНачалаПериодаОтчета, ПараметрыОтчета.ДатаКонцаПериодаОтчета);
	
	Если НЕ ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ЕстьРасшифровкаПоПоказателям(ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ИДИменПоказателей) Тогда
		ТекстВопроса = НСтр("ru = 'Для выбранной ячейки расшифровка не существует." + Символы.ПС
			+ "Показать расшифровку для всех показателей формы?'");
		ДополнительныеПараметры = Новый Структура("ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы", ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьРасшифровкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПоказатьРасшифровкуВсехПоказателейФормы(ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаголовокРасшифровкиДляВсегоРаздела = ДополнительныеПараметры.ЗаголовокРасшифровкиДляВсегоРаздела;
	ПараметрыФормы = ДополнительныеПараметры.ПараметрыФормы;
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПоказатьРасшифровкуВсехПоказателейФормы(ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьРасшифровкуВсехПоказателейФормы(ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы)
	
	ПараметрыФормы.Вставить("ЗаголовокРасшифровки", ЗаголовокРасшифровкиДляВсегоРаздела);
	ОткрытьФорму("ОбщаяФорма.РасшифровкаПоказателейРегламентированныхОтчетов", ПараметрыФормы, , Истина);
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуОтчетПрибыль(ИДИменПоказателей, ПараметрыОтчета)
	
	ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтчета);
	ПараметрыФормы.Вставить("ИДИменПоказателей", ИДИменПоказателей);
	
	ЗаголовокРасшифровкиДляВсегоРаздела = НаименованиеРасшифровкиДекларацииПоНалогуНаПрибыль(
		ПараметрыОтчета.мДатаНачалаПериодаОтчета, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	
	Если НЕ ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ЕстьРасшифровкаПоПоказателям(ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ИДИменПоказателей) Тогда
		ТекстВопроса = НСтр("ru = 'Для выбранной ячейки расшифровка не существует." + Символы.ПС
			+ "Показать расшифровку для всех показателей формы?'");
			
		ДополнительныеПараметры = Новый Структура("ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы", ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьРасшифровкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПоказатьРасшифровкуВсехПоказателейФормы(ЗаголовокРасшифровкиДляВсегоРаздела, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуОтчетДвижениеСредствЗаПределамиРФ(ИДИменПоказателей, ПараметрыОтчета)
	
	ЕстьРасшифровка = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ЕстьРасшифровкаПоПоказателям(
		ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ИДИменПоказателей);
	ДополнительныеПараметры = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ПолучитьДополнительныеПараметрыРасшифровкиПоПараметрам(
			ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ИДИменПоказателей[0], ПараметрыОтчета.КоординатыСтраницыРаздела);
			
	Если ЕстьРасшифровка  И ДополнительныеПараметры <> Неопределено Тогда
		
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных; 
		
		ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
		
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Регистратор",
			ДополнительныеПараметры.ДокументыРасшифровки, ВидСравненияКомпоновкиДанных.ВСписке);
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "КорСубконто1",
			ДополнительныеПараметры.ОтборыИПоказатели.Отбор, ВидСравненияКомпоновкиДанных.Равно);
		Если ДополнительныеПараметры.ОтборыИПоказатели.Свойство("СубконтоКт2") Тогда
				
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СубконтоКт2",
				ДополнительныеПараметры.ОтборыИПоказатели.СубконтоКт2, ВидСравненияКомпоновкиДанных.Равно);

		КонецЕсли;
			
		Если ДополнительныеПараметры.ОтборыИПоказатели.Свойство("СубконтоДт2") Тогда
				
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СубконтоДт2",
				ДополнительныеПараметры.ОтборыИПоказатели.СубконтоДт2, ВидСравненияКомпоновкиДанных.Равно);

		КонецЕсли;
	
		ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
		ДополнительныеСвойства.Вставить("РежимРасшифровки",        Истина);
		ДополнительныеСвойства.Вставить("Организация",             ПараметрыОтчета.Организация);
		ДополнительныеСвойства.Вставить("НачалоПериода",           ПараметрыОтчета.ДатаНачалаПериодаОтчета);
		ДополнительныеСвойства.Вставить("КонецПериода",            ПараметрыОтчета.ДатаКонцаПериодаОтчета);
		ДополнительныеСвойства.Вставить("ПоказательБУ",            ДополнительныеПараметры.ОтборыИПоказатели.ПоказательБУ);
		ДополнительныеСвойства.Вставить("ПоказательВалютнаяСумма", ДополнительныеПараметры.ОтборыИПоказатели.ПоказательВалютнаяСумма);

		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("РежимРасшифровки", 		Истина);
		ПараметрыФормы.Вставить("ВидРасшифровки", 			2); // Из пользовательских настроек
		ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии",	 Истина);
		
		ОткрытьФорму("Отчет.ОтчетПоПроводкам.Форма", ПараметрыФормы,, Истина);
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Для выбранной ячейки расшифровка не существует.'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуОтчетДоходыИностраннымОрганизациям(ИДИменПоказателей, ПараметрыОтчета)
	
	//В первую очередь ищем расшифровку по показателю (по первой версии заполнения отчета).
	ДополнительныеПараметры = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ПолучитьДополнительныеПараметрыРасшифровкиПоПоказателюСИерархиейСтраниц(
		ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ИДИменПоказателей[0], "Лист", ПараметрыОтчета.КоординатыСтраницыРаздела);
		
	Если ТипЗнч(ДополнительныеПараметры) <> Тип("Структура") Тогда
		//Во вторую очередь ищем расшифровку целиком по разделу
		ДополнительныеПараметры = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ПолучитьДополнительныеПараметрыРасшифровкиПоРазделуСИерархиейСтраниц(
			ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ПараметрыОтчета.ИмяРаздела, ПараметрыОтчета.КоординатыСтраницыРаздела);
	КонецЕсли;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		
		Если ДополнительныеПараметры.Действие = "Контрагент" Тогда
		
			ПараметрыФормы = Новый Структура("Ключ", ДополнительныеПараметры.Контрагент);
			
			ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы);
		
		ИначеЕсли ДополнительныеПараметры.Действие = "ОтчетПоПроводкам" Тогда

			ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
			ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
			ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
			
			Если ТипЗнч(ДополнительныеПараметры.Регистратор) = Тип("Массив") Тогда
				ВидСравненияРегистратор = ВидСравненияКомпоновкиДанных.ВСписке;	
			Иначе
				ВидСравненияРегистратор = ВидСравненияКомпоновкиДанных.Равно;	
			КонецЕсли;
			
			Если ТипЗнч(ДополнительныеПараметры.СчетКт) = Тип("Массив") Тогда
				ВидСравненияСчетКт = ВидСравненияКомпоновкиДанных.ВСписке;	
			Иначе
				ВидСравненияСчетКт = ВидСравненияКомпоновкиДанных.Равно;	
			КонецЕсли;
			
			Если ТипЗнч(ДополнительныеПараметры.СубконтоДт1) = Тип("Массив") Тогда
				ВидСравненияСубконтоДт1 = ВидСравненияКомпоновкиДанных.ВСписке;	
			Иначе
				ВидСравненияСубконтоДт1 = ВидСравненияКомпоновкиДанных.Равно;	
			КонецЕсли;
			
			Если ТипЗнч(ДополнительныеПараметры.СубконтоДт2) = Тип("Массив") Тогда
				ВидСравненияСубконтоДт2 = ВидСравненияКомпоновкиДанных.ВСписке;	
			Иначе
				ВидСравненияСубконтоДт2 = ВидСравненияКомпоновкиДанных.Равно;	
			КонецЕсли;
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Регистратор",
				ДополнительныеПараметры.Регистратор, ВидСравненияРегистратор);
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СчетКт",
				ДополнительныеПараметры.СчетКт, ВидСравненияСчетКт);
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СубконтоДт1",
				ДополнительныеПараметры.СубконтоДт1, ВидСравненияСубконтоДт1);
			
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СубконтоДт2",
				ДополнительныеПараметры.СубконтоДт2, ВидСравненияСубконтоДт2);
			
			Если ДополнительныеПараметры.Свойство("СчетДт") Тогда
				
				Если ТипЗнч(ДополнительныеПараметры.СчетДт) = Тип("Массив") Тогда
					ВидСравненияСчетДт = ВидСравненияКомпоновкиДанных.ВСписке;	
				Иначе
					ВидСравненияСчетДт = ВидСравненияКомпоновкиДанных.Равно;	
				КонецЕсли;
				
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СчетДт",
					ДополнительныеПараметры.СчетДт, ВидСравненияСчетДт);
				
			КонецЕсли;
			
			Если ДополнительныеПараметры.Свойство("СубконтоДт3") Тогда
				
				Если ТипЗнч(ДополнительныеПараметры.СубконтоДт3) = Тип("Массив") Тогда
					ВидСравненияСубконтоДт3 = ВидСравненияКомпоновкиДанных.ВСписке;	
				Иначе
					ВидСравненияСубконтоДт3 = ВидСравненияКомпоновкиДанных.Равно;	
				КонецЕсли;
				
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СубконтоДт3",
					ДополнительныеПараметры.СубконтоДт3, ВидСравненияСубконтоДт3);
				
			КонецЕсли;
			
			ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(ПараметрыОтчета.Организация);
				
			ДатаНачалаПериодаОтчета = ОбщегоНазначенияБПВызовСервера.НачалоПоследнейЧастиОтчетногоПериодаПрибыль(ПараметрыОтчета.ДатаКонцаПериодаОтчета,
				ГоловнаяОрганизация);

			ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
			ДополнительныеСвойства.Вставить("РежимРасшифровки",        Истина);
			ДополнительныеСвойства.Вставить("Организация",             ГоловнаяОрганизация);
			ДополнительныеСвойства.Вставить("НачалоПериода",           ДатаНачалаПериодаОтчета);
			ДополнительныеСвойства.Вставить("КонецПериода",            ПараметрыОтчета.ДатаКонцаПериодаОтчета);
			ДополнительныеСвойства.Вставить("ПоказательБУ",            Истина);
			ДополнительныеСвойства.Вставить("ПоказательВалютнаяСумма", Истина);
			ДополнительныеСвойства.Вставить("ВыводитьЗаголовок",	   Ложь);
			
			ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения",
				ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(ГоловнаяОрганизация).Количество() > 1);

			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("РежимРасшифровки", 		 Истина);
			ПараметрыФормы.Вставить("ВидРасшифровки", 			 2); // Из пользовательских настроек
			ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
			ПараметрыФормы.Вставить("СформироватьПриОткрытии",   Истина);
			
			ОткрытьФорму("Отчет.ОтчетПоПроводкам.Форма", ПараметрыФормы,, Истина);
		
		КонецЕсли;
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Для выбранной ячейки расшифровка не задана.'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьРасшифровкуОтчетТуристическийНалог(ИДИменПоказателей, ПараметрыОтчета)
	
	Если ИДИменПоказателей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	Координаты = ПараметрыОтчета.КоординатыСтраницыРаздела;
	
	Если Координаты.Количество() > 0 Тогда
		НомерТекущейстраницы = Координаты[0];
	Иначе
		Возврат;
	КонецЕсли;
	ПоказательДляРасшифровки = ИДИменПоказателей[0] + ?(НомерТекущейстраницы = 0, "", "_" + НомерТекущейстраницы);
	ОбщийПоказательДляРасшифровки = ПараметрыОтчета.ИмяРаздела + ?(НомерТекущейстраницы = 0, "", "_" + НомерТекущейстраницы);
	
	ДопПараметрыРасшифровки = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ПолучитьДополнительныеПараметрыРасшифровкиПоказателя(
		ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ПоказательДляРасшифровки);
		
	ДополнительныеПараметры = ЗаполнениеБухгалтерскойОтчетностиВызовСервера.ПолучитьДополнительныеПараметрыРасшифровкиПоказателя(
		ПараметрыОтчета.АдресВременногоХранилищаРасшифровки, ОбщийПоказательДляРасшифровки);
		
	Если ДополнительныеПараметры  = Неопределено И ДопПараметрыРасшифровки = Неопределено Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Для выбранной ячейки расшифровка не существует.'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат; 
		
	КонецЕсли;

	Если ДопПараметрыРасшифровки = Неопределено И ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		
		Если ДополнительныеПараметры.Действие = "Гостиница" Тогда
		
			ПараметрыФормы = Новый Структура("Ключ", ДополнительныеПараметры.Гостиница);	
			ОткрытьФорму("Справочник.Гостиницы.ФормаОбъекта", ПараметрыФормы);
			
			Возврат;
			
		КонецЕсли;;

	КонецЕсли;	
	
	ИмяФормы = "Отчет.СправкаРасчетТуристическогоНалога.Форма"; 
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии",      Истина);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "РегламентированныйОтчет");
	ПараметрыФормы.Вставить("Организация",                  ПараметрыОтчета.Организация);
	ПараметрыФормы.Вставить("НачалоПериода",                ПараметрыОтчета.ДатаНачалаПериодаОтчета);
	ПараметрыФормы.Вставить("КонецПериода",                 ПараметрыОтчета.ДатаКонцаПериодаОтчета);
	ПараметрыФормы.Вставить("РежимРасшифровки",             Истина);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки",    ДопПараметрыРасшифровки.ПараметрыРасшифровки);
		
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, , Истина);
	
КонецПроцедуры

// Процедура завершения проверки настройки заполнения статистических отчетов
// Вызывается если задан вопрос, про проверку настроек заполнения.
//
Процедура ПроверитьНастройкиЗаполненияОтчетаЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Организация = ДополнительныеПараметры.Организация;
	ФормаСтатистики = ДополнительныеПараметры.ФормаСтатистики;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ФормаСтатистики", ФормаСтатистики);
		ОткрытьФорму("РегистрСведений.НастройкаЗаполненияФормСтатистики.Форма.НастройкаЗаполненияФормСтатистики", ПараметрыФормы);
		Результат = Ложь;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		ЗаполнениеФормСтатистикиВызовСервера.ЗаписатьНастройкиВыполнены(ФормаСтатистики, Организация);
		Результат = Истина;
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
	
КонецПроцедуры

Функция НаименованиеРасшифровкиБухОтчетности(НаименованиеОтчета, НачалоПериода, КонецПериода)
	
	Если НачалоГода(НачалоПериода) <> НачалоДня(НачалоПериода) Тогда
		ПериодОтчета = Формат(НачалоПериода, "ДФ=dd.MM.yyyy") + " - " + Формат(КонецПериода, "ДФ='ММММ гггг'") + " г." ;
	Иначе
		Если Месяц(КонецПериода) = 1 Тогда 
			ПериодОтчета = Формат(КонецПериода, "ДФ='ММММ гггг'") + " г." ;
		Иначе
			ПериодОтчета = "Январь - " + Формат(КонецПериода, "ДФ='ММММ гггг'") + " г." ;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НаименованиеОтчета + " за " + ПериодОтчета + " - расшифровка значений показателей";
	
КонецФункции

Функция НаименованиеРасшифровкиДекларацииПоНалогуНаПрибыль(НачалоПериода, КонецПериода)
	
	ПериодОтчета = ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(КонецПериода), "ФП = Истина");
	
	Возврат "Декларация по налогу на прибыль за " + ПериодОтчета + " - расшифровка значений показателей";
	
КонецФункции 

Процедура ОткрытьРасшифровкуОтчетНДС(ИДИменПоказателей, ПараметрыОтчета)
	
	Если ИДИменПоказателей.Количество() > 0 Тогда
		
		ПользовательскиеНастройки = Неопределено;
		
		ИдентификаторМакета = "";
		
		Если НЕ ПараметрыОтчета.Свойство("ИдентификаторМакета", ИдентификаторМакета) Тогда
		
			ИдПоказателя = ИДИменПоказателей[0];
			
			ИдентификаторМакета = УчетНДСВызовСервера.ПолучитьИдентификаторМакетаРасшифровкиДекларацииПоНДС(ПараметрыОтчета, ИдПоказателя, ПользовательскиеНастройки);
			
		КонецЕсли;
	
		Если ЗначениеЗаполнено(ИдентификаторМакета) Тогда
			
			Если ТипЗнч(ИдентификаторМакета) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
				
				ПараметрыФормы = Новый Структура();
				ПараметрыФормы.Вставить("Ключ", ИдентификаторМакета);
				ОткрытьФорму("Справочник.ОсновныеСредства.ФормаОбъекта", ПараметрыФормы,, Новый УникальныйИдентификатор);
				
			Иначе
				
				Если ПользовательскиеНастройки = Неопределено Тогда
					
					ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
					
					Если ИдентификаторМакета = "РасшифровкаПереченьЗаявленийОВвозеТоваров" Тогда
						
						ПользовательскийОтбор = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
						ПользовательскийОтбор.ИдентификаторПользовательскойНастройки = "Отбор";
						
						ЭлементОтбораПользовательский = ПользовательскийОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
						ЭлементОтбораПользовательский.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор();
						ЭлементОтбораПользовательский.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
						ЭлементОтбораПользовательский.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НомерВходящегоДокумента");
						ЭлементОтбораПользовательский.ПравоеЗначение = ПараметрыОтчета.НомерВходящегоДокумента;
						ЭлементОтбораПользовательский.Использование = Истина;
						
						ЭлементОтбораПользовательский = ПользовательскийОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
						ЭлементОтбораПользовательский.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор();
						ЭлементОтбораПользовательский.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
						ЭлементОтбораПользовательский.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИдентификационныйНомерПокупателя");
						ЭлементОтбораПользовательский.ПравоеЗначение = ПараметрыОтчета.ИдентификационныйНомерПокупателя;
						ЭлементОтбораПользовательский.Использование = Истина;
						
					КонецЕсли;
					
				КонецЕсли;
				
				ЕстьОтбор = ПользовательскиеНастройки.Элементы.Найти("Отбор") <> Неопределено;
				ЗаполняемыеНастройки = Новый Структура("Показатели, Группировка, Отбор, ВыводимыеДанные", Ложь, Ложь, ЕстьОтбор, Ложь);
				
				ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
				ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
				ДополнительныеСвойства.Вставить("Организация",      ПараметрыОтчета.Организация);
				
				Раздел5 = Ложь;
				
				Если ТипЗнч(ИдентификаторМакета) = Тип("Структура") Тогда
					Если ИдентификаторМакета.Свойство("НачалоПериодаРаздел5") Тогда
						Раздел5 = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если Раздел5 Тогда
					
					ДополнительныеСвойства.Вставить("НачалоПериода",ИдентификаторМакета.НачалоПериодаРаздел5);
					ДополнительныеСвойства.Вставить("КонецПериода",ИдентификаторМакета.КонецПериодаРаздел5);
					ИдентификаторМакета = ИдентификаторМакета.ИдентификаторМакета;
					
				Иначе
					
					Если ПараметрыОтчета.Свойство("ДатаНачалаРасширенногоПериодаОтчета") Тогда
						ДополнительныеСвойства.Вставить("НачалоПериода", ПараметрыОтчета.ДатаНачалаРасширенногоПериодаОтчета);
					Иначе
						ДополнительныеСвойства.Вставить("НачалоПериода", ПараметрыОтчета.ДатаНачалаПериодаОтчета);
					КонецЕсли;
					
					ДополнительныеСвойства.Вставить("КонецПериода", ПараметрыОтчета.ДатаКонцаПериодаОтчета);
					
				КонецЕсли;
			
				ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", Истина);
				ДополнительныеСвойства.Вставить("КлючВарианта", ИдентификаторМакета);
				
				ГруппаОрганизаций = Новый СписокЗначений;
				Если ПараметрыОтчета.Свойство("ГруппаОрганизаций", ГруппаОрганизаций)
					И ТипЗнч(ГруппаОрганизаций) = Тип("СписокЗначений")
					И ГруппаОрганизаций.Количество() > 0 Тогда
					
					ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения", Истина);
					
				КонецЕсли;
				
				ПараметрыФормы = Новый Структура();
				ПараметрыФормы.Вставить("ВидРасшифровки", 2);
				ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
				ПараметрыФормы.Вставить("ИДРасшифровки", ИдентификаторМакета);
				ПараметрыФормы.Вставить("ЗаполняемыеНастройки", ЗаполняемыеНастройки);
				ПараметрыФормы.Вставить("ЭтоРасшифровкаДекларации", Истина);
				ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
				
				ОткрытьФорму("Отчет.РасшифровкаПоказателейНДС.Форма.ФормаОтчета", ПараметрыФормы,, Новый УникальныйИдентификатор);
			КонецЕсли;
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Для выбранной ячейки расшифровка не существует.'");
			ПоказатьПредупреждение(,ТекстПредупреждения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура завершения выбора номенклатуры
Процедура ОткрытьФормуПодбораНоменклатурыЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПередаваемыеПоказатели = УчетНДСВызовСервера.ХарактеристикиТовараДляОтчетаСтатистикаФормаУчетаПеремещенияТоваровТС(
							РезультатВыбора,
							ДополнительныеПараметры.ЗапрашиваемыеПоказатели);
							
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗакрытии, ПередаваемыеПоказатели);
	
КонецПроцедуры

#КонецОбласти