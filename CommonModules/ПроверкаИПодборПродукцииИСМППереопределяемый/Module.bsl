#Область СлужебныйПрограммныйИнтерфейс

// Возвращает через второй параметр признак что переданный контрагент является нерезидентом.
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - Проверяемы контрагент
//  НеРезидент - Булево - Признак что контрагент не резидент (Истина) или резидент (Ложь).
//
Процедура ПриОпределенииКонтрагентНеРезидент(Контрагент, НеРезидент) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Вызывается при заверешении проверки и подбора для отражения данных в документе.
// Предназначена для сохранения связей номенклатуры с GTIN, с учетом коээффициентов групповых упаковок. 
// 
// Параметры:
//  ТаблицаОписанияGTIN - см. ПроверкаИПодборПродукцииИСМП.ПустаяТаблицаОписанияGTIN.
Процедура ЗафиксироватьОписаниеGTIN(ТаблицаОписанияGTIN) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаДляЗапроса = ТаблицаОписанияGTIN.Скопировать();
	
	ТаблицаДляЗапроса.Колонки.Добавить("EAN", Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры.Измерения.Штрихкод.Тип);
	
	Для Каждого СтрокаТаблицы Из ТаблицаДляЗапроса Цикл
		
		СтрокаТаблицы.EAN = РазборКодаМаркировкиИССлужебныйКлиентСервер.ШтрихкодEANИзGTIN(СтрокаТаблицы.GTIN);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.EAN КАК EAN,
	|	ИсходнаяТаблица.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ИсходнаяТаблица
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.EAN КАК Штрихкод,
	|	ИсходнаяТаблица.Номенклатура КАК Номенклатура
	|ИЗ
	|	ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ИсходнаяТаблица.EAN = ШтрихкодыНоменклатуры.Штрихкод
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ТаблицаДляЗапроса);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
		
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаДетальныеЗаписи);
		
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет в табличной части служебные реквизиты, например: признак использования характеристик номенклатуры.
//
// Параметры:
//  Форма          - ФормаКлиентскогоПриложения - Форма.
//  ТабличнаяЧасть - ДанныеФормыКоллекция, ТаблицаЗначений - таблица для заполнения.
Процедура ЗаполнитьСлужебныеРеквизитыВКоллекции(Форма, ТабличнаяЧасть) Экспорт
	
Если ТипЗнч(ТабличнаяЧасть) = Тип("ТаблицаЗначений") Тогда
		КолонкиРеквизитов = ТабличнаяЧасть.Колонки;
	Иначе
		КолонкиРеквизитов = ТабличнаяЧасть.Выгрузить(Новый Массив).Колонки;
	КонецЕсли;

	Если КолонкиРеквизитов.Найти("Номенклатура")= Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокРеквизитов = Новый Массив;
	
	Если КолонкиРеквизитов.Найти("Артикул")<> Неопределено Тогда
		СписокРеквизитов.Добавить("Артикул");
	КонецЕсли;
	
	Если КолонкиРеквизитов.Найти("Код")<> Неопределено Тогда
		СписокРеквизитов.Добавить("Код");
	КонецЕсли;
	
	Если КолонкиРеквизитов.Найти("ЕдиницаИзмерения")<> Неопределено Тогда
		СписокРеквизитов.Добавить("ЕдиницаИзмерения");
	КонецЕсли;
	
	Если ТипЗнч(ТабличнаяЧасть) = Тип("ТаблицаЗначений") Тогда
		МассивНоменклатура = ТабличнаяЧасть.ВыгрузитьКолонку("Номенклатура");
	Иначе
		МассивНоменклатура = ТабличнаяЧасть.Выгрузить(,"Номенклатура").ВыгрузитьКолонку("Номенклатура");
	КонецЕсли;
		
	МассивНоменклатура = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивНоменклатура);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(МассивНоменклатура, Справочники.Номенклатура.ПустаяСсылка());
	
	СоответствиеСведенийОНоменклатуре = Новый Соответствие;
	Если СписокРеквизитов.Количество() > 0 Тогда
		СоответствиеСведенийОНоменклатуре = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивНоменклатура, СписокРеквизитов);
	КонецЕсли; 
	
	Если КолонкиРеквизитов.Найти("ТребуетВзвешивания") <> Неопределено Тогда
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Номенклатура", МассивНоменклатура);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОписаниеНоменклатурыИС.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|ГДЕ
		|	ОписаниеНоменклатурыИС.Номенклатура В(&Номенклатура)
		|	И ОписаниеНоменклатурыИС.КоличествоВПотребительскойУпаковке = 0";
		
		МассивТребуетВзвешивания = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	Иначе
		МассивТребуетВзвешивания = Новый Массив;
	КонецЕсли;
	
	Если КолонкиРеквизитов.Найти("ПроизвольнаяЕдиницаУчета")<> Неопределено Тогда
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Номенклатура", МассивНоменклатура);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОписаниеНоменклатурыИС.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|ГДЕ
		|	ОписаниеНоменклатурыИС.Номенклатура В(&Номенклатура)
		|	И ОписаниеНоменклатурыИС.КоличествоВПотребительскойУпаковке <> 1
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиЧастичногоВыбытияПродукцииИС.НоменклатураЧастичногоВыбытия
		|ИЗ
		|	РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НастройкиЧастичногоВыбытияПродукцииИС
		|ГДЕ
		|	НастройкиЧастичногоВыбытияПродукцииИС.НоменклатураЧастичногоВыбытия В(&Номенклатура)";
		
		МассивПроизвольнаяЕдиницаУчета = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	Иначе
		МассивПроизвольнаяЕдиницаУчета = Новый Массив;
	КонецЕсли;
	
	ЕстьДанныеТребуетВзвешивания       = МассивТребуетВзвешивания.Количество() > 0;
	ЕстьДанныеПроизвольнаяЕдиницаУчета = МассивПроизвольнаяЕдиницаУчета.Количество() > 0;
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре[СтрокаТЧ.Номенклатура];
		
		Если СведенияОНоменклатуре <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, СведенияОНоменклатуре);
		КонецЕсли;
		
		Если ЕстьДанныеТребуетВзвешивания Тогда
			СтрокаТЧ.ТребуетВзвешивания = (НЕ МассивТребуетВзвешивания.Найти(СтрокаТЧ.Номенклатура) = Неопределено);
		КонецЕсли;
		
		Если ЕстьДанныеПроизвольнаяЕдиницаУчета Тогда
			СтрокаТЧ.ПроизвольнаяЕдиницаУчета = (НЕ МассивПроизвольнаяЕдиницаУчета.Найти(СтрокаТЧ.Номенклатура) = Неопределено);
		КонецЕсли;
		
	КонецЦикла
	
КонецПроцедуры

// Устанавливает режим просмотра (доступности, для команд) элементам формы.
//   Переопределение необходимо использовать для совместной работы с аналогичными механизмами.
//   Обработанные здесь реквизиты мледует удалить из массива "Блокируемые элементы".
// 
// Параметры:
//  Форма               - ФормаКлиентскогоПриложения - форма в которой производится изменение доступности
//  БлокируемыеЭлементы - Массив - наименования реквизитов
//  Заблокировать       - Булево - заблокировать или разблокировать реквизиты
Процедура УстановитьТолькоПросмотрЭлементов(
		Форма,
		БлокируемыеЭлементы,
		Заблокировать) Экспорт
	
	
КонецПроцедуры

#Область СерииНоменклатуры

// Предназначена для расчета статусов указания серий во всех строках таблицы товаров
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения - форма с таблицей товаров
//  ПараметрыУказанияСерий - Структура - параметры указания серий
Процедура ЗаполнитьСтатусыУказанияСерий(Форма, ПараметрыУказанияСерий) Экспорт
	
	
КонецПроцедуры

// Возвращает через параметр наличие права на добавление элементов справочника СерииНоменклатуры
//
// Параметры:
//  ПравоДобавлениеСерий - Булево - исходящий, наличие права на добавление.
Процедура ОпределитьПравоДобавлениеСерий(ПравоДобавлениеСерий) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыИнтеграцииФормыПроверкиИПодбора

// Заполняет специфику интеграции формы проверки и подбора в конкретную форму.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для которой настраиваются параметры интеграции.
//  ПараметрыИнтеграции - (См. ПроверкаИПодборПродукцииИС.ПараметрыИнтеграцииФормПроверкиИПодбора).
//  ВидПродукции - Перечислениессылка.ВидыПродукцииИС - вид продукции для которого проводится встраивание
//
Процедура ПриОпределенииПараметровИнтеграцииФормыПроверкиИПодбора(Форма, ПараметрыИнтеграции, ВидПродукции = Неопределено) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	ЭтоПростойИнтерфейс = ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс();
	
	Если ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаОбщая"
		Или ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаТовары" Тогда
		
		ПараметрыИнтеграции.ИмяРодительскойГруппыФормы                 = "ГруппаТовары";
		//ПараметрыИнтеграции.ИмяЭлементаФормыТовары                     = "ТоварыГруппаНоменклатура";
		ПараметрыИнтеграции.ИмяСледующейКолонки                        = "ТоварыКиЗ_ГИСМ";
		ПараметрыИнтеграции.РазмещатьЭлементыИнтерфейса                = Истина;
		ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора  = Истина;
		ПараметрыИнтеграции.ИспользоватьБезМаркируемойПродукции        = Истина;
		ПараметрыИнтеграции.БлокироватьТабличнуюЧастьТоварыПриПроверке = Ложь;
		ПараметрыИнтеграции.ДоступныОбъемноСортовыеКоды                = Истина;
		ПараметрыИнтеграции.ИспользоватьСтатусПроверкаЗавершена        = Ложь;
		ПараметрыИнтеграции.ОтображатьОшибкиПроверкиСредствамиККТ      = ИСТИНА;
		ПараметрыИнтеграции.ЕстьПравоИзменение                         = ПравоДоступа("Изменение", Форма.Объект.Ссылка.Метаданные());
		ПараметрыИнтеграции.ИмяТабличнойЧастиСерии                     = "";
		
	ИначеЕсли ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "КорректировкаРеализации") Тогда
		
		ПараметрыИнтеграции.ИмяРодительскойГруппыФормы                 = "ГруппаТовары";
		ПараметрыИнтеграции.ИмяСледующейКолонки                        = "ГруппаТоварыЗаголовкиСтрок";
		ПараметрыИнтеграции.РазмещатьЭлементыИнтерфейса                = Истина;
		ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора  = Истина;
		ПараметрыИнтеграции.ИспользоватьБезМаркируемойПродукции        = Истина;
		ПараметрыИнтеграции.ДоступныОбъемноСортовыеКоды                = Истина;
		ПараметрыИнтеграции.БлокироватьТабличнуюЧастьТоварыПриПроверке = Ложь;
		ПараметрыИнтеграции.ИспользоватьСтатусПроверкаЗавершена        = Ложь;
		ПараметрыИнтеграции.ОтображатьОшибкиПроверкиСредствамиККТ      = ИСТИНА;
		ПараметрыИнтеграции.ЕстьПравоИзменение                         = ПравоДоступа("Изменение", Форма.Объект.Ссылка.Метаданные());
		ПараметрыИнтеграции.ИмяТабличнойЧастиСерии                     = "";
		
	ИначеЕсли ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "ВозвратТоваровОтПокупателя") Тогда
		ПараметрыИнтеграции.ИмяРодительскойГруппыФормы                 = "ГруппаТовары";
		ПараметрыИнтеграции.ИмяСледующейКолонки                        = "ТоварыКиЗ_ГИСМ";
		ПараметрыИнтеграции.РазмещатьЭлементыИнтерфейса                = Истина;
		ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора  = Истина;
		ПараметрыИнтеграции.ИспользоватьБезМаркируемойПродукции        = Истина;
		ПараметрыИнтеграции.ДоступныОбъемноСортовыеКоды                = Истина;
		ПараметрыИнтеграции.БлокироватьТабличнуюЧастьТоварыПриПроверке = Ложь;
		ПараметрыИнтеграции.ИспользоватьСтатусПроверкаЗавершена        = Ложь;
		ПараметрыИнтеграции.ОтображатьОшибкиПроверкиСредствамиККТ      = ИСТИНА;
		ПараметрыИнтеграции.ЕстьПравоИзменение                         = ПравоДоступа("Изменение", Форма.Объект.Ссылка.Метаданные());
		ПараметрыИнтеграции.ИмяТабличнойЧастиСерии                     = "";
		
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокументаОбщая"
		Или ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокументаТовары" Тогда
		
		ПараметрыИнтеграции.ИмяРодительскойГруппыФормы                 = "ГруппаТовары";
		//ПараметрыИнтеграции.ИмяЭлементаФормыТовары                     = "ТоварыНоменклатура";
		ПараметрыИнтеграции.ИмяСледующейКолонки                        = "ТоварыКиЗ_ГИСМ";
		ПараметрыИнтеграции.РазмещатьЭлементыИнтерфейса                = ?(ЭтоПростойИнтерфейс, Ложь, Истина);
		ПараметрыИнтеграции.ЕстьПравоИзменение                         = ПравоДоступа("Изменение", Форма.Объект.Ссылка.Метаданные());
		ПараметрыИнтеграции.ЭтоДокументПриобретения                    = Истина;
		ПараметрыИнтеграции.ЕстьЭлектронныйДокумент                    = ЭлектронноеВзаимодействиеИСМП.ДокументСвязанСЭлектронным(Форма.Объект.Ссылка);
		ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора  = ?(ЭтоПростойИнтерфейс, Ложь, Не ПараметрыИнтеграции.ЕстьЭлектронныйДокумент);
		ПараметрыИнтеграции.БлокироватьТабличнуюЧастьТоварыПриПроверке = ПараметрыИнтеграции.ЕстьЭлектронныйДокумент;
		ПараметрыИнтеграции.ИспользоватьСтатусПроверкаЗавершена        = ПараметрыИнтеграции.ЕстьЭлектронныйДокумент;
		ПараметрыИнтеграции.ИмяТабличнойЧастиСерии                     = "";
		
	ИначеЕсли ИнтеграцияИСБПКлиентСервер.ЭтоДокументПоНаименованию(Форма, "ПередачаТоваров") Тогда
		ПараметрыИнтеграции.ИмяРодительскойГруппыФормы                 = "ГруппаТовары";
		ПараметрыИнтеграции.ИмяСледующейКолонки                        = "ТоварыГруппаКоличество";
		ПараметрыИнтеграции.РазмещатьЭлементыИнтерфейса                = ?(ЭтоПростойИнтерфейс, Ложь, Истина);
		ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора  = ?(ЭтоПростойИнтерфейс, Ложь, Истина);
		ПараметрыИнтеграции.ИспользоватьБезМаркируемойПродукции        = Истина;
		ПараметрыИнтеграции.БлокироватьТабличнуюЧастьТоварыПриПроверке = Ложь;
		ПараметрыИнтеграции.ИспользоватьСтатусПроверкаЗавершена        = Ложь;
		//ПараметрыИнтеграции.ДоступныОбъемноСортовыеКоды                = Истина;
		ПараметрыИнтеграции.ОтображатьОшибкиПроверкиСредствамиККТ      = ИСТИНА;
		ПараметрыИнтеграции.ЕстьПравоИзменение                         = ПравоДоступа("Изменение", Форма.Объект.Ссылка.Метаданные());
		ПараметрыИнтеграции.ИмяТабличнойЧастиСерии                     = "";
	КонецЕсли;
		
КонецПроцедуры

// Заполняет специфику применения интеграции формы проверки и подбора в конкретную форму.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для которой применяются параметры интеграции.
//
Процедура ПриПримененииПараметровИнтеграцииФормыПроверкиИПодбора(Форма) Экспорт
	
	МаркировкаПростойИнтерфейс.ИзменитьПоложениеПодменюПроверкаИПодбор(Форма);	
	
КонецПроцедуры

// Получение организации для складских документов "через точку". 
// Параметры:
// ПроверяемыйДокумент - ДокументСсылка
// Организация - ОпределяемыйТип.Организация - возвращаемое значение.
Процедура ПриОпределенииОрганизации(ПроверяемыйДокумент, Организация) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСТСД

// Обрабатывает таблицу штриховых кодов и получает для каждого из них данные в информационной базе.
//   На входе имеется таблица, в которой заполнены только колонки "Номер строки", "Штриховой код" и "Количество", опционально заполнена 
//   колонка "Родитель".
//   В процедуре заполняется допустимый штрихкод упаковки из справочника или признак новой (неизвестной) упаковки.
//
// Параметры:
//  ТаблицаНеАкцизныеМарки - ТаблицаЗначение - имеет следующие колонки:
//   * ШтриховойКод        - Строка                              - штриховой код полученный с ТСД.
//   * Количество          - Число                               - сколько раз был считан данный штрихкод.
//   * ШтрихКодУпаковки    - Справочник.ШтрихкодыУпаковокТоваров - ссылка на имеющуюся в базе упаковку.
//   * Родитель            - Строка                              - штрихкод внешней упаковки.
//   * НомерСтроки         - Число                               - номер строки таблицы
//   * ЭтоУпаковка         - Булево                              - признак новой упаковки
//
Процедура РаспознатьШтрихкоды(ТаблицаНеАкцизныеМарки) Экспорт
	
	Возврат;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Возвращает через третий параметр признак наличия маркируемой продукции.
//
// Параметры:
//  Коллекция                - ДанныеФормыКоллекция - ТЧ с товарами.
//  ВидМаркируемойПродукции  - ПеречислениеСсылка.ВидыПродукцииИС - вид продукции, наличие которой необходимо определить.
//  ЕстьМаркируемаяПродукция - Булево - Исходящий, признак наличия маркируемой продукции.
Процедура ЕстьМаркируемаяПродукцияВКоллекции(Коллекция, ВидМаркируемойПродукции, ЕстьМаркируемаяПродукция) Экспорт
	
	ИнтеграцияИСМПБП.ЕстьМаркируемаяПродукцияВКоллекции(Коллекция, ВидМаркируемойПродукции, ЕстьМаркируемаяПродукция);
	
КонецПроцедуры

// Возвращает через третий параметр таблицу товаров документа, являющихся маркируемой продукцией требуемого вида.
//
// Параметры:
//  * Контекст                 - ФормаКлиентскогоПриложения, ДокументСсылка - документ, маркируемую продукцию которого необходимо получить.
//  * ВидМаркируемойПродукции  - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции, которую необходимо получить.
//  * ТаблицаМаркируемойПродукции - ТаблицаЗначений - Исходящий, таблица с маркируемой продукцией документа. Должна содержать колонки:
//  ** GTIN           - Строка - GTIN продукции
//  ** Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура маркируемой продукции
//  ** Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатура маркируемой продукции
//  ** Серия          - ОпределяемыйТип.СерияНоменклатуры - серия номенклатура маркируемой продукции
//  ** Количество     - Число - количество маркируемой продукции
Процедура ПриОпределенииМаркируемойПродукцииДокумента(Контекст, ВидМаркируемойПродукции, ТаблицаМаркируемойПродукции, Сценарий = Неопределено) Экспорт
	
	ИнтеграцияИСМПБП.ЗаполнитьМаркируемуюПродукциюДокумента(Контекст, ВидМаркируемойПродукции, ТаблицаМаркируемойПродукции);
	
КонецПроцедуры

// Предназначена для реализации функциональности по отражению результатов проверки и подбора в документе, из которого была вызвана соотвествующая форма.
// 
// Параметры:
//  ПараметрыОкончанияПроверки - (См. ПроверкаИПодборПродукцииИСМП.ПараметрыОкончанияПроверки).
Процедура ОтразитьРезультатыСканированияВДокументе(ПараметрыОкончанияПроверки) Экспорт
	
	ИнтеграцияМОТПБП.ОтразитьРезультатыСканированияВДокументе(ПараметрыОкончанияПроверки);
	
КонецПроцедуры

// Получает сформированный ранее Акт о расхождениях для переданного документа.
// 
// Параметры:
//  Документ         - ДокументСсылка - ссылка на документ, для которого необходимо получить Акт о расхождениях:
//  АктОРасхождениях - ДокументСсылка - ссылка на Акт о расхождениях:
Процедура ПолучитьСформированныйАктОРасхождениях(Документ, АктОРасхождениях) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Документ);
		Запрос.УстановитьПараметр("ВидОперацииАкта", Перечисления.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктОРасхождениях.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.АктОРасхождениях КАК АктОРасхождениях
		|ГДЕ
		|	АктОРасхождениях.ДокументПоступления = &Ссылка
		|	И АктОРасхождениях.ВидОперации = &ВидОперацииАкта
		|	И НЕ АктОРасхождениях.ПометкаУдаления";
		
		ВыборкаЗапроса = запрос.Выполнить().Выбрать();
		Если ВыборкаЗапроса.Следующий() Тогда
			АктОРасхождениях = ВыборкаЗапроса.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
