
#Область ПрограммныйИнтерфейс

#Область АктуализацияДанных

// Возвращает свойства выполняемого фонового задания по актуализации.
// Если ни одного задания не найдено, возвращается значение Неопределено.
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - организация, по которой будет производиться поиск фонового задания по актуализации.
//  УникальныйИдентификаторФормы - Строка - Уникальный идентификатор формы, из которой вызывается поиск фонового задания.
//  ИспользуетсяСтараяВерсияФоновыхЗаданийБСП - Булево - признак использования устаревшего механизма длительных операций.
//  ИскатьРегламентное - Булево - признак необходимости искать фоновое задание запущенное регламентным заданием закрытия месяца.
//
// Возвращаемое значение:
//   Структура - описание фонового задания:
//              * УникальныйИдентификатор - Неопределено - фоновых заданий актуализации по указанной организации сейчас не выполняется.
//                                        - Строка - если задание выполняется, но свойства получить не удалось, возвращается пустая строка.
//                                                 - если задание выполняется, то идентификатор задания.
//              * Ключ         - Строка - ключ фонового задания.
//              * Наименование - Строка - наименование фонового задания.
//              * Организация  - СправочникСсылка.Организация - организация, по которой выполняется задание.
//              * Пользователь - СправочникСсылка.Пользователи - пользователь, запустивший задание.
//
Функция НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Знач Организация,
													 Знач УникальныйИдентификаторФормы,
													 ИспользуетсяСтараяВерсияФоновыхЗаданийБСП = Истина,
													 ИскатьРегламентное = Истина) Экспорт

	Если ИспользуетсяСтараяВерсияФоновыхЗаданийБСП Тогда
		Возврат НайтиФоновоеЗаданиеАктуализацииПоОрганизации_ИдентификаторЗадания(Организация, УникальныйИдентификаторФормы);
	КонецЕсли;
	
	ПользовательСеанса = Пользователи.ТекущийПользователь();
	ВыполняетсяРегламентноеЗадание = Ложь;
	
	СвойстваВыполняемогоЗадания = Новый Структура;
	СвойстваВыполняемогоЗадания.Вставить("УникальныйИдентификатор");
	СвойстваВыполняемогоЗадания.Вставить("Ключ",         "");
	СвойстваВыполняемогоЗадания.Вставить("Наименование", "");
	СвойстваВыполняемогоЗадания.Вставить("Организация",  Справочники.Организации.ПустаяСсылка());
	СвойстваВыполняемогоЗадания.Вставить("Пользователь", ПользовательСеанса);
	
	МассивПрефиксов = Новый Массив;
	МассивПрефиксов.Добавить(ЗакрытиеМесяцаКлиентСервер.ПрефиксКлючаЗаданияАктуализации());
	
	ОтборПоСостоянию = Новый Структура("Состояние", СостояниеФоновогоЗадания.Активно);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборПоСостоянию);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ИскатьРегламентное Тогда
		// Проверим факт выполнения регламентного задания.
		СостояниеВыполнения = РегистрыСведений.РезультатыЗакрытияМесяца.СостояниеВыполненияЗаданияЗакрытияМесяца(Организация);
		ВыполняетсяРегламентноеЗадание = СостояниеВыполнения.Выполняется;
		Если ВыполняетсяРегламентноеЗадание Тогда
			МассивПрефиксов.Добавить(ЗакрытиеМесяцаКлиентСервер.ПрефиксКлючаРегламентногоЗаданияЗакрытияМесяца());
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ФоновоеЗадание Из МассивФоновыхЗаданий Цикл
		
		// Ключ должен состоять из трех частей, разделенных двоеточием: имя фоновых заданий-актуализаций, GUID организации, GUID пользователя.
		Если ИскатьРегламентное И ВыполняетсяРегламентноеЗадание Тогда
			ПараметрыПолученияКлюча = Новый Структура("Организация", Организация);
			КлючФонового = ЗакрытиеМесяцаКлиентСервер.КлючЗаданияАктуализации(ПараметрыПолученияКлюча, , Истина);
			Ключ = СтрРазделить(КлючФонового, ":", Истина);
		Иначе
			Ключ = СтрРазделить(ФоновоеЗадание.Ключ, ":", Истина);
		КонецЕсли;
		
 		Если Ключ.Количество() < 2
		 Или МассивПрефиксов.Найти(Ключ[0]) = Неопределено Тогда
			// Это фоновое задание не выполняет актуализацию.
			Продолжить;
		КонецЕсли;
		
		УникальныйИдентификаторОрганизации = Ключ[1];
		Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(УникальныйИдентификаторОрганизации) Тогда
			СвойстваВыполняемогоЗадания.Организация = Справочники.Организации.ПолучитьСсылку(
				Новый УникальныйИдентификатор(УникальныйИдентификаторОрганизации));
		КонецЕсли;
		Если СвойстваВыполняемогоЗадания.Организация <> Организация Тогда
			// Это фоновое задание по другой организации.
			Продолжить;
		КонецЕсли;
		
		УникальныйИдентификаторПользователя = ?(Ключ.Количество() > 2, Ключ[2], "");
		Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(УникальныйИдентификаторПользователя) Тогда
			СвойстваВыполняемогоЗадания.Пользователь = Справочники.Пользователи.ПолучитьСсылку(
				Новый УникальныйИдентификатор(УникальныйИдентификаторПользователя));
		КонецЕсли;
		
		СвойстваВыполняемогоЗадания.УникальныйИдентификатор = ФоновоеЗадание.УникальныйИдентификатор;
		СвойстваВыполняемогоЗадания.Ключ                    = ФоновоеЗадание.Ключ;
		СвойстваВыполняемогоЗадания.Наименование            = ФоновоеЗадание.Наименование;
		Если СвойстваВыполняемогоЗадания.Пользователь <> ПользовательСеанса Тогда
			// Возможно среди фоновых заданий актуализации есть и запущенное текущим пользователем, и запущенное другим.
			// Тогда ищем, пока не найдем задание другого пользователя.
			Прервать;
		КонецЕсли;
			
	КонецЦикла;
	
	Если СвойстваВыполняемогоЗадания.УникальныйИдентификатор <> Неопределено
	 Или Не ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Возврат СвойстваВыполняемогоЗадания;
	КонецЕсли;
	
	// Если не найдено ни одного задания в файловом режиме работы, это еще не значит, что никто не актуализирует данные.
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(Организация, , УникальныйИдентификаторФормы);
		РазблокироватьДанныеДляРедактирования(Организация, УникальныйИдентификаторФормы);
		// Организация доступна для актуализации.
		
	Исключение
		
		СвойстваВыполняемогоЗадания.УникальныйИдентификатор = "";
		// Работа с данной организацией недоступна, но конкретный идентификатор фонового задания мы не знаем.
		
	КонецПопытки;
	
	Возврат СвойстваВыполняемогоЗадания;
	
КонецФункции

// Запускает фоновое задание проверки актуальности.
//
// Параметры:
//  ПараметрыЗадания - Структура - см. ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности()
//
// Возвращаемое значение:
//   Структура   - см. ДлительныеОперации.ВыполнитьФункцию()
//
Функция ПроверитьАктуальность(Знач ПараметрыЗадания) Экспорт
	
	ПодготовитьДанныеРасшифровкиДляФоновогоЗадания(ПараметрыЗадания);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ПараметрыЗадания.УникальныйИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка актуальности данных'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ОписаниеДлительнойОперации = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "Обработки.ЗакрытиеМесяца.ПроверитьАктуальность", ПараметрыЗадания);

	Возврат ОписаниеДлительнойОперации;
	
КонецФункции

// Перепроводит документы и перевыполняет регламетные операции закрытия месяца до требуемого периода.
//
// Параметры:
//  ПараметрыЗадания - Структура - см. ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации().
//  ВФоне            - Булево    - Признак, выполнять расчет в фоновом задании или непосредственно.
//                                 По умолчанию = Истина.
// Возвращаемое значение:
//   Структура   - для синхронных (без фонового задания) см. ЗакрытиеМесяцаКлиентСервер.НовыйРезультатАктуализации();
//               - для асинхронных (с запуском фонового задания) см. ДлительныеОперации.ВыполнитьВФоне()
//
Функция АктуализироватьДанные(Знач ПараметрыЗадания, ВФоне = Истина) Экспорт
	
	РезультатВыполнения = Новый Структура;
	РезультатВыполнения.Вставить("АдресРезультата",    "");
	РезультатВыполнения.Вставить("ЗаданиеВыполнено",   Истина);
	РезультатВыполнения.Вставить("ИдентификаторЗадания");
	РезультатВыполнения.Вставить("Организация", ПараметрыЗадания.Организация);
	
	АктуализацияДляРасчетаНалога = ПараметрыЗадания.АктуализацияДляРасчетаНалога;
	
	Если АктуализацияДляРасчетаНалога Тогда // востребована частичная актуализация
		
		АктуализацияДляРасчетаНалога = ВозможенПрямойРасчетНалога(ПараметрыЗадания);
		Если Не АктуализацияДляРасчетаНалога Тогда
			// Только сообщаем вариант актуализации (АктуализацияДляРасчетаНалога = Ложь), не выполняя её.
			РезультатАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйРезультатАктуализации();
			РезультатВыполнения.АдресРезультата = ПоместитьВоВременноеХранилище(
				РезультатАктуализации, ПараметрыЗадания.УникальныйИдентификаторФормы);
			Возврат РезультатВыполнения;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ВФоне Тогда
		// Сразу же выполняем актуализацию и возвращаем результат.
		АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ПараметрыЗадания.УникальныйИдентификаторФормы);
		
		Если АктуализацияДляРасчетаНалога Тогда
			Обработки.ЗакрытиеМесяца.АктуализироватьВФонеДляРасчетаНалога(ПараметрыЗадания, АдресРезультата);
		Иначе
			Обработки.ЗакрытиеМесяца.АктуализироватьВФоне(ПараметрыЗадания, АдресРезультата);
		КонецЕсли;
		
		РезультатВыполнения.АдресРезультата = АдресРезультата;
		Возврат РезультатВыполнения;
		
	КонецЕсли;
	
	ТекПользователь = Пользователи.ТекущийПользователь();
		
	НаименованиеЗадания = НСтр("ru='Актуализация данных: %1 (%2)'");
	НаименованиеЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НаименованиеЗадания, ПараметрыЗадания.Организация, ТекПользователь);

	КлючЗадания = ЗакрытиеМесяцаКлиентСервер.КлючЗаданияАктуализации(ПараметрыЗадания, ТекПользователь);
	
	ПодготовитьДанныеРасшифровкиДляФоновогоЗадания(ПараметрыЗадания);

	ИмяЭкспортнойПроцедуры = ?(АктуализацияДляРасчетаНалога,
		"Обработки.ЗакрытиеМесяца.АктуализироватьВФонеДляРасчетаНалога", "Обработки.ЗакрытиеМесяца.АктуализироватьВФоне");

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ПараметрыЗадания.УникальныйИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.КлючФоновогоЗадания = КлючЗадания;
	// Если на файловой базе уже запущено любое другое фоновое задание, то новое встанет в очередь, а не будет запущено в основном потоке.
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(ИмяЭкспортнойПроцедуры, ПараметрыЗадания, ПараметрыВыполнения);
	
	// Для плавной остановки фонового задания, при закрытии формы-владельца длительной операции.
	РезультатВыполнения.Вставить("Организация", ПараметрыЗадания.Организация);
	
	// Для совместимости со старым программным интерфейсом. См. ЗакрытиеМесяцаКлиентСервер.ИспользуетсяОписаниеЗаданияАктуализации()
	РезультатВыполнения.Вставить("ЗаданиеВыполнено", РезультатВыполнения.Статус <> "Выполняется");
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Выполняет актуализацию данных перед публикацией отчетов и помощников через внешний программный интерфейс.
// Актуализация выполняется по "быстрому" алгоритму - проводятся только регламентные операции, влияющие на расчет налогов.
// В случае невозможности быстрой актуализации возвращается сообщение об ошибке.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, по которой данные должны стать актуальными.
//  Период      - Дата - на какую дату данные должны стать актуальными.
//
Функция АктуализироватьДанныеПередПубликацией(Организация, Период, СообщениеОбОшибке) Экспорт
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации();
	ПараметрыАктуализации.Организация = Организация;
	ПараметрыАктуализации.Период      = Период;
	
	// Актуализация при публикации данных всегда выполняется по быстрому алгоритму.
	ПараметрыАктуализации.АктуализацияДляРасчетаНалога = Истина;
	
	Если НЕ ВозможенПрямойРасчетНалога(ПараметрыАктуализации) Тогда
		СообщениеОбОшибке = НСтр("ru = 'Данные неактуальны. Требуется выполнить операции закрытия месяца в приложении 1С.'");
		Возврат Ложь;
	КонецЕсли;
	
	АдресХранилища = "";
	Обработки.ЗакрытиеМесяца.АктуализироватьВФонеДляРасчетаНалога(ПараметрыАктуализации, АдресХранилища);
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если Не РезультатВыполнения.Выполнено ИЛИ РезультатВыполнения.ВывестиИнформациюУведомлений Тогда
		
		ТекстОшибки = НСтр("ru = 'Данные неактуальны. При выполнении актуализации возникли ошибки.'");
		Если РезультатВыполнения.Свойство("ВидОперации") И РезультатВыполнения.Свойство("ПериодРегистрации") Тогда
			
			ТекстШаблона = НСтр("ru = '""%1"" за %2'");
			ТекстОшибки = ТекстОшибки + Символы.ПС
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстШаблона, РезультатВыполнения.ВидОперации, Формат(РезультатВыполнения.ПериодРегистрации, "ДФ='ММММ гггг'"));
			
		КонецЕсли;
		
		Если РезультатВыполнения.Свойство("РегламентнаяОперацияСОшибками") Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + Строка(РезультатВыполнения.РегламентнаяОперацияСОшибками);
		КонецЕсли;
		
		Если РезультатВыполнения.Свойство("СообщенияПользователю") Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + СтрСоединить(РезультатВыполнения.СообщенияПользователю, Символы.ПС);
		КонецЕсли;
		
		СообщениеОбОшибке = ТекстОшибки;
		
	КонецЕсли;
	
	Если РезультатВыполнения.ВывестиИнформациюУведомлений Тогда
		
		ИмяСобытия = ЗакрытиеМесяцаКлиентСервер.СобытиеЖурналаРегистрации(
			НСтр("ru = 'Ошибка актуализации данных для публикации'", ОбщегоНазначения.КодОсновногоЯзыка())); // Строка записывается в журнал
		ЗаписьЖурналаРегистрации(ИмяСобытия,
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.Обработки.ЗакрытиеМесяца, ,
			ТекстОшибки,
			РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
		
	КонецЕсли;
	
	Возврат РезультатВыполнения.Выполнено;
	
КонецФункции

// Возвращает отчет о результате последнего выполнения закрытия месяца.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - ссылка на организацию, для которой требуется получение отчета.
// 
// Возвращаемое значение:
//  ТабличныйДокумент - отчет о выполнении закрытия месяца
//
Функция РезультатПоследнегоЗакрытияМесяца(Организация) Экспорт
	
	Возврат РегистрыСведений.РезультатыЗакрытияМесяца.РезультатПоследнегоЗакрытияМесяца(Организация);
	
КонецФункции

#КонецОбласти

#Область РаботаСНастройками

Функция ПроверятьАктуальность() Экспорт
	
	Возврат Константы.ПроверятьАктуальностьДанныхУчета.Получить()
		И ПравоДоступа("Изменение", Метаданные.Документы.РегламентнаяОперация);
	
КонецФункции

// Определяет, можно ли рассчитать налог, не закрывая месяц.
//
// Параметры:
//  ПараметрыЗадания - Структура - см. ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации().
//                                Используются только поля Организация и Период. 
//
Функция ВозможенПрямойРасчетНалога(ПараметрыЗадания)
	
	КорректныйПериод = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
	Если КонецКвартала(ПараметрыЗадания.Период) > КорректныйПериод.КонецКорректногоПериода Тогда
		// ошибочно широкий интервал
		Возврат Ложь;
	КонецЕсли;
	
	ВариантУсловийПрямогоРасчета = 0;
	Если УчетнаяПолитика.ТолькоОсобыйПорядокНалогообложения(ПараметрыЗадания.Организация, ПараметрыЗадания.Период) Тогда
		ВариантУсловийПрямогоРасчета = 1; // ИП на особой системе налогообложения
	ИначеЕсли УчетнаяПолитика.ПрименяетсяУСНДоходы(ПараметрыЗадания.Организация, ПараметрыЗадания.Период) Тогда
		ВариантУсловийПрямогоРасчета = 2; // ИП применяет УСН (доходы)
	КонецЕсли;
	Если ВариантУсловийПрямогоРасчета = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// ИП на особой системе налогообложения (ЕНВД или патент)
	Если ВариантУсловийПрямогоРасчета = 1 Тогда
		
		ГраницаАктуализации = НачалоНеактуальностиДляПрямогоРасчетаНалога(ПараметрыЗадания.Организация, ПараметрыЗадания.Период);
		
		Если ГраницаАктуализации < КорректныйПериод.НачалоКорректногоПериода Тогда
			// ошибочно широкий интервал
			Возврат Ложь;
		КонецЕсли;
		
		// Проверим, что за период неактуальности не было смены учётной политики.
		ИмяРесурса = "ПрименяетсяОсобыйПорядокНалогообложения";
		ВариантыУчета = НастройкиУчета.ЗначенияРесурсаУчетнойПолитикиЗаПериод(
			"НастройкиСистемыНалогообложения",
			ИмяРесурса,
			ПараметрыЗадания.Организация,
			ГраницаАктуализации,
			ПараметрыЗадания.Период);
		Если ВариантыУчета.Количество() <> 1             // были изменения в учетной политике
		   И (ВариантыУчета.Найти(Ложь) <> Неопределено  // был период без особой системы налогообложения
			// УСН + особая система налогообложения
			Или УчетнаяПолитика.ПрименяетсяУСНЗаПериод(ПараметрыЗадания.Организация, ГраницаАктуализации, ПараметрыЗадания.Период)) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ПараметрыЗадания.ГраницаАктуализацииНачало = ГраницаАктуализации;
		
		Возврат Истина;
		
	ИначеЕсли ВариантУсловийПрямогоРасчета = 2 Тогда // УСН (доходы)
		
		ГраницаАктуализации = НачалоНеактуальностиДляПрямогоРасчетаНалога(ПараметрыЗадания.Организация, ПараметрыЗадания.Период);
		
		Если ГраницаАктуализации < КорректныйПериод.НачалоКорректногоПериода Тогда
			// ошибочно широкий интервал
			Возврат Ложь;
		КонецЕсли;
		
		// Проверим, что за период неактуальности всегда использовалась УСН.
		ВариантыУчета = НастройкиУчета.ЗначенияРесурсаУчетнойПолитикиЗаПериод(
			"НастройкиСистемыНалогообложения",
			"ПрименяетсяУСНДоходы",
			ПараметрыЗадания.Организация,
			ГраницаАктуализации,
			ПараметрыЗадания.Период);
		Если ВариантыУчета.Количество() <> 1                 // были изменения в учетной политике
		   И ВариантыУчета.Найти(Ложь) <> Неопределено Тогда // был период без УСН доходы
			Возврат Ложь;
		КонецЕсли;
		
		ПрямойРасчетНалога = УчетУСН.ДоходыЗаПериодНеЗависятОтПоследовательности(
			ПараметрыЗадания.Организация, ГраницаАктуализации, КонецМесяца(ПараметрыЗадания.Период));
		
		Если ПрямойРасчетНалога Тогда
			ПараметрыЗадания.ГраницаАктуализацииНачало = ГраницаАктуализации;
		КонецЕсли;
		
		Возврат ПрямойРасчетНалога;
				
	КонецЕсли;
	
КонецФункции

// Возвращает период, за который будет произведено автоматическое закрытие месяца на текущую дату.
//
// Параметры:
//  ПараметрыЗакрытия - Структура:
//		* Организация - СправочникСсылка.Организации - ссылка на организацию для которой будет выполняться закрытие месяца.
//		* ВыполнятьВОтчетномПериоде - Булево - закрытие выполняется в отчетном периоде, или за количество месяцев назад.
//		* РазрешенноеЧислоМесяцевНазад - Число - количество месяцев (назад), за которые можно проводить закрытие.
//  	* ДатаАктуальности - Дата - в какой дате находится граница актуальности данных.
//  ЭтоРегламентное   - Булево - если истина, значит функция используется для формирования отчета, о причинах остановки регламентного задания.
// 
// Возвращаемое значение:
//  Структура:
//		* ДатаНачала - Дата - дата с которой нужно выполнять закрытие месяца.
//		* ДатаОкончания - Дата - дата до которой нужно выполнять закрытие месяца.
//		* ЕстьОграничения - Булево - дата актуальности за пределами ограничений, указанных в настройках.
//		* УстановленаДатыЗапрета - Булево - установлена дата запрета.
//		* Сообщение - Строка - информационное сообщение о том какие есть ограничения.
//            
Функция ПериодАвтоматическогоЗакрытияМесяца(ПараметрыЗакрытия, ЭтоРегламентное = Ложь) Экспорт
	
	Если ПараметрыЗакрытия.ТолькоПерепроведениеДокументов Тогда
		КонецТекущегоМесяца = КонецМесяца(ТекущаяДатаСеанса());
		МоментНарушения = РаботаСПоследовательностями.МоментНачалаПерепроведения(
			ПараметрыЗакрытия.Организация, КонецТекущегоМесяца);
		ДатаАктуальности = ?(МоментНарушения = Неопределено, КонецТекущегоМесяца, МоментНарушения.Дата);
	Иначе
		ДатаАктуальности = ПараметрыЗакрытия.ДатаАктуальности;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаНачала",      '00010101');
	Результат.Вставить("ДатаОкончания",   '00010101');
	Результат.Вставить("ДатаАктуальности", ДатаАктуальности);
	Результат.Вставить("ЕстьОграничения", Ложь);
	Результат.Вставить("УстановленаДатыЗапрета", Ложь);
	Результат.Вставить("Сообщение",       "");
	
	// Определим период, за который должна выполняться актуализация регламентным заданием.
	ДатаСервера         = ОбщегоНазначенияБПВызовСервера.ТекущаяДатаНаСервере();
	НачалоЭтогоМесяца   = НачалоМесяца(ДатаСервера);
	НачалаЭтогоКвартала = НачалоКвартала(ДатаСервера);
	
	Если ПараметрыЗакрытия.ВыполнятьВОтчетномПериоде Тогда
		Если НачалоЭтогоМесяца = НачалаЭтогоКвартала И День(ДатаСервера) <= 25 Тогда
			Результат.ДатаНачала = НачалоКвартала(ДобавитьМесяц(ДатаСервера, -3));
		Иначе
			Результат.ДатаНачала = НачалаЭтогоКвартала;
		КонецЕсли;
		Результат.ДатаНачала = Макс(НачалоМесяца(ДатаАктуальности), Результат.ДатаНачала);
	Иначе
		ЧислоМесяцев = ПараметрыЗакрытия.РазрешенноеЧислоМесяцевНазад;
		РазрешеноЗакрыватьМесяц = ДобавитьМесяц(НачалоМесяца(ДатаСервера), -ЧислоМесяцев);
		Результат.ДатаНачала = Макс(НачалоМесяца(ДатаАктуальности), РазрешеноЗакрыватьМесяц);
	КонецЕсли;
	
	Результат.ДатаОкончания = КонецМесяца(ДатаСервера);
	
	// Строковое представление всего периода за который требуется актуализация
	ШаблонПредставления = НСтр("ru = '%1 г.'");
	НачалоАктуализации  = НРег(СтрШаблон(ШаблонПредставления,
		ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(ДатаАктуальности, 2)));
	НачалоРегламентного = НРег(СтрШаблон(ШаблонПредставления,
		ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Результат.ДатаНачала, 2)));
	
	Если ДатыЗапретаИзмененияБП.ЗапрещеноРедактированиеПериода(ДатаАктуальности, ПараметрыЗакрытия.Организация) Тогда
		// Установлены даты запрета редактирования, для даты с которой требуется начать актуализацию.
		ШаблонСообщения = НСтр("ru ='требуется закрытие месяца начиная с %1, для периода установлена дата запрета редактирования.'");
		Результат.УстановленаДатыЗапрета = Истина;
		Результат.Сообщение = СтрШаблон(ШаблонСообщения, НачалоАктуализации);
	ИначеЕсли Результат.ДатаНачала > ДатаАктуальности Тогда
		// Дата актуальности находится за пределами ограничений периода в настройках.
		ИнформацияОНастройках = "";
		ПутьДоНастроек = "";
		Если ЭтоРегламентное Тогда
			Если ОбщегоНазначенияБП.ЭтоПростойИнтерфейс() Тогда
				ШаблонСообщения = НСтр(
					"ru ='требуется закрытие месяца начиная с %1, настройками автоматического закрытия месяца изменения разрешены только с %2
					|Выполните закрытие месяца вручную, либо измените настройки (Настройки - Система - Проведение документов - Автоматическое закрытие месяца).'");
			Иначе
				ШаблонСообщения = НСтр(
					"ru ='требуется закрытие месяца начиная с %1, настройками автоматического закрытия месяца изменения разрешены только с %2
					|Выполните закрытие месяца вручную, либо измените настройки (Администрирование - Настройки программы - Проведение документов - Автоматическое закрытие месяца).'");
			КонецЕсли;
		Иначе
			ШаблонСообщения = НСтр(
				"ru ='требуется закрытие месяца начиная с %1, изменения разрешены только с %2 Выполните закрытие месяца вручную, либо измените настройки.'");
		КонецЕсли;
		Результат.ЕстьОграничения = Истина;
		Результат.Сообщение = СтрШаблон(ШаблонСообщения, НачалоАктуализации, НачалоРегламентного);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает расчетную дату ближайшего запуска регламентного задания
//
// Параметры:
//  ПараметрыРасчета - Структура:
//		* Ежедневно             - Булево - если запуск выполняется не ежедневно, то значит в отдельные дни.
//		* ВремяЗапуска          - Дата - время запуска регламентного задания.
//		* ДеньПервогоЗапуска    - Число - день месяца, когда задание будет запускается впервые.
//		* ДеньПоследнегоЗапуска - Число - день месяца, когда задание будет запускаться в последний раз.
// 
// Возвращаемое значение:
//  Дата - ближайший момент времени, когда должен быть произведен запуск регламентного задания
//
Функция БлижайшаяДатаЗапускаРегламентногоЗадания(ПараметрыРасчета, Знач ДатаОтсчета = Неопределено) Экспорт
	
	МоментЗапуска = '00010101';
	Если ДатаОтсчета = Неопределено Тогда
		ДатаОтсчета = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	КонецЕсли;
	
	Если ПараметрыРасчета.Ежедневно Тогда
		МоментЗапуска = МоментЗапускаРегламентногоЗадания(ДатаОтсчета, ПараметрыРасчета.ВремяЗапуска);
		Если ДатаОтсчета > МоментЗапуска Тогда
			Завтра = КонецДня(ДатаОтсчета) + 1;
			МоментЗапуска = МоментЗапускаРегламентногоЗадания(Завтра, ПараметрыРасчета.ВремяЗапуска);
		КонецЕсли;
	Иначе // В отдельные дни
		// Поищем подходящий день запуска в этом месяце
		Сутки = 60 * 60 * 24;
		ПервыйДеньМесяца = НачалоМесяца(ДатаОтсчета);
		Для ЧислоДней = ПараметрыРасчета.ДеньПервогоЗапуска По ПараметрыРасчета.ДеньПоследнегоЗапуска Цикл
			ДатаЗапуска = ПервыйДеньМесяца + (ЧислоДней - 1) * Сутки;
			МоментЗапуска = МоментЗапускаРегламентногоЗадания(ДатаЗапуска, ПараметрыРасчета.ВремяЗапуска);
			Если ДатаОтсчета <= МоментЗапуска Тогда // Нашли
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		// Если в этом месяце не нашлось подходящего дня для запуска,
		// значит это первый день из настроек, но уже в следующем месяце 
		Если ЧислоДней > ПараметрыРасчета.ДеньПоследнегоЗапуска И ДатаОтсчета > МоментЗапуска Тогда
			НачалоСледующегоМесяца = НачалоМесяца(ДобавитьМесяц(ДатаОтсчета, 1));
			ДатаСледующегоЗапуска = НачалоСледующегоМесяца + Сутки * (ПараметрыРасчета.ДеньПервогоЗапуска - 1);
			МоментЗапуска = МоментЗапускаРегламентногоЗадания(ДатаСледующегоЗапуска, ПараметрыРасчета.ВремяЗапуска);
		КонецЕсли;
	КонецЕсли;
	
	Возврат МоментЗапуска;
	
КонецФункции

#КонецОбласти

#Область РаботаСФоновымиЗаданиями

Функция ЗаданиеВыполнено(Знач ИдентификаторЗадания) Экспорт
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

// Проверяет состояние фонового задания по переданному идентификатору.
// При аварийном завершении задания вызывает исключение, возникшее в нем,
// либо исключение общего вида "Не удалось выполнить данную операцию. Подробности см. в Журнале регистрации".
// Не аварийным считается Выполняется, Выполнено или Отменено.
//
// Параметры:
//  ИдентификаторЗадания - УникальныйИдентификатор - идентификатор фонового задания. 
//
// Возвращаемое значение:
//  Структура - структура со свойствами:
//		* Статус - Строка - статус выполнения фонового задания.
//		* ТекстОшибки  - содержит текст ошибки, если фоновое задание было завершено с ошибками.
// 
Функция СтатусФоновогоЗадания(Знач ИдентификаторЗадания) Экспорт
	
	Результат = Новый Структура("Статус, ТекстОшибки", "", "");
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатВыполнения = ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания, Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат.Статус = РезультатВыполнения.Статус;
	
	Если РезультатВыполнения.Статус = "Ошибка" И РезультатВыполнения.Задание <> Неопределено
	 И РезультатВыполнения.Задание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
		// Программный интерфейс длительных операций, при отмене задания возвращает статус "Ошибка" вместо "Отменено"
		Результат.Статус = "Отменено";
	ИначеЕсли РезультатВыполнения.Задание = Неопределено Тогда
		Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка выполнения фонового задания
					   |
					   |%1
			           |
			           |Технические подробности:
			           |%2
			           |
			           |См. также журнал регистрации.'"),
			РезультатВыполнения.КраткоеПредставлениеОшибки,
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
	ИначеЕсли РезультатВыполнения.Статус = "Ошибка" Тогда 
		Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка выполнения фонового задания
					   |
					   |%1
			           |
			           |Технические подробности:
			           |Ошибка выполнения фонового задания %2 с идентификатором %3 по причине
			           |%4
			           |
			           |См. также журнал регистрации.'"),
			РезультатВыполнения.КраткоеПредставлениеОшибки,
			РезультатВыполнения.Задание.ИмяМетода,
			Строка(ИдентификаторЗадания),
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Процедура ОтменитьВыполнениеЗадания(Знач ИдентификаторЗадания) Экспорт
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

// Читает сообщение от длительной операции о ее текущем состоянии.
//
// Параметры:
//  ОписаниеЗадания - Структура - см. параметр ДлительнаяОперация в ДлительныеОперацииКлиент.ПараметрыОжидания() ;
//                    Строка - Устарел. Для совместимости. Идентификатор задания.
//
// Возвращаемое значение:
//   Структура   - описывает информацию, полученную из фонового задания:
//                * ПрогрессОбновлен - Булево - Истина, если было прочтено значение прогресса; иначе Ложь.
//                * ЗаданиеВыполнено - Булево - Ложь, если существует фоновое задание с ИдентификаторЗадания.
//                * Процент - Число - все проценты выше 99 считаются "99%".
//                * Иные ключи структуры см. ДлительныеОперации.СообщитьПрогресс()
//
Функция ПрочитатьПрогресс(Знач ОписаниеЗадания) Экспорт
	
	ПрогрессЗадания = Новый Структура;
	ПрогрессЗадания.Вставить("ПрогрессОбновлен", Ложь);
	ПрогрессЗадания.Вставить("ЗаданиеВыполнено", Истина);
	ПрогрессЗадания.Вставить("Процент",          99);
	
	Если ТипЗнч(ОписаниеЗадания) = Тип("Строка")
	 Или ТипЗнч(ОписаниеЗадания) = Тип("УникальныйИдентификатор") Тогда
		
		УстановитьПривилегированныйРежим(Истина);
	
		// Различаем случаи, когда отсутствуют сообщения и когда завершено задание.
		Попытка
			ПрогрессЗадания.ЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ОписаниеЗадания);
		Исключение
			// Исключение при попытке проверить статус задания означает, что фоновое задание было удалено после отмены операции
			// закрытия месяца пользователем.
		КонецПопытки;
		
		Если ПрогрессЗадания.ЗаданиеВыполнено Тогда
			
			ПрогрессЗадания.ПрогрессОбновлен = Истина;			
			
		Иначе
			
			ПрочтенныйПрогрессЗадания = ДлительныеОперации.ПрочитатьПрогресс(ОписаниеЗадания);
			ПрогрессЗадания.ПрогрессОбновлен = (ПрочтенныйПрогрессЗадания <> Неопределено);
			
		КонецЕсли;
		
	Иначе
		
		Если ОписаниеЗадания.Статус = "Выполняется" Тогда
			
			ПрогрессЗадания.ЗаданиеВыполнено = Ложь;
			ПрочтенныйПрогрессЗадания = ОписаниеЗадания.Прогресс;
			ПрогрессЗадания.ПрогрессОбновлен = (ПрочтенныйПрогрессЗадания <> Неопределено);
			
		Иначе
			
			ПрогрессЗадания.ПрогрессОбновлен = Истина;
			
		КонецЕсли;

	КонецЕсли;
	
	Если ПрогрессЗадания.ПрогрессОбновлен И Не ПрогрессЗадания.ЗаданиеВыполнено Тогда
	
		Для Каждого КлючПрогресса Из ПрочтенныйПрогрессЗадания Цикл
			ПрогрессЗадания.Вставить(КлючПрогресса.Ключ, КлючПрогресса.Значение);
		КонецЦикла;	
		
	КонецЕсли;
	
	Возврат ПрогрессЗадания;
	
КонецФункции

#КонецОбласти

#Область СервисныеФункции

Функция ПолучитьПредупреждающийЦвет() Экспорт
	
	Возврат ЦветаСтиля.ЦветФонаНекорректногоКонтрагента;
	
КонецФункции

Функция ПоказатьПредупреждениеАктуализацияДанных(Организация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат НЕ Константы.ПроверятьАктуальностьДанныхУчета.Получить()
		И Обработки.ЗакрытиеМесяца.ПравоИзмененияРегламентныхОпераций(Организация);
	
КонецФункции

Процедура ПодготовитьДанныеРасшифровкиДляФоновогоЗадания(ПараметрыЗадания)

	Если Не ПараметрыЗадания.Свойство("АдресХранилищаДанныеРасшифровки")
	 Или Не ЭтоАдресВременногоХранилища(ПараметрыЗадания.АдресХранилищаДанныеРасшифровки) Тогда
		Возврат;
	КонецЕсли;
		
	// В фоновое задание передаем не адрес временного хранилища, а само значение,
	// т.к. внутри фонового задания недоступны данные временного хранилища родительского сеанса.
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(ПараметрыЗадания.АдресХранилищаДанныеРасшифровки);
	СжатыеДанныеРасшифровки = Новый ХранилищеЗначения(ДанныеРасшифровки, Новый СжатиеДанных(9));
	ПараметрыЗадания.Вставить("ДанныеРасшифровки", СжатыеДанныеРасшифровки);
	
	ПараметрыЗадания.Удалить("АдресХранилищаДанныеРасшифровки");
	
КонецПроцедуры

Функция НачалоНеактуальностиДляПрямогоРасчетаНалога(Организация, Период)
	
	// Определяем дату, до которой нам важно отсутствие взаимозависимостей учета.
	ГраницаАктуализации = КонецМесяца(Период);
	
	ПараметрыПроверки = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности();
	ПараметрыПроверки.Организация                               = Организация;
	ПараметрыПроверки.Период                                    = ГраницаАктуализации;
	ПараметрыПроверки.ТребуетсяПолнаяАктуализация               = Истина;
	ПараметрыПроверки.АктуализироватьВесьПериод                 = Истина;
	ПараметрыПроверки.АктуализацияДляРасчетаНалога              = Истина;
	РезультатПроверкиАктуальности = Обработки.ЗакрытиеМесяца.ПроверитьАктуальность(ПараметрыПроверки);
	Если Не РезультатПроверкиАктуальности.ТребуетсяАктуализация Тогда
		ГраницаАктуализации = НачалоКвартала(ГраницаАктуализации);
	Иначе
		ГраницаАктуализации = НачалоКвартала(РезультатПроверкиАктуальности.ДатаАктуальности);
	КонецЕсли;
		
	Возврат ГраницаАктуализации;
	
КонецФункции

// Возвращает количество документов, которые требуется перепровести для актуализации данных.
// Параметры:
//  Организация - СправочникСсылка.Организации - организация по которой необходимо получить количество документов.
//  Период - Дата актуализации.
//
// Возвращаемое значение:
//   Число - количество документов для перепроведения.
//
Функция КоличествоДокументовДляПерепроведения(Организация, Период) Экспорт
	
	КоличествоДокументовДляПерепроведения = 0;
	Если Не ЗначениеЗаполнено(Период)
		ИЛИ Не ЗначениеЗаполнено(Организация) Тогда 
		Возврат КоличествоДокументовДляПерепроведения;
	КонецЕсли;
	
	МоментНарушения = РаботаСПоследовательностями.МоментНачалаПерепроведения(Организация, Период);
	Если МоментНарушения <> Неопределено 
		И МоментНарушения.Дата <= Период Тогда
		КоличествоДокументовДляПерепроведения = РаботаСПоследовательностями.ОценкаКоличестваДокументовДляПерепроведения(
			Организация,
			МоментНарушения.Дата,
			Период,
			МоментНарушения);
	КонецЕсли;
	
	Возврат КоличествоДокументовДляПерепроведения;
	
КонецФункции

// Получает порядок актуализации отчета описанный в общем макете "ТребованияКАктуальностиОтчетов"
// Параметры:
//  ИмяФормы - полное наименование формы отчета
//
// Возвращаемое значение:
//   Число - порядок актуализации:
//   0 - актуализация БУ;
//   1 - ссылка на НДС;
//   2 - актуализация БУ или ссылка на НДС;
//   3 - актуализация БУ и ссылка на НДС.
//
Функция ПорядокАктуализацииОтчета(ИмяФормы) Экспорт
	
	МакетТребованийКАктуализации = ПолучитьОбщийМакет("ТребованияКАктуальностиОтчетов");
	ТаблицаДанных = ОбщегоНазначения.ЗначениеИзСтрокиXML(МакетТребованийКАктуализации.ПолучитьТекст());
	ТаблицаДанных.Индексы.Добавить("Отчет");
	
	ЧастиИмениФормы = СтрРазделить(ИмяФормы, ".");
	Имяобъекта = "";
	Если ЧастиИмениФормы.Количество() > 1 Тогда 
		Имяобъекта = ЧастиИмениФормы[1];
	Иначе
		Имяобъекта = ИмяФормы;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Отчет", Имяобъекта);
	
	СтрокиДанных = ТаблицаДанных.НайтиСтроки(Отбор);
	ПорядокАктуализации = 0;
	Если СтрокиДанных.Количество() = 1 Тогда
		ПорядокАктуализации = Число(СтрокиДанных[0].ПорядокАктуализации);
	КонецЕсли;
	
	Возврат ПорядокАктуализации;
	
КонецФункции

Функция ПолучитьЦветФонаПодсказки() Экспорт
	
	Возврат ЦветаСтиля.ЦветФонаПодсказки;
	
КонецФункции

// Возвращает текущий режим автоматического закрытия месяца
// 
// Возвращаемое значение:
//  Строка - См. РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.ТекущийРежимАвтоматическогоЗакрытияМесяца()
//
Функция ТекущийРежимАвтоматическогоЗакрытияМесяца() Экспорт
	
	Возврат РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.ТекущийРежимАвтоматическогоЗакрытияМесяца();
	
КонецФункции

// Возвращает параметры, необходимые для формирования подсказки следующего запуска.
//
// Параметры:
//  Параметры - Структура - см. ЗакрытиеМесяцаКлиент.ПодсказкаСледующегоЗапуска()
//
Функция ПараметрыДляФормированияПодсказкиСледующегоЗапуска(Параметры) Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ЦветФонаПодсказки", ПолучитьЦветФонаПодсказки());
	Результат.Вставить("ПредупреждающийЦвет", ПолучитьПредупреждающийЦвет());
	Результат.Вставить("ТекущийРежимЗакрытияМесяца", ТекущийРежимАвтоматическогоЗакрытияМесяца());
	Результат.Вставить("ПериодДляЗакрытия", ПериодАвтоматическогоЗакрытияМесяца(Параметры));
	Результат.Вставить("МоментЗапуска", БлижайшаяДатаЗапускаРегламентногоЗадания(Параметры));
	
	Возврат Результат;

КонецФункции

// Запускает проверку актуальности и возвращает необходимые параметры, для формирования подсказки в момент проверки актуальности.
// 
// Параметры:
//  ПараметрыПроверки - Структура - см. ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности().
//	ИдентификаторПредыдущегоЗадания - УникальныйИдентификатор - идентификатор фонового задания проверки актуальности.
//
// Возвращаемое значение:
//	Структура - структура со свойствами:
//		* ЦветФонаПодсказки - Цвет - цвет для фона подсказки в момент проверки даты актуальности.
//		* ДлительнаяОперация - Структура - см. ДлительныеОперации.ВыполнитьФункцию().
//
Функция ЗапуститьПроверкуАктуальностиПодсказкиСледующегоЗапуска(ПараметрыПроверки, ИдентификаторПредыдущегоЗадания) Экспорт
	
	Результат = Новый Структура;
	
	Если ЗначениеЗаполнено(ИдентификаторПредыдущегоЗадания) Тогда
		ЗакрытиеМесяцаВызовСервера.ОтменитьВыполнениеЗадания(ИдентификаторПредыдущегоЗадания);
	КонецЕсли;
	
	Результат.Вставить("ЦветФонаПодсказки",  ПолучитьЦветФонаПодсказки());
	Результат.Вставить("ДлительнаяОперация", ПроверитьАктуальность(ПараметрыПроверки));
	
	Возврат Результат;

КонецФункции

// Запускает плавную и корректную остановку фонового задания закрытия месяца, или группового перепроведения.
//
// Параметры:
//  Организация	- СправочникСсылка.Организации	- ссылка на организацию для которой выполняется остановка задания.
//  УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор формы из которой осуществляется вызов.
//	ИдентификаторЗадания - УникальныйИдентификатор - идентификатор фонового задания, которое требуется остановить.
//	ЖдатьЗавершения - Булево - необходимо-ли запустить длительную операцию, ожидающую завершение фонового задания.
// 
// Возвращаемое значение:
//  Структура - описание длительной операции, см. ДлительныеОперации.ВыполнитьФункцию().
//
Функция ЗапуститьОстановкуФоновогоЗаданияЗакрытияМесяца(Организация, УникальныйИдентификаторФормы,
		ИдентификаторЗадания = Неопределено, ЖдатьЗавершения = Истина) Экспорт
	
	// Если ИдентификаторЗадания пустой, значит требуется остановка регламентного задания. Уточним идентификатор.
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		СостояниеВыполнения = РегистрыСведений.РезультатыЗакрытияМесяца.СостояниеВыполненияЗаданияЗакрытияМесяца(Организация);		
		ИдентификаторЗаданияДляОстановки = СостояниеВыполнения.ИдентификаторФоновогоЗадания;
	Иначе
		ИдентификаторЗаданияДляОстановки = ИдентификаторЗадания;
	КонецЕсли;
	
	// Пошлем фоновому заданию сигнал о необходимости завершить свою работу и сбросить момент нарушения последовательности.
	МенеджерЗаписи = РегистрыСведений.АктивностьСеансовПользователей.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация                          = Организация;
	МенеджерЗаписи.Пользователь                         = Пользователи.ТекущийПользователь();
	МенеджерЗаписи.ДатаПоследнейАктивности              = ТекущаяДатаСеанса();
	МенеджерЗаписи.ДатаПоследнейАктивностиУниверсальная = ТекущаяУниверсальнаяДата();
	МенеджерЗаписи.ИдентификаторЗаданияДляОстановки     = ИдентификаторЗаданияДляОстановки;
	МенеджерЗаписи.Записать();
	
	Если Не ЖдатьЗавершения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КлючФоновогоЗадания = СтрШаблон("ОстановкаФоновогоЗаданияЗакрытияМесяца:%1", Организация.УникальныйИдентификатор());
	ШаблонНаименования  = НСтр("ru = 'Ожидание остановки фонового задания (%1)'");
	
	// Запустим длительную операцию, ожидающую завершения регламентного задания.
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(ШаблонНаименования, Организация);
	ПараметрыВыполнения.КлючФоновогоЗадания         = КлючФоновогоЗадания;
	ПараметрыВыполнения.ОжидатьЗавершение           = 0;
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	
	ОписаниеДлительнойОперации = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "ЗакрытиеМесяца.ОжидатьОстановкиФоновогоЗаданияЗакрытияМесяца", Организация);

	Возврат ОписаниеДлительнойОперации;
	
КонецФункции

// Проверяет, выполняется-ли в данный момент остановка фонового задания закрытия месяца по организации.
//
Функция ВыполняетсяОстановкаФоновогоЗаданияЗакрытияМесяца(Организация) Экспорт 
	
	Результат = Ложь;
	
	ОтборПоСостоянию = Новый Структура;
	ОтборПоСостоянию.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборПоСостоянию);
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого ФоновоеЗадание Из МассивФоновыхЗаданий Цикл
		
		// Ключ должен состоять из двух частей, разделенных двоеточием: имя фонового задания, GUID организации.
		Ключ = СтрРазделить(ФоновоеЗадание.Ключ, ":", Истина);
		
 		Если Ключ.Количество() < 2 Или Ключ[0] <> "ОстановкаФоновогоЗаданияЗакрытияМесяца" Тогда
			// Это фоновое задание не выполняет остановку работы регламентного задания.
			Продолжить;
		КонецЕсли;
		
		УникальныйИдентификаторОрганизации = Ключ[1];
		Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(УникальныйИдентификаторОрганизации) Тогда
			ОрганизацияИзФоновогоЗадания = Справочники.Организации.ПолучитьСсылку(
				Новый УникальныйИдентификатор(УникальныйИдентификаторОрганизации));
		Иначе
			ОрганизацияИзФоновогоЗадания = Неопределено;	
		КонецЕсли;
		
		Если ОрганизацияИзФоновогоЗадания = Организация Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

#Область АктуализацияДанных

// Создает новое, или изменяет существующее регламентное задание автоматического закрытия месяца.
//
// Параметры:
//	ПараметрыЗаписи - Структура - структура со свойствами:
//		* Организация		 - СправочникСсылка.Организации	- ссылка на организацию для регламентного задания.
//  	* ИдентификаторЗадания - УникальныйИдентификатор - уникальный идентификатор регламентного задания для поиска.
//		* ДатаНачала		 - Дата - дата с которой начнется выполнение регламентного задания (при работе в модели сервиса не используется).
//  	* ВремяЗапуска	 - Дата	- время запуска задания (без даты). В модели сервиса дата и время запуска задания.
//		* ОтложенныйЗапуск - Булево - задание создается как основное задание или это копия в результате переноса основного.
// 
// Возвращаемое значение:
//  РегламентноеЗадание - созданное или измененное регламентное задание если найдено по идентификатору.
//
Функция ЗаписатьРегламентноеЗаданиеЗакрытияМесяца(ПараметрыЗаписи) Экспорт
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Если Не РазделениеВключено Тогда
		Расписание.ПериодПовтораДней = 1;
	КонецЕсли;
	
	Если Не РазделениеВключено Тогда
		Расписание.ВремяНачала = ПараметрыЗаписи.ВремяЗапуска;
		Расписание.ДатаНачала  = ПараметрыЗаписи.ДатаНачала;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыЗаписи.Организация) Тогда
		НаименованиеЗадания = НСтр("ru = 'Автоматическое закрытие месяца по всем организациям'");
	Иначе
		НаименованиеЗадания = СтрШаблон(НСтр("ru = 'Автоматическое закрытие месяца %1'"), ПараметрыЗаписи.Организация);
	КонецЕсли;
	
	Если ПараметрыЗаписи.ОтложенныйЗапуск Тогда
		НаименованиеЗадания = СтрШаблон(НСтр("ru = '%1 (Отложенный запуск)'"), НаименованиеЗадания); 
	КонецЕсли;
	
	ПараметрыРегламентногоЗадания = Новый Структура;
	ПараметрыРегламентногоЗадания.Вставить("Организация", ПараметрыЗаписи.Организация);
	ПараметрыРегламентногоЗадания.Вставить("ЭтоОтложенныйЗапуск", ПараметрыЗаписи.ОтложенныйЗапуск);
	
	ПараметрыКлюча = Новый Структура("Организация", ПараметрыЗаписи.Организация);
	Если РазделениеВключено Тогда
		КлючЗадания = ЗакрытиеМесяцаКлиентСервер.КлючЗаданияАктуализации(
			ПараметрыКлюча, Пользователи.ТекущийПользователь(), Истина);
	Иначе
		КлючЗадания = ЗакрытиеМесяцаКлиентСервер.КлючЗаданияАктуализации(ПараметрыКлюча, , Истина);
	КонецЕсли;
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("Использование", Истина);
	ПараметрыСоздания.Вставить("Метаданные",    Метаданные.РегламентныеЗадания.АвтоматическоеЗакрытиеМесяца);
	ПараметрыСоздания.Вставить("Наименование",  НаименованиеЗадания);
	ПараметрыСоздания.Вставить("Параметры",     Новый Массив);
	ПараметрыСоздания.Вставить("Ключ", КлючЗадания);
	ПараметрыСоздания.Параметры.Добавить(ПараметрыРегламентногоЗадания);
	Если РазделениеВключено Тогда
		ПараметрыСоздания.Вставить("ЗапланированныйМоментЗапуска", ПараметрыЗаписи.ВремяЗапуска);
	Иначе
		ПараметрыСоздания.Вставить("Расписание", Расписание);
	КонецЕсли;
	
	Попытка
		УстановитьПривилегированныйРежим(Истина);
		Если Не ПараметрыЗаписи.ОтложенныйЗапуск Тогда
			Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
				// В файловой ИБ проверяется активность по всем организациям. Необходимо очистить всю активность пользователей.
				Организация = Справочники.Организации.ПустаяСсылка();
			Иначе
				Организация = ПараметрыЗаписи.Организация;
			КонецЕсли;
			РегистрыСведений.АктивностьСеансовПользователей.УдалитьАктивностьПользователейПоОрганизации(Организация);
		КонецЕсли;
		
		Задание = РегламентныеЗаданияСервер.Задание(ПараметрыЗаписи.ИдентификаторЗадания);
		Если Задание = Неопределено Тогда
			Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыСоздания);
		Иначе
			РегламентныеЗаданияСервер.ИзменитьЗадание(ПараметрыЗаписи.ИдентификаторЗадания, ПараметрыСоздания);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	Исключение
		УстановитьПривилегированныйРежим(Ложь);
		
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонЗаписиЖурнала = НСтр("ru = 'Не удалось записать регламентное задание автоматического закрытия месяца для %1'");
        ЗаписьЖурналаРегистрации(НСтр("ru = 'Автоматическое закрытие месяца'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.РегламентныеЗадания.АвтоматическоеЗакрытиеМесяца,
			СтрШаблон(ШаблонЗаписиЖурнала, ПараметрыЗаписи.Организация), ИнформацияОбОшибке);
		ВызватьИсключение ИнформацияОбОшибке;
	КонецПопытки;
	
	Возврат Задание;
	
КонецФункции

// Планирует разовый запуск регламентного задания в модели сервиса, если это требуется
//
Процедура ЗапланироватьАвтоматическоеЗакрытиеМесяцаДляОбластиДанных() Экспорт
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		// В обычном режиме выполнение не требуется.
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеРегламентногоЗадания = Метаданные.РегламентныеЗадания.АвтоматическоеЗакрытиеМесяца;
	
	ПараметрыПоискаЗаданий = Новый Структура;
	ПараметрыПоискаЗаданий.Вставить("ИмяМетода",        МетаданныеРегламентногоЗадания.ИмяМетода);
	ПараметрыПоискаЗаданий.Вставить("ОбластьДанных",    РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
	ПараметрыПоискаЗаданий.Вставить("СостояниеЗадания", Перечисления.СостоянияЗаданий.Запланировано);
	ПараметрыПоискаЗаданий.Вставить("Использование",    Истина);
	ЗапланированныеЗадания = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоискаЗаданий);
	
	Если ТекущийРежимАвтоматическогоЗакрытияМесяца() = "Отключено" Тогда
		Для Каждого Задание Из ЗапланированныеЗадания Цикл
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	ВсеНастройки = РегистрыСведений.НастройкиАвтоматическогоЗакрытияМесяца.ВсеНастройкиРегламентныхЗаданий();
	ВсеНастройки.Колонки.Добавить("ТребуетсяПланирование", Новый ОписаниеТипов("Булево"));
	ВсеНастройки.ЗаполнитьЗначения(Истина, "ТребуетсяПланирование");
	
	Для Каждого Задание Из ЗапланированныеЗадания Цикл
		
		Если Задание.Параметры.Количество() > 0 Тогда
			Организация = Задание.Параметры[0].Организация;
		Иначе // Задание без параметров.
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
			Продолжить;
		КонецЕсли;
		
		НастройкиОрганизации = ВсеНастройки.Найти(Организация, "Организация");
		Если НастройкиОрганизации = Неопределено Тогда // Настройки удалены из регистра сведений.
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
		ИначеЕсли Не НастройкиОрганизации.ВыполнениеВключено Тогда // Выключили выполнение.
			НастройкиОрганизации.ТребуетсяПланирование = Ложь;
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
		Иначе // Проверим не изменилось-ли плановое время запуска.
			МоментЗапуска =	БлижайшаяДатаЗапускаРегламентногоЗадания(НастройкиОрганизации);
			Если Задание.ЗапланированныйМоментЗапуска <> МоментЗапуска Тогда
				РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
			Иначе
				НастройкиОрганизации.ТребуетсяПланирование = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Отбор = Новый Структура("ТребуетсяПланирование, ВыполнениеВключено", Истина, Истина);
	ЗаданияКПланированию = ВсеНастройки.НайтиСтроки(Отбор);
	Для Каждого Настройки Из ЗаданияКПланированию Цикл
		ПараметрыЗаписи = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыЗаписиРегламентногоЗаданияЗакрытияМесяца();
		ПараметрыЗаписи.Организация          = Настройки.Организация;
		ПараметрыЗаписи.ВремяЗапуска         = БлижайшаяДатаЗапускаРегламентногоЗадания(Настройки);
		ПараметрыЗаписи.ИдентификаторЗадания = Новый УникальныйИдентификатор;
		ЗакрытиеМесяцаВызовСервера.ЗаписатьРегламентноеЗаданиеЗакрытияМесяца(ПараметрыЗаписи);
	КонецЦикла;
	
КонецПроцедуры

// Записывает в регистр сведений "РезультатыЗакрытияМесяца" данные о последней актуализации.
//
// Параметры:
//  РезультатАктуализации - Структура - см. ЗакрытиеМесяцаКлиентСервер.НовыйРезультатАктуализации().
//
Процедура ЗаписатьРезультатАктуализации(РезультатАктуализации, ЭтоПерепроведение = Ложь) Экспорт 
	
	Если Не ЗначениеЗаполнено(РезультатАктуализации.Организация) Тогда
		// Актуализацию отменили.
		Возврат;
	КонецЕсли;
	
	Если РезультатАктуализации.ВыполнениеОстановлено И РезультатАктуализации.ЭтоИнтерактивныйЗапуск Тогда
		// Для интерактивного запуска из форм отчетов, при плавной остановке фонового задания, запись отчета не требуется.
		Возврат;
	КонецЕсли;
	
	Организация              = РезультатАктуализации.Организация;
	УспешноеВыполнение       = Истина;
	ПериодСОшибкойВыполнения = '00010101';
	ЕстьОбособленные         = Справочники.Организации.ЕстьОбособленныеПодразделения(Организация);
	ВыполнениеОстановлено    = РезультатАктуализации.Свойство("ВыполнениеОстановлено") И РезультатАктуализации.ВыполнениеОстановлено;
										  
	Макет = РегистрыСведений.РезультатыЗакрытияМесяца.ПолучитьМакет("ОтчетыОРезультатах");
	ОтчетОбОшибках = Новый ТабличныйДокумент;
	
	Если ВыполнениеОстановлено Тогда
		
		// Выполнение остановлено в результате действий пользователя в интерактивном сеансе.
		УспешноеВыполнение = Ложь;
		ЧислоСекунд        = ЗакрытиеМесяца.ЧислоСекундДляПереносаРегламентногоЗадания();
		ФорматВремени      = НСтр("ru = 'ДФ=''dd.MM.yyyy ""г. в"" ЧЧ:мм:сс'''");
		ПериодСОшибкойВыполнения = РезультатАктуализации.ПериодСОшибкойВыполнения;
		
		АктивностьСеансов = РегистрыСведений.АктивностьСеансовПользователей.ПоследняяАктивностьПользователей(Организация, Истина);
		ДатаАктивности = Формат(АктивностьСеансов.ДатаПоследнейАктивности, ФорматВремени);
		ВремяСледующегоЗапуска = ОбщегоНазначенияБП.ТекущаяДатаНаСервере() + ЧислоСекунд; 
		
		ОбластьСообщения = Макет.ПолучитьОбласть("ОтчетОПередачеПриоритета");
		ОбластьСообщения.Параметры.Организация = Организация;
		ОбластьСообщения.Параметры.Месяц = Формат(РезультатАктуализации.ПериодСОшибкойВыполнения, НСтр("ru = 'ДФ=''ММММ гггг ""г.""'''"));
		ОбластьСообщения.Параметры.ВремяСледующегоЗапуска = Формат(ВремяСледующегоЗапуска, ФорматВремени);
		
		Если ЗначениеЗаполнено(АктивностьСеансов.ИдентификаторЗаданияДляОстановки) И АктивностьСеансов.Документ = Неопределено Тогда
			ТекстПричины = НСтр("ru = '%1 пользователь %2 остановил автоматическое закрытие месяца, нажав на кнопку ""Отменить"".'");
			ОбластьСообщения.Параметры.ТекстПричины = СтрШаблон(ТекстПричины, ДатаАктивности, АктивностьСеансов.Пользователь);
		Иначе
			ТекстПричины = НСтр("ru = '%1 в момент выполнения автоматического закрытия месяца пользователь %2 записал документ %3.'");
			ОбластьСообщения.Параметры.ТекстПричины = СтрШаблон(
				ТекстПричины, ДатаАктивности, АктивностьСеансов.Пользователь, АктивностьСеансов.Документ);
		КонецЕсли;
		
		ОтчетОбОшибках.Вывести(ОбластьСообщения);
		
		// Если регламентное задание запущено по головной организации, а выполнение актуализации завершилось на обособленном подразделении.
		Если ЕстьОбособленные И Не ЭтоПерепроведение Тогда
			// Запишем отчет о приостановке для отображения в головной организации, даже если была остановка на обособленном подразделении.
			Организация = БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(Организация);
		КонецЕсли;
		
	ИначеЕсли РезультатАктуализации.Свойство("ОграничениеЗапуска") Тогда
		
		УспешноеВыполнение = Ложь;
		ОграничениеЗапуска = РезультатАктуализации.ОграничениеЗапуска;
		
		Если ОграничениеЗапуска.УстановленаДатыЗапрета Тогда
			ТекстПричина = НСтр("ru = 'Измените дату запрета редактирования, %1'");
		Иначе
			ТекстПричина = НСтр("ru = 'Устраните указанные ограничения, %1'");
		КонецЕсли;
		
		ПериодСОшибкойВыполнения = ОграничениеЗапуска.ДатаАктуальности;
		
		ТекстДальнейшиеДействия = НСтр("ru = 'после чего дождитесь следующего запуска или закройте период вручную.'");
		ДействияПользователя = СтрШаблон(ТекстПричина, ТекстДальнейшиеДействия);
		
		ОбластьСообщения = Макет.ПолучитьОбласть("ОграничениеЗапуска");
		ОбластьСообщения.Параметры.Организация = Организация;
		ОбластьСообщения.Параметры.ТекстОграничения = ОграничениеЗапуска.Сообщение;
		ОбластьСообщения.Параметры.ДействияПользователя = ДействияПользователя;
		ОтчетОбОшибках.Вывести(ОбластьСообщения);
		
	ИначеЕсли РезультатАктуализации.Свойство("АдресХранилищаСОшибками")
	 И ЭтоАдресВременногоХранилища(РезультатАктуализации.АдресХранилищаСОшибками) Тогда
		
		// Ошибки восстановления последовательности.
		ИнформацияОбОшибках = ПолучитьИзВременногоХранилища(РезультатАктуализации.АдресХранилищаСОшибками);
		Если ИнформацияОбОшибках <> Неопределено Тогда
			УспешноеВыполнение = Ложь;
			ПериодСОшибкойВыполнения = РезультатАктуализации.ПериодСОшибкойВыполнения;
			ОтчетОбОшибках.Вывести(ИнформацияОбОшибках.ОтчетПоОшибкам);
		КонецЕсли;
		
	ИначеЕсли РезультатАктуализации.Свойство("РегламентнаяОперацияСОшибками") Тогда
		
		// Ошибки выполнения регламентных операций в актуализации.
		УспешноеВыполнение = Ложь;
		ОшибкиВыполнения = РезультатАктуализации.РегламентнаяОперацияСОшибками.ОписаниеОшибок.Получить();
		ОтчетОбОшибках.Вывести(ОшибкиВыполнения);
		ПериодСОшибкойВыполнения = РезультатАктуализации.РегламентнаяОперацияСОшибками.Дата;
		
	ИначеЕсли РезультатАктуализации.Свойство("Операции") Тогда
		
		// Возможны ошибки при выполнении регламентных операций.
		ОтборОшибочных = Новый Структура("Состояние", Перечисления.ВидыСостоянийРегламентныхОпераций.ВыполненоСОшибками);
		ОшибочныеОперации = РезультатАктуализации.Операции.НайтиСтроки(ОтборОшибочных);
		УспешноеВыполнение = ОшибочныеОперации.Количество() = 0;
		Если НЕ УспешноеВыполнение Тогда
			ОперацияСОшибкой = ОшибочныеОперации[0].Ссылка;
			ПериодСОшибкойВыполнения = ОперацияСОшибкой.Дата;
			ОшибкиВыполнения = ОперацияСОшибкой.ОписаниеОшибок.Получить();
			ОтчетОбОшибках.Вывести(ОшибкиВыполнения);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьОбособленные И УспешноеВыполнение Тогда
		ОбособленныеПодразделения =
			БухгалтерскийУчетПереопределяемый.ПолучитьСписокОбособленныхПодразделенийОрганизации(Организация);
		Для Каждого Подразделение Из ОбособленныеПодразделения Цикл
			МенеджерЗаписиОтчета = РегистрыСведений.РезультатыЗакрытияМесяца.СоздатьМенеджерЗаписи();
			МенеджерЗаписиОтчета.Организация = Подразделение.Значение;
			МенеджерЗаписиОтчета.Удалить();
		КонецЦикла;
	КонецЕсли;
	
	МенеджерЗаписиОтчета = РегистрыСведений.РезультатыЗакрытияМесяца.СоздатьМенеджерЗаписи();
	МенеджерЗаписиОтчета.Организация    = Организация;
	МенеджерЗаписиОтчета.ОтчетОбОшибках = Новый ХранилищеЗначения(ОтчетОбОшибках, Новый СжатиеДанных(9));
	МенеджерЗаписиОтчета.ПериодОтчета   = ПериодСОшибкойВыполнения;
	МенеджерЗаписиОтчета.ДатаВыполнения = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	МенеджерЗаписиОтчета.Пользователь   = Пользователи.ТекущийПользователь();
	МенеджерЗаписиОтчета.Выполнено      = УспешноеВыполнение;
	МенеджерЗаписиОтчета.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область АктуализацияДанныхУстарело

// Возвращает свойства выполняемого фонового задания по актуализации.
// Если ни одного задания не найдено, возвращается значение Неопределено.
//
// Параметры:
//  Организация                  - СправочникСсылка.Организации - Организация, 
//                                 по которой будет производиться поиск фонового
//                                 задания по актуализации.
//  УникальныйИдентификаторФормы - Строка - Уникальный идентификатор формы, из
//                                 которой вызывается поиск фонового задания.
//
// Возвращаемое значение:
//   Неопределено - фоновых заданий актуализации по указанной организации сейчас не выполняется.
//   Структура - всегда содержит ключ УникальныйИдентификатор.
//               Если задание выполняется, но его свойства получить не удалось,
//               в значении ключа УникальныйИдентификатор возвращается пустая строка.
//               Другие ключи структуры:
//               * Ключ         - Строка - Ключ фонового задания.
//               * Наименование - Строка - Наименование фонового задания.
//               * Организация  - СправочникСсылка.Организация - Организация, 
//               по которой выполняется задание.
//               * Пользователь - СправочникСсылка.Пользователи - Пользователь,
//               запустивший задание.
//
Функция НайтиФоновоеЗаданиеАктуализацииПоОрганизации_ИдентификаторЗадания(Знач Организация, Знач УникальныйИдентификаторФормы)

	Результат = Неопределено;
	
	Отбор = Новый Структура("Состояние", СостояниеФоновогоЗадания.Активно);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);
	
	Для каждого ФоновоеЗадание Из МассивФоновыхЗаданий Цикл
		
		Ключ = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ФоновоеЗадание.Ключ, ":");
		// Ключ должен состоять из трех частей, разделенных двоеточием: имя фоновых заданий-актуализаций, GUID организации, GUID пользователя.
		
		Если Ключ.Количество() < 2
		 Или Ключ[0] <> ЗакрытиеМесяцаКлиентСервер.ПрефиксКлючаЗаданияАктуализации() Тогда
			// Это фоновое задание не выполняет актуализацию.
			Продолжить;
		КонецЕсли;
		
		УникальныйИдентификаторОрганизации = Ключ[1];
		ОрганизацияЗадания = Справочники.Организации.ПустаяСсылка();
		Если ЗначениеЗаполнено(УникальныйИдентификаторОрганизации)
		   И СтрЧислоВхождений(УникальныйИдентификаторОрганизации, "-") = 4 Тогда
			ОрганизацияЗадания = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификаторОрганизации));
		КонецЕсли;
		Если ОрганизацияЗадания <> Организация Тогда
			// Это фоновое задание по другой организации.
			Продолжить;
		КонецЕсли;
		
		УникальныйИдентификаторПользователя = ?(Ключ.Количество() > 2, Ключ[2], "");
		Если ЗначениеЗаполнено(УникальныйИдентификаторПользователя)
		   И СтрЧислоВхождений(УникальныйИдентификаторПользователя, "-") = 4 Тогда
			ПользовательЗадания = Справочники.Пользователи.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификаторПользователя));
		Иначе
			ПользовательЗадания = Справочники.Пользователи.ПустаяСсылка();
		КонецЕсли;
		
		СвойстваВыполняемогоЗадания = Новый Структура("УникальныйИдентификатор, Ключ, Наименование");
		ЗаполнитьЗначенияСвойств(СвойстваВыполняемогоЗадания, ФоновоеЗадание);
		СвойстваВыполняемогоЗадания.Вставить("Организация", Организация);
		СвойстваВыполняемогоЗадания.Вставить("Пользователь", ПользовательЗадания);
		Результат = СвойстваВыполняемогоЗадания;
		Прервать;
			
	КонецЦикла;
	
	Если Результат <> Неопределено Или Не ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Если не найдено ни одного задания в файловом режиме работы, это еще не значит, 
	// что никто не актуализирует данные.
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(Организация, , УникальныйИдентификаторФормы);
		РазблокироватьДанныеДляРедактирования(Организация, УникальныйИдентификаторФормы);
		
		// Организация доступна для актуализации.
		Возврат Неопределено;
		
	Исключение
		
		// Работа с данной организацией недоступна, но конкретный идентификатор фонового задания мы не знаем.
		Возврат Новый Структура("УникальныйИдентификатор", "");
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область АктуализацияДанных

Функция МоментЗапускаРегламентногоЗадания(ДатаЗапуска, ВремяЗапуска)
	
	ВремяЗапускаВСекундах = ВремяЗапуска - '00010101';
	Результат = НачалоДня(ДатаЗапуска) + ВремяЗапускаВСекундах;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти