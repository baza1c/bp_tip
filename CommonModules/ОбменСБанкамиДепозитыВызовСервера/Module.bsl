////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиДепозитыВызовСервера: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Запускает длительную операцию по получению новых предложений по депозитам из банков
// 
// Параметры:
//  ОбновитьВсеБанки - Булево - если Истина, данные для запроса предложений будут получены для всех доступных банков,
//    вне зависимости от актуальности уже полученных предложений.
// 
// Возвращаемое значение:
//  Структура - результат запуска длительной операции:
//   * ДлительнаяОперация - Структура - см. ДлительныеОперации.ВыполнитьПроцедуру;
//   * ДатаОбновленияПредложений - Дата - дата запуска задания.
//
Функция ЗапускЗаданияОбновленияДанныхДепозитныхПродуктов(ОбновитьВсеБанки) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДлительнаяОперация", Неопределено);
	Результат.Вставить("ДатаОбновленияПредложений", НачалоДня(ТекущаяДатаСеанса()));
	Если Не ОбменСБанкамиДепозиты.ДоступенОбменДепозитами() Тогда
		Возврат Результат;
	КонецЕсли;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.ОжидатьЗавершения = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = '1С:ДиректБанк. Запрос предложений по депозитам'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Результат.ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, 
		"ОбменСБанкамиДепозитыСлужебный.ОбновитьДанныеДепозитныхПродуктов", ОбновитьВсеБанки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПроверитьПродуктыИСформироватьДанныеПредложенийБанков(Организация, 
		ИдентификаторФормы, ПараметрыРасчетаПредложений, АдресХранилищаПредложенийБанков) Экспорт
	
	ДанныеПредложенийБанков =  ОбменСБанкамиДепозитыСлужебный.ПроверитьПродуктыИСформироватьДанныеПредложенийБанков(Организация, 
		ИдентификаторФормы, ПараметрыРасчетаПредложений);
		
	Если ЗначениеЗаполнено(ДанныеПредложенийБанков.ПредложенияБанков) 
		И Не АдресХранилищаПредложенийБанков = Неопределено 
		И Не АдресХранилищаПредложенийБанков = ДанныеПредложенийБанков.АдресХранилища Тогда
		
		УдалитьИзВременногоХранилища(АдресХранилищаПредложенийБанков);
		АдресХранилищаПредложенийБанков = Неопределено;
		
	КонецЕсли;
	
	Возврат ДанныеПредложенийБанков;
	
КонецФункции

Функция ДанныеПредложенийБанков(АдресХранилищаПредложенийБанков, ПараметрыРасчетаПредложений) Экспорт
	
	Возврат ОбменСБанкамиДепозитыСлужебный.ДанныеПредложенийБанков(АдресХранилищаПредложенийБанков, 
		ПараметрыРасчетаПредложений);
	
КонецФункции

Функция ПолучитьУсловияПродукта(АдресХранилища, КлючПродукта) Экспорт
	
	Возврат ОбменСбанкамиДепозитыСлужебный.ПолучитьУсловияПродукта(АдресХранилища, КлючПродукта);
	
КонецФункции

// Запускает фоновое задание запроса расчета депозита в банке
//
// Параметры:
//  ИдентификаторСессии - Строка - идентификатор авторизованной сессии обмена с банком.
//  УникальныйИдентификатор - Строка - уникальный идентификатор формы.
//  ДополнительныеПараметры - Структура - параметры, указанные при создании оповещения 
//     см. ОбменСБанкамиДепозитыСлужебный.СформироватьДанныеЗапросаРасчетаДепозита.
//
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьФункцию;
//
Функция ЗапроситьРасчетДепозитаВБанке(Знач ИдентификаторСессии, Знач УникальныйИдентификатор, 
	Знач ДополнительныеПараметры) Экспорт

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершения = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = '1С:ДиректБанк. Запрос расчета депозита'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
		"ОбменСБанкамиДепозитыСлужебный.ЗапроситьРасчетДепозитаВБанке", ИдентификаторСессии, 
		ДополнительныеПараметры);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Сохраняет решение пользователя закрыть панель расчета депозита в хранилище
// общих настроек, см. ОбменСБанкамиДепозиты.СохранитьЗакрытиеПанелиРасчетаДепозитов.
//
Процедура ОбработатьЗакрытиеПанелиРасчетаДепозитов() Экспорт
	
	ИмяПользователя = ИмяПользователя();
	ОбменСБанкамиДепозиты.СохранитьЗакрытиеПанелиРасчетаДепозитов(ИмяПользователя);
	
КонецПроцедуры

// Проверяет отображение панели депозитов, и удаляет сохраненные данные о закрытии
// панели депозитного калькулятора
//
// Параметры:
//  ВидимостьПанели - Булево - Если Истина, панель депозитов отображается
//
Процедура ОбработатьСостояниеПанелиРасчетаДепозитов(ВидимостьПанели) Экспорт
	
	Если ВидимостьПанели Тогда
		ИмяПользователя = ИмяПользователя();
		ОбменСБанкамиДепозиты.ПроверитьИУдалитьДанныеОЗакрытииПанелиРасчетаДепозитов(ИмяПользователя);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие данных о закрытии пользователем панели депозитного калькулятора.
// см. ОбменСБанкамиДепозиты.ПоказыватьПанельРасчетаДепозитов.
//
// Параметры:
//  ИмяПользователя - Строка - см. ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить.
//
// Возвращаемое значение:
//  Булево - Если Истина, с момента закрытия прошло 30 дней, или пользователь не закрывал депозитный калькулятор.
//
Функция ПоказыватьПанельРасчетаДепозитов() Экспорт
	
	ИмяПользователя = ИмяПользователя();
	Возврат ОбменСБанкамиДепозиты.ПоказыватьПанельРасчетаДепозитов(ИмяПользователя);
	
КонецФункции

#КонецОбласти