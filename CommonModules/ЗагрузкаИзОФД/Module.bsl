#Область ПрограммныйИнтерфейс

Функция ЗагрузкаИзОФДДоступна() Экспорт
	ЕстьПравоРаботыСОФД = 
		ОФД.ПолучениеДанныхИзОФДДоступно() И ЕстьНастройкиКассОФД()
		ИЛИ ОФД.НастройкаПодключенияДоступна() И ПравоДоступа("Использование", Метаданные.Обработки.ЗагрузкаИзОФД);
		
	Возврат  ЕстьПравоРаботыСОФД;
КонецФункции

Функция ЕстьНастройкиКассОФД() Экспорт 
	Если НЕ ПравоДоступа("Чтение", Метаданные.Справочники.ОфлайнОборудование) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОфлайнОборудование.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОфлайнОборудование КАК ОфлайнОборудование
	|ГДЕ
	|	ОфлайнОборудование.ТипОфлайнОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыОфлайнОборудования.ОФД) И ОфлайнОборудование.Организация В (&СписокОрганизаций)");
	
	Запрос.УстановитьПараметр("СписокОрганизаций", ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю());
	
	ПодключениеНастроено = Ложь;
	Для каждого ОписаниеОборудования Из Запрос.Выполнить().Выгрузить() Цикл
		ПодключениеНастроено = ОФД.ПодключениеНастроено(ОписаниеОборудования.Ссылка);
		Если ПодключениеНастроено Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключениеНастроено;
КонецФункции

#КонецОбласти