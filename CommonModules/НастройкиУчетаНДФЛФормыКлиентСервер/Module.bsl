
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиент-серверные методы формы записи регистра сведений НастройкиУчетаНДФЛ
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает дату начала действия порядка признания дохода по факту оплаты от покупателя.
// 
// Возвращаемое значение:
//  Дата
Функция ДатаНачалаДействияПорядкаПризнанияДоходаПоФактуОплатыОтПокупателя() Экспорт
	
	// По умолчанию порядок признания дохода от продаж через комиссионера
	// по факту оплаты от покупателя действует с 2025 года
	Возврат '20250101';
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УправлениеФормой(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Запись   = Форма.НастройкиУчетаНДФЛ;
	
	Элементы.ВсеВидыДеятельности.Доступность = Запись.ВестиУчетПоВидамДеятельности;
	
	Элементы.УчетДоходовНДФЛ.ЗаголовокСвернутогоОтображения = ЗаголовокГруппыУчетДоходов(Форма);
	
	Элементы.АвансыВключаютсяВДоходыВПериодеПолучения.Видимость = Не Запись.ВестиУчетПоВидамДеятельности;
	Элементы.ГруппаАвансыВключаютсяВДоходыВПериодеПолученияПоВидуДеятельности.Видимость = Запись.ВестиУчетПоВидамДеятельности;
	Элементы.ВидДеятельностиДоходовПоАвансам.Доступность = (Форма.АвансыВключаютсяВДоходыВПериодеПолучения = 1);
	
	Если Форма.ИспользуютсяПродажиЧерезКомиссионера Тогда
		Элементы.ГруппаПризнаниеДоходаОтКомиссионераИП.Видимость = Истина;
		
		ДоходПризнаетсяПриПоступленииДенегОтКомиссионера =
			Запись.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера
				= ПредопределенноеЗначение("Перечисление.ПорядокПризнанияДоходаОтПродажЧерезКомиссионера.ПриПоступленииДенегОтКомиссионера");
		Элементы.ГруппаПорядокПризнанияДоходаОтКомиссионераИППодсказка.Видимость =
			Запись.Период >= ДатаНачалаДействияПорядкаПризнанияДоходаПоФактуОплатыОтПокупателя()
			И ДоходПризнаетсяПриПоступленииДенегОтКомиссионера;
	Иначе
		Элементы.ГруппаПризнаниеДоходаОтКомиссионераИП.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.УчетРасходовНДФЛ.ЗаголовокСвернутогоОтображения = ЗаголовокГруппыУчетРасходов(Форма);
	
	Элементы.ПризнаватьРасходыПоОперациямПрошлогоГода.Доступность = Запись.ДляПризнанияРасходовТребуетсяПолучениеДохода;
	
КонецПроцедуры

Функция ЗаголовокГруппыУчетДоходов(Форма)
	
	Запись = Форма.НастройкиУчетаНДФЛ;
	
	Если Форма.ИспользуютсяПродажиЧерезКомиссионера Тогда
		Заголовок = НСтр("ru = 'Учет доходов'");
	ИначеЕсли Запись.АвансыВключаютсяВДоходыВПериодеПолучения Тогда
		Заголовок = НСтр("ru = 'Авансы включаются в доходы всегда в периоде фактического получения'");
	Иначе
		Заголовок = НСтр("ru = 'Авансы включаются в доходы в момент реализации, независимо от периода получения'");
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

Функция ЗаголовокГруппыУчетРасходов(Форма)
	
	Запись = Форма.НастройкиУчетаНДФЛ;
	
	Если Форма.ИспользуютсяПродажиЧерезКомиссионера Тогда
		Заголовок = НСтр("ru = 'Учет расходов'");
	ИначеЕсли Запись.ДляПризнанияРасходовТребуетсяПолучениеДохода Тогда
		Заголовок = НСтр("ru = 'Материальные расходы признаются при получении дохода от реализации'");
	Иначе
		Заголовок = НСтр("ru = 'Материальные расходы признаются независимо от реализации'");
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

#КонецОбласти