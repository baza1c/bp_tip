
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиент-серверные методы формы записи регистра сведений НастройкиУчетаНДС
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура УправлениеФормой(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Запись   = Форма.НастройкиУчетаНДС; 
	
	Элементы.НастройкиУчетаНДС.Видимость = Форма.ПлательщикНДС
		Или Форма.СтавкаНДСПриУСН = 2; 
	Элементы.ГруппаВыборСтавкиНалогообложения.Видимость = Не Форма.ПлательщикНДС;
	Элементы.ПрименяетсяОсвобождениеОтУплатыНДС.Видимость = Форма.ПлательщикНДС;
	
	Элементы.СложныйУчетНДС.Видимость = Истина;
	
	Элементы.ГруппаРаздельныйУчетНДСНаСчете19.Видимость = Запись.РаздельныйУчетНДСДо2014Года;
	Элементы.РаздельныйУчетНДСНаСчете19.Доступность = Запись.СложныйУчетНДС;
	
	Если НЕ Запись.РаздельныйУчетНДСДо2014Года Тогда
		Элементы.СложныйУчетНДС.Заголовок = НСтр("ru = 'Ведется раздельный учет входящего НДС по способам учета'");
	КонецЕсли;
	
	Элементы.ГруппаОткрытияДокументовВводОстатков.Видимость = НЕ Запись.РаздельныйУчетНДСДо2014Года И Запись.СложныйУчетНДС И Форма.ЕстьВводОстатковПоРУ;
	
	Элементы.УпрощенныйУчетНДС.Видимость   = Запись.Период < '20120101';
	Элементы.УпрощенныйУчетНДС.Доступность = Не Запись.СложныйУчетНДС;
	
	Элементы.НачислятьНДСПриПередачеНедвижимости.Видимость = Запись.Период >= '20110101' И Запись.Период < '20140701';
	
	Элементы.БезопаснаяДоляВычета.Доступность = Запись.КонтролироватьДолюВычета;
	
	НастроитьСтавкиНДСПриУСН(Форма);
	
КонецПроцедуры

Процедура НастроитьСтавкиНДСПриУСН(Форма)
	
	Элементы = Форма.Элементы;
	Запись   = Форма.НастройкиУчетаНДС;
	
	Если Форма.ИмяФормы = "ОбщаяФорма.НалогиИОтчеты" Тогда
		Период =  Форма.ДатаИзменения;
	Иначе
		Период = Запись.Период;
	КонецЕсли;
	
	ИспользуетсяКоэффициентДефлятор = УчетУСНКлиентСервер.ИспользуетсяКоэффициентДефлятор(Период);
	Если ИспользуетсяКоэффициентДефлятор Тогда
		КоэффициентДефлятор = УчетУСНКлиентСервер.КоэффициентДефлятор(Период);
		БазоваяГраницаПримененияПониженнойСтавкиНДС = УчетУСНКлиентСервер.БазоваяГраницаПримененияПониженнойСтавкиНДС();
		БазоваяГраницаПримененияПовышеннойСтавкиНДС = УчетУСНКлиентСервер.БазоваяГраницаПримененияПовышеннойСтавкиНДС();
	КонецЕсли;
	ГраницаПримененияПониженнойСтавкиНДС = УчетУСНКлиентСервер.ГраницаПримененияПониженнойСтавкиНДС(Период);
	ГраницаПримененияПовышеннойСтавкиНДС = УчетУСНКлиентСервер.ГраницаПримененияПовышеннойСтавкиНДС(Период);
	ПониженнаяУСНСтавкаНДС = УчетНДСКлиентСервер.ПониженнаяУСНСтавкаНДС(Период);
	ОбщаяУСНСтавкаНДС = УчетНДСКлиентСервер.ОбщаяУСНСтавкаНДС(Период);
	ОбщаяСтавкаНДС = УчетНДСКлиентСервер.ОбщаяСтавкаНДС(Период);
	
	Ставка5 = Элементы.СтавкаНДСПриУСН;
	Ставка5.СписокВыбора[0].Представление = СтрШаблон(
		НСтр("ru = '%1 (при доходе до %2 млн рублей)'"),
			ПониженнаяУСНСтавкаНДС,
			Окр(ГраницаПримененияПониженнойСтавкиНДС / 1000000, 2)); // в млн. рублей
	Если ИспользуетсяКоэффициентДефлятор Тогда
		Ставка5.Подсказка = СтрШаблон(НСтр("ru = 'В %1 году коэффициент-дефлятор составляет %2.
			|Граница применения ставки НДС %5: %3 млн руб * %2 = %4 млн руб.'"),
			Формат(Период, "ДФ=гггг;"),
			КоэффициентДефлятор,
			Окр(БазоваяГраницаПримененияПониженнойСтавкиНДС / 1000000, 2), // в млн. рублей
			Окр(ГраницаПримененияПониженнойСтавкиНДС / 1000000, 2), // в млн. рублей
			ПониженнаяУСНСтавкаНДС);
	Иначе
		Ставка5.Подсказка = "";
	КонецЕсли;
	
	Ставка7 = Элементы.СтавкаНДСПриУСН1;
	Ставка7.СписокВыбора[0].Представление = СтрШаблон(
		НСтр("ru = '%1 (при доходе до %2 млн рублей)'"),
			ОбщаяУСНСтавкаНДС,
			Окр(ГраницаПримененияПовышеннойСтавкиНДС / 1000000, 2)); // в млн. рублей
	Если ИспользуетсяКоэффициентДефлятор Тогда
		Ставка7.Подсказка = СтрШаблон(НСтр("ru = 'В %1 году коэффициент-дефлятор составляет %2.
			|Граница применения ставки НДС %5: %3 млн руб * %2 = %4 млн руб.'"),
			Формат(Период, "ДФ=гггг;"),
			КоэффициентДефлятор,
			Окр(БазоваяГраницаПримененияПовышеннойСтавкиНДС / 1000000, 2), // в млн. рублей
			Окр(ГраницаПримененияПовышеннойСтавкиНДС / 1000000, 2), // в млн. рублей
			ОбщаяУСНСтавкаНДС);
	Иначе
		Ставка7.Подсказка = "";
	КонецЕсли;
	
	ОсновнаяСтавка = Элементы.СтавкаНДСПриУСН2;
	ОсновнаяСтавка.СписокВыбора[0].Представление = СтрШаблон(
		НСтр("ru = '%1 (при доходе свыше %2 млн рублей или добровольно, с правом на вычет)'"),
			ОбщаяСтавкаНДС,
			Окр(ГраницаПримененияПовышеннойСтавкиНДС / 1000000, 2)); // в млн. рублей
	
	ТекстПодсказки = СтрШаблон(
		НСтр("ru='При превышении дохода в %1 млн рублей, организациям и ИП на УСН необходимо уплачивать НДС.
		|Ставка налога зависит от дохода:
		|
		|● %4 при доходе до %2 млн рублей без права на вычет;
		|● %5 при доходе от %2 до %3 млн рублей без права на вычет.'"),
		Окр(УчетУСНКлиентСервер.ГраницаПримененияНДС(Период) / 1000000, 2), // в млн. рублей
		Окр(ГраницаПримененияПониженнойСтавкиНДС / 1000000, 2), // в млн. рублей
		Окр(ГраницаПримененияПовышеннойСтавкиНДС / 1000000, 2), // в млн. рублей
		ПониженнаяУСНСтавкаНДС,
		ОбщаяУСНСтавкаНДС);
	Элементы.ПодсказкаНДСПриУСН1.Заголовок = Новый ФорматированнаяСтрока(ТекстПодсказки);
	
	Элементы.ПодсказкаНДСПриУСН3.Заголовок = СтрШаблон(
		НСтр("ru='Также можно применять общую ставку налога в размере %1 (%2) с возможностью заявить вычет.'"),
		ОбщаяСтавкаНДС,
		УчетНДСКлиентСервер.ПониженнаяСтавкаНДС(Период));
	
КонецПроцедуры

#КонецОбласти