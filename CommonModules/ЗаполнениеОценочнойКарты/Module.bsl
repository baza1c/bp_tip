#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьОтчетОценочнаяКарта(ПараметрыОтчета, Контейнер) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьРаздел1(ПараметрыОтчета, Контейнер);
	
	ЗаполнитьРаздел2(ПараметрыОтчета, Контейнер);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// см. РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОПоказателяхОтчета()
//
Процедура ПолучитьСведенияОПоказателяхОтчетаОценочнаяКарта(ПоказателиОтчета) Экспорт
	
	// Раздел 1
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "СуммаЗадолжФНС", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "СуммаЗадолжКред", Истина, Ложь);
	Для НомерКредитора = 1 По КоличествоКредиторов() Цикл
		ФорматированныйНомер = Формат(НомерКредитора, "ЧГ=");
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета,
			СтрШаблон("Кред%1Наим", ФорматированныйНомер), Истина, Ложь);
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета,
			СтрШаблон("Кред%1ИНН", ФорматированныйНомер), Истина, Ложь);
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета,
			СтрШаблон("Кред%1Сумма", ФорматированныйНомер), Истина, Ложь);
	КонецЦикла;
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "СуммаЗадолжДеб", Истина, Ложь);
	Для НомерДебитора = 1 По КоличествоДебиторов() Цикл
		ФорматированныйНомер = Формат(НомерДебитора, "ЧГ=");
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета,
			СтрШаблон("Деб%1Наим", ФорматированныйНомер), Истина, Ложь);
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета,
			СтрШаблон("Деб%1ИНН", ФорматированныйНомер), Истина, Ложь);
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета,
			СтрШаблон("Деб%1Сумма", ФорматированныйНомер), Истина, Ложь);
	КонецЦикла;
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "СтавкаРефинансирования", Истина, Ложь);
	
	// Раздел 2
	
	// Сальдо на начало
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200040003", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200080003", Истина, Ложь);
	
	Для ИндексПериода = -12 По 0 Цикл
		ФорматированныйИндекс = Формат(-ИндексПериода, "ЧЦ=2; ЧН=; ЧВН=; ЧГ=");
		
		// Месяц периода
		ИмяПоказателя = СтрШаблон("П0002000001%1", ФорматированныйИндекс);
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
		
		// Поступления денежных средств
		КоличествоСтрокПоступлений = 15;
		НомерСтрокиПрочихПоступлений = 5;
		Для НомерПоступления = 1 По КоличествоСтрокПоступлений Цикл
			Если НомерПоступления = НомерСтрокиПрочихПоступлений Тогда
				// Итоговый показатель
				Продолжить;
			КонецЕсли;
			ФорматированныйНомер = Формат(НомерПоступления, "ЧГ=");
			Если НомерПоступления < 10 Тогда
				ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("П0002001%101%2",
					ФорматированныйНомер, ФорматированныйИндекс);
			Иначе
				ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("П000201%101%2",
					ФорматированныйНомер, ФорматированныйИндекс);
			КонецЕсли;
			РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
			
			Если ИндексПериода = 0 И НомерПоступления >= 6 Тогда
				Если НомерПоступления < 10 Тогда
					ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("П0002001%10001", ФорматированныйНомер);
				Иначе
					ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("П000201%10001", ФорматированныйНомер);
				КонецЕсли;
				РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
			КонецЕсли;
		КонецЦикла;
		
		// Платежи
		КоличествоСтрокПлатежей = 7;
		НомерСтрокиПрочихПлатежей = 5;
		Для НомерПлатежа = 1 По КоличествоСтрокПлатежей Цикл
			Если НомерПлатежа = НомерСтрокиПрочихПлатежей Тогда
				// Итоговый показатель
				Продолжить;
			КонецЕсли;
			ФорматированныйНомер = Формат(НомерПлатежа, "ЧГ=");
			ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("П0002002%101%2",
				ФорматированныйНомер, ФорматированныйИндекс);
			РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
		КонецЦикла;
		Для НомерПлатежаПоСтатье = 1 По КоличествоСтрокПрочихПлатежей() Цикл
			ФорматированныйНомер = Формат(НомерПлатежаПоСтатье, "ЧГ=");
			Если НомерПоступления < 10 Тогда
				ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("П0002025%101%2",
					ФорматированныйНомер, ФорматированныйИндекс);
			Иначе
				ИмяПоказателя = СтрШаблон("П0002251001%1", ФорматированныйИндекс);
			КонецЕсли;
			РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
			Если ИндексПериода = 0 Тогда
				Если НомерПоступления < 10 Тогда
					ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("П0002025%10001", ФорматированныйНомер);
				Иначе
					ИмяПоказателя = "П000225100001";
				КонецЕсли;
				РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
			КонецЕсли;
		КонецЦикла;
		
		// Строка 5
		ИмяПоказателя = СтрШаблон("П0002000501%1", ФорматированныйИндекс);
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
		
		// Строка 6
		ИмяПоказателя = СтрШаблон("П0002000601%1", ФорматированныйИндекс);
		РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, ИмяПоказателя, Истина, Ложь);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРаздел1(ПараметрыОтчета, Контейнер)
	
	Раздел1 = Контейнер.Раздел1;
	
	Организация = ПараметрыОтчета.Организация;
	ДатаЗаявления = ПараметрыОтчета.мДатаКонцаПериодаОтчета;
	
	// Отрицательное сальдо ЕНС и предстоящие платежи
	СальдоЕНС = 0;
	ПредстоящиеПлатежи = 0;
	
	МодульЕдиныйНалоговыйСчетИнтеграция = ОбщегоНазначения.ОбщийМодуль("ЕдиныйНалоговыйСчетИнтеграция");
	ДанныеСальдоЕНС = МодульЕдиныйНалоговыйСчетИнтеграция.ДанныеСальдо(Организация);
	Если ДанныеСальдоЕНС <> Неопределено Тогда
		СальдоЕНС = ?(ДанныеСальдоЕНС.СальдоЕНС < 0, -СальдоЕНС, 0);
	КонецЕсли;
	
	ДанныеПредстоящиеПлатежи = МодульЕдиныйНалоговыйСчетИнтеграция.ПредстоящиеПлатежи(Организация);
	Если ДанныеПредстоящиеПлатежи <> Неопределено Тогда
		ВидНалогаНДФЛ = Справочники.ВидыНалоговыхОбязательств.НайтиПоКоду("05");
		Если ЗначениеЗаполнено(ВидНалогаНДФЛ) Тогда
			ПоискНДФЛ = Новый Структура("ВидНалога", ВидНалогаНДФЛ);
			ПлатежиНДФЛ = ДанныеПредстоящиеПлатежи.НайтиСтроки(ПоискНДФЛ);
			Для Каждого СтрокаНДФЛ Из ПлатежиНДФЛ Цикл
				ДанныеПредстоящиеПлатежи.Удалить(СтрокаНДФЛ);
			КонецЦикла;
			ПредстоящиеПлатежи = ДанныеПредстоящиеПлатежи.Итог("Сумма");
		КонецЕсли;
	КонецЕсли;

	Раздел1.СуммаЗадолжФНС = СальдоЕНС + ПредстоящиеПлатежи;
	
	// Просроченная кредиторская задолженность
	Кредиторка = ПросроченнаяКредиторскаяЗадолженность(Организация, ДатаЗаявления);
	Кредиторка.Свернуть("Кредитор", "Просрочено");
	Кредиторка.Сортировать("Просрочено Убыв");
	
	Раздел1.СуммаЗадолжКред = Кредиторка.Итог("Просрочено");
	КоличествоКредиторов = Мин(КоличествоКредиторов(), Кредиторка.Количество());
	
	Для НомерКредитора = 1 По КоличествоКредиторов Цикл
		СтрокаКредитора = Кредиторка[НомерКредитора - 1];
		СведенияОКредиторе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СтрокаКредитора.Кредитор,
			ДатаЗаявления);
		ИмяКредиторНаименование = СтрШаблон("Кред%1Наим", Формат(НомерКредитора, "ЧГ="));
		Раздел1[ИмяКредиторНаименование] = СведенияОКредиторе.НаименованиеДляПечатныхФорм;
		ИмяКредиторИНН = СтрШаблон("Кред%1ИНН", Формат(НомерКредитора, "ЧГ="));
		Раздел1[ИмяКредиторИНН] = СведенияОКредиторе.ИНН;
		ИмяКредиторСумма = СтрШаблон("Кред%1Сумма", Формат(НомерКредитора, "ЧГ="));
		Раздел1[ИмяКредиторСумма] = СтрокаКредитора.Просрочено;
	КонецЦикла;
	
	// Дебиторская задолженность
	Дебиторка = ДебиторскаяЗадолженность(Организация, ДатаЗаявления);
	Дебиторка.Сортировать("Подтверждено Убыв");
	
	Раздел1.СуммаЗадолжДеб = Дебиторка.Итог("Подтверждено");
	КоличествоДебиторов = Мин(КоличествоДебиторов(), Дебиторка.Количество());
	
	Для НомерДебитора = 1 По КоличествоДебиторов Цикл
		СтрокаДебитора = Дебиторка[НомерДебитора - 1];
		СведенияОДебиторе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СтрокаДебитора.Субконто1,
			ДатаЗаявления);
		ИмяДебиторНаименование = СтрШаблон("Деб%1Наим", Формат(НомерДебитора, "ЧГ="));
		Раздел1[ИмяДебиторНаименование] = СведенияОДебиторе.НаименованиеДляПечатныхФорм;
		ИмяДебиторИНН = СтрШаблон("Деб%1ИНН", Формат(НомерДебитора, "ЧГ="));
		Раздел1[ИмяДебиторИНН] = СведенияОДебиторе.ИНН;
		ИмяДебиторСумма = СтрШаблон("Деб%1Сумма", Формат(НомерДебитора, "ЧГ="));
		Раздел1[ИмяДебиторСумма] = СтрокаДебитора.Подтверждено;
	КонецЦикла;
	
	// Ставка рефинансирования
	Раздел1.СтавкаРефинансирования = РегистрыСведений.СтавкаРефинансированияЦБ.СтавкаРефинансированияСрезПоследних(
		ДатаЗаявления);
	
КонецПроцедуры

Функция КоличествоКредиторов()
	
	Возврат 5;
	
КонецФункции

Функция ПросроченнаяКредиторскаяЗадолженность(Организация, ДатаСреза)
	
	КонтекстЗаполнения = Новый Структура();
	КонтекстЗаполнения.Вставить("Организация",   Организация);
	КонтекстЗаполнения.Вставить("Дата",          ДатаСреза);
	КонтекстЗаполнения.Вставить("СчетаРасчетов", СчетаКредиторскойЗадолженности());
	
	ПараметрыРасчета = Документы.ИнвентаризацияРасчетовСКонтрагентами.НовыеПараметрыЗаполнения(КонтекстЗаполнения);
	ПараметрыРасчета.Организация = Организация;
	ЗадолженностьПоБУ = Документы.ИнвентаризацияРасчетовСКонтрагентами.ЗадолженностьПоРасчетам(ПараметрыРасчета,
		Перечисления.ВидыЗадолженности.Кредиторская);
	
	// Отбираем просроченную задолженность
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеУчета.СчетРасчетов КАК СчетРасчетов,
	|	ДанныеУчета.Субконто1 КАК Субконто1,
	|	ДанныеУчета.Субконто2 КАК Субконто2,
	|	ДанныеУчета.Субконто3 КАК Субконто3,
	|	ДанныеУчета.Подтверждено КАК Подтверждено,
	|	ВЫРАЗИТЬ(ДанныеУчета.ДатаПогашения КАК ДАТА) КАК ДатаПогашения
	|ПОМЕСТИТЬ ДанныеУчета
	|ИЗ
	|	&ДанныеУчета КАК ДанныеУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.СчетРасчетов КАК СчетРасчетов,
	|	Данные.Кредитор КАК Кредитор,
	|	Данные.Договор КАК Договор,
	|	Данные.Документ КАК Документ,
	|	СУММА(Данные.ПросроченоДанныеУчета) КАК Просрочено
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеУчета.СчетРасчетов КАК СчетРасчетов,
	|		ДанныеУчета.Субконто1 КАК Кредитор,
	|		ВЫБОР
	|			КОГДА ДанныеУчета.Субконто2 ССЫЛКА Справочник.ДоговорыКонтрагентов
	|				ТОГДА ВЫРАЗИТЬ(ДанныеУчета.Субконто2 КАК Справочник.ДоговорыКонтрагентов)
	|			КОГДА ДанныеУчета.Субконто3 ССЫЛКА Справочник.ДоговорыКонтрагентов
	|				ТОГДА ВЫРАЗИТЬ(ДанныеУчета.Субконто3 КАК Справочник.ДоговорыКонтрагентов)
	|			ИНАЧЕ NULL
	|		КОНЕЦ КАК Договор,
	|		ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(ДанныеУчета.Субконто3) В (&ДоступныеТипыДокументов)
	|				ТОГДА ДанныеУчета.Субконто3
	|			ИНАЧЕ NULL
	|		КОНЕЦ КАК Документ,
	|		ВЫБОР
	|			КОГДА ДанныеУчета.ДатаПогашения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ИЛИ ДанныеУчета.ДатаПогашения >= &Период
	|				ТОГДА 0
	|			ИНАЧЕ ДанныеУчета.Подтверждено
	|		КОНЕЦ КАК ПросроченоДанныеУчета
	|	ИЗ
	|		ДанныеУчета КАК ДанныеУчета) КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.СчетРасчетов,
	|	Данные.Кредитор,
	|	Данные.Документ,
	|	Данные.Договор
	|
	|ИМЕЮЩИЕ
	|	СУММА(Данные.ПросроченоДанныеУчета) <> 0";
	
	Запрос.УстановитьПараметр("ДанныеУчета", ЗадолженностьПоБУ);
	Запрос.УстановитьПараметр("ДоступныеТипыДокументов", ДоступныеТипыЗначенийДокументы());
	Запрос.УстановитьПараметр("Период", НачалоДня(ДатаСреза));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СчетаКредиторскойЗадолженности()
	
	// Здесь только те счета, по которым можно вычислить просроченную задолженность
	
	СчетаРасчетов = Новый Массив;
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	
	Возврат СчетаРасчетов;
	
КонецФункции

Функция ДоступныеТипыЗначенийДокументы()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыСубконтоХозрасчетные.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.ВидыСубконтоХозрасчетные КАК ВидыСубконтоХозрасчетные
	|ГДЕ
	|	ВидыСубконтоХозрасчетные.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)";
	
	Результат = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, Выборка.ТипЗначения.Типы());
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция КоличествоДебиторов()
	
	Возврат 5;
	
КонецФункции

Функция ДебиторскаяЗадолженность(Организация, ДатаСреза)
	
	КонтекстЗаполнения = Новый Структура();
	КонтекстЗаполнения.Вставить("Организация",   Организация);
	КонтекстЗаполнения.Вставить("Дата",          ДатаСреза);
	КонтекстЗаполнения.Вставить("СчетаРасчетов", СчетаДебиторскойЗадолженности());
	
	ПараметрыРасчета = Документы.ИнвентаризацияРасчетовСКонтрагентами.НовыеПараметрыЗаполнения(КонтекстЗаполнения);
	ПараметрыРасчета.Организация = Организация;
	ЗадолженностьПоБУ = Документы.ИнвентаризацияРасчетовСКонтрагентами.ЗадолженностьПоРасчетам(ПараметрыРасчета,
		Перечисления.ВидыЗадолженности.Дебиторская);
	
	ЗадолженностьПоБУ.Свернуть("Субконто1", "Подтверждено");
		
	Возврат ЗадолженностьПоБУ;
	
КонецФункции

Функция СчетаДебиторскойЗадолженности()
	
	СчетаРасчетов = Новый Массив;
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.ФинансовыеВложения);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами_);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоПрочимОперациям);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	
	Возврат СчетаРасчетов;
	
КонецФункции

Процедура ЗаполнитьРаздел2(ПараметрыОтчета, Контейнер)
	
	Раздел2 = Контейнер.Раздел2;
	
	Организация = ПараметрыОтчета.Организация;
	ДатаЗаявления = ПараметрыОтчета.мДатаКонцаПериодаОтчета;
	
	// Используем вспомогательную структуру для хранения промежуточных данных
	ДанныеАлгоритма = Новый Структура;
	ДанныеАлгоритма.Вставить("ПериодыРаздела2");
	ЗаполнениеБухгалтерскойОтчетностиПравила.УстановитьСчетаДенежныеСредства(ДанныеАлгоритма);
	
	ЗаполнитьПериодыРаздела2(ДатаЗаявления, Организация, ДанныеАлгоритма);
	
	ПрочиеПоступления = НоваяТаблицаПрочихДенежныхПотоков();
	ПрочиеПлатежи = НоваяТаблицаПрочихДенежныхПотоков();
	
	КэшИндексПериода = Новый Соответствие;
	
	РезультатДенежныхПотоков = ДанныеАлгоритма.ПериодыРаздела2;
	Для Каждого СтрокаРезультата Из РезультатДенежныхПотоков.Строки Цикл
		Если СтрокаРезультата.Показатель = ПоказательПрочиеПоступления() Тогда
			ТаблицаДенежногоПотока = ПрочиеПоступления;
		ИначеЕсли СтрокаРезультата.Показатель = ПоказательПрочиеПлатежи() Тогда
			ТаблицаДенежногоПотока = ПрочиеПлатежи;
		Иначе
			ТаблицаДенежногоПотока = Неопределено;
		КонецЕсли;
		Если ТаблицаДенежногоПотока <> Неопределено Тогда
			Для Каждого ВложеннаяСтрока Из СтрокаРезультата.Строки Цикл
				Если ВложеннаяСтрока.Сумма = 0 Тогда
					Продолжить;
				КонецЕсли;
				СтрокаПлатежи = ТаблицаДенежногоПотока.Добавить();
				СтрокаПлатежи.Период = ВложеннаяСтрока.Период;
				СтрокаПлатежи.Показатель = ВложеннаяСтрока.Статья;
				СтрокаПлатежи.Сумма = ВложеннаяСтрока.Сумма;
			КонецЦикла;
		Иначе
			Если Раздел2.Свойство(СтрокаРезультата.Показатель) Тогда
				// Сальдо на начало
				ЗаполнитьСуммовойПоказательРаздела2ВКонтейнер(Раздел2, СтрокаРезультата.Показатель, СтрокаРезультата.Сумма);
			Иначе
				// Показатель в периоде
				ИндексПериода = ИндексПериода(СтрокаРезультата.Период, ДатаЗаявления, КэшИндексПериода);
				ИмяПоказателя = ИмяПоказателяПериодаРаздела2(СтрокаРезультата.Показатель, ИндексПериода);
				ЗаполнитьСуммовойПоказательРаздела2ВКонтейнер(Раздел2, ИмяПоказателя, СтрокаРезультата.Сумма);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	//Строку 5 и 6 в месяце подачи заявления заполним по данным раздела 1
	Раздел2.П000200050100 = Контейнер.Раздел1.СуммаЗадолжКред;
	Раздел2.П000200060100 = Контейнер.Раздел1.СуммаЗадолжФНС;
	
	// Иные поступления и платежи детализируем по статьям в порядке убывания итоговых сумм (за все 12 месяцев)
	
	// Выводим 9 строк в показатели 1.6-1.14, остальные - в 1.15 ("Прочие поступления")
	ПрочиеПоступленияИтоги = ПрочиеПоступления.Скопировать(, "Показатель,Сумма");
	ПрочиеПоступленияИтоги.Свернуть("Показатель", "Сумма");
	ПрочиеПоступленияИтоги.Сортировать("Сумма Убыв");
	ВсегоСтрок = ПрочиеПоступленияИтоги.Количество() - 1;
	
	Для ИндексСтроки = 0 По ВсегоСтрок Цикл
		СтрокаПоступленияИтоги = ПрочиеПоступленияИтоги[ИндексСтроки];
		ИмяПоказателяПоступления = ИмяПоказателяПоступленияРаздела2(ИндексСтроки);
		Раздел2[ИмяПоказателяПоступления] = ЗначениеПоказателяПоступленияРаздела2(СтрокаПоступленияИтоги.Показатель,
			ИндексСтроки, ВсегоСтрок);
		
		ПоискПоказателя = Новый Структура("Показатель", СтрокаПоступленияИтоги.Показатель);
		ПрочиеПоступленияПоПериодам = ПрочиеПоступления.НайтиСтроки(ПоискПоказателя);
		Для Каждого СтрокаПоступления Из ПрочиеПоступленияПоПериодам Цикл
			ИндексПериода = ИндексПериода(СтрокаПоступления.Период, ДатаЗаявления, КэшИндексПериода);
			ИмяПоказателяПериода = ИмяПоказателяПоступленияРаздела2(ИндексСтроки, ИндексПериода);
			Раздел2[ИмяПоказателяПериода] = СтрокаПоступления.Сумма;
		КонецЦикла;
	КонецЦикла;
	
	// Выводим 9 строк в показатели 2.5.1-2.5.9, остальные - в 2.5.10 ("Прочие платежи")
	ПрочиеПлатежиИтоги = ПрочиеПлатежи.Скопировать(, "Показатель,Сумма");
	ПрочиеПлатежиИтоги.Свернуть("Показатель", "Сумма");
	ПрочиеПлатежиИтоги.Сортировать("Сумма Возр");
	ВсегоСтрок = ПрочиеПлатежиИтоги.Количество() - 1;
	
	Для ИндексСтроки = 0 По ВсегоСтрок Цикл
		СтрокаПлатежиИтоги = ПрочиеПлатежиИтоги[ИндексСтроки];
		ИмяПоказателяПлатежи = ИмяПоказателяПлатежиРаздела2(ИндексСтроки);
		Раздел2[ИмяПоказателяПлатежи] = ЗначениеПоказателяПлатежиРаздела2(СтрокаПлатежиИтоги.Показатель,
			ИндексСтроки, ВсегоСтрок);
		
		ПоискПоказателя = Новый Структура("Показатель", СтрокаПлатежиИтоги.Показатель);
		ПрочиеПлатежиПоПериодам = ПрочиеПлатежи.НайтиСтроки(ПоискПоказателя);
		Для Каждого СтрокаПлатежи Из ПрочиеПлатежиПоПериодам Цикл
			ИндексПериода = ИндексПериода(СтрокаПлатежи.Период, ДатаЗаявления, КэшИндексПериода);
			ИмяПоказателяПериода = ИмяПоказателяПлатежиРаздела2(ИндексСтроки, ИндексПериода);
			Раздел2[ИмяПоказателяПериода] = -СтрокаПлатежи.Сумма; // Отрицательный показатель
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПериодыРаздела2(ДатаЗаявления, Организация, ДанныеАлгоритма)
	
	// Показатели рассчитываются за предыдущие 12 месяцев + месяц подачи заявления
	НачалоПериода = НачалоМесяца(ДобавитьМесяц(ДатаЗаявления, -12));
	КонецПериода = КонецМесяца(ДатаЗаявления);
	
	КонтекстОтчета = БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета();
	КонтекстОтчета.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	БухгалтерскаяОтчетностьБРО.ЗаполнитьПериодОтчета(КонтекстОтчета.Период, НачалоПериода, КонецПериода);
	
	РезультатДенежныхПотоков = Новый ДеревоЗначений;
	РезультатДенежныхПотоков.Колонки.Добавить("Период");
	РезультатДенежныхПотоков.Колонки.Добавить("Показатель");
	РезультатДенежныхПотоков.Колонки.Добавить("Статья");
	РезультатДенежныхПотоков.Колонки.Добавить("Сумма");
	
	// Сальдо на начало периода
	// Заполняем только для первого анализируемого периода
	Сальдо = СальдоДенежныхСредств(ДанныеАлгоритма, КонтекстОтчета);
	ОбработатьСтрокуПоказателя(РезультатДенежныхПотоков, НачалоПериода, "П000200040003", Сальдо);
	ОбработатьСтрокуПоказателя(РезультатДенежныхПотоков, НачалоПериода, "П000200080003", Сальдо);
	
	ВыборкаДенежныеПотоки = ДенежныеПотоки(ДанныеАлгоритма, КонтекстОтчета);
	
	Пока ВыборкаДенежныеПотоки.Следующий() Цикл
		
		ТекущийПоказатель = Неопределено;
		
		// Поступления денежных средств
		Если ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.СредстваПолученныеОтПокупателейИЗаказчиков
			Или ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.АрендныеПлатежиРоялтиЛицензионныеПлатежиГонорарыКомиссионные
			Или ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ПоступленияОтПерепродажиФинансовыхВложений
			Или ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ПоступлениеПроцентовПоДебиторскойЗадолженностиПокупателей
			Или ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеПоступленияПоТекущейДеятельности Тогда
			ТекущийПоказатель = ПоказательПоступлениеПоОсновнойДеятельности();
		ИначеЕсли ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ПоступленияОтЗаймовИКредитовПредоставленныхДругимиОрганизациями Тогда
			ТекущийПоказатель = "П0002001201";
		ИначеЕсли ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ВыручкаОтПродажиВнеоборотныхАктивовКромеФинансовых Тогда
			ТекущийПоказатель = "П0002001401";
		// Платежи
		ИначеЕсли ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ОплатаОборотныхАктивов
			Или ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеРасходыПоТекущейДеятельности Тогда
			ТекущийПоказатель = ПоказательПлатежиПоОсновнойДеятельности();
		ИначеЕсли ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ОплатаТруда Тогда
			ТекущийПоказатель = "П0002002201";
		ИначеЕсли ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ВыплатаПроцентовПоДолговымОбязательствам
			Или ВыборкаДенежныеПотоки.ВидДвижения = Перечисления.ВидыДвиженийДенежныхСредств.ПогашениеДолговыйЦенныхБумагВозвратКредитовИЗаймов Тогда
			ТекущийПоказатель = "П0002002301";
		ИначеЕсли Не ЗначениеЗаполнено(ВыборкаДенежныеПотоки.Статья) Тогда
			// Отнесем показатель без статьи на основную деятельность в зависимости от знака
			ТекущийПоказатель = ?(ВыборкаДенежныеПотоки.Сумма < 0,
				ПоказательПлатежиПоОсновнойДеятельности(),
				ПоказательПоступлениеПоОсновнойДеятельности());
		Иначе
			// Отнесем показатель в прочие поступления или в прочие платежи в зависимости от знака
			ТекущийПоказатель = ?(ВыборкаДенежныеПотоки.Сумма < 0,
				ПоказательПрочиеПлатежи(),
				ПоказательПрочиеПоступления());
		КонецЕсли;
		
		СтрокаПоказателя = ОбработатьСтрокуПоказателя(РезультатДенежныхПотоков,
			ВыборкаДенежныеПотоки.Период, ТекущийПоказатель, ВыборкаДенежныеПотоки.Сумма);
		
		Если ЗначениеЗаполнено(ВыборкаДенежныеПотоки.Статья) Тогда
			ОбработатьСтрокуСтатьи(СтрокаПоказателя, ВыборкаДенежныеПотоки.Статья, ВыборкаДенежныеПотоки.Сумма);
		КонецЕсли;
	КонецЦикла;
	
	// Заполнение показателя 1.3 Поступления от взыскания дебиторской задолженности
	// Определяем по обороту счетов 58,62,70,71,73,76 и вычитаем из других показателей
	Дебиторка = ОСВ_ПогашениеДебиторскойЗадолженности(ДанныеАлгоритма, КонтекстОтчета);
	ВычестьПоказательИзДенежныхПотоков(Дебиторка, РезультатДенежныхПотоков, "П0002001301", ПоложительныйЗнак());
	
	// Заполнение показателя 2.4 Выдача займов третьим лицам
	// Определяем по обороту счетов 58.03, 73.01 и вычитаем из других показателей
	ВыдачаЗаймов = ОСВ_ВыдачаЗаймов(ДанныеАлгоритма, КонтекстОтчета);
	ВычестьПоказательИзДенежныхПотоков(ВыдачаЗаймов, РезультатДенежныхПотоков, "П0002002401", ОтрицательныйЗнак());
	
	// Заполнение показателя 2.6. Уплата текущих налогов
	// Определяем по обороту счетов 68.90 и 69 и вычитаем из других показателей
	УплатаНалогов = ОСВ_Налоги(ДанныеАлгоритма, КонтекстОтчета);
	ВычестьПоказательИзДенежныхПотоков(УплатаНалогов, РезультатДенежныхПотоков, "П0002002601", ОтрицательныйЗнак());
	
	// Показатели 2.7, 5 и 6 заполняем по каждому месяцу
	Для ИндексМесяца = -12 По -1 Цикл
		НачалоМесяца = НачалоМесяца(ДобавитьМесяц(ДатаЗаявления, ИндексМесяца));
		КонецМесяца = КонецМесяца(НачалоМесяца);
		
		КонтекстОтчетаМесяц = БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета();
		КонтекстОтчетаМесяц.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
		БухгалтерскаяОтчетностьБРО.ЗаполнитьПериодОтчета(КонтекстОтчетаМесяц.Период, НачалоМесяца, КонецМесяца);
	
		// Заполнение показателя 2.7. Уплата просроченной задолженности третьих лиц
		// Определяем по обороту счетов 60,76 (в части просрочки) и вычитаем из других показателей
		Кредиторка = ОСВ_ПогашениеКредиторскойЗадолженности(ДанныеАлгоритма, КонтекстОтчетаМесяц);
		ВычестьПоказательИзДенежныхПотоков(Кредиторка, РезультатДенежныхПотоков, "П0002002701", ОтрицательныйЗнак());
		
		// Заполнение показателя 5. ПРОСРОЧЕННАЯ задолженность перед иными кредиторами (в т.ч. банками)
		// Используется при заполнении 2.7, поэтому заполняем перед ним
		ПросроченнаяКредиторка = ПросроченнаяКредиторскаяЗадолженность(Организация, КонтекстОтчетаМесяц.Период.КонецДата);
		ОбработатьСтрокуПоказателя(РезультатДенежныхПотоков, НачалоМесяца, "П0002000501", ПросроченнаяКредиторка.Итог("Просрочено"));
		// Запоминаем кредиторку для расчета показателя 2.7 на следующем шаге
		ДанныеАлгоритма.Вставить("ПросроченнаяКредиторка", ПросроченнаяКредиторка);
	
		// Заполнение показателя 6. Отрицательное сальдо ЕНС с наступившими сроками уплаты и предстоящие начисления
		СальдоЕНС = ЕдиныйНалоговыйСчет.СальдоЕдиногоНалоговогоСчета(Организация, КонтекстОтчетаМесяц.Период.КонецДата);
		ПредстоящиеПлатежи = ПредстоящиеНачисленияПоДаннымУчета(КонтекстОтчетаМесяц);
		ОбработатьСтрокуПоказателя(РезультатДенежныхПотоков, НачалоМесяца, "П0002000601",
			?(СальдоЕНС < 0, -СальдоЕНС, 0) + ПредстоящиеПлатежи);
	КонецЦикла;
	
	ДанныеАлгоритма.ПериодыРаздела2 = РезультатДенежныхПотоков;
	
КонецПроцедуры

Функция СальдоДенежныхСредств(ДанныеАлгоритма, КонтекстОтчета)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", КонтекстОтчета.Период.Начало);
	Запрос.УстановитьПараметр("Организации",   КонтекстОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаДенег", ДанныеАлгоритма.Счета.Деньги);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачальноеСальдо.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетаДенег), , Организация В (&Организации)) КАК НачальноеСальдо";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Сумма;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция ДенежныеПотоки(ДанныеАлгоритма, КонтекстОтчета)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",    КонтекстОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",     КонтекстОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",      КонтекстОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаДенег",         ДанныеАлгоритма.Счета.Деньги);
	Запрос.УстановитьПараметр("СчетаДенегДетально", ДанныеАлгоритма.Счета.ДеньгиДетально);
	Запрос.УстановитьПараметр("СчетаДенегСводно",   ДанныеАлгоритма.Счета.ДеньгиСводно);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетПоСтатьям.Период КАК Период,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств), ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)) КАК Статья,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).ВидДвиженияДенежныхСредств, ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийДенежныхСредств.ПустаяСсылка)) КАК ВидДвижения,
	|	УчетПоСтатьям.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегДетально), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств), Организация В (&Организации), НЕ КорСчет В (&СчетаДенег), ) КАК УчетПоСтатьям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УчетБезСтатей.Период,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийДенежныхСредств.ПустаяСсылка),
	|	УчетБезСтатей.СуммаОборот
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегСводно), , Организация В (&Организации), НЕ КорСчет В (&СчетаДенег), ) КАК УчетБезСтатей";
	
	Возврат Запрос.Выполнить().Выбрать();

КонецФункции

Функция ОСВ_ПогашениеДебиторскойЗадолженности(ДанныеАлгоритма, КонтекстОтчета)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",    КонтекстОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",     КонтекстОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",      КонтекстОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаДенегДетально", ДанныеАлгоритма.Счета.ДеньгиДетально);
	Запрос.УстановитьПараметр("СчетаДенегСводно",   ДанныеАлгоритма.Счета.ДеньгиСводно);
	Запрос.УстановитьПараметр("СчетаРасчетов", СчетаДебиторовПоДоговорам());
	Запрос.УстановитьПараметр("ПрочиеСчета", ПрочиеСчетаДебиторов());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетПоСтатьям.Период КАК Период,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств), ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)) КАК Статья,
	|	УчетПоСтатьям.СуммаОборотДт КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегДетально), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств), Организация В (&Организации), КорСчет В (&СчетаРасчетов, &ПрочиеСчета), ) КАК УчетПоСтатьям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УчетБезСтатей.Период,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка),
	|	УчетБезСтатей.СуммаОборотДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегСводно), , Организация В (&Организации), КорСчет В (&СчетаРасчетов, &ПрочиеСчета), ) КАК УчетБезСтатей";
	
	Дебиторка = Запрос.Выполнить().Выгрузить();
	Возврат Дебиторка;
	
КонецФункции

Функция СчетаДебиторовПоДоговорам()
	
	// Исключаем пассивные счета, например, счет 62.02 - Расчеты по авансам полученным,
	// а также специальные счета 76.18, 76.К и 76.НА
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйВидыСубконто.Ссылка = Хозрасчетный.Ссылка
	|ГДЕ
	|	НЕ Хозрасчетный.ПометкаУдаления
	|	И ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|	И Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетаРасчетов)
	|	И НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключаемые)
	|	И Хозрасчетный.Вид <> ЗНАЧЕНИЕ(ВидСчета.Пассивный)";
	
	СчетаРасчетов = Новый Массив;
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.ФинансовыеВложения);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	Запрос.УстановитьПараметр("СчетаРасчетов", СчетаРасчетов);
	
	СчетаИсключаемые = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключаемые,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.КорректировкаРасчетовПрошлогоПериода));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключаемые,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыНДСНалоговогоАгента));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключаемые,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПриобретениеПравНаИспользованиеРезультатовИнтеллектуальнойДеятельности));
	Запрос.УстановитьПараметр("СчетаИсключаемые", СчетаИсключаемые);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ПрочиеСчетаДебиторов()
	
	Результат = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		СчетаДебиторовВложения());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами_));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоПрочимОперациям));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСФизическимиЛицами));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Билеты));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументамРаботников));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоПрочимУдержаниямРаботников));
	
	Возврат Результат;
	
КонецФункции

Функция СчетаДебиторовВложения()
	
	// Исключаем те счета, где ведется учет в разрезе Договоров
	// Они получаются отдельно
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконтоКонтрагенты
	|		ПО Хозрасчетный.Ссылка = ХозрасчетныйВидыСубконтоКонтрагенты.Ссылка
	|			И (ХозрасчетныйВидыСубконтоКонтрагенты.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконтоДоговоры
	|		ПО Хозрасчетный.Ссылка = ХозрасчетныйВидыСубконтоДоговоры.Ссылка
	|			И (ХозрасчетныйВидыСубконтоДоговоры.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры))
	|ГДЕ
	|	НЕ Хозрасчетный.ПометкаУдаления
	|	И Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ФинансовыеВложения))
	|	И НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках
	|	И ХозрасчетныйВидыСубконтоДоговоры.НомерСтроки ЕСТЬ NULL";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ОСВ_ВыдачаЗаймов(ДанныеАлгоритма, КонтекстОтчета)
	
	СчетаЗаймов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗаймов,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПредоставленныеЗаймы));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗаймов,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоПредоставленнымЗаймам));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",    КонтекстОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",     КонтекстОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",      КонтекстОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаДенегДетально", ДанныеАлгоритма.Счета.ДеньгиДетально);
	Запрос.УстановитьПараметр("СчетаДенегСводно",   ДанныеАлгоритма.Счета.ДеньгиСводно);
	Запрос.УстановитьПараметр("СчетаЗаймов", СчетаЗаймов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетПоСтатьям.Период КАК Период,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств), ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)) КАК Статья,
	|	УчетПоСтатьям.СуммаОборотКт КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегДетально), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств), Организация В (&Организации), КорСчет В (&СчетаЗаймов), ) КАК УчетПоСтатьям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УчетБезСтатей.Период,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка),
	|	УчетБезСтатей.СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегСводно), , Организация В (&Организации), КорСчет В (&СчетаЗаймов), ) КАК УчетБезСтатей";
	
	ОборотПоСчету = Запрос.Выполнить().Выгрузить();
	Возврат ОборотПоСчету;
	
КонецФункции

Функция ОСВ_ПогашениеКредиторскойЗадолженности(ДанныеАлгоритма, КонтекстОтчета)
	
	// Вначале получаем нужный оборот в разрезе договоров и документов.
	// Отсекаем просрочкой, полученной на конец предыдущего месяца и отраженной в показателе стр. 5
	
	ПросроченнаяКредиторка = Неопределено;
	Если Не ДанныеАлгоритма.Свойство("ПросроченнаяКредиторка", ПросроченнаяКредиторка) Тогда
		ПредыдущаяДата = НачалоМесяца(КонтекстОтчета.Период.КонецДата) - 1;
		ПросроченнаяКредиторка = ПросроченнаяКредиторскаяЗадолженность(КонтекстОтчета.Организации, ПредыдущаяДата);
	КонецЕсли;
	
	Если ПросроченнаяКредиторка = Неопределено
		Или ПросроченнаяКредиторка.Итог("Просрочено") = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВидыСубконтоБезДокументов = Новый Массив;
	ВидыСубконтоБезДокументов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконтоБезДокументов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	ВидыСубконтоСДокументами = Новый Массив;
	ВидыСубконтоСДокументами.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконтоСДокументами.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	ВидыСубконтоСДокументами.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
	
	СчетаРасчетов = СчетаКредиторовПоДоговорам();
	СчетаРасчетовПоДокументам = ОтобратьСчетаПоРасчетнымДокументам(СчетаРасчетов);
	СчетаРасчетовБезДокументов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		СчетаРасчетов, СчетаРасчетовПоДокументам);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",    КонтекстОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",     КонтекстОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",      КонтекстОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаДенегДетально", ДанныеАлгоритма.Счета.ДеньгиДетально);
	Запрос.УстановитьПараметр("СчетаДенегСводно",   ДанныеАлгоритма.Счета.ДеньгиСводно);
	Запрос.УстановитьПараметр("СчетаРасчетовПоДокументам", СчетаРасчетовПоДокументам);
	Запрос.УстановитьПараметр("СчетаРасчетовБезДокументов", СчетаРасчетовБезДокументов);
	Запрос.УстановитьПараметр("СубконтоРасчетов", ВидыСубконтоСДокументами);
	Запрос.УстановитьПараметр("СубконтоРасчетовБезДокументов", ВидыСубконтоБезДокументов);
	Запрос.УстановитьПараметр("ПросроченнаяКредиторка", ПросроченнаяКредиторка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПросроченнаяКредиторка.СчетРасчетов КАК СчетРасчетов,
	|	ПросроченнаяКредиторка.Кредитор КАК Кредитор,
	|	ПросроченнаяКредиторка.Договор КАК Договор,
	|	ПросроченнаяКредиторка.Документ КАК Документ,
	|	ПросроченнаяКредиторка.Просрочено КАК Просрочено
	|ПОМЕСТИТЬ ПросроченнаяКредиторка
	|ИЗ
	|	&ПросроченнаяКредиторка КАК ПросроченнаяКредиторка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств), ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)) КАК Статья,
	|	УчетПоСтатьям.СуммаОборотКт КАК Сумма,
	|	УчетПоСтатьям.КорСчет КАК КорСчет,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.КорСубконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.КорСубконто2 КАК Справочник.ДоговорыКонтрагентов), ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК Договор,
	|	УчетПоСтатьям.КорСубконто3 КАК Документ
	|ПОМЕСТИТЬ ДенежныеПотокиКредиторка
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаДенегДетально), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств), Организация В (&Организации), КорСчет В (&СчетаРасчетовПоДокументам), &СубконтоРасчетов) КАК УчетПоСтатьям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка),
	|	УчетБезСтатей.СуммаОборотКт,
	|	УчетБезСтатей.КорСчет,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетБезСтатей.КорСубконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетБезСтатей.КорСубконто2 КАК Справочник.ДоговорыКонтрагентов), ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)),
	|	УчетБезСтатей.КорСубконто3
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаДенегСводно), , Организация В (&Организации), КорСчет В (&СчетаРасчетовПоДокументам), &СубконтоРасчетов) КАК УчетБезСтатей
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьямИБезДокументов.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств), ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)),
	|	УчетПоСтатьямИБезДокументов.СуммаОборотКт,
	|	УчетПоСтатьямИБезДокументов.КорСчет,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьямИБезДокументов.КорСубконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьямИБезДокументов.КорСубконто2 КАК Справочник.ДоговорыКонтрагентов), ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)),
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаДенегДетально), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств), Организация В (&Организации), КорСчет В (&СчетаРасчетовБезДокументов), &СубконтоРасчетовБезДокументов) КАК УчетПоСтатьямИБезДокументов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка),
	|	УчетБезСтатейИБезДокументов.СуммаОборотКт,
	|	УчетБезСтатейИБезДокументов.КорСчет,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетБезСтатейИБезДокументов.КорСубконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетБезСтатейИБезДокументов.КорСубконто2 КАК Справочник.ДоговорыКонтрагентов), ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)),
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаДенегСводно), , Организация В (&Организации), КорСчет В (&СчетаРасчетовБезДокументов), &СубконтоРасчетовБезДокументов) КАК УчетБезСтатейИБезДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ДенежныеПотокиКредиторкаСводно.Сумма >= ПросроченнаяКредиторка.Просрочено
	|			ТОГДА ПросроченнаяКредиторка.Просрочено
	|		ИНАЧЕ ДенежныеПотокиКредиторкаСводно.Сумма
	|	КОНЕЦ / ДенежныеПотокиКредиторкаСводно.Сумма КАК Процент,
	|	ДенежныеПотокиКредиторкаСводно.КорСчет КАК КорСчет,
	|	ДенежныеПотокиКредиторкаСводно.Контрагент КАК Контрагент,
	|	ДенежныеПотокиКредиторкаСводно.Договор КАК Договор,
	|	ДенежныеПотокиКредиторкаСводно.Документ КАК Документ
	|ПОМЕСТИТЬ ДенежныеПотокиКредиторкаПоПросрочке
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(ДенежныеПотокиКредиторка.Сумма) КАК Сумма,
	|		ДенежныеПотокиКредиторка.КорСчет КАК КорСчет,
	|		ДенежныеПотокиКредиторка.Контрагент КАК Контрагент,
	|		ДенежныеПотокиКредиторка.Договор КАК Договор,
	|		ДенежныеПотокиКредиторка.Документ КАК Документ
	|	ИЗ
	|		ДенежныеПотокиКредиторка КАК ДенежныеПотокиКредиторка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДенежныеПотокиКредиторка.КорСчет,
	|		ДенежныеПотокиКредиторка.Контрагент,
	|		ДенежныеПотокиКредиторка.Договор,
	|		ДенежныеПотокиКредиторка.Документ) КАК ДенежныеПотокиКредиторкаСводно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПросроченнаяКредиторка КАК ПросроченнаяКредиторка
	|		ПО ДенежныеПотокиКредиторкаСводно.КорСчет = ПросроченнаяКредиторка.СчетРасчетов
	|			И ДенежныеПотокиКредиторкаСводно.Контрагент = ПросроченнаяКредиторка.Кредитор
	|			И ДенежныеПотокиКредиторкаСводно.Договор = ПросроченнаяКредиторка.Договор
	|			И ДенежныеПотокиКредиторкаСводно.Документ = ПросроченнаяКредиторка.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПросроченнаяКредиторка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ДенежныеПотокиКредиторка.Статья КАК Статья,
	|	ДенежныеПотокиКредиторка.Сумма * ДенежныеПотокиКредиторкаПоПросрочке.Процент КАК Сумма
	|ИЗ
	|	ДенежныеПотокиКредиторка КАК ДенежныеПотокиКредиторка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДенежныеПотокиКредиторкаПоПросрочке КАК ДенежныеПотокиКредиторкаПоПросрочке
	|		ПО ДенежныеПотокиКредиторка.КорСчет = ДенежныеПотокиКредиторкаПоПросрочке.КорСчет
	|			И ДенежныеПотокиКредиторка.Контрагент = ДенежныеПотокиКредиторкаПоПросрочке.Контрагент
	|			И ДенежныеПотокиКредиторка.Договор = ДенежныеПотокиКредиторкаПоПросрочке.Договор
	|			И ДенежныеПотокиКредиторка.Документ = ДенежныеПотокиКредиторкаПоПросрочке.Документ";
	
	Кредиторка = Запрос.Выполнить().Выгрузить();
	Возврат Кредиторка;
	
КонецФункции

Функция СчетаКредиторовПоДоговорам()
	
	// Оставляем только те счета, по которым можно вычислить просроченную задолженность
	// Исключаем активные счета, например, счет 60.02 - Расчеты по авансам выданным,
	// а также специальные счета 76.К и 76.НА
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйВидыСубконто.Ссылка = Хозрасчетный.Ссылка
	|ГДЕ
	|	НЕ Хозрасчетный.ПометкаУдаления
	|	И ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|	И Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетаРасчетов)
	|	И НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках
	|	И НЕ Хозрасчетный.Ссылка В (&СчетаИсключаемые)
	|	И Хозрасчетный.Вид <> ЗНАЧЕНИЕ(ВидСчета.Активный)";
	
	СчетаРасчетов = Новый Массив;
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	Запрос.УстановитьПараметр("СчетаРасчетов", СчетаРасчетов);
	
	СчетаИсключаемые = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключаемые,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.КорректировкаРасчетовПрошлогоПериода));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключаемые,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыНДСНалоговогоАгента));
	Запрос.УстановитьПараметр("СчетаИсключаемые", СчетаИсключаемые);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ОтобратьСчетаПоРасчетнымДокументам(Счета)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйВидыСубконто.Ссылка = Хозрасчетный.Ссылка
	|ГДЕ
	|	НЕ Хозрасчетный.ПометкаУдаления
	|	И ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)
	|	И Хозрасчетный.Ссылка В ИЕРАРХИИ(&Счета)";
	
	Запрос.УстановитьПараметр("Счета", Счета);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ОСВ_Налоги(ДанныеАлгоритма, КонтекстОтчета)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("НачалоПериода",    КонтекстОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",     КонтекстОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",      КонтекстОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаДенегДетально", ДанныеАлгоритма.Счета.ДеньгиДетально);
	Запрос.УстановитьПараметр("СчетаДенегСводно",   ДанныеАлгоритма.Счета.ДеньгиСводно);
	Запрос.УстановитьПараметр("Счет68", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам));
	Запрос.УстановитьПараметр("Счет69", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетПоСтатьям.Период КАК Период,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(УчетПоСтатьям.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств), ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)) КАК Статья,
	|	УчетПоСтатьям.СуммаОборотКт КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегДетально), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств), Организация В (&Организации), КорСчет В (&Счет68, &Счет69), ) КАК УчетПоСтатьям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УчетБезСтатей.Период,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка),
	|	УчетБезСтатей.СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет В (&СчетаДенегСводно), , Организация В (&Организации), КорСчет В (&Счет68, &Счет69), ) КАК УчетБезСтатей";
	
	ОборотПоСчету = Запрос.Выполнить().Выгрузить();
	Возврат ОборотПоСчету;
	
КонецФункции

Функция ПредстоящиеНачисленияПоДаннымУчета(КонтекстОтчета)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетНачисленияНалоговНаЕНС.СчетУчета КАК СчетУчета,
	|	СУММА(РасчетНачисленияНалоговНаЕНС.Начислено) КАК Начислено
	|ИЗ
	|	РегистрСведений.РасчетНачисленияНалоговНаЕНС КАК РасчетНачисленияНалоговНаЕНС
	|ГДЕ
	|	РасчетНачисленияНалоговНаЕНС.ПериодРасчета <= &Период
	|	И РасчетНачисленияНалоговНаЕНС.Организация В(&Организации)
	|	И РасчетНачисленияНалоговНаЕНС.СрокУплаты > &Период
	|	И НЕ РасчетНачисленияНалоговНаЕНС.СчетУчета В (&СчетаИсключаемые)
	|	И РасчетНачисленияНалоговНаЕНС.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетНачисленияНалоговНаЕНС.СчетУчета
	|ИТОГИ
	|	СУММА(Начислено)
	|ПО
	|	ОБЩИЕ";
	
	СчетаИсключаемые = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключаемые,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключаемые,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДФЛ_ИП));
	
	Запрос.УстановитьПараметр("Период", КонтекстОтчета.Период.КонецДата);
	Запрос.УстановитьПараметр("Организации", КонтекстОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаИсключаемые", СчетаИсключаемые);
	
	ПредстоящиеПлатежи = 0;
	
	// Фиксированные взносы ИП за себя рассчитываются концом года.
	// Если регламентная операция не проведена, то необходимо дополнительно рассчитать и включить в предстоящие начисления
	ЭтоИП = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоИП(КонтекстОтчета.Организации[0]);
	РассчитатьВзносыИПЗаСебя = ЭтоИП И Месяц(КонтекстОтчета.Период.КонецДата) <> 12;
	
	ВыборкаИтого = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаИтого.Следующий() Тогда
		ПредстоящиеПлатежи = ВыборкаИтого.Начислено;
		Если РассчитатьВзносыИПЗаСебя Тогда
			ОтборЗаписи = Новый Структура("СчетУчета", ПланыСчетов.Хозрасчетный.СтраховыеВзносыЕдиныйТариф_ИП);
			Выборка = ВыборкаИтого.Выбрать();
			Если Выборка.НайтиСледующий(ОтборЗаписи) Тогда
				РассчитатьВзносыИПЗаСебя = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если РассчитатьВзносыИПЗаСебя Тогда
		Взносы = УчетСтраховыхВзносовИП.ФиксированныеСтраховыеВзносыКУплате(КонтекстОтчета.Организации[0],
			КонтекстОтчета.Период.КонецДата, Перечисления.Периодичность.Год);
		ПредстоящиеПлатежи = ПредстоящиеПлатежи + Взносы.СуммаВзносаПФР + Взносы.СуммаВзносаФФОМС
			+ Взносы.СуммаВзносаФСС + Взносы.СуммаВзносаЕдиныйТариф;
	КонецЕсли;
	
	Возврат ПредстоящиеПлатежи;
	
КонецФункции

Функция ОбработатьСтрокуПоказателя(Коллекция, Период, Показатель, Сумма)
	
	ОтборПоказателя = Новый Структура;
	ОтборПоказателя.Вставить("Период", Период);
	ОтборПоказателя.Вставить("Показатель", Показатель);
	НайденныйПоказатель = Коллекция.Строки.НайтиСтроки(ОтборПоказателя, Ложь);
	Если НайденныйПоказатель.Количество() > 0 Тогда
		СтрокаПоказателя = НайденныйПоказатель[0];
	Иначе
		СтрокаПоказателя = Коллекция.Строки.Добавить();
		СтрокаПоказателя.Период = Период;
		СтрокаПоказателя.Показатель = Показатель;
		СтрокаПоказателя.Сумма = 0;
	КонецЕсли;
	СтрокаПоказателя.Сумма = СтрокаПоказателя.Сумма + Сумма;
	Возврат СтрокаПоказателя;
	
КонецФункции

Процедура ОбработатьСтрокуСтатьи(СтрокаПоказателя, Статья, Сумма)
	
	ОтборСтатьи = Новый Структура("Статья", Статья);
	НайденнаяСтатья = СтрокаПоказателя.Строки.НайтиСтроки(ОтборСтатьи, Ложь);
	Если НайденнаяСтатья.Количество() > 0 Тогда
		СтрокаСтатьи = НайденнаяСтатья[0];
	Иначе
		СтрокаСтатьи = СтрокаПоказателя.Строки.Добавить();
		СтрокаСтатьи.Период = СтрокаПоказателя.Период;
		СтрокаСтатьи.Показатель = СтрокаПоказателя.Показатель;
		СтрокаСтатьи.Статья = Статья;
		СтрокаСтатьи.Сумма = 0;
	КонецЕсли;
	СтрокаСтатьи.Сумма = СтрокаСтатьи.Сумма + Сумма;
	
КонецПроцедуры

Процедура ВычестьПоказательИзДенежныхПотоков(ДанныеОборотки, РезультатДенежныхПотоков, Показатель, Знак)
	
	Если ДанныеОборотки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПоПериодам = ДанныеОборотки.Скопировать();
	ТаблицаПоПериодам.Свернуть("Период", "Сумма");
	
	Для Каждого СтрокаПериода Из ТаблицаПоПериодам Цикл
		Неразобрано = СтрокаПериода.Сумма;
		ОтборПоПериоду = Новый Структура("Период", СтрокаПериода.Период);
		ОбрабатываемыеСтроки = ДанныеОборотки.НайтиСтроки(ОтборПоПериоду);
		Для Каждого СтрокаОборотки Из ОбрабатываемыеСтроки Цикл
			Если Неразобрано = 0 Тогда
				Прервать;
			КонецЕсли;
			ПоискСтроки = Новый Структура;
			ПоискСтроки.Вставить("Период", СтрокаОборотки.Период);
			Если Не ЗначениеЗаполнено(СтрокаОборотки.Статья) Тогда
				ПоискСтроки.Вставить("Показатель", ?(Знак = ОтрицательныйЗнак(),
					ПоказательПлатежиПоОсновнойДеятельности(), ПоказательПоступлениеПоОсновнойДеятельности()));
				НайденныеСтроки = РезультатДенежныхПотоков.Строки.НайтиСтроки(ПоискСтроки, Ложь);
			Иначе
				ПоискСтроки.Вставить("Статья", СтрокаОборотки.Статья);
				НайденныеСтроки = РезультатДенежныхПотоков.Строки.НайтиСтроки(ПоискСтроки, Истина);
			КонецЕсли;
			Для Каждого СтрокаПоказателя Из НайденныеСтроки Цикл
				Если Неразобрано = 0 Тогда
					Прервать;
				КонецЕсли;
				Вычесть = Макс(0, Мин(СтрокаОборотки.Сумма, Знак * СтрокаПоказателя.Сумма));
				СтрокаПоказателя.Сумма = СтрокаПоказателя.Сумма - Знак * Вычесть;
				Если ЗначениеЗаполнено(СтрокаПоказателя.Родитель) Тогда
					СтрокаПоказателя.Родитель.Сумма = СтрокаПоказателя.Родитель.Сумма - Знак * Вычесть;
				КонецЕсли;
				Неразобрано = Неразобрано - Вычесть;
			КонецЦикла;
		КонецЦикла;
		
		ОбработатьСтрокуПоказателя(РезультатДенежныхПотоков,
			СтрокаПериода.Период, Показатель, Знак * (СтрокаПериода.Сумма - Неразобрано));
	КонецЦикла;
	
КонецПроцедуры

Функция НоваяТаблицаПрочихДенежныхПотоков()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Период");
	Таблица.Колонки.Добавить("Показатель");
	Таблица.Колонки.Добавить("Сумма");
	
	Возврат Таблица;
	
КонецФункции

Функция ПоложительныйЗнак()
	
	Возврат 1;
	
КонецФункции

Функция ОтрицательныйЗнак()
	
	Возврат -1;
	
КонецФункции

Функция ПоказательПоступлениеПоОсновнойДеятельности()
	
	Возврат "П0002001101";
	
КонецФункции

Функция ПоказательПлатежиПоОсновнойДеятельности()
	
	Возврат "П0002002101";
	
КонецФункции

Функция ПоказательПрочиеПоступления()
	
	Возврат "П0002001501";
	
КонецФункции

Функция КоличествоСтрокПрочихПоступлений()
	
	Возврат 10;
	
КонецФункции

Функция НачальныйНомерПрочихПоступлений()
	
	Возврат 6;
	
КонецФункции

Функция КонечныйНомерПрочихПоступлений()
	
	Возврат НачальныйНомерПрочихПоступлений() + КоличествоСтрокПрочихПоступлений() - 1;
	
КонецФункции

Функция ПоказательПрочиеПлатежи()
	
	Возврат "П0002002501";
	
КонецФункции

Функция КоличествоСтрокПрочихПлатежей()
	
	Возврат 10;
	
КонецФункции

Функция НачальныйНомерПрочихПлатежей()
	
	Возврат 1;
	
КонецФункции

Функция КонечныйНомерПрочихПлатежей()
	
	Возврат НачальныйНомерПрочихПлатежей() + КоличествоСтрокПрочихПлатежей() - 1;
	
КонецФункции

Функция ИмяПоказателяПоступленияРаздела2(ИндексСтроки, ИндексПериода = Неопределено)
	
	НомерСтроки = НачальныйНомерПрочихПоступлений() + ИндексСтроки;
	
	Если НомерСтроки >= КонечныйНомерПрочихПоступлений() Тогда
		ИмяПоказателя = "П00020115"; // 1.15
	Иначе
		Префикс = ?(НомерСтроки < 10, "П0002001", "П000201"); // 1.6 - 1.14
		ФорматированныйНомер = Формат(НомерСтроки, "ЧГ=");
		ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1%2", Префикс, ФорматированныйНомер);
	КонецЕсли;
	
	Если ИндексПериода = Неопределено Тогда
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%10001", ИмяПоказателя);
	Иначе
		ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%101", ИмяПоказателя);
		Возврат ИмяПоказателяПериодаРаздела2(ИмяПоказателя, ИндексПериода);
	КонецЕсли;
	
КонецФункции

Функция ЗначениеПоказателяПоступленияРаздела2(Значение, ИндексСтроки, ВсегоСтрок)
	
	Если (ИндексСтроки + 1) >= КоличествоСтрокПрочихПоступлений() И ВсегоСтрок >= КоличествоСтрокПрочихПоступлений() Тогда
		Возврат НСтр("ru = 'Прочие поступления'");
	Иначе
		Возврат Строка(Значение);
	КонецЕсли;
	
КонецФункции

Функция ИмяПоказателяПлатежиРаздела2(ИндексСтроки, ИндексПериода = Неопределено)
	
	НомерСтроки = НачальныйНомерПрочихПлатежей() + ИндексСтроки;
	
	Если НомерСтроки >= КонечныйНомерПрочихПлатежей() Тогда
		ИмяПоказателя = "П00022510"; // 2.5.10
	Иначе
		Префикс = ?(НомерСтроки < 10, "П0002025", "П000225"); // 2.5.1 - 2.5.9
		ФорматированныйНомер = Формат(НомерСтроки, "ЧГ=");
		ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1%2", Префикс, ФорматированныйНомер);
	КонецЕсли;
	
	Если ИндексПериода = Неопределено Тогда
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%10001", ИмяПоказателя);
	Иначе
		ИмяПоказателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%101", ИмяПоказателя);
		Возврат ИмяПоказателяПериодаРаздела2(ИмяПоказателя, ИндексПериода);
	КонецЕсли;
	
КонецФункции

Функция ЗначениеПоказателяПлатежиРаздела2(Значение, ИндексСтроки, ВсегоСтрок)
	
	Если (ИндексСтроки + 1) >= КоличествоСтрокПрочихПлатежей() И ВсегоСтрок >= КоличествоСтрокПрочихПлатежей() Тогда
		Возврат НСтр("ru = 'Прочие платежи'");
	Иначе
		Возврат Строка(Значение);
	КонецЕсли;
	
КонецФункции

Функция ИмяПоказателяПериодаРаздела2(ИмяПоказателя, ИндексПериода)
	
	Возврат СтрШаблон("%1%2", ИмяПоказателя, Формат(ИндексПериода, "ЧЦ=2; ЧН=; ЧВН=; ЧГ="));
	
КонецФункции

Функция ИндексПериода(Период, ДатаЗаявления, КэшИндексПериода)
	
	ИндексПериода = КэшИндексПериода[Период];
	Если ИндексПериода = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РАЗНОСТЬДАТ(&Период, &ДатаЗаявления, МЕСЯЦ) КАК ИндексПериода";
		
		Запрос.УстановитьПараметр("Период", Период);
		Запрос.УстановитьПараметр("ДатаЗаявления", НачалоМесяца(ДатаЗаявления));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ИндексПериода = Выборка.ИндексПериода;
		Иначе
			ИндексПериода = 0;
		КонецЕсли;
		КэшИндексПериода[Период] = ИндексПериода;
	КонецЕсли;
	
	Возврат ИндексПериода;
	
КонецФункции

Процедура ЗаполнитьСуммовойПоказательРаздела2ВКонтейнер(Раздел2, ИмяПоказателя, Сумма)
	
	Если СтрНачинаетсяС(ИмяПоказателя, "П0002002")
		Или СтрНачинаетсяС(ИмяПоказателя, "П0002025") Тогда
		// Платежи заполняются с противоположным знаком
		Раздел2[ИмяПоказателя] = -Сумма;
	Иначе
		Раздел2[ИмяПоказателя] = Сумма;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
