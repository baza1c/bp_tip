#Область ПрограммныйИнтерфейс

// Подробнее
// См. Обработки.КалендарьОтчетности.Форма.Подключаемый_ОтборыПоЗадачамПриИзменении
//
Процедура СобратьСтатистикуИспользованияОтборов(Форма) Экспорт
	
	СобытиеСтатистики = СтрШаблон(НСтр("ru = 'КалендарьОтчетности.ОтборыПоЗадачам.%1%2%3%4%5'"),
		Число(Форма.ПоДокументамСЭДО),
		Число(Форма.ПоНалоговымПлатежам),
		Число(Форма.ПоОтчетам),
		Число(Форма.ПоТребованиямФНС),
		Число(Форма.ПоУведомлениямЕНС));
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(СобытиеСтатистики);
	
КонецПроцедуры

// Подробнее
// См. Обработки.КалендарьОтчетности.Форма.ВыборПериода
//
Процедура ЗаписатьСтатистикуВыбораПериода(Форма, ПредставлениеПериода) Экспорт
	
	Если Форма.ПоказыватьМоиПриложения Тогда
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(
			СтрШаблон("КалендарьОтчетности.ВыбранПредопределенныйПериод.%1", ПредставлениеПериода));
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьСобытиеСтатистикиПоПоказателям(ИмяСобытия) Экспорт
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяСобытия);
	
КонецПроцедуры

// Подробнее
// См. Обработки.КалендарьОтчетности.Форма.ОрганизацияОбработкаВыбора
//
Процедура ОрганизацияОбработкаВыбора(ВыбранноеЗначение, ПереформироватьКалендарь, СтандартнаяОбработка) Экспорт
	
	Если ВыбранноеЗначение = "ОткрытьМоиПриложения" Тогда
		ПереформироватьКалендарь = Ложь;
		СтандартнаяОбработка = Ложь;
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(
			"КалендарьОтчетности.ОткрытыМоиПриложения.ИзОтбораОрганизаций");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. Обработки.КалендарьОтчетности.Форма.ОткрытьФормуНалогиИОтчетыНаТекущейЗакладке
//
Процедура ОткрытьНастройкуНалоговОтчетов(Организация, ИсточникПерехода, ТекущаяЗакладка = Неопределено) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПолныйСписок", Истина);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыОткрытия.Вставить("Организация", Организация);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущаяЗакладка) Тогда
		ПараметрыОткрытия.Вставить("ТекущаяЗакладка", ТекущаяЗакладка);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты", ПараметрыОткрытия);
	
	Если ЗначениеЗаполнено(ИсточникПерехода) Тогда
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(
			СтрШаблон("КалендарьОтчетности.ПереходВНастройкуНалоговОтчетов.%1", ИсточникПерехода));
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. Обработки.КалендарьОтчетности.Форма.ОткрытьМоиПриложения
//
Процедура ОткрытьФормуМоиПриложения(Форма) Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура(
		"МоиПриложенияКонтрольнаяСумма",
		Форма.МоиПриложенияКонтрольнаяСумма);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПослеЗакрытияМоиПриложения", Форма);
	ОткрытьФорму(
		"ОбщаяФорма.КалендарьОтчетностиМоиПриложения",
		ПараметрыОткрытияФормы,,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ЗаполнитьПояснениеДляДокументаСЭДОНаБоковойПанели
//
Процедура ЗаполнитьПояснениеДляДокументаСЭДОНаБоковойПанели(Форма, ПараметрыОтчета) Экспорт
	
	Если ТипЗнч(ПараметрыОтчета.Правило) = Тип("ДокументСсылка.ВходящийЗапросФССДляРасчетаПособия") Тогда
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Заголовок =
			НСтр("ru='Запрос СФР о предоставлении сведений, необходимых для назначения пособия.'");
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Видимость = Истина;
	ИначеЕсли ТипЗнч(ПараметрыОтчета.Правило) = Тип("ДокументСсылка.ИзвещениеФСС") Тогда
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Заголовок =
			НСтр("ru='Извещение СФР о необходимости предоставить недостающие сведения, по ранее отправленным документам.'");
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Видимость = Истина;
	ИначеЕсли ТипЗнч(ПараметрыОтчета.Правило) = Тип("ДокументСсылка.ОтказВВозмещенииВыплатРодителямДетейИнвалидов") Тогда
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Заголовок =
			НСтр("ru='Отказ СФР по заявлению о возмещении выплат родителям детей инвалидов.'");
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Видимость = Истина;
	ИначеЕсли ТипЗнч(ПараметрыОтчета.Правило) = Тип("ДокументСсылка.УведомлениеОСтатусеВыплатыПособия") Тогда
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Заголовок =
			НСтр("ru='Уведомление об изменении статуса выплаты пособия.'");
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Видимость = Истина;
	Иначе
		Форма.Элементы.ДекорацияПанельОтчетИнформация.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.ЗаполнитьПояснениеДляДокументаСЭДОНаБоковойПанели
//
Процедура ПриВыполненииПереходаПоНавигационнойСсылке(ПараметрыСсылки, ПереходВыполнен) Экспорт
	
	Если ПараметрыСсылки["action"] = КалендарьОтчетностиКлиентСервер.ИмяДействияОткрытьПомощникОчетУведомление() Тогда
		ТребуемыеПараметры = КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиПодготовкаОтчета();
		ПереданыВсеПараметры = КалендарьОтчетностиКлиентСервер.ПереданыВсеНеобходимыеПараметрыПереходаПоСсылке(
			ПараметрыСсылки, ТребуемыеПараметры);
		Если Не ПереданыВсеПараметры Тогда
			ПереходВыполнен = Ложь;
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ПараметрыСсылки["action"] = КалендарьОтчетностиКлиентСервер.ИмяДействияОткрытьТребованиеФНС() Тогда
		ТребуемыеПараметры = КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиТребованиеФНС();
		ПереданыВсеПараметры = КалендарьОтчетностиКлиентСервер.ПереданыВсеНеобходимыеПараметрыПереходаПоСсылке(
			ПараметрыСсылки, ТребуемыеПараметры);
		Если Не ПереданыВсеПараметры Тогда
			ПереходВыполнен = Ложь;
			Возврат;
		КонецЕсли;
	ИначеЕсли ПараметрыСсылки["action"] = КалендарьОтчетностиКлиентСервер.ИмяДействияОткрытьДокументСЭДО() Тогда
		ТребуемыеПараметры = КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылкиДокументСЭДО();
		ПереданыВсеПараметры = КалендарьОтчетностиКлиентСервер.ПереданыВсеНеобходимыеПараметрыПереходаПоСсылке(
			ПараметрыСсылки, ТребуемыеПараметры);
		Если Не ПереданыВсеПараметры Тогда
			ПереходВыполнен = Ложь;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Обработчик перехода по навигационной ссылки срабатывает дважды:
	// 1. В приложении, из которого выполняется переход.
	// 2. В приложении, в которое выполнен переход.
	// В первом случае не следует обрабатывать переход по навигационной ссылке.
	Если ТребуемыеПараметры.application_id = КалендарьОтчетностиВызовСервера.ИдентификаторЭтогоПриложения() Тогда
		ПереходВыполнен = Ложь;
		Возврат;
	КонецЕсли;
	
	ПараметрыПомощника = Неопределено;
	Если ТребуемыеПараметры.action = КалендарьОтчетностиКлиентСервер.ИмяДействияОткрытьПомощникОчетУведомление() Тогда
		ПараметрыПомощника = КалендарьОтчетностиВызовСервера.ПодготовитьПараметрыПомощника(ТребуемыеПараметры);
	ИначеЕсли ТребуемыеПараметры.action = КалендарьОтчетностиКлиентСервер.ИмяДействияОткрытьТребованиеФНС() Тогда
		ПараметрыПомощника = КалендарьОтчетностиВызовСервера.ПодготовитьПараметрыТребованияФНС(ТребуемыеПараметры);
	ИначеЕсли ТребуемыеПараметры.action = КалендарьОтчетностиКлиентСервер.ИмяДействияОткрытьДокументСЭДО() Тогда
		ПараметрыПомощника = КалендарьОтчетностиВызовСервера.ПодготовитьПараметрыДокументаСЭДО(ТребуемыеПараметры);
	Иначе
		ПереходВыполнен = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПомощника = Неопределено Тогда
		ПереходВыполнен = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПомощника.Правило) = Тип("ДокументСсылка.ТранспортноеСообщение") Тогда
		// Расшифровка сообщений выполняется синхронно.
		// После расшифровки будет открыта форма со списком полученных сообщений.
		ВыполнениеЗадачБухгалтераКлиент.РасшифроватьСообщения(ПараметрыПомощника.Организация);
	ИначеЕсли ВыполнениеЗадачБухгалтераКлиентСервер.ЭтоДокументСЭДО(ПараметрыПомощника.Правило) Тогда
		Если ПараметрыПомощника.ФормаСписка <> Неопределено Тогда // несколько документов в ячейке таблицы, открываем форму списка.
			ПараметрыФормы = Новый Структура("ТекущаяСтрока", ПараметрыПомощника.Правило);
			ОткрытьФорму(ПараметрыПомощника.ФормаСписка, ПараметрыФормы);
		Иначе
			ПоказатьЗначение(, ПараметрыПомощника.Правило);
		КонецЕсли;
	Иначе
		КалендарьОтчетностиКлиент.ПоказатьПомощник(ПараметрыПомощника);
	КонецЕсли;
	ПереходВыполнен = Истина;
	
КонецПроцедуры

// Подробнее
// См. Обработка.КалендарьОтчетности.Форма.СформироватьЗаголовокОтборыПоЗадачам
//
Процедура ПриОпределенииМаксимальногоКоличестваВидовОтбора(КоличествоВидовОтбора) Экспорт
	
	КоличествоВидовОтбора = 4;
	
КонецПроцедуры

#КонецОбласти