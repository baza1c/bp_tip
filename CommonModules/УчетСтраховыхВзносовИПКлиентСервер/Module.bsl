#Область ПрограммныйИнтерфейс

// Определяет, применяется или нет районный коэффициет при расчете страховых взносов в ФСС в указанном периоде
// 
// Параметры:
//  Период - Дата - страховой период
//
// Возвращаемое значение:
//  Булево - применяется или нет районный коэффициент.
//
Функция ПрименяетсяРайонныйКоэффициентСтраховыхВзносовФСС(Период) Экспорт 
	
	// С 2021 года при определении размера страховых взносов в ФСС применяется районный коэффициент
	// (п. 1 ст. 4, ч. 1 ст. 8 Федерального закона от 29.12.2020 № 478-ФЗ).
	
	Возврат НачалоГода(Период) >= '20210101';
	
КонецФункции

// Возвращает признак доступности тарифа страховых взносов ИП для военных пенсионеров
// п. 1.4. ст. 430 НК РФ
//
// Параметры:
//   Период - Дата - Проверямая дата доступности тарифа
//
// Возвращаемое значение:
//   Булево
//
Функция ДоступенТарифДляВоенныхПенсионеров(Период) Экспорт
	
	// п. 1.4. ст. 430 НК РФ
	// ИП, получающие военную пенсию, уплачивают страховые взносы на ОМС в размере 19,8922% от совокупного фиксированного размера страховых взносов
	
	ДатаНачалаДействия = ДатаНачалаДействияТарифаДляВоенныхПенсионеров();
	
	Возврат Период >= ДатаНачалаДействия;
	
КонецФункции

// Возвращает дату начала действия тарифа страховых взносов ИП для военных пенсионеров
// п. 1.4. ст. 430 НК РФ
//
// Возвращаемое значение:
//   Дата
//
Функция ДатаНачалаДействияТарифаДляВоенныхПенсионеров() Экспорт
	
	// Письмо ФНС России от 18.09.2023 № БС-4-11/11858@:
	// тариф действует с момента вступления в силу Федерального закона N 257-ФЗ с 24.06.2023
	Возврат '20230624';
	
КонецФункции

// Возвращает признак доступности тарифа страховых взносов ИП для участников СЭЗ
// п. 1.3. ст. 430 НК РФ
//
// Параметры:
//   Период - Дата - Проверямая дата доступности тарифа
//
// Возвращаемое значение:
//   Булево
//
Функция ПрименяетсяТарифУчастникСЭЗ(Период) Экспорт
	
	// Федеральный закон от 04.08.2023 N 427-ФЗ
	ПериодДействия = Новый Структура;
	ПериодДействия.Вставить("ДатаНачала", '20230101');
	ПериодДействия.Вставить("ДатаОкончания", '20251231235959');
	
	Возврат Период >= ПериодДействия.ДатаНачала И Период <= ПериодДействия.ДатаОкончания;
	
КонецФункции

// Возвращает статус задачи по уплате взносов
//
// Возвращаемое значение:
//   Строка
//
Функция СтатусОплатыВзноса() Экспорт
	
	Возврат Строка(ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Оплачено"));
	
КонецФункции

// Возвращает дату начала включения единого тарифа в Единый налоговый платеж
// п. 2. ст. 432 НК РФ
// 
// Параметры:
//    Период - Дата - Проверяемая дата включения единого тарифа в ЕНП
//
// Возвращаемое значение:
//   Дата
//
Функция ЕдиныйТарифВключенВЕдиныйНалоговыйПлатеж(Период) Экспорт
	
	// Федеральный закон от 08.08.2024 N 259-ФЗ
	ДатаВключения = '20250101';
	
	Возврат Период >= ДатаВключения;
	
КонецФункции

// Возвращает дату начала действия возможности выбора страховой суммы при добровольном социальном страховании
// 
// Возвращаемое значение:
//  Дата
//
Функция НачалоДействияВыбораСуммыПоСоциальномуСтрахованию() Экспорт
	
	Возврат '20260101';
	
КонецФункции

// Возвращает Истина, если налогоплательщику разрешено выбирать схему уплаты добровольных взносов на социальное страхование
//
// Параметры:
//   Период - Дата - дата, для которой необходимо проверить возможность выбора схемы уплаты взносов
// 
// Возвращаемое значение:
//  Булево - Истина, если разрешено выбирать страховой случай, иначе Ложь
//
Функция РазрешеноВыбиратьСтраховойСлучайПоДобровольнымВзносам(Период) Экспорт
	
	Возврат Период >= '20260101' И Период < '20290101';
	
КонецФункции

// Возвращает Истина, если налогоплательщику разрешено выбирать схему уплаты добровольных взносов на социальное страхование
//
// Параметры:
//   СтраховойСлучай - ПеречисленияСсылка.СлучаиДобровольногоСоциальногоСтрахования - страховой случай
// 
// Возвращаемое значение:
//  Булево - Истина, если случай является только оплаты больничного, иначе Ложь
//
Функция ЭтоСтраховойСлучайНаУплатуБольничного(СтраховойСлучай) Экспорт
	
	Возврат СтраховойСлучай =
		ПредопределенноеЗначение("Перечисление.СлучаиСоциальногоСтрахованияСамозанятых.ВременнаяНетрудоспособность");
	
КонецФункции

// Возвращает текст подсказки для настройки страховых социальных взносов
//
// Параметры:
//   ЭтоИП          - Булево - Признак, что это ИП
//   ПрименяетсяНПД - Булево - Признак, что организация применяет НПД
// 
// Возвращаемое значение:
//   Строка
//
Функция ТекстПодсказкиДобровольныеВзносыПредпринимателя(ЭтоИП, ПрименяетсяНПД) Экспорт
	
	Если ЭтоИП И ПрименяетсяНПД Тогда
		Возврат СтрШаблон(НСтр("ru = 'Предприниматель добровольно вступил в правоотношения по обязательному социальному страхованию на случай временной нетрудоспособности.
			|После установки флага необходимо указать регистрационный номер в карточке организации.'"));
	ИначеЕсли ЭтоИП Тогда
		Возврат СтрШаблон(НСтр("ru = 'Предприниматель добровольно вступил в правоотношения по обязательному социальному страхованию на случай временной нетрудоспособности и в связи с материнством.
			|После установки флага необходимо указать регистрационный номер в карточке организации.'"));
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru = 'Физическое лицо добровольно вступило в правоотношения по обязательному социальному страхованию на случай временной нетрудоспособности.
		|После установки флага необходимо указать регистрационный номер в карточке организации.'"));
	
КонецФункции

// Возвращает страховой случай на социальное страхование по умолчанию
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СлучаиСоциальногоСтрахованияСамозанятых
//
Функция СтраховойСлучайПоУмолчанию() Экспорт
	
	Возврат ПредопределенноеЗначение("Перечисление.СлучаиСоциальногоСтрахованияСамозанятых.ВременнаяНетрудоспособностьМатеринство");
	
КонецФункции

#КонецОбласти