///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет загрузку статусов операций, по которым было отложено получение результата.
//
// Параметры:
//  Подсистемы - Строка - идентификаторы подсистем, которые должны быть обработаны, перечисленные
//    через запятую. Допустимые значения: "Все", "СБП", "ИнтернетЭквайринг", "ПлатежныеСистемы".
//    Если не заполнено, то обрабатываются все подсистемы.
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - данные обработанных документов по типам платежных систем:
//    * Ключ - Строка - платежная система, по которой была выполнена загрузка. Доступные значения: "СБП",
//        "ИнтернетЭквайринг".
//    * Значение - Неопределено - указанная подсистема не используется в конфигурации, либо она не указана
//                 в параметре Подсистемы.
//               - Массив Из ОпределяемыйТип.ДокументОперацииСБП - данные документов, если Ключ = "СБП".
//               - Массив Из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - данные документов, если
//                   Ключ = "ИнтернетЭквайринг".
//
Функция СтатусыОпераций(Знач Подсистемы = "") Экспорт
	
	ОбработанныеОперации = Новый Соответствие;
	ОбработанныеОперации.Вставить("СБП", Неопределено);
	ОбработанныеОперации.Вставить("ИнтернетЭквайринг", Неопределено);
	
	ВсеПодсистемы = Ложь;
	ВыбранныеПодсистемы = СтрРазделить(СокрЛП(Подсистемы), ", ", Ложь);
	Если ВыбранныеПодсистемы.Количество() = 0
		Или ВыбранныеПодсистемы.Найти("Все") <> Неопределено Тогда
		ВсеПодсистемы = Истина;
	КонецЕсли;
	
	Если (ВсеПодсистемы Или ВыбранныеПодсистемы.Найти("СБП") <> Неопределено)
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		ОбработанныеОперации.Вставить(
			"СБП",
			МодульСистемаБыстрыхПлатежейСлужебный.СтатусыОпераций());
			
	КонецЕсли;
	
	Если (ВсеПодсистемы Или ВыбранныеПодсистемы.Найти("ИнтернетЭквайринг") <> Неопределено)
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		ОбработанныеОперации.Вставить(
			"ИнтернетЭквайринг",
			МодульИнтернетЭквайрингСлужебный.СтатусыОпераций());
		
	КонецЕсли;
	
	Возврат ОбработанныеОперации;
	
КонецФункции

#КонецОбласти