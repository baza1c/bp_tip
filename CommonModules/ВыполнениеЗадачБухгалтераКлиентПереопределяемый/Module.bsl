
#Область ПрограммныйИнтерфейс

// Подробнее
// См. ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие
//
Процедура ВыполнитьДействиеПодключение1СЭДО(ОписаниеДействия) Экспорт
	
	ОбменСКонтрагентамиКлиент.ПомощникПодключенияКСервису1СЭДО(ОписаниеДействия.Организация);
	ВыполнениеЗадачБухгалтераКлиент.ЗарегистрироватьВЖурналеОткрытиеФормы("Подключение1СЭДО");
	
КонецПроцедуры 

// Подробнее
// См. ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие
//
Процедура ВыполнитьДействиеФинОтчетность(ОписаниеДействия) Экспорт
	
	ДлительнаяОперация = ЗаполнениеФинОтчетностиВБанкиВызовСервера.НачатьОбновлениеФинОтчетности(Новый УникальныйИдентификатор);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Обновление данных сервиса 1С:ФинОтчетность'");
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОбновлениеФинОтчетностиЗавершение", ЗаполнениеФинОтчетностиВБанкиКлиент,
		ОписаниеДействия.ПараметрыФормы);
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОбработчикОповещения, ПараметрыОжидания);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтераКлиент.ВыполнитьКомандуСпискаЗадачМобильныйКлиент
//
Процедура ПриВыполненииКомандыСпискаЗадачМобильныйКлиент(ПараметрыКоманды) Экспорт
	
	ДоступноВыполнение = Ложь;
	ОписаниеНедоступнойЗадачи = "";
	Если ТипЗнч(ПараметрыКоманды.Правило) = Тип("ПеречислениеСсылка.ЗадачиНачалаРаботы") Тогда
		
		Если ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.СписокНалоговОтчетов") Тогда
			ДоступноВыполнение = Истина;
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.Подключение1СОтчетности") Тогда
			ДоступноВыполнение = Истина;
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.Подключение1СЭДО") Тогда
			ОписаниеНедоступнойЗадачи = НСтр("ru = 'Обмениваться электронными документами с контрагентами'");
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.НастройкаЗаполненияФормСтатистики") Тогда
			ОписаниеНедоступнойЗадачи = НСтр("ru = 'Сдать статотчетность'");
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.ОплатитьЗадолженностьПоНалогамВзносам") Тогда
			ОписаниеНедоступнойЗадачи = НСтр("ru = 'Ввести сведения о начальных остатках'");
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.УказатьДатуПрекращенияДеятельности") Тогда
			ОписаниеНедоступнойЗадачи = НСтр("ru = 'Оформить прекращение деятельности'");
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.ПроверитьОсновнойВидДеятельности") Тогда
			ОписаниеНедоступнойЗадачи = НСтр("ru = 'Проверить основной код ОКВЭД'");
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.ПроверкиКонтролирующимиОрганами") Тогда
		ОписаниеНедоступнойЗадачи = НСтр("ru = 'Узнать о проверках'");
	ИначеЕсли ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей") Тогда
		ДоступноВыполнение = Истина;
	ИначеЕсли ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.Патенты") Тогда
		ДоступноВыполнение = Истина;
	ИначеЕсли ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.ПравилаФинОтчетности") Тогда
		ОписаниеНедоступнойЗадачи = НСтр("ru = 'Отправить финотчетность'");
	ИначеЕсли ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		Если ПараметрыКоманды.Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога") Тогда
			ДоступноВыполнение = Истина;
		ИначеЕсли ПараметрыКоманды.Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Отчет") Тогда
			Если ПараметрыКоманды.ИдентификаторПравила = "2022_Ознакомление" Тогда
				ДоступноВыполнение = Истина;
			Иначе
				ОписаниеНедоступнойЗадачи = НСтр("ru = 'Сформировать отчет'");
			КонецЕсли;
		ИначеЕсли ПараметрыКоманды.Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление") Тогда
			ДоступноВыполнение = Истина;
		ИначеЕсли ПараметрыКоманды.Действие = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата") Тогда
			ДоступноВыполнение = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДоступноВыполнение Тогда
		ОписаниеДействия = ЗадачиБухгалтераКлиентСервер.ОписаниеДействия(ПараметрыКоманды);
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	Иначе
		ОписаниеНедоступнойЗадачи = ?(ПустаяСтрока(ОписаниеНедоступнойЗадачи), НСтр("ru = 'Выполнить задачу'"), ОписаниеНедоступнойЗадачи);
		МобильныйКлиентБПКлиент.ПоказатьФормуНедоступнаяФункциональность(ОписаниеНедоступнойЗадачи);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтераКлиент.ОтметитьЗадачуКакВыполненную
//
Процедура ПроверитьОтметитьЗадачуКакВыполненную(ПараметрыКоманды, Форма, ТекстВопроса, ЗадатьВопрос) Экспорт
	
	Если ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей")
		И Не ПараметрыКоманды.Оплачено Тогда
		ЗадатьВопрос = Истина;
		ТекстВопроса = НСтр("ru = 'Продолжать напоминать об этом платеже?'");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтераКлиент.РасшифроватьСообщения
//
Процедура ПроверитьРасшифроватьСообщения(Организация, НеРасшифровывать) Экспорт
	
	Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
		НеРасшифровывать = Истина;
		МобильныйКлиентБПКлиент.ПоказатьФормуНедоступнаяФункциональность(НСтр("ru = 'Посмотреть расшифровку требований'"));
	КонецЕсли;
	
КонецПроцедуры

// Обработчик команды "Открыть 1С:ФинОтчетность" формы СписокЗадач
//
Процедура Открыть1СФинОтчетность(Команда) Экспорт
	
	ЗаполнениеФинОтчетностиВБанкиКлиентПереопределяемый.ОткрытьСписокПакетовФинОтчетностьВБанки(Неопределено);
	
КонецПроцедуры

#КонецОбласти
