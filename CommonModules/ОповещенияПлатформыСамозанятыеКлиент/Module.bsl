
#Область ПрограммныйИнтерфейс

// См. ОбщегоНазначенияКлиентПереопределяемый.ПослеНачалаРаботыСистемы
Процедура ПослеНачалаРаботы() Экспорт
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов();
	Если ПараметрыВсехЧатов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПоказатьОповещенияПлатформыСамозанятые", 15, Истина);
	
	ПодключитьОбработчикФормированияКоманд();
	
КонецПроцедуры

// Подключает обработчики отдельных событий Системы взаимодействия.
//
Процедура ПодключитьОбработчикФормированияКоманд() Экспорт
	
	Обработчик = Новый ОписаниеОповещения("СформироватьКоманды", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикФормированияКоманд(Обработчик);
	
КонецПроцедуры

// Глобальный обработчик выбора действия сообщения Системы взаимодействия.
// 
// Параметры:
//  Сообщение - СообщениеСистемыВзаимодействия
//  Действие - Строка
//
Процедура ПриВыбореДействияСообщенияСистемыВзаимодействия(Сообщение, Действие) Экспорт
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов();
	Если Не ЭтоЧатПлатформыСамозанятые(Сообщение.Обсуждение, ПараметрыВсехЧатов) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЧата = ПараметрыВсехЧатов.Получить(Строка(Сообщение.Обсуждение));
	
	ДействияСообщений = ОповещенияПлатформыСамозанятыеКлиентСервер.ДействияСообщений();
	
	Если Действие = ДействияСообщений.ПерейтиКОплатеНПД Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЧата) Тогда
			ДействиеПерейтиКОплатеНПД(Сообщение, ПараметрыЧата);
		КонецЕсли;
		
	ИначеЕсли Действие = ДействияСообщений.ПерейтиВЛичныйКабинетМойНалог Тогда
		
		ДействиеПерейтиВЛичныйКабинетМойНалог();
		
	ИначеЕсли Действие = ДействияСообщений.УведомитьОПрочтенииВсех Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЧата) Тогда
			УведомитьОПрочтенииВсех(Сообщение, ПараметрыЧата);
		КонецЕсли;
		
	ИначеЕсли Действие = ДействияСообщений.СтатусНеПрочитано
		Или Действие = ДействияСообщений.СтатусПрочитано Тогда
		
		ОткрытьСостояниеОповещения(Сообщение, ПараметрыЧата);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПослеПодключенияОбсуждений(Организация = Неопределено) Экспорт
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ПараметрыЧата = ПараметрыВсехЧатов(Организация);
	Если ПараметрыЧата <> Неопределено Тогда
		ВыводитьОкноОжидания = Ложь;
		ОбновитьОповещения(Организация, ВыводитьОкноОжидания);
	КонецЕсли;
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов();
	Если ПараметрыВсехЧатов <> Неопределено Тогда
		ИмяОбработчикаОжидания = "Подключаемый_ПоказатьОповещенияПлатформыСамозанятые";
		ОтключитьОбработчикОжидания(ИмяОбработчикаОжидания);
		ПодключитьОбработчикОжидания(ИмяОбработчикаОжидания, 30, Истина);
		ПодключитьОбработчикФормированияКоманд();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьОбсуждение(Организация) Экспорт
	
	Если ОбсужденияКлиент.ОбсужденияДоступны() Тогда
		ОткрытьНавигационнуюСсылкуОбсуждения(Организация);
	Иначе
		ПодключитьОбсуждения(Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьКоманды(ПараметрыКоманд, ВсеКоманды, КомандаПоУмолчанию, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ПараметрыКоманд.Сообщение) <> Тип("СообщениеСистемыВзаимодействия") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов();
	Если ПараметрыВсехЧатов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЧата = ПараметрыВсехЧатов.Получить(Строка(ПараметрыКоманд.Обсуждение.Идентификатор));
	Если ПараметрыЧата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = ИндексКоманды(СтандартнаяКомандаСистемыВзаимодействия.УдалитьСообщение, ВсеКоманды);
	Если Индекс = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОповещенияПлатформыСамозанятыеВызовСервера.ЕстьПравоИзменятьСостоянияОповещений() Тогда
		СтатусОповещения = ОповещенияПлатформыСамозанятыеВызовСервера.СтатусОповещения(
			ПараметрыКоманд.Сообщение.Данные, ОрганизацияОбсуждения(ПараметрыКоманд.Сообщение.Обсуждение));
		ОповещениеНеПрочитано = (СтатусОповещения
			= ПредопределенноеЗначение("Перечисление.СтатусыОповещенийПлатформыСамозанятые.НеПрочитано"));
		
		ОбработчикКомандыУведомитьОПрочтении = Новый ОписаниеОповещения("УведомитьОПрочтении", ЭтотОбъект, ПараметрыКоманд);
		КомандаУведомитьОПрочтении = Новый ОписаниеКомандыСистемыВзаимодействия(
			ОбработчикКомандыУведомитьОПрочтении,
			НСтр("ru = 'Уведомить ФНС о прочтении'"));
		КомандаУведомитьОПрочтении.Доступность = ОповещениеНеПрочитано;
	Иначе
		КомандаУведомитьОПрочтении = Неопределено;
	КонецЕсли;
	
	Если ОповещенияПлатформыСамозанятыеВызовСервера.ЕстьПравоПолучатьОповещения() Тогда
		ОбработчикКомандыУдалить = Новый ОписаниеОповещения("ВопросПоместитьВАрхивАсинх", ЭтотОбъект, ПараметрыКоманд);
		КомандаУдалить = Новый ОписаниеКомандыСистемыВзаимодействия(
			ОбработчикКомандыУдалить,
			НСтр("ru = 'Удалить сообщение...'"));
	Иначе
		КомандаУдалить = Неопределено;
	КонецЕсли;
	
	ВсеКоманды.Удалить(Индекс);
	Если КомандаУдалить <> Неопределено Тогда
		ВсеКоманды.Вставить(Индекс, КомандаУдалить);
	КонецЕсли;
	Если КомандаУведомитьОПрочтении <> Неопределено Тогда
		ВсеКоманды.Вставить(Индекс, КомандаУведомитьОПрочтении);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьНовыеОповещения(ОтборОрганизация = Неопределено, ВыводитьОкноОжидания = Ложь, Быстро = Истина) Экспорт
	
	ИмяОбработчикаОжидания = "Подключаемый_ПоказатьОповещенияПлатформыСамозанятые";
	ОтключитьОбработчикОжидания(ИмяОбработчикаОжидания);
	
	ОбновитьОбсуждения(ОтборОрганизация, ВыводитьОкноОжидания, Быстро);
	
	ИнтервалПроверки = ОповещенияПлатформыСамозанятыеКлиентСервер.ИнтервалПроверки();
	ПодключитьОбработчикОжидания(ИмяОбработчикаОжидания, ИнтервалПроверки, Ложь);
	
КонецПроцедуры

Процедура ОбновитьОбсуждения(ОтборОрганизация = Неопределено, ВыводитьОкноОжидания = Ложь, Быстро = Истина) Экспорт
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_ОбновитьОбсуждения(
		ОтборОрганизация, Быстро);
		
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОтборОрганизация", ОтборОрганизация);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОбновитьОбсуждения_Завершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьОбсуждения_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		АдресРезультата = Результат.АдресРезультата;
		Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
			УспешноеВыполнение = ПолучитьИзВременногоХранилища(АдресРезультата);
			Если ТипЗнч(УспешноеВыполнение) <> Тип("Булево") Тогда
				УспешноеВыполнение = Ложь;
			КонецЕсли;
			УдалитьИзВременногоХранилища(АдресРезультата);
		Иначе
			УспешноеВыполнение = Ложь;
		КонецЕсли;
		
		Если УспешноеВыполнение Тогда
			ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые"));
			ОповеститьОбИзменении(Тип("СправочникСсылка.ОповещенияПлатформыСамозанятые"));
			ОтборОрганизация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДополнительныеПараметры, "ОтборОрганизация");
			Оповестить("КоличествоНепрочитанныхОповещенийПлатформыСамозанятые_Изменение", ОтборОрганизация);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьКоличествоНепрочитанных(ОтборОрганизация = Неопределено, ВыводитьОкноОжидания = Ложь) Экспорт
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_ПолучитьКоличествоНепрочитанных(
		ОтборОрганизация);
		
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОтборОрганизация", ОтборОрганизация);
		ДополнительныеПараметры.Вставить("ВыводитьОкноОжидания", ВыводитьОкноОжидания);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПолучитьКоличествоНепрочитанных_Завершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьКоличествоНепрочитанных_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		АдресРезультата = Результат.АдресРезультата;
		Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
			УспешноеВыполнение = ПолучитьИзВременногоХранилища(АдресРезультата);
			Если ТипЗнч(УспешноеВыполнение) <> Тип("Булево") Тогда
				УспешноеВыполнение = Ложь;
			КонецЕсли;
			УдалитьИзВременногоХранилища(АдресРезультата);
		Иначе
			УспешноеВыполнение = Ложь;
		КонецЕсли;
		
		Если УспешноеВыполнение Тогда
			ОтборОрганизация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДополнительныеПараметры, "ОтборОрганизация");
			ВыводитьОкноОжидания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДополнительныеПараметры, "ВыводитьОкноОжидания", Ложь);
			Если ОбсужденияКлиент.ОбсужденияДоступны() Тогда
				ОбновитьКоличествоНепрочитанных(ОтборОрганизация, ВыводитьОкноОжидания);
			КонецЕсли;
			ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые"));
			Оповестить("КоличествоНепрочитанныхОповещенийПлатформыСамозанятые_Изменение", ОтборОрганизация);
		Иначе
			ПоказатьОповещениеПриОшибке(НСтр("ru = 'Не удалось получить количество непрочитанных сообщений сервиса Мой налог'"));
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьКоличествоНепрочитанных(ОтборОрганизация = Неопределено, ВыводитьОкноОжидания = Ложь) Экспорт
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_ОбновитьКоличествоНепрочитанных(
		ОтборОрганизация);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьКоличествоНепрочитанных_Завершение", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьКоличествоНепрочитанных_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОповещения(ОтборОрганизация = Неопределено, ВыводитьОкноОжидания = Ложь, ПолучатьПрочитанные = Ложь) Экспорт
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_ПолучитьОповещения(
		ОтборОрганизация, ПолучатьПрочитанные);
		
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОтборОрганизация", ОтборОрганизация);
		ДополнительныеПараметры.Вставить("ВыводитьОкноОжидания", ВыводитьОкноОжидания);
		ДополнительныеПараметры.Вставить("ОчиститьПередЗаполнением", ПолучатьПрочитанные);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПолучитьОповещения_Завершение", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОповещения_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		АдресРезультата = Результат.АдресРезультата;
		Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
			УспешноеВыполнение = ПолучитьИзВременногоХранилища(АдресРезультата);
			Если ТипЗнч(УспешноеВыполнение) <> Тип("Булево") Тогда
				УспешноеВыполнение = Ложь;
			КонецЕсли;
			УдалитьИзВременногоХранилища(АдресРезультата);
		Иначе
			УспешноеВыполнение = Ложь;
		КонецЕсли;
		Если УспешноеВыполнение Тогда
			Если ОбсужденияКлиент.ОбсужденияДоступны() Тогда
				ОтборОрганизация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
					ДополнительныеПараметры, "ОтборОрганизация");
				ВыводитьОкноОжидания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
					ДополнительныеПараметры, "ВыводитьОкноОжидания", Ложь);
				ОчиститьПередЗаполнением = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
					ДополнительныеПараметры, "ОчиститьПередЗаполнением", Ложь);
				ОбновитьОповещения(ОтборОрганизация, ВыводитьОкноОжидания, ОчиститьПередЗаполнением);
			КонецЕсли;
			ОповеститьОбИзменении(Тип("СправочникСсылка.ОповещенияПлатформыСамозанятые"));
		Иначе
			ПоказатьОповещениеПриОшибке(НСтр("ru = 'Не удалось получить оповещения сервиса Мой налог'"));
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьОповещения(ОтборОрганизация = Неопределено, ВыводитьОкноОжидания = Ложь, ОчиститьПередЗаполнением = Ложь) Экспорт
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_ОбновитьОповещения(
		ОтборОрганизация, ОчиститьПередЗаполнением);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьОповещения_Завершение", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьОповещения_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УведомитьОПрочтенииВсех(Сообщение, ПараметрыЧата)
	
	Организация = ОрганизацияОбсуждения(Сообщение.Обсуждение);
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_УведомитьОПрочтенииВсех(
		ПараметрыЧата);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("Организация", ПараметрыЧата.Организация);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"УведомитьОПрочтенииВсехЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Отправляем уведомления о прочтении'");
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УведомитьОПрочтенииВсехЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		Результат = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если Результат = Истина Тогда
			Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДополнительныеПараметры, "Организация");
			ОповеститьОбИзменении(Тип("СправочникСсылка.ОповещенияПлатформыСамозанятые"));
			ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые"));
			Оповестить("КоличествоНепрочитанныхОповещенийПлатформыСамозанятые_Изменение", Организация);
			ПоказатьОповещениеПользователя(НСтр("ru = 'Уведомления отправлены'"));
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура УведомитьОПрочтении(ПараметрыКоманд) Экспорт
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов();
	Если ПараметрыВсехЧатов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЧата = ПараметрыВсехЧатов.Получить(Строка(ПараметрыКоманд.Сообщение.Обсуждение));
	
	ИдентификаторОповещения = ИдентификаторОповещения(ПараметрыКоманд.Сообщение);
	Если Не ЗначениеЗаполнено(ИдентификаторОповещения) Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_УведомитьОПрочтении(
		ПараметрыЧата, ИдентификаторОповещения);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("Организация", ПараметрыЧата.Организация);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"УведомитьОПрочтенииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Отправляем уведомление о прочтении'");
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УведомитьОПрочтенииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		Результат = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если Результат = Истина Тогда
			Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДополнительныеПараметры, "Организация");
			ОповеститьОбИзменении(Тип("СправочникСсылка.ОповещенияПлатформыСамозанятые"));
			ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.КоличествоНепрочитанныхОповещенийПлатформыСамозанятые"));
			Оповестить("КоличествоНепрочитанныхОповещенийПлатформыСамозанятые_Изменение", Организация);
			ПоказатьОповещениеПользователя(НСтр("ru = 'Уведомление отправлено'"));
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Асинх Процедура ВопросПоместитьВАрхивАсинх(ПараметрыКоманд) Экспорт
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Удалить сообщение?'"), РежимДиалогаВопрос.ДаНет);
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ДанныеСообщения = ПараметрыКоманд.Сообщение.Данные;
		Организация = ОрганизацияОбсуждения(ПараметрыКоманд.Обсуждение.Идентификатор);
		Если ОповещенияПлатформыСамозанятыеВызовСервера.ЭтоСообщениеПлатформыСамозанятые(ДанныеСообщения, Организация) Тогда
			ПоместитьВАрхив(ПараметрыКоманд);
		Иначе
			ОповещенияПлатформыСамозанятыеВызовСервера.УдалитьСообщение(ПараметрыКоманд.Сообщение.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоместитьВАрхив(ПараметрыКоманд)
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов();
	Если ПараметрыВсехЧатов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЧата = ПараметрыВсехЧатов.Получить(Строка(ПараметрыКоманд.Сообщение.Обсуждение));
	
	ИдентификаторОповещения = ИдентификаторОповещения(ПараметрыКоманд.Сообщение);
	Если Не ЗначениеЗаполнено(ИдентификаторОповещения) Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ОповещенияПлатформыСамозанятыеВызовСервера.ДлительнаяОперация_ПоместитьВАрхив(
		ПараметрыЧата, ИдентификаторОповещения);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоместитьВАрхивЗавершение", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Перемещаем сообщение в архив'");
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоместитьВАрхивЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если Результат = Истина Тогда
			ОповеститьОбИзменении(Тип("СправочникСсылка.ОповещенияПлатформыСамозанятые"));
			ПоказатьОповещениеПользователя(НСтр("ru = 'Сообщение перемещено в архив'"));
		КонецЕсли;
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПриОшибке(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействиеПерейтиКОплатеНПД(Сообщение, ПараметрыЧата)
	
	// Расчет налога выполняется до 12 числа месяца, следующего за налоговым периодом.
	// Уведомления о квитанциях приходят сразу после расчета налога.
	ПериодСобытия = НачалоМесяца(ДобавитьМесяц(Сообщение.Дата, -1));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", ПараметрыЧата.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия", ПериодСобытия);
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
	
	ОткрытьФорму("Обработка.ПомощникУплатыНПД.Форма", ПараметрыФормы);
	
КонецПроцедуры

Процедура ДействиеПерейтиВЛичныйКабинетМойНалог()
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнтеграцияСПлатформойСамозанятыеКлиентСервер.АдресСервиса());
	
КонецПроцедуры

Процедура ПоказатьОповещениеПриОшибке(КраткоеПредставлениеОшибки)
	
	Если ПользователиКлиент.ЭтоПолноправныйПользователь() Тогда
		ДействиеПриНажатии = Новый ОписаниеОповещения(
			"ДействиеПриНажатииНаОповещение",
			ЭтотОбъект,
			Новый Структура("НавигационнаяСсылка", "ОткрытьЖурналРегистрации"));
		ПредставлениеОшибки = КраткоеПредставлениеОшибки;
	Иначе
		ДействиеПриНажатии = Неопределено;
		ПредставлениеОшибки = НСтр("ru = 'Обратитесь к администратору'");
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Возникла ошибка при обмене с сервисом Мой налог'"),
		ДействиеПриНажатии,
		ПредставлениеОшибки);
	
КонецПроцедуры

Процедура ДействиеПриНажатииНаОповещение(ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		НавигационнаяСсылка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "НавигационнаяСсылка");
	КонецЕсли;
	
	Если НавигационнаяСсылка = "ОткрытьЖурналРегистрации" Тогда
		ОтборЖурнала = Новый Структура;
		ОтборЖурнала.Вставить("Уровень", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Ошибка"));
		ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(ОтборЖурнала);
	КонецЕсли;
	
КонецПроцедуры

Функция ОрганизацияОбсуждения(ИдентификаторОбсуждения)
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов();
	Если ПараметрыВсехЧатов = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЧата = ПараметрыВсехЧатов.Получить(Строка(ИдентификаторОбсуждения));
	Если ПараметрыЧата = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПараметрыЧата.Организация;
	
КонецФункции

Процедура ПодключитьОбсуждения(Организация)
	
	ДополнительныеПараметры = Новый Структура("Организация", Организация);
	ОписаниеЗавершения = Новый ОписаниеОповещения("ПодключитьОбсуждения_Завершение", ЭтотОбъект, ДополнительныеПараметры);
	ОбсужденияКлиент.ПоказатьПодключение(ОписаниеЗавершения);
	
КонецПроцедуры

Процедура ПодключитьОбсуждения_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Организация");
	ОткрытьНавигационнуюСсылкуОбсуждения(Организация)
	
КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуОбсуждения(Организация)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВсехЧатов = ПараметрыВсехЧатов(Организация);
	Если Не ЗначениеЗаполнено(ПараметрыВсехЧатов) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из ПараметрыВсехЧатов Цикл
		ИдентификаторЧата = КлючИЗначение.Значение.ИдентификаторЧата;
		Если ЗначениеЗаполнено(ИдентификаторЧата) Тогда
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СтрШаблон("e1ccs/data/conv?id=%1", ИдентификаторЧата));
			ВыводитьОкноОжидания = Истина;
			ОбновитьОповещения(Организация, ВыводитьОкноОжидания);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыВсехЧатов(ОтборОрганизация = Неопределено)
	
	ПараметрыВсехЧатов = ОповещенияПлатформыСамозанятыеКлиентПовтИсп.ПараметрыЧатов();
	
	Если Не ЗначениеЗаполнено(ПараметрыВсехЧатов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОтборОрганизация = Неопределено Тогда
		Возврат ПараметрыВсехЧатов;
	Иначе
		ПараметрыЧатовОрганизации = Новый Соответствие;
		Для Каждого КлючИЗначение Из ПараметрыВсехЧатов Цикл
			Если КлючИЗначение.Значение.Организация = ОтборОрганизация Тогда
				ПараметрыЧатовОрганизации.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(ПараметрыЧатовОрганизации) Тогда
			Возврат ПараметрыЧатовОрганизации
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ЭтоЧатПлатформыСамозанятые(ИдентификаторОбсуждения, ПараметрыВсехЧатов)

	Если ПараметрыВсехЧатов = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыЧата = ПараметрыВсехЧатов.Получить(Строка(ИдентификаторОбсуждения));
	Возврат ПараметрыЧата <> Неопределено;
	
КонецФункции

Функция ИдентификаторОповещения(Сообщение)
	
	Если ТипЗнч(Сообщение.Данные) = Тип("Строка") Тогда
		Возврат СокрЛП(Сообщение.Данные);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ИндексКоманды(ИскомаяКоманда, ВсеКоманды)
	
	Для Индекс = 0 По ВсеКоманды.ВГраница() Цикл
		Команда = ВсеКоманды[Индекс];
		Если Команда.Команда = ИскомаяКоманда Тогда
			Возврат Индекс;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Процедура ОткрытьСостояниеОповещения(Сообщение, ПараметрыЧата)
	
	Оповещение = ОповещенияПлатформыСамозанятыеВызовСервера.НайтиОповещение(Сообщение.Данные, ПараметрыЧата.Организация);
	Если Не ЗначениеЗаполнено(Оповещение) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияКлюча = Новый Структура;
	ЗначенияКлюча.Вставить("Организация", ПараметрыЧата.Организация);
	ЗначенияКлюча.Вставить("Оповещение", Оповещение);
	
	КлючЗаписи = ОповещенияПлатформыСамозанятыеВызовСервера.КлючЗаписиСостоянияОповещенийПлатформыСамозанятые(
		ЗначенияКлюча);
	
	ПараметрыФормы = Новый Структура("Ключ", КлючЗаписи);
	ОткрытьФорму("РегистрСведений.СостоянияОповещенийПлатформыСамозанятые.Форма.СостояниеОповещения", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти
