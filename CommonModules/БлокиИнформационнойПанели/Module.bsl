#Область ПрограммныйИнтерфейс

// Возвращает таблицу свойств предопределенных блоков
//
// Возвращаемое значени:
//   ТаблицаЗначений - описание полей см. в методе НоваяТаблицаБлоков()
//
Функция ПредопределенныеБлоки() Экспорт
	
	ТаблицаБлоков = НоваяТаблицаБлоков();
	
	ИспользуетсяРазделениеДанных = РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных()
		И РаботаВМоделиСервиса.РазделениеВключено();
	
	// Монитор основных показателей
	Если ОстаткиДенежныхСредствДоступны() Тогда
		ДобавитьОстаткиДенежныхСредств(ТаблицаБлоков);
	КонецЕсли;
	
	ВестиУчетРасчетовСКонтрагентами = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСКонтрагентами");
	ИспользоватьДокументыРеализации = ПолучитьФункциональнуюОпцию("ИспользоватьДокументыРеализации");
	
	Если ИспользоватьДокументыРеализации
		И ПокупателиДоступны() Тогда
		ДобавитьПокупатели(ТаблицаБлоков);
	КонецЕсли;
	
	Если ВестиУчетРасчетовСКонтрагентами
		И ПоставщикиДоступны() Тогда
		ДобавитьПоставщики(ТаблицаБлоков);
	КонецЕсли;
	
	Если ВестиУчетРасчетовСКонтрагентами
		И ПродажиДоступны() Тогда
		ДобавитьПродажи(ТаблицаБлоков);
	КонецЕсли;
	
	// Список задач
	Если ЗадачиДоступны() Тогда
		ДобавитьЗадачи(ТаблицаБлоков);
	КонецЕсли;
	
	// Мои задачи
	Если МоиЗадачиДоступны() Тогда
		ДобавитьМоиЗадачи(ТаблицаБлоков);
	КонецЕсли;
	
	// Электронная почта
	Если ПочтовыйКлиентДоступен() Тогда
		ДобавитьЭлектроннаяПочта(ТаблицаБлоков);
	КонецЕсли;
	
	// Мой налог
	Если МойНалогДоступен() Тогда
		ДобавитьМойНалог(ТаблицаБлоков);
	КонецЕсли;
	
	// 1С-Отчетность
	Если РегламентированнаяОтчетностьБП.Используется1СОтчетность() И ОтчетностьДоступна() Тогда
		ДобавитьОтчетность(ТаблицаБлоков);
	КонецЕсли;
	
	// БИП
	РазрешенаРаботаСНовостями = ОбработкаНовостейПовтИсп.РазрешенаРаботаСНовостямиТекущемуПользователю();
	Если РазрешенаРаботаСНовостями И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		ДобавитьНовости(ТаблицаБлоков);
	КонецЕсли;
	
	Если РазрешенаРаботаСНовостями И ЧастоЗадаваемыеВопросыДоступны() Тогда
		ДобавитьЧастоЗадаваемыеВопросы(ТаблицаБлоков);
	КонецЕсли;
	
	Если ИспользуетсяРазделениеДанных И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		// В режиме интеграции предупреждения от менеджера сервиса выводятся в баннере.
		ДобавитьПоддержкаСервиса(ТаблицаБлоков);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСервисСПАРКРиски")
		И СПАРКРиски.ИспользованиеРазрешено() Тогда
		Добавить1СПАРКРиски(ТаблицаБлоков);
	КонецЕсли;
	
	Если КурсыВалютДоступны() Тогда
		ДобавитьКурсыВалют(ТаблицаБлоков);
	КонецЕсли;
	
	Возврат ТаблицаБлоков;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Задачи

Процедура ДобавитьЗадачи(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаЗадачи(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЗадачи(),
		"БлокиИнформационнойПанели.Задачи",
		"БлокиИнформационнойПанели.ОбновитьЗадачи",
		СвойстваЗадачи());
	
КонецПроцедуры

Функция СвойстваЗадачи()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоЗадач() - 1;
	
	Свойства = "Задачи_Заголовок, Задачи_Просрочено";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияЗадачи(Индекс);

	КонецЦикла;
	
	Возврат Свойства;

КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьЗадачи(Параметры) Экспорт

	КалендарьБухгалтера.ОбновитьЗадачи(Параметры.Организация, Параметры.БыстроеОбновление);	
	
КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура Задачи(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("Задачи_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЗадачи()));
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваЗадачи());
	Результат.Вставить("Задачи_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЗадачи()));
	
	ДанныеЗадачиБухгалтера = КалендарьБухгалтера.ДанныеЗадачиБухгалтера(Параметры.Организация);
	
	АктуальныеЗадачи = ДанныеЗадачиБухгалтера.АктуальныеЗадачи;

	ШрифтОбычногоСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветОбычногоСобытия  = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	
	// Просроченные задачи
	Просрочено = ПредставлениеПросроченныхЗадач(ДанныеЗадачиБухгалтера);
	
	Результат.Вставить("Задачи_Просрочено", Просрочено);
	
	// Актуальные задачи
	Если АктуальныеЗадачи.Количество() > 0 Тогда

		КоличествоЗадач = БлокиИнформационнойПанелиКлиентСервер.КоличествоЗадач();
		
		Актуальные = ПредставлениеАктуальныхЗадач(АктуальныеЗадачи);

		Для Индекс = 0 По Мин(Актуальные.ВГраница(), КоличествоЗадач - 1) Цикл
			Результат.Вставить(
				БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияЗадачи(Индекс),
				Актуальные[Индекс]);
		КонецЦикла;
		
	Иначе
		
		НаименованиеЗадачи = Новый ФорматированнаяСтрока(
			НСтр("ru = 'На ближайший месяц задач нет'"),
			ШрифтОбычногоСобытия, ЦветОбычногоСобытия);
		
		Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияЗадачи(0), НаименованиеЗадачи);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

Функция ПредставлениеАктуальныхЗадач(АктуальныеЗадачи)
	
	Представления = Новый Массив();
	
	ОтборЗадач = Новый Структура("ОтносительныйСрок, ОтносительныйСрокПорядок", 0, 0);
	
	КоличествоСвободныхСтрок = БлокиИнформационнойПанелиКлиентСервер.КоличествоЗадач();
	
	ИндексСтроки = 0;
	
	ПараметрыПредметаИсчисления = Перечисления.ВидыДействийКалендаряБухгалтера.ПредметыИсчисления();
	
	ОтносительныеСрокиЗадач = СгруппироватьЗадачиПоСрокам(АктуальныеЗадачи);
	
	КоличествоОставшихсяПериодов = ОтносительныеСрокиЗадач.Количество();
	
	ШрифтТекста = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	
	ЦветОбычногоСобытия        = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	ЦветВажногоСобытия         = ЦветаСтиля.ВажноеСобытие;
	ЦветПриближающегосяСобытия = ЦветаСтиля.ПриближающеесяСобытие;
	
	ПредупреждатьОтправкаОтчетаОграничена = Ложь;
	// Предупреждаем, только если обычно есть возможность отправить отчеты оперативно
	Если ОтносительныеСрокиЗадач.Найти(Истина, "ПредупредитьОтправкаОтчетаОграничена") <> Неопределено Тогда
		ПредупреждатьОтправкаОтчетаОграничена = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом();
	КонецЕсли;
	
	Для Каждого ОтносительныйСрокЗадачи Из ОтносительныеСрокиЗадач Цикл
		
		ОтносительныйСрок = ОтносительныйСрокЗадачи.ОтносительныйСрок;
		
		// Ищем строки с приоритетом
		ОтборЗадач.ОтносительныйСрок        = ОтносительныйСрок;
		ОтборЗадач.ОтносительныйСрокПорядок = ОтносительныйСрокЗадачи.ОтносительныйСрокПорядок;
		
		НайденныеЗадачи = АктуальныеЗадачи.Скопировать(ОтборЗадач);
		НайденныеЗадачи.Сортировать("ОсталосьДней Возр, ДействиеПорядок Возр");
		
		Если ОтносительныйСрок = Перечисления.ОтносительныеСроки.Сегодня Тогда
			ЦветТекста = ЦветВажногоСобытия;
		ИначеЕсли ОтносительныйСрок = Перечисления.ОтносительныеСроки.Завтра Тогда
			ЦветТекста = ЦветПриближающегосяСобытия;
		Иначе
			ЦветТекста = ЦветОбычногоСобытия;
		КонецЕсли;
		
		Задача = НайденныеЗадачи[0];
		ПредставлениеСрока = ПредставлениеСрока(ОтносительныйСрок, Задача.ОсталосьДней);
		
		// "Приблизим" задачи, связанные с отчетами, которые нельзя представить электронно - заранее отметим их цветом.
		Если ПредупреждатьОтправкаОтчетаОграничена 
			И ОтносительныйСрокЗадачи.ПредупредитьОтправкаОтчетаОграничена
			И ЦветТекста = ЦветОбычногоСобытия Тогда
			ЦветТекста = ЦветПриближающегосяСобытия;
		КонецЕсли;
			
		Если НайденныеЗадачи.Количество() = 1 Тогда
			НаименованиеЗадачи = НаименованиеЗадачиПолностью(НайденныеЗадачи[0], ПредставлениеСрока, ЦветТекста, ШрифтТекста);
			Представления.Добавить(НаименованиеЗадачи);
			КоличествоСвободныхСтрок = КоличествоСвободныхСтрок - 1;
			ИндексСтроки = ИндексСтроки + 1;
		Иначе
			КоличествоДоступныхСтрок = Макс(КоличествоСвободныхСтрок - КоличествоОставшихсяПериодов + 1, 1);
			
			ЗадачиВНесколькоСтрок = РаспределитьЗадачиПоСтрокам(НайденныеЗадачи, КоличествоДоступныхСтрок, ПараметрыПредметаИсчисления, ПредставлениеСрока, ЦветТекста, ШрифтТекста);
			Для Каждого НаименованиеЗадачи Из ЗадачиВНесколькоСтрок Цикл
				Представления.Добавить(НаименованиеЗадачи);
				КоличествоСвободныхСтрок = КоличествоСвободныхСтрок - 1;
				ИндексСтроки = ИндексСтроки + 1;
			КонецЦикла;
		КонецЕсли;
		
		КоличествоОставшихсяПериодов = КоличествоОставшихсяПериодов - 1;
		
		Если КоличествоСвободныхСтрок = 0 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Представления;
	
КонецФункции

// Группирует данные о задачах к выполнению по срокам.
//
// Параметры:
//  АктуальныеЗадачи - ТаблицаЗначений - см. КалендарьБухгалтера.НоваяТаблицаЗадачИнформационнойПанели()
// 
// Возвращаемое значение:
//  ТаблицаЗначений - группы задач по срокам. Колонки:
//  * ОтносительныйСрок                    - ПеречислениеСсылка.ОтносительныеСроки 
//  * ОтносительныйСрокПорядок             - Число
//  * ПредупредитьОтправкаОтчетаОграничена - Булево - Истина, если близится срок предупреждения сроков отправки
//                                           и хотя бы у одной из задач с этим сроком отправка ограничена.
//
Функция СгруппироватьЗадачиПоСрокам(ЗадачИнформационнойПанели)
	
	ОтносительныеСрокиЗадач = ЗадачИнформационнойПанели.Скопировать(,"ОтносительныйСрок, ОтносительныйСрокПорядок, ОсталосьДней, ОтправкаОтчетаОграничена");
	
	ОтносительныеСрокиЗадач.Колонки.Добавить("ПредупредитьОтправкаОтчетаОграничена", Новый ОписаниеТипов("Булево"));
	
	Для Каждого Задача Из ОтносительныеСрокиЗадач.НайтиСтроки(Новый Структура("ОтправкаОтчетаОграничена", Истина)) Цикл
		
		Если Задача.ОсталосьДней > ЗадачиБухгалтераКлиентСервер.СрокПредупрежденияПриОграниченииСпособовОтправки() Тогда
			Продолжить;
		КонецЕсли;
		
		Задача.ПредупредитьОтправкаОтчетаОграничена = Истина;
		
	КонецЦикла;
	
	ОтносительныеСрокиЗадач.Свернуть("ОтносительныйСрок, ОтносительныйСрокПорядок", "ПредупредитьОтправкаОтчетаОграничена");
	ОтносительныеСрокиЗадач.Сортировать("ОтносительныйСрокПорядок");
	
	Возврат ОтносительныеСрокиЗадач;
	
КонецФункции

Функция ПредставлениеПросроченныхЗадач(ДанныеЗадачиБухгалтера)
	
	ПараметрыПредметаИсчисления = НСтр("ru = 'задача, задачи, задач'");
	
	ШрифтОбычногоСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветОбычногоСобытия  = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	
	ШрифтВажногоСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветВажногоСобытия  = ЦветаСтиля.ВажноеСобытие;
	
	ШрифтПриближающегосяСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветПриближающегосяСобытия  = ЦветаСтиля.ПриближающеесяСобытие;
	
	ОтборПросроченныхЗадач = Новый Структура("ОтносительныйСрок", Перечисления.ОтносительныеСроки.Просрочено);
	
	ПросроченнаяЗадача = ДанныеЗадачиБухгалтера.ПросроченнаяЗадача;
	
	КоличествоПросроченных = ДанныеЗадачиБухгалтера.КоличествоПросроченных;
	
	Если КоличествоПросроченных = 0 Тогда
		
		Представление = Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = 'Просроченных %1 нет'"), МножественноеПредставление(ПараметрыПредметаИсчисления)),
			ШрифтОбычногоСобытия, ЦветОбычногоСобытия);
		
	ИначеЕсли КоличествоПросроченных = 1 Тогда
		
		ПредставлениеСрока = Формат(ПросроченнаяЗадача.Срок, "Л=ru; ДФ='d MMMM'");
		
		// Исключим возможность переноса строк в веб-клиенте
		НаименованиеЗадачиБезПробелов = СтрокаБезПереносов(
			СтрШаблон(НСтр("ru = '%1 %2'"), ПредставлениеСрока, ПросроченнаяЗадача.Наименование));
		
		Представление = Новый ФорматированнаяСтрока(
			НаименованиеЗадачиБезПробелов,
			ШрифтВажногоСобытия, ЦветВажногоСобытия);
		
	Иначе
		
		КоличествоПросроченныхПрописью = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			КоличествоПросроченных, ПараметрыПредметаИсчисления);
		
		Представление = Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = 'Просрочено: %1'"), КоличествоПросроченныхПрописью),
			ШрифтВажногоСобытия, ЦветВажногоСобытия);
		
	КонецЕсли;

	Возврат Представление;
	
КонецФункции

Функция РаспределитьЗадачиПоСтрокам(Задачи, КоличествоСтрок, ПараметрыПредметаИсчисления, ПредставлениеСрока, ЦветТекста, ШрифтТекста)
	
	СтрокиЗадач = Новый Массив;
	
	НаименованияЗадач = Новый Соответствие();
	Для ИндексЗадачи = 1 По КоличествоСтрок - 1 Цикл
		
		Если Задачи.Количество() > 0 Тогда
			Если НаименованияЗадач[Задачи[0].Наименование] <> Неопределено Тогда
				// Такое наименование уже выведено, повторно выводить не будем.
				Продолжить;
			КонецЕсли;
			СтрокиЗадач.Добавить(НаименованиеЗадачиПолностью(Задачи[0], ПредставлениеСрока, ЦветТекста, ШрифтТекста));
			НаименованияЗадач.Вставить(Задачи[0].Наименование, Истина);
			Задачи.Удалить(0);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого УникальноеНаименование Из НаименованияЗадач Цикл
		Задача = Задачи.Найти(УникальноеНаименование.Ключ, "Наименование");
		// В этом периоде строка с такой задачей уже выведена.
		Если Задача <> Неопределено Тогда
			Задачи.Удалить(Задача);
		КонецЕсли;
	КонецЦикла;
	
	Если Задачи.Количество() = 1 Тогда
		СтрокиЗадач.Добавить(НаименованиеЗадачиПолностью(Задачи[0], ПредставлениеСрока, ЦветТекста, ШрифтТекста));
	Иначе
		СтрокиЗадач.Добавить(ВсеЗадачиОднойСтрокой(Задачи, ПараметрыПредметаИсчисления, ПредставлениеСрока, ЦветТекста, ШрифтТекста));
	КонецЕсли;
	
	Возврат СтрокиЗадач;
	
КонецФункции

Функция ВсеЗадачиОднойСтрокой(Задачи, ПараметрыПредметаИсчисления, ПредставлениеСрока, ЦветТекста, ШрифтТекста)
	
	Действия = Задачи.Скопировать();
	Действия.Колонки.Добавить("Количество");
	Действия.ЗаполнитьЗначения(1, "Количество");
	Действия.Свернуть("Действие, ДействиеПорядок", "Количество");
	
	Действия.Сортировать("ДействиеПорядок Возр");
	
	СоставСтроки = Новый Массив;
	СоставСтроки.Добавить(ПредставлениеСрока);
	РегулярныйПлатеж = Перечисления.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж;
	ТребованиеФНС = Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС;
	ДокументСЭДО = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО;
	
	Для Каждого Задача Из Действия Цикл

		Если Задача.Количество = 1 Тогда
			НайденныеЗадачи = Задачи.НайтиСтроки(Новый Структура("Действие, ДействиеПорядок", Задача.Действие, Задача.ДействиеПорядок));
			Если Задача.Действие <> РегулярныйПлатеж И НайденныеЗадачи.Количество() > 0 Тогда
				КоличествоЗадачПрописью = НайденныеЗадачи[0].Наименование;
			Иначе
				КоличествоЗадачПрописью = НРег(Задача.Действие);
			КонецЕсли;
		ИначеЕсли ТипЗнч(Задача.Действие) = Тип("Строка") Тогда
			КоличествоЗадачПрописью = Задачи.Найти(Задача.Действие,"Действие").Наименование;
		Иначе
			КоличествоЗадачПрописью = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
				Задача.Количество, НРег(ПараметрыПредметаИсчисления.Получить(Задача.Действие)));
		КонецЕсли;
		Если Задача.Действие = ТребованиеФНС Тогда
			КоличествоЗадачПрописью = СтрЗаменить(КоличествоЗадачПрописью, "фнс", "ФНС");
		ИначеЕсли Задача.Действие = ДокументСЭДО Тогда
			КоличествоЗадачПрописью = СтрЗаменить(КоличествоЗадачПрописью, "сэдо", "СЭДО");
		КонецЕсли;
		
		// Все найденные задачи сворачиваем в одну строку
		СоставСтроки.Добавить(Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = ' %1'"),КоличествоЗадачПрописью),
			ШрифтТекста, ЦветТекста));
			
		СоставСтроки.Добавить(",");

	КонецЦикла;
	
	СоставСтроки.Удалить(СоставСтроки.ВГраница());
	
	Если СоставСтроки.Количество() > 2 Тогда
		СоставСтроки.Установить(СоставСтроки.ВГраница() - 1, НСтр("ru = ' и'"));
	КонецЕсли;
	
	НаименованиеЗадачиБезПробелов = СтрокаБезПереносов(СтрСоединить(СоставСтроки, ""));
	
	Возврат Новый ФорматированнаяСтрока(
		НаименованиеЗадачиБезПробелов,
		ШрифтТекста,
		ЦветТекста);

КонецФункции

Функция НаименованиеЗадачиПолностью(Задача, ПредставлениеСрока, ЦветТекста, ШрифтТекста)
	
	// Исключим возможность переноса строк в веб-клиенте
	НаименованиеЗадачиБезПробелов = СтрокаБезПереносов(
		СтрШаблон(НСтр("ru = '%1 %2'"), ПредставлениеСрока, Задача.Наименование));
	
	Возврат Новый ФорматированнаяСтрока(
		НаименованиеЗадачиБезПробелов,
		ШрифтТекста,
		ЦветТекста);

КонецФункции

// Возвращает Истина, если для текущего пользователя можно отображать блок "Задачи" на начальной странице.
// Возможность отображения блока зависит от прав пользователя и состояния регламентного задания по обновлению задач бухгалтера.
//
Функция ЗадачиДоступны()
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.СписокЗадач) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если РаботаВМоделиСервиса.РазделениеВключено() Тогда
		// В режиме сервиса задачи бухгалтера обновляются всегда.
		Возврат Истина;
	КонецЕсли;
	
	// В "коробках" блок с задачами бухгалтера считаем доступным, если включено соответствующее регламентное задание.
	УстановитьПривилегированныйРежим(Истина);
	ЗаданиеИспользуется = РегламентныеЗаданияСервер.РегламентноеЗаданиеИспользуется(Метаданные.РегламентныеЗадания.ОбновлениеЗадачБухгалтера);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЗаданиеИспользуется;
	
КонецФункции

#КонецОбласти

#Область МоиЗадачи

Процедура ДобавитьМоиЗадачи(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаМоиЗадачи(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаМоиЗадачи(),
		"БлокиИнформационнойПанели.МоиЗадачи",
		"БлокиИнформационнойПанели.ОбновитьМоиЗадачи",
		СвойстваМоиЗадачи());
	
КонецПроцедуры

Функция СвойстваМоиЗадачи()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоМоиЗадачи() - 1;
	
	Свойства = "МоиЗадачи_Заголовок";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияМоиЗадачи(Индекс);
	КонецЦикла;
	
	Возврат Свойства;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьМоиЗадачи(Параметры) Экспорт
	
	// Обновление списка моих задач не предусмотрено.
	
КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура МоиЗадачи(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("МоиЗадачи_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаМоиЗадачи()));
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваМоиЗадачи());
	Результат.Вставить("МоиЗадачи_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаМоиЗадачи()));
	
	ДанныеМоиЗадачи = МоиЗадачи.ДанныеМоиЗадачи();
	
	СписокМоихЗадач = ДанныеМоиЗадачи.Задачи;
	
	ШрифтОбычногоСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ШрифтВажногоСобытия = Новый Шрифт(ШрифтОбычногоСобытия, , , Истина);
	ЦветОбычногоСобытия  = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	ЦветВажногоСобытия  = ЦветаСтиля.ВажноеСобытие;
	
	КоличествоСтрокВиджета = БлокиИнформационнойПанелиКлиентСервер.КоличествоМоиЗадачи();
	// Если помещаются все задачи - выводим все, которые есть.
	// Если не помещаются - выводим все, что можем, а в последней строке выводим общее количество оставшихся задач.
	МожноВывестиВсеЗадачи = СписокМоихЗадач.Количество() <= КоличествоСтрокВиджета;
	МожноВывестиЗадач = ?(МожноВывестиВсеЗадачи, СписокМоихЗадач.Количество(), КоличествоСтрокВиджета - 1);
	
	ИндексТекущейСтрокиВиджета = -1;
	
	Для Каждого МояЗадача Из СписокМоихЗадач Цикл
		
		ИндексТекущейСтрокиВиджета = ИндексТекущейСтрокиВиджета + 1;
		
		ЦветЗадачи = ?(МояЗадача.Просрочена, ЦветВажногоСобытия, ЦветОбычногоСобытия);
		ШрифтЗадачи = ?(МояЗадача.Важная, ШрифтВажногоСобытия, ШрифтОбычногоСобытия);
		
		НаименованиеЗадачи = СокрЛП(СтрШаблон("%1 %2", МояЗадача.ОтносительныйСрок, 
			ОбщегоНазначенияБПКлиентСервер.ДекапитализироватьСтроку(МояЗадача.Наименование)));
		Результат.Вставить(
			БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияМоиЗадачи(ИндексТекущейСтрокиВиджета),
			Новый ФорматированнаяСтрока(НаименованиеЗадачи, ШрифтЗадачи, ЦветЗадачи));
		
		Если ИндексТекущейСтрокиВиджета + 1 >= МожноВывестиЗадач Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не МожноВывестиВсеЗадачи Тогда
		
		ИндексТекущейСтрокиВиджета = ИндексТекущейСтрокиВиджета + 1;
		
		// Это последняя свободная строка, при этом сообщений нужно вывести больше, чем одно
		ЕщеЗадач = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			СписокМоихЗадач.Количество() - МожноВывестиЗадач, НСтр("ru = 'задача, задачи, задач'"));
		ТекстСообщения = СтрШаблон(НСтр("ru = 'и еще %1'"), ЕщеЗадач);
		
		Результат.Вставить(
			БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияМоиЗадачи(ИндексТекущейСтрокиВиджета),
			Новый ФорматированнаяСтрока(ТекстСообщения, ШрифтОбычногоСобытия, ЦветОбычногоСобытия));
		
	КонецЕсли;
	
	Если СписокМоихЗадач.Количество() = 0 Тогда
		
		НаименованиеЗадачи = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Задач для выполнения нет'"),
			ШрифтОбычногоСобытия, ЦветОбычногоСобытия);
		
		ИндексТекущейСтрокиВиджета = ИндексТекущейСтрокиВиджета + 1;
		
		Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияМоиЗадачи(ИндексТекущейСтрокиВиджета), НаименованиеЗадачи);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

// Возвращает Истина, если для текущего пользователя можно отображать блок "Задачи" на начальной странице.
// Возможность отображения блока зависит от прав пользователя и состояния регламентного задания по обновлению задач бухгалтера.
//
Функция МоиЗадачиДоступны()
	
	Возврат МоиЗадачи.МоиЗадачиДоступны();
	
КонецФункции

#КонецОбласти

#Область ЭлектроннаяПочта

Процедура ДобавитьЭлектроннаяПочта(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаЭлектроннаяПочта(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЭлектроннаяПочта(),
		"БлокиИнформационнойПанели.ЭлектроннаяПочта",
		"БлокиИнформационнойПанели.ОбновитьЭлектроннаяПочта",
		СвойстваЭлектроннаяПочта());
	
КонецПроцедуры

Функция СвойстваЭлектроннаяПочта()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоЭлектроннаяПочта() - 1;
	
	Свойства = "ЭлектроннаяПочта_Заголовок";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияЭлектроннаяПочта(Индекс);
	КонецЦикла;
	
	Возврат Свойства;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьЭлектроннаяПочта(Параметры) Экспорт

	Параметры.Вставить("Инициализация", Ложь);
	Параметры.Вставить("АдресХранилища", Неопределено);
	
	ЭлектроннаяПочта(Параметры);

КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура ЭлектроннаяПочта(Параметры) Экспорт
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("ЭлектроннаяПочта_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЭлектроннаяПочта()));
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваЭлектроннаяПочта());
	Результат.Вставить("ЭлектроннаяПочта_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЭлектроннаяПочта()));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектронноеПисьмоВходящее.Дата КАК Дата,
		|	ЭлектронноеПисьмоВходящее.Тема КАК Тема
		|ИЗ
		|	Документ.ЭлектронноеПисьмоВходящее КАК ЭлектронноеПисьмоВходящее
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
		|		ПО ЭлектронноеПисьмоВходящее.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|		ПО ЭлектронноеПисьмоВходящее.УчетнаяЗапись = УчетныеЗаписиЭлектроннойПочты.Ссылка
		|			И (УчетныеЗаписиЭлектроннойПочты.ВладелецУчетнойЗаписи = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|				ИЛИ УчетныеЗаписиЭлектроннойПочты.ВладелецУчетнойЗаписи = &ТекущийПользователь
		|				ИЛИ &РольПолныеПраваДоступна)
		|ГДЕ
		|	НЕ ПредметыПапкиВзаимодействий.Рассмотрено
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭлектронноеПисьмоВходящее.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	Запрос.УстановитьПараметр("РольПолныеПраваДоступна", Пользователи.ЭтоПолноправныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();

	ШрифтОбычногоСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветОбычногоСобытия  = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	
	СписокЭлектронныхПисем = РезультатЗапроса.Выгрузить();
	
	КоличествоСтрокВиджета = БлокиИнформационнойПанелиКлиентСервер.КоличествоЭлектроннаяПочта();
	// Если помещаются все письма - выводим все, что есть.
	// Если не помещаются - выводим все, что можем, а в гиперссылке для перехода в почту указываем сколько есть еще непрочитанных писем.
	МожноВывестиВсеПисьма = СписокЭлектронныхПисем.Количество() <= КоличествоСтрокВиджета;
	МожноВывестиПисем = ?(МожноВывестиВсеПисьма, СписокЭлектронныхПисем.Количество(), КоличествоСтрокВиджета - 1);
	
	ИндексТекущейСтрокиВиджета = -1;
	НовыхПисемНет =  СписокЭлектронныхПисем.Количество() = 0;
	
	Результат.Вставить("НовыхПисемНет", НовыхПисемНет);
	
	Для Каждого Данные Из СписокЭлектронныхПисем Цикл
		
		Если Год(Данные.Дата) = Год(ОбщегоНазначения.ТекущаяДатаПользователя()) Тогда
			ПредставлениеДаты = Формат(Данные.Дата, "Л=ru_RU; ДФ=dd.MM;");
		Иначе
			ПредставлениеДаты = Формат(Данные.Дата, "Л=ru_RU; ДФ=dd.MM.yy;");
		КонецЕсли;
		
		ПредставлениеПисьма = СтрШаблон(НСтр("ru='%1 %2'"), ПредставлениеДаты, Данные.Тема);
		ИндексТекущейСтрокиВиджета = ИндексТекущейСтрокиВиджета + 1;

		Результат.Вставить(
			БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияЭлектроннаяПочта(ИндексТекущейСтрокиВиджета),
				Новый ФорматированнаяСтрока(ПредставлениеПисьма, ШрифтыСтиля.ШрифтТекстаИнформационнойПанели));
		
		Если ИндексТекущейСтрокиВиджета >= МожноВывестиПисем Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не МожноВывестиВсеПисьма Тогда
		
		ИндексТекущейСтрокиВиджета = ИндексТекущейСтрокиВиджета + 1;
		
		// Это последняя свободная строка, при этом сообщений нужно вывести больше, чем одно
		ЕщеПисем = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			СписокЭлектронныхПисем.Количество() - МожноВывестиПисем - 1, НСтр("ru = 'письмо, письма, писем'"));
		ТекстКоманды = СтрШаблон(НСтр("ru = 'и еще %1'"), ЕщеПисем);
		
		Результат.Вставить("ТекстКоманды", ТекстКоманды);
	Иначе
		Результат.Вставить("ТекстКоманды", НСтр("ru = 'Все письма'"));
	КонецЕсли;
	
	Если НовыхПисемНет Тогда
		
		ТемаПисьма = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Новых писем нет'"),
			ШрифтОбычногоСобытия, ЦветОбычногоСобытия);
		
		ИндексТекущейСтрокиВиджета = ИндексТекущейСтрокиВиджета + 1;
		
		Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияЭлектроннаяПочта(ИндексТекущейСтрокиВиджета), ТемаПисьма);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

// Возвращает Истина, если для текущего пользователя можно отображать блок "Электронная почта" на начальной странице.
// Возможность отображения блока зависит от прав пользователя, а также включена ли функциональная опция "ИспользоватьПочтовыйКлиент"
//
Функция ПочтовыйКлиентДоступен()
	
	Если Не ПравоДоступа("Просмотр", Метаданные.ЖурналыДокументов.Взаимодействия) 
		Или (Не ПравоДоступа("Просмотр", Метаданные.Документы.ЭлектронноеПисьмоВходящее)
		И Не ПравоДоступа("Просмотр", Метаданные.Документы.ЭлектронноеПисьмоИсходящее)) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьПочтовыйКлиент");
	
КонецФункции

#КонецОбласти

#Область Отчетность

Процедура ДобавитьОтчетность(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаОтчетность(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаОтчетность(),
		"БлокиИнформационнойПанели.Отчетность",
		"БлокиИнформационнойПанели.ОбновитьОтчетность",
		СвойстваОтчетность());
	
КонецПроцедуры

Функция СвойстваОтчетность()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоСообщенийОтчетности() - 1;
	
	Свойства = "Отчетность_Заголовок,Отчетность_ОтчетностьПодключена";
	Свойства = Свойства + ",Отчетность_ПоказыватьБаннер,Отчетность_КартинкаБаннера,Отчетность_ТекстБаннера,Отчетность_ЗаявлениеНаПодключение";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияСообщения(Индекс);
	КонецЦикла;
	
	Возврат Свойства;
	
КонецФункции

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура Отчетность(Параметры) Экспорт
	
	ПараметрыБаннераОтчетности = НовыйПараметрыПолученияБаннераОтчетность();
	
	ЗаполнитьЗначенияСвойств(ПараметрыБаннераОтчетности, Параметры);
	
	Результат = ПодготовленныеПараметрыОтчетности(ПараметрыБаннераОтчетности);
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

Функция ПодготовленныеПараметрыОтчетности(Параметры) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Отчетность_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаОтчетность()));
	Результат.Вставить("Отчетность_ОтчетностьПодключена", 
		ИнтерфейсыВзаимодействияБРО.ОрганизацияИмеетУчетнуюЗапись(Параметры.Организация));
	Результат.Вставить("Отчетность_ПоказыватьБаннер", Не Результат.Отчетность_ОтчетностьПодключена);
	
	Если Параметры.Инициализация Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.Вставить("Отчетность_КартинкаБаннера", Неопределено);
	Результат.Вставить("Отчетность_ТекстБаннера", "");
	Результат.Вставить("Отчетность_ЗаявлениеНаПодключение", Неопределено);
	Результат.Вставить("Состояние", "");
	Результат.Вставить("Ссылка", "");
	Результат.Вставить("ПользовательНачалИспользоватьОтчетность", Ложь);
	
	КоличествоСтрокСообщений = БлокиИнформационнойПанелиКлиентСервер.КоличествоСообщенийОтчетности();
	
	ПредставленияСтрок = Новый Структура();
	Для ИндексСтроки = 0 По КоличествоСтрокСообщений - 1 Цикл
		ПредставленияСтрок.Вставить("Наименование" + ИндексСтроки, "");
	КонецЦикла;
	
	СообщенияОтчетности = ЭлектронныйДокументооборотСКонтролирующимиОрганамиБП.ПолучитьНовыеСообщенияОтчетности(
		Параметры.Организация);
		
	Если Результат.Отчетность_ОтчетностьПодключена Тогда
		// Пользователь может подключить отчетность, но еще не отправить и не получить
		// ни одного отчета. В этом случае считаем, что он не начал использовать 1С-Отчетность.
		Результат.Вставить("ПользовательНачалИспользоватьОтчетность",
			ОбработкаЗаявленийАбонента.ПользовательНачалИспользоватьОтчетность(Параметры.Организация));
	КонецЕсли;

		
	Если Не ЗначениеЗаполнено(СообщенияОтчетности)
		Или НужноПредложитьПодключитьТарифВИнтеграцииСБанками(Параметры.Организация) Тогда
		ЗаполнитьДанныеБаннераОтчетность(Результат, Параметры);
	КонецЕсли;
	
	Если Результат.Отчетность_ОтчетностьПодключена Тогда
		ЗаполнитьПредставленияСообщенийОтчетности(
			СообщенияОтчетности,
			КоличествоСтрокСообщений,
			ПредставленияСтрок);
			
		СостоянияОтчетности = СостоянияРегламентированнойОтчетности();
		Результат.Состояние = СостоянияОтчетности.Подключено;

	КонецЕсли;
	
	Для Индекс = 0 По КоличествоСтрокСообщений - 1 Цикл
		
		Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоНаименованияСообщения(Индекс), ПредставленияСтрок["Наименование" + Индекс]);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ПоказыватьСравнениеСПрошлымГодом - Булево - необходимость сравнения с показателями прошлого года
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьОтчетность(Параметры) Экспорт
	
	// Обновление данных не требуется
	
КонецПроцедуры

Процедура ЗаполнитьПредставленияСообщенийОтчетности(СообщенияОтчетности, КоличествоСтрок, Представления)
	
	ТипыСообщений = РазделитьСообщенияПоТипам(СообщенияОтчетности);
	
	Если ТипыСообщений.Количество()=0 Тогда
		ПараметрыВывода = ПараметрыВыводаПоТипуСообщений("СообщенийНет");
		Представления.Наименование0 = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Новых сообщений нет'"),
			ПараметрыВывода.Шрифт, ПараметрыВывода.Цвет);
		Возврат;
	КонецЕсли;
	
	КоличествоСвободныхСтрок = КоличествоСтрок;
	ИндексСтроки = 0;
	КоличествоТиповДляВывода = ТипыСообщений.Количество();
	Для НомерТипаСообщения = 0 По ТипыСообщений.Количество()-1 Цикл
		
		Если КоличествоСвободныхСтрок = 0 Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоТиповДляВывода = КоличествоТиповДляВывода - 1;
		
		ТипСообщений = ТипыСообщений[НомерТипаСообщения];
		ПараметрыВывода = ПараметрыВыводаПоТипуСообщений(ТипСообщений.ТипСообщения);
		
		Если ПараметрыВывода.ВсегдаГруппировать Тогда
			ГруппироватьСообщения = ТипСообщений.КоличествоСообщений > 1;
		Иначе
			// Определим, можно ли вывести все сообщения в свободные строки
			ГруппироватьСообщения = ТипСообщений.КоличествоСообщений + КоличествоТиповДляВывода > КоличествоСвободныхСтрок;
			// Если все сообщения не умещаются, но это последний тип сообщений и количество свободных строк больше 1,
			// то сообщения в группу не сворачиваются, а в последней строке указывается количество оставшихся сообщений
			Если КоличествоТиповДляВывода = 0
				И ГруппироватьСообщения
				И КоличествоСвободныхСтрок > 1 Тогда
				ГруппироватьСообщения = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если ГруппироватьСообщения Тогда
			
			// Не хватает строк для вывода каждого сообщения данного типа и остальных типов данных.
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"), 
								ПараметрыВывода.Префикс, 
								СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
									ТипСообщений.КоличествоСообщений, ПараметрыВывода.НаименованияДляГруппыСообщений));
			
			ТекстСообщения = ?(СтрДлина(ТекстСообщения) < 37, ТекстСообщения, ЛЕВ(ТекстСообщения, 36) + "...");
			Представления["Наименование"+ИндексСтроки] = Новый ФорматированнаяСтрока(
						ТекстСообщения,
						ПараметрыВывода.Шрифт, ПараметрыВывода.Цвет);
			ИндексСтроки = ИндексСтроки + 1;
			КоличествоСвободныхСтрок = КоличествоСвободныхСтрок - 1;
			
		Иначе
			
			ОсталосьСообщений =  ТипСообщений.Сообщения.Количество();
			// Хватает строк, что бы вывести все сообщения данного типа и остальные типы данных.
			Для Каждого Сообщение Из ТипСообщений.Сообщения Цикл
				
				Если КоличествоСвободныхСтрок = 1 
					И ОсталосьСообщений > 1 Тогда
					// Это последняя свободная строка, при этом сообщений нужно вывести больше, чем одно
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 еще %2'"), 
								ПараметрыВывода.Префикс,
								СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
										ОсталосьСообщений, ПараметрыВывода.НаименованияДляГруппыСообщений));
					ОсталосьСообщений = 0;
				Иначе
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"), 
								ПараметрыВывода.Префикс, Сообщение.Наименование);
					ОсталосьСообщений = ОсталосьСообщений - 1;
				КонецЕсли;
				
				ТекстСообщения = ?(СтрДлина(ТекстСообщения) < 37, ТекстСообщения, ЛЕВ(ТекстСообщения, 36) + "...");
				Представления["Наименование"+ИндексСтроки] = Новый ФорматированнаяСтрока(
							ТекстСообщения,
							ПараметрыВывода.Шрифт, ПараметрыВывода.Цвет);
				ИндексСтроки = ИндексСтроки + 1;
				КоличествоСвободныхСтрок = КоличествоСвободныхСтрок -1;
				Если ОсталосьСообщений = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РазделитьСообщенияПоТипам(СообщенияОтчетности)
	
	ТипыСообщений = СообщенияОтчетности.Скопировать(, "ТипСообщения, Приоритет");
	ТипыСообщений.Свернуть("ТипСообщения, Приоритет");
	ТипыСообщений.Колонки.Добавить("Сообщения", Новый ОписаниеТипов("ТаблицаЗначений"));
	ТипыСообщений.Колонки.Добавить("КоличествоСообщений", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10)));
	
	// Заполним колонку Приоритет и Сообщения
	Для Каждого ТипСообщений Из ТипыСообщений Цикл
		Если ТипСообщений.Приоритет > 0 Тогда
			ТипСообщений.Сообщения = СообщенияОтчетности.Скопировать(Новый Структура("ТипСообщения", ТипСообщений.ТипСообщения));
			ТипСообщений.Сообщения.Сортировать("ДатаСообщения Убыв");
			ТипСообщений.КоличествоСообщений = ТипСообщений.Сообщения.Количество();
		КонецЕсли;
	КонецЦикла;
	
	// Строки с приоритетом 0 не обрабатываются, удаляем их
	УдаляемыеСтроки = ТипыСообщений.НайтиСтроки(Новый Структура("Приоритет", 0));
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		ТипыСообщений.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	ТипыСообщений.Сортировать("Приоритет");
	
	Возврат ТипыСообщений;
	
КонецФункции

Функция ПараметрыВыводаПоТипуСообщений(ТипСообщений)
	
	// Параметры вывода "по умолчанию", но могут быть переопределены для разных типов сообщений.
	ПараметрыВывода = Новый Структура();
	ПараметрыВывода.Вставить("Префикс",                        НСтр("ru = 'Получено:'")); // Префикс, который будет выводится перед сообщением
	ПараметрыВывода.Вставить("НаименованияДляГруппыСообщений", НСтр("ru = 'сообщение, сообщения, сообщений'")); // Склонения предмета сообщения прив выводе группы сообщений
	ПараметрыВывода.Вставить("Цвет",                           ЦветаСтиля.ЦветТекстаИнформационнойПанели); // Цвет сообщения
	ПараметрыВывода.Вставить("Шрифт",                          ШрифтыСтиля.ШрифтТекстаИнформационнойПанели); // Шрифт сообщения
	ПараметрыВывода.Вставить("ВсегдаГруппировать",             Истина); // Если установлена Истина, то сообщения будут выведены группой даже в том случае, если хватает строк
	
	Если ТипСообщений = "ОтклоненныйОтчет" Тогда
		ПараметрыВывода.Префикс                        = НСтр("ru = 'Не сдано:'");
		ПараметрыВывода.НаименованияДляГруппыСообщений = НСтр("ru = 'отчет, отчета, отчетов'");
		ПараметрыВывода.Цвет                           = ЦветаСтиля.ВажноеСобытие;
		ПараметрыВывода.ВсегдаГруппировать             = Ложь;
	ИначеЕсли ТипСообщений = "Требование" Тогда
		ПараметрыВывода.НаименованияДляГруппыСообщений = НСтр("ru = 'требование, требования, требований'");
		ПараметрыВывода.Цвет                           = ЦветаСтиля.ВажноеСобытие;
	ИначеЕсли ТипСообщений = "ВходящееПисьмо" Тогда
		ПараметрыВывода.НаименованияДляГруппыСообщений = НСтр("ru = 'письмо, письма, писем'");
	ИначеЕсли ТипСообщений = "ОтветНаЗапрос" Тогда
		ПараметрыВывода.НаименованияДляГруппыСообщений = НСтр("ru = 'ответ на запрос, ответа на запросы, ответов на запросы'");
		ПараметрыВывода.Цвет                           = ЦветаСтиля.ЦветПоложительногоОтклоненияИнформационнойПанели;
	ИначеЕсли ТипСообщений = "СданныйОтчет" Тогда
		ПараметрыВывода.Префикс = НСтр("ru = 'Сдано:'");
		ПараметрыВывода.НаименованияДляГруппыСообщений = НСтр("ru = 'отчет, отчета, отчетов'");
		ПараметрыВывода.Цвет                           = ЦветаСтиля.ЦветПоложительногоОтклоненияИнформационнойПанели;
		ПараметрыВывода.ВсегдаГруппировать             = Ложь;
	ИначеЕсли ТипСообщений = "СообщенийНет" Тогда
		ПараметрыВывода.Префикс = НСтр("ru = ''");
	КонецЕсли;
	
	Возврат ПараметрыВывода;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераОтчетность(Результат, Параметры)
	
	НужноПоказыватьБаннер = Не Результат.ПользовательНачалИспользоватьОтчетность
		Или НужноПредложитьПодключитьТарифВИнтеграцииСБанками(Параметры.Организация);
	
	Если НужноПоказыватьБаннер Тогда
		ЗаполнитьСвойстваБаннера(Результат, Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСвойстваБаннера(Результат, Параметры)
	
	СостояниеЗаявления = ОбработкаЗаявленийАбонента.СостояниеЗаявленияНаПодключениеОтчетности(
		Параметры.Организация);
	
	Результат.Отчетность_ПоказыватьБаннер = Истина;
	
	СвойстваБаннера = СвойстваБаннера(Параметры,
		Результат.Отчетность_ОтчетностьПодключена, СостояниеЗаявления);
	
	Результат.Отчетность_КартинкаБаннера = СвойстваБаннера.Картинка;
	Результат.Отчетность_ТекстБаннера = СвойстваБаннера.Текст;
	Результат.Отчетность_ЗаявлениеНаПодключение = СвойстваБаннера.Заявление;
	Результат.Состояние = СвойстваБаннера.Состояние;
	Результат.Ссылка = СвойстваБаннера.Ссылка;

КонецПроцедуры

Функция НужноПредложитьПодключитьТарифВИнтеграцииСБанками(Организация)
	
	Возврат ИнтеграцияСБанкамиПовтИсп.ИнтеграцияОрганизацииВключена(Организация)
		И Не ТарификацияБПВызовСервераПовтИсп.РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами();
	
КонецФункции

Функция СвойстваБаннера(ПараметрыЗаполненияБаннера, ОтчетностьПодключена, СостояниеЗаявления)

	СвойстваБаннера = Новый Структура;
	СвойстваБаннера.Вставить("Картинка", Неопределено);
	СвойстваБаннера.Вставить("Текст", "");
	СвойстваБаннера.Вставить("Заявление", Неопределено);
	СвойстваБаннера.Вставить("Состояние", "");
	СвойстваБаннера.Вставить("Ссылка", "");
	СостоянияОтчетности = СостоянияРегламентированнойОтчетности();
	
	Если НужноПредложитьПодключитьТарифВИнтеграцииСБанками(ПараметрыЗаполненияБаннера.Организация) Тогда
		СвойстваБаннера.Картинка = БиблиотекаКартинок.Баннер_ОтчетностьНеПодключена;
		ДанныеБаннера = ТекстБаннераПодключитеОтчетностьВЛичномКабинетеБанка(
			ПараметрыЗаполненияБаннера.Организация, 
			ОтчетностьПодключена, 
			ПараметрыЗаполненияБаннера.ВыводитьДополнительныйРазделитель, 
			ПараметрыЗаполненияБаннера.ВыводитьСсылкиВТекст);
		СвойстваБаннера.Текст = ДанныеБаннера.Текст;
		СвойстваБаннера.Ссылка = ДанныеБаннера.Ссылка;
		СвойстваБаннера.Состояние = СостоянияОтчетности.ПодпискаОтсутствует;
		Возврат СвойстваБаннера;
	КонецЕсли;
	
	Если ОтчетностьПодключена
		Или СостояниеЗаявления.Состояние = "Одобрено" Тогда
		// Отчетность уже подключена, но не отправлено ни одного отчета,
		// или заявление одобрено, но до конца отчетность еще не подключена.
		// Нужно показать, что все готово и можно отправлять отчеты.
		СвойстваБаннера.Картинка = БиблиотекаКартинок.Баннер_ОтчетностьПодключена;
		СвойстваБаннера.Текст = ТекстБаннераПодключенаОтчетность(ПараметрыЗаполненияБаннера.ВыводитьДополнительныйРазделитель,
			ПараметрыЗаполненияБаннера.ВыводитьСсылкиВТекст);
		СвойстваБаннера.Заявление = СостояниеЗаявления.Заявление;
		СвойстваБаннера.Состояние = СостоянияОтчетности.Подключено;
		Возврат СвойстваБаннера;
	КонецЕсли;
	
	Если СостояниеЗаявления.Состояние = "Отправлено" Тогда
		СвойстваБаннера.Картинка = БиблиотекаКартинок.Баннер_ОтчетностьЗаявлениеОтправлено;
		СвойстваБаннера.Текст = ТекстБаннераЗаявлениеОтправлено(ПараметрыЗаполненияБаннера.ВыводитьДополнительныйРазделитель,
			ПараметрыЗаполненияБаннера.ВыводитьСсылкиВТекст);
		СвойстваБаннера.Заявление = СостояниеЗаявления.Заявление;
		СвойстваБаннера.Состояние = СостоянияОтчетности.ЗаявлениеОтправлено;
		Возврат СвойстваБаннера;
	КонецЕсли;
	
	Если СостояниеЗаявления.Состояние = "Отклонено" Тогда
		СвойстваБаннера.Картинка = БиблиотекаКартинок.Баннер_ОтчетностьЗаявлениеОтклонено;
		СвойстваБаннера.Текст = ТекстБаннераЗаявлениеОтклонено(ПараметрыЗаполненияБаннера.ВыводитьДополнительныйРазделитель,
			ПараметрыЗаполненияБаннера.ВыводитьСсылкиВТекст);
		СвойстваБаннера.Заявление = СостояниеЗаявления.Заявление;
		СвойстваБаннера.Состояние = СостоянияОтчетности.ЗаявлениеОтклонено;
		Возврат СвойстваБаннера;
	КонецЕсли;
	
	// Заявление еще не отправлено.
	// Для интеграции с банками показываем баннер про подключенную отчетность, но не завершенное заявление.
	// Для остальных версий показываем рекламу
	
	Если ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВИнформационнойБазеВключена() Тогда
		СвойстваБаннера.Картинка = БиблиотекаКартинок.Баннер_ОтчетностьНеНастроена;
		
		СвойстваБаннера.Текст = ТекстБаннераНастройтеОтчетность(ПараметрыЗаполненияБаннера.ВыводитьДополнительныйРазделитель, 
			ПараметрыЗаполненияБаннера.ВыводитьСсылкиВТекст);
			
		СвойстваБаннера.Состояние = СостоянияОтчетности.Отключено;
		Возврат СвойстваБаннера;
	Иначе
		СвойстваБаннера.Картинка = БиблиотекаКартинок.Баннер_ОтчетностьНеПодключена;
		СвойстваБаннера.Текст = ТекстБаннераПодключитеОтчетность(ПараметрыЗаполненияБаннера.ВыводитьДополнительныйРазделитель, 
			ПараметрыЗаполненияБаннера.ВыводитьСсылкиВТекст);
		СвойстваБаннера.Состояние = СостоянияОтчетности.ПодпискаОтсутствует;
		Возврат СвойстваБаннера;
	КонецЕсли;
	
КонецФункции

Функция ТекстБаннераПодключенаОтчетность(ВыводитьДополнительныйРазделитель, ВыводитьСсылкиВТекст)
	
	ЧастиТекста = Новый Массив;
	
	Если ВыводитьДополнительныйРазделитель Тогда
		ЧастиТекста.Добавить(Символы.ПС);
	КонецЕсли;
	
	ЧастиТекста.Добавить(НСтр("ru = 'Подключена!'"));
	
	ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
	
	ЧастиТекста.Добавить(НСтр("ru = 'Теперь можно отправлять отчетность прямо здесь'"));
	
	Если ВыводитьСсылкиВТекст Тогда
		ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
		ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru = 'Отправить'"), , , , "Открыть1СОтчетность"));
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ЧастиТекста);

КонецФункции

Функция ТекстБаннераПодключитеОтчетность(ВыводитьДополнительныйРазделитель, ВыводитьСсылкиВТекст)
	
	ЧастиТекста = Новый Массив;
	
	Если ВыводитьДополнительныйРазделитель Тогда
		ЧастиТекста.Добавить(Символы.ПС);
	КонецЕсли;
	
	ЧастиТекста.Добавить(НСтр("ru = 'Отправляйте отчетность прямо из программы!'"));
	
	Если ВыводитьСсылкиВТекст Тогда
		ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
		ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Подключиться'"), , , , "ОткрытьПредложениеПодключитьсяКОтчетности"));
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ЧастиТекста);
	
КонецФункции

Функция ТекстБаннераПодключитеОтчетностьВЛичномКабинетеБанка(Организация, ОтчетностьПодключена, ВыводитьДополнительныйРазделитель, ВыводитьСсылкиВТекст)
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("Текст", "");
	ДанныеБаннера.Вставить("Ссылка", "");
	
	ЧастиТекста = Новый Массив;
	
	Если ВыводитьДополнительныйРазделитель Тогда
		ЧастиТекста.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если ОтчетностьПодключена Тогда
		ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Закончилась подписка!'"), , ЦветаСтиля.ВажноеСобытие));
	Иначе
		ЧастиТекста.Добавить(НСтр("ru = 'Отправляйте отчетность прямо из программы!'"));
	КонецЕсли;
	
	ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);

	ЧастиТекста.Добавить(НСтр("ru = 'Подключите в личном кабинете банка'"));
	
	ГиперссылкаДляПереходаКСпискуПлатежей = "";
	НастройкиИнтеграцииСБанком = Справочники.НастройкиИнтеграцииСБанками.НастройкиИнтеграцииСБанкомДляОбменаРегламентированнойОтчетностью(
		Организация);
	
	Если ЗначениеЗаполнено(НастройкиИнтеграцииСБанком)
		И НастройкиИнтеграцииСБанком.Количество() = 1 Тогда
		ГиперссылкаДляПереходаКСпискуПлатежей = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			НастройкиИнтеграцииСБанком[0], "ГиперссылкаДляПереходаКСпискуПлатежей");
	КонецЕсли;
	
	Если ВыводитьСсылкиВТекст Тогда
		ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
		Если ЗначениеЗаполнено(ГиперссылкаДляПереходаКСпискуПлатежей) Тогда
			ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Подключиться'"), , , , ГиперссылкаДляПереходаКСпискуПлатежей));
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ГиперссылкаДляПереходаКСпискуПлатежей) Тогда
		ДанныеБаннера.Вставить("Ссылка", ГиперссылкаДляПереходаКСпискуПлатежей);
	КонецЕсли;
	
	ДанныеБаннера.Вставить("Текст", Новый ФорматированнаяСтрока(ЧастиТекста));
	Возврат ДанныеБаннера;
	
КонецФункции

Функция ТекстБаннераНастройтеОтчетность(ВыводитьДополнительныйРазделитель, ВыводитьСсылкиВТекст)
	
	ЧастиТекста = Новый Массив;
	
	Если ВыводитьДополнительныйРазделитель Тогда
		ЧастиТекста.Добавить(Символы.ПС);
	КонецЕсли;
	
	ЧастиТекста.Добавить(НСтр("ru = 'Подключена!'"));
	
	ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
	
	ЧастиТекста.Добавить(НСтр("ru = 'Осталось настроить'"));
	
	Если ВыводитьСсылкиВТекст Тогда
		ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
		ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Настроить'"), , , , "ОткрытьМастерПодключения"));
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ЧастиТекста);
	
КонецФункции

Функция ТекстБаннераЗаявлениеОтправлено(ВыводитьДополнительныйРазделитель, ВыводитьСсылкиВТекст)
	
	ЧастиТекста = Новый Массив;
	
	Если ВыводитьДополнительныйРазделитель Тогда
		ЧастиТекста.Добавить(Символы.ПС);
	КонецЕсли;
	
	ЧастиТекста.Добавить(НСтр("ru = 'Сейчас подключается...'"));
	
	ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
	
	ЧастиТекста.Добавить(НСтр("ru = 'Скоро придет сообщение о подключении'"));
		
	Если ВыводитьСсылкиВТекст Тогда
		ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
		ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Проверить состояние'"), , , , "ОткрытьЗаявление"));
	КонецЕсли;
		
	Возврат Новый ФорматированнаяСтрока(ЧастиТекста);
	
КонецФункции

Функция ТекстБаннераЗаявлениеОтклонено(ВыводитьДополнительныйРазделитель, ВыводитьСсылкиВТекст)

	ЧастиТекста = Новый Массив;
	
	Если ВыводитьДополнительныйРазделитель Тогда
		ЧастиТекста.Добавить(Символы.ПС);
	КонецЕсли;

	ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru = 'Заявление отклонено!'"), , ЦветаСтиля.ВажноеСобытие));
		
	ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
		
	ЧастиТекста.Добавить(НСтр("ru = 'В заявлении есть ошибки'"));
		
	Если ВыводитьСсылкиВТекст Тогда
		ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель);
		ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Посмотреть и исправить'"), , , , "ОткрытьЗаявление"));
	КонецЕсли;
		
	Возврат Новый ФорматированнаяСтрока(ЧастиТекста);
	
КонецФункции

Процедура ДобавитьРазделительСтрок(ЧастиТекста, ВыводитьДополнительныйРазделитель)

	ЧастиТекста.Добавить(Символы.ПС);
	
	Если ВыводитьДополнительныйРазделитель Тогда
		ЧастиТекста.Добавить(Символы.ПС);
	КонецЕсли;

КонецПроцедуры

Функция ОтчетностьДоступна()
	
	Возврат ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.РегламентированнаяОтчетность);
	
КонецФункции

Функция НовыйПараметрыПолученияБаннераОтчетность() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",                       Неопределено);
	СтруктураПараметров.Вставить("Инициализация",                     Ложь);
	СтруктураПараметров.Вставить("ВыводитьДополнительныйРазделитель", Истина);
	СтруктураПараметров.Вставить("ВыводитьСсылкиВТекст",              Истина);
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция СостоянияРегламентированнойОтчетности() Экспорт
	
	СостоянияОтчетности = Новый Структура;
	СостоянияОтчетности.Вставить("ПодпискаОтсутствует", "ПодпискаОтсутствует");
	СостоянияОтчетности.Вставить("Отключено", "Отключено");
	СостоянияОтчетности.Вставить("ЗаявлениеОтправлено", "ЗаявлениеОтправлено");
	СостоянияОтчетности.Вставить("ЗаявлениеОтклонено", "ЗаявлениеОтклонено");
	СостоянияОтчетности.Вставить("Подключено", "Подключено");
	
	Возврат СостоянияОтчетности;
	
КонецФункции

#КонецОбласти

#Область ОстаткиДенежныхСредств

Процедура ДобавитьОстаткиДенежныхСредств(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаОстаткиДенежныхСредств(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаОстаткиДенежныхСредств(),
		"БлокиИнформационнойПанели.ОстаткиДенежныхСредств",
		"БлокиИнформационнойПанели.ОбновитьОстаткиДенежныхСредств",
		СвойстваОстаткиДенежныхСредств());
	
КонецПроцедуры

Функция СвойстваОстаткиДенежныхСредств()
	
	// ВГраница = Количеству, т.к. последняя строка используется для итоговой записи
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоВалютныхОстатков();
	
	Свойства = "ОстаткиДенежныхСредств_Заголовок, "
					+ "ОстаткиДенежныхСредств_Касса, "
					+ "ОстаткиДенежныхСредств_Банк, "
					+ "ОстаткиДенежныхСредств_ИндексИтога,"
					+ "ОстаткиДенежныхСредств_ПредупрежденияПриЗагрузке";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоОстатокДенежныхСредств(Индекс);
	КонецЦикла;
	
	Возврат Свойства;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьОстаткиДенежныхСредств(Параметры) Экспорт
	
	РазделыМонитора = Новый Массив;
	РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.ОстаткиДенежныхСредств);
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Параметры.Организация);
	ПараметрыЗаполнения.Вставить("РазделыМонитора", РазделыМонитора);
	ПараметрыЗаполнения.Вставить("ПолучатьПрошлыйПериод", Ложь);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	
	УстановитьПривилегированныйРежим(Истина);
	Если Параметры.БыстроеОбновление Тогда
		МониторРуководителя.ОбновитьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	Иначе
		МониторРуководителя.ПерезаписатьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ВариантОкругления - Число - 1 - округлять до целых рублей, 1000 - до тысяч 
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура ОстаткиДенежныхСредств(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("ОстаткиДенежныхСредств_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаОстаткиДенежныхСредств()));
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваОстаткиДенежныхСредств());
	Результат.Вставить("ОстаткиДенежныхСредств_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаОстаткиДенежныхСредств()));
	
	ОстаткиДенежныхСредств = МониторРуководителя.ДанныеОстаткиДенежныхСредств(Параметры);
	
	ИспользоватьВалютныйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет");
	Если ИспользоватьВалютныйУчет Тогда
		ВалютаПоУмолчанию = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Иначе
		ВалютаПоУмолчанию = Неопределено;
	КонецЕсли;
	
	// Остаток в кассе
	Касса = ПредставлениеОстаткаДенежныхСредств(ОстаткиДенежныхСредств.Касса, ВалютаПоУмолчанию);
	Результат.Вставить("ОстаткиДенежныхСредств_Касса", Касса);
	
	ВалютныеОстатки = ОстаткиДенежныхСредств.ВалютныеОстатки;
	
	МожноВывестиВалютныхОстатков = БлокиИнформационнойПанелиКлиентСервер.КоличествоВалютныхОстатков();
	
	//Если подключено автоматическое получение выписок по ДиректБанку, то "ПредупрежденияОЗагрузкеВСписке" не выводим
	СписокДоступныхОрганизаций = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		СписокДоступныхОрганизаций.Добавить(Параметры.Организация);
	Иначе
		СписокДоступныхОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	КонецЕсли;
	АвтоматическоеПолучениеВыпискиДоступно = ЭлектронноеВзаимодействиеБПВызовСервера.АвтоматическоеПолучениеВыпискиДоступно(СписокДоступныхОрганизаций);
	Если АвтоматическоеПолучениеВыпискиДоступно Тогда
		ЕстьПредупрежденияОЗагрузкеВСписке = Ложь;
	Иначе
		ПараметрыОтбораПредупреждений = РегистрыСведений.ПредупрежденияПриЗагрузкеВыписки.НовыеПараметрыОтбораПредупреждений();
		ПараметрыОтбораПредупреждений.Организация = Параметры.Организация;
		ЕстьПредупрежденияОЗагрузкеВСписке = 
			РегистрыСведений.ПредупрежденияПриЗагрузкеВыписки.ЕстьПредупрежденияВСписке(ПараметрыОтбораПредупреждений);
	КонецЕсли;
	Если ЕстьПредупрежденияОЗагрузкеВСписке Тогда
		// Одну строку резервируем под сообщение о том, что документы загружены с предупреждением.
		МожноВывестиВалютныхОстатков = МожноВывестиВалютныхОстатков - 1;
	КонецЕсли;
	
	ИндексСтроки = 0;
	
	// Сообщение о загрузке банковских документов с предупреждениями.
	Если ЕстьПредупрежденияОЗагрузкеВСписке Тогда
		СообщениеОПредупрежденияхПриЗагрузке = СообщениеОПредупрежденияхПриЗагрузке();
		Результат.Вставить("ОстаткиДенежныхСредств_ПредупрежденияПриЗагрузке",
			СообщениеОПредупрежденияхПриЗагрузке);
		ИндексСтроки = ИндексСтроки + 1;
	КонецЕсли;
	
	Если Не ИспользоватьВалютныйУчет Тогда
		
		// Остаток на рублевых счетах
		Банк = ПредставлениеОстаткаДенежныхСредств(ОстаткиДенежныхСредств.Банк, ВалютаПоУмолчанию);
		
	ИначеЕсли ВалютныеОстатки.Количество() <= МожноВывестиВалютныхОстатков Тогда
		
		// Остаток на рублевых счетах
		Банк = ПредставлениеОстаткаДенежныхСредств(ОстаткиДенежныхСредств.Банк, ВалютаПоУмолчанию);
		
		// Остатки на валютных счетах с разбивкой по валютам
		Для Каждого СтрокаОстатка Из ВалютныеОстатки Цикл
			
			Остаток = ПредставлениеОстаткаДенежныхСредств(СтрокаОстатка.СуммаВВалюте, СтрокаОстатка.Валюта);
			Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоОстатокДенежныхСредств(ИндексСтроки), Остаток);
			
			ИндексСтроки = ИндексСтроки + 1;
			
		КонецЦикла;
		
	Иначе
		
		// Группируем все остатки в одну строку
		Банк = ПредставлениеОстаткаДенежныхСредств(ОстаткиДенежныхСредств.Банк + ВалютныеОстатки.Итог("Сумма"), ВалютаПоУмолчанию);
		
	КонецЕсли;
	Результат.Вставить("ОстаткиДенежныхСредств_Банк", Банк);
	
	// Итого
	Итого = ПредставлениеОстаткаДенежныхСредств(ОстаткиДенежныхСредств.Итого, ВалютаПоУмолчанию, Истина);
	Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоОстатокДенежныхСредств(ИндексСтроки), Итого);
	Результат.Вставить("ОстаткиДенежныхСредств_ИндексИтога", ИндексСтроки);
	ИндексСтроки = ИндексСтроки + 1;
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

Функция ПредставлениеОстаткаДенежныхСредств(Остаток, Валюта, Выделить = Ложь)
	
	ФорматнаяСтрока = "ЧДЦ=; ЧН=0";
	
	Если ЗначениеЗаполнено(Валюта) Тогда
		СтроковоеПредставление = СтрШаблон("%1 %2", Формат(Остаток, ФорматнаяСтрока), Валюта);
	Иначе
		СтроковоеПредставление = Формат(Остаток, ФорматнаяСтрока);
	КонецЕсли;
	
	Шрифт      = ?(Выделить, Новый Шрифт(ШрифтыСтиля.ШрифтТекстаИнформационнойПанели,,, Истина), ШрифтыСтиля.ШрифтТекстаИнформационнойПанели);
	ЦветТекста = ?(Остаток >= 0, ЦветаСтиля.ЦветТекстаИнформационнойПанели, ЦветаСтиля.ВажноеСобытие);
	
	Возврат Новый ФорматированнаяСтрока(СтроковоеПредставление, Шрифт, ЦветТекста);
	
КонецФункции

Функция ОстаткиДенежныхСредствДоступны()
	
	// Блок доступен для пользователей, которым доступен отчет "Остатки денежных средств".
	// Сведения об остатках получаются из регистра "ДанныеМонитораРуководителя" в привилегированном режиме.
	Возврат ПравоДоступа("Использование", Метаданные.Отчеты.ОстаткиДенежныхСредств);
	
КонецФункции

Функция СообщениеОПредупрежденияхПриЗагрузке()
	
	СообщениеОПредупрежденияхПриЗагрузке = НСтр("ru = 'Проверьте загрузку документов из банка'");
	Возврат Новый ФорматированнаяСтрока(СообщениеОПредупрежденияхПриЗагрузке, , ЦветаСтиля.ВажноеСобытие, , "ПерейтиВЖурналБанковскихДокументов");
	
КонецФункции

#КонецОбласти

#Область Покупатели

Процедура ДобавитьПокупатели(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаПокупатели(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПокупатели(),
		"БлокиИнформационнойПанели.Покупатели",
		"БлокиИнформационнойПанели.ОбновитьПокупатели",
		СвойстваПокупатели());
	
КонецПроцедуры

Функция СвойстваПокупатели()
	
	Свойства = "Покупатели_Заголовок, Покупатели_НеоплаченныеСчета, "
					+ "Покупатели_Задолженность, Покупатели_ЗадолженностьОтклонение, "
					+ "Покупатели_ПросроченнаяЗадолженность, Покупатели_ПросроченнаяЗадолженностьОтклонение,"
					+ "Покупатели_ДоступностьРазделов";
	
	Возврат Свойства;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ПоказыватьСравнениеСПрошлымГодом - Булево - необходимость сравнения с показателями прошлого года
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьПокупатели(Параметры) Экспорт
	
	РазделыМонитора = Новый Массив;
	РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.ЗадолженностьПокупателей);
	РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.НеоплаченныеСчетаПокупателям);
	
	Если ПравоДоступа("Чтение", Метаданные.Константы.СрокОплатыПокупателей) Тогда
		ЗаданСрокОплатыПокупателей = (Константы.СрокОплатыПокупателей.Получить() > 0);
		Если ЗаданСрокОплатыПокупателей Тогда
			РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПокупателей);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Параметры.Организация);
	ПараметрыЗаполнения.Вставить("РазделыМонитора", РазделыМонитора);
	ПараметрыЗаполнения.Вставить("ПолучатьПрошлыйПериод", Параметры.ПоказыватьСравнениеСПрошлымГодом);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	
	УстановитьПривилегированныйРежим(Истина);
	Если Параметры.БыстроеОбновление Тогда
		МониторРуководителя.ОбновитьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	Иначе
		МониторРуководителя.ПерезаписатьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ВариантОкругления - Число - 1 - округлять до целых рублей, 1000 - до тысяч 
//     * ПоказыватьСравнениеСПрошлымГодом - Булево - необходимость сравнения с показателями прошлого года
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура Покупатели(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("Покупатели_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПокупатели()));
		Результат.Вставить("Покупатели_ДоступностьРазделов", ПокупателиДоступностьРазделов());
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваПокупатели());
	Результат.Вставить("Покупатели_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПокупатели()));
	Результат.Вставить("Покупатели_ДоступностьРазделов", ПокупателиДоступностьРазделов());
	
	Покупатели = МониторРуководителя.ДанныеПокупатели(Параметры);
	
	Шрифт      = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	
	// Задолженность
	Если Покупатели.Задолженность = 0 И Покупатели.ЗадолженностьОтклонение <> 0 И Параметры.ПоказыватьСравнениеСПрошлымГодом Тогда
		ФорматнаяСтрокаЗадолженность = "ЧДЦ=; ЧН=0";
	Иначе
		ФорматнаяСтрокаЗадолженность = "ЧДЦ=";
	КонецЕсли;
	
	Задолженность = Новый ФорматированнаяСтрока(
		Формат(Покупатели.Задолженность, ФорматнаяСтрокаЗадолженность),
		Шрифт,
		ЦветТекста);
	Результат.Вставить("Покупатели_Задолженность", Задолженность);
	
	// Просроченная задолженность
	Если Покупатели.ПросроченнаяЗадолженность = 0 И Покупатели.ПросроченнаяЗадолженностьОтклонение <> 0 И Параметры.ПоказыватьСравнениеСПрошлымГодом Тогда
		ФорматнаяСтрокаПросроченнаяЗадолженность = "ЧДЦ=; ЧН=0";
	Иначе
		ФорматнаяСтрокаПросроченнаяЗадолженность = "ЧДЦ=";
	КонецЕсли;
	
	ПросроченнаяЗадолженность = Новый ФорматированнаяСтрока(
		Формат(Покупатели.ПросроченнаяЗадолженность, ФорматнаяСтрокаПросроченнаяЗадолженность),
		Шрифт,
		ЦветТекста);
	Результат.Вставить("Покупатели_ПросроченнаяЗадолженность", ПросроченнаяЗадолженность);
	
	// Отклонения
	Если Параметры.ПоказыватьСравнениеСПрошлымГодом Тогда
		
		ЗадолженностьОтклонение = ПредставлениеОтклонения(Покупатели.ЗадолженностьОтклонение);
		Результат.Вставить("Покупатели_ЗадолженностьОтклонение", ЗадолженностьОтклонение);
		
		ПросроченнаяЗадолженностьОтклонение = ПредставлениеОтклонения(Покупатели.ПросроченнаяЗадолженностьОтклонение);
		Результат.Вставить("Покупатели_ПросроченнаяЗадолженностьОтклонение",ПросроченнаяЗадолженностьОтклонение);
		
	КонецЕсли;
	
	// Неоплаченные счета
	
	НеоплаченныеСчета = Новый ФорматированнаяСтрока(
		Формат(Покупатели.НеоплаченныеСчета, "ЧДЦ="),
		Шрифт,
		ЦветТекста);
	Результат.Вставить("Покупатели_НеоплаченныеСчета", НеоплаченныеСчета);
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

Функция ПокупателиДоступностьРазделов()
	
	ВестиУчетРасчетовСКонтрагентами = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСКонтрагентами");
	
	ЗадолженностьПокупателей = ВестиУчетРасчетовСКонтрагентами
		И ПравоДоступа("Использование", Метаданные.Отчеты.ЗадолженностьПокупателей);
	
	ЗадолженностьПокупателейПоСрокамДолга = ВестиУчетРасчетовСКонтрагентами
		И ПравоДоступа("Использование", Метаданные.Отчеты.ЗадолженностьПокупателейПоСрокамДолга)
		И ПравоДоступа("Чтение", Метаданные.Константы.СрокОплатыПокупателей)
		И Константы.СрокОплатыПокупателей.Получить() > 0;
		
	АнализНеоплаченныхСчетовПокупателям =
		ПравоДоступа("Использование", Метаданные.Отчеты.АнализНеоплаченныхСчетовПокупателям)
		И Перечисления.РазделыМонитораРуководителя.ДанныеРазделаДоступны(Перечисления.РазделыМонитораРуководителя.НеоплаченныеСчетаПокупателям);
		
	ДоступностьРазделов = Новый Структура();
	ДоступностьРазделов.Вставить("ЗадолженностьПокупателей",              ЗадолженностьПокупателей);
	ДоступностьРазделов.Вставить("ЗадолженностьПокупателейПоСрокамДолга", ЗадолженностьПокупателейПоСрокамДолга);
	ДоступностьРазделов.Вставить("АнализНеоплаченныхСчетовПокупателям",   АнализНеоплаченныхСчетовПокупателям);
	ДоступностьРазделов.Вставить("СозданиеСчета",                         ПравоДоступа("Изменение", Метаданные.Документы.СчетНаОплатуПокупателю));
	ДоступностьРазделов.Вставить("СозданиеРеализации",                    ПравоДоступа("Изменение", Метаданные.Документы.РеализацияТоваровУслуг));
	
	Возврат ДоступностьРазделов;
	
КонецФункции

Функция ПокупателиДоступны()
	
	ДоступностьРазделов = ПокупателиДоступностьРазделов();
	
	Возврат ДоступностьРазделов.ЗадолженностьПокупателей
		ИЛИ ДоступностьРазделов.ЗадолженностьПокупателейПоСрокамДолга
		ИЛИ ДоступностьРазделов.АнализНеоплаченныхСчетовПокупателям;
	
КонецФункции

#КонецОбласти

#Область Поставщики

Процедура ДобавитьПоставщики(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаПоставщики(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПоставщики(),
		"БлокиИнформационнойПанели.Поставщики",
		"БлокиИнформационнойПанели.ОбновитьПоставщики",
		СвойстваПоставщики());
	
КонецПроцедуры

Функция СвойстваПоставщики()
	
	Свойства = "Поставщики_Заголовок, Поставщики_НеоплаченныеСчета, "
					+ "Поставщики_Задолженность, Поставщики_ЗадолженностьОтклонение, "
					+ "Поставщики_ПросроченнаяЗадолженность, Поставщики_ПросроченнаяЗадолженностьОтклонение,"
					+ "Поставщики_ДоступностьРазделов";
	
	Возврат Свойства;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ПоказыватьСравнениеСПрошлымГодом - Булево - необходимость сравнения с показателями прошлого года
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьПоставщики(Параметры) Экспорт
	
	РазделыМонитора = Новый Массив;
	РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.ЗадолженностьПоставщикам);
	РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.НеоплаченныеСчетаПоставщиков);
	
	ЗаданСрокОплатыПоставщикам = (Константы.СрокОплатыПоставщикам.Получить() > 0);
	Если ЗаданСрокОплатыПоставщикам Тогда
		РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПоставщикам);
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Параметры.Организация);
	ПараметрыЗаполнения.Вставить("РазделыМонитора", РазделыМонитора);
	ПараметрыЗаполнения.Вставить("ПолучатьПрошлыйПериод", Параметры.ПоказыватьСравнениеСПрошлымГодом);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	
	УстановитьПривилегированныйРежим(Истина);
	Если Параметры.БыстроеОбновление Тогда
		МониторРуководителя.ОбновитьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	Иначе
		МониторРуководителя.ПерезаписатьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ВариантОкругления - Число - 1 - округлять до целых рублей, 1000 - до тысяч 
//     * ПоказыватьСравнениеСПрошлымГодом - Булево - необходимость сравнения с показателями прошлого года
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура Поставщики(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("Поставщики_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПоставщики()));
		Результат.Вставить("Поставщики_ДоступностьРазделов", ПоставщикиДоступностьРазделов());
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваПоставщики());
	Результат.Вставить("Поставщики_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПоставщики()));
	Результат.Вставить("Поставщики_ДоступностьРазделов", ПоставщикиДоступностьРазделов());
	
	Поставщики = МониторРуководителя.ДанныеПоставщики(Параметры);
	
	Шрифт      = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	
	// Задолженность
	Если Поставщики.Задолженность = 0 И Поставщики.ЗадолженностьОтклонение <> 0 И Параметры.ПоказыватьСравнениеСПрошлымГодом Тогда
		ФорматнаяСтрокаЗадолженность = "ЧДЦ=; ЧН=0";
	Иначе
		ФорматнаяСтрокаЗадолженность = "ЧДЦ=";
	КонецЕсли;
	
	Задолженность = Новый ФорматированнаяСтрока(
		Формат(Поставщики.Задолженность, ФорматнаяСтрокаЗадолженность),
		Шрифт,
		ЦветТекста);
	Результат.Вставить("Поставщики_Задолженность", Задолженность);
	
	// Просроченная задолженность
	Если Поставщики.ПросроченнаяЗадолженность = 0 И Поставщики.ПросроченнаяЗадолженностьОтклонение <> 0 И Параметры.ПоказыватьСравнениеСПрошлымГодом Тогда
		ФорматнаяСтрокаПросроченнаяЗадолженность = "ЧДЦ=; ЧН=0";
	Иначе
		ФорматнаяСтрокаПросроченнаяЗадолженность = "ЧДЦ=";
	КонецЕсли;
	
	ПросроченнаяЗадолженность = Новый ФорматированнаяСтрока(
		Формат(Поставщики.ПросроченнаяЗадолженность, ФорматнаяСтрокаПросроченнаяЗадолженность),
		Шрифт,
		ЦветТекста);
	Результат.Вставить("Поставщики_ПросроченнаяЗадолженность", ПросроченнаяЗадолженность);
	
	// Отклонения
	Если Параметры.ПоказыватьСравнениеСПрошлымГодом Тогда
		
		ЗадолженностьОтклонение = ПредставлениеОтклонения(Поставщики.ЗадолженностьОтклонение);
		Результат.Вставить("Поставщики_ЗадолженностьОтклонение", ЗадолженностьОтклонение);
		
		ПросроченнаяЗадолженностьОтклонение = ПредставлениеОтклонения(Поставщики.ПросроченнаяЗадолженностьОтклонение);
		Результат.Вставить("Поставщики_ПросроченнаяЗадолженностьОтклонение",ПросроченнаяЗадолженностьОтклонение);
		
	КонецЕсли;
	
	// Неоплаченные счета
	
	НеоплаченныеСчета = Новый ФорматированнаяСтрока(
		Формат(Поставщики.НеоплаченныеСчета, "ЧДЦ="),
		Шрифт,
		ЦветТекста);
	Результат.Вставить("Поставщики_НеоплаченныеСчета", НеоплаченныеСчета);
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

Функция ПоставщикиДоступностьРазделов()
	
	ЗадолженностьПоставщикамПоСрокамДолга = 
		ПравоДоступа("Использование", Метаданные.Отчеты.ЗадолженностьПоставщикамПоСрокамДолга)
		И Константы.СрокОплатыПоставщикам.Получить() > 0;
	
	ДоступностьРазделов = Новый Структура();
	ДоступностьРазделов.Вставить("ЗадолженностьПоставщикам",              ПравоДоступа("Использование", Метаданные.Отчеты.ЗадолженностьПоставщикам));
	ДоступностьРазделов.Вставить("ЗадолженностьПоставщикамПоСрокамДолга", ЗадолженностьПоставщикамПоСрокамДолга);
	ДоступностьРазделов.Вставить("АнализНеоплаченныхСчетовПоставщиков",   ПравоДоступа("Использование", Метаданные.Отчеты.АнализНеоплаченныхСчетовПоставщиков));
	
	Возврат ДоступностьРазделов;
	
КонецФункции

Функция ПоставщикиДоступны()
	
	ДоступностьРазделов = ПоставщикиДоступностьРазделов();
	
	Возврат ДоступностьРазделов.ЗадолженностьПоставщикам
		ИЛИ ДоступностьРазделов.ЗадолженностьПоставщикамПоСрокамДолга
		ИЛИ ДоступностьРазделов.АнализНеоплаченныхСчетовПоставщиков;
	
КонецФункции

#КонецОбласти

#Область Продажи

Процедура ДобавитьПродажи(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаПродажи(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПродажи(),
		"БлокиИнформационнойПанели.Продажи",
		"БлокиИнформационнойПанели.ОбновитьПродажи",
		СвойстваПродажи());
	
КонецПроцедуры

Функция СвойстваПродажи()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоПериодовПродаж() - 1;
	
	Свойства = "Продажи_Заголовок";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоПериодПродаж(Индекс) + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоСуммаПродаж(Индекс) + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоОтклонениеПродаж(Индекс);
	КонецЦикла;

	Возврат Свойства;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ПоказыватьСравнениеСПрошлымГодом - Булево - необходимость сравнения с показателями прошлого года
//
Процедура ОбновитьПродажи(Параметры) Экспорт
	
	РазделыМонитора = Новый Массив;
	РазделыМонитора.Добавить(Перечисления.РазделыМонитораРуководителя.ПродажиПоКонтрагентам);
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Параметры.Организация);
	ПараметрыЗаполнения.Вставить("РазделыМонитора", РазделыМонитора);
	ПараметрыЗаполнения.Вставить("ПолучатьПрошлыйПериод", Параметры.ПоказыватьСравнениеСПрошлымГодом);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	
	УстановитьПривилегированныйРежим(Истина);
	Если Параметры.БыстроеОбновление Тогда
		МониторРуководителя.ОбновитьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	Иначе
		МониторРуководителя.ПерезаписатьДанныеМонитораВФоне(ПараметрыЗаполнения, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * ВариантОкругления - Число - 1 - округлять до целых рублей, 1000 - до тысяч 
//     * ПоказыватьСравнениеСПрошлымГодом - Булево - необходимость сравнения с показателями прошлого года
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура Продажи(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("Продажи_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПродажи()));
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваПродажи());
	Результат.Вставить("Продажи_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПродажи()));
	
	Продажи = МониторРуководителя.ДанныеПродажи(Параметры);
	
	КонецПериода = КонецДня(ТекущаяДатаСеанса());
	
	НачалоМесяца = НачалоМесяца(КонецПериода);
	НачалоГода   = НачалоГода(КонецПериода);
	
	НачалоПрошлогоМесяца  = НачалоМесяца(ДобавитьМесяц(КонецПериода, -1));
	НачалоПрошлогоПериода = НачалоГода(НачалоПрошлогоМесяца);
	
	Индекс =-1;
	
	// Текущий месяц
	Индекс = Индекс + 1;
	Если КонецПериода <> КонецМесяца(КонецПериода) Тогда
		ДобавитьСтрокуПродаж(Индекс, Результат,
			СтрШаблон(НСтр("ru = 'с %1'"), Формат(НачалоМесяца, "Л=ru_RU; ДФ='д ММММ'")),
			Продажи.ТекущийМесяц,
			Продажи.ТекущийМесяцОтклонение,
			Параметры.ПоказыватьСравнениеСПрошлымГодом,
			БлокиИнформационнойПанелиКлиентСервер.ИдентификаторПродажиТекущегоМесяца());
	Иначе
		ДобавитьСтрокуПродаж(Индекс, Результат,
			Формат(НачалоМесяца, "Л=ru_RU; ДФ='ММММ'"),
			Продажи.ТекущийМесяц,
			Продажи.ТекущийМесяцОтклонение,
			Параметры.ПоказыватьСравнениеСПрошлымГодом,
			БлокиИнформационнойПанелиКлиентСервер.ИдентификаторПродажиТекущегоМесяца());
	КонецЕсли;
	
	// С начала года. Не показываем, если дублирует продажи за текущий месяц.
	Если НачалоГода <> НачалоМесяца Тогда 
		Индекс = Индекс + 1;
		Если КонецПериода <> КонецМесяца(КонецПериода) Тогда
			ДобавитьСтрокуПродаж(Индекс, Результат,
				СтрШаблон(НСтр("ru = 'с %1'"), Формат(НачалоГода, "Л=ru_RU; ДФ='д ММММ'")),
				Продажи.ТекущийМесяцСНачалаГода,
				Продажи.ТекущийМесяцСНачалаГодаОтклонение,
				Параметры.ПоказыватьСравнениеСПрошлымГодом,
				БлокиИнформационнойПанелиКлиентСервер.ИдентификаторПродажиТекущегоМесяцаСНачалаГода());
		Иначе
			ДобавитьСтрокуПродаж(Индекс, Результат,
				СтрШаблон("%1 – %2", Формат(НачалоГода, "Л=ru_RU; ДФ='MMMM'"), Формат(НачалоМесяца, "Л=ru_RU; ДФ='MMMM'")),
				Продажи.ТекущийМесяцСНачалаГода,
				Продажи.ТекущийМесяцСНачалаГодаОтклонение,
				Параметры.ПоказыватьСравнениеСПрошлымГодом,
				БлокиИнформационнойПанелиКлиентСервер.ИдентификаторПродажиТекущегоМесяцаСНачалаГода());
		КонецЕсли;
	КонецЕсли;
	
	// Прошлый месяц
	Индекс = Индекс + 1;
	ДобавитьСтрокуПродаж(Индекс, Результат,
		Формат(НачалоПрошлогоМесяца, "Л=ru_RU; ДФ='MMMM'"),
		Продажи.ПрошлыйМесяц,
		Продажи.ПрошлыйМесяцОтклонение,
		Параметры.ПоказыватьСравнениеСПрошлымГодом,
		БлокиИнформационнойПанелиКлиентСервер.ИдентификаторПродажиПрошлогоМесяца());
	
	// С начала года по конец прошлого месяца. Не показываем, если дублирует продажи за прошлый месяц.
	Если НачалоПрошлогоПериода <> НачалоПрошлогоМесяца Тогда
		Индекс = Индекс + 1;
		ДобавитьСтрокуПродаж(Индекс, Результат,
			СтрШаблон("%1 – %2", Формат(НачалоПрошлогоПериода, "Л=ru_RU; ДФ='MMMM'"), Формат(НачалоПрошлогоМесяца, "Л=ru_RU; ДФ='MMMM'")),
			Продажи.ПрошлыйМесяцСНачалаГода,
			Продажи.ПрошлыйМесяцСНачалаГодаОтклонение,
			Параметры.ПоказыватьСравнениеСПрошлымГодом,
			БлокиИнформационнойПанелиКлиентСервер.ИдентификаторПродажиПрошлогоМесяцаСНачалаГода());
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

Процедура ДобавитьСтрокуПродаж(Индекс, Результат, Период, Сумма, Отклонение, ДобавитьОтклонение, Идентификатор)
	
	Если Сумма = 0 И Отклонение <> 0 И ДобавитьОтклонение Тогда
		ФорматнаяСтрока = "ЧДЦ=; ЧН=0";
	Иначе
		ФорматнаяСтрока = "ЧДЦ=";
	КонецЕсли;
	
	Шрифт = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
	
	ПредставлениеПериода = Новый ФорматированнаяСтрока(Период, , ЦветГиперссылки, , Идентификатор);
	Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоПериодПродаж(Индекс), ПредставлениеПериода);
	
	ПредставлениеСуммы = Новый ФорматированнаяСтрока(Формат(Сумма, ФорматнаяСтрока), Шрифт, ЦветТекста);
	Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоСуммаПродаж(Индекс), ПредставлениеСуммы);
	
	Если ДобавитьОтклонение Тогда
		
		ПредставлениеОтклонения = ПредставлениеОтклонения(Отклонение);
		Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоОтклонениеПродаж(Индекс), ПредставлениеОтклонения);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПродажиДоступны()
	
	Возврат ПравоДоступа("Использование", Метаданные.Отчеты.Продажи);
	
КонецФункции

#КонецОбласти

#Область Новости

Процедура ДобавитьНовости(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаНовости(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаНовости(),
		"БлокиИнформационнойПанели.Новости",,
		СвойстваНовости());
	
КонецПроцедуры

Функция СвойстваНовости()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоНовостей() - 1;
	
	Свойства = "Новости_Заголовок, Новости_Индекс, Новости_Текст, Новости_АдресХранилища";
	
	Возврат Свойства;
	
КонецФункции

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * УникальныйИдентификатор - УникальныйИдентификатор - уникальный идентификатор формы информационной панели
//
Процедура Новости(Параметры) Экспорт

	Результат = Новый Структура(СвойстваНовости());
	Результат.Вставить("Новости_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаНовости()));
	
	МаксимальноеКоличествоНовостей = БлокиИнформационнойПанелиКлиентСервер.КоличествоНовостей();
	
	ТаблицаНовостей = Новый ТаблицаЗначений;
	Если ОбработкаНовостейПовтИсп.РазрешенаРаботаСНовостямиТекущемуПользователю() Тогда
		
		ПараметрыПолученияНовостей = Новый Структура;
		ПараметрыПолученияНовостей.Вставить("ВариантОтбора",      ИнтернетПоддержкаПользователейБП.ВариантОтбораПоЛентеНовостей());
		ПараметрыПолученияНовостей.Вставить("ЛентаНовостей",      ИнтернетПоддержкаПользователейБП.ЛентаНовостейЧтоНового());
		ПараметрыПолученияНовостей.Вставить("КоличествоНовостей", МаксимальноеКоличествоНовостей);
		ПараметрыПолученияНовостей.Вставить("ПорядокСортировки",  "Прочтена Возр, ДатаПубликации Убыв, Важность Убыв");
		
		Справочники.Новости.ПолучитьСписокНовостей(
			ТаблицаНовостей,
			Пользователи.ТекущийПользователь(),
			ПараметрыПолученияНовостей,
			Новый Массив);
		
	КонецЕсли;
	
	Шрифт           = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста      = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
	
	ТекстыНовостей = Новый СписокЗначений;
	СсылкиНовостей = Новый СписокЗначений;
	
	КоличествоНовостей = ТаблицаНовостей.Количество();
	Если КоличествоНовостей > 0 Тогда
		
		ВГраница = Мин(МаксимальноеКоличествоНовостей, КоличествоНовостей) - 1;
		Для Индекс = 0 По ВГраница Цикл
			
			Новость = ТаблицаНовостей[Индекс];
			
			ТекстНовости = Новый ФорматированнаяСтрока(
				Новый ФорматированнаяСтрока(СокрЛП(Новость.Наименование), Шрифт, ЦветТекста),
				Символы.ПС,
				Символы.ПС,
				Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее'"), , ЦветГиперссылки, , ПолучитьНавигационнуюСсылку(Новость.Ссылка))
			);
			
			ТекстыНовостей.Добавить(ТекстНовости);
			СсылкиНовостей.Добавить(Новость.Ссылка);
			
		КонецЦикла;
		
		Результат.Вставить("Новости_Текст", ТекстыНовостей[0].Значение);
		
	Иначе
		
		Результат.Вставить("Новости_Текст", Новый ФорматированнаяСтрока(БлокиИнформационнойПанелиКлиентСервер.ТекстОтсутствияНовостей(), Шрифт, ЦветТекста));
		
	КонецЕсли;
	
	// Начинаем показывать с первой новости
	Результат.Вставить("Новости_Индекс", 0);
	
	// Список новостей передаем в форму через временное хранилище
	ДанныеНовостей = Новый Структура;
	ДанныеНовостей.Вставить("ТекстыНовостей", ТекстыНовостей);
	ДанныеНовостей.Вставить("СсылкиНовостей", СсылкиНовостей);
	АдресХранилищаНовостей = ПоместитьВоВременноеХранилище(ДанныеНовостей, Параметры.УникальныйИдентификатор);
	Результат.Вставить("Новости_АдресХранилища", АдресХранилищаНовостей);
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область ПоддержкаСервиса

Процедура ДобавитьПоддержкаСервиса(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаПоддержкаСервиса(),
		ТекстЗаголовкаПоддержкаСервиса(),
		"БлокиИнформационнойПанели.ПоддержкаСервиса",,
		СвойстваПоддержкаСервиса());
	
КонецПроцедуры

Функция ТекстЗаголовкаПоддержкаСервиса()
	
	Если ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		ИмяСервиса = НСтр("ru = 'сервиса'");
	Иначе
		ИмяСервиса = ОбщегоНазначенияБП.ПредставлениеПрограммыРодительныйПадеж();
	КонецЕсли;
	
	Возврат БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаПоддержкаСервиса(ИмяСервиса);
	
КонецФункции

Функция СвойстваПоддержкаСервиса()
	
	Свойства = "ПоддержкаСервиса_Заголовок, ПоддержкаСервиса_КартинкаСообщения, ПоддержкаСервиса_ТекстСообщения";
	
	Возврат Свойства;
	
КонецФункции

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//
Процедура ПоддержкаСервиса(Параметры) Экспорт
	
	Результат = Новый Структура(СвойстваПоддержкаСервиса());
	Результат.Вставить("ПоддержкаСервиса_Заголовок", ЗаголовокБлока(ТекстЗаголовкаПоддержкаСервиса()));
	
	Шрифт      = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветГиперссылки;
	
	// ТехнологияСервиса.ИнформационныйЦентр
	ТаблицаСообщенийСервиса = Новый ТаблицаЗначений;
	ТаблицаСообщенийСервиса.Колонки.Добавить(
		"СсылкаНаДанные", Новый ОписаниеТипов("СправочникСсылка.ОбщиеДанныеИнформационногоЦентра"));
	УстановитьПривилегированныйРежим(Истина);
	ИнформационныйЦентрСервер.СформироватьСписокНовостейНаРабочийСтол(ТаблицаСообщенийСервиса, 1);
	УстановитьПривилегированныйРежим(Ложь);
	// Конец ТехнологияСервиса.ИнформационныйЦентр
	
	Если ТаблицаСообщенийСервиса.Количество() > 0 Тогда
		
		Сообщение = ТаблицаСообщенийСервиса[0];
		
		СвойстваСообщения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сообщение.СсылкаНаДанные, "Наименование, Критичность");
		
		ИндексКартинки = БлокиИнформационнойПанелиКлиентСервер.ИндексКартинкиСообщенияСервиса(СвойстваСообщения.Критичность > 5);
		Результат.Вставить("ПоддержкаСервиса_КартинкаСообщения", ИндексКартинки);
		
		НаименованиеБезПробелов = СтрокаБезПереносов(СвойстваСообщения.Наименование);
		
		ТекстСообщения = Новый ФорматированнаяСтрока(НаименованиеБезПробелов, Шрифт, ЦветТекста);
		Результат.Вставить("ПоддержкаСервиса_ТекстСообщения", ТекстСообщения);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область ЧастоЗадаваемыеВопросы

Процедура ДобавитьЧастоЗадаваемыеВопросы(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаЧастоЗадаваемыеВопросы(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЧастоЗадаваемыеВопросы(),
		"БлокиИнформационнойПанели.ЧастоЗадаваемыеВопросы", ,
		СвойстваЧастоЗадаваемыеВопросы());
	
КонецПроцедуры

Функция СвойстваЧастоЗадаваемыеВопросы()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоСсылокЧастоЗадаваемыхВопросов() - 1;
	
	Свойства = Новый Массив;
	Свойства.Добавить("ЧастоЗадаваемыеВопросы_Заголовок");
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства.Добавить(БлокиИнформационнойПанелиКлиентСервер.СвойствоСсылкаЧастоЗадаваемыеВопросы(Индекс));
	КонецЦикла;
	
	Возврат СтрСоединить(Свойства, ",");

КонецФункции

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//
// Возвращаемое значение:
//   Строка
//
Функция ЧастоЗадаваемыеВопросы(Параметры) Экспорт
	
	Результат = Новый Структура(СвойстваЧастоЗадаваемыеВопросы());
	
	МаксимальноеКоличествоНовостей = БлокиИнформационнойПанелиКлиентСервер.КоличествоСсылокЧастоЗадаваемыхВопросов();
	ТаблицаНовостей = ОбщегоНазначенияБП.ЛентаНовостейЧастоЗадаваемыеВопросы(МаксимальноеКоличествоНовостей);
	
	Результат.Вставить("ЧастоЗадаваемыеВопросы_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаЧастоЗадаваемыеВопросы()));
	
	Если Не ЗначениеЗаполнено(ТаблицаНовостей) Тогда
		
		Результат.Вставить(
			БлокиИнформационнойПанелиКлиентСервер.СвойствоСсылкаЧастоЗадаваемыеВопросы(0),
			Новый ФорматированнаяСтрока(ТекстОтсутствияНовыхРоликов()));
		
		Возврат ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		
	КонецЕсли;
	
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
	
	ВГраница = Мин(МаксимальноеКоличествоНовостей, ТаблицаНовостей.Количество()) - 1;
	Для Индекс = 0 По ВГраница Цикл
		
		СсылкаНаРолик = ТаблицаНовостей[Индекс].СсылкаНаРолик;
		Если Не ПустаяСтрока(СсылкаНаРолик) Тогда
			
			Результат.Вставить(
				БлокиИнформационнойПанелиКлиентСервер.СвойствоСсылкаЧастоЗадаваемыеВопросы(Индекс),
				СсылкаНаРолик);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецФункции

Функция ЧастоЗадаваемыеВопросыДоступны()
	
	Возврат ОбщегоНазначенияБП.ЭтоБизнесСтарт();
	
КонецФункции

Функция ТекстОтсутствияНовыхРоликов()
	
	Возврат НСтр("ru = 'Сегодня новых роликов нет'");
	
КонецФункции

#КонецОбласти

#Область Сервис1СПАРКРиски

Процедура Добавить1СПАРКРиски(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлока1СПАРКРиски(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовка1СПАРКРиски(),
		"БлокиИнформационнойПанели.Сервис1СПАРКРиски",
		"БлокиИнформационнойПанели.Обновить1СПАРКРиски",
		Свойства1СПАРКРиски());
	
КонецПроцедуры

Функция Свойства1СПАРКРиски()
	
	Свойства = "Сервис1СПАРКРиски_Заголовок, Сервис1СПАРКРиски_События0, Сервис1СПАРКРиски_События1, Сервис1СПАРКРиски_ВсеСобытия";
	
	Возврат Свойства;
	
КонецФункции

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура Сервис1СПАРКРиски(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Сервис1СПАРКРиски_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовка1СПАРКРиски()));
			
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		
		Возврат;
		
	КонецЕсли;
	
	Результат = Новый Структура(Свойства1СПАРКРиски());
	Результат.Вставить("Сервис1СПАРКРиски_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовка1СПАРКРиски()));
		
	ШрифтОбычногоСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветОбычногоСобытия  = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	
	ШрифтВажногоСобытия = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветВажногоСобытия  = ЦветаСтиля.ВажноеСобытие;
	
	ДанныеДляОтображения = Новый Структура("Строка0, Строка1, СтрокаВсеСобытия",
		Новый ФорматированнаяСтрока(""), Новый ФорматированнаяСтрока(""), Новый ФорматированнаяСтрока(""));
	
	ПоказатьРекламуСПАРК = ХранилищеОбщихНастроек.Загрузить("ПоказатьРекламуСПАРКВВиджете");
	ПоказатьРекламуСПАРК = ?(ПоказатьРекламуСПАРК = Неопределено, Ложь, ПоказатьРекламуСПАРК);
	Если Не ПоказатьРекламуСПАРК Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		СобытияМониторинга = ИнтернетПоддержкаПользователейБП.СобытияМониторинга();
		
		ПараметрыПредметаИсчисления = НСтр("ru = 'сообщение, сообщения, сообщений'");
		
		ВсегоПроблемы  = СобытияМониторинга.Количество();
		ПроблемыПодробно = ВсегоПроблемы <= 2;
		
		КритичныеПроблемы = СобытияМониторинга.НайтиСтроки(Новый Структура("КритичноеСобытие", Истина));
		ПрочаяИнформация = СобытияМониторинга.НайтиСтроки(Новый Структура("КритичноеСобытие", Ложь));
		ВсегоКритичныхПроблем = КритичныеПроблемы.Количество();
		ВсегоПрочейИнформации = ПрочаяИнформация.Количество();
		
		Если ВсегоПроблемы > 0 Тогда
			
			Если ПроблемыПодробно Тогда
				
				Счетчик = 0;
				Для каждого СтрокаСобытия Из КритичныеПроблемы Цикл
					
					ЧастиСтрокиСобытия = Новый Массив;
					
					ЧастиСтрокиСобытия.Добавить(
						Новый ФорматированнаяСтрока(СтрокаБезПереносов(Строка(СтрокаСобытия.Контрагент)), ШрифтОбычногоСобытия, ЦветОбычногоСобытия));
					ЧастиСтрокиСобытия.Добавить(Символы.ПС);
					ЧастиСтрокиСобытия.Добавить(
						Новый ФорматированнаяСтрока(СтрокаБезПереносов(СтрокаСобытия.НазваниеСобытия), ШрифтВажногоСобытия, ЦветВажногоСобытия));
						
					ДанныеДляОтображения["Строка" + Счетчик] = Новый ФорматированнаяСтрока(ЧастиСтрокиСобытия);
					
					Счетчик = Счетчик + 1;
					
				КонецЦикла;
				
				Для каждого СтрокаСобытия Из ПрочаяИнформация Цикл
					
					ЧастиСтрокиСобытия = Новый Массив;
					
					ЧастиСтрокиСобытия.Добавить(
						Новый ФорматированнаяСтрока(СтрокаБезПереносов(Строка(СтрокаСобытия.Контрагент)), ШрифтОбычногоСобытия, ЦветОбычногоСобытия));
					ЧастиСтрокиСобытия.Добавить(Символы.ПС);
					ЧастиСтрокиСобытия.Добавить(
						Новый ФорматированнаяСтрока(СтрокаБезПереносов(СтрокаСобытия.НазваниеСобытия), ШрифтОбычногоСобытия, ЦветОбычногоСобытия));
						
					ДанныеДляОтображения["Строка" + Счетчик] = Новый ФорматированнаяСтрока(ЧастиСтрокиСобытия);
					
					Счетчик = Счетчик + 1;
					
				КонецЦикла;
				
			Иначе
				
				Счетчик = 0;
				Если ВсегоКритичныхПроблем > 0 Тогда
					
					ЧастиСтрокиСобытия = Новый Массив;
					
					ПараметрыПредметаИсчисления = НСтр("ru = 'критичное событие, критичных события, критичных событий'");
					ТекстСтроки = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ВсегоКритичныхПроблем,
						ПараметрыПредметаИсчисления);
					ЧастиСтрокиСобытия.Добавить(Новый ФорматированнаяСтрока(ТекстСтроки, ШрифтВажногоСобытия, ЦветВажногоСобытия));
					
					ДанныеДляОтображения["Строка" + Счетчик] = Новый ФорматированнаяСтрока(ЧастиСтрокиСобытия);
					
					Счетчик = Счетчик + 1;
					
				КонецЕсли;
				
				Если ВсегоПрочейИнформации > 0 Тогда
					
					ЧастиСтрокиСобытия = Новый Массив;
					
					ПараметрыПредметаИсчисления =
						НСтр("ru = 'сообщение об изменении реквизитов, сообщения об изменении реквизитов, сообщений об изменении реквизитов'");
					ТекстСтроки = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ВсегоПрочейИнформации,
						ПараметрыПредметаИсчисления);
					ЧастиСтрокиСобытия.Добавить(Новый ФорматированнаяСтрока(ТекстСтроки, ШрифтОбычногоСобытия, ЦветОбычногоСобытия));
					
					ДанныеДляОтображения["Строка" + Счетчик] = Новый ФорматированнаяСтрока(ЧастиСтрокиСобытия);
					
					Счетчик = Счетчик + 1;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВсегоПроблемы = 0 Тогда
			
			ТекстСообщения = 
				НСтр("ru = 'За последние 15 дней нет сообщений о ликвидации, реорганизации, банкротстве, изменении реквизитов контрагентов.'");
			ДанныеДляОтображения.Строка0 = Новый ФорматированнаяСтрока(ТекстСообщения, ШрифтОбычногоСобытия, ЦветОбычногоСобытия);
			
			ДанныеДляОтображения.СтрокаВсеСобытия = Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее о сервисе'"),,,,
				СПАРКРискиКлиентСервер.АдресСтраницыОписанияСервисаСПАРКРиски());
			
		Иначе
			
			ДанныеДляОтображения.СтрокаВсеСобытия = Новый ФорматированнаяСтрока(НСтр("ru = 'Все события'"), , , , "ОтчетСобытияМониторингаСПАРКРиски");
			
		КонецЕсли;
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Сообщения о закрытии, проблемах или изменении реквизитов контрагентов'");
		ДанныеДляОтображения.Строка0 = Новый ФорматированнаяСтрока(ТекстСообщения, ШрифтОбычногоСобытия, ЦветОбычногоСобытия);
		
		ДанныеДляОтображения.СтрокаВсеСобытия = Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее о сервисе'"),,,,
			СПАРКРискиКлиентСервер.АдресСтраницыОписанияСервисаСПАРКРиски());
		
	КонецЕсли;
	
	Результат.Вставить("Сервис1СПАРКРиски_События0", ДанныеДляОтображения.Строка0);
	Результат.Вставить("Сервис1СПАРКРиски_События1", ДанныеДляОтображения.Строка1);
	Результат.Вставить("Сервис1СПАРКРиски_ВсеСобытия", ДанныеДляОтображения.СтрокаВсеСобытия);
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура Обновить1СПАРКРиски(Параметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ВидОшибки = СПАРКРискиМониторингСобытий.ОбновитьСобытияМониторинга();
	ХранилищеОбщихНастроек.Сохранить("ПоказатьРекламуСПАРКВВиджете",, ЗначениеЗаполнено(ВидОшибки));
	
КонецПроцедуры

#КонецОбласти

#Область КурсыВалют

Процедура ДобавитьКурсыВалют(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаКурсыВалют(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаКурсыВалют(),
		"БлокиИнформационнойПанели.КурсыВалют",
		"БлокиИнформационнойПанели.ОбновитьКурсыВалют",
		СвойстваКурсыВалют());
	
КонецПроцедуры

Функция СвойстваКурсыВалют()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоКурсовВалют() - 1;
	
	Свойства = "КурсыВалют_Заголовок";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоВалюта(Индекс) + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоВалютаСсылка(Индекс) + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоКурсВалюты(Индекс) + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоОтклонениеКурса(Индекс);
	КонецЦикла;

	Возврат Свойства;
	
КонецФункции

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//     * АдресРезультатаКурсыВалют - Строка - адрес временного хранилища, в которое будет помещен результат
//
Процедура ОбновитьКурсыВалют(Параметры) Экспорт
	
	Если РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных()
		И РаботаВМоделиСервиса.РазделениеВключено() Тогда
		// В режиме сервиса курсы валют - поставляемые данные
		Возврат;
	КонецЕсли;
	
	Если Параметры.БыстроеОбновление 
		Или Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.КурсыВалют) Тогда
		Возврат;
	КонецЕсли;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	
	ЕстьОшибкиПриЗагрузке = Ложь;
	Попытка
		Обработки.ЗагрузкаКурсовВалют.ЗагрузитьАктуальныйКурс(Неопределено, АдресХранилища);
	Исключение
		ЕстьОшибкиПриЗагрузке = Истина;
	КонецПопытки;
	
	ПоместитьВоВременноеХранилище(Новый Структура("ЕстьОшибкиПриЗагрузке", ЕстьОшибкиПриЗагрузке),
		Параметры.АдресРезультатаКурсыВалют);
	
КонецПроцедуры

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация    - СправочникСсылка.Организации - отбор по организации
//     * Инициализация  - Булево - Истина, если необходимо заполнить только статическими данными
//
Процедура КурсыВалют(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		ЗаголовокБлока = БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаКурсыВалют();
		Результат.Вставить("КурсыВалют_Заголовок", ЗаголовокБлока(ЗаголовокБлока));
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваКурсыВалют());
	
	МаксимальноеКоличество = БлокиИнформационнойПанелиКлиентСервер.КоличествоКурсовВалют();
	ДанныеКурсовВалют = РаботаСКурсамиВалютБП.ДанныеКурсовВалютДляВиджета(МаксимальноеКоличество);
	
	Валюты = ДанныеКурсовВалют.Валюты;
	
	Для Индекс = 0 По Валюты.Количество() - 1 Цикл
		
		Валюта = Валюты[Индекс];
		ДобавитьСтрокуКурсыВалют(Индекс, Валюта, Результат);
		
	КонецЦикла;
	
	ЗаголовокБлока = БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаКурсыВалют(ДанныеКурсовВалют.ДатаКурсов);
	Результат.Вставить("КурсыВалют_Заголовок", ЗаголовокБлока(ЗаголовокБлока));
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

Процедура ДобавитьСтрокуКурсыВалют(Индекс, Валюта, Результат)
	
	ВалютаСсылка  = Валюта.Ссылка;
	ВалютаСтрокой = Валюта.Наименование;
	Кратность     = Валюта.Кратность;
	Курс          = Валюта.Курс;
	Отклонение    = Валюта.Отклонение;
	
	Шрифт = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
	
	Если Кратность > 1 Тогда
		НаименованиеВалюты = СтрШаблон("%1 %2", Формат(Кратность, "ЧДЦ="), ВалютаСтрокой);
	Иначе
		НаименованиеВалюты = ВалютаСтрокой;
	КонецЕсли;
	
	ПредставлениеВалюты = Новый ФорматированнаяСтрока(НаименованиеВалюты, , ЦветГиперссылки, , "ИсторияКурса");
	Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоВалюта(Индекс), ПредставлениеВалюты);
	Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоВалютаСсылка(Индекс), ВалютаСсылка);
	
	ПредставлениеКурса = Новый ФорматированнаяСтрока(Формат(Курс, "ЧДЦ=4"), Шрифт, ЦветТекста);
	Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоКурсВалюты(Индекс), ПредставлениеКурса);
	
	ПредставлениеОтклонения = ПредставлениеОтклоненияКурса(Отклонение);
	Результат.Вставить(БлокиИнформационнойПанелиКлиентСервер.СвойствоОтклонениеКурса(Индекс), ПредставлениеОтклонения);
	
КонецПроцедуры

Функция КурсыВалютДоступны() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет")
		И РаботаСКурсамиВалютБП.ЕстьУстановленныеКурсыВалют();
	
КонецФункции

#КонецОбласти

#Область МойНалог

Процедура ДобавитьМойНалог(ТаблицаБлоков)
	
	Добавить(ТаблицаБлоков,
		БлокиИнформационнойПанелиКлиентСервер.ИмяБлокаМойНалог(),
		БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаМойНалог(),
		"БлокиИнформационнойПанели.МойНалог",
		"БлокиИнформационнойПанели.ОбновитьМойНалог",
		СвойстваМойНалог());
	
КонецПроцедуры

Функция СвойстваМойНалог()
	
	ВГраница = БлокиИнформационнойПанелиКлиентСервер.КоличествоОповещенийМойНалог() - 1;
	
	Свойства = "МойНалог_Заголовок, МойНалог_НепрочитанныеОповещения";
	
	Для Индекс = 0 По ВГраница Цикл
		Свойства = Свойства + ", "
			+ БлокиИнформационнойПанелиКлиентСервер.СвойствоОповещенияМойНалог(Индекс);
	КонецЦикла;
	
	Возврат Свойства;
	
КонецФункции

// Помещает во временное хранилище данные блока
//
// Параметры:
//   Параметры - Структура:
//     * АдресХранилища - Строка - адрес хранилища результата
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * Инициализация - Булево - Истина, если нужно заполнить только статическими данными
//
Процедура МойНалог(Параметры) Экспорт
	
	Если Параметры.Инициализация Тогда
		Результат = Новый Структура;
		Результат.Вставить("МойНалог_Заголовок",
			ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаМойНалог()));
		ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СвойстваМойНалог());
	Результат.Вставить("МойНалог_Заголовок",
		ЗаголовокБлока(БлокиИнформационнойПанелиКлиентСервер.ТекстЗаголовкаМойНалог()));
	
	КоличествоОповещенийВБлоке = БлокиИнформационнойПанелиКлиентСервер.КоличествоОповещенийМойНалог();
	ОповещенияМойНалог = ОповещенияМойНалог(Параметры.Организация, КоличествоОповещенийВБлоке);
	Для Индекс = 0 По ОповещенияМойНалог.ВГраница() Цикл
		ИмяСвойства = БлокиИнформационнойПанелиКлиентСервер.СвойствоОповещенияМойНалог(Индекс);
		Результат.Вставить(ИмяСвойства, ОповещенияМойНалог[Индекс]);
	КонецЦикла;
	
	НепрочитанныеОповещенияМойНалог = НепрочитанныеОповещенияМойНалог(Параметры.Организация);
	Результат.Вставить("МойНалог_НепрочитанныеОповещения", НепрочитанныеОповещенияМойНалог);
	
	ПоместитьВоВременноеХранилище(Результат, Параметры.АдресХранилища);
	
КонецПроцедуры

// Обновляет данные блока
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - отбор по организации
//     * БыстроеОбновление - Булево - учитывать актуальность данных при обновлении
//
Процедура ОбновитьМойНалог(Параметры) Экспорт
	
	ОповещенияПлатформыСамозанятые.ОбновитьОбсуждения(Параметры.Организация, Параметры.БыстроеОбновление);
	
КонецПроцедуры

Функция МойНалогДоступен() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ВключенаИнтеграцияСПлатформойСамозанятые")
		И ПравоДоступа("Чтение", Метаданные.Справочники.ОповещенияПлатформыСамозанятые);
	
КонецФункции

Функция ОповещенияМойНалог(Организация, КоличествоОповещенийВБлоке)
	
	ОповещенияМойНалог = Новый Массив(КоличествоОповещенийВБлоке);
	
	МаксимальноеКоличествоОтображаемых = КоличествоОповещенийВБлоке - 1;
	
	НаправлениеПоВозрастанию = Ложь;
	НайденныеСсылки = Справочники.ОповещенияПлатформыСамозанятые.НайтиПоСтатусу(
		Организация,
		Перечисления.СтатусыОповещенийПлатформыСамозанятые.НеПрочитано,
		МаксимальноеКоличествоОтображаемых,
		НаправлениеПоВозрастанию);
	
	Если ЗначениеЗаполнено(НайденныеСсылки) Тогда
		ЗаголовкиОповещений = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НайденныеСсылки, "Наименование");
		ИндексПоследнего = 0;
		Для Индекс = 0 По ОповещенияМойНалог.ВГраница() Цикл
			Если Индекс <= НайденныеСсылки.ВГраница() Тогда
				ЗаголовокОповещения = ЗаголовкиОповещений.Получить(НайденныеСсылки[Индекс]);
				ОповещенияМойНалог[Индекс] = Новый ФорматированнаяСтрока(ЗаголовокОповещения);
				ИндексПоследнего = Индекс;
			Иначе
				ОповещенияМойНалог[Индекс] = Новый ФорматированнаяСтрока("");
			КонецЕсли;
		КонецЦикла;
		Если ИндексПоследнего < ОповещенияМойНалог.ВГраница() Тогда
			КоличествоПоказано = ИндексПоследнего + 1;
			КоличествоНепрочитанных = Справочники.ОповещенияПлатформыСамозанятые.КоличествоНепрочитанных(Организация);
			Если КоличествоПоказано < КоличествоНепрочитанных Тогда
				ЗаголовокОповещения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru=';и еще %1 сообщение от ФНС России;;и еще %1 сообщения от ФНС России;и еще %1 сообщений от ФНС России;'"),
					КоличествоНепрочитанных - КоличествоПоказано, ,
					"ЧГ=0;");
				ОповещенияМойНалог[ИндексПоследнего + 1] = Новый ФорматированнаяСтрока(ЗаголовокОповещения);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ОповещенияМойНалог[0] = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Новых сообщений нет'"), , ЦветаСтиля.НедоступныеДанныеЦвет);
		Для Индекс = 1 По ОповещенияМойНалог.ВГраница() Цикл
			ОповещенияМойНалог[Индекс] = Новый ФорматированнаяСтрока("");
		КонецЦикла;
	КонецЕсли;
	
	Возврат ОповещенияМойНалог;
	
КонецФункции

Функция НепрочитанныеОповещенияМойНалог(Организация)
	
	КоличествоНепрочитанных = Справочники.ОповещенияПлатформыСамозанятые.КоличествоНепрочитанных(Организация);
	
	Если Обсуждения.ОбсужденияДоступны() Тогда
		Если КоличествоНепрочитанных > 0 Тогда
			ПредставлениеСсылки = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru=';Не прочитано %1 сообщение;;Не прочитано %1 сообщения;Не прочитано %1 сообщений;'"),
					КоличествоНепрочитанных,, "ЧГ=0;");
			Возврат Новый ФорматированнаяСтрока(
				ПредставлениеСсылки, ,
				ЦветаСтиля.ЦветКоличестваНепрочитанныхИнформационнойПанели, ,
				"ОткрытьОбсуждение");
		Иначе
			Возврат Новый ФорматированнаяСтрока(
				НСтр("ru = 'Все сообщения'"), ,
				ЦветаСтиля.ЦветГиперссылки, ,
				"ОткрытьОбсуждение");
		КонецЕсли;
	Иначе
		Если КоличествоНепрочитанных > 0 И ПравоДоступа("Использование", Метаданные.Обработки.ПодключениеОбсуждений) Тогда
			Возврат СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'Подключите <a href=ПодключениеОбсуждений>Обсуждения</a>, чтобы прочитать'"));
		Иначе
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбщиеПроцедурыИФункции

Функция НоваяТаблицаБлоков()
	
	ТаблицаБлоков = Новый ТаблицаЗначений;
	
	Колонки = ТаблицаБлоков.Колонки;
	
	Колонки.Добавить("Пометка",                   Новый ОписаниеТипов("Булево"));
	Колонки.Добавить("Имя",                       ОбщегоНазначения.ОписаниеТипаСтрока(150));
	Колонки.Добавить("Синоним",                   ОбщегоНазначения.ОписаниеТипаСтрока(150));
	Колонки.Добавить("ПроцедураПолученияДанных",  ОбщегоНазначения.ОписаниеТипаСтрока(0));
	Колонки.Добавить("ПроцедураОбновленияДанных", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	Колонки.Добавить("РеквизитыФормы",            ОбщегоНазначения.ОписаниеТипаСтрока(0));
	Колонки.Добавить("ЦветФона",                  Новый ОписаниеТипов("Цвет"));
	Колонки.Добавить("Порядок",                   ОбщегоНазначения.ОписаниеТипаЧисло(3));
	
	Возврат ТаблицаБлоков;
	
КонецФункции

Процедура Добавить(ТаблицаБлоков, Имя, Синоним, ПроцедураПолученияДанных, ПроцедураОбновленияДанных, РеквизитыФормы, Порядок = 100)
	
	НовыйБлок = ТаблицаБлоков.Добавить();
	НовыйБлок.Имя                       = Имя;
	НовыйБлок.Синоним                   = Синоним;
	НовыйБлок.ПроцедураПолученияДанных  = ПроцедураПолученияДанных;
	НовыйБлок.ПроцедураОбновленияДанных = ПроцедураОбновленияДанных;
	НовыйБлок.РеквизитыФормы            = РеквизитыФормы;
	НовыйБлок.Порядок                   = Порядок;
	
КонецПроцедуры

Функция МножественноеПредставление(ПараметрыПредметаИсчисления)
	
	ПредставленияПредмета = СтрРазделить(ПараметрыПредметаИсчисления, ",");
	
	Возврат ?(ПредставленияПредмета.Количество() >= 3, СокрЛП(ПредставленияПредмета[2]), "");
	
КонецФункции

Функция ПредставлениеСрока(ОтносительныйСрок, ОсталосьДней) Экспорт
	
	Если ОтносительныйСрок = Перечисления.ОтносительныеСроки.НесколькоДней Тогда
		
		ОсталосьДнейПрописью = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			ОсталосьДней, НСтр("ru = 'день, дня, дней'"));
			
		ПредставлениеСрока = СтрШаблон(НСтр("ru = 'Через %1'"), ОсталосьДнейПрописью);
		
	ИначеЕсли ОтносительныйСрок = Перечисления.ОтносительныеСроки.Неделя Тогда
		
		ПредставлениеСрока = НСтр("ru = 'Через неделю'");
		
	ИначеЕсли ОтносительныйСрок = Перечисления.ОтносительныеСроки.ДвеНедели Тогда
		
		ПредставлениеСрока = НСтр("ru = 'Через 2 недели'");
		
	ИначеЕсли ОтносительныйСрок = Перечисления.ОтносительныеСроки.ТриНедели Тогда
		
		ПредставлениеСрока = НСтр("ru = 'Через 3 недели'");
		
	ИначеЕсли ОтносительныйСрок = Перечисления.ОтносительныеСроки.Месяц Тогда
		
		ПредставлениеСрока = НСтр("ru = 'Через месяц'");
		
	Иначе
		
		ПредставлениеСрока = Строка(ОтносительныйСрок); // Сегодня, Завтра
		
	КонецЕсли;
	
	Возврат ПредставлениеСрока;
	
КонецФункции

Функция ПредставлениеОтклонения(Знач Отклонение)
	
	Отклонение = Окр(Отклонение, 0);
	
	Стрелка = ?(Отклонение > 0, СтрелкаВверх(), СтрелкаВниз());
	
	МодульОтклонения = Макс(Отклонение, -Отклонение);
	
	ПределОтклонения = ПределОтклонения();
	
	Если Отклонение <> 0 Тогда
		Если Отклонение < ПределОтклонения Тогда
			ПредставлениеОтклонения = Стрелка + Формат(МодульОтклонения, "ЧДЦ=") + "%";
		Иначе
			ПредставлениеОтклонения = Стрелка;
		КонецЕсли;
	Иначе
		ПредставлениеОтклонения = "";
	КонецЕсли;
	
	Шрифт = ШрифтыСтиля.ШрифтОтклоненияИнформационнойПанели;
	Если Отклонение > 0 Тогда
		ЦветТекста = ЦветаСтиля.ЦветПоложительногоОтклоненияИнформационнойПанели;
	ИначеЕсли Отклонение < 0 Тогда
		ЦветТекста = ЦветаСтиля.ЦветОтрицательногоОтклоненияИнформационнойПанели;
	Иначе
		ЦветТекста = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ПредставлениеОтклонения, Шрифт, ЦветТекста);
	
КонецФункции

Функция ПределОтклонения()
	
	Возврат 999;
	
КонецФункции

Функция СтрелкаВверх()
	
	Возврат Символ(9650);
	
КонецФункции

Функция СтрелкаВниз()
	
	Возврат Символ(9660);
	
КонецФункции

// Заменяет пробелы в строке на неразрывные пробелы для блокировки переносов в браузере
//
Функция СтрокаБезПереносов(ИсходнаяСтрока)
	
	// Для того чтобы строка правильно переносилась в веб-клиенте,
	// вместо запятой используем символ - U+201A:Single Low-9 Quotation Mark (Keystroke: Alt+0130)
	ИсходнаяСтрока = СтрЗаменить(ИсходнаяСтрока, ",", "‚");
	Возврат СтрЗаменить(ИсходнаяСтрока, " ", Символы.НПП);
	
КонецФункции

Функция ЗаголовокБлока(ТекстЗаголовка)
	
	Шрифт = ШрифтыСтиля.ШрифтЗаголовкаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветЗаголовкаИнформационнойПанели;
	
	Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, Шрифт, ЦветТекста);
	
КонецФункции

Функция ПредставлениеОтклоненияКурса(Отклонение)
	
	Если Отклонение <> 0 Тогда
		ПредставлениеОтклонения = СтрШаблон("%1%2",
			?(Отклонение > 0, "+", ""), Формат(Отклонение, "ЧДЦ=4"));
	Иначе
		ПредставлениеОтклонения = "";
	КонецЕсли;
	
	Шрифт = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветПустойГиперссылки;
	
	Возврат Новый ФорматированнаяСтрока(ПредставлениеОтклонения, Шрифт, ЦветТекста);
	
КонецФункции

#КонецОбласти

#КонецОбласти
