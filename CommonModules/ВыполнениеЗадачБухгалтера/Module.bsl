#Область ПрограммныйИнтерфейс

// Записывает задачу со статусом, установленным вручную пользователем. Установка ручного статуса
// может дополнительно сопровождаться записью комментария.
//
// Параметры:
//   Задача - Структура - см. РегистрСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой()
//   Статус - Строка - новый статус задачи
//   Комментарий - Строка, Неопределено - если не требуется записывать комментарий, следует
//                                        передать Неопределено.
//   ПеренестиВАрхив - Булево - указывает на необходимость перенести задачу в архив
//
// Возвращаемое значение:
//   Булево - Истина, если статус успешно записан.
//
Функция ЗаписатьРучнойСтатусЗадачи(Задача, Знач Статус, Комментарий = Неопределено, ПеренестиВАрхив = Ложь) Экспорт
	
	Если ПустаяСтрока(Статус) Тогда
		Статус = ФинальныйСтатусЗадачи(Задача);
		Если ПустаяСтрока(Статус) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаЗадач = РегистрыСведений.ЗадачиБухгалтера.НовыйРасписание();
	ЗаполнитьЗначенияСвойств(ТаблицаЗадач.Добавить(), Задача);
	
	ТекущийПользователь = ТекущийПользователь();
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	
	Результат = ЗаписатьНовоеСостояниеЗадачи(
		ТаблицаЗадач[0], // Задача
		Статус = СтатусыОтчета.НеТребуется Или ПеренестиВАрхив, // Задача со статусом "Не требуется" должна быть отправлена в архив
		Статус, //Статус
		Истина, // СтатусУстановленВручную
		ТекущийПользователь,
		Комментарий);
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриЗаписиРучногоСтатусаЗадачи(Задача, Статус, Комментарий, ТекущийПользователь, ПеренестиВАрхив);
	
	Возврат Результат;
КонецФункции

// Отменяет установку ручного статуса задаче, возвращает этой задаче автоматический статус.
//
// Параметры:
//   Задача - Структура - см. РегистрСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой()
//
// Возвращаемое значение:
//   Строка, Неопределено - новый статус, который был установлен задаче автоматически,
//                          Если не удалось изменить статус, возвращает Неопределено.
//
Функция ВернутьАвтоматическийСтатусЗадачи(Задача) Экспорт
	
	ТаблицаЗадач = РегистрыСведений.ЗадачиБухгалтера.НовыйРасписание();
	ЗаполнитьЗначенияСвойств(ТаблицаЗадач.Добавить(), Задача);
	
	// Сначала сбросим признак ручного статуса и запишем задачу.
	Результат = ЗаписатьНовоеСостояниеЗадачи(
		ТаблицаЗадач[0], // Задача
		Ложь, // ПеренестиВАрхив
		"", //Статус
		Ложь); // СтатусУстановленВручную
		
	Если Не Результат Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриВозвращенииАвтоматическогоСтатусаЗадачи(Задача);
	
	// Затем рассчитаем новый статус автоматически.
	ТаблицаЗадач = РегистрыСведений.ЗадачиБухгалтера.НовыйРасписание();
	ЗаполнитьЗначенияСвойств(ТаблицаЗадач.Добавить(), Задача);
	ДобавитьСтатусыЗадач(ТаблицаЗадач);
	
	Если ЗаписатьНовоеСостояниеЗадачи(ТаблицаЗадач[0], Ложь, ТаблицаЗадач[0].Статус) Тогда
		Возврат ТаблицаЗадач[0].Статус;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Записывает комментарий к задаче. Комментарий может быть записан без изменения статуса.
//
// Параметры:
//   Задача - Структура - см. РегистрСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой()
//   Комментарий - Строка
//
// Возвращаемое значение:
//   Булево - Истина, если комментарий успешно записан.
//
Функция ЗаписатьКомментарийЗадачи(Задача, Комментарий) Экспорт
	
	Возврат ЗаписатьНовоеСостояниеЗадачи(
		Задача,
		Ложь, // ПеренестиВАрхив
		Неопределено, //Статус
		Неопределено, // СтатусУстановленВручную
		ТекущийПользователь(), // ПользовательУстановившийСтатус
		Комментарий);
		
КонецФункции

// Рассчитывает конечный статус задачи, в котором она должна находиться после её выполнения.
//
// Параметры:
//   Задача - Структура - см. РегистрСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой()
//
// Возвращаемое значение:
//   Строка
//
Функция ФинальныйСтатусЗадачи(Задача) Экспорт
	
	Если Задача.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет
		Или Задача.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление
		Или Задача.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ФинОтчетность Тогда
		Возврат НСтр("ru='Сдано'");
	ИначеЕсли Задача.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата Тогда
		Возврат НСтр("ru='Выплачено'");
	ИначеЕсли Задача.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога
		Или Задача.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж Тогда
		Возврат НСтр("ru='Оплачено'");
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Описывает действие, которое необходимо выполнить для события
//
// Параметры:
//  Событие - Структура - см. ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи()
// Возвращаемое значение:
//  Структура - состав см. в теле функции
//
Функция ОписаниеДействия(Событие) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействия(ОписаниеДействия, Событие);
	
	Возврат ОписаниеДействия;
	
КонецФункции

// Функция-конструктор таблицы платежей
//
Функция НовыйТаблицаПлатежи() Экспорт
	
	Платежи = Новый ТаблицаЗначений;
	
	Платежи.Колонки.Добавить("Организация",  Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Платежи.Колонки.Добавить("Период",       Новый ОписаниеТипов("Дата"));
	Платежи.Колонки.Добавить("КБК");
	Платежи.Колонки.Добавить("ОКАТО");
	Платежи.Колонки.Добавить("Сумма",        Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	// СуммаУведомления используется в тех случаях, когда таблица платежей является источником для таблицы расчетов.
	// В таблице расчетов этот показатель требуется в тех случаях, когда сумма уведомления может быть отлична от суммы налога
	// (например, уплата налога на имущество за год)
	Платежи.Колонки.Добавить("СуммаУведомления", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	Платежи.Колонки.Добавить("Аванс",        Новый ОписаниеТипов("Булево"));
	Платежи.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	Платежи.Колонки.Добавить("КодНалоговогоОргана",        Новый ОписаниеТипов("Строка"));
	Платежи.Колонки.Добавить("Налог");
	Платежи.Колонки.Добавить("ВидНалоговогоОбязательства");
	Платежи.Колонки.Добавить("НалоговыйПериод", Новый ОписаниеТипов("Строка"));
	Платежи.Колонки.Добавить("СрокУплаты",      Новый ОписаниеТипов("Дата"));
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииТаблицыПлатежи(Платежи);
	
	Возврат Платежи;
	
КонецФункции

// Дополняет таблицу платежей по декларации платежными документами
// Добавляет в переданную таблицу "Платежи" колонки:
//   * ПлатежноеПоручение - ДокументСсылка.ПлатежноеПоручение, ДокументСсылка.РасходныйКассовыйОрдер - ссылка на документ уплаты.
//   * СуммаОплачено - Число - сумма документа уплаты
//   * Оплачено - Булево - флаг, что документ уплаты в состоянии "Оплачено"
//
// Параметры:
//  Платежи - ТаблицаЗначений - см. НовыйТаблицаПлатежи()
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога, подготовки отчета.
//  ПериодСобытия - Дата - период события
//  Организация - СправочникСсылка.Организации - организация
//  СведенияПоДекларации - Структура - для платежей по декларации, ключи:
//    * Ссылка - ссылка на регламентированный отчет
//    * ПоказательПериода - Строка
//
Процедура ДополнитьНачисленияПлатежнымиДокументами(Платежи, Правило, ПериодСобытия, Организация, СведенияПоДекларации = Неопределено, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	ИмяЗадачи = Правило.Владелец.Код;
	ПлатежПоДекларации = ЗначениеЗаполнено(СведенияПоДекларации);
	
	МассивТиповПлатежныхДокументов = Новый Массив;
	ВыполнениеЗадачБухгалтераПереопределяемый.ЗаполнитьМассивТиповПлатежныхДокументов(МассивТиповПлатежныхДокументов);
	
	Если Платежи.Колонки.Найти("Индекс") = Неопределено Тогда
		Платежи.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	Если Платежи.Колонки.Найти("ПлатежноеПоручение") = Неопределено Тогда
		Платежи.Колонки.Добавить("ПлатежноеПоручение", Новый ОписаниеТипов(МассивТиповПлатежныхДокументов));
	КонецЕсли;
	Если Платежи.Колонки.Найти("СуммаОплачено") = Неопределено Тогда
		Платежи.Колонки.Добавить("СуммаОплачено", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	Если Платежи.Колонки.Найти("Оплачено") = Неопределено Тогда
		Платежи.Колонки.Добавить("Оплачено", Новый ОписаниеТипов("Булево"));
	КонецЕсли;

	ВыполнениеЗадачБухгалтераПереопределяемый.ДополнитьНачисленияПлатежнымиДокументами(Платежи, Правило, ПериодСобытия, Организация, СведенияПоДекларации, РегистрацияВНалоговомОргане);
	
КонецПроцедуры

Функция ОписаниеДействияДекларация(Правило, ПериодСобытия, Организация, РегистрацияВНалоговомОргане, ДлительностьБазовогоПериода = Неопределено) Экспорт
	
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	
	Если СтрНайти(ПолноеИмяПравила, "_СнятиеСУчета") > 0 Тогда
		// Для задачи формирования декларации при снятии ИП с учета открываем форму за год
		НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.БазовыйПериод, ПериодСобытия);
	Иначе
		НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.ФинансовыйПериод, ПериодСобытия);
	КонецЕсли;
	
	КонецПериода  = КонецДня(ПериодСобытия);
	Периодичность = ?(ЗначениеЗаполнено(ДлительностьБазовогоПериода), ДлительностьБазовогоПериода, Правило.Периодичность);
	
	ОписаниеФормы = ИнтерфейсыВзаимодействияБРО.ОписаниеФормыРеглОтчета(
		ПолноеИмяПравила,
		Организация,
		РегистрацияВНалоговомОргане,
		НачалоПериода,
		КонецПериода,
		Периодичность);
	
	Если ОписаниеФормы = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("ИмяФормы",       ОписаниеФормы.Имя);
	ОписаниеДействия.Вставить("ПараметрыФормы", ОписаниеФормы.Параметры);
	
	Возврат ОписаниеДействия;
	
КонецФункции

// Возвращает способ уплаты налога для организации и ИП
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//
// Возвращаемое значение:
//  СпособОплаты - ПеречислениеСсылка.СпособыУплатыНалогов - если возможно несколько способов уплаты,
//  то возвращает пустую ссылку
//
Функция СпособУплатыНалогаВзноса(Организация) Экспорт
	
	Способ = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииСпособаУплатыНалогаВзноса(Способ, Организация);
	Возврат Способ;
	
КонецФункции

// Определяет особые способы выполнения задачи в помощнике уплаты налога.
//
// При этом стандартными способами считаются
// - уплата налога на основании декларации или другого регулярного отчета
// - выполнение действий, не связанных с уплатой налога, таких как
//   -- представление отчета, не предусматривающего уплату, например, статотчетности
//   -- выплата зарплаты
//   -- регулярные платежи поставщикам.
//
// Остальные случаи - особые, имеют свое имя способа выполнения задачи.
// 
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//
// Возвращаемое значение:
//  Строка - имя способа выполнения задачи
//
Функция СпособВыполненияЗадачи(ПараметрыЗадачи) Экспорт
	
	Правило = ПараметрыЗадачи.ПравилоУплаты;
	
	ЭтоПравилоУплатыПатента = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ОпределитьЭтоПравилоУплатыПатента(ЭтоПравилоУплатыПатента, Правило);
	
	Если ЭтоПравилоУплатыПатента Тогда
		Возврат "УплатаПатента";
	КонецЕсли;
	
	Если Правило.Действие <> Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога Тогда
		Возврат "";
	КонецЕсли;
	
	ИдентификаторЗадачи = Правило.Владелец.Код;
	ПолноеИмяПравила = СтрШаблон("%1.%2", ИдентификаторЗадачи, Правило.Код);
	
	Если ИдентификаторЗадачи = "Зарплата"
		Или ИдентификаторЗадачи = "НДФЛ_Агент_Уплата" Тогда
		Возврат "УплатаНДФЛ"; // по данным о суммах, удержанных с физических лиц
	ИначеЕсли ИдентификаторЗадачи = "СтраховыеВзносы" Тогда
		// по данным о начисленной заработной плате
		Если Правило.Код = "2023_Уплата_Травматизм" Тогда
			Возврат "УплатаСтраховыхВзносовНаТравматизм";
		ИначеЕсли Правило.Код = "2013_Уплата_Допвзносы_Накопительная_ПФР" Тогда
			Возврат "УплатаДобровольныхСтраховыхВзносов";
		ИначеЕсли Правило.Код = "2025_УплатаСтраховыеВзносы_ЛетныеЭкипажиИШахтеры" Тогда
			Возврат "УплатаДополнительныхСтраховыхВзносовЛетчиковИШахтеров";
		Иначе
			Возврат "УплатаСтраховыхВзносов";
		КонецЕсли;
	ИначеЕсли (ИдентификаторЗадачи = "НалогНаИмущество" ИЛИ ИдентификаторЗадачи = "ТранспортныйНалог" ИЛИ ИдентификаторЗадачи = "ЗемельныйНалог") 
		И (Не ЗначениеЗаполнено(ПараметрыЗадачи.ПравилоОтчета) Или ПараметрыЗадачи.ПериодСобытия >= '20211231') Тогда
		// В соответствии с 63-ФЗ от 15.04.2019:
		//  - с 2020 года отменены налоговые расчеты по авансам по налогу на имущество.
		//  - с 2021 года (т.е. за налоговый период 2020 год и позже) отменены налоговые декларации по земельному и транспортному налогам.
		// Поэтому за периоды до 2020 года каждому правилу уплаты налога на имущество соответствует правило представления отчета,
		// а с 2020 года при уплате авансов по имуществу правило представления отчета отсутствует (декларация требуется только за год).
		// Также за период 2020 год и последующие налоговые периоды отсутстствуют правила представления декларации по транспортному и земельному налогу.
		// Таким образом, до уплаты налога за 2021 год (т.е. ПериодСобытия меньше 31.12.2021) ориентируемся на наличие 
		// правила по представлению отчета: при его наличии уплата выполняется строго по декларации (касается налога на имущество за 2020 год),
		// а если декларация не требуется, то налог уплачивается по данным расчета (транспортный и земельный налоги).
		
		// С 2022 года меняется срок уплаты налога на имущество: срок уплаты установлен значительно ранее срока подачи декларации. 
		// Таким образом, уплату налога на имущество больше не привязываем к декларации жестко (можно уплатить и по расчету, и по декларации).
		// Поэтому с 2022 года (т.е. за период 2021 года и далее) все имущественные налоги уплачиваются по одному алгоритму:
		// если за период требуется декларация, то можно уплатить налог как по расчету, так и по декларации,
		// а если декларация не требуется, то уплачиваем только по данным расчета.
		Возврат "УплатаИмущественныхНалогов";
	ИначеЕсли ИдентификаторЗадачи = "ТорговыйСбор" Тогда
		Возврат "УплатаТорговогоСбора"; // по данным уведомления об открытии торговой точки, представляемым единовременно
	ИначеЕсли ИдентификаторЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаПрофессиональныйДоход() Тогда 
		Возврат "УплатаНалогаНаПрофессиональныйДоход"; // по данным уведомления
	ИначеЕсли ИдентификаторЗадачи = "СтраховыеВзносыАУСН" Тогда
		Возврат "УплатаСтраховыхВзносовНаТравматизм";
	ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_Декабрь"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_ЕдиныйПомощник"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2024_НДФЛ_Уплата_КонецМесяца" Тогда
		Возврат "УплатаНДФЛ_ЕНС";
	ИначеЕсли ИдентификаторЗадачи = "НДС" Тогда
		Возврат "УплатаНДС";
	ИначеЕсли ИдентификаторЗадачи = "УСН" Тогда
		Возврат "УплатаУСН"; 
	ИначеЕсли ИдентификаторЗадачи = "НДФЛ_Предприниматель" Тогда
		Возврат "УплатаНДФЛ_ИП";
	ИначеЕсли ИдентификаторЗадачи = "СтраховыеВзносы_Предприниматель" Тогда
		Возврат "ВзносыЗаСебя";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Проверяет является ли правило уплатой/отчетом по страховым взносам
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//
// Возвращаемое значение:
//  Булево - признак того является ли правило платой/отчетом по страхвым взносам или нет
//
Функция ЭтоСтраховыеВзносы(Правило) Экспорт
	
	Если ТипЗнч(Правило) <> Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		Возврат Ложь;
	Иначе
		Возврат Правило.Владелец.Код = "СтраховыеВзносы"
			И Правило.Код <> "2013_Уплата_Допвзносы_Накопительная_ПФР";
	КонецЕсли;
	
КонецФункции

// Возвращает данные для платежа по декларации
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//  Организация - СправочникСсылка.Организации
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//  ПериодСобытия - Дата
//  Срок - Дата
//  ПоказательПериода - Строка
//  ДополнительныеПараметры - Структура - дополнительные параметры, если необходимо уточнение получения данных (необязательный параметр),
//                             при этом функция "ДанныеРеглОтчета" соответствующего регламентированного отчета должна поддерживать работу с этим параметром
//                             (пример см. в модуле менеджера отчета "РегламентированныйОтчетНДС").
//                             Также нужно для поиска декларации по налогу на прибыль, см. ИнтерфейсыВзаимодействияБРО.АктуальныйОтчет.
//
// Возвращаемое значение:
//  ДанныеДекларации - Структура - ключи см. ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога().
//    Для задач "НДС" дополняется ключами:
//       * СуммаНДСПоДекларации - Число - сумма НДС по декларации
//    Таблица в ключе "Платежи" дополнена колонками:
//       * Налог - СправочникСсылка.ВидыНалоговИПлатежейВБюджет - ссылка на налог, соответствующий виду налога
//       * КодНалоговогоОргана - Строка - код налогового органа
//       * Наименование - Строка - представление налога
//
Функция ДанныеДляУплатыНалогаПоДекларации(Правило, Организация, РегистрацияВНалоговомОргане, ПериодСобытия, Срок, ПоказательПериода, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЭтоНДС = (Правило.Владелец.Код = "НДС");
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ПериодСобытия);
	
	Показатели                  = ПоказателиБазовогоПериода(НачалоПериода, КонецПериода, Правило);
	ДлительностьБазовогоПериода = Показатели.ДлительностьБазовогоПериода;
	НачалоБазовогоПериода       = Показатели.НачалоБазовогоПериода;
	КонецБазовогоПериода        = Показатели.КонецБазовогоПериода;
	
	ПолноеИмяПравила  = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	
	ДанныеДекларации = ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога(
		ПолноеИмяПравила,
		Организация,
		РегистрацияВНалоговомОргане,
		КонецБазовогоПериода,
		ДополнительныеПараметры);
		
	Если ДанныеДекларации = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыПолученияДанныхПоДекларации = ПараметрыПолученияДанныхПоДекларации();
	ПараметрыПолученияДанныхПоДекларации.Правило = Правило;
	ПараметрыПолученияДанныхПоДекларации.Организация = Организация;
	ПараметрыПолученияДанныхПоДекларации.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
	ПараметрыПолученияДанныхПоДекларации.ПериодСобытия = ПериодСобытия;
	ПараметрыПолученияДанныхПоДекларации.Срок = Срок;
	ПараметрыПолученияДанныхПоДекларации.ПоказательПериода = ПоказательПериода;
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииДанныхДляУплатыНалогаПоДекларации(ДанныеДекларации,
		ПараметрыПолученияДанныхПоДекларации, ДополнительныеПараметры);
	
	Возврат ДанныеДекларации;
	
КонецФункции

// Возвращает данные для уплаты страховых взносов
// 
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//  Организация - СправочникСсылка.Организации
//  ПериодСобытия - Дата
//
// Возвращаемое значение:
//  Платежи - Структура - см. НовыйТаблицаПлатежи()
//
Функция ДанныеДляУплатыСтраховыхВзносов(Правило, Организация, ПериодСобытия, ДанныеЕНП = Ложь, ВариантВыборкиСтраховыхВзносов = 0) Экспорт
	
	Платежи = НовыйТаблицаПлатежи(); 
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииДанныхДляУплатыСтраховыхВзносов(Платежи,
		Правило, Организация, ПериодСобытия, ДанныеЕНП, ВариантВыборкиСтраховыхВзносов);
	
	Возврат Платежи;
	
КонецФункции

// Возвращает данные для уплаты имущественных налогов (транспортного и земельного налогов, налога на имущество)
// 
// Параметры:
//  ПараметрыЗадачи - СтрокаТаблицыЗначений,Структура:
//     * ИдентификаторЗадачи - Строка
//     * Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//     * Действие - ПеречислениеСсылка.ВидыДействийКалендаряБухгалтера
//     * Организация - СправочникСсылка.Организации
//     * ПериодСобытия - Дата
//     * Срок - Дата
//     * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//  ДанныеЕНП - Булево - Данные ЕНП
//  ГруппироватьПоНалоговойБазе - Булево - Истина, если для данной задачи порядок уплаты налога различается в зависимости
//                                         от налоговой базы (имеет смысл только для налога на имущество);
//                                         Ложь, если порядок уплаты единый для всей суммы налога
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыйТаблицаПлатежи()
//  
Функция ДанныеДляУплатыИмущественныхНалогов(ПараметрыЗадачи, ДанныеЕНП = Ложь, ГруппироватьПоНалоговойБазе = Ложь) Экспорт
	
	Платежи = НовыйТаблицаПлатежи();
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииДанныхДляУплатыИмущественныхНалогов(Платежи, ПараметрыЗадачи, ДанныеЕНП, ГруппироватьПоНалоговойБазе);
	
	Возврат Платежи;
	
КонецФункции

// Возвращает данные о сформированных уведомлениях
// 
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов, Массив
//  Организация - СправочникСсылка.Организации
//  ПериодСобытия - Дата
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//  ЭтоПериодПоНДФЛКонецМесяца - Булево, признак задачи НДФЛ по концу месяца
//
// Возвращаемое значение:
//  Уведомления - Структура - см. НовыйТаблицаПлатежи()
//
Функция ДанныеДляУведомлений(Правила, Организация, ПериодСобытия, РегистрацияВНалоговомОргане = Неопределено, ЭтоПериодПоНДФЛКонецМесяца = Ложь) Экспорт
	
	ТаблицаУведомлений = НовыйТаблицаУведомлений();
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииДанныхДляУведомлений(ТаблицаУведомлений,
		Правила, Организация, ПериодСобытия, РегистрацияВНалоговомОргане, ЭтоПериодПоНДФЛКонецМесяца);
	
	Возврат ТаблицаУведомлений;
	
КонецФункции

// Возвращает список правил, уведомления по которым сдавать не обязательно.
// Например, правило с типом Уведомление по задаче "СтраховыеВзносы_Предприниматель",
// не относится к уведомлениям ЕНС и не обязательно для выполнения.
// 
// Возвращаемое значение:
//   - Массив - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - список правил, уведомления по которым не обязательны.
//
Функция ПравилаПоНеобязательнымУведомлениям() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокКодовЗадачНеобязательныхУведомлений", КодыЗадачПоНеобязательнымУведомлениям());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Ссылка = ПравилаПредставленияОтчетовУплатыНалогов.Владелец
	|			И (ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление))
	|ГДЕ
	|	ЗадачиБухгалтера.Код В(&СписокКодовЗадачНеобязательныхУведомлений)"; 
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Возвращает список кодов задач, уведомления по которым сдавать не обязательно.
// Например, по задаче "СтраховыеВзносы_Предприниматель",
// уведомление сдавать не нужно, нужно сдавать заявление. Хотя подчиненные правила все равно имеют тип Уведомление.
// В эту функцию нужно добавлять все задачи, у которых есть правила с действием "Уведомление", но которые не относятся
// у уведомлению по уплате налогов для ЕНС.
// 
// Возвращаемое значение:
//   - Массив - Строка - список кодов задач, уведомления по которым не требуются.
//
Функция КодыЗадачПоНеобязательнымУведомлениям() Экспорт
	
	СписокКодовЗадачНеобязательныхУведомлений = Новый Массив;
	// По страховым взносам за ИП можно подавать заявление ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.
	// Правила по этому заявлению имеют действие Уведомление, но их подавать, в отличие от уведомлений ЕНС не обязательно.
	СписокКодовЗадачНеобязательныхУведомлений.Добавить(ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносыИП());
	
	Возврат СписокКодовЗадачНеобязательныхУведомлений;
	
КонецФункции

// Возвращает показатель периода для платежа по декларации
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//  ПериодСобытия - Дата
//  Срок - Дата
//
// Возвращаемое значение:
//  ПоказательПериода - Строка
//
Функция ПоказательПериодаПлатежаПоДекларации(Правило, ПериодСобытия, Срок) Экспорт
	
	ЭтоНДС = (Правило.Владелец.Код = "НДС");
	
	// Определим период, к которому относится платеж,
	// в соответствии с классификатором, используемым в платежных поручениях
	НалоговыйПериодДляПлатежейВБюджет = Правило.Периодичность;
	Если ЭтоНДС Тогда
		// Особенность НДС в том, что он уплачивается несколькими (ежемесячными) платежами,
		// каждый из которых относится к одному и тому же периоду (кварталу).
		// Из текста правил оформления  документов нельзя сделать однозначный вывод, что указывать в данном случае - месяц или квартал.
		// Сейчас местные УФНС рекомендуют указывать значение "КВ" (квартал).
		НалоговыйПериодДляПлатежейВБюджет = Перечисления.Периодичность.Квартал;
		
	ИначеЕсли Правило.Владелец.Код = "НДФЛ_Предприниматель" Тогда
		
		// Если по годовому платежу предусматривается более одного срока уплаты налогового платежа
		// и установлены конкретные даты уплаты, то указываются эти даты.
		
		НалоговыйПериодДляПлатежейВБюджет = Неопределено;
		
	КонецЕсли;
	
	ПоказательПериода = "0";
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииПоказателяПериодаПлатежаПоДекларации(ПоказательПериода,
		Правило, ПериодСобытия, Срок, НалоговыйПериодДляПлатежейВБюджет);
	
	Возврат ПоказательПериода;
	
КонецФункции

// Записывает в журнал регистрации факт попытки пользователем выполнения действия.
//
// Параметры:
//	ТекстКомментария - Строка - Текст комментария записи журнала регистрации.
//
Процедура ЗарегистрироватьСобытиеВЖурнале(ТекстКомментария) Экспорт

	ЗаписьЖурналаРегистрации(
		СобытиеЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,
		,
		,
		ТекстКомментария);

КонецПроцедуры

// Возвращает массив правил уплаты авансовых платежей по НДФЛ с доходов от предпринимательской деятельности ИП на ОСНО.
//
// Возвращаемое значение:
//   Массив - массив элементов с типом "СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов".
//
Функция ПравилаУплатыАвансовПоНДФЛ() Экспорт
	
	ИдентификаторыПравил = Новый Массив;
	ИдентификаторыПравил.Добавить("2013_ИП_Аванс_1Полугодие");
	ИдентификаторыПравил.Добавить("2013_ИП_Аванс_2Полугодие");
	ИдентификаторыПравил.Добавить("2022_ЕНП_УведомлениеИП_УплатаАвансов");
	
	МассивПравил = Новый Массив;
	
	Для Каждого Идентификатор Из ИдентификаторыПравил Цикл
		
		Правило = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("НДФЛ_Предприниматель", Идентификатор);
		Если Правило <> Неопределено Тогда
			МассивПравил.Добавить(Правило);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивПравил;
	
КонецФункции

// Возвращает порядок уплаты налога или взноса
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация
//   Период - Дата - период уплаты
//   ВидНалога - ПеречислениеСсылка.ВидыНалогов - налог, для которого необходимо определить порядок уплаты
//
// Возвращаемое значение:
//   Структура
//     * Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//     * Срок - Дата
//
Функция ПорядокУплатыНалога(Организация, Период, ВидНалога) Экспорт
	
	Перем СвойстваЗадачи;
	
	ПорядокУплаты = Новый Структура;
	ПорядокУплаты.Вставить("Правило", Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
	ПорядокУплаты.Вставить("Срок", Дата(1, 1, 1));
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Период) Тогда
		
		СвойстваЗадачи = РегистрыСведений.ЗадачиБухгалтера.ПорядокУплатыНалогаЗаПериод(
			Организация, ВидНалога, Период);
	КонецЕсли;
	
	Если СвойстваЗадачи <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПорядокУплаты, СвойстваЗадачи);
	КонецЕсли;
	
	Возврат ПорядокУплаты;
	
КонецФункции

// Определяет возможность отправки отчета средствами конфигурации.
//
// Параметры:
//  КодЗадачи             - Строка - идентификатор задачи (код)
//  Код                   - Строка - идентификатор правила (код)
//  ИсполнителиТребований - Соответствие - возвращаемое РеглОтчетыИсполнителиТребований().
//                           Следует передавать, если функция используется многократно (в цикле).
// 
// Возвращаемое значение:
//  Булево - Истина, если отправка отчета в конфигурации невозможна.
//
Функция ОтправкаОтчетаОграничена(КодЗадачи, Код, ИсполнителиТребований = Неопределено) Экспорт
	
	Требование = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.СкомпоноватьПолноеИмяПравила(КодЗадачи, Код);
	ИмяОтчета  = ИнтерфейсыВзаимодействияБРО.ИмяРеглОтчета(Требование, ИсполнителиТребований);
	Если Не ЗначениеЗаполнено(ИмяОтчета) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не ИнтерфейсыВзаимодействияБРОКлиентСервер.ВозможнаОтправкаЭлектронногоДокумента(ИмяОтчета);
	
КонецФункции

// Возвращает список статусов, соответствующих выполненным задачам.
//
// Возвращаемое значение:
//   Массив - массив статусов.
//
Функция СтатусыВыполненныхЗадач() Экспорт
	
	СтатусыВыполненныхЗадач = Новый Массив;
	СтатусыВыполненныхЗадач.Добавить("Сдано");
	СтатусыВыполненныхЗадач.Добавить("Оплачено");
	СтатусыВыполненныхЗадач.Добавить("Выплачено");
	СтатусыВыполненныхЗадач.Добавить("Прием подтвержден");
	СтатусыВыполненныхЗадач.Добавить("Ответ отправлен");
	
	Возврат СтатусыВыполненныхЗадач;
	
КонецФункции

// Возвращает описание действия помощника уплаты НДФЛ прошлых периодов.
//
// Параметры:
//   Структура:
//     * Организация   - СправочникСсылка.Организации - Организация, для которой действует событие
//     * ПериодСобытия - Дата - Период действия события
//     * Правило       - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - Действующее правило обработки события
//
// Возвращаемое значение:
//   Структура:
//     * ПараметрыФормы - Структура - Параметры формы
//     * ИмяФормы
//
Функция ОписаниеДействияПомощникОплатыНДФЛПрошлыхПериодов(Событие) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Событие.Организация);
	ПараметрыФормы.Вставить("Период",      Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",     Событие.Правило);
	
	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	ОписаниеДействия.Вставить("ИмяФормы");
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникОплатыНДФЛПрошлыхПериодов(ОписаниеДействия, Событие);
	
	Возврат ОписаниеДействия;
	
КонецФункции

// Определяет краткое представление налога в родительном падеже
//
// Параметры:
//  КодЗадачи - Строка - идентификатор задачи (код)
// 
// Возвращаемое значение:
//  Строка - представление. Если передан неизвестный код, то возвращается пустая строка
//
Функция ПредставлениеНалогаРодительныйПадеж(КодЗадачи) Экспорт
	
	Если КодЗадачи = "НДФЛ_Предприниматель" Тогда
		Возврат НСтр("ru = 'НДФЛ'");
	ИначеЕсли КодЗадачи = "НалогНаПрибыль" Тогда
		Возврат НСтр("ru = 'налога на прибыль'");
	ИначеЕсли КодЗадачи = "НДС" Тогда
		Возврат НСтр("ru = 'НДС'");
	ИначеЕсли КодЗадачи = "УСН" Тогда
		Возврат НСтр("ru = 'УСН'");
	ИначеЕсли КодЗадачи = "АУСН" Тогда
		Возврат НСтр("ru = 'АУСН'");
	ИначеЕсли КодЗадачи = "НалогНаПрофессиональныйДоход" Тогда
		Возврат НСтр("ru = 'налога на профессиональный доход'");
	ИначеЕсли КодЗадачи = "ЕНВД" Тогда
		Возврат НСтр("ru = 'ЕНВД'");
	ИначеЕсли КодЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиПатент() Тогда
		Возврат НСтр("ru = 'налога по патенту'");
	ИначеЕсли КодЗадачи = "НалогНаИмущество" Тогда
		Возврат НСтр("ru = 'налога на имущество'");
	ИначеЕсли КодЗадачи = "ТорговыйСбор" Тогда
		Возврат НСтр("ru = 'торгового сбора'");
	ИначеЕсли КодЗадачи = "СтраховыеВзносы_Предприниматель" Тогда
		Возврат НСтр("ru = 'фиксированных страховых взносов'");
	ИначеЕсли КодЗадачи = "СтраховыеВзносы" Тогда
		Возврат НСтр("ru = 'страховых взносов'");
	ИначеЕсли КодЗадачи = "ТранспортныйНалог" Тогда
		Возврат НСтр("ru = 'транспортного налога'");
	ИначеЕсли КодЗадачи = "ЗемельныйНалог" Тогда
		Возврат НСтр("ru = 'земельного налога'");
	ИначеЕсли КодЗадачи = "Акцизы" Тогда
		Возврат НСтр("ru = 'акцизов'");
	ИначеЕсли КодЗадачи = "ВодныйНалог" Тогда
		Возврат НСтр("ru = 'водного налога'");
	ИначеЕсли КодЗадачи = "НалогНаИгорныйБизнес" Тогда
		Возврат НСтр("ru = 'налога на игорный бизнес'");
	ИначеЕсли КодЗадачи = "ЕСХН" Тогда
		Возврат НСтр("ru = 'ЕСХН'");
	ИначеЕсли КодЗадачи = "НДПИ" Тогда
		Возврат НСтр("ru = 'НДПИ'");
	ИначеЕсли КодЗадачи = "ПлатаЗаНегативноеВоздействиеНаОкружающуюСреду" Тогда
		Возврат НСтр("ru = 'экологических платежей'");
	ИначеЕсли КодЗадачи = "ЭкологическийСбор" Тогда
		Возврат НСтр("ru = 'экологического сбора'");
	Иначе
		Возврат НСтр("ru = 'налога'");
	КонецЕсли;
	
КонецФункции

// Возвращает сокращенное представление налога.
//
// Параметры:
//   ИдентификаторЗадачи - Строка - идентификатор задачи (код)
//   ИдентификаторПравила - Строка - идентификатор правила задачи (код)
//   КраткоеОписаниеПравила - Строка - краткое описание правила задачи (описание)
//   ПолноеПредставлениеНалога - Строка - полное представление налога
// 
// Возвращаемое значение:
//   Строка - представление.
//
Функция СокращенноеПредставлениеНалога(ИдентификаторЗадачи, ИдентификаторПравила, КраткоеОписаниеПравила, ПолноеПредставлениеНалога) Экспорт
	
	Если ИдентификаторЗадачи = "СтраховыеВзносы" Тогда
		
		Если СтрНайти(ИдентификаторПравила, "УведомлениеУплатаСтраховыеВзносы") > 0 Тогда
			Возврат НСтр("ru='Страховые взносы, уведомление'");
		ИначеЕсли СтрНайти(ИдентификаторПравила, "ОтчетностьСтраховыеВзносы") > 0 Тогда
			Возврат НСтр("ru='Страховые взносы, отчет'");
		ИначеЕсли СтрНайти(ИдентификаторПравила, "ОтчетностьПерсСведенияОФизлицах") > 0 Тогда
			Возврат НСтр("ru='Перс. сведения'");
		ИначеЕсли СтрНайти(ИдентификаторПравила, "ОтчетностьПерсУчетТравматизм") > 0 Тогда
			Возврат НСтр("ru='Страховые взносы, ЕФС-1'");
		ИначеЕсли СтрНайти(ИдентификаторПравила, "ПодтверждениеВидаДеятельности") > 0 Тогда
			Возврат НСтр("ru='Подтв. вида деятельности'");
		КонецЕсли;

	ИначеЕсли ИдентификаторЗадачи = "НДФЛ_Агент" Тогда
		Возврат НСтр("ru='НДФЛ, отчет'");
		
	ИначеЕсли ИдентификаторЗадачи = "НДФЛ_Агент_Уведомление" Тогда
		Возврат НСтр("ru='НДФЛ, уведомление'");
		
	ИначеЕсли ИдентификаторЗадачи = "НДС" Тогда
		Если СтрНайти(ИдентификаторПравила, "СверкаНДС") > 0 Тогда
			Возврат "НДС, сверка";
		ИначеЕсли СтрНайти(ИдентификаторПравила, "Декларация") > 0 Тогда
			Возврат "НДС, декларация";
		КонецЕсли;
	
	ИначеЕсли ИдентификаторЗадачи = "ФинОтчетность" Тогда
		Возврат НСтр("ru='ФинОтчетность'");
		
	ИначеЕсли ИдентификаторЗадачи = "НДФЛ_Предприниматель" Тогда
		Возврат НСтр("ru='НДФЛ (ИП)'");
		
	ИначеЕсли ИдентификаторЗадачи = "УСН" Тогда
		Если СтрНайти(ИдентификаторПравила, "Уведомление") > 0 Тогда
			Возврат НСтр("ru='УСН, уведомление'");
		ИначеЕсли СтрНайти(ИдентификаторПравила, "Декларация") > 0 Тогда
			Возврат НСтр("ru='УСН, отчет'");
		КонецЕсли;
	ИначеЕсли ИдентификаторЗадачи = "СтатистическаяОтчетность" Тогда
		Возврат СтрШаблон("%1, %2", ПолноеПредставлениеНалога, СокрЛП(СтрЗаменить(КраткоеОписаниеПравила,"форма", "")));
	ИначеЕсли ИдентификаторЗадачи = "ЛьготыПоТранспортномуЗемельномуНалогу" Тогда
		Если СтрНайти(ИдентификаторПравила, "Заявление") > 0 Тогда
			Возврат НСтр("ru='Льготы, заявление'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПолноеПредставлениеНалога;
	
КонецФункции

// Возвращает правило уплаты налога по правилу представления отчета.
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета
//  Организация - СправочникСсылка.Организации - отбор правил по организации
//  ПериодСобытия - Дата - отбор правил по периоду
//  ИгнорироватьИсключения - Булево - возможность отключить проверку исключительных ситуаций
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога или пустая ссылка,
//  если оплата не требуется
//
Функция ПравилоУплатыПоПравилуОтчета(ПравилоОтчета, Организация, ПериодСобытия, ИгнорироватьИсключения = Ложь, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если Не ИгнорироватьИсключения И Не ОтчетыИспользуютсяДляУплатыНалога(ПравилоОтчета) Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	РеквизитыПравилаОтчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоОтчета, "Владелец.Код, Периодичность");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяЗадачи",                   РеквизитыПравилаОтчета.ВладелецКод);
	Запрос.УстановитьПараметр("Периодичность",               РеквизитыПравилаОтчета.Периодичность);
	Запрос.УстановитьПараметр("Организация",                 Организация);
	Запрос.УстановитьПараметр("ПериодСобытия",               ПериодСобытия);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ВЫБОР
	|		КОГДА ЗадачиБухгалтера.Правило.Периодичность = &Периодичность
	|			ТОГДА 999
	|		ИНАЧЕ ЗадачиБухгалтера.Правило.Периодичность.Порядок
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Правило = ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код = &ИмяЗадачи
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|	И &УсловиеРегистрацияВНалоговомОргане
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок УБЫВ";
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ЗадачиБухгалтера.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "Истина");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Правило;
	
КонецФункции

// Возвращает правило представления отчета по правилу уплаты налога.
//
// Параметры:
//  Правило       - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога
//  Организация   - СправочникСсылка.Организации - отбор правил по организации
//  ПериодОтчета  - Дата - дата, обозначающая период, по данным которого уплачивается налог
//  ИгнорироватьИсключения - Булево - возможность отключить проверку исключительных ситуаций
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета; 
//                                                              если не найдено, возвращается пустая ссылка
//
Функция ПравилоОтчетаПоПравилуУплаты(ПравилоУплаты, Организация, ПериодОтчета, ИгнорироватьИсключения = Ложь, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если Не ИгнорироватьИсключения И Не ОтчетыИспользуютсяДляУплатыНалога(ПравилоУплаты) Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	// Ищем задачу по представлению этого отчета, а из нее - подходящее правило
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задача",                      ПравилоУплаты.Владелец);
	Запрос.УстановитьПараметр("ПериодОтчета",                ПериодОтчета);
	Запрос.УстановитьПараметр("Организация",                 Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Правило КАК Правило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаОтчета
	|		ПО ЗадачиБухгалтера.Правило = ПравилаОтчета.Ссылка
	|ГДЕ
	|	ПравилаОтчета.Владелец = &Задача
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодОтчета
	|	И ПравилаОтчета.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|	И &УсловиеРегистрацияВНалоговомОргане
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Правило";
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ЗадачиБухгалтера.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "Истина");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТаблицаЗадач = Результат.Выгрузить();
		ПравилоОтчета = ПодобратьРелевантноеПравилоОтчета(ТаблицаЗадач.ВыгрузитьКолонку("Правило"), ПравилоУплаты);
		Возврат ПравилоОтчета;
	КонецЕсли;
	
	ОтборПравил = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения();
	ОтборПравил.Организация     = Организация;
	// Задачи за базовый период еще могут быть не созданы
	ОтборПравил.ИмяЗадачи       = ПравилоУплаты.Владелец.Код;
	ОтборПравил.Действие        = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет;
	// Принцип составления расписания: в течение периода обзора период должен завершиться.
	ОтборПравил.НачалоИнтервала = ПериодОтчета;
	ОтборПравил.КонецИнтервала  = КонецДня(ПериодОтчета) + 1;
	
	Расписание = РегистрыСведений.ЗадачиБухгалтера.РасписаниеПоНалогамОтчетамЗаПериод(ОтборПравил);
	
	Если ЗначениеЗаполнено(Расписание) Тогда
		Расписание.Сортировать("РегистрацияВНалоговомОргане, Правило", Новый СравнениеЗначений);
		ПравилоОтчета = ПодобратьРелевантноеПравилоОтчета(Расписание.ВыгрузитьКолонку("Правило"), ПравилоУплаты);
		Возврат ПравилоОтчета;
	КонецЕсли;
	
	Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	
КонецФункции

// Возвращает правило подачи уведомления по правилу представления отчета.
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета
//  Организация - СправочникСсылка.Организации - отбор правил по организации
//  ПериодСобытия - Дата - отбор правил по периоду
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога или пустая ссылка,
//  если уведомление не требуется
//
Функция ПравилоУведомленияПоПравилуОтчета(ПравилоОтчета, Организация, ПериодСобытия, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяЗадачи",                   ПравилоОтчета.Владелец.Код);
	Запрос.УстановитьПараметр("Организация",                 Организация);
	Запрос.УстановитьПараметр("ПериодСобытия",               ПериодСобытия);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтера.Правило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Правило = ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код = &ИмяЗадачи
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление)
	|	И &УсловиеРегистрацияВНалоговомОргане";
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ЗадачиБухгалтера.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "Истина");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Правило;
	
КонецФункции

// Возвращает правило представления уведомления по правилу уплаты.
//
// Параметры:
//  Правило       - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога
//  Организация   - СправочникСсылка.Организации - отбор правил по организации
//  ПериодОтчета  - Дата - дата, обозначающая период, по данным которого уплачивается налог
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета; 
//                                                              если не найдено, возвращается пустая ссылка
//
Функция ПравилоУведомленияПоПравилуУплаты(ПравилоУплаты, Организация, ПериодОтчета, ИгнорироватьИсключения = Ложь, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если Не ИгнорироватьИсключения И Не ОтчетыИспользуютсяДляУплатыНалога(ПравилоУплаты) Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	// Ищем задачу по представлению этого отчета, а из нее - подходящее правило
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяЗадачи",                   ПравилоУплаты.Владелец.Код);
	Запрос.УстановитьПараметр("ПериодОтчета",                ПериодОтчета);
	Запрос.УстановитьПараметр("Организация",                 Организация);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Правило КАК Правило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаОтчета
	|		ПО ЗадачиБухгалтера.Правило = ПравилаОтчета.Ссылка
	|ГДЕ
	|	(ПравилаОтчета.Владелец.Код = &ИмяЗадачи И &УсловиеЗадачаУведомлениеНДФЛ)
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодОтчета
	|	И ПравилаОтчета.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление)
	|	И &УсловиеРегистрацияВНалоговомОргане
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Правило";
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ЗадачиБухгалтера.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "Истина");
	КонецЕсли;
	
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(ПравилоУплаты);
	Если ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент_Уведомление");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУведомлениеНДФЛ", "ПравилаОтчета.Код = ""2023_НДФЛ_Уведомление""");
		
	ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_ЕдиныйПомощник" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент_Уведомление");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУведомлениеНДФЛ", "ПравилаОтчета.Код = ""2023_НДФЛ_Уведомление_ЕдиныйПомощник""");
		
	ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_Декабрь" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент_Уведомление");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУведомлениеНДФЛ", "ПравилаОтчета.Код = ""2023_НДФЛ_Уведомление_Декабрь""");
		
	ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент.2024_НДФЛ_Уплата_КонецМесяца" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент_Уведомление");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУведомлениеНДФЛ", "ПравилаОтчета.Код = ""2024_НДФЛ_Уведомление_КонецМесяца""");
		
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУведомлениеНДФЛ", "ИСТИНА");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТаблицаЗадач = Результат.Выгрузить();
		ПравилоУведомления = ПодобратьРелевантноеПравилоОтчета(ТаблицаЗадач.ВыгрузитьКолонку("Правило"), ПравилоУплаты);
		Возврат ПравилоУведомления;
	КонецЕсли;
	
	ОтборПравил = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения();
	ОтборПравил.Организация     = Организация;
	// Задачи за базовый период еще могут быть не созданы
	ОтборПравил.ИмяЗадачи       = ПравилоУплаты.Владелец.Код;
	ОтборПравил.Действие        = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление;
	// Принцип составления расписания: в течение периода обзора период должен завершиться.
	ОтборПравил.НачалоИнтервала = ПериодОтчета;
	ОтборПравил.КонецИнтервала  = КонецДня(ПериодОтчета) + 1;
	
	Расписание = РегистрыСведений.ЗадачиБухгалтера.РасписаниеПоНалогамОтчетамЗаПериод(ОтборПравил);
	
	Если ЗначениеЗаполнено(Расписание) Тогда
		Расписание.Сортировать("РегистрацияВНалоговомОргане, Правило", Новый СравнениеЗначений);
		ПравилоУведомления = ПодобратьРелевантноеПравилоОтчета(Расписание.ВыгрузитьКолонку("Правило"), ПравилоУплаты);
		Возврат ПравилоУведомления;
	КонецЕсли;
	
	Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	
КонецФункции

// Возвращает правило уплаты налога по правилу уведомления.
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета
//  Организация - СправочникСсылка.Организации - отбор правил по организации
//  ПериодСобытия - Дата - отбор правил по периоду
//  ИгнорироватьИсключения - Булево - возможность отключить проверку исключительных ситуаций
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога или пустая ссылка,
//  если оплата не требуется
//
Функция ПравилоУплатыПоПравилуУведомления(ПравилоУведомления, Организация, ПериодСобытия, ИгнорироватьИсключения = Ложь, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если Не ИгнорироватьИсключения И Не ОтчетыИспользуютсяДляУплатыНалога(ПравилоУведомления) Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяЗадачи",                   ПравилоУведомления.Владелец.Код);
	Запрос.УстановитьПараметр("Организация",                 Организация);
	Запрос.УстановитьПараметр("ПериодСобытия",               ПериодСобытия);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Правило КАК Правило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ЗадачиБухгалтера.Правило = ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|			И (&УсловиеЗадачаВыполняетсяЕдинымПомощником)
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код = &ИмяЗадачи
	|	И &УсловиеЗадачаУплатаНДФЛ
	|	И ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|	И &УсловиеРегистрацияВНалоговомОргане";
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ЗадачиБухгалтера.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "Истина");
	КонецЕсли;
	
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(ПравилоУведомления);
	Если ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2023_НДФЛ_Уведомление" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУплатаНДФЛ", "ПравилаПредставленияОтчетовУплатыНалогов.Код = ""2023_НДФЛ_Уплата""");
	ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2023_НДФЛ_Уведомление_Декабрь" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУплатаНДФЛ", "ПравилаПредставленияОтчетовУплатыНалогов.Код = ""2023_НДФЛ_Уплата_Декабрь""");
	ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2023_НДФЛ_Уведомление_ЕдиныйПомощник" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУплатаНДФЛ", "ПравилаПредставленияОтчетовУплатыНалогов.Код = ""2023_НДФЛ_Уплата_ЕдиныйПомощник""");
	ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2024_НДФЛ_Уведомление_КонецМесяца" Тогда
		Запрос.УстановитьПараметр("ИмяЗадачи", "НДФЛ_Агент");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУплатаНДФЛ", "ПравилаПредставленияОтчетовУплатыНалогов.Код = ""2024_НДФЛ_Уплата_КонецМесяца""");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаУплатаНДФЛ", "ИСТИНА");
	КонецЕсли;
	
	Если ТипЗнч(ПравилоУведомления) = Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов")
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилоУведомления, "ВыполняетсяЕдинымПомощником") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаВыполняетсяЕдинымПомощником", "ПравилаПредставленияОтчетовУплатыНалогов.ВыполняетсяЕдинымПомощником");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗадачаВыполняетсяЕдинымПомощником", "ИСТИНА");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТаблицаЗадач = Результат.Выгрузить();
		Для Каждого СтрокаТаблицы Из ТаблицаЗадач Цикл
			ПравилоУплаты = СтрокаТаблицы.Правило;
			Если Не ПравилоНалогаУплачиваемогоОтдельно(ПравилоУплаты) Тогда
				Возврат ПравилоУплаты;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	
КонецФункции

// Определяет период отчета, по данным которого уплачивается налог.
// В общем случае налог может уплачиваться на основании отчета, представленного в другом (базовом, более раннем) периоде.
// 
// Параметры:
//  Правило       - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило уплаты налога
//  Организация   - СправочникСсылка.Организации - отбор правил по организации
//  ПериодСобытия - Дата - дата, обозначающая период, за который уплачивается налог
//
// Возвращаемое значение:
//  Дата - последний день периода, по данным которого определена сумма налога
//         (более строго: по данным которого представляется отчет, содержащий данные о налоге, подлежащем уплате за ПериодСобытия)
//
Функция ПериодОтчетаПоПравилуУплаты(ПравилоУплаты, ПериодСобытия) Экспорт
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ПравилоУплаты.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПравилоУплаты.Периодичность, ПериодСобытия);
	
	ПоказателиБазовогоПериода = ПоказателиБазовогоПериода(НачалоПериода, КонецПериода, ПравилоУплаты);
	Возврат НачалоДня(ПоказателиБазовогоПериода.КонецБазовогоПериода);// Периоды событий в информационной базе - без учета времени
	
КонецФункции

// Определяет набор связанных правил
//
// Параметры:
//  Параметры       - Структура
//
Процедура ОпределитьСвязанныеПравила(ПараметрыНабора) Экспорт
	
	ПараметрыНабора.ПравилоУплаты = ПараметрыНабора.Правило; // Ниже может быть уточнено
	
	РегистрацияВНалоговомОргане = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыНабора, "РегистрацияВНалоговомОргане", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	
	Если ТипЗнч(ПараметрыНабора.Правило) = Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		
		Если ПараметрыНабора.Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога Тогда
		
			ПараметрыНабора.ПравилоУплаты      = ПараметрыНабора.Правило;
			ПараметрыНабора.ПериодОтчета       = ПериодОтчетаПоПравилуУплаты(
				ПараметрыНабора.ПравилоУплаты, ПараметрыНабора.ПериодСобытия);
			ПараметрыНабора.ПравилоОтчета      = ПравилоОтчетаПоПравилуУплаты(
				ПараметрыНабора.ПравилоУплаты, ПараметрыНабора.Организация, ПараметрыНабора.ПериодОтчета, , РегистрацияВНалоговомОргане);
			ПараметрыНабора.ПравилоУведомления = ПравилоУведомленияПоПравилуУплаты(
				ПараметрыНабора.ПравилоУплаты, ПараметрыНабора.Организация, ПараметрыНабора.ПериодСобытия, Истина, РегистрацияВНалоговомОргане);
		
		ИначеЕсли ПараметрыНабора.Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет Тогда
		
			ПараметрыНабора.ПравилоОтчета      = ПараметрыНабора.Правило;
			ПараметрыНабора.ПериодОтчета       = ПараметрыНабора.ПериодСобытия;
			ПараметрыНабора.ПравилоУплаты      = ПравилоУплатыПоПравилуОтчета(
				ПараметрыНабора.ПравилоОтчета, ПараметрыНабора.Организация, ПараметрыНабора.ПериодСобытия, , РегистрацияВНалоговомОргане);
			ПараметрыНабора.ПравилоУведомления = ПравилоУведомленияПоПравилуОтчета(
				ПараметрыНабора.ПравилоОтчета, ПараметрыНабора.Организация, ПараметрыНабора.ПериодСобытия, РегистрацияВНалоговомОргане);
		
		ИначеЕсли ПараметрыНабора.Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление Тогда
		
			ПараметрыНабора.ПравилоУведомления = ПараметрыНабора.Правило;
			ПараметрыНабора.ПериодОтчета       = ПараметрыНабора.ПериодСобытия;
			ПараметрыНабора.ПравилоОтчета      = ПравилоОтчетаПоПравилуУплаты(
				ПараметрыНабора.ПравилоУведомления, ПараметрыНабора.Организация, ПараметрыНабора.ПериодОтчета, Истина, РегистрацияВНалоговомОргане);
			ПараметрыНабора.ПравилоУплаты      = ПравилоУплатыПоПравилуУведомления(
				ПараметрыНабора.ПравилоУведомления, ПараметрыНабора.Организация, ПараметрыНабора.ПериодСобытия, Истина, РегистрацияВНалоговомОргане);
		
		КонецЕсли;
			
	Иначе
		ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииСвязанныхПравил(ПараметрыНабора);
	КонецЕсли;
	
КонецПроцедуры

// Готовит пустую структуру для определения связанных правил
//
// Возвращаемое значение:
//  Структура - структура параметров
//
Функция ПараметрыНабораПравил() Экспорт
	
	ПараметрыНабораПравил = Новый Структура;
	
	ПараметрыНабораПравил.Вставить("Организация",                 Справочники.Организации.ПустаяСсылка());
	ПараметрыНабораПравил.Вставить("РегистрацияВНалоговомОргане", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	ПараметрыНабораПравил.Вставить("Правило",                     Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
	ПараметрыНабораПравил.Вставить("ПравилоОтчета",               Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
	ПараметрыНабораПравил.Вставить("ПравилоУведомления",          Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
	ПараметрыНабораПравил.Вставить("ПравилоУплаты",               Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
	ПараметрыНабораПравил.Вставить("ПериодОтчета",                Дата('00010101'));
	ПараметрыНабораПравил.Вставить("ПериодСобытия",               Дата('00010101'));
	
	Возврат ПараметрыНабораПравил;
	
КонецФункции

// Завершает задачи по проверке банковских операций для организаций, находящихся на АУСН,
// после истечения возможного срока корректировки данных
//
// Возвращаемое значение:
//   Булево - Истина, если выполнено завершение задач проверки банковских операций
//
Функция ЗавершитьПроверкуБанковскихОперацийАУСН() Экспорт
	
	Результат = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриЗавершенииПроверкиБанковскихОперацийАУСН(Результат);
	Возврат Результат;
	
КонецФункции

// Возвращает идентификатор правила подачи сведений о доходах и НДФЛ сотрудников при применении АУСН.
//
// Возвращаемое значение:
//   Строка - идентификатор правила
//
Функция ИдентификаторПравилаПередачаСведенийОСотрудникахАУСН() Экспорт
	
	Возврат "2022_НДФЛ_СведенияАУСН";
	
КонецФункции

// Конструктор для таблицы уведомлений для ДанныеДляУведомлений
//
// Возвращаемое значение:
//   - ТаблицаЗначений - пустая таблица, заполняется в ВыполнениеЗадачБухгалтера.ДанныеДляУведомлений
// 
Функция НовыйТаблицаУведомлений() Экспорт
	
	ТаблицаУведомлений = Новый ТаблицаЗначений;
	
	ТаблицаУведомлений.Колонки.Добавить("Уведомление");
	ТаблицаУведомлений.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаУведомлений.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииТаблицыУведомлений(ТаблицаУведомлений);
	
	Возврат ТаблицаУведомлений;
	
КонецФункции

// Обработчик события "ПриЗаписи" документов СЭДО.
//
// Параметры:
//  Источник - см. ПодпискаНаСобытие.ПриЗаписиДокументаСЭДОДляИзмененияЗадачиБухгалера
//  Отказ - Булево - отказ от записи.
//
Процедура ПриЗаписиДокументаСЭДО(Источник, Отказ) Экспорт
	
	УчетЗарплатыВЭтойПрограмме = Истина;
	ЗадачиБухгалтераПереопределяемый.ПроверитьУчетЗарплатыВЭтойПрограмме(УчетЗарплатыВЭтойПрограмме);
	Если УчетЗарплатыВЭтойПрограмме Тогда
		РегистрыСведений.ЗадачиБухгалтера.ОбновитьИЗаполнитьДокументСЭДО(
			Источник.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает список задач, по которым мог быть создан регламентированный отчет.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на регл. отчет.
//
// Возвращаемое значение:
//   ТаблицаЗначений, Неопределено
//
Функция ЗадачиСвязанныеСОтчетом(Документ) Экспорт
	
	СвойстваОтчета = ИнтерфейсыВзаимодействияБРО.СвойстваОтчета(Документ);
	
	ИсточникОтчета = СвойстваОтчета.ИсточникОтчета;
	Если Не ЗначениеЗаполнено(ИсточникОтчета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗадачиСвязанныеСОтчетом = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииЗадачСвязанныхСОтчетом(ЗадачиСвязанныеСОтчетом, Документ, СвойстваОтчета, ИсточникОтчета);
	
	Возврат ЗадачиСвязанныеСОтчетом;
	
КонецФункции

Функция НоваяЗадачаНалоговогоПлатежа(РеквизитыДокумента, ПредыдущаяЗадача, ПараметрыРасчетаСтатусов) Экспорт
	
	Задача = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииНовойЗадачиНалоговогоПлатежа(Задача, РеквизитыДокумента, ПредыдущаяЗадача, ПараметрыРасчетаСтатусов);
	Возврат Задача;
	
КонецФункции

#Область Статусы

// Добавляет статусы задач бухгалтера по подготовке отчетов.
// Переданной таблице добавляется колонка Статус.
//
//  Параметры:
//   ТаблицаЗадач - ТаблицаЗначений:
//     * Организация                 - СправочникСсылка.Организации
//     * ПериодСобытия               - Дата
//     * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//     * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//     * Действие                    - ПеречислениеСсылка.ВидыДействийКалендаряБухгалтера
//     * Периодичность               - ПеречислениеСсылка.Периодичность
//
Процедура ДобавитьСтатусыЗадач(ТаблицаЗадач) Экспорт
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСтатусыУстановленныеВручную(ТаблицаЗадач);
	
	Если ТаблицаЗадач.Колонки.Найти("Статус") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	ОтборПоДействиюОтчет         = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
	ОтборПоДействиюУплата        = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога);
	ОтборПоДействиюРегПлатеж     = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж);
	ОтборПоДействиюЗарплата      = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата);
	ОтборПоДействиюФинОтчетность = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.ФинОтчетность);
	ОтборПоДействиюУведомление   = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление);
	ОтборПоДействиюТребованияФНС = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС);
	ОтборПоДействиюДокументыСЭДО = Новый Структура("Действие", Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО);
	
	ЗадачиПодготовкаОтчета = ТаблицаЗадач.Скопировать(ОтборПоДействиюОтчет);
	ЗадачиУплатаНалога     = ТаблицаЗадач.Скопировать(ОтборПоДействиюУплата);
	ЗадачиРегПлатежи       = ТаблицаЗадач.Скопировать(ОтборПоДействиюРегПлатеж);
	ЗадачиЗарплата         = ТаблицаЗадач.Скопировать(ОтборПоДействиюЗарплата);
	ЗадачиФинОтчетность    = ТаблицаЗадач.Скопировать(ОтборПоДействиюФинОтчетность);
	ЗадачиУведомление      = ТаблицаЗадач.Скопировать(ОтборПоДействиюУведомление);
	ЗадачиТребованиеФНС    = ТаблицаЗадач.Скопировать(ОтборПоДействиюТребованияФНС);
	ЗадачиПродлениеПатента = ЗадачиПродлениеПатента(ЗадачиПодготовкаОтчета);
	ЗадачиДокументСЭДО     = ТаблицаЗадач.Скопировать(ОтборПоДействиюДокументыСЭДО);
	
	КлючиЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоПодготовкеОтчетов(ЗадачиПодготовкаОтчета) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоУплатеНалогов(ЗадачиУплатаНалога) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоПодготовкеРегПлатежей(ЗадачиРегПлатежи) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоВыплатеЗарплаты(ЗадачиЗарплата) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоФинОтчетности(ЗадачиФинОтчетность) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ФинОтчетность;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоУведомлениям(ЗадачиУведомление) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоТребованиямФНС(ЗадачиТребованиеФНС) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоПродлениюПатентов(ЗадачиПродлениеПатента) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЗадачаСоСтатусом Из СтатусыЗадачПоДокументамСЭДО(ЗадачиДокументСЭДО) Цикл
		
		КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ЗадачаСоСтатусом);
		
		Для Каждого Задача Из ТаблицаЗадач.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = ЗадачаСоСтатусом.Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриДобавленииСтатусовЗадач(ТаблицаЗадач, ЗадачиПодготовкаОтчета, КлючиЗадачи);
	
КонецПроцедуры

// Проверяет является ли записанный в базу статус правильным, если статус неактуальный – записывает правильный статус.
//  Параметры:
//   ПараметрыЗадачи - Структура
//    * Организация
//    * Правило
//    * ПериодСобытия
//    * РегистрацияВНалоговомОргане
//   НовыйСтатус - Строка
//
Процедура ПроверитьАктуальностьСтатуса(ПараметрыЗадачи, НовыйСтатус) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрыЗадачи.Организация)
		Или Не ЗначениеЗаполнено(ПараметрыЗадачи.Правило) Тогда
		Возврат
	КонецЕсли;
	
	ЭтоЗадачаСПустымКодом = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ОпределитьЭтоЗадачаСПустымКодом(ЭтоЗадачаСПустымКодом, ПараметрыЗадачи);
	
	Если ТипЗнч(ПараметрыЗадачи.Правило) = Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		
		ИдентификаторыПравила =
			Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ИдентификаторыПравилаИЗадачи(ПараметрыЗадачи.Правило);
		
		КодЗадачи = ИдентификаторыПравила.ИдентификаторЗадачи;
		
	ИначеЕсли ТипЗнч(ПараметрыЗадачи.Правило) = Тип("ПеречислениеСсылка.ЗадачиНачалаРаботы")
		Или ЭтоЗадачаСПустымКодом Тогда
		
		КодЗадачи = "";
		
	Иначе
		Возврат;
	КонецЕсли;
	
	КодЗадачиВзносы = "СтраховыеВзносы";
	ПравилаИсключения = ПравилаИсключенияЗадачиСтраховыеВзносы();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                 ПараметрыЗадачи.Организация);
	Запрос.УстановитьПараметр("Правило",                     ПараметрыЗадачи.Правило);
	Запрос.УстановитьПараметр("КодЗадачи",                   КодЗадачи);
	Запрос.УстановитьПараметр("ПериодСобытия",               ПараметрыЗадачи.ПериодСобытия);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", ПараметрыЗадачи.РегистрацияВНалоговомОргане);
	Запрос.УстановитьПараметр("КодЗадачиВзносы",             КодЗадачиВзносы);
	Запрос.УстановитьПараметр("ПравилаИсключения",           ПравилаИсключения);
	Запрос.УстановитьПараметр("НовыйСтатус",                 НовыйСтатус);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтера.Статус,
	|	ЗадачиБухгалтера.Организация,
	|	ЗадачиБухгалтера.Правило,
	|	ЗадачиБухгалтера.ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Наименование
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И &УсловиеНаПравило
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ЗадачиБухгалтера.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|	И ЗадачиБухгалтера.Статус <> &НовыйСтатус";
	Если КодЗадачи = КодЗадачиВзносы И Не ПравилоНалогаУплачиваемогоОтдельно(ПараметрыЗадачи.Правило) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаПравило",
			"ЗадачиБухгалтера.Правило.Владелец.Код = &КодЗадачиВзносы "
			+ "И ЗадачиБухгалтера.Правило.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)"
			+ "И Не ЗадачиБухгалтера.Правило В (&ПравилаИсключения)");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаПравило", "ЗадачиБухгалтера.Правило = &Правило");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если КодЗадачи = КодЗадачиВзносы Тогда
		ПараметрыЗадачи.Правило = Выборка.Правило;
	КонецЕсли;
	
	КлючЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьКлючЗаписи(ПараметрыЗадачи);
	
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
		// Обработаем задачи, созданные после регистрации действий
		МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗадачи);
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			// Обработаем задачи, созданные до регистрации действий
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗадачи);
			МенеджерЗаписи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ПустаяСсылка();
			МенеджерЗаписи.Прочитать();
			МенеджерЗаписи.Действие = ПараметрыЗадачи.Действие;
		КонецЕсли;
		
		МенеджерЗаписи.Статус = НовыйСтатус;
		МенеджерЗаписи.Записать();
		
		РазблокироватьДанныеДляРедактирования(КлючЗаписи);
		
	Исключение
		
		ТекстСобытия = НСтр("ru = 'Задачи бухгалтера'", ОбщегоНазначения.КодОсновногоЯзыка());
		ШаблонОшибки = НСтр("ru = 'Не удалось обновить статус задачи ""%1"" по причине:
			|%2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Выборка.Наименование, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ТекстСобытия, УровеньЖурналаРегистрации.Ошибка, , Выборка.Наименование, ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Описывает действие, которое необходимо выполнить для налогового платежа
//
// Параметры:
//  Событие - Структура - см. ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи()
// Возвращаемое значение:
//  Структура - состав см. в теле функции
//
Функция ОписаниеДействияНалоговыйПлатеж(Событие) Экспорт
	
	Правило                     = Событие.Правило;
	ИмяЗадачи                   = Правило.Владелец.Код;
	Действие                    = Правило.Действие;
	
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	
	ВыполнитьЕдинымПомощником   =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Событие, "ВыполнитьЕдинымПомощником", Ложь)
		Или ЭтоПравилоНДФЛКонецМесяца(ПолноеИмяПравила)
		Или ЭтоПравилоУплатыНДСИзЕАЭС(ПолноеИмяПравила);
	
	УчетЗарплатыИКадровВЭтойПрограмме = Истина;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаУчетаЗарплатыИКадровВЭтойПрограмме(УчетЗарплатыИКадровВЭтойПрограмме);
	
	Если Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет
		Или Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление Тогда
		
		Если ИмяЗадачи = "ЕдиныйНалоговыйСчет" Или ВыполнитьЕдинымПомощником Тогда
			
			Возврат ОписаниеДействияЕдиныйПомощникУплатыНалога(Событие);
			
		ИначеЕсли (ПолноеИмяПравила = "СтраховыеВзносы.2013_ОтчетностьПФР"
			ИЛИ ПолноеИмяПравила = "СтраховыеВзносы.2014_ОтчетностьПФРБумажная"
			ИЛИ ПолноеИмяПравила = "СтраховыеВзносы.2014_ОтчетностьПФРЭлектронная")
			И УчетЗарплатыИКадровВЭтойПрограмме Тогда
			
			// БЗКБ может обеспечить выполнение этого действия
			Возврат ОписаниеДействияОтчетПФР(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "СтраховыеВзносы.2016_СведенияПФРЕжемесячные"
			Или ПолноеИмяПравила = "СтраховыеВзносы.2017_СведенияПФРЕжемесячные" Тогда
			
			// БЗКБ может обеспечить выполнение этого действия
			Возврат ОписаниеДействияСведенияПФРЕжемесячные(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "СтраховыеВзносы.2017_СведенияСтажСотрудниковОрганизации"
			Или ПолноеИмяПравила = "СтраховыеВзносы.2017_СведенияСтажСотрудниковПредприниматели"
			Или ПолноеИмяПравила = "СтраховыеВзносы.2023_СведенияСтажСотрудниковОрганизации"
			Или ПолноеИмяПравила = "СтраховыеВзносы.2023_СведенияСтажСотрудниковПредприниматели" Тогда
			
			// БЗКБ может обеспечить выполнение этого действия
			Возврат ОписаниеДействияСведенияПФРЕжегодные(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "СтраховыеВзносы.2013_Отчет_Допвзносы_Накопительная_ПФР" Тогда
			
			// БЗКБ может обеспечить выполнение этого действия
			Возврат ОписаниеДействияРеестрДСВ3(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "СтраховыеВзносы.2022_ЕНП_УведомлениеУплатаСтраховыеВзносы"
			И УчетЗарплатыИКадровВЭтойПрограмме Тогда
			
			Возврат ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Ложь);
			
		ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент.2013_2НДФЛ" 
			Или ПолноеИмяПравила = "НДФЛ_Агент.2013_2НДФЛ_Неудержанный"
			Или ПолноеИмяПравила = "НДФЛ_Агент.2016_2НДФЛ_Неудержанный" Тогда
			
			// БЗКБ может обеспечить выполнение этого действия
			Возврат ОписаниеДействия2НДФЛ(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "СтраховыеВзносы.2020_СведенияЭТКЕжемесячные"
			Или ПолноеИмяПравила = "СтраховыеВзносы.2020_СведенияЭТКЕжедневные"
			Или ПолноеИмяПравила = "СтраховыеВзносы.2023_СведенияЭТКЕжемесячные"
			Или  ПолноеИмяПравила = "СтраховыеВзносы.2023_СведенияЭТКЕжедневные" Тогда
			
			// БЗКБ может обеспечить выполнение этого действия
			Возврат ОписаниеДействияСведенияЭТКЕжемесячные(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "ТрудовыеКнижки.2025_СведенияЭТКЕжемесячные"
			Или ПолноеИмяПравила = "ТрудовыеКнижки.2025_СведенияЭТКЕжедневные" Тогда

			// БЗКБ может обеспечить выполнение этого действия
			Возврат ОписаниеДействияСведенияЭТКЕжемесячные(Событие);
		ИначеЕсли ПолноеИмяПравила = "КосвенныеНалогиТамСоюз.2010_Статформа" Тогда
			
			// Открываем форму списка заявлений о ввозе
			Возврат ОписаниеДействияЗаявленияОВвозе(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "НДС.СверкаНДС" Тогда
		
			// Открываем форму обработки "Сверка данных НДС"
			Возврат ОписаниеДействияСверкаНДС(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "НДС.2015_ЖурналСчетовФактур" Тогда
			
			// Открываем форму документа "Журнал счетов-фактур для передачи в электронном виде"
			Возврат ОписаниеДействияЖурналСчетовФактур(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "НДС.2022_ЕНП_УведомлениеНеплательщик"
			Или ПолноеИмяПравила = "НДС.2022_ЕНП_УведомлениеНеплательщикНалоговыйАгент" Тогда
		
			// Открываем форму списка уведомлений
			Возврат ОписаниеДействияУведомлениеОбИсчисленныхСуммахНалогов(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "СтатистическаяОтчетность.ПроверитьПереченьФормФизлица"
			Или ПолноеИмяПравила = "СтатистическаяОтчетность.ПроверитьПереченьФормЮрлица" Тогда
			
			// Проверить можно, сформировав уведомление на специальном сайте Росстата
			Возврат Новый Структура("Переход", "https://websbor.gks.ru/online/#!/gs/statistic-codes");
			
		ИначеЕсли ПолноеИмяПравила = "Патент.2020_ЗаявлениеНаПатент"
			Или ПолноеИмяПравила = "Патент.2021_ЗаявлениеНаПатент" Тогда
			
			Возврат ОписаниеДействияПомощникЗаявленияНаПолучениеПатента(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "УСН.2020_УведомлениеУСН" Тогда
			
			Возврат ОписаниеДействияУведомлениеУСН(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "АУСН.2021_Ознакомление"
			Или ПолноеИмяПравила = "АУСН.2022_Ознакомление"
			Или ПолноеИмяПравила = "АУСН.2022_Ознакомление_СнятиеСУчета" Тогда
			
			Возврат ОписаниеДействияНапоминаниеОПроверкеНалогаАУСН(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "НДФЛ_Агент.2022_НДФЛ_СведенияАУСН" Тогда
			
			Возврат ОписаниеДействияПередачаСведенийОСотрудникахАУСН(Событие);
			
		ИначеЕсли ПравилоВыполняетсяПомощникомРасчетаНалогаУСН(Правило) Тогда
			
			Если ЭтоОтчетПрошлогоПериода(Событие) Тогда
				// Открываем помощник прошлых периодов
				Возврат ОписаниеДействияПомощникПодготовкиОтчетностиПрошлыхПериодов(Событие);
			Иначе
				// Открываем форму обработки "Помощник расчета налога УСН"
				Возврат ОписаниеДействияПомощникРасчетаУСН(Событие);
			КонецЕсли;
			
		ИначеЕсли ИмяЗадачи = "ЕНВД" Тогда
			
			Возврат ОписаниеДействияРасчетЕНВД(Событие);
			
		ИначеЕсли ПравилоВыполняетсяПомощникомУплатыАвансовыхПлатежейНДФЛ(Событие) Тогда
			
			Возврат ОписаниеДействияПомощникУплатыАвансовыхПлатежейНДФЛ(Событие);
			
		ИначеЕсли ПравилоВыполняетсяПомощникомЗаполнения3НДФЛ(Событие) Тогда
			
			Возврат ОписаниеДействияПомощникЗаполнения3НДФЛ(Событие);
			
		ИначеЕсли ИмяЗадачи = "НДС" Тогда
			
			Возврат ОписаниеДействияПомощникРасчетаНДС(Событие);
			
		ИначеЕсли ИмяЗадачи = "ЛьготыПоТранспортномуЗемельномуНалогу" Тогда
			
			Возврат ОписаниеДействияЗаявлениеОЛьготеПоИмущественнымНалогам(
				Событие.Организация,
				Событие.ПериодСобытия,
				Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеЛьготаТранспортЗемля);
				
		ИначеЕсли ИмяЗадачи = "СтраховыеВзносы_Предприниматель" Тогда
			
			Возврат ОписаниеДействияРасчетСтраховыхВзносовИП(Событие);
		
		ИначеЕсли ПравилоВыполняетсяУниверсальнымПомощником(Правило) Тогда
			
			Возврат ОписаниеДействияПомощникУплатыНалога(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "СтраховыеВзносы_Предприниматель.2023_ЕНП_ЗаявлениеЗачетВзносов" Тогда
			
			Возврат ОписаниеДействияЗаявлениеОЗачетеВзносовИП(Событие);
			
		ИначеЕсли ИмяЗадачи = "СтатистическаяОтчетность" Тогда
			
			// Отчеты, для которых бро не предоставляет возможности использовать "универсальный помощник",
			// могут быть предоставлены Росстату с помощью "универсального отчета".
			// Покажем пользователю информацию про универсальный отчет.
			Возврат Новый Структура("Переход", "http://buh.ru/articles/faq/40735/");
			
		Иначе
			
			ОписаниеДействия = Новый Структура;
			ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияНалоговыйПлатежОтчетУведомление(ОписаниеДействия, Правило, Событие, ПолноеИмяПравила);
			Возврат ОписаниеДействия;
			
		КонецЕсли;
		
	ИначеЕсли Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата Тогда
		
		Если ИмяЗадачи = "Зарплата" 
			И УчетЗарплатыИКадровВЭтойПрограмме Тогда
			Если ПолноеИмяПравила = "Зарплата.Аванс" Тогда
				Возврат ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Истина);
			Иначе
				Возврат ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Ложь);
			КонецЕсли;
		ИначеЕсли ИмяЗадачи = "НДФЛ_Агент_Уплата" 
			И УчетЗарплатыИКадровВЭтойПрограмме Тогда
			Если ПолноеИмяПравила = "НДФЛ_Агент_Уплата.Аванс" Тогда
				Возврат ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Истина);
			Иначе
				Возврат ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Ложь);
			КонецЕсли;
		Иначе
			Если ИмяЗадачи = "НДФЛ_Агент_Уплата"
				И Не УчетЗарплатыИКадровВЭтойПрограмме Тогда
				Возврат ОписаниеДействияОткрытьДокументыРасчетаВнешняяЗарплата(Событие);
			Иначе
				Возврат ОписаниеДействияОткрытьОтражениеВУчетеВнешняяЗарплата(Событие);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Действие = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога Тогда
		
		СтраховыеВзносыЕдиныйПомощник = Ложь;
		ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПравилаСтраховыеВзносыЕдиныйПомощник(СтраховыеВзносыЕдиныйПомощник, Правило, ПолноеИмяПравила);
		
		Если ИмяЗадачи = "ЕдиныйНалоговыйСчет" Или ВыполнитьЕдинымПомощником Тогда
			
			Возврат ОписаниеДействияЕдиныйПомощникУплатыНалога(Событие);
		
		ИначеЕсли ИмяЗадачи = "СтраховыеВзносы_Предприниматель" Тогда
			
			Если ЗадачиБухгалтераКлиентСервер.ЭтоУплатаДобровольныхВзносовИП(Событие.ИдентификаторПравила) Тогда
				Возврат ОписаниеДействияВзносыНаСоциальноеСтрахованиеИП(Событие);
			Иначе
				Возврат ОписаниеДействияРасчетСтраховыхВзносовИП(Событие);
			КонецЕсли;
		
		ИначеЕсли ПравилоВыполняетсяПомощникомРасчетаНалогаУСН(Правило) Тогда
			
			Если ЭтоОплатаНалогаЗаПрошлыйПериод(Событие) Тогда
				Возврат ОписаниеДействияПомощникОплатыНалогаПрошлыхПериодов(Событие);
			Иначе
				// Открываем форму обработки "Помощник расчета налога УСН"
				Возврат ОписаниеДействияПомощникРасчетаУСН(Событие);
			КонецЕсли;
			
		ИначеЕсли ПолноеИмяПравила = "АУСН.2021_Уплата_ФСС_НС"
			Или ПолноеИмяПравила = "СтраховыеВзносыАУСН.2022_Уплата_ФСС_НС_АУСН"
			Или ПолноеИмяПравила = "СтраховыеВзносыАУСН.2022_Уплата_ФСС_НС_АУСН_СнятиеСУчета" Тогда
			
			Возврат ОписаниеДействияУплатаФСС_НС_АУСН(Событие);
			
		ИначеЕсли ПравилоВыполняетсяПомощникомУплатыАУСН(Событие, ПолноеИмяПравила) Тогда
			
			Возврат ОписаниеДействияПомощникУплатыАУСН(Событие);
			
		ИначеЕсли ИмяЗадачи = "АУСН" Тогда
			
			Возврат ОписаниеДействияУплатаНалогаАУСН(Событие);
		
		ИначеЕсли ИмяЗадачи = "ЕНВД" Тогда
			
			Возврат ОписаниеДействияРасчетЕНВД(Событие);
			
		ИначеЕсли ПравилоВыполняетсяПомощникомУплатыАвансовыхПлатежейНДФЛ(Событие) Тогда
			
			Возврат ОписаниеДействияПомощникУплатыАвансовыхПлатежейНДФЛ(Событие);
			
		ИначеЕсли ПравилоВыполняетсяПомощникомЗаполнения3НДФЛ(Событие) Тогда
			
			Возврат ОписаниеДействияПомощникЗаполнения3НДФЛ(Событие);
			
		ИначеЕсли ИмяЗадачи = "СтраховыеВзносы"
			И УчетЗарплатыИКадровВЭтойПрограмме
			И СтраховыеВзносыЕдиныйПомощник Тогда
			
			Возврат ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Ложь);
			
		ИначеЕсли ИмяЗадачи = "НДС" Тогда
			
			Возврат ОписаниеДействияПомощникРасчетаНДС(Событие);
			
		ИначеЕсли ПравилоВыполняетсяПомощникомУплатыНПД(Правило, ИмяЗадачи) Тогда
			
			Возврат ОписаниеДействияПомощникУплатыНПД(Событие);
			
		ИначеЕсли ПравилоВыполняетсяУниверсальнымПомощником(Правило) Тогда
			
			Возврат ОписаниеДействияПомощникУплатыНалога(Событие);
			
		Иначе
			
			Возврат ОписаниеДействияФормированиеПлатежныхПорученийНаУплатуНалогов(Событие);
			
		КонецЕсли;
	
	ИначеЕсли Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Проверка Тогда
		
		Если ПолноеИмяПравила = "ТранспортныйНалог.СверкаРасчета" 
			 Или ПолноеИмяПравила = "ЗемельныйНалог.СверкаРасчета"
			 Или ПолноеИмяПравила = "НалогНаИмущество.СверкаРасчета" Тогда
			
			Возврат ОписаниеДействияСверкаИмущественныхНалогов(Событие);
			
		ИначеЕсли ПолноеИмяПравила = "УСН.2022_ВводОстатковСменыОбъектаУСН" Тогда
			
			Возврат ОписаниеДействияВводОстатковПриСменеОбъектаУСН(Событие);
			
		Иначе
			
			ОписаниеДействия = Новый Структура;
			ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияНалоговыйПлатежПроверка(ОписаниеДействия, Правило, Событие);
			Возврат ОписаниеДействия;
			
		КонецЕсли;
		
	ИначеЕсли Действие = Перечисления.ВидыДействийКалендаряБухгалтера.РегулярныйПлатеж Тогда 
			
		Если ИмяЗадачи = "ОплатаУставногоКапитала" Тогда
			
			Возврат ОписаниеДействияОплатаУставногоКапитала(Событие.Организация);
			
		Иначе
			
			ОписаниеДействия = Новый Структура;
			ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияНалоговыйПлатежРегулярныйПлатеж(ОписаниеДействия, Правило, Событие);
			Возврат ОписаниеДействия;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(Правило.Наименование) Тогда
		ОписаниеДействия = Новый Структура;
		ОписаниеДействия.Вставить("Предупреждение", Правило.Наименование);
		Возврат ОписаниеДействия;
	КонецЕсли;
	
	Возврат Новый Структура; // Никакое действие не можем выполнить
	
КонецФункции

Функция ОписаниеДействияРегулярныйПлатеж(Событие) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияРегулярныйПлатеж(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияФинОтчетность(Событие) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияФинОтчетность(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПлатежПоДаннымДекларации(Организация, РегистрацияВНалоговомОргане, СпособОплаты, Срок, Правило, ПериодСобытия, Описание) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПлатежПоДаннымДекларации(Организация,
		РегистрацияВНалоговомОргане, СпособОплаты, Срок, Правило, ПериодСобытия, Описание, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПлатежУСН(СтруктураРеквизитовУплатыНалога) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПлатежУСН(СтруктураРеквизитовУплатыНалога, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникОплатыПатента(Событие) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникОплатыПатента(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникПодготовкиЗаявленияНаПолучениеПатента(Событие) Экспорт
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникПодготовкиЗаявленияНаПолучениеПатента(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
КонецФункции

Функция ОписаниеДействияСозданиеПатента(Событие) Экспорт
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияСозданиеПатента(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
КонецФункции

Функция ОписаниеДействияЗаявлениеОЛьготеПоИмущественнымНалогам(Организация, Период, ВидУведомления) Экспорт 
		
	ЗаявленияОЛьготах = Документы.УведомлениеОСпецрежимахНалогообложения.АктуальныеУведомления(
		Организация,
		ВидУведомления);
		
	ОписаниеДействия = Новый Структура;
	ПараметрыФормы = Новый Структура;
	
	Если ЗаявленияОЛьготах = Неопределено Тогда
		// Создаем новое заявление
		ОписаниеДействия.Вставить("ИмяФормы", ИнтерфейсыВзаимодействияБРОВызовСервера.ПолучитьИмяФормыУведомленияПоУмолчанию(
			ВидУведомления,
			Период)); 
				
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ВидУведомления", ВидУведомления);
		ПараметрыФормы.Вставить("ЗаполнитьПриОткрытии", Истина);
	ИначеЕсли ЗаявленияОЛьготах.Количество() > 1 Тогда
		// Ранее уже было подговлено несколько заявлений - открываем список заявлений
		ОписаниеДействия.Вставить("ИмяФормы", "ОбщаяФорма.РегламентированнаяОтчетность");
		
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("Раздел", Перечисления.СтраницыЖурналаОтчетность.Уведомления);
		ПараметрыФормы.Вставить("ВидУведомления", ВидУведомления);
	Иначе	
		// Открываем единственное найденное заявление
		ОписаниеДействия.Вставить("ИмяФормы", "Документ.УведомлениеОСпецрежимахНалогообложения.ФормаОбъекта");
		ПараметрыФормы.Вставить("Ключ", ЗаявленияОЛьготах[0]);
	КонецЕсли; 	
	
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	Возврат ОписаниеДействия;
	
КонецФункции

// Возвращает описание действия по нажатию на ссылку оплаты ЕНС
//
// Параметры:
//  СтруктураРеквизитовУплатыНалога - Структура - см. НовыйСтруктураРеквизитовУплатыНалогаЕНС()
// 
// Возвращаемое значение:
//  Структура:
//        * ИмяФормы - Строка - имя открываемой формы
//        * ПараметрыФормы - Структура - параметры передаваемые для открытия формы
//
Функция ОписаниеДействияПлатежЕНС(СтруктураРеквизитовУплатыНалога) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПлатежЕНС(СтруктураРеквизитовУплатыНалога, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияВзносыНаСоциальноеСтрахованиеИП(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияВзносыНаСоциальноеСтрахованиеИП(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникПодготовкиОтчетностиПрошлыхПериодов(Событие) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникПодготовкиОтчетностиПрошлыхПериодов(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияЕдиныйПомощникУплатыНалога(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияЕдиныйПомощникУплатыНалога(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияТребованияФНС(Событие) Экспорт
	
	ОписаниеДействия = Новый Структура;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Событие.Правило);
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов.Форма.ФормаЭлемента");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияДокументСЭДО(Событие) Экспорт
	
	ОписаниеДействия = Новый Структура("ИмяФормы, ПараметрыФормы");
	
	ПараметрыФормы = Новый Структура("Ключ, ТекущаяСтрока");
	Если Событие.КоличествоЗадач = 1 Тогда
		ПараметрыФормы.Ключ = Событие.Правило;
		ВидФормы = НСтр("ru = 'ФормаДокумента'");
	Иначе
		ПараметрыФормы.ТекущаяСтрока = Событие.Правило;
		ВидФормы = НСтр("ru = 'ФормаСписка'");
	КонецЕсли;
	
	Если ВыполнениеЗадачБухгалтераКлиентСервер.ЭтоДокументСЭДО(Событие.Правило) Тогда
		ИмяФормы = СтрШаблон(НСтр("ru = 'Документ.%1.Форма.%2'"), Событие.Правило.Метаданные().Имя, ВидФормы);
		ОписаниеДействия.ИмяФормы = ИмяФормы;
	КонецЕсли;
	
	ОписаниеДействия.ПараметрыФормы = ПараметрыФормы;
	
	Возврат ОписаниеДействия;
	
КонецФункции

// Вместо этой функции рекомендуется использовать функцию ВыполнениеЗадачБухгалтераПовтИспНаВызов.ПараметрыСтраховыхВзносов()
// Получает таблицу счетов и видов налогов для страховых взносов.
//
// Параметры:
//  ПериодСобытия - Дата - месяц, на который нужно получить параметры.
//
// Возвращаемое значение:
//   ТаблицаЗначений - информация о действующих страховых взносах.
//
Функция ПараметрыСтраховыхВзносов(ПериодСобытия, ЗаполнятьКБК = Истина) Экспорт
	
	ТаблицаСчетовСтраховыхВзносов = Новый ТаблицаЗначений;
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("Налог");
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("ВидНалоговогоОбязательства");
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("СчетУчета");
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("КБК", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(20)));
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("НазначениеПлатежа", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(210)));
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПараметровСтраховыхВзносов(
		ТаблицаСчетовСтраховыхВзносов, ПериодСобытия, ЗаполнятьКБК);
	
	Возврат ТаблицаСчетовСтраховыхВзносов;
	
КонецФункции

Функция ПараметрыНДФЛ(Знач ПериодСобытия) Экспорт
	
	ТаблицаСчетовНДФЛ = Новый ТаблицаЗначений;
	ТаблицаСчетовНДФЛ.Колонки.Добавить("Налог");
	ТаблицаСчетовНДФЛ.Колонки.Добавить("ВидНалоговогоОбязательства");
	ТаблицаСчетовНДФЛ.Колонки.Добавить("СчетУчета");
	ТаблицаСчетовНДФЛ.Колонки.Добавить("КБК", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(20)));
	ТаблицаСчетовНДФЛ.Колонки.Добавить("НазначениеПлатежа", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(210)));
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПараметровНДФЛ(ТаблицаСчетовНДФЛ, ПериодСобытия);
	
	Возврат ТаблицаСчетовНДФЛ;
	
КонецФункции

Функция ЭтоОплатаНалогаЗаПрошлыйПериод(Событие) Экспорт

	ЭтоОплатаНалогаЗаПрошлыйПериод = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаОплатыНалогаЗаПрошлыйПериод(ЭтоОплатаНалогаЗаПрошлыйПериод, Событие);
	
	Возврат ЭтоОплатаНалогаЗаПрошлыйПериод;

КонецФункции

Функция ПоказателиБазовогоПериода(НачалоПериода, КонецПериода, Правило) Экспорт
	
	Показатели = Новый Структура;
	
	СобытиеПозжеРелевантногоПериода = КалендарьБухгалтера.СрокИстекаетПослеОкончанияРелевантногоПериода(Правило);
	ПростойБазовыйПериод            = КалендарьБухгалтера.БазовыйПериодОпределяетсяПросто(Правило);
	
	ДлительностьБазовогоПериода = Правило.Периодичность;
	
	Если СобытиеПозжеРелевантногоПериода И ПростойБазовыйПериод Тогда
		// Базовый период и релевантный период совпадают
		НачалоБазовогоПериода = НачалоПериода;
		КонецБазовогоПериода  = КонецПериода;
	Иначе
		
		// Базовый и релевантный периоды не совпадают.
		// Базовый период включает в себя один или несколько релевантных.
		Если Не ПростойБазовыйПериод Тогда
			ДлительностьБазовогоПериода = Правило.БазовыйПериод;
		КонецЕсли;
		
		// Базовый период всегда предшествует сроку
		ОтставаниеБазовогоПериода = Правило.ОтставаниеБазовогоПериода;
		КалендарьБухгалтера.УточнитьОтставаниеБазовогоПериода(
			ОтставаниеБазовогоПериода,
			ДлительностьБазовогоПериода,
			Правило.СрокМесяцев);
		
		// авансы за первое полугодие 2013 платим по данным 2012 года. СрокМесяцев = 0, ОтставаниеБазовогоПериода = 0
		// прогноз на май даем в апреле по данным за март. СрокМесяцев = -2, ОтставаниеБазовогоПериода = 1 (или 0)
		// авансы в апреле платим по данным 1 квартала. СрокМесяцев = -1, ОтставаниеБазовогоПериода = 0
		// авансы в феврале платим по данным 3 квартала. СрокМесяцев = -1, ОтставаниеБазовогоПериода = 1
		ОпережениеРелевантногоПериода = 1 +  ОтставаниеБазовогоПериода;
		
		БазовыйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
			НачалоПериода,
			ДлительностьБазовогоПериода,
			-ОпережениеРелевантногоПериода);
		
		НачалоБазовогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ДлительностьБазовогоПериода, БазовыйПериод);
		КонецБазовогоПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ДлительностьБазовогоПериода,  БазовыйПериод);
		
	КонецЕсли;
	
	Показатели.Вставить("ДлительностьБазовогоПериода", ДлительностьБазовогоПериода);
	Показатели.Вставить("НачалоБазовогоПериода", НачалоБазовогоПериода);
	Показатели.Вставить("КонецБазовогоПериода", КонецБазовогоПериода);
	
	Возврат Показатели;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомЗаполнения3НДФЛ(Событие) Экспорт
	
	Если Событие.ИдентификаторЗадачи <> "НДФЛ_Предприниматель" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выполняется = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПравилоВыполняетсяПомощникомЗаполнения3НДФЛ(Событие, Выполняется);
	Возврат Выполняется;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомУплатыАвансовыхПлатежейНДФЛ(Событие) Экспорт
	
	ИдентификаторыПравил = Новый Массив;
	ИдентификаторыПравил.Добавить("2020_ИП_УплатаАвансов");
	ИдентификаторыПравил.Добавить("2022_ЕНП_Уведомление_ИП_УплатаАвансов");
	ИдентификаторыПравил.Добавить("2023_ЕНП_Уведомление_ИП_УплатаАвансов");
	ИдентификаторыПравил.Добавить("2023_Уведомление_ИП_УплатаАвансов_ЕдиныйПомощник");
	ИдентификаторыПравил.Добавить("2024_ИП_УплатаАвансов_ЕдиныйПомощник");
	
	Если ЗначениеЗаполнено(Событие.ИдентификаторЗадачи)
		И Событие.ИдентификаторЗадачи <> ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель() Тогда
		
		Возврат Ложь;
		
	ИначеЕсли ЗначениеЗаполнено(Событие.ИдентификаторПравила)
		И ИдентификаторыПравил.Найти(Событие.ИдентификаторПравила) = Неопределено Тогда
		
		Возврат Ложь;
		
	ИначеЕсли Не ЗначениеЗаполнено(Событие.ИдентификаторПравила)
		И ИдентификаторыПравил.Найти(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Событие.Правило, "Код")) = Неопределено Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Выполняется = Истина;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПравилоВыполняетсяПомощникомУплатыАвансовыхПлатежейНДФЛ(Событие, Выполняется);
	
	Возврат Выполняется;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомЕНВД(Правило, ИмяЗадачи = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	Если ИмяЗадачи = "ЕНВД" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомРасчетаНалогаУСН(Правило, ПолноеИмяПравила = Неопределено) Экспорт
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Возврат ПравилаВыполняемыеПомощникомРасчетаНалогаУСН().Найти(ПолноеИмяПравила) <> Неопределено;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомУплатыНалогаАУСН(Правило, ПолноеИмяПравила = Неопределено) Экспорт
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Возврат (ПолноеИмяПравила = "АУСН.2021_Уплата");
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомРасчетСтраховыхВзносовИП(Правило, ИмяЗадачи = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	Если ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносыИП() Тогда
		КодПравила = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "Код");
		Возврат Не ЗадачиБухгалтераКлиентСервер.ЭтоУплатаДобровольныхВзносовИП(КодПравила);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомУчетаЗарплаты(Правило, ИмяЗадачи = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	Возврат (ИмяЗадачи = "СтраховыеВзносы");
	
КонецФункции

Функция ПравилоМожетВыполнятьсяУниверсальнымПомощником(
		Правило, ИмяЗадачи = Неопределено, ПолноеИмяПравила = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Если ПолноеИмяПравила = "СтраховыеВзносыАУСН.2022_Уплата_ФСС_НС_АУСН"
		Или ЭтоСтраховыеВзносыВнеЕНП(Правило) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПравилоВыполняетсяУниверсальнымПомощником(
		Правило, ИмяЗадачи = Неопределено, ПолноеИмяПравила = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Признак = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПравилоВыполняетсяУниверсальнымПомощником(Признак, Правило, ИмяЗадачи, ПолноеИмяПравила);
	
	Возврат Признак;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомУплатыНПД(Правило, Знач ИмяЗадачи = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	ДоступнаИнтеграцияСПлатформойСамозанятые = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаДоступнаИнтеграцияСПлатформойСамозанятые(ДоступнаИнтеграцияСПлатформойСамозанятые);
	
	Возврат ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаПрофессиональныйДоход()
		И ДоступнаИнтеграцияСПлатформойСамозанятые;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомРасчетаНДС(Правило, ПолноеИмяПравила = Неопределено) Экспорт
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Возврат ПравилаВыполняемыеПомощникомРасчетаНДС().Найти(ПолноеИмяПравила) <> Неопределено;
	
КонецФункции

Функция ЭтоПравилоУСНАвансовыйПлатеж(ПолноеИмяПравила) Экспорт
	
	Если ПолноеИмяПравила = "УСН.2013_УплатаОрганизация"
		ИЛИ ПолноеИмяПравила = "УСН.2013_УплатаПредприниматель"
		ИЛИ ПолноеИмяПравила = "УСН.2013_УплатаАвансов"
		ИЛИ ПолноеИмяПравила = "УСН.2023_УплатаАвансов" Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция ЭтоПравилоУплатыИмущественногоНалога(ПолноеИмяПравила) Экспорт
	
	// Уплата налогов по указанным ниже правилам выполняется без декларации.
	// Но при этом используется универсальный помощник уплаты налога.
	ПравилаУплатыИмущественныхНалогов = Новый Массив;
	
	ПравилаУплатыИмущественныхНалогов.Добавить("НалогНаИмущество.2021_Налог_Год");
	ПравилаУплатыИмущественныхНалогов.Добавить("НалогНаИмущество.2022_Аванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("НалогНаИмущество.2022_Налог_Год");
	ПравилаУплатыИмущественныхНалогов.Добавить("НалогНаИмущество.2022_ЕНП_УведомлениеАванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("НалогНаИмущество.2022_ЕНП_УведомлениеНалог");
	ПравилаУплатыИмущественныхНалогов.Добавить("НалогНаИмущество.2023_Аванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("НалогНаИмущество.2023_ЕНП_УведомлениеАванс");
	
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2013_УплатаАванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2020_УплатаГод");
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2021_УплатаАванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2022_УплатаГод");
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2022_ЕНП_УведомлениеАванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2022_ЕНП_УведомлениеНалог");
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2023_УплатаАванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ТранспортныйНалог.2023_ЕНП_УведомлениеАванс");
	
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2013_Аванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2015_Аванс"); 
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2020_Налог_Год");
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2021_Аванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2022_Налог_Год");
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2022_ЕНП_УведомлениеАванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2022_ЕНП_УведомлениеНалог");
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2023_Аванс");
	ПравилаУплатыИмущественныхНалогов.Добавить("ЗемельныйНалог.2023_ЕНП_УведомлениеАванс");
	
	Возврат ПравилаУплатыИмущественныхНалогов.Найти(ПолноеИмяПравила) <> Неопределено;
	
КонецФункции

Функция ЭтоПравилоЕдиногоНалоговогоСчета(Правило) Экспорт
	
	Если ТипЗнч(Правило) <> Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		Возврат Ложь;
	Иначе
		Возврат Правило.Владелец.Код = "ЕдиныйНалоговыйСчет";
	КонецЕсли;
	
КонецФункции

Функция ПравилаЕдиногоНалоговогоПлатежа() Экспорт
	
	ПравилаЕдиногоНалоговогоПлатежа = Новый Массив;
	ПравилаЕдиногоНалоговогоПлатежа.Добавить(Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("ЕдиныйНалоговыйСчет", "2023_ЕНС_Уведомление"));
	ПравилаЕдиногоНалоговогоПлатежа.Добавить(Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("ЕдиныйНалоговыйСчет", "2023_ЕНС_Уплата"));
	
	Возврат ПравилаЕдиногоНалоговогоПлатежа;
	
КонецФункции

Функция ПравилаУплатыАУСН() Экспорт
	
	Правила = Новый Структура;
	Правила.Вставить("Задача", "АУСН");
	Правила.Вставить("Уплата", "2021_Уплата");
	
	Возврат Правила;
	
КонецФункции

Функция ПравилаПредоставленияОтчетаУплатыНДФЛПредпринимателя(Организация, Период = Неопределено) Экспорт
	
	Правила = Новый Структура;
	Правила.Вставить("Задача", "НДФЛ_Предприниматель");
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииПравилПредоставленияОтчетаУплатыНДФЛПредпринимателя(Правила, Организация, Период);
	
	Возврат Правила;
	
КонецФункции

Функция ИдентификаторПравилаСтраховыеВзносыИПСДоходов() Экспорт
	
	Возврат "2013_Уплата_СДоходов";
	
КонецФункции

Функция ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа) Экспорт
	
	Значения = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ЗаполнитьЗначенияЗаполненияПлатежногоДокумента(Значения, ОписаниеПлатежа);
	Возврат Значения;
	
КонецФункции

Функция ТребованияНеВыполняемыеБРО() Экспорт
	
	НевыполнимыеТребования = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код КАК ИдентификаторНалогаОтчета,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Код КАК ИдентификаторПравила,
	|	ПравилаПредставленияОтчетовУплатыНалогов.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.РеквизитДопУпорядочивания КАК ВладелецРеквизитДопУпорядочивания
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|ГДЕ
	|	НЕ ПравилаПредставленияОтчетовУплатыНалогов.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВладелецРеквизитДопУпорядочивания,
	|	РеквизитДопУпорядочивания";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИмяТребования = Выборка.ИдентификаторНалогаОтчета + "." + Выборка.ИдентификаторПравила;
		Если Не ИнтерфейсыВзаимодействияБРО.ТребованиеОбрабатывается(ИмяТребования) Тогда
			НевыполнимыеТребования.Добавить(ИмяТребования);
		КонецЕсли;
	КонецЦикла;
	
	Возврат НевыполнимыеТребования;
	
КонецФункции

Функция ПлатежиПоЗадачам(Организация, РабочаяДата, ГоризонтПланирования) Экспорт
	
	ЗадачиБухгалтера = ЗадачиДляКалендаря(Организация, РабочаяДата, ГоризонтПланирования);
	
	Платежи = Новый Структура;
	
	Платежи.Вставить("РегулярныеПлатежи", РегулярныеПлатежи(ЗадачиБухгалтера.РегулярныеПлатежи));
	Платежи.Вставить("УплатаНалогов",     НалоговыеПлатежи(ЗадачиБухгалтера.УплатаНалогов));
	Платежи.Вставить("Зарплата",          ЗарплатныеПлатежи(ЗадачиБухгалтера.Зарплата));
	
	Возврат Платежи;
	
КонецФункции

Функция СрокВыполненияЗадачи(Организация, Правило, ТекущийПериод) Экспорт
	
	ПериодичностьПравила = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "Периодичность");
	
	НачалоОтчетногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
		ПериодичностьПравила, ТекущийПериод);
	ОкончаниеОтчетногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
			ПериодичностьПравила, ТекущийПериод);
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Правило", Правило);
	Запрос.Параметры.Вставить("НачалоПериода", НачалоОтчетногоПериода);
	Запрос.Параметры.Вставить("ОкончаниеПериода", ОкончаниеОтчетногоПериода);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Правило = &Правило
	|	И ЗадачиБухгалтера.ПериодСобытия >= &НачалоПериода
	|	И ЗадачиБухгалтера.ПериодСобытия <= &ОкончаниеПериода";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Срок;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция НовыйОписаниеПлатежа() Экспорт
	
	ОписаниеПлатежа = Новый Структура;
	ОписаниеПлатежа.Вставить("Организация",                Справочники.Организации.ПустаяСсылка());
	ОписаниеПлатежа.Вставить("Налог",                      );
	ОписаниеПлатежа.Вставить("ВидНалоговогоОбязательства", );
	ОписаниеПлатежа.Вставить("СпособОплаты",               );
	ОписаниеПлатежа.Вставить("ИмяЗадачи",                  "");
	ОписаниеПлатежа.Вставить("Период",                     '00010101');
	ОписаниеПлатежа.Вставить("ПоказательПериода",          "");
	ОписаниеПлатежа.Вставить("Аванс",                      Ложь);
	ОписаниеПлатежа.Вставить("КБК",                        "");
	ОписаниеПлатежа.Вставить("ОКАТО",                      "");
	ОписаниеПлатежа.Вставить("КПППлательщика",             "");
	ОписаниеПлатежа.Вставить("Сумма",                      0);
	ОписаниеПлатежа.Вставить("Описание",                   "");
	ОписаниеПлатежа.Вставить("Декларация",                 Документы.РегламентированныйОтчет.ПустаяСсылка());
	ОписаниеПлатежа.Вставить("ДатаДекларации",             '00010101');
	ОписаниеПлатежа.Вставить("КодНалоговогоОргана",        "");
	ОписаниеПлатежа.Вставить("ОплатаЗадолженности",        Ложь);
	ОписаниеПлатежа.Вставить("ОснованиеПлатежа",           "");

	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияПлатежа(ОписаниеПлатежа);
	
	Возврат ОписаниеПлатежа;
	
КонецФункции

// Конструктор структуры реквизитов оплаты ЕНС
// 
// Возвращаемое значение:
//  Структура:
//        * Организация - СправочникСсылка.Организации - ссылка на организацию
//        * Сумма - Число
//        * СпособУплатыНалога - ПеречислениеСсылка.СпособыУплатыНалогов
//        * ПериодСобытия - Дата
//
Функция НовыйСтруктураРеквизитовУплатыНалогаЕНС() Экспорт
	
	СтруктураРеквизитов = Новый Структура("Организация, Сумма, СпособУплатыНалога, ПериодСобытия");
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

Функция ЭтоПравилоТринадцатогоНДФЛ(ПолноеИмяПравила) Экспорт
	
	Возврат ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2023_НДФЛ_Уведомление_Декабрь" 
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_Декабрь";
	
КонецФункции

Функция ЭтоПравилоВыплатыЗарплатыАванса(ПолноеИмяПравила) Экспорт
	
	Возврат ПолноеИмяПравила = "Зарплата.Зарплата" 
		Или ПолноеИмяПравила = "Зарплата.Аванс";
	
КонецФункции

Функция ЭтоПравилоНДФЛКонецМесяца(ПолноеИмяПравила) Экспорт
	
	Возврат ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2024_НДФЛ_Уведомление_КонецМесяца" 
		Или ПолноеИмяПравила = "НДФЛ_Агент.2024_НДФЛ_Уплата_КонецМесяца";
	
КонецФункции

Функция ЭтоПравилоУплатыНДСИзЕАЭС(ПолноеИмяПравила) Экспорт
	
	Возврат ПолноеИмяПравила = "КосвенныеНалогиТамСоюз.2010_Налог";
	
КонецФункции

Функция ЭтоЗадачаУплатыНДСИзЕАЭС(ИдентификаторЗадачи) Экспорт
	
	Возврат ИдентификаторЗадачи = "КосвенныеНалогиТамСоюз";
	
КонецФункции

Функция КодыПравилИсключенийВыполняемыхЕдинымПомощником() Экспорт
	
	ПравилаИсключения = Новый Массив;
	ПравилаИсключения.Добавить("2024_НДФЛ_Уведомление_КонецМесяца");
	ПравилаИсключения.Добавить("2024_НДФЛ_Уплата_КонецМесяца");
	ПравилаИсключения.Добавить("2010_Налог");
	
	Возврат ПравилаИсключения;
	
КонецФункции

Функция КодыПравилИсключенийИзЕНП() Экспорт
	
	ПравилаИсключения = Новый Массив;
	ПравилаИсключения.Добавить("2013_Уплата_Допвзносы_Накопительная_ПФР");
	ПравилаИсключения.Добавить("2025_УплатаСтраховыеВзносы_ЛетныеЭкипажиИШахтеры");
	ПравилаИсключения.Добавить("2023_Уплата_Травматизм");
	
	Возврат ПравилаИсключения;
	
КонецФункции

Функция ЭтоСтраховыеВзносыВнеЕНП(Правило) Экспорт
	
	ЭтоСтраховыеВзносыВнеЕНП = Ложь;
	
	ИмяЗадачи = Правило.Владелец.Код;
	Если ИмяЗадачи = "СтраховыеВзносы" Тогда
		КодыПравилИсключенийИзЕНП = КодыПравилИсключенийИзЕНП();
		Если КодыПравилИсключенийИзЕНП.Найти(Правило.Код) <> Неопределено Тогда
			ЭтоСтраховыеВзносыВнеЕНП = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоСтраховыеВзносыВнеЕНП;
	
КонецФункции

Функция ЭтоПравилоУведомлениеПоНДФЛ(Правило) Экспорт
	
	ЭтоПравилоУведомлениеПоНДФЛ = Ложь;
	
	ИдентификаторыПравилаИЗадачи = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ИдентификаторыПравилаИЗадачи(Правило);
	Если ИдентификаторыПравилаИЗадачи <> Неопределено Тогда
		ЭтоПравилоУведомлениеПоНДФЛ = ИдентификаторыПравилаИЗадачи.ИдентификаторЗадачи = "НДФЛ_Агент_Уведомление";
	КонецЕсли;

	Возврат ЭтоПравилоУведомлениеПоНДФЛ;
	
КонецФункции

#Область Статусы

Процедура ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиОтчета(Документ, СтатусОтправки) Экспорт
	
	ПеренестиВАрхив = Ложь;
	
	Если СтатусОтправки = Перечисления.СтатусыОтправки.Сдан
		Или СтатусОтправки = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой() Тогда
		
		ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаПеренестиВАрхив(Документ, ПеренестиВАрхив);
	КонецЕсли;
	
	ТаблицаЗадач = ЗадачиСвязанныеСОтчетом(Документ);
	Если ТаблицаЗадач = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьСтатусыЗадач(ТаблицаЗадач);
	
	// Если отчет отправлен через 1С-Отчетность и ему присвоен статус "Сдан",
	// то автоматически меняем статус у задач, связанных с отчетом, на "Сдано".
	Если СтатусОтправки = Перечисления.СтатусыОтправки.Сдан Тогда
		Для Каждого Задача Из ТаблицаЗадач Цикл
			Задача.Статус = "Сдано";
		КонецЦикла;
	КонецЕсли;
	
	ЕстьПросроченныеЗадачи = Ложь;
	Если ЗначениеЗаполнено(Документ) И ОбщегоНазначения.ЕстьРеквизитОбъекта("Вид", Документ.Метаданные()) Тогда
		ВидОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Вид");
	Иначе
		ВидОтчета = 0;
	КонецЕсли;
	ТекущийДень = НачалоДня(ТекущаяДатаСеанса());
	Для Каждого Задача Из ТаблицаЗадач Цикл
		Если Задача.СтатусУстановленВручную Тогда
			Продолжить;
		КонецЕсли;
		ЗаписатьНовоеСостояниеЗадачи(Задача, ПеренестиВАрхив, Задача.Статус);
		Если ВидОтчета = 0 И НачалоДня(Задача.Срок) < ТекущийДень Тогда
			ЕстьПросроченныеЗадачи = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПросроченныеЗадачи Тогда
		ЗанестиСведенияОНарушенииСрокаПодачиРегламентированногоОтчета(Документ, ТекущийДень);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиПлатежа(ДокументСсылка, ПараметрыРасчетаСтатусов = Неопределено) Экспорт
	
	РеквизитыПлатежа = РеквизитыПлатежаПоДаннымДокумента(ДокументСсылка);
	
	Если РеквизитыПлатежа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСтатусЗадачиПодготовкиПлатежа(РеквизитыПлатежа, ПараметрыРасчетаСтатусов);
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеСтатусаЗадачиФинОтчетности(Документ, Статус) Экспорт
	
	ФинОтчетностьСдана = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ОпределитьСтатусОтправкиФинОтчетностиВБанкиСдано(ФинОтчетностьСдана, Статус);
	
	Если Не ФинОтчетностьСдана Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ,
		"Организация, Банк, ИдентификаторКомплекта, Дата");
	
	Правило = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПравилаФинОтчетности(РеквизитыДокумента, Правило);
	
	Если НЕ ЗначениеЗаполнено(Правило) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило", Правило);
	Запрос.УстановитьПараметр("ДатаДокумента", РеквизитыДокумента.Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус КАК Статус,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ФинОтчетность) КАК Действие,
	|	ЗадачиБухгалтера.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ЗадачиБухгалтера.Статус КАК РучнойСтатус,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Правило = &Правило
	|	И ЗадачиБухгалтера.ПериодСобытия = НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаДокумента, МЕСЯЦ, 1), МЕСЯЦ)";
	ТаблицаЗадач = Запрос.Выполнить().Выгрузить();
	
	Для каждого Задача Из ТаблицаЗадач Цикл
		Если Задача.СтатусУстановленВручную Тогда
			Продолжить;
		КонецЕсли;
		Задача.Статус = "Сдано";
		ЗаписатьНовоеСостояниеЗадачи(Задача, Ложь, Задача.Статус);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьСтатусЗадачиПодготовкиПлатежа(РеквизитыПлатежа, ПараметрыРасчетаСтатусов = Неопределено)
	
	ТаблицаЗадач = ЗадачиСвязанныеСПлатежом(РеквизитыПлатежа, ПараметрыРасчетаСтатусов);
	Если ТаблицаЗадач = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьСтатусыЗадач(ТаблицаЗадач);
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		Если Задача.СтатусУстановленВручную Тогда
			Продолжить;
		КонецЕсли;
		ЗаписатьНовоеСостояниеЗадачи(Задача, Ложь, Задача.Статус);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеСтатусаЗадачУплатыПатента(Организация, ОтборПатент) Экспорт

	ТаблицаЗадач = ЗадачиУплатыПатента(Организация, ОтборПатент);

	ДобавитьСтатусыЗадач(ТаблицаЗадач);

	Для Каждого Задача Из ТаблицаЗадач Цикл
		Если Задача.СтатусУстановленВручную Тогда
			Продолжить;
		КонецЕсли;
		ЗаписатьНовоеСостояниеЗадачи(Задача, Ложь, Задача.Статус);
	КонецЦикла;

КонецПроцедуры

Процедура ЗарегистрироватьИзменениеСтатусаЗадачиСозданияПатента(ПатентОбъект) Экспорт
	
	Правило = ПатентОбъект.Патент;
	
	Статус = "";
	Если ПатентОбъект.Проведен Тогда
		Статус = НСтр("ru = 'Выполнено'");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило", Правило);
	Запрос.УстановитьПараметр("ПериодСобытия", НачалоДня(ПатентОбъект.ДатаНачала));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.Статус КАК Статус,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ЗадачиБухгалтера.Статус КАК РучнойСтатус,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Правило = &Правило
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ПустаяСсылка)";
	
	ТаблицаЗадач = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		Если Задача.СтатусУстановленВручную Тогда
			Продолжить;
		КонецЕсли;
		Задача.Статус = Статус;
		ЗаписатьНовоеСостояниеЗадачи(Задача, Ложь, Статус);
	КонецЦикла;
	
КонецПроцедуры

Функция ЗаписатьНовоеСостояниеЗадачи(Задача, ПеренестиВАрхив = Ложь, Статус = Неопределено,
		СтатусУстановленВручную = Неопределено, ПользовательУстановившийСтатус = Неопределено, Комментарий = Неопределено) Экспорт
	
	КлючиЗаписи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	
	ЗаполнитьЗначенияСвойств(КлючиЗаписи, Задача);
	
	КлючЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьКлючЗаписи(КлючиЗаписи);
	
	ПоляКлюча = Новый Массив;
	Для Каждого ОписаниеЭлементаКлюча Из КлючиЗаписи Цикл
		ПоляКлюча.Добавить(ОписаниеЭлементаКлюча.Значение);
	КонецЦикла;
	ПредставлениеЗадачи = СтрСоединить(ПоляКлюча, ";"); // Для записи в исключении
	
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
		
		МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючиЗаписи);
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			// Задача ещё не создана. Заполним параметрами переданной задачи
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Задача);
			МенеджерЗаписи.ДатаСоздания = ТекущаяДатаСеанса();
			МенеджерЗаписи.ХешЗадачи = РегистрыСведений.ЗадачиБухгалтера.ХешЗаписи(
				КлючиЗаписи,
				РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач())
		КонецЕсли;
		
		Если Статус <> Неопределено Тогда
			МенеджерЗаписи.Статус = Статус;
		КонецЕсли;
		
		Если СтатусУстановленВручную <> Неопределено Тогда
			МенеджерЗаписи.СтатусУстановленВручную = СтатусУстановленВручную;
		КонецЕсли;
		
		Если Комментарий <> Неопределено Тогда
			МенеджерЗаписи.Комментарий = Комментарий;
		КонецЕсли;
		
		Если МенеджерЗаписи.СтатусУстановленВручную Тогда
			МенеджерЗаписи.ПользовательУстановившийСтатус = ?(ПользовательУстановившийСтатус = Неопределено,
				ТекущийПользователь(),
				ПользовательУстановившийСтатус);
		КонецЕсли;
			
		Если ПеренестиВАрхив Тогда
			МенеджерЗаписи.ВАрхиве = Истина;
		КонецЕсли;
		
		МенеджерЗаписи.Записать();
		
		РазблокироватьДанныеДляРедактирования(КлючЗаписи);
		
	Исключение
		
		ШаблонОшибки = НСтр("ru = 'Не удалось записать изменение задачи.
							|Задача: %1
							|Подробное описание ошибки:
							|%2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ПредставлениеЗадачи, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ПредставлениеЗадачи, ТекстОшибки);
		
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции

Функция ЗадачиДляОтчета(Документ, СвойстваОтчета) Экспорт
	
	ИсточникОтчета = СвойстваОтчета.ИсточникОтчета;
	
	ВсеИдентификаторыТребований = СоответствияПравилИФормОтчетов();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
		ИдентификаторыПравилВыполняющихсяНеРеглОтчетами(), ВсеИдентификаторыТребований);
	
	ИдентификаторыТребованийПоОтчету = ВсеИдентификаторыТребований.Скопировать(
		Новый Структура("ФормаОтчета", ИсточникОтчета), "ИдентификаторПравила, ИдентификаторЗадачи");
	
	Если ИдентификаторыТребованийПоОтчету.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Организация = СвойстваОтчета.Организация;
	ПериодСобытия = СвойстваОтчета.ДатаОкончанияОП;
	
	ДатаСобытия   = НачалоДня(СвойстваОтчета.ДатаСоздания);
	Если СвойстваОтчета.ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		КодНалоговогоОргана = СвойстваОтчета.КодКонтролирующегоОргана;
	Иначе
		КодНалоговогоОргана = "";
	КонецЕсли;
	
	ОтборНалоговыеОрганы = Новый Массив;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииОтбораНалоговыеОрганы(ОтборНалоговыеОрганы, Организация, КодНалоговогоОргана);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторыТребованийПоОтчету", ИдентификаторыТребованийПоОтчету);
	Запрос.УстановитьПараметр("ВсеИдентификаторыТребований",      ВсеИдентификаторыТребований);
	Запрос.УстановитьПараметр("Организация",                      Организация);
	Запрос.УстановитьПараметр("ОтборНалоговыеОрганы",             ОтборНалоговыеОрганы);
	Запрос.УстановитьПараметр("ФормаОтчета",                      ИсточникОтчета);
	
	// Текст запроса может модифицироваться схемой запроса
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИдентификаторыТребований.ФормаОтчета КАК ФормаОтчета,
	|	ИдентификаторыТребований.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ИдентификаторыТребований.ИдентификаторЗадачи КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ ВсеИдентификаторыТребований
	|ИЗ
	|	&ВсеИдентификаторыТребований КАК ИдентификаторыТребований
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИдентификаторыТребований.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ИдентификаторыТребований.ИдентификаторЗадачи КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ ИдентификаторыТребований
	|ИЗ
	|	&ИдентификаторыТребованийПоОтчету КАК ИдентификаторыТребований
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИдентификаторыТребований.ИдентификаторЗадачи КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ ОтборПоЗадаче
	|ИЗ
	|	ИдентификаторыТребований КАК ИдентификаторыТребований
	|ГДЕ
	|	ИдентификаторыТребований.ИдентификаторПравила = """"
	|
	|СГРУППИРОВАТЬ ПО
	|	ИдентификаторыТребований.ИдентификаторЗадачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Правило
	|ПОМЕСТИТЬ ПравилаОбрабатываемыеДругимиОтчетами
	|ИЗ
	|	ВсеИдентификаторыТребований КАК ИдентификаторыТребований
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПоЗадаче КАК ОтборПоЗадаче
	|		ПО ИдентификаторыТребований.ИдентификаторЗадачи = ОтборПоЗадаче.ИдентификаторЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ИдентификаторыТребований.ИдентификаторЗадачи = ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код
	|			И ИдентификаторыТребований.ИдентификаторПравила = ПравилаПредставленияОтчетовУплатыНалогов.Код
	|ГДЕ
	|	ИдентификаторыТребований.ФормаОтчета <> &ФормаОтчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Правило
	|ПОМЕСТИТЬ Правила
	|ИЗ
	|	ИдентификаторыТребований КАК ИдентификаторыТребований
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ИдентификаторыТребований.ИдентификаторЗадачи = ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код
	|			И ИдентификаторыТребований.ИдентификаторПравила = ПравилаПредставленияОтчетовУплатыНалогов.Код
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|ИЗ
	|	ОтборПоЗадаче КАК ОтборПоЗадаче
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО ОтборПоЗадаче.ИдентификаторЗадачи = ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)
	|	И НЕ ПравилаПредставленияОтчетовУплатыНалогов.Ссылка В
	|				(ВЫБРАТЬ
	|					ПравилаОбрабатываемыеДругимиОтчетами.Правило
	|				ИЗ
	|					ПравилаОбрабатываемыеДругимиОтчетами)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус КАК Статус,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.РегистрацияВНалоговомОргане В(&ОтборНалоговыеОрганы)";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	Условия = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество()-1].Операторы[0].Отбор;
	
	УсловиеПравил = 
	"ЗадачиБухгалтера.Правило В
	|	(ВЫБРАТЬ
	|		Правила.Правило
	|	ИЗ
	|		Правила)";
	
	ЭтоДокументСЗВ_ТД = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ОпределитьЭтоДокументСЗВ_ТД(ЭтоДокументСЗВ_ТД, Документ);
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		
		Условия.Добавить(УсловиеПравил);
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		ВсеЗадачи = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ЭтоДокументСЗВ_ТД Тогда
		
		Запрос.УстановитьПараметр("Периодичность", Перечисления.Периодичность.День);
		Условия.Добавить(УсловиеПравил);
		Условия.Добавить("ЗадачиБухгалтера.Периодичность = Периодичность");
		
		ПериодыСобытий = ОбщегоНазначения.ВыгрузитьКолонку(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Документ, "Мероприятия.ДатаМероприятия").Выгрузить(),
			"ДатаМероприятия", Истина);
		Запрос.УстановитьПараметр("ПериодыСобытий", ПериодыСобытий);
		Условия.Добавить("ЗадачиБухгалтера.ПериодСобытия В (&ПериодыСобытий)");
		
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		Запрос.Текст = ТекстЗапроса;
		РезультатЗапроса = Запрос.Выполнить();
		ВсеЗадачи = РезультатЗапроса.Выгрузить(); // Задачи "День"
		Если ВсеЗадачи.Количество() < ПериодыСобытий.Количество() Тогда
			
			Условия.Удалить(Условия.Количество()-1);
			Запрос.УстановитьПараметр("Периодичность", Перечисления.Периодичность.Месяц);
			Запрос.УстановитьПараметр("ПериодСобытия", ПериодСобытия);
			
			Условия.Добавить("ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия ИЛИ НАЧАЛОПЕРИОДА(ЗадачиБухгалтера.Срок, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодСобытия, МЕСЯЦ)");
			
			ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
			Запрос.Текст = ТекстЗапроса;
			РезультатЗапроса = Запрос.Выполнить();
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатЗапроса.Выгрузить(), ВсеЗадачи);
			
		КонецЕсли;
	Иначе
		
		Условия.Добавить(УсловиеПравил);
		// Добавляем отбор по периоду в последний запрос пакета
		Условия.Добавить("ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия");
		Запрос.УстановитьПараметр("ПериодСобытия", ПериодСобытия);
		
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		Запрос.Текст = ТекстЗапроса;
		ВсеЗадачи = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	Возврат ВсеЗадачи;
	
КонецФункции

Функция ЗадачиДляЗаявленияОЗачетеНалога(Документ, СвойстваОтчета) Экспорт
	
	ЗаявлениеОЗачетеНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Основание");
	ДанныеЗадачиПоЗаявлению = Новый Структура("ПериодСобытия, Правило");
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииДанныхЗадачиПоЗаявлению(ДанныеЗадачиПоЗаявлению, ЗаявлениеОЗачетеНалога);

	ПериодСобытия = ДанныеЗадачиПоЗаявлению.ПериодСобытия;
	ОтборПоПравилу = ДанныеЗадачиПоЗаявлению.Правило; 
	
	Если Не ЗначениеЗаполнено(ОтборПоПравилу) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СвойстваОтчета.ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		КодНалоговогоОргана = СвойстваОтчета.КодКонтролирующегоОргана;
	Иначе
		КодНалоговогоОргана = "";
	КонецЕсли;
	
	Организация = СвойстваОтчета.Организация;
	
	ОтборНалоговыеОрганы = Новый Массив;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииОтбораНалоговыеОрганы(ОтборНалоговыеОрганы, Организация, КодНалоговогоОргана);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                      Организация);
	Запрос.УстановитьПараметр("ОтборНалоговыеОрганы",             ОтборНалоговыеОрганы);
	Запрос.УстановитьПараметр("ОтборПоПравилу", ОтборПоПравилу);
	Запрос.УстановитьПараметр("ПериодСобытия", ПериодСобытия);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус КАК Статус,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.РегистрацияВНалоговомОргане В(&ОтборНалоговыеОрганы)
	|	И ЗадачиБухгалтера.Правило = &ОтборПоПравилу
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ЗадачиДляУведомления(Документ, СвойстваОтчета) Экспорт
	
	УведомлениеОбИсчисленныхСуммахНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Основание");
	// При первой записи документа сначала записывается документ БРО с битой ссылкой на документ БП.
	// И эта функция вызывается через интерфейс БРО. Поэтому мы проверим, что ссылка существует, и если нет -
	// значит это первая запись документа. А статус будет установлен уже после записи документа из формы.
	Если Не ЗначениеЗаполнено(УведомлениеОбИсчисленныхСуммахНалога)
		Или Не ОбщегоНазначения.СсылкаСуществует(УведомлениеОбИсчисленныхСуммахНалога) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Организация = СвойстваОтчета.Организация;
	ВидыКодыНалогов = Новый ТаблицаЗначений;
	ВидыКодыНалогов.Колонки.Добавить("ВидНалога", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНалогов"));
	ВидыКодыНалогов.Колонки.Добавить("КодЗадачи", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	
	ЗадачиБухгалтераПереопределяемый.ЗаполнитьТаблицуСоответствияЗадачБухгалтераДляУведомлений(ВидыКодыНалогов);
	
	// Если уведомление содержит НДФЛ за период с 23 числа предыдущего месяца по 9 число текущего месяца,
	// то менять статус задач по НДФЛ не следует, т.к. это промежуточное уведомление.
	Если УведомлениеСодержитПромежуточныйНДФЛ(УведомлениеОбИсчисленныхСуммахНалога) Тогда
		Отбор = Новый Структура("ВидНалога", Перечисления.ВидыНалогов.НДФЛ);
		Для Каждого СтрокаНДФЛ Из ВидыКодыНалогов.НайтиСтроки(Отбор) Цикл
			ВидыКодыНалогов.Удалить(СтрокаНДФЛ);
		КонецЦикла;
	КонецЕсли;
	
	ПравилаИсключения = Новый Массив;
	// Для налоговых агентов по НДФЛ, удержанному за период с 23 по 31 декабря (код отчетного периода 34/04), 
	// установлен особый срок подачи уведомления об исчисленных суммах налогов. 
	// Срок предоставления уведомления - не позднее последнего рабочего дня года.
	// Таким образом, в декабре подается 2 уведомления по НДФЛ. 
	// Исключим лишнее правило для задач, которые не относятся к периоду из уведомления.
	Если УведомлениеСодержитНДФЛЗаТринадцатыйПериод(УведомлениеОбИсчисленныхСуммахНалога) Тогда
		ПравилаИсключения.Добавить(ПравилоУведомленияПоНДФЛ());
	Иначе
		ПравилаИсключения.Добавить(ПравилоУведомленияПоНДФЛТринадцатыйПериод());
	КонецЕсли;
	// Для налоговых агентов по НДФЛ, удержанному за период с 23 по последнее число месяца,
	// установлен особый срок подачи уведомления об исчисленных суммах налогов.
	// Обработаем эти правила особым образом.
	ПравилаИсключения.Добавить(ПравилоУведомленияПоНДФЛКонецМесяца());
	
	ЗадачиДляУведомления = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииЗадачДляУведомления(ЗадачиДляУведомления,
		Организация, ВидыКодыНалогов, УведомлениеОбИсчисленныхСуммахНалога, ПравилаИсключения);
	
	Возврат ЗадачиДляУведомления;
	
КонецФункции

Функция ЗадачиДляЗаявленияНаПолучениеПатента(Документ, СвойстваОтчета) Экспорт
	
	ЗадачиДляЗаявленияНаПолучениеПатента = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииЗадачДляЗаявленияНаПолучениеПатента(ЗадачиДляЗаявленияНаПолучениеПатента,
		Документ, СвойстваОтчета);
	
	Возврат ЗадачиДляЗаявленияНаПолучениеПатента;
	
КонецФункции

// Возвращает таблицу с документами, которыми выполняются задачи бухгалтера по подготовке уведомлений.
//  Параметры:
//   ТаблицаЗадач - ТаблицаЗначений:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * Периодичность               - ПеречислениеСсылка.Периодичность
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   
//  ТолькоАктуальныеФормыОтчетов   - Булево - Если Истина, отбирать отчеты только по форме, действующей в отчетном периоде.
//
//  Возвращаемое значение:
//   ТаблицаЗначений
//   * Индекс                      - Число
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * Документы - ТаблицаЗначений:
//     * Документ                 - ДокументСсылка.РегламентированныйОтчет
//     * Дата                     - Дата документа
//     * Статус                   - Строка
//     * СостояниеСдачиОтчетности - ПеречислениеСсылка.СостояниеСдачиОтчетности
//
Функция ВыполнениеЗадачПоПодготовкеУведомлений(ТаблицаЗадач, ТолькоАктуальныеФормыОтчетов = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	Запрос.УстановитьПараметр("СоответствияПравилИФормОтчетов", СоответствияПравилИФормОтчетов());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствияПравилИФормОтчетов.ФормаОтчета КАК ФормаОтчета,
	|	СоответствияПравилИФормОтчетов.ИдентификаторПравила КАК ИдентификаторПравила,
	|	СоответствияПравилИФормОтчетов.ИдентификаторЗадачи КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ СоответствияПравилИФормОтчетов
	|ИЗ
	|	&СоответствияПравилИФормОтчетов КАК СоответствияПравилИФормОтчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	ВЫРАЗИТЬ(Задачи.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов) КАК Правило,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус
	|ПОМЕСТИТЬ ЗадачиБезИдентификаторов
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.Правило.Код КАК ИдентификаторПравила,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус,
	|	ЗадачиБухгалтера.Код КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ ПараметрыЗадач
	|ИЗ
	|	ЗадачиБезИдентификаторов КАК Задачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО Задачи.Правило.Владелец = ЗадачиБухгалтера.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПоискПоПолномуИдентификатору.ФормаОтчета, ПоискПоИдентификаторуЗадачи.ФормаОтчета) КАК ИсточникОтчета,
	|	ПараметрыЗадач.Организация КАК Организация,
	|	ПараметрыЗадач.ПериодСобытия КАК ПериодСобытия,
	|	ПараметрыЗадач.Правило КАК Правило,
	|	ПараметрыЗадач.Периодичность КАК Периодичность,
	|	ПараметрыЗадач.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПараметрыЗадач.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	ПараметрыЗадач.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ПараметрыЗадач.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ПараметрыЗадач.РучнойСтатус КАК РучнойСтатус
	|ПОМЕСТИТЬ Задачи
	|ИЗ
	|	ПараметрыЗадач КАК ПараметрыЗадач
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствияПравилИФормОтчетов КАК ПоискПоПолномуИдентификатору
	|		ПО ПараметрыЗадач.ИдентификаторЗадачи = ПоискПоПолномуИдентификатору.ИдентификаторЗадачи
	|			И ПараметрыЗадач.ИдентификаторПравила = ПоискПоПолномуИдентификатору.ИдентификаторПравила
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствияПравилИФормОтчетов КАК ПоискПоИдентификаторуЗадачи
	|		ПО ПараметрыЗадач.ИдентификаторЗадачи = ПоискПоИдентификаторуЗадачи.ИдентификаторЗадачи
	|			И (ПоискПоИдентификаторуЗадачи.ИдентификаторПравила = """")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задачи.ИсточникОтчета КАК ИсточникОтчета,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ДатаОкончания,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	Задачи.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ИСТИНА КАК ОбрабатываетсяБРО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление) КАК Действие,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус
	|ИЗ
	|	Задачи КАК Задачи
	|ГДЕ
	|	НЕ Задачи.ИсточникОтчета ЕСТЬ NULL";
	
	ВыполнениеЗадач = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Задача Из ВыполнениеЗадач Цикл
		Задача.ДатаОкончания = РелевантныйПериодСобытияДляОтчета(Задача);
	КонецЦикла;
	
	ЗаполнитьСсылкиНаРеглОтчеты(ВыполнениеЗадач);
	
	Возврат ВыполнениеЗадач;
	
КонецФункции

// Процедура получает ссылки на сохраненные регламентированные отчеты заданного вида.
// В переданную таблицу ПараметрыРеглОтчетов добавляется колонка Документы с колонками:
//  * Ссылка                   - ДокументСсылка.РегламетированныйОтчет - Ссылка на отчет.
//  * Дата                     - Дата - Дата подписи регламентированного отчета.
//  * Статус                   - Строка - Описание статуса сдачи отчета.
//  * СостояниеСдачиОтчетности - ПеречислениеСсылка.СостояниеСдачиОтчетности - Статус отправки через 1С-Отчетность.
//  * НаименованиеОтчета       - Строка - Пользовательское представление отчета.
//  * ФинансовыйПериод         - Строка - Пользовательское представление периода отчета.
//  * ВариантОтчета            - Строка - Описание варианта отчета (П - первичный, К/N - корректировочный).
//  * ПредставлениеКонтролирующегоОргана - Строка - Название контролирующего органа, в который предоставляется отчет.
//  * Комментарий                        - Строка - Пользовательский комментарий к отчету.
//
// Ссылки на регл. отчеты отсортированы в хронологическом порядке по убыванию.
//
//  Параметры:
//   * ПараметрыРеглОтчетов - ТаблицаЗначений - Определяет параметры отбора отчетов:
//   	** ИсточникОтчета      - Строка - Наименование регламентированного отчета (совпадает с именем объекта метаданных).
//   	** Организация         - СправочникСсылка.Организации - Организация отчета.
//   	** ДатаОкончания       - Дата (без времени) - Дата окончания отчетного периода (правая граница отчетного периода).
//   	** РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - Налоговый орган.
//   	** Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - Может не заполняться, но должна присутствовать в таблице.
//
Процедура ЗаполнитьСсылкиНаРеглОтчеты(ПараметрыРеглОтчетов) Экспорт
	
	// Индекс нужен для группировки нескольких документов по одному набору параметров.
	ПараметрыРеглОтчетов.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	ПараметрыРеглОтчетов.Колонки.Добавить("ВыбраннаяФорма", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	
	Для Каждого ПараметрыРеглОтчета Из ПараметрыРеглОтчетов Цикл
		ПараметрыРеглОтчета.Индекс = ПараметрыРеглОтчетов.Индекс(ПараметрыРеглОтчета);
	КонецЦикла;
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ЗаполнитьСсылкиНаРеглОтчеты(ПараметрыРеглОтчетов);
	
КонецПроцедуры

// Возвращает описание правил с указанием исполнителей, задачи по которым не обрабатываются интерфейсом БРО.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - см. НоваяТаблицаСоответствияПравилИФормОтчетов()
//
Функция ИдентификаторыПравилВыполняющихсяНеРеглОтчетами() Экспорт
	
	Идентификаторы = НоваяТаблицаСоответствияПравилИФормОтчетов();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИдентификаторыПравилСведенияОЗастрахованныхЛицах(), Идентификаторы);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИдентификаторыПравилСправкиНДФЛ(), Идентификаторы);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИдентификаторыПравилСведенияОТрудовыхКнижках(), Идентификаторы);
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииИдентификаторовПравилВыполняющихсяНеРеглОтчетами(Идентификаторы);
	
	Возврат Идентификаторы;
	
КонецФункции

// Возвращает таблицу с документами, которыми выполняются задачи бухгалтера по подготовке отчетов.
//  Параметры:
//   ТаблицаЗадач - ТаблицаЗначений:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * Периодичность               - ПеречислениеСсылка.Периодичность
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   
//  ТолькоАктуальныеФормыОтчетов   - Булево - Если Истина, отбирать отчеты только по форме, действующей в отчетном периоде.
//
//  Возвращаемое значение:
//   ТаблицаЗначений
//   * Индекс                      - Число
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * Документы - ТаблицаЗначений:
//     * Документ                 - ДокументСсылка.РегламентированныйОтчет
//     * Дата                     - Дата документа
//     * Статус                   - Строка
//     * СостояниеСдачиОтчетности - ПеречислениеСсылка.СостояниеСдачиОтчетности
//
Функция ВыполнениеЗадачПоПодготовкеОтчетов(ТаблицаЗадач, ТолькоАктуальныеФормыОтчетов = Ложь) Экспорт
	
	Если ТаблицаЗадач.Колонки.Найти("СтатусУстановленВручную") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("СтатусУстановленВручную", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Если ТаблицаЗадач.Колонки.Найти("РучнойСтатус") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("РучнойСтатус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	Если ТаблицаЗадач.Колонки.Найти("ВАрхиве") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("ВАрхиве", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	Запрос.УстановитьПараметр("СоответствияПравилИФормОтчетов", СоответствияПравилИФормОтчетов());
	Запрос.УстановитьПараметр("ИдентификаторыПравилВыполняющихсяНеРеглОтчетами",
		ИдентификаторыПравилВыполняющихсяНеРеглОтчетами());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствияПравилИФормОтчетов.ФормаОтчета КАК ФормаОтчета,
	|	СоответствияПравилИФормОтчетов.ИдентификаторПравила КАК ИдентификаторПравила,
	|	СоответствияПравилИФормОтчетов.ИдентификаторЗадачи КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ СоответствияПравилИФормОтчетов
	|ИЗ
	|	&СоответствияПравилИФормОтчетов КАК СоответствияПравилИФормОтчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИдентификаторыПравил.ФормаОтчета КАК ФормаОтчета,
	|	ИдентификаторыПравил.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ИдентификаторыПравил.ИдентификаторЗадачи КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ ИдентификаторыПравилНеОбрабатываемыхБРО
	|ИЗ
	|	&ИдентификаторыПравилВыполняющихсяНеРеглОтчетами КАК ИдентификаторыПравил
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	ВЫРАЗИТЬ(Задачи.Правило КАК Справочник.ПравилаПредставленияОтчетовУплатыНалогов) КАК Правило,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус,
	|	Задачи.ВАрхиве КАК ВАрхиве
	|ПОМЕСТИТЬ ЗадачиБезИдентификаторов
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус,
	|	Задачи.ВАрхиве КАК ВАрхиве,
	|	Задачи.Правило.Код КАК ИдентификаторПравила,
	|	ЗадачиБухгалтера.Код КАК ИдентификаторЗадачи
	|ПОМЕСТИТЬ ПараметрыЗадач
	|ИЗ
	|	ЗадачиБезИдентификаторов КАК Задачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО Задачи.Правило.Владелец = ЗадачиБухгалтера.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПоискПоПолномуИдентификатору.ФормаОтчета, ПоискПоИдентификаторуЗадачи.ФормаОтчета) КАК ИсточникОтчета,
	|	ПараметрыЗадач.Организация КАК Организация,
	|	ПараметрыЗадач.ПериодСобытия КАК ПериодСобытия,
	|	ПараметрыЗадач.Правило КАК Правило,
	|	ПараметрыЗадач.Периодичность КАК Периодичность,
	|	ПараметрыЗадач.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПараметрыЗадач.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	ПараметрыЗадач.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ПараметрыЗадач.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ПараметрыЗадач.РучнойСтатус КАК РучнойСтатус,
	|	ПараметрыЗадач.ВАрхиве КАК ВАрхиве
	|ПОМЕСТИТЬ Задачи
	|ИЗ
	|	ПараметрыЗадач КАК ПараметрыЗадач
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствияПравилИФормОтчетов КАК ПоискПоПолномуИдентификатору
	|		ПО ПараметрыЗадач.ИдентификаторЗадачи = ПоискПоПолномуИдентификатору.ИдентификаторЗадачи
	|			И ПараметрыЗадач.ИдентификаторПравила = ПоискПоПолномуИдентификатору.ИдентификаторПравила
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствияПравилИФормОтчетов КАК ПоискПоИдентификаторуЗадачи
	|		ПО ПараметрыЗадач.ИдентификаторЗадачи = ПоискПоИдентификаторуЗадачи.ИдентификаторЗадачи
	|			И (ПоискПоИдентификаторуЗадачи.ИдентификаторПравила = """")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задачи.ИсточникОтчета КАК ИсточникОтчета,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ДатаОкончания,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	Задачи.ИдентификаторПравила КАК ИдентификаторПравила,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус,
	|	Задачи.ВАрхиве КАК ВАрхиве,
	|	ИСТИНА КАК ОбрабатываетсяБРО
	|ИЗ
	|	Задачи КАК Задачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИдентификаторыПравилНеОбрабатываемыхБРО КАК ПравилаИсключения
	|		ПО Задачи.ИдентификаторЗадачи = ПравилаИсключения.ИдентификаторЗадачи
	|			И Задачи.ИдентификаторПравила = ПравилаИсключения.ИдентификаторПравила
	|ГДЕ
	|	НЕ Задачи.ИсточникОтчета ЕСТЬ NULL
	|	И ПравилаИсключения.ФормаОтчета ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаИсключения.ФормаОтчета КАК ИсточникОтчета,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ДатаОкончания,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	Задачи.ИдентификаторПравила КАК ИдентификаторПравила,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус,
	|	Задачи.ВАрхиве КАК ВАрхиве,
	|	ЛОЖЬ КАК ОбрабатываетсяБРО
	|ИЗ
	|	Задачи КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИдентификаторыПравилНеОбрабатываемыхБРО КАК ПравилаИсключения
	|		ПО Задачи.ИдентификаторЗадачи = ПравилаИсключения.ИдентификаторЗадачи
	|			И Задачи.ИдентификаторПравила = ПравилаИсключения.ИдентификаторПравила";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// Задачи, обслуживаемые БРО, обрабатываем отдельно от остальных
	ВыполнениеЗадач = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выгрузить();
	
	Для Каждого Задача Из ВыполнениеЗадач Цикл
		Задача.ДатаОкончания = РелевантныйПериодСобытияДляОтчета(Задача);
	КонецЦикла;
	
	ИнтерфейсыВзаимодействияБРО.ЗаполнитьСсылкиНаРеглОтчеты(ВыполнениеЗадач, ТолькоАктуальныеФормыОтчетов);
	
	// Обработаем задачи, не обслуживаемые БРО
	ЗадачиНеОбрабатываемыеБРО = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить();
	ЗаполнитьВыполнениеЗадачНеОбрабатываемыхБРО(ЗадачиНеОбрабатываемыеБРО);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ЗадачиНеОбрабатываемыеБРО, ВыполнениеЗадач);
	
	Возврат ВыполнениеЗадач;
	
КонецФункции

Функция НоваяТаблицаЗадачСвязанныхСПлатежами() Экспорт
	
	ТаблицаЗадач = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач();
	
	Возврат ТаблицаЗадач;
	
КонецФункции

// Возвращает таблицу задач по оплате уставного капитала
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, задолженность перед которой необходимо получить.
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * измерения регистра сведений ЗадачиБухгалтера - см. РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач
//   * Статус - см. РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса
//
Функция ЗадачиПоОплатеУставногоКапитала(Организация) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПравилаОплатаУставногоКапитала
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код = &ИдентификаторЗадачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус КАК Статус,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ЗадачиБухгалтера.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ЗадачиБухгалтера.Статус КАК РучнойСтатус,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Правило В
	|			(ВЫБРАТЬ
	|				ПравилаОплатаУставногоКапитала.Ссылка
	|			ИЗ
	|				ПравилаОплатаУставногоКапитала КАК ПравилаОплатаУставногоКапитала)";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ИдентификаторЗадачи", "ОплатаУставногоКапитала");
	
	ТаблицаЗадач = НовыеПараметрыЗадачБухгалтера();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НоваяЗадача = ТаблицаЗадач.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗадача, Выборка);
	КонецЦикла;
	
	Возврат ТаблицаЗадач;
	
КонецФункции

// Записывает новый статус по задачам оплаты уставного капитала
//
// Параметры:
//  ТаблицаЗадач - см. ВыполнениеЗадачБухгалтера.ЗадачиПоОплатеУставногоКапитала
//  НовыйСтатус - Строка - статус, который будет записан по задачам
//
Процедура ОбновитьСтатусОплатыУставногоКапитала(ТаблицаЗадач, НовыйСтатус) Экспорт 
	
	Если НовыйСтатус = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		Если Задача.СтатусУстановленВручную Тогда
			Продолжить;
		КонецЕсли;
		Если Задача.Статус <> НовыйСтатус Тогда
			Задача.Статус = НовыйСтатус;
			ЗаписатьНовоеСостояниеЗадачи(Задача, Ложь, Задача.Статус);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция УведомлениеСодержитПромежуточныйНДФЛ(УведомлениеОбИсчисленныхСуммахНалога) Экспорт
	
	Содержит = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаУведомлениеСодержитПромежуточныйНДФЛ(Содержит, УведомлениеОбИсчисленныхСуммахНалога);
	Возврат Содержит;
	
КонецФункции

// Для налоговых агентов по НДФЛ, удержанному за период с 23 по 31 декабря (код отчетного периода 34/04), 
// установлен особый срок подачи уведомления об исчисленных суммах налогов. 
// Срок предоставления уведомления - не позднее последнего рабочего дня года.
// Таким образом, в декабре подается 2 уведомления по НДФЛ.
//
// Функция возвращает признак того, что уведомление содержит НДФЛ за указанный период с 23 по 31 декабря
//
// Параметры:
//  УведомлениеОбИсчисленныхСуммахНалога - ДокументСсылка.УведомлениеОбИсчисленныхСуммахНалогов
//
// Возвращаемое значение:
//  Булево
//
Функция УведомлениеСодержитНДФЛЗаТринадцатыйПериод(УведомлениеОбИсчисленныхСуммахНалога) Экспорт
	
	Содержит = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаУведомлениеСодержитНДФЛЗаТринадцатыйПериод(Содержит, УведомлениеОбИсчисленныхСуммахНалога);
	Возврат Содержит;
	
КонецФункции

// Возвращает идентификаторы правил, задачи по которым выполняются документами СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - см. НоваяТаблицаСоответствияПравилИФормОтчетов()
//
Функция ИдентификаторыПравилСведенияОТрудовыхКнижках() Экспорт
	
	Идентификаторы = НоваяТаблицаСоответствияПравилИФормОтчетов();
		
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2020_СведенияЭТКЕжемесячные";
	НоваяСтрока.ИмяИсточника = "СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2020_СведенияЭТКЕжедневные";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2023_СведенияЭТКЕжемесячные";
	НоваяСтрока.ИмяИсточника = "СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2023_СведенияЭТКЕжедневные";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	НоваяСтрока.ИдентификаторЗадачи = "ТрудовыеКнижки";
	НоваяСтрока.ИдентификаторПравила = "2025_СведенияЭТКЕжемесячные";
	НоваяСтрока.ИмяИсточника = "СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД";
	НоваяСтрока.ИдентификаторЗадачи = "ТрудовыеКнижки";
	НоваяСтрока.ИдентификаторПравила = "2025_СведенияЭТКЕжедневные";
	
	Возврат Идентификаторы;
	
КонецФункции

// Для налоговых агентов по НДФЛ, удержанному за период с 23 по последний день месяца
// установлен особый срок подачи уведомления об исчисленных суммах налогов. 
// Срок предоставления уведомления - не позднее 5 чсла следующего месяца.
//
// Функция возвращает признак того, что уведомление содержит НДФЛ за указанный период с 23 числа по конц месяца.
//
// Параметры:
//  УведомлениеОбИсчисленныхСуммахНалога - ДокументСсылка.УведомлениеОбИсчисленныхСуммахНалогов
//
// Возвращаемое значение:
//  Булево
//
Функция УведомлениеСодержитНДФЛЗаКонецМесяца(УведомлениеОбИсчисленныхСуммахНалога, Период = '39991201') Экспорт
	
	Содержит = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаУведомлениеСодержитНДФЛЗаКонецМесяца(Содержит, УведомлениеОбИсчисленныхСуммахНалога, Период);
	Возврат Содержит;
	
КонецФункции

// Для налоговых агентов по НДФЛ, удержанному за период до 23 числа.
//
// Функция возвращает признак того, что уведомление содержит НДФЛ за указанный период по 23 число.
//
// Параметры:
//  УведомлениеОбИсчисленныхСуммахНалога - ДокументСсылка.УведомлениеОбИсчисленныхСуммахНалогов
//
// Возвращаемое значение:
//  Булево
//
Функция УведомлениеСодержитНДФЛЗаНачалоМесяца(УведомлениеОбИсчисленныхСуммахНалога, Период = '39991201') Экспорт
	
	Содержит = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаУведомлениеСодержитНДФЛЗаНачалоМесяца(Содержит, УведомлениеОбИсчисленныхСуммахНалога, Период);
	Возврат Содержит;
	
КонецФункции

// см. ДокументооборотСКОПереопределяемый.ЗарегистрироватьИзменениеСтатусаЗадачТребованиеФНС
Процедура ЗарегистрироватьИзменениеСтатусаЗадачТребованиеФНС(Ссылка) Экспорт
	
	ТаблицаЗадач = ЗадачаТребованиеФНС(Ссылка);
	
	ДобавитьСтатусыЗадач(ТаблицаЗадач);
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		ЗаписатьНовоеСостояниеЗадачи(Задача, Ложь, Задача.Статус);
	КонецЦикла;
	
КонецПроцедуры

// Обновляет статус по задаче с документом СЭДО
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ВходящийЗапросФССДляРасчетаПособия,
//                   ДокументСсылка.ИзвещениеФСС,
//                   ДокументСсылка.ОтказВВозмещенииВыплатРодителямДетейИнвалидов,
//                   ДокументСсылка.УведомлениеОСтатусеВыплатыПособия
//
Процедура ЗарегистрироватьИзменениеСтатусаЗадачиДокументСЭДО(ДокументСсылка) Экспорт
	
	ТаблицаЗадач = ЗадачаДокументСЭДО(ДокументСсылка);
	
	ДобавитьСтатусыЗадач(ТаблицаЗадач);
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		ЗаписатьНовоеСостояниеЗадачи(Задача, Ложь, Задача.Статус);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Возвращает таблицу с соответствием форм регл отчетов и идентификаторами задач и правил подготовки отчетов.
// Возвращаемое значение:
//  ТаблицаЗначений
//   ФормаОтчета          - Строка
//   ИдентификаторПравила - Строка
//   ИдентификаторЗадачи  - Строка
//
Функция СоответствияПравилИФормОтчетов() Экспорт
	
	СоответствияПравилИФормОтчетов = НоваяТаблицаСоответствияПравилИФормОтчетов();
	
	Для Каждого ОтчетПравило Из ИнтерфейсыВзаимодействияБРО.РеглОтчетыИсполнителиТребований() Цикл
		
		НоваяСтрока = СоответствияПравилИФормОтчетов.Добавить();
		НоваяСтрока.ФормаОтчета = ОтчетПравило.Значение;
		
		ЭлементыИмени = СтрРазделить(ОтчетПравило.Ключ, ".");
		ЭлементыИмени.Добавить(""); // На случай отсутствия имени правила в ключе.
		
		НоваяСтрока.ИдентификаторЗадачи  = ЭлементыИмени[0];
		НоваяСтрока.ИдентификаторПравила = ЭлементыИмени[1];
		
		НоваяСтрока.ИмяИсточника = НоваяСтрока.ФормаОтчета;
		
	КонецЦикла;
	
	Возврат СоответствияПравилИФормОтчетов;
	
КонецФункции

Функция ОперацияНачисленияВзносовВыполнена(Организация, ПериодСобытия, ДокументЗарплаты = Неопределено, НесколькоДокументов = Ложь, ИмяДокумента = Неопределено) Экспорт
	
	ОперацияВыполнена = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаОперацияНачисленияВзносовВыполнена(ОперацияВыполнена, 
		Организация, ПериодСобытия, ДокументЗарплаты, НесколькоДокументов, ИмяДокумента);
	Возврат ОперацияВыполнена;
	
КонецФункции

Функция ЕстьПлатежныйДокумент(Платежи) Экспорт
	
	Если Платежи.Колонки.Найти("ПлатежноеПоручение") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВсеПлатежныеДокументы = Платежи.Скопировать(,"ПлатежноеПоручение");
	ВсеПлатежныеДокументы.Свернуть("ПлатежноеПоручение");
	
	Возврат ВсеПлатежныеДокументы.Количество() > 1 
		Или ВсеПлатежныеДокументы.Количество() = 1 И ЗначениеЗаполнено(ВсеПлатежныеДокументы[0].ПлатежноеПоручение);
	
КонецФункции

Функция ТекущееПравилоЗадачи(ИдентификаторЗадачи, ПериодРегистрации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ПериодРегистрации);
	Запрос.УстановитьПараметр("ИдентификаторЗадачи", ИдентификаторЗадачи);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Владелец.Код = &ИдентификаторЗадачи
	|	И ВЫБОР
	|			КОГДА ГОД(ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия) = 1
	|					ИЛИ ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия >= &Период
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И НЕ ПравилаПредставленияОтчетовУплатыНалогов.ПометкаУдаления
	|	И (ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|			ИЛИ ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата))";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

// Подбирает наиболее подходящее правило выполнения отчета
//
// Параметры:
//  ПравилаПодготовкиОтчета       - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило отчета
//  ПравилоУплаты                 - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило отчета
//
// Возвращаемое значение:
//  СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило представления отчета 
//
Функция ПодобратьРелевантноеПравилоОтчета(ПравилаПодготовкиОтчета, ПравилоУплаты) Экспорт
	
	Если ПравилаПодготовкиОтчета.Количество() = 0 Тогда
		Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Если ПравилаПодготовкиОтчета.Количество() = 1 Тогда
		Возврат ПравилаПодготовкиОтчета[0];
	КонецЕсли;
	
	ПолноеИмяПравилаУплаты = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(ПравилоУплаты);
	ИсполнительПравилаУплаты = ИнтерфейсыВзаимодействияБРО.ИмяРеглОтчета(ПолноеИмяПравилаУплаты);
	
	УникальныеПравила = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ПравилаПодготовкиОтчета);
	Для Каждого ПравилоОтчета Из УникальныеПравила Цикл
		
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(ПравилоОтчета);
		ИсполнительПравила = ИнтерфейсыВзаимодействияБРО.ИмяРеглОтчета(ПолноеИмяПравила);
		
		Если ИсполнительПравила = ИсполнительПравилаУплаты Тогда
			Возврат ПравилоОтчета;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПравилаПодготовкиОтчета[0];
	
КонецФункции

// Возвращает код задачи по правилу представления отчетов, уплаты налогов
//
// Параметры:
//  Правило - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - правило, по которому вычисляется код задачи
//
// Возвращаемое значение:
//   Строка
//
Функция КодЗадачиПоПравилу(Правило) Экспорт
	
	Идентификаторы = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ИдентификаторыПравилаИЗадачи(Правило);
	
	Если Идентификаторы = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат Идентификаторы.ИдентификаторЗадачи;
	
КонецФункции

Функция ВариантыВыборкиСтраховыхВзносов() Экспорт
	
	ВариантыВыборкиСтраховыхВзносов = Новый Структура;
	ВариантыВыборкиСтраховыхВзносов.Вставить("СтраховыеВзносыТравматизм", 0);
	ВариантыВыборкиСтраховыхВзносов.Вставить("СтраховыеВзносы",           1);
	ВариантыВыборкиСтраховыхВзносов.Вставить("Травматизм",                2);
	ВариантыВыборкиСтраховыхВзносов.Вставить("ДобровольныеВзносы",        3);
	ВариантыВыборкиСтраховыхВзносов.Вставить("ДопВзносыЛетчикиШахтеры",   4);
	
	Возврат ВариантыВыборкиСтраховыхВзносов;
	
КонецФункции

Функция НовыеПараметрыЗадачБухгалтера() Экспорт
	
	ПараметрыЗадач = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач();
	
	ПараметрыЗадач.Колонки.Добавить("Срок",   ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ПараметрыЗадач.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	ПараметрыЗадач.Колонки.Добавить("СтатусУстановленВручную", Новый ОписаниеТипов("Булево"));
	ПараметрыЗадач.Колонки.Добавить("РучнойСтатус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	
	Возврат ПараметрыЗадач;
	
КонецФункции

Функция ПравилаОтчетности6НДФЛ() Экспорт
	
	МассивПравил6НДФЛ = Новый Массив;
	МассивПравил6НДФЛ.Добавить("НДФЛ_Агент.2023_6НДФЛ_Квартал");
	МассивПравил6НДФЛ.Добавить("НДФЛ_Агент.2023_6НДФЛ_Год");
	
	Возврат МассивПравил6НДФЛ;
	
КонецФункции

Функция ИдентификаторФормыПоПериодуСобытияОрганизации(Событие) Экспорт
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Событие, "Организация");
	ПериодСобытия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Событие, "ПериодСобытия");
	
	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(ПериодСобытия) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Не дублируем повторное открытие формы, если уже открыт помощник для конкретной организации и периода:
	// для того, чтобы можно было открыть конкретную форму или активировать уже открытую за конкретный период,
	// будем считать первые символы идентификатора организации номером месяца и годом платежа
	ИдентификаторОрганизации = Организация.УникальныйИдентификатор();
	
	СтрокаИдентификатора = СтрШаблон("%1%2",
		Формат(ПериодСобытия, "ДФ=MMyyyy;"),
		Прав(ИдентификаторОрганизации, СтрДлина(ИдентификаторОрганизации) - 6));
	
	Возврат Новый УникальныйИдентификатор(СтрокаИдентификатора);
	
КонецФункции

#Область РегистрацияПлатежей

Процедура УстановитьСвойстваПлатежаПриРегистрации(ДокументОбъект, Правило, ПериодСобытия, ПропуститьОбновлениеСтатуса = Ложь, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если НЕ ДокументВыполняетЗадачу(ДокументОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Правило) ИЛИ НЕ ЗначениеЗаполнено(ПериодСобытия) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПравилоУплатыНалогов",        Правило);
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПериодСобытияКалендаря",      ПериодСобытия);
	ДокументОбъект.ДополнительныеСвойства.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Если ПропуститьОбновлениеСтатуса Тогда
		ДокументОбъект.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеСтатусаЗадачи");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписьюПлатежа(ДокументОбъект) Экспорт
	
	Если Не ДокументВыполняетЗадачу(ДокументОбъект) Или ДокументОбъект.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ПравилоУплатыНалогов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДокументОбъект.ДополнительныеСвойства, "ПравилоУплатыНалогов");
	
	ПериодСобытияКалендаря = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДокументОбъект.ДополнительныеСвойства, "ПериодСобытияКалендаря");
	
	Если ЗначениеЗаполнено(ПравилоУплатыНалогов) И ЗначениеЗаполнено(ПериодСобытияКалендаря) Тогда
		// Правило и период задачи переданы из помощника, такой сценарий не требует читать реквизиты платежа до изменения.
		Возврат;
	КонецЕсли;
	
	РеквизитыПлатежа = РеквизитыПлатежаПоДаннымДокумента(ДокументОбъект.Ссылка);
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("РеквизитыПлатежаДоИзменения", РеквизитыПлатежа);
	
КонецПроцедуры

Процедура ПриЗаписиПлатежа(ДокументОбъект) Экспорт
	
	Если Не ДокументВыполняетЗадачу(ДокументОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПлатежа = РеквизитыПлатежаПоДаннымОбъекта(ДокументОбъект);
	РеквизитыПлатежаДоИзменения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДокументОбъект.ДополнительныеСвойства, "РеквизитыПлатежаДоИзменения");
	
	УстановитьДополнительныеСвойстваОплатыАУСН(ДокументОбъект);
	
	ПараметрыРасчетаСтатусов = Новый Структура;
	
	ЗарегистрироватьИсполнениеЗадачиПриЗаписиПлатежа(
		РеквизитыПлатежа, ДокументОбъект.ДополнительныеСвойства, ПараметрыРасчетаСтатусов);
		
	Если ЗарегистрированоИсполнениеЗадачи(РеквизитыПлатежа, ДокументОбъект.ДополнительныеСвойства)
		И Не ИзменилисьРеквизитыПлатежаВлияющиеНаСтатусЗадачи(РеквизитыПлатежаДоИзменения, РеквизитыПлатежа) Тогда
		ПараметрыРасчетаСтатусов.Вставить("ПропуститьОбновлениеСвязиДокументЗадача", Истина);
	КонецЕсли;
	
	Если ИзменятьСтатусыЗадачПриЗаписиПлатежа(РеквизитыПлатежа, ДокументОбъект.ДополнительныеСвойства) Тогда
		ОбновитьСтатусЗадачиПодготовкиПлатежа(РеквизитыПлатежа, ПараметрыРасчетаСтатусов);
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйРеквизитыПлатежаВлияющиеНаСтатусЗадачи() Экспорт
	
	ОписаниеРеквизитов = Новый Структура;
	
	ОписаниеРеквизитов.Вставить("Ссылка", Неопределено);
	ОписаниеРеквизитов.Вставить("ВидДокумента", "");
	ОписаниеРеквизитов.Вставить("ДокументОснование", Неопределено);
	ОписаниеРеквизитов.Вставить("Дата", '00010101');
	ОписаниеРеквизитов.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ОписаниеРеквизитов.Вставить("ВидОперации", Неопределено);
	ОписаниеРеквизитов.Вставить("Налог");
	ОписаниеРеквизитов.Вставить("ВидНалога", Перечисления.ВидыНалогов.ПустаяСсылка());
	ОписаниеРеквизитов.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ОписаниеРеквизитов.Вставить("КПППлательщика", "");
	ОписаниеРеквизитов.Вставить("ПоказательПериода", "");
	ОписаниеРеквизитов.Вставить("НалоговыйПериод", '00010101');
	ОписаниеРеквизитов.Вставить("ПлатежнаяВедомость", Неопределено);
	ОписаниеРеквизитов.Вставить("ПоказательОснования", "");
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииРеквизитовПлатежаВлияющихНаСтатусЗадачи(ОписаниеРеквизитов);
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

#КонецОбласти

#Область РегистрацияУведомлений

Процедура УстановитьСвойстваУведомленияПриРегистрации(ДокументОбъект, Правило, ПериодСобытия, ПропуститьОбновлениеСтатуса = Ложь, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	ВыполнениеЗадачБухгалтераПереопределяемый.УстановитьСвойстваУведомленияПриРегистрации(ДокументОбъект, Правило, ПериодСобытия, ПропуститьОбновлениеСтатуса, РегистрацияВНалоговомОргане);
	
КонецПроцедуры

Процедура ПриЗаписиУведомления(ДокументОбъект) Экспорт
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриЗаписиУведомления(ДокументОбъект);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеДействий

Функция ОписаниеДействияЗаявленияОВвозе(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияЗаявленияОВвозе(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;

КонецФункции 

Функция ОписаниеДействияОткрытьДокументыРасчетаВнешняяЗарплата(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияОткрытьДокументыРасчетаВнешняяЗарплата(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;

КонецФункции 

Функция ОписаниеДействияОткрытьОтражениеВУчетеВнешняяЗарплата(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияОткрытьОтражениеВУчетеВнешняяЗарплата(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;

КонецФункции

Функция ОписаниеДействияОтчетПФР(Событие)
	
	ОписаниеДействия = Новый Структура;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",    Событие.Организация);
	ПараметрыФормы.Вставить("ОтчетныйПериод", НачалоКвартала(Событие.ПериодСобытия));
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПодготовкаКвартальнойОтчетностиВПФР.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	Возврат ОписаниеДействия;
	
КонецФункции

Процедура ЗаполнитьДействиеУчетВоВнешнейПрограмме(ОписаниеДействия, ПредставлениеОтчета)

	УчетЗарплатыИКадровВЭтойПрограмме = Истина;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаУчетаЗарплатыИКадровВЭтойПрограмме(УчетЗарплатыИКадровВЭтойПрограмме);
	
	Если УчетЗарплатыИКадровВЭтойПрограмме Тогда
		Возврат;
	КонецЕсли;
		
	ТекстПредупреждения = НСтр("ru = 'В настройках параметров учета указано, что учет заработной платы ведется во внешней программе.
		|Подготовьте %1 в той программе, в которой начисляете зарплату'");
	
	ОписаниеДействия.Вставить(
		"Предупреждение",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, ПредставлениеОтчета));
	
КонецПроцедуры

Функция ОписаниеДействияСведенияПФРЕжемесячные(Событие)
	
	ОписаниеДействия = Новый Структура;
	
	ЗаполнитьДействиеУчетВоВнешнейПрограмме(ОписаниеДействия, НСтр("ru = 'сведения'"));
	
	Если ЗначениеЗаполнено(ОписаниеДействия) Тогда
		Возврат ОписаниеДействия;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация",    Событие.Организация);
	Отбор.Вставить("ОтчетныйПериод", НачалоМесяца(Событие.ПериодСобытия));
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.СведенияОЗастрахованныхЛицахСЗВ_М.ФормаСписка");
	ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияСведенияПФРЕжегодные(Событие)
	
	ОписаниеДействия = Новый Структура;
	
	ЗаполнитьДействиеУчетВоВнешнейПрограмме(ОписаниеДействия, НСтр("ru = 'сведения'"));
	
	Если ЗначениеЗаполнено(ОписаниеДействия) Тогда
		Возврат ОписаниеДействия;
	КонецЕсли;
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ФормаСписка");
	ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Новый Структура));
	
	ОписаниеДействия.ПараметрыФормы.Отбор.Вставить("Организация",    Событие.Организация);
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияРеестрДСВ3(Событие)
	
	ОписаниеДействия = Новый Структура;
	
	ЗаполнитьДействиеУчетВоВнешнейПрограмме(ОписаниеДействия, НСтр("ru = 'реестр ДСВ-3'"));
	
	Если ЗначениеЗаполнено(ОписаниеДействия) Тогда
		Возврат ОписаниеДействия;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Событие.Организация);
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.РеестрДСВ_3.ФормаСписка");
	ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияСведенияЭТКЕжемесячные(Событие)
	
	ОписаниеДействия = Новый Структура;
	ЗаполнитьДействиеУчетВоВнешнейПрограмме(ОписаниеДействия, НСтр("ru = 'сведения'"));
	
	Если ЗначениеЗаполнено(ОписаниеДействия) Тогда
		Возврат ОписаниеДействия;
	КонецЕсли;
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияСведенияЭТКЕжемесячные(Событие, ОписаниеДействия);
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействия2НДФЛ(Событие)
	
	ОписаниеДействия = Новый Структура;
	
	ЗаполнитьДействиеУчетВоВнешнейПрограмме(ОписаниеДействия, НСтр("ru = 'справки 2-НДФЛ'"));
	
	Если ЗначениеЗаполнено(ОписаниеДействия) Тогда
		Возврат ОписаниеДействия;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация",     Событие.Организация);
	Отбор.Вставить("НалоговыйПериод", Год(Событие.ПериодСобытия));
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.СправкиНДФЛДляПередачиВНалоговыйОрган.ФормаСписка");
	ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияФормированиеПлатежныхПорученийНаУплатуНалогов(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияФормированиеПлатежныхПорученийНаУплатуНалогов(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияРасчетСтраховыхВзносовИП(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияРасчетСтраховыхВзносовИП(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Аванс)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Аванс, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияСверкаНДС(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияСверкаНДС(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПлатежПоПатенту(Организация, ДокументПатент, Сумма, СпособОплаты, ПериодСобытия, Срок) Экспорт
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПлатежПоПатенту(Организация, ДокументПатент, Сумма, СпособОплаты, ПериодСобытия, Срок, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникРасчетаУСН(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникРасчетаУСН(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникОплатыНалогаПрошлыхПериодов(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникОплатыНалогаПрошлыхПериодов(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияЖурналСчетовФактур(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияЖурналСчетовФактур(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ЭтоОтчетПрошлогоПериода(Событие)
	
	ЭтоОтчетПрошлогоПериода = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаОтчетаПрошлогоПериода(ЭтоОтчетПрошлогоПериода, Событие);
	
	Возврат ЭтоОтчетПрошлогоПериода;
	
КонецФункции

Функция ОписаниеДействияРасчетЕНВД(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияРасчетЕНВД(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникЗаполнения3НДФЛ(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникЗаполнения3НДФЛ(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникУплатыАвансовыхПлатежейНДФЛ(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникУплатыАвансовыхПлатежейНДФЛ(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникЗаявленияНаПолучениеПатента(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникЗаявленияНаПолучениеПатента(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияУведомлениеУСН(Событие)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Событие.Организация);
	Отбор.Вставить("ВидУведомления",
		Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеОПереходеНаУСН);
	
	Уведомления = УведомлениеОСпецрежимахНалогообложения.НайтиУведомления(Отбор);
	Уведомления.Сортировать("Дата Убыв, Ссылка Убыв", Новый СравнениеЗначений);
	
	СсылкаНаУведомление = Неопределено;
	Для Индекс = 0 По Уведомления.Количество() - 1 Цикл
		
		СтрокаУведомление = Уведомления[Индекс];
		Если СтрокаУведомление.Дата >= НачалоГода(Событие.ПериодСобытия) 
			И СтрокаУведомление.Дата <= КонецМесяца(Событие.Срок) Тогда
			
			СсылкаНаУведомление = СтрокаУведомление.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СсылкаНаУведомление) Тогда
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
		ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
		ПараметрыФормы.Вставить("ПериодСобытия",               Событие.ПериодСобытия);
		ПараметрыФормы.Вставить("Правило",                     Событие.Правило);
		ПараметрыФормы.Вставить("Срок",                        Событие.Срок);
		ПараметрыФормы.Вставить("Описание",                    Событие.Наименование);
		ПараметрыФормы.Вставить("ЕстьИнформацияНаИТС",         Событие.ЕстьИнформацияНаИТС);
		ПараметрыФормы.Вставить("ИдентификаторЗадачи",         Событие.ИдентификаторЗадачи);
		ПараметрыФормы.Вставить("Ключ", СсылкаНаУведомление);
		
		ОписаниеДействия = Новый Структура;
		
		ОписаниеДействия.Вставить("ИмяФормы",       "Документ.УведомлениеОСпецрежимахНалогообложения.ФормаОбъекта");
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	Иначе
		
		Возврат ОписаниеДействияДекларация(
			Событие.Правило,
			Событие.ПериодСобытия,
			Событие.Организация,
			Событие.РегистрацияВНалоговомОргане);
		
	КонецЕсли;
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникУплатыНалога(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникУплатыНалога(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияСверкаИмущественныхНалогов(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияСверкаИмущественныхНалогов(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияВводОстатковПриСменеОбъектаУСН(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияВводОстатковПриСменеОбъектаУСН(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияУведомлениеОбИсчисленныхСуммахНалогов(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияУведомлениеОбИсчисленныхСуммахНалогов(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;

КонецФункции

Функция ОписаниеДействияЗаявлениеОЗачетеВзносовИП(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияЗаявлениеОЗачетеВзносовИП(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ПравилаВыполняемыеПомощникомРасчетаНалогаУСН()
	
	Правила = Новый Массив;
	Правила.Добавить("УСН.2013_ДекларацияОрганизация");
	Правила.Добавить("УСН.2013_УплатаОрганизация");
	Правила.Добавить("УСН.2013_ДекларацияПредприниматель");
	Правила.Добавить("УСН.2013_УплатаПредприниматель");
	Правила.Добавить("УСН.2013_УплатаАвансов");
	Правила.Добавить("УСН.2022_ЕНП_УведомлениеАванс");
	Правила.Добавить("УСН.2023_ДекларацияОрганизация");
	Правила.Добавить("УСН.2023_УплатаОрганизация");
	Правила.Добавить("УСН.2023_ДекларацияПредприниматель");
	Правила.Добавить("УСН.2023_УплатаПредприниматель");
	Правила.Добавить("УСН.2023_УплатаАвансов");
	Правила.Добавить("УСН.2023_ЕНП_УведомлениеАванс");
	Правила.Добавить("УСН.2023_ДекларацияПредприниматель_СнятиеСУчета");
	Правила.Добавить("УСН.2023_УплатаПредприниматель_СнятиеСУчета");
	Правила.Добавить("УСН.2023_УплатаОрганизация_ЕдиныйПомощник");
	Правила.Добавить("УСН.2023_УплатаПредприниматель_ЕдиныйПомощник");
	Правила.Добавить("УСН.2023_УплатаАвансов_ЕдиныйПомощник");
	Правила.Добавить("УСН.2023_УведомлениеАванс_ЕдиныйПомощник");
	
	Возврат Новый ФиксированныйМассив(Правила);
	
КонецФункции

Функция ОписаниеДействияПомощникРасчетаНДС(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникРасчетаНДС(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПомощникУплатыНПД(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПомощникУплатыНПД(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ПравилаВыполняемыеПомощникомРасчетаНДС()
	
	Правила = Новый Массив;
	
	Правила.Добавить("НДС.2013_Декларация");
	Правила.Добавить("НДС.2013_Уплата1");
	Правила.Добавить("НДС.2013_Уплата2");
	Правила.Добавить("НДС.2013_Уплата3");
	Правила.Добавить("НДС.2015_Декларация");
	Правила.Добавить("НДС.2015_Уплата1");
	Правила.Добавить("НДС.2015_Уплата2");
	Правила.Добавить("НДС.2015_Уплата3");
	Правила.Добавить("НДС.2022_ЕНП_Уведомление1");
	Правила.Добавить("НДС.2022_ЕНП_Уведомление2");
	Правила.Добавить("НДС.2022_ЕНП_Уведомление3");
	Правила.Добавить("НДС.2023_Уплата1");
	Правила.Добавить("НДС.2023_Уплата2");
	Правила.Добавить("НДС.2023_Уплата3");
	
	Возврат Новый ФиксированныйМассив(Правила);
	
КонецФункции

Функция ОписаниеДействияОплатаУставногоКапитала(Организация)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияОплатаУставногоКапитала(Организация, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ПравилоВыполняетсяПомощникомУплатыАУСН(Событие, Знач ПолноеИмяПравила = Неопределено)
	
	Правило = Событие.Правило;
	
	Если Не ЭтоУплатаАУСН(Правило, ПолноеИмяПравила) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выполняется = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПравилоВыполняетсяПомощникомУплатыАУСН(Событие, Выполняется);
	Возврат Выполняется;

КонецФункции

Функция ОписаниеДействияПомощникУплатыАУСН(Событие)
	
	ОписаниеДействия = Новый Структура;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",         Событие.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",       Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",             Событие.Правило);
	ПараметрыФормы.Вставить("КонтекстныйВызов",    Истина);
	ПараметрыФормы.Вставить("Срок",                Событие.Срок);
	ПараметрыФормы.Вставить("Описание",            Событие.Наименование);
	ПараметрыФормы.Вставить("ЕстьИнформацияНаИТС", Событие.ЕстьИнформацияНаИТС);
	ПараметрыФормы.Вставить("ИдентификаторЗадачи", Событие.ИдентификаторЗадачи);
	
	ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ПомощникУплатыНалогаАУСН.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ЭтоУплатаАУСН(Правило, Знач ПолноеИмяПравила = Неопределено)
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Если ПолноеИмяПравила = "АУСН.2021_Уплата" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПравилоНалогаУплачиваемогоОтдельно(
		Правило, ИмяЗадачи = Неопределено, ПолноеИмяПравила = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Если ПолноеИмяПравила = "СтраховыеВзносы.2023_Уплата_Травматизм"
		Или ПолноеИмяПравила = "СтраховыеВзносы.2025_УплатаСтраховыеВзносы_ЛетныеЭкипажиИШахтеры" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПравилоНалогаУплачиваемогоОтдельноПоРеквизитамЕНП(
		Правило, ИмяЗадачи = Неопределено, ПолноеИмяПравила = Неопределено) Экспорт
	
	Если ИмяЗадачи = Неопределено Тогда
		ИмяЗадачи = Правило.Владелец.Код;
	КонецЕсли;
	
	Если ПолноеИмяПравила = Неопределено Тогда
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	КонецЕсли;
	
	Если ПолноеИмяПравила = "СтраховыеВзносы.2025_УплатаСтраховыеВзносы_ЛетныеЭкипажиИШахтеры" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПравилаИсключенияЗадачиСтраховыеВзносы()
	
	МассивПравил = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПравил, ПравилаТравматизм());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПравил, ПравилаДопВзносы());
	
КонецФункции

Функция ПравилаТравматизм()
	
	МассивПравил = Новый Массив;
	МассивПравил.Добавить(Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору(
		ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносы(), "2023_Уплата_Травматизм"));
	
	Возврат МассивПравил;
	
КонецФункции

Функция ПравилаДопВзносы()
	
	МассивПравил = Новый Массив;
	МассивПравил.Добавить(Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору(
		ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносы(), "2025_УплатаСтраховыеВзносы_ЛетныеЭкипажиИШахтеры"));
	
	Возврат МассивПравил;
	
КонецФункции

Функция ОписаниеДействияНапоминаниеОПроверкеНалогаАУСН(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияНапоминаниеОПроверкеНалогаАУСН(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияУплатаНалогаАУСН(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияУплатаНалогаАУСН(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияУплатаФСС_НС_АУСН(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияУплатаФСС_НС_АУСН(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

Функция ОписаниеДействияПередачаСведенийОСотрудникахАУСН(Событие)
	
	ОписаниеДействия = Новый Структура;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияПередачаСведенийОСотрудникахАУСН(Событие, ОписаниеДействия);
	Возврат ОписаниеДействия;
	
КонецФункции

#КонецОбласти

#Область Статусы

Функция ЗадачиСвязанныеСПлатежом(РеквизитыПлатежа, ПараметрыРасчетаСтатусов)
	
	ТаблицаЗадач = НоваяТаблицаЗадачСвязанныхСПлатежами();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
		СвязанныеЗадачиВыплатыЗарплаты(РеквизитыПлатежа),
		ТаблицаЗадач);
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииТаблицыЗадачСвязанныхСПлатежами(ТаблицаЗадач, РеквизитыПлатежа, ПараметрыРасчетаСтатусов);
	
	Возврат ТаблицаЗадач;
	
КонецФункции

Функция СвязанныеЗадачиВыплатыЗарплаты(РеквизитыДокумента)
	
	ТаблицаЗадач = Новый ТаблицаЗначений;
	ПравилоВыплатыЗарплатыАванс = ПравилоВыплатыЗарплатыАванс();
	ВыполнениеЗадачБухгалтераПереопределяемый.ПолучитьСвязанныеЗадачиВыплатыЗарплаты(ТаблицаЗадач, РеквизитыДокумента, ПравилоВыплатыЗарплатыАванс);
	
	Возврат ТаблицаЗадач;
	
КонецФункции

Функция ЗадачиПродлениеПатента(ТаблицаЗадач)

	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	ЗадачиПродлениеПатента = ТаблицаЗадач.СкопироватьКолонки();
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииТаблицыЗадачПродленияПатента(ЗадачиПродлениеПатента, ТаблицаЗадач);
	Возврат ЗадачиПродлениеПатента;
	
КонецФункции

// Возвращает таблицу с параметрами задач по подготовке отчетов и статусами.
//
// Параметры:
//  ТаблицаЗадач - ТаблицаЗначений
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение: ТаблицаЗначений, копия переданной таблицы, дополненная колонкой Статус.
//  Колонки:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * Статус                      - Строка
//
Функция СтатусыЗадачПоПодготовкеОтчетов(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	КлючиЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет;
	
	ЗадачиСоСтатусами = ТаблицаЗадач.Скопировать();
	
	Если ЗадачиСоСтатусами.Колонки.Найти("Статус") = Неопределено Тогда
		ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	СостояниеОтчетностиСдано = Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота;
	СтатусБРОСдано = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой();
	
	МножественныеПравила = ИдентификаторПравилВыполняемыхНесколькимиОтчетами();
	
	Для Каждого ВыполнениеЗадачи Из ВыполнениеЗадачПоПодготовкеОтчетов(ЗадачиСоСтатусами) Цикл
		
		Статус = "";
		
		Если ВыполнениеЗадачи.СтатусУстановленВручную Тогда
			Статус = ВыполнениеЗадачи.РучнойСтатус;
		ИначеЕсли ВыполнениеЗадачи.Документы.Количество() > 0 Тогда
			Если МножественныеПравила.Найти(ВыполнениеЗадачи.ИдентификаторПравила) <> Неопределено Тогда
				// Все задачи должны быть выполнены, при выводе ориентируемся на пессимистичный вариант
				ТекущийСтатус = "";
				Для Каждого Документ ИЗ ВыполнениеЗадачи.Документы Цикл
					Если РегламентированнаяОтчетностьКлиентСервер.ОбъектНеОтправлялсяЧерез1СОтчетность(Документ.СостояниеСдачиОтчетности)
						Или РегламентированнаяОтчетностьВызовСервера.ОбъектРеглОтчетностиОтправлялсяБезИспользования1СОтчетности(Документ.Ссылка) Тогда
						ТекущийСтатус = Документ.Статус;
					Иначе
						ТекущийСтатус = РегламентированнаяОтчетность.СохраненныйСтатусОтправкиРеглОтчета(Документ.Ссылка);
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ТекущийСтатус) Тогда
						Если Документ.СостояниеСдачиОтчетности = СостояниеОтчетностиСдано Тогда
							ТекущийСтатус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой();
						Иначе
							ТекущийСтатус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусВРаботеСтрокой()
						КонецЕсли;
					КонецЕсли;
					
					Если ТекущийСтатус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусВРаботеСтрокой() Тогда
						// Нет смысла проверять дальше
						Статус = ТекущийСтатус;
						Прервать;
					ИначеЕсли (Статус <> ТекущийСтатус И ТекущийСтатус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусПодготовленоСтрокой())
						Или Статус <> ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусПодготовленоСтрокой() И ТекущийСтатус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой() Тогда
						// Перепроверим на случай наличия более пессимистичного статуса
						Статус = ТекущийСтатус;
					КонецЕсли;
				КонецЦикла;
			Иначе
				// Созданные отчеты отсортированы по убыванию даты подписи, определяем статус по самому последнему
				Если РегламентированнаяОтчетностьКлиентСервер.ОбъектНеОтправлялсяЧерез1СОтчетность(ВыполнениеЗадачи.Документы[0].СостояниеСдачиОтчетности)
					Или РегламентированнаяОтчетностьВызовСервера.ОбъектРеглОтчетностиОтправлялсяБезИспользования1СОтчетности(ВыполнениеЗадачи.Документы[0].Ссылка) Тогда
					Статус = ВыполнениеЗадачи.Документы[0].Статус;
				Иначе
					Статус = РегламентированнаяОтчетность.СохраненныйСтатусОтправкиРеглОтчета(ВыполнениеЗадачи.Документы[0].Ссылка)
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ВыполнениеЗадачи);
		
		Для Каждого Задача Из ЗадачиСоСтатусами.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ЗадачиСоСтатусами;
	
КонецФункции

// Возвращает таблицу с параметрами задач по уплате налогов и статусами.
//
// Параметры:
//  КлючиЗадач - ТаблицаЗначений
//   * Организация
//   * РегистрацияВНалоговомОргане
//   * ПериодСобытия
//   * Правило
//
// Возвращаемое значение:
//   ТаблицаЗначений - см. Обработки.ПомощникУплатыНалога.НовыеПараметрыЗадач()
//
Функция СтатусыЗадачПоУплатеНалогов(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаЗадач",      ТаблицаЗадач);
	Запрос.УстановитьПараметр("КодЗадачиВзносы",   ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносы());
	Запрос.УстановитьПараметр("ПравилаИсключения", ПравилаИсключенияЗадачиСтраховыеВзносы());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗадач.Организация КАК Организация,
	|	ТаблицаЗадач.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ТаблицаЗадач.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаЗадач.Правило КАК Правило,
	|	ТаблицаЗадач.Действие КАК Действие,
	|	ТаблицаЗадач.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ТаблицаЗадач.ПользовательУстановившийСтатус КАК ПользовательУстановившийСтатус,
	|	ТаблицаЗадач.Комментарий КАК Комментарий,
	|	ТаблицаЗадач.РучнойСтатус КАК РучнойСтатус
	|ПОМЕСТИТЬ ТаблицаЗадач
	|ИЗ
	|	&ТаблицаЗадач КАК ТаблицаЗадач
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗадач.Организация КАК Организация,
	|	ТаблицаЗадач.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ТаблицаЗадач.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаЗадач.Правило КАК Правило,
	|	ТаблицаЗадач.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ТаблицаЗадач.ПользовательУстановившийСтатус КАК ПользовательУстановившийСтатус,
	|	ТаблицаЗадач.Комментарий КАК Комментарий,
	|	ТаблицаЗадач.РучнойСтатус КАК РучнойСтатус,
	|	ЕСТЬNULL(Правила.Периодичность, ЗНАЧЕНИЕ(Перечисление.Периодичность.ПустаяСсылка)) КАК Периодичность,
	|	ЕСТЬNULL(Правила.Ссылка.Владелец.Код, """") КАК ИдентификаторЗадачи,
	|	ЕСТЬNULL(Правила.Ссылка.Код, """") КАК ИдентификаторПравила,
	|	ЕСТЬNULL(Правила.Действие, ТаблицаЗадач.Действие) КАК Действие
	|ПОМЕСТИТЬ ЗадачиСИдентификаторами
	|ИЗ
	|	ТаблицаЗадач КАК ТаблицаЗадач
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Правила
	|		ПО ТаблицаЗадач.Правило = Правила.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСИдентификаторами.Организация КАК Организация,
	|	ЗадачиСИдентификаторами.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиСИдентификаторами.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиСИдентификаторами.Правило КАК Правило,
	|	ЗадачиСИдентификаторами.Правило КАК ПравилоУплаты,
	|	ЕСТЬNULL(ЗадачиБухгалтера.Периодичность, ЗадачиСИдентификаторами.Периодичность) КАК Периодичность,
	|	ЕСТЬNULL(ЗадачиБухгалтера.Срок, ДАТАВРЕМЯ(1, 1, 1)) КАК Срок,
	|	ЗадачиСИдентификаторами.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	ЗадачиСИдентификаторами.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ЗадачиСИдентификаторами.ИдентификаторЗадачи + ""."" + ЗадачиСИдентификаторами.ИдентификаторПравила КАК ПолноеИмяПравила,
	|	ЕСТЬNULL(ЗадачиБухгалтера.Действие, ЗадачиСИдентификаторами.Действие) КАК Действие,
	|	ЗадачиСИдентификаторами.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ЗадачиСИдентификаторами.ПользовательУстановившийСтатус КАК ПользовательУстановившийСтатус,
	|	ЗадачиСИдентификаторами.Комментарий КАК Комментарий,
	|	ЗадачиСИдентификаторами.РучнойСтатус КАК РучнойСтатус
	|ИЗ
	|	ЗадачиСИдентификаторами КАК ЗадачиСИдентификаторами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО ЗадачиСИдентификаторами.Организация = ЗадачиБухгалтера.Организация
	|			И ЗадачиСИдентификаторами.РегистрацияВНалоговомОргане = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
	|			И ЗадачиСИдентификаторами.ПериодСобытия = ЗадачиБухгалтера.ПериодСобытия
	|			И (ВЫБОР
	|				КОГДА ЗадачиСИдентификаторами.ИдентификаторЗадачи = &КодЗадачиВзносы
	|						И НЕ ЗадачиСИдентификаторами.Правило В (&ПравилаИсключения)
	|					ТОГДА ЗадачиБухгалтера.Правило.Владелец.Код = &КодЗадачиВзносы
	|							И НЕ ЗадачиБухгалтера.Правило В (&ПравилаИсключения)
	|							И ЗадачиБухгалтера.Правило.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|				ИНАЧЕ ЗадачиСИдентификаторами.Правило = ЗадачиБухгалтера.Правило
	|			КОНЕЦ)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗадачиСоСтатусами = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач();
	ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииСтатусовЗадачПоУплатеНалогов(ЗадачиСоСтатусами, Выборка);	
	
	Возврат ЗадачиСоСтатусами;
	
КонецФункции

// Возвращает таблицу с параметрами задач по уплате налогов и статусами.
//
// Параметры:
//  ТаблицаЗадач - ТаблицаЗначений
//   * Организация
//   * ПериодСобытия
//   * Правило
//
// Возвращаемое значение:
//   ТаблицаЗначений - см. Обработки.ПомощникУплатыНалога.НовыеПараметрыЗадач()
//
Функция СтатусыЗадачПоПодготовкеРегПлатежей(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	ЗадачиСоСтатусами = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач();
	ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииСтатусовЗадачПоПодготовкеРегПлатежей(ЗадачиСоСтатусами, ТаблицаЗадач);
	
	Возврат ЗадачиСоСтатусами;
	
КонецФункции

// Возвращает таблицу с параметрами задач по выплате зарплаты и статусами.
//
// Параметры:
//  КлючиЗадач - ТаблицаЗначений
//   * Организация
//   * РегистрацияВНалоговомОргане
//   * ПериодСобытия
//   * Правило
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * Организация
//   * РегистрацияВНалоговомОргане
//   * ПериодСобытия
//   * Правило
//   * Статус
//
Функция СтатусыЗадачПоВыплатеЗарплаты(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	ПравилоАванс = ПравилоВыплатыЗарплатыАванс();
	
	ЗадачиСоСтатусами = ТаблицаЗадач.Скопировать();
	
	Если ЗадачиСоСтатусами.Колонки.Найти("Статус") = Неопределено Тогда
		ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	// Добавим колонки для корректного расчета статусов задач в помощнике. Будут удалены в конце процедуры.
	ЗадачиСоСтатусами.Колонки.Добавить("ПериодРегистрации", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ЗадачиСоСтатусами.Колонки.Добавить("Аванс",             Новый ОписаниеТипов("Булево"));
	
	Для Каждого Задача Из ЗадачиСоСтатусами Цикл
		
		Задача.ПериодРегистрации = Задача.ПериодСобытия;
		Задача.Аванс             = (Задача.Правило = ПравилоАванс);
		
	КонецЦикла;
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииСтатусовЗадачПоВыплатеЗарплаты(ЗадачиСоСтатусами, ТаблицаЗадач);
	
	// Удалим временные колонки
	ЗадачиСоСтатусами.Колонки.Удалить("ПериодРегистрации");
	ЗадачиСоСтатусами.Колонки.Удалить("Аванс");
	
	Возврат ЗадачиСоСтатусами;
	
КонецФункции

// Возвращает таблицу с параметрами задач по ФинОтчетности и статусами.
//
// Параметры:
//  ТаблицаЗадач - ТаблицаЗначений
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаФинОтчетности
//
// Возвращаемое значение: ТаблицаЗначений, копия переданной таблицы, дополненная колонкой Статус.
//  Колонки:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаФинОтчетности
//   * Статус                      - Строка
//
Функция СтатусыЗадачПоФинОтчетности(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	КлючиЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ФинОтчетность;
	
	ЗадачиСоСтатусами = ТаблицаЗадач.Скопировать();
	
	Если ЗадачиСоСтатусами.Колонки.Найти("Статус") = Неопределено Тогда
		ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	Для Каждого ВыполнениеЗадачи Из ВыполнениеЗадачПоПодготовкеОтчетов(ЗадачиСоСтатусами) Цикл
		
		Статус = "";
		Если ВыполнениеЗадачи.СтатусУстановленВручную Тогда
			Статус = ВыполнениеЗадачи.РучнойСтатус;
		Иначе
			Правило = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыполнениеЗадачи.Правило,
				"Организация, Получатель, Периодичность, ИдентификаторКомплекта, КраткоеПредставлениеКомплекта");
			
			КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ВыполнениеЗадачи.ПериодСобытия-1);
			
			Отчет = Неопределено;
			ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииФинОтчетаВБанк(Отчет, Правило, КонецПериода, Статус);
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ВыполнениеЗадачи);
		
		Для каждого Задача Из ЗадачиСоСтатусами.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ЗадачиСоСтатусами;
	
КонецФункции

// Возвращает таблицу с параметрами задач по подготовке уведомлений и статусами.
//
// Параметры:
//  ТаблицаЗадач - ТаблицаЗначений
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение: ТаблицаЗначений, копия переданной таблицы, дополненная колонкой Статус.
//  Колонки:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * Статус                      - Строка
//
Функция СтатусыЗадачПоУведомлениям(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	КлючиЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление;
	
	ЗадачиСоСтатусами = ТаблицаЗадач.Скопировать();
	
	Если ЗадачиСоСтатусами.Колонки.Найти("Статус") = Неопределено Тогда
		ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	СостояниеОтчетностиСдано = Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота;
	СтатусБРОСдано = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой();
	
	Для Каждого ВыполнениеЗадачи Из ВыполнениеЗадачПоПодготовкеУведомлений(ЗадачиСоСтатусами) Цикл
		
		Статус = "";
		Если ВыполнениеЗадачи.СтатусУстановленВручную Тогда
			Статус = ВыполнениеЗадачи.РучнойСтатус;
		ИначеЕсли ВыполнениеЗадачи.Документы.Количество() > 0 Тогда
			Если ВыполнениеЗадачи.Периодичность = Перечисления.Периодичность.День Тогда
				// Все задачи дня должны быть выполнены
				Для Каждого Документ ИЗ ВыполнениеЗадачи.Документы Цикл
					Если Документ.СостояниеСдачиОтчетности = СостояниеОтчетностиСдано
						Или Документ.Статус = СтатусБРОСдано Тогда
						Статус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой();
					Иначе
						Статус = "";
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				// Созданные отчеты отсортированы по убыванию даты подписи, определяем статус по самому последнему
				Если РегламентированнаяОтчетностьКлиентСервер.ОбъектНеОтправлялсяЧерез1СОтчетность(ВыполнениеЗадачи.Документы[0].СостояниеСдачиОтчетности)
					Или РегламентированнаяОтчетностьВызовСервера.ОбъектРеглОтчетностиОтправлялсяБезИспользования1СОтчетности(ВыполнениеЗадачи.Документы[0].Ссылка) Тогда
					Статус = ВыполнениеЗадачи.Документы[0].Статус;
				Иначе
					Статус = РегламентированнаяОтчетность.СохраненныйСтатусОтправкиУведомления(ВыполнениеЗадачи.Документы[0].Ссылка);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ВыполнениеЗадачи);
		
		Для Каждого Задача Из ЗадачиСоСтатусами.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = Статус;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ЗадачиСоСтатусами;
	
КонецФункции

// Возвращает таблицу с параметрами задач по подготовке заявлений на получение патента и статусами.
//
// Параметры:
//  ТаблицаЗадач - ТаблицаЗначений
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.Патенты
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * СтатусУстановленВручную     - Булево
//   * РучнойСтатус                - Строка
//
// Возвращаемое значение: ТаблицаЗначений, копия переданной таблицы, дополненная колонкой Статус.
//  Колонки:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.Патенты
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * Статус                      - Строка
//
Функция СтатусыЗадачПоПродлениюПатентов(ТаблицаЗадач)

	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;

	КлючиЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет;
	ЗадачиСоСтатусами = ТаблицаЗадач.Скопировать();
	Если ЗадачиСоСтатусами.Колонки.Найти("Статус") = Неопределено Тогда
		ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	Для Каждого ВыполнениеЗадачи Из ВыполнениеЗадачПоПродлениюПатентов(ЗадачиСоСтатусами) Цикл
		Статус = "";
		Если ВыполнениеЗадачи.СтатусУстановленВручную Тогда
			Статус = ВыполнениеЗадачи.РучнойСтатус;
		ИначеЕсли ВыполнениеЗадачи.Документы.Количество() > 0 Тогда
			// Созданные заявления отсортированы по убыванию даты подписи, определяем статус по самому последнему
			Если РегламентированнаяОтчетностьКлиентСервер.ОбъектНеОтправлялсяЧерез1СОтчетность(ВыполнениеЗадачи.Документы[0].СостояниеСдачиОтчетности)
				Или РегламентированнаяОтчетностьВызовСервера.ОбъектРеглОтчетностиОтправлялсяБезИспользования1СОтчетности(ВыполнениеЗадачи.Документы[0].Ссылка) Тогда
				Статус = ВыполнениеЗадачи.Документы[0].Статус;
			Иначе
				Статус = РегламентированнаяОтчетность.СохраненныйСтатусОтправкиУведомления(ВыполнениеЗадачи.Документы[0].Ссылка);
			КонецЕсли;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, ВыполнениеЗадачи);

		Для Каждого Задача Из ЗадачиСоСтатусами.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = Статус;
		КонецЦикла;

	КонецЦикла;

	Возврат ЗадачиСоСтатусами;

КонецФункции
 
// Возвращает таблицу с документами, которыми выполняются задачи бухгалтера по продлению патента.
//  Параметры:
//   ТаблицаЗадач - ТаблицаЗначений:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.Патенты
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * СтатусУстановленВручную     - Булево
//   * РучнойСтатус                - Строка
//
//  Возвращаемое значение:
//   ТаблицаЗначений
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.Патенты
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * СтатусУстановленВручную     - Булево
//   * РучнойСтатус                - Строка
//   * Документы - ТаблицаЗначений:
//     * Ссылка                   - ДокументСсылка.УведомлениеОСпецрежимахНалогообложения
//     * Дата                     - Дата документа
//     * Статус                   - Строка
//     * СостояниеСдачиОтчетности - ПеречислениеСсылка.СостояниеСдачиОтчетности
//
Функция ВыполнениеЗадачПоПродлениюПатентов(ТаблицаЗадач)

	Если ТаблицаЗадач.Колонки.Найти("СтатусУстановленВручную") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("СтатусУстановленВручную", Новый ОписаниеТипов("Булево"));
	КонецЕсли;

	Если ТаблицаЗадач.Колонки.Найти("РучнойСтатус") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("РучнойСтатус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;

	ВыполнениеЗадач = ТаблицаЗадач.Скопировать();
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииВыполненияЗадачПоПродлениюПатента(ТаблицаЗадач, ВыполнениеЗадач);
	
	Возврат ВыполнениеЗадач;
	
КонецФункции

// Возвращает идентификаторы правил, задачи по которым выполняются документами
// СведенияОЗастрахованныхЛицахСЗВ_М, СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ и РеестрДСВ_3.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - см. НоваяТаблицаСоответствияПравилИФормОтчетов()
//
Функция ИдентификаторыПравилСведенияОЗастрахованныхЛицах()
	
	Идентификаторы = НоваяТаблицаСоответствияПравилИФормОтчетов();
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОЗастрахованныхЛицахСЗВ_М";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2016_СведенияПФРЕжемесячные";
	НоваяСтрока.ИмяИсточника = "СведенияОЗастрахованныхЛицахСЗВ_М";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОЗастрахованныхЛицахСЗВ_М";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2017_СведенияПФРЕжемесячные";
	НоваяСтрока.ИмяИсточника = "СведенияОЗастрахованныхЛицахСЗВ_М";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2017_СведенияСтажСотрудниковОрганизации";
	НоваяСтрока.ИмяИсточника = "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2017_СведенияСтажСотрудниковПредприниматели";
	НоваяСтрока.ИмяИсточника = "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.РеестрДСВ_3";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2013_Отчет_Допвзносы_Накопительная_ПФР";
	НоваяСтрока.ИмяИсточника = "РеестрДСВ_3";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2023_СведенияСтажСотрудниковОрганизации";
	НоваяСтрока.ИмяИсточника = "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	НоваяСтрока.ИдентификаторЗадачи = "СтраховыеВзносы";
	НоваяСтрока.ИдентификаторПравила = "2023_СведенияСтажСотрудниковПредприниматели";
	НоваяСтрока.ИмяИсточника = "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	
	Возврат Идентификаторы;
	
КонецФункции

// Возвращает идентификаторы правил, задачи по которым выполняются документом СправкиНДФЛДляПередачиВНалоговыйОрган.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - см. НоваяТаблицаСоответствияПравилИФормОтчетов()
//
Функция ИдентификаторыПравилСправкиНДФЛ()
	
	Идентификаторы = НоваяТаблицаСоответствияПравилИФормОтчетов();
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СправкиНДФЛДляПередачиВНалоговыйОрган";
	НоваяСтрока.ИдентификаторЗадачи = "НДФЛ_Агент";
	НоваяСтрока.ИдентификаторПравила = "2013_2НДФЛ";
	НоваяСтрока.ИмяИсточника = "СправкиНДФЛДляПередачиВНалоговыйОрган";
	НоваяСтрока.СложноеУсловиеПроверки = Истина;
	НоваяСтрока.УсловиеПроверки =
		"ОснованиеДляПредставленияСправок = ЗНАЧЕНИЕ(Перечисление.ОснованияДляПредставленияСправок2НДФЛ.ЕжегоднаяОтчетность)";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СправкиНДФЛДляПередачиВНалоговыйОрган";
	НоваяСтрока.ИдентификаторЗадачи = "НДФЛ_Агент";
	НоваяСтрока.ИдентификаторПравила = "2013_2НДФЛ_Неудержанный";
	НоваяСтрока.ИмяИсточника = "СправкиНДФЛДляПередачиВНалоговыйОрган";
	НоваяСтрока.СложноеУсловиеПроверки = Истина;
	НоваяСтрока.УсловиеПроверки =
		"ОснованиеДляПредставленияСправок = ЗНАЧЕНИЕ(Перечисление.ОснованияДляПредставленияСправок2НДФЛ.ОНевозможностиУдержанияНДФЛ)";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.ФормаОтчета = "Документ.СправкиНДФЛДляПередачиВНалоговыйОрган";
	НоваяСтрока.ИдентификаторЗадачи = "НДФЛ_Агент";
	НоваяСтрока.ИдентификаторПравила = "2016_2НДФЛ_Неудержанный";
	НоваяСтрока.ИмяИсточника = "СправкиНДФЛДляПередачиВНалоговыйОрган";
	НоваяСтрока.СложноеУсловиеПроверки = Истина;
	НоваяСтрока.УсловиеПроверки =
		"ОснованиеДляПредставленияСправок = ЗНАЧЕНИЕ(Перечисление.ОснованияДляПредставленияСправок2НДФЛ.ОНевозможностиУдержанияНДФЛ)";
	
	Возврат Идентификаторы;
	
КонецФункции

// Возвращает массив идентификаторы правил, задачи по которым могут выполняться несколькими отчетами одновременно
// 
// Возвращаемое значение:
//   Массив - строкоое представление идентификатора правила
//
Функция ИдентификаторПравилВыполняемыхНесколькимиОтчетами()
	
	МассивПравил = ОбщегоНазначения.ВыгрузитьКолонку(ИдентификаторыПравилСведенияОТрудовыхКнижках(), "ИдентификаторПравила");
	
	Возврат МассивПравил;
	
КонецФункции

// В переданной таблице заполняет колонку "Документы" информацией о документах, выполняющих задачи.
//
// Параметры:
//   ТаблицаЗадач - таблица значений:
//      * Индекс - Число - Индекс строк таблицы значений
//      * ИсточникОтчета - Строка - имя объекта метаданных - исполнителя задачи
//      * Организация - СправочникСсылка.Организации - организация
//      * ДатаОкончания - Дата - Дата события
//      * ОбрабатываетсяБРО - Булево - признак того, что задача выполняется объектами БРО
//      * Документы - Таблица значений, содержащая информацию о документах, выполняющих задачу:
//         * Ссылка - ДокументСсылка, СправочникСсылка - ссылка на объект, связанный с задачей
//         * Дата - Дата - Налоговый период документа
//         * Статус - Строка - строковый статус документа
//         * СостояниеСдачиОтчетности - ПеречислениеСсылка.СостояниеСдачиОтчетности - состояние сдачи отчетности через ЭДКО
//
Процедура ЗаполнитьВыполнениеЗадачНеОбрабатываемыхБРО(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Описание таблицы документов, заполняемой для каждой задачи.
	ТаблицаДокументы = Новый ТаблицаЗначений;
	ТаблицаДокументы.Колонки.Добавить("Ссылка", Метаданные.РегистрыСведений.ЖурналОтчетовСтатусы.Измерения.Ссылка.Тип);
	ТаблицаДокументы.Колонки.Добавить("Дата",   ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаДокументы.Колонки.Добавить("Статус", Новый ОписаниеТипов("Строка"));
	ТаблицаДокументы.Колонки.Добавить("СостояниеСдачиОтчетности",
		Новый ОписаниеТипов("ПеречислениеСсылка.СостояниеСдачиОтчетности"));
	
	ТаблицаЗадач.Колонки.Добавить("Документы", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	// Индекс нужен для группировки нескольких документов по одному набору параметров.
	ТаблицаЗадач.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	
	// Служебная колонка, удаляется в конце процедуры.
	// Предназначена для поиска отчетов, выполняющих задачу.
	ТаблицаЗадач.Колонки.Добавить("ИсполнительЗадачи", Метаданные.РегистрыСведений.ЖурналОтчетовСтатусы.Измерения.Ссылка.Тип);
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		
		Если Не Задача.ОбрабатываетсяБРО Тогда
			Задача.Документы = ТаблицаДокументы.СкопироватьКолонки();
		КонецЕсли;
		
		Задача.Индекс = ТаблицаЗадач.Индекс(Задача);
		
		Если Не ПустаяСтрока(Задача.ИсточникОтчета)
			И Не Задача.ОбрабатываетсяБРО
			И Метаданные.НайтиПоПолномуИмени(Задача.ИсточникОтчета) <> Неопределено Тогда
			МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Задача.ИсточникОтчета);
			Задача.ИсполнительЗадачи = МенеджерОбъекта.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	Запрос.УстановитьПараметр("ИдентификаторыПравилВыполняющихсяНеРеглОтчетами",
		ИдентификаторыПравилВыполняющихсяНеРеглОтчетами());
		
	// Готовим временную таблицу с обрабатываемыми задачами ЗадачиСИсключениями
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Задачи.Индекс КАК Индекс,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ДатаОкончания КАК ДатаОкончания,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Периодичность КАК Периодичность,
	|	Задачи.ИсточникОтчета КАК ИсточникОтчета,
	|	Задачи.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	Задачи.ИдентификаторПравила КАК ИдентификаторПравила
	|ПОМЕСТИТЬ Задачи
	|ИЗ
	|	&Задачи КАК Задачи
	|ГДЕ
	|	НЕ Задачи.ОбрабатываетсяБРО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИдентификаторыПравил.ФормаОтчета КАК ИсточникОтчета,
	|	ИдентификаторыПравил.ИдентификаторЗадачи КАК ИдентификаторЗадачи,
	|	ИдентификаторыПравил.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ИдентификаторыПравил.СложноеУсловиеПроверки КАК СложноеУсловиеПроверки,
	|	ИдентификаторыПравил.УсловиеПроверки КАК УсловиеПроверки
	|ПОМЕСТИТЬ ИдентификаторыПравилНеОбрабатываемыхБРО
	|ИЗ
	|	&ИдентификаторыПравилВыполняющихсяНеРеглОтчетами КАК ИдентификаторыПравил
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задачи.Индекс КАК Индекс,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ДатаОкончания КАК ДатаОкончания,
	|	ПравилаИсключения.СложноеУсловиеПроверки КАК СложноеУсловиеПроверки,
	|	ПравилаИсключения.УсловиеПроверки КАК УсловиеПроверки,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Периодичность КАК Периодичность
	|ПОМЕСТИТЬ ЗадачиСИсключениями
	|ИЗ
	|	Задачи КАК Задачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИдентификаторыПравилНеОбрабатываемыхБРО КАК ПравилаИсключения
	|		ПО Задачи.ИсточникОтчета = ПравилаИсключения.ИсточникОтчета
	|			И Задачи.ИдентификаторЗадачи = ПравилаИсключения.ИдентификаторЗадачи
	|			И Задачи.ИдентификаторПравила = ПравилаИсключения.ИдентификаторПравила";
	
	Запрос.Выполнить();
	
	// Получаем статусы отправки отчетов
	// Основной запрос - задачи со стандартным отчетным периодом ("Месяц", "Квартал", "Полугодие", "Год"...)
	ТекстРезультирующегоЗапроса =
	"ВЫБРАТЬ
	|	Задачи.Индекс КАК Индекс,
	|	Задачи.СложноеУсловиеПроверки КАК СложноеУсловиеПроверки,
	|	Задачи.УсловиеПроверки КАК УсловиеПроверки,
	|	ЖурналОтчетовСтатусы.Ссылка КАК Ссылка,
	|	ЖурналОтчетовСтатусы.Статус КАК Статус,
	|	ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности КАК СостояниеСдачиОтчетности,
	|	ЖурналОтчетовСтатусы.ДатаОкончания КАК Дата,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Периодичность КАК Периодичность,
	|	ЖурналОтчетовСтатусы.ВариантОтчета КАК ВариантОтчета
	|ПОМЕСТИТЬ ЗадачиСоСтатусами
	|ИЗ
	|	ЗадачиСИсключениями КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
	|		ПО (ЖурналОтчетовСтатусы.Организация = Задачи.Организация)
	|			И (ЖурналОтчетовСтатусы.ДатаОкончания = Задачи.ДатаОкончания)
	|			И (Задачи.Периодичность <> ЗНАЧЕНИЕ(Перечисление.Периодичность.День))
	|ГДЕ
	|	НЕ ЖурналОтчетовСтатусы.ПометкаУдаления";
	
	// Дополнительные запросы по задачам ЭТК с особой периодичностью
	ПодготовитьТаблицыПоКадровымЗадачам(Запрос, ТекстРезультирующегоЗапроса);
	
	Запрос.Текст = ТекстРезультирующегоЗапроса;
	Запрос.Выполнить();
	
	// Итоговый запрос по статусам
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиСоСтатусами.Индекс КАК Индекс,
	|	ЗадачиСоСтатусами.СложноеУсловиеПроверки КАК СложноеУсловиеПроверки,
	|	ЗадачиСоСтатусами.УсловиеПроверки КАК УсловиеПроверки,
	|	ЗадачиСоСтатусами.Ссылка КАК Ссылка,
	|	ЗадачиСоСтатусами.Статус КАК Статус,
	|	ЗадачиСоСтатусами.СостояниеСдачиОтчетности КАК СостояниеСдачиОтчетности,
	|	ЗадачиСоСтатусами.Дата КАК Дата,
	|	ЗадачиСоСтатусами.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиСоСтатусами.Периодичность КАК Периодичность,
	|	ЗадачиСоСтатусами.ВариантОтчета КАК ВариантОтчета
	|ИЗ
	|	ЗадачиСоСтатусами КАК ЗадачиСоСтатусами
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ,
	|	ВариантОтчета УБЫВ
	|ИТОГИ ПО
	|	Индекс";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВсеДокументы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	ВыполнениеЗадачБухгалтераПереопределяемый.УдалитьНеЗаполненныеЭлементыМассива(ВсеДокументы);
	НепроведенныеДокументы = ОбщегоНазначения.ПроверитьПроведенностьДокументов(ВсеДокументы);
	
	ВыборкаИндекс = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИндекс.Следующий() Цикл
		
		Задача = ТаблицаЗадач[ВыборкаИндекс.Индекс];
		Задача.Документы = ТаблицаДокументы.СкопироватьКолонки();
		
		Выборка = ВыборкаИндекс.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если Не ЗначениеЗаполнено(Выборка.Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ОбщегоНазначения.СсылкаСуществует(Выборка.Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если НепроведенныеДокументы.Найти(Выборка.Ссылка) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(Выборка.Ссылка) <> ТипЗнч(Задача.ИсполнительЗадачи) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Выборка.СложноеУсловиеПроверки
				И Не СложноеУсловиеПроверкиВыполняется(Выборка.Ссылка, Выборка.УсловиеПроверки) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Задача.Документы.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЦикла;
	
	// Удалим служебные колонки.
	ТаблицаЗадач.Колонки.Удалить("ИсполнительЗадачи");
	
КонецПроцедуры

Процедура ПодготовитьТаблицыПоКадровымЗадачам(Запрос, ТекстРезультирующегоЗапроса)
	
	// Во временной таблице обязательно должны присутствовать поля шаблона
	ШаблонОбъединения = "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ЖурналОтчетовСтатусыВыборка.Индекс,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.СложноеУсловиеПроверки,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.УсловиеПроверки,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.Ссылка,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.Статус,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.СостояниеСдачиОтчетности,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.ДатаОкончания,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.ПериодСобытия,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.Периодичность,
		|	ВТ_ЖурналОтчетовСтатусыВыборка.ВариантОтчета
		|ИЗ
		|	ВТ_ЖурналОтчетовСтатусыВыборка КАК ВТ_ЖурналОтчетовСтатусыВыборка";
	
	// Статус определяется сразу и надо исключить его переопределение
	ШаблонУсловия = "
		|И Не ЖурналОтчетовСтатусы.Ссылка В (Выбрать ВТ_ЖурналОтчетовСтатусыУсловия.Ссылка ИЗ ВТ_ЖурналОтчетовСтатусыУсловия)";
	
	МассивИменВременныхТаблиц = Новый Массив;
	
	ИмяТаблицы = "ВТ_ЖурналОтчетовСтатусыКадровыеМероприятия";
	ВыполнениеЗадачБухгалтераПереопределяемый.ВТЗадачиПоКадровымМероприятиям(Запрос, ИмяТаблицы);
	МассивИменВременныхТаблиц.Добавить(ИмяТаблицы);
	
	Для Каждого ИмяВРеменнойТаблицы ИЗ МассивИменВременныхТаблиц Цикл
		ТекстРезультирующегоЗапроса = ТекстРезультирующегоЗапроса +
			СтрЗаменить(ШаблонУсловия, "ВТ_ЖурналОтчетовСтатусыУсловия", ИмяВРеменнойТаблицы);
	КонецЦикла;
	Для Каждого ИмяВРеменнойТаблицы ИЗ МассивИменВременныхТаблиц Цикл
		ТекстРезультирующегоЗапроса = ТекстРезультирующегоЗапроса + 
			СтрЗаменить(ШаблонОбъединения, "ВТ_ЖурналОтчетовСтатусыВыборка", ИмяВРеменнойТаблицы);
	КонецЦикла;
	ТекстРезультирующегоЗапроса = ТекстРезультирующегоЗапроса + ";";
	
КонецПроцедуры

Функция СложноеУсловиеПроверкиВыполняется(СсылкаНаОбъект, УсловиеПроверки)
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Или ПустаяСтрока(УсловиеПроверки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ОператорЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	ОператорЗапроса.КоличествоПолучаемыхЗаписей = 1;
	
	ОператорЗапроса.Источники.Добавить(СсылкаНаОбъект.Метаданные().ПолноеИмя());
	
	ОператорЗапроса.ВыбираемыеПоля.Добавить("ИСТИНА");
	
	ОператорЗапроса.Отбор.Добавить("Ссылка = &Ссылка");
	ОператорЗапроса.Отбор.Добавить(УсловиеПроверки);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция РелевантныйПериодСобытияДляОтчета(СвойстваЗадачи)
	
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.СкомпоноватьПолноеИмяПравила(
		СвойстваЗадачи.ИдентификаторЗадачи, СвойстваЗадачи.ИдентификаторПравила);
		
	РазрешенУчетРегулярнойДеятельности = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаРазрешенУчетРегулярнойДеятельности(РазрешенУчетРегулярнойДеятельности);
	Если ПолноеИмяПравила = "СведенияОСреднесписочнойЧисленности.2016_РегистрацияОрганизации"
		И РазрешенУчетРегулярнойДеятельности Тогда
		// Отчет всегда имеет периодичность год, но для вновь созданных организаций также предоставляется на 1 число месяца,
		// следующего за месяцем регистрации организации.
		Периодичность = Перечисления.Периодичность.Год;
		Возврат ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, СвойстваЗадачи.ПериодСобытия);
	Иначе
		Возврат СвойстваЗадачи.ПериодСобытия;
	КонецЕсли;
	
КонецФункции

Функция ТекущаяЗадачаНалоговогоПлатежа(РеквизитыДокумента)
	
	Задача = Неопределено;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриПолученииТекущейЗадачиНалоговогоПлатежа(Задача, РеквизитыДокумента);
	Возврат Задача;
	
КонецФункции

Процедура ЗаполнитьСтатусыУстановленныеВручную(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаЗадач.Колонки.Найти("СтатусУстановленВручную") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("СтатусУстановленВручную", 
			Метаданные.РегистрыСведений.ЗадачиБухгалтера.Реквизиты.СтатусУстановленВручную.Тип);
	КонецЕсли;
	
	Если ТаблицаЗадач.Колонки.Найти("РучнойСтатус") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("РучнойСтатус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	Если ТаблицаЗадач.Колонки.Найти("ПользовательУстановившийСтатус") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("ПользовательУстановившийСтатус", 
			Метаданные.РегистрыСведений.ЗадачиБухгалтера.Реквизиты.ПользовательУстановившийСтатус.Тип);
	КонецЕсли;
	
	Если ТаблицаЗадач.Колонки.Найти("Комментарий") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("Комментарий", 
			Метаданные.РегистрыСведений.ЗадачиБухгалтера.Реквизиты.Комментарий.Тип);
	КонецЕсли;
	
	Если ТаблицаЗадач.Колонки.Найти("Индекс") = Неопределено Тогда
		ТаблицаЗадач.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗадач Цикл
		СтрокаТаблицы.Индекс = ТаблицаЗадач.Индекс(СтрокаТаблицы);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Индекс КАК Индекс,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ Задачи
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Задачи.Индекс КАК Индекс,
	|	ЕСТЬNULL(ЗадачиИзРегистра.СтатусУстановленВручную, ЛОЖЬ) КАК СтатусУстановленВручную,
	|	ЕСТЬNULL(ЗадачиИзРегистра.Статус, """") КАК РучнойСтатус,
	|	ЕСТЬNULL(ЗадачиИзРегистра.ПользовательУстановившийСтатус, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК ПользовательУстановившийСтатус,
	|	ЕСТЬNULL(ЗадачиИзРегистра.Комментарий, """") КАК Комментарий
	|ИЗ
	|	Задачи КАК Задачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиИзРегистра
	|		ПО Задачи.Организация = ЗадачиИзРегистра.Организация
	|			И Задачи.Правило = ЗадачиИзРегистра.Правило
	|			И Задачи.ПериодСобытия = ЗадачиИзРегистра.ПериодСобытия
	|			И Задачи.РегистрацияВНалоговомОргане = ЗадачиИзРегистра.РегистрацияВНалоговомОргане";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаЗадач[Выборка.Индекс], Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает таблицу с параметрами задач по требованиям ФНС.
//
// Параметры:
//  ТаблицаЗадач - ТаблицаЗначений
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов
//                                 - ДокументСсылка.ТранспортноеСообщение
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение: ТаблицаЗначений, копия переданной таблицы, дополненная колонкой Статус.
//  Колонки:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов
//                                 - ДокументСсылка.ТранспортноеСообщение
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * Статус                      - Строка
//
Функция СтатусыЗадачПоТребованиямФНС(ТаблицаЗадач)
	
	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	КлючиЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС;
	
	ЗадачиСоСтатусами = ТаблицаЗадач.Скопировать();
	
	Если ЗадачиСоСтатусами.Колонки.Найти("Статус") = Неопределено Тогда
		ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило
	|ПОМЕСТИТЬ ЗадачиТребованияФНС
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиТребованияФНС.Организация КАК Организация,
	|	ЗадачиТребованияФНС.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиТребованияФНС.Правило КАК Правило,
	|	ЕСТЬNULL(ЖурналОтправокВКонтролирующиеОрганы.СтатусОтправки, """") КАК СтатусОтправки,
	|	ЕСТЬNULL(ЖурналОтправокВКонтролирующиеОрганы.СостояниеСдачиОтчетности, """") КАК СостояниеСдачиОтчетности,
	|	ЕСТЬNULL(ЖурналОтправокВКонтролирующиеОрганы.ДатаСоздания, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСоздания,
	|	ЕСТЬNULL(ЖурналОтправокВКонтролирующиеОрганы.ЕстьОтвет, ЛОЖЬ) КАК ЕстьОтвет,
	|	ЕСТЬNULL(СвойстваОтветовНаТребования.ДатаПодтверждения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПодтверждения,
	|	ЕСТЬNULL(СвойстваОтветовНаТребования.ЭДООбязателен, ЛОЖЬ) КАК ЭДООбязателен
	|ИЗ
	|	ЗадачиТребованияФНС КАК ЗадачиТребованияФНС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
	|		ПО ЗадачиТребованияФНС.Правило = ЖурналОтправокВКонтролирующиеОрганы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваОтветовНаТребования КАК СвойстваОтветовНаТребования
	|		ПО ЗадачиТребованияФНС.Правило = СвойстваОтветовНаТребования.Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	// Отправленные ответы на требования
	Требования = ЗадачиСоСтатусами.ВыгрузитьКолонку("Правило");
	ОтветыТребованияФНС = ТребованияФНС.ОтветыНаТребования(Требования, Истина, Ложь);
	Ответы = ОтветыТребованияФНС.Скопировать();
	Ответы.Индексы.Добавить("Требование");
	
	Пока Выборка.Следующий() Цикл
		Статус = "";
		Если ТипЗнч(Выборка.Правило) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
			Если Не Выборка.ЭДООбязателен Тогда
				Статус = Выборка.СтатусОтправки;
			КонецЕсли;
			Если Выборка.ЭДООбязателен 
				Или (Выборка.СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПриемПодтвержден
				// Если прием уже подтвержден, то для требования обычно 2 задачи: первая со статусом "Прием подтвержден",
				// вторая "Отправьте ответ"/"Ответ отправлен", у которой в ПериодСобытия записана ДатаПодтверждения.
				И Выборка.ПериодСобытия = Выборка.ДатаПодтверждения) Тогда
				ОтборПоТребованию = Новый Структура("Требование", Выборка.Правило);
				ОтветыПоТребованию = Ответы.НайтиСтроки(ОтборПоТребованию);
				Статус = ?(Выборка.ЕстьОтвет И ОтветыПоТребованию.Количество() > 0,
					НСтр("ru = 'Ответ отправлен'"), НСтр("ru = 'Отправьте ответ'"));
			КонецЕсли;
		ИначеЕсли ТипЗнч(Выборка.Правило) = Тип("ДокументСсылка.ТранспортноеСообщение") Тогда
			Статус = НСтр("ru = 'Расшифровать'");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, Выборка);
		
		Для Каждого Задача Из ЗадачиСоСтатусами.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = Статус;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ЗадачиСоСтатусами;

КонецФункции

// Возвращает таблицу с со статусами задач по документам СЭДО.
//
// Параметры:
//  ТаблицаЗадач - ТаблицаЗначений
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов
//                                 - ДокументСсылка.ТранспортноеСообщение
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//
// Возвращаемое значение: ТаблицаЗначений, копия переданной таблицы, дополненная колонкой Статус.
//  Колонки:
//   * Организация                 - СправочникСсылка.Организации
//   * ПериодСобытия               - Дата
//   * Правило                     - СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов
//                                 - ДокументСсылка.ТранспортноеСообщение
//   * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане
//   * Статус                      - Строка
//
Функция СтатусыЗадачПоДокументамСЭДО(ТаблицаЗадач)

	Если ТаблицаЗадач.Количество() = 0 Тогда
		Возврат ТаблицаЗадач;
	КонецЕсли;
	
	КлючиЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	КлючиЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО;
	
	ЗадачиСоСтатусами = ТаблицаЗадач.Скопировать();
	
	Если ЗадачиСоСтатусами.Колонки.Найти("Статус") = Неопределено Тогда
		ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	КонецЕсли;
	
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	Запрос.УстановитьПараметр("СтатусРасшифровать", СтатусыОтчета.Расшифровать);
	Запрос.УстановитьПараметр("СтатусОтправьтеОтвет", СтатусыОтчета.ОтправьтеОтвет);
	Запрос.УстановитьПараметр("СтатусОтветОтправлен", СтатусыОтчета.ОтветОтправлен);
	Запрос.УстановитьПараметр("СтатусПодтвердитеПрием", СтатусыОтчета.ПодтвердитеПрием);
	Запрос.УстановитьПараметр("СтатусПриемПодтвержден", СтатусыОтчета.ПриемПодтвержден);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило
	|ПОМЕСТИТЬ ЗадачиДокументыСЭДО
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиДокументыСЭДО.Организация КАК Организация,
	|	ЗадачиДокументыСЭДО.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиДокументыСЭДО.Правило КАК Правило,
	|	"""" КАК Статус
	|ИЗ
	|	ЗадачиДокументыСЭДО КАК ЗадачиДокументыСЭДО";
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииСтатусовЗадачПоДокументамСЭДО(ТаблицаЗадач, ЗадачиСоСтатусами, Запрос);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(КлючиЗадачи, Выборка);
		
		Для Каждого Задача Из ЗадачиСоСтатусами.НайтиСтроки(КлючиЗадачи) Цикл
			Задача.Статус = Выборка.Статус;
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат ЗадачиСоСтатусами;
	
КонецФункции

Функция ТекущийПользователь()
	
	Пользователь = Справочники.Пользователи.ПустаяСсылка();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ТекущийПользовательЯвляетсяСлужебным = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "Служебный");
	Если Не ТекущийПользовательЯвляетсяСлужебным Тогда
		Пользователь = ТекущийПользователь;
	КонецЕсли;
	
	Возврат Пользователь;
	
КонецФункции

#КонецОбласти

#Область ПлатежныйКалендарь

Функция ЗадачиДляКалендаря(Организация, РабочаяДата, ГоризонтПланирования)
	
	ИдентификаторыЗадачЗарплаты = Новый Массив;
	ИдентификаторыЗадачЗарплаты.Добавить("НДФЛ_Уплата");
	ИдентификаторыЗадачЗарплаты.Добавить("НДФЛ_Агент_Уплата");
	ИдентификаторыЗадачЗарплаты.Добавить("Зарплата");
	
	ИдентификаторыЗадачЗарплатыАванс = Новый Массив;
	ИдентификаторыЗадачЗарплатыАванс.Добавить("Аванс");
	
	ДоступныеОрганизации = Новый Массив;
	ВыполнениеЗадачБухгалтераПереопределяемый.ЗаполнитьДоступныеОрганизацииДляОтбора(ДоступныеОрганизации, Организация);
	
	УчетЗарплатыИКадровВЭтойПрограмме = Истина;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаУчетаЗарплатыИКадровВЭтойПрограмме(УчетЗарплатыИКадровВЭтойПрограмме);
	
	ЗадачиБухгалтера = Новый Структура("РегулярныеПлатежи, УплатаНалогов, Зарплата");
	
	ПараметрыПолученияЗадач = Новый Структура;
	ПараметрыПолученияЗадач.Вставить("СписокОрганизаций",           ДоступныеОрганизации);
	ПараметрыПолученияЗадач.Вставить("РабочаяДата",                 РабочаяДата);
	ПараметрыПолученияЗадач.Вставить("ГоризонтПланирования",        ГоризонтПланирования);
	ПараметрыПолученияЗадач.Вставить("СтатусыВыполненныхЗадач",     СтатусыВыполненныхЗадач());
	ПараметрыПолученияЗадач.Вставить("ИдентификаторыЗадачЗарплаты", ИдентификаторыЗадачЗарплаты);
	ПараметрыПолученияЗадач.Вставить("ИдентификаторыЗадачЗарплатыАванс", ИдентификаторыЗадачЗарплатыАванс);
	ПараметрыПолученияЗадач.Вставить("УчетЗарплатыСредствамиБухгалтерии", УчетЗарплатыИКадровВЭтойПрограмме);
	ПараметрыПолученияЗадач.Вставить("ПредыдущийМесяц", ДобавитьМесяц(НачалоМесяца(РабочаяДата), -1));
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ПолучитьЗадачиДляКалендаря(ЗадачиБухгалтера, ПараметрыПолученияЗадач);
	
	Возврат ЗадачиБухгалтера;
	
КонецФункции

Функция РегулярныеПлатежи(ТаблицаЗадач)
	
	РегулярныеПлатежи = Новый ТаблицаЗначений;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииРегулярныхПлатежей(РегулярныеПлатежи, ТаблицаЗадач);
	
	Возврат РегулярныеПлатежи;
	
КонецФункции

Функция ЗарплатныеПлатежи(ТаблицаЗадач)
	
	ЗарплатныеПлатежи = Новый ТаблицаЗначений;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииЗарплатныхПлатежей(ЗарплатныеПлатежи, ТаблицаЗадач);
	
	Возврат ЗарплатныеПлатежи;
	
КонецФункции

Функция НалоговыеПлатежи(ТаблицаЗадач)
	
	НалоговыеПлатежи = Новый ТаблицаЗначений;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииНалоговыхПлатежей(НалоговыеПлатежи, ТаблицаЗадач);
	
	Возврат НалоговыеПлатежи;
	
КонецФункции

#КонецОбласти

#Область РегистрацияПлатежей

Процедура ЗарегистрироватьИсполнениеЗадачиПриЗаписиПлатежа(РеквизитыПлатежа, ДополнительныеСвойства, ПараметрыРасчетаСтатусов)
	
	ПравилоУплатыНалогов =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПравилоУплатыНалогов");
	ПериодСобытияКалендаря =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПериодСобытияКалендаря");
	РегистрацияВНалоговомОргане =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РегистрацияВНалоговомОргане");
		
	ОплатаЗадолженности = Ложь;
	Если ЗначениеЗаполнено(ПравилоУплатыНалогов) И ЗначениеЗаполнено(ПериодСобытияКалендаря) Тогда
		
		// Правило и период задачи переданы из помощника, можно сразу регистрировать документ.
		ПараметрыРасчетаСтатусов.Вставить("ПропуститьОбновлениеСвязиДокументЗадача", Истина);
		
		ОплатаЗадолженности = ЭтоПогашениеНалоговойЗадолженности(РеквизитыПлатежа);
		
		ВыполнениеЗадачБухгалтераПереопределяемый.ЗарегистрироватьИсполнениеЗадачиПриЗаписиПлатежа(
			РеквизитыПлатежа,
			ДополнительныеСвойства,
			ПараметрыРасчетаСтатусов,
			ПравилоУплатыНалогов,
			ПериодСобытияКалендаря,
			ОплатаЗадолженности,
			РегистрацияВНалоговомОргане);
	КонецЕсли;
	
КонецПроцедуры

Функция ЗарегистрированоИсполнениеЗадачи(РеквизитыПлатежа, ДополнительныеСвойства)
	
	ПравилоУплатыНалогов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПравилоУплатыНалогов");
	ПериодСобытияКалендаря = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПериодСобытияКалендаря");
	
	Если ЗначениеЗаполнено(ПравилоУплатыНалогов) И ЗначениеЗаполнено(ПериодСобытияКалендаря) Тогда
		
		// Правило и период задачи переданы из помощника, значит документ уже зарегистрирован в регистре.
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат ТекущаяЗадачаНалоговогоПлатежа(РеквизитыПлатежа) <> Неопределено;
	
КонецФункции

Функция ИзменилисьРеквизитыПлатежаВлияющиеНаСтатусЗадачи(СтарыеРеквизитыПлатежа, НовыеРеквизитыПлатежа)
	
	Если Не ЗначениеЗаполнено(СтарыеРеквизитыПлатежа) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НачалоКвартала(СтарыеРеквизитыПлатежа.Дата) <> НачалоКвартала(НовыеРеквизитыПлатежа.Дата) Тогда
		Возврат Истина;
	КонецЕсли;
	
	КонтролируемыеРеквизиты = "Организация, ВидОперации, Налог, ВидНалога, 
		|ПоказательПериода, НалоговыйПериод, ПоказательОснования, ПлатежнаяВедомость";
	
	СтарыеДанные = Новый Структура(КонтролируемыеРеквизиты);
	ЗаполнитьЗначенияСвойств(СтарыеДанные, СтарыеРеквизитыПлатежа);
	УдалитьПустыеЭлементыСтруктуры(СтарыеДанные);
	
	НовыеДанные = Новый Структура(КонтролируемыеРеквизиты);
	ЗаполнитьЗначенияСвойств(НовыеДанные, НовыеРеквизитыПлатежа);
	УдалитьПустыеЭлементыСтруктуры(НовыеДанные);
	
	Возврат Не ОбщегоНазначения.ДанныеСовпадают(СтарыеДанные, НовыеДанные);
	
КонецФункции

Процедура УдалитьПустыеЭлементыСтруктуры(СтруктураДляОбработки)
	
	ПустыеКлючи = Новый Массив;
	Для Каждого КлючЗначение Из СтруктураДляОбработки Цикл
		Если Не ЗначениеЗаполнено(КлючЗначение.Значение) Тогда
			ПустыеКлючи.Добавить(КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Ключ Из ПустыеКлючи Цикл
		СтруктураДляОбработки.Удалить(Ключ);
	КонецЦикла;
	
КонецПроцедуры

Функция ИзменятьСтатусыЗадачПриЗаписиПлатежа(РеквизитыПлатежа, ДополнительныеСвойства)
	
	Если ДополнительныеСвойства.Свойство("ПропуститьОбновлениеСтатусаЗадачи") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИзменятьСтатусы = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.УточнитьИзменятьСтатусыЗадачПриЗаписиПлатежа(ИзменятьСтатусы, РеквизитыПлатежа, ДополнительныеСвойства);
	
	Возврат ИзменятьСтатусы;
	
КонецФункции

Функция ДокументВыполняетЗадачу(ДокументОбъект)
	
	ДокументВыполняетЗадачу = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииПризнакаДокументВыполняетЗадачу(ДокументВыполняетЗадачу, ДокументОбъект);
	Возврат ДокументВыполняетЗадачу;
	
КонецФункции

Функция ЭтоПогашениеНалоговойЗадолженности(РеквизитыПлатежа)
	
	ЭтоПогашение = Ложь;
	ВыполнениеЗадачБухгалтераПереопределяемый.ОпределитьЭтоПогашениеНалоговойЗадолженности(ЭтоПогашение, РеквизитыПлатежа);
	Возврат ЭтоПогашение;
	
КонецФункции

Функция РеквизитыПлатежаПоДаннымОбъекта(ДокументОбъект)
	
	РеквизитыПлатежа = НовыйРеквизитыПлатежаВлияющиеНаСтатусЗадачи();
	
	ЗаполнитьЗначенияСвойств(РеквизитыПлатежа, ДокументОбъект);
	
	// Особенные реквизиты, не содержащиеся в объекте непосредственно.
	
	РеквизитыПлатежа.ВидДокумента = ДокументОбъект.Метаданные().Имя;
	
	Если ЗначениеЗаполнено(РеквизитыПлатежа.Налог) Тогда
		РеквизитыПлатежа.ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыПлатежа.Налог, "ВидНалога");
	КонецЕсли;
	
	Возврат РеквизитыПлатежа;
	
КонецФункции

Функция РеквизитыПлатежаПоДаннымДокумента(ДокументСсылка)
	
	РеквизитыПлатежа = НовыйРеквизитыПлатежаВлияющиеНаСтатусЗадачи();
	ВыполнениеЗадачБухгалтераПереопределяемый.ОпределитьРеквизитыПлатежаПоДаннымДокумента(РеквизитыПлатежа, ДокументСсылка);
	Возврат РеквизитыПлатежа;
	
КонецФункции

#КонецОбласти

#Область ПрочиеВспомогательныеПроцедурыИФункции

Функция ПараметрыПолученияДанныхПоДекларации()
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Правило");
	Параметры.Вставить("Организация");
	Параметры.Вставить("РегистрацияВНалоговомОргане");
	Параметры.Вставить("ПериодСобытия");
	Параметры.Вставить("Срок");
	Параметры.Вставить("ПоказательПериода");
	
	Возврат Параметры;
	
КонецФункции

Функция СобытиеЖурналаРегистрации()

	Возврат НСтр("ru = 'ЗадачиБухгалтера.ВыполнениеДействия'", ОбщегоНазначения.КодОсновногоЯзыка());

КонецФункции

Функция ЗадачиУплатыПатента(Организация, ОтборПатент)
	
	ЗадачиУплатыПатента = Новый ТаблицаЗначений;
	ВыполнениеЗадачБухгалтераПереопределяемый.ЗаполнитьЗадачиУплатыПатента(ЗадачиУплатыПатента, Организация, ОтборПатент);
	
	Возврат ЗадачиУплатыПатента;
	
КонецФункции

Функция ПравилоВыплатыЗарплатыАванс()
	
	ПравилоАванс = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторАванса", "Аванс");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|ГДЕ
	|	ПравилаПредставленияОтчетовУплатыНалогов.Код = &ИдентификаторАванса
	|	И ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПравилоАванс = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ПравилоАванс;
	
КонецФункции

Функция НоваяТаблицаСоответствияПравилИФормОтчетов()
	
	ТаблицаПравил = Новый ТаблицаЗначений;
	
	ТаблицаПравил.Колонки.Добавить("ФормаОтчета",          ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПравил.Колонки.Добавить("ИдентификаторПравила", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПравил.Колонки.Добавить("ИдентификаторЗадачи",  ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПравил.Колонки.Добавить("ИмяИсточника",         ОбщегоНазначения.ОписаниеТипаСтрока(100));
	
	// В некоторых случаях несколько задач может выполняться одним объектом.
	// При этом, принадлежность к конкретной задачи может зависеть от значений реквизитов объекта.
	// К примеру, правила НДФЛ_Агент.2013_2НДФЛ и НДФЛ_Агент.2016_2НДФЛ_Неудержанный
	// выполняются одним документом и разделяются значениями реквизита "ОснованиеДляПредставленияСправок" документа.
	// "УсловиеПроверки" описывает дополнительную проверку, которую можно выполнить запросом к данным объекта.
	ТаблицаПравил.Колонки.Добавить("СложноеУсловиеПроверки", Новый ОписаниеТипов("Булево"));
	ТаблицаПравил.Колонки.Добавить("УсловиеПроверки",        ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	Возврат ТаблицаПравил;
	
КонецФункции

Процедура ЗанестиСведенияОНарушенииСрокаПодачиРегламентированногоОтчета(Документ, ТекущийДень)
	
	Если ТипЗнч(Документ) <> Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Документ)
		И ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", Документ.Метаданные()) Тогда
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Организация");
	Иначе
		Организация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	ВыполнениеЗадачБухгалтераПереопределяемый.ЗанестиСведенияОНарушенииСрокаПодачиРегламентированногоОтчета(Документ, ТекущийДень, Организация);
	
КонецПроцедуры

Функция ОтчетыИспользуютсяДляУплатыНалога(Правило)
	
	// Как правило, налог уплачивается на основании отчета (декларации).
	// Здесь описаны исключения, когда представляемые отчеты заведомо не связаны с уплатой налога,
	// не содержат данных для уплаты налога.
	// Ситуации, когда никакие отчеты не представляются для уплаты налога, таким исключением не являются и здесь не описаны.
	
	Если ЭтоСтраховыеВзносы(Правило) Или ЭтоОтчетностьПоНДФЛ(Правило) Или ЭтоВзносыИП(Правило) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура УстановитьДополнительныеСвойстваОплатыАУСН(ДокументОбъект)

	ВыполнениеЗадачБухгалтераПереопределяемый.УстановитьДополнительныеСвойстваОплатыАУСН(ДокументОбъект);
	
КонецПроцедуры

Функция ЭтоОтчетностьПоНДФЛ(Правило)
	
	Если Не ЗначениеЗаполнено(Правило)
		Или ТипЗнч(Правило) <> Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(Правило);
	
	МассивПравилОтчетностиПоНДФЛ = ПравилаОтчетности6НДФЛ();
	МассивПравилОтчетностиПоНДФЛ.Добавить("НДФЛ_Агент.2022_НДФЛ_СведенияАУСН");
	
	Возврат МассивПравилОтчетностиПоНДФЛ.Найти(ПолноеИмяПравила) <> Неопределено;
	
КонецФункции

Функция ПравилоУведомленияПоНДФЛ()
	Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("НДФЛ_Агент_Уведомление", "2023_НДФЛ_Уведомление");
КонецФункции

Функция ПравилоУведомленияПоНДФЛТринадцатыйПериод()
	Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("НДФЛ_Агент_Уведомление", "2023_НДФЛ_Уведомление_Декабрь");
КонецФункции

Функция ПравилоУведомленияПоНДФЛКонецМесяца()
	Возврат Справочники.ПравилаПредставленияОтчетовУплатыНалогов.НайтиПоИдентификатору("НДФЛ_Агент_Уведомление", "2024_НДФЛ_Уведомление_КонецМесяца");
КонецФункции

Функция ЗадачаТребованиеФНС(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.ТребованиеФНС) КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Правило = &Правило
	|	И НЕ ЗадачиБухгалтера.ВАрхиве";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ЗадачаДокументСЭДО(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Правило = &Правило";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ЭтоВзносыИП(Правило)
	
	Если Не ЗначениеЗаполнено(Правило)
		Или ТипЗнч(Правило) <> Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	КодПравила = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "Код");
	
	Возврат КодПравила = ЗадачиБухгалтераКлиентСервер.КодЗадачиЕдиныйТарифИП();
	
КонецФункции

#КонецОбласти

#КонецОбласти
