// Опередяет набор параметров алгоритма открытия формы-заглушки для недоступной функциональности.
//
// Возвращаемое значение:
//   Структура - параметры алгоритма.
//
Функция НовыйПараметрыФормаНедоступноНаМобильном() Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ОписаниеНедоступнойФункции", "");
	ПараметрыОткрытия.Вставить("ОписаниеНедоступногоДействия", "");
	ПараметрыОткрытия.Вставить("ОткрытаВнутриОкна", Ложь);
	ПараметрыОткрытия.Вставить("ФормаДоступнаСПредупреждением", Ложь);
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

// Опередяет набор параметров алгоритма открытия недаптированной формы после подтвержения пользователем.
//
// Параметры:
//  ПараметрыКоманды - ПараметрыВыполненияКоманды - список параметров, с которыми была вызвана команда из интерфейса.
//
// Возвращаемое значение:
//   Структура - параметры алгоритма.
//
Функция НовыйПараметрыОткрытияФормыСПредупреждением(ПараметрыКоманды = Неопределено) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИмяФормы", "");
	ПараметрыОткрытия.Вставить("Заголовок", "");
	ПараметрыОткрытия.Вставить("ОписаниеОповещенияОЗакрытии");
	ПараметрыОткрытия.Вставить("РежимОткрытияОкна");
	
	ПараметрыОткрытия.Вставить("Владелец");
	ПараметрыОткрытия.Вставить("Уникальность");
	ПараметрыОткрытия.Вставить("Окно");
	ПараметрыОткрытия.Вставить("НавигационнаяСсылка");
	
	// По-умолчанию вызов процедуры, открывающей форму (выбирающей, стоит ли показывать форму ожидания) происходит
	// Прямой вызов из клиентского кода передает параметры через переменную в модуле формы
	// Вызова из ОбработкаПолученияФормы() передает параметры через серверный вызов, нужно установить Истина.
	ПараметрыОткрытия.Вставить("ПереопределенаНаСервере", Ложь);
	
	Если ПараметрыКоманды = Неопределено Тогда
		Возврат ПараметрыОткрытия;
	КонецЕсли;
	
	ПараметрыОткрытия.Владелец            = ПараметрыКоманды.Источник;
	ПараметрыОткрытия.Уникальность        = ПараметрыКоманды.Уникальность;
	ПараметрыОткрытия.Окно                = ПараметрыКоманды.Окно;
	ПараметрыОткрытия.НавигационнаяСсылка = ПараметрыКоманды.НавигационнаяСсылка;

	// Используем навигационную ссылку из параметров команды только для заполнения по умолчанию
	// имени открываемой формы.
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.НавигационнаяСсылка) Тогда
		Возврат ПараметрыОткрытия;
	КонецЕсли;

	СоставНавигационнойСсылки = СтрРазделить(ПараметрыКоманды.НавигационнаяСсылка, "/");
	Если СоставНавигационнойСсылки.Количество() = 0 Тогда
		Возврат ПараметрыОткрытия;
	КонецЕсли;
	СоставНавигационнойСсылки = СтрРазделить(СоставНавигационнойСсылки[СоставНавигационнойСсылки.ВГраница()], ".");
	Если СоставНавигационнойСсылки.Количество() < 2 Тогда
		Возврат ПараметрыОткрытия;
	КонецЕсли;
	
	// Заполняем имя формы по-умолчанию.
	ИмяФормы = СоставНавигационнойСсылки[0] + "." + СоставНавигационнойСсылки[1];
	ИдентификаторКоманды = ВРег(СоставНавигационнойСсылки[0]);
	Если ИдентификаторКоманды = "ОБЩАЯФОРМА" Или ИдентификаторКоманды = "ОБЩАЯКОМАНДА"
	 Или ИдентификаторКоманды = "COMMONCOMMAND" Или ИдентификаторКоманды = "COMMONFORM" Тогда // по-умочанию предполагаем, что открываем список документов
		ПараметрыОткрытия.ИмяФормы = "Документ." + СоставНавигационнойСсылки[1] + ".ФормаСписка";
	Иначе
		ПараметрыОткрытия.ИмяФормы = ИмяФормы + ".ФормаСписка";
	КонецЕсли;
	
	// Заполняем заголовок формы по-умолчанию из навигационной ссылки, не обращаясь к метаданным.
	// Разбиваем строку пробелами, ориентируясь на заглавные буквы.
	ЗаголовокБезПробелов = СоставНавигационнойСсылки[СоставНавигационнойСсылки.ВГраница()];
	ЗаголовокВНижнемРегистре = НРег(ЗаголовокБезПробелов);
	СловаЗаголовка = Новый Массив;
	ПозицияСимвола = 1;
	НомерСимвола = ПозицияСимвола;
	ВсегоСимволов = СтрДлина(ЗаголовокВНижнемРегистре);
	Пока НомерСимвола < ВсегоСимволов Цикл
		
		НомерСимвола = НомерСимвола + 1;
		Если Сред(ЗаголовокБезПробелов, НомерСимвола, 1) <> Сред(ЗаголовокВНижнемРегистре, НомерСимвола, 1) Тогда
			
			СловоЗаголовка = Сред(ЗаголовокВНижнемРегистре, ПозицияСимвола, НомерСимвола - ПозицияСимвола);
			СловаЗаголовка.Добавить(СловоЗаголовка);
			ПозицияСимвола = НомерСимвола;
			
		КонецЕсли;
		
	КонецЦикла;
	Если ПозицияСимвола = 1 Тогда
		
		ПараметрыОткрытия.Заголовок = ЗаголовокБезПробелов;
		
	Иначе
		
		СловоЗаголовка = Сред(ЗаголовокВНижнемРегистре, ПозицияСимвола, ВсегоСимволов - ПозицияСимвола + 1);
		СловаЗаголовка.Добавить(СловоЗаголовка);
		СловаЗаголовка[0] = ВРег(Лев(СловаЗаголовка[0], 1)) + Сред(СловаЗаголовка[0], 2);
		ПараметрыОткрытия.Заголовок = СтрСоединить(СловаЗаголовка, " ");
		
	КонецЕсли;
	
	Возврат ПараметрыОткрытия;
	
КонецФункции
