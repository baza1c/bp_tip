#Область ПрограммныйИнтерфейс

// Подробнее
// См. СписокЗадачУведомленияПоЭлектроннойПочте.ОтправлятьУведомления
//
Процедура ОпределитьОтправлятьУведомления(ОтправлятьУведомления, ВладелецДанныхВХранилище) Экспорт
	
	РезультатЧтения = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(ВладелецДанныхВХранилище, "ОтправлятьУведомленияПоЭлектроннойПочте");
	
	Если РезультатЧтения = Неопределено Тогда
		ОтправлятьУведомления = Ложь;
	Иначе
		ОтправлятьУведомления = РезультатЧтения;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. СписокЗадачУведомленияПоЭлектроннойПочте.ОтправлятьУведомленияСЭДО
//
Процедура ОпределитьОтправлятьУведомленияСЭДО(ОтправлятьУведомления, ВладелецДанныхВХранилище, ОтправлятьУведомленияСЭДОПоЭлектроннойПочте) Экспорт
	
	ИспользоватьНачислениеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	Если Не ИспользоватьНачислениеЗарплаты Тогда
		ОтправлятьУведомления = Ложь;
		Возврат;
	КонецЕсли;
	
	РезультатЧтения = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(
		ВладелецДанныхВХранилище, ОтправлятьУведомленияСЭДОПоЭлектроннойПочте);
	
	Если РезультатЧтения = Неопределено Тогда
		ОтправлятьУведомления = Ложь;
	Иначе
		ОтправлятьУведомления = РезультатЧтения;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. СписокЗадачУведомленияПоЭлектроннойПочте.Получатель
//
Процедура ОпределитьПолучателя(Получатель, ВладелецДанныхВХранилище) Экспорт
	
	РезультатЧтения = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(ВладелецДанныхВХранилище, "Получатель");
	
	Если РезультатЧтения = Неопределено Тогда
		Получатель = "";
	Иначе
		Получатель = РезультатЧтения;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. СписокЗадачУведомленияПоЭлектроннойПочте.АдресИнформационнойБазы
//
Процедура ЗаполнитьАдресИнформационнойБазы(АдресИнформационнойБазы, ВладелецДанныхВХранилище) Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		АдресИнформационнойБазы = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(
			ВладелецДанныхВХранилище, "АдресИнформационнойБазы");
		Если АдресИнформационнойБазы = Неопределено Тогда
			АдресИнформационнойБазы = "";
			Возврат;
		КонецЕсли;
	Иначе
		АдресИнформационнойБазы = ОбщегоНазначения.АдресПубликацииИнформационнойБазыВИнтернете();
		Если ПустаяСтрока(АдресИнформационнойБазы) Тогда
			АдресИнформационнойБазы = ОбщегоНазначения.АдресПубликацииИнформационнойБазыВЛокальнойСети();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. СписокЗадачУведомленияПоЭлектроннойПочте.УстановитьОтправлятьУведомления
//
Процедура УстановитьОтправлятьУведомления(ОтправлятьУведомления, ВладелецДанныхВХранилище, КлючДанных) Экспорт
	
	ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(ВладелецДанныхВХранилище,
		ОтправлятьУведомления, КлючДанных);
		
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(ВладелецДанныхВХранилище, 
			ПолучитьНавигационнуюСсылкуИнформационнойБазы(), "АдресИнформационнойБазы");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. СписокЗадачУведомленияПоЭлектроннойПочте.УстановитьПолучателя
//
Процедура УстановитьПолучателя(Получатель, ВладелецДанныхВХранилище) Экспорт
	
	ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(ВладелецДанныхВХранилище,
		Получатель, "Получатель");
	
КонецПроцедуры

#КонецОбласти