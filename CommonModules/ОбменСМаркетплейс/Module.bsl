
#Область ПрограммныйИнтерфейс

Функция ЗагрузитьОтчетОПродажах(ПараметрыОтчета) Экспорт
	Перем КодОтветаСервера, СообщениеОбОшибке;
	ДанныеОтчета = Новый Структура("Маркетплейс, ДанныеДокумента, ТекстОшибки, КодОтветаСервера");
	
	Обработчик = ОбработчикМаркетплейса(ПараметрыОтчета.ВидМаркетплейс);
	
	Если Обработчик <> Неопределено Тогда
		ДанныеОтчета.ДанныеДокумента = Обработчик.ПолучитьДанныеДокумента(ПараметрыОтчета, КодОтветаСервера, СообщениеОбОшибке);
		ДанныеОтчета.Маркетплейс     = ПараметрыОтчета.ВидМаркетплейс;
		ДанныеОтчета.КодОтветаСервера = КодОтветаСервера;
	Иначе
		СообщениеОбОшибке = НСтр("ru = 'Неизвестный маркетплейс'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		ДанныеОтчета.ТекстОшибки = СообщениеОбОшибке;
	КонецЕсли;
	
	Возврат ДанныеОтчета;
	
КонецФункции

Функция КонтрагентМаркетплейсаПоУмолчанию(ВидМаркетплейса) Экспорт
	
	Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.WILDBERRIES Тогда
		Инн = "9714053621";
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON Тогда
		Инн = "7704217370";
	Иначе
		Возврат Контрагент;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ПометкаУдаления
	|	И Контрагенты.ИНН = &ИНН";
	Запрос.УстановитьПараметр("ИНН", Инн);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Контрагент = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Контрагент;
	
КонецФункции

Функция ПолучитьОстаткиТоваров(НастройкаИнтеграции) Экспорт
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ВидМаркетплейса");
	МодульИнтеграции = ОбработчикМаркетплейса(ВидМаркетплейса);
	
	Если МодульИнтеграции = Неопределено Тогда
		Возврат НовыйТаблицаОстатковТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаИнтеграции", НастройкаИнтеграции);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	НоменклатураМаркетплейсов.Штрихкод КАК Штрихкод
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураМаркетплейсов КАК НоменклатураМаркетплейсов
	|		ПО НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = НоменклатураМаркетплейсов.Маркетплейс
	|			И НастройкиИнтеграцииМаркетплейс.Владелец = НоменклатураМаркетплейсов.Организация
	|ГДЕ
	|	НастройкиИнтеграцииМаркетплейс.Ссылка = &НастройкаИнтеграции
	|	И НЕ НоменклатураМаркетплейсов.ИдентификаторТовара ЕСТЬ NULL";
	
	Отбор = Запрос.Выполнить().Выгрузить();
	
	Отказ = Ложь;
	ТаблицаОстатки = МодульИнтеграции.ПолучитьОстаткиТоваров(НастройкаИнтеграции, Отбор, Отказ); 
	Если Отказ Тогда
		Возврат НовыйТаблицаОстатковТоваров();
	КонецЕсли;
	
	Возврат ТаблицаОстатки;
КонецФункции

Функция ПодготовитьФайлОстатков(НастройкаИнтеграции, ТаблицаОстатки) Экспорт
	МодульИнтеграции = ОбработчикМаркетплейса(НастройкаИнтеграции.ВидМаркетплейса);
	Возврат МодульИнтеграции.ПодготовитьФайлОстатков(НастройкаИнтеграции, ТаблицаОстатки);
КонецФункции

Функция ПередатьОстаткиТоваров(НастройкаИнтеграции) Экспорт
	Перем СообщениеОбОшибке;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ВидМаркетплейса");
	МодульИнтеграции = ОбработчикМаркетплейса(ВидМаркетплейса);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаИнтеграции", НастройкаИнтеграции);
	Запрос.УстановитьПараметр("ПустаяДата",          '00010101');
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиТоваровМаркетплейсов.ИдентификаторСклада КАК ИдентификаторСклада,
	|	ОстаткиТоваровМаркетплейсов.ИдентификаторТовара КАК ИдентификаторТовара,
	|	ОстаткиТоваровМаркетплейсов.Штрихкод КАК Штрихкод,
	|	ОстаткиТоваровМаркетплейсов.Остаток КАК Остаток
	|ИЗ
	|	РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваровМаркетплейсов
	|ГДЕ
	|	ОстаткиТоваровМаркетплейсов.НастройкаИнтеграции = &НастройкаИнтеграции
	|	И ОстаткиТоваровМаркетплейсов.ДатаВыгрузкиОстатков = &ПустаяДата";
	
	ТаблицаТоваров = НовыйТаблицаОстатковТоваров();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Запрос.Выполнить().Выгрузить(), ТаблицаТоваров);
	
	Отказ = Ложь;
	КодОтветаСервера = 0;
	МодульИнтеграции.ПередатьОстаткиТоваров(НастройкаИнтеграции, ТаблицаТоваров, СообщениеОбОшибке, Отказ, КодОтветаСервера);
	
	Результат = Новый Структура;
	Результат.Вставить("Результат",           НЕ Отказ);
	Результат.Вставить("СообщениеОбОшибке",   СообщениеОбОшибке);
	Результат.Вставить("КодОтветаСервера",    КодОтветаСервера);
	
	Возврат Результат;
	
КонецФункции

Функция СписокТоваровМаркетплейс(НастройкаИнтеграции) Экспорт
	
	Перем СообщениеОбОшибке;
	
	Отказ = Ложь;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ВидМаркетплейса");
	МодульИнтеграции = ОбработчикМаркетплейса(ВидМаркетплейса);
	СписокТоваров = МодульИнтеграции.СписокТоваровМаркетплейс(НастройкаИнтеграции, СообщениеОбОшибке, Отказ);
	
	Результат = Новый Структура;
	Результат.Вставить("Результат",           	НЕ Отказ);
	Результат.Вставить("НастройкаИнтеграции", 	НастройкаИнтеграции);
	Результат.Вставить("СообщениеОбОшибке",   	СообщениеОбОшибке);
	Результат.Вставить("СписокТоваров",       	СписокТоваров);
	
	Возврат Результат;
	
КонецФункции

Функция СписокСкладовМаркетплейс(НастройкаИнтеграции) Экспорт
	Если НЕ ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ВидМаркетплейса");
	МодульИнтеграции = ОбработчикМаркетплейса(ВидМаркетплейса);
	
	Отказ = Ложь;
	СписокСкладов = МодульИнтеграции.СписокСкладов(НастройкаИнтеграции, Отказ);
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СписокСкладов;
КонецФункции 

Функция ИнтеграцияНастроена(НастройкаИнтеграции) Экспорт
	Если НЕ ЗначениеЗаполнено(НастройкаИнтеграции) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаИнтеграции, "ВидМаркетплейса");
	МодульИнтеграции = ОбработчикМаркетплейса(ВидМаркетплейса);
	
	Возврат МодульИнтеграции.ИнтеграцияНастроена(НастройкаИнтеграции);
КонецФункции

Функция ЭтоМаркетплейсКомиссионер(Контрагент) Экспорт
	
	РеквизитыМаркетплейсов = Новый ТаблицаЗначений;
	ОписаниеТипаСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(0);
	РеквизитыМаркетплейсов.Колонки.Добавить("НаименованиеПолное", ОписаниеТипаСтрока);
	РеквизитыМаркетплейсов.Колонки.Добавить("ИНН", ОписаниеТипаСтрока);
	РеквизитыМаркетплейсов.Колонки.Добавить("КПП", ОписаниеТипаСтрока);
	
	//ООО "Вайлдберриз"
	ЗаполнитьЗначенияСвойств(РеквизитыМаркетплейсов.Добавить(), ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB(ТекущаяДатаСеанса()));
	
	//ООО "Интернет Решения"
	ЗаполнитьЗначенияСвойств(РеквизитыМаркетплейсов.Добавить(), ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыOzon());
	
	РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ИНН, КПП");
	
	Возврат (РеквизитыМаркетплейсов.НайтиСтроки(РеквизитыКонтрагента).Количество() > 0);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СобытиеЖурналаРегистрации() Экспорт
	Возврат НСтр("ru = 'Обмен с маркетплейс'", ОбщегоНазначения.КодОсновногоЯзыка());
КонецФункции

Функция ЗарегистрироватьОшибкуОбмена(ТекстОшибки, ВидМаркетплейса) Экспорт
	
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ВидМаркетплейса, ТекстОшибки);
	
КонецФункции

Функция ОбработчикМаркетплейса(ВидМаркетплейса)
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON Тогда
		Возврат ОбщегоНазначения.ОбщийМодуль("ИнтеграцияOZON");
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
		Возврат ОбщегоНазначения.ОбщийМодуль("ИнтеграцияWildberries");
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		Возврат ОбщегоНазначения.ОбщийМодуль("ИнтеграцияЯндексМаркет");
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Мегамаркет Тогда
		Возврат ОбщегоНазначения.ОбщийМодуль("ИнтеграцияМегамаркет");
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru = 'Неизвестный маркетплейс'");
КонецФункции

// Преобразует данные в строку JSON.
//
// Параметры:
//   Значение - Произвольный - объект записи JSON. Представляет собой значение произвольного типа. Ограничения см. в описании функции 
//                глобального контекста ЗаписатьJSON.
//
// Возвращаемое значение:
//   Строка - строка JSON.
//
Функция ВJSON(Значение) Экспорт

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ПроверятьСтруктуру = Истина;
	ЗаписатьJSON(ЗаписьJSON, Значение);
	
	Возврат ЗаписьJSON.Закрыть();

КонецФункции

// Преобразует строку, содержащую текст JSON, в структуру данных.
//
// Параметры:
//   СтрокаJSON                   - Строка - строка, содержащая текст в формате JSON.
//   ИменаСвойствСоЗначениямиДата - Массив Из Строка, Строка - массив, элементы которого содержат имена свойств JSON, для 
//                                    которых нужно вызывать восстановление даты из строки.
//   ПрочитатьВСоответствие       - Булево - если установлено Истина, чтение объекта JSON будет выполнено в Соответствие. Если установлено
//                                    Ложь, объекты будут считываться в объект типа Структура. Значение по умолчанию: Ложь.
//
// Возвращаемое значение:
//   Произвольный - результат преобразования строки JSON.
//
Функция ИзJSON(СтрокаJSON, ИменаСвойствСоЗначениямиДата = "", ПрочитатьВСоответствие = Ложь) Экспорт

	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Результат = ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие, ИменаСвойствСоЗначениямиДата);
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьДанныеОтвета(РезультатЗапроса, ПрочитатьВСоответствие = Ложь) Экспорт
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьПоток(РезультатЗапроса.ПолучитьТелоКакПоток());
	РезультатЧтения = ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие);
	ЧтениеJSON.Закрыть();
	Возврат РезультатЧтения;
КонецФункции

Функция РазобратьДанныеОтвета(ДанныеОтвета, АтрибутыРезультата) Экспорт
	РезультатЗапроса = Новый Структура;
	
	Для каждого АтрибутРезультата Из АтрибутыРезультата Цикл
		ЗначениеАтрибута = ДанныеОтвета;
		КлючиРезультата  = СтрРазделить(АтрибутРезультата.Значение, ".");

		Для Каждого КлючРезультата Из КлючиРезультата Цикл
			Если ТипЗнч(ЗначениеАтрибута) = Тип("Структура") Тогда
				Если НЕ ЗначениеАтрибута.Свойство(КлючРезультата, ЗначениеАтрибута) Тогда
					ЗначениеАтрибута = Неопределено;
					Прервать;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ЗначениеАтрибута) = Тип("Соответствие") Тогда
				Если ЗначениеАтрибута.Получить(КлючРезультата) <> Неопределено Тогда
					ЗначениеАтрибута = ЗначениеАтрибута.Получить(КлючРезультата);
				Иначе
					ЗначениеАтрибута = Неопределено;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеАтрибута = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатЗапроса.Вставить(АтрибутРезультата.Ключ, ЗначениеАтрибута);
	КонецЦикла;
	
	Возврат РезультатЗапроса;
КонецФункции

Процедура ОбновитьДанныеОбОстатках(НастройкаИнтеграции, ОстаткиТоваров) Экспорт
	
	Для Каждого СтрокаТаблицыЗначений Из ОстаткиТоваров Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.ИдентификаторСклада) Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ОстаткиТоваровМаркетплейсов");
			ЭлементБлокировкиДанных.УстановитьЗначение("НастройкаИнтеграции", НастройкаИнтеграции);
			ЭлементБлокировкиДанных.УстановитьЗначение("ИдентификаторТовара", СтрокаТаблицыЗначений.ИдентификаторТовара);
			ЭлементБлокировкиДанных.УстановитьЗначение("Штрихкод",            СтрокаТаблицыЗначений.Штрихкод);
			БлокировкаДанных.Заблокировать();

			НаборЗаписей = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей(); 
			НаборЗаписей.Отбор.НастройкаИнтеграции.Установить(НастройкаИнтеграции);
			НаборЗаписей.Отбор.ИдентификаторТовара.Установить(СтрокаТаблицыЗначений.ИдентификаторТовара);
			НаборЗаписей.Отбор.Штрихкод.Установить(СтрокаТаблицыЗначений.Штрихкод);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				Запись                      = НаборЗаписей[0];
				Запись.Остаток              = СтрокаТаблицыЗначений.Остаток; 
				Запись.ДатаВыгрузкиОстатков = ТекущаяДатаСеанса();
				
			Иначе
				Запись                      = НаборЗаписей.Добавить();  
				Запись.НастройкаИнтеграции  = НастройкаИнтеграции; 
				Запись.ИдентификаторСклада  = СтрокаТаблицыЗначений.ИдентификаторСклада;
				Запись.ИдентификаторТовара  = СтрокаТаблицыЗначений.ИдентификаторТовара;  
				Запись.Штрихкод             = СтрокаТаблицыЗначений.Штрихкод;  
				Запись.Остаток              = СтрокаТаблицыЗначений.Остаток;
				Запись.ДатаВыгрузкиОстатков = ТекущаяДатаСеанса();
			КонецЕсли;
			
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
							
		Исключение
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Функция НовыйТаблицаОстатковТоваров() Экспорт
	
	МетаданныеРегистр = Метаданные.РегистрыСведений.ОстаткиТоваровМаркетплейсов;

	ТаблицаТовары = Новый ТаблицаЗначений;
	ТаблицаТовары.Колонки.Добавить("ИдентификаторСклада", МетаданныеРегистр.Реквизиты.ИдентификаторСклада.Тип);
	ТаблицаТовары.Колонки.Добавить("ИдентификаторТовара", МетаданныеРегистр.Измерения.ИдентификаторТовара.Тип);
	ТаблицаТовары.Колонки.Добавить("Штрихкод",            МетаданныеРегистр.Измерения.Штрихкод.Тип);
	ТаблицаТовары.Колонки.Добавить("Остаток",             МетаданныеРегистр.Ресурсы.Остаток.Тип);
	
	Возврат ТаблицаТовары;
КонецФункции

Функция НовыйНоменклатураМаркетплейса(ВидМаркетплейса, Владелец) Экспорт
	НоменклатураМаркетплейса = Новый Структура;
	НоменклатураМаркетплейса.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	НоменклатураМаркетплейса.Вставить("Владелец",        Владелец);
	НоменклатураМаркетплейса.Вставить("КодМагазина");
	НоменклатураМаркетплейса.Вставить("Наименование");
	НоменклатураМаркетплейса.Вставить("Артикул");
	НоменклатураМаркетплейса.Вставить("Штрихкоды",       Новый Массив);

	Возврат НоменклатураМаркетплейса;
КонецФункции 

Процедура ДобавитьНоменклатуруВСписок(СписокНоменклатурыМаркетплейса, НоменклатураМаркетплейса) Экспорт
	НоменклатураКонтрагента              = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(НоменклатураМаркетплейса.Владелец,);
	НоменклатураКонтрагента.Наименование = НоменклатураМаркетплейса.Наименование;
	НоменклатураКонтрагента.Артикул      = НоменклатураМаркетплейса.Артикул;
	
	Штрихкод = "";
	Если НоменклатураМаркетплейса.Штрихкоды.Количество() = 1 Тогда
		Штрихкод = НоменклатураМаркетплейса.Штрихкоды[0];
	КонецЕсли;
	
	ЭтоWildberries = НоменклатураМаркетплейса.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.Wildberries;
	
	Если Не ЗначениеЗаполнено(НоменклатураКонтрагента.Наименование)
		И Не ЗначениеЗаполнено(НоменклатураКонтрагента.Артикул)
		И ЗначениеЗаполнено(Штрихкод) Тогда
		
		НатуральныйИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(ВРег(СтрЗаменить(НоменклатураКонтрагента.Наименование, " ", ""))
																			+ "#" + ВРег(СтрЗаменить(НоменклатураКонтрагента.Артикул, " ", ""))
																			+ "#" + ВРег(СтрЗаменить(Штрихкод, " ", "")));
	Иначе
		
		НатуральныйИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(ВРег(СтрЗаменить(НоменклатураКонтрагента.Наименование, " ", ""))
																			+ "#" + ВРег(СтрЗаменить(НоменклатураКонтрагента.Артикул, " ", "") + "#"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НоменклатураМаркетплейса.КодМагазина) Тогда
		Если ЭтоWildberries И ЗначениеЗаполнено(Штрихкод) Тогда
			НоменклатураКонтрагента.Идентификатор = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(НоменклатураМаркетплейса.КодМагазина + "#" + Штрихкод);
		Иначе
			НоменклатураКонтрагента.Идентификатор = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(НоменклатураМаркетплейса.КодМагазина);
		КонецЕсли;
		НоменклатураКонтрагента.ИсторияИдентификаторов.Добавить(НатуральныйИД);
	Иначе
		НоменклатураКонтрагента.Идентификатор = НатуральныйИД;
	КонецЕсли;
	
	НоменклатураКонтрагента.ШтрихкодКомбинации        = Штрихкод;
	НоменклатураКонтрагента.ШтрихкодыНоменклатуры     = СтрСоединить(НоменклатураМаркетплейса.Штрихкоды, ",");
	НоменклатураКонтрагента.ИдентификаторНоменклатуры = НоменклатураКонтрагента.Идентификатор;
	
	СписокНоменклатурыМаркетплейса.Добавить(НоменклатураКонтрагента);
КонецПроцедуры

Функция ПодготовитьПорцииДляОбмена(ТаблицаТовары, РазмерПорции) Экспорт
	ПакетыВыгрузки = Новый Массив;
	
	ТекущийПакет = -1;
	Для каждого СтрокаТаблицыТовары Из ТаблицаТовары Цикл
		Если ТекущийПакет = -1 
			ИЛИ ПакетыВыгрузки[ТекущийПакет].Количество() = РазмерПорции Тогда
			
			ПакетыВыгрузки.Добавить(Новый Массив);
			ТекущийПакет = ТекущийПакет + 1;
		КонецЕсли;
		ПакетыВыгрузки[ТекущийПакет].Добавить(СтрокаТаблицыТовары);
	КонецЦикла;

	Возврат ПакетыВыгрузки;
КонецФункции 

Процедура ОбработатьРезультатыСопоставления(РезультатыСопоставления, НастройкаИнтеграции) Экспорт
	Если ТипЗнч(НастройкаИнтеграции) = Тип("СправочникСсылка.НастройкиИнтеграцииМаркетплейс") Тогда
		НастройкаИнтеграцииСвойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаИнтеграции, "Владелец, ВидМаркетплейса");
	Иначе
		НастройкаИнтеграцииСвойства = НастройкаИнтеграции;
	КонецЕсли;
	
	Для каждого РезультатСопоставления Из РезультатыСопоставления Цикл
		РегистрыСведений.НоменклатураМаркетплейсов.ЗаписатьИдентификаторМаркетплейса(
			РезультатСопоставления.НоменклатураИБ.Номенклатура, 
			НастройкаИнтеграцииСвойства.ВидМаркетплейса, 
			РезультатСопоставления.НоменклатураКонтрагента.ИдентификаторНоменклатуры, 
			РезультатСопоставления.НоменклатураКонтрагента.ШтрихкодыНоменклатуры, 
			НастройкаИнтеграцииСвойства.Владелец);
	КонецЦикла;
КонецПроцедуры

Функция ВладелецХранилищаНастроек() Экспорт
	
	Возврат "МаркетплейсыЗагрузкаФайлов";
	
КонецФункции

// Возвращает настройки сопоставления номенклатуры
//
// Параметры:
//  Маркетплейс	 - ПеречислениеСсылка.ВидыМаркетплейсов - маркетплейс
//  ВидДокумента - Строка - вид отчета маркетплейса
// 
// Возвращаемое значение:
//  Структура
//
Функция НастройкиСопоставленияНоменклатуры(Маркетплейс, ВидДокумента = Неопределено) Экспорт
	
	МодульИнтеграции = ОбработчикМаркетплейса(Маркетплейс);
	Возврат МодульИнтеграции.НастройкиСопоставленияНоменклатуры(ВидДокумента);
	
КонецФункции

// Определяет способ загрузки документа
//
// Параметры:
//  ДанныеДокумента	 - Структура - данные отчета маркетплейса
// 
// Возвращаемое значение:
//  Строка - см. ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиЧерезФайл(), СпособЗагрузкиЧерезАПИ(), СпособЗагрузкиФоновый()
//
Функция СпособЗагрузки(ДанныеДокумента) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "СпособЗагрузки", "");
	
КонецФункции

#Область ДанныеМаркетплейсаВПрисоединенномФайле

Процедура ПервичныеДанныеМаркетплейса(ДанныеДокумента, ПервичныеДанные) Экспорт
	
	ДанныеДокумента.Вставить("ПервичныеДанныеМаркетплейса", ПервичныеДанные);
	ДанныеДокумента.Вставить("НачалоТаблицыПервичныхДанныхМаркетплейса", -1);
	ДанныеДокумента.Вставить("КонецТаблицыПервичныхДанныхМаркетплейса", -1);
	
КонецПроцедуры

Процедура ПервичныеДанныеМаркетплейсаИзJSON(ДанныеДокумента, ПервичныеДанные) Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ШапкаТаблицы = Новый Соответствие;
	Для Каждого СтрокаТаблицы Из ПервичныеДанные Цикл
		Для Каждого ДанныеКолонки Из СтрокаТаблицы Цикл
			Если ТипЗнч(ДанныеКолонки.Значение) = Тип("Структура") Тогда
				Для Каждого ДанныеКолонкиПодуровень Из ДанныеКолонки.Значение Цикл
					ИмяКолонки = СтрШаблон("%1_%2", ДанныеКолонки.Ключ, ДанныеКолонкиПодуровень.Ключ);
					Если ТаблицаДанных.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
						ТаблицаДанных.Колонки.Добавить(ИмяКолонки);
						ЗаголовокКолонки = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(
							СтрШаблон("%1.%2", ДанныеКолонки.Ключ, ДанныеКолонкиПодуровень.Ключ));
						ШапкаТаблицы.Вставить(ИмяКолонки, ЗаголовокКолонки);
					КонецЕсли;
				КонецЦикла;
			Иначе
				ИмяКолонки = ДанныеКолонки.Ключ;
				Если ТаблицаДанных.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
					ТаблицаДанных.Колонки.Добавить(ИмяКолонки);
					ШапкаТаблицы.Вставить(ИмяКолонки, ИмяКолонки);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	НоваяСтрока = ТаблицаДанных.Добавить();
	Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
		НоваяСтрока[Колонка.Имя] = ШапкаТаблицы.Получить(Колонка.Имя);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ПервичныеДанные Цикл
		НоваяСтрока = ТаблицаДанных.Добавить();
		Для Каждого ДанныеКолонки Из СтрокаТаблицы Цикл
			Если ТипЗнч(ДанныеКолонки.Значение) = Тип("Структура") Тогда
				Для Каждого ДанныеКолонкиПодуровень Из ДанныеКолонки.Значение Цикл
					ИмяКолонки = СтрШаблон("%1_%2", ДанныеКолонки.Ключ, ДанныеКолонкиПодуровень.Ключ);
					НоваяСтрока[ИмяКолонки] = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(
						Строка(ДанныеКолонкиПодуровень.Значение));
				КонецЦикла;
			Иначе
				ИмяКолонки = ДанныеКолонки.Ключ;
				НоваяСтрока[ИмяКолонки] = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(
					Строка(ДанныеКолонки.Значение));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ПервичныеДанныеМаркетплейса", ТаблицаДанных);
	
КонецПроцедуры

Процедура ПервичныеДанныеМаркетплейсаИзТаблицы(ДанныеДокумента, ТаблицаДанных) Экспорт
	
	ПервичныеДанные = Новый Массив;
	
	Если ТипЗнч(ТаблицаДанных) <> Тип("ТаблицаЗначений")
		Или ТаблицаДанных.Колонки.Найти("ДанныеМаркетплейса") = Неопределено Тогда
		ДанныеДокумента.Вставить("ПервичныеДанныеМаркетплейса", ПервичныеДанные);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		ПервичныеДанные.Добавить(СтрокаДанных.ДанныеМаркетплейса);
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Удалить("ДанныеМаркетплейса");
	
	ПервичныеДанныеМаркетплейсаИзJSON(ДанныеДокумента, ПервичныеДанные);
	
КонецПроцедуры

Процедура НачалоТаблицыПервичныхДанныхМаркетплейса(ДанныеДокумента, ИндексСтроки) Экспорт
	
	ДанныеДокумента.НачалоТаблицыПервичныхДанныхМаркетплейса = ИндексСтроки;
	
КонецПроцедуры

Процедура ОкончаниеТаблицыПервичныхДанныхМаркетплейса(ДанныеДокумента, ИндексСтроки) Экспорт
	
	ДанныеДокумента.КонецТаблицыПервичныхДанныхМаркетплейса = ИндексСтроки;
	
КонецПроцедуры

Процедура ПодготовитьТаблицуПервичныхДанныхМаркетплейса(ДанныеДокумента) Экспорт
	ПервичныеДанныеМаркетплейса = ДанныеДокумента.ПервичныеДанныеМаркетплейса;
	Если ТипЗнч(ПервичныеДанныеМаркетплейса) <> Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоЭлементов = ПервичныеДанныеМаркетплейса.Количество();
	НачалоТаблицы = ДанныеДокумента.НачалоТаблицыПервичныхДанныхМаркетплейса;
	КонецТаблицы  = ДанныеДокумента.КонецТаблицыПервичныхДанныхМаркетплейса;
	Если КонецТаблицы = -1 Тогда
		КонецТаблицы = КоличествоЭлементов - 1;
	КонецЕсли;
	
	Если НачалоТаблицы > -1 И КоличествоЭлементов > НачалоТаблицы И КоличествоЭлементов > КонецТаблицы Тогда
		
		ТаблицаДанных = ПервичныеДанныеМаркетплейса.СкопироватьКолонки();
		Для Индекс = НачалоТаблицы По КонецТаблицы Цикл
			НоваяСтрока = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПервичныеДанныеМаркетплейса[Индекс]);
		КонецЦикла;
		
		// Удаляем полностью пустые колонки
		Если ЗначениеЗаполнено(ТаблицаДанных) Тогда
			КолонкиДляУдаления = Новый Массив;
			Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
				УдалитьКолонку = Истина;
				Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
					Если ЗначениеЗаполнено(СтрокаТаблицы[Колонка.Имя]) Тогда
						УдалитьКолонку = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если УдалитьКолонку Тогда
					КолонкиДляУдаления.Добавить(Колонка.Имя);
				КонецЕсли;
			КонецЦикла;
			Для Каждого ИмяКолонки Из КолонкиДляУдаления Цикл
				ТаблицаДанных.Колонки.Удалить(ИмяКолонки);
			КонецЦикла;
		КонецЕсли;
		
		ПервичныеДанныеМаркетплейса = ТаблицаДанных;
	КонецЕсли;
	
	ДанныеДокумента.ПервичныеДанныеМаркетплейса = ПервичныеДанныеМаркетплейса;
	
КонецПроцедуры

Функция ОтобратьПервичныеДанныеМаркетплейса(ПервичныеДанныеМаркетплейса, КлючиПоиска, Значение) Экспорт
	
	ПервичныеДанные = ПервичныеДанныеМаркетплейса.СкопироватьКолонки();
	
	Если ЗначениеЗаполнено(ПервичныеДанныеМаркетплейса) Тогда
		СтрокаЗаголовка = ПервичныеДанныеМаркетплейса[0];
		НоваяСтрока = ПервичныеДанные.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗаголовка);
		
		Ключи = СтрРазделить(КлючиПоиска,",");
		Для Каждого Ключ Из Ключи Цикл
			КолонкаКлюча = Неопределено;
			Для Каждого Колонка Из ПервичныеДанныеМаркетплейса.Колонки Цикл
				ПодготовленнаяСтрока = ЭлектронноеВзаимодействиеБП.ПодготовитьСтроку(СтрокаЗаголовка[Колонка.Имя]);
				Если СтрНайти(ПодготовленнаяСтрока, НРег(Ключ)) > 0 Тогда
					КолонкаКлюча = Колонка.Имя;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(КолонкаКлюча) Тогда
				ОтбранныеСтроки = ПервичныеДанныеМаркетплейса.Скопировать(Новый Структура(КолонкаКлюча, Значение));
				Для Каждого СтрокаТаблицы Из ОтбранныеСтроки Цикл
					НоваяСтрока = ПервичныеДанные.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПервичныеДанные;
	
КонецФункции

// Создает присоединенный к документу файл с данными маркетплейса
//
// Параметры:
//  Документ					- ДокументСсылка - документ для которого формируется присоединенный файл
//  ПервичныеДанныеМаркетплейса - Массив - коллекция структур
//  СпособЗагрузки		 		- Строка - см. ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиЧерезФайл(), СпособЗагрузкиЧерезАПИ(), СпособЗагрузкиФоновый()
//
Процедура СохранитьДанныеМаркетплейсаВПрисоединенномФайле(Документ, ПервичныеДанныеМаркетплейса, СпособЗагрузки) Экспорт
	
	// Сохраняем данные маркетплейса только при фоновой загрузке или через АПИ
	Если СпособЗагрузки = ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиЧерезФайл() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = Формат(Документ.Дата, "ДФ=dd-MM-yyyy");

	Файлы = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Документ, Файлы);

	ПрисоединенныйФайл = Неопределено;
	ДанныеФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Файлы, "Ссылка, Наименование, ПометкаУдаления");
	Для Каждого ДанныеФайла Из ДанныеФайлов Цикл
		Если ДанныеФайла.Значение.Наименование = ИмяФайла
			И Не ДанныеФайла.Значение.ПометкаУдаления Тогда
			ПрисоединенныйФайл = ДанныеФайла.Ключ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Если ЗначениеЗаполнено(ПервичныеДанныеМаркетплейса) Тогда
		СтрокаЗаголовка = ПервичныеДанныеМаркетплейса[0];
		
		НомерКолонки = 0;
		Для Каждого Колонка Из ПервичныеДанныеМаркетплейса.Колонки Цикл
			НомерКолонки = НомерКолонки + 1;
			ДобавитьЗаголовокТаблицыДанныхМаркетплейса(
				ТабличныйДокумент,
				НомерКолонки,
				СтрокаЗаголовка[Колонка.Имя]);
		КонецЦикла;
		
		НомерСтроки = 2;
		Для Индекс = 1 По ПервичныеДанныеМаркетплейса.Количество() - 1 Цикл
			СтрокаТаблицы = ПервичныеДанныеМаркетплейса[Индекс];
			НомерСтроки = НомерСтроки + 1;
		
			НомерКолонки = 0;
			Для Каждого Колонка Из ПервичныеДанныеМаркетплейса.Колонки Цикл
				НомерКолонки = НомерКолонки + 1;
				
				ДобавитьЯчейкуТаблицыДанныхМаркетплейса(
					ТабличныйДокумент,
					НомерСтроки,
					НомерКолонки,
					СтрокаТаблицы[Колонка.Имя]);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("mxl");
	ТабличныйДокумент.Записать(ИмяВременногоФайла);
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	// При загрузке на фреше файлы в базе не храним
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(
			СтрШаблон("ОбменСМаркетплейс.ДобавлениеФайла.%1", Формат(ДвоичныеДанные.Размер(), "ЧГ=0")));
		Возврат;
	КонецЕсли;
	
	АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыФайла.ВладелецФайлов = Документ;
	ПараметрыФайла.ИмяБезРасширения = ИмяФайла;
	ПараметрыФайла.РасширениеБезТочки = "mxl";
	ПараметрыФайла.Автор = Пользователи.ТекущийПользователь();
	
	Если ПрисоединенныйФайл = Неопределено Тогда
		РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайлаВоВременномХранилище);
	Иначе
		ИнформацияОФайле = Новый Структура;
		ИнформацияОФайле.Вставить("АдресФайлаВоВременномХранилище", АдресФайлаВоВременномХранилище);
		ИнформацияОФайле.Вставить("АдресВременногоХранилищаТекста", "");
		РаботаСФайлами.ОбновитьФайл(ПрисоединенныйФайл, ИнформацияОФайле);
	КонецЕсли;
	
КонецПроцедуры

// Формирование ячейки заголовка таблицы файла с данными маркетплейса
//
// Параметры:
//  ТабличныйДокумент	 - ТабличныйДокумент - содержимое файла с данными маркетплейса
//  НомерКолонки		 - Число - номер колонки
//  ТекстЗаголовка		 - Строка - текст ячейки
//
Процедура ДобавитьЗаголовокТаблицыДанныхМаркетплейса(ТабличныйДокумент, НомерКолонки, ТекстЗаголовка)
	
	НомерСтроки = 2;
	Рамка = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	
	Область = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
	Область.Шрифт = ШрифтыСтиля.ВажнаяНадписьШрифт;
	Область.Текст = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ТекстЗаголовка);
	Область.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
	Область.РастягиватьПоГоризонтали = Истина;
	Область.ГраницаСверху = Рамка;
	Область.ГраницаСлева = Рамка;
	Область.ГраницаСнизу = Рамка;
	Область.ГраницаСправа = Рамка;
	Область.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	
КонецПроцедуры

// Формирование ячейки таблицы файла с данными маркетплейса
//
// Параметры:
//  ТабличныйДокумент	 - ТабличныйДокумент - содержимое файла с данными маркетплейса
//  НомерКолонки		 - Число - номер колонки
//  ТекстЗаголовка		 - Строка - текст ячейки
//
Процедура ДобавитьЯчейкуТаблицыДанныхМаркетплейса(ТабличныйДокумент, НомерСтроки, НомерКолонки, Значение)
	
	Рамка = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	
	Область = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
	Область.Текст = Значение;
	Область.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
	Область.РастягиватьПоГоризонтали = Истина;
	Область.ГраницаСверху = Рамка;
	Область.ГраницаСлева = Рамка;
	Область.ГраницаСнизу = Рамка;
	Область.ГраницаСправа = Рамка;
	Область.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	
КонецПроцедуры

#КонецОбласти

#Область АвтозагрузкаОтчетовМаркетплейсов

// Определяет варианты настроек интеграции с маркетплейсами с точки зрения периодичности их вызова
// 
// Возвращаемое значение:
//  Структура:
//   * Еженедельно - Булево
//   * Ежемесячно - Булево
//   * МаркетплейсыЕженедельно - Строка
//   * МаркетплейсыЕжемесячно - Строка
//
Функция ПериодичностьОтчетовМаркетплейсов() Экспорт
	
	Результат = Новый Структура("Еженедельно, Ежемесячно, МаркетплейсыЕженедельно, МаркетплейсыЕжемесячно",
		Ложь, Ложь, "", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса КАК Маркетплейс,
	|	НастройкиИнтеграцииМаркетплейс.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НЕ НастройкиИнтеграцииМаркетплейс.ПометкаУдаления";
	
	МаркетплейсыЕжемесячно = Новый Массив;
	МаркетплейсыЕженедельно = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МодульИнтеграции = ОбработчикМаркетплейса(Выборка.Маркетплейс);
		Если МодульИнтеграции.ИнтеграцияНастроена(Выборка.Ссылка) Тогда
			Если Выборка.Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries Тогда
				Результат.Еженедельно = Истина;
				МаркетплейсыЕженедельно.Добавить(Строка(Выборка.Маркетплейс));
			Иначе
				Результат.Ежемесячно = Истина;
				МаркетплейсыЕжемесячно.Добавить(Строка(Выборка.Маркетплейс));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	МаркетплейсыЕжемесячно = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МаркетплейсыЕжемесячно);
	МаркетплейсыЕженедельно = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МаркетплейсыЕженедельно);
	
	Результат.МаркетплейсыЕжемесячно = СтрСоединить(МаркетплейсыЕжемесячно, " и ");
	Результат.МаркетплейсыЕженедельно = СтрСоединить(МаркетплейсыЕженедельно, " и ");
	
	Возврат Результат;
	
КонецФункции

// Определяет включена ли автоматическая загрузка отчетов маркетплейсов
// 
// Возвращаемое значение:
//  Булево
//
Функция АвтозагрузкаОтчетовМаркетплейсовВключена() Экспорт
	
	Возврат РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов.АвтозагрузкаВключена();
	
КонецФункции

// Выполняет автозагрузку отчетов маркетплейсов
//
// Параметры:
//  ТипЗагрузки	 - ПеречислениеСсылка.Периодичность - еженедельная или ежемесячная загрузка
// 
// Возвращаемое значение:
//  Булево - Ложь, если необходимо повторить вызов этой функции
//
Функция ВыполнитьАвтозагрузкуОтчетовМаркетплейсов(ТипЗагрузки) Экспорт
	
	ТребуетсяПовторение = Ложь;
	Если Не АвтозагрузкаОтчетовМаркетплейсовВключена() Тогда
		Возврат Не ТребуетсяПовторение;
	КонецЕсли;
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	ЖурналОбмена = РегистрыСведений.ЖурналОбменаСМаркетплейсами;
	
	НастройкиИнтеграции = НастройкиИнтеграции(ТипЗагрузки);
	Если ЗначениеЗаполнено(НастройкиИнтеграции) Тогда
		ЖурналОбмена.ВключитьЛогированиеАвтозагрузки();
	КонецЕсли;
	
	НовыеДокументы = Новый Массив;
	
	Для Каждого Интеграция Из НастройкиИнтеграции Цикл
		ПараметрыИнтеграции = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Интеграция);
		
		ПериодыАвтозагрузки = ПериодыАвтозагрузки(ПараметрыИнтеграции);
		Для Каждого Период Из ПериодыАвтозагрузки Цикл
			ЗаполнитьЗначенияСвойств(ПараметрыИнтеграции, Период);
			
			КодОтветаСервера = 0;
			МодульИнтеграции = ОбработчикМаркетплейса(ПараметрыИнтеграции.Маркетплейс);
			ДанныеМаркеплейса = МодульИнтеграции.ПолучитьДанныеДокумента(ПараметрыИнтеграции, КодОтветаСервера, "");
			Если ДанныеМаркеплейса = Неопределено Тогда
				ТребуетсяПовторение = ТребуетсяПовторение Или КодОтветаСервера <> 200;
				Продолжить;
			КонецЕсли;
			
			СозданныеДокументы = МодульИнтеграции.АвтозагрузкаДокументов(ПараметрыИнтеграции, ДанныеМаркеплейса);
			ПровестиДокументыАвтозагрузки(СозданныеДокументы, ПараметрыИнтеграции.Маркетплейс, ПараметрыИнтеграции.Организация);
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НовыеДокументы, СозданныеДокументы);
		КонецЦикла;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НовыеДокументы) Тогда
		// Предупреждения для отчетов маркетплейсов
		Предупреждения.ОбновитьДляОтчетовМаркетплейсов();
		НастройкиАвтозагрузки.СохранитьДатуПоследнейЗагрузки(ТипЗагрузки);
	КонецЕсли;
	
	Возврат Не ТребуетсяПовторение;
	
КонецФункции

Процедура ЗапуститьАвтозагрузку(ПараметрыФоновогоЗадания, АдресРезультата) Экспорт
	
	ОбменСМаркетплейс.ВыполнитьАвтозагрузкуОтчетовМаркетплейсов(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц);
	ОбменСМаркетплейс.ВыполнитьАвтозагрузкуОтчетовМаркетплейсов(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя);
	
КонецПроцедуры

// Определяет является ли способ загрузки загрузкой в фоне
//
// Параметры:
//  СпособЗагрузки	 - Строка - один из вариантов способа загрузки
// 
// Возвращаемое значение:
//  Булево
//
Функция ЭтоАвтозагрузка(СпособЗагрузки) Экспорт
	
	Возврат СпособЗагрузки = ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиФоновый();
	
КонецФункции

// Проводит документы, созданные в процессе автозагрузки
//
// Параметры:
//  СозданныеДокументы				 - Массив - документы для проведения
//  Маркетплейс						 - ПеречислениеСсылка.ВидыМаркетплейсов - маркетплейс
//  Организация						 - СправочникСсылка.Организации, Неопределено - организация
//  ДокументИсточникСопоставления	 - ДокументСсылка, Неопределено - документ из формы которого вызвана процедура
//
Процедура ПровестиДокументыАвтозагрузки(СозданныеДокументы, Маркетплейс, Организация = Неопределено, ДокументИсточникСопоставления = Неопределено) Экспорт
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	
	ДокументыБезПредупреждений = Предупреждения.ДокументыБезПредупреждений(СозданныеДокументы);
	// не проводим документ, из которого вызвано сопоставление номенклатуры
	ДобавитьДокументИсточникВНепроведенные = Ложь;
	Если ДокументИсточникСопоставления <> Неопределено
		И ДокументыБезПредупреждений.Найти(ДокументИсточникСопоставления) <> Неопределено Тогда
		ДобавитьДокументИсточникВНепроведенные = Истина;
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ДокументыБезПредупреждений, ДокументИсточникСопоставления);
	КонецЕсли;
	НепроведенныеДокументы = ОбщегоНазначения.ПровестиДокументы(ДокументыБезПредупреждений);
	Если ДобавитьДокументИсточникВНепроведенные Тогда
		НепроведенныеДокументы.Добавить(Новый Структура("Ссылка,ОписаниеОшибки", ДокументИсточникСопоставления, ""));
	КонецЕсли;
	Предупреждения.СохранитьНепроведенныеДокументы(
		Организация,
		Маркетплейс,
		НепроведенныеДокументы);
	
КонецПроцедуры

// Возвращает параметры подключенных интеграций с маркетплейсами
//
// Параметры:
//  ТипЗагрузки	 - ПеречислениеСсылка.Периодичность - 
// 
// Возвращаемое значение:
//  Структура
//
Функция НастройкиИнтеграции(ТипЗагрузки)
	
	СпособСверткиОтчетов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ВладелецХранилищаНастроек(), "СпособСверткиОтчетов", "Построчно");
	ГруппаДляНовыхКонтрагентов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ВладелецХранилищаНастроек(), "ГруппаДляНовыхКонтрагентов");
	
	Маркетплейсы = Новый Массив;
	Если ТипЗагрузки = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя Тогда
		Маркетплейсы.Добавить(Перечисления.ВидыМаркетплейсов.Wildberries);
	Иначе
		Маркетплейсы.Добавить(Перечисления.ВидыМаркетплейсов.OZON);
		Маркетплейсы.Добавить(Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СпособСверткиОтчетов", СпособСверткиОтчетов);
	Запрос.УстановитьПараметр("ГруппаДляНовыхКонтрагентов", ГруппаДляНовыхКонтрагентов);
	Запрос.УстановитьПараметр("Маркетплейсы", Маркетплейсы);
	Запрос.УстановитьПараметр("СпособЗагрузки", ОбменСМаркетплейсКлиентСервер.СпособЗагрузкиФоновый());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииМаркетплейс.Владелец КАК Организация,
	|	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса КАК Маркетплейс,
	|	НастройкиИнтеграцииМаркетплейс.Ссылка КАК НастройкаИнтеграции,
	|	НастройкиИнтеграцииМаркетплейс.Контрагент КАК Контрагент,
	|	НастройкиИнтеграцииМаркетплейс.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НастройкиИнтеграцииМаркетплейс.ПлатежныйАгент КАК ПлатежныйАгент,
	|	НастройкиИнтеграцииМаркетплейс.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	НастройкиИнтеграцииМаркетплейс.ИдентификаторСклада КАК ИдентификаторСклада,
	|	НастройкиИнтеграцииМаркетплейс.НаименованиеСклада КАК НаименованиеСклада,
	|	&СпособСверткиОтчетов КАК СпособСверткиОтчетов,
	|	&ГруппаДляНовыхКонтрагентов КАК ГруппаДляНовыхКонтрагентов,
	|	ЛОЖЬ КАК ИнтеграцияНастроена,
	|	&СпособЗагрузки КАК СпособЗагрузки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК НачалоПериода,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОкончаниеПериода,
	|	НастройкиИнтеграцииМаркетплейс.ДатаНачалаАвтозагрузки КАК ДатаНачалаАвтозагрузки,
	|	НастройкиИнтеграцииМаркетплейс.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_Настройки
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НЕ НастройкиИнтеграцииМаркетплейс.ПометкаУдаления
	|	И НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса В(&Маркетплейсы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиИнтеграцииМаркетплейсСкладыМаркетплейс.Ссылка КАК Ссылка,
	|	НастройкиИнтеграцииМаркетплейсСкладыМаркетплейс.Наименование КАК Наименование,
	|	НастройкиИнтеграцииМаркетплейсСкладыМаркетплейс.ИдентификаторСклада КАК ИдентификаторСклада
	|ПОМЕСТИТЬ ВТ_Склады
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс.СкладыМаркетплейс КАК НастройкиИнтеграцииМаркетплейсСкладыМаркетплейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Настройки.Организация КАК Организация,
	|	ВТ_Настройки.Маркетплейс КАК Маркетплейс,
	|	ВТ_Настройки.НастройкаИнтеграции КАК НастройкаИнтеграции,
	|	ВТ_Настройки.Контрагент КАК Контрагент,
	|	ВТ_Настройки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_Настройки.ПлатежныйАгент КАК ПлатежныйАгент,
	|	ВТ_Настройки.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	ЕСТЬNULL(ВТ_Склады.ИдентификаторСклада, ВТ_Настройки.ИдентификаторСклада) КАК ИдентификаторСклада,
	|	ЕСТЬNULL(ВТ_Склады.Наименование, ВТ_Настройки.НаименованиеСклада) КАК НаименованиеСклада,
	|	ВТ_Настройки.СпособСверткиОтчетов КАК СпособСверткиОтчетов,
	|	ВТ_Настройки.ГруппаДляНовыхКонтрагентов КАК ГруппаДляНовыхКонтрагентов,
	|	ВТ_Настройки.ИнтеграцияНастроена КАК ИнтеграцияНастроена,
	|	ВТ_Настройки.СпособЗагрузки КАК СпособЗагрузки,
	|	ВТ_Настройки.НачалоПериода КАК НачалоПериода,
	|	ВТ_Настройки.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ВТ_Настройки.ДатаНачалаАвтозагрузки КАК ДатаНачалаАвтозагрузки,
	|	ВТ_Настройки.Склад КАК Склад
	|ИЗ
	|	ВТ_Настройки КАК ВТ_Настройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Склады КАК ВТ_Склады
	|		ПО ВТ_Настройки.НастройкаИнтеграции = ВТ_Склады.Ссылка";
	
	НастройкиИнтеграции = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТаблицы Из НастройкиИнтеграции Цикл
		МодульИнтеграции = ОбработчикМаркетплейса(СтрокаТаблицы.Маркетплейс);
		СтрокаТаблицы.ИнтеграцияНастроена = МодульИнтеграции.ИнтеграцияНастроена(
			СтрокаТаблицы.НастройкаИнтеграции);
	КонецЦикла;
	
	Возврат НастройкиИнтеграции.Скопировать(Новый Структура("ИнтеграцияНастроена", Истина));
	
КонецФункции

// Возвращает периоды доступные для автозагрузки отчетов маркетплейсов
//
// Параметры:
//  Интеграция - СправочникСсылка.НастройкиИнтеграцииМаркетплейс
// 
// Возвращаемое значение:
//  Массив - структур со свойствами:
//     * НачалоПериода - дата начала периода
//     * КонецПериода - дата окончания периода
//
Функция ПериодыАвтозагрузки(Интеграция)
	
	КоличествоМесяцев = 3; // глубина проверки
	
	ИнтервалНеделя = (Интеграция.Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries);
	
	ТекущаяДата = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	Если ИнтервалНеделя Тогда
		ДатаОкончания = НачалоНедели(ТекущаяДата);
		ДатаНачала = НачалоНедели(ДобавитьМесяц(ДатаОкончания, -КоличествоМесяцев));
	Иначе
		ДатаОкончания = НачалоМесяца(ТекущаяДата);
		ДатаНачала = ДобавитьМесяц(ДатаОкончания, -КоличествоМесяцев);
	КонецЕсли;
	ДатаНачала = Макс(Интеграция.ДатаНачалаАвтозагрузки, ДатаНачала);
	
	ПериодыАвтозагрузки = Новый Массив;
	
	ПроверкаЗапрета = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	ДанныеДляПроверки = ПроверкаЗапрета.Добавить();
	ДанныеДляПроверки.Раздел  = ДатыЗапретаИзмененияБП.РазделБухгалтерскийУчет();
	ДанныеДляПроверки.Объект  = Интеграция.Организация;
	Пока ДатаНачала < ДатаОкончания Цикл
		ДанныеДляПроверки.Дата = ДатаНачала;
		Если Не ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ПроверкаЗапрета) Тогда
			ПериодыАвтозагрузки.Добавить(ДатаНачала);
		КонецЕсли;
		Если ИнтервалНеделя Тогда
			ДатаНачала = ДатаНачала + 7 * 24 * 60 * 60;
		Иначе
			ДатаНачала = ДобавитьМесяц(ДатаНачала, 1);
		КонецЕсли;
	КонецЦикла;
	
	МодульИнтеграции = ОбработчикМаркетплейса(Интеграция.Маркетплейс);
	Возврат МодульИнтеграции.ПериодыАвтозагрузки(Интеграция, ПериодыАвтозагрузки);
	
КонецФункции

Функция РазрешитьУдалениеЗаписейЖурналаОбмена() Экспорт
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	ДатаАвтозагрузки = НастройкиАвтозагрузки.ДатаПоследнейЗагрузки();
	ЕстьОшибкаЗагрузки = НастройкиАвтозагрузки.ЕстьОшибкаЗагрузки()
		И ДатаАвтозагрузки > ОбменСМаркетплейсВызовСервера.ПолучитьДатуЗакрытияБаннераСостояниеАвтозагрузки();
	ЕстьПредупреждения = Предупреждения.ЕстьПредупреждения();
	
	РазрешитьУдалениеЗаписейЖурналаОбмена = Не(ЕстьПредупреждения Или ЕстьОшибкаЗагрузки);
	
	Возврат РазрешитьУдалениеЗаписейЖурналаОбмена;
	
КонецФункции

#КонецОбласти

#Область ОтложенноеСопоставлениеНоменклатуры

// Добавляет строку в коллекцию незаполненной номенклатуры
//
// Параметры:
//  НезаполненнаяНоменклатура	 - Массив
//  СтрокаТовара				 - СтрокаТаблицыЗначений - строка таблицы товаров
//  ИмяТабличнойЧасти			 - Строка - имя табличной части документа
//  ИндексТабличнойЧасти		 - Число - индекс строки табличной части документа
//
Процедура ДобавитьНезаполненнуюНоменклатуру(НезаполненнаяНоменклатура, СтрокаТовара, ИмяТабличнойЧасти, ИндексТабличнойЧасти) Экспорт
	
	НезаполненнаяНоменклатура.Добавить(
		Новый Структура("СтрокаТовара, ИмяТабличнойЧасти, ИндексТабличнойЧасти",
			СтрокаТовара, ИмяТабличнойЧасти, ИндексТабличнойЧасти));
	
КонецПроцедуры

// Сохраняет данные для отложенного сопоставления номенклатуры
//
// Параметры:
//  НезаполненнаяНоменклатура	 - Массив - см. ДобавитьНезаполненнуюНоменклатуру
//  Документ					 - ДокументСсылка - документ с незаполненной номенклатурой
//  Контрагент					 - СправочникСсылка.Контрагенты - маркетплейс
//  ДанныеОтчета				 - Структура - данные отчета маркетплейса
//
Процедура СохранитьНесопоставленнуюНоменклатуру(НезаполненнаяНоменклатура, Документ, Контрагент, ДанныеОтчета) Экспорт
	
	МодульИнтеграции = ОбработчикМаркетплейса(ДанныеОтчета.Маркетплейс);
	НесопоставленнаяНоменклатура = МодульИнтеграции.ПодготовитьОтложенноеСопоставлениеНоменклатуры(
		Контрагент, НезаполненнаяНоменклатура);
	
	// Предупреждения
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	Если ЗначениеЗаполнено(НесопоставленнаяНоменклатура) Тогда
		Параметры = Предупреждения.НовыеПараметрыПредупреждения(
			Перечисления.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НесопоставленаНоменклатура);
		Параметры.ДанныеОтчета = Новый ХранилищеЗначения(ДанныеОтчета, Новый СжатиеДанных(9));
	Иначе
		Параметры = Предупреждения.НовыеПараметрыПредупреждения();
	КонецЕсли;
	Параметры.Документ = Документ;
	Параметры.Организация = ДанныеОтчета.Организация;
	Параметры.Маркетплейс = ДанныеОтчета.Маркетплейс;
	Предупреждения.ЗаписатьПредупреждение(Параметры);
	
	// Несопоставленные объекты
	НаборЗаписей = РегистрыСведений.НесопоставленныеОбъектыМаркетплейса.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	Если ЗначениеЗаполнено(НесопоставленнаяНоменклатура) Тогда
		ДанныеОбъекта = НовыйПараметрыСопоставленияНоменклатуры();
		ДанныеОбъекта.НоменклатураКонтрагента = НесопоставленнаяНоменклатура;
		ДанныеОбъекта.НастройкиСопоставленияНоменклатуры =
			НастройкиСопоставленияНоменклатуры(ДанныеОтчета.Маркетплейс, ДанныеОтчета.ВидДокумента);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Документ = Документ;
		НоваяЗапись.ДанныеОбъекта = Новый ХранилищеЗначения(ДанныеОбъекта, Новый СжатиеДанных(9));
	КонецЕсли;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Сохраняет данные для отложенного сопоставления номенклатуры для Яндекс.Маркет
//
// Параметры:
//  НезаполненнаяНоменклатура	 - Массив - см. ДобавитьНезаполненнуюНоменклатуру
//  Документ					 - ДокументСсылка - документ с незаполненной номенклатурой
//  ДанныеФайла					 - Структура - данные отчета маркетплейса
//  ПараметрыОтчета				 - Структура - параметры отчета маркетплейса
//
Процедура СохранитьНесопоставленнуюНоменклатуруЯндекс(НезаполненнаяНоменклатура, Документ, ДанныеФайла, ПараметрыОтчета) Экспорт
	
	ПараметрыДокумента = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыОтчета);
	ПараметрыДокумента.Вставить("Ссылка", Документ);
	
	НесопоставленнаяНоменклатура = ИнтеграцияЯндексМаркет.ПодготовитьОтложенноеСопоставлениеНоменклатуры(
		ПараметрыДокумента.Контрагент, НезаполненнаяНоменклатура);
	
	// Предупреждения
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	Если ЗначениеЗаполнено(НесопоставленнаяНоменклатура) Тогда
		ДанныеОтчета = Новый Структура("ДанныеФайла, ПараметрыДокумента", ДанныеФайла, ПараметрыДокумента);
		Параметры = Предупреждения.НовыеПараметрыПредупреждения(
			Перечисления.ТипыПроблемАвтозагрузкиОтчетовМаркетплейсов.НесопоставленаНоменклатура);
		Параметры.ДанныеОтчета = Новый ХранилищеЗначения(ДанныеОтчета, Новый СжатиеДанных(9));
	Иначе
		Параметры = Предупреждения.НовыеПараметрыПредупреждения();
	КонецЕсли;
	Параметры.Документ = Документ;
	Параметры.Организация = ПараметрыДокумента.Организация;
	Параметры.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет;
	Предупреждения.ЗаписатьПредупреждение(Параметры);
	
	// Несопоставленные объекты
	НаборЗаписей = РегистрыСведений.НесопоставленныеОбъектыМаркетплейса.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	Если ЗначениеЗаполнено(НесопоставленнаяНоменклатура) Тогда
		ДанныеОбъекта = НовыйПараметрыСопоставленияНоменклатуры();
		ДанныеОбъекта.НоменклатураКонтрагента = НесопоставленнаяНоменклатура;
		ДанныеОбъекта.НастройкиСопоставленияНоменклатуры =
			НастройкиСопоставленияНоменклатуры(Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Документ = Документ;
		НоваяЗапись.ДанныеОбъекта = Новый ХранилищеЗначения(ДанныеОбъекта, Новый СжатиеДанных(9));
	КонецЕсли;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает список маркетплейсов, по которым требуется отложенное сопоставление номенклатуры
// 
// Возвращаемое значение:
//  Массив - перечень маркетплейсов
//
Функция МаркеплейсыКСопоставлениюНоменклатуры() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Организации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Предупреждения.Маркетплейс КАК Маркетплейс
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НесопоставленныеОбъектыМаркетплейса КАК НесопоставленныеОбъекты
	|		ПО Предупреждения.Документ = НесопоставленныеОбъекты.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Организации КАК ВТ_Организации
	|		ПО Предупреждения.Организация = ВТ_Организации.Ссылка
	|ГДЕ
	|	НЕ НесопоставленныеОбъекты.Документ ЕСТЬ NULL
	|	И НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Маркетплейс");
	
КонецФункции

// Возвращает данные, необходимые для отложенного соспоставления номенклатуры
//
// Параметры:
//  Отбор	 - ПеречислениеСсылка.ВидыМаркетплейсов - по всем документам маркетплейса
//			 - ДокументСсылка.ОтчетМаркетплейса - по всем документам, подчиненным документу ОтчетМаркетплейса
//			 - ДокументСсылка - по отдельному документу
// 
// Возвращаемое значение:
//  Структура - см. НовыйПараметрыСопоставленияНоменклатуры
//
Функция ДанныеДляСопоставленияНоменклатуры(Отбор) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Организации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.Документ КАК Документ,
	|	Предупреждения.Организация КАК Организация,
	|	Предупреждения.Маркетплейс КАК Маркетплейс
	|ПОМЕСТИТЬ ВТ_ДокументыМаркетплейсов
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Организации КАК ВТ_Организации
	|		ПО Предупреждения.Организация = ВТ_Организации.Ссылка
	|ГДЕ
	|	НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НесопоставленныеОбъекты.Документ КАК Документ,
	|	ВТ_ДокументыМаркетплейсов.Организация КАК Организация,
	|	НесопоставленныеОбъекты.ДанныеОбъекта КАК ДанныеОбъекта,
	|	ВТ_ДокументыМаркетплейсов.Маркетплейс КАК Маркетплейс
	|ИЗ
	|	РегистрСведений.НесопоставленныеОбъектыМаркетплейса КАК НесопоставленныеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыМаркетплейсов КАК ВТ_ДокументыМаркетплейсов
	|		ПО НесопоставленныеОбъекты.Документ = ВТ_ДокументыМаркетплейсов.Документ
	|ГДЕ
	|	НесопоставленныеОбъекты.Документ В
	|			(ВЫБРАТЬ
	|				ВТ_ДокументыМаркетплейсов.Документ КАК Документ
	|			ИЗ
	|				ВТ_ДокументыМаркетплейсов КАК ВТ_ДокументыМаркетплейсов)";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ОператорыЗапроса = СхемаЗапроса.ПакетЗапросов[1].Операторы[0];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отбор", Отбор);
	
	Если ТипЗнч(Отбор) = Тип("ПеречислениеСсылка.ВидыМаркетплейсов") Тогда
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Маркетплейс = &Отбор");
	ИначеЕсли ТипЗнч(Отбор) = Тип("ДокументСсылка.ОтчетМаркетплейса") Тогда
		ОператорыЗапроса.Отбор.Добавить(
			"ВЫБОР
			|	КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
			|		ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияТоваровУслуг).ОтчетМаркетплейса
			|	КОГДА Предупреждения.Документ ССЫЛКА Документ.РеализацияОтгруженныхТоваров
			|		ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.РеализацияОтгруженныхТоваров).ОтчетМаркетплейса
			|	КОГДА Предупреждения.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
			|		ТОГДА ВЫРАЗИТЬ(Предупреждения.Документ КАК Документ.ВозвратТоваровОтПокупателя).ОтчетМаркетплейса
			|	КОНЕЦ = &Отбор");
	Иначе
		ОператорыЗапроса.Отбор.Добавить("Предупреждения.Документ = &Отбор");
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеДляСопоставления = НовыйПараметрыСопоставленияНоменклатуры();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ДанныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НесопоставленныеДанные = Выборка.ДанныеОбъекта.Получить();
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ДанныеДляСопоставления.НоменклатураКонтрагента,
			НесопоставленныеДанные.НоменклатураКонтрагента);
		ДанныеДляСопоставления.НастройкиСопоставленияНоменклатуры =
			НесопоставленныеДанные.НастройкиСопоставленияНоменклатуры;
			
		// Для заполнения регистра НоменклатураМаркетплейсов
		ДанныеПоОрганизации = ДанныеДляСопоставления.НоменклатураОрганизаций.Получить(Выборка.Организация);
		Если ДанныеПоОрганизации = Неопределено Тогда
			ДанныеПоОрганизации = Новый Массив;
			ДанныеДляСопоставления.НоменклатураОрганизаций.Вставить(Выборка.Организация, ДанныеПоОрганизации);
		КонецЕсли;
		Для Каждого Номенклатура Из НесопоставленныеДанные.НоменклатураКонтрагента Цикл
			КлючПоиска = Новый Структура("ИдентификаторНоменклатуры, ШтрихкодыНоменклатуры");
			ЗаполнитьЗначенияСвойств(КлючПоиска, Номенклатура);
			ДанныеПоОрганизации.Добавить(КлючПоиска);
		КонецЦикла;
	КонецЦикла;
		
	Возврат ДанныеДляСопоставления;
	
КонецФункции

// Заполняет ранее несопоставленную номенклатуру в документах (отложенное сопоставление)
//
// Параметры:
//  Маркетплейс						 - ПеречислениеСсылка.ВидыМаркетплейсов - маркетплейс
//  ИдентификаторыКонтрагента		 - Массив - идентификаторы номенклатуры контрагента
//  ДокументИсточникСопоставления	 - ДокументСсылка - документ из которого вызвано отложенное соспоставление
//
Процедура ОбновитьНесопоставленнуюНоменклатуру(Маркетплейс, ИдентификаторыКонтрагента, ДокументИсточникСопоставления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Маркетплейс", Маркетплейс);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Предупреждения.Документ КАК Документ
	|ПОМЕСТИТЬ ВТ_ДокументыМаркетплейсов
	|ИЗ
	|	РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|ГДЕ
	|	Предупреждения.Маркетплейс = &Маркетплейс
	|	И НЕ ЕСТЬNULL(Предупреждения.Документ.ПометкаУдаления, ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НесопоставленныеОбъекты.Документ КАК Документ,
	|	НесопоставленныеОбъекты.ДанныеОбъекта КАК ДанныеОбъекта,
	|	Предупреждения.ДанныеОтчета КАК ДанныеМаркетплейса,
	|	ВЫБОР
	|		КОГДА НесопоставленныеОбъекты.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА 1
	|		КОГДА НесопоставленныеОбъекты.Документ ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА 2
	|		КОГДА НесопоставленныеОбъекты.Документ ССЫЛКА Документ.РеализацияОтгруженныхТоваров
	|			ТОГДА 3
	|		КОГДА НесопоставленныеОбъекты.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|			ТОГДА 4
	|		ИНАЧЕ 10
	|	КОНЕЦ КАК ПорядокСортировки
	|ИЗ
	|	РегистрСведений.НесопоставленныеОбъектыМаркетплейса КАК НесопоставленныеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
	|		ПО НесопоставленныеОбъекты.Документ = Предупреждения.Документ
	|ГДЕ
	|	НесопоставленныеОбъекты.Документ В
	|			(ВЫБРАТЬ
	|				ВТ_ДокументыМаркетплейсов.Документ КАК Документ
	|			ИЗ
	|				ВТ_ДокументыМаркетплейсов КАК ВТ_ДокументыМаркетплейсов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокСортировки";
	
	ПересоздаваемыеДокументы = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеОбъекта = Выборка.ДанныеОбъекта.Получить();
		
		Для Каждого СтрокаТаблицы Из ДанныеОбъекта.НоменклатураКонтрагента Цикл
			Если ИдентификаторыКонтрагента.Найти(СтрокаТаблицы.Идентификатор) <> Неопределено Тогда
				Если ЗначениеЗаполнено(Выборка.ДанныеМаркетплейса) Тогда
					ДанныеМаркетплейса = Выборка.ДанныеМаркетплейса.Получить();
					ПересоздаваемыеДокументы.Добавить(ДанныеМаркетплейса);
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	
	МодульИнтеграции = ОбработчикМаркетплейса(Маркетплейс);
	СозданныеДокументы = МодульИнтеграции.СоздатьДокументы(ПересоздаваемыеДокументы);
	
	ПровестиДокументыАвтозагрузки(СозданныеДокументы, Маркетплейс,, ДокументИсточникСопоставления);
	
	Если Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		Предупреждения.ОбновитьДляОтчетовМаркетплейсов();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет данные необходимые для отложенного споставляения номенклатуры в форме
//
// Параметры:
//  Форма	 - Форма - форма документа
//
Процедура ПодготовитьСопоставлениеНоменклатурыПоДокументу(Форма) Экспорт
	
	Документ = Форма.Объект.Ссылка;
	ДанныеДляСопоставления = ДанныеДляСопоставленияНоменклатуры(Документ);
	
	Форма.ДанныеДляСопоставленияНоменклатуры = ДанныеДляСопоставления;
	Форма.ТребуетсяСопоставлениеНоменклатуры = ЗначениеЗаполнено(ДанныеДляСопоставления);
	
КонецПроцедуры

// Удаляет предупреждение, связанное с загрузкой отчета маркетплейса, для документа после его успешного проведения
//
// Параметры:
//  Документ		 - ДокументСсылка - проведенный документ
//  ПараметрыЗаписи	 - РежимЗаписиДокумента - режим записи документа
//
Процедура УдалитьПредупреждениеДокумента(Документ, ПараметрыЗаписи) Экспорт
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение
		И Предупреждения.ЕстьПредупреждениеДляДокумента(Документ) Тогда
		Предупреждения.УдалитьПредупреждениеДляДокумента(Документ);
		Предупреждения.УдалитьПредупреждениеДляОтчетаМаркетплейса(Документ);
		ПараметрыЗаписи.Вставить("ОповеститьОбИзмененииСтатусаПредупрежденияАвтозагрузкиМаркетплейса", Истина);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает контейнер сохранения данных отложенного сопоставления номенклатуры
// 
// Возвращаемое значение:
//  Структура
//
Функция НовыйПараметрыСопоставленияНоменклатуры()
	
	Параметры = Новый Структура;
	Параметры.Вставить("НоменклатураКонтрагента", Новый Массив);
	Параметры.Вставить("НастройкиСопоставленияНоменклатуры");
	Параметры.Вставить("НоменклатураОрганизаций", Новый Соответствие);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область РегламентноеЗаданияАвтозагрузки

// Обработчик параметризованного регламентного задания АвтоматическаяЗагрузкаОтчетовМаркетплейсов.
//
Процедура АвтозагрузкаОтчетовМаркетплейсовРегламентноеЗадание(Параметры) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	Настройки = НастройкиАвтозагрузки.НастройкиРегламентныхЗаданий(Параметры.Периодичность);
	
	ФоновоеЗадание = ПолучитьТекущийСеансИнформационнойБазы().ПолучитьФоновоеЗадание();
	Если ФоновоеЗадание <> Неопределено И Настройки = Неопределено Тогда
		// Регламентное задание отключили, или удалили его настройки в регистре сведений.
		Если ФоновоеЗадание.РегламентноеЗадание = Неопределено Тогда
			// Регламентное задание запущено вручную из формы списка регламентных заданий. Идентификатор задания хранится в ключе фонового.
			ИдентификаторРегламентногоЗадания = Новый УникальныйИдентификатор(ФоновоеЗадание.Ключ);
		Иначе
			// Регламентное задание было запущено по расписанию, получим идентификатор из него.
			ИдентификаторРегламентногоЗадания = ФоновоеЗадание.РегламентноеЗадание.УникальныйИдентификатор;
		КонецЕсли;
		РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторРегламентногоЗадания);
		
		ТекстСообщения = НСтр("ru = 'Регламентное задание было удалено т.к.
			|в регистре сведений отсутствуют настройки.'");
		
		ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Автоматическая загрузка отчетов маркетплейсов'"),
				УровеньЖурналаРегистрации.Информация,
				Метаданные.РегламентныеЗадания.АвтоматическаяЗагрузкаОтчетовМаркетплейсов,,
				ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Результат = ВыполнитьАвтозагрузкуОтчетовМаркетплейсов(Параметры.Периодичность);
	Если Результат Или Параметры.ОсталосьПовторныхЗапусков = 0 Тогда
		Если Параметры.ОсталосьПовторныхЗапусков = 0 Тогда
			НастройкиАвтозагрузки.СохранитьОшибкуЗагрузки(Параметры.Периодичность);
		КонецЕсли;
		Если Параметры.ОсталосьПовторныхЗапусков < МаксимальноеКоличествоПовторныхЗапусков() Тогда
			ИзменитьВремяЗапускаЗаданияАвтозагрузкаОтчетовМаркетплейсов(Настройки);
		КонецЕсли;
	Иначе // требуется повторение
		ИзменитьВремяЗапускаЗаданияАвтозагрузкаОтчетовМаркетплейсов(Настройки, Параметры.ОсталосьПовторныхЗапусков);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьВремяЗапускаЗаданияАвтозагрузкаОтчетовМаркетплейсов(Настройки, ОсталосьПовторныхЗапусков = 0)
	
	// Определим новое время запуска и запланируем следующее регламентное задание.
	ДатаСервера  = ОбщегоНазначенияБП.ТекущаяДатаНаСервере();
	
	Если ОсталосьПовторныхЗапусков > 0 Тогда
		ЧислоСекунд = 3 * 60 * 60;
		
		ДатаНачала = '00010101';
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			ВремяЗапуска = ДатаСервера + ЧислоСекунд;
		Иначе
			ТекущееВремя = ДатаСервера - НачалоДня(ДатаСервера);
			ВремяЗапуска = '00010101' + ТекущееВремя + ЧислоСекунд;
		КонецЕсли;
	Иначе
		ВремяЗапуска = Настройки.ВремяЗапуска;
		ДатаНачала   = Настройки.ДатаНачала;
	КонецЕсли;
	
	ПараметрыЗаписи = НовыеПараметрыЗаписиРегламентногоЗаданияАвтозагрузкиОтчетов();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаписи, Настройки);
	ПараметрыЗаписи.ДатаНачала   = ДатаНачала;
	ПараметрыЗаписи.ВремяЗапуска = ВремяЗапуска;
	Если ОсталосьПовторныхЗапусков > 0 Тогда
		ПараметрыЗаписи.ОсталосьПовторныхЗапусков = ОсталосьПовторныхЗапусков - 1;
	КонецЕсли;
	ЗаписатьРегламентноеЗаданиеАвтозагрузкиОтчетов(ПараметрыЗаписи);
	
КонецПроцедуры

// Возвращает структуру параметров, для записи регламентного задания автоматической загрузки отчетов.
// 
// Возвращаемое значение:
//  Структура - структура со свойствами:
//    * ИдентификаторЗадания - УникальныйИдентификатор - уникальный идентификатор регламентного задания для поиска.
//    * ДатаНачала - Дата - дата с которой начнется выполнение регламентного задания (при работе в модели сервиса не используется).
//    * ВремяЗапуска - Дата - время запуска задания (без даты). В модели сервиса дата и время запуска задания.
//    * Наименование - Строка - наименование задания
//    * Периодичность - ПеречислениеСсылка.ПериодичностьОтчетовМаркетплейсов - периодичность запуска регламентного задания
//    * ДеньЗагрузки - Число - день запуска регламентного задания в неделе или месяце
//    * ОсталосьПовторныхЗапусков - Число - количество повторных запусков в случае ошибки
//
Функция НовыеПараметрыЗаписиРегламентногоЗаданияАвтозагрузкиОтчетов() Экспорт
	
	ПараметрыЗаписи = Новый Структура;
	// Идентификатор для поиска и изменения существующего задания. Если идентификатор пустой, запишется новое задание.
	ПараметрыЗаписи.Вставить("ИдентификаторЗадания", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	ПараметрыЗаписи.Вставить("ДатаНачала", '00010101'); // Дата начала запуска задания.
	ПараметрыЗаписи.Вставить("ВремяЗапуска", '00010101'); // Время запуска задания.
	ПараметрыЗаписи.Вставить("Периодичность", Перечисления.ПериодичностьОтчетовМаркетплейсов.ПустаяСсылка());
	ПараметрыЗаписи.Вставить("Наименование", "");
	ПараметрыЗаписи.Вставить("ДеньЗагрузки", 0);
	ПараметрыЗаписи.Вставить("ОсталосьПовторныхЗапусков", МаксимальноеКоличествоПовторныхЗапусков());
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

// Создает новое, или изменяет существующее регламентное задание автоматической загрузки отчетов.
//
// Параметры:
//	ПараметрыЗаписи - Структура - см. НовыеПараметрыЗаписиРегламентногоЗаданияАвтозагрузкиОтчетов
// 
// Возвращаемое значение:
//  РегламентноеЗадание - созданное или измененное регламентное задание если найдено по идентификатору.
//
Функция ЗаписатьРегламентноеЗаданиеАвтозагрузкиОтчетов(ПараметрыЗаписи) Экспорт
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Если Не РазделениеВключено Тогда
		Расписание.ПериодПовтораДней = 1;
		Если ПараметрыЗаписи.Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя Тогда
			ДниНедели = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыЗаписи.ДеньЗагрузки);
			Расписание.ДниНедели = ДниНедели;
		Иначе
			Расписание.ДеньВМесяце = ПараметрыЗаписи.ДеньЗагрузки;
		КонецЕсли;
	КонецЕсли;
	
	Если Не РазделениеВключено Тогда
		Расписание.ВремяНачала = ПараметрыЗаписи.ВремяЗапуска;
		Расписание.ДатаНачала  = ПараметрыЗаписи.ДатаНачала;
	КонецЕсли;
	
	ПараметрыРегламентногоЗадания = Новый Структура;
	ПараметрыРегламентногоЗадания.Вставить("Периодичность", ПараметрыЗаписи.Периодичность);
	ПараметрыРегламентногоЗадания.Вставить("ОсталосьПовторныхЗапусков", ПараметрыЗаписи.ОсталосьПовторныхЗапусков);
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("Использование", Истина);
	ПараметрыСоздания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.АвтоматическаяЗагрузкаОтчетовМаркетплейсов);
	ПараметрыСоздания.Вставить("Наименование", ПараметрыЗаписи.Наименование);
	ПараметрыСоздания.Вставить("Параметры", Новый Массив);
	ПараметрыСоздания.Параметры.Добавить(ПараметрыРегламентногоЗадания);
	Если РазделениеВключено Тогда
		ПараметрыСоздания.Вставить("ЗапланированныйМоментЗапуска", ПараметрыЗаписи.ВремяЗапуска);
	Иначе
		ПараметрыСоздания.Вставить("Расписание", Расписание);
	КонецЕсли;
	
	Попытка
		УстановитьПривилегированныйРежим(Истина);
		Задание = РегламентныеЗаданияСервер.Задание(ПараметрыЗаписи.ИдентификаторЗадания);
		Если Задание = Неопределено Тогда
			Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыСоздания);
		Иначе
			РегламентныеЗаданияСервер.ИзменитьЗадание(ПараметрыЗаписи.ИдентификаторЗадания, ПараметрыСоздания);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	Исключение
		УстановитьПривилегированныйРежим(Ложь);
		
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстЗаписиЖурнала = НСтр("ru = 'Не удалось записать регламентное задание автоматической загрузки отчетов маркетплейсов'");
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Автоматическая загрузка отчетов маркетплейсов'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.РегламентныеЗадания.АвтоматическаяЗагрузкаОтчетовМаркетплейсов,
			ТекстЗаписиЖурнала, ИнформацияОбОшибке);
		ВызватьИсключение ИнформацияОбОшибке;
	КонецПопытки;
	
	Возврат Задание;
	
КонецФункции

// Планирует разовый запуск регламентного задания в модели сервиса, если это требуется
//
Процедура ЗапланироватьАвтозагрузкуОтчетовМаркетплейсовДляОбластиДанных(Периодичность) Экспорт
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		// В обычном режиме выполнение не требуется.
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеРегламентногоЗадания = Метаданные.РегламентныеЗадания.АвтоматическаяЗагрузкаОтчетовМаркетплейсов;
	
	ПараметрыПоискаЗаданий = Новый Структура;
	ПараметрыПоискаЗаданий.Вставить("ИмяМетода",        МетаданныеРегламентногоЗадания.ИмяМетода);
	ПараметрыПоискаЗаданий.Вставить("ОбластьДанных",    РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
	ПараметрыПоискаЗаданий.Вставить("СостояниеЗадания", Перечисления.СостоянияЗаданий.Запланировано);
	ПараметрыПоискаЗаданий.Вставить("Использование",    Истина);
	ЗапланированныеЗадания = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоискаЗаданий);
	
	Если Не АвтозагрузкаОтчетовМаркетплейсовВключена() Тогда
		Для Каждого Задание Из ЗапланированныеЗадания Цикл
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	Настройки = НастройкиАвтозагрузки.НастройкиРегламентныхЗаданий(Периодичность);
	Если Настройки <> Неопределено Тогда
		Настройки.Вставить("ТребуетсяПланирование", Истина);
	КонецЕсли;
	
	Для Каждого Задание Из ЗапланированныеЗадания Цикл
		
		Если Задание.Параметры.Количество() > 0 Тогда
			ПериодичностьЗадания = Задание.Параметры[0].Периодичность;
			Если ПериодичностьЗадания <> Периодичность Тогда
				Продолжить;
			КонецЕсли;
		Иначе // Задание без параметров.
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
			Продолжить;
		КонецЕсли;
		
		Если Настройки = Неопределено Тогда // Настройки удалены из регистра сведений.
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
		ИначеЕсли Не Настройки.ЕстьДействующиеПодключения Тогда
			Настройки.ТребуетсяПланирование = Ложь;
			РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
		Иначе // Проверим не изменилось-ли плановое время запуска.
			МоментЗапуска = Настройки.ВремяЗапуска;
			Если Задание.ЗапланированныйМоментЗапуска <> МоментЗапуска Тогда
				РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
			Иначе
				Настройки.ТребуетсяПланирование = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Настройки <> Неопределено И Настройки.ТребуетсяПланирование И Настройки.ЕстьДействующиеПодключения Тогда
		ПараметрыЗаписи = НовыеПараметрыЗаписиРегламентногоЗаданияАвтозагрузкиОтчетов();
		ПараметрыЗаписи.Периодичность        = Периодичность;
		ПараметрыЗаписи.Наименование         = Настройки.Наименование;
		ПараметрыЗаписи.ВремяЗапуска         = Настройки.ВремяЗапуска;
		ПараметрыЗаписи.ИдентификаторЗадания = Новый УникальныйИдентификатор;
		ЗаписатьРегламентноеЗаданиеАвтозагрузкиОтчетов(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

Функция МаксимальноеКоличествоПовторныхЗапусков()
	
	Возврат 5;
	
КонецФункции

#КонецОбласти

#КонецОбласти
