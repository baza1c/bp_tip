
#Область ПрограммныйИнтерфейс

// См. ЦентрМониторингаПереопределяемый.ПриСбореПоказателейСтатистикиКонфигурации.
Процедура ПриСбореПоказателейСтатистикиКонфигурации() Экспорт
	
	ПоказателиСтатистики = ПоказателиСтатистикиПоПользователям();
	
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(
		"СтатистикаБП.Пользователи.РолиПользователей",
		1,
		ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(ПоказателиСтатистики, Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет)));
	
КонецПроцедуры

// Возвращает количество пользователей, которые могут работать в базе
//
// Возвращаемое значение:
//   Число - 0, если не заведено ни одного пользователя
//
Функция КоличествоАктуальныхПользователей() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(КОЛИЧЕСТВО(Пользователи.Ссылка), 0) КАК Количество
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	НЕ Пользователи.ПометкаУдаления
	|	И НЕ Пользователи.Недействителен
	|	И НЕ Пользователи.Служебный");
	
	Возврат Запрос.Выполнить().Выгрузить()[0].Количество;
	
КонецФункции

// Устанавливает ответственного документа продаж (Счет покупателю, Реализация (акт, накладная, УПД), Возврат от покупателя)
//
// Параметры:
//  Объект - ДокументОбъект - объект документа продаж
//  ЗаполнятьОтветственногоПоПродажамИзКонтрагента - Булево, Неопределено - признак заполнения ответственного 
//                                                   по продажам из контрагента
//
Процедура УстановитьОтветственногоДокументаПродаж(Объект, ЗаполнятьОтветственногоПоПродажамИзКонтрагента = Неопределено) Экспорт
	
	УстановитьБезусловно = ЗаполнятьОтветственногоПоПродажамИзКонтрагента = Неопределено;
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Если ЗаполнятьОтветственногоПоПродажамИзКонтрагента = Неопределено Тогда
			ЗаполнятьОтветственногоПоПродажамИзКонтрагента = Константы.ЗаполнятьОтветственногоПоПродажамИзКонтрагента.Получить();
		КонецЕсли;
		Если ЗаполнятьОтветственногоПоПродажамИзКонтрагента Тогда
			ОтветственныйКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "Ответственный");
			Если ЗначениеЗаполнено(ОтветственныйКонтрагента) Тогда
				Объект.Ответственный = ОтветственныйКонтрагента;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если УстановитьБезусловно Тогда
		Объект.Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

// Обработчик оповещения об изменении настройки заполнения ответственного документа продаж
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой выполняется обработчик
// 
Процедура ОбработатьОповещениеОбИзмененииНастроекОтветственного(Форма) Экспорт
	
	ЗаполнятьОтветственногоПоПродажамИзКонтрагентаНовоеЗначение = Константы.ЗаполнятьОтветственногоПоПродажамИзКонтрагента.Получить();
	
	ИзменилосьЗаполнениеОтветственного = (Форма.ЗаполнятьОтветственногоПоПродажамИзКонтрагента <> ЗаполнятьОтветственногоПоПродажамИзКонтрагентаНовоеЗначение);
	
	Если ИзменилосьЗаполнениеОтветственного Тогда
		Форма.ЗаполнятьОтветственногоПоПродажамИзКонтрагента = ЗаполнятьОтветственногоПоПродажамИзКонтрагентаНовоеЗначение;
		ПользователиБПКлиентСервер.УстановитьПодсказкуОтветственного(Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПоказателиСтатистикиПоПользователям()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоОтбора", НачалоГода(ТекущаяДатаСеанса()));
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	НЕ Пользователи.Служебный
	|	И НЕ Пользователи.Недействителен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналОпераций.Ответственный КАК Пользователь,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЖурналОпераций.Ссылка) КАК Количество
	|ИЗ
	|	ЖурналДокументов.ЖурналОпераций КАК ЖурналОпераций
	|ГДЕ
	|	ЖурналОпераций.Дата >= &НачалоОтбора
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналОпераций.Ответственный";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаПользователи = РезультатыЗапроса[0].Выбрать();
	ТаблицаДокументов = РезультатыЗапроса[1].Выгрузить();
	
	ПоказателиСтатистики = Новый Структура;
	ПоказателиСтатистики.Вставить("ДатаСобытия", НачалоЧаса(ТекущаяДатаСеанса()));
	ПоказателиСтатистики.Вставить("Пользователи", Новый Массив);
	
	Пока ВыборкаПользователи.Следующий() Цикл
		
		СтатистикаПользователя = Новый Структура;
		СтатистикаПользователя.Вставить("Пользователь", ВыборкаПользователи.ИдентификаторПользователяИБ);
		
		ДокументыПользователя = ТаблицаДокументов.Найти(ВыборкаПользователи.Пользователь, "Пользователь");
		Если ДокументыПользователя <> Неопределено Тогда
			КоличествоДокументов = ДокументыПользователя.Количество;
		Иначе
			КоличествоДокументов = 0;
		КонецЕсли;
		
		СтатистикаПользователя.Вставить("КоличествоДокументовСНачалаГода", КоличествоДокументов);
		
		ПоказателиСтатистики.Пользователи.Добавить(СтатистикаПользователя);
		
	КонецЦикла;
	
	Возврат ПоказателиСтатистики;
	
КонецФункции

#КонецОбласти
