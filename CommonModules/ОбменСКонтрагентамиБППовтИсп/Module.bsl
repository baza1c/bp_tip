
#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, ДатаСведений = Неопределено, БанковскийСчет = Неопределено) Экспорт
	
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		ОбъектПолученияСведений = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЮрФизЛицо, "Владелец");
	Иначе
		ОбъектПолученияСведений = ЮрФизЛицо;
	КонецЕсли;
	
	ДанныеЮрФизЛица = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ОбъектПолученияСведений, ДатаСведений, БанковскийСчет);
	ТипЮрФизЛица = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектПолученияСведений, "ЮридическоеФизическоеЛицо");
	
	ДанныеЮрФизЛица.Вставить("ЭтоИндивидуальныйПредприниматель", Ложь);
	Если ТипЮрФизЛица = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		
		ДанныеЮрФизЛица.Вставить("Фамилия",  "");
		ДанныеЮрФизЛица.Вставить("Имя",      "");
		ДанныеЮрФизЛица.Вставить("Отчество", "");
		
		Если ТипЗнч(ОбъектПолученияСведений)=Тип("СправочникСсылка.Организации") Тогда
			НаименованияОрганизации = Справочники.Организации.НаименованияНаДату(ОбъектПолученияСведений, ДатаСведений);
			ДанныеЮрФизЛица.Вставить("Фамилия", НаименованияОрганизации.ФИО.Фамилия);
			ДанныеЮрФизЛица.Вставить("Имя", НаименованияОрганизации.ФИО.Имя);
			ДанныеЮрФизЛица.Вставить("Отчество", НаименованияОрганизации.ФИО.Отчество);
			ДанныеЮрФизЛица.ЭтоИндивидуальныйПредприниматель =
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектПолученияСведений,
				"СтатусФизическогоЛица") = Перечисления.СтатусыФизическихЛиц.ИндивидуальныйПредприниматель;
		Иначе
			ПредставлениеФизЛица = ?(ЗначениеЗаполнено(ДанныеЮрФизЛица.ПолноеНаименование),
				ДанныеЮрФизЛица.ПолноеНаименование, ДанныеЮрФизЛица.ФИОФизлица);
			ФИО = ВыделитьФИОИзНаименованияКонтрагента(ПредставлениеФизЛица);
			ЗаполнитьФИОиДолжность(ДанныеЮрФизЛица, ФИО);
			ДанныеЮрФизЛица.ЭтоИндивидуальныйПредприниматель = Справочники.Контрагенты.ЭтоИндивидуальныйПредприниматель(ОбъектПолученияСведений);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЮрФизЛицо, "КПП, НаименованиеПолное");
		ДанныеЮрФизЛица.Вставить("ПолноеНаименование", Реквизиты.НаименованиеПолное);
		Если ТипЮрФизЛица = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			ДанныеЮрФизЛица.Вставить("КПП", Реквизиты.КПП);
		КонецЕсли;
		ДанныеЮрФизЛица.Вставить("ОбособленноеПодразделениеОрганизации", ЮрФизЛицо);
	КонецЕсли;
	
	ДанныеЮрФизЛица.Вставить("ОфициальноеНаименование", ДанныеЮрФизЛица.ПолноеНаименование);
	ДанныеЮрФизЛица.Вставить("ТипЮрФизЛица", ТипЮрФизЛица);
	ДанныеЮрФизЛица.Вставить("ЮрФизЛицоСсылка", ОбъектПолученияСведений.Ссылка);
	
	Возврат ОбщегоНазначения.ФиксированныеДанные(ДанныеЮрФизЛица, Ложь);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьФИОИДолжность(СтруктураПриемник, ИсточникДанных, Должность = Неопределено)
	
	ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ИсточникДанных);
	
	СтруктураПриемник.Вставить("Фамилия", ФИО.Фамилия);
	СтруктураПриемник.Вставить("Имя", ФИО.Имя);
	СтруктураПриемник.Вставить("Отчество", ФИО.Отчество);
	Если Должность <> Неопределено Тогда
		СтруктураПриемник.Вставить("Должность", Должность);
	КонецЕсли;
	
КонецПроцедуры

Функция ВыделитьФИОИзНаименованияКонтрагента(Знач Наименование)
	
	ШаблоныНаУдаление = Новый Массив;
	ШаблоныНаУдаление.Добавить("ИП");
	ШаблоныНаУдаление.Добавить("Индивидуальный предприниматель");
	ШаблоныНаУдаление.Добавить("Глава КФХ");
	ШаблоныНаУдаление.Добавить("ГКФХ");
	ШаблоныНаУдаление.Добавить("Глава крестьянского хозяйства");
	ШаблоныНаУдаление.Добавить("Глава крестьянского (фермерского) хозяйства");
	
	Разделитель = " " + "\/()'," + """";
	СписокСловНаименования = СтрРазделить(Наименование, Разделитель, Ложь);
	Для Каждого Шаблон Из ШаблоныНаУдаление Цикл
		СписокСловШаблона = СтрРазделить(Шаблон, Разделитель, Ложь);
		НовыйСписокСловНаименования = Новый Массив;
		Индекс = 0;
		Пока Индекс <= СписокСловНаименования.ВГраница() Цикл
			Если ВРег(СписокСловНаименования[Индекс]) = ВРег(СписокСловШаблона[0]) Тогда
				Совпадение = Истина;
				Смещение = 0;
				Для ИндексШаблона = 1 По СписокСловШаблона.ВГраница() Цикл
					Смещение = Смещение + 1;
					Если Индекс + Смещение > СписокСловНаименования.ВГраница()
							Или ВРег(СписокСловНаименования[Индекс + Смещение]) <> ВРег(СписокСловШаблона[ИндексШаблона]) Тогда
						Совпадение = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если Совпадение Тогда
					Индекс = Индекс + СписокСловШаблона.Количество();
				Иначе
					НовыйСписокСловНаименования.Добавить(СписокСловНаименования[Индекс]);
					Индекс = Индекс + 1;
				КонецЕсли;
			Иначе
				НовыйСписокСловНаименования.Добавить(СписокСловНаименования[Индекс]);
				Индекс = Индекс + 1;
			КонецЕсли;
		КонецЦикла;
		СписокСловНаименования = НовыйСписокСловНаименования;
	КонецЦикла;	
		
	Возврат СтрСоединить(СписокСловНаименования, " ");	
КонецФункции

#КонецОбласти